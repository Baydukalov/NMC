
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	сткОтбор = ЗаполнитьСтруктуруОтбора(ПараметрКоманды);
	Если ТипЗнч(сткОтбор) = Тип("Структура") Тогда
		ПараметрыФормы = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("ОбщаяФорма.упТекущиеПравилаРасчетаПоСтатьям", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно,);
	Иначе
		ТекстСообщения = НСтр("ru = 'Список статей должен быть заполнен!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьСтруктуруОтбора(Соглашение)
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиДоходов.СтатьяДоходов КАК Статья
	|ИЗ
	|	Справочник.упСоглашенияСКлиентами.СтатьиДоходов КАК СтатьиДоходов
	|ГДЕ
	|	СтатьиДоходов.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.СтатьяРасходов
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.СтатьяРасходов
	|ИЗ
	|	Справочник.упПрочиеСоглашения.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Соглашение);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		МассивСтатей = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Статья");
		
		Результат = Новый Структура();
		Результат.Вставить("СтатьяДоходовРасходов", МассивСтатей);
		
		сткЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Соглашение, "Организация, ГруппаТарифов");
		Если ЗначениеЗаполнено(сткЗначенияРеквизитов.Организация) Тогда
			Результат.Вставить("Организация", сткЗначенияРеквизитов.Организация);
		КонецЕсли; 
		Если ЗначениеЗаполнено(сткЗначенияРеквизитов.ГруппаТарифов) Тогда
			Результат.Вставить("ГруппаТарифов", сткЗначенияРеквизитов.ГруппаТарифов);
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Результат;	
	
КонецФункции // ЗаполнитьСтруктуруОтбора()
 
#КонецОбласти 