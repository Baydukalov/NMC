
#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Процедура записывает данные по переданным движениям.
//
// Параметры:
//  ДополнительныеСвойства  - Структура - Свойства.
//  Ссылка  - ДокументСсылка - Ссылка на объект.
//  Движения  - КоллекцияДвижений - Движения объекта.
//  Реквизиты  - Структура - ПЕречислины реквизиты.
//  Отказ  - Булево - Флаг сохранения изменений.
//
Процедура СформироватьДвижения(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		
	Если Реквизиты.ВидОперации = Перечисления.упВидыРегламентныхОпераций.АмортизацияТранспортныхСредств Тогда
		НачислениеАмортизацииТранспортныхСредств(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
		
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.упВидыРегламентныхОпераций.КорректировкаСтоимостиГСМ Тогда
		КорректировкаСтоимостиГСМ(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);	
		
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.упВидыРегламентныхОпераций.РаспределениеКосвенныхРасходов Тогда
		РаспределениеКосвенныхРасходов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);	
		
	КонецЕсли;
		
	Если НЕ Отказ Тогда
		Движения.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Функция - Сформировать таблицы для сопоставления.
//
// Параметры:
//  НачалоПериода	 - ДатаВремя - Дата начала периода.
//  ОкончаниеПериода - ДатаВремя - Дата окончания периода.
// 
// Возвращаемое значение:
//  Структура - структура с полями:
//		* ОстаткиГСМ 				- ТаблицаЗначений - таблица движений по заправкам регистра остатки ГСМ.
//		* ДанныеТопливныхКомпаний 	- ТаблицаЗначений - таблица данных топливных компаний, сопоставленная таблице движений ГСМ.
//		* ЕстьНеСопоставленныеСтроки - Булево - Истина, есть не распределенные строки таблицы данных ТК по таблице движений ГСМ. 
//		* Запрос					- Запрос - Запрос, в котором были полученные исходные данные.
//
Функция СформироватьТаблицыДляСопоставления(НачалоПериода, ОкончаниеПериода) Экспорт
	
	сткРезультат = Новый Структура();
	// Формируется таблица отклонений по заправкам в разрезе путевых листов.
	// Для этого выбираются заправки из регистра остатки по ГСМ с детализацией до регистратора 
	// и им сопоставляются загруженныем записи по данным топливных компаний.
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМ.Период КАК ДатаДокумента,
	               |	НАЧАЛОПЕРИОДА(упОстаткиГСМ.Период, ДЕНЬ) КАК Период,
	               |	упОстаткиГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
	               |	упОстаткиГСМ.ВидГСМ КАК ВидГСМ,
	               |	упОстаткиГСМ.ТопливнаяКарта КАК ТопливнаяКарта,
	               |	упОстаткиГСМ.Регистратор КАК Регистратор,
	               |	СУММА(упОстаткиГСМ.Количество) КАК Количество,
	               |	СУММА(упОстаткиГСМ.Стоимость) КАК Стоимость,
	               |	СУММА(упОстаткиГСМ.СтоимостьНДС) КАК СтоимостьНДС,
	               |	ЕСТЬNULL(упПутевойЛист.НачалоРейса, НАЧАЛОПЕРИОДА(упОстаткиГСМ.Период, ДЕНЬ)) КАК НачалоПутевогоЛиста,
	               |	ЕСТЬNULL(упПутевойЛист.ОкончаниеРейса, КОНЕЦПЕРИОДА(упОстаткиГСМ.Период, ДЕНЬ)) КАК ОкончаниеПутевогоЛиста,
	               |	ЛОЖЬ КАК Флаг,
	               |	упОстаткиГСМ.МоментВремени,
	               |	упОстаткиГСМ.НомерСтроки
	               |ПОМЕСТИТЬ втОстаткиГСМ
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ КАК упОстаткиГСМ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК упПутевойЛист
	               |		ПО упОстаткиГСМ.Регистратор = упПутевойЛист.Ссылка
	               |ГДЕ
	               |	(&ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ упОстаткиГСМ.Период >= &ДатаНачала)
	               |	И (&ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ упОстаткиГСМ.Период <= &ДатаОкончания)
	               |	И упОстаткиГСМ.ВидДвиженияТоплива = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упОстаткиГСМ.Период,
	               |	НАЧАЛОПЕРИОДА(упОстаткиГСМ.Период, ДЕНЬ),
	               |	упОстаткиГСМ.ТранспортноеСредство,
	               |	упОстаткиГСМ.ВидГСМ,
	               |	упОстаткиГСМ.ТопливнаяКарта,
	               |	упОстаткиГСМ.Регистратор,
	               |	ЕСТЬNULL(упПутевойЛист.НачалоРейса, НАЧАЛОПЕРИОДА(упОстаткиГСМ.Период, ДЕНЬ)),
	               |	ЕСТЬNULL(упПутевойЛист.ОкончаниеРейса, КОНЕЦПЕРИОДА(упОстаткиГСМ.Период, ДЕНЬ)),
	               |	упОстаткиГСМ.МоментВремени,
	               |	упОстаткиГСМ.НомерСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упДанныеОтТопливныхКомпаний.Период КАК ПериодТК,
	               |	упДанныеОтТопливныхКомпаний.Валюта,
	               |	упДанныеОтТопливныхКомпаний.Количество КАК КоличествоТК,
	               |	упДанныеОтТопливныхКомпаний.СуммаБезНДС КАК СуммаБезНДСТК,
	               |	упДанныеОтТопливныхКомпаний.СуммаСНДС КАК СуммаСНДСТК,
	               |	0 КАК СуммаНДСТК,
	               |	упДанныеОтТопливныхКомпаний.ТопливнаяКарта КАК ТопливнаяКартаТК,
	               |	упДанныеОтТопливныхКомпаний.ВидГСМ КАК ВидГСМТК,
	               |	ЛОЖЬ КАК Флаг,
	               |	упДанныеОтТопливныхКомпаний.НомерТранзакции,
	               |	упВыдачаТопливныхКартСрезПоследних.Водитель,
	               |	упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство
	               |ПОМЕСТИТЬ втДанныеТопливныхКомпаний
	               |ИЗ
	               |	РегистрСведений.упДанныеОтТопливныхКомпаний КАК упДанныеОтТопливныхКомпаний
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних КАК упВыдачаТопливныхКартСрезПоследних
	               |		ПО упДанныеОтТопливныхКомпаний.ТопливнаяКарта = упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта
	               |ГДЕ
	               |	(&ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ упДанныеОтТопливныхКомпаний.Период >= &ДатаНачала)
	               |	И (&ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ИЛИ упДанныеОтТопливныхКомпаний.Период <= &ДатаОкончания)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втОстаткиГСМ.ДатаДокумента,
	               |	втОстаткиГСМ.Период,
	               |	втОстаткиГСМ.ТранспортноеСредство,
	               |	втОстаткиГСМ.ВидГСМ КАК ВидГСМ,
	               |	втОстаткиГСМ.ТопливнаяКарта КАК ТопливнаяКарта,
	               |	втОстаткиГСМ.Регистратор КАК Регистратор,
	               |	втОстаткиГСМ.Количество КАК Количество,
	               |	втОстаткиГСМ.Стоимость КАК Стоимость,
	               |	втОстаткиГСМ.СтоимостьНДС КАК СтоимостьНДС,
	               |	втОстаткиГСМ.НачалоПутевогоЛиста КАК НачалоПутевогоЛиста,
	               |	втОстаткиГСМ.ОкончаниеПутевогоЛиста КАК ОкончаниеПутевогоЛиста,
	               |	втОстаткиГСМ.МоментВремени
	               |ИЗ
	               |	втОстаткиГСМ КАК втОстаткиГСМ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	втОстаткиГСМ.НомерСтроки
	               |ИТОГИ
	               |	СУММА(Количество),
	               |	СУММА(Стоимость),
	               |	СУММА(СтоимостьНДС),
	               |	МАКСИМУМ(НачалоПутевогоЛиста),
	               |	МАКСИМУМ(ОкончаниеПутевогоЛиста)
	               |ПО
	               |	ТопливнаяКарта,
	               |	ВидГСМ,
	               |	Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДанныеТопливныхКомпаний.ПериодТК,
	               |	втДанныеТопливныхКомпаний.Валюта,
	               |	втДанныеТопливныхКомпаний.КоличествоТК,
	               |	втДанныеТопливныхКомпаний.СуммаБезНДСТК,
	               |	втДанныеТопливныхКомпаний.СуммаСНДСТК,
	               |	втДанныеТопливныхКомпаний.СуммаНДСТК,
	               |	втДанныеТопливныхКомпаний.ТопливнаяКартаТК,
	               |	втДанныеТопливныхКомпаний.ВидГСМТК,
	               |	втДанныеТопливныхКомпаний.Флаг,
	               |	втДанныеТопливныхКомпаний.НомерТранзакции,
	               |	втДанныеТопливныхКомпаний.Водитель,
	               |	втДанныеТопливныхКомпаний.ТранспортноеСредство
	               |ИЗ
	               |	втДанныеТопливныхКомпаний КАК втДанныеТопливныхКомпаний
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втОстаткиГСМ.ДатаДокумента,
	               |	втОстаткиГСМ.Период,
	               |	втОстаткиГСМ.ТранспортноеСредство,
	               |	втОстаткиГСМ.ВидГСМ,
	               |	втОстаткиГСМ.ТопливнаяКарта,
	               |	втОстаткиГСМ.Регистратор,
	               |	втОстаткиГСМ.Количество,
	               |	втОстаткиГСМ.Стоимость,
	               |	втОстаткиГСМ.СтоимостьНДС,
	               |	втОстаткиГСМ.НачалоПутевогоЛиста,
	               |	втОстаткиГСМ.ОкончаниеПутевогоЛиста,
	               |	втОстаткиГСМ.Флаг,
	               |	втОстаткиГСМ.МоментВремени
	               |ИЗ
	               |	втОстаткиГСМ КАК втОстаткиГСМ";
   	Запрос.УстановитьПараметр("ДатаНачала", 	НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ОкончаниеПериода);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ИндексЗапросаГСМДерево					= 2;
	ИндексЗапросаДанныеТопливныхКомпаний 	= 3;
	ИндексЗапросаГСМ 						= 4;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	тбзОстаткиГСМ	= РезультатЗапроса[ИндексЗапросаГСМ].Выгрузить();
	дрвОстаткиГСМ 	= РезультатЗапроса[ИндексЗапросаГСМДерево].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
																	 
	тбзДанныеТопливныхКомпаний 	= РезультатЗапроса[ИндексЗапросаДанныеТопливныхКомпаний].Выгрузить();
	тбзДанныеТопливныхКомпаний.Колонки.Добавить("Регистратор", 				Новый ОписаниеТипов("ДокументСсылка.упЗаправкаСливГСМ, ДокументСсылка.упПутевойЛист"));
	
	// Поле добавлено на случай если будет сопоставление не по топливной карте
	тбзДанныеТопливныхКомпаний.Колонки.Добавить("ТопливнаяКартаОстаткиГСМ", Новый ОписаниеТипов("СправочникСсылка.упТопливныеКарты"));   
	тбзДанныеТопливныхКомпаний.Колонки.Добавить("СовпадениеПоПризнакам", 	Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(250, ДопустимаяДлина.Переменная)));
																		 
	// Пересчитать данные топливных компаний в валюту упр. учета.
	УпрВалюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	Для каждого Строка Из тбзДанныеТопливныхКомпаний Цикл
		Если ЗначениеЗаполнено(Строка.Валюта)
				И НЕ Строка.Валюта = УпрВалюта Тогда
			Строка.СуммаБезНДСТК 	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СуммаБезНДСТК, Строка.ПериодТК); 	
			Строка.СуммаСНДСТК 		= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СуммаСНДСТК, Строка.ПериодТК); 	
		КонецЕсли;
		Строка.СуммаНДСТК = Строка.СуммаСНДСТК - Строка.СуммаБезНДСТК;
	КонецЦикла;
																		 
	// Формируется новая таблица данных ТК, связано это с тем что одна строка таблицы ТК,
	// может соответствовать нескольким строкам заправкам по путевому листу. Например, когда одной картой
	// заправлялось еще и дополнительное оборудование.
	
	тбзНовыеДанныеТопливныхКомпаний	= тбзДанныеТопливныхКомпаний.СкопироватьКолонки();
	тбзНовыеДанныеТопливныхКомпаний.Колонки.Удалить(тбзНовыеДанныеТопливныхКомпаний.Колонки.ТранспортноеСредство);
	мсвТипы = Новый Массив;
	мсвТипы.Добавить(Тип("СправочникСсылка.упТранспортныеСредства"));
	мсвТипы.Добавить(Тип("СправочникСсылка.упДополнительноеОборудование"));
	тбзНовыеДанныеТопливныхКомпаний.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов(мсвТипы));   
		
	// Формируется таблица остатков ГСМ
	
	// Сопоставление происходит последовательно в несколько этапов:
	ЕстьНеСопоставленныеСтроки = Ложь;
	СовпадениеПоПризнакам = Нстр("ru = 'Cовпадают дата заправки и интервал путевого листа, вид гсм, топливная карта.'");
	
	// Каждая строка данных "топливных компаний" распределяется по заправкам ГСМ за месяц
	Для каждого СтрокаДанныеТопливныхКомпаний Из тбзДанныеТопливныхКомпаний Цикл
				
		
		ДатаЗаправкиТК 		= СтрокаДанныеТопливныхКомпаний.ПериодТК;
		ВидГСМТК			= СтрокаДанныеТопливныхКомпаний.ВидГСМТК;
		ТопливнаяКартаТК	= СтрокаДанныеТопливныхКомпаний.ТопливнаяКартаТК;
		КоличествоТК		= СтрокаДанныеТопливныхКомпаний.КоличествоТК;
		СтоимостьБезНДС			= СтрокаДанныеТопливныхКомпаний.СуммаБезНДСТК;
		СтоимостьСНДС			= СтрокаДанныеТопливныхКомпаний.СуммаСНДСТК;
		СтоимостьНДС			= СтрокаДанныеТопливныхКомпаний.СуммаНДСТК;
		
		СтрокаТКРаспределена = Ложь;
		
		// Ищется путвой лист который удовлетворяет следующим требованиям.
		Для каждого СтрокаТопливнаяКарта Из дрвОстаткиГСМ.Строки Цикл
			
			
			//	1. Топливная карта по ТК совпадает с ТК заправки путевого листа.
			Если НЕ СтрокаТопливнаяКарта.ТопливнаяКарта = ТопливнаяКартаТК Тогда
				Продолжить;
			КонецЕсли;
			
			Для каждого СтрокаВидГСМ Из СтрокаТопливнаяКарта.Строки Цикл
				
				//	2. ВидГСМ по данным ТК совпадает с видом ГСМ по путевому листу.
				Если НЕ СтрокаВидГСМ.ВидГСМ = ВидГСМТК Тогда
					Продолжить;
				КонецЕсли;
				
				Для каждого СтрокаРегистратор Из СтрокаВидГСМ.Строки Цикл
					
					КоличествоРегистраторЗаправка	= СтрокаРегистратор.Количество;
					
					//	3. Дата транзакции из данных ТК попадает в интервал (Начало путевого листа, окончание путевого листа).
					Если СтрокаРегистратор.НачалоПутевогоЛиста <= ДатаЗаправкиТК
						И СтрокаРегистратор.ОкончаниеПутевогоЛиста >= ДатаЗаправкиТК 
						И КоличествоРегистраторЗаправка > 0 Тогда
											
						КоличествоСумма	= 0;
						СуммаСНДС		= 0;
						СуммаНДС		= 0;
						СуммаБезНДС		= 0;
						КоличествоСтрок	= СтрокаРегистратор.Строки.Количество();
						НомерСтроки		= 1;
																		
						Для каждого СтрокаЗаправка Из СтрокаРегистратор.Строки Цикл
							
							Если НомерСтроки = КоличествоСтрок Тогда
								КоличествоЗаправка 	= КоличествоТК - КоличествоСумма;
								СуммаСНДСЗаправка 	= СтоимостьСНДС - СуммаСНДС;
								СуммаБезНДСЗаправка = СтоимостьБезНДС - СуммаБезНДС;
								СуммаНДСЗаправка 	= СтоимостьНДС - СуммаНДС;
							Иначе
								Коэффициент	= СтрокаЗаправка.Количество / КоличествоРегистраторЗаправка;
								КоличествоЗаправка 	= Окр(Коэффициент * КоличествоТК, 2);
								СуммаСНДСЗаправка 	= Окр(Коэффициент * СтоимостьСНДС, 2);
								СуммаБезНДСЗаправка = Окр(Коэффициент * СтоимостьБезНДС, 2);
								СуммаНДСЗаправка 	= Окр(Коэффициент * СтоимостьНДС, 2);
							КонецЕсли;
							
							КоличествоСумма	= КоличествоСумма + КоличествоЗаправка;
							СуммаСНДС		= СуммаСНДС + СуммаСНДСЗаправка;
							СуммаБезНДС		= СуммаБезНДС + СуммаБезНДСЗаправка;
							СуммаНДС		= СуммаНДС + СуммаНДСЗаправка;
							НомерСтроки		= НомерСтроки + 1;
							
							НоваяСтрокаДанныеТК	= тбзНовыеДанныеТопливныхКомпаний.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрокаДанныеТК, СтрокаДанныеТопливныхКомпаний);
							НоваяСтрокаДанныеТК.Регистратор			 = СтрокаЗаправка.Регистратор;
							НоваяСтрокаДанныеТК.ТранспортноеСредство = СтрокаЗаправка.ТранспортноеСредство;
							НоваяСтрокаДанныеТК.КоличествоТК		 = КоличествоЗаправка;
							НоваяСтрокаДанныеТК.СуммаНДСТК		 	 = СуммаНДСЗаправка;
							НоваяСтрокаДанныеТК.СуммаСНДСТК		 	 = СуммаСНДСЗаправка;
							НоваяСтрокаДанныеТК.СуммаБезНДСТК	 	 = СуммаБезНДСЗаправка;
							НоваяСтрокаДанныеТК.Флаг				 = Истина;
							НоваяСтрокаДанныеТК.ТопливнаяКартаОстаткиГСМ	= СтрокаЗаправка.ТопливнаяКарта;	
							НоваяСтрокаДанныеТК.СовпадениеПоПризнакам= СовпадениеПоПризнакам;
													
						КонецЦикла;
						
						СтрокаТКРаспределена = Истина;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если СтрокаТКРаспределена Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если СтрокаТКРаспределена Тогда
					Прервать;
			КонецЕсли;

		КонецЦикла;
		
		// Если все эти параметры совпадают тогда 
		Если НЕ СтрокаТКРаспределена Тогда
			
			// Формируется новая строка с признаком что сопоставить топливо не удалось.
			ЕстьНеСопоставленныеСтроки = Истина;
			НоваяСтрокаДанныеТК	= тбзНовыеДанныеТопливныхКомпаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДанныеТК, СтрокаДанныеТопливныхКомпаний);
			НоваяСтрокаДанныеТК.Флаг				 = Ложь;
			НоваяСтрокаДанныеТК.ТранспортноеСредство 		= Справочники.упТранспортныеСредства.ПустаяСсылка();
			НоваяСтрокаДанныеТК.ТопливнаяКартаОстаткиГСМ	= Справочники.упТопливныеКарты.ПустаяСсылка();
			НоваяСтрокаДанныеТК.Регистратор					= Документы.упПутевойЛист.ПустаяСсылка();
		КонецЕсли;
				
	КонецЦикла;
	
	тбзНовыеДанныеТопливныхКомпаний.Свернуть("Валюта, ТопливнаяКартаТК, ВидГСМТК, ПериодТК, Флаг, НомерТранзакции, Водитель, ТранспортноеСредство, Регистратор, СовпадениеПоПризнакам, ТопливнаяКартаОстаткиГСМ", "КоличествоТК, СуммаБезНДСТК, СуммаСНДСТК, СуммаНДСТК");
			
	//ЕстьНеСопоставленныеСтроки = тбзДанныеТопливныхКомпаний.НайтиСтроки(Новый Структура("Флаг", Ложь)).Количество() > 0;
	Если ЕстьНеСопоставленныеСтроки Тогда
		НоваяСтрока = тбзОстаткиГСМ.Добавить();
		НоваяСтрока.ТранспортноеСредство 	= Справочники.упТранспортныеСредства.ПустаяСсылка();
		НоваяСтрока.ВидГСМ 					= Справочники.упВидыГСМ.ПустаяСсылка();
		НоваяСтрока.ТопливнаяКарта			= Справочники.упТопливныеКарты.ПустаяСсылка();
		НоваяСтрока.Регистратор				= Документы.упПутевойЛист.ПустаяСсылка();
	КонецЕсли;
	
	сткРезультат.Вставить("ОстаткиГСМ", 				тбзОстаткиГСМ);
	сткРезультат.Вставить("ДанныеТопливныхКомпаний", 	тбзНовыеДанныеТопливныхКомпаний);
	сткРезультат.Вставить("ЕстьНеСопоставленныеСтроки", ЕстьНеСопоставленныеСтроки);
	сткРезультат.Вставить("Запрос", Запрос);

	Возврат сткРезультат;
		
КонецФункции

Процедура НачислениеАмортизацииТранспортныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ)
	
	сткТрассировка      = ДополнительныеСвойства.сткИнформацияОбОшибках;
	ВедетсяВалютныйУчет	= ДополнительныеСвойства.ВедетсяВалютныйУчет;
	сткПрогресс         = ДополнительныеСвойства.Прогресс;
	ФоновоеЗадание      = сткПрогресс.ФоновоеЗадание;
	КоличествоОпераций  = сткПрогресс.КоличествоОпераций;
	НомерОперации       = сткПрогресс.НомерОперации - 1;
	
	Движения.упВыработкаТС.Прочитать();
	Если Движения.упВыработкаТС.Количество() > 0 Тогда
		Движения.упВыработкаТС.Очистить();
		Движения.упВыработкаТС.Записать(Истина);
	КонецЕсли;
	
	ДвиженияРасходы            = Движения.упРасходы;
	ДвиженияРасходы.Записывать = Истина;
	
	ДвиженияРасходы            = Движения.упРасходыНаТС;
	ДвиженияРасходы.Записывать = Истина;

	ДвиженияРасходы            = Движения.упДоходыРасходыПредприятия;
	ДвиженияРасходы.Записывать = Истина;

	ДвиженияРасходы            = Движения.упПрочиеРасходы;
	ДвиженияРасходы.Записывать = Истина;
	
	Если ВедетсяВалютныйУчет Тогда
		ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	Иначе
		ВалютаУпрУчета = Справочники.Валюты.ПустаяСсылка();	
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.Организация КАК Организация
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	|	ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
	|ГДЕ ИСТИНА
	|	И упРейс.Дата >= &ДатаНачала
	|	И упРейс.Дата <= &ДатаОкончания
	|	И упСтатусыРейсовСрезПоследних.Статус В  (&мсвСтатусы)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпРасходы_ОборотыТ.Рейс КАК Рейс,
	|	УпРасходы_ОборотыТ.СуммаФактСНДСОборот КАК СуммаФактСНДСОборот
	|ПОМЕСТИТЬ втФактическиеРасходы
	|ИЗ
	|	РегистрНакопления.упРасходы.Обороты(
	|		,
	|		,
	|		,
	|		Рейс В (
	|		ВЫБРАТЬ
	|		втРейсы.Рейс КАК Рейс
	|		ИЗ
	|		втРейсы КАК втРейсы)) КАК УпРасходы_ОборотыТ
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	СУММА(ЕСТЬNULL(втФактическиеРасходы.СуммаФактСНДСОборот, 0)) КАК СуммаФактСНДСОборот,
	|	МАКСИМУМ(втРейсы.Организация) КАК Организация
	|ПОМЕСТИТЬ втТранспортныеСредстваПоРейсам
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРасходы КАК втФактическиеРасходы
	|	ПО втРейсы.Рейс = втФактическиеРасходы.Рейс
	|СГРУППИРОВАТЬ ПО
	|	втРейсы.ТранспортноеСредство
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТранспортныеСредства.ТранспортноеСредство КАК ТранспортноеСредство,
	|	СУММА(ЕСТЬNULL(упВыработкаТС.Пробег, 0)) КАК Пробег,
	|	СУММА(ЕСТЬNULL(упВыработкаТС.Моточасы, 0)) КАК Моточасы
	|ПОМЕСТИТЬ втПробег
	|ИЗ
	|	втТранспортныеСредстваПоРейсам КАК втТранспортныеСредства
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упВыработкаТС КАК упВыработкаТС
	|	ПО ИСТИНА
	|		И втТранспортныеСредства.ТранспортноеСредство = упВыработкаТС.ТранспортноеСредство
	|		И втТранспортныеСредства.ТранспортноеСредство = упВыработкаТС.ТранспортноеСредство
	|		И упВыработкаТС.ВидДвижения = &ВидДвижения
	|		И &ДатаНачала <= упВыработкаТС.Период
	|		И упВыработкаТС.Период < &ДатаОкончания
	|СГРУППИРОВАТЬ ПО
	|	втТранспортныеСредства.ТранспортноеСредство
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	|	ЕСТЬNULL(втТранспортныеСредстваПоРейсам.СуммаФактСНДСОборот, 0) КАК ФактическиеРасходыПоРейсу,
	|   	упУчетныеПараметрыТранспортныхСредств.СрокИспользования КАК СрокИспользования,
	|	упУчетныеПараметрыТранспортныхСредств.ПервичнаяСтоимость КАК ПервичнаяСтоимость,
	|	упУчетныеПараметрыТранспортныхСредств.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	упВыработкаТСОстатки.СуммаОстаток КАК ВыработкаОстаток,
	|	упВыработкаТСОстатки.ПробегОстаток КАК ПробегДоКапитальногоРемонта,
	|	упВыработкаТСОстатки.МоточасыОстаток КАК МоточасыДоКапитальногоРемонта,
	|	ЕСТЬNULL(втПробег.Пробег, 0) КАК Пробег,
	|	ЕСТЬNULL(втПробег.Моточасы, 0) КАК Моточасы,
	|	втТранспортныеСредства.СчетчикМоточасов КАК СчетчикМоточасов,
	|	ЕСТЬNULL(ПодразделенияТС.Подразделение, ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(НаправленияДеятельности.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	упАналитическиеРазрезыТ.Ссылка КАК АналитическийРазрез,
	|	ЕСТЬNULL(втТранспортныеСредстваПоРейсам.Организация, втТранспортныеСредства.Партнер.Организация) КАК Организация
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК втТранспортныеСредства
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТранспортныеСредстваПоРейсам КАК втТранспортныеСредстваПоРейсам
	|	ПО втТранспортныеСредства.Ссылка = втТранспортныеСредстваПоРейсам.ТранспортноеСредство
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упВыработкаТС.Остатки(
	|		&ДатаОкончания) КАК упВыработкаТСОстатки
	|	ПО втТранспортныеСредства.Ссылка = упВыработкаТСОстатки.ТранспортноеСредство
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(&ДатаОкончания,) КАК упУчетныеПараметрыТранспортныхСредств
	|	ПО втТранспортныеСредства.Ссылка = упУчетныеПараметрыТранспортныхСредств.ТранспортноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПробег КАК втПробег
	|	ПО втТранспортныеСредства.Ссылка = втПробег.ТранспортноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезыТ
	|	ПО ИСТИНА
	|		И упАналитическиеРазрезыТ.ТранспортноеСредство = втТранспортныеСредства.Ссылка
	|		И НЕ (упАналитическиеРазрезыТ.ПометкаУдаления)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПодразделенияТранспортныхСредств.СрезПоследних(
	|		&ДатаОкончания,
	|		) КАК ПодразделенияТС
	|	ПО ПодразделенияТС.ТранспортноеСредство = втТранспортныеСредства.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНаправленияДеятельностиТранспортныхСредств.СрезПоследних(
	|		&ДатаОкончания,
	|		) КАК НаправленияДеятельности
	|	ПО НаправленияДеятельности.ТранспортноеСредство = втТранспортныеСредства.Ссылка
	|ГДЕ ИСТИНА
	|	И упВыработкаТСОстатки.СуммаОстаток > 0
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРейсы.Рейс КАК Рейс
	|ИЗ
	|	втРейсы КАК втРейсы
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упУчетнаяПолитикаСрезПоследних.АмортизацияСтатьяРасходов КАК СтатьяРасходов,
	|	упУчетнаяПолитикаСрезПоследних.АмортизацияСтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	упУчетнаяПолитикаСрезПоследних.АмортизацияСтатьяРасходов.СтавкаНДС КАК СтавкаНДС,
	|	упУчетнаяПолитикаСрезПоследних.СпособУчетаВыработкиТранспортногоСредства КАК СпособУчетаВыработкиТранспортногоСредства,
	|	упУчетнаяПолитикаСрезПоследних.АмортизацияСтатьяРасходов.ВариантРасходов КАК ВариантРасходов,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ИЗ
	|	РегистрСведений.упУчетнаяПолитика.СрезПоследних(
	|		КОНЕЦПЕРИОДА(&ДатаНачала, МЕСЯЦ),
	|		) КАК упУчетнаяПолитикаСрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО упУчетнаяПолитикаСрезПоследних.АмортизацияСтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|";
				   
	мсвСтатусы = Новый Массив;
	мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполнен);
	мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);
	мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Завершен);
				   
	Запрос.УстановитьПараметр("мсвСтатусы", 	мсвСтатусы);
	Запрос.УстановитьПараметр("ВидДвижения", 	ВидДвиженияНакопления.Расход);
	
	Запрос.УстановитьПараметр("ДатаНачала", 	НачалоМесяца(РеквизитыОбъекта.Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", 	ДобавитьМесяц(НачалоМесяца(РеквизитыОбъекта.Дата), 1));
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексЗапросаПараметрыТС     = 4;
	ИндексЗапросаРейсыПоТС       = 5;
	ИндексЗапросаУчетнаяПолитика = 6;
	
	СтатьяРасходов       = Неопределено;
	ПравилоРаспределения = Неопределено;
	СтавкаНДС            = Перечисления.упСтавкиНДС.ПустаяСсылка();
	ЭтоПрямыеРасходы     = Истина;
	СпособУчетаВыработкиТранспортногоСредства = Неопределено;
	НуженАналитическийРазрез           = Ложь;
	НужнаАналитикаАгрегат              = Ложь;
	НужнаАналитикаВидПеревозки         = Ложь;
	НужнаАналитикаСотрудник            = Ложь;
	НужнаАналитикаТранспортноеСредство = Ложь;
	
	ВыборкаУчетнаяПолитика = мсвРезультатЗапроса[ИндексЗапросаУчетнаяПолитика].Выбрать();
	
	Если ВыборкаУчетнаяПолитика.Следующий() Тогда
		СтатьяРасходов       = ВыборкаУчетнаяПолитика.СтатьяРасходов;
		ПравилоРаспределения = ВыборкаУчетнаяПолитика.ПравилоРаспределения;
		СтавкаНДС            = ВыборкаУчетнаяПолитика.СтавкаНДС;
		СпособУчетаВыработкиТранспортногоСредства	= ВыборкаУчетнаяПолитика.СпособУчетаВыработкиТранспортногоСредства;
		ЭтоПрямыеРасходы     = ВыборкаУчетнаяПолитика.ЭтоПрямыеРасходы;
		
		НуженАналитическийРазрез           = ВыборкаУчетнаяПолитика.НуженАналитическийРазрез;
		НужнаАналитикаАгрегат              = ВыборкаУчетнаяПолитика.НужнаАналитикаАгрегат;
		НужнаАналитикаВидПеревозки         = ВыборкаУчетнаяПолитика.НужнаАналитикаВидПеревозки;
		НужнаАналитикаСотрудник            = ВыборкаУчетнаяПолитика.НужнаАналитикаСотрудник;
		НужнаАналитикаТранспортноеСредство = ВыборкаУчетнаяПолитика.НужнаАналитикаТранспортноеСредство;
		
	 КонецЕсли;
	 
	Если НЕ ЗначениеЗаполнено(СтатьяРасходов) Тогда
		ТекстСообщения = Нстр("ru = 'В учетной политике не задана статья расходов амортизации'");
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ И НЕ ЗначениеЗаполнено(ПравилоРаспределения) Тогда
		ТекстСообщения = Нстр("ru = 'В учетной политике у статьи расходов амортизации не задано правило распределения расходов'");
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ВыборкаТранспортныеСредства	= мсвРезультатЗапроса[ИндексЗапросаПараметрыТС].Выбрать();
		тбзРейсы	= мсвРезультатЗапроса[ИндексЗапросаРейсыПоТС].Выгрузить();
		
		НомерСтроки = 1;
		КоличествоСтрок = ВыборкаТранспортныеСредства.Количество();

		Пока ВыборкаТранспортныеСредства.Следующий() Цикл
			
			СуммаРасходов = 0;
			
			ТранспортноеСредство          = ВыборкаТранспортныеСредства.ТранспортноеСредство;
				
			ОстатокВыработка              = ВыборкаТранспортныеСредства.ВыработкаОстаток;
			ПервичнаяСтоимость            = ВыборкаТранспортныеСредства.ПервичнаяСтоимость;
			ЛиквидационнаяСтоимость       = ВыборкаТранспортныеСредства.ЛиквидационнаяСтоимость;
			
			ПробегДоКапитальногоРемонта   = ВыборкаТранспортныеСредства.ПробегДоКапитальногоРемонта;
			ПробегЗаМесяц                 = ВыборкаТранспортныеСредства.Пробег;
			МоточасыДоКапитальногоРемонта	= ВыборкаТранспортныеСредства.МоточасыДоКапитальногоРемонта;
			МоточасыЗаМесяц               = ВыборкаТранспортныеСредства.Моточасы;
			СчетчикМоточасов              = ВыборкаТранспортныеСредства.СчетчикМоточасов;
			СрокИспользования             = ВыборкаТранспортныеСредства.СрокИспользования;
						
			Если СпособУчетаВыработкиТранспортногоСредства = Перечисления.упСпособыУчетаВыработкиТранспортногоСредства.ПоСрокуИспользования Тогда
				Если НЕ СрокИспользования = 0 Тогда
					СуммаРасходов = Мин(ОстатокВыработка, (ПервичнаяСтоимость - ЛиквидационнаяСтоимость)/СрокИспользования);
				КонецЕсли;
			Иначе	
				Если СчетчикМоточасов Тогда
					Если МоточасыДоКапитальногоРемонта + МоточасыЗаМесяц > 0 Тогда	
						СуммаЗаМесяц  = ОстатокВыработка / (МоточасыДоКапитальногоРемонта + МоточасыЗаМесяц) * МоточасыЗаМесяц;
						СуммаРасходов = Мин(ОстатокВыработка, СуммаЗаМесяц);
					КонецЕсли;
				Иначе
					Если ПробегДоКапитальногоРемонта + ПробегЗаМесяц > 0 Тогда
						СуммаЗаМесяц  = ОстатокВыработка / (ПробегДоКапитальногоРемонта + ПробегЗаМесяц) * ПробегЗаМесяц;
						СуммаРасходов = Мин(ОстатокВыработка, СуммаЗаМесяц);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если СуммаРасходов = 0 Тогда
				Продолжить;	
			КонецЕсли;
			
			РеквизитыОбъекта.Вставить("ТранспортноеСредство", ТранспортноеСредство);
			РеквизитыОбъекта.Вставить("Дата",  КонецМесяца(РеквизитыОбъекта.Дата));

			
			Если ЭтоПрямыеРасходы Тогда
		
				тбзРасходы = Новый ТаблицаЗначений;
				тбзРасходы.Колонки.Добавить("СтатьяРасходов",       Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
				тбзРасходы.Колонки.Добавить("Задание",              Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
				тбзРасходы.Колонки.Добавить("ПравилоРаспределения", Новый ОписаниеТипов("СправочникСсылка.упПравилаРаспределенияРасходов"));
				тбзРасходы.Колонки.Добавить("Валюта",               Новый ОписаниеТипов("СправочникСсылка.Валюты"));
				тбзРасходы.Колонки.Добавить("Сумма",                Новый ОписаниеТипов("Число"));
				тбзРасходы.Колонки.Добавить("СтавкаНДС",            Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"));
				тбзРасходы.Колонки.Добавить("СуммаНДС",             Новый ОписаниеТипов("Число"));
				
				НоваяСтрокаРасходы = тбзРасходы.Добавить();
				НоваяСтрокаРасходы.СтатьяРасходов        = СтатьяРасходов;
				НоваяСтрокаРасходы.Задание               = Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка();
				НоваяСтрокаРасходы.ПравилоРаспределения  = ПравилоРаспределения;
				НоваяСтрокаРасходы.Валюта                = ВалютаУпрУчета;	
				НоваяСтрокаРасходы.Сумма                 = СуммаРасходов;	
				НоваяСтрокаРасходы.СтавкаНДС             = СтавкаНДС;	
				
				упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуНДС(НоваяСтрокаРасходы);
				
				тбзРейсыПоТС = Новый ТаблицаЗначений;
				тбзРейсыПоТС.Колонки.Добавить("Рейс", Новый ОписаниеТипов("ДокументСсылка.упРейс"));
				
				мсвСтрокиРейсы = тбзРейсы.НайтиСтроки(Новый Структура("ТранспортноеСредство",ТранспортноеСредство));
				Для каждого СтрокаРейс Из мсвСтрокиРейсы Цикл
					НоваяСтрокаРейсы 		= тбзРейсыПоТС.Добавить();
					НоваяСтрокаРейсы.Рейс 	= СтрокаРейс.Рейс;
				КонецЦикла;
					
				РеквизитыОбъекта.Вставить("СпособВводаРасходов", Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам);
				РеквизитыОбъекта.Вставить("Рейсы", тбзРейсыПоТС);
				ДополнительныеСвойства.Вставить("Расходы", тбзРасходы);

				упРаспределениеРасходов.ЗаполнитьДоходыРасходы_РегламентнаяОперация(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
				
			// Формируются фактические расходы
				упРаспределениеРасходов.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			Иначе
				
				тбзРасходы = Новый ТаблицаЗначений;
				тбзРасходы.Колонки.Добавить("ОбъектАналитическогоРазреза",  Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"));
				тбзРасходы.Колонки.Добавить("Валюта",               Новый ОписаниеТипов("СправочникСсылка.Валюты"));
				тбзРасходы.Колонки.Добавить("СтатьяРасходов",       Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
				тбзРасходы.Колонки.Добавить("ПравилоРаспределения", Новый ОписаниеТипов("СправочникСсылка.упПравилаРаспределенияРасходов"));
				тбзРасходы.Колонки.Добавить("АналитическийРазрез",  Новый ОписаниеТипов("СправочникСсылка.упАналитическиеРазрезы"));
				тбзРасходы.Колонки.Добавить("Подразделение",        Новый ОписаниеТипов("СправочникСсылка.упСтруктураПредприятия"));
				тбзРасходы.Колонки.Добавить("НаправлениеДеятельности",  Новый ОписаниеТипов("СправочникСсылка.упНаправленияДеятельности"));
				тбзРасходы.Колонки.Добавить("Организация",          Новый ОписаниеТипов("СправочникСсылка.Организации"));
				тбзРасходы.Колонки.Добавить("Сумма",                Новый ОписаниеТипов("Число"));
				тбзРасходы.Колонки.Добавить("СтавкаНДС",            Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"));
				тбзРасходы.Колонки.Добавить("СуммаНДС",             Новый ОписаниеТипов("Число"));
				тбзРасходы.Колонки.Добавить("НуженАналитическийРазрез",           Новый ОписаниеТипов("Булево"));
				тбзРасходы.Колонки.Добавить("НужнаАналитикаАгрегат",              Новый ОписаниеТипов("Булево"));
				тбзРасходы.Колонки.Добавить("НужнаАналитикаВидПеревозки",         Новый ОписаниеТипов("Булево"));
				тбзРасходы.Колонки.Добавить("НужнаАналитикаСотрудник",            Новый ОписаниеТипов("Булево"));
				тбзРасходы.Колонки.Добавить("НужнаАналитикаТранспортноеСредство", Новый ОписаниеТипов("Булево"));
				
				НоваяСтрокаРасходы = тбзРасходы.Добавить();
				НоваяСтрокаРасходы.ОбъектАналитическогоРазреза = ТранспортноеСредство;
				НоваяСтрокаРасходы.Валюта                = ВалютаУпрУчета;	
				НоваяСтрокаРасходы.СтатьяРасходов        = СтатьяРасходов;
				НоваяСтрокаРасходы.ПравилоРаспределения  = ПравилоРаспределения;
				НоваяСтрокаРасходы.АналитическийРазрез   = ВыборкаТранспортныеСредства.АналитическийРазрез;
				НоваяСтрокаРасходы.Подразделение         = ВыборкаТранспортныеСредства.Подразделение;
				НоваяСтрокаРасходы.НаправлениеДеятельности   = ВыборкаТранспортныеСредства.НаправлениеДеятельности;
				НоваяСтрокаРасходы.Организация           = ВыборкаТранспортныеСредства.Организация;
                    НоваяСтрокаРасходы.Сумма                 = СуммаРасходов;	
				НоваяСтрокаРасходы.СтавкаНДС             = СтавкаНДС;	
				
				
				
				упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуНДС(НоваяСтрокаРасходы);
				
				НоваяСтрокаРасходы.НуженАналитическийРазрез           = НуженАналитическийРазрез;	
				НоваяСтрокаРасходы.НужнаАналитикаАгрегат              = НужнаАналитикаАгрегат;	
				НоваяСтрокаРасходы.НужнаАналитикаВидПеревозки         = НужнаАналитикаВидПеревозки;	
				НоваяСтрокаРасходы.НужнаАналитикаСотрудник            = НужнаАналитикаСотрудник;	
				НоваяСтрокаРасходы.НужнаАналитикаТранспортноеСредство = НужнаАналитикаТранспортноеСредство;	
				
				упРаспределениеРасходов.ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзРасходы);
				
				сткИтогиПрочиеРасходы = Неопределено;
		
				// Пересчет в валюту управленческого учета.
				упРаспределениеРасходов.ПересчитатьСуммыВВалютуУпрУчета(тбзРасходы, Ложь, сткИтогиПрочиеРасходы, КонецМесяца(РеквизитыОбъекта.Дата), ВедетсяВалютныйУчет);
				
				// Таблицы для формирования движений.
				КолонкиСуммирования = "Сумма, СуммаБезНДС, СуммаНДС, СуммаВалюта, СуммаБезНДСВалюта, СуммаВалютаНДС";
				
				тбзРасходы.Свернуть("Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, ПравилоРаспределения, АналитическийРазрез, Валюта", КолонкиСуммирования);
				
				ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзРасходы);
							
				// Прочие расходы.
				упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
				
			КонецЕсли;
			
			РеквизитыОбъекта.Вставить("Сумма",                СуммаРасходов);
			РеквизитыОбъекта.Вставить("ПробегВыработка",      0);
			РеквизитыОбъекта.Вставить("МоточасыВыработка",    0);
			РеквизитыОбъекта.Вставить("ВидДвиженияВыработка", ВидДвиженияНакопления.Расход);

			
			// Формируется выработка
			упУчетТранспортныхСредств.СформироватьДвижениеВыработкаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			
			Если ФоновоеЗадание Тогда
				
				Процент = Окр((НомерОперации + НомерСтроки / КоличествоСтрок)  *100 / КоличествоОпераций);
				МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
				СтрокаВидОперации = Нстр("ru = 'Обрабатывается операция:%1'");
				СтрокаВидОперации = СтрШаблон(СтрокаВидОперации, "Начисление амортизации");
				МодульДлительныеОперации.СообщитьПрогресс(Процент, СтрокаВидОперации);
				
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;

			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Корректировка стоимости ГСМ. 
//
// Параметры:
//  ДополнительныеСвойства	 - 	 - 
//  Ссылка					 - 	 - 
//  Движения				 - 	 - 
//  РеквизитыОбъекта		 - 	 - 
//  Отказ					 - 	 - 
//
Процедура КорректировкаСтоимостиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ)
	
	сткТрассировка = ДополнительныеСвойства.сткИнформацияОбОшибках;
	сткПрогресс    = ДополнительныеСвойства.Прогресс;
	ФоновоеЗадание = сткПрогресс.ФоновоеЗадание;
	КоличествоОпераций = сткПрогресс.КоличествоОпераций;
	НомерОперации      = сткПрогресс.НомерОперации - 1;

		
	сткТаблицыСопоставления = СформироватьТаблицыДляСопоставления(НачалоМесяца(РеквизитыОбъекта.Дата), КонецМесяца(РеквизитыОбъекта.Дата));
	
	Если сткТаблицыСопоставления.ЕстьНеСопоставленныеСтроки Тогда
		ТекстСообщения = Нстр("ru = 'Не удалось сопоставить данные топливных компаний и заправок по путевым листам. См. отчет ""Анализ данных о заправках от топливных компаний""'");
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Запрос = сткТаблицыСопоставления.Запрос;
		тбзДанныеТопливныхКомпаний	= сткТаблицыСопоставления.ДанныеТопливныхКомпаний.Скопировать(,"НомерТранзакции, ТранспортноеСредство, ТопливнаяКартаОстаткиГСМ, Регистратор, ВидГСМТК, СуммаБезНДСТК, СуммаСНДСТК, СуммаНДСТК, КоличествоТК");
		
		// 1. Данные загруженные из файлов топливных компаний и распределенные по ТС, регистратору(путевой лист, заправки) и виду гсм.
		Запрос.Текст = "ВЫБРАТЬ
		               |	тбСопоставленныеДанныеТопливныхКомпаний.ТранспортноеСредство,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.ВидГСМТК КАК ВидГСМ,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.Регистратор,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.НомерТранзакции,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.КоличествоТК,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.СуммаБезНДСТК,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.СуммаСНДСТК,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.СуммаНДСТК,
		               |	тбСопоставленныеДанныеТопливныхКомпаний.ТопливнаяКартаОстаткиГСМ
		               |ПОМЕСТИТЬ втСопоставленныеДанныеТопливныхКомпаний
		               |ИЗ
		               |	&тбСопоставленныеДанныеТопливныхКомпаний КАК тбСопоставленныеДанныеТопливныхКомпаний
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втОстаткиГСМ.МоментВремени,
		               |	втОстаткиГСМ.ДатаДокумента,
		               |	втОстаткиГСМ.ТранспортноеСредство,
		               |	втОстаткиГСМ.ВидГСМ,
		               |	втСопоставленныеДанныеТопливныхКомпаний.СуммаСНДСТК - втОстаткиГСМ.Стоимость КАК Стоимость,
		               |	втСопоставленныеДанныеТопливныхКомпаний.СуммаНДСТК - втОстаткиГСМ.СтоимостьНДС КАК СтоимостьНДС,
		               |	втСопоставленныеДанныеТопливныхКомпаний.КоличествоТК - втОстаткиГСМ.Количество КАК Количество,
		               |	втОстаткиГСМ.ТопливнаяКарта,
		               |	втСопоставленныеДанныеТопливныхКомпаний.СуммаСНДСТК,
		               |	втСопоставленныеДанныеТопливныхКомпаний.СуммаНДСТК,
		               |	втОстаткиГСМ.Регистратор
		               |ПОМЕСТИТЬ втИзмененнаяСтоимость
		               |ИЗ
		               |	втОстаткиГСМ КАК втОстаткиГСМ
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСопоставленныеДанныеТопливныхКомпаний КАК втСопоставленныеДанныеТопливныхКомпаний
		               |		ПО втОстаткиГСМ.ТопливнаяКарта = втСопоставленныеДанныеТопливныхКомпаний.ТопливнаяКартаОстаткиГСМ
		               |			И втОстаткиГСМ.Регистратор = втСопоставленныеДанныеТопливныхКомпаний.Регистратор
		               |			И втОстаткиГСМ.ТранспортноеСредство = втСопоставленныеДанныеТопливныхКомпаний.ТранспортноеСредство
		               |			И втОстаткиГСМ.ВидГСМ = втСопоставленныеДанныеТопливныхКомпаний.ВидГСМ
		               |ГДЕ
		               |	(НЕ втСопоставленныеДанныеТопливныхКомпаний.СуммаСНДСТК = втОстаткиГСМ.Стоимость
		               |			ИЛИ НЕ втСопоставленныеДанныеТопливныхКомпаний.СуммаНДСТК = втОстаткиГСМ.СтоимостьНДС)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втОстаткиГСМ.ТранспортноеСредство,
		               |	втОстаткиГСМ.ВидГСМ,
		               |	МИНИМУМ(втОстаткиГСМ.ДатаДокумента) КАК ДатаДокумента
		               |ПОМЕСТИТЬ втДатыИзмененияСтоимости
		               |ИЗ
		               |	втИзмененнаяСтоимость КАК втОстаткиГСМ
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втОстаткиГСМ.ТранспортноеСредство,
		               |	втОстаткиГСМ.ВидГСМ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	МИНИМУМ(втДатыИзмененияСтоимости.ДатаДокумента) КАК ДатаДокумента
		               |ПОМЕСТИТЬ втНижняяГраницаПутевыхЛистов
		               |ИЗ
		               |	втДатыИзмененияСтоимости КАК втДатыИзмененияСтоимости
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упПутевойЛист.Ссылка,
		               |	упПутевойЛист.Дата
		               |ПОМЕСТИТЬ втВсеПутевыеЛистыСНужнымиСтатусами
		               |ИЗ
		               |	втНижняяГраницаПутевыхЛистов КАК втНижняяГраницаПутевыхЛистов
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК упПутевойЛист
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
		               |			ПО упПутевойЛист.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ
		               |				И (упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Возвращен)
		               |					ИЛИ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Закрыт))
		               |		ПО втНижняяГраницаПутевыхЛистов.ДатаДокумента <= упПутевойЛист.Дата
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втДатыИзмененияСтоимости.ТранспортноеСредство КАК ТранспортноеСредство,
		               |	втДатыИзмененияСтоимости.ВидГСМ КАК ВидГСМ,
		               |	упПутевойЛистГСМ.Ссылка,
		               |	упПутевойЛистГСМ.Ссылка.МоментВремени,
		               |	упПутевойЛистГСМ.НачОстаток,
		               |	упПутевойЛистГСМ.КонОстаток,
		               |	упПутевойЛистГСМ.РасходТопливаФакт,
		               |	упПутевойЛистГСМ.СуммаРасходов,
		               |	упПутевойЛистГСМ.СуммаРасходовНДС
		               |ПОМЕСТИТЬ втГСМПоПутевымЛистам
		               |ИЗ
		               |	втВсеПутевыеЛистыСНужнымиСтатусами КАК втВсеПутевыеЛистыСНужнымиСтатусами
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.ГСМ КАК упПутевойЛистГСМ
		               |		ПО втВсеПутевыеЛистыСНужнымиСтатусами.Ссылка = упПутевойЛистГСМ.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДатыИзмененияСтоимости КАК втДатыИзмененияСтоимости
		               |		ПО (втДатыИзмененияСтоимости.ДатаДокумента <= упПутевойЛистГСМ.Ссылка.Дата)
		               |			И (втДатыИзмененияСтоимости.ВидГСМ = упПутевойЛистГСМ.ВидГСМ)
		               |			И (втДатыИзмененияСтоимости.ТранспортноеСредство = ВЫБОР
		               |				КОГДА упПутевойЛистГСМ.ДопОборудование = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
		               |					ТОГДА упПутевойЛистГСМ.Ссылка.ТранспортноеСредство
		               |				ИНАЧЕ упПутевойЛистГСМ.ДопОборудование
		               |			КОНЕЦ)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втГСМПоПутевымЛистам.ТранспортноеСредство,
		               |	упПутевойЛистЗаправкиСливыГСМ.ВидГСМ,
		               |	упПутевойЛистЗаправкиСливыГСМ.Ссылка,
		               |	СУММА(ВЫБОР
		               |			КОГДА упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
		               |				ТОГДА упПутевойЛистЗаправкиСливыГСМ.Стоимость
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК СтоимостьЗаправка,
		               |	СУММА(ВЫБОР
		               |			КОГДА упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
		               |				ТОГДА упПутевойЛистЗаправкиСливыГСМ.СтоимостьНДС
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК СтоимостьНДСЗаправка,
		               |	СУММА(ВЫБОР
		               |			КОГДА упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
		               |				ТОГДА упПутевойЛистЗаправкиСливыГСМ.Количество
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК КоличествоЗаправка,
		               |	СУММА(ВЫБОР
		               |			КОГДА упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		               |				ТОГДА упПутевойЛистЗаправкиСливыГСМ.Стоимость
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК СтоимостьСлив,
		               |	СУММА(ВЫБОР
		               |			КОГДА упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		               |				ТОГДА упПутевойЛистЗаправкиСливыГСМ.СтоимостьНДС
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК СтоимостьНДССлив,
		               |	СУММА(ВЫБОР
		               |			КОГДА упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		               |				ТОГДА упПутевойЛистЗаправкиСливыГСМ.Количество
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК КоличествоСлив
		               |ПОМЕСТИТЬ втЗаправки
		               |ИЗ
		               |	втГСМПоПутевымЛистам КАК втГСМПоПутевымЛистам
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.ЗаправкиСливыГСМ КАК упПутевойЛистЗаправкиСливыГСМ
		               |		ПО втГСМПоПутевымЛистам.Ссылка = упПутевойЛистЗаправкиСливыГСМ.Ссылка
		               |			И втГСМПоПутевымЛистам.ВидГСМ = упПутевойЛистЗаправкиСливыГСМ.ВидГСМ
		               |			И (втГСМПоПутевымЛистам.ТранспортноеСредство = ВЫБОР
		               |				КОГДА упПутевойЛистЗаправкиСливыГСМ.ДопОборудование = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
		               |					ТОГДА упПутевойЛистЗаправкиСливыГСМ.Ссылка.ТранспортноеСредство
		               |				ИНАЧЕ упПутевойЛистЗаправкиСливыГСМ.ДопОборудование
		               |			КОНЕЦ)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втГСМПоПутевымЛистам.ТранспортноеСредство,
		               |	упПутевойЛистЗаправкиСливыГСМ.ВидГСМ,
		               |	упПутевойЛистЗаправкиСливыГСМ.Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втИзмененнаяСтоимость.МоментВремени,
		               |	втИзмененнаяСтоимость.ДатаДокумента,
		               |	втИзмененнаяСтоимость.ТранспортноеСредство,
		               |	втИзмененнаяСтоимость.ВидГСМ,
		               |	втИзмененнаяСтоимость.Стоимость,
		               |	втИзмененнаяСтоимость.Количество,
		               |	втИзмененнаяСтоимость.СтоимостьНДС,
		               |	втИзмененнаяСтоимость.ТопливнаяКарта,
		               |	втИзмененнаяСтоимость.СуммаСНДСТК,
		               |	втИзмененнаяСтоимость.СуммаНДСТК,
		               |	втИзмененнаяСтоимость.Регистратор
		               |ИЗ
		               |	втИзмененнаяСтоимость КАК втИзмененнаяСтоимость
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	втПутевыеЛисты.Ссылка КАК ПутевойЛист,
		               |	упПутевойЛистРейсы.Рейс
		               |ИЗ
		               |	втГСМПоПутевымЛистам КАК втПутевыеЛисты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
		               |		ПО втПутевыеЛисты.Ссылка = упПутевойЛистРейсы.Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втГСМПоПутевымЛистам.ТранспортноеСредство КАК ТранспортноеСредство,
		               |	втГСМПоПутевымЛистам.ВидГСМ КАК ВидГСМ,
		               |	втГСМПоПутевымЛистам.Ссылка,
		               |	втГСМПоПутевымЛистам.МоментВремени КАК МоментВремени,
		               |	втГСМПоПутевымЛистам.НачОстаток,
		               |	втГСМПоПутевымЛистам.КонОстаток,
		               |	втГСМПоПутевымЛистам.РасходТопливаФакт,
		               |	втГСМПоПутевымЛистам.СуммаРасходов,
		               |	втГСМПоПутевымЛистам.СуммаРасходовНДС,
		               |	ЕСТЬNULL(втЗаправки.СтоимостьСлив, 0) КАК СтоимостьСлив,
		               |	ЕСТЬNULL(втЗаправки.СтоимостьНДССлив, 0) КАК СтоимостьНДССлив,
		               |	ЕСТЬNULL(втЗаправки.КоличествоСлив, 0) КАК КоличествоСлив,
		               |	ЕСТЬNULL(втИзмененнаяСтоимость.СуммаСНДСТК, ЕСТЬNULL(втЗаправки.СтоимостьЗаправка, 0)) КАК СтоимостьЗаправки,
		               |	ЕСТЬNULL(втИзмененнаяСтоимость.СуммаНДСТК, ЕСТЬNULL(втЗаправки.СтоимостьНДСЗаправка, 0)) КАК СтоимостьЗаправкиНДС,
		               |	ЕСТЬNULL(втЗаправки.КоличествоЗаправка, 0) КАК КоличествоЗаправка,
		               |	втИзмененнаяСтоимость.ТопливнаяКарта,
		               |	втИзмененнаяСтоимость.Регистратор КАК Регистратор,
		               |	втИзмененнаяСтоимость.ДатаДокумента
		               |ИЗ
		               |	втГСМПоПутевымЛистам КАК втГСМПоПутевымЛистам
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втЗаправки КАК втЗаправки
		               |		ПО втГСМПоПутевымЛистам.Ссылка = втЗаправки.Ссылка
		               |			И втГСМПоПутевымЛистам.ВидГСМ = втЗаправки.ВидГСМ
		               |			И втГСМПоПутевымЛистам.ТранспортноеСредство = втЗаправки.ТранспортноеСредство
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втИзмененнаяСтоимость КАК втИзмененнаяСтоимость
		               |		ПО втГСМПоПутевымЛистам.Ссылка = втИзмененнаяСтоимость.Регистратор
		               |			И втГСМПоПутевымЛистам.ВидГСМ = втИзмененнаяСтоимость.ВидГСМ
		               |			И втГСМПоПутевымЛистам.ТранспортноеСредство = втИзмененнаяСтоимость.ТранспортноеСредство
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	упЗаправкаСливГСМ.ТранспортноеСредство,
		               |	упЗаправкаСливГСМ.ВидГСМ,
		               |	упЗаправкаСливГСМ.Ссылка,
		               |	упЗаправкаСливГСМ.МоментВремени,
		               |	0,
		               |	0,
		               |	0,
		               |	0,
		               |	0,
		               |	ВЫБОР
		               |		КОГДА упЗаправкаСливГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		               |			ТОГДА упЗаправкаСливГСМ.Стоимость
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА упЗаправкаСливГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		               |			ТОГДА упЗаправкаСливГСМ.СтоимостьНДС
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА упЗаправкаСливГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		               |			ТОГДА упЗаправкаСливГСМ.Количество
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА упЗаправкаСливГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
		               |			ТОГДА ЕСТЬNULL(втИзмененнаяСтоимость.СуммаСНДСТК, упЗаправкаСливГСМ.Стоимость)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА упЗаправкаСливГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
		               |			ТОГДА ЕСТЬNULL(втИзмененнаяСтоимость.СуммаНДСТК, упЗаправкаСливГСМ.СтоимостьНДС)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА упЗаправкаСливГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
		               |			ТОГДА упЗаправкаСливГСМ.Количество
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	втИзмененнаяСтоимость.ТопливнаяКарта,
		               |	втИзмененнаяСтоимость.Регистратор,
		               |	втИзмененнаяСтоимость.ДатаДокумента
		               |ИЗ
		               |	втДатыИзмененияСтоимости КАК втДатыИзмененияСтоимости
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаправкаСливГСМ КАК упЗаправкаСливГСМ
		               |		ПО втДатыИзмененияСтоимости.ТранспортноеСредство = упЗаправкаСливГСМ.ТранспортноеСредство
		               |			И втДатыИзмененияСтоимости.ВидГСМ = упЗаправкаСливГСМ.ВидГСМ
		               |			И втДатыИзмененияСтоимости.ДатаДокумента <= упЗаправкаСливГСМ.Дата
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втИзмененнаяСтоимость КАК втИзмененнаяСтоимость
		               |		ПО упЗаправкаСливГСМ.Ссылка = втИзмененнаяСтоимость.Регистратор
		               |			И упЗаправкаСливГСМ.ВидГСМ = втИзмененнаяСтоимость.ВидГСМ
					   |			И упЗаправкаСливГСМ.ТранспортноеСредство = втИзмененнаяСтоимость.ТранспортноеСредство
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	МоментВремени
		               |ИТОГИ ПО
		               |	ВидГСМ,
		               |	ТранспортноеСредство
					   |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упУчетнаяПолитикаСрезПоследних.ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива
		               |ИЗ
		               |	РегистрСведений.упУчетнаяПолитика.СрезПоследних(КОНЕЦПЕРИОДА(&ДатаНачала, МЕСЯЦ), ) КАК упУчетнаяПолитикаСрезПоследних";
					   
		Запрос.УстановитьПараметр("тбСопоставленныеДанныеТопливныхКомпаний", тбзДанныеТопливныхКомпаний);
		Запрос.УстановитьПараметр("ДатаНачала", РеквизитыОбъекта.Дата);
		
		мсвРезультат = Запрос.ВыполнитьПакет();
		ИндексЗапросаОстаткиГСМ 		= 7;
		ИндексЗапросаРейсы 				= 8;
		ИндексЗапросаРасходы 			= 9;
		ИндексЗапросаУчетнаяПолитика 	= 10;
						
		тбзРейсыПоПутевымЛистам = мсвРезультат[ИндексЗапросаРейсы].Выгрузить();
		тбзОстаткиГСМ 			= мсвРезультат[ИндексЗапросаОстаткиГСМ].Выгрузить();
				
		// Находится допустимое отклонение остатков ГСМ от данных загружаемых от топливных компаний.
		Выборка = мсвРезультат[ИндексЗапросаУчетнаяПолитика].Выбрать();
		ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива = 0;
		Если Выборка.Следующий() Тогда
			ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива = Выборка.ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива;
		КонецЕсли;
		
		Отклонение = тбзОстаткиГСМ.Итог("Количество");
		
		// Если допустимое отклонение задано и отклонение по количеству превышает допустимое.
		Если НЕ ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива = 0
			И Отклонение > ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива Тогда
			
			ТекстСообщения = Нстр("ru = 'Превышено допустимое отклоненение %1 л от топливных компаний по количеству. Отклонение %2 л.""'");
			ТекстСообщения = СтрШаблон(ТекстСообщения,ДопустимоеОтклонениеДанныхОтТопливныхКомпанийПоКоличествуТоплива, Отклонение);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
			Отказ = Истина;
			
		КонецЕсли;
		
		// Для отклонения формируются движения по суммам в регистре "Остатки ГСМ".
		Если НЕ Отказ Тогда
			тбзОстаткиГСМДвижения = упПутевойЛистУчетГСМ.СтруктураТаблицыОстаткиГСМ();
		
			Для каждого Строка Из тбзОстаткиГСМ Цикл
				НоваяСтрока	= тбзОстаткиГСМДвижения.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.Период					= РеквизитыОбъекта.Дата;
				НоваяСтрока.Количество				= 0;
				НоваяСтрока.ВидДвиженияТоплива		= Перечисления.упВидыДвиженияТоплива.Заправка;
			КонецЦикла;
			
			ДополнительныеСвойства.Вставить("тбзОстаткиГСМ", тбзОстаткиГСМДвижения);
			
			упПутевойЛистУчетГСМ.СформироватьДвижениеОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		КонецЕсли;
		
		// Для всех путевых листов 
		// Формируются отклонения по регистру Расходы
		Если НЕ Отказ Тогда
			
			дрвПутевыеЛисты = мсвРезультат[ИндексЗапросаРасходы].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			НомерСтроки = 1;
			КоличествоСтрок = дрвПутевыеЛисты.Строки.Количество();

			Для каждого СтрокаВидГСМ Из дрвПутевыеЛисты.Строки Цикл
				
				ВидГСМ = СтрокаВидГСМ.ВидГСМ;
				
				Для каждого СтрокаТранспортноеСредство Из СтрокаВидГСМ.Строки Цикл
					
					ТранспортноеСредство 	= СтрокаТранспортноеСредство.ТранспортноеСредство;
					ПересчетРасходов 		= Ложь;
					НачОстатокСумма			= 0;
					НачОстатокСуммаНДС		= 0;
					НачОстатокКоличество 	= 0;
					
					Для каждого СтрокаПутевойЛист Из СтрокаТранспортноеСредство.Строки Цикл
						
						ПутевойЛист = СтрокаПутевойЛист.Ссылка;
						
						Если ЗначениеЗаполнено(СтрокаПутевойЛист.Регистратор) И НЕ ПересчетРасходов Тогда
							сткОстаткиСтоимость = ОстаткиСтоимость(ТранспортноеСредство, ВидГСМ, СтрокаПутевойЛист.ДатаДокумента, СтрокаПутевойЛист.Регистратор);
							НачОстатокСумма 		= сткОстаткиСтоимость.Стоимость;
							НачОстатокСуммаНДС 		= сткОстаткиСтоимость.СтоимостьНДС;
							НачОстатокКоличество	= сткОстаткиСтоимость.Количество;
							ПересчетРасходов 	= Истина;
						КонецЕсли;
						
						Если ПересчетРасходов Тогда
							
							Если ТипЗнч(СтрокаПутевойЛист.Ссылка) = Тип("ДокументСсылка.упЗаправкаСливГСМ")  Тогда
								НачОстатокСумма 	 = НачОстатокСумма 		+ СтрокаПутевойЛист.СтоимостьЗаправки;
								НачОстатокСуммаНДС	 = НачОстатокСуммаНДС 	+ СтрокаПутевойЛист.СтоимостьЗаправкиНДС;
								НачОстатокКоличество = НачОстатокКоличество	+ СтрокаПутевойЛист.КоличествоЗаправка;	
								Продолжить;							
							КонецЕсли;
							
							тбзРейсы = тбзРейсыПоПутевымЛистам.Скопировать(Новый Структура("ПутевойЛист",ПутевойЛист), "Рейс");
							РеквизитыОбъекта.Вставить("Рейсы", тбзРейсы);
							
							Стоимость 			= НачОстатокСумма;
							СтоимостьНДС		= НачОстатокСуммаНДС;
							Количество			= НачОстатокКоличество;
							
							// Считаются сливы							
							Если НЕ СтрокаПутевойЛист.КоличествоСлив = 0 
									И НЕ Количество = 0 Тогда
								Если НЕ СтрокаПутевойЛист.РасходТопливаФакт = СтрокаПутевойЛист.КоличествоСлив
										И Количество Тогда
									СтоимостьЕдиницы 	= Окр(Стоимость / Количество, 2);
									СуммаСлив		= СтоимостьЕдиницы * СтрокаПутевойЛист.КоличествоСлив;	
									
									СтоимостьЕдиницыНДС = Окр(СтоимостьНДС / Количество, 2);
									СуммаСливНДС	= СтоимостьЕдиницыНДС * СтрокаПутевойЛист.КоличествоСлив;	
									
								Иначе
									СуммаСлив		= Стоимость;
									СуммаСливНДС	= СтоимостьНДС;
								КонецЕсли;
								Стоимость 			= Стоимость - СуммаСлив;
								СтоимостьНДС		= СтоимостьНДС - СуммаСливНДС;
								Количество 			= Количество - СтрокаПутевойЛист.КоличествоСлив;
							КонецЕсли;
							
							// Считаются заправки
							Стоимость 			= Стоимость 	+ СтрокаПутевойЛист.СтоимостьЗаправки;
							СтоимостьНДС		= СтоимостьНДС 	+ СтрокаПутевойЛист.СтоимостьЗаправкиНДС;
							Количество			= Количество	+ СтрокаПутевойЛист.КоличествоЗаправка;
							
							
							// Считается расход
							СуммаРасходов = 0;
							СуммаРасходовНДС = 0;
							
							Если НЕ Количество = 0 Тогда
								Если НЕ СтрокаПутевойЛист.РасходТопливаФакт = Количество 
									И НЕ Количество = 0 Тогда
									СтоимостьЕдиницы 	= Окр(Стоимость / Количество, 2);
									СуммаРасходов		= СтоимостьЕдиницы * СтрокаПутевойЛист.РасходТопливаФакт;	
									
									СтоимостьЕдиницыНДС = Окр(СтоимостьНДС / Количество, 2);
									СуммаРасходовНДС	= СтоимостьЕдиницыНДС * СтрокаПутевойЛист.РасходТопливаФакт;	
									
								Иначе
									СуммаРасходов		= Стоимость;
									СуммаРасходовНДС	= СтоимостьНДС;
								КонецЕсли;
							КонецЕсли;
							
							НачОстатокСумма 		= Стоимость - СуммаРасходов;
							НачОстатокСуммаНДС 		= СтоимостьНДС - СуммаРасходовНДС;
							НачОстатокКоличество	= Количество - СтрокаПутевойЛист.РасходТопливаФакт;
							
							ОтклонениеРасходов		= СуммаРасходов - СтрокаПутевойЛист.СуммаРасходов;
							ОтклонениеРасходовНДС	= СуммаРасходовНДС - СтрокаПутевойЛист.СуммаРасходовНДС;
																					
							тбзГСМ = Новый ТаблицаЗначений;
							тбзГСМ.Колонки.Добавить("ВидГСМ", 				Новый ОписаниеТипов("СправочникСсылка.упВидыГСМ"));
							тбзГСМ.Колонки.Добавить("СуммаРасходовПлан", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
							тбзГСМ.Колонки.Добавить("СуммаРасходовПланНДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
							тбзГСМ.Колонки.Добавить("СуммаРасходов", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
							тбзГСМ.Колонки.Добавить("СуммаРасходовНДС", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
							
							НоваяСтрокаГСМ = тбзГСМ.Добавить();
							НоваяСтрокаГСМ.ВидГСМ 				= ВидГСМ;
							НоваяСтрокаГСМ.СуммаРасходовПлан 	= 0;
							НоваяСтрокаГСМ.СуммаРасходовПланНДС = 0;
							НоваяСтрокаГСМ.СуммаРасходов 		= ОтклонениеРасходов;
							НоваяСтрокаГСМ.СуммаРасходовНДС 	= ОтклонениеРасходовНДС;
							
							РеквизитыОбъекта.Вставить("ГСМ", тбзГСМ);
							ДополнительныеСвойства.Вставить("Статус", Неопределено);
							упПутевойЛистУчетГСМ.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
													
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
				Если ФоновоеЗадание Тогда
					
					Процент = Окр((НомерОперации + НомерСтроки / КоличествоСтрок)  *100 / КоличествоОпераций);
					МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
					СтрокаВидОперации = Нстр("ru = 'Обрабатывается операция:%1'");
					СтрокаВидОперации = СтрШаблон(СтрокаВидОперации, "Начисление амортизации");
					МодульДлительныеОперации.СообщитьПрогресс(Процент, СтрокаВидОперации);
					
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
			
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ОстаткиСтоимость(ТранспортноеСредство, ВидГСМ, Дата, Ссылка)
	
	сткРезультат = Новый Структура("Стоимость, СтоимостьНДС, Количество",0,0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМОстатки.СтоимостьОстаток КАК Стоимость,
	               |	упОстаткиГСМОстатки.СтоимостьНДСОстаток КАК СтоимостьНДС,
	               |	упОстаткиГСМОстатки.КоличествоОстаток КАК Количество
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ.Остатки(
	               |			&Период,
	               |			ВидГСМ = &ВидГСМ
	               |				И ТранспортноеСредство = &ТранспортноеСредство) КАК упОстаткиГСМОстатки
	               |ГДЕ
	               |	упОстаткиГСМОстатки.ТранспортноеСредство = &ТранспортноеСредство
	               |	И упОстаткиГСМОстатки.ВидГСМ = &ВидГСМ";
	Запрос.УстановитьПараметр("Период", 			Новый Граница(Новый МоментВремени(Дата, Ссылка), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	Запрос.УстановитьПараметр("ВидГСМ", 				ВидГСМ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЕсли;
	Возврат сткРезультат;
КонецФункции

// Процедура - Распределение косвенных расходов. 
//
// Параметры:
//  ДополнительныеСвойства	 - 	 - 
//  Ссылка					 - 	 - 
//  Движения				 - 	 - 
//  РеквизитыОбъекта		 - 	 - 
//  Отказ					 - 	 - 
//
Процедура РаспределениеКосвенныхРасходов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ)
	
	сткТрассировка = ДополнительныеСвойства.сткИнформацияОбОшибках;
	сткПрогресс    = ДополнительныеСвойства.Прогресс;
	ФоновоеЗадание = сткПрогресс.ФоновоеЗадание;
	КоличествоОпераций = сткПрогресс.КоличествоОпераций;
	НомерОперации      = сткПрогресс.НомерОперации - 1;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упУчетнаяПолитика.РаспределятьПрочиеРасходыСТочностьюДоАналитики, ЛОЖЬ)
	|			ТОГДА ПрочиеРасходы_Остатки.АналитическийРазрез
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)
	|	КОНЕЦ КАК АналитическийРазрез,
	|	ПрочиеРасходы_Остатки.Валюта КАК Валюта,
	|	ПрочиеРасходы_Остатки.Организация КАК Организация,
	|	ПрочиеРасходы_Остатки.СтатьяРасходов КАК СтатьяРасходов,
	|	МАКСИМУМ(ЕСТЬNULL(упРаспределениеПрочихРасходовТ.Ссылка, ЗНАЧЕНИЕ(Документ.упРаспределениеПрочихРасходов.ПустаяСсылка))) КАК Ссылка
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.упПрочиеРасходы.Остатки(
	|		&НачалоСледующегоПериода,
	|		СтатьяРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРасходов.Косвенные)) КАК ПрочиеРасходы_Остатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУчетнаяПолитика.СрезПоследних(
	|		&КонецМесяца) КАК упУчетнаяПолитика
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРаспределениеПрочихРасходов КАК упРаспределениеПрочихРасходовТ
	|	ПО ИСТИНА
	|		И упРаспределениеПрочихРасходовТ.АналитическийРазрез = ПрочиеРасходы_Остатки.АналитическийРазрез
	|		И упРаспределениеПрочихРасходовТ.Валюта = ПрочиеРасходы_Остатки.Валюта
	|		И упРаспределениеПрочихРасходовТ.НаправлениеДеятельности = ПрочиеРасходы_Остатки.НаправлениеДеятельности
	|		И упРаспределениеПрочихРасходовТ.Организация = ПрочиеРасходы_Остатки.Организация
	|		И упРаспределениеПрочихРасходовТ.Подразделение = ПрочиеРасходы_Остатки.Подразделение
	|		И упРаспределениеПрочихРасходовТ.СтатьяРасходов = ПрочиеРасходы_Остатки.СтатьяРасходов
	|		И упРаспределениеПрочихРасходовТ.ДокументОснование = &ДокументОснование
	|		И НЕ (упРаспределениеПрочихРасходовТ.ПометкаУдаления)
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упУчетнаяПолитика.РаспределятьПрочиеРасходыСТочностьюДоАналитики, ЛОЖЬ)
	|			ТОГДА ПрочиеРасходы_Остатки.АналитическийРазрез
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)
	|	КОНЕЦ,
	|	ПрочиеРасходы_Остатки.Валюта,
	|	ПрочиеРасходы_Остатки.Организация,
	|	ПрочиеРасходы_Остатки.СтатьяРасходов
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиТ.АналитическийРазрез КАК АналитическийРазрез,
	|	втОстаткиТ.Валюта КАК Валюта,
	|	втОстаткиТ.Организация КАК Организация,
	|	втОстаткиТ.Ссылка КАК Ссылка,
	|	втОстаткиТ.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.ПорядокРаспределения КАК СтатьяРасходовПорядокРаспределения,
	|	СтатьиРасходов.ПравилоРаспределенияКосвенныхРасходовПоРейсам КАК ПравилоРаспределенияПоРейсам,
	|	СтатьиРасходов.ПравилоРаспределенияКосвенныхРасходовПоТС КАК ПравилоРаспределенияПоТС,
	|	СтатьиРасходов.ПравилоРаспределенияКосвенныхРасходовНаФинансовыйРезультат КАК ПравилоРаспределенияНаФинансовыйРезультат
	|ИЗ
	|	втОстатки КАК втОстаткиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК СтатьиРасходов
	|	ПО СтатьиРасходов.Ссылка = втОстаткиТ.СтатьяРасходов
	|УПОРЯДОЧИТЬ ПО
	|	СтатьиРасходов.ПорядокРаспределения	
	|";



	НачалоСледующегоПериода = НачалоДня(НачалоМесяца(ДобавитьМесяц(РеквизитыОбъекта.Дата, 1)));
	КонецМесяца = НачалоСледующегоПериода - 1;
	
	Запрос.УстановитьПараметр("НачалоСледующегоПериода", НачалоСледующегоПериода);
	Запрос.УстановитьПараметр("КонецМесяца",             КонецМесяца);
	Запрос.УстановитьПараметр("ДокументОснование",       РеквизитыОбъекта.Объект.Ссылка);
	
	НомерСтроки = 1;
	Выборка = Запрос.Выполнить().Выбрать();
	КоличествоСтрок = Выборка.Количество();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			РаспределениеПрочихРасходовОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Иначе	
			РаспределениеПрочихРасходовОбъект = Документы.упРаспределениеПрочихРасходов.СоздатьДокумент();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(РаспределениеПрочихРасходовОбъект, Выборка);
		РаспределениеПрочихРасходовОбъект.Дата = РеквизитыОбъекта.Дата;
		РаспределениеПрочихРасходовОбъект.ПериодРаспределенияЗатратНачало    = НачалоМесяца(РеквизитыОбъекта.Дата);
		РаспределениеПрочихРасходовОбъект.ПериодРаспределенияЗатратОкончание = КонецМесяца(РеквизитыОбъекта.Дата);
		РаспределениеПрочихРасходовОбъект.ДокументОснование = РеквизитыОбъекта.Объект.Ссылка;
		упРаспределениеРасходов.ЗаполнитьПрочиеРасходы(РаспределениеПрочихРасходовОбъект, Отказ);
		РаспределениеПрочихРасходовОбъект.ЗаполнитьРасходы(,,,сткТрассировка);
		
		Попытка
			Если РаспределениеПрочихРасходовОбъект.ПроверитьЗаполнение() Тогда
				РаспределениеПрочихРасходовОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			Иначе
				ТекстСообщения = Нстр("ru = 'Не удалось распределить прочие расходы: Организация:%1; СтатьяРасходов:%2; Валюта:%3; Аналитический разрез:%4; Ошибка при проверке заполнения документа.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Организация, Выборка.СтатьяРасходов, Выборка.Валюта, Выборка.АналитическийРазрез);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
				//Отказ = Истина;

			КонецЕсли;
		Исключение
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			Событие = Нстр("ru = 'Не удалось распределить прочие расходы'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,, , ОписаниеОшибки); 
			
			ТекстСообщения = Нстр("ru = 'Не удалось распределить прочие расходы: Организация:%1; СтатьяРасходов:%2; Валюта:%3; Аналитический разрез:%4; Ошибка:%5'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Организация, Выборка.СтатьяРасходов, Выборка.Валюта, Выборка.АналитическийРазрез, ОписаниеОшибки);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
			Отказ = Истина;
		КонецПопытки;

		Если ФоновоеЗадание Тогда
			
	          Процент = Окр((НомерОперации + НомерСтроки / КоличествоСтрок)  *100 / КоличествоОпераций);
			МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
			СтрокаВидОперации = Нстр("ru = 'Обрабатывается операция:%1'");
			СтрокаВидОперации = СтрШаблон(СтрокаВидОперации, "Распределение косвенных расходов");
			МодульДлительныеОперации.СообщитьПрогресс(Процент, СтрокаВидОперации);
			
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Процедура проведения операции закрытия. 
//	   Результат записывается во временное хранилище по переданному адресу.
//
// Параметры:
//  сткПараметры - Структура - Параметры.
//  АдресХранилища  - Строка - Адрес.
//
Процедура ПровестиОперациюЗакрытия(сткПараметры, АдресХранилища) Экспорт
	
	Ссылка = сткПараметры.Ссылка;
	
	сткВозврата = Новый Структура();
	сткВозврата.Вставить("ВыполненоУспешно", 	Истина);
	сткВозврата.Вставить("Ссылка", 				Ссылка);
	
	РегламентнаяОперацияОбъект = Ссылка.ПолучитьОбъект();
	
	сткПрогресс = Новый Структура();
	сткПрогресс.Вставить("НомерОперации",      1);
	сткПрогресс.Вставить("КоличествоОпераций", 1);
	сткПрогресс.Вставить("ФоновоеЗадание",     сткПараметры.ФоновоеЗадание);
	
	Отказ = Ложь;
	РегламентнаяОперацияОбъект.ПровестиОперациюЗакрытия(сткПрогресс, Отказ);
	сткВозврата.Вставить("ВыполненоУспешно", 	Отказ = Ложь);
		
	ПоместитьВоВременноеХранилище(сткВозврата, АдресХранилища);
		
КонецПроцедуры

#Область ЗакрытиеПериода

// Возвращает таблицу ссылок документа "упРегламентнаяОперация" за месяц в переданном периоде.
//
// Параметры:
//  Период  - Дата - Период.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ТаблицаРегламентныхОпераций(Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упВидыРегламентныхОпераций.Ссылка КАК ВидОперации
	               |ПОМЕСТИТЬ втВидыОпераций
	               |ИЗ
	               |	Перечисление.упВидыРегламентныхОпераций КАК упВидыРегламентныхОпераций
	               |ГДЕ
	               |	&ИспользуетсяЗагрузкаДанныхОтТопливныхКомпаний
	               |				ИЛИ НЕ упВидыРегламентныхОпераций.Ссылка = ЗНАЧЕНИЕ(Перечисление.упВидыРегламентныхОпераций.КорректировкаСтоимостиГСМ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упРегламентнаяОперация.Ссылка,
	               |	упРегламентнаяОперация.ВидОперации,
	               |	упРегламентнаяОперация.Статус
	               |ПОМЕСТИТЬ втРегламентныеОперации
	               |ИЗ
	               |	Документ.упРегламентнаяОперация КАК упРегламентнаяОперация
	               |ГДЕ
	               |	упРегламентнаяОперация.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втРегламентныеОперации.ВидОперации,
	               |	МАКСИМУМ(втРегламентныеОперации.Ссылка) КАК Ссылка
	               |ПОМЕСТИТЬ втВидыОперацийСсылки
	               |ИЗ
	               |	втРегламентныеОперации КАК втРегламентныеОперации
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втРегламентныеОперации.ВидОперации
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втВидыОперацийСсылки.ВидОперации,
	               |	втВидыОперацийСсылки.Ссылка,
	               |	втРегламентныеОперации.Статус
	               |ПОМЕСТИТЬ втВидыОперацийСтатусы
	               |ИЗ
	               |	втВидыОперацийСсылки КАК втВидыОперацийСсылки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегламентныеОперации КАК втРегламентныеОперации
	               |		ПО втВидыОперацийСсылки.Ссылка = втРегламентныеОперации.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втВидыОпераций.ВидОперации,
	               |	ИСТИНА КАК Используется,
	               |	ЕСТЬNULL(втВидыОперацийСтатусы.Ссылка, ЗНАЧЕНИЕ(Документ.упРегламентнаяОперация.ПустаяСсылка)) КАК Ссылка,
	               |	ЕСТЬNULL(втВидыОперацийСтатусы.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыОперацииЗакрытия.НеВыполнена)) КАК Статус
	               |ИЗ
	               |	втВидыОпераций КАК втВидыОпераций
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втВидыОперацийСтатусы КАК втВидыОперацийСтатусы
	               |		ПО втВидыОпераций.ВидОперации = втВидыОперацийСтатусы.ВидОперации";
	Запрос.УстановитьПараметр("ДатаНачала", 	НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", 	КонецМесяца(Период));
	Запрос.УстановитьПараметр("ИспользуетсяЗагрузкаДанныхОтТопливныхКомпаний", 	ПолучитьФункциональнуюОпцию("упИспользуетсяЗагрузкаДанныхОтТопливныхКомпаний"));
	
	тбзРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзРезультат;	
	
КонецФункции // ТаблицаРегламентныхОпераций()

// Процедура закрытия периода.
//
// Параметры:
//  сткПараметры  - Структура - Параметры.
//  АдресХранилища  - Строка - Адрес.
//
Процедура ВыполнитьЗакрытиеПериода(сткПараметры, АдресХранилища) Экспорт
	
	текРезультат = ЗакрытьПериод(сткПараметры.Операции, сткПараметры.Период, сткПараметры.ФоновоеЗадание);
	
	ПоместитьВоВременноеХранилище(текРезультат, АдресХранилища);
		
КонецПроцедуры

// Возвращает результат закрытия периода.
//
// Параметры:
//  Операции		 - ТаблицаЗначений	 - Таблица данных.
//  Период		 - Дата			 - Период.
//  ФоновоеЗадание	 - Булево	           - признак фонового задания.
// 
// Возвращаемое значение:
//  Структура - Результат выполнения.
//
Функция ЗакрытьПериод(Операции, Период, ФоновоеЗадание = Ложь) Экспорт
	
	текРезультат = Новый Структура();
	
	тбзОперации = Операции.СкопироватьКолонки("ВидОперации, Статус, Ссылка");
	
	НомерОперации = 1;
	КоличествоОпераций = Операции.Количество();
	сткПрогресс = Новый Структура();
	сткПрогресс.Вставить("НомерОперации",      НомерОперации);
	сткПрогресс.Вставить("КоличествоОпераций", КоличествоОпераций);
	сткПрогресс.Вставить("ФоновоеЗадание",     ФоновоеЗадание);
		
	Для каждого СтрокаОперация Из Операции Цикл
		
		Если СтрокаОперация.Статус = Перечисления.упСтатусыОперацииЗакрытия.Исключена Тогда
			Продолжить;
		КонецЕсли;	
				
		Отказ = Ложь;
		сткПрогресс.Вставить("НомерОперации", НомерОперации);
		сткРезультат = ОбработатьРегламентнуюОперацию(СтрокаОперация.Ссылка, СтрокаОперация.ВидОперации, Период, СтрокаОперация.Статус, сткПрогресс, Отказ);	
		
		Если ФоновоеЗадание Тогда
			Процент = Окр(НомерОперации *100 / КоличествоОпераций);
			МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
			СтрокаВидОперации = Нстр("ru = 'Обрабатывается операция:%1'");
			СтрокаВидОперации = СтрШаблон(СтрокаВидОперации, СтрокаОперация.ВидОперации);
			МодульДлительныеОперации.СообщитьПрогресс(Процент, СтрокаВидОперации);
		КонецЕсли;
		
		НомерОперации = НомерОперации + 1;
	КонецЦикла;

	текРезультат.Вставить("Операции", 			ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзОперации));
	текРезультат.Вставить("ВыполненоУспешно", 	Истина);
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти

// Проверяет наличия документов "упРегламентнаяОперация" по переданным параметрам.
//
// Параметры:
//  ДокументСсылка  - ДокументСсылка - Ссылка на элемент.
//  ВидОперации  - ПеречислениеСсылка - Ссылка на элемент.
//  ПериодЗакрытия  - Дата - Период.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ЕстьДокументыЗакрытия(ДокументСсылка, ВидОперации, ПериодЗакрытия) Экспорт
	
	текРезультат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	тбОперацияЗакрытия.Ссылка
	|ИЗ
	|	Документ.упРегламентнаяОперация КАК тбОперацияЗакрытия
	|ГДЕ
	|	НЕ тбОперацияЗакрытия.ПометкаУдаления
	|	И тбОперацияЗакрытия.Ссылка <> &Ссылка
	|	И тбОперацияЗакрытия.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	|	И тбОперацияЗакрытия.ВидОперации = &ВидОперации
	|	И тбОперацияЗакрытия.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыОперацииЗакрытия.НеВыполнена)";
	
	Запрос.УстановитьПараметр("ПериодНачало", 		НачалоМесяца(ПериодЗакрытия));
	Запрос.УстановитьПараметр("ПериодОкончание",  	КонецМесяца(ПериодЗакрытия));
	Запрос.УстановитьПараметр("ВидОперации",   		ВидОперации);
	Запрос.УстановитьПараметр("Ссылка",        		ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	текРезультат  = НЕ РезультатЗапроса.Пустой();
	
	текВыборка = РезультатЗапроса.Выбрать();
	Если текВыборка.Следующий() Тогда
		
		ТекстСообщения = Нстр("ru = 'В периоде %1 уже существует документ закрытия %2 с видом операции %3'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПериодЗакрытия, текВыборка.Ссылка, ВидОперации);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,текВыборка.Ссылка);
		
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает представление переданного периода из фиксированного списка.
//
// Параметры:
//  Дата  - Дата - Период.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьПредставлениеПериода(Дата) Экспорт
	
	текРезультат = Неопределено;
	
	текДата = НачалоМесяца(Дата);
	
	СписокПериодов = упСервисныеФункцииКлиентСервер.СписокФиксированныхПериодов(текДата, ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
		
	текЭлемент = СписокПериодов.НайтиПоЗначению(текДата);
	
	Если НЕ текЭлемент = Неопределено Тогда
		текРезультат = текЭлемент.Представление;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Процедура - Отменить проведение связанных документов
//
// Параметры:
//  Ссылка - ДокументСсылка.упРегламентнаяОперация 	 - ссылка на основание.
//  Отказ	 - Булево	 - Истиан, если ошибка.
//
Процедура ОтменитьПроведениеСвязанныхДокументов(Ссылка, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРаспределениеПрочихРасходов.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.упРаспределениеПрочихРасходов КАК упРаспределениеПрочихРасходов
	               |ГДЕ
	               |	упРаспределениеПрочихРасходов.ДокументОснование = &ДокументОснование
	               |	И НЕ упРаспределениеПрочихРасходов.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);	
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Прервать;
		КонецПопытки;
	КонецЦикла; 
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры


#Область СлужебныеПроцедурыИФункции

Функция ОбработатьРегламентнуюОперацию(ДокументСсылка, ВидОперации, ПериодЗакрытия, Статус, сткПрогресс, Отказ)
	
	ОтменитьПовторяющиесяДокументыЗакрытия(ДокументСсылка, ВидОперации, ПериодЗакрытия);
	
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		ДокументОбъект 	= ДокументСсылка.ПолучитьОбъект();
	Иначе	
		ДокументОбъект	= Документы.упРегламентнаяОперация.СоздатьДокумент();
		ДокументОбъект.Автор	= ПользователиКлиентСервер.ТекущийПользователь();
		ДокументОбъект.Статус	= Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена;
	КонецЕсли;
	
	ДокументОбъект.Дата = КонецМесяца(ПериодЗакрытия);
	ДокументОбъект.ВидОперации	= ВидОперации;
	
	Если НЕ Статус = Перечисления.упСтатусыОперацииЗакрытия.Исключена Тогда
		ДокументОбъект.ПровестиОперациюЗакрытия(сткПрогресс, Отказ);
	Иначе
		ДокументОбъект.ИсключитьОперациюЗакрытия(Отказ);
	КонецЕсли;
	
	сткВозврата = Новый Структура();
	сткВозврата.Вставить("Ссылка",      ДокументОбъект.Ссылка);
	сткВозврата.Вставить("ВидОперации", ВидОперации);
	сткВозврата.Вставить("Статус",      ДокументОбъект.Статус);
	сткВозврата.Вставить("Отказ",       Отказ);
		
	Возврат сткВозврата;
	
КонецФункции

Процедура ОтменитьПовторяющиесяДокументыЗакрытия(ДокументСсылка, ВидОперации, ПериодЗакрытия) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбОперацияЗакрытия.Ссылка
	               |ИЗ
	               |	Документ.упРегламентнаяОперация КАК тбОперацияЗакрытия
	               |ГДЕ
	               |	НЕ тбОперацияЗакрытия.ПометкаУдаления
				   |	И тбОперацияЗакрытия.Ссылка <> &Ссылка
	               |	И тбОперацияЗакрытия.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
	               |	И тбОперацияЗакрытия.ВидОперации = &ВидОперации
	               |	И тбОперацияЗакрытия.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыОперацииЗакрытия.НеВыполнена)";
			   
	Запрос.УстановитьПараметр("ПериодНачало", 		НачалоМесяца(ПериодЗакрытия));
	Запрос.УстановитьПараметр("ПериодОкончание",  	КонецМесяца(ПериодЗакрытия));
	Запрос.УстановитьПараметр("ВидОперации",   		ВидОперации);
	Запрос.УстановитьПараметр("Ссылка",        		ДокументСсылка);

	текВыборка = Запрос.Выполнить().Выбрать();
	Пока текВыборка.Следующий() Цикл
		
		докОбъект = текВыборка.Ссылка.ПолучитьОбъект();
		докОбъект.ОтменитьПроведениеОперацииЗакрытия();
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти