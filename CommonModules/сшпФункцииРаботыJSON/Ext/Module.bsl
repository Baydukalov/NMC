
#Область ПрограммныйИнтерфейс

// Функция - Получить объект потокового чтения
//
// Параметры:
//	ЧитаемаяСтрока - данные, которые необходимо последовательно прочитать.
// 
// Возвращаемое значение:
//	ЧтениеJSON - объект чтения JSON данных.
//
Функция ПолучитьОбъектПотоковогоЧтения(ЧитаемаяСтрока) Экспорт
	текЧтение = Новый ЧтениеJSON;
	текЧтение.УстановитьСтроку(ЧитаемаяСтрока);
	Возврат текЧтение;
КонецФункции

// Функция - Получить объект потоковой записи
//
// Параметры:
//	ПараметрыЗаписи - входящие параметры записи XML.
//
// Возвращаемое значение:
//	ЗаписьXML - объект записи XML данных.
//
Функция ПолучитьОбъектПотоковойЗаписи(ПараметрыЗаписи = Неопределено) Экспорт
	текЗапись = Новый ЗаписьJSON;
	Если ПараметрыЗаписи = Неопределено тогда
		ПараметрыЗаписи = текЗапись.Параметры;
	КонецЕсли;	
	текЗапись.УстановитьСтроку(ПараметрыЗаписи);
	Возврат текЗапись;
КонецФункции	

// Функция - Прочитать объект из потока
//
// Параметры:
//	Поток - объект чтения JSON, которых нужно прочитать.
//	типобъекта - тип объекта для чтения.
// 
// Возвращаемое значение:
//	Результат чтения объекта.
//
Функция ПрочитатьОбъектИзПотока(Поток, типобъекта) Экспорт
	Возврат ФабрикаXDTO.ПрочитатьJSON(Поток, типобъекта);
КонецФункции

// Функция - Записать объект поток
//
// Параметры:
//	Объект - объект, который необходимо записать.
//	Фабрика - XDTO-фабрика.
// 
// Возвращаемое значение:
//	Результат записи XDTO-объекта в объект записи JSON.
//
Функция ЗаписатьОбъектВПоток(Объект, Фабрика = Неопределено) Экспорт
	xdtoФабрика = ?(Фабрика = Неопределено, ФабрикаXDTO, Фабрика);
	jsonЗапись = ПолучитьОбъектПотоковойЗаписи();
	xdtoФабрика.ЗаписатьJSON(jsonЗапись, Объект);
	Возврат jsonЗапись.Закрыть();
КонецФункции

// Функция - Сериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо сериализовать.
// 
// Возвращаемое значение:
//	Результат сериализации объекта.	
//
Функция СериализоватьОбъект(Источник) Экспорт
	текОбъектЗаписи = сшпФункцииРаботыJSON.ПолучитьОбъектПотоковойЗаписи();
	СериализаторXDTO.ЗаписатьJSON(текОбъектЗаписи, Источник, НазначениеТипаXML.Явное);
	Возврат текОбъектЗаписи.Закрыть();
КонецФункции	

// Функция - Десериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо десериализовать.
// 
// Возвращаемое значение:
//	Результат десериализации объекта.	
//
Функция ДесериализоватьОбъект(Источник) Экспорт
	результатОбъект = Неопределено;
	потокЧтение = сшпФункцииРаботыJSON.ПолучитьОбъектПотоковогоЧтения(Источник);
	результатОбъект = СериализаторXDTO.ПрочитатьJSON(потокЧтение);
	потокЧтение.Закрыть();
	Возврат результатОбъект;
КонецФункции	

// Функция - Сформировать JSON
//
// Параметры:
//	сткДанные - входящие данные для формирования.
// 
// Возвращаемое значение:
//	Запись JSON.
//
Функция СформироватьJSON(сткДанные) Экспорт
	текВозврат = Неопределено;
	Если ЗначениеЗаполнено(сткДанные) и ТипЗнч(сткДанные) = Тип("Структура") тогда
		текЗапись = Новый ЗаписьJSON;
		текЗапись.УстановитьСтроку();
		СформироватьОбъектJSON(текЗапись, сткДанные);
		текВозврат = текЗапись.Закрыть();
	КонецЕсли;
	Возврат текВозврат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьЭлементJSON(Запись, Данные, Имя = "")
	Если ТипЗнч(Данные) = Тип("Структура") тогда
		СформироватьОбъектJSON(Запись, Данные, Имя);
	ИначеЕсли ТипЗнч(Данные) = Тип("Массив") тогда
		СформироватьМассивJSON(Запись, Данные, Имя);
	Иначе	
		Запись.ЗаписатьИмяСвойства(Имя);
		Запись.ЗаписатьЗначение(Данные);
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьОбъектJSON(Запись, Данные, Имя = "")
	Если ТипЗнч(Данные) = Тип("Структура") тогда
		Если ЗначениеЗаполнено(Имя) тогда
			Запись.ЗаписатьИмяСвойства(Имя);
		КонецЕсли;	
		Запись.ЗаписатьНачалоОбъекта();
		Для каждого текЭлемент из Данные цикл
			СформироватьЭлементJSON(Запись, текЭлемент.Значение, текЭлемент.Ключ);
		КонецЦикла;
		Запись.ЗаписатьКонецОбъекта();
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьМассивJSON(Запись, Данные, Имя = "")
	Если ТипЗнч(Данные) = Тип("Массив") тогда
		Запись.ЗаписатьИмяСвойства(Имя);
		Запись.ЗаписатьНачалоМассива();
		Для каждого текЭлемент из Данные цикл
			СформироватьЭлементJSON(Запись, текЭлемент);
		КонецЦикла;
		Запись.ЗаписатьКонецМассива();			
	КонецЕсли;
КонецПроцедуры

#КонецОбласти