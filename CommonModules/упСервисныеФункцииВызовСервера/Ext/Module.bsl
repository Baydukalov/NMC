#Область ПрограммныйИнтерфейс

// Функция возвращает время последнего исполнения контрольного задания
//
// Возвращаемое значение:
//   Произвольное - Результат выполнения.
//
Функция ВремяПоследнегоИсполненияКонтрольногоЗадания() Экспорт
	
	Возврат Константы.упДатаВремяИсполненияКонтрольногоЗадания.Получить();
	
КонецФункции

// Функция определяет необходимость контроля работы фоновых заданий при запуске системы
//
Функция КонтролироватьИсполненияФоновыхЗаданий() Экспорт
	
	Если Истина
		И ОбщегоНазначенияПовтИсп.РазделениеВключено()
		И ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей()
	Тогда 
		Возврат Ложь;
	КонецЕсли; 
	Возврат Не упСервисныеФункции.ПолучитьЗначениеКонстанты("упЗапретитьПроверкуРаботыФоновыхЗаданийПриНачалеРаботыСистемы");

КонецФункции // КонтролироватьИсполненияФоновыхЗаданий()

// Проверяет, является ли текущий или указанный пользователь полноправным.
//
// Параметры:
//  Пользователь - Неопределено - проверяется текущий пользователь ИБ;
//                 СправочникСсылка.Пользователи,
//                 СправочникСсылка.ВнешниеПользователи - осуществляется поиск
//                    пользователя ИБ по уникальному идентификатору,
//                    заданному в реквизите ИдентификаторПользователяИБ.
//                    Прим.: если пользователь ИБ не найден, возвращается Ложь.
//                 ПользовательИнформационнойБазы - проверяется указанный
//                    пользователь ИБ.
//
// Возвращаемое значение: 
//	Булево - Результат выполнения.
//
Функция ЭтоПолноправныйПользователь(Пользователь) Экспорт
   	Возврат Пользователи.ЭтоПолноправныйПользователь(Пользователь); 
КонецФункции

// Возвращает значение скидки для соглашения.
//
// Параметры:
//  СоглашениеСКлиентом - СправочникСсылка - Ссылка на элемент.
// 
// Возвращаемое значение:
//   Число - Результат выполнения.
//
Функция ПолучитьПроцентСкидкиПоСоглашению(СоглашениеСКлиентом) Экспорт
	
	текРезультат = 100;
	Если ЗначениеЗаполнено(СоглашениеСКлиентом) Тогда
		текМаксПроцентСкидки = СоглашениеСКлиентом.МаксимальныйПроцентСкидки;
		текРезультат = ?(текМаксПроцентСкидки > 0, текМаксПроцентСкидки, 100);
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает управленческую сумму, пересчитанную из Текущей валюты в Новую валюту по параметрам их курсов.
//
// Параметры:
//  Валюта - СправочникСсылка - Ссылка на элемент.
//  Сумма - Число - Значение.
//  Дата - Дата - Период.
// 
// Возвращаемое значение:
//   Число - Результат выполнения.
//
Функция РассчитатьСуммуУпр(Валюта, Сумма, Дата = '00010101') Экспорт
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;	

	УпрВалюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	УпрКурс   = РаботаСКурсамиВалют.ПолучитьКурсВалюты(УпрВалюта, Дата);
	
	Курс      = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
	
	Возврат РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Сумма, Валюта, УпрВалюта, Курс.Курс, УпрКурс.Курс, Курс.Кратность, УпрКурс.Кратность);
		
КонецФункции

// Возвращает регламентированную сумму, пересчитанную из Текущей валюты в Новую валюту по параметрам их курсов.
//
// Параметры:
//  Валюта - СправочникСсылка - Ссылка на элемент.
//  Сумма - Число - Значение.
//  Дата - Дата - Период.
// 
// Возвращаемое значение:
//   Число - Результат выполнения.
//
Функция РассчитатьСуммуРегл(Валюта, Сумма, Дата = '00010101') Экспорт
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;	

	УпрВалюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	УпрКурс   = РаботаСКурсамиВалют.ПолучитьКурсВалюты(УпрВалюта, Дата);
	
	Курс      = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
	
	Возврат РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Сумма, УпрВалюта, Валюта, УпрКурс.Курс, Курс.Курс, УпрКурс.Кратность, Курс.Кратность);
		
КонецФункции


// Возвращает сумму в Новой валюте, пересчитанную из Управляющей по параметрам их курсов.
//
// Параметры:
//  Валюта - СправочникСсылка - Ссылка на элемент.
//  Сумма - Число - Значение.
//  Дата - Дата - Период.
// 
// Возвращаемое значение:
//   Число - Результат выполнения.
//
Функция РассчитатьСуммуИзУпр(Валюта, Сумма, Дата = '00010101') Экспорт
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;	

	УпрВалюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	УпрКурс   = РаботаСКурсамиВалют.ПолучитьКурсВалюты(УпрВалюта, Дата);
	
	Курс      = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
	
	Возврат РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Сумма, УпрВалюта, Валюта, УпрКурс.Курс, Курс.Курс, УпрКурс.Кратность, Курс.Кратность);
		
КонецФункции


// Возвращает возможности увеличения скидки.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция РазрешеноУвеличиватьСкидкуВышеМаксимальной() Экспорт
	
	текРезультат = УправлениеДоступом.ЕстьРоль("упРазрешеноПревышатьМаксимальнуюСкидку");
	Возврат текРезультат;
	
КонецФункции // ()

// Возвращает валюту управленческого учета.
//
// Возвращаемое значение:
//   СправочникСсылка.Валюты - Результат выполнения.
//
Функция ВалютаУправленческогоУчета() Экспорт
	Возврат упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
КонецФункции

// Возвращает валюту регламентированного учета.
//
// Возвращаемое значение:
//   СправочникСсылка.Валюты - Результат выполнения.
//
Функция ВалютаРегламентированногоУчета() Экспорт
	Возврат упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаРегламентированногоУчета");		
КонецФункции

// Возвращает валюту регламентированного учета по объекту.
//
// Параметры:
//  ДокументОбъект - ДокументСсылка - Ссылка на элемент.
//  Соглашение - СправочникСсылка - Ссылка на элемент.
// 
// Возвращаемое значение:
//   СправочникСсылка.Валюты - Результат выполнения.
//
Функция ВалютаРегламентированногоУчетаОбъекта(ДокументОбъект, Соглашение = Неопределено) Экспорт
	
	Валюта = Справочники.Валюты.ПустаяСсылка();
	
	Если НЕ ЗначениеЗаполнено(Соглашение) Тогда
		Соглашение = ДокументОбъект.Соглашение;	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Соглашение) Тогда
		Валюта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Соглашение).ВалютаСоглашения(Соглашение);	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ВалютаРегламентированногоУчета();
	КонецЕсли;
	
	Возврат Валюта;		
КонецФункции

// Возвращает дату сеанса.
//
// Возвращаемое значение:
//   Дата - период.
//
Функция ДатаСеанса() Экспорт
	Возврат ТекущаяДатаСеанса();
КонецФункции

// Возвращает дату сеанса как конец месяца.
//
// Возвращаемое значение:
//   Дата - период.
//
Функция ДатаСеансаНачалоДняКонецМесяца() Экспорт
	Возврат НачалоДня(КонецМесяца(ТекущаяДатаСеанса()));
КонецФункции

// Проверяет корректность введеного логина и пароля.
//
// Параметры:
//  Логин - Строка - имя пользователя.
//  Пароль - Строка - пароль пользователя.
//  ТекстОшибки - Строка - Текст.
// 
// Возвращаемое значение:
//   СправочникСсылка - Ссылка на элемент.
//
Функция АвторизацияСотрудникаЛогинПароль(Логин, Пароль, ТекстОшибки = "") Экспорт
	
	текРезультат = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(Логин) Тогда
		ТекстОшибки = Нстр("ru = 'Не задан логин'");	
		Возврат текРезультат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСотрудники.Ссылка,
	               |	упСотрудники.Пароль
	               |ИЗ
	               |	Справочник.упСотрудники КАК упСотрудники
	               |ГДЕ
	               |	упСотрудники.Логин = &Логин";
	Запрос.УстановитьПараметр("Логин", 	Логин);
	
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Если текВыборка.Пароль = Пароль Тогда
			текРезультат = текВыборка.Ссылка;	
		Иначе	
			ТекстОшибки = Нстр("ru = 'Введен неверный пароль'");	
		КонецЕсли;
	Иначе
		ТекстОшибки = Нстр("ru = 'В базе нет сотрудника с логином:%1'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Логин);
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Проверяет корректность отсканированного ШК для доступа в систему.
//
// Параметры:
//  ШтрихКод - Строка - Штрих Код.
//  ТекстОшибки - Строка - Текст.
// 
// Возвращаемое значение:
//   СправочникСсылка.упСотрудники - Ссылка на элемент.
//
Функция АвторизацияСотрудникаПоШтрихкоду(ШтрихКод, ТекстОшибки = "") Экспорт
	
	текРезультат = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(ШтрихКод) Тогда
		ТекстОшибки = Нстр("ru = 'Штрихкод не задан'");	
		Возврат текРезультат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСотрудники.Ссылка
	               |ИЗ
	               |	Справочник.упСотрудники КАК упСотрудники
	               |ГДЕ
	               |	упСотрудники.Штрихкод = &Штрихкод";
	Запрос.УстановитьПараметр("Штрихкод", 	ШтрихКод);

	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Ссылка;
	Иначе
		Если Пользователи.РолиДоступны("упРазработчик") Тогда
			ТекстОшибки = Нстр("ru = 'В базе нет сотрудника со штрихкодом:%1'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ШтрихКод);	
		Иначе
			ТекстОшибки = Нстр("ru = 'В базе нет сотрудника с введенным штрихкодом'");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Проверяет корректность магнитного кода для доступа в систему.
//
// Параметры:
//  КодМагнитнойКарты - Строка - Код.
//  ТекстОшибки - Строка - Текст.
// 
// Возвращаемое значение:
//   СправочникСсылка.упСотрудники - Ссылка на элемент.
//
Функция АвторизацияСотрудникаПоКодуМагнитнойКарты(КодМагнитнойКарты, ТекстОшибки = "") Экспорт
	
	текРезультат = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(КодМагнитнойКарты) Тогда
		ТекстОшибки = Нстр("ru = 'Не код магнитной карты'");	
		Возврат текРезультат;
	КонецЕсли;

	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСотрудники.Ссылка
	               |ИЗ
	               |	Справочник.упСотрудники КАК упСотрудники
	               |ГДЕ
	               |	упСотрудники.КодМагнитнойКарты = &КодМагнитнойКарты";
	Запрос.УстановитьПараметр("КодМагнитнойКарты", 	КодМагнитнойКарты);

	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Ссылка;
	Иначе
		Если Пользователи.РолиДоступны("упРазработчик") Тогда
			ТекстОшибки = Нстр("ru = 'В базе нет сотрудника с кодом магнитной карты:%1'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, КодМагнитнойКарты);	
		Иначе
			ТекстОшибки = Нстр("ru = 'В базе нет сотрудника такой картой'");
		КонецЕсли;

	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает признак прохождения авторизации.
//
// Параметры:
//  сткПараметры - Структура - Параметры.
//  ТекстОшибки - Строка - Текст.
// 
// Возвращаемое значение:
//   СправочникСсылка.упСотрудники - Ссылка на элемент.
//
Функция Авторизация(сткПараметры, ТекстОшибки = "") Экспорт
	
	текРезультат = Неопределено;
	
	Если ТипЗнч(сткПараметры) = Тип("Структура") Тогда
	
		текПараметрАвторизации1 = Неопределено;
		
		Если сткПараметры.Свойство("Логин", текПараметрАвторизации1) Тогда
			текПараметрАвторизации2 = Неопределено;
			Если  сткПараметры.Свойство("Пароль", текПараметрАвторизации2) Тогда
				текРезультат = АвторизацияСотрудникаЛогинПароль(текПараметрАвторизации1, текПараметрАвторизации2, ТекстОшибки);	
			КонецЕсли;
		ИначеЕсли сткПараметры.Свойство("ШтрихКод", текПараметрАвторизации1) Тогда	
			текРезультат = АвторизацияСотрудникаПоШтрихкоду(текПараметрАвторизации1, ТекстОшибки);	
		ИначеЕсли сткПараметры.Свойство("КодМагнитнойКарты", текПараметрАвторизации1) Тогда	
			текРезультат = АвторизацияСотрудникаПоКодуМагнитнойКарты(текПараметрАвторизации1, ТекстОшибки);	
		КонецЕсли;
		
	КонецЕсли;
		
	Если текРезультат = Неопределено И НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
		ТекстОшибки = Нстр("ru = 'Не удалось авторизоваться'");
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает двочиные данные переданной картинки.
//
// Параметры:
//  Картинка - Картинка - Представляет собой картинку из коллекции картинок.
// 
// Возвращаемое значение:
//   ДвоичныеДанные - Значение содержит двоичные данные.
//
Функция ПолучитьДвоичныеДанныеКартинки(Картинка) Экспорт
	Возврат Картинка.ПолучитьДвоичныеДанные();
КонецФункции

// Восстанавливаем картинку из двоичных данных.
//
// Параметры:
//  сткКартинок - Структура - Параметры.
//
Процедура ЗаполнитьДвоичныеДанныеКартинок(сткКартинок) Экспорт
	
	Для каждого текКартинка Из сткКартинок Цикл
		текКартинка.Значение.ДвоичныеДанные = текКартинка.Значение.Картинка.ПолучитьДвоичныеДанные();
	КонецЦикла;
	
КонецПроцедуры

// Производит запись в служебных регистр информации о наличии заметки по предмету.
//  
//  Параметры совпадают с параметрами обработчика при записи у элемента справочника.
//
// Параметры:
//  Источник	- Истичник	 - источник.
//  Отказ		- Булево	 - Истина, если ошибка.
//
Процедура ПроверитьНаличиеЗаметокПоПредмету(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Источник.Предмет) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Заметки.Ссылка,
	|	Заметки.Пометка.Порядок КАК Порядок
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Предмет = &Предмет
	|	И Заметки.Автор = &Пользователь
	|	И Заметки.ПометкаУдаления = ЛОЖЬ";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Предмет", Источник.Предмет);
	Запрос.УстановитьПараметр("Пользователь", Источник.Автор);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЕстьЗаметки = Выборка.Количество() > 0;

	НаборЗаписей = РегистрыСведений.упНаличиеЗаметокПоПредмету.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Автор.Установить(Источник.Автор);
	НаборЗаписей.Отбор.Предмет.Установить(Источник.Предмет);
	НаборЗаписей.Прочитать();
	
	Если ЕстьЗаметки Тогда 
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Источник);
			НоваяЗапись.ЕстьЗаметки = Истина;				
			НоваяЗапись.Цвет 		= Выборка.Порядок;
		Иначе
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.ЕстьЗаметки = Истина;
				Запись.Цвет        = Выборка.Порядок;
			КонецЦикла;
		КонецЕсли;
	Иначе
		НаборЗаписей.Очистить();
	КонецЕсли;

	НаборЗаписей.Записать();
	
КонецПроцедуры

// Помечает на удаление заметки, связанные с удаляемым объектом.
//
// Параметры:
//  Источник	- Истичник	 - источник.
//  Отказ		- Булево	 - Истина, если ошибка.
//
Процедура ПометитьНаУдалениеЗаметкиПоПредмету(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Не Источник.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Заметки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.ПометкаУдаления = ЛОЖЬ
	|	И Заметки.Предмет = &Предмет";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Предмет", Источник.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаметкаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЗаметкаОбъект.УстановитьПометкуУдаления(Истина, Ложь);
		ЗаметкаОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаленияЗаметки", Истина);
		Попытка
			ЗаметкаОбъект.Записать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Заметки пользователя.Установка пометки удаления'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, ЗаметкаОбъект.Метаданные(), ЗаметкаОбъект.Ссылка, ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Функция - Заголовок клиентского приложения
// 
// Возвращаемое значение:
//  Строка - имя часового пояса.
//
Функция НаименованиеЧасовогоПоясаТекущегоПользователя() Экспорт
	
	Результат = "";
	
	Если Не ОбщегоНазначенияПовтИсп.РазделениеВключено() Или Не ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей() Тогда
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			Если НЕ ЧасовойПоясСеанса() = ПолучитьЧасовойПоясИнформационнойБазы() Тогда
				УстановитьПривилегированныйРежим(Истина);
				сткЧасовойПояс = упСервисныеФункции.ПолучитьЧасовойПоясПользователя();
				УстановитьПривилегированныйРежим(Ложь);
				Результат = сткЧасовойПояс.Наименование;	
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Функция - Заголовок клиентского приложения
// 
// Возвращаемое значение:
//  Строка - имя часового пояса.
//
Функция НаименованиеЧасовогоПоясаТекущегоСеанса() Экспорт
	
	Результат = "";
	
	Если Ложь
		Или Не ОбщегоНазначенияПовтИсп.РазделениеВключено() 
		Или Не ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей() 
	Тогда
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			Если ЧасовойПоясСеанса() <> ПолучитьЧасовойПоясИнформационнойБазы() Тогда
				ЧасовойПояс = упСервисныеФункции.ТекущийЧасовойПояс();
				Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЧасовойПояс, "Наименование");
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Функция - Список номеров сеанса базы
// 
// Возвращаемое значение:
//  СписокЗначений - номера сеансов.
//
Функция СписокНомеровСеансаБазы() Экспорт
	спзНомерСеанса = Новый СписокЗначений;
	спзНомерСеанса.Добавить(НомерСеансаИнформационнойБазы());
	Возврат спзНомерСеанса;
КонецФункции

#Область ФункцииДляРаботыСКомпоновкойДанных

// Копирует элементы из одной коллекции в другую.
//
// Параметры:
//  ПриемникЗначения - Произвольное - Значение компановки данных.
//  ИсточникЗначения - Произвольное - Значение компановки данных.
//  ПроверятьДоступность - Булево - Выполнение.
//  ОчищатьПриемник - Булево - Выполнение.
//
Процедура СкопироватьЭлементы(ПриемникЗначения, ИсточникЗначения, ПроверятьДоступность = Ложь, ОчищатьПриемник = Истина) Экспорт
	
	Если ТипЗнч(ИсточникЗначения) = Тип("УсловноеОформлениеКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ОформляемыеПоляКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		СоздаватьПоТипу = Ложь;
	Иначе
		СоздаватьПоТипу = Истина;
	КонецЕсли;
	ПриемникЭлементов = ПриемникЗначения.Элементы;
	ИсточникЭлементов = ИсточникЗначения.Элементы;
	Если ОчищатьПриемник Тогда
		ПриемникЭлементов.Очистить();
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из ИсточникЭлементов Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
			// Элементы порядка добавляем в начало.
			Индекс = ИсточникЭлементов.Индекс(ЭлементИсточник);
			ЭлементПриемник = ПриемникЭлементов.Вставить(Индекс, ТипЗнч(ЭлементИсточник));
		Иначе
			Если СоздаватьПоТипу Тогда
				ЭлементПриемник = ПриемникЭлементов.Добавить(ТипЗнч(ЭлементИсточник));
			Иначе
				ЭлементПриемник = ПриемникЭлементов.Добавить();
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		// В некоторых коллекциях необходимо заполнить другие коллекции.
		Если ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Поля, ЭлементИсточник.Поля);
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
			ЗаполнитьЭлементы(ЭлементПриемник.Оформление, ЭлементИсточник.Оформление); 
		ИначеЕсли ТипЗнч(ИсточникЭлементов)	= Тип("КоллекцияВариантовПользовательскогоПоляВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
		КонецЕсли;
		
		// В некоторых элементах коллекции необходимо заполнить другие коллекции.
		Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Варианты, ЭлементИсточник.Варианты);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
			ЭлементПриемник.УстановитьВыражениеДетальныхЗаписей (ЭлементИсточник.ПолучитьВыражениеДетальныхЗаписей());
			ЭлементПриемник.УстановитьВыражениеИтоговыхЗаписей(ЭлементИсточник.ПолучитьВыражениеИтоговыхЗаписей());
			ЭлементПриемник.УстановитьПредставлениеВыраженияДетальныхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияДетальныхЗаписей ());
			ЭлементПриемник.УстановитьПредставлениеВыраженияИтоговыхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияИтоговыхЗаписей ());
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет элементы одной коллекции из другой.
//
// Параметры:
//  ПриемникЗначения - Произвольное - Значение компановки данных.
//  ИсточникЗначения - Произвольное - Значение компановки данных.
//  ПервыйУровень - ЗначениеПараметраКомпоновкиДанных - Данные компановки.
//
Процедура ЗаполнитьЭлементы(ПриемникЗначения, ИсточникЗначения, ПервыйУровень = Неопределено) Экспорт
	
	Если ТипЗнч(ПриемникЗначения) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		КоллекцияЗначений = ИсточникЗначения;
	Иначе
		КоллекцияЗначений = ИсточникЗначения.Элементы;
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из КоллекцияЗначений Цикл
		Если ПервыйУровень = Неопределено Тогда
			ЭлементПриемник = ПриемникЗначения.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		Иначе
			ЭлементПриемник = ПервыйУровень.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		КонецЕсли;
		Если ЭлементПриемник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		Если ТипЗнч(ЭлементИсточник) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
			Если ЭлементИсточник.ЗначенияВложенныхПараметров.Количество() <> 0 Тогда
				ЗаполнитьЭлементы(ЭлементПриемник.ЗначенияВложенныхПараметров, ЭлементИсточник.ЗначенияВложенныхПараметров, ПриемникЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура удаляет все элементы настройки компоновки данных из объекта.
//
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных - Настройки.
//
Процедура ОчиститьНастройкиКомпоновкиДанных(Настройки) Экспорт
	
	Если Настройки = Неопределено или ТипЗнч(Настройки) <> Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Параметр Из Настройки.ПараметрыДанных.Элементы Цикл
		Параметр.Значение = Неопределено;
		Параметр.Использование = ложь;
	КонецЦикла;
	
	Для каждого Параметр Из Настройки.ПараметрыВывода.Элементы Цикл
		Параметр.Использование = ложь;
	КонецЦикла;
	
	Настройки.ПользовательскиеПоля.Элементы.Очистить();
	Настройки.Отбор.Элементы.Очистить();
	Настройки.Порядок.Элементы.Очистить();
	Настройки.Выбор.Элементы.Очистить();
	Настройки.Структура.Очистить();
	
КонецПроцедуры

// Копирует настройки компоновки данных.
//
// Параметры:
//  НастройкиПриемник - НастройкиКомпоновкиДанных - Настройки.
//  НастройкиИсточник - НастройкиКомпоновкиДанных - Настройки.
//
Процедура СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник, НастройкиИсточник) Экспорт
	
	Если НастройкиИсточник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиПриемник) = Тип("НастройкиКомпоновкиДанных") Тогда
		Для каждого Параметр Из НастройкиИсточник.ПараметрыДанных.Элементы Цикл
			ЗначениеПараметра = НастройкиПриемник.ПараметрыДанных.НайтиЗначениеПараметра(Параметр.Параметр);
			Если ЗначениеПараметра <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ЗначениеПараметра, Параметр);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		СкопироватьНастройкиКомпоновкиДанных(НастройкиПриемник.Настройки, НастройкиИсточник.Настройки);
		Возврат;
	КонецЕсли;
	
	// Копирование настроек.
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных") Тогда
		
		ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыДанных, НастройкиИсточник.ПараметрыДанных);
		СкопироватьЭлементы(НастройкиПриемник.ПользовательскиеПоля, НастройкиИсточник.ПользовательскиеПоля);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,         НастройкиИсточник.Отбор);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,       НастройкиИсточник.Порядок);
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		СкопироватьЭлементы(НастройкиПриемник.ПоляГруппировки, НастройкиИсточник.ПоляГруппировки);
		СкопироватьЭлементы(НастройкиПриемник.Отбор,           НастройкиИсточник.Отбор);
		СкопироватьЭлементы(НастройкиПриемник.Порядок,         НастройкиИсточник.Порядок);
		ЗаполнитьЗначенияСвойств(НастройкиПриемник, НастройкиИсточник);
		
	КонецЕсли;
	
	СкопироватьЭлементы(НастройкиПриемник.Выбор,              НастройкиИсточник.Выбор);
	СкопироватьЭлементы(НастройкиПриемник.УсловноеОформление, НастройкиИсточник.УсловноеОформление);
	ЗаполнитьЭлементы(НастройкиПриемник.ПараметрыВывода,      НастройкиИсточник.ПараметрыВывода);
	
	// Копирование структуры.
	Если ТипЗнч(НастройкиИсточник) = Тип("НастройкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить(ТипЗнч(ЭлементСтруктурыИсточник));
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиИсточник) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Структура Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Структура.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Строки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Строки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Колонки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Колонки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;                      
	
	Если ТипЗнч(НастройкиИсточник) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Серии Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Серии.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
		Для каждого ЭлементСтруктурыИсточник Из НастройкиИсточник.Точки Цикл
			ЭлементСтруктурыПриемник = НастройкиПриемник.Точки.Добавить();
			СкопироватьНастройкиКомпоновкиДанных(ЭлементСтруктурыПриемник, ЭлементСтруктурыИсточник);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФункцииСвязанныеСТрассировкойРешений

// Заполнение макета печати "РасшифровкаРасчета".
//
// Параметры:
//  сткТрассировка - Структура - Параметры трассировки.
//  ПриемникРасшифровки - Структура - Параметры расшифровки.
//
Процедура СформироватьТабличныйДокументРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки) Экспорт
	
	Макет = Перечисления.упПараметрыРасчета.ПолучитьМакет("РасшифровкаРасчета");
	
	Заголовок 	= Макет.ПолучитьОбласть("Заголовок");
	Статья		= Макет.ПолучитьОбласть("Статья");
	Описание	= Макет.ПолучитьОбласть("Описание");
	Формула		= Макет.ПолучитьОбласть("Формула");
	ТекущийОбъектРасчета 		= Макет.ПолучитьОбласть("ОбъектРасчета");
	ТекущийПоказательРасчета 	= Макет.ПолучитьОбласть("ПоказательРасчета");
	РезультатПоОбъектуРасчета	= Макет.ПолучитьОбласть("РезультатПоОбъектуРасчета");
	РезультатПоОбъектуРасчетаСКоэффициентом	= Макет.ПолучитьОбласть("РезультатПоОбъектуРасчетаСКоэффициентом");
	КоэффициентРаспределения	= Макет.ПолучитьОбласть("КоэффициентРаспределения");
	ИтогоПоСтатье				= Макет.ПолучитьОбласть("ИтогоПоСтатье");
	Итог						= Макет.ПолучитьОбласть("Итог");
	ЗаголовокОписаниеПараметры	= Макет.ПолучитьОбласть("ЗаголовокОписаниеПараметры");
	ОписаниеПараметры			= Макет.ПолучитьОбласть("ОписаниеПараметры");
	Ошибка						= Макет.ПолучитьОбласть("Ошибка");
	ПоказательРасчетаЗаголовок	= Макет.ПолучитьОбласть("ПоказательРасчетаЗаголовок");
	ЗаголовокОбъектРасчета		= Макет.ПолучитьОбласть("ЗаголовокОбъектРасчета");
	
	
	Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 1 Тогда
		
		Заголовок.Параметры.Заполнить(сткТрассировка);
		Заголовок.Параметры.ВремяНачалоРасчета 		=  Формат(сткТрассировка.ВремяНачалоРасчета, "ДФ=Ч:мм:сс");
		Заголовок.Параметры.ВремяОкончаниеРасчета	=  Формат(сткТрассировка.ВремяОкончаниеРасчета, "ДФ=Ч:мм:сс");
		Заголовок.Параметры.ВремяРасчета = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремениДоСекунды(сткТрассировка.ВремяОкончаниеРасчета - сткТрассировка.ВремяНачалоРасчета);
		ПриемникРасшифровки.Приемник.Вывести(Заголовок);
		
		мсвСтатьиДоходовРасходов = сткТрассировка.мсвСтатьиДоходовРасходов;
		
		Для каждого СтатьяДоходовРасходов Из мсвСтатьиДоходовРасходов Цикл
			
			Статья.Параметры.Заполнить(СтатьяДоходовРасходов);
			ПриемникРасшифровки.Приемник.Вывести(Статья);
			
			сткОписаниеПравила = СтатьяДоходовРасходов.Описание;
			Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 2 И ТипЗнч(сткОписаниеПравила) = Тип("Структура") Тогда
		
				Описание.Параметры.Заполнить(сткОписаниеПравила);
				ПриемникРасшифровки.Приемник.Вывести(Описание);
			
			КонецЕсли;
		
			Формула.Параметры.Заполнить(СтатьяДоходовРасходов);
			ПриемникРасшифровки.Приемник.Вывести(Формула);
			
			Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 2 И ТипЗнч(сткОписаниеПравила) = Тип("Структура") Тогда
						
				мсвИспользуемыеПараметры = сткОписаниеПравила.мсвПараметры;
				Если мсвИспользуемыеПараметры.Количество()>0 Тогда
					ПриемникРасшифровки.Приемник.Вывести(ЗаголовокОписаниеПараметры);

					Для каждого текПараметр Из мсвИспользуемыеПараметры Цикл
						ОписаниеПараметры.Параметры.Заполнить(текПараметр);
						ПриемникРасшифровки.Приемник.Вывести(ОписаниеПараметры);
					КонецЦикла;
				КонецЕсли;
			
			КонецЕсли;

			ПриемникРасшифровки.Приемник.Вывести(ЗаголовокОбъектРасчета);

			
			
			мсвОбъектыРасчета = СтатьяДоходовРасходов.мсвОбъектыРасчета;
			Для каждого ОбъектРасчета  Из мсвОбъектыРасчета Цикл
				ТекущийОбъектРасчета.Параметры.Заполнить(ОбъектРасчета);
				ПриемникРасшифровки.Приемник.Вывести(ТекущийОбъектРасчета);
				
				мсвОшибкиПообъекту = ОбъектРасчета.мсвОшибки;
				Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
					Ошибка.Параметры.Заполнить(ЭлементОшибка);
					ПриемникРасшифровки.Приемник.Вывести(Ошибка);
				КонецЦикла;
				
				ПриемникРасшифровки.Приемник.Вывести(ПоказательРасчетаЗаголовок);
				
				мсвПоказателиРасчета = ОбъектРасчета.мсвПоказателиРасчета;
				
				Для каждого ПоказательРасчета Из мсвПоказателиРасчета Цикл
					ТекущийПоказательРасчета.Параметры.Заполнить(ПоказательРасчета);
					ПриемникРасшифровки.Приемник.Вывести(ТекущийПоказательРасчета);
					
					мсвОшибкиПообъекту = ОбъектРасчета.мсвОшибки;
					Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
						Ошибка.Параметры.Заполнить(ЭлементОшибка);
						ПриемникРасшифровки.Приемник.Вывести(Ошибка);
					КонецЦикла;
					
				КонецЦикла;
				
				РезультатПоОбъектуРасчета.Параметры.Заполнить(ОбъектРасчета);
				ПриемникРасшифровки.Приемник.Вывести(РезультатПоОбъектуРасчета);
				
				Если НЕ ОбъектРасчета.КоэффициентРаспределения = 1 Тогда
					КоэффициентРаспределения.Параметры.Заполнить(ОбъектРасчета);
					ПриемникРасшифровки.Приемник.Вывести(КоэффициентРаспределения);	
					
					РезультатПоОбъектуРасчетаСКоэффициентом.Параметры.Заполнить(ОбъектРасчета);
					ПриемникРасшифровки.Приемник.Вывести(РезультатПоОбъектуРасчетаСКоэффициентом);	
				КонецЕсли;
			
			КонецЦикла;
			
			ИтогоПоСтатье.Параметры.Заполнить(СтатьяДоходовРасходов);
			ПриемникРасшифровки.Приемник.Вывести(ИтогоПоСтатье);
			
			мсвОшибкиПообъекту = СтатьяДоходовРасходов.мсвОшибки;
			Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
				Ошибка.Параметры.Заполнить(ЭлементОшибка);
				ПриемникРасшифровки.Приемник.Вывести(Ошибка);
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	мсвОшибкиПообъекту = сткТрассировка.мсвОшибки;
	Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
		Ошибка.Параметры.Заполнить(ЭлементОшибка);
		ПриемникРасшифровки.Приемник.Вывести(Ошибка);
	КонецЦикла;
	
	
	Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений <= 1 Тогда
		Итог.Параметры.Заполнить(сткТрассировка);
		ПриемникРасшифровки.Приемник.Вывести(Итог);
	КонецЕсли;
	
	ПриемникРасшифровки.Приемник.ТолькоПросмотр = Истина;
	ПриемникРасшифровки.Приемник.ОтображатьСетку = Ложь;
	ПриемникРасшифровки.Приемник.ОтображатьЗаголовки = Ложь;
	
КонецПроцедуры

// Заполнение макета печати "РасшифровкаРасчетаРасходаТоплива".
//
// Параметры:
//  сткТрассировка - Структура - Параметры трассировки.
//  ПриемникРасшифровки - Структура - Параметры расшифровки.
//
Процедура СформироватьТабличныйДокументРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка, ПриемникРасшифровки) Экспорт
	
	Макет = Перечисления.упПараметрыРасчета.ПолучитьМакет("РасшифровкаРасчетаРасходаТоплива");
	
	Заголовок 	= Макет.ПолучитьОбласть("Заголовок");
	ЗаголовокВидГСМ	= Макет.ПолучитьОбласть("ВидГСМ");
	ЗаголовокМаршрут	= Макет.ПолучитьОбласть("ЗаголовокМаршрут");
	ЗаголовокНадбавки	= Макет.ПолучитьОбласть("ЗаголовокНадбавки");
	ЗаголовокПрицеп	= Макет.ПолучитьОбласть("ЗаголовокПрицеп");
	ЗаголовокДопОборудование	= Макет.ПолучитьОбласть("ЗаголовокДопОборудование");
	МаршрутТекущий		= Макет.ПолучитьОбласть("Маршрут");
	МаршрутИтого		= Макет.ПолучитьОбласть("МаршрутИтого");
	НадбавкаТекущая	= Макет.ПолучитьОбласть("Надбавка");
	НадбавкаИтого		= Макет.ПолучитьОбласть("НадбавкаИтого");
	ПрицепТекущий		= Макет.ПолучитьОбласть("Прицеп");
	ПрицепИтого		= Макет.ПолучитьОбласть("ПрицепИтого");
	ДопоборудованиеТекущее = Макет.ПолучитьОбласть("ДопОборудование");
	ДопоборудованиеИтого   = Макет.ПолучитьОбласть("ДопоборудованиеИтого");
	Описание			= Макет.ПолучитьОбласть("Описание");
	Формула			= Макет.ПолучитьОбласть("Формула");
	ТекущийОбъектРасчета 		= Макет.ПолучитьОбласть("ОбъектРасчета");
	ТекущийПоказательРасчета 	= Макет.ПолучитьОбласть("ПоказательРасчета");
	РезультатПоОбъектуРасчета	= Макет.ПолучитьОбласть("РезультатПоОбъектуРасчета");
	РезультатПоОбъектуРасчетаСКоэффициентом	= Макет.ПолучитьОбласть("РезультатПоОбъектуРасчетаСКоэффициентом");
	КоэффициентРаспределения	= Макет.ПолучитьОбласть("КоэффициентРаспределения");
	ИтогоПоВидуГСМ				= Макет.ПолучитьОбласть("ИтогоПоВидуГСМ");
	Итог						= Макет.ПолучитьОбласть("Итог");
	Ошибка					= Макет.ПолучитьОбласть("Ошибка");
	

	Статья		= Макет.ПолучитьОбласть("Статья");
	КоэффициентРаспределения	= Макет.ПолучитьОбласть("КоэффициентРаспределения");
	ИтогоПоСтатье				= Макет.ПолучитьОбласть("ИтогоПоСтатье");
	ЗаголовокОписаниеПараметры	= Макет.ПолучитьОбласть("ЗаголовокОписаниеПараметры");
	ОписаниеПараметры			= Макет.ПолучитьОбласть("ОписаниеПараметры");
	Ошибка					= Макет.ПолучитьОбласть("Ошибка");
	ПоказательРасчетаЗаголовок	= Макет.ПолучитьОбласть("ПоказательРасчетаЗаголовок");
	ЗаголовокОбъектРасчета		= Макет.ПолучитьОбласть("ЗаголовокОбъектРасчета");

	
	
	Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 1 Тогда
		
		Заголовок.Параметры.Заполнить(сткТрассировка);
		Заголовок.Параметры.ВремяНачалоРасчета 		=  Формат(сткТрассировка.ВремяНачалоРасчета, "ДФ=Ч:мм:сс");
		Заголовок.Параметры.ВремяОкончаниеРасчета	=  Формат(сткТрассировка.ВремяОкончаниеРасчета, "ДФ=Ч:мм:сс");
		Заголовок.Параметры.ВремяРасчета = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремениДоСекунды(сткТрассировка.ВремяОкончаниеРасчета - сткТрассировка.ВремяНачалоРасчета);
		ПриемникРасшифровки.Приемник.Вывести(Заголовок);
		
		// Виды ГСМ.
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		Для каждого ВидГСМ Из мсвВидыГСМ Цикл
			
			ЗаголовокВидГСМ.Параметры.Заполнить(ВидГСМ);
			Если ВидГСМ.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаСтоКилометров") Тогда
				НадписьРасход 		  	= СтрШаблон("%1 л/100 км",ВидГСМ.Расход);
				НадписьИзменениеРасхода 	= СтрШаблон(Нстр("ru = '%1 л/100 т·км'"), ВидГСМ.ИзменениеРасхода);
			Иначе
				НадписьРасход 		  	= СтрШаблон("%1 л/100 км",ВидГСМ.Расход);
				НадписьИзменениеРасхода 	= СтрШаблон(Нстр("ru = '%1'"), ВидГСМ.ИзменениеРасхода);
			КонецЕсли;
               ЗаголовокВидГСМ.Параметры.Расход 			= НадписьРасход;
			ЗаголовокВидГСМ.Параметры.ИзменениеРасхода 	= НадписьИзменениеРасхода;
			ЗаголовокВидГСМ.Параметры.ПроцентИспользования = СтрШаблон("%1%%",ВидГСМ.ПроцентИспользования);
			ПриемникРасшифровки.Приемник.Вывести(ЗаголовокВидГСМ);
			Формула.Параметры.Заполнить(ВидГСМ);
			ПриемникРасшифровки.Приемник.Вывести(Формула);
			
			// Объекты расчета, в случае если расчет производится по правилам расчета
		 	мсвОбъектыРасчета = ВидГСМ.мсвОбъектыРасчета;
			Для каждого ОбъектРасчета  Из мсвОбъектыРасчета Цикл
				ТекущийОбъектРасчета.Параметры.Заполнить(ОбъектРасчета);
				ПриемникРасшифровки.Приемник.Вывести(ТекущийОбъектРасчета);
				
				мсвОшибкиПообъекту = ОбъектРасчета.мсвОшибки;
				Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
					Ошибка.Параметры.Заполнить(ЭлементОшибка);
					ПриемникРасшифровки.Приемник.Вывести(Ошибка);
				КонецЦикла;
				
				ПриемникРасшифровки.Приемник.Вывести(ПоказательРасчетаЗаголовок);
				
				мсвПоказателиРасчета = ОбъектРасчета.мсвПоказателиРасчета;
				
				Для каждого ПоказательРасчета Из мсвПоказателиРасчета Цикл
					ТекущийПоказательРасчета.Параметры.Заполнить(ПоказательРасчета);
					ПриемникРасшифровки.Приемник.Вывести(ТекущийПоказательРасчета);
					
					мсвОшибкиПообъекту = ОбъектРасчета.мсвОшибки;
					Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
						Ошибка.Параметры.Заполнить(ЭлементОшибка);
						ПриемникРасшифровки.Приемник.Вывести(Ошибка);
					КонецЦикла;
					
				КонецЦикла;
				
				РезультатПоОбъектуРасчета.Параметры.Заполнить(ОбъектРасчета);
				РезультатПоОбъектуРасчета.Параметры.ИтогоПоОбъекту = ФорматГСМ(ОбъектРасчета.ИтогоПоОбъекту);
				ПриемникРасшифровки.Приемник.Вывести(РезультатПоОбъектуРасчета);
				
				Если НЕ ОбъектРасчета.КоэффициентРаспределения = 1 Тогда
					КоэффициентРаспределения.Параметры.Заполнить(ОбъектРасчета);
					ПриемникРасшифровки.Приемник.Вывести(КоэффициентРаспределения);	
					
					РезультатПоОбъектуРасчетаСКоэффициентом.Параметры.Заполнить(ОбъектРасчета);
					ПриемникРасшифровки.Приемник.Вывести(РезультатПоОбъектуРасчетаСКоэффициентом);	
				КонецЕсли;
			
			КонецЦикла;
			
			// Маршрут рейса.
			мсвМаршрут	= ВидГСМ.мсвМаршрут;
			Если мсвМаршрут.Количество() > 0 Тогда
				ПриемникРасшифровки.Приемник.Вывести(ЗаголовокМаршрут);
				РасстояниеИтого 	= 0;
				ВремяИтого		= 0;
				БазовыйРасходИтого	= 0;
				РасходНаТранспортнуюРаботуИтого	= 0;
				РасходТопливаИтого	= 0;
				РасходТопливаСУчетомКоэффициентаИтого = 0;
				Для каждого Маршрут Из мсвМаршрут Цикл
					МаршрутТекущий.Параметры.Заполнить(Маршрут);
					МаршрутТекущий.Параметры.Расстояние 				= ФорматРасстояние(Маршрут.Расстояние);
					МаршрутТекущий.Параметры.БазовыйРасход 				= ФорматГСМ(Маршрут.БазовыйРасход);
					МаршрутТекущий.Параметры.РасходНаТранспортнуюРаботу 	= ФорматГСМ(Маршрут.РасходНаТранспортнуюРаботу);
					МаршрутТекущий.Параметры.РасходТоплива 				= ФорматГСМ(Маршрут.РасходТоплива);
					МаршрутТекущий.Параметры.РасходТопливаСУчетомКоэффициента = ФорматГСМ(Маршрут.РасходТопливаСУчетомКоэффициента);
					ПриемникРасшифровки.Приемник.Вывести(МаршрутТекущий);
					РасстояниеИтого 	= РасстояниеИтого + Маршрут.Расстояние;
					ВремяИтого		= ВремяИтого + Маршрут.Время;
					БазовыйРасходИтого	= БазовыйРасходИтого + Маршрут.БазовыйРасход;
					РасходНаТранспортнуюРаботуИтого	= РасходНаТранспортнуюРаботуИтого + Маршрут.РасходНаТранспортнуюРаботу;
					РасходТопливаИтого	= РасходТопливаИтого + Маршрут.РасходТоплива;
					РасходТопливаСУчетомКоэффициентаИтого = РасходТопливаСУчетомКоэффициентаИтого + Маршрут.РасходТопливаСУчетомКоэффициента;
				КонецЦикла;
				МаршрутИтого.Параметры.РасстояниеИтого 		= ФорматРасстояние(РасстояниеИтого);
				МаршрутИтого.Параметры.ВремяИтого 			= ФорматРасстояние(ВремяИтого);
				МаршрутИтого.Параметры.БазовыйРасходИтого 	= ФорматГСМ(БазовыйРасходИтого);
				МаршрутИтого.Параметры.РасходНаТранспортнуюРаботуИтого = ФорматГСМ(РасходНаТранспортнуюРаботуИтого);
				МаршрутИтого.Параметры.РасходТопливаИтого 	= ФорматГСМ(РасходТопливаИтого);
				МаршрутИтого.Параметры.РасходТопливаСУчетомКоэффициентаИтого = ФорматГСМ(РасходТопливаСУчетомКоэффициентаИтого);
				ПриемникРасшифровки.Приемник.Вывести(МаршрутИтого);
			КонецЕсли;
			
			// Прицепы.
			мсвПрицепы	= ВидГСМ.мсвПрицепы;
			Если мсвПрицепы.Количество() > 0 Тогда
				ПриемникРасшифровки.Приемник.Вывести(ЗаголовокПрицеп);
				РасходТопливаПоНормеИтого = 0;
				РасходТопливаСУчетомКоэффициентаИтого = 0;
				Для каждого Прицеп Из мсвПрицепы Цикл
					ПрицепТекущий.Параметры.Заполнить(Прицеп);
					ПрицепТекущий.Параметры.НормаРасхода  = ФорматГСМ(Прицеп.Расход) + " " + Прицеп.ТипРасхода;
					Если Прицеп.ТипРасхода = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода") Тогда
						ПрицепТекущий.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматГСМ(Прицеп.БазовыйРасходТоплива), Нстр("ru = 'л'"));
					Иначе	
						ПрицепТекущий.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматРасстояние(Прицеп.Количество), упСервисныеФункцииКлиентСервер.ЕдиницыИзмеренияКоличестваНадбавкиНормыГСМ(Прицеп.ТипРасхода));	
					КонецЕсли;
					ПрицепТекущий.Параметры.РасходТопливаПоНорме = ФорматГСМ(Прицеп.РасходТопливаПоНорме);
					ПрицепТекущий.Параметры.РасходТопливаСУчетомКоэффициента = ФорматГСМ(Прицеп.РасходТопливаСУчетомКоэффициента);
					ПриемникРасшифровки.Приемник.Вывести(ПрицепТекущий);
					РасходТопливаПоНормеИтого = РасходТопливаПоНормеИтого + Прицеп.РасходТопливаПоНорме;
					РасходТопливаСУчетомКоэффициентаИтого = РасходТопливаСУчетомКоэффициентаИтого + Прицеп.РасходТопливаСУчетомКоэффициента;
				КонецЦикла;
				ПрицепИтого.Параметры.РасходТопливаПоНормеИтого = ФорматГСМ(РасходТопливаПоНормеИтого);
				ПрицепИтого.Параметры.РасходТопливаСУчетомКоэффициентаИтого = ФорматГСМ(РасходТопливаСУчетомКоэффициентаИтого);
				ПриемникРасшифровки.Приемник.Вывести(ПрицепИтого);
			КонецЕсли;

			
			мсвНадбавки	= ВидГСМ.мсвНадбавки;
			Если мсвНадбавки.Количество() > 0 Тогда
				ПриемникРасшифровки.Приемник.Вывести(ЗаголовокНадбавки);
				РасходТопливаИтого = 0;
				Для каждого Надбавка Из мсвНадбавки Цикл
					НадбавкаТекущая.Параметры.Заполнить(Надбавка);
					НадбавкаТекущая.Параметры.НормаРасхода 	= ФорматГСМ(Надбавка.Расход) + " " + Надбавка.ТипРасхода;
					Если Надбавка.ТипРасхода = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода") Тогда
						НадбавкаТекущая.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматГСМ(Надбавка.БазовыйРасходТоплива), Нстр("ru = 'л'"));
					Иначе	
						НадбавкаТекущая.Параметры.Количество 	= СтрШаблон("%1 %2", Надбавка.Количество, упСервисныеФункцииКлиентСервер.ЕдиницыИзмеренияКоличестваНадбавкиНормыГСМ(Надбавка.ТипРасхода));	
					КонецЕсли;
					
					НадбавкаТекущая.Параметры.РасходТоплива = ФорматГСМ(Надбавка.РасходТоплива);
					ПриемникРасшифровки.Приемник.Вывести(НадбавкаТекущая);
					РасходТопливаИтого = РасходТопливаИтого + Надбавка.РасходТоплива;
				КонецЦикла;
				НадбавкаИтого.Параметры.РасходТопливаИтого = ФорматГСМ(РасходТопливаИтого);
				ПриемникРасшифровки.Приемник.Вывести(НадбавкаИтого);
			КонецЕсли;
			
			
			мсвДопОборудование	= ВидГСМ.мсвДопОборудование;
			Если мсвДопОборудование.Количество() > 0 Тогда
				Для каждого ДопОборудование Из мсвДопОборудование Цикл
					
					мсвРежимыРаботы	= ДопОборудование.мсвРежимыРаботыДопОборудования;
					Если мсвРежимыРаботы.Количество() > 0 Тогда
						ЗаголовокДопОборудование.Параметры.ДопОборудование = ДопОборудование.ДопОборудование;
						ПриемникРасшифровки.Приемник.Вывести(ЗаголовокДопОборудование);
						
						РасходТопливаИтого = 0;
						Для каждого РежимРаботы Из мсвРежимыРаботы Цикл
							ДопоборудованиеТекущее.Параметры.Заполнить(РежимРаботы);
							ДопоборудованиеТекущее.Параметры.НормаРасхода 	= ФорматГСМ(РежимРаботы.Расход) + " " + РежимРаботы.ТипНормыРасходаГСМ;
							Если РежимРаботы.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаОдинЧасРаботы") Тогда
								ДопоборудованиеТекущее.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматГСМ(РежимРаботы.Количество), Нстр("ru = 'ч'"));
							Иначе	
								ДопоборудованиеТекущее.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматГСМ(РежимРаботы.Количество), Нстр("ru = 'км'"));
							КонецЕсли;
							
							ДопоборудованиеТекущее.Параметры.РасходТоплива = ФорматГСМ(РежимРаботы.РасходТоплива);
							ПриемникРасшифровки.Приемник.Вывести(ДопоборудованиеТекущее);	
							РасходТопливаИтого = РасходТопливаИтого + РежимРаботы.РасходТоплива;
						КонецЦикла;
						ДопоборудованиеИтого.Параметры.РасходТопливаИтого = ФорматГСМ(РасходТопливаИтого);
						ПриемникРасшифровки.Приемник.Вывести(ДопоборудованиеИтого);
					КонецЕсли;
					
					мсвНадбавки	= ДопОборудование.мсвНадбавкаДопОборудование;
					Если мсвНадбавки.Количество() > 0 Тогда
						ПриемникРасшифровки.Приемник.Вывести(ЗаголовокНадбавки);
						РасходТопливаИтого = 0;
						Для каждого Надбавка Из мсвНадбавки Цикл
							НадбавкаТекущая.Параметры.Заполнить(Надбавка);
							НадбавкаТекущая.Параметры.НормаРасхода 	= ФорматГСМ(Надбавка.Расход) + " " + Надбавка.ТипРасхода;
							Если Надбавка.ТипРасхода = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода") Тогда
								НадбавкаТекущая.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматГСМ(Надбавка.БазовыйРасходТоплива), Нстр("ru = 'л'"));
							Иначе	
								НадбавкаТекущая.Параметры.Количество 	= СтрШаблон("%1 %2", ФорматРасстояние(Надбавка.Количество), упСервисныеФункцииКлиентСервер.ЕдиницыИзмеренияКоличестваНадбавкиНормыГСМ(Надбавка.ТипРасхода));	
							КонецЕсли;
							
							НадбавкаТекущая.Параметры.РасходТоплива = ФорматГСМ(Надбавка.РасходТоплива);
							ПриемникРасшифровки.Приемник.Вывести(НадбавкаТекущая);
							РасходТопливаИтого = РасходТопливаИтого + Надбавка.РасходТоплива;
						КонецЦикла;
						НадбавкаИтого.Параметры.РасходТопливаИтого = ФорматГСМ(РасходТопливаИтого);
						ПриемникРасшифровки.Приемник.Вывести(НадбавкаИтого);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;

			ИтогоПоВидуГСМ.Параметры.Заполнить(ВидГСМ);
			ПриемникРасшифровки.Приемник.Вывести(ИтогоПоВидуГСМ);
			
			мсвОшибкиПообъекту = ВидГСМ.мсвОшибки;
			Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
				Ошибка.Параметры.Заполнить(ЭлементОшибка);
				ПриемникРасшифровки.Приемник.Вывести(Ошибка);
			КонецЦикла;
						
		КонецЦикла;
	
	КонецЕсли;
	
	мсвОшибкиПообъекту = сткТрассировка.мсвОшибки;
	Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
		Ошибка.Параметры.Заполнить(ЭлементОшибка);
		ПриемникРасшифровки.Приемник.Вывести(Ошибка);
	КонецЦикла;
	
	Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений <= 1 Тогда
		Итог.Параметры.Заполнить(сткТрассировка);
		ПриемникРасшифровки.Приемник.Вывести(Итог);
	КонецЕсли;
	
	ПриемникРасшифровки.Приемник.ТолькоПросмотр = Истина;
	ПриемникРасшифровки.Приемник.ОтображатьСетку = Ложь;
	ПриемникРасшифровки.Приемник.ОтображатьЗаголовки = Ложь;
	
КонецПроцедуры

Функция ФорматГСМ(Расход)
	Возврат Формат(Расход, "ЧЦ=12; ЧДЦ=3");	
КонецФункции

Функция ФорматРасстояние(Расстояние)
	Возврат Формат(Расстояние, "ЧЦ=10; ЧДЦ=3");			
КонецФункции


// Заполнение макета печати "Ошибки".
//
// Параметры:
//  сткТрассировка - Структура - Параметры трассировки.
//  ПриемникРасшифровки - Структура - Параметры расшифровки.
//
Процедура СформироватьТабличныйДокументРасшифровки_РегламентнаяОперация(сткТрассировка, ПриемникРасшифровки) Экспорт
	
	Макет = Документы.упРегламентнаяОперация.ПолучитьМакет("Ошибки");
	
	Заголовок 	= Макет.ПолучитьОбласть("Заголовок");
	ПриемникРасшифровки.Приемник.Вывести(Заголовок);
	Ошибка		= Макет.ПолучитьОбласть("Ошибка");
	ВидОперации	= Макет.ПолучитьОбласть("ВидОперации");
	ВидОперации.Параметры.ВидОперации = сткТрассировка.ВидОперации;
	ПриемникРасшифровки.Приемник.Вывести(ВидОперации);
	мсвОшибки 	= сткТрассировка.мсвОшибки;
	НомерОшибки = 0;
	Для каждого ЭлементОшибка Из мсвОшибки Цикл
		НомерОшибки = НомерОшибки + 1;
		ОписаниеОшибки = ЭлементОшибка;
		Ошибка.Параметры.НомерОшибки 	= НомерОшибки;
		Ошибка.Параметры.ОписаниеОшибки = ОписаниеОшибки.Сообщение;
		ПриемникРасшифровки.Приемник.Вывести(Ошибка);
	КонецЦикла;
	
	ПриемникРасшифровки.Приемник.ТолькоПросмотр = Истина;
	ПриемникРасшифровки.Приемник.ОтображатьСетку = Ложь;
	ПриемникРасшифровки.Приемник.ОтображатьЗаголовки = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ФункцииПереадресацияМодулюОбщегоНазначения

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
// 
//  Если доступа к реквизиту нет, возникнет исключение прав доступа.
//  Если необходимо зачитать реквизит независимо от прав текущего пользователя,
//  то следует использовать предварительный переход в привилегированный режим.
//
// Функция не предназначена для получения значений реквизитов пустых ссылок.
// 
// Параметры:
//  Ссылка - СсылкаНаОбъект, - элемент справочника, документ, ...
//  ИмяРеквизита - Строка - например, "Код".
// 
// Возвращаемое значение:
//  Произвольный - зависит от типа значения прочитанного реквизита.
// 
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции 

// Возвращает значения реквизитов, прочитанные из информационной базы,
// для нескольких объектов.
//
// Параметры:
//  МассивСсылок - Массив, - Ссылки на элемент справочника, документ, ...
//  ИменаРеквизитов - Строка - например, "Код".
// 
// Возвращаемое значение:
//  Произвольный - зависит от типа значения прочитанного реквизита.
//
Функция ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаРеквизитов) Экспорт
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаРеквизитов);		
КонецФункции

// Возвращает структуру, содержащую значения реквизитов прочитанные из информационной базы,
// по ссылке на объект.
//
// Параметры:
//  Ссылка - СсылкаНаОбъект, - Ссылки на элемент справочника, документ, ...
//  Реквизиты - Строка - например, "Код".
// 
// Возвращаемое значение:
//  Произвольный - зависит от типа значения прочитанного реквизита.
//
Функция ЗначенияРеквизитовОбъекта(Ссылка, Знач Реквизиты) Экспорт
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);
КонецФункции

// Получает имя значения перечисления как объекта метаданных.
//
// Параметры:
//  Значение - Произвольный, - Данные значения.
// 
// Возвращаемое значение:
//  Строка - Имя значения.
//
Функция ИмяЗначенияПеречисления(Значение) Экспорт
	
	Возврат ОбщегоНазначения.ИмяЗначенияПеречисления(Значение);
	
КонецФункции 

#КонецОбласти

#Область ДополнительныеОтчетыИОбработки

// Получает макет по ссылке на внешнюю печатную форму.
//
// Параметры:
//  СсылкаДополнительныеОтчетыОбработки - СсылкаНаОбъект, - Дополнительные отчеты.
//  ИмяОбъектаМетаданных - Строка - Наименование.
// 
// Возвращаемое значение:
//  Строка - Результат выполнения.
//
Функция ПолучитьИмяМакетаВнешнейПечатнойФормы(СсылкаДополнительныеОтчетыОбработки, ИмяОбъектаМетаданных) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = ДополнительныеОтчетыИОбработки.НовыйЗапросПоДоступнымКомандам(Перечисления.ВидыДополнительныхОтчетовИОбработок.ПечатнаяФорма, ИмяОбъектаМетаданных);
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	
	ПакетЗапросовСхемы 	= СхемаЗапроса.ПакетЗапросов;
	ПакетЗапросов0 		= ПакетЗапросовСхемы[0];
	ОператорВыбратьСхемыЗапросов 	= ПакетЗапросов0.Операторы[0];
	Отбор				= ОператорВыбратьСхемыЗапросов.Отбор;
	Отбор.Добавить("ТаблицаНазначение.Ссылка = &СсылкаДополнительныеОтчетыОбработки");
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("СсылкаДополнительныеОтчетыОбработки", СсылкаДополнительныеОтчетыОбработки);

	тбзДоступныеКоманды = Запрос.Выполнить().Выгрузить();
	
	Если тбзДоступныеКоманды.Количество() > 0 Тогда
		текРезультат = "ВнешняяПечатнаяФорма." + тбзДоступныеКоманды[0].Идентификатор;
	КонецЕсли;

	Возврат текРезультат;

КонецФункции

// Возвращает список доступных команд для объекта.
//
// Параметры:
//  ИмяОбъектаМетаданных - Строка - Наименование.
// 
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокДополнительныхВнешнихПечатныхФорм(ИмяОбъектаМетаданных) Экспорт
	
	Запрос = ДополнительныеОтчетыИОбработки.НовыйЗапросПоДоступнымКомандам(Перечисления.ВидыДополнительныхОтчетовИОбработок.ПечатнаяФорма, ИмяОбъектаМетаданных);

	тбзДоступныеКоманды = Запрос.Выполнить().Выгрузить();
	
	тбзДоступныеКоманды.Свернуть("Ссылка");
	
	спзДополнительныхФорм = Новый СписокЗначений;
	спзДополнительныхФорм.ЗагрузитьЗначения(тбзДоступныеКоманды.ВыгрузитьКолонку("Ссылка"));

	Возврат спзДополнительныхФорм;
	
КонецФункции

#КонецОбласти

// Возвращает признак наличия полный прав.
//
// Возвращаемое значение:
//  Произвольный - Результат выполнения.
//
Функция ЭтоАдминистратор() Экспорт
	
	Возврат Пользователи.ЭтоПолноправныйПользователь(, Истина);
	
КонецФункции

// Процедура установить значения констант по переданным параметрам.
//
// Параметры:
//  УправлениеПеревозками - Булево, - Выполнить.
//  УправлениеТранспортом - Булево, - Выполнить.
//  СпутниковыйМониторинг - Булево, - Выполнить.
//
Процедура УстановитьИспользованиеГлобальныхКомпонент(УправлениеПеревозками, УправлениеТранспортом, СпутниковыйМониторинг) Экспорт
	
	Если УправлениеПеревозками Тогда
		Константы.упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками.Установить(Истина);
	КонецЕсли; 
	Если УправлениеТранспортом Тогда
		Константы.упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом.Установить(Истина);
	КонецЕсли; 
	Если СпутниковыйМониторинг Тогда
		Константы.упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг.Установить(Истина);
	КонецЕсли; 
	
КонецПроцедуры

// Процедура снять значения констант по переданным параметрам.
//
// Параметры:
//  УправлениеПеревозками - Булево, - Выполнить.
//  УправлениеТранспортом - Булево, - Выполнить.
//  СпутниковыйМониторинг - Булево, - Выполнить.
//
Процедура ОтменитьИспользованиеГлобальныхКомпонент(УправлениеПеревозками, УправлениеТранспортом, СпутниковыйМониторинг) Экспорт
	
	Если УправлениеПеревозками Тогда
		Константы.упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками.Установить(Ложь);
	КонецЕсли; 
	Если УправлениеТранспортом Тогда
		Константы.упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом.Установить(Ложь);
	КонецЕсли; 
	Если СпутниковыйМониторинг Тогда
		Константы.упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг.Установить(Ложь);
	КонецЕсли; 
	
КонецПроцедуры

// Получает описание ошибки, которой завершилось фоновое задание
//
// Параметры:
//  ИдентификаторЗадания	 - Идентификатор - идентификатор
// 
// Возвращаемое значение:
//  ОписаниеОшибки - описание ошибки
//
Функция ПолучитьОписаниеОшибкиФоновогоЗадания(ИдентификаторЗадания) Экспорт 
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
	КонецЕсли; 
	Возврат ОписаниеОшибки

КонецФункции


#Область СозданиеЭлементовФормы

// Процедура отображения прицепов ТС для переданной формы.
//
// Параметры:
//  Форма				 - УправляемаяФорма	 - Для доступа к свойствам, методам и событиям управляемой формы в целом.
//  ТранспортноеСредство	 - СправочникСсылка	 - Ссылка на элемент.
//  ДатаСреза			 - Дата			 - Период.
//  СписокПрицепы		 - СписокЗначений	 - список прицепов.
//
Процедура ОтобразитьПрицепыТранспортногоСредства(Форма, ТранспортноеСредство, ДатаСреза, СписокПрицепы = Неопределено) Экспорт
	
	Если СписокПрицепы = Неопределено Тогда
		СписокПрицепы = Новый СписокЗначений;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	ГруппаРодитель = Элементы.ГруппаПрицепыТранспортногоСредства;
	
    Сч = ГруппаРодитель.ПодчиненныеЭлементы.Количество();
	Пока Сч > 0 Цикл
		Элементы.Удалить(ГруппаРодитель.ПодчиненныеЭлементы.Получить(Сч - 1));
	    Сч = Сч - 1;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПрицепы.Прицеп КАК Прицеп
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК упПрицепы
	|ГДЕ
	|	упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)";
	Запрос.УстановитьПараметр("Период", ДатаСреза);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		НоваяГруппаЛево = Элементы.Добавить("ГруппаПрицепыТранспортногоСредстваЛево", Тип("ГруппаФормы"), ГруппаРодитель);
		НоваяГруппаЛево.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппаЛево.Отображение = ОтображениеОбычнойГруппы.Нет;
		НоваяГруппаЛево.ОтображатьЗаголовок = Ложь;
		
		НоваяКартинка = Элементы.Добавить("ДекорацияПустаяКартинкаПрицеп", Тип("ДекорацияФормы"), НоваяГруппаЛево);
		НоваяКартинка.Вид = ВидДекорацииФормы.Картинка;
		НоваяКартинка.Картинка = БиблиотекаКартинок.упПустаяКартинка;
		НоваяКартинка.РастягиватьПоГоризонтали = Истина;
		
		НоваяГруппаПраво = Элементы.Добавить("ГруппаПрицепыТранспортногоСредстваПраво", Тип("ГруппаФормы"), ГруппаРодитель);
		НоваяГруппаПраво.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппаПраво.Отображение = ОтображениеОбычнойГруппы.Нет;
		НоваяГруппаПраво.ОтображатьЗаголовок = Ложь;
		НоваяГруппаПраво.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		
		НоваяНадпись = Элементы.Добавить("ДекорацияПрицепнойСоставНадпись", Тип("ДекорацияФормы"), НоваяГруппаПраво);
		НоваяНадпись.Вид = ВидДекорацииФормы.Надпись;
		НоваяНадпись.Заголовок = НСтр("ru = 'Прицепной состав:'");
		НоваяНадпись.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		НоваяНадпись.РастягиватьПоГоризонтали = Ложь;
		НоваяНадпись.Шрифт = ШрифтыСтиля.упСреднийШрифт;
		НоваяНадпись.ЦветТекста  = ЦветаСтиля.упЦветЗаголовковФорм;
		
		Выборка = Результат.Выбрать();
		Сч = 1;
		соотНовыеРеквизиты = Новый Соответствие;
		ДобавляемыеРеквизиты = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ИмяРеквизита = СтрШаблон("ПрицепТранспортногоСредства%1", Сч);
			ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства")));
			соотНовыеРеквизиты.Вставить(ИмяРеквизита, Выборка.Прицеп);
			СписокПрицепы.Добавить(Выборка.Прицеп);
			Сч = Сч + 1;
		КонецЦикла;
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты, Форма.ДобавленныеДинамическиРеквизиты.ВыгрузитьЗначения());
		Форма.ДобавленныеДинамическиРеквизиты.Очистить();
		Для каждого текРеквизит Из соотНовыеРеквизиты Цикл
			Форма[текРеквизит.Ключ] = текРеквизит.Значение;
			Форма.ДобавленныеДинамическиРеквизиты.Добавить(текРеквизит.Ключ);
		КонецЦикла;

		Выборка.Сбросить();
		Сч = 1;
		Пока Выборка.Следующий() Цикл
			НоваяГруппаПрицеп = Элементы.Добавить(СтрШаблон("ГруппаУстановленныйПрицеп%1", Сч), Тип("ГруппаФормы"), НоваяГруппаПраво);
			НоваяГруппаПрицеп.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			НоваяГруппаПрицеп.Отображение = ОтображениеОбычнойГруппы.Нет;
			НоваяГруппаПрицеп.ОтображатьЗаголовок = Ложь;
			НоваяГруппаПрицеп.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			
			НоваяКартинка = Элементы.Добавить(СтрШаблон("ДекорацияПрицепКартинкаПрицеп%1", Сч), Тип("ДекорацияФормы"), НоваяГруппаПрицеп);
			НоваяКартинка.Вид = ВидДекорацииФормы.Картинка;
			НоваяКартинка.Картинка = БиблиотекаКартинок.КоннекторВерхПраво;
			НоваяКартинка.РастягиватьПоГоризонтали = Ложь;
			
			НовоеПоле = Элементы.Добавить(СтрШаблон("ПолеПрицеп%1", Сч), Тип("ПолеФормы"), НоваяГруппаПрицеп);
			НовоеПоле.Вид = ВидПоляФормы.ПолеВвода;
			НовоеПоле.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
			НовоеПоле.РастягиватьПоГоризонтали = Ложь;
			НовоеПоле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет; 
			НовоеПоле.ПутьКДанным =  СтрШаблон("ПрицепТранспортногоСредства%1", Сч);
			НовоеПоле.ТолькоПросмотр = Истина;
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли; 

КонецПроцедуры
	
#КонецОбласти 

#КонецОбласти

#Область ПрочиеСерверныеПроцедурыФункции

// Проверяет возможность создания предложения перевозчика по заявке на ТС
//
// Параметры:
//  ЗаявкаНаТС  - ДокументСсылка.упЗаявкаНаТС - Ссылка на документ.
//  ТекстОшибки  - Строка - Представление ошибки.
//  Отказ  - Булево - Фиксировать изменения.
//
// Возвращаемое значение:
//   ДокументСсылка.упПредложениеПеревозчика - Если предложение по заявке существует, возвращается ссылка на документ.
//
Функция ВозможноСозданиеПредложенияПеревозчика(ЗаявкаНаТС, Отказ) Экспорт

	Если Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаНаТС, "Тендер") Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = '%1 не является тендером, предложение не будет сформировано.'"), ЗаявкаНаТС);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат Неопределено;
	КонецЕсли;
	
	СтатусЗаявкиНаТС = Документы.упЗаявкаНаТС.ПолучитьСтатус(ЗаявкаНаТС);
	
	Если СтатусЗаявкиНаТС = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Или СтатусЗаявкиНаТС = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ ""%1"" находится в статусе ""%2"", предложение не будет сформировано.'"), ЗаявкаНаТС, СтатусЗаявкиНаТС);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПредложениеПеревозчика.Ссылка КАК ПредложениеПеревозчика
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.ЗаявкаНаТС = &ЗаявкаНаТС
	|	И НЕ упПредложениеПеревозчика.ПометкаУдаления";
	Запрос.УстановитьПараметр("ЗаявкаНаТС", ЗаявкаНаТС);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ТекстСообщения = СтрШаблон(НСтр("ru = 'По документу ""%1"" уже создан документ ""%2"".'"), ЗаявкаНаТС, Выборка.ПредложениеПеревозчика);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат Выборка.ПредложениеПеревозчика;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

// Проверяет возможность создания предложения перевозчика по заявке на ТС
//
// Параметры:
//  ДокументОснование  - ДокументСсылка.упЗаявкаНаТС, ДокументСсылка.упПредложениеПеревозчика - Ссылка на документ.
//  ТекстОшибки  - Строка - Представление ошибки.
//  Отказ  - Булево - Фиксировать изменения.
//
// Возвращаемое значение:
//   ДокументСсылка.упПодтверждениеЗаявкиНаТС - Если предложение по заявке существует, возвращается ссылка на документ.
//
Функция ВозможноСозданиеПодтвержденияЗаявкиНаТС(ДокументОснование, Отказ, ПоСобытию = Ложь) Экспорт

	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Тендер") Тогда
			ТекстСообщения	= СтрШаблон(НСтр("ru = 'Так как %1 является тендером, то подтверждение формируется на основании предложения перевозчика.'"), ДокументОснование); 
			Если ПоСобытию Тогда
				Отказ = Истина;
				Возврат ТекстСообщения;
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				Возврат Неопределено;
			КонецЕсли; 
		КонецЕсли;
		ЗаявкаНаТС = ДокументОснование;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда
		ЗаявкаНаТС =  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ЗаявкаНаТС");
		
	КонецЕсли;
	
	СтатусЗаявкиНаТС = Документы.упЗаявкаНаТС.ПолучитьСтатус(ЗаявкаНаТС);
	Если СтатусЗаявкиНаТС = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Или СтатусЗаявкиНаТС = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ ""%1"" находится в статусе ""%2"", подтверждение заявки на ТС не будет сформировано.'"), ЗаявкаНаТС, СтатусЗаявкиНаТС);
		Если ПоСобытию Тогда
			Отказ = Истина;
			Возврат ТекстСообщения;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Возврат Неопределено;
		КонецЕсли; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПодтверждениеЗаявкиНаТС.Ссылка КАК ПодтверждениеЗаявкиНаТС
	|ИЗ
	|	Документ.упПодтверждениеЗаявкиНаТС КАК упПодтверждениеЗаявкиНаТС
	|ГДЕ
	|	упПодтверждениеЗаявкиНаТС.Основание = &ДокументОснование
	|	И НЕ упПодтверждениеЗаявкиНаТС.ПометкаУдаления";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ТекстСообщения = СтрШаблон(НСтр("ru = 'По документу ""%1"" уже создан документ ""%2"".'"), ЗаявкаНаТС, Выборка.ПодтверждениеЗаявкиНаТС);
		Если ПоСобытию Тогда
			Отказ = Истина;
			Возврат ТекстСообщения;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Возврат Выборка.ПодтверждениеЗаявкиНаТС;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
	
#КонецОбласти

