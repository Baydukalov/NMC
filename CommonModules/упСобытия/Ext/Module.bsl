
#Область ПрограммныйИнтерфейс

// Процедура регистрирует выполнение документов.
//
// Параметры:
//	Источник - ДокументОбъект - Объект документа.
//	Отказ - Булево - Применить изменения объекта.
//
Процедура ДокументыПриЗаписи(Источник, Отказ) Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник.ДополнительныеСвойства, "НеРегистрироватьСобытие") Тогда
		Возврат;	
	КонецЕсли;
	
	Записывать = Ложь;
	ВозможнаПовторнаяРегистрация = Ложь;
	
	ТипИсточника = ТипЗнч(Источник);
	Если ТипИсточника = Тип("ДокументОбъект.упПодтверждениеЗаявкиНаТС") Тогда 
		Записывать  = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьПодтвержденияЗаявкиНаТС;
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упЗаявкаНаПеревозкуГруза") Тогда	
		Записывать	= Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗаявкиНаПеревозкуГруза;
		ВозможнаПовторнаяРегистрация = Истина;
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упПредложениеПеревозчика") Тогда	
		Записывать	= Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьПредложенияПеревозчика;
		ВозможнаПовторнаяРегистрация = Истина;	
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упЗаданиеНаПеревозкуГруза") Тогда	
		Записывать	= Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗаданияНаПеревозкуГруза;
		ВозможнаПовторнаяРегистрация = Истина;
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упРейс") Тогда	
		Записывать	= Истина;
		Если Источник.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьОперацийСКонтейнером;
		Иначе
			ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьРейса;
		КонецЕсли; 
		ВозможнаПовторнаяРегистрация = Истина;	
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упПрохождениеТочки") Тогда 
		Записывать  = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьПрохождениеТочки;
		ВозможнаПовторнаяРегистрация = Истина;
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упПутевойЛист") Тогда 
		Записывать  = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьПутевогоЛиста;	
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упСобытиеМониторинга") Тогда 
		Записывать  = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьСобытияМониторинга;	
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упФактическиеРасходы") Тогда
		Записывать = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьФактическихРасходов;
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.упЗапросУсловийПеревозки") Тогда
		Записывать = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗапросаУсловийПеревозки;	
	ИначеЕсли ТипИсточника = Тип("ЗадачаОбъект.ЗадачаИсполнителя") Тогда
		Записывать = Истина;
		ТипДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗадачиИсполнителя;	
	КонецЕсли;
	
	Если Записывать Тогда 
		Запись = РегистрыСведений.упЗаписанныеДокументы.СоздатьМенеджерЗаписи();
		Запись.Документ = Источник.Ссылка;
		Запись.ТипДействия = ТипДействия;
		Запись.Прочитать();
		Если Не ЗначениеЗаполнено(Запись.Документ) ИЛИ (Запись.Обработан И ВозможнаПовторнаяРегистрация) Тогда 
			Запись.Обработан       = Ложь;
			Запись.ДатаОбработки   = '00010101';
			Запись.Документ        = Источник.Ссылка;
			Запись.ТипДействия     = ТипДействия;
			Запись.ДатаРегистрации = ТекущаяДатаСеанса();
			Запись.Записать(Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры                       

// Процедура регистрирует изменение статуса.
//
// Параметры:
//	Источник - ДокументОбъект - Объект документа.
//	Отказ - Булево - Применить изменения объекта.
//
Процедура ДокументыПриИзмененииСтатуса(Источник, Отказ) Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	МассивДокументов = Источник.ВыгрузитьКолонку("Документ");
	МассивДокументов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивДокументов);
	
	ТипИсточника = ТипЗнч(Источник);
	Для Каждого Документ Из МассивДокументов Цикл 

		Записывать = Ложь;
		Если ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упСтатусыЗаявокНаПеревозкуГруза") Тогда 
			Записывать  = Истина;
			ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаПеревозку;
		ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упСтатусыРейсов") Тогда	
			Записывать  = Истина;
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ВидТранспортнойУслуги") = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
				ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаОперацийСКонтейнером;
			Иначе
				ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаРейса;
			КонецЕсли; 
		ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упСтатусыЗаданийНаПеревозкуГруза") Тогда	
			Записывать  = Истина;
			ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаданияНаПеревозку;
		ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упСтатусыПутевыхЛистов") Тогда	
			Записывать  = Истина;
			ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаПутевогоЛиста;
		ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упСтатусыЗаявокНаТС") Тогда	
			Записывать  = Истина;
			ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаТС;
		ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упСтатусыЗапросовУсловийПеревозок") Тогда	
			Записывать  = Истина;
			ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗапросаУсловийПеревозки;
		КонецЕсли;
		
		Если Записывать Тогда 
			Запись = РегистрыСведений.упЗаписанныеДокументы.СоздатьМенеджерЗаписи();
			Запись.Документ = Документ;
			Запись.ТипДействия = ТипДействия;
			Запись.Прочитать();
			
			Запись.Обработан = Ложь;
			Запись.ДатаОбработки = '00010101';
			Запись.Документ = Документ;
			Запись.ТипДействия = ТипДействия;
			Запись.ДатаРегистрации = ТекущаяДатаСеанса();
			Запись.Записать(Истина);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура регистрирует действия по рейсу.
//
// Параметры:
//	Источник - ДокументОбъект - Объект документа.
//	Отказ - Булево - Применить изменения объекта.
//
Процедура РейсыПриИзменении(Источник, Отказ) Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Источник.Количество() Тогда
		МассивДокументов = Источник.ВыгрузитьКолонку("Регистратор");
		МассивДокументов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивДокументов);
		
		ТипИсточника = ТипЗнч(Источник);
		Для Каждого Документ Из МассивДокументов Цикл 
			
			Если Ложь
				Или ТипЗнч(Документ) = Тип("ДокументСсылка.упПроисшествиеВПути")
				Или ТипЗнч(Документ) = Тип("ДокументСсылка.упКорректировкаРегистров")
			Тогда
				Продолжить;
			КонецЕсли;
				
			Записывать = Ложь;
			Если ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упМаршрутПоРейсу") Тогда 
				Записывать  = Истина;
				ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеМаршрутаПоРейсу;
			ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упРаботыПоРейсу") Тогда	
				Записывать  = Истина;
				ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеРаботПоРейсу;
			ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.упПеревозчикПоРейсу") Тогда
				
				ВидТранспортнойУслуги = Неопределено;
				
				Если ТипЗнч(Документ) = Тип("ДокументСсылка.упРейс") Тогда
					ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ВидТранспортнойУслуги");
				Иначе
					ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Рейс.ВидТранспортнойУслуги");
				КонецЕсли;
				Если ВидТранспортнойУслуги  = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
					Записывать  = Истина;
					ТипДействия = Перечисления.упТипыДействийСобытий.ИзменениеЭкипажаПоРейсу;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Записывать Тогда 
				Запись = РегистрыСведений.упЗаписанныеДокументы.СоздатьМенеджерЗаписи();
				Запись.Документ        = Документ;
				Запись.ТипДействия     = ТипДействия;
				Запись.Прочитать();
				
				Запись.Обработан       = Ложь;
				Запись.ДатаОбработки   = '00010101';
				Запись.Документ        = Документ;
				Запись.ТипДействия     = ТипДействия;
				Запись.ДатаРегистрации = ТекущаяДатаСеанса();
				Запись.Записать(Истина);
			КонецЕсли;
		КонецЦикла;		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура ищет возникшие события и регистрирует их.
//
Процедура ПроверкаВозникновенияСобытий() Экспорт
		
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = 
	"ВЫБРАТЬ
	|	тбЗаписанныеДокументы.Документ,
	|	тбЗаписанныеДокументы.ПрисоединенныеФайлы КАК ПрисоединенныеФайлы,
	|	тбЗаписанныеДокументы.ДатаРегистрации,
	|	тбЗаписанныеДокументы.ТипДействия КАК ТипДействия
	|ИЗ
	|	РегистрСведений.упЗаписанныеДокументы КАК тбЗаписанныеДокументы
	|ГДЕ
	|	НЕ тбЗаписанныеДокументы.Обработан";
	
	текРезультат = текЗапрос.Выполнить();
	соотДокументов = Новый Соответствие;
	соотДат = Новый Соответствие;
	текДата = ТекущаяДатаСеанса();
	
	текВыборка = текРезультат.Выбрать();
	Пока текВыборка.Следующий() Цикл 
		Попытка
			НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упЗаписанныеДокументы");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Документ",    текВыборка.Документ);
			ЭлементБлокировки.УстановитьЗначение("ТипДействия", текВыборка.ТипДействия);
			
			Блокировка.Заблокировать();
			
			текЗапись = РегистрыСведений.упЗаписанныеДокументы.СоздатьМенеджерЗаписи();
			текЗапись.Документ = текВыборка.Документ;
			текЗапись.ТипДействия = текВыборка.ТипДействия;
			текЗапись.Прочитать();
			
			Если текЗапись.Обработан Тогда 
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			текЗапись.Обработан     = Истина;
			текЗапись.ДатаОбработки = текДата;
			текЗапись.Записать();
			
			ЗафиксироватьТранзакцию();
			
			Если соотДокументов.Получить(текВыборка.ТипДействия) = Неопределено Тогда 
				мсвЗадач = Новый Массив;
				соотДокументов.Вставить(текВыборка.ТипДействия, мсвЗадач);
			КонецЕсли;
			
			Если текВыборка.ТипДействия = Перечисления.упТипыДействийСобытий.ПрисоединениеФайла Тогда 
				соотДокументов.Получить(текВыборка.ТипДействия).Добавить(текВыборка.ПрисоединенныеФайлы);
			Иначе
				соотДокументов.Получить(текВыборка.ТипДействия).Добавить(текВыборка.Документ);
			КонецЕсли;
			
			соотДат.Вставить(текВыборка.Документ, текВыборка.ДатаРегистрации);
		Исключение
			Если ТранзакцияАктивна() Тогда 
				ОтменитьТранзакцию();
			КонецЕсли;
			// ничего страшного - обработаем в следующий раз.
		КонецПопытки;	
	КонецЦикла;
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = 
	"ВЫБРАТЬ
	|	тбТипыСкладскихСобытий.Ссылка,
	|	тбТипыСкладскихСобытий.АлгоритмПроверкиНаСобытие,
	|	тбТипыСкладскихСобытий.СхемаКомпоновкиДанных,
	|	тбТипыСкладскихСобытий.ПроизвольныйАлгоритмПроверки,
	|	тбТипыСкладскихСобытий.Задержка,
	|	тбТипыСкладскихСобытий.ВремяОбработкиСобытия,
	|	тбТипыСкладскихСобытий.ВремяСозданияЗадачи
	|ИЗ
	|	Справочник.упТипыСобытий КАК тбТипыСкладскихСобытий
	|ГДЕ
	|	тбТипыСкладскихСобытий.ТипИсходногоДействия = &ТипИсходногоДействия
	|	И тбТипыСкладскихСобытий.Активно
	|	И НЕ тбТипыСкладскихСобытий.ПометкаУдаления";
	
	Для Каждого текТипДействия Из соотДокументов Цикл
		текЗапрос.УстановитьПараметр("ТипИсходногоДействия", текТипДействия.Ключ);
		
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Пока текВыборка.Следующий() Цикл 
			Если текВыборка.ВремяОбработкиСобытия = Перечисления.упВремяОбработкиСобытия.СоздатьСЗадержкой Тогда 
				ДатаГенерации     = текДата + (60 * текВыборка.Задержка);
			ИначеЕсли текВыборка.ВремяОбработкиСобытия = Перечисления.упВремяОбработкиСобытия.ВОпределенноеВремя Тогда 
				СекундСНачалаДня = текВыборка.ВремяСозданияЗадачи - '00010101000000';
				Если текДата - НачалоДня(текДата) > СекундСНачалаДня Тогда 
					ДатаГенерации = НачалоДня(текДата) + 86400 + СекундСНачалаДня;
				Иначе
					ДатаГенерации = НачалоДня(текДата) + СекундСНачалаДня;
				КонецЕсли;
			Иначе
				ДатаГенерации = текДата;
			КонецЕсли;
			
			Попытка
				Если текВыборка.АлгоритмПроверкиНаСобытие = Перечисления.упАлгоритмыПроверкиСобытия.НастройкаОтбора Тогда 
					СхемаКомпоновкиДанных = текВыборка.СхемаКомпоновкиДанных.Получить();
					
					СписокЗадач = Новый СписокЗначений;
					СписокЗадач.ЗагрузитьЗначения(текТипДействия.Значение);
					ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Ссылка", СписокЗадач, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
					
					// Таблица значений, в которую будет получен результат.
					
					ВозможностьИспользованияВнешнихФункций = Ложь;
					
					Если текТипДействия.Ключ = Перечисления.упТипыДействийСобытий.ЗаписьСобытияМониторинга Тогда
						ВозможностьИспользованияВнешнихФункций = Истина;
					КонецЕсли;
					
					Результат = упСервисныеФункции.ВыполнитьСКДвТЗ(СхемаКомпоновкиДанных,ВозможностьИспользованияВнешнихФункций);
					
					Если Результат.Количество() Тогда 
						Для Каждого текЗадача Из Результат Цикл
							текЗаписьСобытия = РегистрыСведений.упПроизошедшиеСобытия.СоздатьМенеджерЗаписи();
							
							Если текТипДействия.Ключ = Перечисления.упТипыДействийСобытий.ПрисоединениеФайла Тогда 
								текЗаписьСобытия.ДокументОснование = текЗадача.Ссылка.ВладелецФайла;
								текЗаписьСобытия.ДатаЗаписи = соотДат[текЗаписьСобытия.ДокументОснование];
							Иначе
								текЗаписьСобытия.ДокументОснование = текЗадача.Ссылка;
								текЗаписьСобытия.ДатаЗаписи = соотДат[текЗадача.Ссылка];
							КонецЕсли;
							
							текЗаписьСобытия.ТипСобытия        = текВыборка.Ссылка;
							текЗаписьСобытия.СтатусОтработки   = Перечисления.упСтатусыОтработкиСобытий.Открыто;
							текЗаписьСобытия.ДатаРегистрации   = текДата;
							текЗаписьСобытия.ДатаГенерации     = ДатаГенерации;
							текЗаписьСобытия.Записать();
						КонецЦикла;
					КонецЕсли;
				Иначе
					Для Каждого ИсходнаяЗадача Из соотДокументов[текТипДействия.Ключ] Цикл 
						Результат = Ложь;
						
						ПроверкаПроизвольногоАлгоритма(текВыборка.ПроизвольныйАлгоритмПроверки, ИсходнаяЗадача, Результат);
																		
						Если Результат Тогда 
							текЗаписьСобытия = РегистрыСведений.упПроизошедшиеСобытия.СоздатьМенеджерЗаписи();
							Если текТипДействия.Ключ = Перечисления.упТипыДействийСобытий.ПрисоединениеФайла Тогда 
								текЗаписьСобытия.ДокументОснование = ИсходнаяЗадача.ВладелецФайла;
								текЗаписьСобытия.ДатаЗаписи        = соотДат[текЗаписьСобытия.ДокументОснование];
							Иначе
								текЗаписьСобытия.ДокументОснование = ИсходнаяЗадача;
								текЗаписьСобытия.ДатаЗаписи        = соотДат[ИсходнаяЗадача];
							КонецЕсли;
							
							
							текЗаписьСобытия.ТипСобытия        = текВыборка.Ссылка;
							текЗаписьСобытия.СтатусОтработки   = Перечисления.упСтатусыОтработкиСобытий.Открыто;
							текЗаписьСобытия.ДатаРегистрации   = текДата;
							текЗаписьСобытия.ДатаГенерации     = ДатаГенерации;
							текЗаписьСобытия.Записать();
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("Обработка поиска события", УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упТипыСобытий, текВыборка.Ссылка, ТекстОшибки);
			КонецПопытки;
			
			Если соотДокументов[текТипДействия.Ключ].Количество() = 0 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура проверяет произвольный алгоритм отбора событий.
//
// Параметры:
//	ПроизвольныйАлгоритмПроверки - Строка - текст алгоритма.
//	ИсходнаяЗадача - Задача - ОбъектМетаданных.
//	Результат - Произвольный - Результат выполнения.
//
Процедура ПроверкаПроизвольногоАлгоритма(ПроизвольныйАлгоритмПроверки, ИсходнаяЗадача, Результат) Экспорт
	
	УстановитьБезопасныйРежим("TMS3");
	
	Выполнить(ПроизвольныйАлгоритмПроверки);
	
	УстановитьБезопасныйРежим(Ложь);
	
КонецПроцедуры

// Процедура обрабатывает складские события (в несколько потоков).
//
Процедура ОбработкаСобытий() Экспорт
	
	КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковОбработкиСобытий");
	Если Не КоличествоПотоков Тогда 
		КоличествоПотоков = 1;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тбПроизошедшиеСобытия.ДокументОснование,
	|	тбПроизошедшиеСобытия.ДатаЗаписи,
	|	тбПроизошедшиеСобытия.ТипСобытия,
	|	тбПроизошедшиеСобытия.ТипСобытия.АлгоритмОбработки КАК АлгоритмОбработки
	|ИЗ
	|	РегистрСведений.упПроизошедшиеСобытия КАК тбПроизошедшиеСобытия
	|ГДЕ
	|	тбПроизошедшиеСобытия.СтатусОтработки = ЗНАЧЕНИЕ(Перечисление.упСтатусыОтработкиСобытий.Открыто)
	|	И тбПроизошедшиеСобытия.ДатаГенерации <= &текДата
	|ИТОГИ ПО
	|	АлгоритмОбработки";
	Запрос.УстановитьПараметр("текДата", ТекущаяДатаСеанса());
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
	    Возврат;	
	КонецЕсли; 
	
	мсвПотоков = Новый Массив(КоличествоПотоков);
	Для Сч = 0 По КоличествоПотоков - 1 Цикл 
		тбзСобытия = Новый ТаблицаЗначений;
		тбзСобытия.Колонки.Добавить("Документ");
		тбзСобытия.Колонки.Добавить("ДатаЗаписи");
		тбзСобытия.Колонки.Добавить("ТипСобытия");
		
		мсвПотоков[Сч] = тбзСобытия;
	КонецЦикла;
	
	Сеанс   = НомерСеансаИнформационнойБазы();
	текДата = ТекущаяДатаСеанса();
	
	ВыборкаАлгоритмовОбработки = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Сч = 0;
	
	// Типы событий с одинаковыми алгоритмами обработки должны обрабатываться в одном потоке.
	Пока ВыборкаАлгоритмовОбработки.Следующий() Цикл 
		Выборка = ВыборкаАлгоритмовОбработки.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Попытка
				НачатьТранзакцию();
				
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упПроизошедшиеСобытия");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("ДокументОснование", Выборка.ДокументОснование);
				ЭлементБлокировки.УстановитьЗначение("ДатаЗаписи",        Выборка.ДатаЗаписи);
				ЭлементБлокировки.УстановитьЗначение("ТипСобытия",        Выборка.ТипСобытия);
				
				Блокировка.Заблокировать();
				
				текЗапись = РегистрыСведений.упПроизошедшиеСобытия.СоздатьМенеджерЗаписи();
				текЗапись.ДокументОснование = Выборка.ДокументОснование;
				текЗапись.ДатаЗаписи        = Выборка.ДатаЗаписи;
				текЗапись.ТипСобытия        = Выборка.ТипСобытия;
				текЗапись.Прочитать();
				
				Если текЗапись.СтатусОтработки <> Перечисления.упСтатусыОтработкиСобытий.Открыто Тогда 
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;
				
				текЗапись.ДатаНачалаОбработки = текДата;
				текЗапись.НомерСеанса         = Сеанс;
				текЗапись.СтатусОтработки     = Перечисления.упСтатусыОтработкиСобытий.Обрабатывается;
				текЗапись.Записать();
				
				ЗафиксироватьТранзакцию();
				
				НовоеСобытие = мсвПотоков[Сч].Добавить();
				НовоеСобытие.Документ   = текЗапись.ДокументОснование;
				НовоеСобытие.ДатаЗаписи = текЗапись.ДатаЗаписи;
				НовоеСобытие.ТипСобытия = текЗапись.ТипСобытия;
				
			Исключение
				Если ТранзакцияАктивна() Тогда 
					ОтменитьТранзакцию();
				КонецЕсли;
				// ничего страшного - обработаем в следующий раз.
			КонецПопытки;	
		КонецЦикла;
		
		Сч = Сч + 1;
		Если Сч = КоличествоПотоков Тогда 
			Сч = 0;
		КонецЕсли;
	КонецЦикла;	
	
	Для Каждого текЭлемент Из мсвПотоков Цикл
		Если текЭлемент.Количество() Тогда 
			мсвПараметров = Новый Массив(1);
			мсвПараметров[0] = текЭлемент;
			СоздатьПотокОбработкиСобытий(текЭлемент);
			//упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упСобытия.СоздатьПотокОбработкиСобытий", мсвПараметров);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Поток обработки складских событий.
//
// Параметры:
//	тбзСобытия - ТаблицаЗначений - Табличные данные.
//
Процедура СоздатьПотокОбработкиСобытий(тбзСобытия) Экспорт
	
	Сеанс = НомерСеансаИнформационнойБазы();
	Дата  = ТекущаяДатаСеанса();
	Для Каждого текСобытие Из тбзСобытия Цикл 
		текЗапись = РегистрыСведений.упПроизошедшиеСобытия.СоздатьМенеджерЗаписи();
		текЗапись.ДокументОснование   = текСобытие.Документ;
		текЗапись.ДатаЗаписи          = текСобытие.ДатаЗаписи;
		текЗапись.ТипСобытия          = текСобытие.ТипСобытия;
		текЗапись.Прочитать();
		текЗапись.НомерСеанса         = Сеанс;
		текЗапись.ДатаНачалаОбработки = Дата;
		текЗапись.Записать();
	КонецЦикла;
	
	Для Каждого текСобытие Из тбзСобытия Цикл 
		ОбработатьСобытие(текСобытие.Документ, текСобытие.ТипСобытия, текСобытие.ДатаЗаписи);
	КонецЦикла;
	
КонецПроцедуры

// Процедура обрабатывает складское событие.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	ДатаЗаписи - Дата - Период.
//
Процедура ОбработатьСобытие(ДокументОснование, ТипСобытия, ДатаЗаписи) Экспорт

	сткДанныеТипаСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипСобытия, "ТипИсходногоДействия, АлгоритмОбработки, КоличествоПопыток, ВремяОбработкиСобытия, Задержка, ВремяСозданияЗадачи, СрокНапоминания, НастройкаОптимизацииМаршрута");
	
	ДокументРезультат = Неопределено;
	Успешно = Истина;
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", ДокументРезультат, Успешно, "");
	
	Попытка
		Если сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПроизвольныйАлгоритм Тогда 
			ПроизвольныйАлгоритмОбработки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипСобытия, "ПроизвольныйАлгоритмОбработки");
			
			ОбработатьСобытие_ПроизвольноеСобытие(ПроизвольныйАлгоритмОбработки, ДокументОснование, ТипСобытия, сткДанныеТипаСобытия, ДокументРезультат, Успешно);
			сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", ДокументРезультат, Успешно,"");
			
		ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя") И 
				сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.НапоминаниеПользователю Тогда
			
			сткВозврат = ОбработатьСобытие_НапоминаниеПользователю(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);

		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОповеститьПользователя Тогда
			
			сткВозврат = ОбработатьСобытие_ОповеститьПользователя(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОтправитьSMS Тогда
			
			сткВозврат = Обработать_ОтправитьSMS(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОтправитьЗаявкуНаТСПеревозчику Тогда
			
			сткВозврат = ОбработатьСобытие_ОтправитьЗаявкуНаТСПеревозчику(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПерейтиКСледующейИтерацииПодбораПеревозчика Тогда
			
			сткВозврат = ОбработатьСобытие_ПерейтиКСледующейИтерацииПодбораПеревозчика(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПерейтиКСледующемуЭтапуПодбораПеревозчика Тогда
			
			сткВозврат = ОбработатьСобытие_ПерейтиКСледующемуЭтапуПодбораПеревозчика(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗаданиеНаПеревозкуГруза Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗаданиеНаПеревозкуГруза(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьРейс Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьРейс(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПередатьЗаявкуКВыполнению Тогда
			
			сткВозврат = ОбработатьСобытие_ПередатьЗаявкуКВыполнению(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СогласоватьЗаявку Тогда
			
			сткВозврат = ОбработатьСобытие_СогласоватьЗаявку(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПередатьЗаданиеКПланированию Тогда
			
			сткВозврат = ОбработатьСобытие_ПередатьЗаданиеКПланированию(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
		
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ВключитьЗаданиеВПериодическийРейс Тогда
			
			сткВозврат = ОбработатьСобытие_ВключитьЗаданиеВПериодическийРейс(ДокументОснование);		
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СформироватьЗаявкиНаТС Тогда
			
			сткВозврат = ОбработатьСобытие_СформироватьЗаявкиНаТС(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		// ОбновитьУсловияВЗаявкахНаТС - алгоритм не существует
		//ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОбновитьУсловияВЗаявкахНаТС Тогда
		//	
		//	сткВозврат = ОбработатьСобытие_ОбновитьУсловияВЗаявкахНаТС(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);		
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПередатьРейсКВыполнению Тогда
			
			сткВозврат = ОбработатьСобытие_ПередатьРейсКВыполнению(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ЗавершитьРейс Тогда
			
			сткВозврат = ОбработатьСобытие_ЗавершитьРейс(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОтменитьРейс Тогда
			
			сткВозврат = ОбработатьСобытие_ОтменитьРейс(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьПодтверждениеЗаявкиНаТС Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьПодтверждениеЗаявкиНаТС(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);		
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ЗадачаПользователю Тогда
			
			сткВозврат = ОбработатьСобытие_ЗадачуПользователю(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.РасчетПлановыхДоходов Тогда
			
			сткВозврат = ОбработатьСобытие_РасчетПлановыхДоходов(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.РасчетПлановыхРасходов Тогда
			
			сткВозврат = ОбработатьСобытие_РасчетПлановыхРасходов(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СкорректироватьНачальныеОстаткиВСледующемПутевомЛисте Тогда
			
			сткВозврат = ОбработатьСобытие_СкорректироватьНачальныеОстаткиВСледующемПутевомЛисте(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьСобытиеМониторингаОпозданиеВТочкеМаршрута Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьСобытиеМониторингаОпозданиеВТочкеМаршрута(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьСобытиеМониторингаПланируемоеОпозданиеВТочкеМаршрута Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьСобытиеМониторингаПланируемоеОпозданиеВТочкеМаршрута(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОтразитьЗаправкуСливВПутевомЛистеПоДаннымДатчиков Тогда
			
			сткВозврат = ОбработатьСобытие_ОтразитьЗаправкуСливВПутевомЛистеПоДаннымДатчиков(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗаявкуНаВозвратГруза Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗаявкуНаВозвратГруза(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗаявкуНаВозвратТары Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗаявкуНаВозвратТары(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПередатьСледующийВМультимодальнойПеревозкеРейсКВыполнению Тогда
			
			сткВозврат = ОбработатьСобытие_ПередатьСледующийВМультимодальнойПеревозкеРейсКВыполнению(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОптимизироватьМаршрут Тогда
			
			сткВозврат = ОбработатьСобытие_ОптимизироватьМаршрут(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗаявкуНаПеревозкуГруза Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗаявкуНаПеревозкуГруза(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОбработатьЗапросУсловийПеревозки Тогда
			
			сткВозврат = ОбработатьСобытие_ПередатьЗапросУсловийПеревозкиВОбработан(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);		
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗадачуНаПодтверждениеАдреса Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗадачуНаПодтверждениеАдреса(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);			
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗадачуНаСогласованиеИзмененийМаршрута Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗадачуНаСогласованиеИзмененийМаршрута(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);			
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СписатьПланыПоЗаданиямНаПеревозку Тогда
			
			сткВозврат = ОбработатьСобытие_СписатьПланыПоЗаданиямНаПеревозку(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);			
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ЗапроситьЗначенияУровняТопливаНаПериодПутевогоЛиста Тогда
			
			сткВозврат = ОбработатьСобытие_ЗапроситьЗначенияУровняТопливаНаПериодПутевогоЛиста(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ВключитьКонтрольныеТочкиВРейсПоПравилам Тогда
			
			сткВозврат = ОбработатьСобытие_ВключитьКонтрольныеТочкиВРейсПоПравилам(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СкорректироватьПланПеревозки Тогда
			
			сткВозврат = ОбработатьСобытие_СкорректироватьПланПеревозки(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
			
		ИначеЕсли сткДанныеТипаСобытия.АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗаявкуНаПеревозкуКонтейнера Тогда
			
			сткВозврат = ОбработатьСобытие_СоздатьЗаявкуНаПеревозкуКонтейнера(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия);	
				
		КонецЕсли;
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Обработка события", УровеньЖурналаРегистрации.Ошибка, ДокументОснование.Метаданные(), ДокументОснование, ТекстОшибки);
		сткВозврат.Успешно = Ложь;
	КонецПопытки;
	
	текДата = ТекущаяДата();
	Попытка
		текЗапись = РегистрыСведений.упПроизошедшиеСобытия.СоздатьМенеджерЗаписи();
		текЗапись.ДокументОснование = ДокументОснование;
		текЗапись.ДатаЗаписи = ДатаЗаписи;
		текЗапись.ТипСобытия = ТипСобытия;
		текЗапись.Прочитать();
		Если сткВозврат.Успешно Тогда 
			текЗапись.СтатусОтработки   = Перечисления.упСтатусыОтработкиСобытий.Закрыто;
			текЗапись.ДокументРезультат = сткВозврат.ДокументРезультат;
		ИначеЕсли текЗапись.НомерПопытки < сткДанныеТипаСобытия.КоличествоПопыток Тогда
			Если сткДанныеТипаСобытия.ВремяОбработкиСобытия = Перечисления.упВремяОбработкиСобытия.СоздатьСЗадержкой Тогда 
				ДатаГенерации = текДата + (60 * сткДанныеТипаСобытия.Задержка);
			ИначеЕсли сткДанныеТипаСобытия.ВремяОбработкиСобытия = Перечисления.упВремяОбработкиСобытия.ВОпределенноеВремя Тогда 
				СекундСНачалаДня = сткДанныеТипаСобытия.ВремяСозданияЗадачи - '00010101000000';
				Если текДата - НачалоДня(текДата) > СекундСНачалаДня Тогда 
					ДатаГенерации = НачалоДня(текДата) + 86400 + СекундСНачалаДня;
				Иначе
					ДатаГенерации = НачалоДня(текДата) + СекундСНачалаДня;
				КонецЕсли;
			Иначе
				ДатаГенерации = текДата;
			КонецЕсли;
			текЗапись.ДатаГенерации = ДатаГенерации;
			текЗапись.НомерПопытки = текЗапись.НомерПопытки + 1;
			текЗапись.СтатусОтработки = Перечисления.упСтатусыОтработкиСобытий.Открыто;
		Иначе	
			текЗапись.СтатусОтработки = Перечисления.упСтатусыОтработкиСобытий.ОшибкаОбработки;
			текЗапись.ДокументРезультат = Неопределено;
			Если ЗначениеЗаполнено(сткВозврат.Описаниеошибки) Тогда
				ТекстОшибки = НСтр("ru = 'Не удалось обработать событие:""%1"". Ошибка:""%3""'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ТипСобытия, сткДанныеТипаСобытия.АлгоритмОбработки, сткВозврат.ОписаниеОшибки);
				ЗаписьЖурналаРегистрации("упСобытия_ОбработатьСобытие", УровеньЖурналаРегистрации.Ошибка, ДокументОснование.Метаданные(), ДокументОснование, ТекстОшибки);			
			КонецЕсли;
		КонецЕсли;
		текЗапись.ДатаОтработки = ТекущаяДата(); // Берем самую актуальную дату, чтобы потом просто искать интервал журнала регистрации
		текЗапись.Записать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упПроизошедшиеСобытия,, ТекстОшибки);
	КонецПопытки;
КонецПроцедуры

// Вызывается после изменения макетов в справочнике "типы событий",
//  в процедуре обновления информационной базы, для обновления схем в элементах
//
// Параметры:
//  ТипыИсходныхДействий	 - Массив(ПеречислениеСсылка.упТипыДействийСобытий) - массив типов исходных действий;
//  ИмяМакета			 - Строка	 - имя макета.
//
Процедура ПерезаполнитьТипыСобытийПоТипуИсходногоДействия(ТипыИсходныхДействий, ИмяМакета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упТипыСобытий.Ссылка
	|ИЗ
	|	Справочник.упТипыСобытий КАК упТипыСобытий
	|ГДЕ
	|	упТипыСобытий.ТипИсходногоДействия В (&ТипыИсходныхДействий)";
	Запрос.УстановитьПараметр("ТипыИсходныхДействий", ТипыИсходныхДействий);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		
		КоличествоВсего= 0;
		КоличествоУспешно = 0;
		КоличествоОшибка= 0;
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
					
			ТипСобытияОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СхемаКомпоновкиДанных = ТипСобытияОбъект.СхемаКомпоновкиДанных.Получить();
			
			СхемаКомпоновкиДанныхНовая = Справочники.упТипыСобытий.ПолучитьМакет(ИмяМакета);
			Если СхемаКомпоновкиДанных <> Неопределено Тогда 
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию.Отбор, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор);
			КонецЕсли;
			
			ТипСобытияОбъект.СхемаКомпоновкиДанных = Новый ХранилищеЗначения(СхемаКомпоновкиДанныхНовая);
			
			Попытка
				ТипСобытияОбъект.Записать();
				КоличествоУспешно = КоличествоУспешно + 1;
			Исключение
				КоличествоОшибка = КоличествоОшибка + 1;
				ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
					ОписаниеОшибки = ОписаниеОшибки();
				КонецЕсли;
				Событие = Нстр("ru = 'Не удалось записать тип события'");
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,ТипСобытияОбъект.Ссылка, ОписаниеОшибки); 
			КонецПопытки;
			
			КоличествоВсего = КоличествоВсего + 1;
		КонецЦикла;
	КонецЕсли; 
	
	Комментарий = Нстр("ru = 'Количество элементов: всего обработано-%1, успешно-%2, с ошибкой-%3'");
	Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Комментарий, КоличествоВсего, КоличествоУспешно, КоличествоОшибка);
	СобытиеКонец= Нстр("ru = 'ВыполнениеПроцедурыОбновления.Конец'");
	ЗаписьЖурналаРегистрации(СобытиеКонец, УровеньЖурналаРегистрации.Информация,,, Комментарий);
	
КонецПроцедуры

#КонецОбласти

#Область АлгоритмыОбработкиСобытий

// Процедура обработки произвольного события.
//
// Параметры:
//	ПроизвольныйАлгоритмОбработки - Строка - Алгоритм.
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//	ДокументРезультат - ДокументСсылка - Ссылка на элемент.
//	Успешно - Булево - Результат выполнения.
//
Процедура ОбработатьСобытие_ПроизвольноеСобытие(ПроизвольныйАлгоритмОбработки, ДокументОснование, ТипСобытия, сткДанныеТипаСобытия, ДокументРезультат, Успешно)
	
	УстановитьБезопасныйРежим("TMS3");
	
	Выполнить(ПроизвольныйАлгоритмОбработки);
	
	УстановитьБезопасныйРежим(Ложь);
	
КонецПроцедуры

// Функция отправляет почтовые сообщения.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ОповеститьПользователя(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипСобытия, "ШаблонПисьма, Наименование, ВидШаблонаПисьма, ВидПолучателей, Получатели, ШаблонПисьмаСсылка");
	
	сткДанные.Вставить("ТипИсходногоДействия",сткДанныеТипаСобытия.ТипИсходногоДействия);
				
	Возврат ОтправитьПисьмо(ДокументОснование, сткДанные);
	
КонецФункции

// Функция отправляет почтовые сообщения
//
Функция Обработать_ОтправитьSMS(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	СтруктураДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипСобытия, "ТекстSMS, Наименование, ПолучателиSMS");
	СтруктураВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
		
	ТекстSMS = "";
	
	Если ЗначениеЗаполнено(СтруктураДанные.ТекстSMS) Тогда
		УстановитьБезопасныйРежим(Истина);
		Выполнить(СтруктураДанные.ТекстSMS);
		УстановитьБезопасныйРежим(Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Не указан текст SMS'");
	КонецЕсли;
	
	
	Если ТипЗнч(СтруктураДанные.ПолучателиSMS) = Тип("СправочникСсылка.Пользователи") тогда
		Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(СтруктураДанные.ПолучателиSMS,Справочники.ВидыКонтактнойИнформации.ТелефонПользователя);
		Если НЕ ЗначениеЗаполнено(Телефон) Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не указан телефон пользователя %1'"),СтруктураДанные.ПолучателиSMS);
		КонецЕсли;
		МассивПолучатели = Новый Массив;
		МассивПолучатели.Добавить(Телефон);
		
	ИначеЕсли ТипЗнч(СтруктураДанные.ПолучателиSMS) = Тип("СправочникСсылка.ГруппыПользователей") тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Пользователи.Пользователь,
		|	КИ.Представление КАК Телефон
		|ИЗ
		|	Справочник.ГруппыПользователей.Состав КАК Пользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК КИ
		|		ПО Пользователи.Пользователь = КИ.Ссылка
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка
		|	И КИ.Вид = &Вид";
		Запрос.УстановитьПараметр("Ссылка",СтруктураДанные.ПолучателиSMS);
		Запрос.УстановитьПараметр("Вид",Справочники.ВидыКонтактнойИнформации.ТелефонПользователя);
		
		Выборка = Запрос.Выполнить().Выбрать();
		МассивПолучатели = Новый Массив;
		Пока Выборка.Следующий() Цикл 
			МассивПолучатели.Добавить(Выборка.Телефон);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(СтруктураДанные.ПолучателиSMS) = Тип("СправочникСсылка.упСотрудники") тогда
		Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(СтруктураДанные.ПолучателиSMS,Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица);
		Если НЕ ЗначениеЗаполнено(Телефон) Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не указан телефон складского сотрудника %1'"), СтруктураДанные.ПолучателиSMS);
		КонецЕсли;           
		МассивПолучатели = Новый Массив;
		МассивПолучатели.Добавить(Телефон);
	Иначе
		МассивПолучатели = СтрРазделить(СтруктураДанные.ПолучателиSMS,",");
	КонецЕсли;
		
	Если НЕ МассивПолучатели.Количество() Тогда 
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не указаны получатели SMS %1'"),СтруктураДанные.ПолучателиSMS);
	КонецЕсли;
	
	ОтправкаSMS.ОтправитьSMS(МассивПолучатели, ТекстSMS);
				
	Возврат СтруктураВозврат;
	
КонецФункции // Обработать_ОтправитьSMS()

// Функция отправляет почтовые сообщения.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.упЗаявкаНаТС - Ссылка на заявку.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ОтправитьЗаявкуНаТСПеревозчику(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	Статус = Документы.упЗаявкаНаТС.ПолучитьСтатус(ДокументОснование);
	Если Статус = Перечисления.упСтатусыЗаявкиНаТС.Создана Тогда
		текОбъект = ДокументОснование.ПолучитьОбъект();
		текОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаТС.Отправлена);
		текОбъект.Записать(РежимЗаписиДокумента.Проведение);
	ИначеЕсли Не Статус = Перечисления.упСтатусыЗаявкиНаТС.Отправлена Тогда
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Документ ""%1"" находится в статусе ""%2"". Отправка заявки перевозчику отменена.'"), ДокументОснование, Статус);
		Возврат Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Ложь, ОписаниеОшибки);
	КонецЕсли;

	сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипСобытия, "ШаблонПисьма, Наименование, ВидШаблонаПисьма, ШаблонПисьмаСсылка");
	сткДанные.Вставить("ВидПолучателей", Перечисления.упВидыПолучателейСообщений.Перевозчик);
	сткДанные.Вставить("Получатели"    , "");
	
	АдресПубликацииИнформационнойБазыВИнтернете = Константы.АдресПубликацииИнформационнойБазыВИнтернете.Получить();
	ПрефиксПисьма = СтрШаблон(НСтр("ru = 'Добрый день, Вам поступила заявка на транспортное средство.'"), Символы.ПС);
	Если ЗначениеЗаполнено(ПрефиксПисьма) Тогда
		ПрефиксПисьма = ПрефиксПисьма  + Символы.ПС + АдресПубликацииИнформационнойБазыВИнтернете + "#" + ПолучитьНавигационнуюСсылку(ДокументОснование);
	КонецЕсли; 
	сткДанные.Вставить("ПрефиксПисьма", ПрефиксПисьма);
	сткДанные.Вставить("ТипИсходногоДействия",сткДанныеТипаСобытия.ТипИсходногоДействия);
	
	Возврат ОтправитьПисьмо(ДокументОснование, сткДанные);
	
КонецФункции

// Функция производит переход к следующей итерации поэтапного подбора перевозчика. 
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПерейтиКСледующейИтерацииПодбораПеревозчика(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	СтатусЗаявки = Документы.упЗаявкаНаТС.ПолучитьСтатус(ДокументОснование);
	
	Если СтатусЗаявки = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
		сткВозврат.Успешно = Ложь;
		сткВозврат.ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Документ ""%1"" находится в статусе ""%2"". Переход к следующей итерации подбора перевозчика возможен только по необработанным заявкам'", ДокументОснование, СтатусЗаявки));
		
	Иначе
		Рейс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");
		ТекущаяИтерация = Документы.упЭтапПодбораПеревозчика.ПолучитьТекущуюИтерацию(Рейс);
		
		Если ДокументОснование = ТекущаяИтерация Тогда
			Отказ = Ложь;
			ТекстОшибки = "";
			ДокументРезультат = Документы.упЭтапПодбораПеревозчика.ПерейтиКСледующейИтерации(Рейс, ДокументОснование,, ТекстОшибки, Отказ);
			
			Если Отказ Тогда
				сткВозврат.Успешно = Ложь;
				сткВозврат.ОписаниеОшибки = ТекстОшибки;
			ИначеЕсли ЗначениеЗаполнено(ДокументРезультат) Тогда
				сткВозврат.ДокументРезультат = ДокументРезультат;	
			КонецЕсли;
		Иначе	
			сткВозврат.Успешно = Ложь;
			сткВозврат.ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Документ ""%1"" не является текущей итерацией'", ДокументОснование));
			
		КонецЕсли;  
		
	КонецЕсли; 
	
	Возврат сткВозврат;
	
КонецФункции

// Функция производит переход к следующему этапу поэтапного подбора перевозчика. 
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПерейтиКСледующемуЭтапуПодбораПеревозчика(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);

	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "Рейс, Этап");
	
	ТекущийЭтап = Документы.упЭтапПодбораПеревозчика.ПолучитьТекущийЭтап(сткРеквизиты.Рейс);
	
	Если сткРеквизиты.Этап = ТекущийЭтап Тогда
		
		Отказ = Ложь;
		ТекстОшибки = "";
		Документы.упЭтапПодбораПеревозчика.ПерейтиКСледующемуЭтапу(сткРеквизиты.Рейс, ТекущийЭтап,, ТекстОшибки, Отказ);
		
		Если Отказ Тогда
			сткВозврат.Успешно = Ложь;
			сткВозврат.ОписаниеОшибки = ТекстОшибки;
		КонецЕсли;
		
	Иначе
		сткВозврат.Успешно = Ложь;
		сткВозврат.ОписаниеОшибки = НСтр(СтрШаблон("ru = '""%1"" по документу ""%2"" не является текущим'", сткРеквизиты.Этап, ДокументОснование));
	КонецЕсли; 
	
	Возврат сткВозврат;
	
КонецФункции

// По ссылке документа основания получим документ рейс.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипИсходногоДействия - ПеречислениеСсылка.упТипыДействийСобытий - Тип действия.
//
// Возвращаемое значение:
//	ДокументСсылка - ССылка на документ результат проверки.
//
Функция ПолучитьДокументОснование(ДокументОснование,ТипИсходногоДействия)
	
	ТипДокументаОснование = ТипЗнч(ДокументОснование);
	
	ТекущийДокумент = ДокументОснование;
	
	ИмяОбъекта = "";
	
	Если ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ИзменениеМаршрутаПоРейсу")Тогда
		ИмяОбъекта = "упМаршрутПоРейсу";
	ИначеЕсли ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ИзменениеРаботПоРейсу")Тогда 
		ИмяОбъекта = "упРаботыПоРейсу";
	ИначеЕсли ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ИзменениеЭкипажаПоРейсу")
		ИЛИ ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ЗаписьПодтвержденияЗаявкиНаТС") Тогда 
		ИмяОбъекта = "упПеревозчикПоРейсу";
	КонецЕсли;		
	
	ТекстШаблона = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрСведений.Рейс КАК Ссылка
	|ИЗ
	|	РегистрСведений.%1 КАК РегистрСведений
	|ГДЕ
	|	РегистрСведений.Регистратор = &Регистратор";
	
	Если НЕ ПустаяСтрока(ИмяОбъекта) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = СтрШаблон(ТекстШаблона,ИмяОбъекта);
		Запрос.УстановитьПараметр("Регистратор",ДокументОснование);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекущийДокумент = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Если ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаТС")
			ИЛИ ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ИзменениеСтатусаПутевогоЛиста") Тогда 
			
			ИмяОбъекта = ?(ТипИсходногоДействия=Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаТС, "упСтатусыЗаявокНаТС","упСтатусыПутевыхЛистов");
			
			ТекстШаблона = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	РегистрСведений.Документ КАК Ссылка
			|ИЗ
			|	РегистрСведений.%1 КАК РегистрСведений
			|ГДЕ
			|	РегистрСведений.Регистратор = &Регистратор";
			Запрос = Новый Запрос;
			Запрос.Текст = СтрШаблон(ТекстШаблона,ИмяОбъекта);
			Запрос.УстановитьПараметр("Регистратор",ДокументОснование);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ТекущийДокумент = Выборка.Ссылка.Рейс;
			КонецЕсли;
		ИначеЕсли ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ЗаписьПутевогоЛиста")
			ИЛИ  ТипИсходногоДействия = ПредопределенноеЗначение("Перечисление.упТипыДействийСобытий.ЗаписьСобытияМониторинга") Тогда	
			ТекущийДокумент = ДокументОснование.Рейс;
		КонецЕсли;		
		
	КонецЕсли;
	
	Возврат ТекущийДокумент;
	
КонецФункции // ПолучитьДокументОснование()

// Функция отправляет почтовые сообщения.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	сткДанные - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОтправитьПисьмо(ДокументОснование, сткДанные)
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	Тема  = "";
	Текст = "";
	ТипТекста = "";
	ШаблонПисьмаСсылка = Неопределено;
		
	Если сткДанные.ВидШаблонаПисьма = 0 И ЗначениеЗаполнено(сткДанные.ШаблонПисьма) Тогда
		УстановитьБезопасныйРежим(Истина);
		Выполнить(сткДанные.ШаблонПисьма);
		УстановитьБезопасныйРежим(Ложь);
	ИначеЕсли сткДанные.ВидШаблонаПисьма = 1 
		И сткДанные.Свойство("ШаблонПисьмаСсылка", ШаблонПисьмаСсылка) 
		И ЗначениеЗаполнено(ШаблонПисьмаСсылка)
		И ЗначениеЗаполнено(ШаблонПисьмаСсылка.Шаблон) тогда	// по шаблону из справочника упШаблоныПисем.		
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ШаблонПисьмаСсылка.Шаблон.ТекстХранилище.Получить());
		ВнешняяОбработка = ВнешниеОбработки.Создать(ВнешниеОбработки.Подключить(АдресВоВременномХранилище));
		УстановитьБезопасныйРежим(Истина);
		сткДанныеПисьма = ВнешняяОбработка.ПолучитьДанные(ДокументОснование);
		УстановитьБезопасныйРежим(Ложь);
		Тема  = сткДанныеПисьма.Тема;
		ДанныеПисьма = сткДанныеПисьма.ДанныеПисьма;
		Если ТипЗнч(ДанныеПисьма) = Тип("ТабличныйДокумент") тогда
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("html");
			ДанныеПисьма.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.HTML);
			
			ТекстHТМЛ = Новый ТекстовыйДокумент;
			ТекстHТМЛ.Прочитать(ИмяВременногоФайла);
			
			текПрефиксПисьма = "";
			Если НЕ сткДанные.Свойство("ПрефиксПисьма", текПрефиксПисьма)  Тогда
				текПрефиксПисьма = "";	
			КонецЕсли;
			
			Текст = текПрефиксПисьма + ТекстHТМЛ.ПолучитьТекст();
			
			ТипТекста = "HTML";
		ИначеЕсли ТипЗнч(ДанныеПисьма) = Тип("Строка") тогда
			Текст = ДанныеПисьма;
		ИначеЕсли ТипЗнч(ДанныеПисьма) = Тип("ТекстовыйДокумент") тогда
			Текст = ДанныеПисьма.ПолучитьТекст();
		ИначеЕсли ТипЗнч(ДанныеПисьма) = Тип("ФорматированныйДокумент") тогда
			Текст = ДанныеПисьма.ПолучитьHTML();
			ТипТекста = "HTML";
		ИначеЕсли ТипЗнч(ДанныеПисьма) = Тип("ДокументHTML") тогда
			ЗаписьDOM = Новый ЗаписьDOM;
			ЗаписьHTML = Новый ЗаписьHTML;
			ЗаписьHTML.УстановитьСтроку();
			ЗаписьDOM.Записать(ДанныеПисьма, ЗаписьHTML);
			Текст = ЗаписьHTML.Закрыть();
			ТипТекста = "HTML";
		КонецЕсли;
		
		МассивВложений = Новый Соответствие;
		Для Каждого СтрокаВложение из ШаблонПисьмаСсылка.Вложения Цикл
			АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(СтрокаВложение.Шаблон.ТекстХранилище.Получить());
			ВнешняяОбработка = ВнешниеОбработки.Создать(ВнешниеОбработки.Подключить(АдресВоВременномХранилище));
			УстановитьБезопасныйРежим(Истина);
			сткДанныеПисьма = ВнешняяОбработка.ПолучитьДанные(ДокументОснование);
			УстановитьБезопасныйРежим(Ложь);
			
			ДанныеПисьма = сткДанныеПисьма.ДанныеПисьма;
		// Только табличный документ	
			Если ТипЗнч(ДанныеПисьма) = Тип("ТабличныйДокумент") тогда
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла(СтрокаВложение.ТипШаблона);
				ФайлВложения = Новый Файл(ИмяВременногоФайла);				
				
			// Если указано наименование файла в шаблоне, то создаем файл с данным наименованием, иначе с временным
				Если ЗначениеЗаполнено(СтрокаВложение.Описание) Тогда
					НовоеИмяФайла 		= СтрокаВложение.Описание;
				Иначе
					Разделитель 	= "\";
					Строки     		= СтрЗаменить(ИмяВременногоФайла, Разделитель, Символы.ПС);
					НовоеИмяФайла	= СтрЗаменить(СтрПолучитьСтроку(Строки, СтрЧислоСтрок(Строки)), "." + СтрокаВложение.ТипШаблона, "");
				КонецЕсли;
				
				Если СтрокаВложение.ТипШаблона = "DOCX" Тогда
					ТипФайла = ТипФайлаТабличногоДокумента.DOCX;
				ИначеЕсли СтрокаВложение.ТипШаблона = "PDF" Тогда
					ТипФайла = ТипФайлаТабличногоДокумента.PDF;
				ИначеЕсли СтрокаВложение.ТипШаблона = "XLS" Тогда
					ТипФайла = ТипФайлаТабличногоДокумента.XLS;
				ИначеЕсли СтрокаВложение.ТипШаблона = "XLSX" Тогда
					ТипФайла = ТипФайлаТабличногоДокумента.XLSX;
				КонецЕсли;
				
				ДанныеПисьма.Записать(ИмяВременногоФайла, ТипФайла);
				Для нСчетчик = 1 По СтрокаВложение.КоличествоЭкземпляров Цикл
					КлючЗначения = НовоеИмяФайла + ?(нСчетчик > 1, " (" + Строка(нСчетчик) + ")", "") + "." + СтрокаВложение.ТипШаблона;
					МассивВложений.Вставить(КлючЗначения, ФайлВложения);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если Тема = "" Тогда 
		Тема = сткДанные.Наименование + " " + Строка(ДокументОснование);
	КонецЕсли;
	
	Получатели = "";
	Если сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Автор тогда	
		
		ТекущийДокумент = ПолучитьДокументОснование(ДокументОснование,сткДанные.ТипИсходногоДействия);
		
		Получатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийДокумент, "Автор");
		Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Водитель Тогда
		
		Получатели = Новый Массив;
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = 
		"ВЫБРАТЬ
		|	рсВодитель1.Водитель1 КАК Водитель
		|ПОМЕСТИТЬ втВодители
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК рсВодитель1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	рсВодитель2.Водитель2
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК рсВодитель2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упСотрудникиКонтактнаяИнформация.Представление КАК Адрес,
		|	ПРЕДСТАВЛЕНИЕ(упСотрудникиКонтактнаяИнформация.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.упСотрудники.КонтактнаяИнформация КАК упСотрудникиКонтактнаяИнформация
		|ГДЕ
		|	упСотрудникиКонтактнаяИнформация.Ссылка В
		|			(ВЫБРАТЬ
		|				втВодители.Водитель
		|			ИЗ
		|				втВодители КАК втВодители)
		|	И упСотрудникиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)";
		
		ТекущийДокумент = ПолучитьДокументОснование(ДокументОснование,сткДанные.ТипИсходногоДействия);
		
		текЗапрос.УстановитьПараметр("Рейс", ТекущийДокумент);
		 
		РезультатЗапроса = текЗапрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Адрес = ""; Получатель = сткДанные.Получатели;
		Иначе	
			текВыборка = РезультатЗапроса.Выбрать();			
			Пока текВыборка.Следующий() Цикл
				МассивАдресов = СтрРазделить(текВыборка.Адрес,";");
				Для каждого ТекщуийАдрес Из МассивАдресов Цикл
					
					Структура = Новый Структура;
					Структура.Вставить("Адрес",ТекщуийАдрес);
					Структура.Вставить("Представление",текВыборка.Представление);
					Получатели.Добавить(Структура);
					Адрес = текВыборка.Адрес;
					
				КонецЦикла;
			КонецЦикла;			
		КонецЕсли;
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Заказчик
		ИЛИ сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Отправитель
		ИЛИ сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Получатель Тогда	
		
		ТекущийДокумент = ПолучитьДокументОснование(ДокументОснование,сткДанные.ТипИсходногоДействия);
		
		ИмяРеквизита = ?(сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Заказчик, "Заказчик",
			?(сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Отправитель, "Отправитель","Получатель"));
		
		Если ТипЗнч(ТекущийДокумент) = ТИП("ДокументСсылка.упРейс")Тогда
			ТекстШаблона = "ВЫБРАТЬ
			|	РегистрСведений.Рейс,
			|	РегистрСведений.Задание.%1 КАК Партнер,
			|	ПРЕДСТАВЛЕНИЕ(РегистрСведений.Задание.%1) КАК Представление
			|ИЗ
			|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК РегистрСведений
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрСведений.Задание.%1,
			|	РегистрСведений.Рейс";
			
			Запрос = Новый Запрос;
			Запрос.Текст = СтрШаблон(ТекстШаблона,ИмяРеквизита);			
			Запрос.УстановитьПараметр("Рейс", ТекущийДокумент);
			Выборка = Запрос.Выполнить().Выбрать();
			Получатели = Новый Массив;
			Пока Выборка.Следующий() Цикл
				Если ЗначениеЗаполнено(Выборка.Партнер) Тогда
					Получатель = Выборка.Партнер;
					Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailПартнера);
					Если ПустаяСтрока(Адрес) Тогда
						Продолжить;
					КонецЕсли;
					Структура = Новый Структура;
					Структура.Вставить("Адрес",Адрес);
					Структура.Вставить("Представление",Выборка.Представление);
					Получатели.Добавить(Структура);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Получатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийДокумент, ИмяРеквизита);
			Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailПартнера);
		КонецЕсли;
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.КонтактноеЛицоЗаказчика
		ИЛИ сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.КонтактноеЛицоОтправителя
		ИЛИ сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.КонтактноеЛицоПолучателя тогда	
		
		ИмяРеквизита = ?(сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.КонтактноеЛицоЗаказчика, "КонтактноеЛицоЗаказчика",
		?(сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.КонтактноеЛицоОтправителя, "КонтактноеЛицоОтправителя", "КонтактноеЛицоПолучателя"));
		
		Если ТипЗнч(ТекущийДокумент) = ТИП("ДокументСсылка.упРейс")Тогда
			ТекстШаблона = "ВЫБРАТЬ
			|	РегистрСведений.Рейс,
			|	РегистрСведений.Задание.%1 КАК КонтактноеЛицо,
			|	ПРЕДСТАВЛЕНИЕ(РегистрСведений.Задание.%1) КАК Представление
			|ИЗ
			|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК РегистрСведений
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрСведений.Задание.%1,
			|	РегистрСведений.Рейс";
			
			Запрос = Новый Запрос;
			Запрос.Текст = СтрШаблон(ТекстШаблона,ИмяРеквизита);			
			Запрос.УстановитьПараметр("Рейс", ТекущийДокумент);
			Выборка = Запрос.Выполнить().Выбрать();
			Получатели = Новый Массив;
			Пока Выборка.Следующий() Цикл
				Если ЗначениеЗаполнено(Выборка.КонтактноеЛицо) Тогда
					Получатель = Выборка.КонтактноеЛицо;
					Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица);
					Если ПустаяСтрока(Адрес) Тогда
						Продолжить;
					КонецЕсли;
					Структура = Новый Структура;
					Структура.Вставить("Адрес",Адрес);
					Структура.Вставить("Представление",Выборка.Представление);
					Получатели.Добавить(Структура);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Получатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийДокумент, ИмяРеквизита);
			Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица);
		КонецЕсли;
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Организация Тогда	
		
		ТекущийДокумент = ПолучитьДокументОснование(ДокументОснование,сткДанные.ТипИсходногоДействия);
		
		Получатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийДокумент, "Организация");
		Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель, Справочники.ВидыКонтактнойИнформации.EmailОрганизации);
		
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Экспедитор
		ИЛИ сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Перевозчик Тогда	
		
		Получатели = Новый Массив;
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = 
		"ВЫБРАТЬ
		|	ПеревозчикПоРейсу.Перевозчик КАК Перевозчик,
		|	ПеревозчикПоРейсу.Экспедитор КАК Экспедитор
		|ПОМЕСТИТЬ вт_Рейс
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК ПеревозчикПоРейсу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упСотрудникиКонтактнаяИнформация.Представление КАК Адрес,
		|	ПРЕДСТАВЛЕНИЕ(упСотрудникиКонтактнаяИнформация.Ссылка) КАК Представление,
		|	упСотрудникиКонтактнаяИнформация.Ссылка КАК Сотрудник
		|ПОМЕСТИТЬ вт_Сотрудники
		|ИЗ
		|	Справочник.упСотрудники.КонтактнаяИнформация КАК упСотрудникиКонтактнаяИнформация
		|ГДЕ
		|	упСотрудникиКонтактнаяИнформация.Ссылка В
		|			(ВЫБРАТЬ
		|				вт_Рейс.Экспедитор
		|			ИЗ
		|				вт_Рейс КАК вт_Рейс)
		|	И упСотрудникиКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailСотрудника)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Рейс.Перевозчик,
		|	вт_Рейс.Экспедитор,
		|	ЕСТЬNULL(вт_Сотрудники.Адрес, """") КАК Адрес,
		|	ЕСТЬNULL(вт_Сотрудники.Представление, """") КАК Представление
		|ИЗ
		|	вт_Рейс КАК вт_Рейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Сотрудники КАК вт_Сотрудники
		|		ПО вт_Рейс.Экспедитор = вт_Сотрудники.Сотрудник";
		
		Если НЕ сткДанные.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаТС Тогда
			ТекущийДокумент = ПолучитьДокументОснование(ДокументОснование,сткДанные.ТипИсходногоДействия);
			текЗапрос.УстановитьПараметр("Рейс", ТекущийДокумент);
		Иначе
			текЗапрос = Новый Запрос;
			текЗапрос.Текст = 
			"ВЫБРАТЬ
			|	упЗаявкаНаТС.Перевозчик КАК Перевозчик,
			|	"""" КАК Адрес,
			|	"""" КАК Представление,
			|	ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка) КАК Экспедитор
			|ИЗ
			|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
			|ГДЕ
			|	упЗаявкаНаТС.Ссылка = &Ссылка";
			текЗапрос.УстановитьПараметр("Ссылка", ДокументОснование);
		КонецЕсли;
		
		Выборка = текЗапрос.Выполнить().Выбрать();
		Адрес = ""; Получатель = сткДанные.Получатели;
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Экспедитор) И ЗначениеЗаполнено(Выборка.Адрес) Тогда
				Структура = Новый Структура;
				Структура.Вставить("Адрес",Выборка.Адрес);
				Структура.Вставить("Представление",Выборка.Представление);
				Получатели.Добавить(Структура);
				Адрес = Структура.Адрес;
			КонецЕсли;
			Если ЗначениеЗаполнено(Выборка.Перевозчик)Тогда
				АдресП = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Выборка.Перевозчик, Справочники.ВидыКонтактнойИнформации.EmailПартнера);
				Структура = Новый Структура;
				Структура.Вставить("Адрес",АдресП);
				Структура.Вставить("Представление",Строка(Выборка.Перевозчик));
				Получатели.Добавить(Структура);
				Адрес = Структура.Адрес;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Пользователь тогда
		
		Получатель = Строка(сткДанные.Получатели);
		Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(сткДанные.Получатели, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		
	ИначеЕсли сткДанные.ВидПолучателей = Перечисления.упВидыПолучателейСообщений.ГруппаПользователей тогда
		текЗапрос = Новый запрос;
		текЗапрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыДоступаПользователи.Пользователь,
		|	ПользователиКонтактнаяИнформация.Представление КАК Адрес,
		|	ГруппыДоступаПользователи.Пользователь.Представление КАК Представление,
		|	2 КАК Рейтинг
		|ИЗ
		|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
		|		ПО ГруппыДоступаПользователи.Пользователь = ПользователиКонтактнаяИнформация.Ссылка
		|ГДЕ
		|	ГруппыДоступаПользователи.Ссылка = &Ссылка
		|	И ПользователиКонтактнаяИнформация.Вид = &Вид
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыПользователей.Пользователь,
		|	ПользователиКонтактнаяИнформация.Представление,
		|	ГруппыПользователей.Пользователь.Представление,
		|	1
		|ИЗ
		|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
		|		ПО ГруппыПользователей.Пользователь = ПользователиКонтактнаяИнформация.Ссылка
		|ГДЕ
		|	ГруппыПользователей.Ссылка = &Ссылка
		|	И ПользователиКонтактнаяИнформация.Вид = &Вид
		|
		|УПОРЯДОЧИТЬ ПО
		|	Рейтинг";
		текЗапрос.УстановитьПараметр("Ссылка", сткДанные.Получатели);
		текЗапрос.УстановитьПараметр("Вид",    Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Получатели = Новый Массив;
		Пока текВыборка.Следующий() Цикл 
			Получатели.Добавить(Новый Структура("Адрес, Представление", текВыборка.Адрес, текВыборка.Представление));
			Адрес = текВыборка.Адрес; Получатель = текВыборка.Представление;
		КонецЦикла;
		
		Если Не Получатели.Количество() Тогда 
			Адрес = ""; Получатель = сткДанные.Получатели;
		КонецЕсли;
	Иначе
		Адрес = сткДанные.Получатели;
		Получатель = сткДанные.Получатели;
		Получатели = сткДанные.Получатели;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не указан e-mail получателя письма %1'"), сткДанные.Получатели);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если Получатели = "" Тогда
		Получатели = Новый Массив;
		Получатели.Добавить(Новый Структура("Адрес, Представление", Адрес, Строка(Получатель)));
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты, 
			Новый Структура("Кому, Тело, Тема, ТипТекста, Вложения", Получатели, Текст, Тема, ТипТекста, МассивВложений));
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

// Функция создает напоминание пользователю.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_НапоминаниеПользователю(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	сткНапоминания = Новый Структура("Пользователь, ВремяСобытия, Источник, СрокНапоминания, Описание, СпособУстановкиВремениНапоминания, ИнтервалВремениНапоминания, ИмяРеквизитаИсточника, Расписание");
	
	сткДанныеСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипСобытия, "Наименование, Пользователь, СрокНапоминания");
	                                             
	Текст = сткДанныеСобытия.Наименование + " " + Строка(ДокументОснование);
	
	сткНапоминания.ВремяСобытия          = ТекущаяДата();
	сткНапоминания.Источник              = ДокументОснование;
	сткНапоминания.СрокНапоминания       = сткНапоминания.ВремяСобытия + сткДанныеСобытия.СрокНапоминания * 60;
	сткНапоминания.Описание              = Текст;
	сткНапоминания.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ВУказанноеВремя;
	сткНапоминания.ИмяРеквизитаИсточника = Строка(ДокументОснование);
	
	
	Если ТипЗнч(сткНапоминания.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей") тогда
		текЗапрос = Новый запрос;
		текЗапрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыДоступаПользователи.Пользователь
		|ИЗ
		|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		|ГДЕ
		|	ГруппыДоступаПользователи.Ссылка = &Ссылка";
		текЗапрос.УстановитьПараметр("Ссылка", сткДанныеСобытия.Пользователь);
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Пока текВыборка.Следующий() Цикл 
			сткНапоминания.Пользователь = текВыборка.Ссылка;
			НапоминанияПользователяСлужебный.ПодключитьНапоминание(сткНапоминания);
		КонецЦикла;
	Иначе
		сткНапоминания.Пользователь = сткДанныеСобытия.Пользователь;
		НапоминанияПользователяСлужебный.ПодключитьНапоминание(сткНапоминания);
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

// Функция создает задание на перевозку груза.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗаданиеНаПеревозкуГруза(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);

	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	Отказ = Ложь;
	МассивДокументов = Документы.упЗаявкаНаПеревозкуГруза.СформироватьЗаданияНаПервозкуГруза(ДокументОснование, , Истина, ,Отказ);
	
	Если НЕ Отказ Тогда
		Если ТипЗнч(МассивДокументов) = ТИП("Массив") Тогда
			Если МассивДокументов.Количество() Тогда
				сткВозврат.Вставить("ДокументРезультат", МассивДокументов[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
			
	Возврат сткВозврат;
	
КонецФункции

// Функция создает рейс на основании задания на перевозку.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьРейс(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	мсвЗаданияНаПеревозку = Новый Массив();
	мсвЗаданияНаПеревозку.Добавить(ДокументОснование);
	
	Отказ = Ложь;
	ТекстОшибки = "";
	мсвРейсы = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(мсвЗаданияНаПеревозку, Неопределено, Неопределено, Неопределено, ТекстОшибки, Отказ);
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция переводит заявку на перевозку груза в статус "к выполнению".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПередатьЗаявкуКВыполнению(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);	
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументОснование);

	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
		сткВозврат.ОписаниеОшибки = ОписаниеОшибки;
	КонецПопытки;
	
	Если Не Отказ Тогда
		текСтатус	= Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатус(ДокументОснование);
		текЗаявкаНаПеревозкуГруза = ДокументОснование.ПолучитьОбъект();
		
		ВидПеревозки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование,"ВидПеревозки");
		Если ЗначениеЗаполнено(ВидПеревозки) Тогда
			ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза");
			сткПроверка = упЗаданиеНаПеревозку.РазрешеноПеревестиКВыполнению(текСтатус, ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза);
			Если сткПроверка.Результат Тогда
				Попытка
					текЗаявкаНаПеревозкуГруза.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
					текЗаявкаНаПеревозкуГруза.Записать(РежимЗаписиДокумента.Проведение);		
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					сткВозврат.ОписаниеОшибки = ТекстОшибки;
				КонецПопытки;
			Иначе	
				Отказ = Истина;
				сткВозврат.ОписаниеОшибки = сткПроверка.ТекстОшибки;
			КонецЕсли;
		Иначе
			Отказ = Истина;
			сткВозврат.ОписаниеОшибки = НСтр("В заявке на перевозку груза не указан вид перевозки!");
		КонецЕсли;
	КонецЕсли; 
	
	Если ТранзакцияАктивна() Тогда
		ЗафиксироватьТранзакцию()
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли; 
			
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция переводит заявку на перевозку груза в статус "согласовано".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СогласоватьЗаявку(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументОснование);

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если НЕ Отказ Тогда
		
		ТекущийСтатус	= упРаботаСоСтатусами.ПолучитьТекущийСтатус(ДокументОснование);
		Если ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая 
			ИЛИ ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована Тогда
			
			текЗаявкаНаПеревозкуГруза = ДокументОснование.ПолучитьОбъект();
			
			Попытка
				текЗаявкаНаПеревозкуГруза.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована);
				текЗаявкаНаПеревозкуГруза.Записать(РежимЗаписиДокумента.Проведение);		
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				сткВозврат.ОписаниеОшибки = ТекстОшибки;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;	
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция переводит задание на перевозку груза в статус "к планированию".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПередатьЗаданиеКПланированию(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 		= Ложь;
	ОписаниеОшибки	= "";
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументОснование);

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если НЕ Отказ Тогда
		
		ТекущийСтатус	= упРаботаСоСтатусами.ПолучитьТекущийСтатус(ДокументОснование);
		Если ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое Тогда
			
			текЗаданиеНаПеревозкуГруза = ДокументОснование.ПолучитьОбъект();
			Отказ = НЕ текЗаданиеНаПеревозкуГруза.ПроверитьЗаполнение();
			Если НЕ Отказ Тогда
				Попытка
					текЗаданиеНаПеревозкуГруза.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
					текЗаданиеНаПеревозкуГруза.Записать(РежимЗаписиДокумента.Проведение);		
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					сткВозврат.ОписаниеОшибки	= ТекстОшибки;
				КонецПопытки;
			Иначе
				Событие = Нстр("ru = 'Ошибка при проверке заполнения задания на перевозку'");
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,Метаданные.Документы.упЗаданиеНаПеревозкуГруза,ДокументОснование, ОписаниеОшибки);    
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если Отказ Тогда
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
	Иначе	
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЕсли;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция производит попытку включения задания в периодический рейс.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ВключитьЗаданиеВПериодическийРейс(ДокументОснование)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 		= Ложь;
	ОписаниеОшибки	= "";

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
		
	БлокировкаДанных = Новый БлокировкаДанных;		
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
     ЭлементБлокировки.УстановитьЗначение("Задание", ДокументОснование);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументОснование);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Задание", ДокументОснование);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если Не Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = упКомбинаторнаяОптимизация.ПолучитьТекстЗапросаРаспределенияЗаданийПоПериодическимРейсам();
		мсвЗаданий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОснование);
		Запрос.УстановитьПараметр("МассивЗаданий", мсвЗаданий);
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			Если ЗначениеЗаполнено(Выборка.Рейс) Тогда // добавление задания в существующий рейс.
				БлокировкаДанных = Новый БлокировкаДанных;		
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Документ", Выборка.Рейс);
						
				Попытка
					БлокировкаДанных.Заблокировать();
				Исключение
					Отказ = Истина;
					ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
						ОписаниеОшибки = ОписаниеОшибки();
					КонецЕсли;
				КонецПопытки;
				
				Если Не Отказ Тогда
					сткПериодПланирования = Новый Структура;
					сткПериодПланирования.Вставить("НачалоПериодаПланирования"   , НачалоДня(Выборка.ПлановоеНачалоРейса));
					сткПериодПланирования.Вставить("ОкончаниеПериодаПланирования", КонецДня(Выборка.ПлановоеНачалоРейса));
					Документы.упРейс.ДобавитьЗаданияВРейс(мсвЗаданий, Неопределено, Выборка.Рейс, сткПериодПланирования, ОписаниеОшибки, Отказ);
					 
				КонецЕсли;
			Иначе // создание нового рейса.
				сткДанныеТС = Новый Структура;
				сткДанныеТС.Вставить("Перевозчик");
				сткДанныеТС.Вставить("Соглашение");
				сткДанныеТС.Вставить("ТипТС");
				сткДанныеТС.Вставить("ТранспортноеСредство");
				сткДанныеТС.Вставить("Водитель1");
				сткДанныеТС.Вставить("Расписание");
				ЗаполнитьЗначенияСвойств(сткДанныеТС, Выборка);
				
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", Выборка.ПлановоеНачалоРейса);
				ДополнительныеПараметры.Вставить("ДанныеТС", сткДанныеТС);
				Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(мсвЗаданий, Неопределено, сткДанныеТС, ДополнительныеПараметры, ОписаниеОшибки, Отказ);
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЕсли;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция формирует заявки на ТС.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СформироватьЗаявкиНаТС(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	Документы.упРейс.СформироватьЗаявкуНаТСПоКоманде(ДокументОснование, ОписаниеОшибки, Отказ);
	
	//ТекущийСтатус	= Документы.упРейс.ПолучитьСтатус(ДокументОснование);
	//Если ТекущийСтатус = Перечисления.упСтатусыРейса.КПланированиюТС Тогда
	//	
	//	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, Новый Структура("ВидПеревозки, ВыборПеревозчика, ВыборТС", "ВидПеревозки", "ВидПеревозки.ВыборПеревозчика", "ВидПеревозки.ВыборТС"));
	//	Если сткРеквизиты.ВыборТС = Перечисления.упВариантыПодбораТС.НепосредственныйПодборВРейсе Тогда
	//		Отказ = Истина;
	//		ОписаниеОшибки = НСтр("ru = 'Для вида перевозки ""%1"" выбор ТС осуществляется непосредственно в рейсе'");
	//		ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, сткРеквизиты.ВидПеревозки);
	//	Иначе
	//		Если сткРеквизиты.ВыборТС = Перечисления.упВариантыВыбораПеревозчика.ОформляетсяТендер Тогда
	//			Отказ = Истина;
	//			ОписаниеОшибки = НСтр("ru = 'Для вида перевозки ""%1"" выбор ТС осуществляется на конкурсной основе тендером'");
	//			ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, сткРеквизиты.ВидПеревозки);
	//		Иначе
	//			мсвЗаявкиНаТС = Документы.упРейс.СформироватьЗаявкуНаТСПоРейсу(ДокументОснование,,, ОписаниеОшибки,Отказ);
	//		КонецЕсли;
	//	КонецЕсли; 
	//	
	//Иначе
	//	Отказ = Истина;
	//	ОписаниеОшибки = НСтр("ru = 'На основании документа со статусом ""%1"" заявки на ТС не формируются'");
	//	ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, ТекущийСтатус);
	//КонецЕсли;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция обновляет условия в уже сформированных заявках на ТС.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.упРейс - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ОбновитьУсловияВЗаявкахНаТС(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Рейс", ДокументОснование);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаТС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|ГДЕ
	|	упЗаявкаНаТС.Рейс = &Рейс
	|	И НЕ упЗаявкаНаТС.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат сткВозврат;
	КонецЕсли;
	
	тзЗаявокНаТС = РезультатЗапроса.Выгрузить();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументОснование);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тзЗаявокНаТС;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Ссылка");
	
	НачатьТранзакцию();
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных,, Отказ);

	Если Отказ Тогда
		сткВозврат.Успешно        = Ложь;
		сткВозврат.ОписаниеОшибки = НСтр("ru = 'Ошибка установки управляемой блокировки.'");
		Возврат сткВозврат;
	КонецЕсли; 
	
	мсвЗаявокНаТС = тзЗаявокНаТС.ВыгрузитьКолонку("Ссылка");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивЗаявок", мсвЗаявокНаТС);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упЗаявкаНаТС.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(упСтатусыЗаявокНаТССрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Создана)) КАК Статус
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних КАК упСтатусыЗаявокНаТССрезПоследних
	|		ПО (упСтатусыЗаявокНаТССрезПоследних.Документ = упЗаявкаНаТС.Ссылка)
	|ГДЕ
	|	упЗаявкаНаТС.Ссылка В(&МассивЗаявок)";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Статус = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Или Выборка.Статус = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение Или Выборка.Статус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
				ОписаниеПредупреждения = СтрШаблон(НСтр("ru = 'Документ ""%1"" находится в статусе ""%2"". Обновление условий по документу невозможно.'"), Выборка.Ссылка, Выборка.Статус); 
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Обновление условий заявки на ТС'"), УровеньЖурналаРегистрации.Предупреждение, Метаданные.Документы.упЗаявкаНаТС, Выборка.Ссылка, ОписаниеПредупреждения);	
			Иначе
				ЗаявкаНаТСОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Документы.упРейс.ЗаполнитьЗаявкуНаТС(ЗаявкаНаТСОбъект, ДокументОснование);
				Попытка
					ЗаявкаНаТСОбъект.Записать();		
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					ОписаниеОшибки = ТекстОшибки;
					Прервать;
				КонецПопытки;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	Если ТранзакцияАктивна() Тогда
		Если Отказ Тогда
			ОтменитьТранзакцию();
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли; 
	КонецЕсли;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно"       , Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция переводит рейс в статус "к выполнению".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПередатьРейсКВыполнению(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	текРейсСсылка = Неопределено;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упРейс") Тогда
		текРейсСсылка = ДокументОснование;
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС") Тогда
		текРейсСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");	
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПутевойЛист") Тогда
		текРейсСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");	
	КонецЕсли;
	
	Если текРейсСсылка = Неопределено Тогда
		Отказ = Истина;
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не найден документ рейс, для документа основания: %1"), ДокументОснование);
	ИНаче
		сткВозврат = ВыполнитьПопыткуПередатьРейсКВыполнению(текРейсСсылка);
	КонецЕсли;

	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;

	Возврат сткВозврат;

КонецФункции //ОбработатьСобытие_ПередатьРейсКВыполнению()

// Функция переводит рейс в статус "к выполнению".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ВыполнитьПопыткуПередатьРейсКВыполнению(ДокументОснование)
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	РейсСсылка = ДокументОснование;
	сткРеквизиты = Новый Структура();
	сткРеквизиты.Вставить("ИспользуютсяМультимодальныеПеревозки", "ВидПеревозки.ИспользуютсяМультимодальныеПеревозки");
	сткРеквизиты.Вставить("ПеревозкаТранспортнойКомпанией", "ПеревозкаТранспортнойКомпанией");
	сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РейсСсылка, сткРеквизиты);
	
	Если ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки")	Тогда 
		Если Не Документы.упРейс.ПоследовательностьВыполненияЭтаповМультимодальнойПеревозкиСоблюдена(РейсСсылка, ОписаниеОшибки) Тогда
			ОписаниеОшибки = НСтр("ru = 'Документу не может быть назначен статус ""К выполнению""!'") + Символы.ПС + ОписаниеОшибки;
			Отказ = Истина;
		КонецЕсли;
		Если Истина
			И Не Отказ
			И Не Документы.упРейс.ВсеПрерыванияГрузовогоПотокаОбработаны(РейсСсылка, ОписаниеОшибки) 
		Тогда
			ОписаниеОшибки = НСтр("ru = 'Документу не может быть назначен статус ""К выполнению""!'") + Символы.ПС + ОписаниеОшибки;
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И Не сткРеквизитыРейса.ПеревозкаТранспортнойКомпанией 
		И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПутевыхЛистов")
		И упРейс.ТребуетсяОформлениеПутевогоЛиста(РейсСсылка) 
	Тогда 
		Если Не упРейс.ЕстьПутевойЛист(РейсСсылка) Тогда
			ОписаниеОшибки = НСтр("ru = 'Начать исполнение рейса без ввода документа ""Путевой лист"" нельзя!'");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
		Возврат сткВозврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", РейсСсылка);

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если Не Отказ Тогда
			
		ТекущийСтатус = упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(РейсСсылка);
		Если Ложь
			Или ТекущийСтатус.Статус = Перечисления.упСтатусыРейса.Новый
			Или ТекущийСтатус.Статус = Перечисления.упСтатусыРейса.НазначеноТС 
		Тогда
			РейсОбъект = РейсСсылка.ПолучитьОбъект();
			
			Попытка
				РейсОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРейса.КВыполнению);
				РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);		
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				сткВозврат.ОписаниеОшибки = ТекстОшибки;
			КонецПопытки;
		Иначе
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Переводить в статус ""КВыполнению"" рейс в статусе %1 нельзя'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, ТекущийСтатус.Статус);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;

	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;

	Возврат сткВозврат;

КонецФункции // ВыполнитьПопыткуПередатьРейсКВыполнению()

// Функция переводит рейс в статус "завершен".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ЗавершитьРейс(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 		= Ложь;
	ОписаниеОшибки	= "";

	текРейсСсылка = Неопределено;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упРейс") Тогда
		текРейсСсылка = ДокументОснование;
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Или ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
			ВидПеревозкиРейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текРейсСсылка, "ВидПеревозки");
			ПроверятьНаличиеДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозкиРейса, "ЗапрещеноЗавершениеРейсаПриНаличииНедополученныхДокументов");
			Если ПроверятьНаличиеДокументов Тогда
				Если ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Тогда
					ТаблицаОстатков = Документы.упДвижениеДокументовРейса.ПолучитьОстаткиПоДвижениямДокументовРейса(текРейсСсылка);
					Если ТаблицаОстатков <> Неопределено Тогда
						ОписаниеОшибки = НСтр("ru = 'По рейсу есть непереданные/недополученные документы. Перевод рейса в статус ""Завершен"" запрещено'");
						Отказ = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
					ТаблицаОстатков = Документы.упДвижениеДокументовРейса.ПолучитьОстаткиПоНевозвращеннымДокументам(текРейсСсылка);
					Если ТаблицаОстатков <> Неопределено Тогда
						ОписаниеОшибки = НСтр("ru = 'По рейсу есть невозвращенные документы. Перевод рейса в статус ""Завершен"" запрещено'");
						Отказ = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС") Тогда
		текРейсСсылка	=  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");	
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПутевойЛист") Тогда
		текРейсСсылка	=  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");	
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
		Возврат сткВозврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", текРейсСсылка);

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если НЕ Отказ Тогда
		упРаботаСоСтатусами.УстановитьСтатусДокументу(текРейсСсылка, Перечисления.упСтатусыРейса.Завершен, ОписаниеОшибки, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;

	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;

	Возврат сткВозврат;

КонецФункции 

// Функция отменяет рейс.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ОтменитьРейс(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат		= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 			= Ложь;
	ОписаниеОшибки	= "";
	
	текРейсСсылка = Неопределено;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упРейс") Тогда
		текРейсСсылка	= ДокументОснование;
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС") Тогда
		текРейсСсылка	=  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");	
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
		Возврат сткВозврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", текРейсСсылка);

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если НЕ Отказ Тогда
			
		ТекущийСтатус	= упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(текРейсСсылка);
		Если ТекущийСтатус.Статус = Перечисления.упСтатусыРейса.Новый
			ИЛИ ТекущийСтатус.Статус = Перечисления.упСтатусыРейса.КПланированиюТС
			ИЛИ ТекущийСтатус.Статус = Перечисления.упСтатусыРейса.НазначеноТС Тогда
			текРейс = текРейсСсылка.ПолучитьОбъект();
			
			Попытка
				текРейс.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРейса.Отменен);
				текРейс.Записать(РежимЗаписиДокумента.Проведение);		
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				сткВозврат.ОписаниеОшибки	= ТекстОшибки;
			КонецПопытки;
		Иначе
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Отменять рейс в статусе %1 нельзя'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, ТекущийСтатус.Статус);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция создает подтверждение заявки на ТС.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьПодтверждениеЗаявкиНаТС(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки = "";
	
	Отказ = Ложь;
	Результат = упСервисныеФункцииВызовСервера.ВозможноСозданиеПодтвержденияЗаявкиНаТС(ДокументОснование, Отказ, Истина);
	Если Отказ Тогда
		Если ТипЗнч(Результат) = Тип("Строка") Тогда // возвращена ошибка
			ОписаниеОшибки = Результат;
		КонецЕсли; 
	Иначе
		Подтверждение = Документы.упПодтверждениеЗаявкиНаТС.СоздатьДокумент();
		Подтверждение.Дата = ТекущаяДатаСеанса();
		Подтверждение.Основание = ДокументОснование;
		Документы.упПодтверждениеЗаявкиНаТС.ОбработкаЗаполненияПодтвержденияЗаявкиНаТС(Подтверждение);
		
		Если Подтверждение.ПроверитьЗаполнение() Тогда
			Попытка
				Подтверждение.Записать(РежимЗаписиДокумента.Проведение);
				сткВозврат.ДокументРезультат = Подтверждение.Ссылка;
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
					ОписаниеОшибки = ОписаниеОшибки();
				КонецЕсли;
				Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
					ОписаниеОшибки = Нстр("ru = 'Не удалось провести подтверждение заявки на ТС.'");
				КонецЕсли;	
			КонецПопытки;
		Иначе	
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Не удалось создать подтверждение заявки на ТС из-за невозможности заполнения обязательных полей по документу-основанию.'");	
		КонецЕсли;
	КонецЕсли; 
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

// Функция создаёт Задание пользователю (доступно при использовании бизнес-процессов).
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ЗадачуПользователю(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат		= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 			= Ложь;
	ОписаниеОшибки	= "";
	
	НачатьТранзакцию();
	
	Попытка
		
		КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
		
		ТекущаяДата = ТекущаяДатаСеанса();
		
		БизнесПроцесс = БизнесПроцессы.Задание.СоздатьБизнесПроцесс();
		БизнесПроцесс.Наименование	 = НСтр(СтрШаблон("ru = '%1 [%2]'", СокрЛП(ТипСобытия.Описание), ОбщегоНазначения.ПредметСтрокой(ДокументОснование)), КодЯзыка);
		БизнесПроцесс.Автор 		 = ПараметрыСеанса.ТекущийПользователь;
		БизнесПроцесс.АвторСтрокой 	 = НСтр(СтрШаблон("ru = '%1'", ОбщегоНазначения.ПредметСтрокой(ПараметрыСеанса.ТекущийПользователь)), КодЯзыка);
		БизнесПроцесс.Дата 			 = ТекущаяДата;
		БизнесПроцесс.Предмет 		 = ДокументОснование;
		БизнесПроцесс.Важность 		 = ТипСобытия.Важность;
		БизнесПроцесс.Исполнитель 	 = ТипСобытия.Исполнитель;
		БизнесПроцесс.СрокИсполнения = ТекущаяДата;
		
		БизнесПроцесс.Записать();
		
		НоваяЗадача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
		ЗаполнитьЗначенияСвойств(НоваяЗадача, БизнесПроцесс);
		НоваяЗадача.БизнесПроцесс  = БизнесПроцесс;
		НоваяЗадача.ПредметСтрокой = НСтр(СтрШаблон("ru = '%1'", ОбщегоНазначения.ПредметСтрокой(ДокументОснование)), КодЯзыка);
		НоваяЗадача.Записать();
		
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОписаниеОшибки	= ТекстОшибки;
	КонецПопытки;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция заполняет плановые доходы по заявке на перевозку груза и рассчитывает их.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_РасчетПлановыхДоходов(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	текЗаявкаНаПеревозкуГруза = ДокументОснование.ПолучитьОбъект();
	текЗаявкаНаПеревозкуГруза.ДополнительныеСвойства.Вставить("НеРегистрироватьСобытие");
	текЗаявкаНаПеревозкуГруза.ЗаполнитьПлановыеДоходыТиповымиСтатьями();
	
	// Рассчитать доходы.
	упТарификацияВызовСервера.Рассчитать(текЗаявкаНаПеревозкуГруза);
	
	// Рассчитать итоговую сумму доходов.
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаДоходы",			"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыСкидка",		"СкидкаСумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыНДС",		"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(текЗаявкаНаПеревозкуГруза, "ПлановыеДоходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	
	Отказ 		= Ложь;
	
	Попытка
		текЗаявкаНаПеревозкуГруза.Записать(РежимЗаписиДокумента.Проведение);		
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		сткВозврат.ОписаниеОшибки	= ТекстОшибки;
	КонецПопытки;
			
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция заполняет плановые расходы по рейсу и рассчитывает их.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_РасчетПлановыхРасходов(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	текРейс 	= ДокументОснование.ПолучитьОбъект();
	текРейс.ДополнительныеСвойства.Вставить("НеРегистрироватьСобытие");
	
	сткПараметрыПеревозки = упРейс.ПараметрыПеревозкиРейса(ДокументОснование);
	
	тбзРасходы 	= упРаспределениеРасходов.ПолучитьСписокСтатейРасходов(ДокументОснование, сткПараметрыПеревозки.ТипТС, сткПараметрыПеревозки.Соглашение);
	тбзЗадания	= упРейс.ТаблицаЗаданийПоРейсу(ДокументОснование);
	текРейс.ЗаполнитьПлановыеРасходы(тбзРасходы, тбзЗадания);
	
	// Рассчитать плановые расходы.
	ВходныеПараметры = Новый Структура();
	упТарификацияВызовСервера.Рассчитать(текРейс, ВходныеПараметры);
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(текРейс, "ПлановыеРасходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	
	Отказ 		= Ложь;
	Попытка
		текРейс.Записать(РежимЗаписиДокумента.Проведение);		
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		сткВозврат.ОписаниеОшибки	= ТекстОшибки;
	КонецПопытки;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция перезаполняет начальные остатки пробега и ГСМ в следующем путевом листе.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СкорректироватьНачальныеОстаткиВСледующемПутевомЛисте(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	СтатусПутевогоЛиста = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ДокументОснование);
	
	Отказ = Ложь;
	
	Если Ложь
		Или СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Возвращен
		Или СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Закрыт
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	упПутевойЛист.Ссылка КАК Ссылка,
		|	упСтатусыПутевыхЛистовСрезПоследних.Статус КАК Статус
		|ИЗ
		|	Документ.упПутевойЛист КАК упПутевойЛист
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК Основание
		|	ПО ИСТИНА
		|		И Основание.ТранспортноеСредство = упПутевойЛист.ТранспортноеСредство
		|		И упПутевойЛист.МоментВремени > Основание.МоментВремени
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
		|	ПО упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка
		|ГДЕ ИСТИНА
		|	И ЕСТЬNULL(упСтатусыПутевыхЛистовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Новый)) = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Новый)
		|	И Основание.Ссылка = &Ссылка
		|	И НЕ (упПутевойЛист.ПометкаУдаления)
		|";

		Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ПутевойЛистОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ПутевойЛистОбъект.ЗаполнитьГСМ();
			ПутевойЛистОбъект.ЗаполнитьПробег();

			Попытка
				ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				сткВозврат.ОписаниеОшибки = ТекстОшибки;
			КонецПопытки;
		КонецЦикла;
	Иначе	
		Отказ = Истина;	
		ТекстОшибки = Нстр("ru = 'Путевой лист %1 в статусе %2, корректировка начальных остатков следующего путевого невозможна'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДокументОснование, СтатусПутевогоЛиста);
		сткВозврат.ОписаниеОшибки = ТекстОшибки;
	КонецЕсли;
			
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция Выполняет запрос к сервису СКАУТ.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ЗапроситьЗначенияУровняТопливаНаПериодПутевогоЛиста(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.Текст = "ВЫБРАТЬ
	|	упСервераСбораДанных.Тип КАК Тип,
	|	упСервераСбораДанных.Сервер КАК Сервер,
	|	упСервераСбораДанных.Порт КАК Порт,
	|	упСервераСбораДанных.Пользователь КАК Пользователь,
	|	упСервераСбораДанных.Пароль КАК Пароль,
	|	упСервераСбораДанных.СобытияМониторинга КАК СобытияМониторинга,
	|	упСервераСбораДанных.ПериодОтсрочкиПолученияДанныхПоСобытиям КАК Отсрочка,
	|	упСервераСбораДанных.Схема КАК Схема
	|ИЗ
	|	Справочник.упСервераСбораДанных КАК упСервераСбораДанных
	|ГДЕ
	|	упСервераСбораДанных.Тип = ЗНАЧЕНИЕ(Перечисление.упТипСервераСбораДанных.СКАУТ)
	|	И НЕ упСервераСбораДанных.ПометкаУдаления
	|	И упСервераСбораДанных.Активен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛист.Ссылка КАК Ссылка,
	|	упПутевойЛист.НачалоРейса КАК НачалоРейса,
	|	упПутевойЛист.ОкончаниеРейса КАК ОкончаниеРейса,
	|	упТранспортныеСредства.Ссылка КАК ТС,
	|	упТранспортныеСредства.ДатчикУровняТоплива КАК Датчик,
	|	упТранспортныеСредства.ДатчикУровняТоплива.Назначение КАК Назначение,
	|	упПутевойЛист.Рейс КАК Рейс
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО упПутевойЛист.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ПО упПутевойЛист.ТранспортноеСредство = упТранспортныеСредства.Ссылка
	|ГДЕ
	|	упСтатусыПутевыхЛистовСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Возвращен), ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Закрыт))
	|	И упПутевойЛист.Ссылка = &Ссылка
	|	И НЕ упПутевойЛист.НачалоРейса = ДАТАВРЕМЯ(1, 1, 1)
	|	И НЕ упПутевойЛист.ОкончаниеРейса = ДАТАВРЕМЯ(1, 1, 1)
	|	И НЕ упТранспортныеСредства.ДатчикУровняТоплива = ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛист.Ссылка КАК Ссылка,
	|	упТранспортныеСредства.Ссылка КАК ТС,
	|	упУстановленныеТрекерыСрезПоследних.Трекер КАК Трекер,
	|	упМоделиТрекеровДатчики.Датчик КАК Датчик,
	|	упМоделиТрекеровДатчики.КалибровочныйГрафик КАК КалибровочныйГрафик,
	|	упУстановленныеТрекерыСрезПоследних.Трекер.ИдентификаторУстройства КАК ИдентификаторУстройства
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ПО упПутевойЛист.ТранспортноеСредство = упТранспортныеСредства.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУстановленныеТрекеры.СрезПоследних КАК упУстановленныеТрекерыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упМоделиТрекеров.Датчики КАК упМоделиТрекеровДатчики
	|			ПО упУстановленныеТрекерыСрезПоследних.Трекер.Модель = упМоделиТрекеровДатчики.Ссылка
	|		ПО упПутевойЛист.ТранспортноеСредство = упУстановленныеТрекерыСрезПоследних.ТранспортноеСредство
	|ГДЕ
	|	упПутевойЛист.Ссылка = &Ссылка";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	Если НЕ ПакетЗапросов[1].Пустой() И ПакетЗапросов[0].Пустой() Тогда
		Отказ = Истина;	
		ТекстОшибки = НСтр("ru = 'Алгоритм: Запросить значения уровня топлива на период путевого листа поддерживает только обращение к СКАУТ сервису.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
		сткВозврат.ОписаниеОшибки	= ТекстОшибки;
	Иначе
		ВыборкаСерверов = ПакетЗапросов[0].Выбрать();
		Выборка = ПакетЗапросов[1].Выбрать();
		ТаблицаТрекеровТС = ПакетЗапросов[2].Выгрузить();
		ТаблицаЗначенийДатчиков = упИнтеграцияССерверамиСбораДанных.СоздатьТаблицуДатчиковСКАУТ();
		Пока ВыборкаСерверов.Следующий() Цикл
			Токен = "";
			ПроксиАвторизация = Неопределено;
			Попытка
				Если НЕ ПустаяСтрока(ВыборкаСерверов.Пользователь) Тогда 
					Токен = упИнтеграцияССерверамиСбораДанных.ПолучитьТокенScoutSOAP(ПроксиАвторизация, ВыборкаСерверов.Пользователь, ВыборкаСерверов.Пароль, ВыборкаСерверов.Сервер, ВыборкаСерверов.Порт);
					Если ПустаяСтрока(Токен) Тогда 
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			Исключение
				Токен = "";
				СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОписаниеОшибки.Добавить(СтрШаблон("ПолучениеДанныхОтScoutSOAP Токен: %1", СтрокаСообщенияОбОшибке));
				Продолжить;
			КонецПопытки;
			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл
				НачалоПериода = Выборка.НачалоРейса;
				КонецПериода = Выборка.ОкончаниеРейса;
				ДатчикТоплива = Выборка.Датчик;
				Назначение = Выборка.Назначение;
				Трекер = Неопределено;
				КалибровочныйГрафик = Справочники.упКалибровочныеГрафики.ПустаяСсылка();
				ТекущаяУстройство = "";
				Отбор = Новый Структура();
				Отбор.Вставить("Ссылка", Выборка.Ссылка);
				Отбор.Вставить("ТС", Выборка.ТС);
				Отбор.Вставить("Датчик", Выборка.Датчик);
				НайденныеСтроки = ТаблицаТрекеровТС.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() Тогда
					КалибровочныйГрафик = НайденныеСтроки[0].КалибровочныйГрафик;
					Трекер = НайденныеСтроки[0].Трекер;
					ТекущаяУстройство = НайденныеСтроки[0].ИдентификаторУстройства;
				Иначе
					Продолжить;
				КонецЕсли;
				ДанныеИнтервала = Неопределено;
				ЕстьОшибки = Ложь;
				Попытка
					ДанныеИнтервала = упИнтеграцияССерверамиСбораДанных.ПолучитьДанныеТопливаЗаПериодScoutSOAP(ТекущаяУстройство, НачалоПериода, КонецПериода, ВыборкаСерверов.Сервер, ВыборкаСерверов.Порт, Токен, ЕстьОшибки);
				Исключение
					ДанныеИнтервала = Неопределено;
					СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ОписаниеОшибки.Добавить(СтрШаблон("ПолучениеДанныхОтScoutSOAP: %1", СтрокаСообщенияОбОшибке));
				КонецПопытки;
				Если НЕ ДанныеИнтервала = Неопределено Тогда
					Для каждого ОтрезокДанных Из ДанныеИнтервала Цикл
						ТаблицаЗначений = ОтрезокДанных.Значение;
						Для каждого ТекущаяСтрокаЗаправкиСливы Из ТаблицаЗначений Цикл
							упИнтеграцияССерверамиСбораДанных.ДобавитьСтрокаДанныхДатчика(ТаблицаЗначенийДатчиков, ТекущаяСтрокаЗаправкиСливы.НачалоПериода, Трекер, КалибровочныйГрафик, ДатчикТоплива, ТекущаяСтрокаЗаправкиСливы.НачОстаток, Назначение, Выборка.ТС, Выборка.Рейс);
							упИнтеграцияССерверамиСбораДанных.ДобавитьСтрокаДанныхДатчика(ТаблицаЗначенийДатчиков, ТекущаяСтрокаЗаправкиСливы.КонецПериода, Трекер, КалибровочныйГрафик, ДатчикТоплива, ТекущаяСтрокаЗаправкиСливы.КонОстаток, Назначение, Выборка.ТС, Выборка.Рейс);
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла; 
			Если НЕ ПроксиАвторизация = Неопределено Тогда 
				ПроксиАвторизация.Logout();
			КонецЕсли;
		КонецЦикла; 
		Токен = "";
		ПроксиАвторизация = Неопределено;
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			сткВозврат.ДокументРезультат = ДокументОснование;
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство,Трекер);
			СтруктураОбъектов.Вставить("Датчики",ТаблицаЗначенийДатчиков.Скопировать());
			МассивДанных = Новый Массив;
			МассивДанных.Добавить(СтруктураОбъектов);
			упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ(МассивДанных);
		КонецЕсли;
	КонецЕсли; 
	
	
	Если ОписаниеОшибки.Количество() Тогда
		сткВозврат.Вставить("ОписаниеОшибки", СтрСоединить(ОписаниеОшибки, "; "));
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции // ОбработатьСобытие_ЗапроситьЗначенияУровняТопливаНаПериодПутевогоЛиста()

// Функция создает документы "Событие мониторинга" с типом "Опоздание в точке маршрута".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьСобытиеМониторингаОпозданиеВТочкеМаршрута(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Период КАК Период,
	|	упМаршрутПоРейсу.Регистратор КАК Ссылка,
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсу.Адрес КАК Адрес,
	|	упМаршрутПоРейсу.ВременноеОкноНачало КАК ВременноеОкноНачало,
	|	упМаршрутПоРейсу.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
	|	упМаршрутПоРейсу.Пройдена КАК Пройдена,
	|	ВЫБОР
	|		КОГДА упМаршрутПоРейсу.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И упМаршрутПоРейсу.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		КОГДА упМаршрутПоРейсу.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.УбытиеФакт, СЕКУНДА, -60) <= упМаршрутПоРейсу.ВременноеОкноОкончание
	|			ТОГДА ИСТИНА
	|		КОГДА упМаршрутПоРейсу.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПрибытиеФакт, СЕКУНДА, упМаршрутПоРейсу.ВремяОжиданияВТочке * 3600 + 60) >= упМаршрутПоРейсу.ВременноеОкноНачало
	|			ТОГДА ИСТИНА
	|		КОГДА ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.УбытиеФакт, СЕКУНДА, -60) <= упМаршрутПоРейсу.ВременноеОкноОкончание
	//|				И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПрибытиеФакт, СЕКУНДА, упМаршрутПоРейсу.ВремяОжиданияВТочке * 3600 + 60) >= упМаршрутПоРейсу.ВременноеОкноНачало
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПопадаетВоВременноеОкноФакт,
	|	упМаршрутПоРейсу.ПрибытиеФакт
	|ПОМЕСТИТЬ втТекущиеЗаписи
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	МАКСИМУМ(упМаршрутПоРейсу.Пройдена) КАК Пройдена
	|ПОМЕСТИТЬ втПредшествующиеЗаписи
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			упМаршрутПоРейсу.Рейс КАК Рейс,
	|			упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|			МАКСИМУМ(упМаршрутПоРейсу.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТекущиеЗаписи КАК втТекущиеЗаписи
	|				ПО упМаршрутПоРейсу.Рейс = втТекущиеЗаписи.Рейс
	|					И упМаршрутПоРейсу.Идентификатор = втТекущиеЗаписи.Идентификатор
	|					И упМаршрутПоРейсу.Период < втТекущиеЗаписи.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			упМаршрутПоРейсу.Рейс,
	|			упМаршрутПоРейсу.Идентификатор) КАК ВложенныйЗапрос
	|		ПО упМаршрутПоРейсу.Период = ВложенныйЗапрос.Период
	|			И упМаршрутПоРейсу.Рейс = ВложенныйЗапрос.Рейс
	|			И упМаршрутПоРейсу.Идентификатор = ВложенныйЗапрос.Идентификатор
	|
	|СГРУППИРОВАТЬ ПО
	|	упМаршрутПоРейсу.Рейс,
	|	упМаршрутПоРейсу.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеЗаписи.Ссылка,
	|	втТекущиеЗаписи.Адрес КАК АдресТочки,
	|	втТекущиеЗаписи.Адрес.Широта КАК Широта,
	|	втТекущиеЗаписи.Адрес.Долгота КАК Долгота,
	|	втТекущиеЗаписи.Рейс,
	|	втТекущиеЗаписи.ВременноеОкноНачало КАК ВременноеОкноНачало,
	|	втТекущиеЗаписи.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
	|	ЕСТЬNULL(упПеревозчикПоРейсу.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	|	втТекущиеЗаписи.ПрибытиеФакт КАК ФактическоеПрибытие,
	|	втТекущиеЗаписи.Идентификатор КАК Идентификатор
	|ИЗ
	|	втТекущиеЗаписи КАК втТекущиеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредшествующиеЗаписи КАК втПредшествующиеЗаписи
	|		ПО втТекущиеЗаписи.Рейс = втПредшествующиеЗаписи.Рейс
	|			И втТекущиеЗаписи.Идентификатор = втПредшествующиеЗаписи.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|				,
	|				Рейс В
	|					(ВЫБРАТЬ
	|						втТекущиеЗаписи.Рейс
	|					ИЗ
	|						втТекущиеЗаписи)) КАК упПеревозчикПоРейсу
	|		ПО втТекущиеЗаписи.Рейс = упПеревозчикПоРейсу.Рейс
	|ГДЕ
	|	ВЫБОР
	|			КОГДА втТекущиеЗаписи.Пройдена
	|					И НЕ ЕСТЬNULL(втПредшествующиеЗаписи.Пройдена, ИСТИНА)
	|					И НЕ втТекущиеЗаписи.ПопадаетВоВременноеОкноФакт
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПРЕДСТАВЛЕНИЕ(упРаботыПоРейсу.Задание.Получатель) КАК Партнер,
	|	упРаботыПоРейсу.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Идентификатор В
	|			(ВЫБРАТЬ
	|				ВтТекущиеЗаписи.Идентификатор
	|			ИЗ
	|				ВтТекущиеЗаписи КАК ВтТекущиеЗаписи)
	|	И упРаботыПоРейсу.Рейс В
	|			(ВЫБРАТЬ
	|				ВтТекущиеЗаписи.Рейс
	|			ИЗ
	|				ВтТекущиеЗаписи КАК ВтТекущиеЗаписи)";
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[2].Пустой() Тогда
		Отказ = Истина;	
		ТекстОшибки = НСтр("ru = 'Документом ""%1"" не было зафиксировано опозданий в точках маршрута'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
		сткВозврат.ОписаниеОшибки	= ТекстОшибки;
	Иначе
		Выборка = РезультатЗапроса[2].Выбрать();
		Пока Выборка.Следующий() Цикл
			НовоеСобытие = Документы.упСобытиеМониторинга.СоздатьДокумент();
			НовоеСобытие.Дата = ТекущаяДатаСеанса();
			НовоеСобытие.ТипСобытия = Справочники.упТипыСобытийМониторинга.ОпозданиеВТочкеМаршрута;
			СписокПартнеров = "";
			РезультатПартнеры = РезультатЗапроса[3].Выгрузить();
			Отбор = Новый Структура;
    		Отбор.Вставить("Идентификатор", Выборка.Идентификатор);
    		Строки = РезультатПартнеры.НайтиСтроки(Отбор);
			Для каждого Стр Из Строки Цикл
				СписокПартнеров = СписокПартнеров + Стр.Партнер;
			КонецЦикла; 
			ТекстКомментария = НСтр("ru = 'При посещении адреса ""%1"" не удалось попасть во временное окно (%2). Фактическое прибытие ""%3"". Партнеры: %4 '");
			ТекстКомментария = СтрШаблон(ТекстКомментария, Выборка.АдресТочки, упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(Выборка.ВременноеОкноНачало, Выборка.ВременноеОкноОкончание),
			Выборка.ФактическоеПрибытие,СписокПартнеров);
            НовоеСобытие.Комментарий = ТекстКомментария;
			ЗаполнитьЗначенияСвойств(НовоеСобытие, Выборка);
			НовоеСобытие.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла; 
		сткВозврат.ДокументРезультат = НовоеСобытие;
	КонецЕсли; 
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

// Функция выполнить проверку наступления события заправки или слива.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ОтразитьЗаправкуСливВПутевомЛистеПоДаннымДатчиков(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упДатыСобытий.Период КАК Период,
	|	упДатыСобытий.Событие КАК Ссылка,
	|	упДатыСобытий.ТранспортноеСредство КАК ТС,
	|	упДатыСобытий.ТипСобытия КАК ТипСобытия,
	|	упДатыСобытий.Рейс КАК Рейс,
	|	упДатыСобытий.ТранспортноеСредство.ДатчикУровняТоплива КАК Датчик,
	|	ТипыСМ.ПериодИзменения КАК ПериодИзменения,
	|	ДОБАВИТЬКДАТЕ(упДатыСобытий.Период, СЕКУНДА, ТипыСМ.ПериодИзменения) КАК КонецПериода,
	|	ДОБАВИТЬКДАТЕ(упДатыСобытий.Период, СЕКУНДА, -1 * ТипыСМ.ПериодИзменения) КАК НачалоПериода,
	|	ТипыСМ.Датчик.ВидГСМ КАК ВидГСМ,
	|	ЕстьNull(упТранспортныеСредстваВидыГСМ.ВидГСМ, ЗНАЧЕНИЕ(Справочник.упВидыГСМ.ПустаяСсылка)) КАК ВидГСМОсновной,
	|	ТипыСМ.ТипИзменения КАК ТипИзменения
	|ПОМЕСТИТЬ вт_ДокументСобытия
	|ИЗ
	|	РегистрСведений.упДатыСобытий КАК упДатыСобытий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТипыСобытийМониторинга КАК ТипыСМ
	|		ПО упДатыСобытий.ТипСобытия = ТипыСМ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(&Период) КАК упТранспортныеСредстваВидыГСМ
	|		ПО упДатыСобытий.ТранспортноеСредство = упТранспортныеСредстваВидыГСМ.ТранспортноеСредство
	|			И упТранспортныеСредстваВидыГСМ.Используется
	|			И упТранспортныеСредстваВидыГСМ.Основной
	|ГДЕ
	|	упДатыСобытий.Событие = &Ссылка
	|	И НЕ упДатыСобытий.ТранспортноеСредство.ДатчикУровняТоплива = ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка)
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтатусыПутевыхЛистов.Документ КАК ПутевойЛист,
	|	СтатусыПутевыхЛистов.Период КАК Период,
	|	вт_ДокументСобытия.ВидГСМ КАК ВидГСМ,
	|	вт_ДокументСобытия.ВидГСМОсновной КАК ВидГСМОсновной,
	|	вт_ДокументСобытия.Период КАК ДатаЗаправкиСлива
	|ИЗ
	|	вт_ДокументСобытия КАК вт_ДокументСобытия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК ПутевойЛист
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК СтатусыПутевыхЛистов
	|			ПО ПутевойЛист.Ссылка = СтатусыПутевыхЛистов.Документ
	|		ПО вт_ДокументСобытия.ТС = ПутевойЛист.ТранспортноеСредство
	|			И (ПутевойЛист.НачалоРейса <= вт_ДокументСобытия.Период)
	|			И (ВЫБОР
	|				КОГДА вт_ДокументСобытия.Рейс = ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ вт_ДокументСобытия.Рейс = ПутевойЛист.Рейс
	|			КОНЕЦ)
	|ГДЕ
	|	СтатусыПутевыхЛистов.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Новый), ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Выдан))
	|	И НЕ ПутевойЛист.НачалоРейса = ДАТАВРЕМЯ(1, 1, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДанныеДатчиков.Период КАК Период,
	|	упДанныеДатчиков.Значение КАК Значение
	|ИЗ
	|	вт_ДокументСобытия КАК вт_ДокументСобытия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков КАК упДанныеДатчиков
	|		ПО вт_ДокументСобытия.ТС = упДанныеДатчиков.ТранспортноеСредство
	|			И вт_ДокументСобытия.Датчик = упДанныеДатчиков.Датчик
	|			И вт_ДокументСобытия.НачалоПериода < упДанныеДатчиков.Период
	|			И вт_ДокументСобытия.КонецПериода > упДанныеДатчиков.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Значение,
	|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "Дата");
	Запрос.УстановитьПараметр("Период", сткРеквизиты.Дата);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[1].Пустой() Тогда
		Отказ = Истина;	
		ТекстОшибки = НСтр("ru = 'Для документа ""%1"" нет Путевого листа'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
		сткВозврат.ОписаниеОшибки	= ТекстОшибки;
	Иначе
		Выборка = РезультатЗапроса[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(Выборка.ПутевойЛист) Тогда
				Отказ = Истина;	
				ТекстОшибки = НСтр("ru = 'Для документа ""%1"" нет Путевого листа'");
				ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
				сткВозврат.ОписаниеОшибки	= ТекстОшибки;
				Продолжить;
			Иначе
				ТаблицаДанных = РезультатЗапроса[2].Выгрузить();
				Количество = ТаблицаДанных.Количество();
				Если Количество > 1 Тогда
					ПерваяСтрока = ТаблицаДанных[0];
					КрайняяСтрока = ТаблицаДанных[Количество-1];
					ТекущееКоличество = КрайняяСтрока.Значение - ПерваяСтрока.Значение;
					ВидДвижения = Перечисления.упВидыДвиженияТоплива.ПустаяСсылка();
					Если ПерваяСтрока.Период > КрайняяСтрока.Период Тогда
						ВидДвижения = Перечисления.упВидыДвиженияТоплива.Слив;
					ИначеЕсли КрайняяСтрока.Период > ПерваяСтрока.Период Тогда
						ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка;
					КонецЕсли;	
					Если НЕ ЗначениеЗаполнено(ВидДвижения) Тогда
						Отказ = Истина;	
						ТекстОшибки = НСтр("ru = 'Для документа ""%1"" нет данных для вычисления уровня топлива, не определен вид движения'");
						ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
						сткВозврат.ОписаниеОшибки	= ТекстОшибки;
						Продолжить;
					КонецЕсли;
					
					ВидГСМ = ?(ЗначениеЗаполнено(Выборка.ВидГСМ),Выборка.ВидГСМ,Выборка.ВидГСМОсновной);
					
					Отбор = Новый Структура;
					Отбор.Вставить("ВидГСМ", ВидГСМ);
					Отбор.Вставить("ВидДвижения", ВидДвижения);
					Отбор.Вставить("ДатаЗаправкиСлива", Выборка.ДатаЗаправкиСлива);
					Отбор.Вставить("Событие", ДокументОснование);
					
					СсылкаПутевойЛист = Выборка.ПутевойЛист;
					НайденныеСтроки = СсылкаПутевойЛист.ЗаправкиСливыГСМ.НайтиСтроки(Отбор);
					Если НайденныеСтроки.Количество()=0 Тогда
						// Когда нет подобных строк в табличной части
						Статус = Документы.упПутевойЛист.ПолучитьСтатус(СсылкаПутевойЛист);	
						ОбъектПутевогоЛиста = СсылкаПутевойЛист.ПолучитьОбъект();
						ЗаправкиСливыГСМ = ОбъектПутевогоЛиста.ЗаправкиСливыГСМ;
						НоваяСтрока = ЗаправкиСливыГСМ.Добавить();
						НоваяСтрока.ВидГСМ = ВидГСМ;
						НоваяСтрока.ВидДвижения = ВидДвижения;
						НоваяСтрока.ДатаЗаправкиСлива = Выборка.ДатаЗаправкиСлива;
						НоваяСтрока.Количество = ТекущееКоличество;
						НоваяСтрока.Событие = ДокументОснование;
						ОбъектПутевогоЛиста.ДополнительныеСвойства.Вставить("Статус", Статус);
						ОбъектПутевогоЛиста.Записать();
						// документ в который внеси изменения
						сткВозврат.ДокументРезультат = СсылкаПутевойЛист;
					КонецЕсли;
				Иначе
					Отказ = Истина;	
					ТекстОшибки = НСтр("ru = 'Для документа ""%1"" нет данных для вычисления уровня топлива'");
					ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
					сткВозврат.ОписаниеОшибки	= ТекстОшибки;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции // ОбработатьСобытие_ОтразитьЗаправкуСливВПутевомЛистеПоДаннымДатчиков()

// Функция создает документы "Событие мониторинга" с типом "Планируемое опоздание в точке маршрута".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьСобытиеМониторингаПланируемоеОпозданиеВТочкеМаршрута(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Период КАК Период,
	|	упМаршрутПоРейсу.Регистратор КАК Ссылка,
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсу.Адрес КАК Адрес,
	|	упМаршрутПоРейсу.ВременноеОкноНачало КАК ВременноеОкноНачало,
	|	упМаршрутПоРейсу.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
	|	упМаршрутПоРейсу.Пройдена КАК Пройдена,
	|	ВЫБОР
	|		КОГДА упМаршрутПоРейсу.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И упМаршрутПоРейсу.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		КОГДА упМаршрутПоРейсу.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеУбытие, СЕКУНДА, -60) <= упМаршрутПоРейсу.ВременноеОкноОкончание
	|			ТОГДА ИСТИНА
	|		КОГДА упМаршрутПоРейсу.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеПрибытие, СЕКУНДА, упМаршрутПоРейсу.ВремяОжиданияВТочке * 3600 + 60) >= упМаршрутПоРейсу.ВременноеОкноНачало
	|			ТОГДА ИСТИНА
	|		КОГДА ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеУбытие, СЕКУНДА, -60) <= упМаршрутПоРейсу.ВременноеОкноОкончание
	|				И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеПрибытие, СЕКУНДА, упМаршрутПоРейсу.ВремяОжиданияВТочке * 3600 + 60) >= упМаршрутПоРейсу.ВременноеОкноНачало
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПопадаетВоВременноеОкноПлан,
	|	упМаршрутПоРейсу.ПлановоеПрибытие
	|ПОМЕСТИТЬ втТекущиеЗаписи
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	МАКСИМУМ(упМаршрутПоРейсу.Пройдена) КАК Пройдена,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА упМаршрутПоРейсу.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|					И упМаршрутПоРейсу.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			КОГДА упМаршрутПоРейсу.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|					И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеУбытие, СЕКУНДА, -60) <= упМаршрутПоРейсу.ВременноеОкноОкончание
	|				ТОГДА ИСТИНА
	|			КОГДА упМаршрутПоРейсу.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|					И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеПрибытие, СЕКУНДА, упМаршрутПоРейсу.ВремяОжиданияВТочке * 3600 + 60) >= упМаршрутПоРейсу.ВременноеОкноНачало
	|				ТОГДА ИСТИНА
	|			КОГДА ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеУбытие, СЕКУНДА, -60) <= упМаршрутПоРейсу.ВременноеОкноОкончание
	|					И ДОБАВИТЬКДАТЕ(упМаршрутПоРейсу.ПлановоеПрибытие, СЕКУНДА, упМаршрутПоРейсу.ВремяОжиданияВТочке * 3600 + 60) >= упМаршрутПоРейсу.ВременноеОкноНачало
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ПопадаетВоВременноеОкноПлан
	|ПОМЕСТИТЬ втПредшествующиеЗаписи
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			упМаршрутПоРейсу.Рейс КАК Рейс,
	|			упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|			МАКСИМУМ(упМаршрутПоРейсу.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТекущиеЗаписи КАК втТекущиеЗаписи
	|				ПО упМаршрутПоРейсу.Рейс = втТекущиеЗаписи.Рейс
	|					И упМаршрутПоРейсу.Идентификатор = втТекущиеЗаписи.Идентификатор
	|					И упМаршрутПоРейсу.Период < втТекущиеЗаписи.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			упМаршрутПоРейсу.Рейс,
	|			упМаршрутПоРейсу.Идентификатор) КАК ВложенныйЗапрос
	|		ПО упМаршрутПоРейсу.Период = ВложенныйЗапрос.Период
	|			И упМаршрутПоРейсу.Рейс = ВложенныйЗапрос.Рейс
	|			И упМаршрутПоРейсу.Идентификатор = ВложенныйЗапрос.Идентификатор
	|
	|СГРУППИРОВАТЬ ПО
	|	упМаршрутПоРейсу.Рейс,
	|	упМаршрутПоРейсу.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеЗаписи.Ссылка,
	|	втТекущиеЗаписи.Адрес КАК АдресТочки,
	|	втТекущиеЗаписи.Адрес.Широта КАК Широта,
	|	втТекущиеЗаписи.Адрес.Долгота КАК Долгота,
	|	втТекущиеЗаписи.Рейс,
	|	втТекущиеЗаписи.ВременноеОкноНачало КАК ВременноеОкноНачало,
	|	втТекущиеЗаписи.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
	|	ЕСТЬNULL(упПеревозчикПоРейсу.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	|	втТекущиеЗаписи.ПлановоеПрибытие,
	|	втТекущиеЗаписи.Идентификатор
	|ИЗ
	|	втТекущиеЗаписи КАК втТекущиеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредшествующиеЗаписи КАК втПредшествующиеЗаписи
	|		ПО втТекущиеЗаписи.Рейс = втПредшествующиеЗаписи.Рейс
	|			И втТекущиеЗаписи.Идентификатор = втПредшествующиеЗаписи.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|				,
	|				Рейс В
	|					(ВЫБРАТЬ
	|						втТекущиеЗаписи.Рейс
	|					ИЗ
	|						втТекущиеЗаписи)) КАК упПеревозчикПоРейсу
	|		ПО втТекущиеЗаписи.Рейс = упПеревозчикПоРейсу.Рейс
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ втТекущиеЗаписи.Пройдена
	|					И НЕ втТекущиеЗаписи.ПопадаетВоВременноеОкноПлан
	|					И ЕСТЬNULL(втПредшествующиеЗаписи.ПопадаетВоВременноеОкноПлан, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПРЕДСТАВЛЕНИЕ(упРаботыПоРейсу.Задание.Получатель) КАК Партнер,
	|	упРаботыПоРейсу.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Идентификатор В
	|			(ВЫБРАТЬ
	|				ВтТекущиеЗаписи.Идентификатор
	|			ИЗ
	|				ВтТекущиеЗаписи КАК ВтТекущиеЗаписи)
	|	И упРаботыПоРейсу.Рейс В
	|			(ВЫБРАТЬ
	|				ВтТекущиеЗаписи.Рейс
	|			ИЗ
	|				ВтТекущиеЗаписи КАК ВтТекущиеЗаписи)";
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[2].Пустой() Тогда
		Отказ = Истина;	
		ТекстОшибки = НСтр("ru = 'Документом ""%1"" не было зафиксировано новых планируемых опозданий в точках маршрута'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументОснование);
		сткВозврат.ОписаниеОшибки	= ТекстОшибки;
	Иначе
		Выборка = РезультатЗапроса[2].Выбрать();
		Пока Выборка.Следующий() Цикл
			НовоеСобытие = Документы.упСобытиеМониторинга.СоздатьДокумент();
			НовоеСобытие.Дата = ТекущаяДатаСеанса();
			НовоеСобытие.ТипСобытия = Справочники.упТипыСобытийМониторинга.ПланируемоеОпозданиеВТочкеМаршрута;
			СписокПартнеров = "";
			РезультатПартнеры = РезультатЗапроса[3].Выгрузить();
			Отбор = Новый Структура;
    		Отбор.Вставить("Идентификатор", Выборка.Идентификатор);
    		Строки = РезультатПартнеры.НайтиСтроки(Отбор);
			Для каждого Стр Из Строки Цикл
				СписокПартнеров = СписокПартнеров + Стр.Партнер;
			КонецЦикла; 
			ТекстКомментария = НСтр("ru = 'При посещении адреса ""%1"" ожидается непопадание во временное окно (%2). Плановое прибытие ""%3"". Партнеры: %4 '");
			ТекстКомментария = СтрШаблон(ТекстКомментария, Выборка.АдресТочки, упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(Выборка.ВременноеОкноНачало, Выборка.ВременноеОкноОкончание),
			Выборка.ПлановоеПрибытие,СписокПартнеров);
			НовоеСобытие.Комментарий = ТекстКомментария;
			ЗаполнитьЗначенияСвойств(НовоеСобытие, Выборка);
			НовоеСобытие.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла; 
		сткВозврат.ДокументРезультат = НовоеСобытие;
	КонецЕсли; 
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

// Функция создает заявку на возврат груза.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗаявкуНаВозвратГруза(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);

	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	Отказ 		= Ложь;
	ТекстОшибки = "";
	МассивДокументов = Документы.упЗаявкаНаПеревозкуГруза.СформироватьЗаявкуНаВозвратГрузов(ДокументОснование,Истина,ТекстОшибки,Отказ);
	
	Если НЕ Отказ Тогда
		Если ТипЗнч(МассивДокументов) = ТИП("Массив") Тогда
			Если МассивДокументов.Количество() Тогда
				сткВозврат.Вставить("ДокументРезультат", МассивДокументов[0]);
			КонецЕсли;
		КонецЕсли;
	Иначе
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ТекстОшибки);
	КонецЕсли;
			
	Возврат сткВозврат;
	
КонецФункции

// Функция создает заявку на возврат тары.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗаявкуНаВозвратТары(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);

	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	
	Отказ = Ложь;
	ТекстОшибки = "";
	МассивДокументов = Документы.упЗаявкаНаПеревозкуГруза.СформироватьЗаявкуНаВозвратТары(ДокументОснование,Истина,ТекстОшибки,Отказ);
	
	Если НЕ Отказ Тогда
		Если ТипЗнч(МассивДокументов) = ТИП("Массив") Тогда
			Если МассивДокументов.Количество() Тогда
				сткВозврат.Вставить("ДокументРезультат", МассивДокументов[0]);
			КонецЕсли;
		КонецЕсли;
	Иначе
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ТекстОшибки);
	КонецЕсли;
			
	Возврат сткВозврат;
	
КонецФункции

// Функция автоматической передачи рейсов следующих цепей поставок "К выполнению", .
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПередатьСледующийВМультимодальнойПеревозкеРейсКВыполнению(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 		= Ложь;
	ОписаниеОшибки	= "";
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументОснование);

	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;	

	Если НЕ Отказ Тогда
		
		ТекущийСтатус	= упРаботаСоСтатусами.ПолучитьТекущийСтатус(ДокументОснование);
		Если ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполнено Тогда
			РезультатПроверки = упЗаданиеНаПеревозку.ПараметрыЗаданияВЦепиПоставок(ДокументОснование, Справочники.упГрузовыеМеста.ПустаяСсылка());
			Если РезультатПроверки.НомерВЦепиПоставок > 0 И РезультатПроверки.НомерВЦепиПоставок < РезультатПроверки.КрайнийНомер Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК Заявка
				|ПОМЕСТИТЬ вт_Заявки
				|ИЗ
				|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
				|ГДЕ
				|	упЗаданиеНаПеревозкуГруза.Ссылка = &Ссылка
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Ссылка
				|ИЗ
				|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК СтатусыЗаданийНаПеревозкуГруза
				|		ПО упЗаданиеНаПеревозкуГруза.Ссылка = СтатусыЗаданийНаПеревозкуГруза.Документ
				|ГДЕ
				|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза В
				|			(ВЫБРАТЬ
				|				вт_Заявки.Заявка КАК Заявка
				|			ИЗ
				|				вт_Заявки КАК вт_Заявки)
				|	И СтатусыЗаданийНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс)";
				
				Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
				МассивЗаданий = Новый Массив;
				ВыборкаЗадания = Запрос.Выполнить().Выбрать();
				Пока ВыборкаЗадания.Следующий() Цикл
					ТекущаяПроверка = упЗаданиеНаПеревозку.ПараметрыЗаданияВЦепиПоставок(ВыборкаЗадания.Ссылка, Справочники.упГрузовыеМеста.ПустаяСсылка());
					Если ТекущаяПроверка.НомерВЦепиПоставок = РезультатПроверки.НомерВЦепиПоставок+1 Тогда
						МассивЗаданий.Добавить(ВыборкаЗадания.Ссылка);
					КонецЕсли;	
				КонецЦикла;
				
				Если МассивЗаданий.Количество() Тогда
					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	упРейсЗадания.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.упРейс.Задания КАК упРейсЗадания
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК СтатусыРейсов
					|		ПО упРейсЗадания.Ссылка = СтатусыРейсов.Документ
					|ГДЕ
					|	упРейсЗадания.Задание В(&МассивЗаданий)
					|	И СтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.НазначеноТС)";
					
					Запрос.УстановитьПараметр("МассивЗаданий", МассивЗаданий);
					
					ВыборкаРейсов = Запрос.Выполнить().Выбрать();
					КоличествоРейсов = ВыборкаРейсов.Количество();
					Сч = 0;
					Пока ВыборкаРейсов.Следующий() Цикл
						РезультатПопытки = ВыполнитьПопыткуПередатьРейсКВыполнению(ВыборкаРейсов.Ссылка);
						Если НЕ РезультатПопытки.Успешно Тогда
							Сч = Сч+1;
							Событие = Нстр("ru = 'Ошибка при передаче рейс к выполнению'");
							ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,Метаданные.Документы.упРейс,ВыборкаРейсов.Ссылка, РезультатПопытки.ОписаниеОшибки);    
						КонецЕсли;
					КонецЦикла;
					Если КоличествоРейсов=Сч Тогда
						Отказ = Истина;
						ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Документ %1 не перевел рейсов к выполнению в количестве %2'"),ДокументОснование, Формат(Сч, "ЧГ=0"));
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				Отказ = Истина;
				ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Документ %1 в статусе %2, не участвует в цепи поставок или не последний в цепи'"),ДокументОснование, ТекущийСтатус);
			КонецЕсли;
		Иначе
			Отказ = Истина;
			ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Документ %1 в статусе %2, должен быть статус - Выполнено'"),ДокументОснование, ТекущийСтатус);
		КонецЕсли;
		
	КонецЕсли;	
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции // ОбработатьСобытие_ПередатьСледующийВМультимодальнойПеревозкеРейсКВыполнению()

// Функция оптимизирует маршрут рейса.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ОптимизироватьМаршрут(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат	= Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ 		= Ложь;
	ОписаниеОшибки	= "";
	
	текРейсСсылка = Неопределено;
	ОптимизацияМаршрутаПроизведена = Ложь;
	ОптимизацияМаршрутаСобытиеМониторинга = Ложь;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упРейс") Тогда
		текРейсСсылка = ДокументОснование;
		ОптимизацияМаршрутаПроизведена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ОптимизацияМаршрута");
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПутевойЛист") Тогда
		текРейсСсылка	=  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");	
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПрохождениеТочки") Тогда
		текРейсСсылка	=  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упКорректировкаРейса") Тогда
		текРейсСсылка	=  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Рейс");
		ОптимизацияМаршрутаПроизведена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ОптимизацияМаршрута");
		Если ОптимизацияМаршрутаПроизведена Тогда
			// Для исключения зацикливания, документ с таким флагом не обрабатываем.
			сткВозврат.Вставить("Отказ", Ложь);
			сткВозврат.Вставить("Успешно", Истина);
			сткВозврат.Вставить("ДокументРезультат", ДокументОснование);
			Возврат сткВозврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упСобытиеМониторинга") Тогда
		ОптимизацияМаршрутаСобытиеМониторинга = Истина;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упСобытиеМониторинга.Ссылка КАК Ссылка,
		|	упСобытиеМониторинга.Рейс КАК Рейс,
		|	упСобытиеМониторинга.Рейс.ОптимизацияМаршрута КАК ОптимизацияМаршрута,
		|	упСобытиеМониторинга.Трекер КАК Трекер,
		|	упСобытиеМониторинга.ИдентификаторЗадачи КАК ИдентификаторЗадачи
		|ИЗ
		|	Документ.упСобытиеМониторинга КАК упСобытиеМониторинга
		|ГДЕ
		|	упСобытиеМониторинга.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыСобытияМониторинга.Новое)
		|	И НЕ упСобытиеМониторинга.Рейс = ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)
		|	И упСобытиеМониторинга.ТипСобытия = ЗНАЧЕНИЕ(Справочник.упТипыСобытийМониторинга.ЗапросНаОптимизациюМаршрута)
		|	И упСобытиеМониторинга.Ссылка = &Ссылка";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			сткВозврат.Вставить("Отказ", Ложь);
			сткВозврат.Вставить("Успешно", Истина);
			сткВозврат.Вставить("ДокументРезультат", ДокументОснование);
			Возврат сткВозврат;
		Иначе
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				текРейсСсылка = Выборка.Рейс;
				ОптимизацияМаршрутаПроизведена = Выборка.ОптимизацияМаршрута;
				Прервать;
			КонецЦикла;
			ЗаголовокУведомления = НСтр("ru = 'Ответ на запрос об оптимизации маршрута'");
		КонецЕсли;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упИзмененияРейсов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", текРейсСсылка);
	КонецЕсли; 
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", текРейсСсылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", текРейсСсылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упРейс");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", текРейсСсылка);
	
	Если ОптимизацияМаршрутаСобытиеМониторинга Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упСобытиеМониторинга");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументОснование);
	КонецЕсли; 
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки;

	Если НЕ Отказ Тогда
		Если ОптимизацияМаршрутаПроизведена Тогда
			РейсОбъект = текРейсСсылка.ПолучитьОбъект();
			РейсОбъект.ОптимизацияМаршрута = Ложь;
			Попытка
				РейсОбъект.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Не удалось записать документ (%1), ошибка при записи: %2.'", текРейсСсылка, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			КонецПопытки;
		Иначе
			Статус = Документы.упРейс.ПолучитьСтатус(текРейсСсылка);
			Если Статус = Перечисления.упСтатусыРейса.Отменен
				ИЛИ Статус = Перечисления.упСтатусыРейса.Выполнен
				ИЛИ Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
				ИЛИ Статус = Перечисления.упСтатусыРейса.Завершен Тогда
				Отказ = Истина;
				ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Изменить маршрут для рейса %1 в статусе: %2 нельзя.'", текРейсСсылка, Статус));
			Иначе
				НастройкаОптимизацииМаршрута = сткДанныеТипаСобытия.НастройкаОптимизацииМаршрута;
				ИменаРеквизитов = "МетодРешенияЗК, АлгоритмОптимизации, ВариантОптимизацииМаршрута, УменьшатьВремяОжидания, ПространственныйВес, ВременнойВес, ВесСрочности, КонтролироватьНарушениеВременныхОкон, ДопустимоеОтклонениеОтВременногоОкна";
				СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОптимизацииМаршрута, ИменаРеквизитов);
				УменьшатьВремяОжидания = СтруктураРеквизитов.УменьшатьВремяОжидания;
				КонтролироватьНарушениеВременныхОкон = СтруктураРеквизитов.КонтролироватьНарушениеВременныхОкон;
				ДопустимоеОтклонениеОтВременногоОкна = СтруктураРеквизитов.ДопустимоеОтклонениеОтВременногоОкна;
				Если ЗначениеЗаполнено(СтруктураРеквизитов.АлгоритмОптимизации)
					И НЕ ЗначениеЗаполнено(СтруктураРеквизитов.МетодРешенияЗК) Тогда
					Отказ = Истина;
					ОписаниеОшибки = НСтр("ru = 'Поставляемый алгоритм решения задачи коммивояжера не поддерживается.'");
				КонецЕсли;				
				Если НЕ Отказ Тогда
					РежимКорректировки = Ложь;
					Если Статус = Перечисления.упСтатусыРейса.КВыполнению
						ИЛИ Статус = Перечисления.упСтатусыРейса.Выполняется Тогда
						РежимКорректировки = Истина;
					КонецЕсли;
					НастройкаОптимизации = СтруктураРеквизитов;
					НастройкаОптимизации.Вставить("РежимКорректировки", РежимКорректировки);
					АдресВременногоХранилища = Неопределено;
					РезультатИзменения = упКомбинаторнаяОптимизация.ИзменитьПорядокТочекПоРейсу(текРейсСсылка, НастройкаОптимизации, АдресВременногоХранилища);
					
					Если РезультатИзменения = Неопределено Тогда
						ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Порядок точек по рейсу %1 не изменен.'"), текРейсСсылка);
						ЗаписьЖурналаРегистрации("ОптмизироватьМаршрут", УровеньЖурналаРегистрации.Ошибка, ,текРейсСсылка, ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					Иначе
						// Выполним запись данных.
						Если РежимКорректировки Тогда
							КорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
							КорректировкаМаршрутаОбъект.Период = ТекущаяДатаСеанса();
							КорректировкаМаршрутаОбъект.Рейс = текРейсСсылка;
							КорректировкаМаршрутаОбъект.Инициализация(Новый Структура);
							
							мсвРешений = РезультатИзменения.МассивТочек;
							Маршрут = КорректировкаМаршрутаОбъект.Маршрут;
							Работы = КорректировкаМаршрутаОбъект.Работы;
							// Сместим точки.
							ИндексПервойНеПройденнойТочки = РезультатИзменения.ИндексПервойНеПройденнойТочки;
							Сч = ИндексПервойНеПройденнойТочки;
							Для каждого Точка Из мсвРешений Цикл
								ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", Точка));
								Если ИскомыеСтроки.Количество() Тогда
									НайденнаяСтрока = ИскомыеСтроки[0];
									ИндексСтроки = Маршрут.Индекс(НайденнаяСтрока);
									Маршрут.Сдвинуть(ИндексСтроки, Сч - ИндексСтроки);
									НайденнаяСтрока.СтрокаНомер = Сч + 1;
								КонецЕсли;
								Сч = Сч + 1;
							КонецЦикла; 
														
							Структура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текРейсСсылка, "ВидПеревозки");
							ВидПеревозки = Структура.ВидПеревозки;
							КоэффициентРасчетаВремениВПути = 1;
							упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(Маршрут, Работы, ИндексПервойНеПройденнойТочки,,,,,,,,,,,ВидПеревозки, КоэффициентРасчетаВремениВПути,, текРейсСсылка);	
							МаршрутМассив = Неопределено;
							РаботыМассив = Неопределено;
							Если УменьшатьВремяОжидания ИЛИ КонтролироватьНарушениеВременныхОкон Тогда
								МаршрутМассив = Маршрут;
								РаботыМассив = Работы;
								//Отбор = Новый Структура("Пройдена, Отменена", Ложь, Ложь);
								//МаршрутМассив = Маршрут.НайтиСтроки(Отбор);
								//Отбор = Новый Структура("Выполнена, Отменена", Ложь, Ложь);
								//РаботыМассив = Работы.НайтиСтроки(Отбор);
							КонецЕсли;
							Если УменьшатьВремяОжидания Тогда
								упРейсКлиентСервер.СократитьВремяОжиданияВРейсе(МаршрутМассив, РаботыМассив);
							КонецЕсли;
							ПопадаетВоВременноеОкно = Истина;
							Если КонтролироватьНарушениеВременныхОкон Тогда
								ПопадаетВоВременноеОкно = упСервисныеФункцииКлиентСервер.ВыполнитьКонтрольНарушенияВременныхОкон(МаршрутМассив, РаботыМассив, ДопустимоеОтклонениеОтВременногоОкна);
								Если НЕ ПопадаетВоВременноеОкно Тогда 
									ОписаниеОшибки = НСтр(СтрШаблон("ru = 'При оптимизации маршрута (режим корректировки), после пересчета (%1): произошла ошибка, произошло нарушение во временных окнах.'", текРейсСсылка));
									ЗаписьЖурналаРегистрации("ОптимизироватьМаршрут", УровеньЖурналаРегистрации.Ошибка, ,текРейсСсылка, ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
								КонецЕсли;
							КонецЕсли;	
							Если ПопадаетВоВременноеОкно Тогда 
								КорректировкаРейсаОбъект = Документы.упКорректировкаРейса.СоздатьДокумент();
								КорректировкаРейсаОбъект.ИсточникСозданияОбъекта = Перечисления.упИсточникиСозданияОбъекта.Событие;
								КорректировкаРейсаОбъект.Дата = ТекущаяДатаСеанса();
								КорректировкаРейсаОбъект.Рейс = текРейсСсылка;		
								КорректировкаРейсаОбъект.Автор = Пользователи.ТекущийПользователь();
								КорректировкаРейсаОбъект.ОптимизацияМаршрута = Истина;
								КорректировкаРейсаОбъект.ДополнительныеСвойства.Вставить("КорректировкаРейса", КорректировкаМаршрутаОбъект);
								
								Если ОптимизацияМаршрутаСобытиеМониторинга Тогда
									КорректировкаРейсаОбъект.СобытиеЗапросНаОптимизациюМаршрута = ДокументОснование;
									
									сткУведомление = Новый Структура;
									сткУведомление.Вставить("Заголовок"       , ЗаголовокУведомления);
									сткУведомление.Вставить("ТекстУведомления", НСтр("ru = 'Маршрут оптимизирован.'"));
									КорректировкаРейсаОбъект.ДополнительныеСвойства.Вставить("УведомлениеМобильныйКлиент", сткУведомление);
								КонецЕсли;
								
								Попытка
									КорректировкаРейсаОбъект.Записать(РежимЗаписиДокумента.Проведение);
								Исключение
									ИнформацияОбОшибке = ИнформацияОбОшибке();
									ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
									Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда 
										ТекстСообщения = ОписаниеОшибки();
									КонецЕсли;
									ОписаниеОшибки = ТекстСообщения;
									Отказ = Истина;
								КонецПопытки;
							КонецЕсли;
							
						Иначе
							тзМаршрут = Неопределено;
							тзРаботы = Неопределено;
							ДополнительныеСвойства = Новый Структура();
							ДополнительныеСвойства.Вставить("ПересчетМаршрута", РезультатИзменения);
							ДополнительныеСвойства.Вставить("УменьшатьВремяОжидания", УменьшатьВремяОжидания);
							упРейс.ОбработатьПересчетМаршрута(тзМаршрут, тзРаботы, ДополнительныеСвойства, текРейсСсылка, 1);
							
							ПопадаетВоВременноеОкно = Истина;
							Если КонтролироватьНарушениеВременныхОкон Тогда 
								ПопадаетВоВременноеОкно = упСервисныеФункцииКлиентСервер.ВыполнитьКонтрольНарушенияВременныхОкон(тзМаршрут, тзРаботы, ДопустимоеОтклонениеОтВременногоОкна);
								Если НЕ ПопадаетВоВременноеОкно Тогда 
									ОписаниеОшибки = НСтр(СтрШаблон("ru = 'При оптимизации маршрута (изменение рейса), после пересчета (%1): произошла ошибка, произошло нарушение во временных окнах.'", текРейсСсылка));
									ЗаписьЖурналаРегистрации("ОптмизироватьМаршрут", УровеньЖурналаРегистрации.Ошибка, ,текРейсСсылка, ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
								КонецЕсли;
							КонецЕсли;
							Если ПопадаетВоВременноеОкно Тогда 
								РейсОбъект = текРейсСсылка.ПолучитьОбъект();
								РейсОбъект.ДополнительныеСвойства.Вставить("МаршрутРаботыПодготовлены", Истина);
								РейсОбъект.ДополнительныеСвойства.Вставить("Маршрут", тзМаршрут);
								РейсОбъект.ДополнительныеСвойства.Вставить("МодифицированМаршрут", Истина);
								РейсОбъект.ДополнительныеСвойства.Вставить("Работы", тзРаботы);
								РейсОбъект.ДополнительныеСвойства.Вставить("МодифицированыРаботы", Истина);
								Если ОптимизацияМаршрутаСобытиеМониторинга Тогда
									сткУведомление = Новый Структура;
									сткУведомление.Вставить("Заголовок"       , ЗаголовокУведомления);
									сткУведомление.Вставить("ТекстУведомления", НСтр("ru = 'Маршрут оптимизирован.'"));
									КорректировкаРейсаОбъект.ДополнительныеСвойства.Вставить("УведомлениеМобильныйКлиент", сткУведомление);
								КонецЕсли;

								Попытка
									РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
								Исключение
									Отказ = Истина;
									ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Не удалось оптимизировать маршрут (%1): %2.'", текРейсСсылка, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
								КонецПопытки;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//Если НЕ Отказ Тогда
	Если ОптимизацияМаршрутаСобытиеМониторинга Тогда
		Если Отказ Тогда
			упУведомленияМобильныйКлиент = РегистрыСведений.упУведомленияМобильныйКлиент.СоздатьМенеджерЗаписи();
			упУведомленияМобильныйКлиент.Трекер              = Выборка.Трекер;
			упУведомленияМобильныйКлиент.Рейс                = Выборка.Рейс;
			упУведомленияМобильныйКлиент.ИдентификаторЗадачи = Выборка.ИдентификаторЗадачи;
			упУведомленияМобильныйКлиент.Прочитать();
			
			упУведомленияМобильныйКлиент.Трекер              = Выборка.Трекер;
			упУведомленияМобильныйКлиент.Рейс                = Выборка.Рейс;
			упУведомленияМобильныйКлиент.ИдентификаторЗадачи = Выборка.ИдентификаторЗадачи;
			упУведомленияМобильныйКлиент.Отправлено = Ложь;
			упУведомленияМобильныйКлиент.Заголовок = ЗаголовокУведомления;
			упУведомленияМобильныйКлиент.ТекстУведомления = НСтр("ru = 'Маршрут не может быть оптимизирован по ранее отправленному запросу.'");
			упУведомленияМобильныйКлиент.Записать();
		КонецЕсли; 
		СобытиеМониторингаОбъект = ДокументОснование.ПолучитьОбъект();
		СобытиеМониторингаОбъект.Статус = Перечисления.упСтатусыСобытияМониторинга.Обработано;
		Попытка
			СобытиеМониторингаОбъект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Не удалось записать документ (%1), ошибка при записи: %2.'", ДокументОснование, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
		КонецПопытки;
	КонецЕсли;
	//КонецЕсли;
	
	Если Отказ Тогда
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
	Иначе
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЕсли;

	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	КонецЕсли;

	Возврат сткВозврат;

КонецФункции // ОбработатьСобытие_ОптимизироватьМаршрут()

// Функция переводит запрос условия перевозк статус "Обработан".
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ПередатьЗапросУсловийПеревозкиВОбработан(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)

	УстановитьПривилегированныйРежим(Истина);	
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
		
	ДокументОбъект = ДокументОснование.ПолучитьОбъект();
	ДокументОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗапросаУсловийПеревозки.Обработан);
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОписаниеОшибки = ТекстОшибки;
	КонецПопытки;
			
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
	КонецЕсли;
		
	Возврат сткВозврат;

КонецФункции 

// Функция создает заявку на перевозку груза.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗаявкуНаПеревозкуГруза(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК упСтатусыЗаявокНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|	ПО упСтатусыЗаявокНаПеревозкуГруза.Документ = упЗаявкаНаПеревозкуГруза.Ссылка
	|ГДЕ ИСТИНА
	|	И НЕ (упЗаявкаНаПеревозкуГруза.ПометкаУдаления)
	|	И упСтатусыЗаявокНаПеревозкуГруза.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)
	|	И упЗаявкаНаПеревозкуГруза.ДокументОснование = &Ссылка
	|";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ДокументОбъект = Документы.упЗаявкаНаПеревозкуГруза.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Заполнить(ДокументОснование);
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	Иначе	
	     Отказ = Истина;
		ОписаниеОшибки = Нстр("ru = 'По запросу условий превозки уже создана заявка на перевозку груза'");
	КонецЕсли;
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		сткВозврат.ДокументРезультат = ДокументОбъект.Ссылка;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

// Функция создаёт задачу на подтверждение адреса (доступно при использовании бизнес-процессов).
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗадачуНаПодтверждениеАдреса(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	НачатьТранзакцию();
	
	сткРеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "ТипСобытия, АдресТочки, Широта, Долгота");
	
	БизнесПроцессСсылка = Неопределено;
	Попытка
		
		Если сткРеквизитыОснования.ТипСобытия <> Справочники.упТипыСобытийМониторинга.ПрибытиеВТочкуМаршрута Тогда
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Тип события мониторинга у документа должен быть ""Прибытие в точку""'");
		ИначеЕсли НЕ ЗначениеЗаполнено(сткРеквизитыОснования.АдресТочки) Тогда	
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'В событии мониторинга не заполнен адрес точки'");
		ИначеЕсли НЕ ЗначениеЗаполнено(сткРеквизитыОснования.Широта) Тогда	
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'В событии мониторинга не заполнена широта'");	
		ИначеЕсли НЕ ЗначениеЗаполнено(сткРеквизитыОснования.Долгота) Тогда	
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'В событии мониторинга не заполнена долгота'");		
		КонецЕсли;
		
		
		Если НЕ Отказ Тогда
			
			АдресОбъект = сткРеквизитыОснования.АдресТочки.ПолучитьОбъект();		
			АдресОбъект.Подтвержден = Ложь;
			АдресОбъект.Записать();
		
			ТекущаяДата = ТекущаяДатаСеанса();
			
			БизнесПроцесс = БизнесПроцессы.упПодтверждениеАдреса.СоздатьБизнесПроцесс();
			БизнесПроцесс.Дата = ТекущаяДата;
			БизнесПроцесс.Заполнить(ДокументОснование);
			БизнесПроцесс.Важность = ТипСобытия.Важность;
			БизнесПроцесс.Исполнитель = ТипСобытия.Исполнитель;
			БизнесПроцесс.СрокИсполнения = ТекущаяДата;
			
			БизнесПроцесс.Записать();
			
			БизнесПроцессСсылка = БизнесПроцесс.Ссылка;
			
			БизнесПроцесс.Старт();

		КонецЕсли;		
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОписаниеОшибки	= ТекстОшибки;
	КонецПопытки;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		сткВозврат.ДокументРезультат = БизнесПроцессСсылка;
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция создаёт задачу на согласование изменений маршрута (доступно при использовании бизнес-процессов).
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗадачуНаСогласованиеИзмененийМаршрута(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	НачатьТранзакцию();
	
	сткРеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "ТипСобытия, Статус");
	
	БизнесПроцессСсылка = Неопределено;
	Попытка
		
		Если Истина
			И сткРеквизитыОснования.Статус <> Перечисления.упСтатусыСобытияМониторинга.Новое 
		Тогда
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Статус события мониторинга должно быть ""Новое""'");
		КонецЕсли;
		
		Если Истина
			И Не Отказ
			И сткРеквизитыОснования.ТипСобытия <> Справочники.упТипыСобытийМониторинга.ЗапросНаДосрочноеЗавершениеРейса 
			И сткРеквизитыОснования.ТипСобытия <> Справочники.упТипыСобытийМониторинга.ЗапросНаДосрочноеУбытиеИзТочкиМаршрута 
			И сткРеквизитыОснования.ТипСобытия <> Справочники.упТипыСобытийМониторинга.ЗапросНаНазначениеТочкиСледующейПоМаршруту
		Тогда
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Тип события мониторинга у документа должен быть одним из перчисленных ""Запрос на досрочное завершение рейса"", 
			|""Запрос на досрочное убытие из точки маршрута"", ""Запрос на назначение точки следующей по маршруту""'");
		КонецЕсли;
				
		Если НЕ Отказ Тогда
	
			ТекущаяДата = ТекущаяДатаСеанса();
			
			БизнесПроцесс = БизнесПроцессы.упСогласованиеИзмененийМаршрута.СоздатьБизнесПроцесс();
			БизнесПроцесс.Дата = ТекущаяДата;
			БизнесПроцесс.Заполнить(ДокументОснование);
			БизнесПроцесс.Важность = ТипСобытия.Важность;
			БизнесПроцесс.Исполнитель = ТипСобытия.Исполнитель;
			БизнесПроцесс.СрокИсполнения = ТекущаяДата;
			
			БизнесПроцесс.Записать();
			
			БизнесПроцессСсылка = БизнесПроцесс.Ссылка;
			
			БизнесПроцесс.Старт();

		КонецЕсли;		
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОписаниеОшибки	= ТекстОшибки;
	КонецПопытки;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		сткВозврат.ДокументРезультат = БизнесПроцессСсылка;
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция списывает планы по заданиям на перевозку по рейсу
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СписатьПланыПоЗаданиямНаПеревозку(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	мсвСписаниеПлановЗаданий = Документы.упРейс.СформироватьСписаниеПлановПоЗаданиям(ДокументОснование, ОписаниеОшибки, Отказ);
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		Если Истина
			И мсвСписаниеПлановЗаданий <> Неопределено
			И мсвСписаниеПлановЗаданий.Количество() > 0 
		Тогда
			сткВозврат.ДокументРезультат = мсвСписаниеПлановЗаданий[0];	
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция подбирает и добавляет точки по заданным правилам
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_ВключитьКонтрольныеТочкиВРейсПоПравилам(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	РейсСсылка = ДокументОснование;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упРейс");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", РейсСсылка);
					
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", РейсСсылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", РейсСсылка);
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", РейсСсылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПримененныеПравилаВключенияКонтрольныхТочек");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", РейсСсылка);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Ошибка при установке блокировки на ресурсы для документа %1'"), РейсСсылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;
	КонецПопытки;
	
	Если Не Отказ Тогда
		
		СтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(РейсСсылка);
		
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РейсСсылка, Новый Структура("ВидПеревозкиСпособПрохожденияТочекМаршрута","ВидПеревозки.СпособПрохожденияТочекМаршрута"));
		
		СпособПрохожденияТочекМаршрута = сткРеквизиты.ВидПеревозкиСпособПрохожденияТочекМаршрута;
			
		РежимКорректировки = упПрохождениеТочкиКлиентСервер.РежимКорректировки(СпособПрохожденияТочекМаршрута, СтатусРейса.Статус);
		
		упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();
		
		ДатаПримененияПравил = ТекущаяДатаСеанса();
		
		Если РежимКорректировки Тогда
			ДокументОбъект = Документы.упКорректировкаРейса.СоздатьДокумент();
			ДокументОбъект.ИсточникСозданияОбъекта = Перечисления.упИсточникиСозданияОбъекта.Событие;
			ДокументОбъект.Дата = ДатаПримененияПравил;
			ДокументОбъект.Рейс = РейсСсылка;		
			ДокументОбъект.Автор = Пользователи.ТекущийПользователь();
		Иначе	
			ДокументОбъект = РейсСсылка.ПолучитьОбъект();	
			упКорректировкаРейсаОбъект.Ссылка = РейсСсылка;
		КонецЕсли;
	
		упКорректировкаРейсаОбъект.Рейс = РейсСсылка;
		упКорректировкаРейсаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
		
		Если Не ЗначениеЗаполнено(упКорректировкаРейсаОбъект.Период)  Тогда
			упКорректировкаРейсаОбъект.Период = ТекущаяДатаСеанса();
		КонецЕсли;
		
		сткПараметрыЗаполнения = Новый Структура();
		сткПараметрыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками"));
		упКорректировкаРейсаОбъект.Инициализация(сткПараметрыЗаполнения);
		упКорректировкаРейсаОбъект.ПодобратьКонтрольныеТочкиМаршрута();
		Если упКорректировкаРейсаОбъект.ПодобранныеКонтрольныеТочки.Количество() > 0 Тогда
			
			ДополнительныеПараметры = Новый Структура("СпланироватьЗадачиПоМаршруту", Истина);
			ДополнительныеПараметры.Вставить("МестоВызова", "КорректировкаРейса");
			ДополнительныеПараметры.Вставить("ПримененныеПравилаВключенияКонтрольныхТочек", Новый СписокЗначений);
			упКорректировкаРейсаОбъект.ДобавитьПодобранныеКонтрольныеТочкиВМаршрут(ДополнительныеПараметры);
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПримененныеПравилаВключенияКонтрольныхТочек", ДополнительныеПараметры.ПримененныеПравилаВключенияКонтрольныхТочек);
			
			Если Не Отказ Тогда
				ДокументОбъект.ДополнительныеСвойства.Вставить("КорректировкаРейса", упКорректировкаРейсаОбъект);
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Не удалось записать документ %1. Произошла ошибка: %2'"), РейсСсылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					Отказ = Истина;
				КонецПопытки;
			КонецЕсли;
		Иначе	
			ИмяСобытия = Нстр("ru = 'Подбор контрольных точек'");
			ТекстСообщения = Нстр("ru = 'Не удалось подобрать контрольные точки для %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, РейсСсылка);
			ЗаписьЖурналаРегистрации(ИмяСобытия,УровеньЖурналаРегистрации.Предупреждение,,,ТекстСообщения);
		КонецЕсли;
			
	КонецЕсли;
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();		
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		сткВозврат.ДокументРезультат = ДокументОбъект.Ссылка;	
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция корректирует план перевозки по зафиксированным прерываниям грузовых потоков
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СкорректироватьПланПеревозки(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	РейсСсылка = ДокументОснование;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упРейс");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", РейсСсылка);
					
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", РейсСсылка);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Ошибка при установке блокировки на ресурсы для документа %1'"), РейсСсылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;
	КонецПопытки;
	
	Если Не Отказ Тогда
		мсвКорректировкиЭтаповПеревозки = Документы.упКорректировкаЭтаповПеревозки.ОбработатьПрерыванияГрузовыхПотоков(РейсСсылка, ОписаниеОшибки, Отказ); 		
	КонецЕсли;
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();		
	КонецЕсли;
	
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		Если мсвКорректировкиЭтаповПеревозки.Количество() > 0 Тогда
			сткВозврат.ДокументРезультат = мсвКорректировкиЭтаповПеревозки[0];		
		КонецЕсли;
	КонецЕсли;
		
	Возврат сткВозврат;
	
КонецФункции

// Функция создает заявку на перевозку контейнера.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипСобытия - ПеречислениеСсылка - Ссылка на элемент.
//	сткДанныеТипаСобытия - Структура - Данные.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОбработатьСобытие_СоздатьЗаявкуНаПеревозкуКонтейнера(ДокументОснование, ТипСобытия, сткДанныеТипаСобытия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	сткВозврат = Новый Структура("ДокументРезультат, Успешно, ОписаниеОшибки", Неопределено, Истина, "");
	Отказ = Ложь;
	ОписаниеОшибки	= "";
		
	ЗаявкаНаПеревозкуГруза = Документы.упЗаявкаНаПеревозкуГруза.СформироватьЗаявкуНаПеревозкуГрузаДляОперацииСКонтейнером(ДокументОснование, ОписаниеОшибки, Отказ); 
		
	Если Отказ Тогда
		сткВозврат.Вставить("Успешно", Ложь);
		сткВозврат.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	Иначе
		сткВозврат.ДокументРезультат = ЗаявкаНаПеревозкуГруза;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции

#КонецОбласти 

#Область РегламентныеОперации
 
// Процедура выполняет регламентную Операцию.
//
// Параметры:
//	Операция - СправочникСсылка.упРегламентныеОперации - Ссылка на элемент.
//
Процедура ВыполнитьРегламентныеОперации(Операция = Неопределено) Экспорт

	Запрос = Новый Запрос;
	
	ТекстЗапроса  =
	"ВЫБРАТЬ
	|	РегламентныеОперации.Ссылка,
	|	РегламентныеОперации.Наименование,
	|	РегламентныеОперации.Активно,
	|	РегламентныеОперации.ПроизвольныйАлгоритм
	|ИЗ
	|	Справочник.упРегламентныеОперации КАК РегламентныеОперации
	|ГДЕ
	|	НЕ РегламентныеОперации.ПометкаУдаления
	|	И РегламентныеОперации.Активно
	|	И &_УсловиеОтбора";
	
	УсловиеОперация = НЕ Операция = Неопределено;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&_УсловиеОтбора", ?(УсловиеОперация, "РегламентныеОперации.Ссылка = &Ссылка", "НЕ РегламентныеОперации.ИспользоватьРегламентноеЗадание"));

	Запрос.Текст = ТекстЗапроса;								
	Запрос.Параметры.Вставить("Ссылка", Операция);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Алгоритм = Выборка.ПроизвольныйАлгоритм;
		Попытка
			ВыполнитьАлгоритм(Алгоритм);
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации("Регламентные операции:", УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упРегламентныеОперации, Выборка.Ссылка, ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
		
КонецПроцедуры
 
// Процедура выполняет регламентную Операцию.
//
// Параметры:
//	Алгоритм - Строка - Текст алгоритма.
//
Процедура ВыполнитьАлгоритм(Алгоритм)

	УстановитьБезопасныйРежим("TMS3");
	
	Выполнить(Алгоритм);
			
	УстановитьБезопасныйРежим(Ложь);
		
КонецПроцедуры

#КонецОбласти 

#Область ПрисоединениеФайла

// Процедура регистрирует выполнение документов.
//
// Параметры:
//	Источник - ПрисоединенныеФайлы - набор записей.
//	Отказ - Булево - Применить изменения объекта.
//
Процедура ПрисоединениеФайлаПриЗаписи(Источник, Отказ) Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Источник.Количество() Тогда
		МассивПрисоединенныйФайл = Источник.ВыгрузитьКолонку("ПрисоединенныйФайл");
		МассивДокументов = Новый Массив;
		Для Каждого ЭлементПрисоединенный Из МассивПрисоединенныйФайл Цикл
			Если ЗначениеЗаполнено(ЭлементПрисоединенный.ВладелецФайла) Тогда
				ОбъектМетаданных = ЭлементПрисоединенный.ВладелецФайла.ПолучитьОбъект();
				Если ОбщегоНазначения.ЭтоДокумент(ОбъектМетаданных.Метаданные()) Тогда
					Структура = Новый Структура();
					Структура.Вставить("Документ",ЭлементПрисоединенный.ВладелецФайла);
					Структура.Вставить("Ссылка",ЭлементПрисоединенный);
					МассивДокументов.Добавить(Структура);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
		
		Для Каждого ЭлементСтруктура Из МассивДокументов Цикл
			ТипДействия = Перечисления.упТипыДействийСобытий.ПрисоединениеФайла;
			Запись = РегистрыСведений.упЗаписанныеДокументы.СоздатьМенеджерЗаписи();
			Запись.Документ        = ЭлементСтруктура.Документ;
			Запись.ТипДействия     = ТипДействия;
			Запись.Прочитать();
			
			Запись.Обработан       = Ложь;
			Запись.ДатаОбработки   = '00010101';
			Запись.Документ        = ЭлементСтруктура.Документ;
			Запись.ТипДействия     = ТипДействия;
			Запись.ДатаРегистрации = ТекущаяДатаСеанса();
			Запись.ПрисоединенныеФайлы = ЭлементСтруктура.Ссылка;
			Запись.Записать(Истина);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура регистрирует выполнение документов.
//
// Параметры:
//	Источник - ПрисоединенныеФайлыОбъект - объект определяемого типа ПрисоединенныйФайлОбъект.
//	Отказ - Булево - Применить изменения объекта.
//
Процедура ПрисоединениеФайлаОпределяемыйТипПриЗаписи(Источник, Отказ) Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Метаданные.ОпределяемыеТипы.ПрисоединенныйФайлОбъект.Тип.СодержитТип(ТипЗнч(Источник)) И ОбщегоНазначения.ЭтоДокумент(Источник.ВладелецФайла.Метаданные()) Тогда           
		МассивДокументов = Новый Массив;
		Структура = Новый Структура();
		Структура.Вставить("Документ",Источник.ВладелецФайла);
		Структура.Вставить("Ссылка",Источник.Ссылка);
		МассивДокументов.Добавить(Структура);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упЗаписанныеДокументы.Обработан
		|ИЗ
		|	РегистрСведений.упЗаписанныеДокументы КАК упЗаписанныеДокументы
		|ГДЕ
		|	упЗаписанныеДокументы.Документ = &Документ
		|	И упЗаписанныеДокументы.ТипДействия = &ТипДействия
		|	И упЗаписанныеДокументы.ПрисоединенныеФайлы = &ПрисоединенныеФайлы";
		
		Для Каждого ЭлементСтруктура Из МассивДокументов Цикл
			ТипДействия = Перечисления.упТипыДействийСобытий.ПрисоединениеФайла;
			
			Запрос.УстановитьПараметр("Документ", ЭлементСтруктура.Документ);
			Запрос.УстановитьПараметр("ПрисоединенныеФайлы", ЭлементСтруктура.Ссылка);
			Запрос.УстановитьПараметр("ТипДействия", ТипДействия);
			
			Выборка = Запрос.Выполнить();
			
			Если НЕ Выборка.Пустой() Тогда           
				// Не будем записывать уже созданные записи;
				Продолжить;
			КонецЕсли;
			
			Запись = РегистрыСведений.упЗаписанныеДокументы.СоздатьМенеджерЗаписи();
			Запись.Документ        = ЭлементСтруктура.Документ;
			Запись.ТипДействия     = ТипДействия;
			Запись.Прочитать();
			
			Запись.Обработан       = Ложь;
			Запись.ДатаОбработки   = '00010101';
			Запись.Документ        = ЭлементСтруктура.Документ;
			Запись.ТипДействия     = ТипДействия;
			Запись.ДатаРегистрации = ТекущаяДатаСеанса();
			Запись.ПрисоединенныеФайлы = ЭлементСтруктура.Ссылка;
			Запись.Записать(Истина);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ПрисоединениеФайлаОпределяемыйТипПриЗаписи()
	
#КонецОбласти 