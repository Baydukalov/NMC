
#Область ПрограммныйИнтерфейс

// Процедура возвращает терминал.
//
// Параметры:
//  ИдентификаторУстройства - Строка - Код датчика.
//
// Возвращаемое значение:
//   СправочникСсылка.упМобильныеУстройстваИТрекеры - Ссылка на элемент.
//
Функция НайтиТерминал(ИдентификаторУстройства) Экспорт
	
	Возврат Справочники.упМобильныеУстройстваИТрекеры.НайтиПоРеквизиту("ИдентификаторУстройства", ИдентификаторУстройства);
	
КонецФункции

// Процедура возвращает датчик.
//
// Параметры:
//  КодДатчика - Строка - Код датчика.
//	Модель - СправочникСсылка - Ссылка на элемент.
//	Представление - Строка - Представление датчика.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция НайтиДатчик(КодДатчика, Модель, Представление = Неопределено) Экспорт
	
	п_КодДатчика = КодДатчика;
	Если ТипЗнч(КодДатчика)=ТИП("Число")Тогда
		п_КодДатчика = Формат(КодДатчика,"ЧРГ=' '; ЧН=0; ЧГ=0");
	ИначеЕсли НЕ ТипЗнч(КодДатчика)=ТИП("Строка")Тогда
		п_КодДатчика = СокрЛП(КодДатчика);
	КонецЕсли;
	
	Результат = Новый Структура("Датчик, КалибровочныйГрафик, Назначение", Справочники.упДатчики.ПустаяСсылка(), Справочники.упКалибровочныеГрафики.ПустаяСсылка(), Справочники.упНазначенияДатчиков.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМоделиТрекеровДатчики.Датчик,
	|	упМоделиТрекеровДатчики.КалибровочныйГрафик,
	|	упМоделиТрекеровДатчики.Датчик.Назначение КАК Назначение,
	|	упМоделиТрекеровДатчики.НомерДатчика КАК Код
	|ИЗ
	|	Справочник.упМоделиТрекеров.Датчики КАК упМоделиТрекеровДатчики
	|ГДЕ
	|	упМоделиТрекеровДатчики.Ссылка = &Модель
	|	И упМоделиТрекеровДатчики.НомерДатчика = &КодДатчика
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМоделиТрекеровДатчики.НомерСтроки";
	
	Запрос.УстановитьПараметр("КодДатчика",п_КодДатчика);	
	Запрос.УстановитьПараметр("Модель", Модель);
	
	ТаблицаДатчиков = Запрос.Выполнить().Выгрузить();
	ДатчикНайден = Ложь;
	Для каждого НастройкаДатчика Из ТаблицаДатчиков Цикл
		ТекущийКод = СокрЛП(НастройкаДатчика.Код);
		Если ТекущийКод = п_КодДатчика Тогда 
			ЗаполнитьЗначенияСвойств(Результат,НастройкаДатчика);
			ДатчикНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ДатчикНайден Тогда 
		НовДатчик = Справочники.упДатчики.СоздатьЭлемент();
		Если Представление = Неопределено Тогда
			НовДатчик.Наименование = КодДатчика;
		Иначе
			НовДатчик.Наименование = Представление;
		КонецЕсли;
		
		НовДатчик.Записать();
		Датчик = НовДатчик.Ссылка;
		
		спрОбъектМодель = Модель.ПолучитьОбъект();
		стрТЧ = спрОбъектМодель.Датчики.Добавить();
		стрТЧ.НомерДатчика = КодДатчика;
		стрТЧ.Датчик = Датчик;
		спрОбъектМодель.Записать();
		
		Результат.Датчик = Датчик;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
