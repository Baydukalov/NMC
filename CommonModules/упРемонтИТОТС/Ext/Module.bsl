
#Область ПрограммныйИнтерфейс

// Процедура формирует движения по регистру "упСтатусыРемонтовИТОТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияСтатусРемонтаИТОТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСтатуса = Движения.упСтатусыРемонтовИТОТС;
			
	Статус				= ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус	= ДополнительныеСвойства.ЗаписыватьСтатус;
	Если ЗаписыватьСтатус Тогда
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
				
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = ТекущаяДатаСеанса();
		ДвижениеСтатуса.Документ    = Ссылка;
		ДвижениеСтатуса.Статус      = Статус;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упРасписаниеРаботыТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияРасписаниеРаботыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	Если Ложь Тогда // Для контекстной подсказки
	    Ссылка = Документы.упРемонтИТОТС.ПустаяСсылка();
	    РеквизитыОбъекта = Ссылка;
		Движения = Ссылка.ПолучитьОбъект().Движения;
		ДополнительныеСвойства = Ссылка.ПолучитьОбъект().ДополнительныеСвойства;
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	ДвиженияРасписания = Движения.упРасписаниеРаботыТС;
	ДвиженияРасписания.Записывать = Истина;
	//Если Истина
	//	И (Ложь
	//		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован 
	//		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен)
	//	И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) 
	//	И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан) 
	//Тогда
	//	ДвиженияРасписание = ДвиженияРасписания.Добавить();
	//	ДвиженияРасписание.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
	//	ДвиженияРасписание.ДатаНачала 			= РеквизитыОбъекта.НачалоРемонтаПлан;
	//	ДвиженияРасписание.ДатаОкончания 		= РеквизитыОбъекта.ОкончаниеРемонтаПлан;
	//	ДвиженияРасписание.СостояниеТС          = Справочники.упСостоянияТС.Ремонт;
	//	ДвиженияРасписание.ДокументОснование 	= Ссылка;
	//	ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыТС = Истина;
	//КонецЕсли;
	
	//Если Истина
	//	И Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат
	//	И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаФакт) 
	//Тогда
	//	ОкончаниеРемонтаПлан = КонецЧаса(РеквизитыОбъекта.НачалоРемонтаФакт);
	//	Если ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан) Тогда
	//		ОкончаниеРемонтаПлан = РеквизитыОбъекта.НачалоРемонтаФакт + (РеквизитыОбъекта.ОкончаниеРемонтаПлан - РеквизитыОбъекта.НачалоРемонтаПлан);
	//	КонецЕсли;
	//	ДвиженияРасписание = ДвиженияРасписания.Добавить();
	//	ДвиженияРасписание.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
	//	ДвиженияРасписание.ДатаНачала 			= РеквизитыОбъекта.НачалоРемонтаФакт;
	//	ДвиженияРасписание.ДатаОкончания 		= ОкончаниеРемонтаПлан;
	//	ДвиженияРасписание.СостояниеТС			= Справочники.упСостоянияТС.Ремонт;
	//	ДвиженияРасписание.ДокументОснование 	= Ссылка;
	//	ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыТС = Истина;
	//КонецЕсли;
	
	Если Истина
		И Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен
		И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаФакт) 
		И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаФакт) 
	Тогда
		ДвиженияРасписание = ДвиженияРасписания.Добавить();
		ДвиженияРасписание.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
		ДвиженияРасписание.ДатаНачала 			= РеквизитыОбъекта.НачалоРемонтаФакт;
		ДвиженияРасписание.ДатаОкончания 		= РеквизитыОбъекта.ОкончаниеРемонтаФакт;
		ДвиженияРасписание.СостояниеТС          = Справочники.упСостоянияТС.Ремонт;
		ДвиженияРасписание.ДокументОснование 	= Ссылка;
    	ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыТС = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упЗанятостьТранспорта".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияЗанятостьТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    Ссылка = Документы.упРемонтИТОТС.ПустаяСсылка();
	    РеквизитыОбъекта = Ссылка;
		Движения = Ссылка.ПолучитьОбъект().Движения;
		ДополнительныеСвойства = Ссылка.ПолучитьОбъект().ДополнительныеСвойства;
	КонецЕсли;
		
	ВыполнитьОтказДляТС = Ложь;
	Если НЕ Ссылка.ТранспортноеСредство.СобственноеТранспортноеСредство Тогда
		ВыполнитьОтказДляТС = НЕ ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
	КонецЕсли;
	
	Если Отказ ИЛИ ВыполнитьОтказДляТС Тогда 
		Возврат; 
	КонецЕсли;
	
	ИспользоватьЗаявкуНаРемонтИТОТС = Ложь;
	Если ДополнительныеСвойства.Свойство("ИспользоватьЗаявкуНаРемонтИТОТС") Тогда
		ИспользоватьЗаявкуНаРемонтИТОТС = ДополнительныеСвойства.ИспользоватьЗаявкуНаРемонтИТОТС;
	КонецЕсли;
	
	ЗанятостьТранспорта = Движения.упЗанятостьТранспорта;
	ЗанятостьТранспорта.Записывать = Истина;
	Статус = ДополнительныеСвойства.Статус;
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда  
		Если Ссылка.СпособРемонта = Перечисления.упСпособыРемонтов.СторонняяОрганизация Тогда
			Адрес = Ссылка.АдресРемонта; 
		Иначе
			Адрес = Ссылка.МестоРемонта.Владелец.Адрес; 
		КонецЕсли; 
		Зоны = Адрес.ГеографическаяЗона;
		Если Ложь
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
			Или (Истина
				И (Ложь
					Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Новый 
					Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован)
				И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан))
		Тогда
			УчетныеЗоныЗон = упУчетТранспортныхСредств.УчетныеЗоныЗон(Зоны); 
			Если УчетныеЗоныЗон.Количество() > 0 Тогда
				Зона = УчетныеЗоныЗон[0].Родитель;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	Если НЕ ИспользоватьЗаявкуНаРемонтИТОТС И 
		(Ложь
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Новый 
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован
		)Тогда
		Если ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) Тогда
			ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
			ДвиженияСостояние.Период = РеквизитыОбъекта.Дата;
			ДвиженияСостояние.Транспорт = РеквизитыОбъекта.ТранспортноеСредство;
			ДвиженияСостояние.ПланАктуален = 1;
			ДвиженияСостояние.Документ = Ссылка;
			ДвиженияСостояние.ДатаНачала = РеквизитыОбъекта.НачалоРемонтаПлан;
			ДвиженияСостояние.ДатаОкончания = РеквизитыОбъекта.ОкончаниеРемонтаПлан;
			ДвиженияСостояние.ЗонаНачала = Зона;
			ДвиженияСостояние.ЗонаОкончания = Зона;
			ДвиженияСостояние.СостояниеТранспорта = Справочники.упСостоянияТС.Ремонт;
	    	ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС", Истина);
		КонецЕсли; 
	ИначеЕсли Ложь
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
	Тогда
		ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
		ДвиженияСостояние.Период = РеквизитыОбъекта.Дата;
		ДвиженияСостояние.Транспорт = РеквизитыОбъекта.ТранспортноеСредство;
		ДвиженияСостояние.СостояниеТранспорта = Справочники.упСостоянияТС.Ремонт;
		ДвиженияСостояние.ПланАктуален = Истина;
		ДвиженияСостояние.ФактНачала = Истина;
		ДвиженияСостояние.Документ = Ссылка;
		Если ИспользоватьЗаявкуНаРемонтИТОТС Тогда
			ДвиженияСостояние.Документ = РеквизитыОбъекта.ЗаявкаНаРемонтИТО;
		КонецЕсли; 
		ДвиженияСостояние.ДатаНачала = РеквизитыОбъекта.НачалоРемонтаФакт;
		
		Если Истина
			И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) 
			И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан) 
		Тогда 
			ДлительностьРемонта = РеквизитыОбъекта.ОкончаниеРемонтаПлан - РеквизитыОбъекта.НачалоРемонтаПлан;
		ИначеЕсли ЗначениеЗаполнено(РеквизитыОбъекта.ВидРемонта.ВремяРаботПлан) Тогда
			ДлительностьРемонта = РеквизитыОбъекта.ВидРемонта.ВремяРаботПлан * 60 * 60;
		Иначе
			ДлительностьРемонта = 2 * 60 * 60;
		КонецЕсли; 
		ДвиженияСостояние.ДатаОкончания = ДвиженияСостояние.ДатаНачала + ДлительностьРемонта;
		ДвиженияСостояние.ЗонаНачала = Зона;
		ДвиженияСостояние.ЗонаОкончания = Зона;
	    ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС", Истина);
	ИначеЕсли Ложь
		ИЛИ (ИспользоватьЗаявкуНаРемонтИТОТС И Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен) 
	Тогда
		ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
		ДвиженияСостояние.Период = РеквизитыОбъекта.Дата;
		ДвиженияСостояние.Транспорт = РеквизитыОбъекта.ТранспортноеСредство;
		ДвиженияСостояние.СостояниеТранспорта = Справочники.упСостоянияТС.Свободно;
		ДвиженияСостояние.ПланАктуален = Ложь;
		ДвиженияСостояние.ФактНачала = Истина;
		ДвиженияСостояние.ФактОкончания = Истина;
		ДвиженияСостояние.Документ = РеквизитыОбъекта.ЗаявкаНаРемонтИТО;
		ДвиженияСостояние.ДатаНачала = РеквизитыОбъекта.НачалоРемонтаФакт;
		ДвиженияСостояние.ДатаОкончания = РеквизитыОбъекта.ОкончаниеРемонтаФакт;
		ДвиженияСостояние.ЗонаНачала = Зона;
		ДвиженияСостояние.ЗонаОкончания = Зона;
	    ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС", Истина);
		
	КонецЕсли;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен 
	Тогда
		ФактическиеМестоположения = Движения.упФактическиеМестоположенияТС;
		ФактическиеМестоположения.Записывать = Истина;
		ДвижениеПоложения = ФактическиеМестоположения.Добавить();
		ДвижениеПоложения.Период = РеквизитыОбъекта.Дата;
		ДвижениеПоложения.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
		ДвижениеПоложения.Зона = Зона;
		ДвижениеПоложения.Адрес = Адрес;
	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует движения по регистру "упПлановыеМестоположенияТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура _СформироватьДвиженияПлановыеМестоположенияТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    Ссылка = Документы.упРемонтИТОТС.ПустаяСсылка();
	    РеквизитыОбъекта = Ссылка;
		Движения = Ссылка.ПолучитьОбъект().Движения;
		ДополнительныеСвойства = Ссылка.ПолучитьОбъект().ДополнительныеСвойства;
	КонецЕсли;
	Если Отказ 
		//Или Не Ссылка.ТранспортноеСредство.СобственноеТранспортноеСредство 
	Тогда 
		Возврат; 
	КонецЕсли;
	
	ПлановыеМестоположенияТС = Движения.упПлановыеМестоположенияТС;
	ПлановыеМестоположенияТС.Записывать = Истина;
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда  
		Статус = ДополнительныеСвойства.Статус;
		Если Ссылка.СпособРемонта = Перечисления.упСпособыРемонтов.СторонняяОрганизация Тогда
			Зоны = Ссылка.АдресРемонта.ГеографическаяЗона;
		Иначе
			Зоны = Ссылка.МестоРемонта.Владелец.Адрес.ГеографическаяЗона;
		КонецЕсли; 
		Если Ложь
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
			Или (Истина
				И (Ложь
					Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Новый 
					Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован)
				И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан))
		Тогда
			ДвиженияСостояние = ПлановыеМестоположенияТС.Добавить();
			ДвиженияСостояние.Период = РеквизитыОбъекта.Дата;
			ДвиженияСостояние.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
			ДвиженияСостояние.Количество = 1;
			//ДвиженияСостояние.Документ = Ссылка;
			ДвиженияСостояние.ВидДвижения = ВидДвиженияНакопления.Приход;
			Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат Тогда
				ДвиженияСостояние.ПлановоеПрибытие = РеквизитыОбъекта.НачалоРемонтаФакт;
			Иначе
				ДвиженияСостояние.ПлановоеПрибытие = РеквизитыОбъекта.НачалоРемонтаПлан;
			КонецЕсли; 
			УчетныеЗоныЗон = упУчетТранспортныхСредств.УчетныеЗоныЗон(Зоны); 
			Если УчетныеЗоныЗон.Количество() > 0 Тогда
				ДвиженияСостояние.Зона = УчетныеЗоныЗон[0].Родитель;
			КонецЕсли; 
			//ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыТС = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует движения по регистру "упСостоянияТранспортныхСредств".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияТранспортныхСредств = Движения.упСостоянияТранспортныхСредств;
	ДвиженияСостоянияТранспортныхСредств.Записывать = Истина;

	Статус = ДополнительныеСвойства.Статус;
			
	Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда
		Если ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаФакт) Тогда
			ДвиженияСостояние = ДвиженияСостоянияТранспортныхСредств.Добавить();
			ДвиженияСостояние.Период 				= РеквизитыОбъекта.НачалоРемонтаФакт;
			ДвиженияСостояние.ТранспортноеСредство 	= РеквизитыОбъекта.ТранспортноеСредство;
			ДвиженияСостояние.СостояниеТС 			= Справочники.упСостоянияТС.Ремонт;
		КонецЕсли; 
	КонецЕсли;
	
	Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда
		Если ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаФакт) Тогда
			ДвиженияСостояние = ДвиженияСостоянияТранспортныхСредств.Добавить();
			ДвиженияСостояние.Период 				= РеквизитыОбъекта.ОкончаниеРемонтаФакт + 1;
			ДвиженияСостояние.ТранспортноеСредство 	= РеквизитыОбъекта.ТранспортноеСредство;
			ДвиженияСостояние.СостояниеТС 			= Справочники.упСостоянияТС.Свободно;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует движения по регистру "упПройденныеТО".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияПройденныеТО(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ 
		ИЛИ РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПройденныеТО = Движения.упПройденныеТО;
	ДвиженияПройденныеТО.Записывать = Истина;
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если (Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОбъекта.ВидРемонта, "ТипРемонта") = Перечисления.упТипыРемонтов.ПлановоеТО Тогда
		ЭтоРемонтТС = Истина;
		ДвиженияТО = ДвиженияПройденныеТО.Добавить();
		ДвиженияТО.Период 				= ?(Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат, РеквизитыОбъекта.НачалоРемонтаФакт, РеквизитыОбъекта.ОкончаниеРемонтаФакт);
		Если РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
			ЭтоРемонтТС = Ложь;
			ДвиженияТО.ДополнительноеОборудование = РеквизитыОбъекта.ДополнительноеОборудованиеРемонта;
		Иначе
			ДвиженияТО.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
		КонецЕсли;
		ДвиженияТО.ВидРемонта 			= РеквизитыОбъекта.ВидРемонта;
		Если НЕ ЗначениеЗаполнено(ДвиженияТО.Период) Тогда 
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Укажите значение поля %1 !!!'"),?(Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат, "Начало ремонта факт", "Окончание ремонта факт"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"Объект."+?(Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат, "НачалоРемонтаФакт", "ОкончаниеРемонтаФакт"),,Отказ);
		КонецЕсли;
		
		Если НЕ Отказ И ЭтоРемонтТС Тогда
			ПериодДвижения = ДвиженияТО.Период;
			РемонтируемоеДополнительноеОборудование = РеквизитыОбъекта.РемонтируемоеДополнительноеОборудование;
			Для каждого СтрокаТЧ Из РемонтируемоеДополнительноеОборудование Цикл
				ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТЧ.ВидРемонта, "ТипРемонта");
				Если ТипРемонта = Перечисления.упТипыРемонтов.ПлановоеТО Тогда
					ДвиженияТО = ДвиженияПройденныеТО.Добавить();
					ДвиженияТО.Период 				= ПериодДвижения;
					ДвиженияТО.ТранспортноеСредство = СтрокаТЧ.ДополнительноеОборудование;
					ДвиженияТО.ВидРемонта 			= СтрокаТЧ.ВидРемонта;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упПлановыеДатыТО".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияРемонтПлановыеДатыТО(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ ИЛИ НЕ ДополнительныеСвойства.Свойство("ИспользоватьПлановыеДатыТО") Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если (Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован ИЛИ Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат)
		И (ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан)) Тогда
		
		ТаблицаПлана = ДополнительныеСвойства.ПлановыеДатыТО;
		ДвиженияПлановыеДатыТО = Движения.упПлановыеДатыТО;
		ДвиженияПлановыеДатыТО.Записывать = Истина;
		ДвиженияПлановыеДатыТО.Очистить();
		
		Для каждого СтрокаТЧ Из ТаблицаПлана Цикл
			
			ДвиженияТО = ДвиженияПлановыеДатыТО.Добавить();
			ДвиженияТО.Период = РеквизитыОбъекта.НачалоРемонтаПлан;
			ДвиженияТО.НачалоРемонта = РеквизитыОбъекта.НачалоРемонтаПлан;
			ДвиженияТО.ОкончаниеРемонта = РеквизитыОбъекта.ОкончаниеРемонтаПлан;
			ДвиженияТО.ТранспортноеСредство = СтрокаТЧ.ТранспортноеСредство;
			ДвиженияТО.ВидРемонта = СтрокаТЧ.ВидРемонта;
			ДвиженияТО.Документ = Ссылка;
			
		КонецЦикла;
		
		ДополнительныеСвойства.КонтрольПлановыеДатыТО = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвиженияРемонтПлановыеДатыТО()

// Процедура формирует движения по регистру "упПлановоеРасписаниеРаботыГаража".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияПлановоеРасписаниеРаботыГаража(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПлан = Движения.упПлановоеРасписаниеРаботыГаража;
	ДвиженияПлан.Записывать = Истина;
	
	Статус = ДополнительныеСвойства.Статус;
	ИспользоватьЗаявкуНаРемонтИТОТС = Ложь;
	Если ДополнительныеСвойства.Свойство("ИспользоватьЗаявкуНаРемонтИТОТС") Тогда
		ИспользоватьЗаявкуНаРемонтИТОТС = ДополнительныеСвойства.ИспользоватьЗаявкуНаРемонтИТОТС;
	КонецЕсли;
	НеКонтролироватьРасписаниеРаботыГаража = ДополнительныеСвойства.НеКонтролироватьРасписаниеРаботыГаража;
	
	Если Истина 
		И НЕ ИспользоватьЗаявкуНаРемонтИТОТС 
		И (Ложь
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован 
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен)
		И ЗначениеЗаполнено(РеквизитыОбъекта.МестоРемонта) 
		И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) 
		И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан) 
		И НЕ НеКонтролироватьРасписаниеРаботыГаража
	Тогда
				   
		ДвиженияПлан.Очистить();
		ДвижениеПлан = ДвиженияПлан.Добавить();
		ДвижениеПлан.Гараж                = РеквизитыОбъекта.Гараж;
		ДвижениеПлан.МестоРемонта         = РеквизитыОбъекта.МестоРемонта;
		ДвижениеПлан.НачалоРемонта        = РеквизитыОбъекта.НачалоРемонтаПлан;
		ДвижениеПлан.ОкончаниеРемонта     = РеквизитыОбъекта.ОкончаниеРемонтаПлан;		
		ДвижениеПлан.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
		ДвижениеПлан.ДополнительноеОборудование = Справочники.упДополнительноеОборудование.ПустаяСсылка();
		ДвижениеПлан.Агрегат = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка();
			
		Если РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
			ДвижениеПлан.ДополнительноеОборудование = РеквизитыОбъекта.ДополнительноеОборудование;
		ИначеЕсли РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
			ДвижениеПлан.Агрегат = РеквизитыОбъекта.Агрегат;
		Иначе
			ДвижениеПлан.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
		КонецЕсли;
		
		ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыГаража = Истина;	
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует движения по регистру "упФактическоеРасписаниеРаботыГаража".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияФактическоеРасписаниеРаботыГаража(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияФакт = Движения.упФактическоеРасписаниеРаботыГаража;
	ДвиженияФакт.Записывать = Истина;
	НеКонтролироватьРасписаниеРаботыГаража = ДополнительныеСвойства.НеКонтролироватьРасписаниеРаботыГаража;
	
	Статус = ДополнительныеСвойства.Статус;
	Если Истина
		И (Ложь
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
			Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен)
		И ЗначениеЗаполнено(РеквизитыОбъекта.МестоРемонта)
		И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаФакт) 
		И НЕ НеКонтролироватьРасписаниеРаботыГаража
	Тогда
				   
		ДвиженияФакт.Очистить();
		ДвижениеФакт = ДвиженияФакт.Добавить();
		ДвижениеФакт.Гараж                = РеквизитыОбъекта.Гараж;
		ДвижениеФакт.МестоРемонта         = РеквизитыОбъекта.МестоРемонта;
		ДвижениеФакт.НачалоРемонта        = РеквизитыОбъекта.НачалоРемонтаФакт;
		Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда
			ДвижениеФакт.ОкончаниеРемонта     = РеквизитыОбъекта.ОкончаниеРемонтаФакт;
		КонецЕсли; 
		
		ДвижениеФакт.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
		ДвижениеФакт.ДополнительноеОборудование = Справочники.упДополнительноеОборудование.ПустаяСсылка();
		ДвижениеФакт.Агрегат = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка();
			
		Если РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
			ДвижениеФакт.ДополнительноеОборудование = РеквизитыОбъекта.ДополнительноеОборудование;
		ИначеЕсли РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
			ДвижениеФакт.Агрегат = РеквизитыОбъекта.Агрегат;
		Иначе
			ДвижениеФакт.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
		КонецЕсли;
		
		ДополнительныеСвойства.КонтрольФактическоеРасписаниеРаботыГаража = Истина;

	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует движения по регистру "упОстаткиГСМ".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвижениеОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияОстаткиГСМ	= Движения.упОстаткиГСМ;
	ДвиженияОстаткиГСМ.Записывать = Истина;
	ДвиженияОстаткиГСМ.Очистить();
	
	Если НЕ РеквизитыОбъекта.ГСМ.Количество() Тогда
		Возврат;
	КонецЕсли; 
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
		Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен 
	Тогда		
	
		ОстаткиГСМ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ОстаткиГСМ");
		Если ЗначениеЗаполнено(ОстаткиГСМ) Тогда
			Для каждого СтрокаОстаткиГСМ Из ОстаткиГСМ Цикл
				ДвижениеОстаткиГСМ = ДвиженияОстаткиГСМ.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеОстаткиГСМ, СтрокаОстаткиГСМ);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упЛимитыРасходаГСМ".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияЛимитыРасходаГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	ИспользуютсяЛимитыРасходаГСМ = Ложь;
	
	Если РеквизитыОбъекта.Свойство("ИспользуютсяЛимитыРасходаГСМ") Тогда
		Если РеквизитыОбъекта.ИспользуютсяЛимитыРасходаГСМ Тогда
			ИспользуютсяЛимитыРасходаГСМ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ИспользуютсяЛимитыРасходаГСМ Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияЛимитыРасходаГСМ = Движения.упЛимитыРасходаГСМ;
	ДвиженияЛимитыРасходаГСМ.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ЛимитыРасходаГСМ") Тогда
		ТаблицаЛимитыРасходаГСМ = ДополнительныеСвойства.ЛимитыРасходаГСМ;
		Для каждого ТекущаяСтрокаЛимита Из ТаблицаЛимитыРасходаГСМ Цикл
			ДвижениеЛимитыРасходаГСМ = ДвиженияЛимитыРасходаГСМ.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеЛимитыРасходаГСМ, ТекущаяСтрокаЛимита);
			ДвижениеЛимитыРасходаГСМ.Организация = РеквизитыОбъекта.Организация;
			ДвижениеЛимитыРасходаГСМ.Подразделение = РеквизитыОбъекта.Подразделение;
			ДвижениеЛимитыРасходаГСМ.Лимит = 0;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвиженияЛимитыРасходаГСМ()

// Процедура формирует движения по регистру "упРасходы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт

	упРаспределениеРасходов.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

КонецПроцедуры

// Процедура формирует движения по регистру "упАгрегатыНаСкладах", "упАгрегатыТранспортныхСредств".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияШиныУзлыАгрегаты(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Новый Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Отменен Тогда
		Возврат;
	КонецЕсли;
	
	Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат ИЛИ Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда
		
		ДвиженияАгрегатыТС = Движения.упАгрегатыТранспортныхСредств;
		ДвиженияАгрегатыТС.Записывать = Истина;
		
		Если РеквизитыОбъекта.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	тбчСобственные.Склад КАК Склад,
			               |	тбчСобственные.Агрегат КАК Агрегат,
			               |	тбчСобственные.ТипОперацииМонтажа КАК ТипОперацииМонтажа,
			               |	тбчСобственные.ХодоваяШина КАК ХодоваяШина,
			               |	тбчСобственные.НомерСтроки КАК НомерСтроки
			               |ПОМЕСТИТЬ втСобственные
			               |ИЗ
			               |	Документ.упРемонтИТОТС.ШиныУзлыАгрегатыСобственные КАК тбчСобственные
			               |ГДЕ
			               |	тбчСобственные.Ссылка = &Ссылка
			               |
			               |ОБЪЕДИНИТЬ ВСЕ
			               |
			               |ВЫБРАТЬ
			               |	тбчСобственныеДопОборудование.Склад,
			               |	тбчСобственныеДопОборудование.ДополнительноеОборудование,
			               |	тбчСобственныеДопОборудование.ТипОперацииМонтажа,
			               |	ЛОЖЬ,
			               |	тбчСобственныеДопОборудование.НомерСтроки
			               |ИЗ
			               |	Документ.упРемонтИТОТС.ДополнительноеОборудованиеСобственное КАК тбчСобственныеДопОборудование
			               |ГДЕ
			               |	тбчСобственныеДопОборудование.Ссылка = &Ссылка
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	втСобственные.Склад КАК Склад,
			               |	втСобственные.Агрегат КАК Агрегат,
			               |	втСобственные.ТипОперацииМонтажа КАК ТипОперацииМонтажа,
			               |	втСобственные.ХодоваяШина КАК ХодоваяШина,
			               |	ЕСТЬNULL(АгрегатыНаСкладах.КоличествоОстаток, 0) КАК Количество,
			               |	втСобственные.НомерСтроки КАК НомерСтроки
			               |ПОМЕСТИТЬ втСклады
			               |ИЗ
			               |	втСобственные КАК втСобственные
			               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
			               |				&Период,
			               |				Агрегат В
			               |					(ВЫБРАТЬ
			               |						втСобственные.Агрегат
			               |					ИЗ
			               |						втСобственные КАК втСобственные
			               |					ГДЕ
			               |						втСобственные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено))) КАК АгрегатыНаСкладах
			               |		ПО втСобственные.Склад = АгрегатыНаСкладах.Склад
			               |			И втСобственные.Агрегат = АгрегатыНаСкладах.Агрегат
			               |ГДЕ
			               |	втСобственные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	втСобственные.Склад КАК Склад,
			               |	втСобственные.Агрегат КАК Агрегат,
			               |	втСобственные.ТипОперацииМонтажа КАК ТипОперацииМонтажа,
			               |	втСобственные.ХодоваяШина КАК ХодоваяШина,
			               |	ВЫБОР
			               |		КОГДА АгрегатыТС.ТранспортноеСредство ЕСТЬ NULL
			               |			ТОГДА 0
			               |		ИНАЧЕ 1
			               |	КОНЕЦ КАК Количество,
			               |	втСобственные.НомерСтроки КАК НомерСтроки
			               |ПОМЕСТИТЬ втТС
			               |ИЗ
			               |	втСобственные КАК втСобственные
			               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
			               |				&Период,
			               |				ТранспортноеСредство = &ТранспортноеСредство
			               |					И Агрегат В
			               |						(ВЫБРАТЬ
			               |							втСобственные.Агрегат
			               |						ИЗ
			               |							втСобственные КАК втСобственные
			               |						ГДЕ
			               |							втСобственные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято))) КАК АгрегатыТС
			               |		ПО втСобственные.Агрегат = АгрегатыТС.Агрегат
			               |ГДЕ
			               |	втСобственные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	втСклады.Склад КАК Склад,
			               |	втСклады.Агрегат КАК Агрегат,
			               |	втСклады.ТипОперацииМонтажа КАК ТипОперацииМонтажа,
			               |	втСклады.ХодоваяШина КАК ХодоваяШина,
			               |	втСклады.Количество КАК Количество,
			               |	втСклады.НомерСтроки КАК НомерСтроки
			               |ИЗ
			               |	втСклады КАК втСклады
			               |
			               |ОБЪЕДИНИТЬ ВСЕ
			               |
			               |ВЫБРАТЬ
			               |	втТС.Склад,
			               |	втТС.Агрегат,
			               |	втТС.ТипОперацииМонтажа,
			               |	втТС.ХодоваяШина,
			               |	втТС.Количество,
			               |	втТС.НомерСтроки
			               |ИЗ
			               |	втТС КАК втТС";
			
						
			Запрос.УстановитьПараметр("Период",РеквизитыОбъекта.Граница);
			Запрос.УстановитьПараметр("ТранспортноеСредство",РеквизитыОбъекта.ТранспортноеСредство);
			Запрос.УстановитьПараметр("Ссылка",Ссылка);
			// Идея такая: запрос получает остатки на складах для установленных на складах и ТС,
			// если нет остатков тогда нуль, для складов остатки "Установлено" для ТС "Снято",
			// полученные записи обходим и добавляем в движения, дублированых строк быть не должно,
			// есть проверка при записи "проведении", 
			// для приобретенных агрегатов приходуем их на ТС.
			
			ДвиженияАгрегатыНаСкладах = Движения.упАгрегатыНаСкладах;
			ДвиженияАгрегатыНаСкладах.Записывать = Истина;			
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				ТекстОшибки = "";
				
				Если ЗначениеЗаполнено(Выборка.Склад) Тогда
					
					Если Выборка.ТипОперацииМонтажа = Перечисления.упТипыОперацийМонтажа.Установлено Тогда
						Если Выборка.Количество > 0 Тогда
							// СКЛАД.
							ДвижениеАгрегатыНаСкладах = ДвиженияАгрегатыНаСкладах.Добавить();
							ДвижениеАгрегатыНаСкладах.ВидДвижения = ВидДвиженияНакопления.Расход;
							ДвижениеАгрегатыНаСкладах.Период = РеквизитыОбъекта.Дата;
							ДвижениеАгрегатыНаСкладах.Регистратор = Ссылка;
							ДвижениеАгрегатыНаСкладах.Агрегат = Выборка.Агрегат;
							ДвижениеАгрегатыНаСкладах.Склад= Выборка.Склад;
							ДвижениеАгрегатыНаСкладах.Количество = 1;
							// ТС.
							ДвижениеАгрегатыТС  = ДвиженияАгрегатыТС.Добавить();
							ДвижениеАгрегатыТС.Период = РеквизитыОбъекта.Дата;
							ДвижениеАгрегатыТС.Регистратор = Ссылка;
							ДвижениеАгрегатыТС.Агрегат = Выборка.Агрегат;
							ДвижениеАгрегатыТС.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
							ДвижениеАгрегатыТС.Операция = Выборка.ТипОперацииМонтажа;
							ДвижениеАгрегатыТС.ХодоваяШина = Выборка.ХодоваяШина;						
						Иначе
							Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
								ТекстОшибки = НСтр(СтрШаблон("ru = 'Недостаточно агрегата ""%1"" в наличии на складе ""%2""'", Выборка.Агрегат, Выборка.Склад));
							Иначе
								ТекстОшибки = НСтр(СтрШаблон("ru = 'Недостаточно дополнительного оборудования ""%1"" в наличии на складе ""%2""'", Выборка.Агрегат, Выборка.Склад));
							КонецЕсли;		
						КонецЕсли;
					ИначеЕсли Выборка.ТипОперацииМонтажа = Перечисления.упТипыОперацийМонтажа.Снято Тогда
						Если Выборка.Количество > 0 Тогда
							// СКЛАД.
							ДвижениеАгрегатыНаСкладах = ДвиженияАгрегатыНаСкладах.Добавить();
							ДвижениеАгрегатыНаСкладах.ВидДвижения = ВидДвиженияНакопления.Приход;
							ДвижениеАгрегатыНаСкладах.Период = РеквизитыОбъекта.Дата;
							ДвижениеАгрегатыНаСкладах.Регистратор = Ссылка;
							ДвижениеАгрегатыНаСкладах.Агрегат = Выборка.Агрегат;
							ДвижениеАгрегатыНаСкладах.Склад= Выборка.Склад;
							ДвижениеАгрегатыНаСкладах.Количество = 1;
							// ТС.
							ДвижениеАгрегатыТС  = ДвиженияАгрегатыТС.Добавить();
							ДвижениеАгрегатыТС.Период = РеквизитыОбъекта.Дата;
							ДвижениеАгрегатыТС.Регистратор = Ссылка;
							ДвижениеАгрегатыТС.Агрегат = Выборка.Агрегат;
							ДвижениеАгрегатыТС.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
							ДвижениеАгрегатыТС.Операция = Выборка.ТипОперацииМонтажа;
							ДвижениеАгрегатыТС.ХодоваяШина = Выборка.ХодоваяШина;
						Иначе
							Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
								ТекстОшибки = НСтр(СтрШаблон("ru = 'Недостаточно агрегата ""%1"" в наличии на Транспортном средстве ""%2""'", Выборка.Агрегат, РеквизитыОбъекта.ТранспортноеСредство));
							Иначе
								ТекстОшибки = НСтр(СтрШаблон("ru = 'Недостаточно дополнительного оборудования ""%1"" в наличии на Транспортном средстве ""%2""'", Выборка.Агрегат, РеквизитыОбъекта.ТранспортноеСредство));
							КонецЕсли;		
						КонецЕсли;
					КонецЕсли;		
					
				Иначе
					Если Выборка.Количество > 0 Тогда
						// ТС.
						ДвижениеАгрегатыТС  = ДвиженияАгрегатыТС.Добавить();
						ДвижениеАгрегатыТС.Период = РеквизитыОбъекта.Дата;
						ДвижениеАгрегатыТС.Регистратор = Ссылка;
						ДвижениеАгрегатыТС.Агрегат = Выборка.Агрегат;
						ДвижениеАгрегатыТС.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
						ДвижениеАгрегатыТС.Операция = Выборка.ТипОперацииМонтажа;
						ДвижениеАгрегатыТС.ХодоваяШина = Выборка.ХодоваяШина;
					Иначе
						Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
							ТекстОшибки = НСтр(СтрШаблон("ru = 'Недостаточно агрегата ""%1"" в наличии на Транспортном средстве ""%2""'", Выборка.Агрегат, РеквизитыОбъекта.ТранспортноеСредство));
						Иначе
							ТекстОшибки = НСтр(СтрШаблон("ru = 'Недостаточно дополнительного оборудования ""%1"" в наличии на Транспортном средстве ""%2""'", Выборка.Агрегат, РеквизитыОбъекта.ТранспортноеСредство));
						КонецЕсли;		
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ТекстОшибки) Тогда
					Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
						ИмяТабличнойЧасти 	= "ДополнительноеОборудованиеСобственное";
						ИмяПоля			= "ДополнительноеОборудование";
					Иначе
						ИмяТабличнойЧасти 	= "ШиныУзлыАгрегатыСобственные";
						ИмяПоля			= "Агрегат";
					КонецЕсли;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Ссылка,СтрШаблон("%1[%2].%3", ИмяТабличнойЧасти,Формат(Выборка.НомерСтроки-1,"ЧН=0; ЧГ=0"), ИмяПоля),"Объект", Отказ);	
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ШиныУзлыАгрегаты.Агрегат КАК Агрегат,
		               |	ШиныУзлыАгрегаты.ТипОперацииМонтажа КАК ТипОперацииМонтажа,
		               |	ШиныУзлыАгрегаты.ХодоваяШина КАК ХодоваяШина
		               |ИЗ
		               |	Документ.упРемонтИТОТС.ШиныУзлыАгрегаты КАК ШиныУзлыАгрегаты
		               |ГДЕ
		               |	ШиныУзлыАгрегаты.Ссылка = &Ссылка
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ДопОборудование.ДополнительноеОборудование,
		               |	ДопОборудование.ТипОперацииМонтажа,
		               |	ЛОЖЬ
		               |ИЗ
		               |	Документ.упРемонтИТОТС.ДополнительноеОборудование КАК ДопОборудование
		               |ГДЕ
		               |	ДопОборудование.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			// ТС. при ремонте сторонней организации.
			ДвижениеАгрегатыТС  = ДвиженияАгрегатыТС.Добавить();
			ДвижениеАгрегатыТС.Период = РеквизитыОбъекта.Дата;
			ДвижениеАгрегатыТС.Регистратор = Ссылка;
			ДвижениеАгрегатыТС.Агрегат = Выборка.Агрегат;
			ДвижениеАгрегатыТС.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
			ДвижениеАгрегатыТС.Операция = Выборка.ТипОперацииМонтажа;
			ДвижениеАгрегатыТС.ХодоваяШина = Выборка.ХодоваяШина;
		КонецЦикла;		
		
	КонецЕсли;	
	
КонецПроцедуры // СформироватьДвиженияШиныУзлыАгрегаты()

// Процедура выполняет проверку установленного доп оборудывания транспортного средства,
//  после проведения документа.
//
// Параметры:
//  ДополнительныеСвойства	 - Структура						 - свойства.
//  ДокументОбъект			 - ДокументОбъект.упРейс				 - ДокументОбъект.упРемонтИТОТС, объект документа.
//  ТранспортноеСредство		 - СправочникСсылка.упТранспортныеСредства	 - транспортное средство.
//  РеквизитыОбъекта		 - Структура							 - реквизиты.
//  Отказ					 - Булево								 - Признак проведения документа.
//
Процедура ВыполнитьКонтрольУстановленногоОборудованияНаТС(ДополнительныеСвойства, ДокументОбъект, ТранспортноеСредство, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Начат);
	МассивСтатусов.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Завершен);
	
	Если МассивСтатусов.Найти(Статус) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРемонтИТОТСРемонтируемоеДополнительноеОборудование.ДополнительноеОборудование КАК Оборудование
	|ПОМЕСТИТЬ вт_Обр
	|ИЗ
	|	Документ.упРемонтИТОТС.РемонтируемоеДополнительноеОборудование КАК упРемонтИТОТСРемонтируемоеДополнительноеОборудование
	|ГДЕ
	|	упРемонтИТОТСРемонтируемоеДополнительноеОборудование.Ссылка = &Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	упРемонтИТОТСРемонтируемоеДополнительноеОборудование.ДополнительноеОборудование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упАгрегатыТранспортныхСредствСрезПоследних.Агрегат КАК Агрегат
	|ПОМЕСТИТЬ вт_Установлено
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(&Период, ) КАК упАгрегатыТранспортныхСредствСрезПоследних
	|ГДЕ
	|	упАгрегатыТранспортныхСредствСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И упАгрегатыТранспортныхСредствСрезПоследних.ТранспортноеСредство = &ТранспортноеСредство
	|	И упАгрегатыТранспортныхСредствСрезПоследних.Агрегат.Ссылка ССЫЛКА Справочник.упДополнительноеОборудование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Обр.Оборудование КАК Оборудование
	|ИЗ
	|	вт_Обр КАК вт_Обр
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Установлено КАК вт_Установлено
	|		ПО вт_Обр.Оборудование = вт_Установлено.Агрегат
	|ГДЕ
	|	вт_Установлено.Агрегат ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Документ", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Период", РеквизитыОбъекта.Дата + 1); // дата движения документа плюс 1 сек.
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ТекстСообщения = НСтр(СтрШаблон("ru = 'На транспортное средство ""%1"" не установлено оборудование: ""%3"". Проведение не выполнено. %2'",
		Строка(ТранспортноеСредство), Строка(ДокументОбъект), Выборка.Оборудование));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтрольУстановленногоОборудованияНаТС()

// Процедура - Инициализировать данные остатков и лимитов ГСМ
//
// Параметры:
//  ДополнительныеСвойства - Структура	    - передаются доп. свойства объекта документа.
//  Ссылка				   - ДокументСсылка - ссылка на документ.
//  РеквизитыОбъекта	   - Структура	    - передается структура реквзитов документа, необходимых для формирования движений.
//  Отказ				   - Булево	        - Истина, в случае ошибки.
//
Процедура ИнициализироватьДанныеОстатковИЛимитовГСМ(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ) Экспорт
	
	упСЛК.СоздатьОбъект().ИнициализироватьДанныеОстатковИЛимитовГСМ(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

#Область ЗаявкаНаРемонтИТОТС

// Процедура формирует движения по регистру "упСтатусыЗаявокНаРемонтИТОТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура ПодготовитьФормированиеДвиженияСтатусЗаявкаНаРемонтИТОТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению = Ложь;
	ТребуетсяСогласованиеЗаявкиНаРемонт = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	упЗаявкаНаРемонтИТОТС.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(ВидРемонта_спр.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению, ЛОЖЬ) КАК АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению,
		|	ЕСТЬNULL(ВидРемонта_спр.ТребуетсяСогласованиеЗаявкиНаРемонт, ЛОЖЬ) КАК ТребуетсяСогласованиеЗаявкиНаРемонт
		|ИЗ
		|	Документ.упЗаявкаНаРемонтИТОТС КАК упЗаявкаНаРемонтИТОТС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыРемонтов КАК ВидРемонта_спр
		|		ПО упЗаявкаНаРемонтИТОТС.ВидРемонта = ВидРемонта_спр.Ссылка
		|ГДЕ
		|	упЗаявкаНаРемонтИТОТС.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению = Выборка.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению;
		ТребуетсяСогласованиеЗаявкиНаРемонт = Выборка.ТребуетсяСогласованиеЗаявкиНаРемонт;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
	ДополнительныеСвойства.Вставить("ДоступнаРольСогласованиеЗаявкиНаРемонтИТО", РольДоступна("упСогласованиеЗаявкиНаРемонтИТО"));
	ДополнительныеСвойства.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению", АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению);
	ДополнительныеСвойства.Вставить("ТребуетсяСогласованиеЗаявкиНаРемонт", ТребуетсяСогласованиеЗаявкиНаРемонт);
	
	упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
КонецПроцедуры // ПодготовитьФормированиеДвиженияСтатусЗаявкаНаРемонтИТОТС()

// Процедура формирует движения по регистру "упСтатусыЗаявокНаРемонтИТОТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияСтатусЗаявкаНаРемонтИТОТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСтатуса = Движения.упСтатусыЗаявокНаРемонтИТОТС;
	
	мсвЗаявокСИзмененнымСтатусом = Неопределено;
	ДополнительныеСвойства.Свойство("ЗаявокиНаРемонтПоКоторымИзменилсяСтатус",мсвЗаявокСИзмененнымСтатусом);
	СтатусРемонта = Неопределено;
	Если ДополнительныеСвойства.Свойство("СтатусРемонт") Тогда 	
		СтатусРемонта = ДополнительныеСвойства.СтатусРемонт;
	КонецЕсли;	
	Статус				= ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус	= ДополнительныеСвойства.ЗаписыватьСтатус;
	РассчитатьСтатус	= ДополнительныеСвойства.РассчитатьСтатус;
		
	Если Не ЗаписыватьСтатус И РассчитатьСтатус Тогда 
		МассивЗаявкиНаРемонт = Новый Массив;
		МассивЗаявкиНаРемонт.Добавить(Ссылка);
		тбзЗаявкиСтатусы = упРаботаСоСтатусами.РассчитатьСтатусЗаявкиНаРемонт(МассивЗаявкиНаРемонт, СтатусРемонта);
		Если тбзЗаявкиСтатусы.Количество()>0 Тогда
		     РасчетныйСтатус = тбзЗаявкиСтатусы[0].Статус;
		Иначе	
		     РасчетныйСтатус = Неопределено;
		КонецЕсли;
		
		Если НЕ Статус = РасчетныйСтатус Тогда 
			Статус           = РасчетныйСтатус;
			ДополнительныеСвойства.Вставить("Статус", РасчетныйСтатус);
			ЗаписыватьСтатус = Истина;
			Если НЕ мсвЗаявокСИзмененнымСтатусом = Неопределено Тогда
				мсвЗаявокСИзмененнымСтатусом.Добавить(Ссылка);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	Период = ?(Реквизиты.Свойство("ДатаСтатуса"), Реквизиты.ДатаСтатуса, Реквизиты.Дата);
	Если ЗаписыватьСтатус Тогда 
		ДвиженияСтатуса = Движения.упСтатусыЗаявокНаРемонтИТОТС;
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
		
		Для каждого текЗаписьСтатус Из ДвиженияСтатуса Цикл
			Если текЗаписьСтатус.Период = Период Тогда
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = Период;
		ДвижениеСтатуса.Документ    = Ссылка;
		ДвижениеСтатуса.Статус      = Статус;
		Если НЕ мсвЗаявокСИзмененнымСтатусом = Неопределено Тогда
			ДополнительныеСвойства.ЗаявокиНаРемонтПоКоторымИзменилсяСтатус = мсвЗаявокСИзмененнымСтатусом;
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвиженияСтатусЗаявкаНаРемонтИТОТС()

// Процедура формирует движения по регистру "упПлановыеДатыТО".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияЗаявкиНаРемонтПлановыеДатыТО(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ ИЛИ НЕ ДополнительныеСвойства.Свойство("ПлановыеДатыТО") Тогда 
		Возврат; 
	КонецЕсли;
	
	ТаблицаПлана = ДополнительныеСвойства.ПлановыеДатыТО;
	ДвиженияПлановыеДатыТО = Движения.упПлановыеДатыТО;
	ДвиженияПлановыеДатыТО.Записывать = Истина;
	ДвиженияПлановыеДатыТО.Очистить();
	Статус = ДополнительныеСвойства.Статус;
	
	Если (Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению)
		И (ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан))Тогда
		
		Для каждого СтрокаТЧ Из ТаблицаПлана Цикл
			
			ДвиженияТО = ДвиженияПлановыеДатыТО.Добавить();
			ДвиженияТО.Период = РеквизитыОбъекта.НачалоРемонтаПлан;
			ДвиженияТО.НачалоРемонта = РеквизитыОбъекта.НачалоРемонтаПлан;
			ДвиженияТО.ОкончаниеРемонта = РеквизитыОбъекта.ОкончаниеРемонтаПлан;
			ДвиженияТО.ТранспортноеСредство = СтрокаТЧ.ТранспортноеСредство;
			ДвиженияТО.ВидРемонта = СтрокаТЧ.ВидРемонта;
			ДвиженияТО.Документ = Ссылка;
			
		КонецЦикла;
		
		ДополнительныеСвойства.КонтрольПлановыеДатыТО = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвиженияЗаявкиНаРемонтПлановыеДатыТО()

// Процедура формирует движения по регистру "упЗанятостьТранспорта".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияЗанятостьТранспортаЗаявкаНаРемонт(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    Ссылка = Документы.упЗаявкаНаРемонтИТОТС.ПустаяСсылка();
	    РеквизитыОбъекта = Ссылка;
		Движения = Ссылка.ПолучитьОбъект().Движения;
		ДополнительныеСвойства = Ссылка.ПолучитьОбъект().ДополнительныеСвойства;
	КонецЕсли;
	
	ВыполнитьОтказДляТС = Ложь;
	Если НЕ Ссылка.ТранспортноеСредство.СобственноеТранспортноеСредство Тогда
		ВыполнитьОтказДляТС = НЕ ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
	КонецЕсли;
	
	Если Отказ ИЛИ ВыполнитьОтказДляТС Тогда 
		Возврат; 
	КонецЕсли;
	
	ЗанятостьТранспорта = Движения.упЗанятостьТранспорта;
	ЗанятостьТранспорта.Записывать = Истина;
	Статус = ДополнительныеСвойства.Статус;
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда  
		Если Ссылка.СпособРемонта = Перечисления.упСпособыРемонтов.СторонняяОрганизация Тогда
			Адрес = Ссылка.АдресРемонта; 
		Иначе
			Адрес = Ссылка.МестоРемонта.Владелец.Адрес; 
		КонецЕсли; 
		Зоны = Адрес.ГеографическаяЗона;
		Если Ложь
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте 
			Или (Истина
				И (Ложь
					Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая 
					Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована)
				И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан))
		Тогда
			УчетныеЗоныЗон = упУчетТранспортныхСредств.УчетныеЗоныЗон(Зоны); 
			Если УчетныеЗоныЗон.Количество() > 0 Тогда
				Зона = УчетныеЗоныЗон[0].Родитель;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	Если Ложь
		Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая 
		Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована
		Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению
		Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт
	Тогда
		Если ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) Тогда
			ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
			ДвиженияСостояние.Период = РеквизитыОбъекта.Дата;
			ДвиженияСостояние.Транспорт = РеквизитыОбъекта.ТранспортноеСредство;
			ДвиженияСостояние.ПланАктуален = 1;
			ДвиженияСостояние.Документ = Ссылка;
			ДвиженияСостояние.ДатаНачала = РеквизитыОбъекта.НачалоРемонтаПлан;
			ДвиженияСостояние.ДатаОкончания = РеквизитыОбъекта.ОкончаниеРемонтаПлан;
			ДвиженияСостояние.ЗонаНачала = Зона;
			ДвиженияСостояние.ЗонаОкончания = Зона;
			ДвиженияСостояние.СостояниеТранспорта = Справочники.упСостоянияТС.Ремонт;
	    	ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС", Истина);
		КонецЕсли; 
	//ИначеЕсли Ложь
	//	Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте 
	//Тогда
	//	ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
	//	ДвиженияСостояние.Период = РеквизитыОбъекта.Дата;
	//	ДвиженияСостояние.Транспорт = РеквизитыОбъекта.ТранспортноеСредство;
	//	ДвиженияСостояние.СостояниеТранспорта = Справочники.упСостоянияТС.Ремонт;
	//	ДвиженияСостояние.ПланАктуален = Истина;
	//	ДвиженияСостояние.ФактНачала = Истина;
	//	ДвиженияСостояние.Документ = Ссылка;
	//	ДвиженияСостояние.ДатаНачала = РеквизитыОбъекта.НачалоРемонтаФакт;
	//	Если ЗначениеЗаполнено(РеквизитыОбъекта.ВидРемонта.ВремяРаботПлан) Тогда
	//		ДлительностьРемонта = РеквизитыОбъекта.ВидРемонта.ВремяРаботПлан * 60 * 60;
	//	ИначеЕсли Истина
	//		И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан) 
	//		И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан) 
	//	Тогда 
	//		ДлительностьРемонта = РеквизитыОбъекта.ОкончаниеРемонтаПлан - РеквизитыОбъекта.НачалоРемонтаПлан;
	//	Иначе
	//		ДлительностьРемонта = 2 * 60 * 60;
	//	КонецЕсли; 
	//	ДвиженияСостояние.ДатаОкончания = ДвиженияСостояние.ДатаНачала + ДлительностьРемонта;
	//	ДвиженияСостояние.ЗонаНачала = Зона;
	//	ДвиженияСостояние.ЗонаОкончания = Зона;
	//    ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС", Истина);
	КонецЕсли; 
		
КонецПроцедуры // СформироватьДвиженияЗанятостьТранспортаЗаявкаНаРемонт()

// Процедура формирует движения по регистру "упПлановоеРасписаниеРаботыГаража".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияПлановоеРасписаниеРаботыГаражаЗаявкаНаРемонт(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПлан = Движения.упПлановоеРасписаниеРаботыГаража;
	ДвиженияПлан.Записывать = Истина;
	НеКонтролироватьРасписаниеРаботыГаража = ДополнительныеСвойства.НеКонтролироватьРасписаниеРаботыГаража;
	
	Статус = ДополнительныеСвойства.Статус;
	Если Истина
		И (Ложь 
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована 
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте 
			Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена)
		И ЗначениеЗаполнено(РеквизитыОбъекта.МестоРемонта)
		И ЗначениеЗаполнено(РеквизитыОбъекта.НачалоРемонтаПлан)
		И ЗначениеЗаполнено(РеквизитыОбъекта.ОкончаниеРемонтаПлан) 
		И НЕ НеКонтролироватьРасписаниеРаботыГаража
	Тогда
				   
		ДвиженияПлан.Очистить();
		ДвижениеПлан = ДвиженияПлан.Добавить();
		ДвижениеПлан.Гараж                = РеквизитыОбъекта.Гараж;
		ДвижениеПлан.МестоРемонта         = РеквизитыОбъекта.МестоРемонта;
		ДвижениеПлан.НачалоРемонта        = РеквизитыОбъекта.НачалоРемонтаПлан;
		ДвижениеПлан.ОкончаниеРемонта     = РеквизитыОбъекта.ОкончаниеРемонтаПлан;
		ДвижениеПлан.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
		ДвижениеПлан.ДополнительноеОборудование = Справочники.упДополнительноеОборудование.ПустаяСсылка();
		ДвижениеПлан.Агрегат                    = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка();
			
		Если РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
			ДвижениеПлан.ДополнительноеОборудование = РеквизитыОбъекта.ДополнительноеОборудованиеРемонта;
		ИначеЕсли РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
			ДвижениеПлан.Агрегат = РеквизитыОбъекта.Агрегат;
		Иначе
			ДвижениеПлан.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
		КонецЕсли;
		
		ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыГаража = Истина;
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияПлановоеРасписаниеРаботыГаражаЗаявкаНаРемонт()

// Функция - Провести проверки при переводе заявки в статус к выполнению
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Применить изменения.
//
Процедура ПровестиПроверкиПриПереводеЗаявкиНаРемонтВСтатусКВыполнению(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус	= ДополнительныеСвойства.Статус;
	Если Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена Тогда 
				
		ДополнительныеСвойства.Вставить("РассчитатьСтатус", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Проверяет наличие задания по заявке в статусе отличном от "Новый", "Отменено" без статуса.
//
// Параметры:
//  Заявка	 - ДокументСсылка.упЗаявкаНаРемонтИТОТС - заявка.
//  Отказ	 - Булево - признак неудачной проверки.
//
Процедура ЕстьРемонтПоЗаявке(Заявка, Отказ) Экспорт
	Результат = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРемонтИТОТС.Ссылка
	               |ИЗ
	               |	Документ.упРемонтИТОТС КАК упРемонтИТОТС
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРемонтовИТОТС.СрезПоследних КАК упСтатусыРемонтовИТОТС
	               |		ПО упРемонтИТОТС.Ссылка = упСтатусыРемонтовИТОТС.Документ
	               |ГДЕ
	               |	упРемонтИТОТС.ЗаявкаНаРемонтИТО = &ЗаявкаНаРемонтИТО
	               |	И НЕ(упСтатусыРемонтовИТОТС.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Новый), ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Отменен))
	               |				ИЛИ упСтатусыРемонтовИТОТС.Статус ЕСТЬ NULL)";
	Запрос.УстановитьПараметр("ЗаявкаНаРемонтИТО", Заявка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСообщения = Нстр("ru = 'Сначала необходимо отменить %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.Ссылка,,,Отказ);				
	КонецЕсли;
	
КонецПроцедуры // ЕстьРемонтПоЗаявке()

// Функция - Сформировать заявки на ремонт ИТОТС
//
// Параметры:
//  мсвОбъектыРемонта	 - Массив	 - ссылки.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Применить изменения.
// 
// Возвращаемое значение:
//  Массив - заявки на ремонт.
//
Функция СформироватьДокументыНаРемонтИТОТС(мсвОбъектыРемонта, Реквизиты, Отказ) Экспорт
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Документ", Реквизиты.Документ);

	Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
		ДанныеЗаполнения.Вставить("Валюта", упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета()); 	
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("Дата", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаполнения, "Дата", ТекущаяДатаСеанса()));
	ДанныеЗаполнения.Вставить("Организация",   Справочники.Организации.ОрганизацияПоУмолчанию()); 	
	ДанныеЗаполнения.Вставить("СпособРемонта", Реквизиты.СпособРемонта);
	ДанныеЗаполнения.Вставить("Гараж",         Реквизиты.Гараж);
	ДанныеЗаполнения.Вставить("МестоРемонта",  Реквизиты.МестоРемонта);
	ДанныеЗаполнения.Вставить("СервиснаяОрганизация", Реквизиты.СервиснаяОрганизация);
	
	Если Истина
		И ЗначениеЗаполнено(ДанныеЗаполнения.МестоРемонта) 
		И Реквизиты.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами
	Тогда
		сткРеквизитыМестаРемонта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.МестоРемонта, "ВидРемонтаПоУмолчанию");
		ДанныеЗаполнения.Вставить("ВидРемонта",   сткРеквизитыМестаРемонта.ВидРемонтаПоУмолчанию);
		
		
		Если ЗначениеЗаполнено(сткРеквизитыМестаРемонта.ВидРемонтаПоУмолчанию) Тогда
			ДанныеЗаполнения.Вставить("Работы", РаботыПоВидуРемонта(сткРеквизитыМестаРемонта.ВидРемонтаПоУмолчанию));
		КонецЕсли;
		
	КонецЕсли;
		
	ДанныеЗаполнения.Вставить("Автор", ПользователиКлиентСервер.ТекущийПользователь());
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений") Тогда
		ДанныеЗаполнения.Вставить("Подразделение", Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), ДанныеЗаполнения.Автор));
	КонецЕсли;
		
	Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
		ДанныеЗаполнения.Вставить("ЧасовойПояс", упСервисныеФункции.ЧасовойПоясВидаПеревозки());	
	КонецЕсли;
	
	мсвЗаявки = Новый Массив;
	
	Для каждого ОбъектРемонта Из мсвОбъектыРемонта Цикл
		ОтказТекущий = Ложь;
		ЗаявкаСсылка = СформироватьДокументНаРемонтИТОТС(ОбъектРемонта, ДанныеЗаполнения, ОтказТекущий);
		Если НЕ ОтказТекущий Тогда
			сткДокументРемонта  = Новый Структура();
			сткДокументРемонта.Вставить("ОбъектРемонта",  ОбъектРемонта.ОбъектРемонта);
			сткДокументРемонта.Вставить("ДокументСсылка", ЗаявкаСсылка);
			мсвЗаявки.Добавить(сткДокументРемонта);
		КонецЕсли;
	КонецЦикла;
	
	Возврат мсвЗаявки;
	
КонецФункции

// Функция - Сформировать заявку на ремонт ИТОТС
//
// Параметры:
//  сткОбъектРемонта - Структура	 - основания для заполнения.
//  ДанныеЗаполнения - Структура	 - данные для заполнения документа:
//  	* ОбъектРемонта - СправочникСсылка.упДополнительноеОборудование,
//  	                  СправочникСсылка.упТранспортныеСредства        - объект ремонта.
//  	* МестоРемонта  - СправочникСсылка.упМестаРемонта                - место ремонта.
//  Отказ			 - Булево		 - Истина, если не удалось сформировать документ.
// 
// Возвращаемое значение:
//  ДокументСсылка.упЗаявкаНаРемонтИТОТС - заявка на ремонт.
//
Функция СформироватьДокументНаРемонтИТОТС(сткОбъектРемонта, ДанныеЗаполнения, Отказ) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения.Документ) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда
		ДокОбъект = Документы.упЗаявкаНаРемонтИТОТС.СоздатьДокумент();	
		Статус    = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая;
	Иначе	
		ДокОбъект = Документы.упРемонтИТОТС.СоздатьДокумент();	
		Статус    = Перечисления.упСтатусыРемонтаИТОТС.Новый;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокОбъект, ДанныеЗаполнения);
		
	Если ТипЗнч(сткОбъектРемонта.ОбъектРемонта) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
		ДокОбъект.ТранспортноеСредство = сткОбъектРемонта.ОбъектРемонта;	
		ДокОбъект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство;
		ДокОбъект.Организация   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(сткОбъектРемонта.ОбъектРемонта, "Партнер.Организация");
	Иначе
		ДокОбъект.ДополнительноеОборудованиеРемонта =  сткОбъектРемонта.ОбъектРемонта;
		ДокОбъект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(сткОбъектРемонта.ВидРемонта) Тогда
		ДокОбъект.ВидРемонта    = сткОбъектРемонта.ВидРемонта;	
	КонецЕсли;
	
	ДокОбъект.НачалоРемонтаПлан    = сткОбъектРемонта.НачалоРемонтаПлан;
	ДокОбъект.ОкончаниеРемонтаПлан = сткОбъектРемонта.ОкончаниеРемонтаПлан;
	
	Работы = Неопределено;
	Если Истина
		И ДанныеЗаполнения.Свойство("Работы", Работы)
		И ЗначениеЗаполнено(Работы) 
	Тогда
		Для каждого Работа Из Работы Цикл
			
			НоваяРабота = ДокОбъект.Работы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяРабота, Работа);
			НоваяРабота.Валюта = ДокОбъект.Валюта;
			
		КонецЦикла;
	КонецЕсли;	

	ДокОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);	
	
	Если НЕ Отказ Тогда
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			ЗаявкаСсылка = ДокОбъект.Ссылка;
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОшибкаФормированияДокументаНаРемонт", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		КонецПопытки;
	КонецЕсли;
	
	Возврат ЗаявкаСсылка;
	
КонецФункции

// Процедура - Установить статус документа ремонта
//
// Параметры:
//  ДокументРемонт	 - ДокументСсылка.упРемонтИТОТС 	 - ремонт
//  Статус		 - ПеречислениеСсылка.упСтатусыРемонтаИТОТС 	 - статус.
//  ОписаниеОшибки	 - Строка	 - описание ошибки.
//  Отказ			- Булево	 - Истина, если ошибка.
//
Процедура УстановитьСтатусДокументаРемонта(ДокументРемонт, Статус, ОписаниеОшибки, Отказ) Экспорт
	
	Если Истина
		И Статус <> Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена
		И Статус <> Перечисления.упСтатусыРемонтаИТОТС.Отменен 
	Тогда
		Документобъект = ДокументРемонт.ПолучитьОбъект();
		Отказ = НЕ Документобъект.ПроверитьЗаполнение();
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		упРаботаСоСтатусами.УстановитьСтатусДокументу(ДокументРемонт, Статус, ОписаниеОшибки, Отказ); 	
	КонецЕсли;
			
КонецПроцедуры

// Функция - Работы по виду ремонта
//
// Параметры:
//  ВидРемонта	 - СправочникСсылка.упВидыРемонтов  - вид ремонта.
// 
// Возвращаемое значение:
//  Результат - ТаблицаЗначений - таблица с колонками:
//  * РаботаПоРемонту  - СправочникСсылка.упРаботыПоРемонту - работа.
//  * Количество       - Число - количество.
//  * СтатьяРасходоы   - СправочникСсылка.упСтатьиДоходовРасходов - статья.
//
Функция РаботыПоВидуРемонта(ВидРемонта) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВидыРемонтовРаботыПоРемонту.РаботаПоРемонту КАК РаботаПоРемонту,
	|	упВидыРемонтовРаботыПоРемонту.Количество КАК Количество,
	|	упВидыРемонтовРаботыПоРемонту.СтатьяРасходов КАК СтатьяРасходов,
	|	ЕСТЬNULL(упЦеныРаботПоРемонтуСрезПоследних.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(упЦеныРаботПоРемонтуСрезПоследних.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта
	|ИЗ
	|	Справочник.упВидыРемонтов.РаботыПоРемонту КАК упВидыРемонтовРаботыПоРемонту
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упЦеныРаботПоРемонту.СрезПоследних КАК упЦеныРаботПоРемонтуСрезПоследних
	|		ПО упВидыРемонтовРаботыПоРемонту.РаботаПоРемонту = упЦеныРаботПоРемонтуСрезПоследних.РаботаПоРемонту
	|ГДЕ
	|	упВидыРемонтовРаботыПоРемонту.Ссылка = &ВидРемонта";
	Запрос.УстановитьПараметр("ВидРемонта", ВидРемонта);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

// Функция - проверяет возможность перевода в статус к выполнению.
//
// Параметры:
//  ТекущийСтатус - ПеречислениеСсылка - Ссылка на элемент.
//  ТребуетсяСогласованиеЗаявкиНаРемонт - Булево - Флаг.
//
// Возвращаемое значение:
//  Структура - Данные результата обработки.
//
Функция РазрешеноПеревестиКВыполнению(ТекущийСтатус, ТребуетсяСогласованиеЗаявкиНаРемонт) Экспорт

	сткВозврата = Новый Структура("Результат, ТекстОшибки", Ложь, "");
	
	Если (ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована
		ИЛИ ((ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая
		ИЛИ ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.НеСогласована)
		И Не ТребуетсяСогласованиеЗаявкиНаРемонт)) Тогда 
		
		сткВозврата.Вставить("Результат", Истина);
	Иначе
		Если ТребуетсяСогласованиеЗаявкиНаРемонт Тогда 
			сткВозврата.Вставить("ТекстОшибки", НСтр("ru = 'Статус заявки должен быть ""Согласована""'"));
		Иначе	
			сткВозврата.Вставить("ТекстОшибки", НСтр("ru = 'Статус заявки должен быть ""Новая""'"));
		КонецЕсли;	
	КонецЕсли;
	
	Возврат сткВозврата;
	
КонецФункции // РазрешеноПеревестиКВыполнению()

#КонецОбласти
