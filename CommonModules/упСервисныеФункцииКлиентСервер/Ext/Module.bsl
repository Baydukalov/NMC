#Область ПрограммныйИнтерфейс

// Преобразует число в строку - шестнадцатиричное представление.
// 
// Параметры:
//	ЧислоДес    - Число - для которого надо получить представление.
//	КолвоЗнаков - Число - количество симаолов в строке. Если после препразования получилось меньше,
//                чем КолвоЗнаков, то строка будет дополнена справа символми "0" до нужной длины.
//
// Возвращаемое значение:
//	Строка - шестнадцатиричное представление числа.
//
Функция ЧислоВHexСтр(ЧислоДес, КолвоЗнаков = 2) Экспорт
	
	HexСтр  = "";
	Остаток = ЧислоДес;
	Пока Остаток > 0 Цикл
		HexСтр  = ЦифруВHexСтр(Остаток % 16) + HexСтр; 
		Остаток = Цел(Остаток / 16);
	КонецЦикла;
	
	Пока КолвоЗнаков > СтрДлина(HexСтр) Цикл
		HexСтр = "0" + HexСтр;
	КонецЦикла;
	
	Возврат HexСтр;
	
КонецФункции

// Преобразует цифру в шестнадцатиричное представление.
//
// Параметры:
//	ЦифраДес - Число - цифра (число).
//
// Возвращаемое значение:
//	Строка - символ (строка), шестнадцатиричное представление цифры.
//
Функция ЦифруВHexСтр(ЦифраДес)
	
	Если      1  = ЦифраДес Тогда
		текСтр = "1";
	ИначеЕсли 2  = ЦифраДес Тогда
		текСтр = "2";
	ИначеЕсли 3  = ЦифраДес Тогда
		текСтр = "3";
	ИначеЕсли 4  = ЦифраДес Тогда
		текСтр = "4";
	ИначеЕсли 5  = ЦифраДес Тогда
		текСтр = "5";
	ИначеЕсли 6  = ЦифраДес Тогда
		текСтр = "6";
	ИначеЕсли 7  = ЦифраДес Тогда
		текСтр = "7";
	ИначеЕсли 8  = ЦифраДес Тогда
		текСтр = "8";
	ИначеЕсли 9  = ЦифраДес Тогда
		текСтр = "9";
	ИначеЕсли 10  = ЦифраДес Тогда
		текСтр = "A";
	ИначеЕсли 11  = ЦифраДес Тогда
		текСтр = "B";
	ИначеЕсли 12  = ЦифраДес Тогда
		текСтр = "C";
	ИначеЕсли 13  = ЦифраДес Тогда
		текСтр = "D";
	ИначеЕсли 14  = ЦифраДес Тогда
		текСтр = "E";
	ИначеЕсли 15  = ЦифраДес Тогда
		текСтр = "F";
	Иначе
		текСтр = "0";
	КонецЕсли;
	
	Возврат текСтр;
	
КонецФункции

// Преобразует шестнадцатиричную строку в десятеричное число.
// 
// Параметры:
//	HexСтр - Строка - шестнадцатиричное представление числа.
//
// Возвращаемое значение:
//	Число - десятеричное значение строки.
//
Функция HexСтрВЧисло(HexСтр) Экспорт
	
	ЧислоДес = 0;

	Для Сч = 0 По СтрДлина(HexСтр) - 1 Цикл
		ЧислоДес = ЧислоДес + (HexЦифруВЧисло(Сред(HexСтр, СтрДлина(HexСтр) - Сч, 1)) * Pow(16, Сч));
	КонецЦикла;
	
	Возврат ЧислоДес;
	
КонецФункции

// Преобразует цифру в шестнадцатиричном представлении в десятеричное число.
//
// Параметры:
//	ЦифраHex - Число - цифра в шестнадцатиричном представлении (строка).
//
// Возвращаемое значение:
//	Число - десятеричное представление цифры.
//
Функция HexЦифруВЧисло(ЦифраHex)
	
	Если      "1"  = ЦифраHex Тогда
		текЧисло = 1;
	ИначеЕсли "2"  = ЦифраHex Тогда
		текЧисло = 2;
	ИначеЕсли "3"  = ЦифраHex Тогда
		текЧисло = 3;
	ИначеЕсли "4"  = ЦифраHex Тогда
		текЧисло = 4;
	ИначеЕсли "5"  = ЦифраHex Тогда
		текЧисло = 5;
	ИначеЕсли "6"  = ЦифраHex Тогда
		текЧисло = 6;
	ИначеЕсли "7"  = ЦифраHex Тогда
		текЧисло = 7;
	ИначеЕсли "8"  = ЦифраHex Тогда
		текЧисло = 8;
	ИначеЕсли "9"  = ЦифраHex Тогда
		текЧисло = 9;
	ИначеЕсли "A"  = ЦифраHex Тогда
		текЧисло = 10;
	ИначеЕсли "B"  = ЦифраHex Тогда
		текЧисло = 11;
	ИначеЕсли "C"  = ЦифраHex Тогда
		текЧисло = 12;
	ИначеЕсли "D"  = ЦифраHex Тогда
		текЧисло = 13;
	ИначеЕсли "E"  = ЦифраHex Тогда
		текЧисло = 14;
	ИначеЕсли "F"  = ЦифраHex Тогда
		текЧисло = 15;
	Иначе
		текЧисло = 0;
	КонецЕсли;
	
	Возврат текЧисло;
	
КонецФункции

// Функция преобразовывает исходный шрифт в абсолютный.
//
// Параметры:
//  ИсходныйШрифт - Шрифт - преобразуемый шрифт.
//
// Возвращаемое значение:
//  Шрифт - абсолютный шрифт.
//
Функция ПреобразоватьШрифтВАбсолютный(ИсходныйШрифт) Экспорт
	
	#Если Сервер Тогда
	УстановитьПривилегированныйРежим(Истина);
	#КонецЕсли
	
	текТаблица = Новый ТабличныйДокумент;
	текТаблица.Область("R1C1").Шрифт = ИсходныйШрифт;
	текТаблица.Записать(КаталогВременныхФайлов() + "ПреобразованиеШрифта.mxl", ТипФайлаТабличногоДокумента.MXL7); 
	текТаблица.Прочитать(КаталогВременныхФайлов() + "ПреобразованиеШрифта.mxl");
	
	#Если Сервер Тогда
	УстановитьПривилегированныйРежим(Ложь);
	#КонецЕсли
	
	Возврат текТаблица.Область("R1C1").Шрифт;
	
КонецФункции // ПреобразоватьШрифтВАбсолютный()	

// Функция преобразовывает исходный цвет в абсолютный.
//
// Параметры:
//  ИсходныйЦвет - Цвет - преобразуемый цвет.
//
// Возвращаемое значение:
//  Цвет - абсолютный цвет.
//
Функция ПреобразоватьЦветВАбсолютный(ИсходныйЦвет) Экспорт
	
	#Если Сервер Тогда
	УстановитьПривилегированныйРежим(Истина);
	#КонецЕсли
	
	текТаблица = Новый ТабличныйДокумент;
	текТаблица.Область("R1C1").ЦветТекста = ИсходныйЦвет;
	текТаблица.Записать(КаталогВременныхФайлов() + "ПреобразованиеЦвета.mxl", ТипФайлаТабличногоДокумента.MXL7); 
	текТаблица.Прочитать(КаталогВременныхФайлов() + "ПреобразованиеЦвета.mxl");
	
	#Если Сервер Тогда
	УстановитьПривилегированныйРежим(Ложь);
	#КонецЕсли
	
	Возврат текТаблица.Область("R1C1").ЦветТекста;
	
КонецФункции // ПреобразоватьЦветВАбсолютный()	

// Преобразует шестнадцатиричную строку в цвет.
// 
// Параметры:
//	HexСтр - Строка - шестнадцатиричное представление цвета.
//
// Возвращаемое значение:
//	Цвет - абсолютный цвет.
//
Функция HexСтрВЦвет(HexСтр) Экспорт

	Возврат Новый Цвет(HexСтрВЧисло(Сред(HexСтр, 2, 2)),
					   HexСтрВЧисло(Сред(HexСтр, 4, 2)),
					   HexСтрВЧисло(Сред(HexСтр, 6, 2)));
	
КонецФункции

// Преобразует цвет в строку - шестнадцатиричное представление.
// 
// Параметры:
//	Цвет - Цвет - для которого надо получить представление.
//
// Возвращаемое значение:
//	Строка - шестнадцатиричное представление цвета.
//
Функция ЦветВHexСтр(Цвет) Экспорт
	
	Возврат "#" + ЧислоВHexСтр(Цвет.Красный) 
				+ ЧислоВHexСтр(Цвет.Зеленый)
				+ ЧислоВHexСтр(Цвет.Синий);
	
КонецФункции

// Преобразует байт в шестнадцатеричном представлении в двоичное представление
//
// Параметры:
//  HexСтрока	 - Строка	 - байт в шестнадцатеричном представлении.
// 
// Возвращаемое значение:
//   Строка - двоичное представление.
//
Функция ПреобразоватьБайтHexВСтрокуBin(ЗНАЧ HexСтрока) Экспорт
	
	ПервыйСимвол = Сред(HexСтрока, 1, 1);
	ВторойСимвол = Сред(HexСтрока, 2, 1);
	Возврат ПреобразоватьЧислоHexВBin(ПервыйСимвол) + ПреобразоватьЧислоHexВBin(ВторойСимвол);
	
КонецФункции

// Преобразует число в шестнадцатеричном представлении в двоичное представление
//
// Параметры:
//  ТекущийСимвол	 - Строка	 - число в шестнадцатеричном представлении.
// 
// Возвращаемое значение:
//   Строка - двоичное представление
//
Функция ПреобразоватьЧислоHexВBin(ЗНАЧ ТекущийСимвол) Экспорт
	
	Результат = "";
	Если ТекущийСимвол = "0" Тогда
		Результат = "0000";
	ИначеЕсли ТекущийСимвол = "1" Тогда
		Результат = "0001";
	ИначеЕсли ТекущийСимвол = "2" Тогда
		Результат = "0010";
	ИначеЕсли ТекущийСимвол = "3" Тогда
		Результат = "0011";
	ИначеЕсли ТекущийСимвол = "4" Тогда
		Результат = "0100";
	ИначеЕсли ТекущийСимвол = "5" Тогда
		Результат = "0101";
	ИначеЕсли ТекущийСимвол = "6" Тогда
		Результат = "0110";
	ИначеЕсли ТекущийСимвол = "7" Тогда
		Результат = "0111";
	ИначеЕсли ТекущийСимвол = "8" Тогда
		Результат = "1000";
	ИначеЕсли ТекущийСимвол = "9" Тогда
		Результат = "1001";		
	ИначеЕсли ТекущийСимвол = "A" Тогда
		Результат = "1010";
	ИначеЕсли ТекущийСимвол = "B" Тогда
		Результат = "1011";
	ИначеЕсли ТекущийСимвол = "C" Тогда
		Результат = "1100";
	ИначеЕсли ТекущийСимвол = "D" Тогда
		Результат = "1101";
	ИначеЕсли ТекущийСимвол = "E" Тогда
		Результат = "1110";
	ИначеЕсли ТекущийСимвол = "F" Тогда
		Результат = "1111";
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

// Преобразует число в двоичном представлении в шестнадцатеричное представление
//
// Параметры:
//  ТекущийСимвол	 - Строка	 - число в двоичном представлении
// 
// Возвращаемое значение:
//  Строка - число в шестнадцатеричном представлении
//
Функция ПреобразоватьЧислоBinВHex(ЗНАЧ ТекущийСимвол) Экспорт
	
	Результат = "";
	Если ТекущийСимвол = "0000" Тогда
		Результат = "0";
	ИначеЕсли ТекущийСимвол = "0001" Тогда
		Результат = "1";
	ИначеЕсли ТекущийСимвол = "0010" Тогда
		Результат = "2";
	ИначеЕсли ТекущийСимвол = "0011" Тогда
		Результат = "3";
	ИначеЕсли ТекущийСимвол = "0100" Тогда
		Результат = "4";
	ИначеЕсли ТекущийСимвол = "0101" Тогда
		Результат = "5";
	ИначеЕсли ТекущийСимвол = "0110" Тогда
		Результат = "6";
	ИначеЕсли ТекущийСимвол = "0111" Тогда
		Результат = "7";
	ИначеЕсли ТекущийСимвол = "1000" Тогда
		Результат = "8";
	ИначеЕсли ТекущийСимвол = "1001" Тогда
		Результат = "9";		
	ИначеЕсли ТекущийСимвол = "1010" Тогда
		Результат = "A";
	ИначеЕсли ТекущийСимвол = "1011" Тогда
		Результат = "B";
	ИначеЕсли ТекущийСимвол = "1100" Тогда
		Результат = "C";
	ИначеЕсли ТекущийСимвол = "1101" Тогда
		Результат = "D";
	ИначеЕсли ТекущийСимвол = "1110" Тогда
		Результат = "E";
	ИначеЕсли ТекущийСимвол = "1111" Тогда
		Результат = "F";
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

// Преобразует шестнадцетиричное число в строку кодировки ASCII
//
// Параметры:
//  HexСтрока	 - Строка	 - шестнадцетиричное число;
// 
// Возвращаемое значение:
//  Строка - строка в кодировке ASCII
//
Функция ПреобразоватьHexВСтрокуASCII(ЗНАЧ HexСтрока) Экспорт
	
	Результат = "";
	Сч = 1;
	Пока Сч < СтрДлина(HexСтрока) Цикл
		КодСимвола = ПреобразоватьСтрокуHexВInt(Сред(HexСтрока, Сч, 2));
		Если КодСимвола > 33 И КодСимвола <= 127 Тогда
			Результат = Результат + Символ(КодСимвола);
		КонецЕсли;
		Сч = Сч + 2;
	КонецЦикла; 
	
	Возврат СтрЗаменить(СокрЛП(Результат), "", "");
	
КонецФункции

// Преобразует шестнадцатиричную строку в десятичную
//
// Параметры:
//  СтрокаH	 - Строка	 - шестнадцатиричная строка.
// 
// Возвращаемое значение:
// Число  - число.
//
Функция ПреобразоватьСтрокуHexВInt(ЗНАЧ СтрокаH) Экспорт
	
	ПрефиксH = Лев(СтрокаH, 2);
	Если ПрефиксH="0x" Или ПрефиксH="0X" Или ПрефиксH="0х" Или ПрефиксH="0Х" Тогда
		СтрокаH=Сред(СтрокаH,3);
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаH) Тогда
		Возврат 0;
	КонецЕсли;
	
	ДлинаH=СтрДлина(СтрокаH);
	ЧислоD=0;
	Для о = 1 По ДлинаH Цикл
		ЦифраH = Сред(СтрокаH,о,1);
		ЦифраD = ПреобразоватьЧислоHexВDec(ЦифраH);
		Если ЦифраD < 0 Тогда
			Возврат -1; // нарушение формата 16-тиричного числа
		КонецЕсли; 
		ЧислоD = 16*ЧислоD + ЦифраD;
	КонецЦикла; 
	Возврат ЧислоD;
	
КонецФункции

// Преобразует шестнадцатиричную цифру в десятичную
//
// Параметры:
//  ЦифраH - Строка	 - шестнадцатиричная цифра
// 
// Возвращаемое значение:
//  Число - десятичная цифра
//
Функция ПреобразоватьЧислоHexВDec(ЦифраH) Экспорт
	
	Если ЦифраH="0" ИЛИ ЦифраH="1" ИЛИ ЦифраH="2" ИЛИ ЦифраH="3" ИЛИ ЦифраH="4" ИЛИ ЦифраH="5" ИЛИ ЦифраH="6" ИЛИ ЦифраH="7" ИЛИ ЦифраH="8" ИЛИ ЦифраH="9" Тогда
		Возврат Цел(ЦифраH);
	ИначеЕсли ЦифраH="a" ИЛИ ЦифраH="A" Тогда
		Возврат 10;
	ИначеЕсли ЦифраH="b" ИЛИ ЦифраH="B" Тогда
		Возврат 11;
	ИначеЕсли ЦифраH="c" ИЛИ ЦифраH="C" Тогда
		Возврат 12;
	ИначеЕсли ЦифраH="d" ИЛИ ЦифраH="D" Тогда
		Возврат 13;
	ИначеЕсли ЦифраH="e" ИЛИ ЦифраH="E" Тогда
		Возврат 14;
	ИначеЕсли ЦифраH="f" ИЛИ ЦифраH="F" Тогда
		Возврат 15;
	Иначе
		Возврат -1;
	КонецЕсли;
	
КонецФункции

// Получить строку даты начала.
//
// Возвращаемое значение:
//	Строка - Период начала.
//
Функция НачалоОтсчета() Экспорт
	Возврат '00010101' 	
КонецФункции

Функция ВыделитьЧастьСтроки(СтрокаИсходная, Подстрока, Цвет = Неопределено) Экспорт
	
	Если Цвет = Неопределено Тогда
		ЦветВыделения = WebЦвета.Зеленый;
	Иначе	
		ЦветВыделения = Цвет;
	КонецЕсли;
	
	Если Лев(СтрокаИсходная, СтрДлина(Подстрока)) = Подстрока Тогда
		Строка1Формат = Новый ФорматированнаяСтрока(Подстрока, ,ЦветВыделения);
		
		ОстатокПодстроки = Прав(СтрокаИсходная, СтрДлина(СтрокаИсходная) - СтрДлина(Подстрока));
		Строка2Формат = Новый ФорматированнаяСтрока(ОстатокПодстроки);
		
		Результат = Новый ФорматированнаяСтрока(Строка1Формат, Строка2Формат);
	Иначе
		Результат = СтрокаИсходная;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// Установить параметр выбора поля формы.
//
// Параметры:
//  Поле		 - ПолеФормы	 - Предназначен для отображения и редактирования реквизитов формы не табличных типов.
//  ИмяОтбора	 - Строка		 - Наименование.
//  Значение	 - Произвольное - Значение.
//  Удалить	 - Булево		 - Выполнить.
//  ОчиститьВсе - Булево		 - Выполнить.
//
Процедура УстановитьПараметрВыбораПоляФормы(Поле, ИмяОтбора, Значение = Неопределено, Удалить = Ложь, ОчиститьВсе = Ложь) Экспорт 
	
	Массив = Новый Массив();
	Если Не ОчиститьВсе Тогда
		Для Каждого ПараметрВыбора Из Поле.ПараметрыВыбора Цикл
			Если ПараметрВыбора.Имя = "Отбор." + ИмяОтбора Тогда
				Продолжить;
			КонецЕсли; 
			Массив.Добавить(ПараметрВыбора);
		КонецЦикла; 
	КонецЕсли;
	Если Не Удалить Тогда
		Массив.Добавить(Новый ПараметрВыбора("Отбор." + ИмяОтбора, Значение));
	КонецЕсли; 
	Поле.ПараметрыВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры

// Функция - Преобразует строковое представление даты в тип дата
//
// Параметры:
//  СтроковоеПредставлениеДаты	 - Строка - в формате "дд.ММ.гггг ч:мм"
// 
// Возвращаемое значение:
//   Дата - преобразованная дата
//
Функция СтрокаВДату(СтроковоеПредставлениеДаты) Экспорт
	
	Результат = '00010101';
	
	мсвДатаВремя = СтрРазделить(СтроковоеПредставлениеДаты," ");
	Если мсвДатаВремя.Количество() = 2 Тогда
		мсвДата		= СтрРазделить(мсвДатаВремя[0], ".");
		мсвВремя	= СтрРазделить(мсвДатаВремя[1], ":");
		
		Если мсвДата.Количество() = 3
				И мсвВремя.Количество() >= 2 Тогда
			Результат = Дата(мсвДата[2], мсвДата[1], мсвДата[0], мсвВремя[0], мсвВремя[1], ?(мсвВремя.Количество() = 3, мсвВремя[2], 0));
		КонецЕсли;
	
	КонецЕсли;
	
	
	Возврат Результат;
	
КонецФункции

// Функция - Возвращает строку в формате, соответствующему формату стандартного авто подбора.
//
// Параметры:
//  Строка		 - Строка - Исходная строка.
//  Подстрока	 - Строка - Подстрока.
// 
// Возвращаемое значение:
//  ФорматированнаяСтрока - Строка в которой подстрока выделена специальным образом.
//
Функция ФорматированнаяСтрокаДляАвтоПодбора(Строка, Подстрока) Экспорт
	
	Результат = "";
	
	Шрифт 	= Новый Шрифт(,,Истина);
	Цвет	= Новый Цвет(0,153,6);
	
	ПозицияПодстроки = СтрНайти(ВРег(Строка), ВРег(Подстрока));
	мсвФорматированнаяСтрока = Новый Массив;
	ДлинаПодстроки 	= СтрДлина(Подстрока);
	ДлинаСтроки		= СтрДлина(Строка);
	
	Если ПозицияПодстроки = 1 Тогда
		мсвФорматированнаяСтрока.Добавить(Новый ФорматированнаяСтрока(Лев(Строка, ДлинаПодстроки), Шрифт, Цвет));
		мсвФорматированнаяСтрока.Добавить(Прав(Строка, ДлинаСтроки - ДлинаПодстроки));
	ИначеЕсли ПозицияПодстроки + ДлинаПодстроки - 1 = ДлинаСтроки Тогда	
		мсвФорматированнаяСтрока.Добавить(Лев(Строка, ПозицияПодстроки - 1));	
		мсвФорматированнаяСтрока.Добавить(Новый ФорматированнаяСтрока(Прав(Строка, ДлинаПодстроки), Шрифт, Цвет));	
	Иначе	
		мсвФорматированнаяСтрока.Добавить(Лев(Строка, ПозицияПодстроки - 1));	
		мсвФорматированнаяСтрока.Добавить(Новый ФорматированнаяСтрока(Сред(Строка, ПозицияПодстроки, ДлинаПодстроки), Шрифт, Цвет));	
		мсвФорматированнаяСтрока.Добавить(Прав(Строка, ДлинаСтроки - ПозицияПодстроки - ДлинаПодстроки + 1));
	КонецЕсли;
	
	Результат = Новый ФорматированнаяСтрока(мсвФорматированнаяСтрока);
	
	Возврат Результат;
		
КонецФункции
 
// Функция - Преобразует исходную строку из транслита в кириллицу.
//  Используется для приблизительного преобразования, так как однозначного
//  соответствия нет.
//
// Параметры:
//  Строка - Строка	 - строка в транслите
// 
// Возвращаемое значение:
//  Строка - строка в кириллице.
//
Функция СтрокаКириллицей(Знач Строка) Экспорт
	
	Результат = "";
	
	Соответствие = СоответствиеЛатиницыИКириллицы();
	
	Для Позиция = 1 По СтрДлина(Строка) Цикл
		Символ = Сред(Строка, Позиция, 1);
		СимволКириллицей = Соответствие[НРег(Символ)]; // Поиск соответствия без учета регистра.
		Если СимволКириллицей = Неопределено Тогда
			// Другие символы остаются "как есть".
			СимволКириллицей = Символ;
		Иначе
			Если ТипЗнч(СимволКириллицей) = Тип("Строка") Тогда
				Если Символ = ВРег(Символ) Тогда
					СимволКириллицей = ТРег(СимволКириллицей); // восстанавливаем регистр
				КонецЕсли;
			ИначеЕсли ТипЗнч(СимволКириллицей) = Тип("Структура") Тогда	
	       		сткСимволы = СимволКириллицей;
				ВерхнийРегистр 		= Символ = ВРег(Символ);
				СимволКириллицей 	= "";
				Для каждого КомбинацияСимволовКириллицей Из сткСимволы Цикл
									
					Символ = Сред(Строка, Позиция, СтрДлина(КомбинацияСимволовКириллицей.Ключ));
										
					Если Нрег(Символ) = КомбинацияСимволовКириллицей.Ключ Тогда
						Если ВерхнийРегистр Тогда
							СимволКириллицей = ТРег(КомбинацияСимволовКириллицей.Значение);
						Иначе	
							СимволКириллицей = КомбинацияСимволовКириллицей.Значение;
						КонецЕсли;
						Позиция = Позиция + СтрДлина(КомбинацияСимволовКириллицей.Ключ) - 1;
						Прервать;
	    			КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
		КонецЕсли;
		Результат = Результат + СимволКириллицей;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция СоответствиеЛатиницыИКириллицы()
	// Транслитерация, используемая в загранпаспортах 1997-2010.
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("a","а");
	Соответствие.Вставить("b","б");
	Соответствие.Вставить("v","в");
	Соответствие.Вставить("g","г");
	Соответствие.Вставить("d","д");
	Соответствие.Вставить("e","е");
	
	сткВарианты = Новый Структура("zh,z","ж","з");
	Соответствие.Вставить("z",сткВарианты);
	
	Соответствие.Вставить("i","и");
	
	сткВарианты = Новый Структура("kh,k","х","к");
	Соответствие.Вставить("k",сткВарианты);
		
	Соответствие.Вставить("l","л");
	Соответствие.Вставить("m","м");
	Соответствие.Вставить("n","н");
	Соответствие.Вставить("o","о");
	Соответствие.Вставить("p","п");
	Соответствие.Вставить("r","р");
	
	сткВарианты = Новый Структура("shch,sh,s","щ","ш","с");
	Соответствие.Вставить("s",сткВарианты);
	
	сткВарианты = Новый Структура("ts,t","ц","т");
	Соответствие.Вставить("t",сткВарианты);
	
	Соответствие.Вставить("u","у");
	Соответствие.Вставить("f","ф");
	
	сткВарианты = Новый Структура("ch","ч");
	Соответствие.Вставить("c",сткВарианты);
	
	сткВарианты = Новый Структура("ya,yu,y","я","ю","й");
	Соответствие.Вставить("y",сткВарианты);
	//Соответствие.Вставить("y","ы");
	
	Возврат Соответствие;
КонецФункции

// Функция - Новый диагностическое сообщение.
//
// Параметры:
//  мсвПриемникиРасшифровки - Массив - Куда и в какой детализации выводить расшифровку. Содержит:
//	* ЭлементМассива - Структура с полями:
//			** Приемник - ТекстовыйДокумент, ТабличныйДокумент, 
//                           ЖурналРегистрации, Неопределено - если Неопределено, то выводится на экран.
//			** ЗапрашиваемыйУровеньСообщений - Число - запрашиваемый уровень сообщений:
//											2 - выводятся только ошибки,
//											1 - переменные и их значения,
//											0 - поясняющие сообщения.
//  ТипСтруктуры - Строка - пока только "РасчетыПоПравиламРасчета", отвечает за.
// 
// Возвращаемое значение:
//   Структура - расшифровка.
//
Функция Новый_СтруктураТрассировки(мсвПриемникиРасшифровки = Неопределено, ТипСтруктуры = "") Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ПриемникиРасшифровки", 		мсвПриемникиРасшифровки);														
	сткРезультат.Вставить("ТипСтруктуры",				ТипСтруктуры);
	сткРезультат.Вставить("ВидОперации",				ТипСтруктуры);
	
	ИнициализироватьСтруктуруРасшифровки(сткРезультат);
	
	Возврат сткРезультат;
		
КонецФункции

// Добавляет дополнительное сообщение расшифровки.
//
// Параметры:
//	сткТрассировка - Структура - Параметры трассировки.
//	ИмяСтруктуры - Строка - Наименование.
//	ИмяПараметра - Строка - Наименование.
//	ЭтоНовыйЭлемент - Булево - Флаг.
//	ЗначениеПараметра - Произвольное - Значение.
//	УровеньСообщения - Число - Уровень.
//	ДополнительныеПараметры - Структура - Параметры.
//
Процедура ДобавитьСообщениеРасшифровки(сткТрассировка, ИмяСтруктуры = "", ИмяПараметра, ЭтоНовыйЭлемент = Ложь, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если сткТрассировка = Неопределено Тогда
		сткТрассировка = Новый_СтруктураТрассировки(,"РасчетыПоПравиламРасчета");	
	КонецЕсли;	
	
	// Если не заданы приемники.
	Если сткТрассировка.ПриемникиРасшифровки = Неопределено Тогда
		
		// Если добавляется сообщение об ошибке.
		Если УровеньСообщения = 0 Тогда
			Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
				
				ДобавитьСообщениеРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка, "", "Ошибка", , ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры);	
				
			ИначеЕсли	сткТрассировка.ТипСтруктуры = "РасчетыПоПутевомуЛисту" Тогда	
				
				ДобавитьСообщениеРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка, "", "Ошибка", , ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры);
				
			ИначеЕсли сткТрассировка.ТипСтруктуры = "РегламентнаяОперация" Тогда	
				
				ДобавитьСообщениеРасшифровки_РегламентнаяОперация(сткТрассировка, "", "Ошибка", , ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры);	
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе	
		Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
			
			ДобавитьСообщениеРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка, ИмяСтруктуры, ИмяПараметра, ЭтоНовыйЭлемент, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры);	
			
		ИначеЕсли	сткТрассировка.ТипСтруктуры = "РасчетыПоПутевомуЛисту" Тогда
			
			ДобавитьСообщениеРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка, ИмяСтруктуры, ИмяПараметра, ЭтоНовыйЭлемент, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры);	
			
		ИначеЕсли сткТрассировка.ТипСтруктуры = "РегламентнаяОперация" Тогда
			
			ДобавитьСообщениеРасшифровки_РегламентнаяОперация(сткТрассировка, ИмяСтруктуры, ИмяПараметра, ЭтоНовыйЭлемент, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры);	
			
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Вывод сообщения в различные форматы.
//
// Параметры:
//	сткТрассировка - Структура - Параметры трассировки.
//
Процедура СформироватьРасшифровку(сткТрассировка) Экспорт
		
	Если НЕ сткТрассировка = Неопределено Тогда
		
		ПриемникиРасшифровки = сткТрассировка.ПриемникиРасшифровки;
		
		Если ПриемникиРасшифровки = Неопределено Тогда
			ВывестиСообщенияНаЭкран(сткТрассировка);
		иначе	
			
			Для каждого сткПриемникРасшифровки Из ПриемникиРасшифровки Цикл
				
				ПриемникРасшифровки = сткПриемникРасшифровки.Приемник;
				
				Если ПриемникиРасшифровки = Неопределено Тогда
					
					ВывестиСообщенияНаЭкран(сткТрассировка);
					
				ИначеЕсли ТипЗнч(ПриемникРасшифровки) = Тип("ТабличныйДокумент") Тогда 	
					
					ВывестиСообщенияВТабличныйДокумент(сткТрассировка, сткПриемникРасшифровки);
					
				ИначеЕсли ТипЗнч(ПриемникРасшифровки) = Тип("ТекстовыйДокумент") Тогда 		
					
					ВывестиСообщенияВТекстовыйДокумент(сткТрассировка, сткПриемникРасшифровки);
					
					
				ИначеЕсли ТипЗнч(ПриемникРасшифровки) = Тип("Строка") Тогда 			
					
					ВывестиСообщенияВЖурналРегистрации(сткТрассировка, сткПриемникРасшифровки);
					
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;

	КонецЕсли;
		
КонецПроцедуры

// Выведем пользователю все накопленные сообщения.
//
// Параметры:
//	сткТрассировка - Структура - Параметры трассировки.
//	СообщенияПользователю - Массив - Сообщения.
//
Процедура ДополнитьРасшифровкуСообщениямиПользователю(сткТрассировка, СообщенияПользователю) Экспорт
	
	// Среди сообщений могут быть.
	// - "служебные" - они относятся к данной регламентной операции. Их записываем в ошибки.
	// - все остальные - они были отправлены до выполнения регламентной операции,
	//   но в том же серверном вызове и еще не выведены пользователю. Их пропускаем без изменений.
	
	Для Индекс = 0 По СообщенияПользователю.ВГраница() Цикл
		
		СообщениеПользователю = СообщенияПользователю[Индекс];
		
		СсылкаДляПерехода     = "";
		ТекстСообщения        = "";
		
		Служебное = ЭтоСлужебноеСообщение(
			СообщениеПользователю, 
			ТекстСообщения,
			СсылкаДляПерехода);
			
		Если Служебное Тогда
			
			ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
			
		Иначе
			
			// Это сообщение не относится к данной регламентной операции.
			// Вернем его обратно в очередь сообщений для вывода пользователю.
			СообщениеПользователю.Сообщить();
			
		КонецЕсли;
		
	КонецЦикла;

	
КонецПроцедуры

Процедура ИнициализироватьСтруктуруРасшифровки(сткТрассировка)
	
	Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
		
		ИнициализироватьСтруктуруРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка);
		
	ИначеЕсли сткТрассировка.ТипСтруктуры = "РасчетыПоПутевомуЛисту" Тогда
		
		ИнициализироватьСтруктуруРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка);	
		  
	ИначеЕсли сткТрассировка.ТипСтруктуры = "РегламентнаяОперация" Тогда
		
		ИнициализироватьСтруктуруРасшифровки_РегламентнаяОперация(сткТрассировка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиСообщенияНаЭкран(сткТрассировка)
	Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
		  ВывестиСообщенияНаЭкран_РасчетыПоПравиламРасчета(сткТрассировка );
	КонецЕсли;	
КонецПроцедуры

Процедура ВывестиСообщенияВТекстовыйДокумент(сткТрассировка, ПриемникРасшифровки)
	Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
		  ВывестиСообщенияВТекстовыйДокумент_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки);
	КонецЕсли;	
КонецПроцедуры

Процедура ВывестиСообщенияВТабличныйДокумент(сткТрассировка, ПриемникРасшифровки)
	Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
		  ВывестиСообщенияВТабличныйДокумент_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки);
	ИначеЕсли сткТрассировка.ТипСтруктуры = "РасчетыПоПутевомуЛисту" Тогда
		  ВывестиСообщенияВТабличныйДокумент_РасчетыПоПутевомуЛисту(сткТрассировка, ПриемникРасшифровки);
	ИначеЕсли сткТрассировка.ТипСтруктуры = "РегламентнаяОперация" Тогда
		  ВывестиСообщенияВТабличныйДокумент_РегламентнаяОперация(сткТрассировка, ПриемникРасшифровки);
	КонецЕсли;	
КонецПроцедуры

Процедура ВывестиСообщенияВЖурналРегистрации(сткТрассировка, ПриемникРасшифровки)
	Если сткТрассировка.ТипСтруктуры = "РасчетыПоПравиламРасчета" Тогда
		  ВывестиСообщенияВЖурналРегистрации_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки);
	КонецЕсли;	
КонецПроцедуры

// Определяет, является ли сообщение служебным, предназначенным для заполнения документа РегламентнаяОперация.
//
// Параметры:
//  Сообщение - Сообщение - проверяемое сообщение.
//  ТекстДляПользователя - Строка - Текст.
//  СсылкаДляПерехода - ЛюбаяСсылка - Ссылка перехода.
//
// Возвращаемое значение:
//  Булево - является ли сообщение служебным.
//
// Возвращаемые параметры:
//  ТекстДляПользователя - часть текста сообщения, предназначенная для пользователя.
//  СсылкаДляПерехода    - часть текста сообщения, не предназначенная для пользователя.
//
Функция ЭтоСлужебноеСообщение(Сообщение, ТекстДляПользователя = Неопределено, СсылкаДляПерехода = Неопределено) Экспорт
	
	ТекстСообщения = Сообщение.Текст;
	ПозицияНавигационнойСсылки = Найти(Сообщение.Текст, "e1cib");
	
	Если ПозицияНавигационнойСсылки = 0 Тогда
		ТекстДляПользователя = ТекстСообщения;
		Возврат Ложь;
	КонецЕсли;
	
	ТекстДляПользователя = Лев(ТекстСообщения, ПозицияНавигационнойСсылки - 1);
	СсылкаДляПерехода    = Сред(ТекстСообщения, ПозицияНавигационнойСсылки);
	
	Возврат Истина;
	
КонецФункции

// Найти элемент коллекции по значению свойства
//
// Параметры:
//  Коллекция			 - Коллекция	 - Коллекция
//  Свойство			 - Свойство	 - Свойство
//  Значение			 - Значение	 - Значение
//  ТипЭлемента		 - ТипЭлемента	 - ТипЭлемента
//  ВключаяПодчиненные	 - Булево	 -  включая подчиненные.
// 
// Возвращаемое значение:
//  Произвольное - элемент коллекции.
//
Функция НайтиЭлементКоллекцииПоЗначениюСвойства(Знач Коллекция, Знач Свойство, Знач Значение, Знач ТипЭлемента = Неопределено,
	ВключаяПодчиненные = Ложь) Экспорт
	
	Структура = Новый Структура(Свойство);
	Для каждого Элемент Из Коллекция Цикл
		Если ВключаяПодчиненные Тогда
			Если Ложь
				Или ТипЗнч(Элемент) = Тип("ГруппаФормы")
			Тогда
				Результат = НайтиЭлементКоллекцииПоЗначениюСвойства(Элемент.ПодчиненныеЭлементы, Свойство, Значение, ТипЭлемента, Истина);
			КонецЕсли; 
		КонецЕсли; 
		Если Истина
			И ТипЭлемента <> Неопределено
			И ТипЗнч(Элемент) <> ТипЭлемента 
		Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Структура, Элемент, Свойство); 
		Если Структура[Свойство] = Значение Тогда
			Результат = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

// Найти элементы коллекции по значению свойства
//
// Параметры:
//  Коллекция			 - Коллекция	 - Коллекция
//  Свойство			 - Свойство	 - Свойство
//  Значение			 - Значение	 - Значение
//  ТипЭлемента		 - ТипЭлемента	 - ТипЭлемента
// 
// Возвращаемое значение:
//  Массив - элементы коллекции.
//
Функция НайтиЭлементыКоллекцииПоЗначениюСвойства(Знач Коллекция, Знач Свойство, Знач Значение, Знач ТипЭлемента = Неопределено) Экспорт
	
	Результат = Новый Массив;
	Структура = Новый Структура(Свойство);
	Для каждого Элемент Из Коллекция Цикл
		Если Истина
			И ТипЭлемента <> Неопределено
			И ТипЗнч(Элемент) <> ТипЭлемента 
		Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Структура, Элемент, Свойство); 
		Если Структура[Свойство] = Значение Тогда
			Результат.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

// Функция - Получить имя по наименованию
//
// Параметры:
//  Наименование	 - Строка	 - наименованик
// 
// Возвращаемое значение:
//  Строка - Имя.
//
Функция ПолучитьИмяПоНаименованию(Наименование) Экспорт
	ТекИмя = "";
	Длина = СтрДлина(Наименование);
	Для Номер = 1 По Длина Цикл
		Символ = Сред(Наименование, Номер, 1);
		Если Не (Ложь
			Или (Символ>="0" и Символ<="9") 
			Или (Символ="_")
			Или (Символ>="a" и Символ<="z") 
			или (Символ>="A" и Символ<="Z")
			Или (Символ>="а" и Символ<="я") 
			или (Символ>="А" и Символ<="Я"))
		Тогда
			Символ = "_";
		КонецЕсли;
		ТекИмя = ТекИмя+Символ;
	КонецЦикла;
	Символ = Лев(ТекИмя, 1);
	Если Символ>="0" и Символ<="9" Тогда
		ТекИмя = "_"+ТекИмя;
	КонецЕсли;
	Возврат ТекИмя;
КонецФункции

// Функция - Добавить недостающие элементы структуру
//
// Параметры:
//  ПараметрыПоУмолчанию	 - Структура	 - параметры по умолчанию.
//  СтруктураПараметров	 - Структура	 - параметры.
// 
// Возвращаемое значение:
//  Булево - Истина, если надо записать.
//
Функция ДобавитьНедостающиеЭлементыСтруктуру(Знач ПараметрыПоУмолчанию, СтруктураПараметров = Неопределено) Экспорт 
	
	Если СтруктураПараметров = Неопределено Тогда
		СтруктураПараметров = Новый Структура;
	КонецЕсли; 
	НадоЗаписать = Ложь;
	Для Каждого КлючИЗначение Из ПараметрыПоУмолчанию Цикл
		Если Не СтруктураПараметров.Свойство(КлючИЗначение.Ключ) Тогда
			СтруктураПараметров.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			НадоЗаписать = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат НадоЗаписать;

КонецФункции

#Если НЕ ТонкийКлиент И НЕ ВебКлиент Тогда
	
// Функция - Пакет запросов найти по таблице для помещения
//
// Параметры:
//  ПакетЗапросов			 - ПакетЗапросов	 - пакет запросов.
//  ИмяТаблицыДляПомещения	 - Строка	 - имя таблицы запроса.
// 
// Возвращаемое значение:
//   Запрос - запрос.
//
Функция ПакетЗапросовНайтиПоТаблицеДляПомещения(ПакетЗапросов, ИмяТаблицыДляПомещения) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    СхемаЗапроса = Новый СхемаЗапроса;
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
	КонецЕсли;
	Для Каждого ЗапросПакета Из ПакетЗапросов Цикл
		Если ТипЗнч(ЗапросПакета) = Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Если СтрСравнить(ЗапросПакета.ТаблицаДляПомещения, ИмяТаблицыДляПомещения) = 0 Тогда
				Возврат ЗапросПакета;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

#КонецЕсли

#Область ФормированиеСообщенийПоРасчетам

Процедура ИнициализироватьСтруктуруРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка)
	сткТрассировка.Вставить("ОбъектРасчета");
	сткТрассировка.Вставить("ВремяНачалоРасчета");
	сткТрассировка.Вставить("ВремяОкончаниеРасчета");
	сткТрассировка.Вставить("СпособВводаРасходов", Неопределено);
	сткТрассировка.Вставить("мсвСтатьиДоходовРасходов", Новый Массив);		
	сткТрассировка.Вставить("мсвОшибки", Новый Массив);		
КонецПроцедуры

Процедура ВывестиСообщенияНаЭкран_РасчетыПоПравиламРасчета(сткТрассировка)
	
	мсвОшибкиПообъекту = сткТрассировка.мсвОшибки;
	Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
		
		ПараметрыСообщения = ЭлементОшибка.Параметры;
		
		Ссылка 	= Неопределено;
		Поле 	= "";

		
		Если ТипЗнч(ПараметрыСообщения) = Тип("Структура") Тогда
			
			Если НЕ ПараметрыСообщения.Свойство("Ссылка", Ссылка) Тогда
				Ссылка = Неопределено;
			КонецЕсли;
			Если НЕ ПараметрыСообщения.Свойство("Поле", Поле) Тогда
				Поле = "";
			КонецЕсли;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ЭлементОшибка.Сообщение,Ссылка,Поле);
	КонецЦикла;
КонецПроцедуры

Процедура ВывестиСообщенияВТекстовыйДокумент_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки)
	
	Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 1 Тогда
		ТекстСообщения = Нстр("ru = 'Начало расчета объекта:""%1"" в %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, сткТрассировка.ОбъектРасчета, сткТрассировка.ВремяНачалоРасчета);
		ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
		
		мсвСтатьиДоходовРасходов = сткТрассировка.мсвСтатьиДоходовРасходов;
		
		текОтступСтатья 	= "		";
		текОтступПравило	= текОтступСтатья + "	";
		
		Для каждого СтатьяДоходовРасходов Из мсвСтатьиДоходовРасходов Цикл
			
			ТекстСообщения = текОтступСтатья + Нстр("ru = '%1. Статья:""%2""'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтатьяДоходовРасходов.НомерСтроки, СтатьяДоходовРасходов.Наименование);
			ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
			
			ТекстСообщения = текОтступПравило + Нстр("ru = 'Правило: %1;'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтатьяДоходовРасходов.Правило);
			ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
			
			ТекстСообщения = текОтступПравило + Нстр("ru = 'Рассчитывается для каждой строки: %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,  СтатьяДоходовРасходов.РасчетПоСтроке);
			ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
			
			Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 2 И ТипЗнч(СтатьяДоходовРасходов.Описание) = Тип("Структура") Тогда
				ПриемникРасшифровки.Приемник.ДобавитьСтроку(текОтступПравило + Нстр("ru = 'Описание:'"));
				сткОписаниеПравила = СтатьяДоходовРасходов.Описание;
				ПриемникРасшифровки.Приемник.ДобавитьСтроку(текОтступПравило + сткОписаниеПравила.Описание);
				
				мсвИспользуемыеПараметры = сткОписаниеПравила.мсвПараметры;
				Если мсвИспользуемыеПараметры.Количество()>0 Тогда
					ПриемникРасшифровки.Приемник.ДобавитьСтроку(текОтступПравило + Нстр("ru = 'Используются параметры расчета:'"));
					Для каждого текПараметр Из мсвИспользуемыеПараметры Цикл
						ПриемникРасшифровки.Приемник.ДобавитьСтроку(текОтступПравило + текПараметр);
					КонецЦикла;
				КонецЕсли;
			
			КонецЕсли;
			
			ТекстСообщения = текОтступПравило + Нстр("ru = 'Формула: %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,СтатьяДоходовРасходов.Формула);
			ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
			ПриемникРасшифровки.Приемник.ДобавитьСтроку("Расчет:");
			
					
			мсвОбъектыРасчета = СтатьяДоходовРасходов.мсвОбъектыРасчета;
			Для каждого ОбъектРасчета  Из мсвОбъектыРасчета Цикл
				ТекстСообщения = Нстр("ru = '%1. %2: %3'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОбъектРасчета.НомерСтроки, ОбъектРасчета.Имя, ОбъектРасчета.Значение);
				ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
				
				мсвОшибкиПообъекту = ОбъектРасчета.мсвОшибки;
				Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
					ТекстСообщения = Нстр("ru = 'ОШИБКА:%1'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ЭлементОшибка.Сообщение);
					ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
				КонецЦикла;
				
				мсвПоказателиРасчета = ОбъектРасчета.мсвПоказателиРасчета;
				
				Для каждого ПоказательРасчета Из мсвПоказателиРасчета Цикл
					ТекстСообщения = Нстр("ru = '%1=%2'");	
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПоказательРасчета.Имя, ПоказательРасчета.Значение);
					ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
					
					мсвОшибкиПообъекту = ОбъектРасчета.мсвОшибки;
					Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
						ТекстСообщения = Нстр("ru = 'ОШИБКА:%1'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ЭлементОшибка.Сообщение);
						ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
					КонецЦикла;

				КонецЦикла;
				
				ТекстСообщения = Нстр("ru = 'Результат: %1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОбъектРасчета.ИтогоПоОбъекту);
				ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
				
			КонецЦикла;
					
			ТекстСообщения = текОтступСтатья + Нстр("ru = 'Итого по статье: %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтатьяДоходовРасходов.ИтогоПоСтатье);
			ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
			
			
			мсвОшибкиПообъекту = СтатьяДоходовРасходов.мсвОшибки;
			Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
				ТекстСообщения = Нстр("ru = 'ОШИБКА:%1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ЭлементОшибка.Сообщение);
				ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	мсвОшибкиПообъекту = сткТрассировка.мсвОшибки;
	Для каждого ЭлементОшибка Из мсвОшибкиПообъекту Цикл
		ТекстСообщения = Нстр("ru = 'ОШИБКА:%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ЭлементОшибка.Сообщение);
		ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);
	КонецЦикла;
	
	Если ПриемникРасшифровки.ЗапрашиваемыйУровеньСообщений >= 1 Тогда
		ТекстСообщения = Нстр("ru = 'Закончен расчет объекта :""%1"" в %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, сткТрассировка.ОбъектРасчета, сткТрассировка.ВремяОкончаниеРасчета);
		ПриемникРасшифровки.Приемник.ДобавитьСтроку(ТекстСообщения);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиСообщенияВТабличныйДокумент_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки)
	
	упСервисныеФункцииВызовСервера.СформироватьТабличныйДокументРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки);
	
КонецПроцедуры

Процедура ВывестиСообщенияВЖурналРегистрации_РасчетыПоПравиламРасчета(сткТрассировка, ПриемникРасшифровки)
	
	// Нет.
	
КонецПроцедуры

Процедура ДобавитьСообщениеРасшифровки_РасчетыПоПравиламРасчета(сткТрассировка, ИмяСтруктуры = "", ИмяПараметра, ЭтоНовыйЭлемент = Ложь, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры = Неопределено)
	
	мсвОшибки = Неопределено;
		
	Если ИмяСтруктуры  = "" Тогда
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			сткТрассировка.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = сткТрассировка.мсвОшибки;
		
		Если ИмяПараметра = "ОбъектРасчета" Тогда
			Ссылка =  сткТрассировка.ОбъектРасчета;
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы") Тогда
				СпособВводаРасходов = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Ссылка, "СпособВводаРасходов");
				сткТрассировка.Вставить("СпособВводаРасходов", СпособВводаРасходов);
			Иначе	
				сткТрассировка.Вставить("СпособВводаРасходов", ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.ПустаяСсылка"));
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСтруктуры  = "Статья" Тогда	
		мсвСтатьиДоходовРасходов = сткТрассировка.мсвСтатьиДоходовРасходов;
			
		Если ЭтоНовыйЭлемент ИЛИ мсвСтатьиДоходовРасходов.Количество() = 0 Тогда
			СтатьяДоходовРасходов = Новый Структура();
			СтатьяДоходовРасходов.Вставить("НомерСтроки",	0);
			СтатьяДоходовРасходов.Вставить("Наименование",	"");
			СтатьяДоходовРасходов.Вставить("Правило",		Неопределено);
			СтатьяДоходовРасходов.Вставить("РасчетПоСтроке",Ложь);
			СтатьяДоходовРасходов.Вставить("Описание",		"");
			СтатьяДоходовРасходов.Вставить("ИтогоПоСтатье",	0);
			СтатьяДоходовРасходов.Вставить("мсвОбъектыРасчета",Новый Массив);
			СтатьяДоходовРасходов.Вставить("мсвОшибки",Новый Массив);
			мсвСтатьиДоходовРасходов.Добавить(СтатьяДоходовРасходов);
		КонецЕсли;
		
		СтатьяДоходовРасходов = мсвСтатьиДоходовРасходов[мсвСтатьиДоходовРасходов.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			
			Если ИмяПараметра = "Описание" Тогда
				СтатьяДоходовРасходов.Вставить(ИмяПараметра, упТарификацияВызовСервера.ОписаниеПравила(СтатьяДоходовРасходов.Правило, ТипЗнч(сткТрассировка.ОбъектРасчета), СтатьяДоходовРасходов.РасчетПоСтроке, сткТрассировка.СпособВводаРасходов));			
			Иначе
				СтатьяДоходовРасходов.Вставить(ИмяПараметра, ЗначениеПараметра);			
			КонецЕсли;
						
		КонецЕсли;
		мсвОшибки = СтатьяДоходовРасходов.мсвОшибки;
				
	ИначеЕсли ИмяСтруктуры = "ОбъектРасчета" Тогда 	
		
		мсвСтатьиДоходовРасходов = сткТрассировка.мсвСтатьиДоходовРасходов;
		СтатьяДоходовРасходов = мсвСтатьиДоходовРасходов[мсвСтатьиДоходовРасходов.ВГраница()];	
		
		мсвОбъектыРасчета = СтатьяДоходовРасходов.мсвОбъектыРасчета;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвОбъектыРасчета.Количество() = 0 Тогда
			ОбъектРасчета = Новый Структура();
			ОбъектРасчета.Вставить("НомерСтроки",	0);
			ОбъектРасчета.Вставить("Имя",			"");
			ОбъектРасчета.Вставить("Значение",		Неопределено);
			ОбъектРасчета.Вставить("АдресОтправления",		Неопределено);
			ОбъектРасчета.Вставить("АдресПрибытия",			Неопределено);
			ОбъектРасчета.Вставить("ИтогоПоОбъекту",0);
			ОбъектРасчета.Вставить("Формула","");
			ОбъектРасчета.Вставить("КоэффициентРаспределения", 1);
			ОбъектРасчета.Вставить("ИтогоПоОбъектуСКоэффициентом", 0);
			ОбъектРасчета.Вставить("мсвОшибки",		Новый Массив);
			ОбъектРасчета.Вставить("мсвПоказателиРасчета",		Новый Массив);
			мсвОбъектыРасчета.Добавить(ОбъектРасчета);
		КонецЕсли;
		
		ОбъектРасчета = мсвОбъектыРасчета[мсвОбъектыРасчета.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			ОбъектРасчета.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = ОбъектРасчета.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "ПоказательРасчета" Тогда 	
		
		мсвСтатьиДоходовРасходов 	= сткТрассировка.мсвСтатьиДоходовРасходов;
		СтатьяДоходовРасходов 		= мсвСтатьиДоходовРасходов[мсвСтатьиДоходовРасходов.ВГраница()];	
		мсвОбъектыРасчета 			= СтатьяДоходовРасходов.мсвОбъектыРасчета;
		ОбъектРасчета				= мсвОбъектыРасчета[мсвОбъектыРасчета.ВГраница()];
		
		мсвПоказателейРасчета		= ОбъектРасчета.мсвПоказателиРасчета;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвПоказателейРасчета	.Количество() = 0 Тогда
			ПоказательРасчета = Новый Структура();
			ПоказательРасчета.Вставить("Имя",			"");
			ПоказательРасчета.Вставить("Значение",		Неопределено);
			ПоказательРасчета.Вставить("мсвОшибки",		Новый Массив);
			мсвПоказателейРасчета.Добавить(ПоказательРасчета);
		КонецЕсли;
		
		ПоказательРасчета = мсвПоказателейРасчета[мсвПоказателейРасчета.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			ПоказательРасчета.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = ПоказательРасчета.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "КоэффициентРаспределения" Тогда 		
		
	КонецЕсли;
	
	Если ИмяПараметра = "Ошибка" И НЕ мсвОшибки = Неопределено Тогда
		сткОшибка = Новый Структура("Сообщение, Параметры", ЗначениеПараметра, ДополнительныеПараметры);
		мсвОшибки.Добавить(сткОшибка);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеСообщенийПоПутевомуЛисту

Процедура ИнициализироватьСтруктуруРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка)
	сткТрассировка.Вставить("ОбъектРасчета");
	сткТрассировка.Вставить("ВремяНачалоРасчета");
	сткТрассировка.Вставить("ВремяОкончаниеРасчета");
	сткТрассировка.Вставить("СпособВводаРасходов", Неопределено);
	сткТрассировка.Вставить("мсвВидыГСМ", 	Новый Массив);		
	сткТрассировка.Вставить("мсвОшибки",	Новый Массив);		
КонецПроцедуры

Процедура ВывестиСообщенияВТабличныйДокумент_РасчетыПоПутевомуЛисту(сткТрассировка, ПриемникРасшифровки)
	
	упСервисныеФункцииВызовСервера.СформироватьТабличныйДокументРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка, ПриемникРасшифровки);
	
КонецПроцедуры

Процедура ДобавитьСообщениеРасшифровки_РасчетыПоПутевомуЛисту(сткТрассировка, ИмяСтруктуры = "", ИмяПараметра, ЭтоНовыйЭлемент = Ложь, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры = Неопределено)
	
	мсвОшибки = Неопределено;
		
	Если ИмяСтруктуры  = "" Тогда
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			сткТрассировка.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = сткТрассировка.мсвОшибки;
		
		Если ИмяПараметра = "ОбъектРасчета" Тогда
			Ссылка =  сткТрассировка.ОбъектРасчета;
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы") Тогда
				СпособВводаРасходов = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Ссылка, "СпособВводаРасходов");
				сткТрассировка.Вставить("СпособВводаРасходов", СпособВводаРасходов);
			Иначе	
				сткТрассировка.Вставить("СпособВводаРасходов", ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.ПустаяСсылка"));
			КонецЕсли;
		КонецЕсли;
				
	ИначеЕсли ИмяСтруктуры = "ОбъектРасчета" Тогда 	
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
	
		мсвОбъектыРасчета = ВидГСМ.мсвОбъектыРасчета;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвОбъектыРасчета.Количество() = 0 Тогда
			ОбъектРасчета = Новый Структура();
			ОбъектРасчета.Вставить("НомерСтроки",	0);
			ОбъектРасчета.Вставить("Имя",			"");
			ОбъектРасчета.Вставить("Значение",		Неопределено);
			ОбъектРасчета.Вставить("ИтогоПоОбъекту",0);
			ОбъектРасчета.Вставить("Формула","");
			ОбъектРасчета.Вставить("КоэффициентРаспределения", 1);
			ОбъектРасчета.Вставить("ИтогоПоОбъектуСКоэффициентом", 0);
			ОбъектРасчета.Вставить("мсвОшибки",		Новый Массив);
			ОбъектРасчета.Вставить("мсвПоказателиРасчета",		Новый Массив);
			мсвОбъектыРасчета.Добавить(ОбъектРасчета);
		КонецЕсли;
		
		ОбъектРасчета = мсвОбъектыРасчета[мсвОбъектыРасчета.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			ОбъектРасчета.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = ОбъектРасчета.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "ПоказательРасчета" Тогда 	
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвОбъектыРасчета 			= ВидГСМ.мсвОбъектыРасчета;
		ОбъектРасчета				= мсвОбъектыРасчета[мсвОбъектыРасчета.ВГраница()];
		
		мсвПоказателейРасчета		= ОбъектРасчета.мсвПоказателиРасчета;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвПоказателейРасчета	.Количество() = 0 Тогда
			ПоказательРасчета = Новый Структура();
			ПоказательРасчета.Вставить("Имя",			"");
			ПоказательРасчета.Вставить("Значение",		Неопределено);
			ПоказательРасчета.Вставить("мсвОшибки",		Новый Массив);
			мсвПоказателейРасчета.Добавить(ПоказательРасчета);
		КонецЕсли;
		
		ПоказательРасчета = мсвПоказателейРасчета[мсвПоказателейРасчета.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			ПоказательРасчета.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = ПоказательРасчета.мсвОшибки;
	
		
		
	ИначеЕсли ИмяСтруктуры  = "ВидГСМ" Тогда
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
			
		Если ЭтоНовыйЭлемент ИЛИ мсвВидыГСМ.Количество() = 0 Тогда
			ВидГСМ = Новый Структура();
			ВидГСМ.Вставить("НомерСтроки",			0);
			ВидГСМ.Вставить("Наименование",		"");
			ВидГСМ.Вставить("Правило",			Неопределено);
			ВидГСМ.Вставить("РасчетПоСтроке",		Ложь);
			ВидГСМ.Вставить("Описание",			"");
			ВидГСМ.Вставить("ИтогоПоВидуГСМ",		0);
			ВидГСМ.Вставить("Расход",			0);
			ВидГСМ.Вставить("ПроцентИспользования",	0);
			ВидГСМ.Вставить("ИзменениеРасхода",	0);
			ВидГСМ.Вставить("БазовыйРасходТоплива",	0);
			ВидГСМ.Вставить("ТипНормыРасходаГСМ",	Неопределено);
			ВидГСМ.Вставить("мсвОбъектыРасчета",	Новый Массив);
			ВидГСМ.Вставить("мсвМаршрут",			Новый Массив);
			ВидГСМ.Вставить("мсвНадбавки",		Новый Массив);
			ВидГСМ.Вставить("мсвПрицепы",			Новый Массив);
			ВидГСМ.Вставить("мсвДопОборудование",	Новый Массив);
			ВидГСМ.Вставить("мсвОшибки",			Новый Массив);
			мсвВидыГСМ.Добавить(ВидГСМ);
		КонецЕсли;
		
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			
			Если ИмяПараметра = "Описание" Тогда
				ВидГСМ.Вставить(ИмяПараметра, упТарификацияВызовСервера.ОписаниеПравила(ВидГСМ.Правило, ТипЗнч(сткТрассировка.ОбъектРасчета), ВидГСМ.РасчетПоСтроке, сткТрассировка.СпособВводаРасходов));			
			Иначе
				ВидГСМ.Вставить(ИмяПараметра, ЗначениеПараметра);			
			КонецЕсли;
						
		КонецЕсли;
		мсвОшибки = ВидГСМ.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "Маршрут" Тогда 	
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвМаршрут = ВидГСМ.мсвМаршрут;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвМаршрут.Количество() = 0 Тогда
			Маршрут = Новый Структура();
			Маршрут.Вставить("НомерСтроки",		0);
			Маршрут.Вставить("АдресОтправления",	"");
			Маршрут.Вставить("АдресПрибытия",		"");
			Маршрут.Вставить("РасстояниеИтого",	0);
			Маршрут.Вставить("ВремяИтого",		0);
			Маршрут.Вставить("МассаГруза",		0);
			Маршрут.Вставить("Расстояние", 		0);
			Маршрут.Вставить("Время", 			0);
			Маршрут.Вставить("БазовыйРасход", 		0);
			Маршрут.Вставить("РасходНаТранспортнуюРаботу", 			0);
			Маршрут.Вставить("РасходТоплива", 		0);
			Маршрут.Вставить("мсвОшибки",		Новый Массив);
			мсвМаршрут.Добавить(Маршрут);
		КонецЕсли;
		
		Маршрут = мсвМаршрут[мсвМаршрут.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			Маршрут.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = Маршрут.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "Надбавка" Тогда 	
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвНадбавки = ВидГСМ.мсвНадбавки;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвНадбавки.Количество() = 0 Тогда
			Надбавка = Новый Структура();
			Надбавка.Вставить("НомерСтроки",		0);
			Надбавка.Вставить("ВидЭксплуатационнойНормы",	"");
			Надбавка.Вставить("НормаРасхода",		"");
			Надбавка.Вставить("Количество",		0);
			Надбавка.Вставить("РасходТоплива",		0);
			Надбавка.Вставить("БазовыйРасходТоплива",		0);
			Надбавка.Вставить("мсвОшибки",		Новый Массив);
			мсвНадбавки.Добавить(Надбавка);
		КонецЕсли;
		
		Надбавка = мсвНадбавки[мсвНадбавки.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			Надбавка.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = Надбавка.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "Прицеп" Тогда 	
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвПрицепы = ВидГСМ.мсвПрицепы;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвПрицепы.Количество() = 0 Тогда
			Прицеп = Новый Структура();
			Прицеп.Вставить("Прицеп",	Неопределено);
			Прицеп.Вставить("ТипРасхода",	Неопределено);
			Прицеп.Вставить("Расход",		0);
			Прицеп.Вставить("РасходТоплива",	0);
			Прицеп.Вставить("Количество",		0);
			Прицеп.Вставить("РасходНаТранспортнуюРаботу",0);
			Прицеп.Вставить("РасходТопливаПоНорме",		0);
		
			Прицеп.Вставить("БазовыйРасходТоплива",		0);
			Прицеп.Вставить("мсвОшибки",		Новый Массив);
			мсвПрицепы.Добавить(Прицеп);
		КонецЕсли;
		
		Прицеп = мсвПрицепы[мсвПрицепы.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			Прицеп.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = Прицеп.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "ДопОборудование" Тогда 	
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвДопОборудование = ВидГСМ.мсвДопОборудование;
		
		Если ЭтоНовыйЭлемент ИЛИ мсвДопОборудование.Количество() = 0 Тогда
			Допоборудование = Новый Структура();
			ДопОборудование.Вставить("ДопОборудование",	Неопределено);
			ДопОборудование.Вставить("мсвРежимыРаботыДопОборудования",	Новый Массив);
			ДопОборудование.Вставить("мсвНадбавкаДопОборудование",		Новый Массив);
			ДопОборудование.Вставить("мсвОшибки",		Новый Массив);
			мсвДопОборудование.Добавить(ДопОборудование);
		КонецЕсли;
		
		ДопОборудование = мсвДопОборудование[мсвДопОборудование.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			ДопОборудование.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = ДопОборудование.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры  = "РежимРаботыДопОборудования" Тогда
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвДопОборудование 	= ВидГСМ.мсвДопОборудование;
		ДопОборудование	= мсвДопОборудование[мсвДопОборудование.ВГраница()];
		
		мсвРежимыРаботыДопОборудования = Допоборудование.мсвРежимыРаботыДопОборудования;
			
		Если ЭтоНовыйЭлемент ИЛИ мсвРежимыРаботыДопОборудования.Количество() = 0 Тогда
			РежимРаботы = Новый Структура();
			РежимРаботы.Вставить("РежимРаботы",	Неопределено);
			РежимРаботы.Вставить("ТипНормыРасходаГСМ",	Неопределено);
			РежимРаботы.Вставить("Количество",	0);
			РежимРаботы.Вставить("Расход",	0);
			РежимРаботы.Вставить("РасходТоплива",	0);
			РежимРаботы.Вставить("мсвОшибки",		Новый Массив);

			мсвРежимыРаботыДопОборудования.Добавить(РежимРаботы);
		КонецЕсли;
		
		РежимРаботы = мсвРежимыРаботыДопОборудования[мсвРежимыРаботыДопОборудования.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			РежимРаботы.Вставить(ИмяПараметра, ЗначениеПараметра);			
		КонецЕсли;
		мсвОшибки = РежимРаботы.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры  = "НадбавкаДопОборудование" Тогда
		
		мсвВидыГСМ = сткТрассировка.мсвВидыГСМ;
		ВидГСМ = мсвВидыГСМ[мсвВидыГСМ.ВГраница()];	
		
		мсвДопОборудование 	= ВидГСМ.мсвДопОборудование;
		ДопОборудование	= мсвДопОборудование[мсвДопОборудование.ВГраница()];
		
		мсвНадбавкаДопОборудование = Допоборудование.мсвНадбавкаДопОборудование;
			
		Если ЭтоНовыйЭлемент ИЛИ мсвНадбавкаДопОборудование.Количество() = 0 Тогда
			Надбавка = Новый Структура();
			Надбавка.Вставить("НомерСтроки",		0);
			Надбавка.Вставить("ВидЭксплуатационнойНормы",	"");
			Надбавка.Вставить("НормаРасхода",		"");
			Надбавка.Вставить("Количество",		0);
			Надбавка.Вставить("РасходТоплива",		0);
			Надбавка.Вставить("БазовыйРасходТоплива",		0);
			Надбавка.Вставить("мсвОшибки",		Новый Массив);
			мсвНадбавкаДопОборудование.Добавить(Надбавка);
		КонецЕсли;
		
		Надбавка = мсвНадбавкаДопОборудование[мсвНадбавкаДопОборудование.ВГраница()];	
		
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			Надбавка.Вставить(ИмяПараметра, ЗначениеПараметра);			
		КонецЕсли;
		мсвОшибки = Надбавка.мсвОшибки;
	
		
	ИначеЕсли ИмяСтруктуры = "ОбъектРасчета" Тогда 	
		
		//мсвСтатьиДоходовРасходов = сткТрассировка.мсвСтатьиДоходовРасходов;
		//СтатьяДоходовРасходов = мсвСтатьиДоходовРасходов[мсвСтатьиДоходовРасходов.ВГраница()];	
		//
		//мсвОбъектыРасчета = СтатьяДоходовРасходов.мсвОбъектыРасчета;
		//
		//Если ЭтоНовыйЭлемент ИЛИ мсвОбъектыРасчета.Количество() = 0 Тогда
		//	ОбъектРасчета = Новый Структура();
		//	ОбъектРасчета.Вставить("НомерСтроки",	0);
		//	ОбъектРасчета.Вставить("Имя",			"");
		//	ОбъектРасчета.Вставить("Значение",		Неопределено);
		//	ОбъектРасчета.Вставить("ИтогоПоОбъекту",0);
		//	ОбъектРасчета.Вставить("Формула","");
		//	ОбъектРасчета.Вставить("КоэффициентРаспределения", 1);
		//	ОбъектРасчета.Вставить("ИтогоПоОбъектуСКоэффициентом", 0);
		//	ОбъектРасчета.Вставить("мсвОшибки",		Новый Массив);
		//	ОбъектРасчета.Вставить("мсвПоказателиРасчета",		Новый Массив);
		//	мсвОбъектыРасчета.Добавить(ОбъектРасчета);
		//КонецЕсли;
		//
		//ОбъектРасчета = мсвОбъектыРасчета[мсвОбъектыРасчета.ВГраница()];	
		//
		//Если НЕ ИмяПараметра = "Ошибка" Тогда
		//	ОбъектРасчета.Вставить(ИмяПараметра, ЗначениеПараметра);		
		//КонецЕсли;
		//мсвОшибки = ОбъектРасчета.мсвОшибки;
		//
	ИначеЕсли ИмяСтруктуры = "ПоказательРасчета" Тогда 	
		
		//мсвСтатьиДоходовРасходов 	= сткТрассировка.мсвСтатьиДоходовРасходов;
		//СтатьяДоходовРасходов 		= мсвСтатьиДоходовРасходов[мсвСтатьиДоходовРасходов.ВГраница()];	
		//мсвОбъектыРасчета 			= СтатьяДоходовРасходов.мсвОбъектыРасчета;
		//ОбъектРасчета				= мсвОбъектыРасчета[мсвОбъектыРасчета.ВГраница()];
		//
		//мсвПоказателейРасчета		= ОбъектРасчета.мсвПоказателиРасчета;
		//
		//Если ЭтоНовыйЭлемент ИЛИ мсвПоказателейРасчета	.Количество() = 0 Тогда
		//	ПоказательРасчета = Новый Структура();
		//	ПоказательРасчета.Вставить("Имя",			"");
		//	ПоказательРасчета.Вставить("Значение",		Неопределено);
		//	ПоказательРасчета.Вставить("мсвОшибки",		Новый Массив);
		//	мсвПоказателейРасчета.Добавить(ПоказательРасчета);
		//КонецЕсли;
		//
		//ПоказательРасчета = мсвПоказателейРасчета[мсвПоказателейРасчета.ВГраница()];	
		//
		//Если НЕ ИмяПараметра = "Ошибка" Тогда
		//	ПоказательРасчета.Вставить(ИмяПараметра, ЗначениеПараметра);		
		//КонецЕсли;
		//мсвОшибки = ПоказательРасчета.мсвОшибки;
		
	ИначеЕсли ИмяСтруктуры = "КоэффициентРаспределения" Тогда 		
		
	КонецЕсли;
	
	Если ИмяПараметра = "Ошибка" И НЕ мсвОшибки = Неопределено Тогда
		сткОшибка = Новый Структура("Сообщение, Параметры", ЗначениеПараметра, ДополнительныеПараметры);
		мсвОшибки.Добавить(сткОшибка);
	КонецЕсли;
	
	
КонецПроцедуры

// Функция - Надпись количества надбавки
//
// Параметры:
//  ТипРасхода	 - ПеречислениеСсылка.упСпособыРасчетаНадбавокГСМ 	 - способ.
// 
// Возвращаемое значение:
//  Строка - описание единиц.
//
Функция ЕдиницыИзмеренияКоличестваНадбавкиНормыГСМ(ТипРасхода) Экспорт
	СтрокаЕдениц	= "";
	Если ТипРасхода = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗа100Км") Тогда
		СтрокаЕдениц = Нстр("ru = 'км'")	
	ИначеЕсли ТипРасхода = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗаЧас") Тогда	
		СтрокаЕдениц = Нстр("ru = 'ч'")	
	КонецЕсли;
	Возврат СтрШаблон("%1", СтрокаЕдениц);		
КонецФункции


#КонецОбласти


#Область ФормированиеСообщенийПоРегламентнымОперациям

Процедура ИнициализироватьСтруктуруРасшифровки_РегламентнаяОперация(сткТрассировка)
	сткТрассировка.Вставить("Ссылка");
	сткТрассировка.Вставить("мсвОшибки", Новый Массив);		
КонецПроцедуры

Процедура ВывестиСообщенияВТабличныйДокумент_РегламентнаяОперация(сткТрассировка, ПриемникРасшифровки)
	
	упСервисныеФункцииВызовСервера.СформироватьТабличныйДокументРасшифровки_РегламентнаяОперация(сткТрассировка, ПриемникРасшифровки);
	
КонецПроцедуры

Процедура ДобавитьСообщениеРасшифровки_РегламентнаяОперация(сткТрассировка, ИмяСтруктуры = "", ИмяПараметра, ЭтоНовыйЭлемент = Ложь, ЗначениеПараметра, УровеньСообщения, ДополнительныеПараметры = Неопределено)
	
	мсвОшибки = Неопределено;
		
	Если ИмяСтруктуры  = "" Тогда
		Если НЕ ИмяПараметра = "Ошибка" Тогда
			сткТрассировка.Вставить(ИмяПараметра, ЗначениеПараметра);		
		КонецЕсли;
		мсвОшибки = сткТрассировка.мсвОшибки;
	КонецЕсли;
	
	Если ИмяПараметра = "Ошибка" И НЕ мсвОшибки = Неопределено Тогда
		сткОшибка = Новый Структура("Сообщение, Параметры", ЗначениеПараметра, ДополнительныеПараметры);
		мсвОшибки.Добавить(сткОшибка);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

// Функция - Источник создания объекта
//
// Параметры:
//  ИсточникСоздания - СправочникСсылка.Пользователи - источник создания объекта
// 
// Возвращаемое значение:
//  ИсточникСоздания - источник создания объекта.
//
Функция ИсточникСозданияОбъекта(ИсточникСоздания = Неопределено) Экспорт
	
	Результат = ИсточникСоздания;
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Результат = ?(ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя(), 
				ПредопределенноеЗначение("Перечисление.упИсточникиСозданияОбъекта.ВнешнийПользователь"), 
				ПредопределенноеЗначение("Перечисление.упИсточникиСозданияОбъекта.Пользователь"));	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Автор последнего изменения объекта
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - свойства
// 
// Возвращаемое значение:
//   Пользователь - автор.
//
Функция АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства = Неопределено) Экспорт

	Результат = Неопределено;
	Попытка
		Результат = ?(ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя(),
		ПользователиКлиентСервер.ТекущийВнешнийПользователь(),
		ПользователиКлиентСервер.ТекущийПользователь());
	Исключение
		// пользователь не создан, есть только пользователь ИБ
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Получает разность двух структур.
//
// Параметры:
//  сткУменьшаемое	 - Структура	 - Параметры.
//  сткВычитаемое	 - Структура	 - Параметры.
//  Отказ			 - Булево		 - Выполнить фиксирование изменений.
// 
// Возвращаемое значение:
//  Структура - Результат выполнения.
//
Функция РазностьСтруктур(сткУменьшаемое, сткВычитаемое, Отказ) Экспорт
	
	сткРезультат = Неопределено;
	
	КолЭлементовУменьшаемое = сткУменьшаемое.Количество();
	КолЭлементовВычитаемое  = сткВычитаемое.Количество();
	Если НЕ КолЭлементовУменьшаемое = КолЭлементовВычитаемое Тогда
		Отказ = Истина;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткУменьшаемое);
	
	Для каждого сткЭлемент Из сткУменьшаемое Цикл
		сткРезультат.Вставить(сткЭлемент.Ключ, сткУменьшаемое[сткЭлемент.Ключ] - сткВычитаемое[сткЭлемент.Ключ]);
	КонецЦикла;
	
	Возврат сткРезультат;
			
КонецФункции

// Получает сумму двух структур.
//
// Параметры:
//	сткСлагаемое1 - Структура - Параметры.
//	сткСлагаемое2 - Структура - Параметры.
//	Отказ - Булево - Выполнить фиксирование изменений.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция СложитьСтруктуры(сткСлагаемое1, сткСлагаемое2, Отказ) Экспорт
	
	сткРезультат = Неопределено;
	
	КолЭлементовСлагаемое1 = сткСлагаемое1.Количество();
	КолЭлементовСлагаемое2 = сткСлагаемое2.Количество();
	Если НЕ КолЭлементовСлагаемое1 = КолЭлементовСлагаемое2 Тогда
		Отказ = Истина;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткСлагаемое1);
	
	Для каждого сткЭлемент Из сткСлагаемое1 Цикл
		сткРезультат.Вставить(сткЭлемент.Ключ, сткСлагаемое1[сткЭлемент.Ключ] + сткСлагаемое2[сткЭлемент.Ключ]);
	КонецЦикла;
	
	Возврат сткРезультат;
			
КонецФункции

// Получает произведение двух структур.
//
// Параметры:
//	сткИсходная - Структура - Параметры.
//	Множитель - Число - Множитель.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция УмножитьСтруктуНаЗначение(сткИсходная, Множитель) Экспорт
	
	сткРезультат = Неопределено;
	
	сткРезультат = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткИсходная);
	
	Для каждого сткЭлемент Из сткИсходная Цикл
		сткРезультат.Вставить(сткЭлемент.Ключ, Окр(сткИсходная[сткЭлемент.Ключ]* Множитель, 2));
	КонецЦикла;
	
	Возврат сткРезультат;
			
КонецФункции

// Обнуляет переданную структуру.
//
// Параметры:
//	сткИсходная - Структура - Параметры.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция СтруктураРавнаНулю(сткИсходная) Экспорт
	
	текРезультат = Истина;
	
	Для каждого сткЭлемент Из сткИсходная Цикл
		Если НЕ сткЭлемент.Значение = 0 Тогда
			текРезультат = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат текРезультат;
		
КонецФункции

// Возвращает список периодов в диапазоне начала периода.
// Перенесено из старого БСП
Функция СписокФиксированныхПериодов(Знач НачалоПериода, ВидПериода) Экспорт
	СписокПериодов = Новый СписокЗначений;
	
	Если НачалоПериода = '00010101' Тогда
		Возврат СписокПериодов;
	КонецЕсли;
	
	НачалоПериода = НачалоДня(НачалоПериода);
	ВыборОтносительногоПериода = (НачалоПериода = "ВыборОтносительногоПериода");
	ПоказыватьВсеОтносительныеПериоды = Ложь;
	
	#Если Клиент Тогда
		Сегодня = ОбщегоНазначенияКлиент.ДатаСеанса();
	#Иначе
		Сегодня = ТекущаяДатаСеанса();
	#КонецЕсли
	Сегодня = НачалоДня(Сегодня);
	
	НавигационныйПунктРанееПредставление = НСтр("ru = 'Ранее...'");
	НавигационныйПунктПозжеПредставление = НСтр("ru = 'Позже...'");
	
	Если ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.День") Тогда
		ТекущийДеньНедели   = ДеньНедели(Сегодня);
		ВыбранныйДеньНедели = ДеньНедели(НачалоПериода);
		
		// Вычисление начального и конечного периода по формуле.
		НачальныйДеньНедели = ТекущийДеньНедели - 5;
		КонечныйДеньНедели  = ТекущийДеньНедели + 1;
		Если ВыбранныйДеньНедели > КонечныйДеньНедели Тогда
			ВыбранныйДеньНедели = ВыбранныйДеньНедели - 7;
		КонецЕсли;
		
		Период = НачалоПериода - 86400 * (ВыбранныйДеньНедели - НачальныйДеньНедели);
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		СписокПериодов.Добавить(Период - 86400 * 7, НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Счетчик = 1 По 7 Цикл
			СписокПериодов.Добавить(Период, Формат(Период, "ДФ='dd MMMM yyyy, dddd'") + ?(Период = Сегодня, " - " + НСтр("ru = 'сегодня'"), ""));
			Период = Период + 86400;
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		СписокПериодов.Добавить(Период + 86400 * 6, НавигационныйПунктПозжеПредставление);
		
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Неделя") Тогда
		ТекущееНачалоНедели   = НачалоНедели(Сегодня);
		ВыбранноеНачалоНедели = НачалоНедели(НачалоПериода);
		
		// Вычисление начального и конечного периода по формуле.
		РазностьНедель = (ВыбранноеНачалоНедели - ТекущееНачалоНедели) / 604800;
		Коэффициент = (РазностьНедель - 2)/7;
		Коэффициент = Цел(Коэффициент - ?(Коэффициент < 0, 0.9, 0)); // Отрицательные числа округляются в большую часть.
		НачальнаяНеделя = ТекущееНачалоНедели + (2 + Коэффициент*7) * 604800;
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		СписокПериодов.Добавить(НачальнаяНеделя - 7 * 604800, НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Счетчик = 0 По 6 Цикл
			Период = НачальнаяНеделя + Счетчик * 604800;
			КонецПериода  = КонецНедели(Период);
			ПредставлениеПериода = Формат(Период, "ДФ=dd.MM") + " - " + Формат(КонецПериода, "ДЛФ=D") + " (" + НеделяГода(КонецПериода) + " " + НСтр("ru = 'неделя года'") + ")";
			Если Период = ТекущееНачалоНедели Тогда
				ПредставлениеПериода = ПредставлениеПериода + " - " + НСтр("ru = 'эта неделя'");
			КонецЕсли;
			СписокПериодов.Добавить(Период, ПредставлениеПериода);
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		СписокПериодов.Добавить(НачальнаяНеделя + 13 * 604800, НавигационныйПунктПозжеПредставление);
		
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Декада") Тогда
		ТекущийГод   = Год(Сегодня);
		ВыбранныйГод = Год(НачалоПериода);
		ТекущийМесяц   = Месяц(Сегодня);
		ВыбранныйМесяц = Месяц(НачалоПериода);
		ТекущийДень   = День(Сегодня);
		ВыбранныйДень = День(НачалоПериода);
		ТекущаяДекада   = ?(ТекущийДень   <= 10, 1, ?(ТекущийДень   <= 20, 2, 3));
		ВыбраннаяДекада = ?(ВыбранныйДень <= 10, 1, ?(ВыбранныйДень <= 20, 2, 3));
		ТекущаяДекадаАбсолютно   = ТекущийГод*36 + (ТекущийМесяц-1)*3 + (ТекущаяДекада-1);
		ВыбраннаяДекадаАбсолютно = ВыбранныйГод*36 + (ВыбранныйМесяц-1)*3 + (ВыбраннаяДекада-1);
		СтрокаДекада = НСтр("ru = 'декада'");
		
		// Вычисление начального и конечного периода по формуле.
		Коэффициент = (ВыбраннаяДекадаАбсолютно - ТекущаяДекадаАбсолютно - 2)/7;
		Коэффициент = Цел(Коэффициент - ?(Коэффициент < 0, 0.9, 0)); // Отрицательные числа округляются в большую часть.
		НачальнаяДекада = ТекущаяДекадаАбсолютно + 2 + Коэффициент*7;
		КонечнаяДекада  = НачальнаяДекада + 6;
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		Декада = НачальнаяДекада - 7;
		Год = Цел(Декада/36);
		ДекадаВГоду = Декада - Год*36;
		МесяцВГоду = Цел(ДекадаВГоду/3) + 1;
		ДекадаВМесяце = ДекадаВГоду - (МесяцВГоду-1)*3 + 1;
		Период = Дата(Год, МесяцВГоду, (ДекадаВМесяце - 1) * 10 + 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Декада = НачальнаяДекада По КонечнаяДекада Цикл
			Год = Цел(Декада/36);
			ДекадаВГоду = Декада - Год*36;
			МесяцВГоду = Цел(ДекадаВГоду/3) + 1;
			ДекадаВМесяце = ДекадаВГоду - (МесяцВГоду-1)*3 + 1;
			Период = Дата(Год, МесяцВГоду, (ДекадаВМесяце - 1) * 10 + 1);
			Представление = Формат(Период, "ДФ='MMMM yyyy'") + ", " + Лев("III", ДекадаВМесяце) + " " + СтрокаДекада + ?(Декада = ТекущаяДекадаАбсолютно, " - " + НСтр("ru = 'эта декада'"), "");
			СписокПериодов.Добавить(Период, Представление);
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		Декада = КонечнаяДекада + 1;
		Год = Цел(Декада/36);
		ДекадаВГоду = Декада - Год*36;
		МесяцВГоду = Цел(ДекадаВГоду/3) + 1;
		ДекадаВМесяце = ДекадаВГоду - (МесяцВГоду-1)*3 + 1;
		Период = Дата(Год, МесяцВГоду, (ДекадаВМесяце - 1) * 10 + 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктПозжеПредставление);
		
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц") Тогда
		ТекущийГод   = Год(Сегодня);
		ВыбранныйГод = Год(НачалоПериода);
		ТекущийМесяц   = ТекущийГод*12   + Месяц(Сегодня);
		ВыбранныйМесяц = ВыбранныйГод*12 + Месяц(НачалоПериода);
		
		// Вычисление начального и конечного периода по формуле.
		Коэффициент = (ВыбранныйМесяц - ТекущийМесяц - 2)/7;
		Коэффициент = Цел(Коэффициент - ?(Коэффициент < 0, 0.9, 0)); // Отрицательные числа округляются в большую часть.
		НачальныйМесяц = ТекущийМесяц + 2 + Коэффициент*7;
		КонечныйМесяц  = НачальныйМесяц + 6;
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		Месяц = НачальныйМесяц - 7;
		Год = Цел((Месяц - 1) / 12);
		МесяцВГоду = Месяц - Год * 12;
		Период = Дата(Год, МесяцВГоду, 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Месяц = НачальныйМесяц По КонечныйМесяц Цикл
			Год = Цел((Месяц - 1) / 12);
			МесяцВГоду = Месяц - Год * 12;
			Период = Дата(Год, МесяцВГоду, 1);
			СписокПериодов.Добавить(Период, Формат(Период, "ДФ='MMMM yyyy'") + ?(Год = ТекущийГод И ТекущийМесяц = Месяц, " - " + НСтр("ru = 'этот месяц'"), ""));
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		Месяц = КонечныйМесяц + 1;
		Год = Цел((Месяц - 1) / 12);
		МесяцВГоду = Месяц - Год * 12;
		Период = Дата(Год, МесяцВГоду, 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктПозжеПредставление);
		
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Квартал") Тогда
		ТекущийГод = Год(Сегодня);
		ВыбранныйГод = Год(НачалоПериода);
		ТекущийКвартал   = 1 + Цел((Месяц(Сегодня)-1)/3);
		ВыбранныйКвартал = 1 + Цел((Месяц(НачалоПериода)-1)/3);
		ТекущийКварталАбсолютно   = ТекущийГод*4   + ТекущийКвартал   - 1;
		ВыбранныйКварталАбсолютно = ВыбранныйГод*4 + ВыбранныйКвартал - 1;
		СтрокаКвартал = НСтр("ru = 'квартал'");
		
		// Вычисление начального и конечного периода по формуле.
		Коэффициент = (ВыбранныйКварталАбсолютно - ТекущийКварталАбсолютно - 2)/7;
		Коэффициент = Цел(Коэффициент - ?(Коэффициент < 0, 0.9, 0)); // Отрицательные числа округляются в большую часть.
		НачальныйКвартал = ТекущийКварталАбсолютно + 2 + Коэффициент*7;
		КонечныйКвартал  = НачальныйКвартал + 6;
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		Квартал = НачальныйКвартал - 7;
		Год = Цел(Квартал/4);
		КварталВГоду = Квартал - Год*4 + 1;
		МесяцВГоду = (КварталВГоду-1)*3 + 1;
		Период = Дата(Год, МесяцВГоду, 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Квартал = НачальныйКвартал По КонечныйКвартал Цикл
			Год = Цел(Квартал/4);
			КварталВГоду = Квартал - Год*4 + 1;
			МесяцВГоду = (КварталВГоду-1)*3 + 1;
			Период = Дата(Год, МесяцВГоду, 1);
			Представление = ?(КварталВГоду = 4, "IV", Лев("III", КварталВГоду)) + " " + СтрокаКвартал + " " + Формат(Период, "ДФ='yyyy'") + ?(Квартал = ТекущийКварталАбсолютно, " - " + НСтр("ru = 'этот квартал'"), "");
			СписокПериодов.Добавить(Период, Представление);
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		Квартал = КонечныйКвартал + 1;
		Год = Цел(Квартал/4);
		КварталВГоду = Квартал - Год*4 + 1;
		МесяцВГоду = (КварталВГоду-1)*3 + 1;
		Период = Дата(Год, МесяцВГоду, 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктПозжеПредставление);
		
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Полугодие") Тогда
		ТекущийГод = Год(Сегодня);
		ВыбранныйГод = Год(НачалоПериода);
		ТекущееПолугодие   = 1 + Цел((Месяц(Сегодня)-1)/6);
		ВыбранноеПолугодие = 1 + Цел((Месяц(НачалоПериода)-1)/6);
		ТекущееПолугодиеАбсолютно   = ТекущийГод*2   + ТекущееПолугодие   - 1;
		ВыбранноеПолугодиеАбсолютно = ВыбранныйГод*2 + ВыбранноеПолугодие - 1;
		СтрокаПолугодие = НСтр("ru = 'полугодие'");
		
		// Вычисление начального и конечного периода по формуле.
		Коэффициент = (ВыбранноеПолугодиеАбсолютно - ТекущееПолугодиеАбсолютно - 2)/7;
		Коэффициент = Цел(Коэффициент - ?(Коэффициент < 0, 0.9, 0)); // Отрицательные числа округляются в большую часть.
		НачальноеПолугодие = ТекущееПолугодиеАбсолютно + 2 + Коэффициент*7;
		КонечноеПолугодие  = НачальноеПолугодие + 6;
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		Полугодие = НачальноеПолугодие - 7;
		Год = Цел(Полугодие/2);
		ПолугодиеВГоду = Полугодие - Год*2 + 1;
		МесяцВГоду = (ПолугодиеВГоду-1)*6 + 1;
		Период = Дата(Год, МесяцВГоду, 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Полугодие = НачальноеПолугодие По КонечноеПолугодие Цикл
			Год = Цел(Полугодие/2);
			ПолугодиеВГоду = Полугодие - Год*2 + 1;
			МесяцВГоду = (ПолугодиеВГоду-1)*6 + 1;
			Период = Дата(Год, МесяцВГоду, 1);
			Представление = Лев("II", ПолугодиеВГоду) + " " + СтрокаПолугодие + " " + Формат(Период, "ДФ='yyyy'") + ?(Полугодие = ТекущееПолугодиеАбсолютно, " - " + НСтр("ru = 'это полугодие'"), "");
			СписокПериодов.Добавить(Период, Представление);
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		Полугодие = КонечноеПолугодие + 1;
		Год = Цел(Полугодие/2);
		ПолугодиеВГоду = Полугодие - Год*2 + 1;
		МесяцВГоду = (ПолугодиеВГоду-1)*6 + 1;
		Период = Дата(Год, МесяцВГоду, 1);
		СписокПериодов.Добавить(Период, НавигационныйПунктПозжеПредставление);
		
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Год") Тогда
		ТекущийГод = Год(Сегодня);
		ВыбранныйГод = Год(НачалоПериода);
		
		// Вычисление начального и конечного периода по формуле.
		Коэффициент = (ВыбранныйГод - ТекущийГод - 2)/7;
		Коэффициент = Цел(Коэффициент - ?(Коэффициент < 0, 0.9, 0)); // Отрицательные числа округляются в большую часть.
		НачальныйГод = ТекущийГод + 2 + Коэффициент*7;
		КонечныйГод = НачальныйГод + 6;
		
		// Добавление навигационного пункта "<Ранее>..." для перехода к более ранним периодам.
		СписокПериодов.Добавить(Дата(НачальныйГод-7, 1, 1), НавигационныйПунктРанееПредставление);
		
		// Добавление значений.
		Для Год = НачальныйГод По КонечныйГод Цикл
			СписокПериодов.Добавить(Дата(Год, 1, 1), Формат(Год, "ЧГ=") + ?(Год = ТекущийГод, " - " + НСтр("ru = 'этот год'"), ""));
		КонецЦикла;
		
		// Добавление навигационного пункта "<Позже>..." для перехода к более поздним периодам.
		СписокПериодов.Добавить(Дата(КонечныйГод+7, 1, 1), НавигационныйПунктПозжеПредставление);
		
	КонецЕсли;
	
	Возврат СписокПериодов;
КонецФункции


#Область ПредставлениеОтображений

// Функция возвращает представление времени.
//
// Параметры:
//  Время - Число - Время в секундах.
//
// Возвращаемое значение:
//   Строка - Представление времени.
//
Функция СформироватьПредставлениеВремени(Время) Экспорт
	
	Часы    = Цел(Время/3600);
	Минуты  = Окр((Время - Часы*3600)/60);
	Если Часы = 0 И Минуты = 0 Тогда
		Возврат НСтр("ru = '0 мин.'");
	Иначе	
		Возврат НСтр(СтрШаблон("ru = '%1%2'", ?(Часы = 0, "", Строка(Часы) + " ч. "), ?(Минуты = 0, "", Строка(Минуты) + " мин.")));
	КонецЕсли;
	
КонецФункции // СформироватьПредставлениеВремени()

// Функция возвращает представление времени.
//
// Параметры:
//  Время - Число - Время в секундах.
//
// Возвращаемое значение:
//   Строка - Представление времени.
//
Функция СформироватьПредставлениеВремениДоСекунды(Время) Экспорт
	
	Часы    = Цел(Время/3600);
	Минуты  = Цел((Время - Часы*3600)/60);
	Секунды = Время - Часы * 3600 - Минуты * 60;
	Если Часы = 0 И Минуты = 0 И Секунды = 0 Тогда
		Возврат НСтр("ru = '0 сек.'");
	Иначе	
		Возврат НСтр(СтрШаблон("ru = '%1%2%3'", ?(Часы = 0, "", Строка(Часы) + " ч. "), ?(Минуты = 0, "", Строка(Минуты) + " мин."), ?(Секунды = 0, "", Строка(Секунды) + " сек.")));
	КонецЕсли;
	
КонецФункции // СформироватьПредставлениеВремениДоСекунды()

// Функция возвращает представление расстояния.
//
// Параметры:
//  Расстояние - Число - Расстояние в км.
//
// Возвращаемое значение:
//   Строка - Представление времени.
//
Функция СформироватьПредставлениеРасстояния(Расстояние) Экспорт
	
	Километры    = Цел(Расстояние);
	Метры        = (Расстояние - Километры)*1000;
	Если Километры = 0 Тогда
		Возврат НСтр(СтрШаблон("ru = '%1 %2'", Метры, ?(Метры > 0, "м", "км")));
	ИначеЕсли Метры = 0 Тогда
		Возврат НСтр(СтрШаблон("ru = '%1 км'", Километры));
	Иначе
		Возврат НСтр(СтрШаблон("ru = '%1 км %2 м'", Километры, Метры));
	КонецЕсли;
	
КонецФункции // СформироватьПредставлениеРасстояния()

// Функция возвращает представление временного окна.
//
// Параметры:
//  ВременноеОкноНачало    - Дата - Период.
//  ВременноеОкноОкончание - Дата - Период.
//
// Возвращаемое значение:
//   Строка - Представление временного окна.
// 
Функция СформироватьПредставлениеВременногоОкна(ВременноеОкноНачало, ВременноеОкноОкончание) Экспорт
	
	ОткрытоеНачало    = (ВременноеОкноНачало = Дата('00010101'));
	ОткрытоеОкончание = (ВременноеОкноОкончание = Дата('00010101'));
	Представление = "";
	Если ОткрытоеНачало И ОткрытоеОкончание Тогда
		Представление = НСтр("ru = 'постоянная доступность'");
	ИначеЕсли ОткрытоеНачало И Не ОткрытоеОкончание Тогда
		Представление = НСтр(СтрШаблон("ru = 'по %1'", Формат(ВременноеОкноОкончание, "Л=ru_RU; ДФ='dd.MM.yy ЧЧ:mm'")));
	ИначеЕсли Не ОткрытоеНачало И ОткрытоеОкончание Тогда
		Представление = НСтр(СтрШаблон("ru = 'с %1'", Формат(ВременноеОкноНачало, "Л=ru_RU; ДФ='dd.MM.yy ЧЧ:mm'")));
	Иначе
		НачалоДня = НачалоДня(ВременноеОкноНачало);
		Если НачалоДня = НачалоДня(ВременноеОкноОкончание) Тогда
			Представление = НСтр(СтрШаблон("ru = '%1 с %2 по %3'", Формат(НачалоДня, "Л=ru_RU; ДФ='dd.MM.yy'"), 
			                                                       Формат(ВременноеОкноНачало, "Л=ru_RU; ДФ='ЧЧ:mm'"),
			                                                       Формат(ВременноеОкноОкончание, "Л=ru_RU; ДФ='ЧЧ:mm'")));
		Иначе
			Представление = НСтр(СтрШаблон("ru = 'с %1 по %2'", Формат(ВременноеОкноНачало, "Л=ru_RU; ДФ='dd.MM.yy ЧЧ:mm'"), Формат(ВременноеОкноОкончание, "Л=ru_RU; ДФ='dd.MM.yy ЧЧ:mm'")));
		КонецЕсли;
	КонецЕсли;
	Возврат Представление;		
	
КонецФункции // СформироватьПредставлениеВременногоОкна()

// Функция возвращает представление суммы расходов/доходов в валюте.
//
// Параметры:
//  Сумма - Число - Сумма.
//	Валюта - СправочникСсылка.Валюты - валюта.
//
// Возвращаемое значение:
//   Строка - Представление суммы.
//
Функция СформироватьПредставлениеСуммыСВалютой(Сумма, Валюта = Неопределено) Экспорт
	
	Если НЕ Валюта = Неопределено Тогда
		Возврат НСтр(СтрШаблон("ru = '%1 %2'",Сумма, Валюта));
	Иначе
		Возврат НСтр(СтрШаблон("ru = '%1'",Сумма));
	КонецЕсли;
	
КонецФункции // СформироватьПредставлениеСуммыСВалютой()

// Функция возвращает представление географической координаты
//
// Параметры:
//  Широта - Число - Координата.
//	Долгота - Число - Координата.
//
// Возвращаемое значение:
//   Строка - Представление координаты.
//
Функция СформироватьПредставлениеКоординаты(Широта, Долгота) Экспорт
	
	Возврат СтрШаблон("%1;%2", Формат(Широта, "ЧРД=.; ЧН=; ЧГ=0"), Формат(Долгота, "ЧРД=.; ЧН=; ЧГ=0"));
	
КонецФункции

#КонецОбласти 

#Область УправлениеОтображениемФормы

// Процедура изменяет значения свойств элементов управляемой формы.
//
// Параметры:
//  ЭлементФормы - Элемент формы.
//	МассивСвойств - массив структур
//		* "Ключ" - имя свойства ("ТолькоПросмотр", "Видимость", "Доступность")
//		* "Значение" - значение свойства.
//	МассивИсключенийСПодчинением - Массив - перечислены имена элементов, по которым не требуется приименение обозначенных изменений свойств, не устанавливаются свойства и у всех подчиненных элементов.
//	МассивИсключений - Массив - перечислены имена элементов, по которым не требуется приименение обозначенных изменений свойств.
//
Процедура ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(ЭлементФормы, МассивСвойств, МассивИсключенийСПодчинением = Неопределено, МассивИсключений = Неопределено) Экспорт
	
	Если Не МассивИсключенийСПодчинением = Неопределено Тогда
		Если Не МассивИсключенийСПодчинением.Найти(ЭлементФормы.Имя) = Неопределено Тогда
		    Возврат;
		КонецЕсли; 
	КонецЕсли;
	
	УстановитьСвойство = Истина;
	
	Если НЕ МассивИсключений = Неопределено Тогда
		Если Не МассивИсключений.Найти(ЭлементФормы.Имя) = Неопределено Тогда
		    УстановитьСвойство = Ложь;
		КонецЕсли; 
	КонецЕсли;
	
	Если УстановитьСвойство Тогда
		ТекущееСвойство = Неопределено;
		Значение = Неопределено;
		Для каждого текСвойство Из МассивСвойств Цикл
			мсвДоступныеТипы = ПолучитьДоступныеТипыЭлементовФормы(текСвойство.Ключ);
			ТипЭлементаФормы = ТипЗнч(ЭлементФормы);
			Если мсвДоступныеТипы.Найти(ТипЭлементаФормы) = Неопределено Тогда
				Продолжить;
			Иначе
				ТекущееСвойство = текСвойство.Ключ;
				Значение       = текСвойство.Значение;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если ТекущееСвойство = Неопределено Тогда
			Возврат;
		КонецЕсли; 
		
	КонецЕсли;
	
	Если Истина
		И ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы")
		И ЭлементФормы.ИмяКоманды = "" 
	Тогда
		Возврат;
	КонецЕсли; 
	
	Если УстановитьСвойство Тогда
		ЭлементФормы[ТекущееСвойство] = Значение;
	КонецЕсли; 	
	
	Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") Или ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
		ПодчиненныеЭлементы = ЭлементФормы.ПодчиненныеЭлементы;
		Если ПодчиненныеЭлементы.Количество() > 0 Тогда
			Для каждого ПодчиненныйЭлемент Из ПодчиненныеЭлементы Цикл
				ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(ПодчиненныйЭлемент, МассивСвойств, МассивИсключенийСПодчинением, МассивИсключений)
			КонецЦикла; 
		КонецЕсли; 
		
		Если ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
			ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(ЭлементФормы.КоманднаяПанель, МассивСвойств, МассивИсключенийСПодчинением, МассивИсключений)
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Функция ПолучитьДоступныеТипыЭлементовФормы(Свойство)
	
	мсвДоступныеТипы = Новый Массив;
	Если Свойство = "ТолькоПросмотр" Тогда
		мсвДоступныеТипы.Добавить(Тип("ГруппаФормы"));
		мсвДоступныеТипы.Добавить(Тип("ТаблицаФормы"));
		мсвДоступныеТипы.Добавить(Тип("ПолеФормы"));
	ИначеЕсли Свойство = "Доступность" Тогда
		мсвДоступныеТипы.Добавить(Тип("ГруппаФормы"));
		мсвДоступныеТипы.Добавить(Тип("ТаблицаФормы"));
		мсвДоступныеТипы.Добавить(Тип("ПолеФормы"));
		//мсвДоступныеТипы.Добавить(Тип("ДекорацияФормы"));
		мсвДоступныеТипы.Добавить(Тип("КнопкаФормы"));
		мсвДоступныеТипы.Добавить(Тип("ДополнениеЭлементаФормы"));
	ИначеЕсли Свойство = "Видимость" Тогда
	КонецЕсли; 
	
	Возврат мсвДоступныеТипы;
	
КонецФункции

Процедура УстановитьКартинкуКомментария(СтраницаКомментария, Комментарий) Экспорт
	Если ПустаяСтрока(Комментарий) Тогда
		СтраницаКомментария.Картинка = Новый Картинка;
	Иначе
		СтраницаКомментария.Картинка = БиблиотекаКартинок.Комментарий;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОптмизироватьМаршрут

// Сравнивает элементы массивов по значениям.
//
// Параметры:
//  Массив1  - Массив - Список элеменотов сравнения.
//  Массив2  - Массив - Список элеменотов сравнения.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция МассивыСИдентичнымПорядком(Массив1, Массив2) Экспорт
	
	МассивИдентичны = Истина;
	
	Если НЕ Массив1.Количество() = Массив2.Количество() Тогда
		МассивИдентичны = Ложь;
	КонецЕсли;
	
	Если МассивИдентичны Тогда
		Сч = 0;
		Для каждого ЭлементМассива1 Из Массив1 Цикл
			Если НЕ ЭлементМассива1 = Массив2[Сч] Тогда
				МассивИдентичны = Ложь;
				Прервать;
			КонецЕсли;
			Сч = Сч+1;
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивИдентичны;
	
КонецФункции // МассивыСИдентичнымПорядком()

// Выполним проверку данныхпоподания во временные окна.
//
// Параметры:
//  Маршрут  - ТаблицаЗначений - Маршрут рейса.
//  Работы  - ТаблицаЗначений - работы по рейсу.
//  ДопустимоеОтклонениеОтВременногоОкна  - Число - Количество минут отклонения во времени.
//
// Возвращаемое значение:
//   Булево - Истина если ошибок нет, ложь если не попадает во временное окно.
//
Функция ВыполнитьКонтрольНарушенияВременныхОкон(Маршрут, Работы, ДопустимоеОтклонениеОтВременногоОкна) Экспорт
	
	ПопадаетВоВременноеОкно = Истина;
	ОтклонениеСекунд = ДопустимоеОтклонениеОтВременногоОкна * 60;
	Для каждого текТочка Из Маршрут Цикл								
		Если Не ЗначениеЗаполнено(текТочка.ВременноеОкноНачало) И Не ЗначениеЗаполнено(текТочка.ВременноеОкноОкончание) Тогда
			ПопадаетВоВременноеОкно = Истина;
		ИначеЕсли Не ЗначениеЗаполнено(текТочка.ВременноеОкноНачало)
			И (текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание
			ИЛИ (текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание + ОтклонениеСекунд
			И текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание - ОтклонениеСекунд)
			)Тогда
			ПопадаетВоВременноеОкно = Истина;
		ИначеЕсли Не ЗначениеЗаполнено(текТочка.ВременноеОкноОкончание)
			И (текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало
			ИЛИ(текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало + ОтклонениеСекунд
			И текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало - ОтклонениеСекунд)
			)Тогда
			ПопадаетВоВременноеОкно = Истина;
		ИначеЕсли (текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание
			ИЛИ (текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание + ОтклонениеСекунд
			И текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание - ОтклонениеСекунд))
			И (текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало
			ИЛИ (текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало + ОтклонениеСекунд
			И текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало - ОтклонениеСекунд))Тогда
			ПопадаетВоВременноеОкно = Истина;
		Иначе
			ПопадаетВоВременноеОкно = Ложь;	
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	Если ПопадаетВоВременноеОкно Тогда
		
		Для каждого текРабота Из Работы Цикл
			Если Не ЗначениеЗаполнено(текРабота.ВременноеОкноНачало) И Не ЗначениеЗаполнено(текРабота.ВременноеОкноОкончание) Тогда
				ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текРабота.ВременноеОкноНачало)
				И ((текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание
				ИЛИ ((текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание + ОтклонениеСекунд
				И (текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание - ОтклонениеСекунд)
				)Тогда
				ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текРабота.ВременноеОкноОкончание)
				И (текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало
				ИЛИ (текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало + ОтклонениеСекунд
				И текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало - ОтклонениеСекунд)
				)Тогда
				ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли (текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало
				ИЛИ(текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало + ОтклонениеСекунд
				И текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало - ОтклонениеСекунд)) 
				И ((текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание
				ИЛИ ((текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание + ОтклонениеСекунд
				И (текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание - ОтклонениеСекунд)
				)Тогда	
				ПопадаетВоВременноеОкно = Истина;
			Иначе
				ПопадаетВоВременноеОкно = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	Возврат ПопадаетВоВременноеОкно;
	
КонецФункции // ВыполнитьКонтрольНарушенияВременныхОкон()

#КонецОбласти

#КонецОбласти