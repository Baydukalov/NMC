
#Область ПрограммныйИнтерфейс

// Процедура - Устанавливает видимость команда группы "Создать на основании"
//             Видимость команд в зависимости от формы и данных объекта,
//             настраивается в экспортной процедуре менеджера объекта
//	          УстановитьВидимостьКомандВводаНаОсновании(Форма, ДополнительныеПараметры)
//              * список команд задается процедурой  - ДобавитьКомандуВСписокКоманд
//              * вид списка, то есть видимые команды в нем содержатся или наоборот
//                устанавливается процедурой - УстановитьПризнакДоступностиСпискаКоманд 
//             
//
// Параметры:
//  Форма	 - Форма - форма объекта или списка.
//
Процедура УстановитьВидимостьКоманд(Форма) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	
	МетаданныеФормы = Метаданные.НайтиПоПолномуИмени(Форма.ИмяФормы);
	МетаданныеОбъект = МетаданныеФормы.Родитель();
	
	Если ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъект) Тогда
		
		ДополнительныеПараметры.Вставить("ЭтоФормаОбъекта", МетаданныеОбъект.ОсновнаяФормаОбъекта.Имя = МетаданныеФормы.Имя);
		ДополнительныеПараметры.Вставить("ЭтоФормаСписка", МетаданныеОбъект.ОсновнаяФормаСписка.Имя = МетаданныеФормы.Имя);
		ДополнительныеПараметры.Вставить("ЭтоФормаДляВыборка", МетаданныеОбъект.ОсновнаяФормаДляВыбора.Имя = МетаданныеФормы.Имя);
		
	КонецЕсли;
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеОбъект.ПолноеИмя());
	
	СписокКоманд = Новый Массив;
	
	ДополнительныеПараметры.Вставить("СписокКоманд", СписокКоманд);
	ДополнительныеПараметры.Вставить("ЭтоСписокДоступныхКоманд", Ложь);
	
	МенеджерОбъекта.УстановитьВидимостьКомандВводаНаОсновании(Форма, ДополнительныеПараметры);
	
	ЭтоДоступныеКоманды = ДополнительныеПараметры.ЭтоСписокДоступныхКоманд;
	
	мсвКоманды = Новый Массив;
	
	Для каждого сткКоманды Из СписокКоманд Цикл
		ИмяМенеджераОбъекта = ?(сткКоманды.ИмяМенеджераОбъекта = Неопределено, МетаданныеОбъект.Имя, сткКоманды.ИмяМенеджераОбъекта);
		ПолноеИмяКоманды = СтрШаблон("Форма%1%2%3", сткКоманды.ИмяТипаОбъекта, ИмяМенеджераОбъекта, сткКоманды.ИмяКоманды);
		мсвКоманды.Добавить(ПолноеИмяКоманды);
	КонецЦикла;
	
	КомандыСоздатьНаОсновании = Форма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаСоздатьНаОсновании.ПодчиненныеЭлементы;
		
	Для каждого КомандаСоздатьНаОсновании Из КомандыСоздатьНаОсновании Цикл
		
		Если ЭтоДоступныеКоманды Тогда
			Если мсвКоманды.Найти(КомандаСоздатьНаОсновании.Имя) = Неопределено  Тогда
				КомандаСоздатьНаОсновании.Видимость = Ложь;
			Иначе
				КомандаСоздатьНаОсновании.Видимость = Истина;
			КонецЕсли;
		Иначе
			Если мсвКоманды.Найти(КомандаСоздатьНаОсновании.Имя) = Неопределено  Тогда
				КомандаСоздатьНаОсновании.Видимость = Истина;
			Иначе
				КомандаСоздатьНаОсновании.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура - Добавляет команду в список команд
//
// Параметры:
//  ИмяКоманды				 - Строка	 - Имя команды, как оно задано в списке команд объекта,
//                                         например, "СформироватьПутевойЛист" 
//                                         или "ОбработатьПрерыванияГрузовыхПотоков".
//  ИмяТипаОбъекта			 - Строка	 - имя типа объекта "Документ", "Справочник", "БизнесПроцесс".
//  ИмяМенеджераОбъекта		 - Строка, Неопределено - имя указывается тогда, когда команда не принадлежит объекту 
//                               менеджера в котором добавляется. Например, если в модуле менеджера документа
//                               "упРейс", добавляется команда бизнес-процесса "Задание", то здесь следует 
//                               указать "Задание". Если в модуле менеджера документа "упРейс", добавляется команда 
//                               рейса "СформироватьПрохождениеТочки" то можно ничего не указывать. 
//						   По умолчанию берется имя менеджера того модуля в котором добавляется команда.
//                               
//  ДополнительныеПараметры	 - Структура - параметры настройки видимости команд.
//
Процедура ДобавитьКомандуВСписокКоманд(ИмяКоманды, ИмяТипаОбъекта = "Документ", ИмяМенеджераОбъекта = Неопределено, ДополнительныеПараметры) Экспорт
	НоваяКоманда = Новый Структура();
	НоваяКоманда.Вставить("ИмяТипаОбъекта", ИмяТипаОбъекта);
	НоваяКоманда.Вставить("ИмяМенеджераОбъекта", ИмяМенеджераОбъекта);
	НоваяКоманда.Вставить("ИмяКоманды", ИмяКоманды);
	ДополнительныеПараметры.СписокКоманд.Добавить(НоваяКоманда);
КонецПроцедуры

// Процедура - Установить признак доступности списка команд
//
// Параметры:
//  ЭтоСписокДоступныхКоманд	 - Булево	 - для настройки видимости команд используется список,
//                               он может использоваться в двух ролях как список доступных(видимых) команд
//                               или как список недоступных(команд с отключенной видимостью).
//                               * Истина - список используется как список доступных команд;
//                               * Ложь - список используется как список не доступных команд
//                              
//  ДополнительныеПараметры	 - Структура - параметры настройки видимости команд.
//
Процедура УстановитьПризнакДоступностиСпискаКоманд(ЭтоСписокДоступныхКоманд = Ложь, ДополнительныеПараметры) Экспорт
	ДополнительныеПараметры.Вставить("ЭтоСписокДоступныхКоманд", ЭтоСписокДоступныхКоманд);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

