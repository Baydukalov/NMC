
#Область ПрограммныйИнтерфейс

// Процедура - Выполнить обмен СШП
//
// Параметры:
//  Источник - Справочник.Объект, Документ.Объект - объект-источник события. 
//  Отказ	 - Булево - Признак отказа от дальнейшей обработки. 
//
Процедура ВыполнитьОбменСШП(Источник, Отказ) экспорт			
	Если Отказ = Ложь тогда
		ЗарегистрироватьСобытие(Источник);
	КонецЕсли;		
КонецПроцедуры

// Процедура - Выполнить обмен СШПРС
//
// Параметры:
//  Источник	 - Регистрсведений.НаборЗаписей - набор-источник события.
//  Отказ		 - Булево - Признак отказа от дальнейшей обработки.
//  Замещение	 - Булево - Признак необходимости замещения существующей записи.
//
Процедура ВыполнитьОбменСШПРС(Источник, Отказ, Замещение) Экспорт 	
	Если Отказ = Ложь И Источник.Количество() > 0 Тогда 
// +++.17/06/09-09:56:48.<УП> третий параметр делает сериализацию для наборов записей обязательной. Оставим это на выбор разработчиков. 
		//ЗарегистрироватьСобытие(Источник, Истина, Истина);
		ЗарегистрироватьСобытие(Источник, Истина);		
// ---.17/06/09-09:56:48.<УП>
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Зарегистрировать событие
//
// Параметры:
//  Источник - Справочни.Объект, Документ.Объект, РегистрСведений.НаборЗаписей - объект-источник события.
//
Процедура ЗарегистрироватьСобытие(Источник, эторегистр = Ложь, ЭтоУдаление = Ложь)
	////Расскоментировать для использования
	//// вап783а
	//ОбъектМетаданных = Источник.Метаданные();
	////Если Источник.Метаданные() = Метаданные.Справочники.Найти("ИдентификаторыОбъектовМетаданных") Тогда
	//Если Истина
	//	И ОбщегоНазначенияПовтИсп.РазделениеВключено()
	//	И ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей()
	//	И Не ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(ОбъектМетаданных, Метаданные.ОбщиеРеквизиты.ОбластьДанныхОсновныеДанные.Имя)
	//	//И Не ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(ОбъектМетаданных, Метаданные.ОбщиеРеквизиты.ОбластьДанныхВспомогательныеДанные.Имя)
	//Тогда
	//	// Чтобы обновление информационной базы выполнилось во фреше.
	//	Возврат;
	//КонецЕсли;
	исключитьИзОбработки = Источник.ДополнительныеСвойства.Свойство("СШПНеобрабатывать");
	Если исключитьИзОбработки = Ложь тогда
		типОбъекта = Источник.Метаданные().ПолноеИмя();
		Если сшпКэшируемыеФункции.УчаствуетВИнтеграции(типОбъекта, сшпФункциональныеОпции.ВерсияОбработчиков()) тогда
			значениеХранения = Неопределено;
			
			Если ЭтоУдаление Тогда 
				методХранения = Перечисления.сшпМетодХранения.Сериализация;
			Иначе
				методХранения = сшпКэшируемыеФункции.ПолучитьМетодХранения(типОбъекта, сшпФункциональныеОпции.ВерсияОбработчиков());
			КонецЕсли;
			
			форматСериализация = сшпФункциональныеОпции.ФорматСериализации();
			Если методХранения = Перечисления.сшпМетодХранения.Сериализация тогда				
				значениеХранения = сшпОбщегоНазначения.СериализоватьОбъект(форматСериализация, Источник);
			Иначе
				Если эторегистр тогда
					значениеХранения = Источник.Отбор;
				Иначе
					значениеХранения = Источник.Ссылка;
					// +++.17/06/05-11:25:00.<УП>
					сшпМодульОбработкиДанных.ЗаписатьВИдентификаторОбмена(Источник.Ссылка);
					// ---.17/06/05-11:25:00.<УП>
				КонецЕсли;
			КонецЕсли;
			
			Если эторегистр Или ЭтоУдаление тогда
				СсылкаНаОбъект = Неопределено;
			Иначе
				СсылкаНаОбъект = Источник.Ссылка;
			КонецЕсли;
			
			Если НЕ значениеХранения = Неопределено тогда
				#Если ТолстыйКлиентОбычноеПриложение Тогда
					значениеХранения = Новый ХранилищеЗначения(значениеХранения);
					ПомещатьВХранилищеЗначения = Ложь;
				#Иначе
					ПомещатьВХранилищеЗначения = Истина;
				#КонецЕсли	
				сшпРаботаСДанными.ПоместитьВОчередьИсходящих(типОбъекта, значениеХранения, форматСериализация, методХранения, ПомещатьВХранилищеЗначения, ЭтоУдаление, СсылкаНаОбъект);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры	

// Процедура - Зарегистрировать событие удаления
//
// Параметры:
//  Источник - Справочни.Объект, Документ.Объект, РегистрСведений.НаборЗаписей - объект-источник события.
//  Отказ - Булево - объект-источник события.
//
Процедура ОбменСправочникиДокументыПередУдалениемПередУдалением(Источник, Отказ) Экспорт
	Если Отказ = Ложь тогда
		ЗарегистрироватьСобытие(Источник, , Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ВыполнитьОбменСШПРР(Источник, Отказ, Замещение, ТолькоЗапись, ЗаписьФактическогоПериодаДействия, ЗаписьПерерасчетов) Экспорт
	Если Отказ = Ложь И Источник.Количество() > 0 Тогда 
		ЗарегистрироватьСобытие(Источник, Истина, Истина);
	КонецЕсли;		
КонецПроцедуры

#КонецОбласти
