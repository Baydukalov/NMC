
#Область ПрограммныйИнтерфейс

// Функция - возвращает список допустимых ТС для указанной зоны.
//
// Параметры:
//  тбзТранспортныеСредства  - ТаблицаЗначений - Таблица данных.
//  ЗонаПогрузки  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СписокЗначений - Результат выполнения.
//
Функция ПолучитьДоступныеПоЗонеПогрузкиТранспортныеСредства(тбзТранспортныеСредства, ЗонаПогрузки) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	тбТранспортныеСредства.Объект КАК ТранспортноеСредство
	               |ПОМЕСТИТЬ втТСНачальные
	               |ИЗ
	               |	&тбТранспортныеСредства КАК тбТранспортныеСредства
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбТС.Ссылка КАК ТранспортноеСредство,
	               |	ВЫБОР
	               |		КОГДА тбТС.ДоступныеЗоны = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиГеографическихЗон.ПустаяСсылка)
	               |			ТОГДА тбТС.ТипТС.ДоступныеЗоны
	               |		ИНАЧЕ тбТС.ДоступныеЗоны
	               |	КОНЕЦ КАК ПравилаДоступностиГеографическихЗон
	               |ПОМЕСТИТЬ втПараметрыТС
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК тбТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТСНачальные КАК втТСНачальные
	               |		ПО тбТС.Ссылка = втТСНачальные.ТранспортноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбТранспортныеСредства.ТранспортноеСредство
	               |ИЗ
	               |	втПараметрыТС КАК тбТранспортныеСредства
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДоступныеГеографическиеЗоны КАК упДоступныеГеографическиеЗоны
	               |		ПО тбТранспортныеСредства.ПравилаДоступностиГеографическихЗон = упДоступныеГеографическиеЗоны.ПравилаДоступности
	               |ГДЕ
	               |	(упДоступныеГеографическиеЗоны.ГеографическаяЗона = &ГеографическаяЗона
	               |			ИЛИ тбТранспортныеСредства.ПравилаДоступностиГеографическихЗон = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиГеографическихЗон.ПустаяСсылка))";
	Запрос.УстановитьПараметр("ГеографическаяЗона", 	ЗонаПогрузки);
	Запрос.УстановитьПараметр("тбТранспортныеСредства", тбзТранспортныеСредства);
	
	спзДоступныеТранспортныеСредства = Новый СписокЗначений;			   
	спзДоступныеТранспортныеСредства.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТранспортноеСредство"));
					   
	Возврат спзДоступныеТранспортныеСредства;

КонецФункции // ПолучитьДоступныеПоЗонеПогрузкиТранспортныеСредства()

// Функция - возвращает список допустимых типов ТС для указанной зоны.
//
// Параметры:
//  тбзТипыТС  - ТаблицаЗначений - Таблица данных.
//  ЗонаПогрузки  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СписокЗначений - Результат выполнения.
//
Функция ПолучитьДоступныеПоЗонеПогрузкиТипыТС(тбзТипыТС, ЗонаПогрузки) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	тбТипыТС.Объект КАК ТипТС
	               |ПОМЕСТИТЬ втТипыТС
	               |ИЗ
	               |	&тбТипыТС КАК тбТипыТС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбТипыТС.Ссылка КАК ТипТС,
	               |	тбТипыТС.ДоступныеЗоны КАК ПравилаДоступностиГеографическихЗон
	               |ПОМЕСТИТЬ втТипыТСДоступныеЗоны
	               |ИЗ
	               |	Справочник.упТипыТС КАК тбТипыТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТипыТС КАК втТипыТС
	               |		ПО тбТипыТС.Ссылка = втТипыТС.ТипТС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбТипыТС.ТипТС
	               |ИЗ
	               |	втТипыТСДоступныеЗоны КАК тбТипыТС
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДоступныеГеографическиеЗоны КАК упДоступныеГеографическиеЗоны
	               |		ПО тбТипыТС.ПравилаДоступностиГеографическихЗон = упДоступныеГеографическиеЗоны.ПравилаДоступности
	               |ГДЕ
	               |	(упДоступныеГеографическиеЗоны.ГеографическаяЗона = &ГеографическаяЗона
	               |			ИЛИ тбТипыТС.ПравилаДоступностиГеографическихЗон = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиГеографическихЗон.ПустаяСсылка))";
	Запрос.УстановитьПараметр("ГеографическаяЗона", 	ЗонаПогрузки);
	Запрос.УстановитьПараметр("тбТипыТС", тбзТипыТС);
	
	спзДоступныеТипыТС = Новый СписокЗначений;			   
	спзДоступныеТипыТС.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТипТС"));
					   
	Возврат спзДоступныеТипыТС;

КонецФункции // ПолучитьДоступныеПоЗонеПогрузкиТранспортныеСредства()

// Функция - возвращает список допустимых ТС работащих по указанному графику.
//
// Параметры:
//  тбзТранспортныеСредства  - ТаблицаЗначений - Таблица данных.
//  ПлановоеНачалоВыполнения - Дата - Период.
//  ПлановоеОкончаниеВыполнения - Дата - Период.
//
// Возвращаемое значение:
//   СписокЗначений - Результат выполнения.
//
Функция ПолучитьДоступныеПоГрафикуРаботыТранспортныеСредства(тбзТранспортныеСредства,  ПлановоеНачалоВыполнения, ПлановоеОкончаниеВыполнения) Экспорт
	
	Запрос = Новый Запрос;
	текМенеджерВТ	= Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = текМенеджерВТ;

	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	тбТранспортныеСредства.Объект КАК ТранспортноеСредство
	               |ПОМЕСТИТЬ втТСНачальные
	               |ИЗ
	               |	&тбТранспортныеСредства КАК тбТранспортныеСредства
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбТС.Ссылка КАК ТранспортноеСредство,
	               |	тбТС.ГрафикРаботы
	               |ПОМЕСТИТЬ втГрафикиРаботыПоТС
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК тбТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТСНачальные КАК втТСНачальные
	               |		ПО тбТС.Ссылка = втТСНачальные.ТранспортноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА НЕ упРасписаниеРаботыТС.Транспорт ЕСТЬ NULL 
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ЕстьРасписаниеНаНужныйИнтервал,
	               |	втТСГрафикиРаботы.ТранспортноеСредство,
	               |	втТСГрафикиРаботы.ГрафикРаботы
	               |ПОМЕСТИТЬ втТранспортныеСредстваБезРасписания
	               |ИЗ
	               |	втГрафикиРаботыПоТС КАК втТСГрафикиРаботы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упЗанятостьТранспорта.СрезПоследних КАК упРасписаниеРаботыТС
	               |		ПО втТСГрафикиРаботы.ТранспортноеСредство = упРасписаниеРаботыТС.Транспорт
	               |			И (упРасписаниеРаботыТС.ДатаНачала < &ДатыОкончанияРабот)
	               |			И (ДОБАВИТЬКДАТЕ(упРасписаниеРаботыТС.ДатаОкончания, СЕКУНДА, 1 + втТСГрафикиРаботы.ТранспортноеСредство.ПерерывМеждуРейсами * 3600) > &ДатыНачалаРабот)
	               |			И (НЕ упРасписаниеРаботыТС.СостояниеТранспорта = ЗНАЧЕНИЕ(Справочник.упСостоянияТС.Свободно))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТСГрафикиРаботы.ТранспортноеСредство,
	               |	втТСГрафикиРаботы.ГрафикРаботы
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВЫБОР
	               |			КОГДА НЕ упРасписаниеРаботыТС.Транспорт ЕСТЬ NULL 
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) = 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	тбТранспортныеСредства.ГрафикРаботы
	               |ИЗ
	               |	втТранспортныеСредстваБезРасписания КАК тбТранспортныеСредства
	               |ГДЕ
	               |	НЕ тбТранспортныеСредства.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)";
				   
	Запрос.УстановитьПараметр("ДатыНачалаРабот", 		ПлановоеНачалоВыполнения);
	Запрос.УстановитьПараметр("ДатыОкончанияРабот", 	ПлановоеОкончаниеВыполнения);
	Запрос.УстановитьПараметр("тбТранспортныеСредства", тбзТранспортныеСредства);
	
	мсвКалендари = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГрафикРаботы");
	
	ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(текМенеджерВТ,мсвКалендари,ПлановоеНачалоВыполнения,ПлановоеОкончаниеВыполнения);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТРасписанияРаботы.ГрафикРаботы,
				   //|	ВТРасписанияРаботы.ВремяОкончания,
				   //|	ВТРасписанияРаботы.ВремяНачала,
				   //|	ВТРасписанияРаботы.ДатаГрафика,
				   //|	РАЗНОСТЬДАТ(ВТРасписанияРаботы.ВремяОкончания, ВТРасписанияРаботы.ВремяНачала, СЕКУНДА) КАК РазностьДат
				      |	СУММА(РАЗНОСТЬДАТ(
					|  ВЫБОР
				   |				КОГДА ВТРасписанияРаботы.ВремяНачала ЕСТЬ NULL 
				   |					ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ, 1), ДЕНЬ)
				   |				ИНАЧЕ ВТРасписанияРаботы.ВремяНачала
				   |			КОНЕЦ, ВЫБОР
				   |				КОГДА ВТРасписанияРаботы.ВремяОкончания ЕСТЬ NULL 
				   |					ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
				   |				ИНАЧЕ ВТРасписанияРаботы.ВремяОкончания
				   |			КОНЕЦ, СЕКУНДА)) КАК ВремяРаботыСекунды
	               |ПОМЕСТИТЬ втВремяРаботыПоГрафику
	               |ИЗ
	               |	ВТРасписанияРаботы КАК ВТРасписанияРаботы
	               |
				   |СГРУППИРОВАТЬ ПО
				   |	ВТРасписанияРаботы.ГрафикРаботы
	               |
				  |ИМЕЮЩИЕ
				      |	СУММА(РАЗНОСТЬДАТ(
					|  ВЫБОР
				   |				КОГДА ВТРасписанияРаботы.ВремяНачала ЕСТЬ NULL 
				   |					ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ, 1), ДЕНЬ)
				   |				ИНАЧЕ ВТРасписанияРаботы.ВремяНачала
				   |			КОНЕЦ, ВЫБОР
				   |				КОГДА ВТРасписанияРаботы.ВремяОкончания ЕСТЬ NULL 
				   |					ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
				   |				ИНАЧЕ ВТРасписанияРаботы.ВремяОкончания
				   |			КОНЕЦ, СЕКУНДА)) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТранспортныеСредстваБезРасписания.ТранспортноеСредство
				  //|	тбВремяРаботыПоГрафику.ВремяОкончания,
				  // |	тбВремяРаботыПоГрафику.ВремяНачала,
				  // |	тбВремяРаботыПоГрафику.ДатаГрафика,
				  // |	тбВремяРаботыПоГрафику.РазностьДат

				   //|	тбВремяРаботыПоГрафику.ВремяРаботыСекунды
	               |ИЗ
	               |	втТранспортныеСредстваБезРасписания КАК втТранспортныеСредстваБезРасписания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втВремяРаботыПоГрафику КАК тбВремяРаботыПоГрафику
	               |		ПО втТранспортныеСредстваБезРасписания.ГрафикРаботы = тбВремяРаботыПоГрафику.ГрафикРаботы
				   |ГДЕ
				   |	(втТранспортныеСредстваБезРасписания.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
				   |			ИЛИ ЕСТЬNULL(тбВремяРаботыПоГрафику.ВремяРаботыСекунды, 0) > 0)
				   |";
	
	
	спзДоступныеТранспортныеСредства = Новый СписокЗначений;			   
	спзДоступныеТранспортныеСредства.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТранспортноеСредство"));
					   
	Возврат спзДоступныеТранспортныеСредства;

КонецФункции

// Функция - Возвращает таблицу доступных для заданного рейса типов транспортных средств,
//				с признаком доступности географической зоны.
//
// Параметры:
//  Рейс - 	ДокументСсылка.упРейс - ссылка на рейс.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица значений с колонками:
//  						Транспортное средство			- СправочникСсылка.упТранспортноеСредство.
//							ГеографическаяЗонаДоступнаТС	- Булево.
//
Функция ПолучитьСписокДоступныхТиповТСПоРейсу(Рейс) Экспорт

	сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, "ЗонаПогрузки, ПлановоеНачалоВыполнения, ПлановоеОкончаниеВыполнения, Перевозчик");
		
	Запрос = Новый Запрос;
	текМенеджерВТ	= Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = текМенеджерВТ;
	
	// На выходе из запроса список транспортных средств без расписания, 
	//	по которым либо нет графика работы, либо в графике есть хотя бы секунда рабочего времени.
	// 
	
	// Расшифровка временнных таблиц:
	// 		втТранспортныеСредства 						- транспортные средства, ссылка на правила доступных зон и график работы.
	// 		втТранспортныеСредстваГеографическиеЗоны	- добавляется признак, доступности зоны погрузки для транспортных средств.
	//		втТранспортныеСредстваБезРасписания			- транспортные средства по которым нет расписания за заданные период,
	//                                                    учитывается перерыв между рейсами у ТС.
	//		втГрафикиРаботы								- всевозможные графики работы по ТС за заданный период.
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	               |	ВЫБОР
	               |		КОГДА тбТранспортныеСредства.ДоступныеЗоны = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиГеографическихЗон.ПустаяСсылка)
	               |			ТОГДА тбТипыТС.ДоступныеЗоны
	               |		ИНАЧЕ тбТранспортныеСредства.ДоступныеЗоны
	               |	КОНЕЦ КАК ПравилаДоступностиГеографическихЗон,
	               |	тбТранспортныеСредства.ГрафикРаботы,
	               |	тбТранспортныеСредства.ТипТС,
	               |	тбТранспортныеСредства.Партнер КАК Перевозчик
	               |ПОМЕСТИТЬ втТранспортныеСредства
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК тбТранспортныеСредства
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК тбТипыТС
	               |		ПО тбТранспортныеСредства.ТипТС = тбТипыТС.Ссылка
	               |ГДЕ
	               |	(тбТранспортныеСредства.Партнер = &Партнер
	               |			ИЛИ &Партнер = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбТранспортныеСредства.ТранспортноеСредство,
	               |	ВЫБОР
	               |		КОГДА СУММА(ВЫБОР
	               |					КОГДА тбТранспортныеСредства.ПравилаДоступностиГеографическихЗон = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиГеографическихЗон.ПустаяСсылка)
	               |						ТОГДА 1
	               |					КОГДА упДоступныеГеографическиеЗоны.ГеографическаяЗона = &ГеографическаяЗона
	               |						ТОГДА 1
	               |					ИНАЧЕ 0
	               |				КОНЕЦ) > 0
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ГеографическаяЗонаДоступнаТС,
	               |	тбТранспортныеСредства.ГрафикРаботы,
	               |	тбТранспортныеСредства.ТипТС,
	               |	тбТранспортныеСредства.Перевозчик
	               |ПОМЕСТИТЬ втТранспортныеСредстваГеографическиеЗоны
	               |ИЗ
	               |	втТранспортныеСредства КАК тбТранспортныеСредства
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДоступныеГеографическиеЗоны КАК упДоступныеГеографическиеЗоны
	               |		ПО тбТранспортныеСредства.ПравилаДоступностиГеографическихЗон = упДоступныеГеографическиеЗоны.ПравилаДоступности
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбТранспортныеСредства.ТранспортноеСредство,
	               |	тбТранспортныеСредства.ГрафикРаботы,
	               |	тбТранспортныеСредства.ТипТС,
	               |	тбТранспортныеСредства.Перевозчик
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА НЕ упРасписаниеРаботыТС.Транспорт ЕСТЬ NULL 
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ЕстьРасписаниеНаНужныйИнтервал,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ТранспортноеСредство,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ГеографическаяЗонаДоступнаТС,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ГрафикРаботы,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ТипТС,
	               |	втТранспортныеСредстваДоступностьГеоЗон.Перевозчик
	               |ПОМЕСТИТЬ втТранспортныеСредстваБезРасписания
	               |ИЗ
	               |	втТранспортныеСредстваГеографическиеЗоны КАК втТранспортныеСредстваДоступностьГеоЗон
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упЗанятостьТранспорта.СрезПоследних КАК упРасписаниеРаботыТС
	               |		ПО втТранспортныеСредстваДоступностьГеоЗон.ТранспортноеСредство = упРасписаниеРаботыТС.Транспорт
	               |			И (упРасписаниеРаботыТС.ДатаНачала < &ДатыОкончанияРабот)
	               |			И (ДОБАВИТЬКДАТЕ(упРасписаниеРаботыТС.ДатаОкончания, СЕКУНДА, 1 + втТранспортныеСредстваДоступностьГеоЗон.ТранспортноеСредство.ПерерывМеждуРейсами * 3600) > &ДатыНачалаРабот)
	               |			И (НЕ упРасписаниеРаботыТС.СостояниеТранспорта = ЗНАЧЕНИЕ(Справочник.упСостоянияТС.Свободно))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТранспортныеСредстваДоступностьГеоЗон.ТранспортноеСредство,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ГеографическаяЗонаДоступнаТС,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ГрафикРаботы,
	               |	втТранспортныеСредстваДоступностьГеоЗон.ТипТС,
	               |	втТранспортныеСредстваДоступностьГеоЗон.Перевозчик
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВЫБОР
	               |			КОГДА НЕ упРасписаниеРаботыТС.Транспорт ЕСТЬ NULL 
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) = 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	тбТранспортныеСредства.ГрафикРаботы
	               |ИЗ
	               |	втТранспортныеСредстваБезРасписания КАК тбТранспортныеСредства
	               |ГДЕ
	               |	НЕ тбТранспортныеСредства.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)";
				   
	Запрос.УстановитьПараметр("ГеографическаяЗона", сткРеквизитыРейса.ЗонаПогрузки);
	Запрос.УстановитьПараметр("ДатыНачалаРабот", 	сткРеквизитыРейса.ПлановоеНачалоВыполнения);
	Запрос.УстановитьПараметр("ДатыОкончанияРабот", сткРеквизитыРейса.ПлановоеОкончаниеВыполнения);
	Запрос.УстановитьПараметр("Партнер", 			сткРеквизитыРейса.Перевозчик);
	мсвКалендари = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГрафикРаботы");
	
	ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(текМенеджерВТ,мсвКалендари,сткРеквизитыРейса.ПлановоеНачалоВыполнения,сткРеквизитыРейса.ПлановоеОкончаниеВыполнения);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТРасписанияРаботы.ГрафикРаботы,
	               |	СУММА(РАЗНОСТЬДАТ(ВЫБОР
	               |				КОГДА ВТРасписанияРаботы.ВремяОкончания ЕСТЬ NULL 
	               |					ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
	               |				ИНАЧЕ ВТРасписанияРаботы.ВремяОкончания
	               |			КОНЕЦ, ВЫБОР
	               |				КОГДА ВТРасписанияРаботы.ВремяНачала ЕСТЬ NULL 
	               |					ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ, 1), ДЕНЬ)
	               |				ИНАЧЕ ВТРасписанияРаботы.ВремяНачала
	               |			КОНЕЦ, СЕКУНДА)) КАК ВремяРаботыСекунды
	               |ПОМЕСТИТЬ втГрафикиРаботы
	               |ИЗ
	               |	ВТРасписанияРаботы КАК ВТРасписанияРаботы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТРасписанияРаботы.ГрафикРаботы
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(РАЗНОСТЬДАТ(ВЫБОР
	               |				КОГДА ВТРасписанияРаботы.ВремяОкончания ЕСТЬ NULL 
	               |					ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
	               |				ИНАЧЕ ВТРасписанияРаботы.ВремяОкончания
	               |			КОНЕЦ, ВЫБОР
	               |				КОГДА ВТРасписанияРаботы.ВремяНачала ЕСТЬ NULL 
	               |					ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ, 1), ДЕНЬ)
	               |				ИНАЧЕ ВТРасписанияРаботы.ВремяНачала
	               |			КОНЕЦ, СЕКУНДА)) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТранспортныеСредстваБезРасписания.ТранспортноеСредство,
	               |	втТранспортныеСредстваБезРасписания.ГеографическаяЗонаДоступнаТС,
	               |	втТранспортныеСредстваБезРасписания.ТипТС,
	               |	втТранспортныеСредстваБезРасписания.Перевозчик
	               |ИЗ
	               |	втТранспортныеСредстваБезРасписания КАК втТранспортныеСредстваБезРасписания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	               |		ПО втТранспортныеСредстваБезРасписания.ГрафикРаботы = втГрафикиРаботы.ГрафикРаботы
	               |ГДЕ
	               |	(втТранспортныеСредстваБезРасписания.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	               |			ИЛИ ЕСТЬNULL(втГрафикиРаботы.ВремяРаботыСекунды, 0) > 0)";
	
	тбзРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзРезультат;

КонецФункции // ПолучитьСписокДоступныхТСПоРейсу()

// Функция - Получить количество заявок на ТС по рейсам.
//
// Параметры:
//  мсвРейсы - Массив - ДокументСсылка.упРейс, массив ссылок на рейс.
//
// Возвращаемое значение:
//  Число - Количество заявок
//
Функция ПолучитьКоличествоЗаявокНаТСпоРейсу(мсвРейсы) Экспорт

	текРезультат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КОЛИЧЕСТВО(упЗаявкаНаТС.Ссылка) КАК КоличествоЗаявок
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|ГДЕ
	|	упЗаявкаНаТС.Рейс В (&Рейсы)";
	Запрос.УстановитьПараметр("Рейсы", мсвРейсы);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.КоличествоЗаявок;	
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции // ПолучитьКоличествоЗаявокНаТСпоРейсу(Рейс)

// Функция - возвращает строку представления маршрута для рейса.
//
// Параметры:
//  тбзМаршурт - ТаблицаЗначений - Таблица данных.
//  ВидЗоныДляФормированияПредставленияРейса - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Строка - Результат выпонления.
//
Функция ПолучитьПредставлениеДляРейса(тбзМаршурт, ВидЗоныДляФормированияПредставленияРейса) Экспорт

	текРезультат = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбМаршрут.Адрес
	               |ПОМЕСТИТЬ втАдреса
	               |ИЗ
	               |	&тбзМаршрут КАК тбМаршрут
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
				   |	ЕСТЬNULL(втАдреса.Адрес" + ?(ЗначениеЗаполнено(ВидЗоныДляФормированияПредставленияРейса),"."+ОбщегоНазначения.ИмяЗначенияПеречисления(ВидЗоныДляФормированияПредставленияРейса),"") + ", втАдреса.Адрес" + ?(ЗначениеЗаполнено(ВидЗоныДляФормированияПредставленияРейса),"."+ОбщегоНазначения.ИмяЗначенияПеречисления(ВидЗоныДляФормированияПредставленияРейса),"") + ") КАК АдресПредставление
	               |ИЗ
	               |	втАдреса КАК втАдреса";
	Запрос.УстановитьПараметр("тбзМаршрут", тбзМаршурт.Скопировать(,"Адрес"));
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	АдресПредыдущееПредставление = "";
	Пока текВыборка.Следующий() Цикл
		АдресПредставление = текВыборка.АдресПредставление;
		Если АдресПредыдущееПредставление = АдресПредставление Тогда
			Продолжить;
		Иначе
			АдресПредыдущееПредставление = АдресПредставление;
		КонецЕсли;
		текРезультат = текРезультат + АдресПредставление + " -> ";		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(текРезультат) Тогда
		текРезультат = Лев(текРезультат, СтрДлина(текРезультат) - 4);	
	КонецЕсли;
			
	Возврат текРезультат;
	
КонецФункции // ПолучитьПредставлениеДляРейса()

// Функция - возвращает типовую структуру параметров планирования заполненных по переданному Виду Перевозки.
//
// Параметры:
//  ВидПеревозки - СправочникСсылка.упВидыПеревозок - Ссылка на элемент.
//
// Возвращаемое значение:
//  Структура - Результат выпонления.
//
Функция ПолучитьПараметрыПланирования(ВидПеревозки) Экспорт
	
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		КлючОбъекта = "Обработка.упРМПланированиеРейсов.ПараметрыПланирования";
		ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упВариантыНастроек.КлючВарианта
		|ИЗ
		|	Справочник.упВариантыНастроек КАК упВариантыНастроек
		|ГДЕ
		|   упВариантыНастроек.ВидПеревозки = &ВидПеревозки
		|	И упВариантыНастроек.Автор = &Автор
		|	И упВариантыНастроек.Объект = &Объект
		|	И НЕ упВариантыНастроек.ПометкаУдаления";
		Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
		Запрос.УстановитьПараметр("Объект", КлючОбъекта);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ПараметрыПланирования = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
				Если ПараметрыПланирования <> Неопределено Тогда
					ПараметрыПланирования = ПараметрыПланирования.Результат;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПоУмолчанию = упВариантыНастроек.ПараметрыПланированияРейсовПоУмолчанию();
	упСервисныеФункцииКлиентСервер.ДобавитьНедостающиеЭлементыСтруктуру(ПараметрыПоУмолчанию, ПараметрыПланирования);
	
	Возврат ПараметрыПланирования;
	
КонецФункции // ПолучитьПараметрыПланирования()

// Функция - возвращает типовую структуру параметров заполненных по переданному рейсу.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Структура - Результат выпонления.
//
Функция ПараметрыРейса(Рейс) Экспорт
	
	ПустаяГеоЗона = Справочники.упГеографическиеЗоны.ПустаяСсылка();
	сткРезультат = Новый Структура("Время, ЗонаПогрузки, ЗонаРазгрузки, Расстояние, Перевозчик, ТранспортноеСредство, ТипТС, Соглашение, ГруппаТарифов, Масса, Объем, КоличествоЗагрузочныхМетров, КоличествоМест, СуммаИнкассации",
	                                0, ПустаяГеоЗона, ПустаяГеоЗона, 0, Справочники.упПартнеры.ПустаяСсылка(), Справочники.упТранспортныеСредства.ПустаяСсылка(),
									   Справочники.упТипыТС.ПустаяСсылка(), Справочники.упСоглашенияСПеревозчиками.ПустаяСсылка(), Справочники.упГруппыТарифов.ПустаяСсылка(), 0, 0, 0, 0, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.Время,
	|	упПлановыеПараметрыРейса.ЗонаПогрузки,
	|	упПлановыеПараметрыРейса.ЗонаРазгрузки,
	|	упПлановыеПараметрыРейса.Расстояние,
	|	упПлановыеПараметрыРейса.Масса,
	|	упПлановыеПараметрыРейса.Объем,
	|	упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров,
	|	упПлановыеПараметрыРейса.КоличествоМест,
	|	упПлановыеПараметрыРейса.СуммаИнкассации
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
	|	упПеревозчикПоРейсуСрезПоследних.Соглашение,
	|	упПеревозчикПоРейсуСрезПоследних.ТипТС,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство,
	|	упПеревозчикПоРейсуСрезПоследних.Соглашение.ГруппаТарифов КАК ГруппаТарифов
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[0].Пустой() Тогда
		Выборка = Результат[0].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЕсли; 
	
	Если Не Результат[1].Пустой() Тогда
		Выборка = Результат[1].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЕсли; 
	
	Возврат сткРезультат;
	
КонецФункции

// Функция - возвращает типовую структуру параметров заполненных по перевозчику.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Структура - Результат выпонления.
//
Функция ПараметрыПеревозкиРейса(Рейс) Экспорт

	сткРезультат = Новый Структура("Перевозчик, ТранспортноеСредство, ТипТС, Соглашение, КонтрагентПеревозчика",
	                                Справочники.упПартнеры.ПустаяСсылка(), 
									Справочники.упТранспортныеСредства.ПустаяСсылка(),
									Справочники.упТипыТС.ПустаяСсылка(), 
									Справочники.упСоглашенияСПеревозчиками.ПустаяСсылка(),
									Справочники.упКонтрагенты.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
	|	упПеревозчикПоРейсуСрезПоследних.Соглашение,
	|	упПеревозчикПоРейсуСрезПоследних.ТипТС,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство,
	|	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЕсли; 
	
	Возврат сткРезультат;
	
КонецФункции

// Функция возвращает ссылку на гараж для ТС.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//  ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//  СправочникСсылка.упГаражи - Результат выпонления.
//
Функция ГаражПоТочкеОтправленияРейса(Рейс, ТранспортноеСредство) Экспорт
	
	текРезультат = Справочники.упГаражи.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТранспортныеСредстваГаражи.Гараж
	               |ИЗ
	               |	Справочник.упТранспортныеСредства.Гаражи КАК упТранспортныеСредстваГаражи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |		ПО упТранспортныеСредстваГаражи.Зона = упПлановыеПараметрыРейса.ЗонаПогрузки
	               |ГДЕ
	               |	упПлановыеПараметрыРейса.Рейс = &Рейс
	               |	И упТранспортныеСредстваГаражи.Ссылка = &ТранспортноеСредство";
	Запрос.УстановитьПараметр("Рейс", 					Рейс);
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Гараж;
	КонецЕсли;

	Возврат текРезультат;
		
КонецФункции

// Функция - возвращает признак необходимости оформления путевого листа.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Булево - Результат выпонления.
//
Функция ТребуетсяОформлениеПутевогоЛиста(Рейс) Экспорт
	
	текРезультат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик.ТребуетсяОформлениеПутевогоЛиста, ЛОЖЬ) КАК ТребуетсяОформлениеПутевогоЛистаПеревозчик,
	|	упРейс.ВидПеревозки.ТребуетсяОформлениеПутевогоЛиста КАК ТребуетсяОформлениеПутевогоЛистаВидПеревозки
	|ИЗ
	|	Документ.упРейс КАК упРейс,
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Ссылка) КАК упПеревозчикПоРейсуСрезПоследних
	|ГДЕ
	|	упРейс.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.ТребуетсяОформлениеПутевогоЛистаПеревозчик И текВыборка.ТребуетсяОформлениеПутевогоЛистаВидПеревозки;	
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

// Функция - возвращает признак необходимости оформления путевого листа.
//
// Параметры:
//  ВидПеревозки	 - СправочникСсылка.упВидыПеревозок - вид перевозки.
//  Перевозчик		 - СправочникСсылка.упПартнеры      - партнер.
// 
// Возвращаемое значение:
//  Булево - Результат выпонления.
//
Функция ТребуетсяОформлениеПутевогоЛистаДляПеревозчика(ВидПеревозки, Перевозчик) Экспорт
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст ="
	|ВЫБРАТЬ
	|	упВидыПеревозок.ТребуетсяОформлениеПутевогоЛиста КАК ТребуетсяОформлениеПутевогоЛистаВидПеревозки,
	|	упПартнеры.ТребуетсяОформлениеПутевогоЛиста КАК ТребуетсяОформлениеПутевогоЛистаПеревозчик
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок,
	|	Справочник.упПартнеры КАК упПартнеры
	|ГДЕ ИСТИНА
	|	И упВидыПеревозок.Ссылка = &ВидПеревозки
	|	И упПартнеры.Ссылка = &Партнер
	|";

	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("Партнер",      Перевозчик);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ТребуетсяОформлениеПутевогоЛистаВидПеревозки И Выборка.ТребуетсяОформлениеПутевогоЛистаПеревозчик;	
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Функция - возвращает типовую структуру параметров заполненных по сотруднику.
//
// Параметры:
//  Сотрудник - СправочникСсылка.упСотрудники - Ссылка на элемент.
//  ТекстОшибки - Строка - Описание.
//
// Возвращаемое значение:
//  Структура - Результат выпонления.
//
Функция РейсПоСотруднику(Сотрудник, ТекстОшибки = "") Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("НомерРейса", 			"");
	сткРезультат.Вставить("ПредставлениеМаршрута", 	"");
	сткРезультат.Вставить("Рейс",					Документы.упРейс.ПустаяСсылка());
	сткРезультат.Вставить("ПутевойЛист",			Документы.упПутевойЛист.ПустаяСсылка());
	сткРезультат.Вставить("ТранспортноеСредство",	Справочники.упТранспортныеСредства.ПустаяСсылка());
	сткРезультат.Вставить("СтатусРейса",			Неопределено);
	сткРезультат.Вставить("СтатусПутевогоЛиста",	Неопределено);
	сткРезультат.Вставить("ПробегНачало",			0);
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстОшибки = Нстр("ru = 'Не задан сотрудник'");	
		Возврат сткРезультат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРейс.Номер КАК НомерРейса,
	               |	упРейс.Ссылка КАК Рейс,
	               |	упРейс.Представление,
	               |	упСтатусыРейсовСрезПоследних.Статус КАК СтатусРейса,
	               |	ЕСТЬNULL(упСтатусыПутевыхЛистовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Новый)) КАК СтатусПутевогоЛиста,
	               |	упПлановыеПараметрыРейса.ПредставлениеМаршрута,
	               |	ЕСТЬNULL(упПутевойЛист.Ссылка, ЗНАЧЕНИЕ(Документ.упПутевойЛист.ПустаяСсылка)) КАК ПутевойЛист,
	               |	ВЫБОР
	               |		КОГДА упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	               |				ИЛИ НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус ЕСТЬ NULL
	               |					И НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Новый)
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК ПорядокВыполнения,
	               |	упПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	               |	ЕСТЬNULL(упПутевойЛист.ПробегНачало, 0) КАК ПробегНачало
	               |ИЗ
	               |	Документ.упРейс КАК упРейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |		ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу КАК упПеревозчикПоРейсу
	               |		ПО упРейс.Ссылка = упПеревозчикПоРейсу.Рейс
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |		ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	               |		ПО упРейс.Ссылка = упПутевойЛистРейсы.Рейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК упПутевойЛист
	               |		ПО (упПутевойЛистРейсы.Ссылка = упПутевойЛист.Ссылка)
	               |			И (упПутевойЛист.Проведен)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	               |		ПО (упПутевойЛист.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ)
	               |			И (НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	               |ГДЕ
	               |	упСтатусыРейсовСрезПоследних.Статус В(&СтатусыРейса)
	               |	И (упПеревозчикПоРейсу.Водитель1 = &Сотрудник
	               |			ИЛИ упПеревозчикПоРейсу.Водитель2 = &Сотрудник
	               |			ИЛИ упПеревозчикПоРейсу.Экспедитор = &Сотрудник)
	               |	И упРейс.ВидПеревозки.СпособПрохожденияТочекМаршрута = ЗНАЧЕНИЕ(Перечисление.упСпособыПрохожденияТочекМаршрута.ОтдельнымДокументом)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	упРейс.Дата,
	               |	ПорядокВыполнения";
	мсвСтатусыРейса = Новый Массив;
	
	текСтатусНачалаРаботыСРейсомНаРМВодителя	= упСервисныеФункции.ПолучитьЗначениеКонстанты("упСтатусРейсаДляНачалаРаботыНаРМВодителя");
	мсвСтатусыРейса.Добавить(текСтатусНачалаРаботыСРейсомНаРМВодителя);
	мсвСтатусыРейса.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	Запрос.УстановитьПараметр("СтатусыРейса", мсвСтатусыРейса);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, текВыборка);
	Иначе	
		ТекстОшибки = Нстр("ru = 'Рейс по сотруднику %1 подобрать не получилось'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Сотрудник);
	КонецЕсли;
	
	Возврат сткРезультат;
		
КонецФункции

// Процедура - контролирует правильность заполнения временных интервалов шапки рейса.
//
// Параметры:
//  НачалоВыполнения - Дата - Период.
//  ОкончаниеВыполнения - Дата - Период.
//  ПлановоеНачалоВыполнения - Дата - Период.
//  ПлановоеОкончаниеВыполнения - Дата - Период.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура ПроконтролироватьВременныеИнтервалыПогрузкиРазгрузки(НачалоВыполнения, ОкончаниеВыполнения, ПлановоеНачалоВыполнения , ПлановоеОкончаниеВыполнения, Отказ) Экспорт

	Если ЗначениеЗаполнено(ПлановоеНачалоВыполнения) И ЗначениеЗаполнено(ПлановоеОкончаниеВыполнения) Тогда
		Если ПлановоеОкончаниеВыполнения < ПлановоеНачалоВыполнения Тогда
			ТекстСообщения = Нстр("ru = 'Планируемое время разгрузки раньше плана погрузки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							"Объект.ПлановоеНачалоВыполнения",
							,
							Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоВыполнения) И ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
		Если ОкончаниеВыполнения < НачалоВыполнения Тогда
			ТекстСообщения = Нстр("ru = 'Время разгрузка раньше погрузки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							"Объект.НачалоВыполнения",
							,
							Отказ);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Функция - возвращает признак наличия путевого листа по рейсу.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Булево - Результат выпонления.
//
Функция ЕстьПутевойЛист(Рейс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПутевойЛистРейсы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	               |		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛистРейсы.Ссылка)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК упПутевойЛист
	               |		ПО упПутевойЛистРейсы.Ссылка = упПутевойЛист.Ссылка
	               |ГДЕ
	               |	упПутевойЛистРейсы.Рейс = &Рейс
	               |	И (упСтатусыПутевыхЛистовСрезПоследних.Статус ЕСТЬ NULL
	               |			ИЛИ НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	               |	И НЕ упПутевойЛист.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текРезультат = Запрос.Выполнить().Пустой();
	
	Возврат НЕ текРезультат;
			
КонецФункции

// Функция - возвращает параметры путевого путевого листа по рейсу.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Результат - Структура - параметры путевого листа по рейсу.
//	* ПутевойЛист          - ДокументСсылка.упПутевойЛист            - путевой лист.
//	* ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - транспортное средство путевого листа.
//
Функция ПараметрыПутевогоЛистаПоРейсу(Рейс) Экспорт
	
	Результат = Новый Структура("ПутевойЛист, ТранспортноеСредство", Неопределено, Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПутевойЛист.Ссылка КАК ПутевойЛист,
	               |	упПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство
	               |ИЗ
	               |	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	               |		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛистРейсы.Ссылка)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК упПутевойЛист
	               |		ПО упПутевойЛистРейсы.Ссылка = упПутевойЛист.Ссылка
	               |ГДЕ
	               |	упПутевойЛистРейсы.Рейс = &Рейс
	               |	И (упСтатусыПутевыхЛистовСрезПоследних.Статус ЕСТЬ NULL
	               |			ИЛИ НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	               |	И НЕ упПутевойЛист.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
			
КонецФункции

// Функция - Возвращает документы препятствующие отмене рейса.
//
// Параметры:
//  Рейсы	 - Массив	 - массив рейсов.
// 
// Возвращаемое значение:
//  Структура - описывает документы препятствующие отмене.
//  * ЕстьДокументыОтменаВручную 	- Булево 	- Истина, если есть связанные документы, 
//                                   которые невозможно отменить автоматом, такие как "корректировка", 
//                                   "путевой лист", "подтверждение заявки на тс".
//  * ЕстьДокументыОтменаАвтоматом - Булево 	- Истина, если есть связанные документы, 
//                                   которые невозможно отменить автоматом, такие как "заявка на ТС", 
//                                   "предложение перевозчика".
//  * Рейсы	- Массив 	- массив структур данных по рейсам:
//  ** Элемент массива - Структура - структура данных по рейсу.
//  *** Рейс		- ДокументСсылка.Рейс 	 - рейс, по данные по которому содержатся в структуре.
//  *** ДокументыОтменаВручную				- Массив - массив документов, отменяемых только вручную.
//  *** ДокументыОтменаАвтоматомЗаявкиНаТС	- Массив - массив "заявок на ТС".
//  *** ДокументыОтменаАвтоматомПредложения	- Массив - массив "предложений перевозчика".
//
Функция ДокументыПрепятствующиеОтменеРейса(Рейсы) Экспорт
	
	мсвДокументыОтменаВручную = Новый Массив;
	мсвДокументыОтменаАвтоматомЗаявкиНаТС = Новый Массив;
	мсвДокументыОтменаАвтоматомПредложения = Новый Массив;
	
	мсвРейсы	 = Новый Массив;
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ЕстьДокументыОтменаВручную", Ложь);// Корректировка, путевой лист, подтверждение заявки на тс.
	сткРезультат.Вставить("ЕстьДокументыОтменаАвтоматом", Ложь);// Заявка на ТС, предложение перевозчика.
	сткРезультат.Вставить("Рейсы", 							мсвРейсы);

	сткДанныеПоРейсу	= Новый Структура;
	сткДанныеПоРейсу.Вставить("Рейс", Неопределено); 
	сткДанныеПоРейсу.Вставить("ДокументыОтменаВручную", Неопределено); 
	сткДанныеПоРейсу.Вставить("ДокументыОтменаАвтоматомЗаявкиНаТС", Неопределено); 
	сткДанныеПоРейсу.Вставить("ДокументыОтменаАвтоматомПредложения",	Неопределено); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упКорректировкаРейса.Ссылка КАК Ссылка,
	|	упКорректировкаРейса.Рейс КАК Рейс
	|ИЗ
	|	Документ.упКорректировкаРейса КАК упКорректировкаРейса
	|ГДЕ ИСТИНА
	|	И упКорректировкаРейса.Рейс В  (&Рейсы)
	|	И НЕ (упКорректировкаРейса.ПометкаУдаления)
	|	И упКорректировкаРейса.Проведен
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упПодтверждениеЗаявкиНаТС.Ссылка КАК Ссылка,
	|	упПодтверждениеЗаявкиНаТС.Рейс КАК Рейс
	|ИЗ
	|	Документ.упПодтверждениеЗаявкиНаТС КАК упПодтверждениеЗаявкиНаТС
	|ГДЕ ИСТИНА
	|	И упПодтверждениеЗаявкиНаТС.Рейс В  (&Рейсы)
	|	И упПодтверждениеЗаявкиНаТС.Проведен
	|	И НЕ (упПодтверждениеЗаявкиНаТС.ПометкаУдаления)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упПутевойЛист.Ссылка КАК Ссылка,
	|	упПутевойЛист.Рейс КАК Рейс
	|ИЗ
	|	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист КАК упПутевойЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО упПутевойЛист.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ
	|	ПО упПутевойЛистРейсы.Ссылка = упПутевойЛист.Ссылка
	|ГДЕ ИСТИНА
	|	И упПутевойЛистРейсы.Рейс В  (&Рейсы)
	|	И упПутевойЛист.Проведен
	|	И НЕ (упПутевойЛист.ПометкаУдаления)
	|	И НЕ (упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упДоверенность.Ссылка КАК Ссылка,
	|	упДоверенность.Рейс КАК Рейс
	|ИЗ
	|	Документ.упДоверенность КАК упДоверенность
	|ГДЕ ИСТИНА
	|	И упДоверенность.Рейс В  (&Рейсы)
	|	И упДоверенность.Проведен
	|	И НЕ (упДоверенность.ПометкаУдаления)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упРегистрацияЗадолженностиПоДокументамРейса.Ссылка КАК Ссылка,
	|	упРегистрацияЗадолженностиПоДокументамРейса.Рейс КАК Рейс
	|ИЗ
	|	Документ.упРегистрацияЗадолженностиПоДокументамРейса КАК упРегистрацияЗадолженностиПоДокументамРейса
	|ГДЕ ИСТИНА
	|	И упРегистрацияЗадолженностиПоДокументамРейса.Рейс В  (&Рейсы)
	|	И упРегистрацияЗадолженностиПоДокументамРейса.Проведен
	|	И НЕ (упРегистрацияЗадолженностиПоДокументамРейса.ПометкаУдаления)
	|ИТОГИ
	|ПО
	|	Рейс
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаТС.Ссылка КАК ЗаявкаНаТС,
	|	упПредложениеПеревозчика.Ссылка КАК ПредложениеПеревозчика,
	|	упЗаявкаНаТС.Рейс КАК Рейс
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|	ПО ИСТИНА
	|		И упПредложениеПеревозчика.ЗаявкаНаТС = упЗаявкаНаТС.Ссылка
	|		И НЕ (упПредложениеПеревозчика.ПометкаУдаления)
	|		И упПредложениеПеревозчика.Проведен
	|ГДЕ ИСТИНА
	|	И упЗаявкаНаТС.Рейс В  (&Рейсы)
	|	И НЕ (упЗаявкаНаТС.ПометкаУдаления)
	|	И упЗаявкаНаТС.Проведен
	|ИТОГИ
	|ПО
	|	Рейс
	|";

	Запрос.УстановитьПараметр("Рейсы", Рейсы);
	мсвРезультатЗапрос = Запрос.ВыполнитьПакет();
	РезультатДокументыОтменаВручную = мсвРезультатЗапрос[0];
	РезультатДокументыОтменаАвтоматически = мсвРезультатЗапрос[1];
	
	Если РезультатДокументыОтменаВручную.Пустой() Тогда
		
		Если НЕ РезультатДокументыОтменаАвтоматически.Пустой() Тогда
			
			сткРезультат.Вставить("ЕстьДокументыОтменаАвтоматом",	Истина);
									
			ВыборкаРейс = РезультатДокументыОтменаАвтоматически.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаРейс.Следующий() Цикл
				
				РейсТекущий = ВыборкаРейс.Рейс;
				мсвДокументыОтменаАвтоматомЗаявкиНаТС = Новый Массив;
				мсвДокументыОтменаАвтоматомПредложения = Новый Массив;
				
				сткДанныеПоРейсуТекущему = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоРейсу);
				сткДанныеПоРейсуТекущему.Рейс = РейсТекущий;
				сткДанныеПоРейсуТекущему.ДокументыОтменаАвтоматомЗаявкиНаТС = мсвДокументыОтменаАвтоматомЗаявкиНаТС;
				сткДанныеПоРейсуТекущему.ДокументыОтменаАвтоматомПредложения = мсвДокументыОтменаАвтоматомПредложения;
				
				ВыборкаДокументы = ВыборкаРейс.Выбрать();
				Пока ВыборкаДокументы.Следующий() Цикл
					Если мсвДокументыОтменаАвтоматомЗаявкиНаТС.Найти(ВыборкаДокументы.ЗаявкаНаТС) = Неопределено Тогда
						мсвДокументыОтменаАвтоматомЗаявкиНаТС.Добавить(ВыборкаДокументы.ЗаявкаНаТС);
					КонецЕсли;	
					
					Если ЗначениеЗаполнено(ВыборкаДокументы.ПредложениеПеревозчика) 
						И мсвДокументыОтменаАвтоматомПредложения.Найти(ВыборкаДокументы.ПредложениеПеревозчика) = Неопределено Тогда
						мсвДокументыОтменаАвтоматомПредложения.Добавить(ВыборкаДокументы.ПредложениеПеревозчика);
					КонецЕсли;	
				КонецЦикла;
				
				мсвРейсы.Добавить(сткДанныеПоРейсуТекущему);
				
				Если Ложь
					Или мсвДокументыОтменаАвтоматомЗаявкиНаТС.Количество() > 0 
					Или мсвДокументыОтменаАвтоматомПредложения.Количество() > 0 
				Тогда
				
					СтрокаДокументов1 = СтрСоединить(мсвДокументыОтменаАвтоматомЗаявкиНаТС, ",");
					СтрокаДокументов2 = СтрСоединить(мсвДокументыОтменаАвтоматомПредложения, ",");
					ТекстСообщения = Нстр("ru = 'Документы препятствующие отмене %1:%2%3'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, РейсТекущий, СтрокаДокументов1, СтрокаДокументов2);
					ЗаписьЖурналаРегистрации("Документы препятствующие отмене рейса", УровеньЖурналаРегистрации.Предупреждение,,,ТекстСообщения);
				
				КонецЕсли;
				
				
			КонецЦикла;			
	
		КонецЕсли;
				
	Иначе
		
		ВыборкаРейс = РезультатДокументыОтменаВручную.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРейс.Следующий() Цикл
			
			РейсТекущий = ВыборкаРейс.Рейс;
			
			мсвДокументыОтменаВручную = Новый Массив;
			сткДанныеПоРейсуТекущему = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоРейсу);
			сткДанныеПоРейсуТекущему.Рейс = РейсТекущий;
			сткДанныеПоРейсуТекущему.ДокументыОтменаВручную = мсвДокументыОтменаВручную;
			
			ВыборкаДокументы = ВыборкаРейс.Выбрать();
			Пока ВыборкаДокументы.Следующий() Цикл
				мсвДокументыОтменаВручную.Добавить(ВыборкаДокументы.Ссылка);		
			КонецЦикла;
			
			мсвРейсы.Добавить(сткДанныеПоРейсуТекущему);
			
			СтрокаДокументов = СтрСоединить(мсвДокументыОтменаВручную, ",");
			ТекстСообщения = Нстр("ru = 'Документы препятствующие отмене %1:%2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, РейсТекущий, СтрокаДокументов);
			ЗаписьЖурналаРегистрации("Документы препятствующие отмене рейса", УровеньЖурналаРегистрации.Предупреждение,,,ТекстСообщения);
				
		КонецЦикла;
			
		сткРезультат.Вставить("ЕстьДокументыОтменаВручную",	Истина);
						
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Функция - возвращает статус путевого листа по рейсу.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Статус - Результат выпонления.
//
Функция СтатусПутевогоЛистаПоРейсу(Рейс) Экспорт
	
	Статус = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПутевойЛист.Ссылка,
				   |	упСтатусыПутевыхЛистовСрезПоследних.Статус
	               |ИЗ
	               |	Документ.упПутевойЛист КАК упПутевойЛист
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	               |		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка)
	               |ГДЕ
	               |	упПутевойЛист.Рейс = &Рейс
	               |	И НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)
	               |	И НЕ упПутевойЛист.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Статус = Выборка.Статус;
	КонецЕсли;
		
	Возврат Статус;
			
КонецФункции

// Процедура - изменяет статус документа "упРейс".
//
// Параметры:
//  Рейс					 - ДокументСсылка.упРейс	 - Ссылка на элемент.
//  Статус				 - ПеречислениеСсылка	 - Ссылка на элемент.
//  ОписаниеОшибки			 - Строка				 - Описание ошибки.
//  Отказ					 - Булево				 - Зафиксировать изменения.
//  ДополнительныеСвойства	 - Структура			 - доп. свойства.
//
Процедура УстановитьСтатусРейса(Рейс, Статус, ОписаниеОшибки, Отказ, ДополнительныеСвойства = Неопределено) Экспорт
	
	ПорядокИсполненияЗаявки = Неопределено;
	
	Если ДополнительныеСвойства <> Неопределено Тогда
		ПорядокИсполненияЗаявки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПорядокИсполненияЗаявки");
	КонецЕсли;
	
	Если ПорядокИсполненияЗаявки <> Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом Тогда
		Отказ = НЕ упРаботаСоСтатусами.ЕстьВозможностьУстановитьСтатусРейса(Рейс, Статус, ОписаниеОшибки);	
	КонецЕсли;
		
	Если НЕ Отказ Тогда
		РейсОбъект = Рейс.ПолучитьОбъект();
		РейсОбъект.ДополнительныеСвойства.Вставить("НеПроводитьПроверкуНаВозможныеСтатусы", Истина);
		РейсОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);
		Попытка
			РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);	
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
		КонецПопытки;	
	КонецЕсли;
		
КонецПроцедуры

// Функция - проверяет наличие пожеланий перевозчика.
//
// Параметры:
//  ЗаявкаНаТС - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Булево - Результат выпонления.
//
Функция ПредложениеПоЗаявкеНаТС(ЗаявкаНаТС) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упПредложениеПеревозчика.Ссылка
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.ЗаявкаНаТС = &ЗаявкаНаТС
	|	И упПредложениеПеревозчика.Проведен";
	Запрос.УстановитьПараметр("ЗаявкаНаТС", ЗаявкаНаТС);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Следующий();
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Функция - Возвращает таблицу заданий по рейсу.
// 
// Параметры:
//  Рейсы - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Массив - Результат выпонления.
//
Функция ТаблицаЗаданийПоРейсу(Рейсы) Экспорт
	
	тбзРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тбРаботы.Рейс,
	|	тбРаботы.Отменена,
	|	тбРаботы.Задание,
	|	тбРаботы.Операция
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс В (&Рейсы)) КАК тбРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРаботыПоРейсу.Рейс,
	|	упРаботыПоРейсу.Регистратор
	|ПОМЕСТИТЬ втРегистраторы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Рейс В(&Рейсы)
	|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Рейс,
	|	втРаботы.Задание,
	|	СУММА(ВЫБОР
	|			КОГДА втРаботы.Отменена
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоОтменена,
	|	СУММА(1) КАК КоличествоОпераций
	|ПОМЕСТИТЬ втОтмененныеЗадания
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ
	|	(втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Рейс,
	|	втРаботы.Задание
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА втРаботы.Отменена
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) = СУММА(1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаданиям
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|		ПО упРейсЗадания.Ссылка = втОтмененныеЗадания.Рейс
	|			И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
	|ГДЕ
	|	упРейсЗадания.Ссылка В(&Рейсы)
	|	И втОтмененныеЗадания.Задание ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРегистраторы.Рейс,
	|	упКорректировкаРейсаЗадания.Задание
	|ИЗ
	|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	|		ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
	|			И упКорректировкаРейсаЗадания.Ссылка.Рейс = втРегистраторы.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|		ПО упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
	|			И упКорректировкаРейсаЗадания.Ссылка.Рейс = втОтмененныеЗадания.Рейс
	|ГДЕ
	|	втОтмененныеЗадания.Задание ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втГрузовыеМестаПоЗаданиям.Задание
	|ИЗ
	|	втГрузовыеМестаПоЗаданиям КАК втГрузовыеМестаПоЗаданиям";
	Запрос.УстановитьПараметр("Рейсы", Рейсы);
	тбзРезультат = Запрос.Выполнить().Выгрузить();		
	Возврат тбзРезультат;
КонецФункции

// Функция - Возвращает список заданий по рейсу.
// 
// Параметры:
//  Рейсы - ДокументСсылка.упРейс - Ссылка на элемент.
//
// Возвращаемое значение:
//  Массив - Результат выпонления.
//
Функция СписокЗаданийПоРейсу(Рейсы) Экспорт
	
	мсвЗаданий = ТаблицаЗаданийПоРейсу(Рейсы).ВыгрузитьКолонку("Задание");
	Возврат мсвЗаданий;

КонецФункции

// Функция - Возвращает таблицу плановых расходов по заданиям рейса.
//
// Параметры:
//  Рейсы - ДокументСсылка.упРейс - Ссылка на элемент.
//  ВедетсяВалютныйУчет - Булево - Учет.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выпонления.
//
Функция ПлановыеРасходыПоЗаданиямРейсов(Рейсы, ВедетсяВалютныйУчет = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Если ВедетсяВалютныйУчет Тогда
		Запрос.Текст = "ВЫБРАТЬ 
					   |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		               |	упРасходыОбороты.ЗаданиеНаПеревозку КАК Задание,
		               |	упРасходыОбороты.Валюта,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС КАК СтавкаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланВалютаСНДСОборот - упРасходыОбороты.СуммаПланВалютаБезНДСОборот) КАК СуммаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланВалютаСНДСОборот) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.упРасходы.Обороты(, , Регистратор, Рейс В (&Рейсы)) КАК упРасходыОбороты
		               |ГДЕ
		               |	упРасходыОбороты.СуммаПланСНДСОборот > 0
		               |	И НЕ упРасходыОбороты.Регистратор ССЫЛКА Документ.упПутевойЛист
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения,
		               |	упРасходыОбороты.ЗаданиеНаПеревозку,
		               |	упРасходыОбороты.Валюта,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		               |	упРасходыОбороты.ЗаданиеНаПеревозку КАК Задание,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС КАК СтавкаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланСНДСОборот - упРасходыОбороты.СуммаПланБезНДСОборот) КАК СуммаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланСНДСОборот) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.упРасходы.Обороты(, , Регистратор, Рейс В (&Рейсы)) КАК упРасходыОбороты
		               |ГДЕ
		               |	упРасходыОбороты.СуммаПланСНДСОборот > 0
		               |	И НЕ упРасходыОбороты.Регистратор ССЫЛКА Документ.упПутевойЛист
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения,
		               |	упРасходыОбороты.ЗаданиеНаПеревозку,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС";
		
	КонецЕсли;

	
	Запрос.УстановитьПараметр("Рейсы", Рейсы);
	тбзПлановыеРасходы = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзПлановыеРасходы;

КонецФункции

// Функция - Возвращает таблицу плановых расходов по рейсам.
//
// Параметры:
//  Рейсы				 - ДокументСсылка.упРейс	 - Ссылка на элемент.
//  ВедетсяВалютныйУчет	 - Булево				 - Учет.
//  ГруппироватьПоРейсам	 - Булево				 - признак группировки по рейсам.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выпонления.
//
Функция ПлановыеРасходыПоРейсам(Рейсы, ВедетсяВалютныйУчет = Ложь, ГруппироватьПоРейсам = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Если ВедетсяВалютныйУчет Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		               |	упРасходыОбороты.Валюта,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС КАК СтавкаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланВалютаСНДСОборот - упРасходыОбороты.СуммаПланВалютаБезНДСОборот) КАК СуммаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланВалютаСНДСОборот) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.упРасходы.Обороты(, , Регистратор, Рейс В (&Рейсы)) КАК упРасходыОбороты
		               |ГДЕ
		               |	упРасходыОбороты.СуммаПланСНДСОборот > 0
		               |	И НЕ упРасходыОбороты.Регистратор ССЫЛКА Документ.упПутевойЛист
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения,
		               |	упРасходыОбороты.Валюта,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС";
	Иначе	
		Запрос.Текст = "ВЫБРАТЬ
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС КАК СтавкаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланСНДСОборот - упРасходыОбороты.СуммаПланБезНДСОборот) КАК СуммаНДС,
		               |	СУММА(упРасходыОбороты.СуммаПланСНДСОборот) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.упРасходы.Обороты(, , Регистратор, Рейс В (&Рейсы)) КАК упРасходыОбороты
		               |ГДЕ
		               |	упРасходыОбороты.СуммаПланСНДСОборот > 0
		               |	И НЕ упРасходыОбороты.Регистратор ССЫЛКА Документ.упПутевойЛист
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упРасходыОбороты.СтатьяРасходов,
		               |	упРасходыОбороты.СтатьяРасходов.ПравилаРаспределения,
		               |	упРасходыОбороты.СтатьяРасходов.СтавкаНДС";
				   КонецЕсли;
				   
	Запрос.УстановитьПараметр("Рейсы", Рейсы);
	
	Если ГруппироватьПоРейсам Тогда
		СхемаЗапроса = Новый СхемаЗапроса();
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
		
		ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Добавить("упРасходыОбороты.Рейс");
		ПакетЗапросов[0].Операторы[0].Группировка.Добавить("упРасходыОбороты.Рейс");
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	тбзПлановыеРасходы = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзПлановыеРасходы;

КонецФункции

// Процедура - проверят полный комплект документво по рейсу.
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура ПроверитьНаличиеНеобходимыхДокументовПоРейсу(Ссылка, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство,
	               |	упПеревозчикПоРейсуСрезПоследних.Водитель1 КАК Водитель,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ДокументВыдается КАК ДокументВыдается,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ИнтервалПредупреждения КАК ИнтервалПредупреждения,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ПредупреждатьОбОкончанииСрокаДействия КАК ПредупреждатьОбОкончанииСрокаДействия
	               |ПОМЕСТИТЬ втПеревозчикПоРейсу
	               |ИЗ
	               |	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыДокументовТС.СписокДокументов КАК упПакетыДокументовТССписокДокументов
	               |		ПО упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.ПакетНеобходимыхДокументов = упПакетыДокументовТССписокДокументов.Ссылка
	               |ГДЕ
	               |	НЕ упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	               |	И НЕ упПеревозчикПоРейсуСрезПоследних.Водитель1 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втПеревозчикПоРейсу.ТипДокумента КАК ТипДокумента,
	               |	тбДокументы.ДействуетПо КАК ДействуетПо,
	               |	тбДокументы.Регистратор КАК Регистратор,
	               |	тбДокументы.Регистратор.ДействуетС КАК ДействуетС
	               |ПОМЕСТИТЬ втДействующиеДокументы
	               |ИЗ
	               |	втПеревозчикПоРейсу КАК втПеревозчикПоРейсу
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(, ) КАК тбДокументы
	               |		ПО втПеревозчикПоРейсу.ТипДокумента = тбДокументы.ТипДокумента
	               |			И (втПеревозчикПоРейсу.ТранспортноеСредство = тбДокументы.ТранспортноеСредство
	               |				ИЛИ втПеревозчикПоРейсу.Водитель = тбДокументы.Водитель)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втПеревозчикПоРейсу.ТипДокумента,
	               |	втДействующиеДокументы.Регистратор,
	               |	втПеревозчикПоРейсу.ДокументВыдается,
	               |	втПеревозчикПоРейсу.ИнтервалПредупреждения,
	               |	втПеревозчикПоРейсу.ПредупреждатьОбОкончанииСрокаДействия,
	               |	втДействующиеДокументы.ДействуетПо,
	               |	втПеревозчикПоРейсу.ТранспортноеСредство,
	               |	втПеревозчикПоРейсу.Водитель,
	               |	втДействующиеДокументы.ДействуетС
	               |ИЗ
	               |	втПеревозчикПоРейсу КАК втПеревозчикПоРейсу
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втДействующиеДокументы КАК втДействующиеДокументы
	               |		ПО втПеревозчикПоРейсу.ТипДокумента = втДействующиеДокументы.ТипДокумента";
	Запрос.УстановитьПараметр("Рейс", Ссылка);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	текДата = НачалоДня(ТекущаяДатаСеанса());
	
	Пока текВыборка.Следующий() Цикл
		
		ДействующийДокумент = ?(текВыборка.Регистратор = null, Неопределено, текВыборка.Регистратор);
		
		ТекстСообщения = ПроверитьДокумент(ДействующийДокумент, 
											текВыборка.ТипДокумента, 
											текВыборка.ДокументВыдается, 
											текВыборка.ДействуетС, 
											текВыборка.ДействуетПо,
											текВыборка.ПредупреждатьОбОкончанииСрокаДействия,
											текВыборка.ИнтервалПредупреждения, 
											текВыборка.ТранспортноеСредство, 
											текВыборка.Водитель, 
											Отказ);
		
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДействующийДокумент);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

// Процедура - проверят полный комплект документво по водителю и ТС.
//
// Параметры:
//  ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  Водитель - СправочникСсылка - Ссылка на элемент.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	               |	&Водитель КАК Водитель,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ДокументВыдается КАК ДокументВыдается,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ИнтервалПредупреждения КАК ИнтервалПредупреждения,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ПредупреждатьОбОкончанииСрокаДействия КАК ПредупреждатьОбОкончанииСрокаДействия
	               |ПОМЕСТИТЬ втТипыДокументов
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыДокументовТС.СписокДокументов КАК упПакетыДокументовТССписокДокументов
	               |		ПО упТранспортныеСредства.ПакетНеобходимыхДокументов = упПакетыДокументовТССписокДокументов.Ссылка
	               |ГДЕ
	               |	упТранспортныеСредства.Ссылка = &ТранспортноеСредство
				   |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТипыДокументов.ТипДокумента КАК ТипДокумента,
	               |	тбДокументы.ДействуетПо КАК ДействуетПо,
	               |	тбДокументы.Регистратор КАК Регистратор,
	               |	тбДокументы.Регистратор.ДействуетС КАК ДействуетС
	               |ПОМЕСТИТЬ втДействующиеДокументы
	               |ИЗ
	               |	втТипыДокументов КАК втТипыДокументов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(, ) КАК тбДокументы
	               |		ПО втТипыДокументов.ТипДокумента = тбДокументы.ТипДокумента
	               |			И (втТипыДокументов.ТранспортноеСредство = тбДокументы.ТранспортноеСредство
	               |				ИЛИ втТипыДокументов.Водитель = тбДокументы.Водитель)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТипыДокументов.ТипДокумента,
	               |	втДействующиеДокументы.Регистратор,
	               |	втТипыДокументов.ДокументВыдается,
	               |	втТипыДокументов.ИнтервалПредупреждения,
	               |	втТипыДокументов.ПредупреждатьОбОкончанииСрокаДействия,
	               |	втДействующиеДокументы.ДействуетПо,
	               |	втТипыДокументов.ТранспортноеСредство,
	               |	втТипыДокументов.Водитель,
	               |	втДействующиеДокументы.ДействуетС
	               |ИЗ
	               |	втТипыДокументов КАК втТипыДокументов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втДействующиеДокументы КАК втДействующиеДокументы
	               |		ПО втТипыДокументов.ТипДокумента = втДействующиеДокументы.ТипДокумента";
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	Запрос.УстановитьПараметр("Водитель", 				Водитель);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	текДата = НачалоДня(ТекущаяДатаСеанса());
	
	Пока текВыборка.Следующий() Цикл
		
		ДействующийДокумент = ?(текВыборка.Регистратор = null, Неопределено, текВыборка.Регистратор);
		
		ТекстСообщения = ПроверитьДокумент(ДействующийДокумент, 
											текВыборка.ТипДокумента, 
											текВыборка.ДокументВыдается, 
											текВыборка.ДействуетС, 
											текВыборка.ДействуетПо,
											текВыборка.ПредупреждатьОбОкончанииСрокаДействия,
											текВыборка.ИнтервалПредупреждения, 
											текВыборка.ТранспортноеСредство, 
											текВыборка.Водитель, 
											Отказ);
		
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДействующийДокумент);
		КонецЕсли;
	
	КонецЦикла;

	
КонецПроцедуры

// Процедура - проверят полный комплект документво по ТС.
//
// Параметры:
//  ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура ПроверитьНаличиеНеобходимыхДокуметовПоТС(ТранспортноеСредство, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ДокументВыдается КАК ДокументВыдается,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ИнтервалПредупреждения КАК ИнтервалПредупреждения,
	               |	упПакетыДокументовТССписокДокументов.ТипДокумента.ПредупреждатьОбОкончанииСрокаДействия КАК ПредупреждатьОбОкончанииСрокаДействия
	               |ПОМЕСТИТЬ втТипыДокументов
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыДокументовТС.СписокДокументов КАК упПакетыДокументовТССписокДокументов
	               |		ПО упТранспортныеСредства.ПакетНеобходимыхДокументов = упПакетыДокументовТССписокДокументов.Ссылка
	               |ГДЕ
	               |	упТранспортныеСредства.Ссылка = &ТранспортноеСредство
	               |	И упПакетыДокументовТССписокДокументов.ТипДокумента.ДокументВыдается = ЗНАЧЕНИЕ(Перечисление.упОбъектДокументовТСВодителей.ТранспортноеСредство)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТипыДокументов.ТипДокумента КАК ТипДокумента,
	               |	тбДокументы.ДействуетПо КАК ДействуетПо,
	               |	тбДокументы.Регистратор КАК Регистратор,
	               |	тбДокументы.Регистратор.ДействуетС КАК ДействуетС
	               |ПОМЕСТИТЬ втДействующиеДокументы
	               |ИЗ
	               |	втТипыДокументов КАК втТипыДокументов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(, ) КАК тбДокументы
	               |		ПО втТипыДокументов.ТипДокумента = тбДокументы.ТипДокумента
	               |			И втТипыДокументов.ТранспортноеСредство = тбДокументы.ТранспортноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТипыДокументов.ТипДокумента,
	               |	втДействующиеДокументы.Регистратор,
	               |	втТипыДокументов.ДокументВыдается,
	               |	втТипыДокументов.ИнтервалПредупреждения,
	               |	втТипыДокументов.ПредупреждатьОбОкончанииСрокаДействия,
	               |	втДействующиеДокументы.ДействуетПо,
	               |	втТипыДокументов.ТранспортноеСредство,
	               |	втДействующиеДокументы.ДействуетС
	               |ИЗ
	               |	втТипыДокументов КАК втТипыДокументов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втДействующиеДокументы КАК втДействующиеДокументы
	               |		ПО втТипыДокументов.ТипДокумента = втДействующиеДокументы.ТипДокумента";
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	текДата = НачалоДня(ТекущаяДатаСеанса());
	
	Пока текВыборка.Следующий() Цикл
		
		ДействующийДокумент = ?(текВыборка.Регистратор = null, Неопределено, текВыборка.Регистратор);
		
		ТекстСообщения = ПроверитьДокумент(ДействующийДокумент, 
											текВыборка.ТипДокумента, 
											текВыборка.ДокументВыдается, 
											текВыборка.ДействуетС, 
											текВыборка.ДействуетПо,
											текВыборка.ПредупреждатьОбОкончанииСрокаДействия,
											текВыборка.ИнтервалПредупреждения, 
											текВыборка.ТранспортноеСредство, 
											Справочники.упСотрудники.ПустаяСсылка(), 
											Отказ);
		
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДействующийДокумент);
		КонецЕсли;
	
	КонецЦикла;

	
КонецПроцедуры

// Функция - возвращает текст сообщения пользователю о проблемах с документами.
//
// Параметры:
//  ДокументПоТСВодителю - СправочникСсылка - Ссылка на элемент.
//  ТипДокумента - СправочникСсылка - Ссылка на элемент.
//  ДокументВыдается - ПеречислениеСсылка - Ссылка на элемент.
//  ДействуетС - Дата - Период.
//  ДействуетПо - Дата - Период.
//  ПредупреждатьОбОкончанииСрокаДействия - Булево - Выполнить.
//  ИнтервалДоПредупреждения - Число - Количество.
//  ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  Водитель - СправочникСсылка - Ссылка на элемент.
//  Отказ - Булево - Зафиксировать изменения.
//
// Возвращаемое значение:
//  Строка - Результат выпонления.
//
Функция ПроверитьДокумент(ДокументПоТСВодителю, ТипДокумента, ДокументВыдается, ДействуетС, ДействуетПо, ПредупреждатьОбОкончанииСрокаДействия, ИнтервалДоПредупреждения, ТранспортноеСредство, Водитель, Отказ) Экспорт
	
	ДатаПроверки = КонецДня(ТекущаяДатаСеанса());
	
	ТекстСообщения = "";
	
	// Если необходимого документа нет.
	Если ДокументПоТСВодителю = Неопределено Тогда
		
		Отказ = Истина;
		
		Если ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство Тогда
			ТекстСообщения = Нстр("ru = 'Не хватает документа ""%1"" по транспортному средству %2'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТипДокумента, ТранспортноеСредство);
			
		ИначеЕсли ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель Тогда	
			ТекстСообщения = Нстр("ru = 'Не хватает документа ""%1"" по водителю %2'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТипДокумента, Водитель);
			
		Иначе	
			ТекстСообщения = Нстр("ru = 'Не хватает документа ""%1"" по водителю %2 и транспортному средству %3'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТипДокумента, Водитель, ТранспортноеСредство);
		КонецЕсли;
		
		
	// Если документ просрочен.
	ИначеЕсли КонецДня(ДействуетПо) < ДатаПроверки  Тогда
		
		Отказ = Истина;
		Если ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство Тогда
			ТекстСообщения = Нстр("ru = 'Документ %1 по транспортному средству %2 просрочен'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументПоТСВодителю, ТранспортноеСредство);
		ИначеЕсли ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель Тогда	
			ТекстСообщения = Нстр("ru = 'Документ %1 по водителю %2 просрочен'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументПоТСВодителю, Водитель);
		Иначе	
			ТекстСообщения = Нстр("ru = 'Документ %1 по водителю %2 и транспортному средству %3 просрочен'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументПоТСВодителю, Водитель, ТранспортноеСредство);
		КонецЕсли;
		
	// Если документ еще не начал действовать.
	ИначеЕсли НачалоДня(ДействуетС) > ДатаПроверки  Тогда
		
		Отказ = Истина;
		Если ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство Тогда
			ТекстСообщения = Нстр("ru = 'Документ %1 по транспортному средству %2 начинает действовать с %3'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументПоТСВодителю, ТранспортноеСредство, ДействуетС);
			
		ИначеЕсли ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель Тогда	
			ТекстСообщения = Нстр("ru = 'Документ %1 по водителю %2 начинает действовать с %3'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументПоТСВодителю, Водитель, ДействуетС);
			
		Иначе	
			ТекстСообщения = Нстр("ru = 'Документ %1 по водителю %2 и транспортному средству %3 начинает действовать с %4'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументПоТСВодителю, Водитель, ТранспортноеСредство, ДействуетС);
		КонецЕсли;	
		
		
	// Если требуется предупредить об окончании срока действия документа.
	ИначеЕсли НЕ ДействуетПо = '29991231' И ПредупреждатьОбОкончанииСрокаДействия Тогда
		
		СекундВСутках = 3600*24;
		текДатаНачалаОповещенияОбОкончанииДействияДокумента = НачалоДня(ДействуетПо) - СекундВСутках * ИнтервалДоПредупреждения;
		Если текДатаНачалаОповещенияОбОкончанииДействияДокумента <= ДатаПроверки Тогда
			текДнейКоличество = Цел((НачалоДня(ДействуетПо) - ДатаПроверки)/СекундВСутках);
			Если текДнейКоличество > 0 Тогда
				текДней = "Через " +НРег(ЧислоПрописью(текДнейКоличество,"" ,Нстр("ru = 'день,дня,дней,м,,,,,0'")));	
			Иначе	
				текДней = "Сегодня";	
			КонецЕсли;
			
			Если ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство Тогда
				ТекстСообщения = Нстр("ru = '%1 истекает срок документа %2 по транспортному средству %3'");	
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текДней ,ДокументПоТСВодителю, ТранспортноеСредство);
			ИначеЕсли ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель Тогда	
				ТекстСообщения = Нстр("ru = '%1 истекает срок документа %2 по водителю %3'");	
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текДней, ДокументПоТСВодителю, Водитель);
			Иначе	
				ТекстСообщения = Нстр("ru = '%1 истекает срок документа %2 по водителю %3 и транспортному средству %4'");	
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текДней, ДокументПоТСВодителю, Водитель, ТранспортноеСредство);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстСообщения;
		
КонецФункции

// Функция - возвращает оценочную стоимость по заданиям рейса.
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс	 - Ссылка на элемент.
// 
// Возвращаемое значение:
//  Число - Результат выпонления.
//
Функция ОценочнаяСтоимостьРейса(Рейс) Экспорт
	
	текРезультат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбРаботы.Регистратор,
	               |	тбРаботы.Рейс,
	               |	тбРаботы.Отменена,
	               |	тбРаботы.Операция,
	               |	тбРаботы.Задание,
	               |	тбРаботы.ГрузовоеМесто
	               |ПОМЕСТИТЬ втРаботыПослеКорректировки
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК тбРаботы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	               |		ПО тбРаботы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	               |		ПО тбРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	упРаботыПоРейсу.Регистратор
	               |ПОМЕСТИТЬ втРегистраторы
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	               |ГДЕ
	               |	упРаботыПоРейсу.Рейс = &Рейс
	               |	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втРаботы.Задание,
	               |	ВЫБОР
	               |		КОГДА &ПустыеГрузовыеМеста
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	               |		ИНАЧЕ втРаботы.ГрузовоеМесто
	               |	КОНЕЦ КАК ГрузовоеМесто,
	               |	СУММА(ВЫБОР
	               |			КОГДА втРаботы.Отменена
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК КоличествоОтменена,
	               |	СУММА(1) КАК КоличествоОпераций
	               |ПОМЕСТИТЬ втОтмененныеЗадания
	               |ИЗ
	               |	втРаботыПослеКорректировки КАК втРаботы
	               |ГДЕ
	               |	(втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	               |			ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втРаботы.Задание,
	               |	ВЫБОР
	               |		КОГДА &ПустыеГрузовыеМеста
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	               |		ИНАЧЕ втРаботы.ГрузовоеМесто
	               |	КОНЕЦ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упРейсЗадания.Задание,
	               |	упРейсЗадания.ГрузовоеМесто,
	               |	упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	               |				И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Отменено
	               |ПОМЕСТИТЬ втЗадания
	               |ИЗ
	               |	Документ.упРейс.Задания КАК упРейсЗадания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	               |		ПО упРейсЗадания.Задание = втОтмененныеЗадания.Задание
	               |			И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	               |ГДЕ
	               |	упРейсЗадания.Ссылка = &Рейс
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	упКорректировкаРейсаЗадания.Задание,
	               |	упКорректировкаРейсаЗадания.ГрузовоеМесто,
	               |	упКорректировкаРейсаЗадания.КоличествоГрузов,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	               |				И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ
	               |ИЗ
	               |	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	               |		ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	               |		ПО упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
	               |			И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА упРейсЗадания.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	               |				ТОГДА упРейсЗадания.Задание.ОценочнаяСтоимость
	               |			ИНАЧЕ упРейсЗадания.ГрузовоеМесто.ОценочнаяСтоимость * упРейсЗадания.КоличествоГрузов
	               |		КОНЕЦ) КАК ОценочнаяСтоимость
	               |ИЗ
	               |	втЗадания КАК упРейсЗадания
	               |ГДЕ
	               |	НЕ упРейсЗадания.Отменено
	               |
	               |ИМЕЮЩИЕ
	               |	НЕ СУММА(ВЫБОР
	               |				КОГДА упРейсЗадания.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПУстаяСсылка)
	               |					ТОГДА упРейсЗадания.Задание.ОценочнаяСтоимость
	               |				ИНАЧЕ упРейсЗадания.ГрузовоеМесто.ОценочнаяСтоимость * упРейсЗадания.КоличествоГрузов
	               |			КОНЕЦ) ЕСТЬ NULL";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	ПараметрыВводаИнформацииОГрузовыхМестах	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах");
	ПустыеГрузовыеМеста = ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса;					   
	Запрос.УстановитьПараметр("ПустыеГрузовыеМеста", ПустыеГрузовыеМеста);		
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текРезультат + текВыборка.ОценочнаяСтоимость;
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

// Функция - возвращает структуру с суммами фактического и планового оборота.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
//  Дата - Дата - Период.
//  ВедетсяВалютныйУчет - Булево - Учет.
//
// Возвращаемое значение:
//  Структура - Результат выпонления.
//
Функция СуммыИнкассацииРейса(Рейс, Дата, ВедетсяВалютныйУчет) Экспорт
	
	сткРезультат = Новый Структура("Плановая, Фактическая", 0, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	               |	МАКСИМУМ(НЕ упРаботыПоРейсу.Отменена) КАК НеОтменена
	               |ПОМЕСТИТЬ втЗаявки
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза
	               |
	               |ИМЕЮЩИЕ
	               |	МАКСИМУМ(НЕ упРаботыПоРейсу.Отменена) = ИСТИНА
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(упИнкассацияДенежныхСредствОбороты.СуммаПланОборот) КАК СуммаПланОборот,
	               |	упИнкассацияДенежныхСредствОбороты.Валюта,
	               |	СУММА(упИнкассацияДенежныхСредствОбороты.СуммаФактОборот) КАК СуммаФактОборот
	               |ИЗ
	               |	РегистрНакопления.упИнкассацияДенежныхСредств.Обороты(
	               |			,
	               |			,
	               |			,
	               |			ЗаявкаНаПеревозкуГруза В
	               |				(ВЫБРАТЬ
	               |					втзаявки.ЗаявкаНаПеревозкуГруза
	               |				ИЗ
	               |					втЗаявки КАК втзаявки)) КАК упИнкассацияДенежныхСредствОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упИнкассацияДенежныхСредствОбороты.Валюта";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	текПлановая 	= 0;
	текФактическая = 0;
	
	Если текВыборка.Следующий() Тогда
		Если ВедетсяВалютныйУчет 
			И ЗначениеЗаполнено(текВыборка.Валюта) Тогда
			текПлановая		= текПлановая + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(текВыборка.Валюта, текВыборка.СуммаПланОборот, Дата);
			текФактическая 	= текФактическая + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(текВыборка.Валюта, текВыборка.СуммаФактОборот, Дата);			
		Иначе	
			текПлановая		= текПлановая + текВыборка.СуммаПланОборот;
			текФактическая 	= текФактическая + текВыборка.СуммаФактОборот;	
		КонецЕсли;
	КонецЕсли;
	сткРезультат.Вставить("Плановая", 		текПлановая);
	сткРезультат.Вставить("Фактическая", 	текФактическая);
		
	Возврат сткРезультат;
		
КонецФункции

// Функция - Возвращает массив ссылкок рейсов по транспортному средству.
//
// Параметры:
//  ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  Начало - Дата - Период.
//  Окончание - Дата - Период.
//
// Возвращаемое значение:
//  Массив - Результат выпонления.
//
Функция СписокРейсовПоТранспортномуСредствуЗаПериод(ТранспортноеСредство, Начало, Окончание) Экспорт
	
	мсвРейсы = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Рейс
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, ) КАК упПеревозчикПоРейсуСрезПоследних
	|ГДЕ
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = &ТранспортноеСредство
	|	И упПеревозчикПоРейсуСрезПоследних.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("НачалоПериода"       , Начало);
	Запрос.УстановитьПараметр("КонецПериода"        , Окончание);
	мсвРейсов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Рейс");
	
	Возврат мсвРейсов;
	
КонецФункции

#Область ФормированиеДвижений

#Область Рейс

// Процедура - формирует движения по регистру "упВыработкаСотрудников".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеВыработкаСотрудников(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	РассчитыватьСтатус = ДополнительныеСвойства.РассчитыватьСтатус;
	
	Если РассчитыватьСтатус Тогда
		сткСтатусы = упРаботаСоСтатусами.РассчитатьСтатусРейса(Реквизиты.Рейс);
		Статус     = сткСтатусы.Статус;
		ДополнительныеСвойства.Вставить("Статус", Статус);		
	Иначе
		Статус = ДополнительныеСвойства.Статус;		
	КонецЕсли;
	
	Если НЕ Статус = Перечисления.упСтатусыРейса.Завершен Тогда
		// При переводе рейса в статус"Завершен" делать движения по РН "упВыработкаСотрудников".
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Рейс
	|ПОМЕСТИТЬ вт_Рейсы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК СтатусыРейсовСП
	|		ПО упРейс.Ссылка = СтатусыРейсовСП.Документ
	|			И (СтатусыРейсовСП.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен))
	|ГДЕ
	|	упРейс.Ссылка = &Рейс
	|
	|СГРУППИРОВАТЬ ПО
	|	упРейс.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП1.Рейс КАК Рейс,
	|	ПеревозчикПоРейсуСП1.Водитель1 КАК Сотрудник
	|ПОМЕСТИТЬ вт_Сотрудники
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			НЕ Водитель1 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК ПеревозчикПоРейсуСП1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП2.Рейс,
	|	ПеревозчикПоРейсуСП2.Водитель2
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			НЕ Водитель2 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК ПеревозчикПоРейсуСП2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСПЭ.Рейс,
	|	ПеревозчикПоРейсуСПЭ.Экспедитор
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			НЕ Экспедитор = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК ПеревозчикПоРейсуСПЭ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботыПРСП_КЗ.Рейс КАК Рейс,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РаботыПРСП_КЗ.Задание) КАК КоличествоЗаданий,
	|	0 КАК ОценочнаяСтоимость
	|ПОМЕСТИТЬ вт_КоличествоЗаданий
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					вт_Рейсы.Рейс
	|				ИЗ
	|					вт_Рейсы КАК вт_Рейсы)) КАК РаботыПРСП_КЗ
	|ГДЕ
	|	РаботыПРСП_КЗ.Отменена = ЛОЖЬ
	|	И РаботыПРСП_КЗ.Выполнена = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботыПРСП_КЗ.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактПараметрыРейса.Рейс,
	|	ФактПараметрыРейса.Масса,
	|	ФактПараметрыРейса.Объем,
	|	ФактПараметрыРейса.КоличествоМест,
	|	ФактПараметрыРейса.КоличествоЗагрузочныхМетров,
	|	ФактПараметрыРейса.ВремяВПути,
	|	ФактПараметрыРейса.Время,
	|	ФактПараметрыРейса.Расстояние,
	|	ФактПараметрыРейса.СуммаИнкассации
	|ПОМЕСТИТЬ вт_ФактПРейса
	|ИЗ
	|	РегистрСведений.упФактическиеПараметрыРейса КАК ФактПараметрыРейса
	|ГДЕ
	|	ФактПараметрыРейса.Рейс В
	|			(ВЫБРАТЬ
	|				вт_Рейсы.Рейс
	|			ИЗ
	|				вт_Рейсы КАК вт_Рейсы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутРейсаСП_П.Рейс,
	|	КОЛИЧЕСТВО(МаршрутРейсаСП_П.Пройдена) КАК Пройдена
	|ПОМЕСТИТЬ вт_Пройдена
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					вт_Рейсы.Рейс
	|				ИЗ
	|					вт_Рейсы КАК вт_Рейсы)) КАК МаршрутРейсаСП_П
	|ГДЕ
	|	МаршрутРейсаСП_П.Пройдена = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутРейсаСП_П.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутРейсаСП_П.Рейс,
	|	КОЛИЧЕСТВО(МаршрутРейсаСП_П.Отменена) КАК Отменена
	|ПОМЕСТИТЬ вт_Отменена
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					вт_Рейсы.Рейс
	|				ИЗ
	|					вт_Рейсы КАК вт_Рейсы)) КАК МаршрутРейсаСП_П
	|ГДЕ
	|	МаршрутРейсаСП_П.Отменена = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутРейсаСП_П.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутРейсаСП_О.Рейс,
	|	СУММА(ВЫБОР
	|			КОГДА РаботыПоРейсуСП_О.ВременноеОкноНачало ЕСТЬ NULL 
	|				ТОГДА 0
	|			КОГДА НЕ(МаршрутРейсаСП_О.ПрибытиеФакт МЕЖДУ РаботыПоРейсуСП_О.ВременноеОкноНачало И РаботыПоРейсуСП_О.ВременноеОкноОкончание
	|						И (МаршрутРейсаСП_О.УбытиеФакт МЕЖДУ РаботыПоРейсуСП_О.ВременноеОкноНачало И РаботыПоРейсуСП_О.ВременноеОкноОкончание))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Опоздание
	|ПОМЕСТИТЬ вт_Опаздания
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					вт_Рейсы.Рейс
	|				ИЗ
	|					вт_Рейсы КАК вт_Рейсы)) КАК МаршрутРейсаСП_О
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|				,
	|				Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК РаботыПоРейсуСП_О
	|		ПО МаршрутРейсаСП_О.Рейс = РаботыПоРейсуСП_О.Рейс
	|			И МаршрутРейсаСП_О.Идентификатор = РаботыПоРейсуСП_О.Идентификатор
	|ГДЕ
	|	МаршрутРейсаСП_О.Отменена = ЛОЖЬ
	|	И МаршрутРейсаСП_О.Пройдена = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутРейсаСП_О.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутПоРейсуСП_ТО.Рейс КАК Рейс,
	|	ВЫБОР
	|		КОГДА МаршрутПРСП_Сл.ПрибытиеФакт ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА МаршрутПоРейсуСП_ТО.УбытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(МаршрутПРСП_Сл.ПрибытиеФакт, МаршрутПоРейсуСП_ТО.УбытиеФакт, ЧАС)
	|	КОНЕЦ КАК ВремяВПутиОтГаража
	|ПОМЕСТИТЬ вт_ВремяВПутиОтГаража
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ТочкаОтправления)
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК МаршрутПоРейсуСП_ТО
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МаршрутРСП_Сл.Рейс КАК Рейс,
	|			МИНИМУМ(МаршрутРСП_Сл.СтрокаНомер) КАК СтрокаНомер
	|		ИЗ
	|			РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|					,
	|					НЕ ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ТочкаОтправления)
	|						И Рейс В
	|							(ВЫБРАТЬ
	|								вт_Рейсы.Рейс
	|							ИЗ
	|								вт_Рейсы КАК вт_Рейсы)) КАК МаршрутРСП_Сл
	|		ГДЕ
	|			МаршрутРСП_Сл.Отменена = ЛОЖЬ
	|			И МаршрутРСП_Сл.Пройдена = ИСТИНА
	|		
	|		СГРУППИРОВАТЬ ПО
	|			МаршрутРСП_Сл.Рейс) КАК вз_Следующий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК МаршрутПРСП_Сл
	|			ПО вз_Следующий.Рейс = МаршрутПРСП_Сл.Рейс
	|				И вз_Следующий.СтрокаНомер = МаршрутПРСП_Сл.СтрокаНомер
	|		ПО МаршрутПоРейсуСП_ТО.Рейс = вз_Следующий.Рейс
	|ГДЕ
	|	МаршрутПоРейсуСП_ТО.Отменена = ЛОЖЬ
	|	И МаршрутПоРейсуСП_ТО.Пройдена = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутПоРейсуСП_ТЗ.Рейс КАК Рейс,
	|	ВЫБОР
	|		КОГДА МаршрутПРСП_Пред.УбытиеФакт ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА МаршрутПоРейсуСП_ТЗ.ПрибытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(МаршрутПоРейсуСП_ТЗ.ПрибытиеФакт, МаршрутПРСП_Пред.УбытиеФакт, ЧАС)
	|	КОНЕЦ КАК ВремяВПутиДоГаража
	|ПОМЕСТИТЬ вт_ВремяВПутиДоГаража
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ТочкаЗавершения)
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК МаршрутПоРейсуСП_ТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МаршрутРСП_Пред.Рейс КАК Рейс,
	|			МАКСИМУМ(МаршрутРСП_Пред.СтрокаНомер) КАК СтрокаНомер
	|		ИЗ
	|			РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|					,
	|					НЕ ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ТочкаЗавершения)
	|						И Рейс В
	|							(ВЫБРАТЬ
	|								вт_Рейсы.Рейс
	|							ИЗ
	|								вт_Рейсы КАК вт_Рейсы)) КАК МаршрутРСП_Пред
	|		ГДЕ
	|			МаршрутРСП_Пред.Отменена = ЛОЖЬ
	|			И МаршрутРСП_Пред.Пройдена = ИСТИНА
	|		
	|		СГРУППИРОВАТЬ ПО
	|			МаршрутРСП_Пред.Рейс) КАК вз_Предыдущий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК МаршрутПРСП_Пред
	|			ПО вз_Предыдущий.Рейс = МаршрутПРСП_Пред.Рейс
	|				И вз_Предыдущий.СтрокаНомер = МаршрутПРСП_Пред.СтрокаНомер
	|		ПО МаршрутПоРейсуСП_ТЗ.Рейс = вз_Предыдущий.Рейс
	|ГДЕ
	|	МаршрутПоРейсуСП_ТЗ.Отменена = ЛОЖЬ
	|	И МаршрутПоРейсуСП_ТЗ.Пройдена = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Рейсы.Рейс КАК Рейс,
	|	ЕСТЬNULL(вт_Пройдена.Пройдена, 0) КАК КоличествоПройденныхТочек,
	|	ЕСТЬNULL(вт_Отменена.Отменена, 0) КАК КоличествоОтмененныхТочек,
	|	ЕСТЬNULL(вт_Опаздания.Опоздание, 0) КАК КоличествоОпозданий,
	|	ЕСТЬNULL(вт_ВремяВПутиОтГаража.ВремяВПутиОтГаража, 0) КАК ВремяВПутиОтГаража,
	|	ЕСТЬNULL(вт_ВремяВПутиДоГаража.ВремяВПутиДоГаража, 0) КАК ВремяВПутиДоГаража
	|ПОМЕСТИТЬ вт_АгрегатныеДанные_Р
	|ИЗ
	|	вт_Рейсы КАК вт_Рейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Пройдена КАК вт_Пройдена
	|		ПО вт_Рейсы.Рейс = вт_Пройдена.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Отменена КАК вт_Отменена
	|		ПО вт_Рейсы.Рейс = вт_Отменена.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Опаздания КАК вт_Опаздания
	|		ПО вт_Рейсы.Рейс = вт_Опаздания.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ВремяВПутиОтГаража КАК вт_ВремяВПутиОтГаража
	|		ПО вт_Рейсы.Рейс = вт_ВремяВПутиОтГаража.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ВремяВПутиДоГаража КАК вт_ВремяВПутиДоГаража
	|		ПО вт_Рейсы.Рейс = вт_ВремяВПутиДоГаража.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботыПРСП.Рейс,
	|	ИСТИНА КАК ПередачаДокументов
	|ПОМЕСТИТЬ вт_ПередачаДокументов
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов))
	|				И Рейс В
	|					(ВЫБРАТЬ
	|						вт_Рейсы.Рейс
	|					ИЗ
	|						вт_Рейсы КАК вт_Рейсы)) КАК РаботыПРСП
	|ГДЕ
	|	РаботыПРСП.Выполнена = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботыПРСП.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.Рейс,
	|	упПлановыеПараметрыРейса.ОценочнаяСтоимость
	|ПОМЕСТИТЬ вт_ОценочнаяСтоимость
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Сотрудники.Рейс,
	|	вт_Сотрудники.Сотрудник,
	|	ЕСТЬNULL(вт_ФактПРейса.Масса, 0) КАК Масса,
	|	ЕСТЬNULL(вт_ФактПРейса.Объем, 0) КАК Объем,
	|	ЕСТЬNULL(вт_ФактПРейса.КоличествоМест, 0) КАК КоличествоМест,
	|	ЕСТЬNULL(вт_ФактПРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(вт_КоличествоЗаданий.КоличествоЗаданий, 0) КАК КоличествоЗаданий,
	|	ЕСТЬNULL(вт_АгрегатныеДанные_Р.КоличествоПройденныхТочек, 0) КАК КоличествоПройденныхТочек,
	|	ЕСТЬNULL(вт_АгрегатныеДанные_Р.КоличествоОтмененныхТочек, 0) КАК КоличествоОтмененныхТочек,
	|	ЕСТЬNULL(вт_АгрегатныеДанные_Р.КоличествоОпозданий, 0) КАК КоличествоОпозданий,
	|	ЕСТЬNULL(вт_АгрегатныеДанные_Р.ВремяВПутиОтГаража, 0) КАК ВремяВПутиОтГаража,
	|	ЕСТЬNULL(вт_АгрегатныеДанные_Р.ВремяВПутиДоГаража, 0) КАК ВремяВПутиДоГаража,
	|	ЕСТЬNULL(вт_ФактПРейса.ВремяВПути, 0) КАК ВремяВПути,
	|	ЕСТЬNULL(вт_ФактПРейса.Время, 0) КАК Время,
	|	ЕСТЬNULL(вт_ФактПРейса.Расстояние, 0) КАК Расстояние,
	|	ЕСТЬNULL(вт_ОценочнаяСтоимость.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимостьГруза,
	|	ЕСТЬNULL(вт_ПередачаДокументов.ПередачаДокументов, ЛОЖЬ) КАК ПередачаДокументов,
	|	ЕСТЬNULL(вт_ФактПРейса.СуммаИнкассации, 0) КАК СуммаИнкассации
	|ИЗ
	|	вт_Сотрудники КАК вт_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ФактПРейса КАК вт_ФактПРейса
	|		ПО вт_Сотрудники.Рейс = вт_ФактПРейса.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_КоличествоЗаданий КАК вт_КоличествоЗаданий
	|		ПО вт_Сотрудники.Рейс = вт_КоличествоЗаданий.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_АгрегатныеДанные_Р КАК вт_АгрегатныеДанные_Р
	|		ПО вт_Сотрудники.Рейс = вт_АгрегатныеДанные_Р.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПередачаДокументов КАК вт_ПередачаДокументов
	|		ПО вт_Сотрудники.Рейс = вт_ПередачаДокументов.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ОценочнаяСтоимость КАК вт_ОценочнаяСтоимость
	|		ПО вт_Сотрудники.Рейс = вт_ОценочнаяСтоимость.Рейс";
	
	Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ДвиженияВыработкаСотрудников = Движения.упВыработкаСотрудников;
		
		ДвиженияВыработкаСотрудников.Очистить();		
		ДвиженияВыработкаСотрудников.Записывать = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДвижениеВыработкаСотрудников = ДвиженияВыработкаСотрудников.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеВыработкаСотрудников,Выборка);
			
			ДвижениеВыработкаСотрудников.Период = ТекущаяДатаСеанса();
			ДвижениеВыработкаСотрудников.Рейс   = Реквизиты.Рейс;	
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвижениеВыработкаСотрудников()

// Процедура - формирует движения по регистру "упСтатусыРейсов".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	РассчитыватьСтатус = ДополнительныеСвойства.РассчитыватьСтатус;
	ДобавитьСтатус = ДополнительныеСвойства.ДобавитьСтатус;
	ЗаписыватьСтатус = Ложь;
	ВыполненЧастично = Ложь;
	ОтложенаЧастьРабот = Ложь;
	Состояние = "";
	
	ДвиженияСтатуса = Движения.упСтатусыРейсов;
	
	Если РассчитыватьСтатус Тогда
		сткСтатусы = упРаботаСоСтатусами.РассчитатьСтатусРейса(Реквизиты.Рейс);
		Статус = сткСтатусы.Статус;
		ДополнительныеСвойства.Вставить("Статус", Статус);
		ВыполненЧастично = сткСтатусы.ВыполненЧастично; 
		ОтложенаЧастьРабот = сткСтатусы.ОтложенаЧастьРабот;
		ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = сткСтатусы.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера;
		ВидТранспортнойУслуги = сткСтатусы.ВидТранспортнойУслуги;
		Состояние = сткСтатусы.Состояние;
		Если Статус <> сткСтатусы.ТекущийСтатус Тогда
			
			Если Истина
				И сткСтатусы.ТекущийСтатус = Перечисления.упСтатусыРейса.КВыполнению
				И Статус = Перечисления.упСтатусыРейса.Выполняется 
			Тогда
				ТекстСообщения = "";
				РейсПоТСВСтатусеВыполняется = Неопределено;
				Если упРейс.ЕстьЕщеРейсыПоТСВСтатусеВыполняется(Реквизиты.Рейс, ТекстСообщения, РейсПоТСВСтатусеВыполняется) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,РейсПоТСВСтатусеВыполняется,,,Отказ);
				КонецЕсли;
				Если Не Отказ
					И ПолучитьФункциональнуюОпцию("упИспользуютсяКонтейнерныеПеревозки")
					И ВидТранспортнойУслуги <> Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами
				Тогда
					ДополнительныеСвойства.Вставить("КонтролироватьСтаффировкуКонтейнера", Истина);
				КонецЕсли;
			КонецЕсли;
			
			Если Истина
				И ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера
				И сткСтатусы.ТекущийСтатус = Перечисления.упСтатусыРейса.Выполняется
				И (Ложь
					Или Статус = Перечисления.упСтатусыРейса.Выполнен 
					Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично)
			Тогда
				ТекстСообщения = "";
				ЗаявкаНаПеревозкуГруза = Неопределено;
				Если Не упРейс.ЕстьВыполненнаяЗаявкаНаПеревозкуГрузаПоОперацииСКонтейнером(Реквизиты.Рейс, ТекстСообщения, ЗаявкаНаПеревозкуГруза) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЗаявкаНаПеревозкуГруза,,,Отказ);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				ЗаписыватьСтатус = Истина;
				Если Истина
					И Не ОтложенаЧастьРабот
					И Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
				Тогда
					ВыполненЧастично = Истина;		
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Статус = ДополнительныеСвойства.Статус;	
		ЗаписыватьСтатус = ДополнительныеСвойства.ЗаписыватьСтатус;
		Если ДополнительныеСвойства.Свойство("ВыполненЧастично") Тогда
			ВыполненЧастично = ДополнительныеСвойства.ВыполненЧастично;
		Иначе	
			сткСтатус = упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(Реквизиты.Рейс); 
			ВыполненЧастично = сткСтатус.ВыполненЧастично;
		КонецЕсли;
	КонецЕсли;
	
	
	Период = ?(Реквизиты.Свойство("ДатаСтатуса"), Реквизиты.ДатаСтатуса, Реквизиты.Дата);
	Если ЗаписыватьСтатус Тогда
		
		ДвиженияСтатуса.Записывать = Истина;
		
		Если ДобавитьСтатус Тогда
			ДвиженияСтатуса.Прочитать();
		КонецЕсли;
				
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период = Период;
		ДвижениеСтатуса.Документ = Реквизиты.Рейс;
		ДвижениеСтатуса.Статус = Статус;
		
		ВидТранспортнойУслуги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "ВидТранспортнойУслуги", Неопределено);
		
		Если ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			ДвижениеСтатуса.Состояние = упРаботаСоСтатусами.СостояниеОперацииСКонтейнером(Статус);
		Иначе
			ДвижениеСтатуса.Состояние = Состояние;
		КонецЕсли;
		
		
		ДвижениеСтатуса.ВыполненЧастично = ВыполненЧастично;
		ДвижениеСтатуса.ОтложенаЧастьРабот = ОтложенаЧастьРабот;
		
		Если Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда
					
			Если ТребуетсяОформлениеПутевогоЛиста(Реквизиты.Рейс) Тогда
			 	ТекстОшибки = "";
				ПутевойЛист = Документы.упРейс.СформироватьПутевойЛист(Реквизиты.Рейс, ТекстОшибки, Отказ);			
			КонецЕсли;
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ИзменилсяСтатусРейса");
	КонецЕсли;

КонецПроцедуры

// Процедура - формирует движения по регистру "упЗанятостьТранспорта".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Ложь Тогда // Для контекстной подсказки
		Ссылка = Документы.упРейс.ПустаяСсылка();
		Реквизиты = Ссылка.ПолучитьОбъект();
		Движения = Реквизиты.Движения;
	КонецЕсли;
	Статус = ДополнительныеСвойства.Статус;
	ЗанятостьТранспорта = Движения.упЗанятостьТранспорта;
	Если Ложь
		Или Статус = Перечисления.упСтатусыРейса.Новый 
		Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС 
		Или Статус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС
		Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
		Или Статус = Перечисления.упСтатусыРейса.КВыполнению 
		Или (Истина
			И ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрохождениеТочки")  
			И Реквизиты.СтрокаНомер = 1)
	Тогда	
		
		//ДвиженияРасписания.Записывать = Истина;
		ЗанятостьТранспорта.Записывать = Истина;
		
		ВыполнитьДвиженияДляТС = Истина;
		Если ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство) 
			И НЕ Реквизиты.ТранспортноеСредство.СобственноеТранспортноеСредство Тогда
			ВыполнитьДвиженияДляТС = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство) И ВыполнитьДвиженияДляТС Тогда
			Маршрут = Неопределено;
			ЕстьМаршрут = ДополнительныеСвойства.Свойство("МаршрутПолный", Маршрут);
			Если Маршрут = Неопределено Тогда
				Если Не ПрочитатьПлановыеПараметрыРейса(Реквизиты, Реквизиты.Рейс) Тогда
					Возврат;
				КонецЕсли; 
				Маршрут = ПолучитьТекущийМаршрутРейса(Реквизиты.Рейс);
			КонецЕсли;
			
			ПлановоеНачалоВыполнения 	= Неопределено;
			ПлановоеОкончаниеВыполнения = Неопределено;
			
			Если НЕ Реквизиты.Свойство("ПлановыеПараметры") Тогда
				
				Если Маршрут.Количество() > 0 Тогда
					ПлановоеНачалоВыполнения 	= Маршрут[0].ПлановоеПрибытие;
					ПлановоеОкончаниеВыполнения	= Маршрут[Маршрут.Количество()-1].ПлановоеУбытие; 	
				КонецЕсли;
							
			Иначе 
				Если НЕ Реквизиты.ПлановыеПараметры.Свойство("ПлановоеНачалоВыполнения", ПлановоеНачалоВыполнения)	Тогда
					Если Маршрут.Количество() > 0 Тогда
						ПлановоеНачалоВыполнения = Маршрут[0].ПлановоеПрибытие;
					КонецЕсли;		
				КонецЕсли;
				
				Если НЕ Реквизиты.ПлановыеПараметры.Свойство("ПлановоеОкончаниеВыполнения", ПлановоеОкончаниеВыполнения)	Тогда
					Если Маршрут.Количество() > 0 Тогда
						ПлановоеОкончаниеВыполнения = Маршрут[Маршрут.Количество()-1].ПлановоеУбытие;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПлановоеНачалоВыполнения) Тогда
				//Если ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон") Тогда
				//	Если Не Реквизиты.ПлановыеПараметры.Свойство("ЗонаПогрузки") Тогда
				//		ОпределитьЗоныПогрузкиРазгрузки(Реквизиты.Работы, Реквизиты.Маршрут, Реквизиты.ПлановыеПараметры);
				//	КонецЕсли;
				//	ДвижениеРасписания.ЗонаНачала 			= Реквизиты.ПлановыеПараметры.ЗонаПогрузки;
				//	ДвижениеРасписания.ЗонаЗавершения 		= Реквизиты.ПлановыеПараметры.ЗонаРазгрузки;
				//КонецЕсли;
				ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
				ДвиженияСостояние.Период = Реквизиты.Дата;
				ДвиженияСостояние.Документ = Реквизиты.Рейс;
				Если ДополнительныеСвойства.Свойство("ИзмененЭкипаж") Тогда
					ДвиженияСостояние.Транспорт = ДополнительныеСвойства.ИзмененЭкипаж.ТранспортноеСредство;
				Иначе
					ДвиженияСостояние.Транспорт = Реквизиты.ТранспортноеСредство;
				КонецЕсли; 
				ДвиженияСостояние.ПланАктуален = Истина;
				ДвиженияСостояние.СостояниеТранспорта = Справочники.упСостоянияТС.ВРейсе;
				ДвиженияСостояние.ДатаНачала = ПлановоеНачалоВыполнения;
				ДвиженияСостояние.ДатаОкончания = ПлановоеОкончаниеВыполнения;
				Если Маршрут.Количество() > 0 Тогда
					ЗонаНачала = Маршрут[0].Адрес.ГеографическаяЗона;
					ЗонаКонца = Маршрут[Маршрут.Количество() - 1].Адрес.ГеографическаяЗона;
								
					МассивЗон = Новый Массив;
					МассивЗон.Добавить(ЗонаНачала);
					МассивЗон.Добавить(ЗонаКонца);
					УчетныеЗоны = упУчетТранспортныхСредств.УчетныеЗоныЗон(МассивЗон);
					СтрокаЗоны = УчетныеЗоны.Найти(ЗонаНачала, "Зона");
					Если СтрокаЗоны <> Неопределено Тогда
						ДвиженияСостояние.ЗонаНачала = СтрокаЗоны.Родитель;
					КонецЕсли; 
					СтрокаЗоны = УчетныеЗоны.Найти(ЗонаКонца, "Зона");
					Если СтрокаЗоны <> Неопределено Тогда
						ДвиженияСостояние.ЗонаОкончания = СтрокаЗоны.Родитель;
					КонецЕсли; 
				КонецЕсли;
				ДвиженияСостояние.ФактНачала = Статус = Перечисления.упСтатусыРейса.Выполняется;
			КонецЕсли; 
		КонецЕсли;
		
	ИначеЕсли Ложь
		Или Статус = Перечисления.упСтатусыРейса.Выполнен 
		Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично 
		Или Статус = Перечисления.упСтатусыРейса.Завершен 
		Или Статус = Перечисления.упСтатусыРейса.Отменен 
	Тогда	
		ЗанятостьТранспорта.Записывать = Истина;
		ОтложенноеПрохождение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "ОтложенноеПрохождение", Ложь);
		Если Истина
			И ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство)
			И Не ОтложенноеПрохождение 
		Тогда 
			СрезПоследних = упУчетТранспортныхСредств.СрезПоследнихЗанятостьТранспорта(Ссылка.МоментВремени(), Реквизиты.Рейс, Реквизиты.ТранспортноеСредство);
			Если Ложь Тогда // Для контекстной подсказки
				СрезПоследних = Новый  ТаблицаЗначений;
			КонецЕсли;
			СрезПоследних.ЗаполнитьЗначения(Ссылка.Дата, "Период");
			СрезПоследних.ЗаполнитьЗначения(Ложь, "ПланАктуален");
			СрезПоследних.ЗаполнитьЗначения(Статус <> Перечисления.упСтатусыРейса.Отменен , "ФактОкончания");
			ЗанятостьТранспорта.Загрузить(СрезПоследних);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упРасписаниеРаботыТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияРасписаниеРаботыТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	ЕстьОтложенныеРаботы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ЕстьОтложенныеРаботы", Ложь);
	Если Истина
		И Не ЕстьОтложенныеРаботы
		И (Ложь
		   Или Статус = Перечисления.упСтатусыРейса.Выполнен 
		   Или Статус = Перечисления.упСтатусыРейса.Завершен 
		   Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично 
		   Или Статус = Перечисления.упСтатусыРейса.Отменен)
	Тогда	
		ДвиженияРасписания = Движения.упРасписаниеРаботыТС;
		ДвиженияРасписания.Записывать = Истина;
		ФактическиеПараметры = Реквизиты.ФактическиеПараметры;
		
		Если ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство) Тогда 
			ВыполнитьДвиженияДляТС = Истина;
			Если НЕ Реквизиты.ТранспортноеСредство.СобственноеТранспортноеСредство Тогда
				ВыполнитьДвиженияДляТС = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
			КонецЕсли;
			
			Если ВыполнитьДвиженияДляТС И НЕ Статус = Перечисления.упСтатусыРейса.Отменен Тогда
				
				НачалоВыполнения 	= Неопределено;
				ОкончаниеВыполнения	= Неопределено;
				
				Если НЕ ФактическиеПараметры.Свойство("НачалоВыполнения", НачалоВыполнения) Тогда
					 НачалоВыполнения 	= Неопределено;
				КонецЕсли;
				
				Если НЕ ФактическиеПараметры.Свойство("ОкончаниеВыполнения", ОкончаниеВыполнения) Тогда
					ОкончаниеВыполнения	= Неопределено;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(НачалоВыполнения) 
					ИЛИ НЕ ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
					
					Запрос = Новый Запрос;
					Запрос.Текст = "ВЫБРАТЬ
					|	ПараметрыРейсаФакт.НачалоВыполнения,
					|	ПараметрыРейсаФакт.ОкончаниеВыполнения
					|ИЗ
					|	РегистрСведений.упФактическиеПараметрыРейса КАК ПараметрыРейсаФакт
					|ГДЕ
					|	ПараметрыРейсаФакт.Рейс = &Рейс";
					Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
					текВыборка	= Запрос.Выполнить().Выбрать();
					Если текВыборка.Следующий() Тогда
						НачалоВыполнения 	= текВыборка.НачалоВыполнения;
						ОкончаниеВыполнения = текВыборка.ОкончаниеВыполнения;	
					КонецЕсли;	
				КонецЕсли;
				
				Если ЗначениеЗаполнено(НачалоВыполнения)
					И ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
					
					ДвижениеРасписания = ДвиженияРасписания.Добавить();
					ДвижениеРасписания.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
					ДвижениеРасписания.ТипРасписания 		= Перечисления.упТипыРасписанийРаботыТС.Факт;
					ДвижениеРасписания.ДатаНачала 		= НачалоВыполнения;
					ДвижениеРасписания.ДатаОкончания 		= ОкончаниеВыполнения;
					ДвижениеРасписания.СостояниеТС          = Справочники.упСостоянияТС.ВРейсе;
					ДвижениеРасписания.ДокументОснование 	= Ссылка;
			
				КонецЕсли;
				
			КонецЕсли;					
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упРасписаниеРаботыЭкипажей".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеРасписаниеРаботыВодителей(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	Статус = ДополнительныеСвойства.Статус;
	ДвиженияРасписания = Движения.упРасписаниеРаботыЭкипажей;
	
	Если Ложь Тогда // Для контекстной подсказки.
		Ссылка = Документы.упРейс.ПустаяСсылка();
		Реквизиты = Ссылка;
		Текущий_Объект = Ссылка.ПолучитьОбъект();
	    ДвиженияРасписания = Текущий_Объект.Движения.упРасписаниеРаботыЭкипажей;
	КонецЕсли;
	Водители = Новый Массив;
	Если ЗначениеЗаполнено(Реквизиты.Водитель1) Тогда
		Водители.Добавить(Реквизиты.Водитель1);
	КонецЕсли; 
	Если ЗначениеЗаполнено(Реквизиты.Водитель2) И Водители.Найти(Реквизиты.Водитель2)=Неопределено Тогда
		Водители.Добавить(Реквизиты.Водитель2);
	КонецЕсли; 
	Если ЗначениеЗаполнено(Реквизиты.Экспедитор) И Водители.Найти(Реквизиты.Экспедитор)=Неопределено Тогда
		Водители.Добавить(Реквизиты.Экспедитор);
	КонецЕсли; 
	Если Ложь
		Или Статус = Перечисления.упСтатусыРейса.Новый 
		Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС 
		Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
		Или Статус = Перечисления.упСтатусыРейса.КВыполнению
	Тогда	
		Маршрут = ДополнительныеСвойства.Маршрут;
		Работы = ДополнительныеСвойства.Работы;
		ПлановыеПараметры = Реквизиты.ПлановыеПараметры;
		
		Если Ложь
			Или Маршрут = Неопределено 
			Или Работы = Неопределено
		Тогда
			Если Не ПрочитатьПлановыеПараметрыРейса(Реквизиты, Ссылка) Тогда
				Возврат;
			КонецЕсли; 
		КонецЕсли;
		
		// Если не задано плановое начало выполнение рейса.
		Если Истина
			И НЕ ПлановыеПараметры.Свойство("ПлановоеНачалоВыполнения") 
			И ДополнительныеСвойства.Маршрут.Количество() > 0 
		Тогда
		
			// Началом будет плановое прибытие в первую точку маршрута.
			ПлановыеПараметры.Вставить("ПлановоеНачалоВыполнения", ДополнительныеСвойства.Маршрут[0].ПлановоеПрибытие);
			
		КонецЕсли; 
		
		// Если не задано плановое окончание выполнение рейса.
		Если Истина
			И НЕ ПлановыеПараметры.Свойство("ПлановоеОкончаниеВыполнения") 
			И ДополнительныеСвойства.Маршрут.Количество() > 0 
		Тогда
		
			// Окончанием будет плановое убытие из последней точки маршрута.
			ПлановыеПараметры.Вставить("ПлановоеОкончаниеВыполнения", ДополнительныеСвойства.Маршрут[ДополнительныеСвойства.Маршрут.Количество()-1].ПлановоеУбытие);
			
		КонецЕсли; 
		
		// Если не удалось определить плановые начало и окончание.
		Если Ложь
			Или Не ПлановыеПараметры.Свойство("ПлановоеНачалоВыполнения")
			Или Не ПлановыеПараметры.Свойство("ПлановоеОкончаниеВыполнения")
			Или Не ЗначениеЗаполнено(ПлановыеПараметры.ПлановоеНачалоВыполнения)
			Или Не ЗначениеЗаполнено(ПлановыеПараметры.ПлановоеОкончаниеВыполнения)
		Тогда
			Возврат;
		КонецЕсли;
		
		ДвиженияРасписания.Записывать = Истина;
		Для Каждого Водитель Из Водители Цикл
			ДвижениеРасписания = ДвиженияРасписания.Добавить();
			ДвижениеРасписания.Водитель = Водитель;
			ДвижениеРасписания.ТипРасписания = Перечисления.упТипыРасписанийРаботыТС.План;
			ДвижениеРасписания.ДатаНачала = ПлановыеПараметры.ПлановоеНачалоВыполнения;
			ДвижениеРасписания.ДатаОкончания = ПлановыеПараметры.ПлановоеОкончаниеВыполнения;
			ДвижениеРасписания.Документ = Ссылка;
			
			Если ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон") Тогда
				Если Не Реквизиты.ПлановыеПараметры.Свойство("ЗонаПогрузки") Тогда
					ОпределитьЗоныПогрузкиРазгрузки(ДополнительныеСвойства.Работы, ДополнительныеСвойства.Маршрут, ПлановыеПараметры, Реквизиты.ВариантФормированияМаршрута);
				КонецЕсли;
				
				ДвижениеРасписания.ЗонаНачала = ПлановыеПараметры.ЗонаПогрузки;
				ДвижениеРасписания.ЗонаЗавершения = ПлановыеПараметры.ЗонаРазгрузки;
			КонецЕсли;
			
		КонецЦикла; 
		
	ИначеЕсли Ложь
		Или Статус = Перечисления.упСтатусыРейса.Выполнен 
		Или Статус = Перечисления.упСтатусыРейса.Завершен 
	Тогда	
	
		ДвиженияРасписания.Записывать = Истина;
		ФактическиеПараметры = Реквизиты.ФактическиеПараметры;
		
		Если Ложь
			Или Не ФактическиеПараметры.Свойство("ПлановоеНачалоВыполнения")
			Или Не ФактическиеПараметры.Свойство("ПлановоеОкончаниеВыполнения")
			Или Не ЗначениеЗаполнено(ФактическиеПараметры.ПлановоеНачалоВыполнения)
			Или Не ЗначениеЗаполнено(ФактическиеПараметры.ПлановоеОкончаниеВыполнения)
		Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого Водитель Из Водители Цикл
			ДвижениеРасписания = ДвиженияРасписания.Добавить();
			ДвижениеРасписания.Водитель = Водитель;
			ДвижениеРасписания.ТипРасписания = Перечисления.упТипыРасписанийРаботыТС.Факт;
			ДвижениеРасписания.ДатаНачала = ФактическиеПараметры.НачалоВыполнения;
			ДвижениеРасписания.ДатаОкончания = ФактическиеПараметры.ОкончаниеВыполнения;
			ДвижениеРасписания.Документ = Ссылка;
		КонецЦикла; 
		
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		
		ДвиженияРасписания.Записывать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполнить чтение плановых параметров рейса.
//
// Параметры:
//  Реквизиты - Структура - Реквизиты объекта.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Булево - Результат выпонления.
//
Функция ПрочитатьПлановыеПараметрыРейса(Знач Реквизиты, Знач Ссылка)
	
	ПлановыеПараметрыНайдены = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.ЗонаПогрузки,
	|	упПлановыеПараметрыРейса.ЗонаРазгрузки,
	|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения,
	|	упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Рейс", Ссылка);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ПлановыеПараметрыНайдены = Истина;
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если Не Реквизиты.Свойство("ПлановыеПараметры") Тогда
			Реквизиты.Вставить("ПлановыеПараметры", Новый Структура);
		КонецЕсли; 
		Реквизиты.ПлановыеПараметры.Вставить("ПлановоеНачалоВыполнения"   , Выборка.ПлановоеНачалоВыполнения);
		Реквизиты.ПлановыеПараметры.Вставить("ПлановоеОкончаниеВыполнения", Выборка.ПлановоеОкончаниеВыполнения);
		Реквизиты.ПлановыеПараметры.Вставить("ЗонаПогрузки"               , Выборка.ЗонаПогрузки);
		Реквизиты.ПлановыеПараметры.Вставить("ЗонаРазгрузки"              , Выборка.ЗонаРазгрузки);
	КонецЕсли;
	Возврат ПлановыеПараметрыНайдены;

КонецФункции

// Процедура - формирует движения по регистру "упСостоянияТранспортныхСредств".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	Статус = ДополнительныеСвойства.Статус;
	ТранспортноеСредство = Неопределено;
	Рейс = Реквизиты.Рейс;
	
	СостояниеТС = Неопределено;
	ИзменитьПериод = Ложь;
	ДатаИзмененияСостояния = '00010101';
	НовоеСостояниеТС = Неопределено;
	
	ДвиженияСостоянияТС	= Движения.упСостоянияТранспортныхСредств;
	ДвиженияСостоянияТС.Записывать = Истина;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыРейса.Выполняется 
		Или Статус = Перечисления.упСтатусыРейса.Выполнен 
	Тогда
		ТранспортноеСредство = ПолучитьТранспортноеСредствоРейса(Рейс);
		Если Не ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			Возврат;
		КонецЕсли; 
		Реквизиты.Вставить("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭтоСобственноеТС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "СобственноеТранспортноеСредство");
		
		ИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
		
		// Если это не собственное ТС то движения делать не надо.
		Если НЕ ЭтоСобственноеТС И НЕ ИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
			
	Если Статус = Перечисления.упСтатусыРейса.Выполняется Тогда
		Если НЕ Реквизиты.Свойство("НачалоВыполнения", ДатаИзмененияСостояния) Тогда
			ДатаИзмененияСостояния 	= ТекущаяДатаСеанса();
		Иначе
			ИзменитьПериод = Истина;	
		КонецЕсли;	
		
		сткСостояниеТС = упУчетТранспортныхСредств.СостояниеТранспортногоСредства(ТранспортноеСредство, ДатаИзмененияСостояния);
		СостояниеТС = сткСостояниеТС.СостояниеТС;
		ПериодИзмененияТС = сткСостояниеТС.Период;
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, Новый Структура("СостояниеТС", "ВидПеревозки.СостояниеТСПриИсполненииРейса"));														  
		
		Если Реквизиты.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			НовоеСостояниеТС = ?(ЗначениеЗаполнено(сткРеквизиты.СостояниеТС), сткРеквизиты.СостояниеТС, Справочники.упСостоянияТС.Используется);
		Иначе	
			НовоеСостояниеТС = ?(ЗначениеЗаполнено(сткРеквизиты.СостояниеТС), сткРеквизиты.СостояниеТС, Справочники.упСостоянияТС.ВРейсе);
		КонецЕсли;
		
		Если Ложь
			Или НЕ ЗначениеЗаполнено(СостояниеТС)
			Или СостояниеТС = Справочники.упСостоянияТС.Свободно 
		Тогда
				
			Если НЕ ЗначениеЗаполнено(ДатаИзмененияСостояния) Тогда
				ДатаИзмененияСостояния = ТекущаяДатаСеанса();	
			КонецЕсли;
						
			ДвижениеСостояниеТС = ДвиженияСостоянияТС.Добавить();
			ДвижениеСостояниеТС.Период = ДатаИзмененияСостояния;
			ДвижениеСостояниеТС.ТранспортноеСредство = ТранспортноеСредство;
			ДвижениеСостояниеТС.СостояниеТС = НовоеСостояниеТС;
			ИзменитьПериод = Ложь;
			
		КонецЕсли;
	КонецЕсли;	
		
	Если Истина
		И Статус = Перечисления.упСтатусыРейса.Выполнен 
		И Реквизиты.ФактическиеПараметры.Свойство("ОкончаниеВыполнения", ДатаИзмененияСостояния) 
	Тогда
			
		ИзменитьПериод = Истина;
			
		сткСостояниеТС = упУчетТранспортныхСредств.СостояниеТранспортногоСредства(ТранспортноеСредство, ДатаИзмененияСостояния);
		СостояниеТС = сткСостояниеТС.СостояниеТС;
		ПериодИзмененияТС = сткСостояниеТС.Период;
		НовоеСостояниеТС = Справочники.упСостоянияТС.Свободно;
		ПерерывМеждуРейсами	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "ПерерывМеждуРейсами");
	
		Если Истина
			И ЗначениеЗаполнено(СостояниеТС)
			И СостояниеТС <> Справочники.упСостоянияТС.Свободно 
		Тогда 
			ДвижениеСостояниеТС = ДвиженияСостоянияТС.Добавить();
			ДвижениеСостояниеТС.Период = ДатаИзмененияСостояния + ПерерывМеждуРейсами;
			ДвижениеСостояниеТС.ТранспортноеСредство = ТранспортноеСредство;
			ДвижениеСостояниеТС.СостояниеТС = НовоеСостояниеТС;
			ИзменитьПериод = Ложь;
		КонецЕсли;	
	КонецЕсли;
		
КонецПроцедуры	

// Процедура - формирует движения по регистру "упЗаданияКПланированию".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;

	ДвиженияЗаданияНаПеревозку = Движения.упЗаданияКПланированию;
	ДвиженияЗаданияНаПеревозку.Записывать = Ложь;
	
	Если Статус = Перечисления.упСтатусыРейса.Новый Тогда
		
		ДвиженияЗаданияНаПеревозку.Записывать = Истина;
		ДополнительныеСвойства.КонтролироватьЗадания	= Истина;										
		
		Для каждого текСтрока Из Реквизиты.Задания Цикл 
			ДвижениеЗадание = ДвиженияЗаданияНаПеревозку.Добавить();
			ДвижениеЗадание.Задание = текСтрока.Задание;
			ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеЗадание.Период = Реквизиты.Дата;
			ДвижениеЗадание.ГрузовоеМесто = текСтрока.ГрузовоеМесто;
			ДвижениеЗадание.Тара = текСтрока.Тара;
			ДвижениеЗадание.КоличествоКПланированию = текСтрока.КоличествоГрузов;
			ДвижениеЗадание.КоличествоКВыполнению = 0;
			ДвижениеЗадание.КоличествоНеПодтверждено = 0;
			ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = 0;
		КонецЦикла;
		
	ИначеЕсли Ложь
		Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС
		Или Статус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС
		Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
		Или Статус = Перечисления.упСтатусыРейса.КВыполнению 
		Или Статус = Перечисления.упСтатусыРейса.Выполняется
		Или Статус = Перечисления.упСтатусыРейса.Выполнен
		Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
		Или Статус = Перечисления.упСтатусыРейса.Завершен Тогда 				
		ДвиженияЗаданияНаПеревозку.Записывать = Истина;
		ДополнительныеСвойства.КонтролироватьЗадания	= Истина;
		
		
		Для каждого текСтрока Из Реквизиты.Задания Цикл 
			ДвижениеЗадание = ДвиженияЗаданияНаПеревозку.Добавить();
			ДвижениеЗадание.Задание = текСтрока.Задание;
			ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеЗадание.Период = Реквизиты.Дата;
			ДвижениеЗадание.ГрузовоеМесто = текСтрока.ГрузовоеМесто;
			ДвижениеЗадание.Тара = текСтрока.Тара;
			ДвижениеЗадание.КоличествоКПланированию = текСтрока.КоличествоГрузов;
			ДвижениеЗадание.КоличествоКВыполнению = 0;
			ДвижениеЗадание.КоличествоНеПодтверждено = текСтрока.КоличествоГрузов;
			
			Если Ложь
				Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС
				Или Статус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС
				Или Статус = Перечисления.упСтатусыРейса.НазначеноТС Тогда
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = 0;
			Иначе	 
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = текСтрока.КоличествоГрузов;
			КонецЕсли;
			
		КонецЦикла;
				
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Отменен Тогда 
		ДвиженияЗаданияНаПеревозку.Записывать = Истина;
		ДополнительныеСвойства.КонтролироватьЗадания	= Истина;										
	КонецЕсли;
	
	Если Истина
		И ДополнительныеСвойства.СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе 
		И (Ложь
		   Или Статус = Перечисления.упСтатусыРейса.Выполнен 
		   Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
		   Или Статус = Перечисления.упСтатусыРейса.Завершен) 
	Тогда
		Задания = ДополнительныеСвойства.Задания;
		Если Задания.Количество() Тогда
			ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Истина);				
			
			Для каждого текСтрока Из Задания Цикл
				ДвижениеЗадание = ДвиженияЗаданияНаПеревозку.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеЗадание, текСтрока);
				ДвижениеЗадание.Период = Реквизиты.Дата;			
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры	

// Процедура - формирует движения по регистру "упМаршрутПоРейсу", "упРаботыПоРейсу".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьМаршрутРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Реквизиты.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами И Не ДополнительныеСвойства.Маршрут = Неопределено И ДополнительныеСвойства.Маршрут.Количество() > 2 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Операции с контейнером допустимы не более чем в двух точкам маршрута.",,,, Отказ);
		Возврат;
	КонецЕсли; 
	
	Статус 			= ДополнительныеСвойства.Статус;
	ТекущийСтатус	= ?(ДополнительныеСвойства.Свойство("ТекущийСтатус"), ДополнительныеСвойства.ТекущийСтатус, Статус);
	
	ДвиженияМаршрутПоРейсу = Движения.упМаршрутПоРейсу;
	ДвиженияМаршрутПоРейсу.Записывать = Ложь;
	ДвиженияРаботыПоРейсу = Движения.упРаботыПоРейсу;
	ДвиженияРаботыПоРейсу.Записывать  = Ложь;
	
	Если Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		
		ДвиженияМаршрутПоРейсу.Записывать = Истина;									
		ДвиженияРаботыПоРейсу.Записывать  = Истина;
		
	ИначеЕсли упРаботаСоСтатусами.ДокументРейсМожетИзменятьМаршрут(Ссылка, Реквизиты.Дата, ТекущийСтатус) Тогда
		
		Маршрут = Неопределено;
		Работы  = Неопределено;
		КоэффициентРасчетаВремениВПути = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "КоэффициентРасчетаВремениВПути", 0);
		
		Если ДополнительныеСвойства.Свойство("ИсключенныеЗадания") Тогда
			ДвиженияМаршрутПоРейсу.Записывать = Истина;									
			ДвиженияРаботыПоРейсу.Записывать  = Истина;
			
			ОбработатьИсключениеЗаданий(Маршрут, Работы, ДополнительныеСвойства.ИсключенныеЗадания, Ссылка, ДополнительныеСвойства.ПараметрыВводаИнформацииОГрузовыхМестах, КоэффициентРасчетаВремениВПути);
			
		ИначеЕсли ДополнительныеСвойства.Свойство("ДобавленныеЗадания") Тогда
			ДвиженияМаршрутПоРейсу.Записывать = Истина;									
			ДвиженияРаботыПоРейсу.Записывать  = Истина;
			
			ОбработатьДобавлениеЗаданий(Маршрут, Работы, ДополнительныеСвойства, Ссылка, КоэффициентРасчетаВремениВПути);
			
		ИначеЕсли ДополнительныеСвойства.Свойство("ПересчетМаршрута") Тогда	
			ДвиженияМаршрутПоРейсу.Записывать = Истина;	
			ДвиженияРаботыПоРейсу.Записывать  = Истина;
			
			ОбработатьПересчетМаршрута(Маршрут, Работы, ДополнительныеСвойства, Ссылка, КоэффициентРасчетаВремениВПути);
			
		ИначеЕсли ДополнительныеСвойства.Свойство("ОбновитьРаботыПоЗаданию") Тогда
			ДвиженияМаршрутПоРейсу.Записывать = Истина;	
			ДвиженияРаботыПоРейсу.Записывать  = Истина;
			
			ОбработатьОбновлениеРаботПоЗаданию(Маршрут, Работы, ДополнительныеСвойства, Ссылка, КоэффициентРасчетаВремениВПути);
			
		ИначеЕсли ДополнительныеСвойства.Свойство("МаршрутРаботыПодготовлены") И ДополнительныеСвойства.МаршрутРаботыПодготовлены Тогда
			Если ДополнительныеСвойства.МодифицированМаршрут Тогда
				ДвиженияМаршрутПоРейсу.Записывать = Истина;									
				Маршрут = ДополнительныеСвойства.Маршрут;
				
				//УдалитьЗависшихПредшественников(Маршрут);
			КонецЕсли;
			
			Если ДополнительныеСвойства.МодифицированыРаботы Тогда
				ДвиженияРаботыПоРейсу.Записывать  = Истина;
				Работы  = ДополнительныеСвойства.Работы;
			КонецЕсли; 
			
		ИначеЕсли ДополнительныеСвойства.Свойство("ОбновитьПоЗаданиям") Тогда
			ДвиженияМаршрутПоРейсу.Записывать = Истина;									
			ДвиженияРаботыПоРейсу.Записывать  = Истина;
			
			СформироватьМаршрутРаботы(Маршрут, Работы, Реквизиты, ДополнительныеСвойства, Ссылка);
			
		КонецЕсли;
		
		Если Маршрут <> Неопределено Тогда
			Для каждого текСтрока Из Маршрут Цикл 
				Движение					= ДвиженияМаршрутПоРейсу.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, текСтрока);
				Движение.Рейс			= Ссылка;
				Движение.Период			= Реквизиты.Дата;
				
				Если Работы = Неопределено Тогда
					текПлановоеВремяНачалаРабот =  текСтрока.ПлановоеПрибытие;
				Иначе	
					мсвСтрокиРаботПоТочке = Работы.НайтиСтроки(Новый Структура("Идентификатор",текСтрока.Идентификатор));
					
					Если мсвСтрокиРаботПоТочке.Количество() > 0 Тогда
						текПлановоеВремяНачалаРабот =  мсвСтрокиРаботПоТочке[0].ПлановоеВремяНачалаРабот;	
						
						Для каждого текРабота Из мсвСтрокиРаботПоТочке Цикл
							Если текРабота.ПлановоеВремяНачалаРабот < текПлановоеВремяНачалаРабот Тогда
								текПлановоеВремяНачалаРабот = текРабота.ПлановоеВремяНачалаРабот;
							КонецЕсли;
						КонецЦикла;
					Иначе
						текПлановоеВремяНачалаРабот = текСтрока.ПлановоеПрибытие;
					КонецЕсли;
					текПлановоеВремяНачалаРабот = Макс(текПлановоеВремяНачалаРабот,  текСтрока.ПлановоеПрибытие);
				КонецЕсли; 
				
				Движение.НачалоРаботПлан = текПлановоеВремяНачалаРабот;
			КонецЦикла;
		КонецЕсли; 
		
		Если Работы <> Неопределено Тогда
			Для каждого текСтрока Из Работы Цикл 
				Движение = ДвиженияРаботыПоРейсу.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, текСтрока);
				Движение.Рейс			= Ссылка;
				Движение.Период			= Реквизиты.Дата;
			КонецЦикла;
		КонецЕсли; 
		
		Если Не (Работы = Неопределено И Маршрут = Неопределено) Тогда
			Если Работы = Неопределено Тогда
				Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
					Работы = СоздатьТаблицуРаботы();
				Иначе	
					Работы = ПолучитьТекущиеРаботыРейса(Ссылка);
				КонецЕсли;
			КонецЕсли; 
			Если Маршрут = Неопределено Тогда
			    Маршрут = ПолучитьТекущийМаршрутРейса(Ссылка);
			КонецЕсли; 
		КонецЕсли; 
		
		ДополнительныеСвойства.Вставить("Маршрут", Маршрут);
		ДополнительныеСвойства.Вставить("Работы", Работы);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упПеревозчикПоРейсу".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеПеревозчикПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	ДвиженияПеревозчикПоРейсу = Движения.упПеревозчикПоРейсу;
	
	Если Истина
		И Не ДополнительныеСвойства.РейсПодтвержден 
		И ДополнительныеСвойства.Свойство("ИзмененЭкипаж") 
		И (Ложь
		   Или Статус = Перечисления.упСтатусыРейса.Новый
		   Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС
		   Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
		   Или Статус = Перечисления.упСтатусыРейса.КВыполнению
		   Или Истина
		   	  И Статус = Перечисления.упСтатусыРейса.Выполнен 
			  И ДополнительныеСвойства.СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе)
	Тогда
		ДвиженияПеревозчикПоРейсу.Записывать = Истина;
		
		сткЭкипаж = ДополнительныеСвойства.ИзмененЭкипаж;
		Если ЗначениеЗаполнено(сткЭкипаж.ТипТС) Или ЗначениеЗаполнено(сткЭкипаж.Перевозчик) Тогда
			Движение = ДвиженияПеревозчикПоРейсу.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, сткЭкипаж);
			Движение.Рейс   = Реквизиты.Рейс;
			Движение.Период = Реквизиты.Дата;													
		КонецЕсли; 
		
		// Если назначили собственное транспортное средство, 
		//   следует пересчитать статус рейса и назначить ему статус "Назначено ТС".
		Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс") И ЗначениеЗаполнено(сткЭкипаж.ТранспортноеСредство) И Не Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда
			ДополнительныеСвойства.РассчитыватьСтатус = Истина;
		КонецЕсли; 
		
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Отменен Тогда	
		ДвиженияПеревозчикПоРейсу.Записывать	= Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упДополнительныеСотрудникиРейса".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияДополнительныеСотрудникиРейса(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	ДвиженияДополнительныеСотрудникиРейса = Движения.упДополнительныеСотрудникиРейса;
	ДвиженияДополнительныеСотрудникиРейса.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ДополнительныеСотрудники") И Не Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		
		тзДополнительныеСотрудники = ДополнительныеСвойства.ДополнительныеСотрудники;
		Для каждого текСотрудник Из тзДополнительныеСотрудники Цикл
			Движение = ДвиженияДополнительныеСотрудникиРейса.Добавить();
			Движение.Рейс   = Реквизиты.Рейс;
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСотрудник);
		КонецЦикла; 

	КонецЕсли;	
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упРасходы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьПлановыеРасходыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	упРаспределениеРасходов.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
		
КонецПроцедуры

// Процедура - формирует движения по регистру "упПлановыеПараметрыРейса".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если Статус = Перечисления.упСтатусыРейса.Новый 
		Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС 
		Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
		Или Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда	
		
		Если НЕ (ДополнительныеСвойства.Маршрут = Неопределено Или ДополнительныеСвойства.Работы = Неопределено) Тогда
        		Маршрут = ДополнительныеСвойства.Маршрут;
			Работы = ДополнительныеСвойства.Работы;
		Иначе
			Возврат;
		КонецЕсли;

		Если ДополнительныеСвойства.Свойство("ТЧЗадания") Тогда 	
			тбзЗадания = ДополнительныеСвойства.ТЧЗадания.Скопировать(,"Задание");
		ИначеЕсли Реквизиты.Свойство("Задания") Тогда
			тбзЗадания = Реквизиты.Задания.Выгрузить(,"Задание");				
		КонецЕсли;
		
		// определяется структура для хранения плановых параметров рейса.
			
		сткПлановыеПараметры = Реквизиты.ПлановыеПараметры;
			
		ЗаполнитьПлановыеПараметрыРейса(Ссылка, тбзЗадания, Маршрут, Работы, ДополнительныеСвойства.ВедетсяВалютныйУчет, сткПлановыеПараметры, Реквизиты.Дата,Реквизиты.ВидЗоныДляФормированияПредставленияРейса, Реквизиты.ВариантФормированияМаршрута, ДополнительныеСвойства); 
					
		Если сткПлановыеПараметры.Количество() Тогда
			НаборЗаписей = РегистрыСведений.упПлановыеПараметрыРейса.СоздатьНаборЗаписей(); 
			
			НаборЗаписей.Отбор.Рейс.Установить(Ссылка); 
			НоваяЗапись = НаборЗаписей.Добавить(); 
			НоваяЗапись.Рейс = Ссылка;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, сткПлановыеПараметры);
			
			НаборЗаписей.Записать(); 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - подготавливает структуру для формирования записей по регистру "упФактическиеПараметрыРейса".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьФактическиеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично 
		Или Статус = Перечисления.упСтатусыРейса.Выполнен 
		Или Статус = Перечисления.упСтатусыРейса.Завершен Тогда	

		Рейс = Реквизиты.Рейс; 
		
		// определяется структура для хранения фактических параметров рейса.
		сткФактическиеПараметры = Реквизиты.ФактическиеПараметры;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт,
		|	упМаршрутПоРейсуСрезПоследних.УбытиеФакт,
		|	упМаршрутПоРейсуСрезПоследних.Адрес,
		|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочкиФакт,
		|	упМаршрутПоРейсуСрезПоследних.Идентификатор,
		|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
		|ГДЕ
		|	упМаршрутПоРейсуСрезПоследних.Пройдена
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтрокаНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРаботыПоРейсуСрезПоследних.Идентификатор,
		|	упРаботыПоРейсуСрезПоследних.Операция,
		|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов,
		|	упРаботыПоРейсуСрезПоследних.Масса,
		|	упРаботыПоРейсуСрезПоследних.Объем,
		|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто,
		|	упРаботыПоРейсуСрезПоследних.Задание,
		|	упРаботыПоРейсуСрезПоследних.Тара
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
		|ГДЕ
		|	упРаботыПоРейсуСрезПоследних.Выполнена
		|	И упРаботыПоРейсуСрезПоследних.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))";
		
		ИндексЗапросаМаршрут = 0;
		ИндексЗапросаРаботы = 1;
		
		Запрос.УстановитьПараметр("Рейс", Рейс);
		мсвРезультаты = Запрос.ВыполнитьПакет();
				
		тбзРаботы = мсвРезультаты[ИндексЗапросаРаботы].Выгрузить();
		тбзМаршрут = мсвРезультаты[ИндексЗапросаМаршрут].Выгрузить();
			
		// Представление маршрута.
		сткФактическиеПараметры.Вставить("ПредставлениеМаршрута", упРейс.ПолучитьПредставлениеДляРейса(тбзМаршрут, Реквизиты.ВидЗоныДляФормированияПредставленияРейса));
								
		// Зоны погрузки/разгрузки.
		Если ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон") Тогда
			ВыводитьОшибкиПриОпределенииЗонТолькоВЖР = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ВыводитьОшибкиПриОпределенииЗонТолькоВЖР", Ложь);
			Если ВыводитьОшибкиПриОпределенииЗонТолькоВЖР Тогда
				сткФактическиеПараметры.Вставить("ВыводитьОшибкиПриОпределенииЗонТолькоВЖР", ВыводитьОшибкиПриОпределенииЗонТолькоВЖР);
			КонецЕсли;
			ОпределитьЗоныПогрузкиРазгрузки(тбзРаботы, тбзМаршрут, сткФактическиеПараметры, Реквизиты.ВариантФормированияМаршрута);
		КонецЕсли;	
		
		МассаМакс = 0;
		ОбъемМакс = 0;
		КоличествоМестМакс = 0;
		КоличествоЗагрузочныхМетровМакс = 0;
		КоличествоГрузовыхМестМакс = 0;
		Расстояние = 0;
		РасстояниеПоДаннымТрекеров = 0;
		НулевойПробег = 0;
		текВремяВПутиСекунды = 0;
		текВремяПрибытияВТекущуюТочку = Неопределено;
		текВремяУбытияИзПредыдущейТочки = Неопределено;
		МинимальноеВремяНачала = '39991231';
		МаксимальноеОкончаниеВыполнения = '00010101';
		НеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
		
		Если НеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			соотПоказателиПоТочкам = упРасчетПоказателейЗагрузки.РассчитатьЗагрузкуВТочкахМаршрутаУправлениеТранспортом(тбзМаршрут.Скопировать(, "СтрокаНомер, Идентификатор"), тбзРаботы.Скопировать(,"Идентификатор, Операция, Масса, Объем"));
		Иначе	
			соотПоказателиПоТочкам = упРасчетПоказателейЗагрузки.РассчитатьЗагрузкуВТочкахМаршрута(тбзМаршрут.Скопировать(, "СтрокаНомер, Идентификатор"), тбзРаботы.Скопировать(,"Идентификатор, Операция, Задание, Тара, ГрузовоеМесто, КоличествоГрузов"), ДополнительныеСвойства);
		КонецЕсли; 
		
		КоличествоГрузовДоПрибытияВТочку = 0;
		МассаДоПрибытияВТочку = 0;
		ОбъемДоПрибытияВТочку = 0;
		
		РассчитыватьВремя = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ФактическиеПараметрыРассчитыватьВремя", Истина);
		РассчитыватьРасстояние = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ФактическиеПараметрыРассчитыватьРасстояние", Истина);
				
		Для каждого СтрокаТочка Из тбзМаршрут Цикл
			
			// Показатели загрузки.
			сткПоказатели = соотПоказателиПоТочкам.Получить(Строка(СтрокаТочка.Идентификатор));
			МассаМакс = Макс(МассаМакс, сткПоказатели.Масса);
			ОбъемМакс = Макс(ОбъемМакс, сткПоказатели.Объем);
			КоличествоМестМакс = Макс(КоличествоМестМакс, сткПоказатели.КоличествоМест);
			КоличествоЗагрузочныхМетровМакс = Макс(КоличествоЗагрузочныхМетровМакс, сткПоказатели.КоличествоЗагрузочныхМетров);
			КоличествоГрузовыхМестМакс = Макс(КоличествоГрузовыхМестМакс, сткПоказатели.КоличествоГрузовыхМест);
			
			// Время в пути.
			текВремяПрибытияВТекущуюТочку	= СтрокаТочка.ПрибытиеФакт;
					
			Если Истина
				И РассчитыватьВремя
				И ЗначениеЗаполнено(текВремяПрибытияВТекущуюТочку)
				И ЗначениеЗаполнено(текВремяУбытияИзПредыдущейТочки) 
			Тогда
				текВремяДоПредыдущейТочкиСекунды = текВремяПрибытияВТекущуюТочку - текВремяУбытияИзПредыдущейТочки;
				текВремяВПутиСекунды = текВремяВПутиСекунды + текВремяДоПредыдущейТочкиСекунды
			КонецЕсли;
					
			текВремяУбытияИзПредыдущейТочки = СтрокаТочка.УбытиеФакт;

			// Время начала и окончания.
			Если СтрокаТочка.ПрибытиеФакт < МинимальноеВремяНачала Тогда
				МинимальноеВремяНачала = СтрокаТочка.ПрибытиеФакт;
			КонецЕсли;
			
			Если СтрокаТочка.УбытиеФакт > МаксимальноеОкончаниеВыполнения Тогда
				МаксимальноеОкончаниеВыполнения = СтрокаТочка.УбытиеФакт;
			КонецЕсли;
			
			ЭтоНулевойПробег = Ложь;
			
			Если НеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
				Если МассаДоПрибытияВТочку = 0 И ОбъемДоПрибытияВТочку = 0 Тогда
					ЭтоНулевойПробег = Истина;
				КонецЕсли;
			Иначе	
				Если КоличествоГрузовДоПрибытияВТочку = 0 Тогда
					ЭтоНулевойПробег = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ЭтоНулевойПробег Тогда
				НулевойПробег = НулевойПробег + СтрокаТочка.РасстояниеОтПредыдущейТочкиФакт; 	
			КонецЕсли;
			
			КоличествоГрузовДоПрибытияВТочку = сткПоказатели.КоличествоГрузов;
			МассаДоПрибытияВТочку = сткПоказатели.Масса;
			ОбъемДоПрибытияВТочку = сткПоказатели.Объем;
			
			// Расстояние.
			Если РассчитыватьРасстояние Тогда
				Расстояние = Расстояние + СтрокаТочка.РасстояниеОтПредыдущейТочкиФакт;
			КонецЕсли;
					
		КонецЦикла; 
		
		сткФактическиеПараметры.Вставить("Масса", МассаМакс);
		сткФактическиеПараметры.Вставить("Объем", ОбъемМакс);
		сткФактическиеПараметры.Вставить("КоличествоМест", КоличествоМестМакс);
		сткФактическиеПараметры.Вставить("КоличествоЗагрузочныхМетров", КоличествоЗагрузочныхМетровМакс);
		сткФактическиеПараметры.Вставить("КоличествоГрузовыхМест", КоличествоГрузовыхМестМакс);
		сткФактическиеПараметры.Вставить("НачалоВыполнения", МинимальноеВремяНачала);
		сткФактическиеПараметры.Вставить("ОкончаниеВыполнения", МаксимальноеОкончаниеВыполнения);
		сткФактическиеПараметры.Вставить("ВремяВПути", Окр(текВремяВПутиСекунды / 3600, 2));
		сткФактическиеПараметры.Вставить("Расстояние", Расстояние);
		сткФактическиеПараметры.Вставить("РасстояниеПоДаннымТрекеров", РасстояниеПоДаннымТрекеров);
		сткФактическиеПараметры.Вставить("НулевойПробег", НулевойПробег);
		
		// Время.
		Если ЗначениеЗаполнено(сткФактическиеПараметры.НачалоВыполнения) 
			И ЗначениеЗаполнено(сткФактическиеПараметры.ОкончаниеВыполнения) Тогда
			текВремяЧасы	= Окр((сткФактическиеПараметры.ОкончаниеВыполнения - сткФактическиеПараметры.НачалоВыполнения) / 3600, 2);
			сткФактическиеПараметры.Вставить("Время", текВремяЧасы);
			
			Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг") Тогда
				
				НачалоПериода = сткФактическиеПараметры.НачалоВыполнения;
				ОкончаниеПериода = сткФактическиеПараметры.ОкончаниеВыполнения;
				ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЕСТЬNULL(ПеревозчикПоРейсуСП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС,
				|	ПеревозчикПоРейсуСП.Рейс КАК Рейс
				|ИЗ
				|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК ПеревозчикПоРейсуСП";
				
				Запрос.УстановитьПараметр("Рейс", Рейс);
				
				Выборка = Запрос.Выполнить().Выбрать();					
				Если Выборка.Следующий() Тогда
					ТранспортноеСредство = Выборка.ТС;				
				КонецЕсли;		
				
				Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(ТранспортноеСредство) Тогда
					ТекущееРасстояние = упМониторинг.упВычислитьРасстояниеПоДаннымТрекеров(НачалоПериода,ОкончаниеПериода,ТранспортноеСредство);
					// Значение расстояния получаем в метрах делим на 1000 для получения км.
					РасстояниеПоДаннымТрекеров = ?(ТекущееРасстояние=0,0,ТекущееРасстояние/1000);
					сткФактическиеПараметры.Вставить("РасстояниеПоДаннымТрекеров", РасстояниеПоДаннымТрекеров);
				КонецЕсли;    
			КонецЕсли;
		КонецЕсли;
		
		СуммаИнкассации = 0;
		Если ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") Тогда
			сткСуммыИнкассации = упРейс.СуммыИнкассацииРейса(Рейс, Реквизиты.Дата, ДополнительныеСвойства.ВедетсяВалютныйУчет);
			СуммаИнкассации = сткСуммыИнкассации.Фактическая;	
		КонецЕсли;
		
		сткФактическиеПараметры.Вставить("СуммаИнкассации", СуммаИнкассации);
		
		Если сткФактическиеПараметры.Количество() Тогда
			УстановитьФактическиеПараметрыРейса(сткФактическиеПараметры, Рейс);
		КонецЕсли;
	Иначе
		упРейс.СброситьФактическиеПараметрыРейса(Рейс);	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует записи по регистру "упВсегоРасходыПоРейсу".
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеВсегоРасходыПоРейсу(Рейс, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	сткПлановыеРасходыПоРейсу =	упРаспределениеРасходов.ПлановыеРасходыПоРейсу(Рейс);
	
	текЗапись = РегистрыСведений.упВсегоРасходыПоРейсу.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(текЗапись, сткПлановыеРасходыПоРейсу);
	текЗапись.Рейс		=  Рейс;
	текЗапись.Записать();

КонецПроцедуры

// Процедура - формирует движения по регистру "упНепереданныеДокументы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	СпособУчетаВозвращенныхДокументов = ДополнительныеСвойства.СпособУчетаВозвращенныхДокументов;
	СпособРегистрацииЗадолженностиПоДокументамРейса = ДополнительныеСвойства.СпособРегистрацииЗадолженностиПоДокументамРейса;
	ДвиженияНепереданныеДокументы = Движения.упНепереданныеДокументы;
	ДвиженияНепереданныеДокументы.Записывать = Ложь;
		
	Если Ложь
		Или Не ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса")
		Или СпособРегистрацииЗадолженностиПоДокументамРейса = Перечисления.упСпособыРегистрацииЗадолженностиПоДокументамРейса.ОтдельнымДокументом
	Тогда
		ДвиженияНепереданныеДокументы.Записывать = Истина;
		Возврат;
	КонецЕсли; 
	
	Если Статус = Перечисления.упСтатусыРейса.КВыполнению Или Статус = Перечисления.упСтатусыРейса.Выполняется Тогда
		
		ДвиженияНепереданныеДокументы.Записывать = Истина;
		

		ЗаполнитьНепереданныеДокументыПоРейсу(ДвиженияНепереданныеДокументы, Ссылка, СпособУчетаВозвращенныхДокументов, Отказ);
		Если НЕ Отказ Тогда
			Для каждого ДвижениеНепереданныеДокументы Из ДвиженияНепереданныеДокументы Цикл
				ДвижениеНепереданныеДокументы.Рейс = Реквизиты.Рейс;
				ДвижениеНепереданныеДокументы.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеНепереданныеДокументы.Период = Реквизиты.Дата;
			КонецЦикла;
		КонецЕсли;

	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Отменен Тогда 
		ДвиженияНепереданныеДокументы.Записывать = Истина;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗаполнитьНепереданныеДокументыПоРейсу(НепереданныеДокументы, Рейс, СпособУчетаВозвращенныхДокументов, Отказ) Экспорт
	
	Если СпособУчетаВозвращенныхДокументов = Перечисления.упСпособыУчетаВозвращенныхДокументов.СТочностьюДоТиповДокументов Тогда
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упРейсЗадания.Задание,
		|	упЗаданиеНаПеревозкуГруза.Заказчик
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|		ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
		|ГДЕ
		|	упРейсЗадания.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадания.Задание КАК ЗаданиеНаПеревозку,
		|	упПакетДокументов.ТипРегламентногоДокумента,
		|	упПакетДокументов.КоличествоПолучитьОтУчастника КАК КоличествоПолучить,
		|	упПакетДокументов.КоличествоПередатьУчастнику КАК КоличествоПередать
		|ПОМЕСТИТЬ втДокументы
		|ИЗ
		|	втЗадания КАК втЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыРегламентныхДокументов.ПакетДокументов КАК упПакетДокументов
		|		ПО втЗадания.Заказчик.ПакетРегламентныхДокументов = упПакетДокументов.Ссылка
		|			И (упПакетДокументов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Заказчик))
		|			И (упПакетДокументов.ВозвратДокументовПослеИсполненияРейса)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДокументы.ЗаданиеНаПеревозку,
		|	втДокументы.ТипРегламентногоДокумента,
		|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов) КАК Операция,
		|	втДокументы.КоличествоПолучить КАК Количество
		|ИЗ
		|	втДокументы КАК втДокументы
		|ГДЕ
		|	втДокументы.КоличествоПолучить > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втДокументы.ЗаданиеНаПеревозку,
		|	втДокументы.ТипРегламентногоДокумента,
		|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов),
		|	втДокументы.КоличествоПередать
		|ИЗ
		|	втДокументы КАК втДокументы
		|ГДЕ
		|	втДокументы.КоличествоПередать > 0";
		Запрос.УстановитьПараметр("Ссылка", Рейс);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = НепереданныеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КонецЦикла; 
		КонецЕсли;
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК ДокументОснование,
		|	упЗаданиеНаПеревозкуГруза.Получатель КАК Получатель,
		|	упШаблоныПакетовПечатныхФормМакетыПФ.КоличествоВернуть КАК Количество,
		|	упШаблоныПакетовПечатныхФормМакетыПФ.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	упТипыРегламентныхДокументов.СпособОпределенияДатыЭкземпляра КАК СпособОпределенияДатыЭкземпляра,
		|	упТипыРегламентныхДокументов.СпособОпределенияНомераЭкземпляра КАК СпособОпределенияНомераЭкземпляра,
		|	упТипыРегламентныхДокументов.ФормулаДата КАК ФормулаДата,
		|	упТипыРегламентныхДокументов.ФормулаНомер КАК ФормулаНомер,
		|	ВЫБОР
		|		КОГДА упЗаданиеНаПеревозкуГруза.НомерКИС = """"
		|			ТОГДА упЗаявкаНаПеревозкуГруза.НомерКИС
		|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.НомерКИС
		|	КОНЕЦ КАК НомерКИС,
		|	ВЫБОР
		|		КОГДА упЗаданиеНаПеревозкуГруза.НомерКИС = """"
		|			ТОГДА упЗаявкаНаПеревозкуГруза.ДатаКИС
		|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.ДатаКИС
		|	КОНЕЦ КАК ДатаКИС,
		|	упЗаданиеНаПеревозкуГруза.Номер КАК Номер,
		|	упЗаданиеНаПеревозкуГруза.Дата КАК Дата
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры КАК упПартнеры
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упШаблоныПакетовПечатныхФорм.МакетыПФ КАК упШаблоныПакетовПечатныхФормМакетыПФ
		|					ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыРегламентныхДокументов КАК упТипыРегламентныхДокументов
		|					ПО упШаблоныПакетовПечатныхФормМакетыПФ.ТипРегламентногоДокумента = упТипыРегламентныхДокументов.Ссылка
		|				ПО упПартнеры.ПакетПечатныхФормЗадание = упШаблоныПакетовПечатныхФормМакетыПФ.Ссылка
		|					И (упШаблоныПакетовПечатныхФормМакетыПФ.КоличествоВернуть > 0)
		|			ПО упЗаданиеНаПеревозкуГруза.Получатель = упПартнеры.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
		|			ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГруза.Ссылка
		|		ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
		|ГДЕ
		|	упРейсЗадания.Ссылка = &Рейс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПеревозчикПоРейсуСрезПоследних.Рейс КАК ДокументОснование,
		|	упШаблоныПакетовПечатныхФормМакетыПФ.КоличествоВернуть КАК Количество,
		|	упШаблоныПакетовПечатныхФормМакетыПФ.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	упТипыРегламентныхДокументов.СпособОпределенияДатыЭкземпляра КАК СпособОпределенияДатыЭкземпляра,
		|	упТипыРегламентныхДокументов.СпособОпределенияНомераЭкземпляра КАК СпособОпределенияНомераЭкземпляра,
		|	упТипыРегламентныхДокументов.ФормулаДата КАК ФормулаДата,
		|	упТипыРегламентныхДокументов.ФормулаНомер КАК ФормулаНомер,
		|	упРейс.Номер КАК Номер,
		|	упРейс.Дата КАК Дата,
		|	"""" КАК НомерКИС,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаКИС
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры КАК упПартнеры
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упШаблоныПакетовПечатныхФорм.МакетыПФ КАК упШаблоныПакетовПечатныхФормМакетыПФ
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыРегламентныхДокументов КАК упТипыРегламентныхДокументов
		|				ПО упШаблоныПакетовПечатныхФормМакетыПФ.ТипРегламентногоДокумента = упТипыРегламентныхДокументов.Ссылка
		|			ПО упПартнеры.ПакетПечатныхФормРейс = упШаблоныПакетовПечатныхФормМакетыПФ.Ссылка
		|		ПО упПеревозчикПоРейсуСрезПоследних.Перевозчик = упПартнеры.Ссылка
		|			И (упШаблоныПакетовПечатныхФормМакетыПФ.КоличествоВернуть > 0)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
		|		ПО упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка";
		Запрос.УстановитьПараметр("Рейс", Рейс);
		мсвРезультат	= Запрос.ВыполнитьПакет();
		
		сткПараметрыПодбораЭкземпляра = Новый Структура();
		сткПараметрыПодбораЭкземпляра.Вставить("ДокументОснование", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("Рейс", Рейс);
		сткПараметрыПодбораЭкземпляра.Вставить("Дата", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("Номер", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("ДатаКИС", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("НомерКИС", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("СпособОпределенияДатыЭкземпляра", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("СпособОпределенияНомераЭкземпляра", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("ФормулаДата", Неопределено);
		сткПараметрыПодбораЭкземпляра.Вставить("ФормулаНомер", Неопределено);
		
		Выборка = мсвРезультат[0].Выбрать();
		Ошибки = Неопределено;
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(сткПараметрыПодбораЭкземпляра, Выборка);
			ЭкземплярДокумента	= Справочники.упЭкземплярыДокументов.ПодобратьЭкземплярДокумента(сткПараметрыПодбораЭкземпляра, Ошибки);
			
			НоваяСтрока = НепереданныеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);	
			НоваяСтрока.ЗаданиеНаПеревозку = Выборка.ДокументОснование;
			НоваяСтрока.ЭкземплярДокумента = ЭкземплярДокумента;
			
		КонецЦикла; 
		
		Выборка	= мсвРезультат[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(сткПараметрыПодбораЭкземпляра, Выборка);
			ЭкземплярДокумента	= Справочники.упЭкземплярыДокументов.ПодобратьЭкземплярДокумента(сткПараметрыПодбораЭкземпляра, Ошибки);
			
			НоваяСтрока = НепереданныеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);	
			НоваяСтрока.ЗаданиеНаПеревозку = Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка();
			НоваяСтрока.ЭкземплярДокумента = ЭкземплярДокумента;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	КонецЕсли;
КонецПроцедуры


// Процедура - формирует записи по регистру "упФактическиеПараметрыРейса".
//
// Параметры:
//  сткПараметры - Структура - Параметры.
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
Процедура УстановитьФактическиеПараметрыРейса(сткПараметры, Рейс) Экспорт
	
	текЗапись = РегистрыСведений.упФактическиеПараметрыРейса.СоздатьМенеджерЗаписи();
	текЗапись.Рейс = Рейс;
	текЗапись.Прочитать();
	Если НЕ текЗапись.Выбран() Тогда
		текЗапись.Рейс = Рейс;	
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(текЗапись, сткПараметры);
	текЗапись.Записать();
	
КонецПроцедуры

// Процедура - очищает значения записей по регистру "упФактическиеПараметрыРейса".
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
Процедура СброситьФактическиеПараметрыРейса(Рейс) Экспорт
	
	текЗапись = РегистрыСведений.упФактическиеПараметрыРейса.СоздатьМенеджерЗаписи();
	текЗапись.Рейс = Рейс;
	текЗапись.Прочитать();
	Если текЗапись.Выбран() Тогда
		текЗапись.Рейс = Рейс;	
		текЗапись.Время = 0;
		текЗапись.ВремяВПути = 0;
		текЗапись.ЗонаПогрузки = Справочники.упГеографическиеЗоны.ПустаяСсылка();
		текЗапись.ЗонаРазгрузки = Справочники.упГеографическиеЗоны.ПустаяСсылка();
		текЗапись.КоличествоЗагрузочныхМетров = 0;
		текЗапись.КоличествоМест = 0;
		текЗапись.Масса = 0;
		текЗапись.НачалоВыполнения = '00010101';
		текЗапись.Объем = 0;
		текЗапись.ОкончаниеВыполнения = '00010101';
		текЗапись.ПредставлениеМаршрута = "";
		текЗапись.Пробег = 0;
		текЗапись.Расстояние = 0;
		текЗапись.СуммаИнкассации = 0;
		текЗапись.Записать();
	КонецЕсли;
			
КонецПроцедуры

// Процедура - Перезаполнить плановую дату окончания рейса
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс	 - рейс.
//
Процедура ПерезаполнитьПлановуюДатуОкончанияРейса(Рейс) Экспорт
	
	текПлановоеОкончаниеВыполнения = ПлановаяДатаОкончанияРейса(Рейс);
	
	текЗапись = РегистрыСведений.упПлановыеПараметрыРейса.СоздатьМенеджерЗаписи();
	текЗапись.Рейс = Рейс;
	текЗапись.Прочитать();
	Если текЗапись.Выбран() Тогда
		текЗапись.ПлановоеОкончаниеВыполнения = текПлановоеОкончаниеВыполнения;
		текЗапись.Записать();
	КонецЕсли;
			
КонецПроцедуры

// Функция - Плановая дата окончания рейса.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на элемент.
// 
// Возвращаемое значение:
//  ДатаВремя - Период.
//
Функция ПлановаяДатаОкончанияРейса(Рейс) Экспорт
	
	текРезультат = Дата(1,1,1);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ
	|	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер УБЫВ";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		
		текРезультат = текВыборка.ПлановоеУбытие;
		
	КонецЕсли;
		
	Возврат текРезультат;
		
КонецФункции

// Процедура формирует записи по регистру "упФактическиеМестоположенияТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//  Маршрут - ТаблицаЗначений - Таблица данных.
//
Процедура _СформироватьДвиженияМестоположенияТранспортныхСредств(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ, Маршрут = Неопределено) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки.
		Ссылка = Документы.упРейс.ПустаяСсылка();
		Текущий_ДокументОбъект = Ссылка.ПолучитьОбъект();
		Движения = Текущий_ДокументОбъект.Движения;
		Реквизиты = Ссылка;
	КонецЕсли;
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	Если Истина
		И ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство) 
		И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") 
	Тогда 
		Статус = ДополнительныеСвойства.Статус;
		Если Ложь
			Или Статус = Перечисления.упСтатусыРейса.Новый 
			Или Статус = Перечисления.упСтатусыРейса.КПланированиюТС 
			Или Статус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС
			Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
			Или Статус = Перечисления.упСтатусыРейса.КВыполнению 
		Тогда	
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
			Если Маршрут = Неопределено Тогда
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	упМаршрутПоРейсуТ.*
				|ПОМЕСТИТЬ Адреса
				|ИЗ
				|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК упМаршрутПоРейсуТ
				|ГДЕ
				|	упМаршрутПоРейсуТ.Рейс = &Рейс
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	*, ЛОЖЬ КАК Отменена
				|ИЗ
				|	Адреса КАК Адреса";
				Маршрут = Запрос.Выполнить().Выгрузить();
			Иначе
				Запрос.УстановитьПараметр("Маршрут", Маршрут);
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Т.Адрес КАК Адрес
				|ПОМЕСТИТЬ Адреса
				|ИЗ
				|	&Маршрут КАК Т";
				Запрос.Выполнить();
			КонецЕсли; 
			Запрос.Текст = 
		"////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Адреса.Адрес КАК Адрес,
		|	упИерархияГеографическихЗонТ.Родитель КАК Зона
		|ИЗ
		|	Адреса КАК Адреса
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			упИерархияГеографическихЗонТ.Зона КАК Зона,
		|			МАКСИМУМ(упИерархияГеографическихЗонТ.Уровень) КАК Уровень
		|		ИЗ
		|			РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонТ
		|		ГДЕ
		|			упИерархияГеографическихЗонТ.Зона В
		|					(ВЫБРАТЬ
		|						Адреса.Адрес.ГеографическаяЗона КАК ГеографическаяЗона
		|					ИЗ
		|						Адреса КАК Адреса)
		|			И упИерархияГеографическихЗонТ.Родитель.ИспользоватьДляУчетаМестоположений
		|		
		|		СГРУППИРОВАТЬ ПО
		|			упИерархияГеографическихЗонТ.Зона) КАК МаксимальныеУровни
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонТ
		|			ПО МаксимальныеУровни.Зона = упИерархияГеографическихЗонТ.Зона
		|				И МаксимальныеУровни.Уровень = упИерархияГеографическихЗонТ.Уровень
		|		ПО (Адреса.Адрес.ГеографическаяЗона = упИерархияГеографическихЗонТ.Зона)";
			УчетныеЗоныАдресов = Запрос.Выполнить().Выгрузить();
			ДвижениеЗанятостьТранспорта = Движения.упЗанятостьТранспорта;
			Для Каждого СтрокаМаршрута Из Маршрут Цикл
				УчетнаяЗона = УчетныеЗоныАдресов.Найти(СтрокаМаршрута.Адрес, "Адрес").Зона;
				Если Не ЗначениеЗаполнено(УчетнаяЗона) Тогда
					Продолжить;
				КонецЕсли; 
				СтрокаДвижения = ДвижениеЗанятостьТранспорта.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДвижения, СтрокаМаршрута);
				СтрокаДвижения.Период = Реквизиты.Дата;
				СтрокаДвижения.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
				СтрокаДвижения.Рейс = Реквизиты.Рейс;
				СтрокаДвижения.ПланАктуален = Истина;
				СтрокаДвижения.СостояниеТранспорта = Справочники.упСостоянияТС.ВРейсе;
				СтрокаДвижения.ЗонаНачала = УчетнаяЗона;
				СтрокаДвижения.ЗонаОкончания = УчетнаяЗона;
				Если СтрокаМаршрута.Отменена Тогда
					СтрокаДвижения.ВидДвижения = ВидДвиженияНакопления.Расход;
				Иначе
					СтрокаДвижения.ВидДвижения = ВидДвиженияНакопления.Приход;
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

// Выполнить списание остатков.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура _СписатьОстаткиМестоположенияТранспортныхСредств(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки.
		Ссылка = Документы.упРейс.ПустаяСсылка();
		Текущий_ДокументОбъект = Ссылка.ПолучитьОбъект();
		Движения = Текущий_ДокументОбъект.Движения;
		Реквизиты = Ссылка;
	КонецЕсли;
	Если Ложь
		Или Отказ 
		Или Не ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство) 
		Или Не ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") 
	Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	упПлановыеМестоположенияТСОстатки.*,
	|	упПлановыеМестоположенияТСОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.упПлановыеМестоположенияТС.Остатки(, Рейс = &Рейс) КАК упПлановыеМестоположенияТСОстатки
	|";
	Запрос.УстановитьПараметр("Рейс", Ссылка);
	Остатки = Запрос.Выполнить().Выгрузить();
	Остатки.Колонки.Добавить("ВидДвижения");
	Остатки.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	Остатки.Колонки.Добавить("Период");
	Остатки.ЗаполнитьЗначения(Реквизиты.Дата, "Период");
	ДвиженияМестоположения = Движения.упПлановыеМестоположенияТС;
	ДвиженияМестоположения.Записывать = Истина;
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДвиженияМестоположения, Остатки);
	ДвиженияМестоположения.Загрузить(Остатки);

КонецПроцедуры

// Процедура формирует записи по регистру "упФактическиеМестоположенияТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на объект.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты объект.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияФактическиеМестоположенияТранспорта(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Или Не ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда 
		Возврат; 
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеСвойства, "ФактическиеМестоположенияТранспорта") Тогда
		
		ФактическиеМестоположенияТранспорта = ДополнительныеСвойства.ФактическиеМестоположенияТранспорта;

		Если ЗначениеЗаполнено(ФактическиеМестоположенияТранспорта) Тогда
			
			ДвиженияМестоположения = Движения.упФактическиеМестоположенияТС;
			ДвиженияМестоположения.Записывать = Истина;
			
			Для каждого Строка Из ФактическиеМестоположенияТранспорта Цикл
				Движение				= ДвиженияМестоположения.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Строка);
				Движение.Период			= Строка.Дата;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти 

#Область ЗаявкаНаТС

// Процедура - формирует движения по регистру "упСтатусыЗаявокНаТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияСтатусЗаявкиНаТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСтатуса = Движения.упСтатусыЗаявокНаТС;
			
	Статус				= ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус	= ДополнительныеСвойства.ЗаписыватьСтатус;
	Если ЗаписыватьСтатус Тогда
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
				
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = ТекущаяДата();
		ДвижениеСтатуса.Документ    = Ссылка;
		ДвижениеСтатуса.Статус      = Статус;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодтверждениеЗаявкиНаТС

// Процедура - формирует движения по регистру "упСтатусыЗаявокНаТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияПодтвердитьЗаявкуНаТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаТС.Ссылка
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних КАК упСтатусыЗаявокНаТССрезПоследних
	|		ПО (упСтатусыЗаявокНаТССрезПоследних.Документ = упЗаявкаНаТС.Ссылка)
	|ГДЕ
	|	упЗаявкаНаТС.Рейс = &Рейс
	|	И НЕ упЗаявкаНаТС.Ссылка = &Ссылка
	|	И (НЕ упСтатусыЗаявокНаТССрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Отклонена)
	|			ИЛИ упСтатусыЗаявокНаТССрезПоследних.Статус ЕСТЬ NULL )";
	Запрос.УстановитьПараметр("Рейс", 	ДополнительныеСвойства.Рейс);
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ЗаявкаНаТС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения.упСтатусыЗаявокНаТС.Записывать = Истина;

	Пока Выборка.Следующий() Цикл
		Движение = Движения.упСтатусыЗаявокНаТС.Добавить();
		Движение.Период 	= ТекущаяДата();
		Движение.Документ 	= Выборка.Ссылка;
		Движение.Статус 	= Перечисления.упСтатусыЗаявкиНаТС.Отклонена;
	КонецЦикла;
	
	Движение = Движения.упСтатусыЗаявокНаТС.Добавить();
	Движение.Период 	= ТекущаяДатаСеанса();
	Движение.Документ 	= ДополнительныеСвойства.ЗаявкаНаТС;
	Движение.Статус 	= Перечисления.упСтатусыЗаявкиНаТС.Подтверждена;
	
	Этап = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеСвойства.ЗаявкаНаТС, "Этап");
	Если ЗначениеЗаполнено(Этап) Тогда
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упЭтапПодбораПеревозчика");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Этап);
		
		Попытка
			БлокировкаДанных.Заблокировать();
		Исключение
			Возврат;
		КонецПопытки;
		
		ЭтапОбъект = Этап.ПолучитьОбъект();
		ЭтапОбъект.ЗавершенУспешно = Истина;
		ЭтапОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОтменаПеревозкиГруза
// Процедура - формирует движения по регистру "упЗаданияКПланированию".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияОтменитьЗаданиеНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаданияНаПеревозку	= Движения.упЗаданияКПланированию;
	ДвиженияЗаданияНаПеревозку.Записывать	= Истина;
	
	Для каждого текСтрока Из Реквизиты.Задания Цикл 
		Если текСтрока.Отменено Тогда
						
			ДвижениеЗадание					= ДвиженияЗаданияНаПеревозку.Добавить();
			ДвижениеЗадание.ВидДвижения		= ВидДвиженияНакопления.Расход;
			ДвижениеЗадание.Задание			= текСтрока.Задание;
			ДвижениеЗадание.Период       	= Реквизиты.Дата;
			ДвижениеЗадание.ГрузовоеМесто 	= текСтрока.ГрузовоеМесто;
			ДвижениеЗадание.КоличествоКПланированию  = 0;
			ДвижениеЗадание.КоличествоКВыполнению    = текСтрока.КоличествоГрузов;
			ДвижениеЗадание.КоличествоНеПодтверждено = 0;
			ДвижениеЗадание.КоличествоНеПереданноеКВыполнению  = 0;
			

						
			ДвижениеЗадание					= ДвиженияЗаданияНаПеревозку.Добавить();
			ДвижениеЗадание.ВидДвижения		= ВидДвиженияНакопления.Приход;
			ДвижениеЗадание.Задание			= текСтрока.Задание;
			ДвижениеЗадание.Период       	= Реквизиты.Дата;
			ДвижениеЗадание.ГрузовоеМесто 	= текСтрока.ГрузовоеМесто;
			ДвижениеЗадание.КоличествоКПланированию  = текСтрока.КоличествоГрузов;
			ДвижениеЗадание.КоличествоКВыполнению    = текСтрока.КоличествоГрузов;
			ДвижениеЗадание.КоличествоНеПодтверждено = текСтрока.КоличествоГрузов;
			ДвижениеЗадание.КоличествоНеПереданноеКВыполнению  = текСтрока.КоличествоГрузов;

		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

// Процедура - формирует движения по регистру "упЗаявкиКВыполнению" только Приход.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвижениеОтменитьЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаданияНаПеревозку	= Движения.упЗаявкиКВыполнению;
	ДвиженияЗаданияНаПеревозку.Записывать	= Истина;
	
	Для каждого текСтрока Из Реквизиты.Задания Цикл 
		Если текСтрока.Отменено Тогда
			ДвижениеЗадание					= ДвиженияЗаданияНаПеревозку.Добавить();
			ДвижениеЗадание.ВидДвижения		= ВидДвиженияНакопления.Приход;
			текЗаявкаНаПеревозку			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрока.Задание,"ЗаявкаНаПеревозкуГруза");
			ДвижениеЗадание.Заявка			= текЗаявкаНаПеревозку;
			ДвижениеЗадание.Период       	= Реквизиты.Дата;
			ДвижениеЗадание.ГрузовоеМесто 	= текСтрока.ГрузовоеМесто;
			ДвижениеЗадание.Количество		= текСтрока.КоличествоГрузов;
		КонецЕсли;	
	КонецЦикла;	

КонецПроцедуры // СформироватьДвижениеЗаявкиКВыполнению()

// Процедура - Вызывается при отмене работ погрузки, по переданным работам погрузки,
//				определяются и отменяются соответствующие работы разгрузки.
//				Если после этого не остается основных работ(погрузки/разгрузки),
//				 то все дополнительные операции отменяются.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияОтменитьРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРаботыПоРейсу	= Движения.упРаботыПоРейсу;
	ДвиженияРаботыПоРейсу.Записывать	= Истина;
    	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тбОтмененныеСтрокиПогрузки.Задание,
	|	тбОтмененныеСтрокиПогрузки.Проблема,
	|	тбОтмененныеСтрокиПогрузки.ГрузовоеМесто
	|ПОМЕСТИТЬ втОтмененныеСтрокиПогрузки
	|ИЗ
	|	&ОтмененныеСтрокиПогрузки КАК тбОтмененныеСтрокиПогрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упРаботыПоРейсуСрезПоследних.СтрокаНомер,
	|	упРаботыПоРейсуСрезПоследних.Операция,
	|	ВЫБОР
	|		КОГДА НЕ втОтмененныеСтрокиПогрузки.Задание ЕСТЬ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоОтменненыхСтрок,
	|	ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоОперацийСГрузом,
	|	упРаботыПоРейсуСрезПоследних.Рейс,
	|	упРаботыПоРейсуСрезПоследних.Задание,
	|	упРаботыПоРейсуСрезПоследних.ДополнительнаяОперация,
	|	упРаботыПоРейсуСрезПоследних.ВремяРабот,
	|	упРаботыПоРейсуСрезПоследних.ВременноеОкноНачало,
	|	упРаботыПоРейсуСрезПоследних.ВременноеОкноОкончание,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто,
	|	упРаботыПоРейсуСрезПоследних.ВремяОжидания,
	|	упРаботыПоРейсуСрезПоследних.ПлановоеВремяНачалаРабот,
	|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов,
	|	упРаботыПоРейсуСрезПоследних.КоличествоДопОпераций,
	|	упРаботыПоРейсуСрезПоследних.АдресРазгрузки,
	|	ЕСТЬNULL(втОтмененныеСтрокиПогрузки.Проблема, ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)) КАК Проблема,
	|	упРаботыПоРейсуСрезПоследних.ИдентификаторРаботы,
	|	упРаботыПоРейсуСрезПоследних.ТипРегламентногоДокумента,
	|	упРаботыПоРейсуСрезПоследних.КоличествоЭкземпляровДокумента,
	|	упРаботыПоРейсуСрезПоследних.Отменена
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеСтрокиПогрузки КАК втОтмененныеСтрокиПогрузки
	|		ПО упРаботыПоРейсуСрезПоследних.Задание = втОтмененныеСтрокиПогрузки.Задание
	|			И упРаботыПоРейсуСрезПоследних.ГрузовоеМесто = втОтмененныеСтрокиПогрузки.ГрузовоеМесто
	|ГДЕ
	|	упРаботыПоРейсуСрезПоследних.Операция В(&ДоступныеОперации)
	|ИТОГИ
	|	СУММА(КоличествоОтменненыхСтрок),
	|	СУММА(КоличествоОперацийСГрузом)
	|ПО
	|	Идентификатор";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Рейс", 	Реквизиты.Рейс);
	
	мсвДоступныеОперации = Новый Массив();
	мсвДоступныеОперации.Добавить(Перечисления.упТипыОпераций.Погрузка);
	мсвДоступныеОперации.Добавить(Перечисления.упТипыОпераций.Разгрузка);
	мсвДоступныеОперации.Добавить(Перечисления.упТипыОпераций.ПередачаЦенностей);
	мсвДоступныеОперации.Добавить(Перечисления.упТипыОпераций.ВыемкаЦенностей);
	мсвДоступныеОперации.Добавить(Перечисления.упТипыОпераций.ДополнительнаяОперация);
	
	Запрос.УстановитьПараметр("ДоступныеОперации", 			мсвДоступныеОперации);
	Запрос.УстановитьПараметр("ОтмененныеСтрокиПогрузки", 	Реквизиты.ОтмененныеСтрокиПогрузки);
	
	текВыборкаИдентификатор = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока текВыборкаИдентификатор.Следующий() Цикл
		ОтменятьВсеРаботы	= (текВыборкаИдентификатор.КоличествоОтменненыхСтрок > 0) 
								И (текВыборкаИдентификатор.КоличествоОтменненыхСтрок = текВыборкаИдентификатор.КоличествоОперацийСГрузом);
		
		текВыборкаРабота = текВыборкаИдентификатор.Выбрать();
	    Пока текВыборкаРабота.Следующий() Цикл
			Если ОтменятьВсеРаботы ИЛИ текВыборкаРабота.КоличествоОтменненыхСтрок > 0 Тогда
				Если Реквизиты.Свойство("ИдентификаторТочки") И текВыборкаРабота.Идентификатор = Реквизиты.ИдентификаторТочки Тогда
					Продолжить;	
				КонецЕсли;
				ДвижениеРабота			= ДвиженияРаботыПоРейсу.Добавить();
		        ЗаполнитьЗначенияСвойств(ДвижениеРабота, текВыборкаРабота);
				ДвижениеРабота.Отменена = Истина;		
				ДвижениеРабота.Период	= Реквизиты.Дата;
			КонецЕсли;
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры // СформироватьДвиженияОтменитьРаботыПоРейсу()

// Процедура - формирует движения по регистру "упМаршрутПоРейсу".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияОтменитьТочкиПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ И НЕ ДополнительныеСвойства.Свойство("ОтменитьТочки") Тогда 
		Возврат; 
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРаботыПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	               |	упРаботыПоРейсуСрезПоследних.Отменена КАК Отменена,
	               |	упРаботыПоРейсуСрезПоследних.Проблема КАК Проблема,
	               |	ВЫБОР
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Проблема.ТипОперации = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПустаяСсылка)
	               |			ТОГДА 99
	               |		ИНАЧЕ упРаботыПоРейсуСрезПоследних.Проблема.ТипОперации.Порядок
	               |	КОНЕЦ КАК ПроблемаТипОперацииПорядок
	               |ПОМЕСТИТЬ втРаботыПоРейсу
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбРаботыПоРейсу.Идентификатор,
	               |	МИНИМУМ(тбРаботыПоРейсу.ПроблемаТипОперацииПорядок) КАК ПроблемаТипОперацииПорядок
	               |ПОМЕСТИТЬ втОтмененныеТочки
	               |ИЗ
	               |	втРаботыПоРейсу КАК тбРаботыПоРейсу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбРаботыПоРейсу.Идентификатор
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВЫБОР
	               |			КОГДА тбРаботыПоРейсу.Отменена
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) = КОЛИЧЕСТВО(тбРаботыПоРейсу.Идентификатор)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втРаботыПоРейсу.Идентификатор,
	               |	МАКСИМУМ(втРаботыПоРейсу.Проблема) КАК Проблема
	               |ПОМЕСТИТЬ втПроблемыПоТочкам
	               |ИЗ
	               |	втОтмененныеТочки КАК втОтмененныеТочки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРаботыПоРейсу КАК втРаботыПоРейсу
	               |		ПО втОтмененныеТочки.Идентификатор = втРаботыПоРейсу.Идентификатор
	               |			И втОтмененныеТочки.ПроблемаТипОперацииПорядок = втРаботыПоРейсу.ПроблемаТипОперацииПорядок
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втРаботыПоРейсу.Идентификатор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упМаршрутПоРейсуСрезПоследних.Рейс,
	               |	упМаршрутПоРейсуСрезПоследних.Идентификатор,
	               |	упМаршрутПоРейсуСрезПоследних.Адрес,
				   |	упМаршрутПоРейсуСрезПоследних.Гараж,
	               |	упМаршрутПоРейсуСрезПоследних.ТипТочки,
	               |	упМаршрутПоРейсуСрезПоследних.СтрокаНомер,
	               |	упМаршрутПоРейсуСрезПоследних.Пройдена,
	               |	упМаршрутПоРейсуСрезПоследних.Отменена,
	               |	ЕСТЬNULL(втПроблемыПоТочкам.Проблема, ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)) КАК Проблема
	               |ИЗ
	               |	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОтмененныеТочки КАК втОтмененныеТочки
	               |		ПО упМаршрутПоРейсуСрезПоследних.Идентификатор = втОтмененныеТочки.Идентификатор
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втПроблемыПоТочкам КАК втПроблемыПоТочкам
	               |		ПО упМаршрутПоРейсуСрезПоследних.Идентификатор = втПроблемыПоТочкам.Идентификатор
	               |ГДЕ
	               |	НЕ упМаршрутПоРейсуСрезПоследних.Отменена";
	Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
	
	текРезультат 	= Запрос.Выполнить();
	текВыборка 		= текРезультат.Выбрать();
	
	ДвиженияМаршрутПоРейсу				= Движения.упМаршрутПоРейсу;
	
	Если НЕ текРезультат.Пустой() Тогда
		
		ДвиженияМаршрутПоРейсу.Записывать	= Истина;
		
		Пока текВыборка.Следующий() Цикл
			ДвижениеМаршрут	= ДвиженияМаршрутПоРейсу.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеМаршрут, текВыборка);
			ДвижениеМаршрут.Пройдена		= Ложь;
			ДвижениеМаршрут.Отменена		= Истина;
			ДвижениеМаршрут.Период			= Реквизиты.Дата;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // ОтменитьТочкиПоРейсу()

// Процедура - сообщает о невозможности выполнения действия над документом "упРейс".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура ПроверитьСтатусРейса(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	мсвВозможныеСтатусыРейсаДляОтмены = Новый Массив;
	мсвВозможныеСтатусыРейсаДляОтмены.Добавить(Перечисления.упСтатусыРейса.Новый);
	мсвВозможныеСтатусыРейсаДляОтмены.Добавить(Перечисления.упСтатусыРейса.КПланированиюТС);
	мсвВозможныеСтатусыРейсаДляОтмены.Добавить(Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС);
	мсвВозможныеСтатусыРейсаДляОтмены.Добавить(Перечисления.упСтатусыРейса.НазначеноТС);
	мсвВозможныеСтатусыРейсаДляОтмены.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
	мсвВозможныеСтатусыРейсаДляОтмены.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	
	текСтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Реквизиты.Рейс);
		
	Если мсвВозможныеСтатусыРейсаДляОтмены.Найти(текСтатусРейса) = Неопределено Тогда
		ТекстСообщения = Нстр("ru = 'Нельзя отменять задания на перевозку рейса в статусе %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текСтатусРейса);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если НЕ Отказ И текСтатусРейса = Перечисления.упСтатусыРейса.Выполняется Тогда
		
		// Проверяется нет ли уже выполненных работ погрузки по отменяемым заданиям.
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упРаботыПоРейсуСрезПоследних.Задание
		               |ИЗ
		               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
		               |ГДЕ
		               |	(упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
					   |	ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
		               |	И упРаботыПоРейсуСрезПоследних.Задание В(&Задания)
		               |	И упРаботыПоРейсуСрезПоследних.Выполнена";
		Запрос.УстановитьПараметр("Задания", 	Реквизиты.Задания.ВыгрузитьКолонку("Задание"));
		Запрос.УстановитьПараметр("Рейс", 		Реквизиты.Рейс);
		Выборка	= Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = Нстр("ru = 'По заданию %1 уже выполнена работа погрузки. Нельзя отменить задание'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Задание);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Устанавливает заголовок формы.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//
Процедура УстановитьЗаголовокФормы(Форма) Экспорт
	
	ОбъектДокумент 	 = Форма.Объект;
	ОбъектСсылка	      = ОбъектДокумент.Ссылка;
	Форма.Автозаголовок  = Ложь;
	ЗаголовокПоУмолчанию = Истина;
	
	Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упРейс") Тогда
		
		Если ЗначениеЗаполнено(ОбъектДокумент.Расписание) Тогда
			Форма.Заголовок      = НСтр(СтрШаблон("ru = 'Периодический рейс %1 от %2 (создан по расписанию ""%3"")'", ОбъектДокумент.Номер, ОбъектДокумент.Дата, ОбъектДокумент.Расписание));
			ЗаголовокПоУмолчанию = Ложь;
		ИначеЕсли ОбъектДокумент.ПеревозкаТранспортнойКомпанией Тогда	
			Форма.Заголовок      = НСтр(СтрШаблон("ru = 'Рейс перевозчика %1 от %2'", ОбъектДокумент.Номер, ОбъектДокумент.Дата));
			ЗаголовокПоУмолчанию = Ложь;
		КонецЕсли;
		
		Если Истина
			И Не ОбъектСсылка.Пустая()
			И ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") 
			И РейсИсполняетсяНаМобильномКлиенте(ОбъектСсылка) 
		Тогда
			Если ЗаголовокПоУмолчанию Тогда
				ПредставлениеОбъекта = ПолучитьПредставлениеОбъекта(ОбъектДокумент, ОбъектСсылка);
				Форма.Заголовок      = НСтр(СтрШаблон("ru = '%1 %2 от %3, исполняется на мобильном клиенте'", ПредставлениеОбъекта, ОбъектДокумент.Номер, ОбъектДокумент.Дата)); 
			Иначе
				Форма.Заголовок      = НСтр(СтрШаблон("ru = '%1, исполняется на мобильном клиенте'",Форма.Заголовок));
			КонецЕсли; 
			ЗаголовокПоУмолчанию = Ложь;
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда	
		Если ЗначениеЗаполнено(ОбъектДокумент.НомерВЦепиПоставок) Тогда
			Форма.Заголовок      = НСтр(СтрШаблон("ru = 'Задание на перевозку груза %1 от %2 (этап %3 - %4)'", ОбъектДокумент.Номер, ОбъектДокумент.Дата, ОбъектДокумент.НомерВЦепиПоставок, ОбъектДокумент.НомерВЦепиПоставокКонец));				
			ЗаголовокПоУмолчанию = Ложь;	
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаголовокПоУмолчанию Тогда
		
		Если ОбъектСсылка.Пустая() Тогда
			ПредставлениеОбъекта = ПолучитьПредставлениеОбъекта(ОбъектДокумент, ОбъектСсылка);
			ТекстСоздание        = НСтр("ru = ' (создание)'");
			Форма.Заголовок      = НСтр(СтрШаблон("ru = '%1 %2'", ПредставлениеОбъекта, ТекстСоздание)); 
		Иначе	
			ПредставлениеОбъекта = ПолучитьПредставлениеОбъекта(ОбъектДокумент, ОбъектСсылка);
			ТекстПроведен        = ?(ОбъектДокумент.Проведен, НСтр("ru = ' (проведен)'"), "");
			Форма.Заголовок      = НСтр(СтрШаблон("ru = '%1 %2 от %3%4'", ПредставлениеОбъекта, ОбъектДокумент.Номер, ОбъектДокумент.Дата, ТекстПроведен)); 	
		КонецЕсли;
		
	КонецЕсли;	
			
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ОбъектДокумент, "ЧасовойПояс") Тогда
		упСервисныеФункции.ДобавитьВЗаголовокЧасовойПояс(Форма, ОбъектДокумент.ЧасовойПояс);
	КонецЕсли;	

КонецПроцедуры

Функция ПолучитьПредставлениеОбъекта(ОбъектДокумент, ОбъектСсылка)
	Результат = "";
	Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		
		Если ОбъектДокумент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов Тогда
			Результат = Нстр("ru = 'Заявка на перевозку груза'");	
			
		ИначеЕсли ОбъектДокумент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.УслугиСпецтехники Тогда
			Результат = Нстр("ru = 'Заявка на спецтехнику'");	
			
		ИначеЕсли ОбъектДокумент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаПассажиров Тогда
			Результат = Нстр("ru = 'Заявка на пассажирские перевозки'");	
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда
		Результат = Нстр("ru = 'Запрос условий перевозки'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		Результат = Нстр("ru = 'Задание на перевозку груза'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упКорректировкаРейса") Тогда	
		Результат = Нстр("ru = 'Корректировка рейса'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда	
		Результат = Нстр("ru = 'Предложение перевозчика'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС") Тогда	
		Результат = Нстр("ru = 'Подтверждение заявки на транспортное средство'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда	
		Результат = Нстр("ru = 'Путевой лист'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда	
		Результат = Нстр("ru = 'Ремонт (ТО) транспортного средства'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упПересчетГрузовыхМест") Тогда	
		Результат = Нстр("ru = 'Пересчет грузовых мест'");
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упРейс") Тогда	
		
		Если ОбъектДокумент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			Результат = Нстр("ru = 'Операции с контейнером'");
		Иначе
			Результат = Нстр("ru = 'Рейс'");
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.упСписаниеПлановПоЗаданиюНаПеревозку") Тогда	
		Результат = Нстр("ru = 'Списание планов по заданию на перевозку'");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Проверяет есть ли еще рейсы по ТС в статусе Выполняются,
//  отличные от заданного.
//
// Параметры:
//  Рейс						 - ДокументСсылка.упРейс	 - рейс
//  ТекстСообщения				 - Строка					 - описание ошибки.
//  РейсПоТСВСтатусеВыполняется	 - ДокументСсылка.упРейс 	 - рейс по ТС в статусе "выполняется".
// 
// Возвращаемое значение:
//  Булево - Истина, если такие рейсы есть.
//
Функция ЕстьЕщеРейсыПоТСВСтатусеВыполняется(Рейс, ТекстСообщения = "", РейсПоТСВСтатусеВыполняется = Неопределено) Экспорт
	
	Результат = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство
	|ПОМЕСТИТЬ втТранспортРейса
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК упПеревозчикПоРейсу
	|ГДЕ НЕ (упПеревозчикПоРейсу.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упСтатусыРейсов.Документ КАК Рейс
	|ПОМЕСТИТЬ втРейсыВСтатусеВыполняется
	|ИЗ
	|	РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|		,
	|		) КАК упСтатусыРейсов
	|ГДЕ упСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упПеревозчикПоРейсу.Рейс КАК Рейс,
	|	упПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упРейсТ.ВидТранспортнойУслуги КАК ВидТранспортнойУслуги
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсыВСтатусеВыполняется.Рейс КАК Рейс
	|			ИЗ
	|				втРейсыВСтатусеВыполняется КАК втРейсыВСтатусеВыполняется)) КАК упПеревозчикПоРейсу
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТранспортРейса КАК втТранспортРейса
	|	ПО упПеревозчикПоРейсу.ТранспортноеСредство = втТранспортРейса.ТранспортноеСредство
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
	|	ПО упРейсТ.Ссылка = упПеревозчикПоРейсу.Рейс
	|";

	Запрос.УстановитьПараметр("Рейс", Рейс);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			ТекстСообщения = Нстр("ru = 'Документ %1 в статусе ""К выполнению"" нельзя переводить в статус ""Контейнер сформирован"", т.к. для текущего контейнера (%2) существует %3 в статусе ""Контейнер сформирован"".'");
		Иначе
			ТекстСообщения = Нстр("ru = 'Документ %1 в статусе ""К выполнению"" нельзя переводить в статус ""Выполняется"", т.к. для текущего ТС (%2) существует другой рейс %3 в статусе ""Выполняется"".'");
		КонецЕсли;
		
		РейсПоТСВСтатусеВыполняется = Выборка.Рейс;
		ТекстСообщения = СтрШаблон(ТекстСообщения, Рейс, Выборка.ТранспортноеСредство, РейсПоТСВСтатусеВыполняется);
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Функция - Проверяет есть ли еще не выполненные заявки по операции с контейнером.
//
// Параметры:
//  Рейс						 - ДокументСсылка.упРейс	 - рейс
//  ТекстСообщения				 - Строка					 - описание ошибки.
// 
// Возвращаемое значение:
//  Булево - Истина, если есть такие заявки.
//
Функция ЕстьВыполненнаяЗаявкаНаПеревозкуГрузаПоОперацииСКонтейнером(Рейс, ТекстСообщения = "", ЗаявкаНаПеревозкуГруза = Неопределено) Экспорт
	
	Результат = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|		упЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка,
	|		упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Статус КАК Статус
	|	ИЗ
	|		РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|			,
	|			) КАК упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|		ПО упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Документ = упЗаявкаНаПеревозкуГруза.Ссылка
	|	ГДЕ ИСТИНА
	|		И упЗаявкаНаПеревозкуГруза.ДокументОснование = &Рейс
	|		И НЕ (упЗаявкаНаПеревозкуГруза.ПометкаУдаления)
	|		И упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)
	|";

	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = СтрШаблон(Нстр("ru = 'Для текущего вида перевозки, перед разгрузкой контейнера требуется выполнить заявку на перевозку контейнера(%1).'"), Рейс);
	Иначе
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаявкаНаПеревозкуГруза = Выборка.Ссылка;
			Если Ложь
				Или Выборка.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
				Или Выборка.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично
			Тогда
				Результат = Истина;
			Иначе	
				ТекстСообщения = Нстр("ru = 'Запрещено разгружать контейнер, есть невыполненная заявка %1 в статусе %2.'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ЗаявкаНаПеревозкуГруза,  Выборка.Статус);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Контролирует попадание связанных грузовых мест из рейса в другие рейсы.
//
// Параметры:
//  ДокументОбъект	 - ДокументОбъект.упРейс 	 - рейс.
//  Отказ			 - Булево	 - Истина, в случае ошибки.
// 
// Возвращаемое значение:
//  Булево - Истина, если такие грузовые места найдены
//
Функция ВыполнитьКонтрольСвязанныхЗаданий(ДокументОбъект, Отказ) Экспорт 

	Если Ложь Тогда // Для контекстной подсказки
	    ДокументОбъект = Документы.упРейс.СоздатьДокумент();
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Задания.Задание КАК Документ.упЗаданиеНаПеревозкуГруза) КАК Задание,
	|	Задания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	Задания.Ссылка КАК Ссылка,
	|	Задания.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ ЗаданияРейса
	|ИЗ
	|	&Задания КАК Задания
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрузовыеМеста.Ссылка КАК Ссылка,
	|	ГрузовыеМеста.Ссылка.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ГрузовыеМеста.Ссылка.АдресОтправления КАК АдресОтправления,
	|	ГрузовыеМеста.Ссылка.АдресПолучения КАК АдресПолучения,
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.ГрузовоеМесто.ТипГруза КАК ТипГруза,
	|	ГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ втЗависимыеГрузовыеМеста
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	|	ПО ИСТИНА
	|		И ГрузовыеМеста.Ссылка = упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ
	|		И упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
	|	ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто КАК ЗависимоеГрузовоеМесто,
	|	ЗаданиеЗависимоеГрузовыеМеста.КоличествоГрузов КАК ЗависимоеКоличествоГрузовыхМест,
	|	ЗависимыеЗаданияРейса.Ссылка КАК ЗависимыйРейс,
	|	ЗаданиеОсновноеГрузовыеМеста.Ссылка КАК ОсновноеЗадание,
	|	ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто КАК ОсновноеГрузовоеМесто,
	|	ЗаданиеОсновноеГрузовыеМеста.КоличествоГрузов КАК ОсновноеКоличествоГрузовыхМест,
	|	ОсновныеЗаданияРейса.Ссылка КАК ОсновнойРейс
	|ПОМЕСТИТЬ ЗависимыеЗадания
	|ИЗ
	|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
	|	ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗависимыеГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
	|	ПО ИСТИНА
	|		И ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ТипГруза
	|		И ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.НомерВЦепиПоставок
	|		И ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.ЗаявкаНаПеревозкуГруза
	|		И ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе
	|		//		И ЗаданиеОсновноеГрузовыеМеста.Ссылка <> ЗаданиеЗависимоеГрузовыеМеста.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗаданияРейса КАК ЗависимыеЗаданияРейса
	|	ПО ИСТИНА
	|		И ЗависимыеЗаданияРейса.ГрузовоеМесто = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто
	|		И ЗависимыеЗаданияРейса.Задание = ЗаданиеЗависимоеГрузовыеМеста.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗаданияРейса КАК ОсновныеЗаданияРейса
	|	ПО ИСТИНА
	|		И ОсновныеЗаданияРейса.ГрузовоеМесто = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто
	|		И ОсновныеЗаданияРейса.Задание = ЗаданиеОсновноеГрузовыеМеста.Ссылка
	|ГДЕ ЕСТЬNULL(ЗависимыеЗаданияРейса.Ссылка, 1) <> ЕСТЬNULL(ОсновныеЗаданияРейса.Ссылка, 1)
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
	|	ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто КАК ЗависимоеГрузовоеМесто,
	|	ЗаданиеЗависимоеГрузовыеМеста.КоличествоГрузов КАК ЗависимоеКоличествоГрузовыхМест,
	|	ЗависимыеЗаданияРейса.Ссылка КАК ЗависимыйРейс,
	|	ЗаданиеОсновноеГрузовыеМеста.Задание КАК ОсновноеЗадание,
	|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ОсновноеГрузовоеМесто,
	|	1 КАК ОсновноеКоличествоГрузовыхМест,
	|	ЗаданиеОсновноеГрузовыеМеста.Ссылка КАК ОсновнойРейс
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗаданияРейса.Ссылка КАК Ссылка,
	|		ЗаданияРейса.Задание КАК Задание
	|	ИЗ
	|		ЗаданияРейса КАК ЗаданияРейса) КАК ЗаданиеОсновноеГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗависимыеГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
	|	ПО ИСТИНА
	|		И ЗаданиеОсновноеГрузовыеМеста.Задание.АдресПолучения = ЗаданиеЗависимоеГрузовыеМеста.АдресОтправления
	|		И ЗаданиеОсновноеГрузовыеМеста.Задание.АдресОтправления = ЗаданиеЗависимоеГрузовыеМеста.АдресПолучения
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗаданияРейса КАК ЗависимыеЗаданияРейса
	|	ПО ИСТИНА
	|		И ЗависимыеЗаданияРейса.ГрузовоеМесто = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто
	|		И ЗависимыеЗаданияРейса.Задание = ЗаданиеЗависимоеГрузовыеМеста.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
	|	ПО ИСТИНА
	|		И ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки = ВидыТочекОтправления.Ссылка
	|		И ЗаданиеОсновноеГрузовыеМеста.Задание.АдресОтправления.ВидАдреса = ВидыТочекОтправления.ВидТочкиОтправления
	|ГДЕ ИСТИНА
	|	И (ЛОЖЬ
	|		ИЛИ ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки.АвтоматическиДобавлятьЗаданияНаВозврат
	|		ИЛИ (ИСТИНА
	|			И ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки.АвтоматическиДобавлятьЗаданияИнкассации
	|			И ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)))
	|	И НЕ (ВидыТочекОтправления.ВидТочкиОтправления ЕСТЬ NULL)
	|	И ЕСТЬNULL(ЗависимыеЗаданияРейса.Ссылка, 1) <> ЕСТЬNULL(ЗаданиеОсновноеГрузовыеМеста.Ссылка, 1)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ЗависимоеЗадание КАК Задание,
	|	Т.ЗависимыйРейс КАК Рейс,
	|	Т.ЗависимоеГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ ВсеГрузовыеМеста
	|ИЗ
	|	ЗависимыеЗадания КАК Т
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ОсновноеЗадание КАК Задание,
	|	Т.ОсновнойРейс КАК Рейс,
	|	Т.ОсновноеГрузовоеМесто КАК ГрузовоеМесто
	|ИЗ
	|	ЗависимыеЗадания КАК Т
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпЗаданияКПланированию_ОстаткиТ.*
	|ПОМЕСТИТЬ ОстаткиЗаданий
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|		,
	|		(Задание, ГрузовоеМесто) В (
	|				ВЫБРАТЬ
	|				ВсеГрузовыеМеста.Задание КАК Задание,
	|				ВсеГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто
	|			ИЗ
	|				ВсеГрузовыеМеста КАК ВсеГрузовыеМеста)) КАК УпЗаданияКПланированию_ОстаткиТ
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпЗаявкиКВыполнению_ОстаткиТ.*
	|ПОМЕСТИТЬ ОстаткиЗаявок
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|		,
	|		(Заявка, ГрузовоеМесто) В (
	|				ВЫБРАТЬ
	|				Т.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|				Т.ГрузовоеМесто КАК ГрузовоеМесто
	|			ИЗ
	|				ВсеГрузовыеМеста КАК Т)) КАК УпЗаявкиКВыполнению_ОстаткиТ
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗависимыеЗадания.ЗависимоеЗадание КАК ЗависимоеЗадание,
	|	ЗависимыеЗадания.ЗависимоеГрузовоеМесто КАК ЗависимоеГрузовоеМесто,
	|	ЗависимыеЗадания.ЗависимыйРейс КАК ЗависимыйРейс,
	|	ЗависимыеЗадания.ОсновноеЗадание КАК ОсновноеЗадание,
	|	ЗависимыеЗадания.ОсновноеГрузовоеМесто КАК ОсновноеГрузовоеМесто,
	|	ЗависимыеЗадания.ОсновнойРейс КАК ОсновнойРейс,
	|	ЕСТЬNULL(ОстаткиЗаявокЗависимые.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийЗависимые.КоличествоКВыполнениюОстаток, 0) КАК ЗависимоеКоличествоГрузовыхМест,
	|	ЕСТЬNULL(ОстаткиЗаявокОсновные.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийОсновные.КоличествоКВыполнениюОстаток, 0) КАК ОсновноеКоличествоГрузовыхМест
	|ИЗ
	|	ЗависимыеЗадания КАК ЗависимыеЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаявок КАК ОстаткиЗаявокОсновные
	|	ПО ИСТИНА
	|		И ОстаткиЗаявокОсновные.Заявка = ЗависимыеЗадания.ОсновноеЗадание.ЗаявкаНаПеревозкуГруза
	|		И ОстаткиЗаявокОсновные.ГрузовоеМесто = ЗависимыеЗадания.ОсновноеГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаданий КАК ОстаткиЗаданийОсновные
	|	ПО ИСТИНА
	|		И ОстаткиЗаданийОсновные.Задание = ЗависимыеЗадания.ОсновноеЗадание
	|		И ОстаткиЗаданийОсновные.ГрузовоеМесто = ЗависимыеЗадания.ОсновноеГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаявок КАК ОстаткиЗаявокЗависимые
	|	ПО ИСТИНА
	|		И ОстаткиЗаявокЗависимые.Заявка = ЗависимыеЗадания.ЗависимоеЗадание.ЗаявкаНаПеревозкуГруза
	|		И ОстаткиЗаявокОсновные.ГрузовоеМесто = ЗависимыеЗадания.ЗависимоеГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаданий КАК ОстаткиЗаданийЗависимые
	|	ПО ИСТИНА
	|		И ОстаткиЗаданийЗависимые.Задание = ЗависимыеЗадания.ЗависимоеЗадание
	|		И ОстаткиЗаданийЗависимые.ГрузовоеМесто = ЗависимыеЗадания.ЗависимоеГрузовоеМесто
	|ГДЕ ЛОЖЬ
	|	ИЛИ ЗависимыеЗадания.ОсновноеГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|	ИЛИ (ИСТИНА
	|		И ЕСТЬNULL(ОстаткиЗаявокЗависимые.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийЗависимые.КоличествоКВыполнениюОстаток, 0) > 0
	|		И ЕСТЬNULL(ОстаткиЗаявокОсновные.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийОсновные.КоличествоКВыполнениюОстаток, 0) > 0)
	|";
	
	тбзЗадания = ДокументОбъект.Задания.Выгрузить(,"Задание, ГрузовоеМесто, КоличествоГрузов");
	тбзЗадания.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.упРейс"));
	тбзЗадания.ЗаполнитьЗначения(ДокументОбъект.Ссылка, "Ссылка");
	Запрос.УстановитьПараметр("Задания", тбзЗадания);
	Запрос.УстановитьПараметр("Рейс", ДокументОбъект.Ссылка);
	ГрузовыеМеста = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаГрузовогоМеста Из ГрузовыеМеста Цикл
		Отказ = Истина;
		Текст = НСтр("ru = 'По грузовому месту ""%1"" существует связанное грузовое место ""%2"", не включенное в рейс'");
		Если СтрокаГрузовогоМеста.ЗависимыйРейс = ДокументОбъект.Ссылка Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, СтрокаГрузовогоМеста.ЗависимоеГрузовоеМесто, СтрокаГрузовогоМеста.ОсновноеГрузовоеМесто, СтрокаГрузовогоМеста.ОсновнойРейс);
		Иначе
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, СтрокаГрузовогоМеста.ОсновноеГрузовоеМесто, СтрокаГрузовогоМеста.ЗависимоеГрузовоеМесто, СтрокаГрузовогоМеста.ЗависимыйРейс);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,, ДокументОбъект.Ссылка, Текст, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецЦикла;
	Возврат Не Отказ;

КонецФункции // ВыполнитьКонтрольСвязанныхЗаданий()

// Функция - Список недостающих связанных грузовых мест по заданиям в рейсе
//
// Параметры:
//  тбзЗадания	 - ТаблицаЗначений - таблица заданий рейса
//  Рейс		 - ДокументСсылка.упРейс - рейс
// 
// Возвращаемое значение:
//  ТаблицаЗначений - список недостающих связанных грузовых мест
//		* Задание - ДокументСсылка.упЗаданиеНаПеревозкуГруза - задание
//		* ГрузовоеМесто - СправочникСсылка.упГрузовыеМеста - грузовое место
//		* КоличествоГрузов - Число - Количество грузов
//
Функция СписокНедостающихСвязанныхГрузовыхМестПоЗаданиямВРейсе(тбзЗадания, Рейс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Задания.Задание КАК Документ.упЗаданиеНаПеревозкуГруза) КАК Задание,
	|	Задания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	Задания.Ссылка КАК Ссылка,
	|	Задания.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ ЗаданияРейса
	|ИЗ
	|	&Задания КАК Задания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
	|	ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто КАК ЗависимоеГрузовоеМесто,
	|	ЗаданиеЗависимоеГрузовыеМеста.КоличествоГрузов КАК ЗависимоеКоличествоГрузовыхМест,
	|	ЗависимыеЗаданияРейса.Ссылка КАК ЗависимыйРейс,
	|	ЗаданиеОсновноеГрузовыеМеста.Ссылка КАК ОсновноеЗадание,
	|	ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто КАК ОсновноеГрузовоеМесто,
	|	ЗаданиеОсновноеГрузовыеМеста.КоличествоГрузов КАК ОсновноеКоличествоГрузовыхМест,
	|	ОсновныеЗаданияРейса.Ссылка КАК ОсновнойРейс
	|ПОМЕСТИТЬ ЗависимыеЗадания
	|ИЗ
	|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
	|		ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
	|		ПО (ИСТИНА)
	|			И ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза
	|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.НомерВЦепиПоставок)
	|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ВидПеревозки)
	|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза)
	|			И (ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	|		ПО (ИСТИНА)
	|			И (ЗаданиеЗависимоеГрузовыеМеста.Ссылка = упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ)
	|			И (упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаданияРейса КАК ЗависимыеЗаданияРейса
	|		ПО (ИСТИНА)
	|			И (ЗависимыеЗаданияРейса.ГрузовоеМесто = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто)
	|			И (ЗависимыеЗаданияРейса.Задание = ЗаданиеЗависимоеГрузовыеМеста.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаданияРейса КАК ОсновныеЗаданияРейса
	|		ПО (ИСТИНА)
	|			И (ОсновныеЗаданияРейса.ГрузовоеМесто = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто)
	|			И (ОсновныеЗаданияРейса.Задание = ЗаданиеОсновноеГрузовыеМеста.Ссылка)
	|ГДЕ
	|	ЕСТЬNULL(ЗависимыеЗаданияРейса.Ссылка, 1) <> ЕСТЬNULL(ОсновныеЗаданияРейса.Ссылка, 1)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка,
	|	ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто,
	|	ЗаданиеЗависимоеГрузовыеМеста.КоличествоГрузов,
	|	ЗависимыеЗаданияРейса.Ссылка,
	|	ЗаданиеОсновноеГрузовыеМеста.Задание,
	|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка),
	|	1,
	|	ЗаданиеОсновноеГрузовыеМеста.Ссылка
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗаданияРейса.Ссылка КАК Ссылка,
	|		ЗаданияРейса.Задание КАК Задание
	|	ИЗ
	|		ЗаданияРейса КАК ЗаданияРейса) КАК ЗаданиеОсновноеГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
	|		ПО (ИСТИНА)
	|			И ЗаданиеОсновноеГрузовыеМеста.Задание.АдресПолучения = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.АдресОтправления
	|			И ЗаданиеОсновноеГрузовыеМеста.Задание.АдресОтправления = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.АдресПолучения
	|			И ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ВидПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	|		ПО (ИСТИНА)
	|			И (ЗаданиеЗависимоеГрузовыеМеста.Ссылка = упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ)
	|			И (упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаданияРейса КАК ЗависимыеЗаданияРейса
	|		ПО (ИСТИНА)
	|			И (ЗависимыеЗаданияРейса.ГрузовоеМесто = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто)
	|			И (ЗависимыеЗаданияРейса.Задание = ЗаданиеЗависимоеГрузовыеМеста.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
	|		ПО (ИСТИНА)
	|			И ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки = ВидыТочекОтправления.Ссылка
	|			И ЗаданиеОсновноеГрузовыеМеста.Задание.АдресОтправления.ВидАдреса = ВидыТочекОтправления.ВидТочкиОтправления
	|ГДЕ
	|	ИСТИНА
	|	И (ЛОЖЬ
	|			ИЛИ ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки.АвтоматическиДобавлятьЗаданияНаВозврат
	|			ИЛИ ИСТИНА
	|				И ЗаданиеОсновноеГрузовыеМеста.Задание.ВидПеревозки.АвтоматическиДобавлятьЗаданияИнкассации
	|				И ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность))
	|	И НЕ ВидыТочекОтправления.ВидТочкиОтправления ЕСТЬ NULL
	|	И ЕСТЬNULL(ЗависимыеЗаданияРейса.Ссылка, 1) <> ЕСТЬNULL(ЗаданиеОсновноеГрузовыеМеста.Ссылка, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ЗависимоеЗадание КАК Задание,
	|	Т.ЗависимыйРейс КАК Рейс,
	|	Т.ЗависимоеГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ ВсеГрузовыеМеста
	|ИЗ
	|	ЗависимыеЗадания КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ОсновноеЗадание,
	|	Т.ОсновнойРейс,
	|	Т.ОсновноеГрузовоеМесто
	|ИЗ
	|	ЗависимыеЗадания КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпЗаданияКПланированию_ОстаткиТ.Задание КАК Задание,
	|	УпЗаданияКПланированию_ОстаткиТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	УпЗаданияКПланированию_ОстаткиТ.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнениюОстаток
	|ПОМЕСТИТЬ ОстаткиЗаданий
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|			,
	|			(Задание, ГрузовоеМесто) В
	|				(ВЫБРАТЬ
	|					ВсеГрузовыеМеста.Задание КАК Задание,
	|					ВсеГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто
	|				ИЗ
	|					ВсеГрузовыеМеста КАК ВсеГрузовыеМеста)) КАК УпЗаданияКПланированию_ОстаткиТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпЗаявкиКВыполнению_ОстаткиТ.Заявка КАК Заявка,
	|	УпЗаявкиКВыполнению_ОстаткиТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	УпЗаявкиКВыполнению_ОстаткиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	УпЗаявкиКВыполнению_ОстаткиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	УпЗаявкиКВыполнению_ОстаткиТ.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиЗаявок
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|			,
	|			(Заявка, ГрузовоеМесто) В
	|				(ВЫБРАТЬ
	|					Т.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|					Т.ГрузовоеМесто КАК ГрузовоеМесто
	|				ИЗ
	|					ВсеГрузовыеМеста КАК Т)) КАК УпЗаявкиКВыполнению_ОстаткиТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗависимыеЗадания.ЗависимоеЗадание КАК ЗависимоеЗадание,
	|	ЗависимыеЗадания.ЗависимоеЗадание.ВидПеревозки КАК ВидПеревозки,
	|	ЗависимыеЗадания.ЗависимоеГрузовоеМесто КАК ЗависимоеГрузовоеМесто,
	|	ЗависимыеЗадания.ЗависимыйРейс КАК ЗависимыйРейс,
	|	ЗависимыеЗадания.ОсновноеЗадание КАК ОсновноеЗадание,
	|	ЗависимыеЗадания.ОсновноеГрузовоеМесто КАК ОсновноеГрузовоеМесто,
	|	ЗависимыеЗадания.ОсновнойРейс КАК ОсновнойРейс,
	|	ЕСТЬNULL(ОстаткиЗаявокЗависимые.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийЗависимые.КоличествоКВыполнениюОстаток, 0) КАК ЗависимоеКоличествоГрузовыхМест,
	|	ЕСТЬNULL(ОстаткиЗаявокОсновные.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийОсновные.КоличествоКВыполнениюОстаток, 0) КАК ОсновноеКоличествоГрузовыхМест
	|ИЗ
	|	ЗависимыеЗадания КАК ЗависимыеЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаявок КАК ОстаткиЗаявокОсновные
	|		ПО (ИСТИНА)
	|			И (ОстаткиЗаявокОсновные.Заявка = ЗависимыеЗадания.ОсновноеЗадание.ЗаявкаНаПеревозкуГруза)
	|			И (ОстаткиЗаявокОсновные.ГрузовоеМесто = ЗависимыеЗадания.ОсновноеГрузовоеМесто)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаданий КАК ОстаткиЗаданийОсновные
	|		ПО (ИСТИНА)
	|			И (ОстаткиЗаданийОсновные.Задание = ЗависимыеЗадания.ОсновноеЗадание)
	|			И (ОстаткиЗаданийОсновные.ГрузовоеМесто = ЗависимыеЗадания.ОсновноеГрузовоеМесто)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаявок КАК ОстаткиЗаявокЗависимые
	|		ПО (ИСТИНА)
	|			И (ОстаткиЗаявокЗависимые.Заявка = ЗависимыеЗадания.ЗависимоеЗадание.ЗаявкаНаПеревозкуГруза)
	|			И (ОстаткиЗаявокОсновные.ГрузовоеМесто = ЗависимыеЗадания.ЗависимоеГрузовоеМесто)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиЗаданий КАК ОстаткиЗаданийЗависимые
	|		ПО (ИСТИНА)
	|			И (ОстаткиЗаданийЗависимые.Задание = ЗависимыеЗадания.ЗависимоеЗадание)
	|			И (ОстаткиЗаданийЗависимые.ГрузовоеМесто = ЗависимыеЗадания.ЗависимоеГрузовоеМесто)
	|ГДЕ
	|	(ЛОЖЬ
	|			ИЛИ ЗависимыеЗадания.ОсновноеГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ИЛИ ИСТИНА
	|				И ЕСТЬNULL(ОстаткиЗаявокЗависимые.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийЗависимые.КоличествоКВыполнениюОстаток, 0) > 0
	|				И ЕСТЬNULL(ОстаткиЗаявокОсновные.КоличествоОстаток, 0) + ЕСТЬNULL(ОстаткиЗаданийОсновные.КоличествоКВыполнениюОстаток, 0) > 0)";
	
	Запрос.УстановитьПараметр("Задания", 	тбзЗадания);
	Выборка = Запрос.Выполнить().Выбрать();
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("Задание", 			Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	тбзРезультат.Колонки.Добавить("ГрузовоеМесто", 		Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
	тбзРезультат.Колонки.Добавить("КоличествоГрузов", 	Новый ОписаниеТипов("Число"));
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = тбзРезультат.Добавить();
		Если Выборка.ЗависимыйРейс = Рейс Тогда
			НоваяСтрока.ГрузовоеМесто	= Выборка.ОсновноеГрузовоеМесто;
			НоваяСтрока.Задание			= Выборка.ОсновноеЗадание;
			НоваяСтрока.КоличествоГрузов	= Выборка.ОсновноеКоличествоГрузовыхМест;
		Иначе
			НоваяСтрока.ГрузовоеМесто	= Выборка.ЗависимоеГрузовоеМесто;
			НоваяСтрока.Задание			= Выборка.ЗависимоеЗадание;
			НоваяСтрока.КоличествоГрузов	= Выборка.ЗависимоеКоличествоГрузовыхМест;
		КонецЕсли;
		
	КонецЦикла;

	тбзРезультат.Свернуть("Задание, ГрузовоеМесто, КоличествоГрузов");
	
	Возврат тбзРезультат;
	
КонецФункции

Функция ПолучитьТранспортноеСредствоРейса(Рейс)
	
	ТранспортноеСредство = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ТранспортноеСредство = Выборка.ТранспортноеСредство;
	КонецЕсли;
	
	Возврат ТранспортноеСредство;
	
КонецФункции

// Выполнить действия с подтверждением рейса.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  ДокументСсылка - Результат выпонления.
//
Функция ДокументПодтверждающийРейс(Рейс) Экспорт

	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Регистратор
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	|ГДЕ
	|	упПеревозчикПоРейсуСрезПоследних.Регистратор ССЫЛКА Документ.упПодтверждениеЗаявкиНаТС
	|	И НЕ упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
	    Выборка = Результат.Выбрать();
		Выборка.Следующий();
		текРезультат = Выборка.Регистратор;
	КонецЕсли; 
	
	Возврат текРезультат;

КонецФункции // РейсПодтвержден()

// Выполнить действия с экипажем рейса.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Структура - Результат выпонления.
//
Функция ПолучитьЭкипажРейса(Рейс) Экспорт
	
	сткЭкипаж = Новый Структура("Подтверждение, Перевозчик, Соглашение, КонтрагентПеревозчика, ТипТС, ТранспортноеСредство, Водитель1, Водитель2, Экспедитор", 
	                            Неопределено, Неопределено, Неопределено, Неопределено, Неопределено, Неопределено, Неопределено, Неопределено, Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(упПеревозчикПоРейсуСрезПоследних.Регистратор КАК Документ.упПодтверждениеЗаявкиНаТС) КАК Подтверждение,
	|	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
	|	упПеревозчикПоРейсуСрезПоследних.Соглашение,
	|	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика,
	|	упПеревозчикПоРейсуСрезПоследних.ТипТС,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель1,
	|	упПеревозчикПоРейсуСрезПоследних.Экспедитор,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель2
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткЭкипаж, Выборка);
	КонецЕсли; 
	
	Возврат сткЭкипаж;	
	
КонецФункции // ПолучитьЭкипажРейса()
 
Функция ПолучитьТекстЗапросаМаршрутРаботыПоРейсу(ВариантФормированияМаршрута)
	
	ТекстЗапроса = "";
	
	Разделитель =
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";
	
	ОбъединитьВсе =
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	
	ФормироватьПолныйМаршрут = ВариантФормированияМаршрута = Перечисления.упВариантыФормированияМаршрута.Полный;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах,
	|	упВидыПеревозок.ИспользуютсяУслугиИнкассации,
	|	упВидыПеревозок.ВидЗоныДляФормированияПредставленияРейса,
	|	упВидыПеревозок.ВыборПеревозчика,
	|	упВидыПеревозок.ПакетРегламентныхДокументов
	|ПОМЕСТИТЬ втВидыПеревозок02
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задания.Задание КАК ЗаданиеНаПеревозку,
	|	Задания.ГрузовоеМесто,
	|	Задания.КоличествоГрузов
	|ПОМЕСТИТЬ втИсходныеДанные
	|ИЗ
	|	&Задания КАК Задания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втИсходныеДанные.ЗаданиеНаПеревозку
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	втИсходныеДанные КАК втИсходныеДанные
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(Документ_упЗаданиеНаПеревозкуГруза_ГрузовыеМестаТ.ГрузовоеМесто.ТипГруза.ВидГруза) КАК ВидГруза,
	|	Документ_упЗаданиеНаПеревозкуГруза_ГрузовыеМестаТ.Ссылка КАК ЗаданиеНаПеревозкуГруза
	|ПОМЕСТИТЬ
	| втВидыГрузаПоЗаданиямБезВводаГруза	
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК Документ_упЗаданиеНаПеревозкуГруза_ГрузовыеМестаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗаданияТ
	|	ПО втЗаданияТ.ЗаданиеНаПеревозку = Документ_упЗаданиеНаПеревозкуГруза_ГрузовыеМестаТ.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок02 КАК втВидыПеревозок02Т
	|	ПО втВидыПеревозок02Т.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|СГРУППИРОВАТЬ ПО
	|	Документ_упЗаданиеНаПеревозкуГруза_ГрузовыеМестаТ.Ссылка
	|;	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданияКПланированиюОстатки.Задание КАК ЗаданиеНаПеревозку,
	|	упЗаданияКПланированиюОстатки.ГрузовоеМесто,
	|	упЗаданияКПланированиюОстатки.Тара,
	|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|			,
	|			Задание В
	|				(ВЫБРАТЬ
	|					втЗадания.ЗаданиеНаПеревозку
	|				ИЗ
	|					втЗадания КАК втЗадания)) КАК упЗаданияКПланированиюОстатки
	|ГДЕ
	|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.ЗаданиеНаПеревозку,
	|	втОстатки.ГрузовоеМесто,
	|	втОстатки.Тара,
	|	втОстатки.КоличествоКВыполнениюОстаток КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМеста05
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок02 КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОстатки.ЗаданиеНаПеревозку,
	|	втОстатки.ГрузовоеМесто,
	|	втОстатки.Тара,
	|	ВЫБОР
	|		КОГДА втИсходныеДанные.КоличествоГрузов > втОстатки.КоличествоКВыполнениюОстаток
	|			ТОГДА втОстатки.КоличествоКВыполнениюОстаток
	|		ИНАЧЕ втИсходныеДанные.КоличествоГрузов
	|	КОНЕЦ
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок02 КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсходныеДанные КАК втИсходныеДанные
	|		ПО втОстатки.ЗаданиеНаПеревозку = втИсходныеДанные.ЗаданиеНаПеревозку
	|			И втОстатки.ГрузовоеМесто = втИсходныеДанные.ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗадания.ЗаданиеНаПеревозку,
	|	упЗаданиеНаПеревозкуГруза.ВидПеревозки,
	|	упЗаданиеНаПеревозкуГруза.Заказчик,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.Заказчик.ПакетРегламентныхДокументов = ЗНАЧЕНИЕ(Справочник.упПакетыРегламентныхДокументов.ПустаяСсылка)
	|			ТОГДА втВидыПеревозок.ПакетРегламентныхДокументов
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.Заказчик.ПакетРегламентныхДокументов
	|	КОНЕЦ КАК ПакетРегламентныхДокументов,
	|	упЗаданиеНаПеревозкуГруза.Заказчик.АдресВозвратаДокументов КАК АдресВозвратаДокументов,
	|	упЗаданиеНаПеревозкуГруза.Организация,
	|	упЗаданиеНаПеревозкуГруза.АдресОтправления,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения,
	|	упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаС,
	|	упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаПо,
	|	упЗаданиеНаПеревозкуГруза.ПолучениеГрузаС,
	|	упЗаданиеНаПеревозкуГруза.ПолучениеГрузаПо,
	|	упЗаданиеНаПеревозкуГруза.ЧасовойПояс
	|ПОМЕСТИТЬ втЗаданияШапка10
	|ИЗ
	|	втЗадания КАК втЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ПО втЗадания.ЗаданиеНаПеревозку = упЗаданиеНаПеревозкуГруза.Ссылка
	|			И (втЗадания.ЗаданиеНаПеревозку В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					втГрузовыеМеста05.ЗаданиеНаПеревозку
	|				ИЗ
	|					втГрузовыеМеста05))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок02 КАК втВидыПеревозок
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ЗаданиеНаПеревозку,
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.Тара,
	|	ЕСТЬNULL(упТипыГрузов.Ссылка, ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)) КАК ТипГруза,
	|	втГрузовыеМеста.КоличествоГрузов,
	|	втЗаданияШапка.АдресОтправления.ВидАдреса КАК ВидАдресаОтправления,
	|	втЗаданияШапка.АдресПолучения.ВидАдреса КАК ВидАдресаПолучения,
	|	ЕСТЬNULL(упТипыГрузов.ВидГруза, ЕСТЬNULL(втВидыГрузаПоЗаданиямБезВводаГруза.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз))) КАК ВидГруза
	|ПОМЕСТИТЬ втГрузовыеМеста10
	|ИЗ
	|	втГрузовыеМеста05 КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияШапка10 КАК втЗаданияШапка
	|		ПО втГрузовыеМеста.ЗаданиеНаПеревозку = втЗаданияШапка.ЗаданиеНаПеревозку
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК упТипыГрузов
	|			ПО упГрузовыеМеста.ТипГруза = упТипыГрузов.Ссылка
	|		ПО втГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|			И (НЕ втГрузовыеМеста.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВидыГрузаПоЗаданиямБезВводаГруза КАК втВидыГрузаПоЗаданиямБезВводаГруза
	|	ПО втГрузовыеМеста.ЗаданиеНаПеревозку = втВидыГрузаПоЗаданиямБезВводаГруза.ЗаданиеНаПеревозкуГруза	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ЗаданиеНаПеревозку,
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.Тара,
	|	втГрузовыеМеста.ТипГруза,
	|	втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.ВидАдресаОтправления КАК ВидТочки,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.ВидГруза В (ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз), ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|	КОНЕЦ КАК ТипОперации
	|ПОМЕСТИТЬ втГрузовыеМеста15
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста";
	
	Если ФормироватьПолныйМаршрут Тогда
		ТекстЗапроса = ТекстЗапроса + ОбъединитьВсе + 
		"ВЫБРАТЬ
		|	втГрузовыеМеста.ЗаданиеНаПеревозку,
		|	втГрузовыеМеста.ГрузовоеМесто,
		|	втГрузовыеМеста.Тара,
		|	втГрузовыеМеста.ТипГруза,
		|	втГрузовыеМеста.КоличествоГрузов,
		|	втГрузовыеМеста.ВидАдресаПолучения,
		|	ВЫБОР
		|		КОГДА втГрузовыеМеста.ВидГруза В (ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз), ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|	КОНЕЦ КАК Поле1
		|ИЗ
		|	втГрузовыеМеста10 КАК втГрузовыеМеста";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + Разделитель + "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втГрузовыеМеста.ТипОперации КАК ТипОперации,
	|	СУММА(упВремяВыполненияРабот.Время / 3600 * упГрузовыеМеста.КоличествоГрузов) КАК ВремяРабот
	|ПОМЕСТИТЬ втВремяРаботВЦеломПоЗаданиям
	|ИЗ
	|	втГрузовыеМеста15 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упГрузовыеМеста
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		И втГрузовыеМеста.ЗаданиеНаПеревозку = упГрузовыеМеста.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упВремяВыполненияРабот КАК упВремяВыполненияРабот
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ВидТочки = упВремяВыполненияРабот.ВидТочки
	|		И втГрузовыеМеста.ТипОперации = упВремяВыполненияРабот.ТипОперации
	|		И упГрузовыеМеста.ГрузовоеМесто.ТипГруза = упВремяВыполненияРабот.ТипГруза
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.ЗаданиеНаПеревозку,
	|	втГрузовыеМеста.ТипОперации
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста15.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста15.Тара КАК Тара,
	|	МАКСИМУМ(упОстаткиГрузовыхМестОстатки.СкладскаяЯчейка) КАК СкладскаяЯчейка
	|ПОМЕСТИТЬ втОстаткиГрузовыхМест
	|ИЗ
	|	втГрузовыеМеста15 КАК втГрузовыеМеста15
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияШапка10 КАК втЗаданияШапка10
	|	ПО втГрузовыеМеста15.ЗаданиеНаПеревозку = втЗаданияШапка10.ЗаданиеНаПеревозку
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиГрузовыхМест.Остатки КАК упОстаткиГрузовыхМестОстатки
	|	ПО ИСТИНА
	|		И втГрузовыеМеста15.ГрузовоеМесто = упОстаткиГрузовыхМестОстатки.ГрузовоеМесто
	|		И втГрузовыеМеста15.Тара = упОстаткиГрузовыхМестОстатки.Тара
	|		И втЗаданияШапка10.АдресОтправления.Склад = упОстаткиГрузовыхМестОстатки.СкладскаяЯчейка.Владелец
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста15.ГрузовоеМесто,
	|	втГрузовыеМеста15.Тара
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.ВидТочки КАК ВидТочки,
	|	втГрузовыеМеста.ТипОперации КАК ТипОперации,
	|	ЕСТЬNULL(упВремяВыполненияРабот.Время / 3600 * втГрузовыеМеста.КоличествоГрузов, ЕСТЬNULL(втВремяРаботВЦеломПоЗаданиям.ВремяРабот, 0)) КАК ВремяРабот
	|ПОМЕСТИТЬ втВремяПоГрузовымМестам
	|ИЗ
	|	втГрузовыеМеста15 КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВремяВыполненияРабот КАК упВремяВыполненияРабот
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ВидТочки = упВремяВыполненияРабот.ВидТочки
	|		И втГрузовыеМеста.ТипОперации = упВремяВыполненияРабот.ТипОперации
	|		И втГрузовыеМеста.ТипГруза = упВремяВыполненияРабот.ТипГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВремяРаботВЦеломПоЗаданиям КАК втВремяРаботВЦеломПоЗаданиям
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ЗаданиеНаПеревозку = втВремяРаботВЦеломПоЗаданиям.ЗаданиеНаПеревозку
	|		И втГрузовыеМеста.ТипОперации = втВремяРаботВЦеломПоЗаданиям.ТипОперации
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВидыТочекОтправления.ВидТочкиОтправления ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриоритетДепо,
	|	втЗаданияШапка.АдресОтправления КАК Адрес,
	|	втЗаданияШапка.ОтправлениеГрузаС КАК ВременноеОкноНачало,
	|	втЗаданияШапка.ОтправлениеГрузаПо КАК ВременноеОкноОкончание,
	|	втВидыПеревозок.ВидЗоныДляФормированияПредставленияРейса КАК ВидЗоныДляФормированияПредставленияРейса,
	|	втВидыПеревозок.ВыборПеревозчика КАК ВыборПеревозчика,
	|	втВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах,
	|	втВидыПеревозок.ИспользуютсяУслугиИнкассации КАК ИспользуютсяУслугиИнкассации
	|ПОМЕСТИТЬ втВидыПеревозок05
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок02 КАК втВидыПеревозок
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
	|	ПО ИСТИНА
	|		И ВидыТочекОтправления.Ссылка = &ВидПеревозки
	|		И втЗаданияШапка.АдресОтправления.ВидАдреса = ВидыТочекОтправления.ВидТочкиОтправления
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗаданияШапка.ПакетРегламентныхДокументов КАК ПакетРегламентныхДокументов
	|ПОМЕСТИТЬ втПакетРегламентныхДокументов
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|ГДЕ
	|	НЕ &ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПакет.ПакетРегламентныхДокументов КАК ПакетРегламентныхДокументов,
	|	ПакетДокументов.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	ПакетДокументов.Участник КАК Участник,
	|	ПакетДокументов.КоличествоПолучитьОтУчастника КАК КоличествоПолучитьОтУчастника,
	|	ПакетДокументов.КоличествоПередатьУчастнику КАК КоличествоПередатьУчастнику,
	|	ПакетДокументов.ВозвратДокументовПослеИсполненияРейса КАК ВозвратДокументовПослеИсполненияРейса
	|ПОМЕСТИТЬ втДокументыПакетов
	|ИЗ
	|	втПакетРегламентныхДокументов КАК втПакет
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыРегламентныхДокументов.ПакетДокументов КАК ПакетДокументов
	|	ПО втПакет.ПакетРегламентныхДокументов = ПакетДокументов.Ссылка
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияШапка.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка) КАК ВлияющаяОперация,
	|	втДокументыПакетов.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	втДокументыПакетов.КоличествоПолучитьОтУчастника КАК КоличествоЭкземпляровДокумента
	|ПОМЕСТИТЬ втПолучениеПередачаДокументов
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПакетов КАК втДокументыПакетов
	|	ПО ИСТИНА
	|		И втЗаданияШапка.ПакетРегламентныхДокументов = втДокументыПакетов.ПакетРегламентныхДокументов
	|		И втДокументыПакетов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Грузоотправитель)
	|		И втДокументыПакетов.КоличествоПолучитьОтУчастника > 0
	|";

	Если ФормироватьПолныйМаршрут Тогда
		ТекстЗапроса = ТекстЗапроса + ОбъединитьВсе + 
		"ВЫБРАТЬ
		|	втЗаданияШапка.ЗаданиеНаПеревозку,
		|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов) КАК Поле1,
		|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка) КАК Поле2,
		|	втДокументыПакетов.ТипРегламентногоДокумента,
		|	втДокументыПакетов.КоличествоПередатьУчастнику
		|ИЗ
		|	втЗаданияШапка10 КАК втЗаданияШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПакетов КАК втДокументыПакетов
		|		ПО втЗаданияШапка.ПакетРегламентныхДокументов = втДокументыПакетов.ПакетРегламентныхДокументов
		|			И (втДокументыПакетов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Грузополучатель))
		|			И (втДокументыПакетов.КоличествоПередатьУчастнику > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втЗаданияШапка.ЗаданиеНаПеревозку,
		|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов),
		|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка),
		|	втДокументыПакетов.ТипРегламентногоДокумента,
		|	втДокументыПакетов.КоличествоПолучитьОтУчастника
		|ИЗ
		|	втЗаданияШапка10 КАК втЗаданияШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПакетов КАК втДокументыПакетов
		|		ПО втЗаданияШапка.ПакетРегламентныхДокументов = втДокументыПакетов.ПакетРегламентныхДокументов
		|			И (втДокументыПакетов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Грузополучатель))
		|			И (втДокументыПакетов.КоличествоПолучитьОтУчастника > 0)";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + ОбъединитьВсе + "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияШапка.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов) КАК Поле1,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка) КАК Поле2,
	|	втДокументыПакетов.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	втДокументыПакетов.КоличествоПолучитьОтУчастника КАК КоличествоПолучитьОтУчастника
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПакетов КАК втДокументыПакетов
	|	ПО ИСТИНА
	|		И втЗаданияШапка.ПакетРегламентныхДокументов = втДокументыПакетов.ПакетРегламентныхДокументов
	|		И втДокументыПакетов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Заказчик)
	|		И НЕ (втДокументыПакетов.ВозвратДокументовПослеИсполненияРейса)
	|		И втДокументыПакетов.КоличествоПолучитьОтУчастника > 0
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияШапка.ЗаданиеНаПеревозку КАК Задание,
	|	втЗаданияШапка.АдресВозвратаДокументов КАК АдресВозвратаДокументов,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов) КАК Операция,
	|	втДокументыПакетов.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	втДокументыПакетов.КоличествоПередатьУчастнику КАК КоличествоЭкземпляровДокумента
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПакетов КАК втДокументыПакетов
	|	ПО ИСТИНА
	|		И втЗаданияШапка.ПакетРегламентныхДокументов = втДокументыПакетов.ПакетРегламентныхДокументов
	|		И втДокументыПакетов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Заказчик)
	|		И НЕ (втДокументыПакетов.ВозвратДокументовПослеИсполненияРейса)
	|		И втДокументыПакетов.КоличествоПередатьУчастнику > 0
	|		И втЗаданияШапка.АдресВозвратаДокументов <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втЗаданияШапка.ЗаданиеНаПеревозку КАК Задание,
	|	втЗаданияШапка.АдресОтправления КАК АдресВозвратаДокументов,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов) КАК Операция,
	|	втДокументыПакетов.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	втДокументыПакетов.КоличествоПередатьУчастнику КАК КоличествоЭкземпляровДокумента
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПакетов КАК втДокументыПакетов
	|	ПО ИСТИНА
	|		И втЗаданияШапка.ПакетРегламентныхДокументов = втДокументыПакетов.ПакетРегламентныхДокументов
	|		И втДокументыПакетов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Грузоотправитель)
	|		И втДокументыПакетов.КоличествоПередатьУчастнику > 0
	|ИТОГИ
	|ПО
	|	АдресВозвратаДокументов
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Гаражи.Гараж КАК Гараж,
	|	Гаражи.Гараж.Адрес КАК Адрес
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(Гаражи.Гараж) КАК Гараж
	|	ИЗ
	|		Справочник.упТранспортныеСредства.Гаражи КАК Гаражи
	|	ГДЕ Гаражи.Ссылка = &ТранспортноеСредство) КАК Гаражи
	|ГДЕ НЕ (Гаражи.Гараж.Адрес ЕСТЬ NULL)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втВидыПеревозок.Адрес) КАК Адрес,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА &ПлановоеНачалоВыполнения <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ПлановоеНачалоВыполнения
	|		КОГДА втВидыПеревозок.ВременноеОкноНачало <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втВидыПеревозок.ВременноеОкноНачало
	|		ИНАЧЕ &ТекущаяДата
	|	КОНЕЦ) КАК ПлановоеПрибытие,
	|	втВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах,
	|	втВидыПеревозок.ВидЗоныДляФормированияПредставленияРейса КАК ВидЗоныДляФормированияПредставленияРейса,
	|	втВидыПеревозок.ИспользуютсяУслугиИнкассации КАК ИспользуютсяУслугиИнкассации
	|ИЗ
	|	втВидыПеревозок05 КАК втВидыПеревозок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		МИНИМУМ(втВидыПеревозок.ПриоритетДепо) КАК ПриоритетДепо
	|	ИЗ
	|		втВидыПеревозок05 КАК втВидыПеревозок) КАК ВложенныйЗапрос
	|	ПО втВидыПеревозок.ПриоритетДепо = ВложенныйЗапрос.ПриоритетДепо
	|СГРУППИРОВАТЬ ПО
	|	втВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах,
	|	втВидыПеревозок.ВидЗоныДляФормированияПредставленияРейса,
	|	втВидыПеревозок.ИспользуютсяУслугиИнкассации
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияШапка.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втЗаданияШапка.Организация КАК Организация,
	|	втЗаданияШапка.АдресОтправления КАК АдресОтправления,
	|	втЗаданияШапка.АдресПолучения КАК АдресПолучения,
	|	втЗаданияШапка.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЗаданияШапка.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЗаданияШапка.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЗаданияШапка.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ВидыТочекОтправленияАдресПолучения.ВидТочкиОтправления ЕСТЬ NULL
	|					И ВидыТочекОтправленияАдресОтправления.ВидТочкиОтправления ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЭтоВозвратВБазовыйАдрес,
	|	втЗаданияШапка.ЧасовойПояс КАК ЧасовойПояс
	|ИЗ
	|	втЗаданияШапка10 КАК втЗаданияШапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправленияАдресПолучения
	|		ПО (ИСТИНА)
	|			И (ВидыТочекОтправленияАдресПолучения.Ссылка = втЗаданияШапка.ЗаданиеНаПеревозку.ВидПеревозки)
	|			И втЗаданияШапка.ЗаданиеНаПеревозку.АдресПолучения.ВидАдреса = ВидыТочекОтправленияАдресПолучения.ВидТочкиОтправления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправленияАдресОтправления
	|		ПО (ИСТИНА)
	|			И (ВидыТочекОтправленияАдресОтправления.Ссылка = втЗаданияШапка.ЗаданиеНаПеревозку.ВидПеревозки)
	|			И втЗаданияШапка.ЗаданиеНаПеревозку.АдресОтправления.ВидАдреса = ВидыТочекОтправленияАдресОтправления.ВидТочкиОтправления
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗаданияШапка.ЗаданиеНаПеревозку,
	|	втЗаданияШапка.Организация,
	|	втЗаданияШапка.АдресОтправления,
	|	втЗаданияШапка.АдресПолучения,
	|	втЗаданияШапка.ОтправлениеГрузаС,
	|	втЗаданияШапка.ОтправлениеГрузаПо,
	|	втЗаданияШапка.ПолучениеГрузаС,
	|	втЗаданияШапка.ПолучениеГрузаПо,
	|	втЗаданияШапка.ЧасовойПояс
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоВозвратВБазовыйАдрес,
	|	втЗаданияШапка.АдресОтправления
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВремяПоГрузовымМестам.ЗаданиеНаПеревозку КАК Задание,
	|	втВремяПоГрузовымМестам.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втВремяПоГрузовымМестам.ГрузовоеМесто.Тара КАК ЭтоТара,
	|	втВремяПоГрузовымМестам.Тара КАК Тара,
	|	NULL КАК ДополнительнаяОперация,
	|	втВремяПоГрузовымМестам.КоличествоГрузов КАК КоличествоГрузов,
	|	0 КАК КоличествоДопОпераций,
	|	втВремяПоГрузовымМестам.ТипОперации КАК Операция,
	|	ВЫБОР
	|		КОГДА втВремяПоГрузовымМестам.ТипОперации.Порядок = 0
	|			ТОГДА втЗаданияШапка.ОтправлениеГрузаС
	|		ИНАЧЕ втЗаданияШапка.ПолучениеГрузаС
	|	КОНЕЦ КАК ВременноеОкноНачало,
	|	ВЫБОР
	|		КОГДА втВремяПоГрузовымМестам.ТипОперации.Порядок = 0
	|			ТОГДА втЗаданияШапка.ОтправлениеГрузаПо
	|		ИНАЧЕ втЗаданияШапка.ПолучениеГрузаПо
	|	КОНЕЦ КАК ВременноеОкноОкончание,
	|	втВремяПоГрузовымМестам.ВремяРабот КАК ВремяРабот,
	|	NULL КАК ВлияющаяОперация,
	|	NULL КАК ТипРегламентногоДокумента,
	|	NULL КАК КоличествоЭкземпляровДокумента,
	|	втВремяПоГрузовымМестам.ТипОперации.Порядок КАК Порядок,
	|	ЕСТЬNULL(упГрузовыеМеста.Сумма, 0) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втВремяПоГрузовымМестам.ГрузовоеМесто.ТипГруза.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|				И ЕСТЬNULL(втВремяПоГрузовымМестам.ГрузовоеМесто.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки, ЛОЖЬ)
	|				И втВидыПеревозок02Т.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)) 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИнкассацияЗаГруз,
	|	ЕСТЬNULL(втОстаткиГрузовыхМест.СкладскаяЯчейка, ЗНАЧЕНИЕ(СПравочник.упСкладскиеЯчейки.ПустаяСсылка)) КАК СкладскаяЯчейка
	|ИЗ
	|	втВремяПоГрузовымМестам КАК втВремяПоГрузовымМестам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияШапка10 КАК втЗаданияШапка
	|	ПО втВремяПоГрузовымМестам.ЗаданиеНаПеревозку = втЗаданияШапка.ЗаданиеНаПеревозку
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок02 КАК втВидыПеревозок02Т
	|	ПО (ИСТИНА)
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|	ПО ИСТИНА
	|		И втВремяПоГрузовымМестам.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|		И упГрузовыеМеста.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиГрузовыхМест КАК втОстаткиГрузовыхМест
	|	ПО ИСТИНА
	|		И втВремяПоГрузовымМестам.ГрузовоеМесто = втОстаткиГрузовыхМест.ГрузовоеМесто
	|		И втВремяПоГрузовымМестам.Тара = втОстаткиГрузовыхМест.Тара
	|		И втВремяПоГрузовымМестам.ТипОперации = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втЗаданияШапка.ЗаданиеНаПеревозку КАК Задание,
	|	NULL КАК ГрузовоеМесто,
	|	NULL КАК ЭтоТара,
	|	NULL КАК Тара,
	|	упЗаданиеДополнительныеОперации.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|	NULL КАК КоличествоГрузов,
	|	упЗаданиеДополнительныеОперации.Количество КАК КоличествоДопОпераций,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ДополнительнаяОперация) КАК Операция,
	|	ВЫБОР
	|		КОГДА упЗаданиеДополнительныеОперации.ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ТОГДА втЗаданияШапка.ОтправлениеГрузаС
	|		ИНАЧЕ втЗаданияШапка.ПолучениеГрузаС
	|	КОНЕЦ КАК ВременноеОкноНачало,
	|	ВЫБОР
	|		КОГДА упЗаданиеДополнительныеОперации.ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ТОГДА втЗаданияШапка.ОтправлениеГрузаПо
	|		ИНАЧЕ втЗаданияШапка.ПолучениеГрузаПо
	|	КОНЕЦ КАК ВременноеОкноОкончание,
	|	упЗаданиеДополнительныеОперации.ДополнительнаяОперация.ВремяВыполненияОперации * упЗаданиеДополнительныеОперации.Количество КАК ВремяРабот,
	|	упЗаданиеДополнительныеОперации.ТипТочки КАК ВлияющаяОперация,
	|	NULL КАК ТипРегламентногоДокумента,
	|	NULL КАК КоличествоЭкземпляровДокумента,
	|	3 КАК Порядок,
	|	0 КАК Сумма,
	|	ЛОЖЬ КАК ИнкассацияЗаГруз,
	|	ЗНАЧЕНИЕ(СПравочник.упСкладскиеЯчейки.ПустаяСсылка) КАК СкладскаяЯчейка
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ДополнительныеОперации КАК упЗаданиеДополнительныеОперации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.упИспользуютсяДополнительныеОперации КАК упИспользуютсяДополнительныеОперации
	|	ПО упИспользуютсяДополнительныеОперации.Значение = ИСТИНА
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияШапка10 КАК втЗаданияШапка
	|	ПО упЗаданиеДополнительныеОперации.Ссылка = втЗаданияШапка.ЗаданиеНаПеревозку
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втПолучениеПередачаДокументов.ЗаданиеНаПеревозку КАК Задание,
	|	NULL КАК ГрузовоеМесто,
	|	NULL КАК ЭтоТара,
	|	NULL КАК Тара,
	|	NULL КАК ДополнительнаяОперация,
	|	NULL КАК КоличествоГрузов,
	|	NULL КАК КоличествоДопОпераций,
	|	втПолучениеПередачаДокументов.Операция КАК Операция,
	|	NULL КАК ВременноеОкноНачало,
	|	NULL КАК ВременноеОкноОкончание,
	|	NULL КАК ВремяРабот,
	|	втПолучениеПередачаДокументов.ВлияющаяОперация КАК ВлияющаяОперация,
	|	втПолучениеПередачаДокументов.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	СУММА(втПолучениеПередачаДокументов.КоличествоЭкземпляровДокумента) КАК КоличествоЭкземпляровДокумента,
	|	втПолучениеПередачаДокументов.Операция.Порядок КАК Порядок,
	|	0 КАК Сумма,
	|	ЛОЖЬ КАК ИнкассацияЗаГруз,
	|	ЗНАЧЕНИЕ(СПравочник.упСкладскиеЯчейки.ПустаяСсылка) КАК СкладскаяЯчейка
	|ИЗ
	|	втПолучениеПередачаДокументов КАК втПолучениеПередачаДокументов
	|СГРУППИРОВАТЬ ПО
	|	втПолучениеПередачаДокументов.ЗаданиеНаПеревозку,
	|	втПолучениеПередачаДокументов.Операция,
	|	втПолучениеПередачаДокументов.ВлияющаяОперация,
	|	втПолучениеПередачаДокументов.ТипРегламентногоДокумента,
	|	втПолучениеПередачаДокументов.Операция.Порядок
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|";

	Возврат ТекстЗапроса;
		
КонецФункции

Функция ПолучитьТекущийМаршрутРейса(Рейс)
	
	тзМаршрут = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.*
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		тзМаршрут = Результат.Выгрузить();
	КонецЕсли; 
	
	Возврат тзМаршрут;
	
КонецФункции

Функция ПолучитьТекущиеРаботыРейса(Рейс)
	
	тзРаботы = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс,
	|	упРаботыПоРейсуСрезПоследних.Идентификатор,
	|	упРаботыПоРейсуСрезПоследних.ИдентификаторРаботы,
	|	упРаботыПоРейсуСрезПоследних.СтрокаНомер,
	|	упРаботыПоРейсуСрезПоследних.Отменена,
	|	упРаботыПоРейсуСрезПоследних.Задание,
	|	упРаботыПоРейсуСрезПоследних.Операция,
	|	упРаботыПоРейсуСрезПоследних.ДополнительнаяОперация,
	|	упРаботыПоРейсуСрезПоследних.ВремяРабот,
	|	упРаботыПоРейсуСрезПоследних.ВременноеОкноНачало,
	|	упРаботыПоРейсуСрезПоследних.ВременноеОкноОкончание,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто,
	|	упРаботыПоРейсуСрезПоследних.Тара,
	|	упРаботыПоРейсуСрезПоследних.ВремяОжидания,
	|	упРаботыПоРейсуСрезПоследних.ПлановоеВремяНачалаРабот,
	|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов,
	|	упРаботыПоРейсуСрезПоследних.КоличествоДопОпераций,
	|	упРаботыПоРейсуСрезПоследних.Проблема,
	|	упРаботыПоРейсуСрезПоследних.АдресРазгрузки,
	|	упРаботыПоРейсуСрезПоследних.Выполнена,
	|	упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы,
	|	упРаботыПоРейсуСрезПоследних.ТипРегламентногоДокумента,
	|	упРаботыПоРейсуСрезПоследних.КоличествоЭкземпляровДокумента,
	|	упРаботыПоРейсуСрезПоследних.Масса,
	|	упРаботыПоРейсуСрезПоследних.Объем
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		тзРаботы = Результат.Выгрузить();
	КонецЕсли; 
	
	Возврат тзРаботы;
	
КонецФункции

Процедура СформироватьМаршрутРаботы(Маршрут, Работы, Реквизиты, ДополнительныеСвойства, Ссылка)
	
	Если Ложь Тогда // Для контекстной подсказки
	    Реквизиты = Документы.упРейс.СоздатьДокумент();
		Маршрут = Новый ТаблицаЗначений;
		Работы = Новый ТаблицаЗначений;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаМаршрутРаботыПоРейсу(Реквизиты.ВариантФормированияМаршрута);
	Запрос.УстановитьПараметр("Задания", Реквизиты.Задания.Выгрузить());
	Запрос.УстановитьПараметр("ВидПеревозки", Реквизиты.ВидПеревозки);
	Запрос.УстановитьПараметр("ТранспортноеСредство", Реквизиты.ТранспортноеСредство);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ПлановоеНачалоВыполнения", ?(ДополнительныеСвойства.Свойство("ПлановоеНачалоВыполнения"), ДополнительныеСвойства.ПлановоеНачалоВыполнения, '00010101'));
	Запрос.УстановитьПараметр("ВидТранспортнойУслуги"   , Реквизиты.ВидТранспортнойУслуги);
	ВариантМаршрутаПолный =  упКорректировкаМаршрута.ВариантФормированияМаршрутаПолный(Реквизиты.ВариантФормированияМаршрута);
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	СписокЗаданий = Реквизиты.Задания.Выгрузить();
	ПравилоСвязиТиповГрузовВРейсе = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.ВидПеревозки, "ПравилоСвязиТиповГрузовВРейсе");
	Если ЗначениеЗаполнено(ПравилоСвязиТиповГрузовВРейсе) Тогда
		ТаблицаЗависимых = ЗависимыеЗаданияПоСпискуЗаданий(СписокЗаданий);
	КонецЕсли; 
	
	ВедетсяРаботаВРазныхЧасовыхПоясах =  ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах");
	
	Если Не Результат[КоличествоТаблиц - 2].Пустой() Тогда
		ВыборкаВидыПеревозок = Результат[КоличествоТаблиц - 3].Выбрать();	
		ВыборкаВидыПеревозок.Следующий();
		
		Если Истина
			И ВыборкаВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса
			И ВыборкаВидыПеревозок.ИспользуютсяУслугиИнкассации Тогда
			ПроверитьИнкассациюЗаГруз = Истина;
		Иначе
			ПроверитьИнкассациюЗаГруз = Ложь;
		КонецЕсли;			
		
		ВыгрузкаЗаданий      = Результат[КоличествоТаблиц - 2].Выгрузить();	
		ВыгрузкаРабот        = Результат[КоличествоТаблиц - 1].Выгрузить();	
		Если Не Результат[КоличествоТаблиц - 4].Пустой() Тогда
			ДанныеГараж = Результат[КоличествоТаблиц - 4].Выбрать();
			ДанныеГараж.Следующий();
		Иначе
			ДанныеГараж = Неопределено;
		КонецЕсли; 
		
		Если Не Результат[КоличествоТаблиц - 5].Пустой() Тогда
			ДанныеВозвратДокументов = Результат[КоличествоТаблиц - 5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Иначе
			ДанныеВозвратДокументов = Неопределено;
		КонецЕсли; 
		Маршрут = СоздатьТаблицуМаршрут();
		Работы  = СоздатьТаблицуРаботы();
		
		соотТочкиЗаданий = Новый Соответствие;
		
		мсвАдресовОтправления = ВыгрузкаЗаданий.ВыгрузитьКолонку("АдресОтправления");
		мсвАдресовОтправления = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвАдресовОтправления);
		соотВидовАдресов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(мсвАдресовОтправления, "ВидАдреса");
		
		Для каждого СтрокаЗадания Из ВыгрузкаЗаданий Цикл
			
			ТекущееЗадание = СтрокаЗадания.ЗаданиеНаПеревозку;
			текАдресОтправления = СтрокаЗадания.АдресОтправления;
			текАдресПолучения	= СтрокаЗадания.АдресПолучения;
			ЧасовойПоясЗадания	= СтрокаЗадания.ЧасовойПояс;
			СтруктураСвязанных = СтруктураСвязанныхЗаданийДляЗадания(ПравилоСвязиТиповГрузовВРейсе, ТаблицаЗависимых, ТекущееЗадание);

			сткРаботПоЗаданию	= Новый Структура("ОсновныеРаботы, ДополнительныеРаботы, ПолучениеПередачаДокументов");
			мсвОсновныеРаботы	           = Новый Массив;
			мсвДополнительныеРаботы	       = Новый Массив;
			мсвПолучениеПередачаДокументов = Новый Массив;
			
			// накопление тары
			мсвТары                        = Новый Массив;
			мсвВТаре                       = Новый Массив;
			СтруктураТары                  = Неопределено;			
			
			ИскомыеРаботы = ВыгрузкаРабот.НайтиСтроки(Новый Структура("Задание, ИнкассацияЗаГруз", ТекущееЗадание, Ложь));
			Для каждого текРабота Из ИскомыеРаботы Цикл
				Если упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(текРабота.Операция) Тогда
					сткРабота = Новый Структура("Задание, ГрузовоеМесто, Тара, КоличествоГрузов, Операция, ВремяРабот, ВременноеОкноНачало, ВременноеОкноОкончание, Сумма, СкладскаяЯчейка");
					ЗаполнитьЗначенияСвойств(сткРабота, текРабота);
					Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
						ПеревестиВременноеОкноЗаданияВЧасовойПоясРейса(сткРабота, ЧасовойПоясЗадания, ДополнительныеСвойства.ЧасовойПояс);
					КонецЕсли;
					мсвОсновныеРаботы.Добавить(сткРабота);
					
					Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
						Если ЗначениеЗаполнено(текРабота.ГрузовоеМесто) Тогда
							Если текРабота.ЭтоТара Тогда
								мсвТары.Добавить(текРабота.ГрузовоеМесто);
							КонецЕсли;
							Если ЗначениеЗаполнено(текРабота.Тара) Тогда
								мсвВТаре.Добавить(текРабота.Тара);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли; 
					
				ИначеЕсли текРабота.Операция = Перечисления.упТипыОпераций.ПередачаДокументов Или текРабота.Операция = Перечисления.упТипыОпераций.ПолучениеДокументов Тогда	
					сткРабота = Новый Структура("Задание, Операция, ВлияющаяОперация, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента");
					ЗаполнитьЗначенияСвойств(сткРабота, текРабота);
					мсвПолучениеПередачаДокументов.Добавить(сткРабота);
				ИначеЕсли текРабота.Операция = Перечисления.упТипыОпераций.ДополнительнаяОперация Тогда	
					сткРабота = Новый Структура("Задание, Операция, ВлияющаяОперация, ВремяРабот, ВременноеОкноНачало, ВременноеОкноОкончание, ДополнительнаяОперация, КоличествоДопОпераций");
					ЗаполнитьЗначенияСвойств(сткРабота, текРабота); 
					Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
						ПеревестиВременноеОкноЗаданияВЧасовойПоясРейса(сткРабота, ЧасовойПоясЗадания, ДополнительныеСвойства.ЧасовойПояс);
					КонецЕсли;
					мсвДополнительныеРаботы.Добавить(сткРабота);
				КонецЕсли;
			КонецЦикла; 
			
			сткРаботПоЗаданию.ОсновныеРаботы              = мсвОсновныеРаботы;
			сткРаботПоЗаданию.ДополнительныеРаботы        = мсвДополнительныеРаботы;
			сткРаботПоЗаданию.ПолучениеПередачаДокументов = мсвПолучениеПередачаДокументов;
			
			Если мсвТары.Количество() Или мсвВТаре.Количество() Тогда
				СтруктураТары = Новый Структура;
				СтруктураТары.Вставить("Тара", ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвТары));
				СтруктураТары.Вставить("ВТаре",  ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВТаре));
			КонецЕсли; 
			
			сткВозврат = упКорректировкаМаршрута.ДобавитьРаботыПоЗаданиюВМаршрут(Ссылка, Маршрут, Работы, текАдресОтправления, текАдресПолучения, ТекущееЗадание,, Ложь, сткРаботПоЗаданию,,,
				Реквизиты.ВариантФормированияМаршрута, СтруктураТары, СтруктураСвязанных, СтрокаЗадания.ЭтоВозвратВБазовыйАдрес,ДополнительныеСвойства.ЧасовойПояс, ОбщегоНазначенияКлиентСервер.СкопироватьСоответствие(соотВидовАдресов)); 
			
			мсвИдентификатор = Новый Массив;
			мсвИдентификатор.Добавить(сткВозврат.ТочкаПогрузки.Идентификатор);
			Если ВариантМаршрутаПолный Тогда
				мсвИдентификатор.Добавить(сткВозврат.ТочкаРазгрузки.Идентификатор);
			КонецЕсли;
			
			соотТочкиЗаданий.Вставить(ТекущееЗадание, мсвИдентификатор);
						
			Если ПроверитьИнкассациюЗаГруз Тогда
				сткРаботПоЗаданию	= Новый Структура("ОсновныеРаботы, ДополнительныеРаботы, ПолучениеПередачаДокументов");
				мсвОсновныеРаботы	           = Новый Массив;
				
				ИскомыеРаботы = ВыгрузкаРабот.НайтиСтроки(Новый Структура("Задание, ИнкассацияЗаГруз", ТекущееЗадание, Истина));
				Для каждого текРабота Из ИскомыеРаботы Цикл
					Если упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(текРабота.Операция) Тогда
						сткРабота = Новый Структура("Задание, ГрузовоеМесто, Тара, КоличествоГрузов, Операция, ВремяРабот, ВременноеОкноНачало, ВременноеОкноОкончание, Сумма");
						ЗаполнитьЗначенияСвойств(сткРабота, текРабота);
						
						Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
							ПеревестиВременноеОкноЗаданияВЧасовойПоясРейса(сткРабота, ЧасовойПоясЗадания, ДополнительныеСвойства.ЧасовойПояс);
						КонецЕсли;
						
						мсвОсновныеРаботы.Добавить(сткРабота);
					КонецЕсли;
				КонецЦикла; 
				Если мсвОсновныеРаботы.Количество() > 0 Тогда
					сткРаботПоЗаданию.ОсновныеРаботы = мсвОсновныеРаботы;
					сткРаботПоЗаданию.ДополнительныеРаботы        = Новый Массив;
					сткРаботПоЗаданию.ПолучениеПередачаДокументов = Новый Массив;
					
					сткВозврат =  упКорректировкаМаршрута.ДобавитьРаботыПоЗаданиюВМаршрут(Ссылка, Маршрут, Работы, текАдресПолучения, текАдресОтправления, ТекущееЗадание,, Ложь, сткРаботПоЗаданию,,,
					Реквизиты.ВариантФормированияМаршрута, Неопределено, СтруктураСвязанных,,ДополнительныеСвойства.ЧасовойПояс, ОбщегоНазначенияКлиентСервер.СкопироватьСоответствие(соотВидовАдресов));
				КонецЕсли; 
			КонецЕсли;
			
		КонецЦикла; 
		
		// Если рейс создается после планирования, требуется упорядочить точки маршрута.
		Если ДополнительныеСвойства.Свойство("ТаблицаАдресов") Тогда
			УчитыватьВременныеОкна = (ДополнительныеСвойства.Свойство("УчитыватьВременныеОкна") И ДополнительныеСвойства.УчитыватьВременныеОкна);
			тзАдресов = ДополнительныеСвойства.ТаблицаАдресов;
			
			Если Маршрут.Количество() Тогда
				КопияМаршрута = Маршрут.Скопировать();
				УспешноеУпорядочивание = Истина;
				
				Для ИндексТочки = 0 По Маршрут.Количество() - 1 Цикл
					НайденнаяСтрока = Маршрут[ИндексТочки];
					Если Не ПроверитьСдвинутьСтрокуМаршрутаСУчетомПредшественников(Маршрут, НайденнаяСтрока, ИндексТочки) Тогда 
						УспешноеУпорядочивание = Ложь;
					КонецЕсли; 
				КонецЦикла;
				
				Сч = 0; // возврат
				// Может быть несколько строк для адреса с разными временными окнами. В этом случае они будут в маршруте слиты в одну точку. Защитимся от явных ошибок.
				ОбработанныеАдреса = Новый Соответствие;
				Для каждого Точка Из тзАдресов Цикл
					Если ОбработанныеАдреса[Точка.Адрес] = 1 Тогда
						Продолжить;
					КонецЕсли; 
					ОбработанныеАдреса[Точка.Адрес] = 1;
					НайденныеСтроки = Маршрут.НайтиСтроки(Новый Структура("Адрес", Точка.Адрес));
					Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
						Если Не ПроверитьСдвинутьСтрокуМаршрутаСУчетомПредшественников(Маршрут, НайденнаяСтрока, Сч) Тогда 
							УспешноеУпорядочивание = Ложь;
						КонецЕсли; 
						Если УчитыватьВременныеОкна И Не Точка.ПолнаяДоступность Тогда
							ЗаполнитьЗначенияСвойств(НайденнаяСтрока, Точка);
							ИскомыеРаботы = Работы.НайтиСтроки(Новый Структура("Идентификатор", НайденнаяСтрока.Идентификатор));
							Для каждого текРабота Из ИскомыеРаботы Цикл
								ЗаполнитьЗначенияСвойств(текРабота, Точка);
							КонецЦикла;
						КонецЕсли;
						Сч = Сч + 1;
						Прервать; // в случае планирования с нескольких складов в тзАдресов будут присутствовать склады, для которых могут быть возвраты в рамках одного маршрута
					КонецЦикла;
				КонецЦикла; 
				Для ИндексТочки = Сч По Маршрут.Количество() - 1 Цикл
					НайденнаяСтрока = Маршрут[ИндексТочки];
					Если Не ПроверитьСдвинутьСтрокуМаршрутаСУчетомПредшественников(Маршрут, НайденнаяСтрока, ИндексТочки) Тогда 
						УспешноеУпорядочивание = Ложь;
					КонецЕсли; 
				КонецЦикла;
				Если Не УспешноеУпорядочивание Тогда
					Маршрут = КопияМаршрута;
				КонецЕсли; 
				
				ТочкаДепоБезРабот = тзАдресов.Найти(Истина, "БезРабот");
				Если Не ТочкаДепоБезРабот = Неопределено Тогда
					Если Маршрут.Найти(ТочкаДепоБезРабот.Адрес, "Адрес") = Неопределено Тогда
						НоваяТочка = Маршрут.Вставить(0);
						НоваяТочка.Адрес = ТочкаДепоБезРабот.Адрес;
						НоваяТочка.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию;
						НоваяТочка.Идентификатор = Новый УникальныйИдентификатор;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		
		Если ДанныеВозвратДокументов <> Неопределено Тогда
			Пока ДанныеВозвратДокументов.Следующий() Цикл
				
				 Предшественники = Новый Массив;
				 АдресВозвратаДокументов = ДанныеВозвратДокументов.АдресВозвратаДокументов;
				 
				 НоваяКонтрольнаяТочка = Маршрут.Добавить();
				 НоваяКонтрольнаяТочка.Адрес = АдресВозвратаДокументов;
				 НоваяКонтрольнаяТочка.ТипТочки = Перечисления.упТипыТочекМаршрута.КонтрольнаяТочка;
				 НоваяКонтрольнаяТочка.Идентификатор = Новый УникальныйИдентификатор;
				 
				 ВыборкаРабот = ДанныеВозвратДокументов.Выбрать();
				 Пока ВыборкаРабот.Следующий() Цикл
					 НоваяРабота = Работы.Добавить();
					 ЗаполнитьЗначенияСвойств(НоваяРабота, ВыборкаРабот);
					 НоваяРабота.Идентификатор = НоваяКонтрольнаяТочка.Идентификатор;
					 НоваяРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
					 ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Предшественники, соотТочкиЗаданий.Получить(НоваяРабота.Задание));
				 КонецЦикла; 
				 Если Предшественники.Количество() Тогда
					 НоваяКонтрольнаяТочка.Предшественники = СтрСоединить(ОбщегоНазначенияКлиентСервер.СвернутьМассив(Предшественники), ",");
				 КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
		ИндексТочкиОтправления = 0;
		// подбор гаража выезда / возвращения
		Если ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство) Тогда
			
			сткРеквизитыТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.ТранспортноеСредство, "АвтоматическиПодбиратьГаражВРейсе, АвтоматическиПодбиратьВозвращениеВГаражВРейсе");
			АвтоматическиПодбиратьГаражОтправления = сткРеквизитыТС.АвтоматическиПодбиратьГаражВРейсе;
			АвтоматическиПодбиратьГаражВозвращения = сткРеквизитыТС.АвтоматическиПодбиратьВозвращениеВГаражВРейсе;
			
			Если Ложь
				Или АвтоматическиПодбиратьГаражОтправления
				Или АвтоматическиПодбиратьГаражВозвращения
			Тогда
				Предшественники = Новый Массив;
				сткВозврат = Справочники.упГаражи.ПодобратьГаражиДляТранспортногоСредства(Реквизиты.ТранспортноеСредство, Маршрут[0].Адрес, Маршрут[Маршрут.Количество() -1].Адрес);
				
				Если Истина
					И АвтоматическиПодбиратьГаражОтправления
					И ЗначениеЗаполнено(сткВозврат.ГаражВыезда)
				Тогда
					НачалоМаршрута = Маршрут.Вставить(0);
					НачалоМаршрута.Адрес = сткВозврат.АдресВыезда; 
					НачалоМаршрута.Гараж = сткВозврат.ГаражВыезда;
					НачалоМаршрута.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления;
					НачалоМаршрута.Идентификатор = Новый УникальныйИдентификатор;
					
					Предшественники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НачалоМаршрута.Идентификатор);
					Для Сч = 1 По Маршрут.Количество() - 1 Цикл
						текТочка = Маршрут[Сч];
						Если ЗначениеЗаполнено(текТочка.Предшественники) Тогда
							текТочка.Предшественники = СтрШаблон("%1,%2", текТочка.Предшественники, НачалоМаршрута.Идентификатор);
						Иначе
							текТочка.Предшественники = Строка(НачалоМаршрута.Идентификатор);
						КонецЕсли; 
						Предшественники.Добавить(текТочка.Идентификатор);
					КонецЦикла;
					ИндексТочкиОтправления = 1;
				КонецЕсли; 
				
				Если Истина
					И АвтоматическиПодбиратьГаражВозвращения
					И ЗначениеЗаполнено(сткВозврат.ГаражВозвращения)
				Тогда
					Если Предшественники.Количество() = 0 Тогда
						Предшественники = Маршрут.ВыгрузитьКолонку("Идентификатор");
					КонецЕсли; 
					ОкончаниеМаршрута = Маршрут.Вставить(Маршрут.Количество());
					ОкончаниеМаршрута.Адрес = сткВозврат.АдресВозвращения; 
					ОкончаниеМаршрута.Гараж = сткВозврат.ГаражВозвращения;
					ОкончаниеМаршрута.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения;
					ОкончаниеМаршрута.Идентификатор = Новый УникальныйИдентификатор;
					ОкончаниеМаршрута.Предшественники = СтрСоединить(Предшественники, ",");
				КонецЕсли;
				
			КонецЕсли;	
		КонецЕсли; 
		
		// планирование работ по точкам.
		КоэффициентРасчетаВремениВПути = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "КоэффициентРасчетаВремениВПути", 0);
		
		Если ДополнительныеСвойства.Свойство("МаршрутСледования") Тогда
			упКорректировкаМаршрута.УпорядочитьТочкиМаршрутаПоШаблону(Маршрут, ДополнительныеСвойства.МаршрутСледования);
		КонецЕсли;
		
		Сч = 1;
		Для каждого текСтрока Из Маршрут Цикл
			текСтрока.СтрокаНомер = Сч;
			Сч = Сч + 1;
		КонецЦикла;
		упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(Маршрут, Работы, ИндексТочкиОтправления, ВыборкаВидыПеревозок.ПлановоеПрибытие,,,,,,,,,,Реквизиты.ВидПеревозки, КоэффициентРасчетаВремениВПути,, Ссылка);		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеревестиВременноеОкноЗаданияВЧасовойПоясРейса(сткРабота, ЧасовойПоясЗадания, ЧасовойПоясРейса)
	Если ЗначениеЗаполнено(ЧасовойПоясЗадания) 
		И ЗначениеЗаполнено(ЧасовойПоясРейса) 
		И НЕ ЧасовойПоясЗадания = ЧасовойПоясРейса Тогда
		Если ЗначениеЗаполнено(сткРабота.ВременноеОкноНачало) Тогда
			сткРабота.ВременноеОкноНачало		= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(сткРабота.ВременноеОкноНачало, ЧасовойПоясЗадания, ЧасовойПоясРейса); 	
		КонецЕсли;
		Если ЗначениеЗаполнено(сткРабота.ВременноеОкноОкончание) Тогда
			сткРабота.ВременноеОкноОкончание	= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(сткРабота.ВременноеОкноОкончание, ЧасовойПоясЗадания, ЧасовойПоясРейса); 	
		КонецЕсли;
	КонецЕсли;						
КонецПроцедуры

// Функция проверяет, можно ли сдвинуть точку маршрута с учетом предшественнников
//
Функция ПроверитьСдвинутьСтрокуМаршрутаСУчетомПредшественников(Знач Маршрут, Знач НайденнаяСтрока, Знач БазоваяПозиция) Экспорт
	
	ИндексСтроки = Маршрут.Индекс(НайденнаяСтрока);
	НоваяПозиция = БазоваяПозиция;
	НашлиСвоегоПредшественника = Ложь;
	Для СмещениеИндекса = 1 По БазоваяПозиция Цикл
		ТочкаВыше = Маршрут[БазоваяПозиция - СмещениеИндекса];
		// Ищем среди расположенных выше точек высшую, у которой в предшественниках есть текущая точка
		Если Найти(ТочкаВыше.Предшественники, "" + НайденнаяСтрока.Идентификатор) > 0 Тогда 
			Если НашлиСвоегоПредшественника Тогда
				Возврат Ложь;
			КонецЕсли; 
			НоваяПозиция = БазоваяПозиция - СмещениеИндекса;
		КонецЕсли; 
		// Останавливаемся при обнаружении первого предшественника
		Если Найти(НайденнаяСтрока.Предшественники, "" + ТочкаВыше.Идентификатор) > 0 Тогда 
			НашлиСвоегоПредшественника = Истина;
		КонецЕсли; 
	КонецЦикла;
	Маршрут.Сдвинуть(ИндексСтроки, Мин(Маршрут.Количество() - 1, НоваяПозиция) - ИндексСтроки);
	Возврат Истина;

КонецФункции

// Функция - Зависимые задания по списку заданий
//
// Параметры:
//  СписокЗаданий	 - Массив - задания.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица заданий.
//
Функция ЗависимыеЗаданияПоСпискуЗаданий(Знач СписокЗаданий) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задания", СписокЗаданий);
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задания.Задание КАК Задание
	|ПОМЕСТИТЬ Задания
	|ИЗ
	|	&Задания КАК Задания
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза,
	|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ПравилаСвязиТиповГрузов.ОсновнойТипГруза КАК ОсновнойТипГруза,
	|	ПравилаСвязиТиповГрузов.ЗависимыйТипГруза КАК ЗависимыйТипГруза,
	|	упГрузовыеМестаТ.ТипГруза КАК ТипГруза
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Задания КАК ЗаданияТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокТ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
	|			ПО ПравилаСвязиТиповГрузов.Ссылка = упВидыПеревозокТ.ПравилоСвязиТиповГрузовВРейсе
	|		ПО упВидыПеревозокТ.Ссылка = ЗаданиеНаПеревозкуГруза.ВидПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМестаЗадания
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМестаТ
	|			ПО упГрузовыеМестаТ.Ссылка = ГрузовыеМестаЗадания.ГрузовоеМесто
	|		ПО ГрузовыеМестаЗадания.Ссылка = ЗаданиеНаПеревозкуГруза.Ссылка
	|	ПО ЗаданияТ.Задание = ЗаданиеНаПеревозкуГруза.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ОсновныеЗадания.ЗаданиеНаПеревозкуГруза) КАК ОсновноеЗадание,
	|	ЗависимыеЗадания.ЗаданиеНаПеревозкуГруза КАК ЗависимоеЗадание
	|ИЗ
	|	втЗадания КАК ОсновныеЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК ЗависимыеЗадания
	|	ПО ИСТИНА
	|		И ЗависимыеЗадания.ЗаданиеНаПеревозкуГруза <> ОсновныеЗадания.ЗаданиеНаПеревозкуГруза
	|		И ЗависимыеЗадания.ЗаявкаНаПеревозкуГруза = ОсновныеЗадания.ЗаявкаНаПеревозкуГруза
	|		И ЗависимыеЗадания.НомерВЦепиПоставок = ОсновныеЗадания.НомерВЦепиПоставок
	|ГДЕ ИСТИНА
	|	И ОсновныеЗадания.ОсновнойТипГруза = ОсновныеЗадания.ТипГруза
	|	И ЗависимыеЗадания.ЗависимыйТипГруза = ОсновныеЗадания.ЗависимыйТипГруза
	|СГРУППИРОВАТЬ ПО
	|	ЗависимыеЗадания.ЗаданиеНаПеревозкуГруза
	|";

	ТаблицаЗависимых = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаЗависимых;

КонецФункции

Процедура ОбработатьИсключениеЗаданий(Маршрут, Работы, ИсключенныеЗадания, Рейс, ПараметрыВводаИнформацииОГрузовыхМестах, КоэффициентРасчетаВремениВПути = 0)
	
	Если Ложь Тогда // Для контекстной подсказки
	    Рейс = Документы.упРейс.ПустаяСсылка();
	КонецЕсли;
	ВидПеревозки = Рейс.ВидПеревозки;
	ПравилоСвязиТиповГрузовВРейсе = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ПравилоСвязиТиповГрузовВРейсе");
	Если Не ПравилоСвязиТиповГрузовВРейсе.Пустая() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|// 820т17аипв
		|ВЫБРАТЬ
		|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
		|	ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ЗаданиеОсновноеГрузовыеМеста.Ссылка) КАК ОсновноеЗадание
		|ИЗ
		|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
		|		ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
		|		ПО ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза
		|			И ЗаданиеОсновноеГрузовыеМеста.Ссылка <> ЗаданиеЗависимоеГрузовыеМеста.Ссылка
		|			И ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.НомерВЦепиПоставок
		|			И ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза
		|			И ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе
		|ГДЕ
		|	ЗаданиеОсновноеГрузовыеМеста.Ссылка В(&Задания) И НЕ ЗаданиеЗависимоеГрузовыеМеста.Ссылка В(&Задания)
		|СГРУППИРОВАТЬ ПО
		|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка,
		|	ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто
		|";
		Если ТипЗнч(ИсключенныеЗадания) = Тип("ТаблицаЗначений") Тогда
			ОсновныеЗадания = ИсключенныеЗадания.ВыгрузитьКолонку("ЗаданиеНаПеревозку");
		Иначе
			ОсновныеЗадания = ИсключенныеЗадания;
		КонецЕсли; 
		Запрос.УстановитьПараметр("Задания", ОсновныеЗадания);
		ЗависимыеЗадания = Запрос.Выполнить().Выгрузить();
		Если ТипЗнч(ИсключенныеЗадания) = Тип("ТаблицаЗначений") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ЗависимыеЗадания, ИсключенныеЗадания);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключенныеЗадания, ЗависимыеЗадания.ВыгрузитьКолонку("ЗависимоеЗадание"));
		КонецЕсли; 
	КонецЕсли; 
	СхемаЗапроса = Новый СхемаЗапроса;
	ТекстЗапроса =
	// Важно оставлять в запросах звездочки!
	"ВЫБРАТЬ
	|	Задания.ЗаданиеНаПеревозку КАК Задание,
	|	Задания.ГрузовоеМесто
	|ПОМЕСТИТЬ втИсходныеДанные
	|ИЗ
	|	&Задания КАК Задания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.*
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.*
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Регистратор = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.ИдентификаторРаботы
	|ПОМЕСТИТЬ втИсключенныеРаботы
	|ИЗ
	|	втРаботы КАК втРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсходныеДанные КАК втИсходныеДанные
	|		ПО втРаботы.Задание = втИсходныеДанные.Задание
	|			И (&ПустыеГрузовыеМеста ИЛИ втРаботы.ГрузовоеМесто В (втИсходныеДанные.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРаботы.Идентификатор
	|ПОМЕСТИТЬ втОставшиесяТочкиМаршрута
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ
	|	втРаботы.Идентификатор В
	|			(ВЫБРАТЬ
	|				втИсключенныеРаботы.Идентификатор
	|			ИЗ
	|				втИсключенныеРаботы)
	|	И НЕ втРаботы.ИдентификаторРаботы В
	|				(ВЫБРАТЬ
	|					втИсключенныеРаботы.ИдентификаторРаботы
	|				ИЗ
	|					втИсключенныеРаботы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втИсключенныеРаботы.Идентификатор
	|ПОМЕСТИТЬ втИсключенныеТочки
	|ИЗ
	|	втИсключенныеРаботы КАК втИсключенныеРаботы
	|ГДЕ
	|	НЕ втИсключенныеРаботы.Идентификатор В
	|				(ВЫБРАТЬ
	|					втОставшиесяТочкиМаршрута.Идентификатор
	|				ИЗ
	|					втОставшиесяТочкиМаршрута)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втИсключенныеТочки.Идентификатор
	|ИЗ
	|	втИсключенныеТочки КАК втИсключенныеТочки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.*
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|ГДЕ
	|	НЕ втМаршрут.Идентификатор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					втИсключенныеТочки.Идентификатор
	|				ИЗ
	|					втИсключенныеТочки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.СтрокаНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.*
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ
	|	НЕ втРаботы.ИдентификаторРаботы В
	|				(ВЫБРАТЬ
	|					втИсключенныеРаботы.ИдентификаторРаботы
	|				ИЗ
	|					втИсключенныеРаботы)";
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Если ТипЗнч(ИсключенныеЗадания) = Тип("Массив") Тогда
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
		ЗапросВыбораСхемыЗапроса = ПакетЗапросов[0];
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
		|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто
		|ПОМЕСТИТЬ втИсходныеДанные
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|ГДЕ
		|	упЗаданиеНаПеревозкуГруза.Ссылка В(&Задания)";
		ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Задания", ИсключенныеЗадания);
	Запрос.УстановитьПараметр("Рейс"   , Рейс);
	Запрос.УстановитьПараметр("ПустыеГрузовыеМеста", ?(ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса, Истина, Ложь));
	Результат = Запрос.ВыполнитьПакет();
    КоличествоТаблиц = Результат.Количество();
	//Если Не Результат[КоличествоТаблиц - 2].Пустой() Тогда
		Маршрут = Результат[КоличествоТаблиц - 2].Выгрузить();
		Сч = 1;
		Для каждого текСтрока Из Маршрут Цикл
			текСтрока.СтрокаНомер = Сч;
			Сч = Сч + 1;
		КонецЦикла; 
	//КонецЕсли; 
	//Если Не Результат[КоличествоТаблиц - 1].Пустой() Тогда
		Работы = Результат[КоличествоТаблиц - 1].Выгрузить();
	//КонецЕсли; 
	
	Если Не Результат[КоличествоТаблиц - 3].Пустой() Тогда
		мсвИдентификаторов = Новый Массив;
		тзИдентификаторов = Результат[КоличествоТаблиц - 3].Выгрузить();
		Для каждого текИдентификатор Из тзИдентификаторов Цикл
			мсвИдентификаторов.Добавить(Строка(текИдентификатор.Идентификатор));
		КонецЦикла; 
		
		Для каждого текСтрока Из Маршрут Цикл
			Если ЗначениеЗаполнено(текСтрока.Предшественники) Тогда
				мсвПредшественников = СтрРазделить(текСтрока.Предшественники, ",", Ложь); 
				ОбщегоНазначенияКлиентСервер.СократитьМассив(мсвПредшественников, мсвИдентификаторов);
				текСтрока.Предшественники = СтрСоединить(мсвПредшественников, ",");
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(Маршрут, Работы,,,,,,,,,,,,Рейс.ВидПеревозки, КоэффициентРасчетаВремениВПути,, Рейс);
	
КонецПроцедуры

Процедура ОбработатьДобавлениеЗаданий(Маршрут, Работы, ДополнительныеСвойства, Рейс, КоэффициентРасчетаВремениВПути = 0)
	
	Если Ложь Тогда // Для контекстной подсказки
	    Рейс = Документы.упРейс.ПустаяСсылка();
	КонецЕсли;
	ДобавленныеЗадания = ДополнительныеСвойства.ДобавленныеЗадания;
	
	// Для расчета статусов, задания добавляются в "мсвЗадания".
	Если Истина
		И ДополнительныеСвойства.Свойство("мсвЗадания")
		И ТипЗнч(ДополнительныеСвойства.мсвЗадания) = Тип("Массив")
	Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеСвойства.мсвЗадания, ДобавленныеЗадания);
	КонецЕсли;
	
	Маршрут = СоздатьТаблицуМаршрут();
	Работы = СоздатьТаблицуРаботы();
	ГрузовыеМеста = Неопределено;
	МаршрутСледования = Неопределено;
	ПодготовитьДанныеДобавленияЗаданий(Рейс, ДобавленныеЗадания, Маршрут, Работы, ГрузовыеМеста, МаршрутСледования);
	
	ВидПеревозки = Рейс.ВидПеревозки;
	ПравилоСвязиТиповГрузовВРейсе = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ПравилоСвязиТиповГрузовВРейсе");

	ПериодПланирования = ДополнительныеСвойства.ПериодПланирования;
	Задания = ГрузовыеМеста.Скопировать(, "Задание, АдресОтправления, АдресПолучения");
	Задания.Свернуть("Задание, АдресОтправления, АдресПолучения");
	Задания.Сортировать("Задание");
	ПравилоСвязиТиповГрузовВРейсе = Рейс.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе;
	ВариантФормированияМаршрута = Рейс.ВидПеревозки.ВариантФормированияМаршрута;
	Если ЗначениеЗаполнено(ПравилоСвязиТиповГрузовВРейсе) Тогда
		ТаблицаЗависимых = ЗависимыеЗаданияПоСпискуЗаданий(Задания);
	КонецЕсли; 
	Для каждого Строка Из Задания Цикл
		тзГрузовыеМеста = ГрузовыеМеста.Скопировать(Новый Структура("Задание", Строка.Задание));
		СтруктураСвязанных = СтруктураСвязанныхЗаданийДляЗадания(ПравилоСвязиТиповГрузовВРейсе, ТаблицаЗависимых, Строка.Задание);
		
		мсвТары = Новый Массив;
		мсвВТаре = Новый Массив;
		СтруктураТары = Неопределено;
		мсвГрузовыхМест = Новый Массив;
		Для каждого текГрузовоеМесто Из тзГрузовыеМеста Цикл
			мсвГрузовыхМест.Добавить(Новый Структура("ГрузовоеМесто, Тара, КоличествоГрузов", текГрузовоеМесто.ГрузовоеМесто, текГрузовоеМесто.Тара, текГрузовоеМесто.КоличествоГрузов));
			Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
				Если ЗначениеЗаполнено(текГрузовоеМесто.ГрузовоеМесто) Тогда
					Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текГрузовоеМесто.ГрузовоеМесто, "Тара") Тогда
						мсвТары.Добавить(текГрузовоеМесто.ГрузовоеМесто);
					КонецЕсли;
					Если ЗначениеЗаполнено(текГрузовоеМесто.Тара) Тогда
						мсвВТаре.Добавить(текГрузовоеМесто.Тара);
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 
		
		Если мсвТары.Количество() Или мсвВТаре.Количество() Тогда
			СтруктураТары = Новый Структура;
			СтруктураТары.Вставить("Тара", ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвТары));
			СтруктураТары.Вставить("ВТаре",  ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВТаре));
		КонецЕсли; 
			
		сткРезультат = упКорректировкаМаршрута.ДобавитьРаботыПоЗаданиюВМаршрут(Рейс, Маршрут, Работы, Строка.АдресОтправления, Строка.АдресПолучения, Строка.Задание, мсвГрузовыхМест, Истина,,,,
		ВариантФормированияМаршрута, СтруктураТары, СтруктураСвязанных,,ДополнительныеСвойства.ЧасовойПояс);
	КонецЦикла;
	
	Маршрут.Сортировать("СтрокаНомер");
	
	упКорректировкаМаршрута.УпорядочитьТочкиМаршрутаПоШаблону(Маршрут, МаршрутСледования);
	Сч = 1;
	Для каждого текСтрока Из Маршрут Цикл
		текСтрока.СтрокаНомер = Сч;
		Сч = Сч + 1;
	КонецЦикла;
	
	упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(Маршрут, Работы,,,,,,,,,,,,Рейс.ВидПеревозки, КоэффициентРасчетаВремениВПути,, Рейс);
	
КонецПроцедуры

Процедура ПодготовитьДанныеДобавленияЗаданий(Рейс, ДобавленныеЗадания, Маршрут, Работы, ГрузовыеМеста, МаршрутСледования)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Задания.ЗаданиеНаПеревозку КАК Задание,
	|	Задания.ГрузовоеМесто,
	|	Задания.КоличествоГрузов
	|ПОМЕСТИТЬ втИсходныеДанные
	|ИЗ
	|	&Задания КАК Задания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданияКПланированиюОстатки.Задание,
	|	упЗаданияКПланированиюОстатки.ГрузовоеМесто,
	|	упЗаданияКПланированиюОстатки.Тара,
	|	ВЫБОР
	|		КОГДА втИсходныеДанные.КоличествоГрузов > упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток
	|			ТОГДА упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток
	|		ИНАЧЕ втИсходныеДанные.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|			,
	|			Задание В
	|				(ВЫБРАТЬ
	|					втИсходныеДанные.Задание
	|				ИЗ
	|					втИсходныеДанные КАК втИсходныеДанные)) КАК упЗаданияКПланированиюОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсходныеДанные КАК втИсходныеДанные
	|		ПО упЗаданияКПланированиюОстатки.Задание = втИсходныеДанные.Задание
	|			И упЗаданияКПланированиюОстатки.ГрузовоеМесто = втИсходныеДанные.ГрузовоеМесто
	|			И (упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упРейс.Расписание.ИзменятьМаршрутСледованияРейса, ЛОЖЬ) = ИСТИНА
	|			ТОГДА упРейс.Расписание.МаршрутСледования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упТиповыеМаршруты.ПустаяСсылка)
	|	КОНЕЦ КАК МаршрутСледования
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|ГДЕ
	|	упРейс.Ссылка = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Задание,
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.Тара,
	|	втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.Задание.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.Задание.АдресПолучения КАК АдресПолучения
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(упМаршрутПоРейсу.ВременноеОкноНачало КАК ДАТА) КАК ВременноеОкноНачало,
	|	ВЫРАЗИТЬ(упМаршрутПоРейсу.ВременноеОкноОкончание КАК ДАТА) КАК ВременноеОкноОкончание,
	|	упМаршрутПоРейсу.*
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Рейс
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсу.СтрокаНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.Рейс,
	|	упРаботыПоРейсу.Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы,
	|	упРаботыПоРейсу.СтрокаНомер,
	|	упРаботыПоРейсу.Отменена,
	|	упРаботыПоРейсу.Задание,
	|	упРаботыПоРейсу.Операция,
	|	упРаботыПоРейсу.ДополнительнаяОперация,
	|	упРаботыПоРейсу.ВремяРабот,
	|	ВЫРАЗИТЬ(упРаботыПоРейсу.ВременноеОкноНачало КАК ДАТА) КАК ВременноеОкноНачало,
	|	ВЫРАЗИТЬ(упРаботыПоРейсу.ВременноеОкноОкончание КАК ДАТА) КАК ВременноеОкноОкончание,
	|	упРаботыПоРейсу.ГрузовоеМесто,
	|	упРаботыПоРейсу.Тара,
	|	упРаботыПоРейсу.ВремяОжидания,
	|	упРаботыПоРейсу.ПлановоеВремяНачалаРабот,
	|	упРаботыПоРейсу.КоличествоГрузов,
	|	упРаботыПоРейсу.КоличествоДопОпераций,
	|	упРаботыПоРейсу.Проблема,
	|	упРаботыПоРейсу.АдресРазгрузки,
	|	упРаботыПоРейсу.Выполнена,
	|	упРаботыПоРейсу.ПроблемаПричинаВозникновенияЭтойРаботы,
	|	упРаботыПоРейсу.ТипРегламентногоДокумента,
	|	упРаботыПоРейсу.КоличествоЭкземпляровДокумента,
	|	упРаботыПоРейсу.Сумма
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Регистратор = &Рейс";
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Если ТипЗнч(ДобавленныеЗадания) = Тип("Массив") Тогда
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
		ПакетЗапросов.Удалить(0);
		ЗапросВыбораСхемыЗапроса = ПакетЗапросов[0];
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	упЗаданияКПланированиюОстатки.Задание,
		|	упЗаданияКПланированиюОстатки.ГрузовоеМесто,
		|	упЗаданияКПланированиюОстатки.Тара,
		|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток КАК КоличествоГрузов
		|ПОМЕСТИТЬ втГрузовыеМеста
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(, Задание В (&Задания)) КАК упЗаданияКПланированиюОстатки
		|ГДЕ
		|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток > 0";
		ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Задания", ДобавленныеЗадания);
	Запрос.УстановитьПараметр("Рейс"   , Рейс);
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	
	ГрузовыеМеста = Результат[КоличествоТаблиц - 3].Выгрузить();
	ВыгрузкаМаршрут = Результат[КоличествоТаблиц - 2].Выгрузить();
	Для каждого текСтрока Из ВыгрузкаМаршрут Цикл
		ЗаполнитьЗначенияСвойств(Маршрут.Добавить(), текСтрока);
	КонецЦикла;
	ВыгрузкаРаботы = Результат[КоличествоТаблиц - 1].Выгрузить();
	Для каждого текСтрока Из ВыгрузкаРаботы Цикл
		ЗаполнитьЗначенияСвойств(Работы.Добавить(), текСтрока);
	КонецЦикла;
	
	ВыборкаМаршрутСледования = Результат[КоличествоТаблиц - 4].Выбрать();
	ВыборкаМаршрутСледования.Следующий();
	МаршрутСледования = ВыборкаМаршрутСледования.МаршрутСледования;

КонецПроцедуры

Процедура ОбработатьОбновлениеРаботПоЗаданию(Маршрут, Работы, ДополнительныеСвойства, Рейс, КоэффициентРасчетаВремениВПути = 0)
	
	Если Ложь Тогда // Для контекстной подсказки
	    Рейс = Документы.упРейс.ПустаяСсылка();
	КонецЕсли;
	Маршрут = СоздатьТаблицуМаршрут();
	Работы  = СоздатьТаблицуРаботы();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаданияКПланированиюОстатки.Задание,
	|	упЗаданияКПланированиюОстатки.ГрузовоеМесто,
	|	упЗаданияКПланированиюОстатки.Тара,
	|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(, Задание = &Задание) КАК упЗаданияКПланированиюОстатки
	|ГДЕ
	|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Задание,
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.Тара,
	|	втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.Задание.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.Задание.АдресПолучения КАК АдресПолучения
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.*
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Рейс
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсу.СтрокаНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.*
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Регистратор = &Рейс
	|	И упРаботыПоРейсу.Задание <> &Задание";
	Запрос.УстановитьПараметр("Задание", ДополнительныеСвойства.ОбновитьРаботыПоЗаданию);
	Запрос.УстановитьПараметр("Рейс"   , Рейс);
	Результат = Запрос.ВыполнитьПакет();
    КоличествоТаблиц = Результат.Количество();
	ГрузовыеМеста = Результат[КоличествоТаблиц - 3].Выгрузить();
	
	ВыгрузкаМаршрут = Результат[КоличествоТаблиц - 2].Выгрузить();
	Для каждого текСтрока Из ВыгрузкаМаршрут Цикл
		ЗаполнитьЗначенияСвойств(Маршрут.Добавить(), текСтрока);
	КонецЦикла;
	ВыгрузкаРаботы = Результат[КоличествоТаблиц - 1].Выгрузить();
	Для каждого текСтрока Из ВыгрузкаРаботы Цикл
		ЗаполнитьЗначенияСвойств(Работы.Добавить(), текСтрока);
	КонецЦикла;
	
	Задания = ГрузовыеМеста.Скопировать(, "Задание, АдресОтправления, АдресПолучения");
	Задания.Свернуть("Задание, АдресОтправления, АдресПолучения");
	Задания.Сортировать("Задание");
	ПравилоСвязиТиповГрузовВРейсе = Рейс.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе;
	Если ЗначениеЗаполнено(ПравилоСвязиТиповГрузовВРейсе) Тогда
		ТаблицаЗависимых = ЗависимыеЗаданияПоСпискуЗаданий(Задания);
	КонецЕсли; 
	Для каждого Строка Из Задания Цикл
		тзГрузовыеМеста = ГрузовыеМеста.Скопировать(Новый Структура("Задание", Строка.Задание));
		мсвГрузовыхМест = Новый Массив;
		СтруктураСвязанных = СтруктураСвязанныхЗаданийДляЗадания(ПравилоСвязиТиповГрузовВРейсе, ТаблицаЗависимых, Строка.Задание);
		
		// накопление тары
		мсвТары                        = Новый Массив;
		мсвВТаре                       = Новый Массив;
		СтруктураТары                  = Неопределено;
		
		Для каждого текГрузовоеМесто Из тзГрузовыеМеста Цикл
			мсвГрузовыхМест.Добавить(Новый Структура("ГрузовоеМесто, Тара, КоличествоГрузов", текГрузовоеМесто.ГрузовоеМесто, текГрузовоеМесто.Тара, текГрузовоеМесто.КоличествоГрузов));
			
			Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
				Если ЗначениеЗаполнено(текГрузовоеМесто.ГрузовоеМесто) Тогда
					Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текГрузовоеМесто.ГрузовоеМесто, "Тара") Тогда
						мсвТары.Добавить(текГрузовоеМесто.ГрузовоеМесто);
					КонецЕсли;
					Если ЗначениеЗаполнено(текГрузовоеМесто.Тара) Тогда
						мсвВТаре.Добавить(текГрузовоеМесто.Тара);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла; 
		
		Если мсвТары.Количество() Или мсвВТаре.Количество() Тогда
			СтруктураТары = Новый Структура;
			СтруктураТары.Вставить("Тара", ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвТары));
			СтруктураТары.Вставить("ВТаре",  ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВТаре));
		КонецЕсли; 
		
		сткРезультат = упКорректировкаМаршрута.ДобавитьРаботыПоЗаданиюВМаршрут(Рейс, Маршрут, Работы, Строка.АдресОтправления, Строка.АдресПолучения, Строка.Задание, мсвГрузовыхМест, Ложь,,,,, СтруктураТары, СтруктураСвязанных,,ДополнительныеСвойства.ЧасовойПояс);
	КонецЦикла;
	
	Маршрут.Сортировать("СтрокаНомер");
	Сч = 1;
	Для каждого текСтрока Из Маршрут Цикл
		текСтрока.СтрокаНомер = Сч;
		Сч = Сч + 1;
	КонецЦикла; 	

	упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(Маршрут, Работы,,,,,,,,,,,,Рейс.ВидПеревозки, КоэффициентРасчетаВремениВПути,, Рейс);	
	
КонецПроцедуры

// Функция - Структура связанных заданий для задания
//
// Параметры:
//  ПравилоСвязиТиповГрузовВРейсе	 - СправочникСсылка.упПравилаСвязиТиповГрузов  - правило.
//  ТаблицаЗависимых			 - ТаблицаЗначений	 - зависимые задания.
//  ТекущееЗадание				 - ДокументСсылка.упЗаданиеНаПеревозкуГруза 	 - задание.
// 
// Возвращаемое значение:
//  Структура - связанные задания.
//
Функция СтруктураСвязанныхЗаданийДляЗадания(Знач ПравилоСвязиТиповГрузовВРейсе, Знач ТаблицаЗависимых, Знач ТекущееЗадание) Экспорт 
	
	СтруктураСвязанных = Неопределено;
	Если ЗначениеЗаполнено(ПравилоСвязиТиповГрузовВРейсе) Тогда
		Если ЗначениеЗаполнено(ТекущееЗадание) Тогда
			мсвРодителей = Новый Массив;
			мсвДетей = Новый Массив;
			СтрокиДетей = ТаблицаЗависимых.НайтиСтроки(Новый Структура("ОсновноеЗадание", ТекущееЗадание));
			Для Каждого СтрокаДетя Из СтрокиДетей Цикл
				мсвДетей.Добавить(СтрокаДетя.ЗависимоеЗадание);
			КонецЦикла;
			СтрокиРодителей = ТаблицаЗависимых.НайтиСтроки(Новый Структура("ЗависимоеЗадание", ТекущееЗадание));
			Для Каждого СтрокаРодителей Из СтрокиРодителей Цикл
				мсвРодителей.Добавить(СтрокаРодителей.ОсновноеЗадание);
			КонецЦикла;
			Если мсвДетей.Количество() Или мсвРодителей.Количество() Тогда
				СтруктураСвязанных = Новый Структура;
				СтруктураСвязанных.Вставить("Родители", мсвРодителей);
				СтруктураСвязанных.Вставить("Дети",  мсвДетей);				
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	Возврат СтруктураСвязанных;

КонецФункции

Процедура ОбработатьПересчетМаршрута(Маршрут, Работы, ДополнительныеСвойства, Рейс, КоэффициентРасчетаВремениВПути = 0) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	упМаршрутПоРейсу.*
	 |ИЗ
	 |	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	 |ГДЕ
	 |	упМаршрутПоРейсу.Регистратор = &Рейс
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	упМаршрутПоРейсу.СтрокаНомер
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	упРаботыПоРейсу.*
	 |ИЗ
	 |	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	 |ГДЕ
	 |	упРаботыПоРейсу.Регистратор = &Рейс
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	упРаботыПоРейсу.СтрокаНомер";
	 Запрос.УстановитьПараметр("Рейс", Рейс);
	 Результат = Запрос.ВыполнитьПакет();
	 Если Не Результат[0].Пустой() Тогда
		 Маршрут = Результат[0].Выгрузить(); 
		 Работы  = Результат[1].Выгрузить();
		 
		 мсвРешений = ДополнительныеСвойства.ПересчетМаршрута.МассивТочек;
		 Сч = 0;
		 Для каждого Точка Из мсвРешений Цикл
			 НайденнаяСтрока = Маршрут.Найти(Точка, "Идентификатор");
			 ИндексСтроки = Маршрут.Индекс(НайденнаяСтрока);
			 Маршрут.Сдвинуть(ИндексСтроки, Сч - ИндексСтроки);
			 
			 Сч = Сч + 1;
			 НайденнаяСтрока.СтрокаНомер = Сч;
		 КонецЦикла; 

		упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(Маршрут, Работы,,,,,,,,,,,,Рейс.ВидПеревозки, КоэффициентРасчетаВремениВПути,, Рейс);	
		 Если ДополнительныеСвойства.УменьшатьВремяОжидания Тогда
		 	упРейсКлиентСервер.СократитьВремяОжиданияВРейсе(Маршрут, Работы);
		 КонецЕсли; 
		 
	 КонецЕсли; 
	 
 КонецПроцедуры

// Функция - возвращает типовую таблицу маршрута.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выпонления.
//
Функция СоздатьТаблицуМаршрут() Экспорт
	
	Маршрут = Новый ТаблицаЗначений;
	Маршрут.Колонки.Добавить("Рейс"                       , Новый ОписаниеТипов("ДокументСсылка.упРейс"));
	Маршрут.Колонки.Добавить("Адрес"                      , Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	Маршрут.Колонки.Добавить("Идентификатор");
	Маршрут.Колонки.Добавить("СтрокаНомер"                , Новый ОписаниеТипов("Число"));
	Маршрут.Колонки.Добавить("ПлановоеПрибытие"           , Новый ОписаниеТипов("Дата"));
	Маршрут.Колонки.Добавить("ПлановоеУбытие"             , Новый ОписаниеТипов("Дата"));
	Маршрут.Колонки.Добавить("НачалоРаботПлан"            , Новый ОписаниеТипов("Дата"));
	Маршрут.Колонки.Добавить("ВремяРабот"                 , Новый ОписаниеТипов("Число"));
	Маршрут.Колонки.Добавить("ТипТочки"                   , Новый ОписаниеТипов("ПеречислениеСсылка.упТипыТочекМаршрута"));
	Маршрут.Колонки.Добавить("ВременноеОкноНачало"        , Новый ОписаниеТипов("Дата"));
	Маршрут.Колонки.Добавить("ВременноеОкноОкончание"     , Новый ОписаниеТипов("Дата"));
	Маршрут.Колонки.Добавить("РасстояниеОтПредыдущейТочки", Новый ОписаниеТипов("Число"));
	Маршрут.Колонки.Добавить("ВремяОтПредыдущейТочки"     , Новый ОписаниеТипов("Число"));
	Маршрут.Колонки.Добавить("ВремяОтдыхаВПутиОтПредыдущейТочки", Новый ОписаниеТипов("Число"));
	Маршрут.Колонки.Добавить("ВремяОжиданияВТочке"        , Новый ОписаниеТипов("Число"));
	Маршрут.Колонки.Добавить("ПроездНеРассчитан"          , Новый ОписаниеТипов("Булево"));
	Маршрут.Колонки.Добавить("Гараж"                      , Новый ОписаниеТипов("СправочникСсылка.упГаражи"));
	Маршрут.Колонки.Добавить("Предшественники");
	Маршрут.Колонки.Добавить("Пройдена" 	              , Новый ОписаниеТипов("Булево"));
	Маршрут.Колонки.Добавить("Отменена" 	              , Новый ОписаниеТипов("Булево"));
	Маршрут.Колонки.Добавить("НеИспользоватьАвторасчетУбытияИзТочки" , Новый ОписаниеТипов("Булево"));
	Маршрут.Колонки.Добавить("ВремяОжиданияВТочкеПослеРабот", Новый ОписаниеТипов("Число"));
	
	Возврат Маршрут;
	
КонецФункции
	
Функция	СоздатьТаблицуРаботы()
	
	Работы = Новый ТаблицаЗначений;
	Работы.Колонки.Добавить("Идентификатор");
	Работы.Колонки.Добавить("ИдентификаторРаботы");
	Работы.Колонки.Добавить("СтрокаНомер"                   , Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("Задание"                       , Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	Работы.Колонки.Добавить("Операция"                      , Новый ОписаниеТипов("ПеречислениеСсылка.упТипыОпераций"));            
	Работы.Колонки.Добавить("ДополнительнаяОперация");
	Работы.Колонки.Добавить("ВремяРабот"                    , Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("Выполнена" 	                , Новый ОписаниеТипов("Булево"));
	Работы.Колонки.Добавить("ВременноеОкноНачало"           , Новый ОписаниеТипов("Дата"));
	Работы.Колонки.Добавить("ВременноеОкноОкончание"        , Новый ОписаниеТипов("Дата"));
	Работы.Колонки.Добавить("ГрузовоеМесто"                 , Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
	Работы.Колонки.Добавить("Тара"                          , Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
	Работы.Колонки.Добавить("ВремяОжидания"                 , Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("ПлановоеВремяНачалаРабот"      , Новый ОписаниеТипов("Дата"));
	Работы.Колонки.Добавить("КоличествоДопОпераций"         , Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("КоличествоГрузов"              , Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("ТипРегламентногоДокумента"     , Новый ОписаниеТипов("СправочникСсылка.упТипыРегламентныхДокументов"));
	Работы.Колонки.Добавить("КоличествоЭкземпляровДокумента", Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("Сумма", 						  Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("Масса", 						  Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("Объем", 						  Новый ОписаниеТипов("Число"));
	Работы.Колонки.Добавить("СкладскаяЯчейка",				  Новый ОписаниеТипов("СправочникСсылка.упСкладскиеЯчейки"));
	
	Возврат Работы;
	
КонецФункции

// Производится удаление неиспользуемых идентификаторов в предшественниках
//
// Параметры:
//  Маршрут	 - ТаблицаЗначений	 - маршрут.
//
Процедура УдалитьЗависшихПредшественников(Маршрут) Экспорт
	
	мсвИдентификаторов = Новый Массив;
	Для каждого текСтрока Из Маршрут Цикл
		
		Если мсвИдентификаторов.Количество() Тогда
			Если ЗначениеЗаполнено(текСтрока.Предшественники) Тогда
				мсвПредшественников = СтрРазделить(текСтрока.Предшественники, ",", Ложь);
				мсвПредшественниковНовый = Новый Массив;
				Для каждого текПредшественник Из мсвПредшественников Цикл
					Если Не мсвИдентификаторов.Найти(текПредшественник) = Неопределено Тогда
						мсвПредшественниковНовый.Добавить(текПредшественник);
					КонецЕсли; 
				КонецЦикла;
				текСтрока.Предшественники = СтрСоединить(мсвПредшественниковНовый, ",");
			КонецЕсли; 
		КонецЕсли; 
		
		мсвИдентификаторов.Добавить(Строка(текСтрока.Идентификатор));
	КонецЦикла; 
	
КонецПроцедуры

// Процедура - Определить зоны погрузки разгрузки
//
// Параметры:
//  Работы					 - ТаблицаЗначений	 - работы по рейсу.
//  Маршрут					 - ТаблицаЗначений	 - маршрут по рейсу.
//  сткПараметры				 - Структура	 - структура в которую вернутся зоны погрузки и разгрузки.
//  ВариантФормированияМаршрута	 - Перечисления.упВариантыФормированияМаршрута	 - вариант формирования маршрута.
//
Процедура ОпределитьЗоныПогрузкиРазгрузки(Работы, Маршрут, сткПараметры, ВариантФормированияМаршрута)
	
	текГеографическаяЗонаПогрузки = Неопределено;
	текГеографическаяЗонаРазгрузки = Неопределено;
	
	мсвТочкиПогрузки = Новый Массив;
	мсвТочкиРазгрузки = Новый Массив;

	Для каждого текРабота Из Работы Цикл
		
		текИдентификатор = текРабота.Идентификатор;
		
		Если упПрохождениеТочкиКлиентСервер.ОперацияПогрузки(текРабота.Операция) Тогда
			Если мсвТочкиПогрузки.Найти(текИдентификатор) = Неопределено Тогда
				мсвТочкиПогрузки.Добавить(текИдентификатор);
			КонецЕсли;
		ИначеЕсли упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(текРабота.Операция) Тогда	
			Если мсвТочкиРазгрузки.Найти(текИдентификатор) = Неопределено Тогда
				мсвТочкиРазгрузки.Добавить(текИдентификатор);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
	Для каждого текТочкаПогрузки Из мсвТочкиПогрузки Цикл
		
		текМаршрут = Маршрут.Найти(текТочкаПогрузки, "Идентификатор");
		
		Если текМаршрут <> Неопределено Тогда
			Если текГеографическаяЗонаПогрузки = Неопределено Тогда
				текГеографическаяЗона = текМаршрут.Адрес.ГеографическаяЗона;
				Если ЗначениеЗаполнено(текГеографическаяЗона) Тогда
					текГеографическаяЗонаПогрузки = текГеографическаяЗона;
				КонецЕсли;
			Иначе
				текГеоЗона = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текМаршрут.Адрес, "ГеографическаяЗона");
				Если ЗначениеЗаполнено(текГеоЗона) Тогда
					Пока НЕ (текГеоЗона = текГеографическаяЗонаПогрузки ИЛИ текГеоЗона.ПринадлежитЭлементу(текГеографическаяЗонаПогрузки))  Цикл
						текГеографическаяЗонаПогрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текГеографическаяЗонаПогрузки, "Родитель");
						
						Если текГеографическаяЗонаПогрузки = Справочники.упГеографическиеЗоны.ПустаяСсылка() Тогда
							текГеографическаяЗонаПогрузки = Неопределено;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;
		
		Если текГеографическаяЗонаПогрузки = Неопределено Тогда
			Прервать;		
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого текТочкаРазгрузки Из мсвТочкиРазгрузки Цикл
		
		текМаршрут = Маршрут.Найти(текТочкаРазгрузки, "Идентификатор");
		
		Если текМаршрут <> Неопределено Тогда
			Если текГеографическаяЗонаРазгрузки = Неопределено Тогда
				текГеографическаяЗона = текМаршрут.Адрес.ГеографическаяЗона;
				Если ЗначениеЗаполнено(текГеографическаяЗона) Тогда
					текГеографическаяЗонаРазгрузки = текГеографическаяЗона;
				КонецЕсли;
			Иначе
				текГеоЗона = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текМаршрут.Адрес, "ГеографическаяЗона");
				Если ЗначениеЗаполнено(текГеоЗона) Тогда
					Пока НЕ (текГеоЗона = текГеографическаяЗонаРазгрузки ИЛИ текГеоЗона.ПринадлежитЭлементу(текГеографическаяЗонаРазгрузки))  Цикл
						текГеографическаяЗонаРазгрузки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текГеографическаяЗонаРазгрузки, "Родитель");	
						
						Если текГеографическаяЗонаРазгрузки = Справочники.упГеографическиеЗоны.ПустаяСсылка() Тогда
							текГеографическаяЗонаРазгрузки = Неопределено;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если текГеографическаяЗонаРазгрузки = Неопределено Тогда
			Прервать;		
		КонецЕсли;
		
	КонецЦикла;
	
	ВыводитьОшибкиПриОпределенииЗонТолькоВЖР = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткПараметры, "ВыводитьОшибкиПриОпределенииЗонТолькоВЖР", Ложь);
	
	Если текГеографическаяЗонаПогрузки = Неопределено Тогда
		
		ТекстСообщения = Нстр("ru = 'Не удалось определить общую географическую зону для адресов погрузки, скорректируйте маршрут'");
		Если ВыводитьОшибкиПриОпределенииЗонТолькоВЖР Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Определение зоны погрузки(разгрузки)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Информация,,,ТекстСообщения);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);								
		КонецЕсли;
	
		сткПараметры.Вставить("ЗонаПогрузки", Неопределено);
	Иначе
		сткПараметры.Вставить("ЗонаПогрузки", текГеографическаяЗонаПогрузки);
	КонецЕсли;
	
	Если Истина
		И текГеографическаяЗонаРазгрузки = Неопределено 
		И ВариантФормированияМаршрута <> Перечисления.упВариантыФормированияМаршрута.Самовывоз
	Тогда
		ТекстСообщения = Нстр("ru = 'Не удалось определить общую географическую зону для адресов разгрузки, скорректируйте маршрут'");
		Если ВыводитьОшибкиПриОпределенииЗонТолькоВЖР Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Определение зоны погрузки(разгрузки)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Информация,,,ТекстСообщения);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);								
		КонецЕсли;

		сткПараметры.Вставить("ЗонаРазгрузки", Неопределено);
	Иначе
		сткПараметры.Вставить("ЗонаРазгрузки", текГеографическаяЗонаРазгрузки);
	КонецЕсли;
	
КонецПроцедуры

// Функция - Проверяет доступность водителей в заданный период времени. 
//		   Осуществляются следующие проверки:
//			* сотрудник не назначен ни на какие рейсы;
//			* если у сотрудника заполнен индивидуальный график работы, то проверяется доступен ли он в заданный период;
//			* если используется общий график, то доступность сотрудника проверяется по нему.
//
// Параметры:
//  Водитель						 - Массив, СправочникСсылка.упСотрудники - сотрудники для проверки.
//  НачалоПериодаПланирования			 - ДатаВремя - начало периода, за который проверяется доступность сотрудника.
//  ОкончаниеПериодаПланирования		 - ДатаВремя - окончание периода, за который проверяется доступность сотрудника.
//  КонтрольРасписанияРаботыВодителей	 - Булево	- Истина, если необходимо контролировать индивидуальные
//                                         графики сотрудников.
//  ВодителиНеПрошедшиеПроверку		 - Массив - возвращается массив водителей, которые не могут быть назначены на рейсы.
//  УчитыватьРасписаниеРаботыЭкипажей    - Булево - Истина, учитываются данные РС.РасписаниеРаботыЭкипажей. 
//                                         Ошибочно устанавливать Истина, если могла быть выполнена корректировка рейса. 
//                                         Так как данные по экипажу и датам при корректировке в этот регистр 
//                                         не передаются(по техническим причинам), поэтому учитывать старые данные
//                                         регистра ошибочно.
//  Рейс							 - ДокументСсылка.упРейс - ссылка на рейс в котором осуществляется проверка,
//                                         что бы исключить его из проверки.
//										  
// 
// Возвращаемое значение:
//  Булево - Истина, если сотрудники доступны в заданный период времени.
//
Функция ВодительМожетБытьНазначенНаРейсПоПлановымДатам(Водитель, НачалоПериодаПланирования, ОкончаниеПериодаПланирования, КонтрольРасписанияРаботыВодителей = Истина, ВодителиНеПрошедшиеПроверку = Неопределено, УчитыватьРасписаниеРаботыЭкипажей = Истина, Рейс = Неопределено) Экспорт
	
	Если ТипЗнч(Водитель) = Тип("Массив") Тогда
		Сотрудник = Водитель;
	Иначе	
		Сотрудник = Новый Массив;
		Сотрудник.Добавить(Водитель);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =	
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСотрудникиТ.Ссылка КАК Сотрудник,
	|	упСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы КАК ИспользуетсяИндивидуальныйГрафикРаботы,
	|	упСотрудникиТ.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ втВсеСотрудники
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудникиТ
	|ГДЕ упСотрудникиТ.Ссылка В  (&Сотрудник)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Сотрудники которые были назначены на рейсы за период
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВсеСотрудники.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ втРейсыПоСотруднику
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК УпПеревозчикПоРейсу_СрезПоследнихТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейсаТ
	|	ПО УпПеревозчикПоРейсу_СрезПоследнихТ.Рейс = упПлановыеПараметрыРейсаТ.Рейс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВсеСотрудники КАК втВсеСотрудники
	|	ПО ЛОЖЬ
	|		ИЛИ втВсеСотрудники.Сотрудник = УпПеревозчикПоРейсу_СрезПоследнихТ.Водитель1
	|		ИЛИ втВсеСотрудники.Сотрудник = УпПеревозчикПоРейсу_СрезПоследнихТ.Водитель2
	|ГДЕ ИСТИНА
	|	И упПлановыеПараметрыРейсаТ.ПлановоеНачалоВыполнения < &ДатаОкончания
	|	И упПлановыеПараметрыРейсаТ.ПлановоеОкончаниеВыполнения > &ДатаНачала
	|	И УпПеревозчикПоРейсу_СрезПоследнихТ.Рейс <> &Рейс
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРасписаниеРаботыЭкипажейТ.Водитель КАК Сотрудник
	|ИЗ
	|	РегистрСведений.упРасписаниеРаботыЭкипажей КАК упРасписаниеРаботыЭкипажейТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВсеСотрудники КАК втВсеСотрудники
	|	ПО втВсеСотрудники.Сотрудник = упРасписаниеРаботыЭкипажейТ.Водитель
	|ГДЕ ИСТИНА
	|	И &УчитыватьРасписаниеРаботыЭкипажей
	|	И упРасписаниеРаботыЭкипажейТ.ТипРасписания = ЗНАЧЕНИЕ(Перечисление.упТипыРасписанийРаботыТС.План)
	|	И упРасписаниеРаботыЭкипажейТ.ДатаНачала < &ДатаОкончания
	|	И упРасписаниеРаботыЭкипажейТ.ДатаОкончания > &ДатаНачала
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Не занятые сотрудники
	|ВЫБРАТЬ
	|	упСотрудникиТ.Сотрудник КАК Ссылка,
	|	упСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы КАК ИспользуетсяИндивидуальныйГрафикРаботы,
	|	упСотрудникиТ.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ втСотрудники
	|ИЗ
	|	втВсеСотрудники КАК упСотрудникиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРейсыПоСотруднику КАК втРейсыПоСотрудникуТ
	|	ПО упСотрудникиТ.Сотрудник = втРейсыПоСотрудникуТ.Сотрудник
	|ГДЕ втРейсыПоСотрудникуТ.Сотрудник ЕСТЬ NULL
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// вап3456вс3р Определение расписания работ при помощи механизма БСП.Начало
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСотрудникиТ.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ втГрафикиРаботы
	|ИЗ
	|	втСотрудники КАК втСотрудникиТ
	|ГДЕ ИСТИНА
	|	И &КонтрольРасписанияРаботыВодителей
	|	И НЕ (втСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы)
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШаблонЗаполнения.Ссылка КАК ГрафикРаботы,
	|	МАКСИМУМ(ШаблонЗаполнения.НомерСтроки) КАК ДлинаЦикла
	|ПОМЕСТИТЬ ВТДлинаЦиклаГрафиков
	|ИЗ
	|	Справочник.Календари.ШаблонЗаполнения КАК ШаблонЗаполнения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|	ПО ШаблонЗаполнения.Ссылка = втГрафикиРаботы.ГрафикРаботы
	|СГРУППИРОВАТЬ ПО
	|	ШаблонЗаполнения.Ссылка
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Календари.Ссылка КАК ГрафикРаботы,
	|	ДанныеПроизводственногоКалендаря.Дата КАК ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПредпраздничныйДень
	|ПОМЕСТИТЬ ВТПредпраздничныеДни
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|	ПО ИСТИНА
	|		И ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = Календари.ПроизводственныйКалендарь
	|		И ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &НачалоДняДатаНачала И &КонецДняДатаОкончания
	|		И ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|	ПО Календари.Ссылка = втГрафикиРаботы.ГрафикРаботы
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Календари.Ссылка КАК ГрафикРаботы,
	|	ДанныеПроизводственногоКалендаря.Дата КАК ДатаГрафика,
	|	ДанныеПроизводственногоКалендаря.ДатаПереноса КАК ДатаПереноса
	|ПОМЕСТИТЬ ВТДатыПереноса
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|	ПО ИСТИНА
	|		И ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = Календари.ПроизводственныйКалендарь
	|		И ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &НачалоДняДатаНачала И &КонецДняДатаОкончания
	|		И ДанныеПроизводственногоКалендаря.ДатаПереноса <> ДАТАВРЕМЯ(1, 1, 1)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|	ПО Календари.Ссылка = втГрафикиРаботы.ГрафикРаботы
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалендарныеГрафики.Календарь КАК ГрафикРаботы,
	|	КалендарныеГрафики.ДатаГрафика КАК ДатаГрафика,
	|	РАЗНОСТЬДАТ(Календари.ДатаОтсчета, КалендарныеГрафики.ДатаГрафика, ДЕНЬ) + 1 КАК ДнейОтДатыОтсчета,
	|	ПредпраздничныеДни.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|	ДатыПереноса.ДатаПереноса КАК ДатаПереноса
	|ПОМЕСТИТЬ ВТДниВключенныеВГрафик
	|ИЗ
	|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|	ПО ИСТИНА
	|		И КалендарныеГрафики.Календарь = Календари.Ссылка
	|		И КалендарныеГрафики.ДатаГрафика МЕЖДУ &НачалоДняДатаНачала И &КонецДняДатаОкончания
	|		И КалендарныеГрафики.ДеньВключенВГрафик
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|	ПО Календари.Ссылка = втГрафикиРаботы.ГрафикРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПредпраздничныеДни КАК ПредпраздничныеДни
	|	ПО ИСТИНА
	|		И ПредпраздничныеДни.ГрафикРаботы = КалендарныеГрафики.Календарь
	|		И ПредпраздничныеДни.ДатаГрафика = КалендарныеГрафики.ДатаГрафика
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыПереноса КАК ДатыПереноса
	|	ПО ИСТИНА
	|		И ДатыПереноса.ГрафикРаботы = КалендарныеГрафики.Календарь
	|		И ДатыПереноса.ДатаГрафика = КалендарныеГрафики.ДатаГрафика
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|	ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДниВключенныеВГрафик.РезультатДеленияПоМодулю = 0
	|			ТОГДА ДниВключенныеВГрафик.ДлинаЦикла
	|		ИНАЧЕ ДниВключенныеВГрафик.РезультатДеленияПоМодулю
	|	КОНЕЦ КАК НомерДня,
	|	ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень
	|ПОМЕСТИТЬ ВТДатыНомераДней
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|		ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|		ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|		ДниВключенныеВГрафик.ДлинаЦикла КАК ДлинаЦикла,
	|		ДниВключенныеВГрафик.ДнейОтДатыОтсчета - ДниВключенныеВГрафик.ЦелаяЧастьРезультатаДеления * ДниВключенныеВГрафик.ДлинаЦикла КАК РезультатДеленияПоМодулю
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|			ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|			ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|			ДниВключенныеВГрафик.ДнейОтДатыОтсчета КАК ДнейОтДатыОтсчета,
	|			ДлинаЦиклов.ДлинаЦикла КАК ДлинаЦикла,
	|			(ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) - ВЫБОР
	|					КОГДА (ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) > ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЦелаяЧастьРезультатаДеления
	|		ИЗ
	|			ВТДниВключенныеВГрафик КАК ДниВключенныеВГрафик
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|			ПО ИСТИНА
	|				И ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
	|				И Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоЦикламПроизвольнойДлины)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДлинаЦиклаГрафиков КАК ДлинаЦиклов
	|			ПО ДниВключенныеВГрафик.ГрафикРаботы = ДлинаЦиклов.ГрафикРаботы) КАК ДниВключенныеВГрафик) КАК ДниВключенныеВГрафик
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|	ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДниВключенныеВГрафик.ДатаПереноса ЕСТЬ NULL
	|			ТОГДА ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаГрафика)
	|		ИНАЧЕ ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаПереноса)
	|	КОНЕЦ КАК НомерДня,
	|	ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень
	|ИЗ
	|	ВТДниВключенныеВГрафик КАК ДниВключенныеВГрафик
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|	ПО ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
	|ГДЕ Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоНеделям)
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|// Определение расписания работ при помощи механизма БСП.Окончание
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|	ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|	ДниВключенныеВГрафик.НомерДня КАК НомерДня,
	|	ДОБАВИТЬКДАТЕ(ДниВключенныеВГрафик.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяНачала, РасписанияРаботы.ВремяНачала), СЕКУНДА)) КАК ДатаНачала,
	|	ДОБАВИТЬКДАТЕ(ДниВключенныеВГрафик.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяОкончания, РасписанияРаботы.ВремяОкончания), СЕКУНДА)) КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТРасписанияРаботы
	|ИЗ
	|	ВТДатыНомераДней КАК ДниВключенныеВГрафик
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботы
	|	ПО ИСТИНА
	|		И РасписанияРаботы.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы
	|		И РасписанияРаботы.НомерДня = ДниВключенныеВГрафик.НомерДня
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботыПредпраздничногоДня
	|	ПО ИСТИНА
	|		И РасписанияРаботыПредпраздничногоДня.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы
	|		И РасписанияРаботыПредпраздничногоДня.НомерДня = 0
	|		И ДниВключенныеВГрафик.ПредпраздничныйДень
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикиРаботы.Сотрудник КАК Сотрудник,
	|	ГрафикиРаботы.Период КАК ДатаГрафика,
	|	ГрафикиРаботы.ДатаНачала КАК ДатаНачала,
	|	ГрафикиРаботы.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТРасписанияРаботыИндивидуальныеГрафики
	|ИЗ
	|	РегистрСведений.упГрафикиРаботыСотрудников КАК ГрафикиРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСотрудники КАК втСотрудники
	|	ПО ИСТИНА
	|		И ГрафикиРаботы.Сотрудник = втСотрудники.Ссылка
	|		И втСотрудники.ИспользуетсяИндивидуальныйГрафикРаботы
	|ГДЕ ИСТИНА
	|	И &КонтрольРасписанияРаботыВодителей
	|	И ГрафикиРаботы.Период <= &КонецДняДатаОкончания
	|	И ГрафикиРаботы.Период >= &НачалоДняДатаНачала
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|// вап3456вс32 Склеивание периодов по дням в непрерывные периоды.Начало
	|ВЫБРАТЬ
	|	втСотрудникиТ.Ссылка КАК Сотрудник,
	|	ВТРасписанияРаботыТ.ДатаГрафика КАК Период,
	|	ВТРасписанияРаботыТ.ДатаНачала КАК ДатаНачала,
	|	ВТРасписанияРаботыТ.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ втПериодыРаботы
	|ИЗ
	|	втСотрудники КАК втСотрудникиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК ВТРасписанияРаботыТ
	|	ПО ИСТИНА
	|		И ВТРасписанияРаботыТ.ГрафикРаботы = втСотрудникиТ.ГрафикРаботы
	|		И НЕ (втСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втСотрудникиТ.Ссылка КАК Сотрудник,
	|	ВТРасписанияРаботыИндивидуальныеГрафикиТ.ДатаГрафика КАК Период,
	|	ВТРасписанияРаботыИндивидуальныеГрафикиТ.ДатаНачала КАК ДатаНачала,
	|	ВТРасписанияРаботыИндивидуальныеГрафикиТ.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	втСотрудники КАК втСотрудникиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботыИндивидуальныеГрафики КАК ВТРасписанияРаботыИндивидуальныеГрафикиТ
	|	ПО ИСТИНА
	|		И ВТРасписанияРаботыИндивидуальныеГрафикиТ.Сотрудник = втСотрудникиТ.Ссылка
	|		И втСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|// Для каждого периода определяется есть ли для него
	|// следующий(начало которого совпадает с окончанием
	|// текущего) и предыдущий(окончания которого
	|// совпадает с началом текущего)
	|ВЫБРАТЬ
	|	втПериоды.Сотрудник КАК Сотрудник,
	|	втПериоды.Период КАК Период,
	|	втПериоды.ДатаНачала КАК ДатаНачала,
	|	втПериоды.ДатаОкончания КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА втПериоды2.Период ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПредыдущийПериодКонецРавен,
	|	// Имеется предыдущий период
	|	// Имеется следующий период
	|	ВЫБОР
	|		КОГДА втПериоды3.Период ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СледующийПериодНачалоРавен
	|ПОМЕСТИТЬ втПериодыПодготовка
	|ИЗ
	|	втПериодыРаботы КАК втПериоды
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПериодыРаботы КАК втПериоды2
	|	ПО ИСТИНА
	|		И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|		И втПериоды.Период = втПериоды2.Период
	|		И втПериоды.ДатаНачала = втПериоды2.ДатаОкончания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПериодыРаботы КАК втПериоды3
	|	ПО ИСТИНА
	|		И втПериоды.Сотрудник = втПериоды3.Сотрудник
	|		И втПериоды.Период = втПериоды3.Период
	|		И втПериоды.ДатаОкончания = втПериоды3.ДатаНачала
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|// Периоды по дням с объедененными интервалами по времени.
	|// Здесь нет периодов по которым объединять нечего.
	|ВЫБРАТЬ
	|	втПериоды.Сотрудник КАК Сотрудник,
	|	втПериоды.Период КАК Период,
	|	втПериоды.ДатаНачала КАК ДатаНачала,
	|	МИНИМУМ(втПериоды2.ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ втОбъединенныеВремена
	|ИЗ
	|	втПериодыПодготовка КАК втПериоды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериодыПодготовка КАК втПериоды2
	|	ПО ИСТИНА
	|		И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|		И втПериоды.Период = втПериоды2.Период
	|		И втПериоды2.ДатаНачала > втПериоды.ДатаНачала
	|		И втПериоды.СледующийПериодНачалоРавен
	|		И втПериоды2.ПредыдущийПериодКонецРавен
	|		И НЕ (втПериоды2.СледующийПериодНачалоРавен)
	|		И НЕ (ИСТИНА
	|			И втПериоды.ПредыдущийПериодКонецРавен = ИСТИНА
	|			И втПериоды.СледующийПериодНачалоРавен = ИСТИНА)
	|СГРУППИРОВАТЬ ПО
	|	втПериоды.Сотрудник,
	|	втПериоды.Период,
	|	втПериоды.ДатаНачала
	|;
	|//{Запрос: 14 ////////////////////////////////////////
	|// Объединение периодов-дней с объединенными временными
	|// интервалами и периодов-дней по которым объединенять нечего
	|ВЫБРАТЬ
	|	втПериоды.Сотрудник КАК Сотрудник,
	|	втПериоды.Период КАК Период,
	|	втПериоды.ДатаНачала КАК ДатаНачала,
	|	втПериоды.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ втПериодыВремена
	|ИЗ
	|	втПериодыПодготовка КАК втПериоды
	|ГДЕ ИСТИНА
	|	И втПериоды.ПредыдущийПериодКонецРавен = ЛОЖЬ
	|	И втПериоды.СледующийПериодНачалоРавен = ЛОЖЬ
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втОбъединенныеВремена.Сотрудник КАК Сотрудник,
	|	втОбъединенныеВремена.Период КАК Период,
	|	втОбъединенныеВремена.ДатаНачала КАК ДатаНачала,
	|	втОбъединенныеВремена.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	втОбъединенныеВремена КАК втОбъединенныеВремена
	|;
	|//{Запрос: 15 ////////////////////////////////////////
	|// Для каждого периода определяется:
	|// Оканчивается ли он в 23:59:59
	|// Длится ли он целые сутки
	|// Имеется ли следующий период, который начинается в 00:00:00 следующего дня
	|// Имеется ли предыдущий период, который оканичивается в 23:59:29 предыдущего дня
	|ВЫБРАТЬ
	|	втПериоды.Сотрудник КАК Сотрудник,
	|	втПериоды.Период КАК Период,
	|	втПериоды.ДатаНачала КАК ДатаНачала,
	|	втПериоды.ДатаОкончания КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(втПериоды.Период, ДЕНЬ) = втПериоды.ДатаОкончания
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоКонцаСуток,
	|	// Дата окончания текущего периода - 23:59:59
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(втПериоды.ДатаНачала, втПериоды.ДатаОкончания, СЕКУНДА) = 86399
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПолныеСутки,
	|	// Текущий период длится сутки с 00:00:00 по 23:59:59
	|	ВЫБОР
	|		КОГДА втПериоды2.Период ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПредыдущийПериодДоКонцаСуток,
	|	// У текущего опериода есть предыущий, оканчивающийся в 23:59 предыдущих суток
	|	// У текущего опериода есть следующий, начинающийся в 00:00 следующих суток
	|	ВЫБОР
	|		КОГДА втПериоды3.Период ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СледующийПериодСНачалаСуток
	|ПОМЕСТИТЬ втПериоды
	|ИЗ
	|	втПериодыВремена КАК втПериоды
	|	ЛЕВОЕ СОЕДИНЕНИЕ // Периоды оканчивающиеся в 23:59 предыдущих суток
	|	втПериодыВремена КАК втПериоды2
	|	ПО ИСТИНА
	|		И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|		И втПериоды.ДатаНачала = втПериоды.Период
	|		И втПериоды.ДатаНачала = ДОБАВИТЬКДАТЕ(втПериоды2.ДатаОкончания, СЕКУНДА, 1)
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПериодыВремена КАК втПериоды3
	|	ПО ИСТИНА
	|		И втПериоды.Сотрудник = втПериоды3.Сотрудник
	|		И ДОБАВИТЬКДАТЕ(втПериоды.ДатаОкончания, СЕКУНДА, 1) = втПериоды3.ДатаНачала
	|		// Периоды начинающиеся в 00:00 следующих суток
	|	
	|;
	|//{Запрос: 16 ////////////////////////////////////////
	|// Периоды длительностью более суток
	|ВЫБРАТЬ
	|	втПериоды.Сотрудник КАК Сотрудник,
	|	втПериоды.ДатаНачала КАК ДатаНачала,
	|	МИНИМУМ(втПериоды2.ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ втОбъединенныеИнтервалы
	|ИЗ
	|	втПериоды КАК втПериоды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериоды КАК втПериоды2
	|	ПО ИСТИНА
	|		И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|		И втПериоды2.Период > втПериоды.Период
	|		И втПериоды.ДоКонцаСуток = ИСТИНА
	|		И втПериоды.СледующийПериодСНачалаСуток = ИСТИНА
	|		И НЕ (ИСТИНА
	|			И втПериоды.ПолныеСутки = ИСТИНА
	|			И втПериоды.ПредыдущийПериодДоКонцаСуток = ИСТИНА)
	|		И НЕ (ИСТИНА
	|			И втПериоды2.ПолныеСутки = ИСТИНА
	|			И втПериоды2.СледующийПериодСНачалаСуток = ИСТИНА)
	|СГРУППИРОВАТЬ ПО
	|	втПериоды.Сотрудник,
	|	втПериоды.ДатаНачала
	|;
	|//{Запрос: 17 ////////////////////////////////////////
	|//////////////////////////
	|// Склеивание периодов по дням в непрерывные периоды.Окончание
	|// Объединяются периоды длительностью дент и периоды
	|// длительностью более суток
	|ВЫБРАТЬ
	|	втПериоды.Сотрудник КАК Сотрудник,
	|	втПериоды.ДатаНачала КАК ДатаНачала,
	|	втПериоды.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ втПериодыСотрудникаПоРасписанию
	|ИЗ
	|	втПериоды КАК втПериоды
	|ГДЕ ИСТИНА
	|	И НЕ (ИСТИНА
	|		И втПериоды.Период = втПериоды.ДатаНачала
	|		И втПериоды.ПредыдущийПериодДоКонцаСуток = ИСТИНА)
	|	И НЕ (ИСТИНА
	|		И втПериоды.ДоКонцаСуток
	|		И втПериоды.СледующийПериодСНачалаСуток = ИСТИНА)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втОбъединенныеИнтервалы.Сотрудник КАК Сотрудник,
	|	втОбъединенныеИнтервалы.ДатаНачала КАК ДатаНачала,
	|	втОбъединенныеИнтервалы.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	втОбъединенныеИнтервалы КАК втОбъединенныеИнтервалы
	|;
	|//{Запрос: 18 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСотрудникиТ.ГрафикРаботы КАК ГрафикРаботы,
	|	втСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы КАК ИспользуетсяИндивидуальныйГрафикРаботы,
	|	втСотрудникиТ.Ссылка КАК Ссылка
	|ИЗ
	|	втСотрудники КАК втСотрудникиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПериодыСотрудникаПоРасписанию КАК втПериодыСотрудникаПоРасписаниюТ
	|	ПО ИСТИНА
	|		И &КонтрольРасписанияРаботыВодителей
	|		И втПериодыСотрудникаПоРасписаниюТ.Сотрудник = втСотрудникиТ.Ссылка
	|		И втПериодыСотрудникаПоРасписаниюТ.ДатаНачала <= &ДатаНачала
	|		И втПериодыСотрудникаПоРасписаниюТ.ДатаОкончания >= &ДатаОкончания
	|ГДЕ ЛОЖЬ
	|	ИЛИ НЕ (&КонтрольРасписанияРаботыВодителей)
	|	ИЛИ НЕ (втПериодыСотрудникаПоРасписаниюТ.Сотрудник ЕСТЬ NULL)
	|";
	
	Запрос.УстановитьПараметр("КонтрольРасписанияРаботыВодителей", КонтрольРасписанияРаботыВодителей);
	Запрос.УстановитьПараметр("УчитыватьРасписаниеРаботыЭкипажей", УчитыватьРасписаниеРаботыЭкипажей);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоПериодаПланирования);
	Запрос.УстановитьПараметр("ДатаОкончания", ОкончаниеПериодаПланирования);
	Запрос.УстановитьПараметр("НачалоДняДатаНачала", НачалоДня(НачалоПериодаПланирования));
	Запрос.УстановитьПараметр("КонецДняДатаОкончания", КонецДня(ОкончаниеПериодаПланирования));
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Рейс", ?(Рейс = Неопределено, Документы.упРейс.ПустаяСсылка(),Рейс));
	
	РезультатЗапроса = Запрос.Выполнить();
	Результат = Не РезультатЗапроса.Пустой();
	
	Если ТипЗнч(ВодителиНеПрошедшиеПроверку) = Тип("Массив") Тогда
		Если Результат Тогда
			мсвДоступныхВодителей = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
			ВодителиНеПрошедшиеПроверку = Новый Массив;
			Для каждого СотрудникТекущий Из Сотрудник Цикл
				Если мсвДоступныхВодителей.Найти(СотрудникТекущий) = Неопределено Тогда
					ВодителиНеПрошедшиеПроверку.Добавить(СотрудникТекущий);
				КонецЕсли;
			КонецЦикла;
		Иначе	
			ВодителиНеПрошедшиеПроверку = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Сотрудник);
		КонецЕсли;
	КонецЕсли;
			
	Возврат Результат;
	
КонецФункции

// Процедура выполняет проверку на пересечение планового и фактического периода рейса 
// с его другими фактическими периодами занятости.
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Набор движений.
//  РеквизитыОбъекта		 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//
Процедура ПроверитьПересечениеСФактическимРасписанием(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	//Если Ложь Тогда // Для контекстной подсказки
	//	Ссылка = Документы.упРейс.ПустаяСсылка();
	//	РеквизитыОбъекта = Ссылка;
	//	Движения = Ссылка.ПолучитьОбъект().Движения;
	//	ДополнительныеСвойства = Ссылка.ПолучитьОбъект().ДополнительныеСвойства;
	//КонецЕсли;

	//ПлановыеПараметры = ДополнительныеСвойства.ПлановыеПараметры;
	//ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
	//
	//Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//|	упРасписаниеРаботыТС.Регистратор КАК Документ,
	//|	упРасписаниеРаботыТС.ДатаНачала,
	//|	ДОБАВИТЬКДАТЕ(упРасписаниеРаботыТС.ДатаОкончания, СЕКУНДА, упРасписаниеРаботыТС.ТранспортноеСредство.ПерерывМеждуРейсами * 3600) КАК ДатаОкончания
	//|ПОМЕСТИТЬ втПериодыДоступности
	//|ИЗ
	//|	РегистрСведений.упРасписаниеРаботыТС КАК упРасписаниеРаботыТС
	//|ГДЕ
	//|	упРасписаниеРаботыТС.ТранспортноеСредство = &ТранспортноеСредство
	//|	И упРасписаниеРаботыТС.Документ <> &Рейс
	//|	И &ДатаНачала МЕЖДУ ПД2.ДатаНачала И ПД2.ДатаОкончания
	//|					ТОГДА ИСТИНА
	//|				КОГДА &ДатаОкончания МЕЖДУ ПД2.ДатаНачала И ПД2.ДатаОкончания
	//|					ТОГДА ИСТИНА
	//|				КОГДА ПД2.ДатаНачала МЕЖДУ ПД1.ДатаНачала И ПД1.ДатаОкончания
	//|					ТОГДА ИСТИНА
	//|				КОГДА ПД2.ДатаОкончания МЕЖДУ ПД1.ДатаНачала И ПД1.ДатаОкончания
	//|					ТОГДА ИСТИНА
	//|				ИНАЧЕ ЛОЖЬ
	//|			КОНЕЦ
	//|";
	//Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	//Запрос.УстановитьПараметр("ДатаНачала", ПлановыеПараметры.ПлановоеНачалоВыполнения);
	//Запрос.УстановитьПараметр("ДатаОкончания", ПлановыеПараметры.ПлановоеОкончаниеВыполнения);
	//Запрос.УстановитьПараметр("Рейс", Ссылка);
	//Выборка = Запрос.Выполнить().Выбрать();
	//Если Выборка.Следующий() Тогда 
	//	ТекстСообщения = НСтр(СтрШаблон("ru = 'Транспортное средство %1 фактически занято на весь планируемый период документа. Проведение не выполнено. %2'", Строка(ТранспортноеСредство), Строка(Ссылка)));
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,, Отказ);
	//КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтрольРезультатовПроведения()

// Процедура - Отменить документы по рейсу
//
// Параметры:
//  мсвПредложения	 - Массив	 - предложения на перевозку груза.
//  мсвЗаявкиНаТС	 - Массив	 - заявки на транспортное средство.
//  ТекстОшибки	 - Строка	 - описание ошибки.
//  Отказ			 - Булево	 - Истина, при отмене произошла ошибка.
//
Процедура ОтменитьДокументыПоРейсу(мсвПредложения, мсвЗаявкиНаТС, ТекстОшибки, Отказ) Экспорт
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Для каждого ПредложениеНаПеревозку Из мсвПредложения Цикл
		
		ПредложениеНаПеревозкуОбъект = ПредложениеНаПеревозку.ПолучитьОбъект();
		
		Попытка
			ПредложениеНаПеревозкуОбъект.ПометкаУдаления = Истина;
			ПредложениеНаПеревозкуОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);	
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОшибкаПриОтменеДокументовПоРейсу", УровеньЖурналаРегистрации.Ошибка, ,ПредложениеНаПеревозку, ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Прервать;
		КонецПопытки;	
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Для каждого ЗаявкаНаТС Из мсвЗаявкиНаТС Цикл
			
			ЗаявкаНаТСОбъект = ЗаявкаНаТС.ПолучитьОбъект();
			
			Попытка
				ЗаявкаНаТСОбъект.ПометкаУдаления = Истина;
				ЗаявкаНаТСОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);	
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
					ОписаниеОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("ОшибкаПриОтменеДокументовПоРейсу", УровеньЖурналаРегистрации.Ошибка, ,ЗаявкаНаТС, ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				Прервать;
			КонецПопытки;	
		КонецЦикла;
	КонецЕсли;

	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе	
		ТекстОшибки = Нстр("ru = 'При отмене документов, введенных на основании рейса, произошла ошибка'");
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Функция возвращает адрес возврата непринятого при разгрузке груза
//
// Параметры:
//  Рейс			 - ДокументСсылка.упРейс 	 - рейс.
//  Задание		 - ДокументСсылка.упЗаданиеНаПеревозкуГруза 	 - задание.
//  ГрузовоеМесто	 - СправочникСсылка.упГрузовыеМеста 	 - грузовое место.
//  Операция		 - ПеречислениеСсылка.упТипыОпераций 	 - тип операции.
// 
// Возвращаемое значение:
//  СправочникСсылка.упАдреса  - адрес разгрузки.
//
Функция ОпределитьАдресВозвратаОтмененногоГруза(Рейс, Задание, ГрузовоеМесто, Операция = Неопределено) Экспорт
	
	АдресРазгрузки = Справочники.упАдреса.ПустаяСсылка();
	
	Если Операция = Неопределено Тогда
		Операция = Перечисления.упТипыОпераций.Погрузка;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	упМаршрутПоРейсуСрезПоследних.Адрес,
	|	упРаботыПоРейсуСрезПоследних.ИдентификаторРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК упРаботыПоРейсуСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК упМаршрутПоРейсуСрезПоследних
	|		ПО упРаботыПоРейсуСрезПоследних.Рейс = упМаршрутПоРейсуСрезПоследних.Рейс
	|			И упРаботыПоРейсуСрезПоследних.Идентификатор = упМаршрутПоРейсуСрезПоследних.Идентификатор
	|ГДЕ
	|	упРаботыПоРейсуСрезПоследних.Рейс = &Рейс
	|	И упРаботыПоРейсуСрезПоследних.Операция = &Операция
	|	И &ГрузовоеМесто В (упРаботыПоРейсуСрезПоследних.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	|	И упРаботыПоРейсуСрезПоследних.Задание = &Задание";
	Запрос.УстановитьПараметр("Рейс",          Рейс);
	Запрос.УстановитьПараметр("Операция",      Операция);
	Запрос.УстановитьПараметр("ГрузовоеМесто", ГрузовоеМесто);
	Запрос.УстановитьПараметр("Задание",       Задание);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		АдресРазгрузки = Выборка.Адрес;
	КонецЕсли;	
	
	Возврат АдресРазгрузки;
	
КонецФункции

// Проверяет, исполняется ли рейс на мобильном клиенте
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс 	 - рейс.
// 
// Возвращаемое значение:
//  Булево - Истина, если исполняется на МК.
//
Функция РейсИсполняетсяНаМобильномКлиенте(Рейс) Экспорт
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	упУстановленныеТрекерыСрезПервых.Рейс
	|ИЗ
	|	РегистрСведений.упУстановленныеТрекеры.СрезПервых КАК упУстановленныеТрекерыСрезПервых
	|ГДЕ
	|	упУстановленныеТрекерыСрезПервых.Рейс = &Рейс
	|	И упУстановленныеТрекерыСрезПервых.Трекер.Модель.ТипУстройства = ЗНАЧЕНИЕ(Перечисление.упТипУстройства.АндроидКлиент)";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает плановое время начала рейса с учетом уже запланированных рейсов по указанному транспорту
//
Функция ПолучитьПлановоеВремяНачалаРейса(ТранспортноеСредство, НачалоПериода, ОкончаниеПериода) Экспорт
	
	НовоеВремя = Неопределено;
	
	мсвСтатусов = Новый Массив;
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.НазначеноТС);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения) КАК ПлановоеОкончаниеВыполнения,
	|	ВЫРАЗИТЬ(&ТранспортноеСредство КАК Справочник.упТранспортныеСредства).ПерерывМеждуРейсами КАК ПерерывМеждуРейсамиЧасы
	|ИЗ
	|	РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	|		ПО упСтатусыРейсовСрезПоследних.Документ = упПеревозчикПоРейсуСрезПоследних.Рейс
	|			И (упСтатусыРейсовСрезПоследних.Статус В (&МассивСтатусов))
	|			И (упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = &ТранспортноеСредство)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО упСтатусыРейсовСрезПоследних.Документ = упПлановыеПараметрыРейса.Рейс";	
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("МассивСтатусов"      , мсвСтатусов);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ОкончаниеПредыдущегоРейса = Выборка.ПлановоеОкончаниеВыполнения + Выборка.ПерерывМеждуРейсамиЧасы * 3600;
		Если ОкончаниеПредыдущегоРейса > НачалоПериода И ОкончаниеПредыдущегоРейса < ОкончаниеПериода Тогда
			НовоеВремя = ОкончаниеПредыдущегоРейса;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат НовоеВремя;
	
КонецФункции

// Функция - Определяет, является ли рейс операцией с контейнером
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс  - ссылка на документ
// 
// Возвращаемое значение:
//  Булево - Истина, является.
//
Функция ЭтоОперацияСКонтейнером(Рейс) Экспорт
	Результат = Ложь;
	
	Если Истина
		И ЗначениеЗаполнено(Рейс)
		И ТипЗнч(Рейс) = Тип("ДокументСсылка.упРейс")
	Тогда
		Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидТранспортнойУслуги") = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти

#Область КонтрольныеТочкиИПравилаИхВключенияВМаршрут

// Функция - Подобрает контрольные точки для каждого звена маршрута
//
// Параметры:
//  Рейс		 - ДокументСсылка.упРейс	 - рейс
//  Маршрут	 - ТаблицаЗначений - маршрут, передается в том случае если необходимо подобрать контрольные точки
//                                  не по текущему маршруту рейса, а по заданному маршруту в таблице.
//    * Адрес - СправочникСсылка.упАдреса - адрес точки
//    * ТипТочки - ПеречислениеСсылка.упТипыТочекМаршрута - тип точки
//    * СтрокаНомер - Число - порядковый номер в маршруте
//    * Пройдена - Булево - признак того что точка пройдена
//    * Отменена - Булево - признак отмены
//    * Гараж - СправочникСсылка.упГаражи - гараж
//    * Предшественники - Строка - предшественники
//    * Идентификатор - УникальныйИдентификатор - идентификатор
// 
// Возвращаемое значение:
//   Структура - контрольные точки и правила по звеньям маршрута
//    * Точки - ТаблицаЗначений - добавленные контрольные точки
//    * ПравилаВключенияПоЗвеньям - ТаблицаЗначений - таблица правил по звеньям,
//                                                    используется в сочетании с таблицей Точки, для добавления
//                                                    точек в маршрут.
//    * ЕстьТочки - Булевое - Истина, если удалось подобрать хотя бы одно правило.
//
Функция ПодобратьКонтрольныеТочкиМаршрута(Рейс, Маршрут = Неопределено) Экспорт
	
	сткРезультат = Новый Структура("Точки, ПравилаВключенияПоЗвеньям, ЕстьТочки", Неопределено, Неопределено, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Маршрут.Адрес КАК Адрес,
	|	Маршрут.ТипТочки КАК ТипТочки,
	|	Маршрут.НомерСтроки КАК СтрокаНомер,
	|	Маршрут.Пройдена КАК Пройдена,
	|	Маршрут.Отменена КАК Отменена,
	|	Маршрут.Гараж КАК Гараж,
	|	Маршрут.Предшественники КАК Предшественники,
	|	Маршрут.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втМаршрутТаблица
	|ИЗ
	|	&Маршрут КАК Маршрут
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правила.Ссылка КАК Ссылка,
	|	Правила.Важность КАК Важность,
	|	Правила.ПроверятьОтборПоНачальнойТочке КАК ПроверятьОтборПоНачальнойТочке,
	|	Правила.АдресНачальный КАК АдресНачальный,
	|	Правила.ВидАдресаНачальный КАК ВидАдресаНачальный,
	|	Правила.ЗонаНачальная КАК ЗонаНачальная,
	|	Правила.ПроверятьОтборПоКонечнойТочке КАК ПроверятьОтборПоКонечнойТочке,
	|	Правила.АдресКонечный КАК АдресКонечный,
	|	Правила.ВидАдресаКонечный КАК ВидАдресаКонечный,
	|	Правила.ЗонаКонечная КАК ЗонаКонечная,
	|	Правила.СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута КАК СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута,
	|	Правила.ЗапрещеноМногократноеПрименениеПравилаКРейсу КАК ЗапрещеноМногократноеПрименениеПравилаКРейсу
	|ПОМЕСТИТЬ втПравилаВключенияКонтрольныхТочек
	|ИЗ
	|	Справочник.упПравилаВключенияКонтрольныхТочекВРейс КАК Правила
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК РейсТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК ПлановыеПараметрыРейса
	|		ПО ПлановыеПараметрыРейса.Рейс = РейсТ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФактическиеПараметрыРейса
	|		ПО ФактическиеПараметрыРейса.Рейс = РейсТ.Ссылка
	|	ПО ИСТИНА
	|		И (ЛОЖЬ
	|			ИЛИ Правила.ВидПеревозкиОтбор = ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)
	|			ИЛИ Правила.ВидПеревозкиОтбор = РейсТ.ВидПеревозки)
	|		И (ЛОЖЬ
	|			ИЛИ Правила.ОрганизацияОтбор = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ Правила.ОрганизацияОтбор = РейсТ.Организация)
	|		И (ЛОЖЬ
	|			ИЛИ Правила.НаправлениеДеятельностиОтбор = ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка)
	|			ИЛИ Правила.НаправлениеДеятельностиОтбор = РейсТ.НаправлениеДеятельности)
	|ГДЕ ИСТИНА
	|	И Правила.Активно
	|	И РейсТ.Ссылка = &Рейс
	|	И (ЛОЖЬ
	|		ИЛИ Правила.ДатаНачалаДействия = ДАТАВРЕМЯ(1, 1, 1)
	|		ИЛИ Правила.ДатаНачалаДействия <= ЕСТЬNULL(ФактическиеПараметрыРейса.НачалоВыполнения, ЕСТЬNULL(ПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(3999, 12, 31))))
	|	И (ЛОЖЬ
	|		ИЛИ Правила.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
	|		ИЛИ Правила.ДатаОкончанияДействия >= ЕСТЬNULL(ФактическиеПараметрыРейса.НачалоВыполнения, ЕСТЬNULL(ПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1))))
	|	И НЕ (Правила.ПометкаУдаления)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упАдресаТ.Широта КАК Широта,
	|	упАдресаТ.Долгота КАК Долгота,
	|	Маршрут.Адрес КАК Адрес,
	|	упАдресаТ.ВидАдреса КАК ВидАдреса,
	|	упАдресаТ.ГеографическаяЗона КАК ГеографическаяЗона,
	|	Маршрут.ТипТочки КАК ТипТочки,
	|	Маршрут.СтрокаНомер КАК СтрокаНомер,
	|	Маршрут.Пройдена КАК Пройдена,
	|	Маршрут.Отменена КАК Отменена,
	|	Маршрут.Гараж КАК Гараж,
	|	Маршрут.Предшественники КАК Предшественники,
	|	Маршрут.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК Маршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресаТ
	|	ПО Маршрут.Адрес = упАдресаТ.Ссылка
	|ГДЕ ИСТИНА
	|	И НЕ (&МаршрутВТаблице)
	|	И НЕ (Маршрут.Отменена)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упАдресаТ.Широта КАК Широта,
	|	упАдресаТ.Долгота КАК Долгота,
	|	втМаршрутТаблицаТ.Адрес КАК Адрес,
	|	упАдресаТ.ВидАдреса КАК ВидАдреса,
	|	упАдресаТ.ГеографическаяЗона КАК ГеографическаяЗона,
	|	втМаршрутТаблицаТ.ТипТочки КАК ТипТочки,
	|	втМаршрутТаблицаТ.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрутТаблицаТ.Пройдена КАК Пройдена,
	|	втМаршрутТаблицаТ.Отменена КАК Отменена,
	|	втМаршрутТаблицаТ.Гараж КАК Гараж,
	|	втМаршрутТаблицаТ.Предшественники КАК Предшественники,
	|	втМаршрутТаблицаТ.Идентификатор КАК Идентификатор
	|ИЗ
	|	втМаршрутТаблица КАК втМаршрутТаблицаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресаТ
	|	ПО втМаршрутТаблицаТ.Адрес = упАдресаТ.Ссылка
	|ГДЕ ИСТИНА
	|	И &МаршрутВТаблице
	|	И НЕ (втМаршрутТаблицаТ.Отменена)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНомераДоступныхТочекНачало.СтрокаНомер КАК СтрокаНомерНачало,
	|	МИНИМУМ(втНомераДоступныхТочекКонец.СтрокаНомер) КАК СтрокаНомерКонец
	|ПОМЕСТИТЬ втНомераТочекМаршрута
	|ИЗ
	|	втМаршрут КАК втНомераДоступныхТочекНачало
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК втНомераДоступныхТочекКонец
	|	ПО втНомераДоступныхТочекКонец.СтрокаНомер > втНомераДоступныхТочекНачало.СтрокаНомер
	|СГРУППИРОВАТЬ ПО
	|	втНомераДоступныхТочекНачало.СтрокаНомер
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрутНачало.СтрокаНомер КАК НомерЗвена,
	|	втМаршрутНачало.Адрес КАК АдресНачало,
	|	втМаршрутНачало.ВидАдреса КАК ВидАдресаНачало,
	|	втМаршрутНачало.ГеографическаяЗона КАК ГеографическаяЗонаНачало,
	|	втМаршрутНачало.ТипТочки КАК ТипТочкиНачало,
	|	втМаршрутКонец.Адрес КАК АдресКонец,
	|	втМаршрутКонец.ВидАдреса КАК ВидАдресаКонец,
	|	втМаршрутКонец.ГеографическаяЗона КАК ГеографическаяЗонаКонец,
	|	втМаршрутКонец.ТипТочки КАК ТипТочкиКонец,
	|	втМаршрутКонец.Гараж КАК ГаражКонец,
	|	втМаршрутКонец.Идентификатор КАК ИдентификаторКонец,
	|	втМаршрутКонец.Предшественники КАК ПредшественникиКонец,
	|	втМаршрутКонец.Пройдена КАК ПройденаКонец,
	|	втМаршрутКонец.СтрокаНомер КАК СтрокаНомерКонец,
	|	втМаршрутНачало.Гараж КАК ГаражНачало,
	|	втМаршрутНачало.Идентификатор КАК ИдентификаторНачало,
	|	втМаршрутНачало.Предшественники КАК ПредшественникиНачало,
	|	втМаршрутНачало.Пройдена КАК ПройденаНачало,
	|	втМаршрутНачало.СтрокаНомер КАК СтрокаНомерНачало
	|ПОМЕСТИТЬ втЗвеньяМаршрута
	|ИЗ
	|	втНомераТочекМаршрута КАК втНомераТочекМаршрута
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрутНачало
	|	ПО втНомераТочекМаршрута.СтрокаНомерНачало = втМаршрутНачало.СтрокаНомер
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрутКонец
	|	ПО втНомераТочекМаршрута.СтрокаНомерКонец = втМаршрутКонец.СтрокаНомер
	|ГДЕ НЕ (втМаршрутКонец.Пройдена)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗвеньяМаршрутаТ.НомерЗвена КАК НомерЗвена,
	|	втПравила.Ссылка КАК Ссылка,
	|	втПравила.Важность.Порядок КАК Важность,
	|	втЗвеньяМаршрутаТ.ИдентификаторНачало КАК ИдентификаторНачало,
	|	втЗвеньяМаршрутаТ.АдресНачало КАК АдресНачало,
	|	втЗвеньяМаршрутаТ.АдресКонец КАК АдресКонец,
	|	втЗвеньяМаршрутаТ.ИдентификаторКонец КАК ИдентификаторКонец,
	|	втЗвеньяМаршрутаТ.ПредшественникиНачало КАК ПредшественникиНачало,
	|	втПравила.СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута КАК СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута,
	|	втПравила.ЗапрещеноМногократноеПрименениеПравилаКРейсу КАК ЗапрещеноМногократноеПрименениеПравилаКРейсу
	|ПОМЕСТИТЬ втПравила
	|ИЗ
	|	втПравилаВключенияКонтрольныхТочек КАК втПравила
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗвеньяМаршрута КАК втЗвеньяМаршрутаТ
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК ИерархияЗонаНачальная
	|	ПО ИСТИНА
	|		И втПравила.ПроверятьОтборПоНачальнойТочке
	|		И втПравила.ЗонаНачальная = ИерархияЗонаНачальная.Родитель
	|		И втЗвеньяМаршрутаТ.ГеографическаяЗонаНачало = ИерархияЗонаНачальная.Зона
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК ИерархияЗонаКонечная
	|	ПО ИСТИНА
	|		И втПравила.ПроверятьОтборПоКонечнойТочке
	|		И втПравила.ЗонаКонечная = ИерархияЗонаКонечная.Родитель
	|		И втЗвеньяМаршрутаТ.ГеографическаяЗонаКонец = ИерархияЗонаКонечная.Зона
	|ГДЕ ЛОЖЬ
	|	ИЛИ (ИСТИНА
	|		И НЕ (втПравила.ПроверятьОтборПоНачальнойТочке)
	|		И НЕ (втПравила.ПроверятьОтборПоКонечнойТочке))
	|	ИЛИ (ИСТИНА
	|		И втПравила.ПроверятьОтборПоНачальнойТочке
	|		И втПравила.ПроверятьОтборПоКонечнойТочке
	|		И НЕ (ИерархияЗонаНачальная.Зона ЕСТЬ NULL)
	|		И НЕ (ИерархияЗонаКонечная.Зона ЕСТЬ NULL)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.АдресНачальный = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ИЛИ втПравила.АдресНачальный = втЗвеньяМаршрутаТ.АдресНачало)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.ВидАдресаНачальный = ЗНАЧЕНИЕ(Справочник.упВидыАдресов.ПустаяСсылка)
	|			ИЛИ втПравила.ВидАдресаНачальный = втЗвеньяМаршрутаТ.ВидАдресаНачало)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.АдресКонечный = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ИЛИ втПравила.АдресКонечный = втЗвеньяМаршрутаТ.АдресКонец)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.ВидАдресаКонечный = ЗНАЧЕНИЕ(Справочник.упВидыАдресов.ПустаяСсылка)
	|			ИЛИ втПравила.ВидАдресаКонечный = втЗвеньяМаршрутаТ.ВидАдресаКонец))
	|	ИЛИ (ИСТИНА
	|		И втПравила.ПроверятьОтборПоНачальнойТочке
	|		И НЕ (втПравила.ПроверятьОтборПоКонечнойТочке)
	|		И НЕ (ИерархияЗонаНачальная.Зона ЕСТЬ NULL)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.АдресНачальный = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ИЛИ втПравила.АдресНачальный = втЗвеньяМаршрутаТ.АдресНачало)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.ВидАдресаНачальный = ЗНАЧЕНИЕ(Справочник.упВидыАдресов.ПустаяСсылка)
	|			ИЛИ втПравила.ВидАдресаНачальный = втЗвеньяМаршрутаТ.ВидАдресаНачало))
	|	ИЛИ (ИСТИНА
	|		И НЕ (втПравила.ПроверятьОтборПоНачальнойТочке)
	|		И втПравила.ПроверятьОтборПоКонечнойТочке
	|		И НЕ (ИерархияЗонаКонечная.Зона ЕСТЬ NULL)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.АдресКонечный = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ИЛИ втПравила.АдресКонечный = втЗвеньяМаршрутаТ.АдресКонец)
	|		И (ЛОЖЬ
	|			ИЛИ втПравила.ВидАдресаКонечный = ЗНАЧЕНИЕ(Справочник.упВидыАдресов.ПустаяСсылка)
	|			ИЛИ втПравила.ВидАдресаКонечный = втЗвеньяМаршрутаТ.ВидАдресаКонец))
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПравилаТ.НомерЗвена КАК НомерЗвена,
	|	МАКСИМУМ(втПравилаТ.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ втПравилаПоЗвеньям
	|ИЗ
	|	втПравила КАК втПравилаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втПравила.НомерЗвена КАК НомерЗвена,
	|		МАКСИМУМ(втПравила.Важность) КАК Важность
	|	ИЗ
	|		втПравила КАК втПравила
	|	СГРУППИРОВАТЬ ПО
	|		втПравила.НомерЗвена) КАК втПравилаВажность
	|	ПО ИСТИНА
	|		И втПравилаТ.НомерЗвена = втПравилаВажность.НомерЗвена
	|		И втПравилаТ.Важность = втПравилаВажность.Важность
	|СГРУППИРОВАТЬ ПО
	|	втПравилаТ.НомерЗвена
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПравилаТ.НомерЗвена КАК НомерЗвена,
	|	втПравилаТ.Ссылка КАК Ссылка,
	|	втПравилаТ.ИдентификаторНачало КАК ИдентификаторНачало,
	|	втПравилаТ.ИдентификаторКонец КАК ИдентификаторКонец,
	|	втПравилаТ.АдресНачало КАК АдресНачало,
	|	втПравилаТ.АдресКонец КАК АдресКонец,
	|	втПравилаТ.ПредшественникиНачало КАК ПредшественникиНачало,
	|	втПравилаТ.СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута КАК СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута,
	|	втПравилаТ.ЗапрещеноМногократноеПрименениеПравилаКРейсу КАК ЗапрещеноМногократноеПрименениеПравилаКРейсу
	|ПОМЕСТИТЬ втПодробранныеПравила
	|ИЗ
	|	втПравила КАК втПравилаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПравилаПоЗвеньям КАК втПравилаПоЗвеньям
	|	ПО ИСТИНА
	|		И втПравилаТ.Ссылка = втПравилаПоЗвеньям.Ссылка
	|		И втПравилаТ.НомерЗвена = втПравилаПоЗвеньям.НомерЗвена
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаВключенияКонтрольныхТочек.Адрес КАК Адрес,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПравилаВключенияКонтрольныхТочек.Адрес) КАК АдресПредставление,
	|	втПодробранныеПравилаТ.НомерЗвена КАК НомерЗвена,
	|	ПравилаВключенияКонтрольныхТочек.Адрес.Широта КАК Широта,
	|	ПравилаВключенияКонтрольныхТочек.Адрес.Долгота КАК Долгота
	|ИЗ
	|	Справочник.упПравилаВключенияКонтрольныхТочекВРейс.КонтрольныеТочки КАК ПравилаВключенияКонтрольныхТочек
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПодробранныеПравила КАК втПодробранныеПравилаТ
	|	ПО втПодробранныеПравилаТ.Ссылка = ПравилаВключенияКонтрольныхТочек.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	НомерЗвена
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПодробранныеПравилаТ.ИдентификаторКонец КАК ИдентификаторКонец,
	|	втПодробранныеПравилаТ.ИдентификаторНачало КАК ИдентификаторНачало,
	|	втПодробранныеПравилаТ.АдресНачало КАК АдресНачало,
	|	втПодробранныеПравилаТ.АдресКонец КАК АдресКонец,
	|	втПодробранныеПравилаТ.НомерЗвена КАК НомерЗвена,
	|	втПодробранныеПравилаТ.Ссылка КАК Правило,
	|	втПодробранныеПравилаТ.ПредшественникиНачало КАК ПредшественникиНачало,
	|	втПодробранныеПравилаТ.СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута КАК СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута,
	|	втПодробранныеПравилаТ.ЗапрещеноМногократноеПрименениеПравилаКРейсу
	|ИЗ
	|	втПодробранныеПравила КАК втПодробранныеПравилаТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПримененныеПравилаВключенияКонтрольныхТочек КАК упПримененныеПравилаВключенияКонтрольныхТочекТ
	|	ПО ИСТИНА
	|		И упПримененныеПравилаВключенияКонтрольныхТочекТ.ПравилоВключенияКонтрольныхТочек = втПодробранныеПравилаТ.Ссылка
	|		И упПримененныеПравилаВключенияКонтрольныхТочекТ.Рейс = &Рейс
	|ГДЕ ЛОЖЬ
	|	ИЛИ (ИСТИНА
	|		И втПодробранныеПравилаТ.ЗапрещеноМногократноеПрименениеПравилаКРейсу
	|		И упПримененныеПравилаВключенияКонтрольныхТочекТ.ПравилоВключенияКонтрольныхТочек ЕСТЬ NULL)
	|	ИЛИ НЕ втПодробранныеПравилаТ.ЗапрещеноМногократноеПрименениеПравилаКРейсу
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЗвена
	|";


	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	тбзМаршрут = Новый ТаблицаЗначений;
	тбзМаршрут.Колонки.Добавить("Адрес", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	тбзМаршрут.Колонки.Добавить("ТипТочки", Новый ОписаниеТипов("ПеречислениеСсылка.упТипыТочекМаршрута"));
	тбзМаршрут.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число",
									Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Любой)));
	тбзМаршрут.Колонки.Добавить("Пройдена", Новый ОписаниеТипов("Булево"));
	тбзМаршрут.Колонки.Добавить("Отменена", Новый ОписаниеТипов("Булево"));
	тбзМаршрут.Колонки.Добавить("Гараж", Новый ОписаниеТипов("СправочникСсылка.упГаражи"));
	тбзМаршрут.Колонки.Добавить("Предшественники", Новый ОписаниеТипов("Строка", ,
									       Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
	тбзМаршрут.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка", ,
									     Новый КвалификаторыСтроки(36, ДопустимаяДлина.Фиксированная)));
	
	Если Маршрут = Неопределено Тогда
		Запрос.УстановитьПараметр("МаршрутВТаблице", Ложь);
	Иначе
		Запрос.УстановитьПараметр("МаршрутВТаблице", Истина);
		Для каждого СтрокаМаршрута Из Маршрут Цикл
			НоваяСтрока = тбзМаршрут.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМаршрута);
			НоваяСтрока.Идентификатор = Строка(СтрокаМаршрута.Идентификатор);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Маршрут", тбзМаршрут);
		
	ИндексЗапросаКонтрольныеТочки = 8;
	ИндексЗапросаЗвенья = 9;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Не РезультатЗапроса[ИндексЗапросаЗвенья].Пустой() Тогда
		сткРезультат.Вставить("Точки",РезультатЗапроса[ИндексЗапросаКонтрольныеТочки].Выгрузить());
		тбзПравила = РезультатЗапроса[ИндексЗапросаЗвенья].Выгрузить();
		Для каждого Строка Из тбзПравила Цикл
			Строка.ИдентификаторНачало = Строка(Строка.ИдентификаторНачало);
			Строка.ИдентификаторКонец = Строка(Строка.ИдентификаторКонец);
		КонецЦикла;
		сткРезультат.Вставить("ПравилаВключенияПоЗвеньям",тбзПравила);
		сткРезультат.Вставить("ЕстьТочки", Истина);
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Процедура - Добавить подобранные контрольные точки в маршрут
//
// Параметры:
//  Рейс						 - ДокументСсылка.упРейс	 - рейс.
//  Маршрут					 - ТабличнаяЧасть	 - маршрут.
//  ПодобранныеКонтрольныеТочки	 - ТаблицаЗначений	 - подобранные точки.
//  ПравилаПодбораТочек			 - ТаблицаЗначений	 - правила.
//  ДополнительныеПараметры		 - Структура	 - дополнительные параметры.
//
Процедура ДобавитьПодобранныеКонтрольныеТочкиВМаршрут(Рейс, Маршрут, ПодобранныеКонтрольныеТочки, ПравилаПодбораТочек, ДополнительныеПараметры) Экспорт
	
	ИмяСобытия = Нстр("ru = 'Подбор контрольных точек'");
	
	МестоВызова = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "МестоВызова");
	ПримененныеПравилаВключенияКонтрольныхТочек = ДополнительныеПараметры.ПримененныеПравилаВключенияКонтрольныхТочек;
	МестоВызоваКорректировкаРейса = МестоВызова = "КорректировкаРейса";
	
	ТипТочки = Перечисления.упТипыТочекМаршрута.КонтрольнаяТочка;
	ИндексКартинкиТочки = упПрохождениеТочкиКлиентСервер.ИндексКартинкиТочки(ТипТочки);
	
	ИндексКонтрольнойТочки = 0;
	НомерСтрокиТочкиПересчета = Неопределено;
	ПересчитыватьПредыдущиеТочки = Ложь;
	ПересчитыватьСледующиеТочки = Ложь;
	ИндексТочкиПересчета = 0;
	
	Для каждого СтрокаПравило Из ПравилаПодбораТочек Цикл
		
		Если СтрокаПравило.ЗапрещеноМногократноеПрименениеПравилаКРейсу Тогда
			ПримененныеПравилаВключенияКонтрольныхТочек.Добавить(СтрокаПравило.Правило);
		КонецЕсли;
				
		СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута = СтрокаПравило.СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута;
		
		ПересчитыватьПредыдущиеТочки = Ложь
		                               Или ПересчитыватьПредыдущиеТочки
								 Или СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута;
								 
		ПересчитыватьСледующиеТочки = Ложь
		                              Или ПересчитыватьСледующиеТочки
								Или Не СдвигатьВремяНачалаРейсаНаРазницуОтИзмененияМаршрута;
		
		Если НомерСтрокиТочкиПересчета = Неопределено Тогда
			НомерСтрокиТочкиПересчета = СтрокаПравило.НомерЗвена;
			Если ПересчитыватьСледующиеТочки Тогда
				ИндексТочкиПересчета = СтрокаПравило.НомерЗвена - 1;
			КонецЕсли;
		КонецЕсли;
					
		мсвСтрокКонтрольныеТочки = ПодобранныеКонтрольныеТочки.НайтиСтроки(Новый Структура("НомерЗвена", СтрокаПравило.НомерЗвена)); 
		
		ИндексТочкиОкончания = СтрокаПравило.НомерЗвена;
		
		Если мсвСтрокКонтрольныеТочки.Количество() > 0 Тогда
			
			мсвПредшественники = Новый Массив;
			Если ЗначениеЗаполнено(СтрокаПравило.ПредшественникиНачало) Тогда
				мсвПредшественники = СтрРазделить(СтрокаПравило.ПредшественникиНачало,",");
			КонецЕсли;	
			
			мсвПредшественники.Добавить(СтрокаПравило.ИдентификаторНачало);
			
			ТекстСообщения = СтрШаблон("Для звена %1
			|Начало: ""%2""(%3) 
			|Окончание: ""%4""(%5)
			|подобрано правило %6", 
			СтрокаПравило.НомерЗвена, 
			СтрокаПравило.АдресНачало, 
			СтрокаПравило.ИдентификаторНачало, 
			СтрокаПравило.АдресКонец, 
			СтрокаПравило.ИдентификаторКонец, 
			СтрокаПравило.Правило);
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Примечание,,Рейс, ТекстСообщения);
			
			мсвИдентификаторыКонтрольныхТочек = Новый Массив;
			ИндексНачальнойТочки = Неопределено;
			мсвНачальнаяТочка = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки", СтрокаПравило.НомерЗвена));
			Если мсвНачальнаяТочка.Количество() > 0 Тогда
				ИндексНачальнойТочки = Маршрут.Индекс(мсвНачальнаяТочка[0]) + 1;
			КонецЕсли;
			
			Если ИндексНачальнойТочки <> Неопределено Тогда
				
				КоличествоДобавленныхТочек = 0;
				
				// Добавляются контрольные точки.
				Для каждого СтрокаТочка Из мсвСтрокКонтрольныеТочки Цикл
					
					ИндексКонтрольнойТочки = ИндексНачальнойТочки + КоличествоДобавленныхТочек;
					СтрокаМаршрута = Маршрут.Вставить(ИндексКонтрольнойТочки);
					СтрокаМаршрута.Адрес = СтрокаТочка.Адрес;
					СтрокаМаршрута.ТипТочки = ТипТочки;
					СтрокаМаршрута.ИндексКартинки = ИндексКартинкиТочки;
					СтрокаМаршрута.Предшественники = СтрСоединить(мсвПредшественники, ",");
					СтрокаМаршрута.Идентификатор = Новый УникальныйИдентификатор;
					Если МестоВызоваКорректировкаРейса Тогда
						СтрокаМаршрута.СтрокаДокумента = Истина;	
					Иначе
						СтрокаМаршрута.Пометка = Истина;
						СтрокаМаршрута.Позиционирование = Истина;
						СтрокаМаршрута.ПроездНеРассчитан = Истина;
						СтрокаМаршрута.Координаты = Формат(СтрокаТочка.Широта, "ЧРД=.; ЧН=; ЧГ=0") + ";" + Формат(СтрокаТочка.Долгота, "ЧРД=.; ЧН=; ЧГ=0") + ";";
						СтрокаМаршрута.АдресПредставление = СтрокаТочка.АдресПредставление;
					КонецЕсли;
					КоличествоДобавленныхТочек = КоличествоДобавленныхТочек + 1;
					мсвПредшественники.Добавить(СтрокаМаршрута.Идентификатор);
					мсвИдентификаторыКонтрольныхТочек.Добавить(Строка(СтрокаМаршрута.Идентификатор));
				КонецЦикла;
				
				// Для последней точки в предшественники добавляются все контрольные точки
				ИндексТочкиОкончания = ИндексКонтрольнойТочки + 1;
				ТочкаОкончание = Маршрут.Получить(ИндексТочкиОкончания);
				
				мсвПредшественники = Новый Массив;
				Если ЗначениеЗаполнено(ТочкаОкончание.Предшественники) Тогда
					мсвПредшественники = СтрРазделить(ТочкаОкончание.Предшественники,",");
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвПредшественники, мсвИдентификаторыКонтрольныхТочек);
				мсвПредшественники = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПредшественники);
							
				ТочкаОкончание.Предшественники = СтрСоединить(мсвПредшественники, ",");
			КонецЕсли;
		КонецЕсли;
		
		Если ПересчитыватьПредыдущиеТочки Тогда
			ИндексТочкиПересчета = ИндексТочкиОкончания;
		КонецЕсли;
		
	КонецЦикла;
	
	// Пересчитать номера строк маршрута
	Если НомерСтрокиТочкиПересчета <> Неопределено Тогда
		Для й = НомерСтрокиТочкиПересчета По Маршрут.Количество() Цикл
			СтрокаМаршрута = Маршрут.Получить(й-1);
			Если МестоВызоваКорректировкаРейса Тогда
				СтрокаМаршрута.СтрокаНомер = СтрокаМаршрута.НомерСтроки;
			Иначе
				СтрокаМаршрута.НомерСтроки = СтрокаМаршрута.НомерСтроки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ИндексТочкиПересчета", ИндексТочкиПересчета);
	ДополнительныеПараметры.Вставить("ПересчитыватьПредыдущиеТочки", ПересчитыватьПредыдущиеТочки);
	ДополнительныеПараметры.Вставить("ПересчитыватьСледующиеТочки", ПересчитыватьСледующиеТочки);
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ЗаполнитьПлановыеПараметрыРейса(Рейс, тбзЗадания, Маршрут, Работы, ВедетсяВалютныйУчет, сткПлановыеПараметры, Дата, ВидЗоныДляФормированияПредставленияРейса, ВариантФормированияМаршрута, ДополнительныеСвойства) Экспорт
				
		Если Маршрут.Количество() > 0 Тогда
			сткПлановыеПараметры.Вставить("ПлановоеНачалоВыполнения", Маршрут[0].ПлановоеПрибытие);
			сткПлановыеПараметры.Вставить("ПлановоеОкончаниеВыполнения", Маршрут[Маршрут.Количество() - 1].ПлановоеУбытие);
		КонецЕсли;
		
		Если сткПлановыеПараметры.Свойство("ПлановоеОкончаниеВыполнения") Тогда
			сткПлановыеПараметры.Вставить("ПлановоеОкончаниеВыполненияИсходное", сткПлановыеПараметры.ПлановоеОкончаниеВыполнения);	
		КонецЕсли;
						
		Если ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон") Тогда
			Если Не сткПлановыеПараметры.Свойство("ЗонаПогрузки")  Тогда
				//ОпределитьЗоныПогрузкиРазгрузки(Реквизиты.Работы, Реквизиты.Маршрут, Реквизиты.ПлановыеПараметры);
				ОпределитьЗоныПогрузкиРазгрузки(Работы, Маршрут, сткПлановыеПараметры, ВариантФормированияМаршрута);
			КонецЕсли;
		КонецЕсли;
		
		ОценочнаяСтоимость = 0;
		ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
		Если ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			ОценочнаяСтоимость = упРейс.ОценочнаяСтоимостьРейса(Рейс);	
		КонецЕсли;
		сткПлановыеПараметры.Вставить("ОценочнаяСтоимость", ОценочнаяСтоимость);
		
		СуммаИнкассации = 0;
		Если ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") Тогда
			сткСуммыИнкассации = упРейс.СуммыИнкассацииРейса(Рейс, Дата, ВедетсяВалютныйУчет);
			СуммаИнкассации = сткСуммыИнкассации.Плановая;	
		КонецЕсли;
		сткПлановыеПараметры.Вставить("СуммаИнкассации", СуммаИнкассации);
	
		#Область РасчетПоказателейЗагрузкиРейса
		
		РасстояниеВсего = 0;
		НулевойПробегВсего = 0;
		ВремяВПутиВсего = 0;
		ВремяВсего = 0;
		ВремяОтдыхаВПутиВсего = 0;
			
		МассаМакс = 0;
		ОбъемМакс = 0;
		КоличествоМестМакс = 0;
		КоличествоЗагрузочныхМетровМакс = 0;
		КоличествоГрузовыхМестМакс = 0;
		
		НеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
		
		Если НеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			соотПоказателиПоТочкам = упРасчетПоказателейЗагрузки.РассчитатьЗагрузкуВТочкахМаршрутаУправлениеТранспортом(Маршрут.Скопировать(, "СтрокаНомер, Идентификатор"), Работы.Скопировать(,"Идентификатор, Операция, Масса, Объем"));
		Иначе	
			соотПоказателиПоТочкам = упРасчетПоказателейЗагрузки.РассчитатьЗагрузкуВТочкахМаршрута(Маршрут.Скопировать(, "СтрокаНомер, Идентификатор"), Работы.Скопировать(,"Идентификатор, Операция, Задание, Тара, ГрузовоеМесто, КоличествоГрузов"), ДополнительныеСвойства);
		КонецЕсли; 
		
		КоличествоГрузовДоПрибытияВТочку = 0;
		МассаДоПрибытияВТочку = 0;
		ОбъемДоПрибытияВТочку = 0;
		
		Для каждого текТочка Из Маршрут Цикл
			сткПоказатели = соотПоказателиПоТочкам.Получить(Строка(текТочка.Идентификатор));
			МассаМакс = Макс(МассаМакс, сткПоказатели.Масса);
			ОбъемМакс = Макс(ОбъемМакс, сткПоказатели.Объем);
			КоличествоМестМакс = Макс(КоличествоМестМакс, сткПоказатели.КоличествоМест);
			КоличествоЗагрузочныхМетровМакс = Макс(КоличествоЗагрузочныхМетровМакс, сткПоказатели.КоличествоЗагрузочныхМетров);
			КоличествоГрузовыхМестМакс = Макс(КоличествоГрузовыхМестМакс, сткПоказатели.КоличествоГрузовыхМест);
			ЗаполнитьЗначенияСвойств(текТочка, сткПоказатели);
			РасстояниеВсего = РасстояниеВсего + текТочка.РасстояниеОтПредыдущейТочки;
			ЭтоНулевойПробег = Ложь;
					
			Если НеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
				Если МассаДоПрибытияВТочку = 0 И ОбъемДоПрибытияВТочку = 0 Тогда
					ЭтоНулевойПробег = Истина;
				КонецЕсли;
			Иначе	
				Если КоличествоГрузовДоПрибытияВТочку = 0 Тогда
					ЭтоНулевойПробег = Истина;
				КонецЕсли;
			КонецЕсли;
			Если ЭтоНулевойПробег Тогда
				НулевойПробегВсего = НулевойПробегВсего + текТочка.РасстояниеОтПредыдущейТочки; 	
			КонецЕсли;
			
			Если ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
				КоличествоГрузовДоПрибытияВТочку = сткПоказатели.КоличествоГрузов;
			КонецЕсли;
			
			МассаДоПрибытияВТочку = сткПоказатели.Масса;
			ОбъемДоПрибытияВТочку = сткПоказатели.Объем;
			
			ВремяВПутиВсего = ВремяВПутиВсего + текТочка.ВремяОтПредыдущейТочки;
			ВремяОтдыхаВПутиВсего = ВремяОтдыхаВПутиВсего + текТочка.ВремяОтдыхаВПутиОтПредыдущейТочки;
			ВремяВсего = ВремяВсего + текТочка.ВремяОтПредыдущейТочки + текТочка.ВремяОжиданияВТочке + текТочка.ВремяРабот + текТочка.ВремяОтдыхаВПутиОтПредыдущейТочки;
		КонецЦикла; 
		
		сткПлановыеПараметры.Вставить("Масса", МассаМакс);
		сткПлановыеПараметры.Вставить("Объем", ОбъемМакс);
		сткПлановыеПараметры.Вставить("КоличествоМест", КоличествоМестМакс);
		сткПлановыеПараметры.Вставить("КоличествоЗагрузочныхМетров", КоличествоЗагрузочныхМетровМакс);
		сткПлановыеПараметры.Вставить("КоличествоГрузовыхМест", КоличествоГрузовыхМестМакс);
		сткПлановыеПараметры.Вставить("Расстояние", РасстояниеВсего);
		сткПлановыеПараметры.Вставить("НулевойПробег", НулевойПробегВсего);
		сткПлановыеПараметры.Вставить("ВремяВПути", ВремяВПутиВсего);
		сткПлановыеПараметры.Вставить("Время", ВремяВсего);
		сткПлановыеПараметры.Вставить("ВремяОтдыхаВПути", ВремяОтдыхаВПутиВсего);
		
		#КонецОбласти  		
				
		тбзЗадания.Свернуть("Задание");
		сткПлановыеПараметры.Вставить("ПланируемоеКоличествоЗаданий", тбзЗадания.Количество());
		сткПлановыеПараметры.Вставить("ПредставлениеМаршрута", упРейс.ПолучитьПредставлениеДляРейса(Маршрут, ВидЗоныДляФормированияПредставленияРейса));
	
КонецПроцедуры

Функция ТекстЗапросаРейсыЗаПериодПоСотруднику() 
	
	Результат = "
	|// По сотруднику за период существуют рейсы
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Сотрудник КАК Сотрудник
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Водитель1 = &Сотрудник
	|			Или Водитель2 = &Сотрудник) КАК УпПеревозчикПоРейсу_СрезПоследнихТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейсаТ
	|	ПО ИСТИНА
	|ГДЕ ИСТИНА
	|	И упПлановыеПараметрыРейсаТ.ПлановоеНачалоВыполнения < &ОкончаниеПериода
	|	И упПлановыеПараметрыРейсаТ.ПлановоеОкончаниеВыполнения > &НачалоПериода
	|";

	Возврат Результат
	
КонецФункции

Функция ТекстЗапросаРасписанияРаботыЗаПериод() 
	
	Результат = "
	|// По сотруднику за период существуют рейсы
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Сотрудник КАК Сотрудник
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Водитель1 = &Сотрудник
	|			Или Водитель2 = &Сотрудник) КАК УпПеревозчикПоРейсу_СрезПоследнихТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейсаТ
	|	ПО ИСТИНА
	|ГДЕ ИСТИНА
	|	И упПлановыеПараметрыРейсаТ.ПлановоеНачалоВыполнения < &ОкончаниеПериода
	|	И упПлановыеПараметрыРейсаТ.ПлановоеОкончаниеВыполнения > &НачалоПериода
	|";

	Возврат Результат
	
КонецФункции

#КонецОбласти
