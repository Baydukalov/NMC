#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Процедура формирует записи по регистру "упСостоянияТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	Состояние 				= ДополнительныеСвойства.Состояние;
	ДатаИзмененияСостояния 	= ДополнительныеСвойства.ДатаИзмененияСостояния;
		
	ДвиженияСостояние = Движения.упСостоянияТранспортныхСредств;
	ДвиженияСостояние.Записывать = Истина;
	
	ДвижениеСостояние = ДвиженияСостояние.Добавить();
	ДвижениеСостояние.Период					= ДатаИзмененияСостояния;
	ДвижениеСостояние.ТранспортноеСредство      = Реквизиты.ТранспортноеСредство;
	ДвижениеСостояние.СостояниеТС      			= Состояние;
		
КонецПроцедуры	

// Процедура формирует записи по регистру "упНаправленияДеятельностиТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеНаправленияДеятельностиТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаправленияДеятельностиТС = Движения.упНаправленияДеятельностиТранспортныхСредств;
	ДвиженияНаправленияДеятельностиТС.Записывать = Истина;
	ДвиженияНаправленияДеятельностиТС.Очистить();
	
	Если Реквизиты.Свойство("НаправлениеДеятельности") Тогда 
		НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
		Если ЗначениеЗаполнено(НаправлениеДеятельности) Тогда 		
			
			Движение = ДвиженияНаправленияДеятельностиТС.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
			Движение.Период = Реквизиты.Дата;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвижениеНаправленияДеятельностиТС

// Процедура формирует записи по регистру "упВыработкаТС".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеВыработкаТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Моточасы = Реквизиты.МоточасыВыработка;
	Пробег   = Реквизиты.ПробегВыработка;
	ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
	Если ДополнительныеСвойства.Свойство("АнализироватьОстатокВыработки") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упВыработкаТСОстатки.МоточасыОстаток КАК ОстатокМоточасовДоКапитальногоРемонта,
		|	упВыработкаТСОстатки.ПробегОстаток	КАК ОстатокПробегаДоКапитальногоРемонта
		|ИЗ
		|	РегистрНакопления.упВыработкаТС.Остатки(, ТранспортноеСредство = &ТранспортноеСредство) КАК упВыработкаТСОстатки";
		Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Моточасы = Мин(Выборка.ОстатокМоточасовДоКапитальногоРемонта, Моточасы); 	
			Пробег   = Мин(Выборка.ОстатокПробегаДоКапитальногоРемонта, Пробег); 	
		Иначе
			Моточасы = 0;
			Пробег   = 0;
		КонецЕсли; 
	КонецЕсли;
	
	ДвиженияВыработкаТС	= Движения.упВыработкаТС;
	ДвиженияВыработкаТС.Записывать = Ложь;
	Если Ложь
		Или Реквизиты.Сумма > 0 
		Или Моточасы > 0 
		Или Пробег > 0
	Тогда
		ДвиженияВыработкаТС.Записывать = Истина;
		
		ДвижениеВыработкаТС	= ДвиженияВыработкаТС.Добавить();
		ДвижениеВыработкаТС.ВидДвижения = Реквизиты.ВидДвиженияВыработка;
		ДвижениеВыработкаТС.Период   = Реквизиты.Дата;
		ДвижениеВыработкаТС.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
		ДвижениеВыработкаТС.Сумма    = Реквизиты.Сумма;
		ДвижениеВыработкаТС.Моточасы = Моточасы;
		ДвижениеВыработкаТС.Пробег   = Пробег;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упВыработкаТС".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеИзмененаВыработкаТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	ДвиженияВыработкаТС	= Движения.упВыработкаТС;
	ДвиженияВыработкаТС.Записывать = Истина;
	
	ИзмененаВыработкаТС = Неопределено;
	
	Если Истина
		И Реквизиты.Свойство("ИзмененаВыработкаТС", ИзмененаВыработкаТС)
		И ИзмененаВыработкаТС 
	Тогда
		Моточасы = Реквизиты.МоточасыВыработка;
		Пробег   = Реквизиты.ПробегВыработка;
		ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
		Если Ложь
			Или Реквизиты.Сумма > 0 
			Или Моточасы > 0 
			Или Пробег > 0
		Тогда
				
			ДвижениеВыработкаТС	= ДвиженияВыработкаТС.Добавить();
			ДвижениеВыработкаТС.ВидДвижения = Реквизиты.ВидДвиженияВыработка;
			ДвижениеВыработкаТС.Период   = Реквизиты.Дата;
			ДвижениеВыработкаТС.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
			ДвижениеВыработкаТС.Сумма    = Реквизиты.Сумма;
			ДвижениеВыработкаТС.Моточасы = Моточасы;
			ДвижениеВыработкаТС.Пробег   = Пробег;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упУчетныеПараметрыТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеУчетныеПараметрыТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияУчетныеПараметрыТранспортныхСредств	= Движения.упУчетныеПараметрыТранспортныхСредств;
	ДвиженияУчетныеПараметрыТранспортныхСредств.Записывать = Истина;
	
	ДвижениеУчетныеПараметрыТранспортныхСредств	= ДвиженияУчетныеПараметрыТранспортныхСредств.Добавить();
	ДвижениеУчетныеПараметрыТранспортныхСредств.Период = Реквизиты.Дата;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ДатаПринятияКУчету   = Реквизиты.Дата;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ДатаОтсчетаТО        = Реквизиты.ДатаОтсчетаТО;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ПробегОтсчетаТО      = Реквизиты.ПробегОтсчетаТО;
	ДвижениеУчетныеПараметрыТранспортныхСредств.МоточасыОтсчетаТО    = Реквизиты.МоточасыОтсчетаТО;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ПринятоКУчету        = Истина;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ПервичнаяСтоимость   = Реквизиты.ПервичнаяСтоимость;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ЛиквидационнаяСтоимость = Реквизиты.ЛиквидационнаяСтоимость;
	ДвижениеУчетныеПараметрыТранспортныхСредств.СрокИспользования    = Реквизиты.СрокИспользования;
		
КонецПроцедуры

// Процедура формирует записи по регистру "упУчетныеПараметрыТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеУчетныеПараметрыТССнятиеСУчета(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияУчетныеПараметрыТранспортныхСредств	= Движения.упУчетныеПараметрыТранспортныхСредств;
	ДвиженияУчетныеПараметрыТранспортныхСредств.Записывать = Истина;
	
	ДвижениеУчетныеПараметрыТранспортныхСредств	= ДвиженияУчетныеПараметрыТранспортныхСредств.Добавить();
	ДвижениеУчетныеПараметрыТранспортныхСредств.Период				 = Реквизиты.Дата;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
	ДвижениеУчетныеПараметрыТранспортныхСредств.ДатаСнятияСУчета     = Реквизиты.Дата;
	ДвижениеУчетныеПараметрыТранспортныхСредств.СнятоСУчета	         = Истина;
		
КонецПроцедуры

// Процедура формирует записи по регистру "упПробегТС".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияПробегТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
		
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияПробегТС = Движения.упПробегТС;
	ДвиженияПробегТС.Записывать = Истина;
	
	Если ЗначениеЗаполнено(Реквизиты.Пробег) ИЛИ ЗначениеЗаполнено(Реквизиты.Моточасы) Тогда
		Движение = ДвиженияПробегТС.Добавить();
		Движение.Период = Реквизиты.Дата;
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты); 
	КонецЕсли;     
	
КонецПроцедуры

// Процедура формирует записи по регистру "упОстаткиГСМ".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияОстаткиГСМ				= Движения.упОстаткиГСМ;
	ДвиженияОстаткиГСМ.Записывать	= Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиГСМ") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиГСМ Цикл
			Движение = ДвиженияОстаткиГСМ.Добавить();   
			Движение.ВидДвижения = Реквизиты.ВидДвижения;
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует записи по регистру "упПрицепыТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияПрицепыТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПрицепыТС				= Движения.упПрицепыТранспортныхСредств;
	ДвиженияПрицепыТС.Записывать	= Истина;
	
	Если ДополнительныеСвойства.Свойство("Прицепы") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.Прицепы Цикл
			Движение = ДвиженияПрицепыТС.Добавить();   
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла;                   
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияПрицепыТС()

// Процедура формирует записи по регистру "упАгрегатыТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеАгрегатовТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияАгрегатыТС				= Движения.упАгрегатыТранспортныхСредств;
	ДвиженияАгрегатыТС.Записывать	= Истина;
	
	Если ДополнительныеСвойства.Свойство("Агрегаты") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.Агрегаты Цикл
			Движение = ДвиженияАгрегатыТС.Добавить();   
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла;                   
	КонецЕсли; 
		
КонецПроцедуры // СформироватьДвижениеАгрегатовТС()

// Процедура формирует записи по регистру "упПодразделенияТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеПодразделенияТранспортныхСредств(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПодразделенияТранспортныхСредств = Движения.упПодразделенияТранспортныхСредств;
	ДвиженияПодразделенияТранспортныхСредств.Записывать	= Истина;
	
	Если ЗначениеЗаполнено(Реквизиты.Подразделение) Тогда
		Движение = ДвиженияПодразделенияТранспортныхСредств.Добавить();
		Движение.Период = Реквизиты.Дата;
		ЗаполнитьЗначенияСвойств(Движение, Реквизиты); 
	КонецЕсли;	
	
КонецПроцедуры // СформироватьДвижениеПодразделенияТранспортныхСредств()

#Область СостоянияОборудования

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСостоянияОборудования(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("АгрегатыНаТС") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.АгрегатыНаТС Цикл
			Движение = ДвиженияСостоянияОборудования.Добавить();
			Движение.Период = Реквизиты.Дата;
			Движение.Оборудование = текСтрока.Агрегат;
			Движение.Склад = Справочники.упСклады.ПустаяСсылка();
			Движение.ТранспортноеСредство = текСтрока.ТранспортноеСредство;
			Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(текСтрока.Агрегат);
			Движение.Состояние = Состояния.СостояниеУстановленоНаТСПоУмолчанию;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияСостоянияОборудования()

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСостоянияОборудованияПоступлениеАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиАгрегатов") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиАгрегатов Цикл
			Движение = ДвиженияСостоянияОборудования.Добавить();
			Движение.Период = Реквизиты.Дата;
			Движение.Оборудование = текСтрока.Агрегат;
			Движение.Склад = текСтрока.Склад;
			Движение.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
			Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(текСтрока.Агрегат);
			Движение.Состояние = Состояния.СостояниеОприходованоНаСкладПоУмолчанию;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияСостоянияОборудованияПоступлениеАгрегатов()

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСостоянияОборудованияПеремещениеАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиАгрегатов") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиАгрегатов Цикл
			Движение = ДвиженияСостоянияОборудования.Добавить();
			Движение.Период = Реквизиты.Дата;
			Если текСтрока.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
				Движение.Период = Реквизиты.Дата-1;
			КонецЕсли; 
			Движение.Оборудование = текСтрока.Агрегат;
			Движение.Склад = текСтрока.Склад;
			Движение.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
			Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(текСтрока.Агрегат);
			Движение.Состояние = Состояния.СостояниеОприходованоНаСкладПоУмолчанию;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияСостоянияОборудованияПеремещениеАгрегатов()

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСостоянияОборудованияСписаниеАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиАгрегатов") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиАгрегатов Цикл
			Движение = ДвиженияСостоянияОборудования.Добавить();
			Движение.Период = Реквизиты.Дата;
			Движение.Оборудование = текСтрока.Агрегат;
			Движение.Склад = Реквизиты.Склад;
			Движение.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
			Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(текСтрока.Агрегат);
			Движение.Состояние = Состояния.СостояниеСписаноПоУмолчанию;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияСостоянияОборудованияСписаниеАгрегатов()

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСостоянияОборудованияРемонтИТОТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиАгрегатов") Тогда
		ТаблицаАгрегатовСкладов = Движения.упАгрегатыНаСкладах.Выгрузить(,"Агрегат, ВидДвижения");
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиАгрегатов Цикл
			ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
			Склад = Справочники.упСклады.ПустаяСсылка();
			Состояние = Справочники.упСостоянияОборудования.ПустаяСсылка();
			Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(текСтрока.Агрегат);
			ТипМестаХранения = ТипЗнч(текСтрока.Склад);
			Если текСтрока.ТипОперацииМонтажа = Перечисления.упТипыОперацийМонтажа.Снято 
				И ТипМестаХранения=ТИП("СправочникСсылка.упСклады") Тогда
				Склад = текСтрока.Склад;
				Состояние = Состояния.СостояниеОприходованоНаСкладПоУмолчанию;
			ИначеЕсли текСтрока.ТипОперацииМонтажа = Перечисления.упТипыОперацийМонтажа.Установлено
				ИЛИ ТипМестаХранения=ТИП("СправочникСсылка.упТранспортныеСредства") Тогда
				ТранспортноеСредство = текСтрока.Склад;
				Если НЕ ТипМестаХранения=ТИП("СправочникСсылка.упТранспортныеСредства") Тогда
					ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
				КонецЕсли;	
				Если текСтрока.ТипОперацииМонтажа = Перечисления.упТипыОперацийМонтажа.Установлено Тогда
					Состояние = Состояния.СостояниеУстановленоНаТСПоУмолчанию;
				ИначеЕсли текСтрока.ТипОперацииМонтажа = Перечисления.упТипыОперацийМонтажа.Снято Тогда
					ОтборСклада = Новый Структура();
					ОтборСклада.Вставить("Агрегат", текСтрока.Агрегат);
					НайденныеСтрокиСклада = ТаблицаАгрегатовСкладов.НайтиСтроки(ОтборСклада);
					Для каждого ТекущаяСтрокаСклада Из НайденныеСтрокиСклада Цикл
						Если ТекущаяСтрокаСклада.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
							Состояние = Состояния.СостояниеОприходованоНаСкладПоУмолчанию;
						КонецЕсли;	
					КонецЦикла;
				КонецЕсли;
			ИначеЕсли ТипЗнч(текСтрока.Склад)=ТИП("СправочникСсылка.упСклады") Тогда
				
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Состояние) Тогда
				Состояние = Состояния.СостояниеНеИспользуетсяПоУмолчанию;
			КонецЕсли;
			Движение = ДвиженияСостоянияОборудования.Добавить();
			Движение.Период = Реквизиты.Дата;
			Движение.Оборудование = текСтрока.Агрегат;
			Движение.Склад = Склад;
			Движение.ТранспортноеСредство = ТранспортноеСредство;
			Движение.Состояние = Состояние;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияСостоянияОборудованияРемонтИТОТС()

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСостоянияОборудованияКорректировки(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	Агрегат = Реквизиты.Агрегат;
	Движение = ДвиженияСостоянияОборудования.Добавить();
	Движение.Период = Реквизиты.Дата;
	Движение.Оборудование = Агрегат;
	Движение.Склад = Реквизиты.Склад;
	Движение.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
	Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(Агрегат);
	Если ЗначениеЗаполнено(Реквизиты.Склад) Тогда
		Движение.Состояние = Состояния.СостояниеОприходованоНаСкладПоУмолчанию;
	Иначе
		Движение.Состояние = Состояния.СостояниеНеИспользуетсяПоУмолчанию;
	КонецЕсли;
		
КонецПроцедуры // СформироватьДвиженияСостоянияОборудованияКорректировки()

// Процедура формирует записи регистра "упСостоянияОборудования".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияИзменениеСостоянияОборудования(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостоянияОборудования = Движения.упСостоянияОборудования;
	ДвиженияСостоянияОборудования.Записывать = Истина;
	ДвиженияАгрегатыТС = Движения.упАгрегатыТранспортныхСредств;
	ДвиженияАгрегатыТС.Записывать = Истина;
	ДвиженияАгрегатыНаСкладах = Движения.упАгрегатыНаСкладах;
	ДвиженияАгрегатыНаСкладах.Записывать = Истина;
	
	СостояниеОборудования = Неопределено;
	Если ДополнительныеСвойства.Свойство("Оборудование") Тогда
		СостояниеОборудования = ДополнительныеСвойства.Оборудование;
	КонецЕсли;	
	Если Реквизиты.Свойство("Оборудование") Тогда
		Оборудование = Реквизиты.Оборудование;
		Для каждого текСтрока Из Оборудование Цикл
			Движение = ДвиженияСостоянияОборудования.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
			Движение.Период = текСтрока.ДатаИзмененияСостояния;
			ТипСостояния = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(текСтрока.Состояние, "ТипСостояния");
			Если НЕ СостояниеОборудования = Неопределено Тогда
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Оборудование", текСтрока.Оборудование);
				Если ТипСостояния = Перечисления.упТипыСостоянийОборудования.УстановленоНаТС
					ИЛИ ТипСостояния = Перечисления.упТипыСостоянийОборудования.ОприходованоНаСклад
					ИЛИ ТипСостояния = Перечисления.упТипыСостоянийОборудования.Списано Тогда
					НайденныеСтроки = СостояниеОборудования.НайтиСтроки(СтруктураПоиска);
					Если НайденныеСтроки.Количество() Тогда
						ТекущееСостояние = НайденныеСтроки[0];
						//Движение.Период = ТекущееСостояние.Период;
						Если ТекущееСостояние.ЭтоТС 
							И ТекущееСостояние.Операция = Перечисления.упТипыОперацийМонтажа.Установлено Тогда
							Если ТекущееСостояние.ТранспортноеСредство = текСтрока.ТранспортноеСредство Тогда
								// Оборудывание уже установлено не нужно делать движения.
								Продолжить;
							КонецЕсли;
							ДвижениеТС = ДвиженияАгрегатыТС.Добавить();
							ЗаполнитьЗначенияСвойств(ДвижениеТС, ТекущееСостояние);
							ДвижениеТС.Период = текСтрока.ДатаИзмененияСостояния-1;
							ДвижениеТС.Агрегат = текСтрока.Оборудование;
							ДвижениеТС.Операция = Перечисления.упТипыОперацийМонтажа.Снято;
						ИначеЕсли ТекущееСостояние.ЭтоСклад И ТекущееСостояние.Количество>0 Тогда
							Если ТекущееСостояние.Склад = текСтрока.Склад Тогда
								// Оборудывание уже оприходовано не нужно делать движения.
								Продолжить;
							КонецЕсли;
							ДвижениеСклад = ДвиженияАгрегатыНаСкладах.Добавить();
							ЗаполнитьЗначенияСвойств(ДвижениеСклад, ТекущееСостояние);
							ДвижениеСклад.Период = текСтрока.ДатаИзмененияСостояния-1;
							ДвижениеСклад.Агрегат = текСтрока.Оборудование;
							ДвижениеСклад.ВидДвижения = ВидДвиженияНакопления.Расход;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если ТипСостояния = Перечисления.упТипыСостоянийОборудования.УстановленоНаТС Тогда
				ДвижениеТС = ДвиженияАгрегатыТС.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеТС, текСтрока);
				ДвижениеТС.Период = текСтрока.ДатаИзмененияСостояния;
				ДвижениеТС.Агрегат = текСтрока.Оборудование;
				ДвижениеТС.Операция = Перечисления.упТипыОперацийМонтажа.Установлено;
				ДвижениеТС.ХодоваяШина = Справочники.упШиныУзлыИАгрегаты.ЭтоШина(текСтрока.Оборудование);
			ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийОборудования.ОприходованоНаСклад Тогда
				ДвижениеСклад = ДвиженияАгрегатыНаСкладах.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеСклад, текСтрока);
				ДвижениеСклад.Период = текСтрока.ДатаИзмененияСостояния;
				ДвижениеСклад.Агрегат = текСтрока.Оборудование;
				ДвижениеСклад.Количество = 1;
				ДвижениеСклад.ВидДвижения = ВидДвиженияНакопления.Приход;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияИзменениеСостоянияОборудования()

#КонецОбласти

// Функция - Это последний документ изменявший состояние оборудования
//
// Параметры:
//  Ссылка		 - Ссылка								 - документ.
//  Оборудование	 - СправочникСсылка.упДополнительноеОборудование	 - дополнительное оборудование.
//  Период		 - Дата									 - дата.
//  Отказ			 - Булево									 - Истина, если ошибка.
//
Процедура ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, Оборудование, Период, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СостояниеОборудования.Оборудование КАК Оборудование,
	               |	СостояниеОборудования.Регистратор КАК Регистратор
	               |ИЗ
	               |	РегистрСведений.упСостоянияОборудования КАК СостояниеОборудования
	               |ГДЕ
	               |	ИСТИНА
	               |	И СостояниеОборудования.Оборудование В(&Оборудование)
	               |	И СостояниеОборудования.МоментВремени > &МоментВремени
	               |	И НЕ СостояниеОборудования.Регистратор = &Регистратор";

	Запрос.УстановитьПараметр("Оборудование",  Оборудование);
	Запрос.УстановитьПараметр("Регистратор",   Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Период, Ссылка));

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Существуют документы изменяющие состояние оборудования проведенные позднее.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Ссылка,,,Отказ);
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстОшибки = НСтр("ru = 'Документ:%1; Оборудование:%2.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Выборка.Регистратор, Выборка.Оборудование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Выборка.Регистратор,,,Отказ);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

// Получение остатков по планированию транспортных средств
//
// Параметры:
//  НаМоментВремени	 - МоментВремени - период.
//  Документ		 - Ссылка	 - документ.
//  Транспорт		 - СправочникСсылка.упТранспортныеСредства 	 - транспортное средство.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - срез последних.
//
Функция СрезПоследнихЗанятостьТранспорта(НаМоментВремени, Знач Документ, Знач Транспорт = Неопределено) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    Документ = Документы.упИзменениеСостоянияТранспортногоСредства.ПустаяСсылка();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Транспорт) Тогда
		Транспорт = Документ.ТранспортноеСредство;
	КонецЕсли; 
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений.упЗанятостьТранспорта.СрезПоследних(&НаМоментВремени, Транспорт = &Транспорт И Документ = &Документ) КАК упЗанятостьТранспорта
	|ГДЕ упЗанятостьТранспорта.ПланАктуален
	|";
	Запрос.УстановитьПараметр("Транспорт", Транспорт);
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("НаМоментВремени", НаМоментВремени);
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;
	
КонецФункции

// Функция - Учетные зоны зон
//
// Параметры:
//  Зоны	 - СправочникСсылка.упГеографическиеЗоны 	 - зона.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - зона, родитель, уровень.
//
Функция УчетныеЗоныЗон(Зоны) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	упИерархияГеографическихЗонТ.Зона,
	|	упИерархияГеографическихЗонТ.Родитель,
	|	упИерархияГеографическихЗонТ.Уровень
	|ИЗ
	|	(ВЫБРАТЬ
	|		упИерархияГеографическихЗонТ.Зона КАК Зона,
	|		МАКСИМУМ(упИерархияГеографическихЗонТ.Уровень) КАК Уровень
	|	ИЗ
	|		РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонТ
	|	ГДЕ
	|		упИерархияГеографическихЗонТ.Зона В(&Зоны)
	|		И упИерархияГеографическихЗонТ.Родитель.ИспользоватьДляУчетаМестоположений
	|	
	|	СГРУППИРОВАТЬ ПО
	|		упИерархияГеографическихЗонТ.Зона) КАК МаксимальныеУровни
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонТ
	|		ПО МаксимальныеУровни.Зона = упИерархияГеографическихЗонТ.Зона
	|			И МаксимальныеУровни.Уровень = упИерархияГеографическихЗонТ.Уровень
	|";
	Запрос.УстановитьПараметр("Зоны", Зоны);
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;

КонецФункции

// Функция - Учетные зоны адресов
//
// Параметры:
//  Зоны	 - СправочникСсылка.упАдреса  	 - адрес.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - зона, родитель, уровень, адрес.
//
Функция УчетныеЗоныАдресов(Адреса) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упАдреса.Ссылка КАК Адрес,
	               |	упАдреса.ГеографическаяЗона
	               |ПОМЕСТИТЬ втАдреса
	               |ИЗ
	               |	Справочник.упАдреса КАК упАдреса
	               |ГДЕ
	               |	упАдреса.Ссылка В(&Адреса)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(упИерархияГеографическихЗонТ.Зона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК Зона,
	               |	упИерархияГеографическихЗонТ.Родитель,
	               |	упИерархияГеографическихЗонТ.Уровень,
	               |	втАдреса.Адрес
	               |ИЗ
	               |	втАдреса КАК втАдреса
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			упИерархияГеографическихЗонТ.Зона КАК Зона,
	               |			МАКСИМУМ(упИерархияГеографическихЗонТ.Уровень) КАК Уровень
	               |		ИЗ
	               |			РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонТ
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	               |				ПО упИерархияГеографическихЗонТ.Зона = втАдреса.ГеографическаяЗона
	               |		ГДЕ
	               |			упИерархияГеографическихЗонТ.Родитель.ИспользоватьДляУчетаМестоположений
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			упИерархияГеографическихЗонТ.Зона) КАК МаксимальныеУровни
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонТ
	               |			ПО МаксимальныеУровни.Зона = упИерархияГеографическихЗонТ.Зона
	               |				И МаксимальныеУровни.Уровень = упИерархияГеографическихЗонТ.Уровень
	               |		ПО (МаксимальныеУровни.Зона = втАдреса.ГеографическаяЗона)";
	Запрос.УстановитьПараметр("Адреса", Адреса);
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Возвращает структуру состояния ТС. 
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//	ДатаСостояния - Дата - Период.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция СостояниеТранспортногоСредства(ТранспортноеСредство, ДатаСостояния = Неопределено) Экспорт
	
	текРезультат = Неопределено;
	сткРезультат = Новый Структура("Период, СостояниеТС, Регистратор", Неопределено, Неопределено, Неопределено);
	
	Если ДатаСостояния = Неопределено Тогда
		ДатаСостояния = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упСостоянияТранспортныхСредствСрезПоследних.Период,
	|	упСостоянияТранспортныхСредствСрезПоследних.Регистратор,
	|	упСостоянияТранспортныхСредствСрезПоследних.СостояниеТС
	|ИЗ
	|	РегистрСведений.упСостоянияТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК упСостоянияТранспортныхСредствСрезПоследних";
	Запрос.УстановитьПараметр("Период", 				ДатаСостояния);
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, текВыборка);
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Возвращает ссылку на документ принятие к учету ТС. 
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//	ДокументСсылка - Результат выполнения.
//
Функция ДокументПринятияКУчетуТранспортногоСредства(ТранспортноеСредство) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	УчетныеПараметрыТССП.Регистратор КАК ДокументПринятиеКУчетуТранспортногоСредства
	|ИЗ
	|	РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(, ТранспортноеСредство = &ТранспортноеСредство) КАК УчетныеПараметрыТССП
	|ГДЕ
	|	УчетныеПараметрыТССП.ПринятоКУчету
	|	И НЕ УчетныеПараметрыТССП.СнятоСУчета";
	
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.ДокументПринятиеКУчетуТранспортногоСредства;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает структуру учетного состояния ТС.
//
// Параметры:
//  ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства	 - Ссылка на элемент.
//  Период				 - Дата										 - Дата на которую возвращается учетное состояние.
// 
// Возвращаемое значение:
//  Структура - Результат выполнения.
//
Функция УчетноеСостояниеТранспортногоСредства(ТранспортноеСредство, Период = Неопределено) Экспорт
	
	сткВозврат = Новый Структура("ПринятоКУчету, СнятоСУчета, Регистратор", Ложь, Ложь, Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упУчетныеПараметры.ПринятоКУчету,
	|	упУчетныеПараметры.СнятоСУчета,
	|	упУчетныеПараметры.Регистратор
	|ИЗ
	|	РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК упУчетныеПараметры";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	ПериодСостояния = ?(Период = Неопределено, '00010101', Период);
	Запрос.УстановитьПараметр("Период", ПериодСостояния);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка); 
	КонецЕсли; 
	
	Возврат сткВозврат;
	
КонецФункции

// Возвращает ссылку на документ снятия с учета ТС. 
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//	ДокументСсылка - Результат выполнения.
//
Функция ДокументСнятияСУчетаТранспортногоСредства(ТранспортноеСредство) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "ВЫБРАТЬ
	|	УчетныеПараметрыТССП.Регистратор КАК ДокументСнятияСУчетаТранспортногоСредства
	|ИЗ
	|	РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(, ТранспортноеСредство = &ТранспортноеСредство) КАК УчетныеПараметрыТССП
	|ГДЕ
	|	НЕ УчетныеПараметрыТССП.ПринятоКУчету
	|	И УчетныеПараметрыТССП.СнятоСУчета";
	
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.ДокументСнятияСУчетаТранспортногоСредства;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Функция - Подразделение транспортного средства на дату.
//
// Параметры:
//  Период			 - ДатаВремы - период запроса.
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства - транспортное средство.
// 
// Возвращаемое значение:
//  СправочникСсылка.упСтруктураПредприятия - подразделение по ТС.
//
Функция ПодразделениеТранспортногоСредстваНаДату(Период, ТранспортноеСредство) Экспорт
	
	Результат = Справочники.упСтруктураПредприятия.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упПодразделенияТранспортныхСредствСрезПоследних.Подразделение
	|ИЗ
	|	РегистрСведений.упПодразделенияТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК упПодразделенияТранспортныхСредствСрезПоследних";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Период", Период);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Подразделение;		
	КонецЕсли; 
		
	Возврат Результат;
		
КонецФункции

// Функция - Направление деятельности транспортного средства на дату.
//
// Параметры:
//  Период			 - ДатаВремы - период запроса.
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства - транспортное средство.
// 
// Возвращаемое значение:
//  СправочникСсылка.упНаправленияДеятельности - направление деятельности по ТС.
//
Функция НаправлениеДеятельностиТранспортногоСредстваНаДату(Период, ТранспортноеСредство) Экспорт
	
	Результат = Справочники.упСтруктураПредприятия.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	упНаправленияДеятельностиТранспортныхСредствСрезПоследних.НаправлениеДеятельности КАК НаправлениеДеятельности
	               |ИЗ
	               |	РегистрСведений.упНаправленияДеятельностиТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК упНаправленияДеятельностиТранспортныхСредствСрезПоследних";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Период", Период);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.НаправлениеДеятельности;		
	КонецЕсли; 
		
	Возврат Результат;
		
КонецФункции


#КонецОбласти

#КонецОбласти