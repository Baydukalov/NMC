
#Область ПрограммныйИнтерфейс

// Рассчитываются итоговые параметры по набору грузов
//
// Параметры:
//  ТаблицаГрузов  - ТаблицаЗначений - Поля:
//    ГрузовоеМесто    - СправочникСсылка.упГрузовыеМеста.
//    Тара             - СправочникСсылка.упГрузовыеМеста.
//    КоличествоГрузов - ОпределяемыйТип.КоличествоГрузов.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция РассчитатьИтоговыеПараметрыПоНаборуГрузов(ТаблицаГрузов, ЗаявкаНаПеревозку = Неопределено, ЗаданиеНаПеревозку = Неопределено) Экспорт
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Масса"                      , 0);
	сткВозврат.Вставить("Объем"                      , 0);
	сткВозврат.Вставить("КоличествоМест"             , 0);
	сткВозврат.Вставить("КоличествоГрузов"           , 0);
	сткВозврат.Вставить("КоличествоГрузовыхМест"     , 0);
	сткВозврат.Вставить("КоличествоЗагрузочныхМетров", 0);
	сткВозврат.Вставить("Сумма"                      , 0);
	сткВозврат.Вставить("СуммаНДС"                   , 0);
	сткВозврат.Вставить("ОценочнаяСтоимость"         , 0);
	сткВозврат.Вставить("СуммаИнкассацииПлан"        , 0);
	сткВозврат.Вставить("Длина"                      , 0);
	сткВозврат.Вставить("Ширина"                     , 0);
	сткВозврат.Вставить("Высота"                     , 0);
	
	ЕстьИзмененияПлановыхЭтаповПеревозки = Ложь;
	
	Если ЗначениеЗаполнено(ЗаявкаНаПеревозку) Тогда
		ЕстьИзмененияПлановыхЭтаповПеревозки = упУправленияЗаявками.ЕстьИзмененияПлановыхЭтаповПеревозки(ЗаявкаНаПеревозку);
	КонецЕсли;
	
	// Обоснование установки привилегированного режима:
	// Если записывается вновь созданная заявка на перевозку груза
	// то при чтении данных возникает ошибка доступа к таблице грузовых мест.
	// Так как в роли доступа к справочнику грузовых мест установлено ограничение
	// по документу основанию, а ссылки на заявку в базе еще нет.
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Если ЕстьИзмененияПлановыхЭтаповПеревозки Тогда
		ТекстЗапроса = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеЭтапыПеревозки.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ПлановыеЭтапыПеревозки.Тара КАК Тара,
		|	МАКСИМУМ(ПлановыеЭтапыПеревозки.КоличествоГрузов) КАК КоличествоГрузов
		|ПОМЕСТИТЬ втГрузовыеМестаПодготовка
		|ИЗ
		|	РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(
		|		,
		|		ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозку) КАК ПлановыеЭтапыПеревозки
		|ГДЕ НЕ (ПлановыеЭтапыПеревозки.Отменен)
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеЭтапыПеревозки.ГрузовоеМесто,
		|	ПлановыеЭтапыПеревозки.Тара
		|";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаГрузов.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ТаблицаГрузов.Тара КАК Тара,
		|	ТаблицаГрузов.КоличествоГрузов КАК КоличествоГрузов
		|ПОМЕСТИТЬ втГрузовыеМестаПодготовка
		|ИЗ
		|	&ТаблицаГрузов КАК ТаблицаГрузов
		|";
		Запрос.УстановитьПараметр("ТаблицаГрузов", ТаблицаГрузов);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозку", ?(ЗаявкаНаПеревозку = Неопределено, Документы.упЗаявкаНаПеревозкуГруза.ПустаяСсылка(), ЗаявкаНаПеревозку));
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", ?(ЗаданиеНаПеревозку = Неопределено, Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка(), ЗаданиеНаПеревозку));
	
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
	"ВЫБРАТЬ
	|	0 КАК НомерВЦепиПоставок,	
	|	0 КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упГрузовыеМеста.Масса) * втГрузовыеМеста.КоличествоГрузов КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упГрузовыеМеста.Объем) * втГрузовыеМеста.КоличествоГрузов КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упГрузовыеМеста.КоличествоМест) * втГрузовыеМеста.КоличествоГрузов КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упГрузовыеМеста.КоличествоЗагрузочныхМетров) * втГрузовыеМеста.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
	|	упГрузовыеМеста.Сумма * втГрузовыеМеста.КоличествоГрузов КАК Сумма,
	|	упГрузовыеМеста.СуммаНДС * втГрузовыеМеста.КоличествоГрузов КАК СуммаНДС,
	|	упГрузовыеМеста.ОценочнаяСтоимость * втГрузовыеМеста.КоличествоГрузов КАК ОценочнаяСтоимость,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|			ТОГДА упГрузовыеМеста.Сумма * втГрузовыеМеста.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаИнкассацииПлан,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА втГрузовыеМеста.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоГрузовыхМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, упГрузовыеМеста.Высота) КАК Высота,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, упГрузовыеМеста.Длина) КАК Длина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, упГрузовыеМеста.Ширина) КАК Ширина
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втГрузовыеМестаПодготовка КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ПО втГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку = &ЗаявкаНаПеревозку) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО втГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		И НЕ упПараметрыЗаявкиНаПеревозку.Регистратор = &ЗаданиеНаПеревозку
	|;";
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	МАКСИМУМ(втПараметрыИтог.Длина) КАК Длина,
	|	МАКСИМУМ(втПараметрыИтог.Ширина) КАК Ширина,
	|	МАКСИМУМ(втПараметрыИтог.Высота) КАК Высота,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса,
	|	СУММА(втПараметрыИтог.Сумма) КАК Сумма,
	|	СУММА(втПараметрыИтог.СуммаНДС) КАК СуммаНДС,
	|	СУММА(втПараметрыИтог.ОценочнаяСтоимость) КАК ОценочнаяСтоимость,
	|	СУММА(втПараметрыИтог.СуммаИнкассацииПлан) КАК СуммаИнкассацииПлан,
	|	СУММА(втПараметрыИтог.КоличествоГрузовыхМест) КАК КоличествоГрузовыхМест,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПараметрыИтог";
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат сткВозврат;
	
КонецФункции

// Рассчитывает параметры для формирования движений по регистрам "упПараметрыЗаданияНаПеревозку" и "упПараметрыЗаявкиНаПеревозку"
//
Функция РассчитатьТаблицуПараметровГрузовДляПроведения(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ) Экспорт
	
	ПараметрыНаПеревозку = Новый ТаблицаЗначений;
	Статус = ДополнительныеСвойства.Статус;
	
	Если Отказ Тогда 
		Возврат ПараметрыНаПеревозку; 
	КонецЕсли;
	
	ПараметрыНаПеревозку.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
	ПараметрыНаПеревозку.Колонки.Добавить("Высота");
	ПараметрыНаПеревозку.Колонки.Добавить("Длина");
	ПараметрыНаПеревозку.Колонки.Добавить("Ширина");
	ПараметрыНаПеревозку.Колонки.Добавить("КоличествоЗагрузочныхМетров");
	ПараметрыНаПеревозку.Колонки.Добавить("КоличествоМест");
	ПараметрыНаПеревозку.Колонки.Добавить("Масса");
	ПараметрыНаПеревозку.Колонки.Добавить("Объем");
	
	Если Не (Ложь
			Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию 
			Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс 
			Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению) Тогда
			
		Возврат ПараметрыНаПеревозку;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится Тогда
		// пишем итоги по заданию
		ЗаполнитьЗначенияСвойств(ПараметрыНаПеревозку.Добавить(), ДополнительныеСвойства.СтруктураИтоговыхПараметров);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Период", РеквизитыОбъекта.Дата);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
		|    упПараметрыЗаявкиНаПеревозку.Регистратор,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упГрузовыеМеста.ГрузовоеМесто.Масса) КАК Масса,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упГрузовыеМеста.ГрузовоеМесто.Объем) КАК Объем,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упГрузовыеМеста.ГрузовоеМесто.КоличествоМест) КАК КоличествоМест,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, упГрузовыеМеста.ГрузовоеМесто.Высота) КАК Высота,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, упГрузовыеМеста.ГрузовоеМесто.Длина) КАК Длина,
		|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, упГрузовыеМеста.ГрузовоеМесто.Ширина) КАК Ширина
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упГрузовыеМеста
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
		|				&Период,
		|				ЗаявкаНаПеревозку = ВЫРАЗИТЬ(&Ссылка КАК Документ.упЗаданиеНаПеревозкуГруза).ЗаявкаНаПеревозкуГруза
		|					И Регистратор <> &Ссылка) КАК упПараметрыЗаявкиНаПеревозку
		|		ПО упГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
		|ГДЕ
		|	упГрузовыеМеста.Ссылка = &Ссылка";
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ПараметрыНаПеревозку.Добавить(), Выборка); 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат ПараметрыНаПеревозку;
	
КонецФункции

// Функция рассчитывает суммарные показатели в каждой точке маршрута.
//
// Параметры:
//   Маршрут  - ТаблицаЗначений - Таблица данных.
//   Работы  - ТаблицаЗначений - Таблица данных.
//
// Возвращаемое значение:
//   Соответствие - Значения:
//      Ключ     - уникальныеИдентификатор - Идентификатор точки маршрута.
//      Значение - Структура.
//
Функция РассчитатьЗагрузкуВТочкахМаршрута(Маршрут, Работы, ДополнительныеСвойства = Неопределено) Экспорт
	
	Маршрут.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Строка"));
	Для каждого текТочка Из Маршрут Цикл
		текТочка.ИДСтроки = Строка(текТочка.Идентификатор);
	КонецЦикла;
	
	Работы.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Строка"));
	Для каждого текРабота Из Работы Цикл
		текРабота.ИДСтроки = Строка(текРабота.Идентификатор);
	КонецЦикла;
	
	соотЗагрузка = Новый Соответствие;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Маршрут.СтрокаНомер КАК НомерСтроки,
	|	ВЫРАЗИТЬ(Маршрут.ИДСтроки КАК СТРОКА(100)) КАК Идентификатор
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	&Маршрут КАК Маршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Работы.ИДСтроки КАК СТРОКА(100)) КАК Идентификатор,
	|	Работы.Операция,
	|	Работы.Задание,
	|	Работы.ГрузовоеМесто,
	|	Работы.Тара,
	|	Работы.КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА Работы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА Работы.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втРаботыПодготовка
	|ИЗ
	|	&Работы КАК Работы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Операция,
	|	ВЫБОР
	|		КОГДА втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК ПоправочныйКоэффициент,
	|	ВЫРАЗИТЬ(втРаботы.Задание КАК Документ.упЗаданиеНаПеревозкуГруза).ЗаявкаНаПеревозкуГруза  КАК ЗаявкаНаПеревозку,
	|	втРаботы.Задание,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Тара,
	|	втРаботы.КоличествоГрузов,
	|	втРаботы.КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втРаботыПодготовка КАК втРаботы
	|ГДЕ
	|	втРаботы.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|;
	|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	втРаботы.Идентификатор,
	//|	втРаботы.ПоправочныйКоэффициент * упЗаданиеНаПеревозкуГруза.Масса КАК Масса,
	//|	втРаботы.ПоправочныйКоэффициент * упЗаданиеНаПеревозкуГруза.Объем КАК Объем,
	//|	втРаботы.ПоправочныйКоэффициент * упЗаданиеНаПеревозкуГруза.КоличествоМест КАК КоличествоМест,
	//|	втРаботы.ПоправочныйКоэффициент * упЗаданиеНаПеревозкуГруза.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	//|	втРаботы.ПоправочныйКоэффициент * втРаботы.КоличествоГрузов КАК КоличествоГрузов,
	//|	втРаботы.ПоправочныйКоэффициент * втРаботы.КоличествоГрузовыхМест КАК КоличествоГрузовыхМест
	//|ПОМЕСТИТЬ втПоказателиЗагрузкиПодготовка
	//|ИЗ
	//|	втРаботы КАК втРаботы
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	//|		ПО (втРаботы.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	//|			И втРаботы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	втРаботы.Идентификатор,
	//|	втРаботы.ПоправочныйКоэффициент * упГрузовыеМеста.Масса * втРаботы.КоличествоГрузов,
	//|	ВЫБОР
	//|		КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	//|			ТОГДА втРаботы.ПоправочныйКоэффициент * упГрузовыеМеста.Объем * втРаботы.КоличествоГрузов
	//|		ИНАЧЕ 0
	//|	КОНЕЦ,
	//|	ВЫБОР
	//|		КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	//|			ТОГДА втРаботы.ПоправочныйКоэффициент * упГрузовыеМеста.КоличествоМест * втРаботы.КоличествоГрузов
	//|		ИНАЧЕ 0
	//|	КОНЕЦ,
	//|	ВЫБОР
	//|		КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	//|			ТОГДА втРаботы.ПоправочныйКоэффициент * упГрузовыеМеста.КоличествоЗагрузочныхМетров * втРаботы.КоличествоГрузов
	//|		ИНАЧЕ 0
	//|	КОНЕЦ,
	//|	втРаботы.ПоправочныйКоэффициент * втРаботы.КоличествоГрузов,
	//|	втРаботы.ПоправочныйКоэффициент * втРаботы.КоличествоГрузовыхМест
	//|ИЗ
	//|	втРаботы КАК втРаботы
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	//|		ПО втРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	//|			И (НЕ втРаботы.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Операция,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Тара,
	|	втРаботы.ПоправочныйКоэффициент * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упПараметрыЗаданияНаПеревозку.Масса) * втРаботы.КоличествоГрузов  КАК Масса,
	|	втРаботы.ПоправочныйКоэффициент * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упПараметрыЗаданияНаПеревозку.Объем) * втРаботы.КоличествоГрузов КАК Объем,
	|	втРаботы.ПоправочныйКоэффициент * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упПараметрыЗаданияНаПеревозку.КоличествоМест) * втРаботы.КоличествоГрузов КАК КоличествоМест,
	|	втРаботы.ПоправочныйКоэффициент * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров) * втРаботы.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
	|	втРаботы.ПоправочныйКоэффициент * втРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|	втРаботы.ПоправочныйКоэффициент * втРаботы.КоличествоГрузовыхМест КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку В (ВЫБРАТЬ втРаботы.ЗаявкаНаПеревозку ИЗ втРаботы КАК втРаботы ГДЕ НЕ втРаботы.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО втРаботы.ЗаявкаНаПеревозку = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И втРаботы.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(, ЗаданиеНаПеревозку В (ВЫБРАТЬ втРаботы.Задание ИЗ втРаботы КАК втРаботы)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО втРаботы.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И втРаботы.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;";
	
	Если Истина
		И ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") 
		И (Ложь
			Или Не ЗначениеЗаполнено(ДополнительныеСвойства)
			Или (Истина
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеСвойства, "ПараметрыВводаИнформацииОГрузовыхМестах")
					И Не ДополнительныеСвойства.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится))
	Тогда
		ТекстЗапроса = ТекстЗапроса + ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузовДляРейса();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	втПоказателиЗагрузки.Идентификатор,
	|	СУММА(втПоказателиЗагрузки.Масса) КАК Масса,
	|	СУММА(втПоказателиЗагрузки.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПоказателиЗагрузки.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПоказателиЗагрузки.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПоказателиЗагрузки.КоличествоГрузов) КАК КоличествоГрузов,
	|	СУММА(втПоказателиЗагрузки.КоличествоГрузовыхМест) КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втПоказателиЗагрузки
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПоказателиЗагрузки
	|
	|СГРУППИРОВАТЬ ПО
	|	втПоказателиЗагрузки.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.НомерСтроки,
	|	втМаршрут.Идентификатор,
	|	ЕСТЬNULL(втПоказателиЗагрузки.Масса, 0) КАК Масса,
	|	ЕСТЬNULL(втПоказателиЗагрузки.Объем, 0) КАК Объем,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоМест, 0) КАК КоличествоМест,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоГрузов, 0) КАК КоличествоГрузов,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоГрузовыхМест, 0) КАК КоличествоГрузовыхМест
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПоказателиЗагрузки КАК втПоказателиЗагрузки
	|		ПО втМаршрут.Идентификатор = втПоказателиЗагрузки.Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.НомерСтроки";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	Запрос.УстановитьПараметр("Работы", Работы);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Масса                       = 0;
		Объем                       = 0;
		КоличествоМест              = 0;
		КоличествоЗагрузочныхМетров = 0;
		КоличествоГрузов			= 0;
		КоличествоГрузовыхМест		= 0;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			сткПоказатели = Новый Структура("Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КоличествоГрузов, КоличествоГрузовыхМест");
			
			Масса                       = Масса + Выборка.Масса;
			Объем                       = Объем + Выборка.Объем;
			КоличествоМест              = КоличествоМест + Выборка.КоличествоМест;
			КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров + Выборка.КоличествоЗагрузочныхМетров;
			КоличествоГрузов			= КоличествоГрузов + Выборка.КоличествоГрузов;
			КоличествоГрузовыхМест		= КоличествоГрузовыхМест + Выборка.КоличествоГрузовыхМест;
			сткПоказатели.Масса                       = Масса;
			сткПоказатели.Объем                       = Объем;
			сткПоказатели.КоличествоМест              = КоличествоМест;
			сткПоказатели.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров;
			сткПоказатели.КоличествоГрузов			  = КоличествоГрузов;
			сткПоказатели.КоличествоГрузовыхМест	  = КоличествоГрузовыхМест;
			
			соотЗагрузка.Вставить(СокрП(Выборка.Идентификатор), сткПоказатели);
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат соотЗагрузка;
	
КонецФункции // РассчитатьЗагрузкуВТочкахМаршрута()

// Функция рассчитывает суммарные показатели в каждой точке маршрута.
//
// Параметры:
//   Маршрут  - ТаблицаЗначений - Таблица данных.
//   Работы  - ТаблицаЗначений - Таблица данных.
//
// Возвращаемое значение:
//   Соответствие - Значения:
//      Ключ     - уникальныеИдентификатор - Идентификатор точки маршрута.
//      Значение - Структура.
//
Функция РассчитатьЗагрузкуВТочкахМаршрутаПодробно(Маршрут, Работы, ДополнительныеСвойства = Неопределено) Экспорт
	
	Маршрут.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Строка"));
	Для каждого текТочка Из Маршрут Цикл
		текТочка.ИДСтроки = Строка(текТочка.Идентификатор);
	КонецЦикла;
	
	Работы.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Строка"));
	Для каждого текРабота Из Работы Цикл
		текРабота.ИДСтроки = Строка(текРабота.Идентификатор);
	КонецЦикла;
	
	соотЗагрузка = Новый Соответствие;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Маршрут.НомерСтроки,
	|	ВЫРАЗИТЬ(Маршрут.ИДСтроки КАК СТРОКА(100)) КАК Идентификатор,
	|	ВЫРАЗИТЬ(Маршрут.Адрес КАК Справочник.упАдреса) КАК Адрес
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	&Маршрут КАК Маршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Работы.ИДСтроки КАК СТРОКА(100)) КАК Идентификатор,
	|	Работы.Операция,
	|	Работы.Задание,
	|	Работы.ГрузовоеМесто,
	|	Работы.Тара,
	|	Работы.КоличествоГрузов
	|ПОМЕСТИТЬ втРаботыПодготовка
	|ИЗ
	|	&Работы КАК Работы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Операция,
	|	втРаботы.Задание,
	|	ВЫРАЗИТЬ(втРаботы.Задание КАК Документ.упЗаданиеНаПеревозкуГруза).ЗаявкаНаПеревозкуГруза  КАК ЗаявкаНаПеревозку,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Тара,
	|	втРаботы.КоличествоГрузов
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втРаботыПодготовка КАК втРаботы
	|ГДЕ
	|	втРаботы.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), 
	|							ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка), 
	|							ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей),
	|							ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	втРаботы.Идентификатор,
	//|	втРаботы.Операция,
	//|	упЗаданиеНаПеревозкуГруза.Масса КАК Масса,
	//|	упЗаданиеНаПеревозкуГруза.Объем КАК Объем,
	//|	упЗаданиеНаПеревозкуГруза.КоличествоМест КАК КоличествоМест,
	//|	упЗаданиеНаПеревозкуГруза.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	//|ПОМЕСТИТЬ втПоказателиЗагрузкиПодготовка
	//|ИЗ
	//|	втРаботы КАК втРаботы
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	//|		ПО (втРаботы.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	//|			И втРаботы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	втРаботы.Идентификатор,
	//|	втРаботы.Операция,
	//|	упГрузовыеМеста.Масса * втРаботы.КоличествоГрузов,
	//|	упГрузовыеМеста.Объем * втРаботы.КоличествоГрузов,
	//|	упГрузовыеМеста.КоличествоМест * втРаботы.КоличествоГрузов,
	//|	упГрузовыеМеста.КоличествоЗагрузочныхМетров * втРаботы.КоличествоГрузов
	//|ИЗ
	//|	втРаботы КАК втРаботы
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	//|		ПО втРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	//|;
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Операция,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Тара,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упПараметрыЗаданияНаПеревозку.Масса)  КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упПараметрыЗаданияНаПеревозку.Объем) КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упПараметрыЗаданияНаПеревозку.КоличествоМест) КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	втРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|	втРаботы.КоличествоГрузов КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку В (ВЫБРАТЬ втРаботы.ЗаявкаНаПеревозку ИЗ втРаботы КАК втРаботы ГДЕ НЕ втРаботы.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО втРаботы.ЗаявкаНаПеревозку = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И втРаботы.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(, ЗаданиеНаПеревозку В (ВЫБРАТЬ втРаботы.Задание ИЗ втРаботы КАК втРаботы)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО втРаботы.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И втРаботы.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;";
	
	Если Истина
		И ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") 
		И (Ложь
			Или Не ЗначениеЗаполнено(ДополнительныеСвойства)
			Или (Истина
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеСвойства, "ПараметрыВводаИнформацииОГрузовыхМестах")
					И Не ДополнительныеСвойства.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится))
	Тогда
		ТекстЗапроса = ТекстЗапроса + ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузовДляРейса();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Идентификатор,
	|	СУММА(ВложенныйЗапрос.МассаПогрузка) КАК МассаПогрузка,
	|	СУММА(ВложенныйЗапрос.ОбъемПогрузка) КАК ОбъемПогрузка,
	|	СУММА(ВЫРАЗИТЬ(ВложенныйЗапрос.КоличествоМестПогрузка КАК ЧИСЛО(8, 0))) КАК КоличествоМестПогрузка,
	|	СУММА(ВложенныйЗапрос.КоличествоЗагрузочныхМетровПогрузка) КАК КоличествоЗагрузочныхМетровПогрузка,
	|	СУММА(ВложенныйЗапрос.МассаРазгрузка) КАК МассаРазгрузка,
	|	СУММА(ВложенныйЗапрос.ОбъемРазгрузка) КАК ОбъемРазгрузка,
	|	СУММА(ВЫРАЗИТЬ(ВложенныйЗапрос.КоличествоМестРазгрузка КАК ЧИСЛО(8, 0))) КАК КоличествоМестРазгрузка,
	|	СУММА(ВложенныйЗапрос.КоличествоЗагрузочныхМетровРазгрузка) КАК КоличествоЗагрузочныхМетровРазгрузка
	|ПОМЕСТИТЬ втПоказателиЗагрузки
	|ИЗ
	|	(ВЫБРАТЬ
	|		втПоказателиЗагрузки.Идентификатор КАК Идентификатор,
	|		СУММА(втПоказателиЗагрузки.Масса) КАК МассаПогрузка,
	|		СУММА(втПоказателиЗагрузки.Объем) КАК ОбъемПогрузка,
	|		СУММА(ВЫРАЗИТЬ(втПоказателиЗагрузки.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМестПогрузка,
	|		СУММА(втПоказателиЗагрузки.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетровПогрузка,
	|		0 КАК МассаРазгрузка,
	|		0 КАК ОбъемРазгрузка,
	|		0 КАК КоличествоМестРазгрузка,
	|		0 КАК КоличествоЗагрузочныхМетровРазгрузка
	|	ИЗ
	|		" + ИмяИтоговойТаблицы + "  КАК втПоказателиЗагрузки
	|	ГДЕ
	|		втПоказателиЗагрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|		 ИЛИ втПоказателиЗагрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		втПоказателиЗагрузки.Идентификатор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втПоказателиЗагрузки.Идентификатор,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СУММА(втПоказателиЗагрузки.Масса),
	|		СУММА(ВЫРАЗИТЬ(втПоказателиЗагрузки.КоличествоМест КАК ЧИСЛО(8, 0))),
	|		СУММА(втПоказателиЗагрузки.КоличествоМест),
	|		СУММА(втПоказателиЗагрузки.КоличествоЗагрузочныхМетров)
	|	ИЗ
	|		" + ИмяИтоговойТаблицы + " КАК втПоказателиЗагрузки
	|	ГДЕ
	|		втПоказателиЗагрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|		ИЛИ втПоказателиЗагрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)	
	|	СГРУППИРОВАТЬ ПО
	|		втПоказателиЗагрузки.Идентификатор) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.НомерСтроки,
	|	втМаршрут.Идентификатор,
	|	ЕСТЬNULL(втПоказателиЗагрузки.МассаПогрузка, 0) КАК МассаПогрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.ОбъемПогрузка, 0) КАК ОбъемПогрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоМестПогрузка, 0) КАК КоличествоМестПогрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоЗагрузочныхМетровПогрузка, 0) КАК КоличествоЗагрузочныхМетровПогрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.МассаРазгрузка, 0) КАК МассаРазгрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.ОбъемРазгрузка, 0) КАК ОбъемРазгрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоМестРазгрузка, 0) КАК КоличествоМестРазгрузка,
	|	ЕСТЬNULL(втПоказателиЗагрузки.КоличествоЗагрузочныхМетровРазгрузка, 0) КАК КоличествоЗагрузочныхМетровРазгрузка
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПоказателиЗагрузки КАК втПоказателиЗагрузки
	|		ПО втМаршрут.Идентификатор = втПоказателиЗагрузки.Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.НомерСтроки";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	Запрос.УстановитьПараметр("Работы", Работы);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			сткПоказатели = Новый Структура("МассаПогрузка, ОбъемПогрузка, КоличествоМестПогрузка, КоличествоЗагрузочныхМетровПогрузка,
											|МассаРазгрузка, ОбъемРазгрузка, КоличествоМестРазгрузка, КоличествоЗагрузочныхМетровРазгрузка");
			ЗаполнитьЗначенияСвойств(сткПоказатели, Выборка);								
			соотЗагрузка.Вставить(СокрП(Выборка.Идентификатор), сткПоказатели);
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат соотЗагрузка;
	
КонецФункции // РассчитатьЗагрузкуВТочкахМаршрутаПодробно()

// Функция рассчитывает суммарные показатели в каждой точке маршрута в режиме работы "Управление транспортом".
//
// Параметры:
//   Маршрут  - ТаблицаЗначений - Таблица данных.
//   Работы  - ТаблицаЗначений - Таблица данных.
//
// Возвращаемое значение:
//   Соответствие - Значения:
//      Ключ     - уникальныеИдентификатор - Идентификатор точки маршрута.
//      Значение - Структура.
//
Функция РассчитатьЗагрузкуВТочкахМаршрутаУправлениеТранспортом(Маршрут, Работы) Экспорт
	
	Маршрут.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Строка"));
	Для каждого текТочка Из Маршрут Цикл
		текТочка.ИДСтроки = Строка(текТочка.Идентификатор);
	КонецЦикла;
	
	Работы.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Строка"));
	Для каждого текРабота Из Работы Цикл
		текРабота.ИДСтроки = Строка(текРабота.Идентификатор);
	КонецЦикла;
	
	соотЗагрузка = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Маршрут.СтрокаНомер КАК НомерСтроки,
	|	ВЫРАЗИТЬ(Маршрут.ИДСтроки КАК СТРОКА(100)) КАК Идентификатор
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	&Маршрут КАК Маршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Работы.ИДСтроки КАК СТРОКА(100)) КАК Идентификатор,
	|	Работы.Операция,
	|	Работы.Масса,
	|	Работы.Объем
	|ПОМЕСТИТЬ втРаботыПодготовка
	|ИЗ
	|	&Работы КАК Работы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	ВЫБОР
	|		КОГДА втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * втРаботы.Масса КАК Масса,
	|	ВЫБОР
	|		КОГДА втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ * втРаботы.Объем КАК Объем
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втРаботыПодготовка КАК втРаботы
	|ГДЕ
	|	втРаботы.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), 
	|							ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка), 
	|							ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей),
	|							ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.НомерСтроки,
	|	втМаршрут.Идентификатор,
	|	ЕСТЬNULL(втРаботы.Масса, 0) КАК Масса,
	|	ЕСТЬNULL(втРаботы.Объем, 0) КАК Объем
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК втРаботы
	|		ПО втМаршрут.Идентификатор = втРаботы.Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.НомерСтроки";
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	Запрос.УстановитьПараметр("Работы", Работы);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Масса                       = 0;
		Объем                       = 0;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			сткПоказатели = Новый Структура("Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КоличествоГрузовыхМест");
			
			Масса                       = Масса + Выборка.Масса;
			Объем                       = Объем + Выборка.Объем;
			
			сткПоказатели.Масса                       = Масса;
			сткПоказатели.Объем                       = Объем;
			сткПоказатели.КоличествоМест              = 0;
			сткПоказатели.КоличествоЗагрузочныхМетров = 0;
			сткПоказатели.КоличествоГрузовыхМест	  = 0;
			
			соотЗагрузка.Вставить(СокрП(Выборка.Идентификатор), сткПоказатели);
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат соотЗагрузка;
	
КонецФункции // РассчитатьЗагрузкуВТочкахМаршрутаУправлениеТранспортом()

#Область ФормированиеДвижений

// Процедура - формирует движения по регистру "упПараметрыЗаданияНаПеревозку".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПараметрыЗаданияНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаборЗаписей = Движения.упПараметрыЗаданияНаПеревозку;
	ДвиженияНаборЗаписей.Записывать = Истина;
		
	Для каждого Строка Из Реквизиты.ГрузовыеМеста Цикл
		
		ДвижениеЗапись = ДвиженияНаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеЗапись, Строка);
		ДвижениеЗапись.ЗаданиеНаПеревозку = Реквизиты.ЗаданиеНаПеревозкуГруза;
		ДвижениеЗапись.Период = Реквизиты.Дата;
		
	КонецЦикла;

КонецПроцедуры

// Процедура - формирует движения по регистру "упПараметрыЗаявкиНаПеревозку".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПараметрыЗаявкиНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаборЗаписей = Движения.упПараметрыЗаявкиНаПеревозку;
	ДвиженияНаборЗаписей.Записывать = Истина;
	
	Если ЗначениеЗаполнено(Реквизиты.ЗаявкаНаПеревозкуГруза) Тогда
		
		Для каждого Строка Из  Реквизиты.ГрузовыеМеста Цикл
			ДвижениеЗапись = ДвиженияНаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеЗапись, Строка);
			ДвижениеЗапись.ЗаявкаНаПеревозку = Реквизиты.ЗаявкаНаПеревозкуГруза;
			ДвижениеЗапись.Период = Реквизиты.Дата;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов() Экспорт
	
	Возврат 
	"
	|// Вложенные грузовые места и тара с уровнями вложенности 1-3
	|ВЫБРАТЬ
	|	втГрузовыеМестаВТаре.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаВТаре.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаВТаре.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаВТаре.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньОдинТри
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМестаВТаре
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаВТаре.Тара
	|		И втГрузовыеМеста.НомерВЦепиПоставок = втГрузовыеМестаВТаре.НомерВЦепиПоставок
	|		И втГрузовыеМеста.НомерВЦепиПоставокКонец = втГрузовыеМестаВТаре.НомерВЦепиПоставокКонец
	|;
	|// Вложенные грузовые места и тара с уровнями вложенности 2-3
	|ВЫБРАТЬ
	|	втГрузовыеМестаТара.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаТара.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньДваТри
	|ИЗ
	|	втУровеньОдинТри КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньОдинТри КАК втГрузовыеМестаТара
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара
	|		И втГрузовыеМеста.НомерВЦепиПоставок = втГрузовыеМестаТара.НомерВЦепиПоставок
	|		И втГрузовыеМеста.НомерВЦепиПоставокКонец = втГрузовыеМестаТара.НомерВЦепиПоставокКонец
	|;
	|// Вложенные грузовые места и тара с уровнем вложенности 3
	|ВЫБРАТЬ
	|	втГрузовыеМестаТара.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаТара.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньТри
	|ИЗ
	|	втУровеньДваТри КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньДваТри КАК втГрузовыеМестаТара
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара
	|		И втГрузовыеМеста.НомерВЦепиПоставок = втГрузовыеМестаТара.НомерВЦепиПоставок
	|		И втГрузовыеМеста.НомерВЦепиПоставокКонец = втГрузовыеМестаТара.НомерВЦепиПоставокКонец
	|;
	|ВЫБРАТЬ
	|	втУровеньТри.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втУровеньТри.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втУровеньТри.Тара КАК Тара,
	|	ВЫБОР
	|		КОГДА СУММА(втПараметрыГрузов.Объем) > втПараметрыТары.Объем
	|			ТОГДА СУММА(втПараметрыГрузов.Объем)
	|		ИНАЧЕ втПараметрыТары.Объем
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА СУММА(втПараметрыГрузов.КоличествоМест) > втПараметрыТары.КоличествоМест
	|			ТОГДА СУММА(втПараметрыГрузов.КоличествоМест)
	|		ИНАЧЕ втПараметрыТары.КоличествоМест
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА СУММА(втПараметрыГрузов.КоличествоЗагрузочныхМетров) > втПараметрыТары.КоличествоЗагрузочныхМетров
	|			ТОГДА СУММА(втПараметрыГрузов.КоличествоЗагрузочныхМетров)
	|		ИНАЧЕ втПараметрыТары.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(втПараметрыГрузов.Длина) > втПараметрыТары.Длина
	|			ТОГДА МАКСИМУМ(втПараметрыГрузов.Длина)
	|		ИНАЧЕ втПараметрыТары.Длина
	|	КОНЕЦ КАК Длина,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(втПараметрыГрузов.Ширина) > втПараметрыТары.Ширина
	|			ТОГДА МАКСИМУМ(втПараметрыГрузов.Ширина)
	|		ИНАЧЕ втПараметрыТары.Ширина
	|	КОНЕЦ КАК Ширина,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(втПараметрыГрузов.Высота) > втПараметрыТары.Высота
	|			ТОГДА МАКСИМУМ(втПараметрыГрузов.Высота)
	|		ИНАЧЕ втПараметрыТары.Высота
	|	КОНЕЦ КАК Высота
	|ПОМЕСТИТЬ втПараметрыУровеньТри
	|ИЗ
	|	втУровеньТри КАК втУровеньТри
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыГрузов
	|	ПО ИСТИНА
	|		И втУровеньТри.ГрузовоеМесто = втПараметрыГрузов.ГрузовоеМесто
	|		И втУровеньТри.НомерВЦепиПоставок = втПараметрыГрузов.НомерВЦепиПоставок
	|		И втУровеньТри.НомерВЦепиПоставокКонец = втПараметрыГрузов.НомерВЦепиПоставокКонец
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыТары
	|	ПО ИСТИНА
	|		И втУровеньТри.Тара = втПараметрыТары.ГрузовоеМесто
	|		И втУровеньТри.НомерВЦепиПоставок = втПараметрыТары.НомерВЦепиПоставок
	|		И втУровеньТри.НомерВЦепиПоставокКонец = втПараметрыТары.НомерВЦепиПоставокКонец
	|СГРУППИРОВАТЬ ПО
	|	втУровеньТри.НомерВЦепиПоставок,
	|	втУровеньТри.НомерВЦепиПоставокКонец,
	|	втУровеньТри.Тара,
	|	втПараметрыТары.Объем,
	|	втПараметрыТары.КоличествоМест,
	|	втПараметрыТары.КоличествоЗагрузочныхМетров,
	|	втПараметрыТары.Длина,
	|	втПараметрыТары.Ширина,
	|	втПараметрыТары.Высота
	|;
	|ВЫБРАТЬ
	|	втУровеньДва.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втУровеньДва.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втУровеньДва.Тара КАК Тара,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.Объем, втПараметрыГрузов.Объем)) > втПараметрыТары.Объем
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.Объем, втПараметрыГрузов.Объем))
	|		ИНАЧЕ втПараметрыТары.Объем
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоМест, втПараметрыГрузов.КоличествоМест)) > втПараметрыТары.КоличествоМест
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоМест, втПараметрыГрузов.КоличествоМест))
	|		ИНАЧЕ втПараметрыТары.КоличествоМест
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров)) > втПараметрыТары.КоличествоЗагрузочныхМетров
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ втПараметрыТары.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньТри.Длина, втПараметрыГрузов.Длина)) > втПараметрыТары.Длина
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньТри.Длина, втПараметрыГрузов.Длина))
	|		ИНАЧЕ втПараметрыТары.Длина
	|	КОНЕЦ КАК Длина,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньТри.Ширина, втПараметрыГрузов.Ширина)) > втПараметрыТары.Ширина
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньТри.Ширина, втПараметрыГрузов.Ширина))
	|		ИНАЧЕ втПараметрыТары.Ширина
	|	КОНЕЦ КАК Ширина,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньТри.Высота, втПараметрыГрузов.Высота)) > втПараметрыТары.Высота
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньТри.Высота, втПараметрыГрузов.Высота))
	|		ИНАЧЕ втПараметрыТары.Высота
	|	КОНЕЦ КАК Высота
	|ПОМЕСТИТЬ втПараметрыУровеньДва
	|ИЗ
	|	(ВЫБРАТЬ
	|		втУровеньДваТри.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		втУровеньДваТри.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|		втУровеньДваТри.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втУровеньДваТри.Тара КАК Тара
	|	ИЗ
	|		втУровеньДваТри КАК втУровеньДваТри
	|	ГДЕ НЕ (втУровеньДваТри.Тара В (
	|			ВЫБРАТЬ
	|				втПараметрыУровеньТри.Тара КАК Тара
	|			ИЗ
	|				втПараметрыУровеньТри КАК втПараметрыУровеньТри))) КАК втУровеньДва
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыУровеньТри КАК втПараметрыУровеньТри
	|	ПО втУровеньДва.ГрузовоеМесто = втПараметрыУровеньТри.Тара
	|		И втУровеньДва.НомерВЦепиПоставок = втПараметрыУровеньТри.НомерВЦепиПоставок
	|		И втУровеньДва.НомерВЦепиПоставокКонец = втПараметрыУровеньТри.НомерВЦепиПоставокКонец
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыГрузов
	|	ПО ИСТИНА
	|		И втУровеньДва.ГрузовоеМесто = втПараметрыГрузов.ГрузовоеМесто
	|		И втУровеньДва.НомерВЦепиПоставок = втПараметрыГрузов.НомерВЦепиПоставок
	|		И втУровеньДва.НомерВЦепиПоставокКонец = втПараметрыГрузов.НомерВЦепиПоставокКонец
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыТары
	|	ПО ИСТИНА
	|		И втУровеньДва.Тара = втПараметрыТары.ГрузовоеМесто
	|		И втУровеньДва.НомерВЦепиПоставок = втПараметрыТары.НомерВЦепиПоставок
	|		И втУровеньДва.НомерВЦепиПоставокКонец = втПараметрыТары.НомерВЦепиПоставокКонец
	|СГРУППИРОВАТЬ ПО
	|	втУровеньДва.НомерВЦепиПоставок,
	|	втУровеньДва.НомерВЦепиПоставокКонец,
	|	втУровеньДва.Тара,
	|	втПараметрыТары.Объем,
	|	втПараметрыТары.КоличествоМест,
	|	втПараметрыТары.КоличествоЗагрузочныхМетров,
	|	втПараметрыТары.Длина,
	|	втПараметрыТары.Ширина,
	|	втПараметрыТары.Высота
	|;
	|ВЫБРАТЬ
	|	втУровеньОдин.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втУровеньОдин.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втУровеньОдин.Тара КАК Тара,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.Объем, втПараметрыГрузов.Объем)) > втПараметрыТары.Объем
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.Объем, втПараметрыГрузов.Объем))
	|		ИНАЧЕ втПараметрыТары.Объем
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоМест, втПараметрыГрузов.КоличествоМест)) > втПараметрыТары.КоличествоМест
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоМест, втПараметрыГрузов.КоличествоМест))
	|		ИНАЧЕ втПараметрыТары.КоличествоМест
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров)) > втПараметрыТары.КоличествоЗагрузочныхМетров
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ втПараметрыТары.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньДва.Длина, втПараметрыГрузов.Длина)) > втПараметрыТары.Длина
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньДва.Длина, втПараметрыГрузов.Длина))
	|		ИНАЧЕ втПараметрыТары.Длина
	|	КОНЕЦ КАК Длина,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньДва.Ширина, втПараметрыГрузов.Ширина)) > втПараметрыТары.Ширина
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньДва.Ширина, втПараметрыГрузов.Ширина))
	|		ИНАЧЕ втПараметрыТары.Ширина
	|	КОНЕЦ КАК Ширина,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньДва.Высота, втПараметрыГрузов.Высота)) > втПараметрыТары.Высота
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньДва.Высота, втПараметрыГрузов.Высота))
	|		ИНАЧЕ втПараметрыТары.Высота
	|	КОНЕЦ КАК Высота
	|ПОМЕСТИТЬ втПараметрыУровеньОдин
	|ИЗ
	|	(ВЫБРАТЬ
	|		втУровеньОдинТри.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		втУровеньОдинТри.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|		втУровеньОдинТри.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втУровеньОдинТри.Тара КАК Тара
	|	ИЗ
	|		втУровеньОдинТри КАК втУровеньОдинТри
	|	ГДЕ НЕ (втУровеньОдинТри.Тара В (
	|			ВЫБРАТЬ
	|				втУровеньДваТри.Тара КАК Тара
	|			ИЗ
	|				втУровеньДваТри КАК втУровеньДваТри))) КАК втУровеньОдин
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыУровеньДва КАК втПараметрыУровеньДва
	|	ПО втУровеньОдин.ГрузовоеМесто = втПараметрыУровеньДва.Тара
	|		И втУровеньОдин.НомерВЦепиПоставок = втПараметрыУровеньДва.НомерВЦепиПоставок
	|		И втУровеньОдин.НомерВЦепиПоставокКонец = втПараметрыУровеньДва.НомерВЦепиПоставокКонец
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыГрузов
	|	ПО ИСТИНА
	|		И втУровеньОдин.ГрузовоеМесто = втПараметрыГрузов.ГрузовоеМесто
	|		И втУровеньОдин.НомерВЦепиПоставок = втПараметрыГрузов.НомерВЦепиПоставок
	|		И втУровеньОдин.НомерВЦепиПоставокКонец = втПараметрыГрузов.НомерВЦепиПоставокКонец
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыТары
	|	ПО ИСТИНА
	|		И втУровеньОдин.Тара = втПараметрыТары.ГрузовоеМесто
	|		И втУровеньОдин.НомерВЦепиПоставок = втПараметрыТары.НомерВЦепиПоставок
	|		И втУровеньОдин.НомерВЦепиПоставокКонец = втПараметрыТары.НомерВЦепиПоставокКонец
	|СГРУППИРОВАТЬ ПО
	|	втУровеньОдин.НомерВЦепиПоставок,
	|	втУровеньОдин.НомерВЦепиПоставокКонец,
	|	втУровеньОдин.Тара,
	|	втПараметрыТары.Объем,
	|	втПараметрыТары.КоличествоМест,
	|	втПараметрыТары.КоличествоЗагрузочныхМетров,
	|	втПараметрыТары.Длина,
	|	втПараметрыТары.Ширина,
	|	втПараметрыТары.Высота
	|;
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	СУММА(ЕСТЬNULL(втПараметрыУровеньОдин.Объем, втГрузовыеМеста.Объем)) КАК Объем,
	|	СУММА(ЕСТЬNULL(ВЫРАЗИТЬ(втПараметрыУровеньОдин.КоличествоМест КАК ЧИСЛО(8, 0)), ВЫРАЗИТЬ(втГрузовыеМеста.КоличествоМест КАК ЧИСЛО(8, 0)))) КАК КоличествоМест,
	|	СУММА(ЕСТЬNULL(втПараметрыУровеньОдин.КоличествоЗагрузочныхМетров, втГрузовыеМеста.КоличествоЗагрузочныхМетров)) КАК КоличествоЗагрузочныхМетров,
	|	МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньОдин.Длина, втГрузовыеМеста.Длина)) КАК Длина,
	|	МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньОдин.Ширина, втГрузовыеМеста.Ширина)) КАК Ширина,
	|	МАКСИМУМ(ЕСТЬNULL(втПараметрыУровеньОдин.Высота, втГрузовыеМеста.Высота)) КАК Высота,
	|	0 КАК Масса,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест,
	|	0 КАК КоличествоГрузов
	|ПОМЕСТИТЬ втПараметрыИтог
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыУровеньОдин КАК втПараметрыУровеньОдин
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ГрузовоеМесто = втПараметрыУровеньОдин.Тара
	|		И втГрузовыеМеста.НомерВЦепиПоставок = втПараметрыУровеньОдин.НомерВЦепиПоставок
	|		И втГрузовыеМеста.НомерВЦепиПоставокКонец = втПараметрыУровеньОдин.НомерВЦепиПоставокКонец
	|ГДЕ втГрузовыеМеста.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	0 КАК Объем,
	|	0 КАК КоличествоМест,
	|	0 КАК КоличествоЗагрузочныхМетров,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	втГрузовыеМеста.Масса КАК Масса,
	|	втГрузовыеМеста.Сумма КАК Сумма,
	|	втГрузовыеМеста.СуммаНДС КАК СуммаНДС,
	|	втГрузовыеМеста.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	втГрузовыеМеста.СуммаИнкассацииПлан КАК СуммаИнкассацииПлан,
	|	втГрузовыеМеста.КоличествоГрузовыхМест КАК КоличествоГрузовыхМест,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|;";

	
КонецФункции

Функция ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузовДляРейса()
	
	Возврат 
	"ВЫБРАТЬ
	|	втГрузовыеМеста.Идентификатор КАК Идентификатор,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньОдинТри
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.Тара = втГрузовыеМестаТара.ГрузовоеМесто
	|			И втГрузовыеМеста.Идентификатор = втГрузовыеМестаТара.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаТара.Идентификатор КАК Идентификатор,
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньДваТри
	|ИЗ
	|	втУровеньОдинТри КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньОдинТри КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара
	|			И втГрузовыеМеста.Идентификатор = втГрузовыеМестаТара.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаТара.Идентификатор КАК Идентификатор,
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньТри
	|ИЗ
	|	втУровеньДваТри КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньДваТри КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара
	|			И втГрузовыеМеста.Идентификатор = втГрузовыеМестаТара.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУровеньТри.Идентификатор КАК Идентификатор,
	|	втУровеньТри.Тара КАК Тара,
	|	ВЫБОР
	|		КОГДА СУММА(втПараметрыГрузов.Объем) > втПараметрыТары.Объем
	|			ТОГДА СУММА(втПараметрыГрузов.Объем)
	|		ИНАЧЕ втПараметрыТары.Объем
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА СУММА(втПараметрыГрузов.КоличествоМест) > втПараметрыТары.КоличествоМест
	|			ТОГДА СУММА(втПараметрыГрузов.КоличествоМест)
	|		ИНАЧЕ втПараметрыТары.КоличествоМест
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА СУММА(втПараметрыГрузов.КоличествоЗагрузочныхМетров) > втПараметрыТары.КоличествоЗагрузочныхМетров
	|			ТОГДА СУММА(втПараметрыГрузов.КоличествоЗагрузочныхМетров)
	|		ИНАЧЕ втПараметрыТары.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втПараметрыУровеньТри
	|ИЗ
	|	втУровеньТри КАК втУровеньТри
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыГрузов
	|		ПО втУровеньТри.ГрузовоеМесто = втПараметрыГрузов.ГрузовоеМесто
	|			И втУровеньТри.Идентификатор = втПараметрыГрузов.Идентификатор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыТары
	|		ПО втУровеньТри.Тара = втПараметрыТары.ГрузовоеМесто
	|			И втУровеньТри.Идентификатор = втПараметрыТары.Идентификатор
	|
	|СГРУППИРОВАТЬ ПО
	|	втУровеньТри.Идентификатор,
	|	втУровеньТри.Тара,
	|	втПараметрыТары.Объем,
	|	втПараметрыТары.КоличествоМест,
	|	втПараметрыТары.КоличествоЗагрузочныхМетров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУровеньДва.Идентификатор КАК Идентификатор,
	|	втУровеньДва.Тара КАК Тара,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.Объем, втПараметрыГрузов.Объем)) > втПараметрыТары.Объем
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.Объем, втПараметрыГрузов.Объем))
	|		ИНАЧЕ втПараметрыТары.Объем
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоМест, втПараметрыГрузов.КоличествоМест)) > втПараметрыТары.КоличествоМест
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоМест, втПараметрыГрузов.КоличествоМест))
	|		ИНАЧЕ втПараметрыТары.КоличествоМест
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров)) > втПараметрыТары.КоличествоЗагрузочныхМетров
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньТри.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ втПараметрыТары.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втПараметрыУровеньДва
	|ИЗ
	|	(ВЫБРАТЬ
	|		втУровеньДваТри.Идентификатор КАК Идентификатор,
	|		втУровеньДваТри.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втУровеньДваТри.Тара КАК Тара
	|	ИЗ
	|		втУровеньДваТри КАК втУровеньДваТри
	|	ГДЕ
	|		НЕ втУровеньДваТри.Тара В
	|					(ВЫБРАТЬ
	|						втПараметрыУровеньТри.Тара
	|					ИЗ
	|						втПараметрыУровеньТри)) КАК втУровеньДва
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыУровеньТри КАК втПараметрыУровеньТри
	|		ПО втУровеньДва.ГрузовоеМесто = втПараметрыУровеньТри.Тара
	|			И втУровеньДва.Идентификатор = втПараметрыУровеньТри.Идентификатор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыГрузов
	|		ПО втУровеньДва.ГрузовоеМесто = втПараметрыГрузов.ГрузовоеМесто
	|			И втУровеньДва.Идентификатор = втПараметрыГрузов.Идентификатор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыТары
	|		ПО втУровеньДва.Тара = втПараметрыТары.ГрузовоеМесто
	|			И втУровеньДва.Идентификатор = втПараметрыТары.Идентификатор
	|
	|СГРУППИРОВАТЬ ПО
	|	втУровеньДва.Идентификатор,
	|	втУровеньДва.Тара,
	|	втПараметрыТары.Объем,
	|	втПараметрыТары.КоличествоМест,
	|	втПараметрыТары.КоличествоЗагрузочныхМетров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУровеньОдин.Идентификатор КАК Идентификатор,
	|	втУровеньОдин.Тара КАК Тара,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.Объем, втПараметрыГрузов.Объем)) > втПараметрыТары.Объем
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.Объем, втПараметрыГрузов.Объем))
	|		ИНАЧЕ втПараметрыТары.Объем
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоМест, втПараметрыГрузов.КоличествоМест)) > втПараметрыТары.КоличествоМест
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоМест, втПараметрыГрузов.КоличествоМест))
	|		ИНАЧЕ втПараметрыТары.КоличествоМест
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров)) > втПараметрыТары.КоличествоЗагрузочныхМетров
	|			ТОГДА СУММА(ЕСТЬNULL(втПараметрыУровеньДва.КоличествоЗагрузочныхМетров, втПараметрыГрузов.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ втПараметрыТары.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втПараметрыУровеньОдин
	|ИЗ
	|	(ВЫБРАТЬ
	|		втУровеньОдинТри.Идентификатор КАК Идентификатор,
	|		втУровеньОдинТри.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втУровеньОдинТри.Тара КАК Тара
	|	ИЗ
	|		втУровеньОдинТри КАК втУровеньОдинТри
	|	ГДЕ
	|		НЕ втУровеньОдинТри.Тара В
	|					(ВЫБРАТЬ
	|						втУровеньДваТри.Тара
	|					ИЗ
	|						втУровеньДваТри)) КАК втУровеньОдин
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыУровеньДва КАК втПараметрыУровеньДва
	|		ПО втУровеньОдин.ГрузовоеМесто = втПараметрыУровеньДва.Тара
	|			И втУровеньОдин.Идентификатор = втПараметрыУровеньДва.Идентификатор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыГрузов
	|		ПО втУровеньОдин.ГрузовоеМесто = втПараметрыГрузов.ГрузовоеМесто
	|			И втУровеньОдин.Идентификатор = втПараметрыГрузов.Идентификатор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втПараметрыТары
	|		ПО втУровеньОдин.Тара = втПараметрыТары.ГрузовоеМесто
	|			И втУровеньОдин.Идентификатор = втПараметрыТары.Идентификатор
	|
	|СГРУППИРОВАТЬ ПО
	|	втУровеньОдин.Идентификатор,
	|	втУровеньОдин.Тара,
	|	втПараметрыТары.Объем,
	|	втПараметрыТары.КоличествоМест,
	|	втПараметрыТары.КоличествоЗагрузочныхМетров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Операция,
	|	втГрузовыеМеста.Идентификатор КАК Идентификатор,
	|	СУММА(ЕСТЬNULL(втПараметрыУровеньОдин.Объем, втГрузовыеМеста.Объем)) КАК Объем,
	|	СУММА(ЕСТЬNULL(втПараметрыУровеньОдин.КоличествоМест, втГрузовыеМеста.КоличествоМест)) КАК КоличествоМест,
	|	СУММА(ЕСТЬNULL(втПараметрыУровеньОдин.КоличествоЗагрузочныхМетров, втГрузовыеМеста.КоличествоЗагрузочныхМетров)) КАК КоличествоЗагрузочныхМетров,
	|	0 КАК Масса,
	|	0 КАК КоличествоГрузовыхМест,
	|	0 КАК КоличествоГрузов
	|ПОМЕСТИТЬ втПараметрыИтог
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыУровеньОдин КАК втПараметрыУровеньОдин
	|		ПО втГрузовыеМеста.ГрузовоеМесто = втПараметрыУровеньОдин.Тара
	|			И втГрузовыеМеста.Идентификатор = втПараметрыУровеньОдин.Идентификатор
	|ГДЕ
	|	втГрузовыеМеста.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Операция,
	|	втГрузовыеМеста.Идентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Операция,
	|	втГрузовыеМеста.Идентификатор,
	|	0,
	|	0,
	|	0,
	|	втГрузовыеМеста.Масса,
	|	втГрузовыеМеста.КоличествоГрузовыхМест,
	|	втГрузовыеМеста.КоличествоГрузов
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|;";
	
КонецФункции

#КонецОбласти 

#Область Удалить

// Функция рассчитывает суммарную загрузку по грузовым местам.
//
// Параметры:
//  ТаблицаГрузов  - ТаблицаЗначений - Поля:
//    ГрузовоеМесто    - СправочникСсылка.упГрузовыеМеста.
//    КоличествоГрузов - ОпределяемыйТип.КоличествоГрузов.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция _РассчитатьПоказателиЗагрузки(ТаблицаГрузов) Экспорт
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Масса"                      , 0);
	сткВозврат.Вставить("Объем"                      , 0);
	сткВозврат.Вставить("КоличествоМест"             , 0);
	сткВозврат.Вставить("КоличествоГрузов"           , 0);
	сткВозврат.Вставить("КоличествоГрузовыхМест"     , 0);
	сткВозврат.Вставить("КоличествоЗагрузочныхМетров", 0);
	сткВозврат.Вставить("Сумма"                      , 0);
	сткВозврат.Вставить("СуммаНДС"                   , 0);
	сткВозврат.Вставить("ОценочнаяСтоимость"         , 0);
	сткВозврат.Вставить("СуммаИнкассацииПлан"        , 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаГрузов.ГрузовоеМесто,
	|	ТаблицаГрузов.Тара,
	|	ТаблицаГрузов.КоличествоГрузов
	|ПОМЕСТИТЬ втГрузы
	|ИЗ
	|	&ТаблицаГрузов КАК ТаблицаГрузов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упГрузовыеМеста.Масса * втГрузы.КоличествоГрузов КАК Масса,
	|	ВЫБОР
	|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА упГрузовыеМеста.Объем * втГрузы.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА упГрузовыеМеста.КоличествоМест * втГрузы.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА упГрузовыеМеста.КоличествоЗагрузочныхМетров * втГрузы.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
	|	упГрузовыеМеста.Сумма * втГрузы.КоличествоГрузов КАК Сумма,
	|	упГрузовыеМеста.СуммаНДС * втГрузы.КоличествоГрузов КАК СуммаНДС,
	|	упГрузовыеМеста.ОценочнаяСтоимость * втГрузы.КоличествоГрузов КАК ОценочнаяСтоимость,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|			ТОГДА упГрузовыеМеста.Сумма * втГрузы.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаИнкассацииПлан,
	|	ВЫБОР
	|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА втГрузы.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоГрузовыхМест,
	|	втГрузы.КоличествоГрузов
	|ПОМЕСТИТЬ втПоказателиГрузов
	|ИЗ
	|	втГрузы КАК втГрузы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ПО втГрузы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Масса), 0) КАК Масса,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Объем), 0) КАК Объем,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоМест), 0) КАК КоличествоМест,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоЗагрузочныхМетров), 0) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоГрузов), 0) КАК КоличествоГрузов,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоГрузовыхМест), 0) КАК КоличествоГрузовыхМест,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Сумма), 0) КАК Сумма,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.СуммаНДС), 0) КАК СуммаНДС,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.ОценочнаяСтоимость), 0) КАК ОценочнаяСтоимость,
	|	ЕСТЬNULL(СУММА(втПоказателиГрузов.СуммаИнкассацииПлан), 0) КАК СуммаИнкассацииПлан
	|ИЗ
	|	втПоказателиГрузов КАК втПоказателиГрузов";
	
	Запрос.УстановитьПараметр("ТаблицаГрузов", ТаблицаГрузов);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка); 
	КонецЕсли; 
	
	Возврат сткВозврат;
	
КонецФункции // РассчитатьПоказателиЗагрузки()

// Процедура заполняет показатели загрузки в задании на перевозку груза
// по грузовым местам.
//
// Параметры:
//  ЭтотОбъект - ДокументОбъект.упЗаданиеНаПеревозкуГруза - задание на перевозку груза.
//
Процедура _ЗаполнитьПоказателиЗагрузки(ЗаданиеНаПеревозкуГрузаОбъект, ЗНАЧ ФормироватьПараметрыПеревозки = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	ФормироватьТаблицу = Истина
					 И ФормироватьПараметрыПеревозки
					 И (Ложь 
						Или ЗаданиеНаПеревозкуГрузаОбъект.НомерВЦепиПоставок = 0
						Или ЗаданиеНаПеревозкуГрузаОбъект.НомерВЦепиПоставок = 1);
		
	Запрос.УстановитьПараметр("ТаблицаГрузов", ЗаданиеНаПеревозкуГрузаОбъект.ГрузовыеМеста.Выгрузить());
	Запрос.УстановитьПараметр("ФормироватьТаблицу", ФормироватьТаблицу);
	
	Если ЗаданиеНаПеревозкуГрузаОбъект.ДополнительныеСвойства.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится Тогда
		Запрос.Текст =
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаГрузов.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ТаблицаГрузов.Тара КАК Тара,
		|	ТаблицаГрузов.КоличествоГрузов КАК КоличествоГрузов
		|ПОМЕСТИТЬ втГрузы
		|ИЗ
		|	&ТаблицаГрузов КАК ТаблицаГрузов
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	упГрузовыеМеста.Масса * втГрузы.КоличествоГрузов КАК Масса,
		|	упГрузовыеМеста.Объем * втГрузы.КоличествоГрузов КАК Объем,
		|	упГрузовыеМеста.КоличествоМест * втГрузы.КоличествоГрузов КАК КоличествоМест,
		|	упГрузовыеМеста.КоличествоЗагрузочныхМетров * втГрузы.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
		|	упГрузовыеМеста.Сумма * втГрузы.КоличествоГрузов КАК Сумма,
		|	упГрузовыеМеста.СуммаНДС * втГрузы.КоличествоГрузов КАК СуммаНДС,
		|	упГрузовыеМеста.ОценочнаяСтоимость * втГрузы.КоличествоГрузов КАК ОценочнаяСтоимость,
		|	ВЫБОР
		|		КОГДА упГрузовыеМеста.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
		|			ТОГДА упГрузовыеМеста.Сумма * втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаИнкассацииПлан,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоГрузовыхМест,
		|	втГрузы.КоличествоГрузов КАК КоличествоГрузов,
		|	упГрузовыеМеста.Высота КАК Высота,
		|	упГрузовыеМеста.Длина КАК Длина,
		|	упГрузовыеМеста.Ширина КАК Ширина,
		|	втГрузы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втГрузы.Тара КАК Тара,
		|	упГрузовыеМеста.Тара КАК ЭтоТара
		|ПОМЕСТИТЬ втГрузовыеМеста
		|ИЗ
		|	втГрузы КАК втГрузы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		|	ПО втГрузы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Масса), 0) КАК Масса,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоГрузов), 0) КАК КоличествоГрузов,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоГрузовыхМест), 0) КАК КоличествоГрузовыхМест,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Сумма), 0) КАК Сумма,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.СуммаНДС), 0) КАК СуммаНДС,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.ОценочнаяСтоимость), 0) КАК ОценочнаяСтоимость,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.СуммаИнкассацииПлан), 0) КАК СуммаИнкассацииПлан
		|ИЗ
		|	втГрузовыеМеста КАК втПоказателиГрузов
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|// Грузовые места и тара находящиеся в таре
		|ВЫБРАТЬ
		|	Номенклатура.Тара КАК Тара,
		|	Номенклатура.ГрузовоеМесто КАК ГрузовоеМесто,
		|	1 КАК Уровень
		|ПОМЕСТИТЬ ГрузыИТараУровень1
		|ИЗ
		|	втГрузовыеМеста КАК Номенклатура
		|ГДЕ Номенклатура.Тара <> ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|ОБЪЕДИНИТЬ
		|// Все грузовые места и тара
		|ВЫБРАТЬ
		|	Номенклатура.ГрузовоеМесто КАК Тара,
		|	Номенклатура.ГрузовоеМесто КАК ГрузовоеМесто,
		|	0 КАК Уровень
		|ИЗ
		|	втГрузовыеМеста КАК Номенклатура
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|// Грузовые места и тара + все комбинации иерархия до уровня вложенности два
		|// (груз и его тара, тара и её тара - вложенность 1
		|//  груз и тара тары груза, тара и тара её тары - вложенность 2)
		|ВЫБРАТЬ
		|	ПерваяТаблица.Тара КАК Тара,
		|	ВтораяТаблица.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ПерваяТаблица.Уровень + ВтораяТаблица.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ГрузыИТараДоУровня2
		|ИЗ
		|	ГрузыИТараУровень1 КАК ПерваяТаблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрузыИТараУровень1 КАК ВтораяТаблица
		|	ПО ПерваяТаблица.ГрузовоеМесто = ВтораяТаблица.Тара
		|СГРУППИРОВАТЬ ПО
		|	ПерваяТаблица.Тара,
		|	ВтораяТаблица.ГрузовоеМесто
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|// Грузовые места и тара + все комбинации иерархия до уровня вложенности 4
		|ВЫБРАТЬ
		|	ПерваяТаблица.Тара КАК Тара,
		|	ВтораяТаблица.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ПерваяТаблица.Уровень + ВтораяТаблица.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ГрузыИТараДоУровня4
		|ИЗ
		|	ГрузыИТараДоУровня2 КАК ПерваяТаблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрузыИТараДоУровня2 КАК ВтораяТаблица
		|	ПО ПерваяТаблица.ГрузовоеМесто = ВтораяТаблица.Тара
		|СГРУППИРОВАТЬ ПО
		|	ПерваяТаблица.Тара,
		|	ВтораяТаблица.ГрузовоеМесто
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|// Грузовые места и тара + все комбинации иерархия до уровня вложенности 8
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерваяТаблица.Тара КАК Тара,
		|	ВтораяТаблица.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ПерваяТаблица.Уровень + ВтораяТаблица.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ГрузыИТараДоУровня8
		|ИЗ
		|	ГрузыИТараДоУровня4 КАК ПерваяТаблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрузыИТараДоУровня4 КАК ВтораяТаблица
		|	ПО ПерваяТаблица.ГрузовоеМесто = ВтораяТаблица.Тара
		|СГРУППИРОВАТЬ ПО
		|	ПерваяТаблица.Тара,
		|	ВтораяТаблица.ГрузовоеМесто
		|;
		|//{Запрос: 7 ////////////////////////////////////////
		|// Грузовые места и тара + все комбинации иерархия до уровня вложенности 16
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерваяТаблица.Тара КАК Тара,
		|	ВтораяТаблица.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ПерваяТаблица.Уровень + ВтораяТаблица.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ГрузыИТараДоУровня16
		|ИЗ
		|	ГрузыИТараДоУровня8 КАК ПерваяТаблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрузыИТараДоУровня8 КАК ВтораяТаблица
		|	ПО ПерваяТаблица.ГрузовоеМесто = ВтораяТаблица.Тара
		|СГРУППИРОВАТЬ ПО
		|	ПерваяТаблица.Тара,
		|	ВтораяТаблица.ГрузовоеМесто
		|;
		|//{Запрос: 8 ////////////////////////////////////////
		|// Грузовые места и тара + все комбинации иерархия до уровня вложенности 32
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерваяТаблица.Тара КАК Тара,
		|	ВтораяТаблица.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ПерваяТаблица.Уровень + ВтораяТаблица.Уровень) КАК Уровень
		|ПОМЕСТИТЬ ГрузыИТараДоУровня32
		|ИЗ
		|	ГрузыИТараДоУровня16 КАК ПерваяТаблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГрузыИТараДоУровня16 КАК ВтораяТаблица
		|	ПО ПерваяТаблица.ГрузовоеМесто = ВтораяТаблица.Тара
		|СГРУППИРОВАТЬ ПО
		|	ПерваяТаблица.Тара,
		|	ВтораяТаблица.ГрузовоеМесто
		|;
		|//{Запрос: 9 ////////////////////////////////////////
		|// Характеристики грузовых мест и тары
		|ВЫБРАТЬ
		|	втГрузовыеМестаТ.*
		|ИЗ
		|	втГрузовыеМеста КАК втГрузовыеМестаТ
		|;
		|//{Запрос: 10 ////////////////////////////////////////
		|// Порядок расчета характеристик тары
		|ВЫБРАТЬ
		|	ГрузыИТараДоУровня32Т.ГрузовоеМесто КАК ГрузовоеМесто,
		|	МАКСИМУМ(ГрузыИТараДоУровня32Т.Уровень) КАК Уровень
		|ИЗ
		|	ГрузыИТараДоУровня32 КАК ГрузыИТараДоУровня32Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМеста
		|	ПО ИСТИНА
		|		И ГрузыИТараДоУровня32Т.ГрузовоеМесто = втГрузовыеМеста.ГрузовоеМесто
		|		И втГрузовыеМеста.ЭтоТара
		|СГРУППИРОВАТЬ ПО
		|	ГрузыИТараДоУровня32Т.ГрузовоеМесто
		|УПОРЯДОЧИТЬ ПО
		|	Уровень УБЫВ
		|;
		|//{Запрос: 11 ////////////////////////////////////////
		|// Грузовые места и тара, у которых нет тары
		|ВЫБРАТЬ
		|	Номенклатура.ГрузовоеМесто КАК ГрузовоеМесто
		|ИЗ
		|	втГрузовыеМеста КАК Номенклатура
		|ГДЕ Номенклатура.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		РезультатЗапросаПоказателиЗадания = РезультатЗапроса[2];
		Если Не РезультатЗапросаПоказателиЗадания.Пустой() Тогда
			Выборка = РезультатЗапросаПоказателиЗадания.Выбрать();
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(ЗаданиеНаПеревозкуГрузаОбъект, Выборка); 
		КонецЕсли; 
		
		// Заполняется объем, кол. мест, кол. загрузочных метров, высота, длина, ширина.
		тбзХарактеристикиГрузовыхМест = РезультатЗапроса[9].Выгрузить();
		тбзПорядокРасчетаТары = РезультатЗапроса[10].Выгрузить();
		тбзГрузовыеМеста = РезультатЗапроса[11].Выгрузить(); 
		
		// Рассчитываются параметры тары.
		Для каждого СтрокаТара Из тбзПорядокРасчетаТары Цикл
			
			Объем = 0;
			КоличествоЗагрузочныхМетров = 0;
			КоличествоМест = 0;
			Длина = 0;
			Ширина = 0;
			Высота = 0;
			
			мсвСтроки = тбзХарактеристикиГрузовыхМест.НайтиСтроки(Новый Структура("Тара", СтрокаТара.ГрузовоеМесто));
			Для каждого СтрокаГрузовоеМесто Из мсвСтроки Цикл
				Объем = Объем + СтрокаГрузовоеМесто.Объем;
				КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров + СтрокаГрузовоеМесто.КоличествоЗагрузочныхМетров;
				КоличествоМест = КоличествоМест + СтрокаГрузовоеМесто.КоличествоМест;
				Если СтрокаГрузовоеМесто.Длина > Длина Тогда
					Длина = СтрокаГрузовоеМесто.Длина;
				КонецЕсли;
				Если СтрокаГрузовоеМесто.Ширина > Ширина Тогда
					Ширина = СтрокаГрузовоеМесто.Ширина;
				КонецЕсли;
				Если СтрокаГрузовоеМесто.Высота > Высота Тогда
					Высота = СтрокаГрузовоеМесто.Высота;
				КонецЕсли;
			КонецЦикла;
			
			мсвСтроки = тбзХарактеристикиГрузовыхМест.НайтиСтроки(Новый Структура("ГрузовоеМесто", СтрокаТара.ГрузовоеМесто));
			Если мсвСтроки.Количество() > 0 Тогда
				СтрокаТараТекущая = мсвСтроки[0];
				
				Если Объем > СтрокаТараТекущая.Объем Тогда
					СтрокаТараТекущая.Объем = Объем;
				КонецЕсли;
				
				Если КоличествоЗагрузочныхМетров > СтрокаТараТекущая.КоличествоЗагрузочныхМетров Тогда
					СтрокаТараТекущая.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров;
				КонецЕсли;

				Если КоличествоМест > СтрокаТараТекущая.КоличествоМест Тогда
					СтрокаТараТекущая.КоличествоМест = КоличествоМест;
				КонецЕсли;

				Если Длина > СтрокаТараТекущая.Длина Тогда
					СтрокаТараТекущая.Длина = Длина;
				КонецЕсли;

				Если Ширина > СтрокаТараТекущая.Ширина Тогда
					СтрокаТараТекущая.Ширина = Ширина;
				КонецЕсли;

				Если Высота > СтрокаТараТекущая.Высота Тогда
					СтрокаТараТекущая.Высота = Высота;
				КонецЕсли;

			КонецЕсли;
			
		КонецЦикла;
		
		// Рассчитываются параметры задания.
		Объем = 0;
		КоличествоЗагрузочныхМетров = 0;
		КоличествоМест = 0;
		Длина = 0;
		Ширина = 0;
		Высота = 0;
		
		Для каждого СтрокаГрузовоеМесто Из тбзГрузовыеМеста Цикл
						
			мсвСтроки = тбзХарактеристикиГрузовыхМест.НайтиСтроки(Новый Структура("ГрузовоеМесто", СтрокаГрузовоеМесто.ГрузовоеМесто));
			Для каждого СтрокаГрузовоеМесто Из мсвСтроки Цикл
				Объем = Объем + СтрокаГрузовоеМесто.Объем;
				КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров + СтрокаГрузовоеМесто.КоличествоЗагрузочныхМетров;
				КоличествоМест = КоличествоМест + СтрокаГрузовоеМесто.КоличествоМест;
				Если СтрокаГрузовоеМесто.Длина > Длина Тогда
					Длина = СтрокаГрузовоеМесто.Длина;
				КонецЕсли;
				Если СтрокаГрузовоеМесто.Ширина > Ширина Тогда
					Ширина = СтрокаГрузовоеМесто.Ширина;
				КонецЕсли;
				Если СтрокаГрузовоеМесто.Высота > Высота Тогда
					Высота = СтрокаГрузовоеМесто.Высота;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ЗаданиеНаПеревозкуГрузаОбъект.Объем = Объем;
		ЗаданиеНаПеревозкуГрузаОбъект.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров;
		ЗаданиеНаПеревозкуГрузаОбъект.КоличествоМест = КоличествоМест;
				
		Если ФормироватьТаблицу Тогда
			ПараметрыНаПеревозку = Новый ТаблицаЗначений;
			ПараметрыНаПеревозку.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
			ПараметрыНаПеревозку.Колонки.Добавить("Высота");
			ПараметрыНаПеревозку.Колонки.Добавить("Длина");
			ПараметрыНаПеревозку.Колонки.Добавить("Ширина");
			ПараметрыНаПеревозку.Колонки.Добавить("КоличествоЗагрузочныхМетров");
			ПараметрыНаПеревозку.Колонки.Добавить("КоличествоМест");
			ПараметрыНаПеревозку.Колонки.Добавить("Масса");
			ПараметрыНаПеревозку.Колонки.Добавить("Объем");
			
			НоваяСтрока = ПараметрыНаПеревозку.Добавить();
			НоваяСтрока.ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
			НоваяСтрока.Высота = Высота;
			НоваяСтрока.Длина = Длина;
			НоваяСтрока.Ширина = Ширина;
			НоваяСтрока.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров;
			НоваяСтрока.КоличествоМест = КоличествоМест;
			НоваяСтрока.Объем = Объем;
			НоваяСтрока.Масса = ЗаданиеНаПеревозкуГрузаОбъект.Масса;
			
			ЗаданиеНаПеревозкуГрузаОбъект.ДополнительныеСвойства.Вставить("ПараметрыНаПеревозку", ПараметрыНаПеревозку); 	
		КонецЕсли;

	Иначе	
		
		Запрос.Текст =
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаГрузов.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ТаблицаГрузов.Тара КАК Тара,
		|	ТаблицаГрузов.КоличествоГрузов КАК КоличествоГрузов
		|ПОМЕСТИТЬ втГрузы
		|ИЗ
		|	&ТаблицаГрузов КАК ТаблицаГрузов
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	упГрузовыеМеста.Масса * втГрузы.КоличествоГрузов КАК Масса,
		|	упГрузовыеМеста.Масса КАК ГрузовоеМестоМасса,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА упГрузовыеМеста.Объем * втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Объем,
		|	упГрузовыеМеста.КоличествоМест КАК ГрузовоеМестоОбъем,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА упГрузовыеМеста.КоличествоМест * втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМест,
		|	упГрузовыеМеста.КоличествоМест КАК ГрузовоеМестоКоличествоМест,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА упГрузовыеМеста.КоличествоЗагрузочныхМетров * втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
		|	упГрузовыеМеста.КоличествоЗагрузочныхМетров КАК ГрузовоеМестоКоличествоЗагрузочныхМетров,
		|	упГрузовыеМеста.Сумма * втГрузы.КоличествоГрузов КАК Сумма,
		|	упГрузовыеМеста.СуммаНДС * втГрузы.КоличествоГрузов КАК СуммаНДС,
		|	упГрузовыеМеста.ОценочнаяСтоимость * втГрузы.КоличествоГрузов КАК ОценочнаяСтоимость,
		|	ВЫБОР
		|		КОГДА упГрузовыеМеста.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
		|			ТОГДА упГрузовыеМеста.Сумма * втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаИнкассацииПлан,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА втГрузы.КоличествоГрузов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоГрузовыхМест,
		|	втГрузы.КоличествоГрузов КАК КоличествоГрузов,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА упГрузовыеМеста.Высота
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Высота,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА упГрузовыеМеста.Длина
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Длина,
		|	ВЫБОР
		|		КОГДА втГрузы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА упГрузовыеМеста.Ширина
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Ширина,
		|	упГрузовыеМеста.Высота КАК ГрузовоеМестоВысота,
		|	упГрузовыеМеста.Длина КАК ГрузовоеМестоДлина,
		|	упГрузовыеМеста.Ширина КАК ГрузовоеМестоШирина,
		|	упГрузовыеМеста.Ссылка КАК ГрузовоеМесто
		|ПОМЕСТИТЬ втПоказателиГрузов
		|ИЗ
		|	втГрузы КАК втГрузы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		|	ПО втГрузы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Масса), 0) КАК Масса,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Объем), 0) КАК Объем,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоМест), 0) КАК КоличествоМест,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоЗагрузочныхМетров), 0) КАК КоличествоЗагрузочныхМетров,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоГрузов), 0) КАК КоличествоГрузов,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.КоличествоГрузовыхМест), 0) КАК КоличествоГрузовыхМест,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.Сумма), 0) КАК Сумма,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.СуммаНДС), 0) КАК СуммаНДС,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.ОценочнаяСтоимость), 0) КАК ОценочнаяСтоимость,
		|	ЕСТЬNULL(СУММА(втПоказателиГрузов.СуммаИнкассацииПлан), 0) КАК СуммаИнкассацииПлан
		|ИЗ
		|	втПоказателиГрузов КАК втПоказателиГрузов
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(втПоказателиГрузовТ.ГрузовоеМестоВысота) КАК Высота,
		|	втПоказателиГрузовТ.ГрузовоеМесто КАК ГрузовоеМесто,
		|	СУММА(втПоказателиГрузовТ.ГрузовоеМестоКоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
		|	СУММА(втПоказателиГрузовТ.ГрузовоеМестоКоличествоМест) КАК КоличествоМест,
		|	СУММА(втПоказателиГрузовТ.ГрузовоеМестоМасса) КАК Масса,
		|	МАКСИМУМ(втПоказателиГрузовТ.ГрузовоеМестоОбъем) КАК Объем,
		|	МАКСИМУМ(втПоказателиГрузовТ.ГрузовоеМестоДлина) КАК Длина,
		|	МАКСИМУМ(втПоказателиГрузовТ.ГрузовоеМестоШирина) КАК Ширина
		|ИЗ
		|	втПоказателиГрузов КАК втПоказателиГрузовТ
		|ГДЕ &ФормироватьТаблицу
		|СГРУППИРОВАТЬ ПО
		|	втПоказателиГрузовТ.ГрузовоеМесто
		|";

		РезультатЗапроса = Запрос.ВыполнитьПакет();
		РезультатЗапросаПоказателиЗадания = РезультатЗапроса[2];
		Если Не РезультатЗапросаПоказателиЗадания.Пустой() Тогда
			Выборка = РезультатЗапросаПоказателиЗадания.Выбрать();
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(ЗаданиеНаПеревозкуГрузаОбъект, Выборка); 
		КонецЕсли; 
		
		Если ФормироватьТаблицу Тогда
			ЗаданиеНаПеревозкуГрузаОбъект.ДополнительныеСвойства.Вставить("ПараметрыНаПеревозку", РезультатЗапроса[3].Выгрузить()); 	
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
