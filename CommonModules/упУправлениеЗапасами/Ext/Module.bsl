
#Область ПрограммныйИнтерфейс

// Процедура расчета суммы списания материалов.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура РассчитатьСуммыСписанияМатериалов(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	упСЛК.СоздатьОбъект().РассчитатьСуммыСписанияМатериалов(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ);
		
КонецПроцедуры

// Формирует строки сторнирования по остаткам материалов.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура ДобавитьСторнированиеОстатковМатериалов(ДополнительныеСвойства, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Материалы", ДополнительныеСвойства.ОстаткиМатериалов);
	Запрос.УстановитьПараметр("Дата"     , Реквизиты.Граница);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Материалы.Склад,
	|	Материалы.Материал
	|ПОМЕСТИТЬ втМатериалы
	|ИЗ
	|	&Материалы КАК Материалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	упОстаткиМатериалов.Склад,
	|	упОстаткиМатериалов.Материал,
	|	упОстаткиМатериалов.Партия,
	|	-упОстаткиМатериалов.КоличествоОстаток КАК Количество,
	|	-упОстаткиМатериалов.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	-упОстаткиМатериалов.СуммаСНДСОстаток КАК СуммаСНДС
	|ИЗ
	|	РегистрНакопления.упОстаткиМатериалов.Остатки(
	|			&Дата,
	|			(Материал, Склад) В
	|				(ВЫБРАТЬ
	|					втМатериалы.Материал,
	|					втМатериалы.Склад
	|				ИЗ
	|					втМатериалы)) КАК упОстаткиМатериалов";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
		    ЗаполнитьЗначенияСвойств(ДополнительныеСвойства.ОстаткиМатериалов.Добавить(),  Выборка);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

// Контроль отрицательных остатков материалов.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Реквизиты.Граница);	
	Запрос.УстановитьПараметр("ДатаИсключаяДвижения", Реквизиты.ГраницаИсключая);	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМатериалы.Склад) КАК Склад,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМатериалы.Материал) КАК Материал,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМатериалы.Материал.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(- упОстаткиМатериалов.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(упОстаткиМатериаловДоПроведения.КоличествоОстаток, 0) КАК КоличествоОстатокДоПроведения
	|ИЗ
	|	втМатериалы КАК втМатериалы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиМатериалов.Остатки(
	|		&Дата,
	|		(Материал, Склад) В (
	|				ВЫБРАТЬ
	|				втМатериалы.Материал КАК Материал,
	|				втМатериалы.Склад КАК Склад
	|			ИЗ
	|				втМатериалы КАК втМатериалы)) КАК упОстаткиМатериалов
	|	ПО ИСТИНА
	|		И втМатериалы.Материал = упОстаткиМатериалов.Материал
	|		И втМатериалы.Склад = упОстаткиМатериалов.Склад
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиМатериалов.Остатки(
	|		&ДатаИсключаяДвижения,
	|		(Материал, Склад) В (
	|				ВЫБРАТЬ
	|				втМатериалы.Материал КАК Материал,
	|				втМатериалы.Склад КАК Склад
	|			ИЗ
	|				втМатериалы КАК втМатериалы)) КАК упОстаткиМатериаловДоПроведения
	|	ПО ИСТИНА
	|		И втМатериалы.Материал = упОстаткиМатериаловДоПроведения.Материал
	|		И втМатериалы.Склад = упОстаткиМатериаловДоПроведения.Склад
	|ГДЕ ЕСТЬNULL(упОстаткиМатериалов.КоличествоОстаток, 0) < 0
	|";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = НСтр(СтрШаблон("ru = 'Недостаточно остатков материала ""%1"". На складе: ""%2"" доступно для списания %3 %4'", Выборка.Материал, Выборка.Склад, Выборка.КоличествоОстатокДоПроведения, Выборка.ЕдиницаИзмерения));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЦикла; 
	КонецЕсли;  
	
КонецПроцедуры

#Область ФормированиеДвижений

// Процедура формирует записи регистра "упОстаткиМатериалов".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияОстаткиМатериалов = Движения.упОстаткиМатериалов;
	ДвиженияОстаткиМатериалов.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиМатериалов Цикл
			Движение = ДвиженияОстаткиМатериалов.Добавить();
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры	

// Процедура формирует записи регистра "упЦеныЗапчастей".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияЦеныЗапчастей(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияЦеныЗапчастей = Движения.упЦеныЗапчастей;
	ДвиженияЦеныЗапчастей.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ЦеныЗапчастей") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ЦеныЗапчастей Цикл
			Движение = ДвиженияЦеныЗапчастей.Добавить();
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры	

// Процедура формирует записи по регистру "упПрочиеРасходы".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРасходы		  = Движения.упПрочиеРасходы;
	ДвиженияРасходы.Записывать = Истина;
	
	ВедетсяВалютныйУчет	= ДополнительныеСвойства.ВедетсяВалютныйУчет; 
	тбзПрочиеРасходы 	= ДополнительныеСвойства.ПрочиеРасходы;
	
	ОрганизацияПоУмолчанию = Справочники.Организации.ПустаяСсылка();
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "Организация") Тогда
		ОрганизацияПоУмолчанию = Реквизиты.Организация;	
	КонецЕсли;
	
	ПодразделениеПоУмолчанию = Справочники.упСтруктураПредприятия.ПустаяСсылка();
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "Подразделение") Тогда
		ПодразделениеПоУмолчанию = Реквизиты.Подразделение;	
	КонецЕсли;

	НаправлениеДеятельностиПоУмолчанию = Справочники.упНаправленияДеятельности.ПустаяСсылка();
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "НаправлениеДеятельности") Тогда
		НаправлениеДеятельностиПоУмолчанию = Реквизиты.НаправлениеДеятельности;	
	КонецЕсли;

	АналитическийРазрезПоУмолчанию = Справочники.упАналитическиеРазрезы.ПустаяСсылка();
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "АналитическийРазрез") Тогда
		АналитическийРазрезПоУмолчанию = Реквизиты.АналитическийРазрез;	
	КонецЕсли;

	СтатьяРасходовПоУмолчанию = Справочники.упСтатьиДоходовРасходов.ПустаяСсылка();
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, "СтатьяРасходов") Тогда
		СтатьяРасходовПоУмолчанию = Реквизиты.СтатьяРасходов;	
	КонецЕсли;
	
	
	Если НЕ тбзПрочиеРасходы = Неопределено Тогда
		Для каждого СтрокаРасход Из тбзПрочиеРасходы Цикл
			ДвижениеРасход = ДвиженияРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеРасход, СтрокаРасход);
			
			Если НЕ ЗначениеЗаполнено(ДвижениеРасход.Период) Тогда
				ДвижениеРасход.Период      = Реквизиты.Дата;
			КонецЕсли;
			
			ДвижениеРасход.Регистратор = Ссылка;
						
			Если Истина
				И НЕ ЗначениеЗаполнено(ДвижениеРасход.Организация)
				И ЗначениеЗаполнено(ОрганизацияПоУмолчанию)
			Тогда
				ДвижениеРасход.Организация = ОрганизацияПоУмолчанию;
			КонецЕсли;
			
			Если Истина
				И НЕ ЗначениеЗаполнено(ДвижениеРасход.Подразделение)
				И ЗначениеЗаполнено(ПодразделениеПоУмолчанию) 
			Тогда
				ДвижениеРасход.Подразделение = ПодразделениеПоУмолчанию;
			КонецЕсли;
			
			Если Истина
				И НЕ ЗначениеЗаполнено(ДвижениеРасход.НаправлениеДеятельности)
				И ЗначениеЗаполнено(НаправлениеДеятельностиПоУмолчанию)
			Тогда
				ДвижениеРасход.НаправлениеДеятельности = НаправлениеДеятельностиПоУмолчанию;
			КонецЕсли;
			
			Если Истина
				И НЕ ЗначениеЗаполнено(ДвижениеРасход.АналитическийРазрез)
				И ЗначениеЗаполнено(АналитическийРазрезПоУмолчанию)
			Тогда
				ДвижениеРасход.АналитическийРазрез = АналитическийРазрезПоУмолчанию;
			КонецЕсли;
			
			Если Истина
				И НЕ ЗначениеЗаполнено(ДвижениеРасход.СтатьяРасходов)
				И ЗначениеЗаполнено(СтатьяРасходовПоУмолчанию)
			Тогда
				ДвижениеРасход.СтатьяРасходов = СтатьяРасходовПоУмолчанию;
			КонецЕсли;
			
			Если ВедетсяВалютныйУчет Тогда
				ДвижениеРасход.Валюта            = СтрокаРасход.Валюта;
				ДвижениеРасход.СуммаСНДС         = СтрокаРасход.Сумма;
				ДвижениеРасход.СуммаБезНДС       = СтрокаРасход.СуммаБезНДС;
				ДвижениеРасход.СуммаВалютаСНДС   = СтрокаРасход.СуммаВалюта;
				ДвижениеРасход.СуммаВалютаБезНДС = СтрокаРасход.СуммаБезНДСВалюта;
			Иначе
				ДвижениеРасход.Валюта            = Справочники.Валюты.ПустаяСсылка();
				ДвижениеРасход.СуммаСНДС         = СтрокаРасход.Сумма;
				ДвижениеРасход.СуммаБезНДС       = СтрокаРасход.СуммаБезНДС;	
				ДвижениеРасход.СуммаВалютаСНДС   = 0;
				ДвижениеРасход.СуммаВалютаБезНДС = 0;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

// Процедура формирует записи по регистру "упПробегТС".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияНаработкаОборудования(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
		
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияНаработкаОборудования = Движения.упНаработкаОборудования;
	ДвиженияНаработкаОборудования.Записывать = Истина;
	
	ТаблицаДвижений = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("ДополнительноеОборудованиеПробег", ТаблицаДвижений) Тогда 
		Для каждого Строка Из ТаблицаДвижений Цикл
			Если Ложь
				Или ЗначениеЗаполнено(Строка.Пробег) 
				Или ЗначениеЗаполнено(Строка.Моточасы)
			Тогда
				Движение = ДвиженияНаработкаОборудования.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период 	 = Реквизиты.Дата;
				Движение.ВидДвиженияПробег = Перечисления.упВидДвиженияПробег.ВводНачальныхОстатков;
				ЗаполнитьЗначенияСвойств(Движение, Строка); 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область УчетШинУзловИАгрегатов

// Проверка табличной части на дубли.
// 
// Параметры:
//	ТабличнаяЧасть - ТабличнаяЧасть - Табличная Часть.
//	ИмяТаблЧасти - Строка - имя табличной части.
//	Ссылка - ДокументСсылка - ссылка на документ.
//	Отказ - Булево - истина, если найдены дубли. Ложь если дублей нет.
//
Процедура ТабличнаяЧастьНеСодержитДублей(ТабличнаяЧасть, ИмяТаблЧасти, Ссылка, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
			
	Если Лев(ИмяТаблЧасти, СтрДлина("ДополнительноеОборудование")) = "ДополнительноеОборудование" Тогда
		ТекстСообщения = НСтр("ru = 'В табличной части ""%1"" присутствуют строки с продублированным доп. оборудованием. %2.'");	
		ИмяПоля						= "ДополнительноеОборудование";
		ИмяТабличнойЧастиДляПользователя	= Нстр("ru = 'Дополнительное оборудование'");
	Иначе
		ТекстСообщения	= НСтр("ru = 'В табличной части ""%1"" присутствуют строки с продублированными агрегатами. %2.'");	
		ИмяПоля		= "Агрегат";
		ИмяТабличнойЧастиДляПользователя	= Нстр("ru = 'Агрегаты'");
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда
		Если ИмяТаблЧасти = "ШиныУзлыАгрегатыСобственные" Тогда
			ИмяТабличнойЧастиДляПользователя = Нстр("ru = 'Шины, узлы, агрегаты собственные'");	
		ИначеЕсли ИмяТаблЧасти = "ШиныУзлыАгрегаты" Тогда
			ИмяТабличнойЧастиДляПользователя = Нстр("ru = 'Шины, узлы, агрегаты'");	
		ИначеЕсли ИмяТаблЧасти = "ДополнительноеОборудованиеСобственные" Тогда
			ИмяТабличнойЧастиДляПользователя = Нстр("ru = 'Дополнительное оборудование собственное'");	
		КонецЕсли;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упИзменениеСостоянийОборудования") Тогда
		ИмяПоля	= "Оборудование";
		ИмяТабличнойЧастиДляПользователя	= Нстр("ru = 'Оборудование'");
	КонецЕсли;
	
	ТекстСообщения = СтрШаблон(ТекстСообщения, ИмяТабличнойЧастиДляПользователя, Ссылка);
	
	Для каждого тбчСтрока Из ТабличнаяЧасть Цикл
		ТекущийИндекс = ТабличнаяЧасть.Индекс(тбчСтрока);
		Отбор = Новый Структура(ИмяПоля,тбчСтрока[ИмяПоля]);	  
		НайденныеСтроки = ТабличнаяЧасть.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 1 Тогда
			Для каждого текСтрока Из НайденныеСтроки Цикл
				ИндексСтроки = ТабличнаяЧасть.Индекс(текСтрока);
				Если НЕ ТекущийИндекс = ИндексСтроки Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,"Объект." + ИмяТаблЧасти + "[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "]." + ИмяПоля);
				КонецЕсли;
			КонецЦикла;		
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;  
	
КонецПроцедуры // КонтрольТабличнойЧастиАгрегаты()

// Процедура формирует записи регистра "упАгрегатыНаСкладах".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияАгрегатыНаСкладах = Движения.упАгрегатыНаСкладах;
	ДвиженияАгрегатыНаСкладах.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиАгрегатов") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиАгрегатов Цикл
			Движение = ДвиженияАгрегатыНаСкладах.Добавить();
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияАгрегатов()

// Процедура формирует записи регистра "упАгрегатыТранспортныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияАгрегатовНаТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияАгрегатыТС = Движения.упАгрегатыТранспортныхСредств;
	ДвиженияАгрегатыТС.Записывать = Истина;
	
	Если ДополнительныеСвойства.Свойство("АгрегатыНаТС") Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.АгрегатыНаТС Цикл
			Движение = ДвиженияАгрегатыТС.Добавить();
			Движение.Период = Реквизиты.Дата;
			ЗаполнитьЗначенияСвойств(Движение, текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // СформироватьДвиженияАгрегатовНаТС()

// Функция - Агрегаты по транспортному средству.
//
// Параметры:
//  Период			 - Дата - дата запроса.
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства - транспортное средство.
// 
// Возвращаемое значение:
//  Массив - агрегаты (СправочникСсылка.упШиныУзлыИАгрегаты) установленные на ТС.
//
Функция АгрегатыПоТранспортномуСредству(Период, ТранспортноеСредство) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =	"ВЫБРАТЬ
	|	упАгрегатыТранспортныхСредствСрезПоследних.Агрегат КАК Агрегат
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(&Период, ) КАК упАгрегатыТранспортныхСредствСрезПоследних
	|ГДЕ
	|	упАгрегатыТранспортныхСредствСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И упАгрегатыТранспортныхСредствСрезПоследних.ТранспортноеСредство = &ТранспортноеСредство
	|	И упАгрегатыТранспортныхСредствСрезПоследних.Агрегат.Ссылка ССЫЛКА Справочник.упШиныУзлыИАгрегаты";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл                                                   
		Результат.Добавить(Выборка.Агрегат);			
	КонецЦикла; 
	
	Возврат Результат;
		
КонецФункции

// Функция - На какое ТС установлен агрегат или доп. оборудование.
//
// Параметры:
//  Период	 - Дата - дата запроса.
//  Агрегат	 - СправочникСсылка.упДополнительноеОборудование - оборудование или агрегат.
//			 - СправочникСсылка.упШиныУзлыИАгрегаты             
// 
// Возвращаемое значение:
//  СправочникСсылка.упТранспортныеСредства - транспортное средство.
//
Функция ТранспортноеСредстваПоАгрегату(Период, Агрегат) Экспорт
	
	Результат = Справочники.упТранспортныеСредства.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =	"ВЫБРАТЬ
	|	упАгрегатыТранспортныхСредствСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(&Период, Агрегат = &Агрегат) КАК упАгрегатыТранспортныхСредствСрезПоследних
	|ГДЕ
	|	упАгрегатыТранспортныхСредствСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)";
	Запрос.УстановитьПараметр("Агрегат", Агрегат);
	Запрос.УстановитьПараметр("Период",  Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ТранспортноеСредство;			
	КонецЕсли; 
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыФункции

// Нет.

#КонецОбласти
