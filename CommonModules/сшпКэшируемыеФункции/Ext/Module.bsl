
#Область ПрограммныйИнтерфейс

// Функция - Получить HTTPЗапрос
//
// Параметры:
//  сервис	 - Строка - адрес web-сервиса.
//  точка	 - Строка - имя точки web-сервиса.
//  формат	 - Перечисление.сшпФорматыСообщений - формат сообщения. 
// 
// Возвращаемое значение:
// HTTPЗапрос  - объект типа HTTPЗапрос.
//
Функция ПолучитьHTTPЗапрос(сервис, точка, формат) Экспорт
	httpЗапрос = Новый HTTPЗапрос;
	httpЗапрос.АдресРесурса = сервис + точка;
	// +++.17/08/22-10:45:00.<УП>
	//httpЗапрос.Заголовки.Вставить("Content-type", ПолучитьОписаниеТипаСообщения(формат));
	httpЗапрос.Заголовки.Вставить("Content-Type", ПолучитьОписаниеТипаСообщения(формат));
	// ---.17/08/22-10:45:00.<УП>
	Возврат httpЗапрос; 
КонецФункции	

// Функция - Получить тип сообщения
//
// Параметры:
//  ОписаниеТипа - Строка - строка с описанием формата сообщения.
// 
// Возвращаемое значение:
//  Перечисления.сшпФорматыСообщений - формат сообщения по указанному описанию.
//
Функция ПолучитьТипСообщения(ОписаниеТипа) Экспорт
	текВозврат = Неопределено;
	Если ОписаниеТипа = "application/json;charset=utf-8" тогда
		текВозврат = Перечисления.сшпФорматыСообщений.JSON;		
	ИначеЕсли ОписаниеТипа = "application/xml;charset=utf-8" тогда
		текВозврат = Перечисления.сшпФорматыСообщений.XML;
	ИначеЕсли ОписаниеТипа = "raw" тогда
		текВозврат = Перечисления.сшпФорматыСообщений.FastInfoset;
	КонецЕсли;	
	
	Возврат текВозврат;
КонецФункции	

// Функция - Получить описание типа сообщения
//
// Параметры:
//  ФорматСообщений	 - Перечисления.сшпФорматыСообщений - формат сообщения для которого необходимо получить строковое описание.
// 
// Возвращаемое значение:
//  Строка - описание формата сообщений.
//
Функция ПолучитьОписаниеТипаСообщения(ФорматСообщений) Экспорт
	текВозврат = Неопределено;
	Если ФорматСообщений = Перечисления.сшпФорматыСообщений.JSON тогда
		// +++.17/08/22-10:45:00.<УП>
		// Добавил условие на проверку обмена Web
		////текВозврат = "application/json;charset=utf-8";		
		//текВозврат = "text/json";		
		Если сшпФункциональныеОпции.ТипОбмена() = Перечисления.упТипОбмена.Web Тогда 
			текВозврат = "application/json;charset=utf-8";		
		Иначе
			текВозврат = "text/json";		
		КонецЕсли;	
		// ---.17/08/22-10:45:00.<УП>
	ИначеЕсли ФорматСообщений = Перечисления.сшпФорматыСообщений.XML тогда
		// +++.17/08/22-10:45:00.<УП>
		////текВозврат = "application/xml;charset=utf-8";
		//текВозврат = "text/xml";		
		Если сшпФункциональныеОпции.ТипОбмена() = Перечисления.упТипОбмена.Web Тогда 
			текВозврат = "application/xml;charset=utf-8";
		Иначе
			текВозврат = "text/xml";		
		КонецЕсли;	
		// ---.17/08/22-10:45:00.<УП>
	ИначеЕсли ФорматСообщений = Перечисления.сшпФорматыСообщений.FastInfoset тогда
		текВозврат = "raw";
	КонецЕсли;	
	
	Возврат текВозврат;
КонецФункции	

// Функция - Участвует в интеграции
//
// Параметры:
//  ТипОбъекта		 - Строка - тип объекта для проверки участия в интеграции. 
//  КлючАктуальности - Строка - ключ актуалного состояния списка объектов интеграции (для обновления кэшированного значения функции).
// 
// Возвращаемое значение:
//  Булево - Признак участия в исходящем потоке интеграции.
//
Функция УчаствуетВИнтеграции(ТипОбъекта, КлючАктуальности = Неопределено) Экспорт
	текРезультатПроверки = Ложь;
	Если сшпФункциональныеОпции.ИспользоватьСШП() тогда
		текЗапрос = Новый Запрос("ВЫБРАТЬ
		|	тбРепозиторий.ИмяКлассаОбъекта
		|ИЗ
		|	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСтатусыОбработчиков КАК сшпСтатусыОбработчиков
		|		ПО тбРепозиторий.ИдентификаторШаблона = сшпСтатусыОбработчиков.ИдентификаторОбработчика
		|			И (НЕ сшпСтатусыОбработчиков.Статус = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыОбработчиков.Отключен))
		|ГДЕ
		|	тбРепозиторий.ИмяКлассаОбъекта = &ИмяКлассаОбъекта
		|	И тбРепозиторий.ТипИнтеграции = ЗНАЧЕНИЕ(Перечисление.сшпТипыИнтеграции.Исходящая)");
		текЗапрос.Параметры.Вставить("ИмяКлассаОбъекта", ТипОбъекта);
		текРезультат = текЗапрос.Выполнить();
		текРезультатПроверки = НЕ текРезультат.Пустой();
	КонецЕсли;
	Возврат  текРезультатПроверки;
КонецФункции	

// Функция - Получить обработчик
//
// Параметры:
//  Типобъекта	 - Строка - описание имени объекта/класса.
//  Направление	 - Перечисление.сшпТипыИнтеграции - направление потока данных.
//  Ключ		 - Строка - ключ актуалного состояния списка объектов интеграции (для обновления кэшированного значения функции). 
// 
// Возвращаемое значение:
//  Строка - тело обработчика для указанного объекта/класса. 
//
Функция ПолучитьОбработчик(типобъекта, направление, ключ) Экспорт
	сткВозврат = Новый Структура("Наименование, ПроцедураОбработки, ИдентификаторШаблона, Версия, Статус");
	текЗапрос = Новый Запрос("ВЫБРАТЬ
	|	тбРепозиторий.Наименование КАК Наименование,
	|	тбРепозиторий.ПроцедураОбработки,
	|	тбРепозиторий.ИдентификаторШаблона,
	|	тбРепозиторий.Версия,
	|	тбСтатусы.Статус
	|ИЗ
	|	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСтатусыОбработчиков КАК тбСтатусы
	|		ПО тбРепозиторий.ИдентификаторШаблона = тбСтатусы.ИдентификаторОбработчика
	|ГДЕ
	|	тбРепозиторий.ИмяКлассаОбъекта = &ИмяКлассаОбъекта
	|	И тбРепозиторий.ТипИнтеграции = &ТипИнтеграции");
	
	текЗапрос.УстановитьПараметр("ИмяКлассаОбъекта", типобъекта);
	текЗапрос.УстановитьПараметр("ТипИнтеграции", направление);
	текРезультат = текЗапрос.Выполнить();
	Если НЕ текРезультат.Пустой() тогда
		текВыборка = текРезультат.Выбрать();
		текВыборка.Следующий();
		
		сткВозврат.Наименование 		= текВыборка.Наименование;
		сткВозврат.ПроцедураОбработки 	= текВыборка.ПроцедураОбработки;
		сткВозврат.ИдентификаторШаблона	= текВыборка.ИдентификаторШаблона;
		сткВозврат.Версия 				= текВыборка.Версия;
		сткВозврат.Статус 				= текВыборка.Статус;
	КонецЕсли;	
	Возврат Новый ФиксированнаяСтруктура(сткВозврат);
КонецФункции	

// Функция - Получить метод хранения
//
// Параметры:
//  ТипОбъекта		 - Строка - тип объекта для проверки участия в интеграции. 
//  КлючАктуальности - Строка - ключ актуалного состояния списка объектов интеграции (для обновления кэшированного значения функции)
// 
// Возвращаемое значение:
//  Перечисление.сшпМетодХранения - текущий метод хранения данных объекта события. 
//
Функция ПолучитьМетодХранения(типОбъекта, КлючАктуальности = Неопределено) Экспорт
	методХранения = Перечисления.сшпМетодХранения.Сериализация;
	текЗапрос = Новый Запрос("ВЫБРАТЬ
	|	тбРепозиторий.МетодХранения
	|ИЗ
	|	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
	|ГДЕ
	|	тбРепозиторий.ИмяКлассаОбъекта = &ИмяКлассаОбъекта
	|	И тбРепозиторий.ТипИнтеграции = ЗНАЧЕНИЕ(Перечисление.сшпТипыИнтеграции.Исходящая)");
	текЗапрос.Параметры.Вставить("ИмяКлассаОбъекта", типОбъекта);
	текРезультат = текЗапрос.Выполнить();
	Если НЕ текРезультат.Пустой() тогда
		выборкаМетод = текРезультат.Выбрать();
		выборкаМетод.Следующий();
		методХранения = выборкаМетод.МетодХранения;
	КонецЕсли;
	Возврат  методХранения;
КонецФункции

// Функция - Получить имя объекта
//
// Параметры:
//  типобъекта	 - Строка - возвращает имя объекта по переданному типу.
// 
// Возвращаемое значение:
//  Строка - имя объекта.
//
Функция ПолучитьИмяОбъекта(типобъекта) Экспорт
	имяОбъекта = Прав(типобъекта, СтрДлина(типобъекта) - Найти(типобъекта, ".")); // Для старых версий платформы.
	Возврат имяОбъекта;
КонецФункции	

// Функция - Получить хэш-сумму
//
// Параметры:
//  текст - текст, которых необходимо захэшировать.
//
// Возвращаемое значение:
//  Число - значение хэш-суммы.
//
Функция ПолучитьХэшСумму(текст) Экспорт
	хэшТекста = Новый ХешированиеДанных(ХешФункция.CRC32);
	хэшТекста.Добавить(текст);
	Возврат хэшТекста.ХешСумма;
КонецФункции	

// Функция - Получить список рабочих статусов
//
// Параметры:
//
// Возвращаемое значение:
//  Массив - массив, сождержащий элементы перечисления "Статусы сообщений".
//
Функция ПоучитьСписокРабочихСтатусов() Экспорт
	массивСтатусы = Новый Массив;
	массивСтатусы.Добавить(Перечисления.сшпСтатусыСообщений.Новое);
	массивСтатусы.Добавить(Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки);
	Возврат массивСтатусы;
КонецФункции	

// Функция - Это системное сообщение
//
// Параметры:
//  типсообщения - Строка - тип сообщения. 
// 
// Возвращаемое значение:
//  Булево - признак, является ли сообщение данного типа системным.
//
функция ЭтоСистемноеСообщение(типсообщения) Экспорт
	результатВозврат = Ложь;
	Если ТипСообщения = "TRM"		// Команда на удаление обработчика события
		ИЛИ ТипСообщения = "TUM" 	// Сообщение для обновления обработчиков событий и состава зарегистрированных объектов.
		ИЛИ ТипСообщения = "TLR" 	// Запрос на получение списка версий обработчиков
		ИЛИ ТипСообщения = "TSR" 	// Запрос на получение списка обработчиков
		ИЛИ ТипСообщения = "TCS" 	// Команда на изменение состояния обработчика
		ИЛИ ТипСообщения = "RML" 	// Команда на возврат в обработку пакетов в состоянии "Ошибка обработки".
		ИЛИ ТипСообщения = "BMR" 	// Запрос структуры конфигурации
		ИЛИ ТипСообщения = "V1C" 	// Запрос версии подсистемы 1С
		ИЛИ ТипСообщения = "CSB" 	// Запрос строки подключения
		ИЛИ ТипСообщения = "DEB" 	// debug
		ИЛИ ТипСообщения = "GPS" 	// Запрос состояния обработки get processing status
		ИЛИ ТипСообщения = "FND" 	// Поиск сообщений
	тогда       
		результатВозврат = Истина;
	КонецЕсли;
	Возврат результатВозврат;
КонецФункции

// Функция - Это транспортное сообщение
//
// Параметры:
//  типсообщения - Строка - тип сообщения.
// 
// Возвращаемое значение:
//  Булево - признак, является ли сообщение данного типа транспортным.
//
Функция ЭтоТранспортноеСообщение(типсообщения) Экспорт
	результатВозврат = Ложь;
	Если ТипСообщения = "DTP"		// Сообщение передачи данных.
	тогда
		результатВозврат = Истина;
	КонецЕсли;
	Возврат результатВозврат;
КонецФункции

// Функция - Это командное сообщение
//
// Параметры:
//  типсообщения - Строка - тип сообщения. 
// 
// Возвращаемое значение:
//  Булево - признак, является ли сообщение данного типа командным.
//
функция ЭтоКомандноеСообщение(типсообщения) Экспорт
	результатВозврат = Ложь;
	Если ТипСообщения = "GCM" 		// Команда на выполнение произвольного кода
		ИЛИ ТипСообщения = "CSM" 	// Команда на запуск пула обработчиков
		ИЛИ ТипСообщения = "CSA" 	// Команда на изменение параметров подключения к адаптеру
		ИЛИ ТипСообщения = "SUS" 	// Команда на установку настроек пользователя сервиса
	тогда           
		результатВозврат = Истина;
	КонецЕсли;
	Возврат результатВозврат;
КонецФункции

// Функция - Получить значение атрибута типа
//
// Параметры:
//  типсообщения - входящий тип для определния атрибута.
// 
// Возвращаемое значение:
// Строка  -  атрибут, соответствующий типу.
//
Функция ПолучитьЗначениеАтрибутаТипа(типзначения) Экспорт
	возвратСтрока = Неопределено;
	Если типзначения = Тип("Булево") тогда
		возвратСтрока = "xs:boolean";
	ИначеЕсли типзначения = Тип("Число") тогда
		возвратСтрока = "xs:int";
	ИначеЕсли типзначения = Тип("Строка") тогда
		возвратСтрока = "xs:String";
	КонецЕсли;	
	Возврат возвратСтрока;
КонецФункции

// Функция - Получить структуру объекта
//
// Параметры:
//  ссылка - ссылка на объект, структуру которого следует получить.
//
Функция ПолучитьСтруктуруОбъекта(ссылка) Экспорт
КонецФункции	

// Функция - Версия подсистемы
//
// Возвращаемое значение:
//  Строка - номер текущей версии подсистемы ESB 
//
Функция ВерсияПодсистемы() Экспорт 
	текущаяВерсия = Константы.сшпВерсияПодсистемы.Получить();
	Если текущаяВерсия = 0 Тогда 
		текущаяВерсия = 10000;
	КонецЕсли;

	Возврат текущаяВерсия;
КонецФункции

функция ДлительностьОжиданияПриАвтоматическомСтарте() Экспорт 
	Возврат 15;
КонецФункции

#КонецОбласти
