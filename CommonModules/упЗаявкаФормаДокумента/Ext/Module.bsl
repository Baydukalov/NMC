#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

// Процедура - При создании на сервере форма документа
//
// Параметры:
//  ЭтотОбъект			 - Форма	 - форма заявки.
//  Отказ				 - Булево	 - Истина, в случае ошибки.
//  СтандартнаяОбработка	 - Булево	 - признак стандартной обработки.
//
Процедура ПриСозданииНаСервереФормаДокумента(ЭтотОбъект, Отказ, СтандартнаяОбработка) Экспорт
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ЭтотОбъект.ИспользуетсяУчетПодразделений     = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтотОбъект.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	
КонецПроцедуры

// Процедура - При создании при чтении на сервере инициализация нового объекта форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  Параметры	 - Структура							 - параметры открытия формы.
//
Процедура ПриСозданииПриЧтенииНаСервереИнициализацияНовогоОбъектаФормаДокумента(ЭтаФорма, Объект, Параметры) Экспорт
	
	ЭтаФорма.Статус = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка);
	
	ЭтоНовыйОбъект  = Не ЗначениеЗаполнено(Объект.Ссылка);
		
	Если ЭтоНовыйОбъект Тогда
		
		ЭтоКопирование  = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
		
		Объект.Автор                   = ПользователиКлиентСервер.ТекущийПользователь();
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		
		Если НЕ ЭтоКопирование Тогда
			
			Если Параметры.Свойство("ПорядокИсполненияЗаявки") Тогда
				Объект.ПорядокИсполненияЗаявки = Параметры.ПорядокИсполненияЗаявки;
			КонецЕсли;
			
			Если Параметры.Свойство("ВидТранспортнойУслуги") Тогда
				Объект.ВидТранспортнойУслуги = Параметры.ВидТранспортнойУслуги;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
			КонецЕсли;
						
			Если Истина
				И ЗначениеЗаполнено(Объект.ВидПеревозки)
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки, "ВидТранспортнойУслуги") <> Объект.ВидТранспортнойУслуги 
			Тогда
				Объект.ВидПеревозки = Справочники.упВидыПеревозок.ПустаяСсылка();	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
				Объект.ВидПеревозки = Справочники.упВидыПеревозок.ЗначениеПоУмолчанию(Объект.ВидПеревозки, Объект.ВидТранспортнойУслуги, Объект.ПорядокИсполненияЗаявки);
			КонецЕсли;
			
			Если Истина
				И ЗначениеЗаполнено(Объект.ВидПеревозки)
				И Объект.ПорядокИсполненияЗаявки = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом
			Тогда
				Объект.КоличествоРабочихСмен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки, "КоличествоРабочихСмен");
			КонецЕсли;

			
		КонецЕсли;
						
		//упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(Объект);
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки, Объект.Автор);
		КонецЕсли;
		
		Если ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
			
			ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
			
			// Если при копировании отличаются часовые пояса источника и нового документа.
			Если Истина
				И ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
				И ЗначениеЗаполнено(Объект.ЧасовойПояс)
				И ЗначениеЗаполнено(ЧасовойПоясВидаПеревозки)
				И НЕ Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки
			Тогда
				Если ЗначениеЗаполнено(Объект.ОтправлениеГрузаС) Тогда
					Объект.ОтправлениеГрузаС		= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ОтправлениеГрузаС, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 	
				КонецЕсли;
				Если ЗначениеЗаполнено(Объект.ОтправлениеГрузаПо) Тогда
					Объект.ОтправлениеГрузаПо	= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ОтправлениеГрузаПо, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;
				Если ЗначениеЗаполнено(Объект.ПолучениеГрузаС) Тогда
					Объект.ПолучениеГрузаС		= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ПолучениеГрузаС, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;	
				Если ЗначениеЗаполнено(Объект.ПолучениеГрузаПо) Тогда
					Объект.ПолучениеГрузаПо		= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ПолучениеГрузаПо, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;	
			КонецЕсли;	
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;		
		КонецЕсли;
						
	КонецЕсли;
	
	ЭтаФорма.НастроитьФормуПоВидуПеревозки();
		
КонецПроцедуры

// Процедура - При создании при чтении на сервере инициализация реквизитов формы форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  Параметры	 - Структура							 - параметры открытия формы.
//
Процедура ПриСозданииПриЧтенииНаСервереИнициализацияРеквизитовФормыФормаДокумента(ЭтаФорма, Объект, Параметры) Экспорт
		
	ЭтаФорма.ЗаблокироватьЭлементы();
	
	// Валютный учет
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
		ЭтаФорма.ВалютаУпрУчета  = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
		Объект.Валюта            = ЭтаФорма.ВалютаУпрУчета;
	КонецЕсли;
	
	Взаимодействия.ПодготовитьОповещения(ЭтаФорма, Параметры, Ложь);
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Взаиморасчеты
	ЭтаФорма.РазрешеноЗагружатьВзаиморасчеты = ПолучитьФункциональнуюОпцию("упЗагружатьВзаиморасчетыСПартнерами");
	ЭтаФорма.ВзаиморасчетыЗагружаются        = Ложь;
	ЭтаФорма.ВзаиморасчетыЗагрузить          = Ложь;
	ЭтаФорма.Элементы.ГруппаСостояниеВзаиморасчетов.Видимость = ЭтаФорма.РазрешеноЗагружатьВзаиморасчеты;
	
	УказываютсяФактическиОказанныеУслуги = ПолучитьФункциональнуюОпцию("упУказываютсяФактическиОказанныеУслуги");
	
	ЭтаФорма.Элементы.ГруппаПлановыеДоходы.Видимость = УказываютсяФактическиОказанныеУслуги;
	ЭтаФорма.Элементы.ГруппаСумма.Видимость = УказываютсяФактическиОказанныеУслуги;
	
	// Основание.
	Если ЗначениеЗаполнено(Объект.ДокументОснование)Тогда
		ЭтаФорма.Элементы.ДокументОснования.Видимость = Истина;
		ЭтаФорма.Элементы.ДокументОснования.ТолькоПросмотр = Истина;
	Иначе
		ЭтаФорма.Элементы.ДокументОснования.Видимость = Ложь;
	КонецЕсли;
		
	упРейс.УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

// Процедура - Обработка проверки заполнения на сервере форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  Отказ				 - Булево	 - Истина, в случае ошибки.
//  ПроверяемыеРеквизиты	 - Массив	 - проверяемые реквизиты.
//
Процедура ОбработкаПроверкиЗаполненияНаСервереФормаДокумента(ЭтаФорма, Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	упЗаданиеНаПеревозку.ПроконтролироватьИнтервалОтправкиПолучения(Объект.ОтправлениеГрузаС, Объект.ОтправлениеГрузаПо, Объект.ПолучениеГрузаС, Объект.ПолучениеГрузаПо, Отказ);	
	
КонецПроцедуры

// Процедура - Перед записью на сервере форма документа
//
// Параметры:
//  ЭтаФорма		 - Форма							 - форма.
//  Отказ			 - Булево							 - Истина, в случае ошибки.
//  ТекущийОбъект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  ПараметрыЗаписи	 - Структура							 - параметры записи.
//
Процедура ПередЗаписьюНаСервереФормаДокумента(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
	Если ЗначениеЗаполнено(ЭтаФорма.НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", ЭтаФорма.НовыйСтатус);
	КонецЕсли;	
	
	ЭтаФорма.НовыйСтатус = Неопределено;
		
КонецПроцедуры

// Процедура - После записи на сервере форма документа
//
// Параметры:
//  ЭтаФорма		 - Форма							 - форма.
//  Отказ			 - Булево							 - Истина, в случае ошибки.
//  ТекущийОбъект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  ПараметрыЗаписи	 - Структура							 - параметры записи.
//
Процедура ПослеЗаписиНаСервереФормаДокумента(ЭтаФорма, Объект, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
	ЭтаФорма.Статус = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка);
	ЭтаФорма.ЗаблокироватьЭлементы();
	
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		сткРеквизиты = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, "ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза, ИспользуютсяМультимодальныеПеревозки, ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки");
		ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза                     = сткРеквизиты.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза;
		ИспользуютсяМультимодальныеПеревозки                            = сткРеквизиты.ИспользуютсяМультимодальныеПеревозки;
		ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки = сткРеквизиты.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки;
	Иначе
		ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = Ложь;
		ИспользуютсяМультимодальныеПеревозки = Ложь;
		ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки = Ложь;
	КонецЕсли;
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиНаПеревозкуГруза(Объект.Ссылка, ЭтаФорма.Статус, ЭтаФорма.Элементы, ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза, (ИспользуютсяМультимодальныеПеревозки И ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки));
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	упРейс.УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Вид перевозки при изменении на сервере
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//
Процедура ВидПеревозкиПриИзмененииНаСервере(ЭтаФорма, Объект) Экспорт
	
	ЭтаФорма.НастроитьФормуПоВидуПеревозки();
	
	Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки);
		Если Истина
			И ЗначениеЗаполнено(Подразделение)
			И НЕ Подразделение = Объект.Подразделение 
		Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли; 
	
	Если ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
		Если ЧасовойПоясВидаПеревозки <> Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;
		КонецЕсли;
	КонецЕсли;
	
	упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(Объект);
	
КонецПроцедуры

// Процедура - Заполнить типовыми статьями доходов на сервере
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//
Процедура ЗаполнитьТиповымиСтатьямиДоходовНаСервере(ЭтаФорма, Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	                  |	тбВидыПеревозокСписокТиповыхСтатейДоходов.СтатьяДоходов КАК СтатьяДоходов,
	                  |	1 КАК Количество
	                  |ПОМЕСТИТЬ втСтатьиДоходов
	                  |ИЗ
	                  |	Справочник.упВидыПеревозок.СписокСтатейДоходов КАК тбВидыПеревозокСписокТиповыхСтатейДоходов
	                  |ГДЕ
	                  |	тбВидыПеревозокСписокТиповыхСтатейДоходов.Ссылка = &ВидПеревозки
	                  |	И НЕ тбВидыПеревозокСписокТиповыхСтатейДоходов.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	                  |
	                  |ОБЪЕДИНИТЬ
	                  |
	                  |ВЫБРАТЬ
	                  |	упСоглашенияСКлиентамиСтатьиДоходов.СтатьяДоходов,
	                  |	1
	                  |ИЗ
	                  |	Справочник.упСоглашенияСКлиентами.СтатьиДоходов КАК упСоглашенияСКлиентамиСтатьиДоходов
	                  |ГДЕ
	                  |	упСоглашенияСКлиентамиСтатьиДоходов.Ссылка = &Соглашение
	                  |	И НЕ упСоглашенияСКлиентамиСтатьиДоходов.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	                  |
	                  |ОБЪЕДИНИТЬ ВСЕ
	                  |
	                  |ВЫБРАТЬ
	                  |	упЗаявкаНаПеревозкуГрузаДополнительныеОперации.ДополнительнаяОперация.СтатьяДоходов,
	                  |	упЗаявкаНаПеревозкуГрузаДополнительныеОперации.Количество
	                  |ИЗ
	                  |	Документ.упЗаявкаНаПеревозкуГруза.ДополнительныеОперации КАК упЗаявкаНаПеревозкуГрузаДополнительныеОперации
	                  |ГДЕ
	                  |	упЗаявкаНаПеревозкуГрузаДополнительныеОперации.Ссылка = &Ссылка
	                  |	И НЕ упЗаявкаНаПеревозкуГрузаДополнительныеОперации.ДополнительнаяОперация.СтатьяДоходов = ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	втСтатьиДоходов.СтатьяДоходов,
	                  |	СУММА(втСтатьиДоходов.Количество) КАК Количество,
	                  |	упСтатьиДоходовРасходов.СтавкаНДС
	                  |ИЗ
	                  |	втСтатьиДоходов КАК втСтатьиДоходов
	                  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	                  |		ПО втСтатьиДоходов.СтатьяДоходов = упСтатьиДоходовРасходов.Ссылка
	                  |
	                  |СГРУППИРОВАТЬ ПО
	                  |	втСтатьиДоходов.СтатьяДоходов,
	                  |	упСтатьиДоходовРасходов.СтавкаНДС";
	Запрос.УстановитьПараметр("ВидПеревозки", Объект.ВидПеревозки);
	Запрос.УстановитьПараметр("Соглашение",   Объект.Соглашение);
	Запрос.УстановитьПараметр("Ссылка",       Объект.Ссылка);
	
	Объект.ПлановыеДоходы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// Функция - Изменить статус заявки
//
// Параметры:
//  ЭтаФорма		 - Форма									 - форма.
//  Объект		 - ДокументОбъект.упЗаявкаНаПеревозкуГруза		 - объект заявки.
//  СтатусЗаявки	 - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза	 - статус заявки
// 
// Возвращаемое значение:
//  Булево - Истина, если успешно.
//
Функция ИзменитьСтатусЗаявки(ЭтаФорма, Объект, СтатусЗаявки) Экспорт
	
	ЭтаФорма.НовыйСтатус = СтатусЗаявки;
	
	Если ЭтаФорма.ПроверитьЗаполнение() Тогда 
		Попытка
			ЭтаФорма.Записать();		
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Ошибка при проведении заявки на перевозку груза", УровеньЖурналаРегистрации.Ошибка, ,Объект.Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
        	Возврат Ложь;    
		КонецПопытки;
	
		Возврат Истина;
		
	КонецЕсли;
	
	ЭтаФорма.НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

// Процедура - Изменился статус
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//
Процедура ИзменилсяСтатус(ЭтаФорма, Объект) Экспорт
	
	ЭтаФорма.Статус = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка);
	ЭтаФорма.ЗаблокироватьЭлементы();
	
	сткРеквизиты = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, "ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза, ИспользуютсяМультимодальныеПеревозки, ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки");
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиНаПеревозкуГруза(Объект.Ссылка, ЭтаФорма.Статус, ЭтаФорма.Элементы,
			сткРеквизиты.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза,
			(сткРеквизиты.ИспользуютсяМультимодальныеПеревозки И сткРеквизиты.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки));			
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти