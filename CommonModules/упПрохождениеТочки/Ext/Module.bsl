#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Процедура формирует записи по регистру "упСостояниеПоЗаданиям", "упПланПоЗаданиям".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на объект.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты объект.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияСостояниеПоЗаданиямИПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	// Формируем движения по состояниям по заданиям.
	ПараметрыПеревозкиРейса = упРейс.ПараметрыПеревозкиРейса(Реквизиты.Рейс);
	ТранспортноеСредство = ПараметрыПеревозкиРейса.ТранспортноеСредство;
	
	ДвиженияСостояниеПоЗаданиям = Движения.упСостояниеПоЗаданиям;
	ДвиженияСостояниеПоЗаданиям.Записывать = Истина;
	
	ДвиженияПланПоЗаданиям = Движения.упПланПоЗаданиям;
	ДвиженияПланПоЗаданиям.Записывать = Истина;
	
	СвернутыеОперации = Реквизиты.Работы.Выгрузить();
	СвернутыеОперации.Свернуть("Задание, Операция, ГрузовоеМесто, Проблема, АдресРазгрузки", "КоличествоГрузов");
	Для каждого СтрокаРаботы Из СвернутыеОперации Цикл 
		Если НЕ упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(СтрокаРаботы.Операция) Тогда 
			Продолжить;
		КонецЕсли; 
		
		Если Ложь Тогда // Для контекстной подсказки.
			Ссылка = Документы.упПрохождениеТочки.ПустаяСсылка();
			Реквизиты = Ссылка;
			ДвиженияСостояния = РегистрыНакопления.упСостояниеПоЗаданиям.СоздатьНаборЗаписей();
			Проблема = Справочники.упПроблемы.ПустаяСсылка();
			ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
		КонецЕсли;
		
		ЭтоРазгрузка = упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(СтрокаРаботы.Операция);
		ЭтоПогрузка = упПрохождениеТочкиКлиентСервер.ОперацияПогрузки(СтрокаРаботы.Операция);
		
		Если упПрохождениеТочкиКлиентСервер.ДействиеОтменяющееЗадание(СтрокаРаботы.Проблема.Действие) Тогда
			
			СрезПоследних = РегистрыСведений.упПланПоЗаданиям.СрезПоследних(, Новый Структура("Задание, ГрузовоеМесто", СтрокаРаботы.Задание, СтрокаРаботы.ГрузовоеМесто));
			
			Если ЭтоПогрузка Тогда
				
				// Отменяется выезд из точки погрузки.
				ДвижениеСостояния	= ДвиженияСостояниеПоЗаданиям.Добавить();
				ДвижениеСостояния.Задание = СтрокаРаботы.Задание;
				ДвижениеСостояния.Местоположение = Реквизиты.АдресТочки;
				ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.Количество = СтрокаРаботы.КоличествоГрузов;
				ДвижениеСостояния.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
								
				// Отменяется плановое прибытие в точку разгрузки по заданию.
				ДвижениеСостояния	= ДвиженияСостояниеПоЗаданиям.Добавить();
				ДвижениеСостояния.Задание = СтрокаРаботы.Задание;
				ДвижениеСостояния.Местоположение = СрезПоследних[0].Местоположение;
				ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.Количество = СтрокаРаботы.КоличествоГрузов;
				ДвижениеСостояния.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
				
			ИначеЕсли ЭтоРазгрузка Тогда	
				
				// Отменяется плановое прибытие в точку разгрузки по заданию.
				ДвижениеСостояния	= Движения.упСостояниеПоЗаданиям.Добавить();
				ДвижениеСостояния.Задание = СтрокаРаботы.Задание;
				ДвижениеСостояния.Местоположение = Реквизиты.АдресТочки;
				ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.Количество = СтрокаРаботы.КоличествоГрузов;
				ДвижениеСостояния.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
				
				// Добавляется плановое прибытие в точку разгрузки при возникновении проблемы.
				ДвижениеСостояния	= Движения.упСостояниеПоЗаданиям.Добавить();
				ДвижениеСостояния.Задание = СтрокаРаботы.Задание;
				ДвижениеСостояния.Местоположение = СтрокаРаботы.АдресРазгрузки;
				ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.Количество = СтрокаРаботы.КоличествоГрузов;
				ДвижениеСостояния.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
			
			КонецЕсли;
						
			ДвижениеПлана	=ДвиженияПланПоЗаданиям.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеПлана, СрезПоследних[0]); 
			ДвижениеПлана.Период = Реквизиты.Дата;
			ДвижениеПлана.ПричинаОтмены = СтрокаРаботы.Проблема;
			ДвижениеПлана.Выполнено = Ложь;
			ДвижениеПлана.Отменено = Истина;
			ДвижениеПлана.Местоположение = СрезПоследних[0].Местоположение;
			ДвижениеПлана.Количество = 0;
					
			Если ЭтоРазгрузка И СтрокаРаботы.АдресРазгрузки <> СрезПоследних[0].Местоположение Тогда
				ДвижениеПлана = ДвиженияПланПоЗаданиям.Добавить();
				ДвижениеПлана.Задание = СтрокаРаботы.Задание;
				ДвижениеПлана.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
				ДвижениеПлана.Период = Реквизиты.Дата;
				ДвижениеПлана.Отменено = Ложь;
				ДвижениеПлана.Местоположение = СтрокаРаботы.АдресРазгрузки;
				ДвижениеПлана.Количество = СтрокаРаботы.КоличествоГрузов;
			КонецЕсли; 
			
		Иначе
			Если Ложь
				Или СтрокаРаботы.Проблема.Действие <> Перечисления.упДействияПоРаботе.ВернутьВПланирование 
				Или Не ЗначениеЗаполнено(СтрокаРаботы.Проблема)
			Тогда 
				ДвижениеСостояния	= ДвиженияСостояниеПоЗаданиям.Добавить();
				ДвижениеСостояния.Задание = СтрокаРаботы.Задание;
				ДвижениеСостояния.Местоположение = ТранспортноеСредство;
				Если ЭтоРазгрузка Тогда
					ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Расход;
				Иначе
					ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Приход;
				КонецЕсли; 
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.Количество = СтрокаРаботы.КоличествоГрузов;
				ДвижениеСостояния.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
				
				ДвижениеСостояния = ДвиженияСостояниеПоЗаданиям.Добавить();
				ДвижениеСостояния.Задание = СтрокаРаботы.Задание;
				ДвижениеСостояния.Местоположение = Реквизиты.АдресТочки;
				Если ЭтоРазгрузка Тогда
					ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Приход;
				Иначе
					ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Расход;
				КонецЕсли; 
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.Количество = СтрокаРаботы.КоличествоГрузов;
				ДвижениеСостояния.ГрузовоеМесто = СтрокаРаботы.ГрузовоеМесто;
				
				Если ЭтоРазгрузка Тогда
					ДвижениеПлана = ДвиженияПланПоЗаданиям.Добавить();
					ЗаполнитьЗначенияСвойств(ДвижениеПлана, СтрокаРаботы); 
					ДвижениеПлана.Период = Реквизиты.Дата;
					ДвижениеПлана.Местоположение = Реквизиты.АдресТочки;
					ДвижениеПлана.Выполнено = Истина;
					ДвижениеПлана.Отменено = Ложь;
					ДвижениеПлана.ПричинаОтмены = СтрокаРаботы.Проблема;
					ДвижениеПлана.Количество = СтрокаРаботы.КоличествоГрузов;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

// Процедура формирует записи по регистру "упФактическиеМестоположенияТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на объект.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты объект.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияФактическиеМестоположенияТранспорта(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки.
		Ссылка = Документы.упПрохождениеТочки.ПустаяСсылка();
		Текущий_ДокументОбъект = Ссылка.ПолучитьОбъект();
		Движения = Текущий_ДокументОбъект.Движения;
		Реквизиты = Ссылка;
	КонецЕсли;
	Если Отказ Или Не ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда 
		Возврат; 
	КонецЕсли;
	ПараметрыПеревозкиРейса = упРейс.ПараметрыПеревозкиРейса(Реквизиты.Рейс);
	ДвиженияМестоположения = Движения.упФактическиеМестоположенияТС;
	ДвиженияМестоположения.Записывать = Истина;
	УчетныеЗоны = упУчетТранспортныхСредств.УчетныеЗоныЗон(Реквизиты.АдресТочки.ГеографическаяЗона);
	//СрезПоследних = РегистрыСведений.упФактическиеМестоположенияТС.СрезПоследних(Ссылка.МоментВремени(), Новый Структура("ТранспортноеСредство", ПараметрыПеревозкиРейса.ТранспортноеСредство));
	//Если Истина
	//	И УчетныеЗоны.Количество() > 0 
	//	И (Ложь
	//		Или СрезПоследних.Количество() = 0 
	//		Или (Истина
	//			И СрезПоследних.Количество() > 0 
	//			И СрезПоследних[0].Зона <> УчетныеЗоны[0].Родитель))
	//Тогда
		ДвижениеМестоположения = ДвиженияМестоположения.Добавить();
		ДвижениеМестоположения.Период = Реквизиты.Дата;
		ДвижениеМестоположения.ТранспортноеСредство = ПараметрыПеревозкиРейса.ТранспортноеСредство;
		ДвижениеМестоположения.Адрес = Реквизиты.АдресТочки;
		Если УчетныеЗоны.Количество() > 0 Тогда 
			ДвижениеМестоположения.Зона = УчетныеЗоны[0].Родитель; 
		КонецЕсли; 
	//КонецЕсли; 

КонецПроцедуры

// Процедура формирует движения по регистру "упЗаданияКПланированию".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на объект.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объект.
//  Отказ					 - Булево			 - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаданияНаПеревозку = Движения.упЗаданияКПланированию;
	ДвиженияЗаданияНаПеревозку.Записывать = Истина;
	
	Задания = ДополнительныеСвойства.Задания;
	Если Задания.Количество() Тогда
		ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Истина);				
		
		Для каждого текСтрока Из Задания Цикл
			ДвижениеЗадание = ДвиженияЗаданияНаПеревозку.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеЗадание, текСтрока);
			ДвижениеЗадание.Период = Реквизиты.Дата;			
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры	

// Процедура формирует движения по регистру "упСостояниеПоЗаданиям".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на объект.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объект.
//  Отказ					 - Булево			 - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостояниеПоЗаданиям	= Движения.упСостояниеПоЗаданиям;
	ДвиженияСостояниеПоЗаданиям.Записывать = Истина;
		
	СостояниеПоЗаданиямТбз = ДополнительныеСвойства.СостояниеПоЗаданиям;
	Если СостояниеПоЗаданиямТбз.Количество() Тогда
		Для каждого текСтрока Из СостояниеПоЗаданиямТбз Цикл
			Если текСтрока.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			ДвижениеСостояниеПоЗаданиям	= ДвиженияСостояниеПоЗаданиям.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеСостояниеПоЗаданиям, текСтрока);
			ДвижениеСостояниеПоЗаданиям.Период       	= Реквизиты.Дата;
			ДвижениеСостояниеПоЗаданиям.Количество		= ?(текСтрока.Количество > 0, текСтрока.Количество, -текСтрока.Количество);
			ДвижениеСостояниеПоЗаданиям.ВидДвижения		= ?(текСтрока.Количество > 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
		КонецЦикла; 
	КонецЕсли; 
КонецПроцедуры	

// Процедура формирует движения по регистру "упНепереданныеДокументы".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на объект.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объект.
//  Отказ					 - Булево			 - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНепереданныеДокументы	= Движения.упНепереданныеДокументы;
	ДвиженияНепереданныеДокументы.Записывать = Истина;
	
	СпособУчетаВозвращенныхДокументов 			= ДополнительныеСвойства.СпособУчетаВозвращенныхДокументов;
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
	             	  |	тбзЗадания.ЗаданиеНаПеревозку,
	             	  |	тбзЗадания.Количество,
	             	  |	тбзЗадания.КоличествоОтменено,
	             	  |	тбзЗадания.КоличествоДобавлено
	             	  |ПОМЕСТИТЬ втЗадания
	             	  |ИЗ
	             	  |	&тбзЗадания КАК тбзЗадания
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ РАЗЛИЧНЫЕ
	             	  |	упЗаданиеНаПеревозкуГруза.Заказчик,
	             	  |	втЗадания.ЗаданиеНаПеревозку,
	             	  |	СУММА(втЗадания.Количество) КАК Количество,
	             	  |	СУММА(втЗадания.КоличествоОтменено) КАК КоличествоОтменено,
	             	  |	СУММА(втЗадания.КоличествоДобавлено) КАК КоличествоДобавлено
	             	  |ПОМЕСТИТЬ втЗаданияЗаказчик
	             	  |ИЗ
	             	  |	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	             	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	             	  |		ПО (втЗадания.ЗаданиеНаПеревозку = упЗаданиеНаПеревозкуГруза.Ссылка)
	             	  |
	             	  |СГРУППИРОВАТЬ ПО
	             	  |	втЗадания.ЗаданиеНаПеревозку,
	             	  |	упЗаданиеНаПеревозкуГруза.Заказчик
	             	  |;
	             	  |
	             	  |////////////////////////////////////////////////////////////////////////////////
	             	  |ВЫБРАТЬ
	             	  |	ВЫБОР
	             	  |		КОГДА втЗаданияЗаказчик.Количество = втЗаданияЗаказчик.КоличествоОтменено
	             	  |			ТОГДА -1
	             	  |		ИНАЧЕ 1
	             	  |	КОНЕЦ КАК Коэффициент,
	             	  |	втЗаданияЗаказчик.Заказчик,
	             	  |	втЗаданияЗаказчик.ЗаданиеНаПеревозку
	             	  |ПОМЕСТИТЬ втДвиженияПоЗаданиям
	             	  |ИЗ
	             	  |	втЗаданияЗаказчик КАК втЗаданияЗаказчик
	             	  |ГДЕ
	             	  |	(втЗаданияЗаказчик.Количество = втЗаданияЗаказчик.КоличествоОтменено
	             	  |			ИЛИ втЗаданияЗаказчик.Количество = втЗаданияЗаказчик.КоличествоДобавлено)";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов(); 				  
	Запрос.УстановитьПараметр("тбзЗадания", ДополнительныеСвойства.НепереданныеДокументы);
					  
	Если СпособУчетаВозвращенныхДокументов = Перечисления.упСпособыУчетаВозвращенныхДокументов.СТочностьюДоТиповДокументов Тогда
				
		Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
		                              |	упПакетДокументов.ТипРегламентногоДокумента,
		                              |	упПакетДокументов.КоличествоПолучитьОтУчастника * втДвиженияПоЗаданиям.Коэффициент КАК КоличествоПолучить,
		                              |	упПакетДокументов.КоличествоПередатьУчастнику * втДвиженияПоЗаданиям.Коэффициент КАК КоличествоПередать,
		                              |	втДвиженияПоЗаданиям.ЗаданиеНаПеревозку
		                              |ПОМЕСТИТЬ втДокументы
		                              |ИЗ
		                              |	втДвиженияПоЗаданиям КАК втДвиженияПоЗаданиям
		                              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыРегламентныхДокументов.ПакетДокументов КАК упПакетДокументов
		                              |		ПО втДвиженияПоЗаданиям.Заказчик.ПакетРегламентныхДокументов = упПакетДокументов.Ссылка
		                              |			И (упПакетДокументов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Заказчик))
		                              |			И (упПакетДокументов.ВозвратДокументовПослеИсполненияРейса)
		                              |;
		                              |
		                              |////////////////////////////////////////////////////////////////////////////////
		                              |ВЫБРАТЬ
		                              |	втДокументы.ТипРегламентногоДокумента,
		                              |	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов) КАК Операция,
		                              |	втДокументы.КоличествоПолучить КАК Количество,
		                              |	втДокументы.ЗаданиеНаПеревозку
		                              |ИЗ
		                              |	втДокументы КАК втДокументы
		                              |ГДЕ
		                              |	НЕ втДокументы.КоличествоПолучить = 0
		                              |
		                              |ОБЪЕДИНИТЬ ВСЕ
		                              |
		                              |ВЫБРАТЬ
		                              |	втДокументы.ТипРегламентногоДокумента,
		                              |	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов),
		                              |	втДокументы.КоличествоПередать,
		                              |	втДокументы.ЗаданиеНаПеревозку
		                              |ИЗ
		                              |	втДокументы КАК втДокументы
		                              |ГДЕ
		                              |	НЕ втДокументы.КоличествоПередать = 0";
									  
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ДвижениеНепереданныеДокументы = ДвиженияНепереданныеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеНепереданныеДокументы, Выборка);
				ДвижениеНепереданныеДокументы.Рейс        = Реквизиты.Рейс;
				ДвижениеНепереданныеДокументы.ВидДвижения = ?(Выборка.Количество  > 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
				ДвижениеНепереданныеДокументы.Период      = Реквизиты.Дата;
				ДвижениеНепереданныеДокументы.Количество  = ?(Выборка.Количество  > 0, Выборка.Количество, -Выборка.Количество);
				
			КонецЦикла; 
		КонецЕсли;
	Иначе
		
		Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
		                              |	втДвиженияПоЗаданиям.ЗаданиеНаПеревозку КАК ДокументОснование,
		                              |	упЗаданиеНаПеревозкуГруза.Получатель КАК Получатель,
		                              |	упШаблоныПакетовПечатныхФормМакетыПФ.КоличествоВернуть * втДвиженияПоЗаданиям.Коэффициент КАК Количество,
		                              |	упШаблоныПакетовПечатныхФормМакетыПФ.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		                              |	упТипыРегламентныхДокументов.СпособОпределенияДатыЭкземпляра КАК СпособОпределенияДатыЭкземпляра,
		                              |	упТипыРегламентныхДокументов.СпособОпределенияНомераЭкземпляра КАК СпособОпределенияНомераЭкземпляра,
		                              |	упТипыРегламентныхДокументов.ФормулаДата КАК ФормулаДата,
		                              |	упТипыРегламентныхДокументов.ФормулаНомер КАК ФормулаНомер,
		                              |	ВЫБОР
		                              |		КОГДА упЗаданиеНаПеревозкуГруза.НомерКИС = """"
		                              |			ТОГДА упЗаявкаНаПеревозкуГруза.НомерКИС
		                              |		ИНАЧЕ упЗаданиеНаПеревозкуГруза.НомерКИС
		                              |	КОНЕЦ КАК НомерКИС,
		                              |	ВЫБОР
		                              |		КОГДА упЗаданиеНаПеревозкуГруза.НомерКИС = """"
		                              |			ТОГДА упЗаявкаНаПеревозкуГруза.ДатаКИС
		                              |		ИНАЧЕ упЗаданиеНаПеревозкуГруза.ДатаКИС
		                              |	КОНЕЦ КАК ДатаКИС,
		                              |	упЗаданиеНаПеревозкуГруза.Номер КАК Номер,
		                              |	упЗаданиеНаПеревозкуГруза.Дата КАК Дата
		                              |ИЗ
		                              |	втДвиженияПоЗаданиям КАК втДвиженияПоЗаданиям
		                              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		                              |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры КАК упПартнеры
		                              |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упШаблоныПакетовПечатныхФорм.МакетыПФ КАК упШаблоныПакетовПечатныхФормМакетыПФ
		                              |					ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыРегламентныхДокументов КАК упТипыРегламентныхДокументов
		                              |					ПО упШаблоныПакетовПечатныхФормМакетыПФ.ТипРегламентногоДокумента = упТипыРегламентныхДокументов.Ссылка
		                              |				ПО упПартнеры.ПакетПечатныхФормЗадание = упШаблоныПакетовПечатныхФормМакетыПФ.Ссылка
		                              |					И (упШаблоныПакетовПечатныхФормМакетыПФ.КоличествоВернуть > 0)
		                              |			ПО упЗаданиеНаПеревозкуГруза.Получатель = упПартнеры.Ссылка
		                              |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
		                              |			ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГруза.Ссылка
		                              |		ПО втДвиженияПоЗаданиям.ЗаданиеНаПеревозку = упЗаданиеНаПеревозкуГруза.Ссылка";
									  
	
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			
			Ошибки = Неопределено;
			
			сткПараметрыПодбораЭкземпляра = Новый Структура();
			сткПараметрыПодбораЭкземпляра.Вставить("ДокументОснование", Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("Рейс", 				Реквизиты.Рейс);
			сткПараметрыПодбораЭкземпляра.Вставить("Дата", 				Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("Номер", 			Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("ДатаКИС", 			Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("НомерКИС", 			Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("СпособОпределенияДатыЭкземпляра", Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("СпособОпределенияНомераЭкземпляра", Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("ФормулаДата", 		Неопределено);
			сткПараметрыПодбораЭкземпляра.Вставить("ФормулаНомер", 		Неопределено);
			
			
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(сткПараметрыПодбораЭкземпляра, Выборка);
				ЭкземплярДокумента	= Справочники.упЭкземплярыДокументов.ПодобратьЭкземплярДокумента(сткПараметрыПодбораЭкземпляра, Ошибки);
				
				ДвижениеНепереданныеДокументы = ДвиженияНепереданныеДокументы.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеНепереданныеДокументы, Выборка);	
				ДвижениеНепереданныеДокументы.ЗаданиеНаПеревозку 	= Выборка.ДокументОснование;
				ДвижениеНепереданныеДокументы.Рейс        			= Реквизиты.Рейс;
				ДвижениеНепереданныеДокументы.Период      			= Реквизиты.Дата;
				ДвижениеНепереданныеДокументы.ЭкземплярДокумента 	= ЭкземплярДокумента;
				ДвижениеНепереданныеДокументы.ВидДвижения 			= ?(Выборка.Количество  > 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
				ДвижениеНепереданныеДокументы.Количество  			= ?(Выборка.Количество  > 0, Выборка.Количество, -Выборка.Количество);
			
			КонецЦикла; 
			
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
			
		КонецЕсли;

	КонецЕсли;	
	
КонецПроцедуры	

// Процедура формирует движения по регистру "упИнкассацияДенежныхСредств".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на объект.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объект.
//  Отказ					 - Булево			 - выполнить фиксирование изменений.
//
Процедура СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияИнкассацияДенежныхСредств	= Движения.упИнкассацияДенежныхСредств;
	ДвиженияИнкассацияДенежныхСредств.Записывать = Истина;
	
	тбзДвижения	= ДополнительныеСвойства.ИнкассацияДенежныхСредств;
	
	Для каждого Строка Из тбзДвижения Цикл
		ДвижениеИнкассацияДенежныхСредств 		= ДвиженияИнкассацияДенежныхСредств.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеИнкассацияДенежныхСредств, Строка);
		ДвижениеИнкассацияДенежныхСредств.Период    = Реквизиты.Дата;
	КонецЦикла;
		
КонецПроцедуры	

// Процедура - формирует движения по регистру "упПараметрыЗаданияНаПеревозку".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПараметрыЗаданияНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаборЗаписей = Движения.упПараметрыЗаданияНаПеревозку;
	ДвиженияНаборЗаписей.Записывать = Истина;
	
	ПараметрыНаПеревозку = ДополнительныеСвойства.ПараметрыНаПеревозку;
		
	Для каждого Строка Из ПараметрыНаПеревозку Цикл
						
		ДвижениеЗапись = ДвиженияНаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеЗапись, Строка);
		ДвижениеЗапись.Период = Реквизиты.ОкончаниеВыполнения;
		
	КонецЦикла;

КонецПроцедуры

// Процедура - формирует движения по регистру "упПараметрыЗаявкиНаПеревозку".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПараметрыЗаявкиНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаборЗаписей = Движения.упПараметрыЗаявкиНаПеревозку;
	ДвиженияНаборЗаписей.Записывать = Истина;
		
	ПараметрыНаПеревозку = ДополнительныеСвойства.ПараметрыНаПеревозку;
		
	Для каждого Строка Из ПараметрыНаПеревозку Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.ЗаявкаНаПеревозку) Тогда
			Продолжить;
		КонецЕсли;
		
		ДвижениеЗапись = ДвиженияНаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеЗапись, Строка);
		ДвижениеЗапись.Период = Реквизиты.ОкончаниеВыполнения;
	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область Проверки

// Проверяет заполненность полей переданной строки.
//
// Параметры:
//  Строка		 - Строка					 - Строка табличной части,
//  					используется для доступа к свойствам и методам строки табличной части.
//  Операция		 - ПеречислениеСсылка.упТипыОпераций	 - Ссылка на элемент.
//  Задание		 - ДокументСсылка					 - Ссылка на элемент.
//  ГрузовоеМесто	 - СправочникСсылка					 - Ссылка на элемент.
//  ТекстСообщения	 - Строка							 - Текст.
//  Отказ			 - Булево							 - выполнить фиксирование изменений.
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция ПроверитьЗаполнениеСтрокиРаботы(Строка, Операция, Задание, ГрузовоеМесто, ТекстСообщения, Отказ) Экспорт
	
	ГрузРазделен = Ложь;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Строка, "ГрузРазделен") Тогда
		ГрузРазделен = Строка.ГрузРазделен;	
	КонецЕсли;

	Если НЕ ГрузРазделен Тогда
		Если Строка.Отменена Тогда
			
			Если Истина
				И НЕ ЗначениеЗаполнено(Строка.Проблема) 
				И упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(Операция) Тогда
				
				ТекстСообщения = Нстр("ru = 'Для не выполненной строки задания %1, операции %2 %3 не заполнена проблема'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Задание, Операция, ?(ЗначениеЗаполнено(ГрузовоеМесто), Нстр("ru = ', грузового места ""'") + ГрузовоеМесто + """", ""));
				Отказ = Истина;
			
			Иначе
				Если Истина
					И упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(Операция)
					И Не Строка.ЗапрещеноСозданиеНовойТочкиМаршрута
					И Не ЗначениеЗаполнено(Строка.АдресРазгрузки)
				Тогда
					ТекстСообщения = Нстр("ru = 'Для не выполненной строки %1 не заполнен адрес разгрузки'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Задание, Операция, ?(ЗначениеЗаполнено(ГрузовоеМесто), Нстр("ru = ', грузового места ""'") + ГрузовоеМесто + """", ""));
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли НЕ Строка.Выполнена Тогда	
			ТекстСообщения = Нстр("ru = 'Для не выполненной строки %1 не заполнена проблема'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Задание, Операция, ?(ЗначениеЗаполнено(ГрузовоеМесто), Нстр("ru = ', грузового места ""'") + ГрузовоеМесто + """", ""));
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;	
КонецФункции

// Проверить заполненность поля складская ячейка.
//
// Параметры:
//  Строка		 - Строка					 - Строка табличной части,
//  					используется для доступа к свойствам и методам строки табличной части.
//  Задания		 - ТаблицаЗначений					 - таблица заданий.
//  Задание		 - ДокументСсылка			 - Ссылка на элемент.
//  Операция		 - ПеречислениеСсылка.упТипыОпераций	 - Ссылка на элемент.
//  ГрузовоеМесто	 - СправочникСсылка					 - Ссылка на элемент.
//  СкладскаяЯчейка	 - СправочникСсылка.упСкладскиеЯчейки 	 - складская ячейка.
//  ТекстСообщения	 - Строка							 - Текст.
//  Отказ			 - Булево							 - выполнить фиксирование изменений.
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция ПроверитьЗаполнениеСкладскойЯчейкиРаботы(Строка, Задания, Задание, Операция, ГрузовоеМесто, СкладскаяЯчейка, ТекстСообщения, Отказ) Экспорт

	мсвЗадания = Задания.НайтиСтроки(Новый Структура("Задание", Задание));
	
	Если мсвЗадания.Количество() > 0 Тогда
		ЭтоМультимодальнаяПеревозка = мсвЗадания[0].ЭтоМультимодальнаяПеревозка;
		ЭтоПервоеЗвено 				= мсвЗадания[0].ЭтоПервоеЗвено;
		ЭтоПоследнееЗвено 			= мсвЗадания[0].ЭтоПоследнееЗвено;
		Если Строка.Выполнена 
				И ЭтоМультимодальнаяПеревозка 
				И НЕ ЗначениеЗаполнено(СкладскаяЯчейка) Тогда
			
			Если (ЭтоПервоеЗвено И Операция = Перечисления.упТипыОпераций.Разгрузка) 
					ИЛИ (ЭтоПоследнееЗвено И Операция = Перечисления.упТипыОпераций.Погрузка)
					ИЛИ (НЕ ЭтоПервоеЗвено И НЕ ЭтоПоследнееЗвено И упПрохождениеТочкиКлиентСервер.ОсновнаяОперацияДляВидаГрузаГруз(Операция)) Тогда
					
				ТекстСообщения = Нстр("ru = 'Для строки задания %1, операции %2 %3 не заполнена складская ячейка'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Задание, Операция, ?(ЗначениеЗаполнено(ГрузовоеМесто), Нстр("ru = ', грузового места ""'") + ГрузовоеМесто + """", ""));
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Проверяет последовательность проходению точек маршрута.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на объект.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты объект.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура ПроверитьПорядокПрохожденияТочек(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	упМаршрутПоРейсуСрезПоследних.Адрес,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер
	|
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ
	|	НЕ упМаршрутПоРейсуСрезПоследних.Пройдена
	|	И НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	|
	|УПОРЯДОЧИТЬ ПО
    |	СтрокаНомер";
	Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Если текВыборка.СтрокаНомер < Реквизиты.СтрокаНомер Тогда
			ТекстСообщения = НСтр("ru = 'Нарушен порядок прохождения точек. Необходимо пройти точку с адресом %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текВыборка.Адрес);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);	
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'Точки с адресом %1 нет в маршруте'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Ссылка, Реквизиты.АдресТочки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие документа по точке.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//	ИдентификаторТочки - Строка - Идентификатор.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура ПроверитьСуществованиеДокументаПоТочке(Рейс, ИдентификаторТочки, Ссылка, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
		Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Пройдена,
	|	ЕстьNull(упМаршрутПоРейсу.Регистратор,упМаршрутПоРейсуСрезПоследних.Регистратор) КАК Регистратор
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И Идентификатор = &Идентификатор) КАК упМаршрутПоРейсуСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|		ПО упМаршрутПоРейсуСрезПоследних.Период = упМаршрутПоРейсу.Период
	|			И упМаршрутПоРейсу.Рейс = &Рейс
	|			И упМаршрутПоРейсу.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Рейс"              , Рейс);
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторТочки);
	текРезультат = Запрос.Выполнить();
	
	текВыборка = текРезультат.Выбрать();
	Если текВыборка.Следующий() Тогда
		Если НЕ текВыборка.Регистратор = Ссылка И текВыборка.Пройдена Тогда
			ТекстСообщения = НСтр(СтрШаблон("ru = 'Для данной точки уже сформировано прохождение: %1'", текВыборка.Регистратор));	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,, Отказ);
			Событие = Нстр("ru = 'Проверка существования документа по точке'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,,Ссылка,ТекстСообщения);    
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьСтатусРейса(Рейс, ЭтоОперацииСКонтейнером, ТекстОшибки, Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийСтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(Рейс);
	Если ТекущийСтатусРейса.Статус = Перечисления.упСтатусыРейса.Новый
		Или ТекущийСтатусРейса.Статус = Перечисления.упСтатусыРейса.КПланированиюТС
		Или ТекущийСтатусРейса.Статус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС
		Или ТекущийСтатусРейса.Статус = Перечисления.упСтатусыРейса.НазначеноТС Тогда
		Отказ = Истина;
		Если ЭтоОперацииСКонтейнером Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = 'На основании операции с контейнером в статусе ""%1"" не может быть сформировано прохождение точки'", ТекущийСтатусРейса.Состояние));
		Иначе	
			ТекстОшибки = НСтр(СтрШаблон("ru = 'На основании рейса в статусе ""%1"" не может быть сформировано прохождение точки'", ТекущийСтатусРейса.Статус));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Проверить способ прохождения маршрута
//
// Параметры:
//  Рейс		 - ДокументСсылка.упРейс  -  рейс.
//  ТекстОшибки - Строка	 - ошибка.
//  Отказ		 - Булево	 - Истина, если ошибка.
//
Процедура ПроверитьСпособПрохожденияМаршрута(Рейс, ТекстОшибки, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СпособПрохожденияМаршрута = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидПеревозки.СпособПрохожденияТочекМаршрута");
	
	Если СпособПрохожденияМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'Для вида перевозки установлен способ прохождения маршрута ""В рейсе"", формирование документов запрещено.'");
	КонецЕсли;
	
КонецПроцедуры

// Проверка корректности переданных дат. 
//
// Параметры:
//  ДатаПрибытия - Дата - Период.
//	ДатаНачалаРабот - Дата - Период.
//	ДатаУбытия - Дата - Период.
//	ТекстОшибки - Строка - Текст.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура ПроверитьДатуНачалаРабот(ДатаПрибытия, ДатаНачалаРабот, ДатаУбытия, ТекстОшибки, Отказ) Экспорт
	
	Если ДатаНачалаРабот < ДатаПрибытия Тогда
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Дата начала работ %1 раньше даты прибытия %2'");		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДатаНачалаРабот, ДатаПрибытия);
		Возврат;
	КонецЕсли;	
	
	Если ДатаНачалаРабот > ДатаУбытия Тогда	
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Дата начала работ %1 позже даты убытия %2'");		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДатаНачалаРабот, ДатаУбытия);	
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

// Проверка корректности переданных дат. 
//
// Параметры:
//  ДатаПрибытия - Дата - Период.
//  Рейс - ДокументСсылка - Ссылка на элемент.
//	НомерТочки - Число - Номер.
//	ТекстОшибки - Строка - Текст.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура ПроверитьДатуПрибытияФакт(ДатаПрибытия, Рейс, НомерТочки, ТекстОшибки, Отказ) Экспорт
		
	ДатаУбытияФактИзПредыдущейТочки = ДатаУбытияИзПредыдущейТочки(Рейс, НомерТочки);
	
	Если НЕ ДатаУбытияФактИзПредыдущейТочки = Неопределено
			И ДатаУбытияФактИзПредыдущейТочки > ДатаПрибытия Тогда
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Прибытие в точку %1 раньше убытие из предыдущей точки %2'");		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДатаПрибытия, ДатаУбытияФактИзПредыдущейТочки);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Получаем следующую точку относительно текущей.
//
// Параметры:
//  НомерТекущейТочки - Число - Номер.
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Массив - Результат выполнения.
//
Функция НайтиПрохождениеБолееПозднейТочки(НомерТекущейТочки, Рейс) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПрохождениеТочки.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.упПрохождениеТочки КАК упПрохождениеТочки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК втМаршрут
	               |		ПО упПрохождениеТочки.ИдентификаторТочки = втМаршрут.Идентификатор
	               |ГДЕ
	               |	упПрохождениеТочки.Проведен
	               |	И упПрохождениеТочки.СтрокаНомер > &СтрокаНомер";
	Запрос.УстановитьПараметр("Рейс", 			Рейс);
	Запрос.УстановитьПараметр("СтрокаНомер", 	НомерТекущейТочки);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		мсвРезультат.Добавить(текВыборка.Ссылка);
	КонецЦикла;
	
	Возврат мсвРезультат;
	
КонецФункции

// Проверяет последовательность проведения погрузки/разгрузки по рейсу.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//	МоментВремени - МоментВремени - Содержит дату и время, а также ссылку на объект базы данных.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура ПроконтролироватьПогрузкуРазгрузку(Рейс, МоментВремени, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упРаботыПоРейсуСрезПоследних.Задание,
		               |	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто,
		               |	СУММА(упРаботыПоРейсуСрезПоследних.КоличествоГрузов * ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		               |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		               |				ТОГДА 1
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		               |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		               |				ТОГДА -1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК КоличествоГрузов
		               |ИЗ
		               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(&Период, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
		               |ГДЕ
		               |	упРаботыПоРейсуСрезПоследних.Выполнена
		               |	И НЕ упРаботыПоРейсуСрезПоследних.Отменена
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упРаботыПоРейсуСрезПоследних.Задание,
		               |	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто
		               |
		               |ИМЕЮЩИЕ
		               |	СУММА(упРаботыПоРейсуСрезПоследних.КоличествоГрузов * ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		               |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		               |				ТОГДА 1
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		               |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		               |				ТОГДА -1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) < 0";

	Запрос.УстановитьПараметр("Рейс", 	Рейс);
	Запрос.УстановитьПараметр("Период", МоментВремени);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(текВыборка.ГрузовоеМесто) Тогда
			ТекстСообщения = НСтр("ru = 'По заданию %1 грузовому месту %2 разгрузка(передача ценностей) производится раньше погрузки(выемки ценностей)'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текВыборка.Задание, текВыборка.ГрузовоеМесто);
		Иначе	
			ТекстСообщения = НСтр("ru = 'По заданию %1 разгрузка(передача ценностей) производится раньше погрузки(выемки ценностей)'");		
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текВыборка.Задание);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЦикла;
КонецПроцедуры

// Функция - Дата убытия из предыдущей точки.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - рейс.
//  НомерТочки - Число - Номер.
// 
// Возвращаемое значение:
//   Дата - Результат выполнения.
//
Функция ДатаУбытияИзПредыдущейТочки(Рейс, НомерТочки)
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(упМаршрутПоРейсуСрезПоследних.УбытиеФакт) КАК УбытиеФакт
	               |ИЗ
	               |	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	               |ГДЕ
	               |	упМаршрутПоРейсуСрезПоследних.Пройдена
	               |	И НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	               |	И упМаршрутПоРейсуСрезПоследних.СтрокаНомер < &СтрокаНомер";
	Запрос.УстановитьПараметр("Рейс",		Рейс);
	Запрос.УстановитьПараметр("СтрокаНомер",НомерТочки);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() 
		И ЗначениеЗаполнено(текВыборка.УбытиеФакт) Тогда
		текРезультат = текВыборка.УбытиеФакт;
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

#КонецОбласти

#Область ФормированиеДокумента

// Формируем документ "упПрохождениеТочки" для следущей по параметрам полученных на основании рейса.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - рейс.
//	ТекстОшибки - Строка - Текст.
//	Отказ - Булево - выполнить фиксирование изменений.
// 
// Возвращаемое значение:
//   Дата - Результат выполнения.
//
Функция СформироватьПрохождениеСледующейТочки(Рейс, ТекстОшибки, Отказ) Экспорт
	
	текРезультат = Неопределено;
		
	ПроверитьСпособПрохожденияМаршрута(Рейс, ТекстОшибки, Отказ);
	
	ЭтоОперацииСКонтейнером = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидТранспортнойУслуги") = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
	
	ПроверитьСтатусРейса(Рейс, ЭтоОперацииСКонтейнером, ТекстОшибки, Отказ);
	
	Если НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упМаршрутПоРейсуСрезПоследних.Период КАК Период,
		|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
		|	упМаршрутПоРейсуСрезПоследних.Адрес КАК Адрес,
		|	упМаршрутПоРейсуСрезПоследних.ТипТочки КАК ТипТочки,
		|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
		|	упМаршрутПоРейсуСрезПоследних.Пройдена КАК Пройдена,
		|	упМаршрутПоРейсуСрезПоследних.Отменена КАК Отменена,
		|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПлановоеПрибытие,
		|	упМаршрутПоРейсуСрезПоследних.НачалоРаботПлан КАК НачалоРаботПлан,
		|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК ПлановоеУбытие,
		|	упМаршрутПоРейсуСрезПоследних.Гараж КАК Гараж,
		|	упМаршрутПоРейсуСрезПоследних.Отложена КАК Отложена,
		|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки
		|ПОМЕСТИТЬ втТочки
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	втТочки.Период КАК Период,
		|	втТочки.Идентификатор КАК Идентификатор,
		|	втТочки.Адрес КАК Адрес,
		|	втТочки.ТипТочки КАК ТипТочки,
		|	втТочки.СтрокаНомер КАК СтрокаНомер,
		|	втТочки.Пройдена КАК Пройдена,
		|	втТочки.Отменена КАК Отменена,
		|	втТочки.ПлановоеПрибытие КАК ПлановоеПрибытие,
		|	втТочки.НачалоРаботПлан КАК НачалоРаботПлан,
		|	втТочки.ПлановоеУбытие КАК ПлановоеУбытие,
		|	втТочки.Гараж КАК Гараж,
		|	втТочки.Отложена КАК Отложена,
		|	втТочки.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки
		|ПОМЕСТИТЬ втТочка
		|ИЗ
		|	втТочки КАК втТочки
		|ГДЕ ИСТИНА
		|	И НЕ (втТочки.Пройдена)
		|	И НЕ (втТочки.Отменена)
		|УПОРЯДОЧИТЬ ПО
		|	СтрокаНомер
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбРаботы.Период КАК Период,
		|	тбРаботы.Регистратор КАК Регистратор,
		|	тбРаботы.Рейс КАК Рейс,
		|	тбРаботы.Идентификатор КАК Идентификатор,
		|	тбРаботы.СтрокаНомер КАК СтрокаНомер,
		|	тбРаботы.Отменена КАК Отменена,
		|	тбРаботы.Задание КАК Задание,
		|	тбРаботы.Операция КАК Операция,
		|	тбРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
		|	тбРаботы.ВремяРабот КАК ВремяРабот,
		|	тбРаботы.ВременноеОкноНачало КАК ВременноеОкноНачало,
		|	тбРаботы.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
		|	тбРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	тбРаботы.Тара КАК Тара,
		|	тбРаботы.ВремяОжидания КАК ВремяОжидания,
		|	тбРаботы.ПлановоеВремяНачалаРабот КАК ПлановоеВремяНачалаРабот,
		|	тбРаботы.КоличествоГрузов КАК КоличествоГрузов,
		|	тбРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
		|	тбРаботы.Проблема КАК Проблема,
		|	тбРаботы.АдресРазгрузки КАК АдресРазгрузки,
		|	тбРаботы.Выполнена КАК Выполнена,
		|	тбРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
		|	тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
		|	тбРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	тбРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
		|	тбРаботы.Сумма КАК Сумма,
		|	тбРаботы.СкладскаяЯчейка КАК СкладскаяЯчейка,
		|	ПараметрыЗаданияНаПеревозку.Высота КАК Высота,
		|	ПараметрыЗаданияНаПеревозку.Длина КАК Длина,
		|	ПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	ПараметрыЗаданияНаПеревозку.КоличествоМест КАК КоличествоМест,
		|	ПараметрыЗаданияНаПеревозку.Масса КАК Масса,
		|	ПараметрыЗаданияНаПеревозку.Объем КАК Объем,
		|	ПараметрыЗаданияНаПеревозку.Ширина КАК Ширина
		|ПОМЕСТИТЬ втРаботы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс
		|		И Идентификатор В (
		|		ВЫБРАТЬ
		|		втТочка.Идентификатор КАК Идентификатор
		|		ИЗ
		|		втТочка КАК втТочка)) КАК тбРаботы
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
		|		,
		|		) КАК ПараметрыЗаданияНаПеревозку
		|	ПО ПараметрыЗаданияНаПеревозку.ГрузовоеМесто = тбРаботы.ГрузовоеМесто
		|		И ПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку = тбРаботы.Задание
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	втТочка.Период КАК Период,
		|	втТочка.Идентификатор КАК Идентификатор,
		|	втТочка.Адрес КАК Адрес,
		|	втТочка.ТипТочки КАК ТипТочки,
		|	втТочка.СтрокаНомер КАК СтрокаНомер,
		|	втТочка.Пройдена КАК Пройдена,
		|	втТочка.Отменена КАК Отменена,
		|	втТочка.ПлановоеПрибытие КАК ПлановоеПрибытие,
		|	втТочка.НачалоРаботПлан КАК НачалоРаботПлан,
		|	втТочка.ПлановоеУбытие КАК ПлановоеУбытие,
		|	втТочка.Гараж КАК Гараж,
		|	втТочка.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
		|	втТочка.Отложена КАК Отложена
		|ИЗ
		|	втТочка КАК втТочка
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.Период КАК Период,
		|	втРаботы.Регистратор КАК Регистратор,
		|	втРаботы.Рейс КАК Рейс,
		|	втРаботы.Идентификатор КАК Идентификатор,
		|	втРаботы.СтрокаНомер КАК СтрокаНомер,
		|	втРаботы.Отменена КАК Отменена,
		|	втРаботы.Задание КАК Задание,
		|	втРаботы.Операция КАК Операция,
		|	втРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
		|	втРаботы.ВремяРабот КАК ВремяРабот,
		|	втРаботы.ВременноеОкноНачало КАК ВременноеОкноНачало,
		|	втРаботы.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
		|	втРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втРаботы.Тара КАК Тара,
		|	втРаботы.ВремяОжидания КАК ВремяОжидания,
		|	втРаботы.ПлановоеВремяНачалаРабот КАК ПлановоеВремяНачалаРабот,
		|	втРаботы.КоличествоГрузов КАК КоличествоГрузов,
		|	втРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
		|	втРаботы.Проблема КАК Проблема,
		|	втРаботы.АдресРазгрузки КАК АдресРазгрузки,
		|	втРаботы.Выполнена КАК Выполнена,
		|	втРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
		|	втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
		|	втРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	втРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
		|	втРаботы.Сумма КАК Сумма,
		|	втРаботы.СкладскаяЯчейка КАК СкладскаяЯчейка,
		|	втРаботы.Высота КАК Высота,
		|	втРаботы.Длина КАК Длина,
		|	втРаботы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	втРаботы.КоличествоМест КАК КоличествоМест,
		|	втРаботы.Масса КАК Масса,
		|	втРаботы.Объем КАК Объем,
		|	втРаботы.Ширина КАК Ширина
		|ИЗ
		|	втРаботы КАК втРаботы
		|ГДЕ НЕ (втРаботы.Отменена)
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВложенныйЗапрос.Период) КАК КрайнийПериод
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(втТочки.Период) КАК Период
		|	ИЗ
		|		втТочки КАК втТочки
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		МАКСИМУМ(втРаботы.Период) КАК Период
		|	ИЗ
		|		втРаботы КАК втРаботы) КАК ВложенныйЗапрос
		|";

		Запрос.УстановитьПараметр("Рейс", Рейс);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		КоличествоТаблиц = РезультатЗапроса.Количество();
		
		текВыборкаТочка = РезультатЗапроса[КоличествоТаблиц - 3].Выбрать();
		
		Если текВыборкаТочка.Следующий() Тогда
			
			тбзРаботыВТочке = РезультатЗапроса[КоличествоТаблиц - 2].Выгрузить();
			КрайнийПериод = РезультатЗапроса[КоличествоТаблиц - 1].Выгрузить()[0].КрайнийПериод;
			
			ТекущаяДата = ТекущаяДатаСеанса();
			
			сткРеквизиты = Новый Структура();
			сткРеквизиты.Вставить("Рейс", Рейс);
			сткРеквизиты.Вставить("ИдентификаторТочки", текВыборкаТочка.Идентификатор);
			сткРеквизиты.Вставить("АдресТочки", текВыборкаТочка.Адрес);
			сткРеквизиты.Вставить("Гараж", текВыборкаТочка.Гараж);
			сткРеквизиты.Вставить("ТипТочки", текВыборкаТочка.ТипТочки);
			сткРеквизиты.Вставить("СтрокаНомер", текВыборкаТочка.СтрокаНомер);
			сткРеквизиты.Вставить("ОтложенноеПрохождение", текВыборкаТочка.Отложена);
			
			Если КрайнийПериод < текВыборкаТочка.ПлановоеПрибытие Тогда
				сткРеквизиты.Вставить("НачалоВыполнения", текВыборкаТочка.ПлановоеПрибытие);
				сткРеквизиты.Вставить("НачалоРабот", текВыборкаТочка.НачалоРаботПлан);
				сткРеквизиты.Вставить("ОкончаниеВыполнения", текВыборкаТочка.ПлановоеУбытие);
			Иначе
				сткРеквизиты.Вставить("НачалоВыполнения", ТекущаяДата);
			КонецЕсли; 
			
			сткРеквизиты.Вставить("РасстояниеОтПредыдущейТочки", текВыборкаТочка.РасстояниеОтПредыдущейТочки);
			сткРеквизиты.Вставить("Работы", тбзРаботыВТочке);
			
			текРезультат = СформироватьДокумент(сткРеквизиты, , , ТекстОшибки, Отказ);
			
			Если Отказ Тогда
				Если ЭтоОперацииСКонтейнером Тогда
					ДопТекстОшибки = Нстр("ru = 'Ошибка произошла в рейсе %1 при формировании точки в адресе %2'");
				Иначе
					ДопТекстОшибки = Нстр("ru = 'Ошибка произошла в операции с контейнером %1 при формировании точки в адресе %2'");
				КонецЕсли;
				
				ДопТекстОшибки = СтрШаблон(ДопТекстОшибки, Рейс, текВыборкаТочка.Адрес);	
				ТекстОшибки = ДопТекстОшибки + Символы.ПС + ТекстОшибки;
			КонецЕсли;			
			
		Иначе
			Отказ = Истина;
			Если ЭтоОперацииСКонтейнером Тогда
				ТекстОшибки = Нстр("ru = 'Все точки в операции с контейнром %1 пройдены'");
			Иначе
				ТекстОшибки = Нстр("ru = 'Все точки в рейсе %1 пройдены'");
			КонецЕсли;	
			
			ТекстОшибки = СтрШаблон(ТекстОшибки, Рейс);
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ЗаписьЖурналаРегистрации("СформироватьСледующееПрохождениеТочки", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПрохождениеТочки, , ТекстОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // СформироватьСледующееПрохождениеТочки()

// Формируем документ "упПрохождениеТочки" для заданной точки по параметрам полученных на основании рейса.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - рейс.
//	ПорядокТочки - Число - Номер порядка.
//	ИдентификаторТочки - Строка - Идентификатор.
//	сткРеквизиты - Структура - Реквизиты.
//	ТекстОшибки - Строка - Текст.
//	Отказ - Булево - выполнить фиксирование изменений.
// 
// Возвращаемое значение:
//   Дата - Результат выполнения.
//
Функция СформироватьПрохождениеЗаданнойТочки(Рейс, ПорядокТочки, ИдентификаторТочки, сткРеквизиты, ТекстОшибки, Отказ) Экспорт
	
	текРезультат = Неопределено;
	ТекстОшибкиДляЖурнала = "";
	
	ПроверитьСпособПрохожденияМаршрута(Рейс, ТекстОшибки, Отказ);
	
	ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидТранспортнойУслуги");
	сткРеквизиты.Вставить("ВидТранспортнойУслуги", ВидТранспортнойУслуги);
	ЭтоОперацииСКонтейнером = ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
	
	ПроверитьСтатусРейса(Рейс, ЭтоОперацииСКонтейнером, ТекстОшибки, Отказ);	
	
	Если НЕ Отказ Тогда
		
		докПрохождениеТочкиСсылка = Неопределено;
		
		Если НЕ сткРеквизиты.Свойство("Ссылка", докПрохождениеТочкиСсылка) Тогда
			
			Если ЗначениеЗаполнено(ИдентификаторТочки) Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	тбРаботы.Период,
				|	тбРаботы.Регистратор,
				|	тбРаботы.Рейс,
				|	тбРаботы.Идентификатор,
				|	тбРаботы.СтрокаНомер,
				|	тбРаботы.Отменена,
				|	тбРаботы.Задание,
				|	тбРаботы.Операция,
				|	тбРаботы.ДополнительнаяОперация,
				|	тбРаботы.ВремяРабот,
				|	тбРаботы.ВременноеОкноНачало,
				|	тбРаботы.ВременноеОкноОкончание,
				|	тбРаботы.ГрузовоеМесто,
				|	тбРаботы.ВремяОжидания,
				|	тбРаботы.ПлановоеВремяНачалаРабот,
				|	тбРаботы.КоличествоГрузов,
				|	тбРаботы.КоличествоДопОпераций,
				|	тбРаботы.Проблема,
				|	тбРаботы.АдресРазгрузки,
				|	тбРаботы.Выполнена,
				|	тбРаботы.ИдентификаторРаботы,
				|	тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы,
				|	тбРаботы.ТипРегламентногоДокумента,
				|	тбРаботы.КоличествоЭкземпляровДокумента,
				|	тбРаботы.Сумма,
				|	тбРаботы.ГрузРазделен,
				|	тбРаботы.СкладскаяЯчейка,
				|	ПараметрыЗаданияНаПеревозку.Высота КАК Высота,
				|	ПараметрыЗаданияНаПеревозку.Длина КАК Длина,
				|	ПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
				|	ПараметрыЗаданияНаПеревозку.КоличествоМест КАК КоличествоМест,
				|	ПараметрыЗаданияНаПеревозку.Масса КАК Масса,
				|	ПараметрыЗаданияНаПеревозку.Объем КАК Объем,
				|	ПараметрыЗаданияНаПеревозку.Ширина КАК Ширина,
				|	Ложь КАК ИзмененыПараметры,
				|	тбРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
				|ИЗ
				|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
				|			,
				|			Рейс = &Рейс
				|				И Идентификатор = &ИдентификаторТочки) КАК тбРаботы
				|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
				|		,
				|		) КАК ПараметрыЗаданияНаПеревозку
				|	ПО ПараметрыЗаданияНаПеревозку.ГрузовоеМесто = тбРаботы.ГрузовоеМесто
				|		И ПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку = тбРаботы.Задание
				|ГДЕ
				|	НЕ тбРаботы.Отменена";
				Запрос.УстановитьПараметр("Рейс", Рейс);
				Запрос.УстановитьПараметр("ИдентификаторТочки", ИдентификаторТочки);	
			Иначе	
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	тбРаботы.Период,
				|	тбРаботы.Регистратор,
				|	тбРаботы.Рейс,
				|	тбРаботы.Идентификатор,
				|	тбРаботы.СтрокаНомер,
				|	тбРаботы.Отменена,
				|	тбРаботы.Задание,
				|	тбРаботы.Операция,
				|	тбРаботы.ДополнительнаяОперация,
				|	тбРаботы.ВремяРабот,
				|	тбРаботы.ВременноеОкноНачало,
				|	тбРаботы.ВременноеОкноОкончание,
				|	тбРаботы.ГрузовоеМесто,
				|	тбРаботы.ВремяОжидания,
				|	тбРаботы.ПлановоеВремяНачалаРабот,
				|	тбРаботы.КоличествоГрузов,
				|	тбРаботы.КоличествоДопОпераций,
				|	тбРаботы.Проблема,
				|	тбРаботы.АдресРазгрузки,
				|	тбРаботы.Выполнена,
				|	тбРаботы.ИдентификаторРаботы,
				|	тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы,
				|	тбРаботы.ТипРегламентногоДокумента,
				|	тбРаботы.КоличествоЭкземпляровДокумента,
				|	тбРаботы.Сумма,
				|	тбРаботы.ГрузРазделен,
				|	тбРаботы.СкладскаяЯчейка,
				|	ПараметрыЗаданияНаПеревозку.Высота КАК Высота,
				|	ПараметрыЗаданияНаПеревозку.Длина КАК Длина,
				|	ПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
				|	ПараметрыЗаданияНаПеревозку.КоличествоМест КАК КоличествоМест,
				|	ПараметрыЗаданияНаПеревозку.Масса КАК Масса,
				|	ПараметрыЗаданияНаПеревозку.Объем КАК Объем,
				|	ПараметрыЗаданияНаПеревозку.Ширина КАК Ширина,
				|	Ложь КАК ИзмененыПараметры,
				|	тбРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
				|ИЗ
				|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК тбРаботы
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК упМаршрутПоРейсуСрезПоследних
				|		ПО тбРаботы.Идентификатор = упМаршрутПоРейсуСрезПоследних.Идентификатор
				|			И тбРаботы.Рейс = упМаршрутПоРейсуСрезПоследних.Рейс
				|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
				|		,
				|		) КАК ПараметрыЗаданияНаПеревозку
				|	ПО ПараметрыЗаданияНаПеревозку.ГрузовоеМесто = тбРаботы.ГрузовоеМесто
				|		И ПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку = тбРаботы.Задание
				|ГДЕ
				|	упМаршрутПоРейсуСрезПоследних.Адрес = &Адрес
				|	И упМаршрутПоРейсуСрезПоследних.СтрокаНомер >= &ПорядокТочки
				|	И НЕ тбРаботы.Отменена";
				Запрос.УстановитьПараметр("Рейс",  Рейс);
				Запрос.УстановитьПараметр("Адрес", сткРеквизиты.АдресТочки);	
				Запрос.УстановитьПараметр("ПорядокТочки", ПорядокТочки);	
			КонецЕсли;

			
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	упПрохождениеТочкиРаботы.Ссылка,
			|	упПрохождениеТочкиРаботы.НомерСтроки,
			|	упПрохождениеТочкиРаботы.Выполнена,
			|	упПрохождениеТочкиРаботы.СтрокаНомер,
			|	упПрохождениеТочкиРаботы.Проблема,
			|	упПрохождениеТочкиРаботы.АдресРазгрузки,
			|	упПрохождениеТочкиРаботы.Задание,
			|	упПрохождениеТочкиРаботы.ГрузовоеМесто,
			|	упПрохождениеТочкиРаботы.Операция,
			|	упПрохождениеТочкиРаботы.КоличествоГрузов,
			|	упПрохождениеТочкиРаботы.Отменена,
			|	упПрохождениеТочкиРаботы.ИдентификаторРаботы,
			|	упПрохождениеТочкиРаботы.ДополнительнаяОперация,
			|	упПрохождениеТочкиРаботы.КоличествоДопОпераций,
			|	упПрохождениеТочкиРаботы.ПроблемаПричинаВозникновенияЭтойРаботы,
			|	упПрохождениеТочкиРаботы.ТипРегламентногоДокумента,
			|	упПрохождениеТочкиРаботы.КоличествоЭкземпляровДокумента,
			|	упПрохождениеТочкиРаботы.Сумма,
			|	упПрохождениеТочкиРаботы.ГрузРазделен,
			|	упПрохождениеТочкиРаботы.СкладскаяЯчейка,
			|	упПрохождениеТочкиРаботы.Высота КАК Высота,
			|	упПрохождениеТочкиРаботы.Длина КАК Длина,
			|	упПрохождениеТочкиРаботы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
			|	упПрохождениеТочкиРаботы.КоличествоМест КАК КоличествоМест,
			|	упПрохождениеТочкиРаботы.Масса КАК Масса,
			|	упПрохождениеТочкиРаботы.Объем КАК Объем,
			|	упПрохождениеТочкиРаботы.Ширина КАК Ширина,
			|	упПрохождениеТочкиРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
			|	упПрохождениеТочкиРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
			|ИЗ
			|	Документ.упПрохождениеТочки.Работы КАК упПрохождениеТочкиРаботы
			|ГДЕ
			|	упПрохождениеТочкиРаботы.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", докПрохождениеТочкиСсылка);
		КонецЕсли;
		
		тбзРаботы = Запрос.Выполнить().Выгрузить();
		
		Если тбзРаботы.Количество() > 0 Тогда
			// Если точка выбиралась по адресу.
			Если НЕ ЗначениеЗаполнено(сткРеквизиты.ИдентификаторТочки) Тогда
				сткРеквизиты.ИдентификаторТочки = тбзРаботы[0].Идентификатор; // Заполнить ее идентификатор.
			КонецЕсли;	

			тбзЗаполненныеРаботы = Неопределено;
			
			Если сткРеквизиты.Свойство("Работы", тбзЗаполненныеРаботы) Тогда
				
				Для каждого СтрокаЗаполненнаяРабота Из тбзЗаполненныеРаботы Цикл
					
					СтрокаРабота = Неопределено;
					
					Если ЗначениеЗаполнено(СтрокаЗаполненнаяРабота.ИдентификаторРаботы) Тогда
						мсвСтроки = тбзРаботы.НайтиСтроки(Новый Структура("ИдентификаторРаботы",СтрокаЗаполненнаяРабота.ИдентификаторРаботы));	
					Иначе
						Если Ложь
							Или СтрокаЗаполненнаяРабота.Операция = Перечисления.упТипыОпераций.ПолучениеДокументов
							Или СтрокаЗаполненнаяРабота.Операция = Перечисления.упТипыОпераций.ПередачаДокументов
						Тогда
							сткОтбора = Новый Структура("Задание, Операция, ДополнительнаяОперация");
							ЗаполнитьЗначенияСвойств(сткОтбора, СтрокаЗаполненнаяРабота);
							сткОтбора.Вставить("ТипРегламентногоДокумента", СтрокаЗаполненнаяРабота.ГрузовоеМесто);
						Иначе
							сткОтбора = Новый Структура("Задание, Операция, ГрузовоеМесто, ДополнительнаяОперация");
							ЗаполнитьЗначенияСвойств(сткОтбора, СтрокаЗаполненнаяРабота);	
						КонецЕсли;
						
						мсвСтроки = тбзРаботы.НайтиСтроки(сткОтбора);	
					КонецЕсли;
					
					Если мсвСтроки.Количество() > 0  Тогда
						СтрокаРабота = мсвСтроки[0];
						СтрокаРабота.Выполнена = СтрокаЗаполненнаяРабота.Выполнена;
						СтрокаРабота.Отменена = СтрокаЗаполненнаяРабота.Отменена;
						СтрокаРабота.Проблема = СтрокаЗаполненнаяРабота.Проблема;
						СтрокаРабота.Сумма = СтрокаЗаполненнаяРабота.Сумма;
						СтрокаРабота.ГрузРазделен = СтрокаЗаполненнаяРабота.ГрузРазделен;
						СтрокаРабота.СкладскаяЯчейка = СтрокаЗаполненнаяРабота.СкладскаяЯчейка;
						СтрокаРабота.ПроблемаПричинаВозникновенияЭтойРаботы = СтрокаЗаполненнаяРабота.ПроблемаПричинаВозникновенияЭтойРаботы;
						СтрокаРабота.АдресРазгрузки = СтрокаЗаполненнаяРабота.АдресРазгрузки;
						СтрокаРабота.КоличествоЭкземпляровДокумента = СтрокаЗаполненнаяРабота.КоличествоЭкземпляровДокумента;
						СтрокаРабота.Высота = СтрокаЗаполненнаяРабота.Высота;
						СтрокаРабота.Ширина = СтрокаЗаполненнаяРабота.Ширина;
						СтрокаРабота.Длина = СтрокаЗаполненнаяРабота.Длина;
						СтрокаРабота.Масса = СтрокаЗаполненнаяРабота.Масса;
						СтрокаРабота.Объем = СтрокаЗаполненнаяРабота.Объем;
						СтрокаРабота.КоличествоМест = СтрокаЗаполненнаяРабота.КоличествоМест;
						СтрокаРабота.КоличествоЗагрузочныхМетров = СтрокаЗаполненнаяРабота.КоличествоЗагрузочныхМетров;
						СтрокаРабота.ИзмененыПараметры = СтрокаЗаполненнаяРабота.ИзмененыПараметры;
						СтрокаРабота.ЗапрещеноСозданиеНовойТочкиМаршрута = СтрокаЗаполненнаяРабота.ЗапрещеноСозданиеНовойТочкиМаршрута;
					КонецЕсли;
					
				КонецЦикла;
				
				сткРеквизиты.Работы = тбзРаботы;
				
			КонецЕсли;
			
		КонецЕсли;
			
		текРезультат = СформироватьДокумент(сткРеквизиты, докПрохождениеТочкиСсылка, Истина, ТекстОшибкиДляЖурнала, Отказ);
		
		Если Отказ Тогда
			ТекстОшибки = Нстр("ru = 'Ошибка произошла при формировании прохождения из рабочего места ""РМ: Контроль и исполнение рейсов"" по документу %1 в точке с адресом %2. Подробности см. в журнале регистрации'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Рейс, сткРеквизиты.АдресТочки);	
			
			ТекстДополнениеКОшибкеДляЖурнала = "(%1;%2)";
			ТекстДополнениеКОшибкеДляЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДополнениеКОшибкеДляЖурнала, Рейс, сткРеквизиты.АдресТочки);	
			ТекстОшибкиДляЖурнала = ТекстОшибкиДляЖурнала + ТекстДополнениеКОшибкеДляЖурнала;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		ЗаписьЖурналаРегистрации("СформироватьСледующееПрохождениеТочки", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПрохождениеТочки, , ТекстОшибкиДляЖурнала);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, докПрохождениеТочкиСсылка);
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // СформироватьСледующееПрохождениеТочки()

Функция СформироватьДокумент(сткРеквизиты, Ссылка = Неопределено, ПровестиДокумент = Ложь,  ТекстОшибки = "", Отказ, ЗаписатьНепроведенныйПриОтказе = Ложь)
	
	текРезультат = Неопределено;
	
	Если Ссылка = Неопределено Тогда
		ДокументОбъект = Документы.упПрохождениеТочки.СоздатьДокумент();
		ДокументОбъект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		Если Не ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя() Тогда
			ДокументОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		ДатаДокументаПоПредыдущейТочке = Неопределено;
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		
		// Если точка отменяется из РМ:Контроль и исполнение или из РМ:Водителя, то нет возможности
		// передать дату документа напрямую, поэтому она передается через реквизит "Начало выполнения".
		Если Истина
			И Не ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткРеквизиты, "ДосрочноеУбытие", Ложь)
			И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткРеквизиты, "ТочкаОтменена", Ложь)
			И ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткРеквизиты, "НачалоВыполнения"))
		Тогда
			ДокументОбъект.Дата = сткРеквизиты.НачалоВыполнения;
		КонецЕсли;
				
		// Для того что бы записи в регистре "работы по рейсу" были уникальны,
		// документы разносятся по разным секундам.
		Если сткРеквизиты.Свойство("ДатаДокументаПоПредыдущейТочке", ДатаДокументаПоПредыдущейТочке) Тогда
			
			Если Истина
				И ДатаДокументаПоПредыдущейТочке <> Неопределено 
				И ДокументОбъект.Дата <= ДатаДокументаПоПредыдущейТочке
			Тогда
				ДокументОбъект.Дата = ДатаДокументаПоПредыдущейТочке  + 1;
			КонецЕсли;
			сткРеквизиты.Вставить("ДатаДокументаПоПредыдущейТочке", ДокументОбъект.Дата);
		КонецЕсли;
	Иначе
		ДокументОбъект = Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, сткРеквизиты);
	
	текРаботы = Неопределено;
	Если сткРеквизиты.Свойство("Работы",текРаботы) Тогда
		ДокументОбъект.Работы.Загрузить(текРаботы);
	КонецЕсли;
	
	тбзТоварныйСоставГруза = Неопределено;
			
	Если сткРеквизиты.Свойство("ТоварныйСоставГруза", тбзТоварныйСоставГруза) Тогда
		ДокументОбъект.ТоварныйСоставГруза.Загрузить(тбзТоварныйСоставГруза.Выгрузить());
	КонецЕсли;
	
	Попытка
		РежимЗаписи = ?(ПровестиДокумент, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
		Отказ = Ложь;
		Если сткРеквизиты.Свойство("ПроверитьЗаполнение") Тогда
			Отказ = НЕ ДокументОбъект.ПроверитьЗаполнение();			
		КонецЕсли;
		Если НЕ Отказ Тогда
			ДокументОбъект.Записать(РежимЗаписи);
		КонецЕсли;
		текРезультат = ДокументОбъект.Ссылка;
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		Если ПровестиДокумент И ЗаписатьНепроведенныйПриОтказе Тогда
			Попытка
				ДокументОбъект.Записать();
			Исключение
				ТекстОшибки = ОписаниеОшибки(); // Для отладки
			КонецПопытки; 
		КонецЕсли; 
	КонецПопытки;
	
	Возврат текРезультат;

КонецФункции

// Процедура устанавливает время окончания рейса.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на объект.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты объект.
//	Отказ - Булево - выполнить фиксирование изменений.
//
Процедура ЗаполнитьДатуОкончанияПутевогоЛиста(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ПутевойЛист 				= Неопределено;
	ЭтоПоследняяТочкаМаршрута 	= Ложь;
	ЭтоСобственноеТС			= Ложь;
	ДатаОкончанияРейса			= '00010101';
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПутевойЛист.Ссылка КАК ПутевойЛист,
	               |	упПутевойЛист.ОкончаниеРейса,
	               |	упТранспортныеСредства.СобственноеТранспортноеСредство КАК ЭтоСобственноеТС
	               |ИЗ
	               |	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ПО упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = упТранспортныеСредства.Ссылка,
	               |	Документ.упПутевойЛист КАК упПутевойЛист
	               |ГДЕ
	               |	упПутевойЛист.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		ПутевойЛист 		= текВыборка.ПутевойЛист;
		ДатаОкончанияРейса 	= текВыборка.ОкончаниеРейса;
		ЭтоСобственноеТС	= текВыборка.ЭтоСобственноеТС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончанияРейса) ИЛИ НЕ ЗначениеЗаполнено(ПутевойЛист) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	               |	упМаршрутПоРейсуСрезПоследних.Гараж,
	               |	упМаршрутПоРейсуСрезПоследних.Отменена,
	               |	упМаршрутПоРейсуСрезПоследних.Идентификатор,
	               |	ВЫБОР
	               |		КОГДА &ЭтоСобственноеТС
	               |			ТОГДА упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт
	               |		ИНАЧЕ упМаршрутПоРейсуСрезПоследних.УбытиеФакт
	               |	КОНЕЦ КАК ВремяОкончанияРейса
	               |ПОМЕСТИТЬ втМаршрутПоРейсу
	               |ИЗ
	               |	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	               |ГДЕ
	               |	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(упМаршрутПоРейсуСрезПоследних.СтрокаНомер) КАК СтрокаНомер
	               |ПОМЕСТИТЬ втПоследняяТочкаМаршрута
	               |ИЗ
	               |	втМаршрутПоРейсу КАК упМаршрутПоРейсуСрезПоследних
	               |ГДЕ
	               |	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	               |	И (&ЭтоСобственноеТС
	               |				И НЕ упМаршрутПоРейсуСрезПоследних.Гараж = ЗНАЧЕНИЕ(Справочник.упГаражи.ПустаяСсылка)
	               |			ИЛИ НЕ &ЭтоСобственноеТС
	               |				И упМаршрутПоРейсуСрезПоследних.Гараж = ЗНАЧЕНИЕ(Справочник.упГаражи.ПустаяСсылка))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втМаршрутПоРейсу.Идентификатор,
	               |	втМаршрутПоРейсу.ВремяОкончанияРейса
	               |ИЗ
	               |	втМаршрутПоРейсу КАК втМаршрутПоРейсу
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследняяТочкаМаршрута КАК втПоследняяТочкаМаршрута
	               |		ПО втМаршрутПоРейсу.СтрокаНомер = втПоследняяТочкаМаршрута.СтрокаНомер
	               |ГДЕ
	               |	втМаршрутПоРейсу.Идентификатор = &Идентификатор";
				   
	текИдентификаторТочки = Неопределено;
	Если Реквизиты.Свойство("ИдентификаторТочки", текИдентификаторТочки) Тогда
		
	ИначеЕсли ДополнительныеСвойства.Свойство("ИдентификаторТочки", текИдентификаторТочки) Тогда
		
	КонецЕсли;
				   
	Запрос.УстановитьПараметр("Идентификатор", 		текИдентификаторТочки);			   
	Запрос.УстановитьПараметр("ЭтоСобственноеТС", 	ЭтоСобственноеТС);			   
	текВыборка = Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		
		ПутевойЛистОбъект = ПутевойЛист.ПолучитьОбъект();
		ПутевойЛистОбъект.ОкончаниеРейса	= текВыборка.ВремяОкончанияРейса;
		
		Попытка
			ПутевойЛистОбъект.Записать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
			ЗаписьЖурналаРегистрации("ОшибкаЗаписиПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка, ,ПутевойЛист, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Возврат;
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Возвращает значение номера(НомерСтроки) последнего маршрута по рейсу.
//
// Параметры:
//	Рейс - ДокументСсылка.упРейс - Ссылка на объект.
//
// Возвращаемое значение:
//   Число - Результат выполнения.
//
Функция НомерПоследнейТочкиМаршрута(Рейс) Экспорт
	
	текРезультат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МАКСИМУМ(упМаршрутПоРейсуСрезПоследних.СтрокаНомер) КАК СтрокаНомер
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат =  текВыборка.СтрокаНомер;
	КонецЕсли;
	
	текРезультат = текРезультат + 1;
	
	Возврат текРезультат;
				
КонецФункции

#КонецОбласти	

#Область ЗаполнениеДереваПрохождений

// Процедура заполнения дерева работ.
//
// Параметры:
//  ЭлементДереваДанные - СтрокаДереваЗначений - Позволяет читать и записывать данные в конкретных колонках строки.
//	ЭлементДереваПриемник - СтрокаДереваЗначений - Позволяет читать и записывать данные в конкретных колонках строки.
//	сткДанныеЗаполнения - Структура - Данные.
//
Процедура ЗаполнитьДеревоРаботВТочке(ЭлементДереваДанные, ЭлементДереваПриемник, сткДанныеЗаполнения) Экспорт
	
	ЕстьКолонкаЗаявка = Ложь;
	ЕстьКолонкаЕстьФайлы = Ложь;
	
	СтрокиЗадания	= ЭлементДереваДанные.Строки;
	
	Для каждого СтрокаЗадание Из СтрокиЗадания Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаЗадание.Задание) Тогда
			Продолжить;	
		КонецЕсли;
		
		НоваяСтрокаЗадание = ДобавитьСтрокуВДеревоПриемник(ЭлементДереваПриемник);
		НоваяСтрокаЗадание.НазначениеСтроки = СтрокаЗадание.Задание;
		НоваяСтрокаЗадание.ИзмененыПараметры = СтрокаЗадание.ИзмененыПараметры;
						
		ЕстьКолонкаЕстьФайлы = Ложь 
		                    Или ЕстьКолонкаЕстьФайлы
						Или ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаЗадание, "ЕстьФайлы");
		Если ЕстьКолонкаЕстьФайлы Тогда
			НоваяСтрокаЗадание.ЕстьФайлы = СтрокаЗадание.ЕстьФайлы;
			НоваяСтрокаЗадание.ЗаданиеЗаявкаНаПеревозкуГруза = СтрокаЗадание.ЗаданиеЗаявкаНаПеревозкуГруза;
		КонецЕсли;				
					
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗадание, сткДанныеЗаполнения);
	
		КоличествоВыполненныхОпераций = 0;
		КоличествоНеВыполненныхОпераций = 0;
		КоличествоОтмененныхОпераций = 0;
		
		СтрокиОперации = СтрокаЗадание.Строки;
		НоваяСтрокаЗадание.ИндексКартинки = -1;
		
		Для каждого СтрокаОперация Из СтрокиОперации Цикл
			
			НоваяСтрокаОперация = ДобавитьСтрокуВДеревоПриемник(НоваяСтрокаЗадание);
			НоваяСтрокаОперация.НазначениеСтроки = СтрокаОперация.Операция;
			НоваяСтрокаОперация.ИзмененыПараметры = СтрокаОперация.ИзмененыПараметры;
			ЗаполнитьЗначенияСвойств(НоваяСтрокаОперация, сткДанныеЗаполнения);
		
			Если ЕстьКолонкаЕстьФайлы Тогда
				НоваяСтрокаОперация.ЕстьФайлы = 0;
			КонецЕсли;
			
			
			КоличествоВыполненныхСтрок = 0;
			КоличествоОтмененныхСтрок = 0;
			
			СтрокиГрузовоеМесто = СтрокаОперация.Строки;
			
			Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовоеМесто Цикл
				НоваяСтрокаГрузовоеМесто = ДобавитьСтрокуВДеревоПриемник(НоваяСтрокаОперация);
				ЗаполнитьЗначенияСвойств(НоваяСтрокаГрузовоеМесто, СтрокаГрузовоеМесто);
				ЗаполнитьЗначенияСвойств(НоваяСтрокаГрузовоеМесто, сткДанныеЗаполнения);
				НоваяСтрокаГрузовоеМесто.Выполнена = ?(СтрокаГрузовоеМесто.Выполнена, 1, 0);
				Если ЕстьКолонкаЕстьФайлы Тогда
					НоваяСтрокаГрузовоеМесто.ЕстьФайлы = 0;
				КонецЕсли;

				
				Если НоваяСтрокаГрузовоеМесто.Операция = Перечисления.упТипыОпераций.ПолучениеДокументов Или НоваяСтрокаГрузовоеМесто.Операция = Перечисления.упТипыОпераций.ПередачаДокументов Тогда
					НоваяСтрокаГрузовоеМесто.ЭтоПолучениеПередачаДокументов = Истина;
					НоваяСтрокаГрузовоеМесто.Количество = НоваяСтрокаГрузовоеМесто.КоличествоЭкземпляровДокумента;
					НоваяСтрокаГрузовоеМесто.НазначениеСтроки = СтрокаГрузовоеМесто.ТипРегламентногоДокумента;
				Иначе
					Если НоваяСтрокаГрузовоеМесто.Операция = Перечисления.упТипыОпераций.ДополнительнаяОперация Тогда
						НоваяСтрокаГрузовоеМесто.НазначениеСтроки = СтрокаГрузовоеМесто.ДополнительнаяОперация;
						НоваяСтрокаГрузовоеМесто.Количество = СтрокаГрузовоеМесто.КоличествоДопОпераций;
					Иначе
						НоваяСтрокаГрузовоеМесто.НазначениеСтроки = СтрокаГрузовоеМесто.ГрузовоеМесто;
    						НоваяСтрокаГрузовоеМесто.Количество = СтрокаГрузовоеМесто.КоличествоГрузов;
					КонецЕсли;
					НоваяСтрокаГрузовоеМесто.ЭтоРабота = Истина;
				КонецЕсли;
				
				Если СтрокаГрузовоеМесто.Выполнена Тогда
					КоличествоВыполненныхСтрок = КоличествоВыполненныхСтрок + 1;
				ИначеЕсли СтрокаГрузовоеМесто.Отменена Тогда	
					КоличествоОтмененныхСтрок = КоличествоОтмененныхСтрок + 1;
				КонецЕсли;
				
				НоваяСтрокаОперация.ИндексКартинки = НоваяСтрокаГрузовоеМесто.ИндексКартинки;
				Если НоваяСтрокаГрузовоеМесто.ГрузРазделен Тогда
					НоваяСтрокаГрузовоеМесто.ИндексКартинки = 12;
				Иначе
					НоваяСтрокаГрузовоеМесто.ИндексКартинки = -1;
				КонецЕсли;
				
			КонецЦикла;
			
			Если КоличествоВыполненныхСтрок = 0 Тогда
				КоличествоНеВыполненныхОпераций = КоличествоНеВыполненныхОпераций + 1;
				НоваяСтрокаОперация.Выполнена	= 0;
			ИначеЕсли КоличествоВыполненныхСтрок = СтрокаОперация.Строки.Количество() Тогда 	
				КоличествоВыполненныхОпераций = КоличествоВыполненныхОпераций + 1;
				НоваяСтрокаОперация.Выполнена	= 1;
			Иначе	
				НоваяСтрокаОперация.Выполнена	= 2;
			КонецЕсли;
			
			Если КоличествоОтмененныхСтрок = СтрокаОперация.Строки.Количество() Тогда
				КоличествоОтмененныхОпераций = КоличествоОтмененныхОпераций + 1;
				НоваяСтрокаОперация.Отменена = Истина;
			Иначе
				НоваяСтрокаОперация.Отменена = Ложь;	
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоНеВыполненныхОпераций = СтрокаЗадание.Строки.Количество() Тогда
			НоваяСтрокаЗадание.Выполнена	= 0;
		ИначеЕсли КоличествоВыполненныхОпераций = СтрокаЗадание.Строки.Количество() Тогда 	
			НоваяСтрокаЗадание.Выполнена	= 1;
		Иначе	
			НоваяСтрокаЗадание.Выполнена	= 2;
		КонецЕсли;
		
		Если КоличествоОтмененныхОпераций = СтрокаЗадание.Строки.Количество() Тогда
			НоваяСтрокаЗадание.Отменена = Истина;
		Иначе
			НоваяСтрокаЗадание.Отменена = Ложь;	
		КонецЕсли;	
		
	КонецЦикла; 
		
КонецПроцедуры

Функция ДобавитьСтрокуВДеревоПриемник(ЭлементДереваПриемник)
	
	текРезультат = Неопределено;
	
	Если ТипЗнч(ЭлементДереваПриемник) = Тип("ДанныеФормыЭлементДерева")
		ИЛИ ТипЗнч(ЭлементДереваПриемник) = Тип("ДанныеФормыДерево") Тогда
		текРезультат = ЭлементДереваПриемник.ПолучитьЭлементы().Добавить();
	Иначе	
		текРезультат = ЭлементДереваПриемник.Строки.Добавить()
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

Процедура ОчиститьЭлементыДерева(Дерево)
	Если ТипЗнч(Дерево) = Тип("ДанныеФормыДерево") Тогда
		Дерево.ПолучитьЭлементы().Очистить();
	Иначе	
		Дерево.Строки.Очистить();
	КонецЕсли;
КонецПроцедуры

// Заполняет дерево работ по переданному рейсу. 
//
// Параметры:
//   дрвРаботыПоТекущемуРейсу - СтрокаДереваЗначений - Позволяет читать и записывать данные,
//                                                     в конкретных колонках строки.
//	дрвРаботыПоТекущемуРейсуЭталон - СтрокаДереваЗначений - Позволяет читать и записывать данные,
//                                                           в конкретных колонках строки.
//	Рейс - ДокументСсылка - Ссылка на объект.
//	ИзменитьТолькоВыполненные - Булево - Выполнить.
//
Процедура ЗаполнитьДеревоРаботПоРейсу(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон = Неопределено, Рейс, ИзменитьТолькоВыполненные = Ложь, ДополнительныеПараметры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Запрос.Текст = ТекстЗапросаДеревоРаботПоРейсу();		
		ИндексЗапросаРаботы = 6;
		ИндексЗапросаМаршрут = 8;
		ИндексЗапросаПроблемыТоварныйСоставГруза = 9;
	Иначе
		ЗаданияПоРейсам = ДополнительныеПараметры.ЗаданияПоРейсам;
		тбзЗаданияПоРейсам = ЗаданияПоРейсам.Скопировать(Новый Структура("Рейс", Рейс));
		Запрос.УстановитьПараметр("ЗаданияПоРейсам", тбзЗаданияПоРейсам); 
	
		Запрос.Текст = ТекстЗапросаДеревоРаботПоРейсуПоЗаданиямПолучателя();
		ИндексЗапросаРаботы = 8;
		ИндексЗапросаМаршрут = 10;
		ИндексЗапросаПроблемыТоварныйСоставГруза = 11;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Рейс", Рейс); 
	ЗаполнятьСписокПроблем = дрвРаботыПоТекущемуРейсу.Колонки.Найти("ЕстьПроблемы") <> Неопределено;
	Запрос.УстановитьПараметр("ЗаполнятьСписокПроблем", ЗаполнятьСписокПроблем); 
	мсвРезультаты = Запрос.ВыполнитьПакет();
	тбзМаршрутПоРейсу = мсвРезультаты[ИндексЗапросаМаршрут].Выгрузить();
	дрвРаботы = мсвРезультаты[ИндексЗапросаРаботы].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	тбзПроблемыТоварногоСостава = мсвРезультаты[ИндексЗапросаПроблемыТоварныйСоставГруза].Выгрузить();
		
	Если НЕ ИзменитьТолькоВыполненные Тогда
		
		дрвРаботыПоТекущемуРейсу.Строки.Очистить();
		
		СтрокиТочки = дрвРаботы.Строки;
		ЕстьТекущаяТочка = Ложь;
		Для каждого СтрокаТочка Из СтрокиТочки Цикл
			
			сткДанныеЗаполнения = Новый Структура();
			сткДанныеЗаполнения.Вставить("ЗаполнятьСписокПроблем", ЗаполнятьСписокПроблем);
			сткДанныеЗаполнения.Вставить("ЭтоРабота", Ложь);
			сткДанныеЗаполнения.Вставить("ЭтоПолучениеПередачаДокументов", Ложь);
			сткДанныеЗаполнения.Вставить("ЭтоАдрес", Ложь);
			сткДанныеЗаполнения.Вставить("ТочкаПройдена", Ложь);
			сткДанныеЗаполнения.Вставить("ТочкаОтменена", Ложь);
			сткДанныеЗаполнения.Вставить("ТочкаТекущая", Ложь);
			сткДанныеЗаполнения.Вставить("ИзмененыПараметры", СтрокаТочка.ИзмененыПараметры);
			сткДанныеЗаполнения.Вставить("Склад", Справочники.упСклады.ПустаяСсылка());
			ГрузРазделен = СтрокаТочка.КоличествоГрузРазделен > 0;
			сткДанныеЗаполнения.Вставить("ГрузРазделен", ГрузРазделен);
			Если ГрузРазделен Тогда
				сткДанныеЗаполнения.Вставить("ИндексКартинки", 12);
			КонецЕсли;
			сткДанныеЗаполнения.Вставить("ПрохождениеТочки",Документы.упПрохождениеТочки.ПустаяСсылка());
			
			ЕстьПроблемыВТоварномСоставе = Ложь;
			мсвПроблемы = Новый Массив;
			
			Если ЗаполнятьСписокПроблем Тогда
				
				// Проблемы в товарах.
				мсвСтрокиПроблемы = тбзПроблемыТоварногоСостава.НайтиСтроки(Новый Структура("ИдентификаторТочки",СтрокаТочка.ИдентификаторТочки));
				ЕстьПроблемыВТоварномСоставе = мсвСтрокиПроблемы.Количество() > 0;
				Для каждого СтрокаПроблемаТоварногоСостава Из мсвСтрокиПроблемы Цикл
					мсвПроблемы.Добавить(СтрокаПроблемаТоварногоСостава.Проблема);
				КонецЦикла;			
								
			КонецЕсли;
			сткДанныеЗаполнения.Вставить("ЕстьПроблемы", ЕстьПроблемыВТоварномСоставе);
									
			// Заполняются данные по точке.
			НоваяСтрокаТочка = дрвРаботыПоТекущемуРейсу.Строки.Добавить();
			
			ТочкаПройдена = Ложь;
			ТочкаОтменена = Ложь;
			
			мсвСтрокиТочка	 = тбзМаршрутПоРейсу.НайтиСтроки(Новый Структура("ИдентификаторТочки",СтрокаТочка.ИдентификаторТочки));
			Если мсвСтрокиТочка.Количество() > 0 Тогда
				текДанныеТочки = мсвСтрокиТочка[0];
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, текДанныеТочки);
				НоваяСтрокаТочка.НазначениеСтроки = текДанныеТочки.АдресТочки;
				
				сткДанныеЗаполнения.ТочкаПройдена = текДанныеТочки.ТочкаПройдена;
				сткДанныеЗаполнения.ТочкаОтменена = текДанныеТочки.ТочкаОтменена;
				сткДанныеЗаполнения.ПрохождениеТочки = текДанныеТочки.ПрохождениеТочки;
				сткДанныеЗаполнения.Склад = текДанныеТочки.Склад;
				
				ТочкаПройдена = текДанныеТочки.ТочкаПройдена;
				ТочкаОтменена = текДанныеТочки.ТочкаОтменена;
				
				// Проблемы в грузах.
				Если Ложь
					Или ТочкаПройдена 
					Или ТочкаОтменена Тогда
										
					Для каждого СтрокаЗадание Из СтрокаТочка.Строки Цикл
						Для каждого СтрокаОперация Из СтрокаЗадание.Строки Цикл
							Для каждого СтрокаГрузовоеМесто Из СтрокаОперация.Строки Цикл
								Если ЗначениеЗаполнено(СтрокаГрузовоеМесто.Проблема) Тогда
									мсвПроблемы.Добавить(СтрокаГрузовоеМесто.Проблема);
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
				
				мсвПроблемы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПроблемы);
				сткДанныеЗаполнения.Вставить("СписокПроблем", СтрСоединить(мсвПроблемы, ", "));
				
				Если	Истина
					И НЕ сткДанныеЗаполнения.ТочкаПройдена 
					И НЕ сткДанныеЗаполнения.ТочкаОтменена 
					И НЕ ЕстьТекущаяТочка 
				Тогда
					сткДанныеЗаполнения.ТочкаТекущая = Истина;
					ЕстьТекущаяТочка = Истина;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, сткДанныеЗаполнения);
				НоваяСтрокаТочка.Выполнена = сткДанныеЗаполнения.ТочкаПройдена;
				НоваяСтрокаТочка.ИдентификаторТочки = текДанныеТочки.ИдентификаторТочки;
				НоваяСтрокаТочка.ЭтоАдрес = Истина;
				НоваяСтрокаТочка.ПорядокТочки	= текДанныеТочки.СтрокаНомер;
				НоваяСтрокаТочка.ФормироватьПрохождениеТочки = Истина;

			КонецЕсли;
									
			// Заполняется дерево работ в точке.
			Если Истина
				И НЕ ТочкаПройдена 
				И НЕ ТочкаОтменена
			Тогда
				упПрохождениеТочки.ЗаполнитьДеревоРаботВТочке(СтрокаТочка, НоваяСтрокаТочка, сткДанныеЗаполнения);	
			КонецЕсли;
					
		КонецЦикла;
		Если дрвРаботыПоТекущемуРейсуЭталон <> Неопределено Тогда
			дрвРаботыПоТекущемуРейсуЭталон = дрвРаботыПоТекущемуРейсу.Скопировать();
		КонецЕсли;
		
	Иначе

		СтрокиТочки = дрвРаботы.Строки;
		ЕстьТекущаяТочка = Ложь;
		ТочкаПройденаИлиОтменена = Ложь;
		Для каждого СтрокаТочка Из СтрокиТочки Цикл
			
			сткДанныеЗаполнения = Новый Структура();
			сткДанныеЗаполнения.Вставить("ЭтоРабота", Ложь);
			сткДанныеЗаполнения.Вставить("ЭтоПолучениеПередачаДокументов", Ложь);
			сткДанныеЗаполнения.Вставить("ЭтоАдрес", Ложь);
			сткДанныеЗаполнения.Вставить("ТочкаПройдена", Ложь);
			сткДанныеЗаполнения.Вставить("ТочкаОтменена", Ложь);
			сткДанныеЗаполнения.Вставить("ТочкаТекущая", Ложь);
			сткДанныеЗаполнения.Вставить("ИзмененыПараметры", СтрокаТочка.ИзмененыПараметры);
			ГрузРазделен = СтрокаТочка.КоличествоГрузРазделен > 0;
			сткДанныеЗаполнения.Вставить("ГрузРазделен", ГрузРазделен);
			Если ГрузРазделен Тогда
				сткДанныеЗаполнения.Вставить("ИндексКартинки", 12);
			КонецЕсли;
			сткДанныеЗаполнения.Вставить("ПрохождениеТочки",Документы.упПрохождениеТочки.ПустаяСсылка());
			
			ЕстьПроблемыВТоварномСоставе = Ложь;
			мсвПроблемы = Новый Массив;
			
			Если ЗаполнятьСписокПроблем Тогда
				
				мсвСтрокиПроблемы = тбзПроблемыТоварногоСостава.НайтиСтроки(Новый Структура("ИдентификаторТочки",СтрокаТочка.ИдентификаторТочки));
				ЕстьПроблемыВТоварномСоставе = мсвСтрокиПроблемы.Количество() > 0;
				Для каждого СтрокаПроблемаТоварногоСостава Из мсвСтрокиПроблемы Цикл
					мсвПроблемы.Добавить(СтрокаПроблемаТоварногоСостава.Проблема);
				КонецЦикла;			
														
			КонецЕсли;
			сткДанныеЗаполнения.Вставить("ЕстьПроблемы", ЕстьПроблемыВТоварномСоставе);
						
			мсвСтрокиТочка	= тбзМаршрутПоРейсу.НайтиСтроки(Новый Структура("ИдентификаторТочки",СтрокаТочка.ИдентификаторТочки));
			Если мсвСтрокиТочка.Количество() > 0 Тогда
				
				текДанныеТочки = мсвСтрокиТочка[0];
				ТочкаПройденаИлиОтменена = текДанныеТочки.ТочкаПройдена ИЛИ текДанныеТочки.ТочкаОтменена;
				
				Если ТочкаПройденаИлиОтменена Тогда
					
					НоваяСтрокаТочка = дрвРаботыПоТекущемуРейсу.Строки.Найти(СтрокаТочка.ИдентификаторТочки, "ИдентификаторТочки");
					Если НоваяСтрокаТочка = Неопределено Тогда
						НоваяСтрокаТочка = дрвРаботыПоТекущемуРейсу.Строки.Найти(текДанныеТочки.СтрокаНомер, "ПорядокТочки");
					КонецЕсли;
					НоваяСтрокаТочка.Строки.Очистить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, текДанныеТочки);
					НоваяСтрокаТочка.НазначениеСтроки = текДанныеТочки.АдресТочки;
					
					сткДанныеЗаполнения.ТочкаПройдена = текДанныеТочки.ТочкаПройдена;
					сткДанныеЗаполнения.ТочкаОтменена = текДанныеТочки.ТочкаОтменена;
					сткДанныеЗаполнения.ПрохождениеТочки = текДанныеТочки.ПрохождениеТочки;
					
					ТочкаПройдена = текДанныеТочки.ТочкаПройдена;
					ТочкаОтменена = текДанныеТочки.ТочкаОтменена;
					
					// Проблемы в грузах.
					Если Ложь
						Или ТочкаПройдена 
						Или ТочкаОтменена Тогда
						
						Для каждого СтрокаЗадание Из СтрокаТочка.Строки Цикл
							Для каждого СтрокаОперация Из СтрокаЗадание.Строки Цикл
								Для каждого СтрокаГрузовоеМесто Из СтрокаОперация.Строки Цикл
									Если ЗначениеЗаполнено(СтрокаГрузовоеМесто.Проблема) Тогда
										мсвПроблемы.Добавить(СтрокаГрузовоеМесто.Проблема);
									КонецЕсли;
								КонецЦикла;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
					
					мсвПроблемы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПроблемы);
					сткДанныеЗаполнения.Вставить("СписокПроблем", СтрСоединить(мсвПроблемы, ", "));
					
					Если Истина
						И НЕ сткДанныеЗаполнения.ТочкаПройдена 
						И НЕ сткДанныеЗаполнения.ТочкаОтменена 
						И НЕ ЕстьТекущаяТочка 
					Тогда
						сткДанныеЗаполнения.ТочкаТекущая = Истина;
						ЕстьТекущаяТочка = Истина;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, сткДанныеЗаполнения);
					НоваяСтрокаТочка.Выполнена = сткДанныеЗаполнения.ТочкаПройдена;
					НоваяСтрокаТочка.ИдентификаторТочки = текДанныеТочки.ИдентификаторТочки;
					НоваяСтрокаТочка.ЭтоАдрес = Истина;
					НоваяСтрокаТочка.ПорядокТочки = текДанныеТочки.СтрокаНомер;
					НоваяСтрокаТочка.ФормироватьПрохождениеТочки = Истина;
					
					Если дрвРаботыПоТекущемуРейсуЭталон <> Неопределено Тогда
						
						СтрокаЭталон = дрвРаботыПоТекущемуРейсуЭталон.Строки.Найти(СтрокаТочка.ИдентификаторТочки, "ИдентификаторТочки");
						Если СтрокаЭталон = Неопределено Тогда
							СтрокаЭталон = дрвРаботыПоТекущемуРейсуЭталон.Строки.Добавить();
						Иначе	
							СтрокаЭталон.Строки.Очистить();
						КонецЕсли;
						
						ЗаполнитьЗначенияСвойств(СтрокаЭталон, НоваяСтрокаТочка);
						
					КонецЕсли;
										
				КонецЕсли;
								
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры

Функция ТекстЗапросаДеревоРаботПоРейсу()
	Результат = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упМаршрутПоРейсу.Регистратор КАК Регистратор,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсу.Период КАК Период,
	|	упМаршрутПоРейсу.Пройдена КАК Пройдена,
	|	упМаршрутПоРейсу.Отменена КАК Отменена
	|ПОМЕСТИТЬ втЗаписиПоРСМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ упМаршрутПоРейсу.Рейс = &Рейс
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаписиПоРСМаршрут.Идентификатор КАК Идентификатор,
	|	втЗаписиПоРСМаршрут.Пройдена КАК Пройдена,
	|	втЗаписиПоРСМаршрут.Отменена КАК Отменена,
	|	втЗаписиПоРСМаршрут.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втДокументыРегистраторы
	|ИЗ
	|	втЗаписиПоРСМаршрут КАК втЗаписиПоРСМаршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втЗаписиПоРСМаршрут.Идентификатор КАК Идентификатор,
	|		МАКСИМУМ(втЗаписиПоРСМаршрут.Период) КАК Период
	|	ИЗ
	|		втЗаписиПоРСМаршрут КАК втЗаписиПоРСМаршрут
	|	СГРУППИРОВАТЬ ПО
	|		втЗаписиПоРСМаршрут.Идентификатор) КАК втПоследниеЗаписи
	|	ПО ИСТИНА
	|		И втПоследниеЗаписи.Идентификатор = втЗаписиПоРСМаршрут.Идентификатор
	|		И втПоследниеЗаписи.Период = втЗаписиПоРСМаршрут.Период
	|ГДЕ ИСТИНА
	|	И (ЛОЖЬ
	|		ИЛИ втЗаписиПоРСМаршрут.Пройдена
	|		ИЛИ втЗаписиПоРСМаршрут.Отменена)
	|	И (ЛОЖЬ
	|		ИЛИ втЗаписиПоРСМаршрут.Регистратор ССЫЛКА Документ.упПрохождениеТочки
	|		ИЛИ втЗаписиПоРСМаршрут.Регистратор ССЫЛКА Документ.упПроисшествиеВПути)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упПрохождениеТочки.ИдентификаторТочки КАК ИдентификаторТочки,
	|	упПрохождениеТочки.Ссылка КАК Ссылка,
	|	упПрохождениеТочки.Дата КАК Дата,
	|	упПрохождениеТочки.НачалоВыполнения КАК НачалоВыполнения,
	|	упПрохождениеТочки.ОкончаниеВыполнения КАК ОкончаниеВыполнения,
	|	упПрохождениеТочки.НачалоРабот КАК НачалоРабот,
	|	упПрохождениеТочки.ТипТочки КАК ТипТочки,
	|	упПрохождениеТочки.ДосрочноеУбытие КАК ДосрочноеУбытие
	|ПОМЕСТИТЬ втДокументыПрохождениеТочки
	|ИЗ
	|	Документ.упПрохождениеТочки КАК упПрохождениеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыРегистраторы КАК втДокументыРегистраторы
	|	ПО втДокументыРегистраторы.Регистратор = упПрохождениеТочки.Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упПроисшествиеВПути.ИдентификаторТочки КАК ИдентификаторТочки,
	|	упПроисшествиеВПути.Ссылка КАК Ссылка,
	|	упПроисшествиеВПути.Дата КАК Дата,
	|	NULL КАК НачалоВыполнения,
	|	NULL КАК ОкончаниеВыполнения,
	|	NULL КАК НачалоРабот,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ПроисшествиеВПути) КАК ТипТочки,
	|	ЛОЖЬ КАК ДосрочноеУбытие
	|ИЗ
	|	Документ.упПроисшествиеВПути КАК упПроисшествиеВПути
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыРегистраторы КАК втДокументыРегистраторы
	|	ПО втДокументыРегистраторы.Регистратор = упПроисшествиеВПути.Ссылка
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК ИдентификаторТочки,
	|	упМаршрутПоРейсуСрезПоследних.Адрес КАК АдресТочки,
	|	упМаршрутПоРейсуСрезПоследних.Гараж КАК Гараж,
	|	ЕСТЬNULL(втДанныеТочки.ТипТочки, упМаршрутПоРейсуСрезПоследних.ТипТочки) КАК ТипТочки,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.Пройдена КАК ТочкаПройдена,
	|	упМаршрутПоРейсуСрезПоследних.Отменена КАК ТочкаОтменена,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|	упМаршрутПоРейсуСрезПоследних.НачалоРаботПлан КАК НачалоРаботПлан,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК ПлановоеУбытие,
	|	ЕСТЬNULL(втДанныеТочки.НачалоВыполнения, упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт) КАК ПрибытиеФакт,
	|	ЕСТЬNULL(втДанныеТочки.НачалоРабот, упМаршрутПоРейсуСрезПоследних.НачалоРаботФакт) КАК НачалоРаботФакт,
	|	ЕСТЬNULL(втДанныеТочки.ОкончаниеВыполнения, упМаршрутПоРейсуСрезПоследних.УбытиеФакт) КАК УбытиеФакт,
	|	втДанныеТочки.Ссылка КАК ПрохождениеТочки,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
	|	упМаршрутПоРейсуСрезПоследних.Предшественники КАК Предшественники,
	|	упМаршрутПоРейсуСрезПоследних.Адрес.Склад КАК Склад,
	|	втДанныеТочки.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|	упРейс.ВидПеревозки КАК ВидПеревозки
	|ПОМЕСТИТЬ втМаршрутРейса
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ втДокументыПрохождениеТочки КАК втДанныеТочки
	|	ПО упМаршрутПоРейсуСрезПоследних.Идентификатор = втДанныеТочки.ИдентификаторТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|	ПО упМаршрутПоРейсуСрезПоследних.Рейс = упРейс.Ссылка
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|	ЕСТЬNULL(тбРаботы.СтрокаНомер, 0) КАК СтрокаНомер,
	|	ЕСТЬNULL(тбРаботы.Отменена, ЛОЖЬ) КАК Отменена,
	|	тбРаботы.Задание КАК Задание,
	|	тбРаботы.Операция КАК Операция,
	|	ЕСТЬNULL(тбРаботы.Операция.Порядок + 5, - 1) КАК ИндексКартинки,
	|	тбРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|	тбРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|	тбРаботы.Тара КАК Тара,
	|	тбРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|	тбРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
	|	тбРаботы.Проблема КАК Проблема,
	|	тбРаботы.АдресРазгрузки КАК АдресРазгрузки,
	|	тбРаботы.Выполнена КАК Выполнена,
	|	тбРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|	втМаршрутРейса.СтрокаНомер КАК ПорядокТочки,
	|	тбРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	тбРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
	|	тбРаботы.Сумма КАК Сумма,
	|	упГрузовыеМеста.Валюта КАК Валюта,
	|	тбРаботы.ГрузРазделен КАК ГрузРазделен,
	|	ВЫБОР
	|		КОГДА тбРаботы.ГрузРазделен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоГрузРазделен,
	|	тбРаботы.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|	тбРаботы.Задание.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЛОЖЬ КАК ИзмененыПараметры,
	|	втМаршрутРейса.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|	втМаршрутРейса.Рейс КАК Рейс,
	|	тбРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втМаршрутРейса КАК втМаршрутРейса
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК тбРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ПО тбРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|	ПО ИСТИНА
	|		И тбРаботы.Рейс = втМаршрутРейса.Рейс
	|		И тбРаботы.Идентификатор = втМаршрутРейса.ИдентификаторТочки
	|		И втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|		И НЕ (тбРаботы.Отменена)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|	упПрохождениеТочкиРаботы.СтрокаНомер КАК СтрокаНомер,
	|	упПрохождениеТочкиРаботы.Отменена КАК Отменена,
	|	упПрохождениеТочкиРаботы.Задание КАК Задание,
	|	упПрохождениеТочкиРаботы.Операция КАК Операция,
	|	упПрохождениеТочкиРаботы.Операция.Порядок + 5 КАК ИндексКартинки,
	|	упПрохождениеТочкиРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|	упПрохождениеТочкиРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упПрохождениеТочкиРаботы.Тара КАК Тара,
	|	упПрохождениеТочкиРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|	упПрохождениеТочкиРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
	|	упПрохождениеТочкиРаботы.Проблема КАК Проблема,
	|	упПрохождениеТочкиРаботы.АдресРазгрузки КАК АдресРазгрузки,
	|	упПрохождениеТочкиРаботы.Выполнена КАК Выполнена,
	|	упПрохождениеТочкиРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	упПрохождениеТочкиРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|	втМаршрутРейса.СтрокаНомер КАК ПорядокТочки,
	|	упПрохождениеТочкиРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	упПрохождениеТочкиРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
	|	NULL КАК Сумма,
	|	NULL КАК Валюта,
	|	упПрохождениеТочкиРаботы.ГрузРазделен КАК ГрузРазделен,
	|	ВЫБОР
	|		КОГДА упПрохождениеТочкиРаботы.ГрузРазделен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоГрузРазделен,
	|	упПрохождениеТочкиРаботы.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|	упПрохождениеТочкиРаботы.Задание.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упПрохождениеТочкиРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
	|	NULL КАК ДосрочноеУбытие,
	|    втМаршрутРейса.Рейс КАК Рейс,
	|	упПрохождениеТочкиРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
	|ИЗ
	|	Документ.упПрохождениеТочки.Работы КАК упПрохождениеТочкиРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутРейса КАК втМаршрутРейса
	|	ПО упПрохождениеТочкиРаботы.Ссылка = втМаршрутРейса.ПрохождениеТочки
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|	МАКСИМУМ(упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок) КАК НомерВЦепиПоставок
	|ПОМЕСТИТЬ втМаксимальныеНомераПоставок
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза = упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.АдресРазгрузки КАК АдресРазгрузки,
	|	втРаботы.Валюта КАК Валюта,
	|	втРаботы.Выполнена КАК Выполнена,
	|	втРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРаботы.ГрузРазделен КАК ГрузРазделен,
	|	втРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|	втРаботы.Задание КАК Задание,
	|	втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|	втРаботы.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	втРаботы.ИдентификаторТочки КАК ИдентификаторТочки,
	|	втРаботы.ИндексКартинки КАК ИндексКартинки,
	|	втРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|	втРаботы.КоличествоГрузРазделен КАК КоличествоГрузРазделен,
	|	втРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
	|	втРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
	|	втРаботы.Операция КАК Операция,
	|	втРаботы.Отменена КАК Отменена,
	|	втРаботы.ПорядокТочки КАК ПорядокТочки,
	|	втРаботы.Проблема КАК Проблема,
	|	втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|	втРаботы.СтрокаНомер КАК СтрокаНомер,
	|	втРаботы.Сумма КАК Сумма,
	|	втРаботы.Тара КАК Тара,
	|	втРаботы.Рейс КАК Рейс,
	|	втРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|	втМаксимальныеНомераПоставок.НомерВЦепиПоставок КАК НомерВЦепиПоставокМаксимальный,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|				ИЛИ ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьТараВСтроке,
	|	ВЫБОР
	|		КОГДА НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА втРаботы.Тара
	|		КОГДА ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|			ТОГДА втРаботы.ГрузовоеМесто
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|	КОНЕЦ КАК ТараГрузовогоМеста,
	|	ВЫБОР
	|		КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЭтоТара,
	|	втТипыГрузов.Ссылка КАК ТипГруза,
	|	втТипыГрузов.ВидГруза КАК ВидГруза,
	|	втРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
	|	втРаботы.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|	втРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМаксимальныеНомераПоставок КАК втМаксимальныеНомераПоставок
	|	ПО втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза = втМаксимальныеНомераПоставок.ЗаданиеЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК втГрузовыеМеста
	|	ПО втРаботы.ГрузовоеМесто = втГрузовыеМеста.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК втТипыГрузов
	|	ПО втГрузовыеМеста.ТипГруза = втТипыГрузов.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокТочки,
	|	ЕстьТараВСтроке УБЫВ,
	|	ТараГрузовогоМеста,
	|	ЭтоТара УБЫВ,
	|	СтрокаНомер
	|ИТОГИ
	|	СУММА(КоличествоГрузРазделен) КАК КоличествоГрузРазделен,
	|	МАКСИМУМ(ИзмененыПараметры) КАК ИзмененыПараметры
	|ПО
	|	ИдентификаторТочки,
	|	Задание,
	|	Операция
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРаботы.ИдентификаторТочки КАК Идентификатор
	|ПОМЕСТИТЬ втИдентификаторыТочекСозданныхПриОтменеРазгрузки
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ ИСТИНА
	|	И втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|	И втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ДосрочноеПрохождение)
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|	втМаршрутРейса.АдресТочки КАК АдресТочки,
	|	втМаршрутРейса.Гараж КАК Гараж,
	|	втМаршрутРейса.ТипТочки КАК ТипТочки,
	|	втМаршрутРейса.ТипТочки.Порядок КАК ИндексКартинки,
	|	втМаршрутРейса.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрутРейса.ТочкаПройдена
	|		ИЛИ втМаршрутРейса.ТочкаОтменена КАК ТочкаПройдена,
	|	втМаршрутРейса.ТочкаОтменена КАК ТочкаОтменена,
	|	втМаршрутРейса.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|	втМаршрутРейса.НачалоРаботПлан КАК НачалоРаботПлан,
	|	втМаршрутРейса.ПлановоеУбытие КАК ПлановоеУбытие,
	|	ВЫБОР
	|		КОГДА НЕ втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|			ТОГДА втМаршрутРейса.ПрибытиеФакт
	|		КОГДА втМаршрутРейса.ПрибытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втМаршрутРейса.ПлановоеПрибытие
	|		ИНАЧЕ втМаршрутРейса.ПрибытиеФакт
	|	КОНЕЦ КАК ПрибытиеФакт,
	|	ВЫБОР
	|		КОГДА НЕ втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|			ТОГДА втМаршрутРейса.НачалоРаботФакт
	|		КОГДА втМаршрутРейса.НачалоРаботФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втМаршрутРейса.НачалоРаботПлан
	|		ИНАЧЕ втМаршрутРейса.НачалоРаботФакт
	|	КОНЕЦ КАК НачалоРаботФакт,
	|	ВЫБОР
	|		КОГДА НЕ втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|			ТОГДА втМаршрутРейса.УбытиеФакт
	|		КОГДА втМаршрутРейса.УбытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втМаршрутРейса.ПлановоеУбытие
	|		ИНАЧЕ втМаршрутРейса.УбытиеФакт
	|	КОНЕЦ КАК УбытиеФакт,
	|	втМаршрутРейса.ПрохождениеТочки КАК ПрохождениеТочки,
	|	втМаршрутРейса.Рейс КАК Рейс,
	|	втМаршрутРейса.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
	|	втМаршрутРейса.Предшественники КАК Предшественники,
	|	втМаршрутРейса.Склад КАК Склад,
	|	втМаршрутРейса.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|	ВЫБОР
	|		КОГДА НЕ ВидыТочекОтправления.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НЕ втИдентификаторыТочекСозданныхПриОтменеРазгрузки.Идентификатор ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	втМаршрутРейса КАК втМаршрутРейса
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
	|	ПО ИСТИНА
	|		И втМаршрутРейса.АдресТочки.ВидАдреса = ВидыТочекОтправления.ВидТочкиОтправления
	|		И втМаршрутРейса.ВидПеревозки = ВидыТочекОтправления.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втИдентификаторыТочекСозданныхПриОтменеРазгрузки КАК втИдентификаторыТочекСозданныхПриОтменеРазгрузки
	|	ПО втМаршрутРейса.ИдентификаторТочки = втИдентификаторыТочекСозданныхПриОтменеРазгрузки.Идентификатор
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварныйСоставГруза.Проблема КАК Проблема,
	|	ТоварныйСоставГруза.ГрузовоеМестоРодитель КАК ГрузовоеМесто,
	|	втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|	втРаботыТ.Операция КАК Операция,
	|	втРаботыТ.Задание КАК Задание
	|ИЗ
	|	Документ.упПрохождениеТочки.ТоварныйСоставГруза КАК ТоварныйСоставГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутРейса КАК втМаршрутРейса
	|	ПО ТоварныйСоставГруза.Ссылка = втМаршрутРейса.ПрохождениеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
	|	ПО ИСТИНА
	|		И упРейсТ.ВидПеревозки.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава
	|		И упРейсТ.Ссылка = &Рейс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРаботы КАК втРаботыТ
	|	ПО ИСТИНА
	|		И втРаботыТ.Задание = ТоварныйСоставГруза.ЗаданиеНаПеревозкуГруза
	|		И втРаботыТ.ГрузовоеМесто = ТоварныйСоставГруза.ГрузовоеМестоРодитель
	|		И втРаботыТ.ИдентификаторТочки = втМаршрутРейса.ИдентификаторТочки
	|ГДЕ ИСТИНА
	|	И &ЗаполнятьСписокПроблем
	|	И НЕ (ТоварныйСоставГруза.Проблема В (ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)))
	|";
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаДеревоРаботПоРейсуПоЗаданиямПолучателя()
	Результат = 
	"
	|
	|	//{Запрос: 0 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		ЗаданияПоРейсам.ГрузовоеМесто КАК ГрузовоеМесто,
	|		ЗаданияПоРейсам.Задание КАК Задание,
	|		ЗаданияПоРейсам.ПустыеГрузовыеМеста КАК ПустыеГрузовыеМеста
	|	ПОМЕСТИТЬ втЗаданияПоРейсу
	|	ИЗ
	|		&ЗаданияПоРейсам КАК ЗаданияПоРейсам
	|	;
	|	//{Запрос: 1 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		упМаршрутПоРейсу.Регистратор КАК Регистратор,
	|		упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|		упМаршрутПоРейсу.Период КАК Период,
	|		упМаршрутПоРейсу.Пройдена КАК Пройдена,
	|		упМаршрутПоРейсу.Отменена КАК Отменена,
	|		упМаршрутПоРейсу.Рейс КАК Рейс
	|	ПОМЕСТИТЬ втЗаписиПоРСМаршрут
	|	ИЗ
	|		РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу КАК тбРаботы
	|		ПО ИСТИНА
	|			И тбРаботы.Идентификатор = упМаршрутПоРейсу.Идентификатор
	|			И тбРаботы.Рейс = &Рейс
	|			И тбРаботы.Рейс = упМаршрутПоРейсу.Рейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияПоРейсу КАК тбЗаданияПоРейсу
	|		ПО ИСТИНА
	|			И тбРаботы.Задание = тбЗаданияПоРейсу.Задание
	|			И (ЛОЖЬ
	|				ИЛИ тбЗаданияПоРейсу.ПустыеГрузовыеМеста
	|				ИЛИ тбРаботы.ГрузовоеМесто = тбЗаданияПоРейсу.ГрузовоеМесто)
	|	;
	|	//{Запрос: 2 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|		упМаршрутПоРейсу.Рейс КАК Рейс
	|	ПОМЕСТИТЬ втМаршрут
	|	ИЗ
	|		втЗаписиПоРСМаршрут КАК упМаршрутПоРейсу
	|	;
	|	//{Запрос: 3 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		втЗаписиПоРСМаршрут.Идентификатор КАК Идентификатор,
	|		втЗаписиПоРСМаршрут.Пройдена КАК Пройдена,
	|		втЗаписиПоРСМаршрут.Отменена КАК Отменена,
	|		втЗаписиПоРСМаршрут.Регистратор КАК Регистратор
	|	ПОМЕСТИТЬ втДокументыРегистраторы
	|	ИЗ
	|		втЗаписиПоРСМаршрут КАК втЗаписиПоРСМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			втЗаписиПоРСМаршрут.Идентификатор КАК Идентификатор,
	|			МАКСИМУМ(втЗаписиПоРСМаршрут.Период) КАК Период
	|		ИЗ
	|			втЗаписиПоРСМаршрут КАК втЗаписиПоРСМаршрут
	|		СГРУППИРОВАТЬ ПО
	|			втЗаписиПоРСМаршрут.Идентификатор) КАК втПоследниеЗаписи
	|		ПО ИСТИНА
	|			И втПоследниеЗаписи.Идентификатор = втЗаписиПоРСМаршрут.Идентификатор
	|			И втПоследниеЗаписи.Период = втЗаписиПоРСМаршрут.Период
	|	ГДЕ ИСТИНА
	|		И (ЛОЖЬ
	|			ИЛИ втЗаписиПоРСМаршрут.Пройдена
	|			ИЛИ втЗаписиПоРСМаршрут.Отменена)
	|		И (ЛОЖЬ
	|			ИЛИ втЗаписиПоРСМаршрут.Регистратор ССЫЛКА Документ.упПрохождениеТочки
	|			ИЛИ втЗаписиПоРСМаршрут.Регистратор ССЫЛКА Документ.упПроисшествиеВПути)
	|	;
	|	//{Запрос: 4 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|		упПрохождениеТочки.ИдентификаторТочки КАК ИдентификаторТочки,
	|		упПрохождениеТочки.Ссылка КАК Ссылка,
	|		упПрохождениеТочки.Дата КАК Дата,
	|		упПрохождениеТочки.НачалоВыполнения КАК НачалоВыполнения,
	|		упПрохождениеТочки.ОкончаниеВыполнения КАК ОкончаниеВыполнения,
	|		упПрохождениеТочки.НачалоРабот КАК НачалоРабот,
	|		упПрохождениеТочки.ТипТочки КАК ТипТочки,
	|		упПрохождениеТочки.ДосрочноеУбытие КАК ДосрочноеУбытие
	|	ПОМЕСТИТЬ втДокументыПрохождениеТочки
	|	ИЗ
	|		Документ.упПрохождениеТочки КАК упПрохождениеТочки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыРегистраторы КАК втДокументыРегистраторы
	|		ПО втДокументыРегистраторы.Регистратор = упПрохождениеТочки.Ссылка
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упПроисшествиеВПути.ИдентификаторТочки КАК ИдентификаторТочки,
	|		упПроисшествиеВПути.Ссылка КАК Ссылка,
	|		упПроисшествиеВПути.Дата КАК Дата,
	|		NULL КАК НачалоВыполнения,
	|		NULL КАК ОкончаниеВыполнения,
	|		NULL КАК НачалоРабот,
	|		ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ПроисшествиеВПути) КАК ТипТочки,
	|		ЛОЖЬ КАК ДосрочноеУбытие
	|	ИЗ
	|		Документ.упПроисшествиеВПути КАК упПроисшествиеВПути
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыРегистраторы КАК втДокументыРегистраторы
	|		ПО втДокументыРегистраторы.Регистратор = упПроисшествиеВПути.Ссылка
	|	;
	|	//{Запрос: 5 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|		упМаршрутПоРейсуСрезПоследних.Идентификатор КАК ИдентификаторТочки,
	|		упМаршрутПоРейсуСрезПоследних.Адрес КАК АдресТочки,
	|		упМаршрутПоРейсуСрезПоследних.Гараж КАК Гараж,
	|		ЕСТЬNULL(втДанныеТочки.ТипТочки, упМаршрутПоРейсуСрезПоследних.ТипТочки) КАК ТипТочки,
	|		упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|		упМаршрутПоРейсуСрезПоследних.Пройдена КАК ТочкаПройдена,
	|		упМаршрутПоРейсуСрезПоследних.Отменена КАК ТочкаОтменена,
	|		упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|		упМаршрутПоРейсуСрезПоследних.НачалоРаботПлан КАК НачалоРаботПлан,
	|		упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК ПлановоеУбытие,
	|		ЕСТЬNULL(втДанныеТочки.НачалоВыполнения, упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт) КАК ПрибытиеФакт,
	|		ЕСТЬNULL(втДанныеТочки.НачалоРабот, упМаршрутПоРейсуСрезПоследних.НачалоРаботФакт) КАК НачалоРаботФакт,
	|		ЕСТЬNULL(втДанныеТочки.ОкончаниеВыполнения, упМаршрутПоРейсуСрезПоследних.УбытиеФакт) КАК УбытиеФакт,
	|		втДанныеТочки.Ссылка КАК ПрохождениеТочки,
	|		упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|		упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
	|		упМаршрутПоРейсуСрезПоследних.Предшественники КАК Предшественники,
	|		упМаршрутПоРейсуСрезПоследних.Адрес.Склад КАК Склад,
	|		втДанныеТочки.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|		упРейс.ВидПеревозки КАК ВидПеревозки
	|	ПОМЕСТИТЬ втМаршрутРейса
	|	ИЗ
	|		РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДокументыПрохождениеТочки КАК втДанныеТочки
	|		ПО упМаршрутПоРейсуСрезПоследних.Идентификатор = втДанныеТочки.ИдентификаторТочки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|		ПО упМаршрутПоРейсуСрезПоследних.Рейс = упРейс.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК _МаршрутПоРейсуТ
	|		ПО ИСТИНА
	|			И упМаршрутПоРейсуСрезПоследних.Рейс = _МаршрутПоРейсуТ.Рейс
	|			И упМаршрутПоРейсуСрезПоследних.Идентификатор = _МаршрутПоРейсуТ.Идентификатор
	|	;
	|	//{Запрос: 6 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|		втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|		ЕСТЬNULL(тбРаботы.СтрокаНомер, 0) КАК СтрокаНомер,
	|		ЕСТЬNULL(тбРаботы.Отменена, ЛОЖЬ) КАК Отменена,
	|		тбРаботы.Задание КАК Задание,
	|		тбРаботы.Операция КАК Операция,
	|		ЕСТЬNULL(тбРаботы.Операция.Порядок + 5, - 1) КАК ИндексКартинки,
	|		тбРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|		тбРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|		тбРаботы.Тара КАК Тара,
	|		тбРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|		тбРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
	|		тбРаботы.Проблема КАК Проблема,
	|		тбРаботы.АдресРазгрузки КАК АдресРазгрузки,
	|		тбРаботы.Выполнена КАК Выполнена,
	|		тбРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|		тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|		втМаршрутРейса.СтрокаНомер КАК ПорядокТочки,
	|		тбРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|		тбРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
	|		тбРаботы.Сумма КАК Сумма,
	|		упГрузовыеМеста.Валюта КАК Валюта,
	|		тбРаботы.ГрузРазделен КАК ГрузРазделен,
	|		ВЫБОР
	|			КОГДА тбРаботы.ГрузРазделен
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоГрузРазделен,
	|		тбРаботы.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|		тбРаботы.Задание.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		ЛОЖЬ КАК ИзмененыПараметры,
	|		втМаршрутРейса.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|		тбРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута,
	|		втМаршрутРейса.Рейс КАК Рейс
	|	ПОМЕСТИТЬ втРаботы
	|	ИЗ
	|		втМаршрутРейса КАК втМаршрутРейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс) КАК тбРаботы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|			ПО тбРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|		ПО ИСТИНА
	|			И тбРаботы.Рейс = втМаршрутРейса.Рейс
	|			И тбРаботы.Идентификатор = втМаршрутРейса.ИдентификаторТочки
	|			И втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|			И НЕ (тбРаботы.Отменена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияПоРейсу КАК втЗаданияПоРейсу
	|		ПО ИСТИНА
	|			И тбРаботы.Задание = втЗаданияПоРейсу.Задание
	|			И (ЛОЖЬ
	|				ИЛИ втЗаданияПоРейсу.ПустыеГрузовыеМеста
	|				ИЛИ тбРаботы.ГрузовоеМесто = втЗаданияПоРейсу.ГрузовоеМесто)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|		упПрохождениеТочкиРаботы.СтрокаНомер КАК СтрокаНомер,
	|		упПрохождениеТочкиРаботы.Отменена КАК Отменена,
	|		упПрохождениеТочкиРаботы.Задание КАК Задание,
	|		упПрохождениеТочкиРаботы.Операция КАК Операция,
	|		упПрохождениеТочкиРаботы.Операция.Порядок + 5 КАК ИндексКартинки,
	|		упПрохождениеТочкиРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|		упПрохождениеТочкиРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|		упПрохождениеТочкиРаботы.Тара КАК Тара,
	|		упПрохождениеТочкиРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|		упПрохождениеТочкиРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
	|		упПрохождениеТочкиРаботы.Проблема КАК Проблема,
	|		упПрохождениеТочкиРаботы.АдресРазгрузки КАК АдресРазгрузки,
	|		упПрохождениеТочкиРаботы.Выполнена КАК Выполнена,
	|		упПрохождениеТочкиРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|		упПрохождениеТочкиРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|		втМаршрутРейса.СтрокаНомер КАК ПорядокТочки,
	|		упПрохождениеТочкиРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|		упПрохождениеТочкиРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
	|		NULL КАК Сумма,
	|		NULL КАК Валюта,
	|		упПрохождениеТочкиРаботы.ГрузРазделен КАК ГрузРазделен,
	|		ВЫБОР
	|			КОГДА упПрохождениеТочкиРаботы.ГрузРазделен
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоГрузРазделен,
	|		упПрохождениеТочкиРаботы.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|		упПрохождениеТочкиРаботы.Задание.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		упПрохождениеТочкиРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
	|		NULL КАК ДосрочноеУбытие,
	|		упПрохождениеТочкиРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута,
	|		втМаршрутРейса.Рейс КАК Рейс
	|	ИЗ
	|		Документ.упПрохождениеТочки.Работы КАК упПрохождениеТочкиРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутРейса КАК втМаршрутРейса
	|		ПО упПрохождениеТочкиРаботы.Ссылка = втМаршрутРейса.ПрохождениеТочки
	|	;
	|	//{Запрос: 7 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|		втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|		МАКСИМУМ(упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок) КАК НомерВЦепиПоставок
	|	ПОМЕСТИТЬ втМаксимальныеНомераПоставок
	|	ИЗ
	|		втРаботы КАК втРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ПО втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза = упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза
	|	СГРУППИРОВАТЬ ПО
	|		втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза
	|	;
	|	//{Запрос: 8 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		втРаботы.АдресРазгрузки КАК АдресРазгрузки,
	|		втРаботы.Валюта КАК Валюта,
	|		втРаботы.Выполнена КАК Выполнена,
	|		втРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втРаботы.ГрузРазделен КАК ГрузРазделен,
	|		втРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|		ПРЕДСТАВЛЕНИЕ(втРаботы.Задание) КАК Задание,
	|		втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|		втРаботы.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		втРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|		втРаботы.ИдентификаторТочки КАК ИдентификаторТочки,
	|		втРаботы.ИндексКартинки КАК ИндексКартинки,
	|		втРаботы.КоличествоГрузов КАК КоличествоГрузов,
	|		втРаботы.КоличествоГрузРазделен КАК КоличествоГрузРазделен,
	|		втРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
	|		втРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
	|		втРаботы.Операция КАК Операция,
	|		втРаботы.Отменена КАК Отменена,
	|		втРаботы.ПорядокТочки КАК ПорядокТочки,
	|		втРаботы.Проблема КАК Проблема,
	|		втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|		втРаботы.СтрокаНомер КАК СтрокаНомер,
	|		втРаботы.Сумма КАК Сумма,
	|		втРаботы.Тара КАК Тара,
	|		втРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	|		втМаксимальныеНомераПоставок.НомерВЦепиПоставок КАК НомерВЦепиПоставокМаксимальный,
	|		ВЫБОР
	|			КОГДА ЛОЖЬ
	|					ИЛИ НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|					ИЛИ ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЕстьТараВСтроке,
	|		ВЫБОР
	|			КОГДА НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|				ТОГДА втРаботы.Тара
	|			КОГДА ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|				ТОГДА втРаботы.ГрузовоеМесто
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		КОНЕЦ КАК ТараГрузовогоМеста,
	|		ВЫБОР
	|			КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЭтоТара,
	|		втТипыГрузов.Ссылка КАК ТипГруза,
	|		втТипыГрузов.ВидГруза КАК ВидГруза,
	|		втРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
	|		втРаботы.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|		ВЫБОР
	|			КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|				ТОГДА 0
	|			КОГДА НаличиеФайлов.ЕстьФайлы
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЕстьФайлы,
	|		втРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута,
	|         втРаботы.Рейс КАК Рейс
	|	ИЗ
	|		втРаботы КАК втРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМаксимальныеНомераПоставок КАК втМаксимальныеНомераПоставок
	|		ПО втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза = втМаксимальныеНомераПоставок.ЗаданиеЗаявкаНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК втГрузовыеМеста
	|		ПО втРаботы.ГрузовоеМесто = втГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК втТипыГрузов
	|		ПО втГрузовыеМеста.ТипГруза = втТипыГрузов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеПрисоединенныхФайлов КАК НаличиеФайлов
	|		ПО втРаботы.ЗаданиеЗаявкаНаПеревозкуГруза = НаличиеФайлов.ОбъектСФайлами
	|	УПОРЯДОЧИТЬ ПО
	|		ПорядокТочки,
	|		ЕстьТараВСтроке УБЫВ,
	|		ТараГрузовогоМеста,
	|		ЭтоТара УБЫВ,
	|		СтрокаНомер
	|	ИТОГИ
	|		СУММА(КоличествоГрузРазделен) КАК КоличествоГрузРазделен,
	|		МАКСИМУМ(ИзмененыПараметры) КАК ИзмененыПараметры,
	|		МАКСИМУМ(ЕстьФайлы) КАК ЕстьФайлы,
	|		МАКСИМУМ(ЗаданиеЗаявкаНаПеревозкуГруза) КАК ЗаданиеЗаявкаНаПеревозкуГруза
	|	ПО
	|		ИдентификаторТочки,
	|		Задание,
	|		Операция
	|	;
	|	//{Запрос: 9 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		втРаботы.ИдентификаторТочки КАК Идентификатор
	|	ПОМЕСТИТЬ втИдентификаторыТочекСозданныхПриОтменеРазгрузки
	|	ИЗ
	|		втРаботы КАК втРаботы
	|	ГДЕ ИСТИНА
	|		И втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|		И втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ДосрочноеПрохождение)
	|	;
	|	//{Запрос: 10 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|		втМаршрутРейса.АдресТочки КАК АдресТочки,
	|		втМаршрутРейса.Гараж КАК Гараж,
	|		втМаршрутРейса.ТипТочки КАК ТипТочки,
	|		втМаршрутРейса.ТипТочки.Порядок КАК ИндексКартинки,
	|		втМаршрутРейса.СтрокаНомер КАК СтрокаНомер,
	|		втМаршрутРейса.ТочкаПройдена
	|			ИЛИ втМаршрутРейса.ТочкаОтменена КАК ТочкаПройдена,
	|		втМаршрутРейса.ТочкаОтменена КАК ТочкаОтменена,
	|		втМаршрутРейса.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|		втМаршрутРейса.НачалоРаботПлан КАК НачалоРаботПлан,
	|		втМаршрутРейса.ПлановоеУбытие КАК ПлановоеУбытие,
	|		ВЫБОР
	|			КОГДА НЕ втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|				ТОГДА втМаршрутРейса.ПрибытиеФакт
	|			КОГДА втМаршрутРейса.ПрибытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА втМаршрутРейса.ПлановоеПрибытие
	|			ИНАЧЕ втМаршрутРейса.ПрибытиеФакт
	|		КОНЕЦ КАК ПрибытиеФакт,
	|		ВЫБОР
	|			КОГДА НЕ втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|				ТОГДА втМаршрутРейса.НачалоРаботФакт
	|			КОГДА втМаршрутРейса.НачалоРаботФакт = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА втМаршрутРейса.НачалоРаботПлан
	|			ИНАЧЕ втМаршрутРейса.НачалоРаботФакт
	|		КОНЕЦ КАК НачалоРаботФакт,
	|		ВЫБОР
	|			КОГДА НЕ втМаршрутРейса.ПрохождениеТочки ЕСТЬ NULL
	|				ТОГДА втМаршрутРейса.УбытиеФакт
	|			КОГДА втМаршрутРейса.УбытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА втМаршрутРейса.ПлановоеУбытие
	|			ИНАЧЕ втМаршрутРейса.УбытиеФакт
	|		КОНЕЦ КАК УбытиеФакт,
	|		втМаршрутРейса.ПрохождениеТочки КАК ПрохождениеТочки,
	|		втМаршрутРейса.Рейс КАК Рейс,
	|		втМаршрутРейса.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
	|		втМаршрутРейса.Предшественники КАК Предшественники,
	|		втМаршрутРейса.Склад КАК Склад,
	|		втМаршрутРейса.ДосрочноеУбытие КАК ДосрочноеУбытие,
	|		ВЫБОР
	|			КОГДА НЕ ВидыТочекОтправления.Ссылка ЕСТЬ NULL
	|				ТОГДА 0
	|			КОГДА НЕ втИдентификаторыТочекСозданныхПриОтменеРазгрузки.Идентификатор ЕСТЬ NULL
	|				ТОГДА 1
	|			ИНАЧЕ 2
	|		КОНЕЦ КАК Приоритет,
	|		ВЫБОР
	|			КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|				ТОГДА 0
	|			КОГДА НаличиеФайлов.ЕстьФайлы
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЕстьФайлы
	|	ИЗ
	|		втМаршрутРейса КАК втМаршрутРейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
	|		ПО ИСТИНА
	|			И втМаршрутРейса.АдресТочки.ВидАдреса = ВидыТочекОтправления.ВидТочкиОтправления
	|			И втМаршрутРейса.ВидПеревозки = ВидыТочекОтправления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИдентификаторыТочекСозданныхПриОтменеРазгрузки КАК втИдентификаторыТочекСозданныхПриОтменеРазгрузки
	|		ПО втМаршрутРейса.ИдентификаторТочки = втИдентификаторыТочекСозданныхПриОтменеРазгрузки.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеПрисоединенныхФайлов КАК НаличиеФайлов
	|		ПО втМаршрутРейса.ПрохождениеТочки = НаличиеФайлов.ОбъектСФайлами
	|	;
	|	//{Запрос: 11 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		ТоварныйСоставГруза.Проблема КАК Проблема,
	|		ТоварныйСоставГруза.ГрузовоеМестоРодитель КАК ГрузовоеМесто,
	|		втМаршрутРейса.ИдентификаторТочки КАК ИдентификаторТочки,
	|		втРаботыТ.Операция КАК Операция,
	|		втРаботыТ.Задание КАК Задание
	|	ИЗ
	|		Документ.упПрохождениеТочки.ТоварныйСоставГруза КАК ТоварныйСоставГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутРейса КАК втМаршрутРейса
	|		ПО ТоварныйСоставГруза.Ссылка = втМаршрутРейса.ПрохождениеТочки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
	|		ПО ИСТИНА
	|			И упРейсТ.ВидПеревозки.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава
	|			И упРейсТ.Ссылка = &Рейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРаботы КАК втРаботыТ
	|		ПО ИСТИНА
	|			И втРаботыТ.Задание = ТоварныйСоставГруза.ЗаданиеНаПеревозкуГруза
	|			И втРаботыТ.ГрузовоеМесто = ТоварныйСоставГруза.ГрузовоеМестоРодитель
	|			И втРаботыТ.ИдентификаторТочки = втМаршрутРейса.ИдентификаторТочки
	|	ГДЕ ИСТИНА
	|		И &ЗаполнятьСписокПроблем
	|		И НЕ (ТоварныйСоставГруза.Проблема В (ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)))
	|	
	|";
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСписокРейсовПоПолучателю(СписокРейсов, ЗаданияПоРейсам, Партнеры, ДатаНачала, ДатаОкончания, Отказ) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыРейсов.Документ КАК Рейс,
	|	СтатусыРейсов.Статус КАК Статус,
	|	упРейсТ.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА упВидыПеревозокТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустыеГрузовыеМеста
	|ПОМЕСТИТЬ втРейсыКВыполнению
	|ИЗ
	|	РегистрСведений.упСтатусыРейсов КАК СтатусыРейсов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокТ
	|		ПО упРейсТ.ВидПеревозки = упВидыПеревозокТ.Ссылка
	|	ПО упРейсТ.Ссылка = СтатусыРейсов.Документ
	|ГДЕ ИСТИНА
	|	И СтатусыРейсов.Период >= &ДатаНачала
	|	И СтатусыРейсов.Период <= &ДатаОкончания
	|	И СтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА втРейсыКВыполнению.ПустыеГрузовыеМеста
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ тбРаботы.ГрузовоеМесто
	|	КОНЕЦ КАК ГрузовоеМесто,
	|	тбРаботы.Отменена КАК Отменена,
	|	тбРаботы.ГрузРазделен КАК ГрузРазделен,
	|	тбРаботы.Операция КАК Операция,
	|	тбРаботы.Идентификатор КАК Идентификатор,
	|	тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
	|	тбРаботы.Рейс КАК Рейс,
	|	ВЫБОР
	|		КОГДА тбРаботы.Проблема <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьПроблемы
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсыКВыполнению КАК втРейсы)) КАК тбРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыКВыполнению КАК втРейсыКВыполнению
	|	ПО тбРаботы.Рейс = втРейсыКВыполнению.Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Задание КАК Задание,
	|	втРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
	|	СУММА(ВЫБОР
	|		КОГДА втРаботы.Отменена
	|				И НЕ втРаботы.ГрузРазделен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтменена,
	|	СУММА(1) КАК КоличествоОпераций,
	|	втРаботы.Рейс КАК Рейс
	|ПОМЕСТИТЬ втОтмененныеЗаданияПогрузки
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ ЛОЖЬ
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Задание,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Рейс
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОтмененныеЗаданияТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втОтмененныеЗаданияТ.Задание КАК Задание,
	|	втОтмененныеЗаданияТ.Рейс КАК Рейс
	|ПОМЕСТИТЬ втОтмененныеЗадания
	|ИЗ
	|	втОтмененныеЗаданияПогрузки КАК втОтмененныеЗаданияТ
	|ГДЕ втОтмененныеЗаданияТ.КоличествоОпераций = втОтмененныеЗаданияТ.КоличествоОтменена
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РейсЗадания.Ссылка КАК Рейс,
	|	РейсЗадания.Задание КАК Задание,
	|	РейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРейсы.ВидПеревозки КАК ВидПеревозки,
	|	втРейсы.ПустыеГрузовыеМеста КАК ПустыеГрузовыеМеста,
	|	ВЫБОР
	|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НаличиеФайлов.ЕстьФайлы
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьФайлы
	|ПОМЕСТИТЬ втРейсыПоЗаданиям
	|ИЗ
	|	втРейсыКВыполнению КАК втРейсы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс.Задания КАК РейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
	|		ПО упЗаданиеНаПеревозкуГрузаТ.Ссылка = РейсЗадания.Задание
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеПрисоединенныхФайлов КАК НаличиеФайлов
	|		ПО упЗаданиеНаПеревозкуГрузаТ.ЗаявкаНаПеревозкуГруза = НаличиеФайлов.ОбъектСФайлами
	|	ПО втРейсы.Рейс = РейсЗадания.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗаданияТ
	|	ПО ИСТИНА
	|		И втОтмененныеЗаданияТ.Задание = РейсЗадания.Задание
	|		И втОтмененныеЗаданияТ.Рейс = РейсЗадания.Ссылка
	|		И втОтмененныеЗаданияТ.ГрузовоеМесто = РейсЗадания.ГрузовоеМесто
	|ГДЕ ИСТИНА
	|	И втОтмененныеЗаданияТ.Рейс ЕСТЬ NULL
	|	И упЗаданиеНаПеревозкуГрузаТ.Получатель В (&Партнеры)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	КорректировкаРейсаЗадания.Ссылка.Рейс КАК Рейс,
	|	КорректировкаРейсаЗадания.Задание КАК Задание,
	|	КорректировкаРейсаЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРейсы.ВидПеревозки КАК ВидПеревозки,
	|	втРейсы.ПустыеГрузовыеМеста КАК ПустыеГрузовыеМеста,
	|	ВЫБОР
	|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НаличиеФайлов.ЕстьФайлы
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьФайлы
	|ИЗ
	|	втРейсыКВыполнению КАК втРейсы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упКорректировкаРейса.Задания КАК КорректировкаРейсаЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
	|		ПО упЗаданиеНаПеревозкуГрузаТ.Ссылка = КорректировкаРейсаЗадания.Задание
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеПрисоединенныхФайлов КАК НаличиеФайлов
	|		ПО упЗаданиеНаПеревозкуГрузаТ.ЗаявкаНаПеревозкуГруза = НаличиеФайлов.ОбъектСФайлами
	|	ПО втРейсы.Рейс = КорректировкаРейсаЗадания.Ссылка.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗаданияТ
	|	ПО ИСТИНА
	|		И втОтмененныеЗаданияТ.Задание = КорректировкаРейсаЗадания.Задание
	|		И втОтмененныеЗаданияТ.Рейс = КорректировкаРейсаЗадания.Ссылка.Рейс
	|		И втОтмененныеЗаданияТ.ГрузовоеМесто = КорректировкаРейсаЗадания.ГрузовоеМесто
	|ГДЕ ИСТИНА
	|	И втОтмененныеЗаданияТ.Рейс ЕСТЬ NULL
	|	И упЗаданиеНаПеревозкуГрузаТ.Получатель В (&Партнеры)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботыТ.Идентификатор КАК Идентификатор,
	|	втРаботыТ.Рейс КАК Рейс,
	|	втРейсыПоЗаданиямТ.ВидПеревозки КАК ВидПеревозки,
	|	СУММА(втРаботыТ.ЕстьПроблемы) КАК ЕстьПроблемы
	|ПОМЕСТИТЬ втИдентификаторыТочекМаршрута
	|ИЗ
	|	втРаботы КАК втРаботыТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
	|	ПО ИСТИНА
	|		И втРейсыПоЗаданиямТ.ГрузовоеМесто = втРаботыТ.ГрузовоеМесто
	|		И втРейсыПоЗаданиямТ.Задание = втРаботыТ.Задание
	|		И втРейсыПоЗаданиямТ.Рейс = втРаботыТ.Рейс
	|СГРУППИРОВАТЬ ПО
	|	втРаботыТ.Идентификатор,
	|	втРаботыТ.Рейс,
	|	втРейсыПоЗаданиямТ.ВидПеревозки
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	СУММА(втРейсы.ЕстьФайлы) КАК ЕстьФайлы
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	втРейсыПоЗаданиям КАК втРейсы
	|СГРУППИРОВАТЬ ПО
	|	втРейсы.Рейс
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Маршрут.Идентификатор КАК Идентификатор,
	|	Маршрут.Рейс КАК Рейс,
	|	Маршрут.Адрес КАК Адрес,
	|	Маршрут.Адрес.ВидАдреса КАК ВидАдресаТочки,
	|	втИдентификаторыТочекМаршрута.ВидПеревозки КАК ВидПеревозки,
	|	втИдентификаторыТочекМаршрута.ЕстьПроблемы КАК ЕстьПроблемы,
	|	Маршрут.ПрибытиеФакт КАК ПрибытиеФакт,
	|	Маршрут.ПлановоеУбытие КАК ПлановоеУбытие,
	|	Маршрут.Пройдена КАК Пройдена
	|ПОМЕСТИТЬ втМаршрутПоРейсу
	|ИЗ
	|	втИдентификаторыТочекМаршрута КАК втИдентификаторыТочекМаршрута
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК Маршрут
	|	ПО ИСТИНА
	|		И Маршрут.Идентификатор = втИдентификаторыТочекМаршрута.Идентификатор
	|		И Маршрут.Рейс = втИдентификаторыТочекМаршрута.Рейс
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Регистратор КАК Регистратор,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсу.Период КАК Период,
	|	упМаршрутПоРейсу.Пройдена КАК Пройдена,
	|	упМаршрутПоРейсу.Отменена КАК Отменена
	|ПОМЕСТИТЬ втЗаписиПоРСМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутПоРейсу КАК втМаршрут
	|	ПО ИСТИНА
	|		И втМаршрут.Идентификатор = упМаршрутПоРейсу.Идентификатор
	|		И втМаршрут.Рейс = упМаршрутПоРейсу.Рейс
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаписиПоРСМаршрут.Рейс КАК Рейс,
	|	втЗаписиПоРСМаршрут.Идентификатор КАК Идентификатор,
	|	втЗаписиПоРСМаршрут.Пройдена КАК Пройдена,
	|	втЗаписиПоРСМаршрут.Отменена КАК Отменена,
	|	втЗаписиПоРСМаршрут.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втДокументыРегистраторы
	|ИЗ
	|	втЗаписиПоРСМаршрут КАК втЗаписиПоРСМаршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втЗаписиПоРСМаршрут.Идентификатор КАК Идентификатор,
	|		МАКСИМУМ(втЗаписиПоРСМаршрут.Период) КАК Период
	|	ИЗ
	|		втЗаписиПоРСМаршрут КАК втЗаписиПоРСМаршрут
	|	СГРУППИРОВАТЬ ПО
	|		втЗаписиПоРСМаршрут.Идентификатор) КАК втПоследниеЗаписи
	|	ПО ИСТИНА
	|		И втПоследниеЗаписи.Идентификатор = втЗаписиПоРСМаршрут.Идентификатор
	|		И втПоследниеЗаписи.Период = втЗаписиПоРСМаршрут.Период
	|ГДЕ ИСТИНА
	|	И (ЛОЖЬ
	|		ИЛИ втЗаписиПоРСМаршрут.Пройдена
	|		ИЛИ втЗаписиПоРСМаршрут.Отменена)
	|	И (ЛОЖЬ
	|		ИЛИ втЗаписиПоРСМаршрут.Регистратор ССЫЛКА Документ.упПрохождениеТочки
	|		ИЛИ втЗаписиПоРСМаршрут.Регистратор ССЫЛКА Документ.упПроисшествиеВПути)
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокументыРегистраторы.Рейс КАК Рейс,
	|	СУММА(ВЫБОР
	|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА НаличиеФайлов.ЕстьФайлы
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЕстьФайлы
	|ПОМЕСТИТЬ втРегистраторыПоРейсам
	|ИЗ
	|	втДокументыРегистраторы КАК втДокументыРегистраторы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеПрисоединенныхФайлов КАК НаличиеФайлов
	|	ПО втДокументыРегистраторы.Регистратор = НаличиеФайлов.ОбъектСФайлами
	|СГРУППИРОВАТЬ ПО
	|	втДокументыРегистраторы.Рейс
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|		КОГДА ВидыПеревозок_ВидыТочекОтправления.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоПосещениийТочекПолучателя,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|		КОГДА ВидыПеревозок_ВидыТочекОтправления.Ссылка ЕСТЬ NULL
	|			ТОГДА втМаршрутПоРейсуТ.Адрес
	|		ИНАЧЕ NULL
	|	КОНЕЦ) КАК КоличествоТочекПолучателя,
	|	СУММА(втМаршрутПоРейсуТ.ЕстьПроблемы) КАК ЕстьПроблемы,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втМаршрутПоРейсуТ.Идентификатор) КАК КоличествоТочек,
	|	втМаршрутПоРейсуТ.Рейс КАК Рейс
	|ПОМЕСТИТЬ втКоличествоТочекПоРейсам
	|ИЗ
	|	втМаршрутПоРейсу КАК втМаршрутПоРейсуТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыПеревозок_ВидыТочекОтправления
	|	ПО ИСТИНА
	|		И ВидыПеревозок_ВидыТочекОтправления.Ссылка = втМаршрутПоРейсуТ.ВидПеревозки
	|		И ВидыПеревозок_ВидыТочекОтправления.ВидТочкиОтправления = втМаршрутПоРейсуТ.ВидАдресаТочки
	|СГРУППИРОВАТЬ ПО
	|	втМаршрутПоРейсуТ.Рейс
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсыТ.Рейс КАК Рейс,
	|	втКоличествоТочекПоРейсамТ.КоличествоТочек КАК КоличествоТочек,
	|	втКоличествоТочекПоРейсамТ.КоличествоТочекПолучателя КАК КоличествоТочекПолучателя,
	|	ВЫБОР
	|		КОГДА втКоличествоТочекПоРейсамТ.ЕстьПроблемы > 0
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьПроблемы,
	|	ПеревозчикПоРейсу.Водитель1 КАК Водитель1,
	|	ПеревозчикПоРейсу.Водитель2 КАК Водитель2,
	|	ПеревозчикПоРейсу.КонтрагентПеревозчика КАК КонтрагентПеревозчика,
	|	ПеревозчикПоРейсу.Перевозчик КАК Перевозчик,
	|	ПеревозчикПоРейсу.Соглашение КАК Соглашение,
	|	ПеревозчикПоРейсу.ТипТС КАК ТипТС,
	|	ПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ПеревозчикПоРейсу.Экспедитор КАК Экспедитор,
	|	ФактическиеПараметры.Время КАК Время,
	|	ФактическиеПараметры.ВремяВПути КАК ВремяВПути,
	|	ПРЕДСТАВЛЕНИЕ(ФактическиеПараметры.ЗонаПогрузки) КАК ЗонаПогрузки,
	|	ПРЕДСТАВЛЕНИЕ(ФактическиеПараметры.ЗонаРазгрузки) КАК ЗонаРазгрузки,
	|	ЕСТЬNULL(ФактическиеПараметры.КоличествоГрузовыхМест, ПлановыеПараметры.КоличествоГрузовыхМест) КАК КоличествоГрузовыхМест,
	|	ЕСТЬNULL(ФактическиеПараметры.КоличествоЗагрузочныхМетров, ПлановыеПараметры.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(ФактическиеПараметры.КоличествоМест, ПлановыеПараметры.КоличествоМест) КАК КоличествоМест,
	|	ЕСТЬNULL(ФактическиеПараметры.Масса, ПлановыеПараметры.Масса) КАК Масса,
	|	ЕСТЬNULL(ФактическиеПараметры.НачалоВыполнения, ПлановыеПараметры.ПлановоеНачалоВыполнения) КАК НачалоВыполнения,
	|	ЕСТЬNULL(ФактическиеПараметры.НулевойПробег, ПлановыеПараметры.НулевойПробег) КАК НулевойПробег,
	|	ЕСТЬNULL(ФактическиеПараметры.Объем, ПлановыеПараметры.Объем) КАК Объем,
	|	ЕСТЬNULL(ФактическиеПараметры.ОкончаниеВыполнения, ПлановыеПараметры.ПлановоеОкончаниеВыполнения) КАК ОкончаниеВыполнения,
	|	ЕСТЬNULL(ФактическиеПараметры.ПредставлениеМаршрута, ПлановыеПараметры.ПредставлениеМаршрута) КАК ПредставлениеМаршрута,
	|	ЕСТЬNULL(ФактическиеПараметры.Пробег, 0) КАК Пробег,
	|	ЕСТЬNULL(ФактическиеПараметры.Расстояние, ПлановыеПараметры.Расстояние) КАК Расстояние,
	|	ЕСТЬNULL(ФактическиеПараметры.РасстояниеПоДаннымТрекеров, 0) КАК РасстояниеПоДаннымТрекеров,
	|	ЕСТЬNULL(ФактическиеПараметры.СуммаИнкассации, ПлановыеПараметры.СуммаИнкассации) КАК СуммаИнкассации,
	|	ЕСТЬNULL(СтатусыРейсов.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)) КАК Статус,
	|	ЕСТЬNULL(СтатусыРейсов.Статус.Порядок, 0) КАК ИндексКартинки,
	|	упРейсТ.Дата КАК Дата,
	|	упРейсТ.Номер КАК Номер,
	|	ВЫБОР
	|		КОГДА втМаршрутПоРейсу.Рейс ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьОпозданияПрибытияВТочку,
	|	ВЫБОР
	|		КОГДА (втРейсыТ.ЕстьФайлы + ЕСТЬNULL(втРегистраторыПоРейсам.ЕстьФайлы, 0)) > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕстьФайлы,
	|	ЕСТЬNULL(втМаршрутПоРейсу.КоличествоТочекСОпозданиями, 0) КАК КоличествоТочекСОпозданиями,
	|	втКоличествоТочекПоРейсамТ.КоличествоПосещениийТочекПолучателя КАК КоличествоПосещениийТочекПолучателя
	|ИЗ
	|	втРейсы КАК втРейсыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТочекПоРейсам КАК втКоличествоТочекПоРейсамТ
	|	ПО втКоличествоТочекПоРейсамТ.Рейс = втРейсыТ.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК ПеревозчикПоРейсу
	|	ПО ПеревозчикПоРейсу.Рейс = втРейсыТ.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФактическиеПараметры
	|	ПО ФактическиеПараметры.Рейс = втРейсыТ.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК ПлановыеПараметры
	|	ПО ПлановыеПараметры.Рейс = втРейсыТ.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРегистраторыПоРейсам КАК втРегистраторыПоРейсам
	|	ПО втРейсыТ.Рейс = втРегистраторыПоРейсам.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|		,
	|		) КАК СтатусыРейсов
	|	ПО втРейсыТ.Рейс = СтатусыРейсов.Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
	|	ПО втРейсыТ.Рейс = упРейсТ.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		Маршрут.Рейс КАК Рейс,
	|		КОЛИЧЕСТВО(Маршрут.Идентификатор) КАК КоличествоТочекСОпозданиями
	|	ИЗ
	|		втМаршрутПоРейсу КАК Маршрут
	|	ГДЕ ИСТИНА
	|		И Маршрут.ПрибытиеФакт > Маршрут.ПлановоеУбытие
	|		И Маршрут.Пройдена
	|	СГРУППИРОВАТЬ ПО
	|		Маршрут.Рейс) КАК втМаршрутПоРейсу
	|	ПО втРейсыТ.Рейс = втМаршрутПоРейсу.Рейс
	|ГДЕ ЕСТЬNULL(СтатусыРейсов.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)) <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсыПоЗаданиямТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРейсыПоЗаданиямТ.Задание КАК Задание,
	|	втРейсыПоЗаданиямТ.Рейс КАК Рейс,
	|	втРейсыПоЗаданиямТ.ПустыеГрузовыеМеста КАК ПустыеГрузовыеМеста
	|ИЗ
	|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
	|";

	СписокРейсов.Очистить();
	ЗаданияПоРейсам.Очистить();

	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Партнеры", Партнеры);
	
	ИндексЗапросаРейсы = 12;
	ИндексЗапросаЗадания = 13;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[ИндексЗапросаРейсы].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокРейсов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
	
	Выборка = РезультатЗапроса[ИндексЗапросаЗадания].Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ЗаданияПоРейсам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
		
КонецПроцедуры
 
// Сохраняем изменения в дереве точек.
//
// Параметры:
//   дрвРаботыПоТекущемуРейсу - СтрокаДереваЗначений - Позволяет читать и записывать данные,
//                                                    в конкретных колонках строки.
//	дрвРаботыПоТекущемуРейсуЭталон - СтрокаДереваЗначений - Позволяет читать и записывать данные,
//                                                           в конкретных колонках строки.
//	Рейс - ДокументСсылка - Ссылка на объект.
//	мсвИндексовИзмененныхТочек - Массив - Индексы.
//	ПерезаполнятьДеревоТочекРейса - Булево - Выполнить.
//	мсвНеправильноЗаполненныхСтрок - Массив - Строки.
//	ТоварныйСоставГруза - ТабличнаяЧасть - таблица измененного товарного состава по точке.
//	ИдентификаторТочкиСИзмененнымСоставомГрузов - УникальныйИдентификатор - идентификатор точки с измененным товарным составом.
//
Процедура ПрименитьИзмененияВДеревеТочекРейса(дрвРаботыПоТекущемуРейсу, 
		                                    дрвРаботыПоТекущемуРейсуЭталон, 
									 Рейс, 
									 мсвИндексовИзмененныхТочек = Неопределено, 
									 ПерезаполнятьДеревоТочекРейса = Истина, 
									 мсвНеправильноЗаполненныхСтрок = Неопределено, 
									 ТоварныйСоставГруза = Неопределено, 
									 ИдентификаторТочкиСИзмененнымСоставомГрузов = Неопределено) Экспорт
	
	Если мсвИндексовИзмененныхТочек = Неопределено Тогда
		мсвИндексовИзмененныхТочек = ЕстьИзмененияВДеревеИсполненияРейсов(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон);
	КонецЕсли;	
	
	текВыводитьСообщенияОбОшибках = мсвНеправильноЗаполненныхСтрок = Неопределено;
	
	Если мсвИндексовИзмененныхТочек.Количество() > 0 Тогда
		
		сткРезультатыПроверки = ПроверитьЗаполнениеСтрокДереваРейса(дрвРаботыПоТекущемуРейсу, мсвИндексовИзмененныхТочек);
			
		мсвПравильноЗаполненныхТочек = сткРезультатыПроверки.ПравильноЗаполненныеТочки;      
		мсвНеправильноЗаполненныхСтрок = сткРезультатыПроверки.НеправильноЗаполненныеСтроки;
		
		Если мсвПравильноЗаполненныхТочек.Количество() > 0 Тогда
			СформироватьПрохождениеТочекРейса(дрвРаботыПоТекущемуРейсу, мсвПравильноЗаполненныхТочек, Рейс, ТоварныйСоставГруза, ИдентификаторТочкиСИзмененнымСоставомГрузов);
			Если ПерезаполнятьДеревоТочекРейса Тогда
				ЗаполнитьДеревоРаботПоРейсу(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, Рейс, Истина);	
			КонецЕсли;
		КонецЕсли;	
		
		Если текВыводитьСообщенияОбОшибках Тогда
			Для каждого НеправильноЗаполненнаяТочка Из мсвНеправильноЗаполненныхСтрок Цикл
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НеправильноЗаполненнаяТочка.ТекстСообщения);
			КонецЦикла;
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

// Функция - Проверяет корректность заполнения строк дерева рейса.
//
// Параметры:
//  дрвРаботыПоТекущемуРейсу				 - ДеревоЗначений	 - дерево работ по рейсу.
//  мсвИндексовПроверяемыхТочекВДеревеРейса	 - Массив	 - массив индексов точек, которые необходимо проверить.
// 
// Возвращаемое значение:
//  Структура
//     * НеправильноЗаполненныеСтроки - Массив - массив неправильно заполненных строк представляющих собой структуры со свойствами:
//		** ИндексТочки - Число - индекс точки в дереве рейса.
//        ** ИндексРаботы - Число, Неопределено - индекс работы в дереве рейса, если речь идет о строке точки, то значение Неопределено.
//        ** ТекстСообщения - Строка - сообщение об ошибке.
//     * ПравильноЗаполненныеТочки  - Массив - массив индексов, правильно заполненных точек.
//
Функция ПроверитьЗаполнениеСтрокДереваРейса(дрвРаботыПоТекущемуРейсу, мсвИндексовПроверяемыхТочекВДеревеРейса)
	
	сткРезультат = Новый Структура("НеправильноЗаполненныеСтроки, ПравильноЗаполненныеТочки");
	
	мсвНеправильноЗаполненныхСтрок = Новый Массив;
	мсвПравильноЗаполненныеТочки = Новый Массив;
	
	ТочкаЗаполненаПравильно = Истина;
	УбытиеИзПредыдущейТочки = Неопределено;
	
	ИндексТочки = мсвИндексовПроверяемыхТочекВДеревеРейса[0] - 1;
	Пока ИндексТочки >= 0 Цикл
		СтрокаТочка = дрвРаботыПоТекущемуРейсу.Строки.Получить(ИндексТочки);
		Если СтрокаТочка.Выполнена И СтрокаТочка.ТочкаПройдена И НЕ СтрокаТочка.ТочкаОтменена Тогда
			УбытиеИзПредыдущейТочки	= СтрокаТочка.УбытиеФакт;	
			Прервать
		Иначе
			ИндексТочки = ИндексТочки - 1;
		КонецЕсли;
	КонецЦикла;
		
	Для каждого ИндексТочки Из мсвИндексовПроверяемыхТочекВДеревеРейса Цикл
		СтрокаТочка = дрвРаботыПоТекущемуРейсу.Строки.Получить(ИндексТочки);
				
		Если НЕ ЗначениеЗаполнено(СтрокаТочка.ПрибытиеФакт)	 Тогда
			ТочкаЗаполненаПравильно = Ложь;
			ТекстСообщения = Нстр("ru = 'Для точки с адресом %1 не заполнена дата прибытия'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТочка.АдресТочки);
			мсвНеправильноЗаполненныхСтрок.Добавить(Новый Структура("ИндексТочки, ИндексРаботы, ТекстСообщения",ИндексТочки, Неопределено, ТекстСообщения));
		ИначеЕсли НЕ УбытиеИзПредыдущейТочки = Неопределено И УбытиеИзПредыдущейТочки > СтрокаТочка.ПрибытиеФакт Тогда	
			ТочкаЗаполненаПравильно = Ложь;
			ТекстСообщения = Нстр("ru = 'Прибытие в точку с адресом %1 раньше чем убытие из предыдущей точки'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТочка.АдресТочки);
			мсвНеправильноЗаполненныхСтрок.Добавить(Новый Структура("ИндексТочки, ИндексРаботы, ТекстСообщения",ИндексТочки, Неопределено, ТекстСообщения));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТочка.НачалоРаботФакт) И НЕ СтрокаТочка.Отменена И НЕ СтрокаТочка.ДосрочноеУбытие Тогда
			ТочкаЗаполненаПравильно = Ложь;
			ТекстСообщения = Нстр("ru = 'Для точки с адресом %1 не заполнена дата начала работ'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТочка.АдресТочки);
			мсвНеправильноЗаполненныхСтрок.Добавить(Новый Структура("ИндексТочки, ИндексРаботы, ТекстСообщения",ИндексТочки, Неопределено, ТекстСообщения));	
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(СтрокаТочка.УбытиеФакт) И НЕ СтрокаТочка.Отменена Тогда
			ТочкаЗаполненаПравильно = Ложь;
			ТекстСообщения = Нстр("ru = 'Для точки с адресом %1 не заполнена дата убытия'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТочка.АдресТочки);
			мсвНеправильноЗаполненныхСтрок.Добавить(Новый Структура("ИндексТочки, ИндексРаботы, ТекстСообщения",ИндексТочки, Неопределено, ТекстСообщения));	
		КонецЕсли;
		
		СтрокиЗадания 	= СтрокаТочка.Строки;
		Для каждого СтрокаЗадание Из СтрокиЗадания Цикл
			СтрокиОперации = СтрокаЗадание.Строки;
			Для каждого СтрокаОперация Из СтрокиОперации Цикл
				СтрокиГрузовоеМесто = СтрокаОперация.Строки;
				Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовоеМесто Цикл
					ТекстСообщения = "";
					Отказ = Ложь;
				
					упПрохождениеТочки.ПроверитьЗаполнениеСтрокиРаботы(СтрокаГрузовоеМесто, СтрокаОперация.НазначениеСтроки, СтрокаЗадание.НазначениеСтроки, СтрокаГрузовоеМесто.НазначениеСтроки, ТекстСообщения, Отказ);
					Если Отказ Тогда
						ТочкаЗаполненаПравильно = Ложь;
						ИндексРаботы = СтрокиГрузовоеМесто.Индекс(СтрокаГрузовоеМесто);
						мсвНеправильноЗаполненныхСтрок.Добавить(Новый Структура("ИндексТочки, ИндексРаботы, ТекстСообщения",ИндексТочки, ИндексРаботы, ТекстСообщения));
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		// Проверяется что точки проходятся последовательно.
		Если мсвПравильноЗаполненныеТочки.Количество() > 0  Тогда
			ИндексПоследнейПравильноЗаполненнойТочки = мсвПравильноЗаполненныеТочки[мсвПравильноЗаполненныеТочки.ВГраница()];
			ТочкаЗаполненаПравильно = ТочкаЗаполненаПравильно И ИндексПоследнейПравильноЗаполненнойТочки + 1 = ИндексТочки;
		КонецЕсли;
				
		Если ТочкаЗаполненаПравильно Тогда
			мсвПравильноЗаполненныеТочки.Добавить(ИндексТочки);	
		КонецЕсли;
		
		Если СтрокаТочка.Выполнена Тогда
			УбытиеИзПредыдущейТочки	= СтрокаТочка.УбытиеФакт;	
		КонецЕсли;
			
	КонецЦикла;                 

	сткРезультат.НеправильноЗаполненныеСтроки = мсвНеправильноЗаполненныхСтрок;
	сткРезультат.ПравильноЗаполненныеТочки = мсвПравильноЗаполненныеТочки;
		
	Возврат сткРезультат;
КонецФункции

Процедура СформироватьПрохождениеТочекРейса(дрвРаботыПоТекущемуРейсу, мсвИндексовТочекВДеревеРейса, Рейс, ТоварныйСоставГруза = Неопределено, ИдентификаторТочкиСИзмененнымСоставомГрузов = Неопределено)
	
	ДатаДокументаПоПредыдущейТочке = Неопределено;
	
	Для каждого ИндексТочки Из мсвИндексовТочекВДеревеРейса Цикл
		
		СтрокаТочка = дрвРаботыПоТекущемуРейсу.Строки.Получить(ИндексТочки);
		
		ТекстОшибки = "";
		Отказ = Ложь;
		сткРеквизиты = Новый Структура();
		сткРеквизиты.Вставить("Рейс",	Рейс);
		сткРеквизиты.Вставить("ИдентификаторТочки", СтрокаТочка.ИдентификаторТочки);
		сткРеквизиты.Вставить("АдресТочки", СтрокаТочка.АдресТочки);
		сткРеквизиты.Вставить("Гараж", СтрокаТочка.Гараж);
		сткРеквизиты.Вставить("ТипТочки", СтрокаТочка.ТипТочки);
		сткРеквизиты.Вставить("ДатаДокументаПоПредыдущейТочке", ДатаДокументаПоПредыдущейТочке);
		сткРеквизиты.Вставить("СтрокаНомер", СтрокаТочка.ПорядокТочки);
		сткРеквизиты.Вставить("НачалоВыполнения", СтрокаТочка.ПрибытиеФакт);
		сткРеквизиты.Вставить("ОкончаниеВыполнения", СтрокаТочка.УбытиеФакт);
		сткРеквизиты.Вставить("НачалоРабот", СтрокаТочка.НачалоРаботФакт);
		сткРеквизиты.Вставить("РасстояниеОтПредыдущейТочки", СтрокаТочка.РасстояниеОтПредыдущейТочки);
		сткРеквизиты.Вставить("ДосрочноеУбытие", СтрокаТочка.ДосрочноеУбытие);
		сткРеквизиты.Вставить("ТочкаОтменена", СтрокаТочка.Отменена);
				
		тбзРаботы = Новый ТаблицаЗначений; 
		тбзРаботы.Колонки.Добавить("Выполнена", Новый ОписаниеТипов("Булево"));
		тбзРаботы.Колонки.Добавить("Отменена", Новый ОписаниеТипов("Булево"));
		тбзРаботы.Колонки.Добавить("Проблема", Новый ОписаниеТипов("СправочникСсылка.упПроблемы"));
		тбзРаботы.Колонки.Добавить("ПроблемаПричинаВозникновенияЭтойРаботы", Новый ОписаниеТипов("СправочникСсылка.упПроблемы"));		
		тбзРаботы.Колонки.Добавить("АдресРазгрузки", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРаботы.Колонки.Добавить("ИдентификаторРаботы");
		тбзРаботы.Колонки.Добавить("Задание");
		тбзРаботы.Колонки.Добавить("Операция");
		тбзРаботы.Колонки.Добавить("ГрузовоеМесто");
		тбзРаботы.Колонки.Добавить("КоличествоГрузов");
		тбзРаботы.Колонки.Добавить("ТипРегламентногоДокумента");
		тбзРаботы.Колонки.Добавить("КоличествоЭкземпляровДокумента");
		тбзРаботы.Колонки.Добавить("Сумма");
		тбзРаботы.Колонки.Добавить("ГрузРазделен");
		тбзРаботы.Колонки.Добавить("ДополнительнаяОперация");
		тбзРаботы.Колонки.Добавить("СкладскаяЯчейка");
		тбзРаботы.Колонки.Добавить("Высота");
		тбзРаботы.Колонки.Добавить("Длина");
		тбзРаботы.Колонки.Добавить("Ширина");
		тбзРаботы.Колонки.Добавить("Масса");
		тбзРаботы.Колонки.Добавить("Объем");
		тбзРаботы.Колонки.Добавить("КоличествоМест");
		тбзРаботы.Колонки.Добавить("КоличествоЗагрузочныхМетров");
		тбзРаботы.Колонки.Добавить("ИзмененыПараметры");
		тбзРаботы.Колонки.Добавить("ЗапрещеноСозданиеНовойТочкиМаршрута");
		
		СтрокиЗадание = СтрокаТочка.Строки;
		
		Для каждого СтрокаЗадание Из СтрокиЗадание Цикл
			
			СтрокиОперация = СтрокаЗадание.Строки;
			
			Для каждого СтрокаОперация Из СтрокиОперация Цикл
				
				СтрокиГрузовоеМесто = СтрокаОперация.Строки;
				
				Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовоеМесто Цикл
					НоваяСтрока = тбзРаботы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаГрузовоеМесто);
					НоваяСтрока.Выполнена = НЕ СтрокаГрузовоеМесто.Отменена; 
					НоваяСтрока.Задание = СтрокаЗадание.НазначениеСтроки;
					НоваяСтрока.Операция = СтрокаОперация.НазначениеСтроки;
					НоваяСтрока.ГрузовоеМесто = СтрокаГрузовоеМесто.НазначениеСтроки;
					НоваяСтрока.КоличествоГрузов = СтрокаГрузовоеМесто.КоличествоГрузов;
					НоваяСтрока.ДополнительнаяОперация	= СтрокаГрузовоеМесто.ДополнительнаяОперация;
					НоваяСтрока.Сумма = СтрокаГрузовоеМесто.Сумма;
					Если Ложь
						Или СтрокаГрузовоеМесто.Операция = Перечисления.упТипыОпераций.ПередачаДокументов 
						Или СтрокаГрузовоеМесто.Операция = Перечисления.упТипыОпераций.ПолучениеДокументов
					Тогда
						НоваяСтрока.КоличествоЭкземпляровДокумента = СтрокаГрузовоеМесто.Количество;
					КонецЕсли;
				КонецЦикла;

			КонецЦикла;
			
		КонецЦикла;
		
		сткРеквизиты.Вставить("Работы", тбзРаботы);
		Если Истина
			И ТоварныйСоставГруза <> Неопределено
			И СтрокаТочка.ИдентификаторТочки = ИдентификаторТочкиСИзмененнымСоставомГрузов 
			И ТоварныйСоставГруза.Количество() > 0
		Тогда
			сткРеквизиты.Вставить("ТоварныйСоставГруза", ТоварныйСоставГруза);	
		КонецЕсли;
		
		сткРеквизиты.Вставить("ПроверитьЗаполнение",Истина);
		
		текРезультат = упПрохождениеТочки.СформироватьПрохождениеЗаданнойТочки(Рейс, СтрокаТочка.ПорядокТочки, СтрокаТочка.ИдентификаторТочки, сткРеквизиты, ТекстОшибки, Отказ);
		
		ДатаДокументаПоПредыдущейТочке = сткРеквизиты.ДатаДокументаПоПредыдущейТочке;
			
	КонецЦикла;
		
КонецПроцедуры

// Возвращает признак наличия изменений в дереве.
//
// Параметры:
//  дрвРаботыПоТекущемуРейсу - СтрокаДереваЗначений - Позволяет читать и записывать данные в конкретных колонках строки.
//	дрвРаботыПоТекущемуРейсуЭталон - СтрокаДереваЗначений - Позволяет читать и записывать данные в конкретных колонках строки.
//
// Возвращаемое значение:
//   Массив - Результат выполнения.
//
Функция ЕстьИзмененияВДеревеИсполненияРейсов(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон) Экспорт
	
	мсвИзмененныхПрохожденийТочек = Новый Массив;
	
	КоличествоКолонок = дрвРаботыПоТекущемуРейсу.Колонки.Количество();
	
	СтрокиТочки = дрвРаботыПоТекущемуРейсу.Строки;
	Для каждого СтрокаТочка Из СтрокиТочки Цикл
		
		Если Ложь
			Или СтрокаТочка.ТочкаПройдена 
			Или НЕ СтрокаТочка.ФормироватьПрохождениеТочки
		Тогда
			Продолжить;
		КонецЕсли;
		
		Если Истина
			И НЕ СтрокаТочка.Выполнена 
			И НЕ СтрокаТочка.Отменена
		Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьИзменения = Ложь;
		
		ИндексСтроки = СтрокиТочки.Индекс(СтрокаТочка);
		
		ИдентификаторТочки = СтрокаТочка.ИдентификаторТочки;
		
		КоллекцияСтрокДрвРаботыЭталон = дрвРаботыПоТекущемуРейсуЭталон.Строки;
		
		СтрокаТочкаЭталон = КоллекцияСтрокДрвРаботыЭталон.Найти(ИдентификаторТочки, "ИдентификаторТочки");
		
		Если СтрокаТочкаЭталон = Неопределено Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если НЕ ЕстьИзменения Тогда
			Для й = 0 По КоличествоКолонок - 1 Цикл
				Если НЕ СтрокаТочка[й] = СтрокаТочкаЭталон[й] Тогда
					ЕстьИзменения = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ ЕстьИзменения Тогда
			
			СтрокиЗадания = СтрокаТочка.Строки;
			Для каждого СтрокаЗадание Из СтрокиЗадания Цикл
				
				ИндексСтрокиЗадание =  СтрокиЗадания.Индекс(СтрокаЗадание);
				
				Если ИндексСтрокиЗадание < СтрокаТочкаЭталон.Строки.Количество() Тогда
					
					СтрокаЗаданиеЭталон = СтрокаТочкаЭталон.Строки.Получить(ИндексСтрокиЗадание);
					
					СтрокиОперации	= СтрокаЗадание.Строки;
					Для каждого СтрокаОперация Из СтрокиОперации Цикл
						ИндексСтрокиОперация = СтрокиОперации.Индекс(СтрокаОперация);
						
						Если ИндексСтрокиОперация < СтрокаЗаданиеЭталон.Строки.Количество() Тогда
							
							СтрокаОперацияЭталон = СтрокаЗаданиеЭталон.Строки.Получить(ИндексСтрокиОперация);
							СтрокиГрузовыеМеста = СтрокаОперация.Строки;
							
							Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовыеМеста Цикл
								
								ИндексСтрокиГрузовоеМесто = СтрокиГрузовыеМеста.Индекс(СтрокаГрузовоеМесто);
								СтрокаГрузовоеМестоЭталон = СтрокаОперацияЭталон.Строки.Получить(ИндексСтрокиГрузовоеМесто);
								
								Для й = 0 По КоличествоКолонок - 1 Цикл
									Если НЕ СтрокаГрузовоеМесто[й] = СтрокаГрузовоеМестоЭталон[й] Тогда
										ЕстьИзменения = Истина;
										Прервать;
									КонецЕсли;
								КонецЦикла;
								
								Если ЕстьИзменения Тогда
									Прервать;
								КонецЕсли;	
								
							КонецЦикла;
						Иначе
							ЕстьИзменения = Истина;
						КонецЕсли;
						
						Если ЕстьИзменения Тогда
							Прервать;
						КонецЕсли;	
						
					КонецЦикла;
				Иначе
					ЕстьИзменения = Истина;
				КонецЕсли;
				
				Если ЕстьИзменения Тогда
					Прервать;
				КонецЕсли;	

			КонецЦикла;
			
		КонецЕсли;
			
		Если ЕстьИзменения Тогда
			мсвИзмененныхПрохожденийТочек.Добавить(ИндексСтроки);
		КонецЕсли;
				
	КонецЦикла;
		
	Возврат мсвИзмененныхПрохожденийТочек;
		
КонецФункции

// Процедура - Восстанавливает точку разгрузки.
//
// Параметры:
//  дрвРаботыПоТекущемуРейсу - ДеревоЗначений - Объект, представляющий собой древовидную структуру.
//	дрвРаботыПоТекущемуРейсуЭталон - ДеревоЗначений - Объект, представляющий собой древовидную структуру.
//	сткВозврата - Структура - Результат выполнения.
//
Процедура ВосстановитьТочкуРазгрузки(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, сткВозврата) Экспорт
	
	Задание 			= сткВозврата.Задание;
	ГрузовоеМесто 		= сткВозврата.ГрузовоеМесто;
	ОперацияРазгрузка	= Перечисления.упТипыОпераций.Разгрузка;
	ОперацияПогрузка    = Перечисления.упТипыОпераций.Погрузка;
	ОперацияВыемкаЦенностей 	= Перечисления.упТипыОпераций.ВыемкаЦенностей;
	ОперацияПередачаЦенностей	= Перечисления.упТипыОпераций.ПередачаЦенностей; 
	
	текРабота 		= Неопределено;
	РаботаНайдена 	= Ложь;
	
	// Поиск работы в эталонном дереве работ.
	Для каждого СтрокаТочкаЭталон Из дрвРаботыПоТекущемуРейсуЭталон.Строки Цикл
		СтрокиЗаданийЭталон = СтрокаТочкаЭталон.Строки;
		Для каждого СтрокаЗаданиеЭталон Из СтрокиЗаданийЭталон Цикл
			Если НЕ СтрокаЗаданиеЭталон.НазначениеСтроки = Задание Тогда
				Продолжить;
			КонецЕсли;
			СтрокиОперацияЭталон = СтрокаЗаданиеЭталон.Строки;
			Для каждого СтрокаОперацияЭталон  Из СтрокиОперацияЭталон Цикл
				Если НЕ СтрокаОперацияЭталон.НазначениеСтроки = ОперацияРазгрузка
						И НЕ СтрокаОперацияЭталон.НазначениеСтроки = ОперацияПередачаЦенностей Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокиРаботаЭталон = СтрокаОперацияЭталон.Строки;
				Для каждого СтрокаРаботаЭталон  Из СтрокиРаботаЭталон Цикл
					Если НЕ СтрокаРаботаЭталон.НазначениеСтроки = ГрузовоеМесто Тогда
						Продолжить;
					КонецЕсли;
					текРабота 		= СтрокаРаботаЭталон;
					РаботаНайдена 	= Истина;
					Прервать;
				КонецЦикла;
				
				Если РаботаНайдена Тогда Прервать КонецЕсли;
				
			КонецЦикла;
			
			Если РаботаНайдена Тогда Прервать КонецЕсли;
			
		КонецЦикла;
		
		Если РаботаНайдена Тогда Прервать КонецЕсли;
		
	КонецЦикла;
	
	// Восстановление работы в текущем дереве работ.
	Если НЕ текРабота = Неопределено Тогда
		
		Точка = Неопределено;
		
		СтрокиТочки = дрвРаботыПоТекущемуРейсу.Строки;
		
		Точка = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(СтрокиТочки, "ИдентификаторТочки", СтрокаТочкаЭталон.ИдентификаторТочки);
			
		Если Точка = Неопределено Тогда
			Точка = СтрокиТочки.Добавить();
			ЗаполнитьЗначенияСвойств(Точка, СтрокаТочкаЭталон);
			СтрокиТочки.Сортировать("ПорядокТочки");
			
			СтрокаЗадание = Точка.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаЗадание, СтрокаЗаданиеЭталон);
			
			СтрокаОперация = СтрокаЗадание.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОперация, СтрокаОперацияЭталон);
			
			// Добавляется основная работа - разгрузка.
			СтрокаРабота	= СтрокаОперация.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРабота, СтрокаРаботаЭталон);
			
			// Добавляются не основные работы.
			
			Для каждого СтрокаОперацияЭталон Из СтрокиОперацияЭталон Цикл
				
				Если СтрокаОперацияЭталон.НазначениеСтроки = ОперацияПогрузка
					ИЛИ  СтрокаОперацияЭталон.НазначениеСтроки = ОперацияРазгрузка 
					ИЛИ  СтрокаОперацияЭталон.НазначениеСтроки = ОперацияВыемкаЦенностей  
					ИЛИ  СтрокаОперацияЭталон.НазначениеСтроки = ОперацияПередачаЦенностей Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаОперация = СтрокаЗадание.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаОперация, СтрокаОперацияЭталон);
				
				СтрокиРаботаЭталон = СтрокаОперацияЭталон.Строки;
				Для каждого СтрокаРаботаЭталон Из СтрокиРаботаЭталон Цикл
					СтрокаРабота	= СтрокаОперация.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРабота, СтрокаРаботаЭталон);
				КонецЦикла;
								
			КонецЦикла;
			
		Иначе
			
			текЗадание = Неопределено;
			
			Точка.Отменена = Ложь;
			СтрокиЗадание = Точка.Строки;
			текЗадание = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(СтрокиЗадание, "НазначениеСтроки", СтрокаЗаданиеЭталон.НазначениеСтроки);
	
			Если текЗадание = Неопределено Тогда
				
				СтрокаЗадание = Точка.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗадание, СтрокаЗаданиеЭталон);
				
				СтрокаОперация = СтрокаЗадание.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаОперация, СтрокаОперацияЭталон);
				
				СтрокаРабота	= СтрокаОперация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРабота, СтрокаРаботаЭталон);

			Иначе
				
				текОперация = Неопределено;
				СтрокиОперации = текЗадание.Строки;
				текОперация = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(СтрокиОперации, "НазначениеСтроки", СтрокаОперацияЭталон.НазначениеСтроки);
				
				Если текОперация = Неопределено Тогда
					
					СтрокаОперация = СтрокиОперации.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОперация, СтрокаОперацияЭталон);
								
				КонецЕсли;
				
				текРабота = текОперация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(текРабота, СтрокаРаботаЭталон);
								
			КонецЕсли;
				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Установка флага "Выполнена" для все иерархии переданной строки.
//
// Параметры:
//  ЭлементыДерева - КоллекцияСтрокДереваЗначений - Представляет собой коллекцию строк,
//			подчиненных какой-либо строке дерева значений.
//	Выполнена - Булево - Выполнить.
//
Процедура УстановитьФлагиПотомкам(ЭлементыДерева, Выполнена) Экспорт
	
	Если ЭлементыДерева.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоНеВыполненных 	= 0;
	КоличествоВыполненных		= 0;
	
	Для каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если НЕ ЭлементДерева.Отменена 
			И НЕ ЗначениеЗаполнено(ЭлементДерева.Проблема) 
			И НЕ ЗначениеЗаполнено(ЭлементДерева.АдресРазгрузки) Тогда
			ЭлементДерева.Выполнена = Выполнена;	
			Потомки	= ЭлементДерева.Строки;				
			УстановитьФлагиПотомкам(Потомки, Выполнена);	
		КонецЕсли;
		
		Родитель = ЭлементДерева.Родитель;	
		
		Если ЭлементДерева.Выполнена = 0 Тогда
			КоличествоНеВыполненных = КоличествоНеВыполненных + 1;	
		ИначеЕсли ЭлементДерева.Выполнена = 1 Тогда
			КоличествоВыполненных = КоличествоВыполненных + 1;	
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Родитель = Неопределено Тогда
		Если КоличествоНеВыполненных = ЭлементыДерева.Количество() Тогда
			Родитель.Выполнена = 0;
		ИначеЕсли КоличествоВыполненных = ЭлементыДерева.Количество() Тогда
			Родитель.Выполнена = 1;
		Иначе
			Родитель.Выполнена = 2;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения элемента дерева.
//
// Параметры:
//  ЭлементДереваПриемник - Элемент - Приемник.
//	ЭлементДереваИсточник - Элемент - Источник.
//
Процедура ЗаполнитьЭлементДерева(ЭлементДереваПриемник, ЭлементДереваИсточник) Экспорт
	
	Точка = ЭлементДереваИсточник;	
	
	ЭтоДанныеФормы	= ТипЗнч(ЭлементДереваИсточник) = Тип("ДанныеФормыДерево");
	
	Если ЭтоДанныеФормы Тогда
		ЕстьКартинкаВыполнено = ЭлементДереваПриемник.Свойство("ИндексКартинкиВыполнено"); 
	Иначе
		ЕстьКартинкаВыполнено = ЭлементДереваПриемник.Владелец().Колонки.Найти("ИндексКартинкиВыполнено") <> Неопределено;
	КонецЕсли;
	
	НоваяСтрокаТочка = ЭлементДереваПриемник;
	ОчиститьЭлементыДерева(НоваяСтрокаТочка);
	
	ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, Точка);
	
	Если ЕстьКартинкаВыполнено Тогда
		УстановитьИндексКартинкиКВыполнению(НоваяСтрокаТочка, Точка);
	КонецЕсли;
	
	СтрокиЗадания	= ПолучитьСтроки(Точка, ЭтоДанныеФормы);
	
	Для каждого СтрокаЗадание Из СтрокиЗадания Цикл
		
		НоваяСтрокаЗадание = ДобавитьСтрокуВДеревоПриемник(НоваяСтрокаТочка);
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗадание, СтрокаЗадание);
		
		Если ЕстьКартинкаВыполнено Тогда
			УстановитьИндексКартинкиКВыполнению(НоваяСтрокаЗадание, СтрокаЗадание);
		КонецЕсли;
	
		СтрокиОперации = ПолучитьСтроки(СтрокаЗадание, ЭтоДанныеФормы);
		
		Для каждого СтрокаОперация Из СтрокиОперации Цикл
			
			НоваяСтрокаОперация = ДобавитьСтрокуВДеревоПриемник(НоваяСтрокаЗадание);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаОперация, СтрокаОперация);
			Если ЕстьКартинкаВыполнено Тогда
				УстановитьИндексКартинкиКВыполнению(НоваяСтрокаОперация, СтрокаОперация);
			КонецЕсли;

			СтрокиГрузовоеМесто = ПолучитьСтроки(СтрокаОперация, ЭтоДанныеФормы);
			
			Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовоеМесто Цикл
				НоваяСтрокаГрузовоеМесто = ДобавитьСтрокуВДеревоПриемник(НоваяСтрокаОперация);
				ЗаполнитьЗначенияСвойств(НоваяСтрокаГрузовоеМесто, СтрокаГрузовоеМесто);
				Если ЕстьКартинкаВыполнено Тогда
					УстановитьИндексКартинкиКВыполнению(НоваяСтрокаГрузовоеМесто, СтрокаГрузовоеМесто);
				КонецЕсли;

			КонецЦикла;
		КонецЦикла;
	КонецЦикла; 
		
КонецПроцедуры

// Изменяет индекс картинки строки.
//
// Параметры:
//  СтрокаПриемник - СтрокаДереваЗначений - Позволяет читать и записывать данные в конкретных колонках строки.
//	СтрокаИсточник - СтрокаДереваЗначений - Позволяет читать и записывать данные в конкретных колонках строки.
//
Процедура УстановитьИндексКартинкиКВыполнению(СтрокаПриемник, СтрокаИсточник)
	Если СтрокаИсточник.Отменена Тогда
		СтрокаПриемник.ИндексКартинкиВыполнено = 3;
	Иначе	
		СтрокаПриемник.ИндексКартинкиВыполнено = СтрокаИсточник.Выполнена;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти	

#Область ЗаполнениеТочкиПоДеревуПрохождений

Функция НайтиТочкуВДереве(КоллекцияТочекРейса, ТекущийПорядокТочки = Неопределено, Отказ)
	
	текРезультат = Неопределено;
	
	// Если не задан порядок.
	Если НЕ ЗначениеЗаполнено(ТекущийПорядокТочки) Тогда
		Для каждого РейсТочка Из КоллекцияТочекРейса Цикл
			
			// Берется первая не выполненная и не отмененная строка.
			Если НЕ РейсТочка.ТочкаПройдена И НЕ РейсТочка.ТочкаОтменена Тогда
				текРезультат = РейсТочка;
				Прервать
			КонецЕсли;
		КонецЦикла;
	Иначе
		мсвТочек 	= КоллекцияТочекРейса.НайтиСтроки(Новый Структура("ПорядокТочки", ТекущийПорядокТочки));
		Если мсвТочек.Количество() > 0 Тогда
			текРезультат = мсвТочек[0];	
		Иначе
			Отказ = Истина
		КонецЕсли;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Ищет первую не пройденную точку.
//
// Параметры:
//  ДанныеФормыДеревоРейс - ДанныеФормыДерево - Данные.
//	ДанныеФормыДеревоТочка - ДанныеФормыДерево - Данные.
//	Отказ - Булево - выполнить фиксирование изменений.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ЗаполнитьДеревоТочкиПервойНеПройденнойТочкой(ДанныеФормыДеревоРейс, ДанныеФормыДеревоТочка, Отказ) Экспорт
	
	сткРезультат = Новый Структура;
	сткРезультат.Вставить("ЭтоПерваяТочка", 	Ложь);
	сткРезультат.Вставить("ЭтоПоследняяТочка", 	Ложь);
	сткРезультат.Вставить("НачалоВыполнения", 	'00010101');
	сткРезультат.Вставить("НачалоРабот",  		'00010101');
	сткРезультат.Вставить("ОкончаниеВыполнения",'00010101');
	сткРезультат.Вставить("ЕстьНеПройденныеТочки", 	Ложь);
	сткРезультат.Вставить("НомерТекущейТочки", 	0);

	ЭтоДанныеФормы = ТипЗнч(ДанныеФормыДеревоРейс) = Тип("ДанныеФормыДерево");
	КоллекцияТочекРейса = ПолучитьСтроки(ДанныеФормыДеревоРейс, ЭтоДанныеФормы);
	текКоличествоТочек 	= КоллекцияТочекРейса.Количество();
	
	РейсТочка = НайтиТочкуВДереве(КоллекцияТочекРейса, , Отказ);
	Если Истина
		И НЕ Отказ 
		И РейсТочка <> Неопределено 
		И НЕ РейсТочка.ТочкаПройдена 
	Тогда
		текИндексТочки = КоллекцияТочекРейса.Индекс(РейсТочка);
		сткРезультат.ЭтоПерваяТочка    = текИндексТочки = 0;
		сткРезультат.ЭтоПоследняяТочка = текИндексТочки = текКоличествоТочек - 1;
		сткРезультат.НачалоВыполнения  = РейсТочка.ПрибытиеФакт;
		сткРезультат.НачалоРабот       = РейсТочка.НачалоРаботФакт;
		сткРезультат.ОкончаниеВыполнения   = РейсТочка.УбытиеФакт;
		сткРезультат.ЕстьНеПройденныеТочки	= Истина;
		сткРезультат.НомерТекущейТочки     = текИндексТочки + 1;
		ОчиститьЭлементыДерева(ДанныеФормыДеревоТочка);
		НоваяСтрокаТочка = ДобавитьСтрокуВДеревоПриемник(ДанныеФормыДеревоТочка);
		ЗаполнитьЭлементДерева(НоваяСтрокаТочка, РейсТочка);
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Возвращает структуру с данными по точке с учетом сдвига.
//
// Параметры:
//  ДанныеФормыДеревоРейс - ДанныеФормыДерево - Данные.
//	ДанныеФормыДеревоТочка - ДанныеФормыДерево - Данные.
//	ТекущийПорядокТочки - Число - Номер.
//	Сдвиг - Число - Выполнить.
//	НайтиТочку - Булево - выполнить.
//	Отказ - Булево - выполнить фиксирование изменений.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ЗаполнитьДеревоТочкиДаннымиТочкиСоСдвигом(ДанныеФормыДеревоРейс, ДанныеФормыДеревоТочка, ТекущийПорядокТочки = Неопределено, Сдвиг = 0, НайтиТочку = Ложь, Отказ) Экспорт
	
	сткРезультат = Новый Структура;
	сткРезультат.Вставить("ЭтоПерваяТочка",    Ложь);
	сткРезультат.Вставить("ЭтоПоследняяТочка", Ложь);
	сткРезультат.Вставить("НачалоВыполнения",   '00010101');
	сткРезультат.Вставить("НачалоРабот",        '00010101');
	сткРезультат.Вставить("ОкончаниеВыполнения",'00010101');
	сткРезультат.Вставить("ЕстьНеПройденныеТочки", Истина);
	сткРезультат.Вставить("НомерТекущейТочки",     0);
	
	ЭтоДанныеФормы = ТипЗнч(ДанныеФормыДеревоРейс) = Тип("ДанныеФормыДерево");
	КоллекцияТочекРейса = ПолучитьСтроки(ДанныеФормыДеревоРейс, ЭтоДанныеФормы);
	текКоличествоТочек 	= КоллекцияТочекРейса.Количество();
	
	РейсТочка = Неопределено;
	
	Если НайтиТочку Тогда
		РейсТочка = НайтиТочкуВДереве(КоллекцияТочекРейса, ТекущийПорядокТочки, Отказ);
	Иначе	
		// Перенести данные точки в соответствующую точку рейса.
		РейсТочка = ПеренестиТочкуВДерево(КоллекцияТочекРейса, ДанныеФормыДеревоТочка, ТекущийПорядокТочки, Отказ);
	КонецЕсли;
	
	Если Истина
		И НЕ Отказ 
		И РейсТочка <> Неопределено
	Тогда
		текИндексТочки = КоллекцияТочекРейса.Индекс(РейсТочка) + Сдвиг;
		Если НЕ Сдвиг = 0 Тогда
			Если текИндексТочки > -1 
				И текИндексТочки < текКоличествоТочек Тогда
				РейсТочка	= КоллекцияТочекРейса.Получить(текИндексТочки); 
			Иначе
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			сткРезультат.ЭтоПерваяТочка    = текИндексТочки = 0;
			сткРезультат.ЭтоПоследняяТочка = текИндексТочки = текКоличествоТочек - 1;
			сткРезультат.НачалоВыполнения  = РейсТочка.ПрибытиеФакт;
			сткРезультат.НачалоРабот       = РейсТочка.НачалоРаботФакт;
			сткРезультат.ОкончаниеВыполнения = РейсТочка.УбытиеФакт;
			сткРезультат.НомерТекущейТочки   = текИндексТочки + 1;
			ОчиститьЭлементыДерева(ДанныеФормыДеревоТочка);
			НоваяСтрокаТочка = ДобавитьСтрокуВДеревоПриемник(ДанныеФормыДеревоТочка);
			ЗаполнитьЭлементДерева(НоваяСтрокаТочка, РейсТочка);

		КонецЕсли;
	КонецЕсли;

	Возврат сткРезультат;
		
КонецФункции

// Перености данные по точки в дереве.
// 
// Параметры:
//  ДанныеФормыДеревоРейс - ДанныеФормыДерево - Данные.
//	ДанныеФормыДеревоТочка - ДанныеФормыДерево - Данные.
//	ТекущийПорядокТочки - Число - Номер.
//	Отказ - Булево - выполнить фиксирование изменений.
//
// Возвращаемое значение:
//   Произвольный - Результат выполнения.
//
Функция ПеренестиДанныеТочкиВДерево(ДанныеФормыДеревоРейс, ДанныеФормыДеревоТочка, ТекущийПорядокТочки, Отказ) Экспорт
	
	ЭтоДанныеФормы = ТипЗнч(ДанныеФормыДеревоРейс) = Тип("ДанныеФормыДерево");
	КоллекцияТочекРейса = ПолучитьСтроки(ДанныеФормыДеревоРейс, ЭтоДанныеФормы);
	РейсТочка = ПеренестиТочкуВДерево(КоллекцияТочекРейса,ДанныеФормыДеревоТочка, ТекущийПорядокТочки , Отказ);
	
	Возврат РейсТочка;
	
КонецФункции

// Возвращает ссылку на найденную точку в дереве по переданным параметрам.
//
// Параметры:
//  КоллекцияТочекРейса - ДанныеФормыКоллекция - Данные.
//	ДанныеТочка - ДанныеФормыДерево - Данные.
//	ТекущийПорядокТочки - Число - Номер.
//	Отказ - Булево - выполнить фиксирование изменений.
//
// Возвращаемое значение:
//   Произвольный - Результат выполнения.
//
Функция ПеренестиТочкуВДерево(КоллекцияТочекРейса, ДанныеТочка, ТекущийПорядокТочки, Отказ) Экспорт
	
	РейсТочка = Неопределено;
	РейсТочка = НайтиТочкуВДереве(КоллекцияТочекРейса, ТекущийПорядокТочки, Отказ);
	Если Истина
		И НЕ Отказ 
		И РейсТочка <> Неопределено 
	Тогда
		ЭтоДанныеФормы	= ТипЗнч(ДанныеТочка) = Тип("ДанныеФормыДерево");
		
		ОчиститьЭлементыДерева(РейсТочка);
		КоллекцияСтрокТочки = ПолучитьСтроки(ДанныеТочка, ЭтоДанныеФормы);
		Если КоллекцияСтрокТочки.Количество() > 0 Тогда
			КорневойЭлементТочки = КоллекцияСтрокТочки[0];
			ЗаполнитьЭлементДерева(РейсТочка, КорневойЭлементТочки);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат РейсТочка;
	
КонецФункции

Функция ПолучитьСтроки(СтрокаДерева, ЭтоДанныеФормы = Ложь)
	Если ЭтоДанныеФормы Тогда
		Строки = СтрокаДерева.ПолучитьЭлементы();
	Иначе	
		Строки = СтрокаДерева.Строки;
	КонецЕсли;
	Возврат Строки;
КонецФункции

#Конецобласти

#Область СобытиеМониторингаОстановка

// Создадим документы прохождения точки для указанных в Соответствие значений.
//
// Параметры:
//  СоответствиеОстановок - Соответствие - Ключ ДокументСсылка.упЗаданиеНаПеревозкуГруза, Значение таблица значений.
//  Отметка			 - Булево		 - Значение реквизита документа "СозданоПоСобытию".
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция ВыполнитьПроверкаСобытияОстановкиМониторингаОстановка(СоответствиеОстановок, Отметка=Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МаршрутПоРейсуСП.Регистратор КАК Документ,
	|	МаршрутПоРейсуСП.Идентификатор
	|ПОМЕСТИТЬ втПройденные
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И Идентификатор = &Идентификатор) КАК МаршрутПоРейсуСП
	|ГДЕ
	|	МаршрутПоРейсуСП.Пройдена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутПоРейсуСП.Регистратор,
	|	МаршрутПоРейсуСП.Рейс,
	|	МаршрутПоРейсуСП.Идентификатор,
	|	МаршрутПоРейсуСП.Предшественники,
	|	МаршрутПоРейсуСП.Адрес,
	|	МаршрутПоРейсуСП.Гараж,
	|	МаршрутПоРейсуСП.СтрокаНомер,
	|	МаршрутПоРейсуСП.ТипТочки,
	|	МаршрутПоРейсуСП.РасстояниеОтПредыдущейТочки
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И Идентификатор = &Идентификатор
	|				И НЕ Идентификатор В
	|						(ВЫБРАТЬ
	|							втПройденные.Идентификатор
	|						ИЗ
	|							втПройденные КАК втПройденные)) КАК МаршрутПоРейсуСП
	|ГДЕ
	|	НЕ МаршрутПоРейсуСП.Отменена
	|	И НЕ МаршрутПоРейсуСП.Пройдена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втМаршрут.Гараж ЕСТЬ NULL 
	|			ТОГДА РаботыПоРейсуСП.Выполнена
	|		КОГДА втМаршрут.Гараж = ЗНАЧЕНИЕ(Справочник.упГаражи.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ РаботыПоРейсуСП.Выполнена
	|	КОНЕЦ КАК Выполнена,
	|	РаботыПоРейсуСП.СтрокаНомер,
	|	РаботыПоРейсуСП.Проблема,
	|	РаботыПоРейсуСП.АдресРазгрузки,
	|	РаботыПоРейсуСП.Задание,
	|	РаботыПоРейсуСП.ГрузовоеМесто,
	|	РаботыПоРейсуСП.Операция,
	|	РаботыПоРейсуСП.КоличествоГрузов,
	|	РаботыПоРейсуСП.Отменена,
	|	РаботыПоРейсуСП.ИдентификаторРаботы,
	|	РаботыПоРейсуСП.ДополнительнаяОперация,
	|	РаботыПоРейсуСП.КоличествоДопОпераций,
	|	РаботыПоРейсуСП.ПроблемаПричинаВозникновенияЭтойРаботы,
	|	РаботыПоРейсуСП.ТипРегламентногоДокумента,
	|	РаботыПоРейсуСП.КоличествоЭкземпляровДокумента
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|					(ВЫБРАТЬ
	|						втМаршрут.Рейс
	|					ИЗ
	|						втМаршрут КАК втМаршрут)
	|				И Идентификатор В
	|					(ВЫБРАТЬ
	|						втМаршрут.Идентификатор
	|					ИЗ
	|						втМаршрут КАК втМаршрут)) КАК РаботыПоРейсуСП
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрут
	|		ПО РаботыПоРейсуСП.Рейс = втМаршрут.Рейс
	|			И РаботыПоРейсуСП.Идентификатор = втМаршрут.Идентификатор
	|ГДЕ
	|	НЕ РаботыПоРейсуСП.Отменена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Рейс КАК Рейс,
	|	втМаршрут.Предшественники КАК ПредшественникиТочки,
	|	втМаршрут.Адрес КАК АдресТочки,
	|	втМаршрут.Гараж КАК Гараж,
	|	втМаршрут.СтрокаНомер КАК ПорядокТочки,
	|	втМаршрут.ТипТочки КАК ТипТочки,
	|	втМаршрут.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокТочки
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	МаршрутПоРейсуСП.Идентификатор КАК Идентификатор
	//|ИЗ
	//|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК МаршрутПоРейсуСП
	//|ГДЕ
	//|	МаршрутПоРейсуСП.Пройдена
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	МаршрутПоРейсуСП.Идентификатор
	|";	
	
	Для каждого текСоответствие Из СоответствиеОстановок Цикл
		ДатаДокументаПоПредыдущейТочке = Неопределено;
		ТочкиМаршрута = текСоответствие.Значение;
		ИдентификаторыТочек = ПолучитьТаблицуИдентификаторов(ТочкиМаршрута);
		Для каждого ТочкаМаршрута Из ИдентификаторыТочек Цикл
			Отбор = Новый Структура("ИдентификаторТочки", ТочкаМаршрута.ИдентификаторТочки);
			НайденныеСтроки = ТочкиМаршрута.НайтиСтроки(Отбор);
			КоличествоТочек = НайденныеСтроки.Количество();
			НачалоПериода = НайденныеСтроки[0].НачалоВыполнения;
			КонецПериода  = НайденныеСтроки[0].ОкончаниеВыполнения;
			КоличествоСекунд = КонецПериода - НачалоПериода;
			СекундаРаспределения = КоличествоСекунд / КоличествоТочек;
			Сч = 0;
			ТекущийПериод = НачалоПериода;
			Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				Сч = Сч+1;
				НайденнаяСтрока.НачалоВыполнения = ТекущийПериод;
				НайденнаяСтрока.НачалоРабот = НайденнаяСтрока.НачалоВыполнения;					
				НайденнаяСтрока.ОкончаниеВыполнения = ТекущийПериод + СекундаРаспределения;
				ТекущийПериод = НайденнаяСтрока.ОкончаниеВыполнения;
				Если Сч >= КоличествоТочек Тогда
					НайденнаяСтрока.ОкончаниеВыполнения = КонецПериода;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Для каждого ТочкаМаршрута Из ТочкиМаршрута Цикл
			Если ТочкаМаршрута.Предшественники.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли; 
			ТекстОшибки = "";
			Отказ		= Ложь;
			ПроверитьСпособПрохожденияМаршрута(ТочкаМаршрута.Рейс, ТекстОшибки, Отказ);
			Если Отказ Тогда
				Событие = Нстр("ru = 'Ошибка при формировании прохождения точки'");
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,,ТочкаМаршрута.Рейс, ТекстОшибки);    
				Продолжить;
			КонецЕсли;
			ИдентификаторТочки = Новый УникальныйИдентификатор(ТочкаМаршрута.ИдентификаторТочки);
			Запрос.УстановитьПараметр("Рейс", ТочкаМаршрута.Рейс);
			Запрос.УстановитьПараметр("Идентификатор", ИдентификаторТочки);
			ПакетЗапросов = Запрос.ВыполнитьПакет();
			тбзРаботы		   = ПакетЗапросов[2].Выгрузить(); // табличная часть Работы.
			Выборка			   = ПакетЗапросов[3].Выбрать(); // реквизиты документа.
			// тбзПройденныеТочки = ПакетЗапросов[4].Выгрузить(); // пройденные точки .
			
			//мсвПройденныеТочки = Новый Массив;
			//Если тбзПройденныеТочки.Количество() Тогда
			//	мсвПройденныеТочки = тбзПройденныеТочки.ВыгрузитьКолонку("Идентификатор");
			//КонецЕсли;
						
			Если Выборка.Следующий() Тогда
				
				//// выполним проверку последовательности точек.
				//Если НЕ ПустаяСтрока(Выборка.ПредшественникиТочки)Тогда
				//	Если мсвПройденныеТочки.Количество() Тогда						
				//		мсвПредшественникиТочки = СтрРазделить(Выборка.ПредшественникиТочки,",");
				//		Для каждого текЭлемент Из мсвПредшественникиТочки Цикл
				//			
				//			ТекущийИдентификаторПоиска = текЭлемент;
				//			
				//			Если ТипЗнч(текЭлемент) = ТИП("Строка") Тогда
				//				ТекущийИдентификаторПоиска = Новый УникальныйИдентификатор(текЭлемент);
				//			КонецЕсли;
				//			
				//			Если мсвПройденныеТочки.Найти(ТекущийИдентификаторПоиска) = Неопределено Тогда
				//				ТекстОшибки = "Нарушена последовательность прохождения точек";
				//				Отказ		= Истина;
				//				Прервать;
				//			КонецЕсли;
				//		КонецЦикла;
				//	Иначе
				//		ТекстОшибки = "Нарушена последовательность прохождения точек";
				//		Отказ		= Истина;
				//	КонецЕсли;
				//КонецЕсли;
							
				Если НЕ Отказ Тогда
						
					сткРеквизиты = Новый Структура();
					сткРеквизиты.Вставить("Рейс"              ,	ТочкаМаршрута.Рейс);
					сткРеквизиты.Вставить("ИдентификаторТочки", ИдентификаторТочки);
					сткРеквизиты.Вставить("АдресТочки"        ,	Выборка.АдресТочки);
					сткРеквизиты.Вставить("Гараж"             , Выборка.Гараж);
					сткРеквизиты.Вставить("ТипТочки"          , Выборка.ТипТочки);
					сткРеквизиты.Вставить("ДатаДокументаПоПредыдущейТочке", ДатаДокументаПоПредыдущейТочке);
					сткРеквизиты.Вставить("СобытиеПрибытияНаТочку", ТочкаМаршрута.СобытиеМониторинга);
					
					сткРеквизиты.Вставить("СтрокаНомер", 		Выборка.ПорядокТочки);
					сткРеквизиты.Вставить("НачалоВыполнения", 	ТочкаМаршрута.НачалоВыполнения);
					сткРеквизиты.Вставить("ОкончаниеВыполнения",ТочкаМаршрута.ОкончаниеВыполнения);
					сткРеквизиты.Вставить("НачалоРабот", 		ТочкаМаршрута.НачалоРабот);
					сткРеквизиты.Вставить("РасстояниеОтПредыдущейТочки", Выборка.РасстояниеОтПредыдущейТочки);
					
					сткРеквизиты.Вставить("СозданоПоСобытию", Отметка);
					
					сткРеквизиты.Вставить("Работы", тбзРаботы);
					
					текРезультат = СформироватьДокумент(сткРеквизиты, , Истина, ТекстОшибки, Отказ, Истина);
					Если ЗначениеЗаполнено(текРезультат)  Тогда
						Для Каждого ТочкаМаршрутаПотомок Из ТочкиМаршрута Цикл
							ИндексПредшественника = ТочкаМаршрутаПотомок.Предшественники.Найти(ТочкаМаршрута.ИдентификаторТочки);
							Если ИндексПредшественника <> Неопределено Тогда
								ТочкаМаршрутаПотомок.Предшественники.Удалить(ИндексПредшественника);
							КонецЕсли; 
						КонецЦикла;
					КонецЕсли; 
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецФункции // ВыполнитьПроверкаСобытияОстановкиМониторингаОстановка()

// Выполним анализ таблицы данных для последующего обхода.
//
// Параметры:
//  ТаблицаДанных - ТаблицаЗначений - Данные для работы.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица идентификаторов строк.
//
Функция ПолучитьТаблицуИдентификаторов(ТаблицаДанных)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаДанных.ИдентификаторТочки КАК СТРОКА(36)) КАК ИдентификаторТочки
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	&ТаблицаДанных КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.ИдентификаторТочки,
	|	КОЛИЧЕСТВО(втДанные.ИдентификаторТочки) КАК Количество
	|ПОМЕСТИТЬ втГруппы
	|ИЗ
	|	втДанные КАК втДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанные.ИдентификаторТочки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГруппы.ИдентификаторТочки
	|ИЗ
	|	втГруппы КАК втГруппы
	|ГДЕ
	|	втГруппы.Количество > 1";
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаДанных",ТаблицаДанных);
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	Запрос.МенеджерВременныхТаблиц.Закрыть();

	Возврат ТаблицаРезультат;
	

КонецФункции // ПолучитьТаблицуИдентификаторов()

#КонецОбласти

#Область СобытиеМониторингаУстановкаСтатусаЗапроса

// Процедура - Установить статус запроса события мониторинга
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.упСобытиеМониторинга	 - Событие мониторинга.
//  СтатусЗапроса			 - ПеречислениеСсылка.упСтатусыЗапросовМобильныйКлиент - Статус запроса. 
//  ДополнительныеПараметры	 - Структура - дополнительные парамтеры для заполнения.
//  Отказ					 - Булево	 - Истина, в случае ошибки.
//
Процедура УстановитьСтатусЗапросаСобытияМониторинга(ДокументСсылка, СтатусЗапроса, ДополнительныеПараметры, Отказ) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если Ложь Тогда
		ДокументСсылка = Документы.упСобытиеМониторинга.ПустаяСсылка();
	КонецЕсли;
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	ДокументОбъект.СтатусЗапроса = СтатусЗапроса;
	ДокументОбъект.Статус = Перечисления.упСтатусыСобытияМониторинга.Обработано;
	ДокументОбъект.КонечнаяДата = ТекущаяДатаСеанса();
	ЗаполнитьЗначенияСвойств(ДокументОбъект, ДополнительныеПараметры);
	
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументСсылка,,,Отказ);
		ЗаписьЖурналаРегистрации("СобытиеМониторинга.УстановитьСтатус", УровеньЖурналаРегистрации.Ошибка, ,ДокументСсылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти


// Устанавливает заголовок формы прохождения точки.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//
Процедура УстановитьЗаголовокФормыПрохожденияТочки(Форма) Экспорт

	ОбъектПрохождениеТочки = Форма.Объект;
	Если Не ОбъектПрохождениеТочки.Ссылка.Пустая() Тогда
		
		ВидДокумента = НСтр("ru = 'Прохождение точки'");
		
		Если ОбъектПрохождениеТочки.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			Если ОбъектПрохождениеТочки.ВидКонтейнернойОперации = Перечисления.упВидыКонтейнерныхОпераций.Формирование Тогда
				ВидДокумента = НСтр("ru = 'Формирование контейнера'");
			Иначе	
				ВидДокумента = НСтр("ru = 'Расформирование контейнера'");
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОбъектПрохождениеТочки.Гараж) Тогда
			Если ОбъектПрохождениеТочки.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления Тогда
				ЗаголовокФормы = СтрШаблон(НСтр("ru = '%1 %2 от %3, выезд из гаража'"), ВидДокумента, ОбъектПрохождениеТочки.Номер, ОбъектПрохождениеТочки.Дата);
			ИначеЕсли ОбъектПрохождениеТочки.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения Тогда	
				ЗаголовокФормы = СтрШаблон(НСтр("ru = '%1 %2 от %3, возвращение в гараж'"), ВидДокумента, ОбъектПрохождениеТочки.Номер, ОбъектПрохождениеТочки.Дата);
			КонецЕсли; 
		Иначе
			ЗаголовокФормы = СтрШаблон(НСтр("ru = '%1 %2 от %3'"), ВидДокумента, ОбъектПрохождениеТочки.Номер, ОбъектПрохождениеТочки.Дата);
		КонецЕсли;
		
		Если ОбъектПрохождениеТочки.Проведен Тогда
			ЗаголовокФормы = СтрШаблон(НСтр("ru = '%1 (проведен)'"), ЗаголовокФормы);
		КонецЕсли; 
		
		Форма.Автозаголовок = Ложь;
		Форма.Заголовок = ЗаголовокФормы;
		
	КонецЕсли;

КонецПроцедуры

// Устанавливает заголовок формы движения документов рейса.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//
Процедура УстановитьЗаголовокФормыДвиженияДокументовРейса(Форма) Экспорт
	
	ОбъектДокумент = Форма.Объект;
	Если Не ОбъектДокумент.Ссылка.Пустая() Тогда
		Форма.Автозаголовок = Ложь;
		Если ОбъектДокумент.Проведен Тогда
			Форма.Заголовок = НСтр(СтрШаблон("ru = 'Движение документов рейса %1 от %2 (проведен)'", ОбъектДокумент.Номер, ОбъектДокумент.Дата));
		Иначе	
			Форма.Заголовок = НСтр(СтрШаблон("ru = 'Движение документов рейса %1 от %2'", ОбъектДокумент.Номер, ОбъектДокумент.Дата));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Получить тип операции в точке.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//  ИдентификаторТочки - Строка - содержит Идентификатор.
//
// Возвращаемое значение:
//   ПеречислениеСсылка - Результат выполнения, Операция.
//
Функция ПолучитьТипСамойПриоритетнойОперацииВТочке(Рейс, ИдентификаторТочки) Экспорт
	
	текРезультат = Перечисления.упТипыОпераций.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	упРаботыПоРейсуСрезПоследних.Операция,
	               |	ВЫБОР
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	               |			ТОГДА 1
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	               |			ТОГДА 2
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	               |			ТОГДА 3
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	               |			ТОГДА 4
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов)
	               |			ТОГДА 5
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов)
	               |			ТОГДА 6
	               |		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ДополнительнаяОперация)
	               |			ТОГДА 7
	               |		ИНАЧЕ 10
	               |	КОНЕЦ КАК Порядок
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	               |			,
	               |			Рейс = &Рейс
	               |				И Идентификатор = &ИдентификаторТочки) КАК упРаботыПоРейсуСрезПоследних
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Порядок";
	Запрос.УстановитьПараметр("Рейс", 				Рейс);
	Запрос.УстановитьПараметр("ИдентификаторТочки", ИдентификаторТочки);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Операция;	
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Получает таблицу адресов для погрузки.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьАдресаПогрузкиПоРейсу(Рейс) Экспорт
	
	тбзРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упМаршрутПоРейсуСрезПоследних.Адрес,
	               |	упРаботыПоРейсуСрезПоследних.Задание,
	               |	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	               |		ПО упРаботыПоРейсуСрезПоследних.Рейс = упМаршрутПоРейсуСрезПоследних.Рейс
	               |			И упРаботыПоРейсуСрезПоследних.Идентификатор = упМаршрутПоРейсуСрезПоследних.Идентификатор
	               |ГДЕ
	               |	упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
				   |	ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	тбзРезультат	= Запрос.Выполнить().Выгрузить();
		
	Возврат тбзРезультат;
		
КонецФункции

// Получает адрес для погрузки.
//
// Параметры:
//  Рейс			 - ДокументСсылка	 - Ссылка на элемент.
//  ГрузовоеМесто	 - СправочникСсылка.упГрузовыеМеста	 - грузовое место.
// 
// Возвращаемое значение:
//  СправочникСсылка.упАдреса - Результат выполнения.
//
Функция ПолучитьАдресПогрузки(Рейс, ГрузовоеМесто) Экспорт
	
	Результат = Справочники.упАдреса.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упМаршрутПоРейсуСрезПоследних.Адрес
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	               |		ПО упРаботыПоРейсуСрезПоследних.Рейс = упМаршрутПоРейсуСрезПоследних.Рейс
	               |			И упРаботыПоРейсуСрезПоследних.Идентификатор = упМаршрутПоРейсуСрезПоследних.Идентификатор
	               |ГДЕ
	               |	(упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	               |			ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	               |	И упРаботыПоРейсуСрезПоследних.ГрузовоеМесто = &ГрузовоеМесто";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("ГрузовоеМесто", ГрузовоеМесто);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Адрес;
	КонецЕсли;
		
	Возврат Результат;
		
КонецФункции

// Получает ссылку на последнюю пройденную точку рейсом.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ДокументСсылка - Результат выполнения.
//
Функция ПоследнееПрохождениеТочки(Рейс) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	упПрохождениеТочки.Ссылка
	               |ИЗ
	               |	Документ.упПрохождениеТочки КАК упПрохождениеТочки
	               |ГДЕ
	               |	упПрохождениеТочки.Рейс = &Рейс
	               |	И упПрохождениеТочки.Проведен
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	упПрохождениеТочки.СтрокаНомер УБЫВ";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Ссылка;
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

// Функция - Возвращает последняя дату изменения маршрута или работ рейса.
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс	 - рейс.
// 
// Возвращаемое значение:
//  ДатаВремя - последняя дата изменения маршрута или работ по рейсу.
//
Функция ПоследняяДатаИзмененияМаршрутаРаботРейса(Рейс) Экспорт
	
	сткРезультат = Новый Структура("Период, Регистратор", '00010101', Неопределено);
	
	Если ЗначениеЗаполнено(Рейс) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упМаршрутПоРейсу.Период КАК Период,
		               |	упМаршрутПоРейсу.Регистратор
		               |ИЗ
		               |	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
		               |ГДЕ
		               |	упМаршрутПоРейсу.Рейс = &Рейс
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	упРаботыПоРейсу.Период,
		               |	упРаботыПоРейсу.Регистратор
		               |ИЗ
		               |	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
		               |ГДЕ
		               |	упРаботыПоРейсу.Рейс = &Рейс
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период УБЫВ";
		Запрос.УстановитьПараметр("Рейс", Рейс);
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
		КонецЕсли; 
	КонецЕсли;

	Возврат сткРезультат;		
		
КонецФункции

// Выполнить получение точек.
//
// Параметры:
//  ГрузовоеМесто - СправочникСсылка - Ссылка на элемент.
//  ЭтоНачальнаяТочка - Булево - Начальная точка.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ПлановаяНачальнаяКонечнаяТочкаГрузовогоМеста(ГрузовоеМесто, ЭтоНачальнаяТочка = Истина) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки.
	    ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ АдресОтправления.Местоположение
	|ИЗ РегистрНакопления.упСостояниеПоЗаданиям КАК АдресОтправления
	|ГДЕ ИСТИНА
	|И (ЛОЖЬ
	|	ИЛИ (ИСТИНА
	|		И &ЭтоНачальнаяТочка
	|		И АдресОтправления.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|	ИЛИ (ИСТИНА
	|		И НЕ &ЭтоНачальнаяТочка
	|		И АдресОтправления.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)))
	|И АдресОтправления.ГрузовоеМесто = &ГрузовоеМесто
	|И ТИПЗНАЧЕНИЯ(АдресОтправления.Регистратор) = ТИП(Документ.упЗаданиеНаПеревозкуГруза)
	|";
	Запрос.УстановитьПараметр("ГрузовоеМесто", ГрузовоеМесто);
	Запрос.УстановитьПараметр("ЭтоНачальнаяТочка", ЭтоНачальнаяТочка);
	Результат = Запрос.Выполнить().Выгрузить()[0][0];
	Возврат Результат;
	
КонецФункции

#Область РегламентныеЗадания

// Процедура формирует записи по регистру "упСтатистикаНахожденияВТочкахПартнеров".
//
Процедура СобратьСтатистикуНахожденияВТочкахПартнеровЗаПериод() Экспорт
	
	Если ПолучитьФункциональнуюОпцию("упУчитыватьСтатистикуНахожденияВТочкахПартнеров") Тогда
		ПериодАнализаДней            = упСервисныеФункции.ПолучитьЗначениеКонстанты("упСтатистикаНахождениеВТочкахПартнеровПериодАнализа");
		ТребуемоеКоличествоПосещений = упСервисныеФункции.ПолучитьЗначениеКонстанты("упСтатистикаНахождениеВТочкахПартнеровТребуемоеКоличествоПосещений");
		
		ДатаСреза   = '00010101';
		ТекущаяДата = ТекущаяДата();
		
		Если ПериодАнализаДней > 0 Тогда
			ДатаСреза = НачалоДня(ТекущаяДата - 3600*24*ПериодАнализаДней);
		КонецЕсли;
		
		// очистим статистику
		ОтметкаВремени = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Пока Истина Цикл
						
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 100
			|	упСтатистикаНахожденияВТочкахПартнеров.Адрес
			|ИЗ
			|	РегистрСведений.упСтатистикаНахожденияВТочкахПартнеров КАК упСтатистикаНахожденияВТочкахПартнеров";
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				Прервать;
			Иначе	
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					НачатьТранзакцию();
					
					МенеджерЗаписи = РегистрыСведений.упСтатистикаНахожденияВТочкахПартнеров.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.Адрес = Выборка.Адрес;
					МенеджерЗаписи.Прочитать();
					МенеджерЗаписи.Удалить();
					
					Если ТранзакцияАктивна() Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;
				КонецЦикла; 
			КонецЕсли; 
			
			Если ТекущаяУниверсальнаяДатаВМиллисекундах() - ОтметкаВремени > 300000 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
				
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	упСтатусыРейсовСрезПоследних.Документ КАК Рейс
		|ПОМЕСТИТЬ втРейсы
		|ИЗ
		|	РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
		|ГДЕ
		|	упСтатусыРейсовСрезПоследних.Период > &ДатаСреза
		|	И упСтатусыРейсовСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Рейс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упМаршрутПоРейсуСрезПоследних.Адрес,
		|	СУММА(РАЗНОСТЬДАТ(упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт, упМаршрутПоРейсуСрезПоследних.УбытиеФакт, СЕКУНДА)) КАК ОбщееВремя,
		|	КОЛИЧЕСТВО(упМаршрутПоРейсуСрезПоследних.Идентификатор) КАК КоличествоПосещений,
		|	СУММА(РАЗНОСТЬДАТ(упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт, упМаршрутПоРейсуСрезПоследних.УбытиеФакт, СЕКУНДА)) / КОЛИЧЕСТВО(упМаршрутПоРейсуСрезПоследних.Идентификатор) КАК СреднееВремя
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
		|			,
		|			Рейс В
		|				(ВЫБРАТЬ
		|					втРейсы.Рейс
		|				ИЗ
		|					втРейсы)) КАК упМаршрутПоРейсуСрезПоследних
		|ГДЕ
		|	упМаршрутПоРейсуСрезПоследних.Пройдена
		|	И упМаршрутПоРейсуСрезПоследних.Регистратор ССЫЛКА Документ.упПрохождениеТочки
		|	И НЕ упМаршрутПоРейсуСрезПоследних.Регистратор.СобытиеПрибытияНаТочку = ЗНАЧЕНИЕ(Документ.упСобытиеМониторинга.ПустаяСсылка)
		|	И НЕ упМаршрутПоРейсуСрезПоследних.Регистратор.СобытиеУбытияСТочки = ЗНАЧЕНИЕ(Документ.упСобытиеМониторинга.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	упМаршрутПоРейсуСрезПоследних.Адрес
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(упМаршрутПоРейсуСрезПоследних.Идентификатор) >= &ТребуемоеКоличествоПосещений";
		Запрос.УстановитьПараметр("ДатаСреза"                   , ДатаСреза);
		Запрос.УстановитьПараметр("ТребуемоеКоличествоПосещений", ТребуемоеКоличествоПосещений);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				НачатьТранзакцию();
				НоваяЗапись = РегистрыСведений.упСтатистикаНахожденияВТочкахПартнеров.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				НоваяЗапись.ПериодАнализа = ПериодАнализаДней;
				НоваяЗапись.ТребуемоеКоличествоПосещений = ТребуемоеКоличествоПосещений;
				НоваяЗапись.Записать(Истина);
				Если ТранзакцияАктивна() Тогда
					ЗафиксироватьТранзакцию();
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#Конецобласти


#Область РазделениеРаботВТочке

// Процедура - Разделить работы в точке
//
// Параметры:
//  ОбработкаКорректировкаРейса	 - ОбработкаОбъект		 - обработка корректировка рейса.
//  ИдентификаторТочки			 - УникальныйИдентификатор	 - идентификатор точки в которой разделяются работы.
//  СписокОпераций				 - Массив					 - массив операций, которые перейдут в новую точку.
//  Маршрут					 - ТаблицаЗначений			 - маршрут.
//  Работы					 - ТаблицаЗначений			 - работы.
//  Отказ						 - Булево					 - Истина, в случае ошибки.
//
Процедура РазделитьРаботыВТочке(ОбработкаКорректировкаРейса = Неопределено, ИдентификаторТочки, СписокОпераций, Маршрут = Неопределено, Работы = Неопределено, Отказ) Экспорт
	
	Если НЕ ОбработкаКорректировкаРейса = Неопределено Тогда
		Маршрут 	= ОбработкаКорректировкаРейса.Маршрут;
		Работы	= ОбработкаКорректировкаРейса.Работы;
	КонецЕсли;
	
	мсвТочки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТочки));
	
	Если мсвТочки.Количество() > 0 Тогда
		
		ТочкаТекущая = мсвТочки[0];
		ИндексТекущейТочки = Маршрут.Индекс(ТочкаТекущая);
		сткПараметры = Новый Структура("ИдентификаторИсходнойТочки,
									|СтрокаДокумента,
									|ВременноеОкноНачало,
									|ВременноеОкноОкончание,
									|Адрес,
									|Гараж,
									|Предшественники");
		ЗаполнитьЗначенияСвойств(сткПараметры, ТочкаТекущая);
		сткПараметры.ИдентификаторИсходнойТочки	= ТочкаТекущая.Идентификатор;
		
		ИдентификаторНовойТочки = Неопределено;
		СтрокаНомер 	= ДобавитьТочкуВМаршрут(ОбработкаКорректировкаРейса, ТочкаТекущая, сткПараметры, ИндексТекущейТочки + 1, ИдентификаторНовойТочки, Маршрут);
		мсвТочкиНовая 	= Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторНовойТочки));
	     Если мсвТочкиНовая.Количество() > 0 Тогда
			ТочкаНовая =  мсвТочкиНовая[0];
		КонецЕсли;
	     			
		// Изменяется идентификатор у новых работ.
		Если НЕ ОбработкаКорректировкаРейса = Неопределено Тогда
			мсвРаботы = Работы.НайтиСтроки(Новый Структура("Идентификатор, Выполнена, Отменена", ИдентификаторТочки, Ложь, Ложь));	
		Иначе
			мсвРаботы = Работы.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТочки));
		КонецЕсли;	
		Для каждого СтрокаРабота Из мсвРаботы Цикл
			Если НЕ СписокОпераций.Найти(СтрокаРабота.Операция) = Неопределено Тогда
				СтрокаРабота.Идентификатор = ИдентификаторНовойТочки;
			КонецЕсли;
		КонецЦикла;
		
		// Изменяются номера строк у новых работ.
		тбзРаботыНовойТочки =  Работы.Выгрузить(Новый Структура("Идентификатор", ИдентификаторНовойТочки));
		ПронумероватьРаботы(тбзРаботыНовойТочки, Работы);
		
		тбзРаботыТекущейТочки = Работы.Выгрузить(Новый Структура("Идентификатор", ИдентификаторТочки));
		// Изменяются номера оставшихся строк в разделяемой точке.
		Если НЕ ОбработкаКорректировкаРейса = Неопределено Тогда
			мсвСтрокиСозданныеНеТекущимДокументом = тбзРаботыТекущейТочки.НайтиСтроки(Новый Структура("СтрокаДокумента", Ложь));
			Если мсвСтрокиСозданныеНеТекущимДокументом.Количество() = 0 Тогда
				ПронумероватьРаботы(тбзРаботыТекущейТочки, Работы);	
			КонецЕсли;
		Иначе
			ПронумероватьРаботы(тбзРаботыТекущейТочки, Работы);	
		КонецЕсли;	
				
		ОбновитьПредшественниковТочки(ТочкаТекущая, тбзРаботыТекущейТочки, Маршрут, Работы);
		ОбновитьПредшественниковТочки(ТочкаНовая, тбзРаботыНовойТочки, Маршрут, Работы);
		
		КоличествоТочек = Маршрут.Количество();
		Для й = ИндексТекущейТочки + 2 По КоличествоТочек - 1 Цикл
			СледующаяТочка = Маршрут[й];
			мсвПредшественников = СтрРазделить(СледующаяТочка.Предшественники, ",");
			ИндексПредшественникаВМассиве = мсвПредшественников.Найти(Строка(ИдентификаторТочки));
			// Если текущая точка была предшественником следующей.
			Если НЕ ИндексПредшественникаВМассиве = Неопределено Тогда
				
				тбзРаботыСледующейТочки	= Работы.Выгрузить(Новый Структура("Идентификатор", СледующаяТочка.Идентификатор));
				
				// Если текущая точка больше не предшественник следующей.
				Если НЕ ЭтоТочкаПредшественник(СледующаяТочка, ТочкаТекущая, тбзРаботыСледующейТочки, тбзРаботыТекущейТочки) Тогда
					мсвПредшественников.Удалить(ИндексПредшественникаВМассиве); // Из предшественников удаляется текущая точка.
				КонецЕсли;
				
				// Если новая точка становится предшественником следующей.
				Если ЭтоТочкаПредшественник(СледующаяТочка, ТочкаНовая, тбзРаботыСледующейТочки, тбзРаботыНовойТочки) Тогда
					мсвПредшественников.Добавить(Строка(ТочкаНовая.Идентификатор)); // Добавить новую точку в предшественники.
				КонецЕсли;
			КонецЕсли;
			СледующаяТочка.Предшественники = СтрСоединить(мсвПредшественников, ",");
		КонецЦикла;	
		
		СпланироватьРаботыПоМаршруту(ОбработкаКорректировкаРейса, ИндексТекущейТочки);
						
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПредшественниковТочки(Точка, тбзРаботыВТочке, Маршрут, Работы) 
	
	мсвНовыйМассивПредшественников = Новый Массив;
	мсвПредшественников = СтрРазделить(Точка.Предшественники, ",");
	
	Для каждого Предшественник Из мсвПредшественников Цикл
		
		ИдентификаторПредшественника = Новый УникальныйИдентификатор(Предшественник);
		
		мсвТочкаПредшественник = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторПредшественника));
		
		Если мсвТочкаПредшественник.Количество() > 0 Тогда
			ТочкаПредшественник 		= мсвТочкаПредшественник[0];
			тбзРаботыВТочкеПредшественника = Работы.Выгрузить(Новый Структура("Идентификатор", ИдентификаторПредшественника));
			
			Если ЭтоТочкаПредшественник(Точка, ТочкаПредшественник, тбзРаботыВТочке, тбзРаботыВТочкеПредшественника) Тогда
				мсвНовыйМассивПредшественников.Добавить(Предшественник);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Точка.Предшественники = СтрСоединить(мсвНовыйМассивПредшественников, ",");
	
КонецПроцедуры

Процедура ПронумероватьРаботы(тбзРаботы, Работы)
	
	тбзРаботы.Сортировать("СтрокаНомер");
	
	й = 1;
	Для каждого СтрокаРабота Из тбзРаботы Цикл
		СтрокаРабота.СтрокаНомер = й;
		й = й + 1;
	КонецЦикла;
	
	Для каждого СтрокаРабота Из тбзРаботы Цикл
		мсвСтрокиРаботы = Работы.НайтиСтроки(Новый Структура("ИдентификаторРаботы", СтрокаРабота.ИдентификаторРаботы));
		Если мсвСтрокиРаботы.Количество() > 0 Тогда
			мсвСтрокиРаботы[0].СтрокаНомер	= СтрокаРабота.СтрокаНомер;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция - проверяет является ли проверяемая точка предшественником текущей точки
//
// Параметры:
//  ТекущаяТочка			 - СтрокаТабличнойЧасти	 - точка маршрута.
//  ПроверяемаяТочка		 - СтрокаТабличнойЧасти	 - точка маршрута.
//  РаботыТекущаяТочка		 - ТаблицаЗначений - работы в текущей точке.
//  РаботыПроверяемаяТочка	 - ТаблицаЗначений - работы в проверяемой точке.
// 
// Возвращаемое значение:
//  Булево - Истина, если проверяемая точка, предшественник текущей.
//
Функция ЭтоТочкаПредшественник(ТекущаяТочка, ПроверяемаяТочка, РаботыТекущаяТочка, РаботыПроверяемаяТочка)
	
	Результат = Ложь;
	
	// Если проверяемая или текущая точка гараж.
	Если ЗначениеЗаполнено(ПроверяемаяТочка.Гараж)
			Или ЗначениеЗаполнено(ТекущаяТочка.Гараж) Тогда
		Результат = Истина;
	Иначе
		
		ЕстьПризнакОтмны = НЕ РаботыПроверяемаяТочка.Колонки.Найти("Отменена") = Неопределено;
		
		Для каждого СтрокаРабота Из РаботыТекущаяТочка Цикл
			
			// Если ведется работа с грузом в таре.
			Если ЗначениеЗаполнено(СтрокаРабота.Тара) Тогда
				
				сткОтбора = Новый Структура("Задание, 
										|ГрузовоеМесто, 
										|Операция", 
										СтрокаРабота.Задание, 
										СтрокаРабота.Тара, 
										упПрохождениеТочкиКлиентСервер.ОперацияПогрузкиПоГрузовомуМесту(СтрокаРабота.Тара));
				Если ЕстьПризнакОтмны Тогда
					сткОтбора.Вставить("Отменена", Ложь);
				КонецЕсли;						
				
				мсвРаботыВПроверяемойТочке = РаботыПроверяемаяТочка.НайтиСтроки(сткОтбора);
				// Если у предшественника есть работы по погрузке тары.																	
				Если мсвРаботыВПроверяемойТочке.Количество() > 0 Тогда
					Результат = Истина;	
				КонецЕсли;
			КонецЕсли;
			
			Если Не Результат Тогда

				// Если работа разгрузки
				Если упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(СтрокаРабота.Операция) Тогда
					
					сткОтбора = Новый Структура("Задание, 
											|ГрузовоеМесто, 
											|Операция", 
											СтрокаРабота.Задание, 
											СтрокаРабота.ГрузовоеМесто, 
											упПрохождениеТочкиКлиентСервер.ОперацияПогрузкиПоГрузовомуМесту(СтрокаРабота.ГрузовоеМесто));
					Если ЕстьПризнакОтмны Тогда
						сткОтбора.Вставить("Отменена", Ложь);
					КонецЕсли;	
					
					мсвРаботыВПроверяемойТочке = РаботыПроверяемаяТочка.НайтиСтроки(сткОтбора);
					// Анализируется наличие работ по погрузке грузового места у предшественника																
					Если мсвРаботыВПроверяемойТочке.Количество() > 0 Тогда
						Результат = Истина;	
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли; 
			
			Если Результат Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		
	КонецЕсли;	
		
	Возврат Результат;
	
КонецФункции

Функция ДобавитьТочкуВМаршрут(ОбработкаКорректировкаРейса, ТочкаТекущая, сткПараметры, ИндексТочки, ИдентификаторНовойТочки, Маршрут);
	
	Результат = Неопределено;
	
	Если НЕ ОбработкаКорректировкаРейса = Неопределено Тогда
		Результат = ОбработкаКорректировкаРейса.ДобавитьТочкуВМаршрут(ТочкаТекущая.ТипТочки, сткПараметры, ИндексТочки, ИдентификаторНовойТочки);
	Иначе	
		НоваяТочка = Маршрут.Вставить(ИндексТочки);
		
		ЗаполнитьЗначенияСвойств(НоваяТочка, ТочкаТекущая);	
		
		ИдентификаторНовойТочки	= Новый УникальныйИдентификатор; 
		НоваяТочка.Идентификатор	= ИдентификаторНовойТочки;
		НоваяТочка.ВремяРабот				= 0;
		НоваяТочка.РасстояниеОтПредыдущейТочки	= 0;
		НоваяТочка.ВремяОтПредыдущейТочки 		= 0;
	
		Для й = ИндексТочки По Маршрут.Количество()-1 Цикл
			Строка = Маршрут.Получить(й);
			Строка.НомерСтроки = й + 1;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СпланироватьРаботыПоМаршруту(ОбработкаКорректировкаРейса = Неопределено, ИндексТекущейТочки)
	
	Если НЕ ОбработкаКорректировкаРейса = Неопределено Тогда
		ПересчитыватьПредыдущиеТочки 	= Ложь;
		ПересчитыватьТекущуюТочку 	= Истина;
		ПересчитыватьСледующиеТочки 	= Истина;
		ОбработкаКорректировкаРейса.СпланироватьРаботыПоМаршруту(ИндексТекущейТочки, ПересчитыватьПредыдущиеТочки, ПересчитыватьТекущуюТочку,, ПересчитыватьСледующиеТочки);
		ОбработкаКорректировкаРейса.УстановитьПредставлениеМаршурта();
	КонецЕсли;
	
КонецПроцедуры

// Функция - Разрешено разделять работы
//
// Параметры:
//  СтрокаТочка - СтрокаТаблицы	 - строка таблицы маршрута.
//  СписокРабот - СтрокаТаблицы	 - строка таблицы работа.
// 
// Возвращаемое значение:
//  Булево - разрешено ли разделять работы.
//
Функция РазрешеноРазделятьРаботы(СтрокаТочка, СписокРабот) Экспорт
	Результат = Истина;
	Если СтрокаТочка.Отменена 
			Или СтрокаТочка.Пройдена Тогда
		Результат = Ложь;
	КонецЕсли;
	
	// Проверяется, что в точке есть работы добавленые этим документом.
	Если Результат Тогда
		Если СписокРабот.Количество() > 0  Тогда
			мсвСтрокиРаботВТочкеДоКорректировки = СписокРабот.НайтиСтроки(Новый Структура("Идентификатор", СтрокаТочка.Идентификатор));
			Если мсвСтрокиРаботВТочкеДоКорректировки.Количество() = 0 Тогда
				Результат = Ложь;	
			КонецЕсли;
		Иначе	
	          Результат = Ложь;	
		КонецЕсли;
	КонецЕсли;	
	
	// Проверяется, что в точке более чем один тип операций
	Если Результат Тогда
		тбзРаботыВТочке = СписокРабот.Выгрузить(Новый Структура("Идентификатор", СтрокаТочка.Идентификатор), "Операция");
		мсвТипыОпераций = тбзРаботыВТочке.ВыгрузитьКолонку("Операция");
		мсвТипыОпераций = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвТипыОпераций);
		Если мсвТипыОпераций.Количество() < 2 Тогда
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Результат;		
КонецФункции

#КонецОбласти

#КонецОбласти
