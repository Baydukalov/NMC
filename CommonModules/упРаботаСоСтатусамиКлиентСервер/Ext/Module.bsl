
#Область ПрограммныйИнтерфейс

// Функция - проверяет возможность редактирования маршрута рейса.
//
// Параметры:
//  Статус - ПеречислениеСсылка - Ссылка на элемент.
//	ОписаниеОшибки - Строка - Описание ошибки.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция РазрешеноРедактироватьМаршрутРейса(Статус, ОписаниеОшибки) Экспорт
	
	текРезультат = Истина;
	
	Если РейсВыполнен(Статус) Тогда	
		ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Нельзя редактировать маршрут рейса со статусом ""%1""'", Статус));
		текРезультат = Ложь;
	КонецЕсли;	
	
	Возврат текРезультат;
	
КонецФункции

// Функция - проверяет возможность создания корректировки рейса.
//
// Параметры:
//  Статус		 - ПеречислениеСсылка	 - Ссылка на элемент.
//  РежимПроведения	 - РежимПроведения		 - режим проведения.
// 
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция РазрешеноФормироватьКорректировкуРейса(Статус, РежимПроведения = Неопределено) Экспорт
	
	Результат = Истина;
	
	Если Ложь
		Или РежимПроведения = РежимЗаписиДокумента.Проведение 
		Или РежимПроведения = Неопределено 
	Тогда
		
		Если Ложь
			Или НЕ РейсПереданКВыполнению(Статус) 
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен")
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Завершен") 
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.ВыполненЧастично") 
		Тогда
			Результат = Ложь;
		КонецЕсли;
		
	Иначе	
		Если Ложь
			Или НЕ РейсПереданКВыполнению(Статус) 
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Завершен") 
		Тогда
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Функция - проверят состояние рейса.
//
// Параметры:
//  Статус - ПеречислениеСсылка.упСтатусыРейса - Ссылка на элемент.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция РейсПереданКВыполнению(Статус) Экспорт
	
	Результат = Ложь;
	
	Если Истина
		И Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Новый") 
		И Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КПланированиюТС")  
		И Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыРейса.НазначеноТС") 
	Тогда	
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - проверят состояние рейса.
//
// Параметры:
//  Статус - ПеречислениеСсылка.упСтатусыРейса - Ссылка на элемент.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция РейсВыполнен(Статус)
	
	текРезультат = Ложь;
	
	Если НЕ (Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Новый") 
		    Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КПланированиюТС")  
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.НазначеноТС")
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению")
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполняется")) Тогда	
		текРезультат = Истина;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти