
#Область ПрограммныйИнтерфейс

// Функция - Получить объект потокового чтения
//
// Параметры:
//	ЧитаемыеДанные - данные, которые необходимо последовательно прочитать.
// 
// Возвращаемое значение:
//	ЧтениеFastInfoset - объект чтения XML данных.
//
Функция ПолучитьОбъектПотоковогоЧтения(ЧитаемыеДанные) Экспорт
	текЧтение = Новый ЧтениеFastInfoset;
	текЧтение.УстановитьДвоичныеДанные(ЧитаемыеДанные);
	Возврат текЧтение;
КонецФункции

// Функция - Получить объект потоковой записи
//
// Параметры:
// 
// Возвращаемое значение:
//	ЗаписьFastInfoset - объект записи XML данных.
//
Функция ПолучитьОбъектПотоковойЗаписи() Экспорт
	текЗапись = Новый ЗаписьFastInfoset;
	текЗапись.УстановитьДвоичныеДанные();
	Возврат текЗапись;
КонецФункции

// Функция - Прочитать объект из потока
//
// Параметры:
//	Поток - объект чтения XML, которых нужно прочитать.
//	типобъекта - тип объекта для чтения.
// 
// Возвращаемое значение:
//	Результат чтения объекта.
//
Функция ПрочитатьОбъектИзПотока(Поток, типобъекта) Экспорт
	Возврат ФабрикаXDTO.ПрочитатьXML(Поток, типобъекта);
КонецФункции

// Функция - Записать объект поток
//
// Параметры:
//	Объект - объект, который необходимо записать.
//	Фабрика - XDTO-фабрика.
// 
// Возвращаемое значение:
//	Результат записи XDTO-объекта в объект записи XML.
//
Функция ЗаписатьОбъектВПоток(Объект, Фабрика = Неопределено) Экспорт
	xdtoФабрика = ?(Фабрика = Неопределено, ФабрикаXDTO, Фабрика);
	xmlЗапись = ПолучитьОбъектПотоковойЗаписи();
	xdtoФабрика.ЗаписатьXML(xmlЗапись, Объект);
	Возврат xmlЗапись.Закрыть();
КонецФункции

// Функция - Сериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо сериализовать.
// 
// Возвращаемое значение:
//	Результат сериализации объекта.	
//
Функция СериализоватьОбъект(Источник) Экспорт
	текОбъектЗаписи = сшпФункцииРаботыFastInfoset.ПолучитьОбъектПотоковойЗаписи();
	СериализаторXDTO.ЗаписатьXML(текОбъектЗаписи, Источник, НазначениеТипаXML.Явное);
	Возврат текОбъектЗаписи.Закрыть();
КонецФункции	

// Функция - Десериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо десериализовать.
// 
// Возвращаемое значение:
//	Результат десериализации объекта.	
//
Функция ДесериализоватьОбъект(Источник) Экспорт
	результатОбъект = Неопределено;
	потокЧтение = сшпФункцииРаботыFastInfoset.ПолучитьОбъектПотоковогоЧтения(Источник);
	результатОбъект = СериализаторXDTO.ПрочитатьXML(потокЧтение);
	потокЧтение.Закрыть();
	Возврат результатОбъект;
КонецФункции	

// Функция - Сформировать FastInfoset
//
// Параметры:
//	сткДанные - входящие данные для формирования.
//	ДобавитьОбъявление - признак необходимости добавления объявления в начало XML-документа.
// 
// Возвращаемое значение:
//	Запись FastInfoset.
//
Функция СформироватьFastInfoset(сткДанные, ДобавитьОбъявление = Ложь) Экспорт
	текВозврат = Неопределено;
	Если ЗначениеЗаполнено(сткДанные) и ТипЗнч(сткДанные) = Тип("Структура") тогда
		текЗапись = Новый ЗаписьFastInfoset;
		текЗапись.УстановитьДвоичныеДанные();
		Если ДобавитьОбъявление тогда
			текЗапись.ЗаписатьОбъявлениеXML();
		КонецЕсли;
		СформироватьУзелFastInfoset(текЗапись, сткДанные, "classData", Истина);
		текВозврат = текЗапись.Закрыть();
	КонецЕсли;
	Возврат текВозврат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьЭлементFastInfoset(ЗаписьXML, Данные, Имя)
	типЗначения = ТипЗнч(Данные);
	Если типЗначения = Тип("Структура") или типЗначения = Тип("Массив") тогда
		СформироватьУзелFastInfoset(ЗаписьXML, Данные, Имя);
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента(Имя);
		Если ЗначениеЗаполнено(Данные) тогда
			ЗаписьXML.ЗаписатьТекст(XMLСтрока(Данные));
		Иначе
			ЗаписьXML.ЗаписатьАтрибут("xsi:nil", "true");
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьУзелFastInfoset(Запись, Данные, Имя, ДобавитьПространстваИмен = Ложь)
	Запись.ЗаписатьНачалоЭлемента(Имя);
	Если ДобавитьПространстваИмен тогда
		Запись.ЗаписатьСоответствиеПространстваИмен("", "http://com.example/2012/10/31/sometext");
		Запись.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	КонецЕсли;	
	Если ТипЗнч(Данные) = Тип("Структура") тогда
		Для каждого текЭлемент из Данные цикл
			СформироватьЭлементFastInfoset(Запись, текЭлемент.Значение, текЭлемент.Ключ);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Данные) = Тип("Массив") тогда
		Для каждого текЭлемент из Данные цикл
			типЗначения = ТипЗнч(текЭлемент);
			Если типЗначения = Тип("Структура") тогда
				СформироватьУзелFastInfoset(Запись, текЭлемент, "row");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Запись.ЗаписатьКонецЭлемента();
КонецПроцедуры	

#КонецОбласти