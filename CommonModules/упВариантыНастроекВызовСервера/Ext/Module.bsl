#Область СлужебныеПроцедурыИФункции

// Сохраняет вариант настроек рабочих мест с точностью до пользователя.
//
// Параметры:
//    Настройки - Сохраняемые настройки.
//    КлючОбъекта - Ключ, по которому идентифицируются сохраняемые настройки.
//    ТекущийПользователь - Пользователь, для которого сохраняются настройки.
//
Процедура СохранитьВариантНастроек(Настройки, КлючОбъекта, ТекущийПользователь = Неопределено) Экспорт
	
	Если ТекущийПользователь = Неопределено Тогда
	    ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
	СоздатьНовыйВариант = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор"       , ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект"      , КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено Тогда
				ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, Выборка.КлючВарианта, Настройки);
				СоздатьНовыйВариант = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если СоздатьНовыйВариант Тогда
		НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
		НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
		НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
		НовыйВариант.Автор = ТекущийПользователь;
		НовыйВариант.ТолькоДляАвтора = Истина;
		НовыйВариант.Объект = КлючОбъекта;
		НовыйВариант.Записать();
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, Настройки);
	КонецЕсли;
	
КонецПроцедуры // СохранитьВариантНастроек()

#КонецОбласти

