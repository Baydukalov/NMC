
#Область ПрограммныйИнтерфейс

// Возвращает массив структур по данным рейса.
//
// Параметры:
//  мсвРейсы - Массив - Ссылка на элемент.
//	ТипыМаршрута - Строка - Тип маршрута.
//
// Возвращаемое значение:
//   Массив - Результат выполнения.
//
Функция ПолучитьШагиМаршрутовРейсов(мсвРейсы, ТипыМаршрута = "Факт") Экспорт

	мсвМаршрутыПоРейсам = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Адрес,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК НомерСтроки,
	|	упМаршрутПоРейсуСрезПоследних.Рейс
	|ПОМЕСТИТЬ втМаршрутРейса
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс В (&мсвРейсы)) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ
	|	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбМаршрутНачало.Адрес КАК АдресНачальный,
	|	тбМаршрутКонец.Адрес КАК АдресКонечный,
	|	тбМаршрутКонец.Адрес.Широта КАК НачальнаяТочкаШирота,
	|	тбМаршрутКонец.Адрес.Долгота КАК НачальнаяТочкаДолгота,
	|	тбМаршрутНачало.Адрес.Широта КАК КонечнаяТочкаШирота,
	|	тбМаршрутНачало.Адрес.Долгота КАК КонечнаяТочкаДолгота,
	|	тбМаршрутНачало.НомерСтроки,
	|	тбМаршрутКонец.Рейс
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	втМаршрутРейса КАК тбМаршрутНачало
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутРейса КАК тбМаршрутКонец
	|		ПО (тбМаршрутНачало.НомерСтроки = тбМаршрутКонец.НомерСтроки + 1)
	|			И тбМаршрутНачало.Рейс = тбМаршрутКонец.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРасстоянияМеждуТочками.Путь,
	|	втМаршрут.Рейс КАК Рейс,
	|	втМаршрут.НачальнаяТочкаШирота,
	|	втМаршрут.НачальнаяТочкаДолгота,
	|	втМаршрут.КонечнаяТочкаШирота,
	|	втМаршрут.КонечнаяТочкаДолгота
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	|		ПО втМаршрут.НачальнаяТочкаШирота = упРасстоянияМеждуТочками.НачальнаяШирота
	|			И втМаршрут.НачальнаяТочкаДолгота = упРасстоянияМеждуТочками.НачальнаяДолгота
	|			И втМаршрут.КонечнаяТочкаШирота = упРасстоянияМеждуТочками.КонечнаяШирота
	|			И втМаршрут.КонечнаяТочкаДолгота = упРасстоянияМеждуТочками.КонечнаяДолгота
	|			И втМаршрут.Рейс.ОграничениеМаршрутизатора = упРасстоянияМеждуТочками.Ограничение
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.НомерСтроки
	|ИТОГИ ПО
	|	Рейс";
	
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	текВыборкаРейсы	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока текВыборкаРейсы.Следующий() Цикл
		
		текРейс				= текВыборкаРейсы.Рейс;
		
		текВыборкаШагМаршрута = текВыборкаРейсы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		мсвМаршрутПоРейсу = Новый Массив;
		
		Пока текВыборкаШагМаршрута.Следующий() Цикл
			
			сткМаршрутШаг	= Новый Структура("НачальнаяТочкаШирота, НачальнаяТочкаДолгота, КонечнаяТочкаШирота, КонечнаяТочкаДолгота, Путь");
			
			ЗаполнитьЗначенияСвойств(сткМаршрутШаг, текВыборкаШагМаршрута); 
			мсвМаршрутПоРейсу.Добавить(сткМаршрутШаг);	
			
		КонецЦикла;
		
		сткРейс = Новый Структура("Рейс, ШагиМаршрута", текРейс, мсвМаршрутПоРейсу);
		мсвМаршрутыПоРейсам.Добавить(сткРейс);
		
	КонецЦикла;
	
	Возврат мсвМаршрутыПоРейсам;

КонецФункции // ПолучитьКоординатыМаршрутов()

// Процедура - назначения реквизита рейса.
//
// Параметры:
//  мсвРейсов - Массив - Ссылка на элемент.
//	сткРеквизиты - Структура - Перечислены реквизиты объекта.
//	Отказ - Булево - Зафиксировать изменение.
//
Процедура НазначитьРеквизитыРейса(мсвРейсов, сткРеквизиты, Отказ) Экспорт
	
	Для каждого текРейс Из мсвРейсов Цикл
		
		ОбъектРейс = текРейс.ПолучитьОбъект();
		сткЭкипаж = упРейс.ПолучитьЭкипажРейса(текРейс);
		
		Если ЗначениеЗаполнено(сткЭкипаж.Подтверждение) Тогда
			ОбъектПодтверждениеЗаявки = сткЭкипаж.Подтверждение.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(ОбъектПодтверждениеЗаявки, сткРеквизиты);
			
			Попытка
				ОбъектПодтверждениеЗаявки.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			КонецПопытки;
			
		Иначе
			НазначеноТС = ЗначениеЗаполнено(сткЭкипаж.ТранспортноеСредство);
			
			ЗаполнитьЗначенияСвойств(сткЭкипаж, сткРеквизиты);
			ОбъектРейс.ДополнительныеСвойства.Вставить("ИзмененЭкипаж", сткЭкипаж);
			
			Если НазначеноТС И Не ЗначениеЗаполнено(сткЭкипаж.ТранспортноеСредство) Тогда
				тзМаршрут = ПолучитьМаршрут(текРейс);
				Если тзМаршрут.Количество() Тогда
					мсвТочкиЗавершения	= тзМаршрут.НайтиСтроки(Новый Структура("ТипТочки", Перечисления.упТипыТочекМаршрута.ТочкаЗавершения));	
					мсвТочкиОтправления = тзМаршрут.НайтиСтроки(Новый Структура("ТипТочки", Перечисления.упТипыТочекМаршрута.ТочкаОтправления));
					Если мсвТочкиОтправления.Количество() Или мсвТочкиЗавершения.Количество() Тогда
						Если мсвТочкиОтправления.Количество() Тогда
							тзМаршрут.Удалить(мсвТочкиОтправления[0]);
							Если тзМаршрут.Количество() Тогда
								НачалоМаршрута = тзМаршрут[0];
								НачалоМаршрута.РасстояниеОтПредыдущейТочки = 0;
								НачалоМаршрута.ВремяОтПредыдущейТочки = 0;
							КонецЕсли;
						КонецЕсли;
						Если мсвТочкиЗавершения.Количество() Тогда
							тзМаршрут.Удалить(мсвТочкиЗавершения[0]);
						КонецЕсли; 
						ОбъектРейс.ДополнительныеСвойства.Вставить("МаршрутРаботыПодготовлены", Истина);
						ОбъектРейс.ДополнительныеСвойства.Вставить("МодифицированМаршрут"     , Истина);
						ОбъектРейс.ДополнительныеСвойства.Вставить("МодифицированыРаботы"     , Ложь);
						ОбъектРейс.ДополнительныеСвойства.Вставить("Маршрут"                  , тзМаршрут);
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
			
			Если Истина
				И Не НазначеноТС 
				И ЗначениеЗаполнено(сткЭкипаж.ТранспортноеСредство)
			Тогда
			
				сткРеквизитыТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(сткЭкипаж.ТранспортноеСредство, "АвтоматическиПодбиратьГаражВРейсе, АвтоматическиПодбиратьВозвращениеВГаражВРейсе");
				АвтоматическиПодбиратьГаражОтправления = сткРеквизитыТС.АвтоматическиПодбиратьГаражВРейсе;
				АвтоматическиПодбиратьГаражВозвращения = сткРеквизитыТС.АвтоматическиПодбиратьВозвращениеВГаражВРейсе;
				
				Если Ложь
					Или АвтоматическиПодбиратьГаражОтправления
					Или АвтоматическиПодбиратьГаражВозвращения
				Тогда
					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	упПлановыеПараметрыРейса.ЗонаПогрузки,
					|	упПлановыеПараметрыРейса.ЗонаРазгрузки
					|ИЗ
					|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
					|ГДЕ
					|	упПлановыеПараметрыРейса.Рейс = &Рейс";
					Запрос.УстановитьПараметр("Рейс", текРейс);
					Результат = Запрос.Выполнить();
					Если Не Результат.Пустой() Тогда
						Выборка = Результат.Выбрать();
						Выборка.Следующий();
						
						сткГаражи = Справочники.упГаражи.ПодобратьГаражиДляТранспортногоСредства(сткЭкипаж.ТранспортноеСредство, Выборка.ЗонаПогрузки, Выборка.ЗонаРазгрузки);
						
						Если Ложь
							Или сткГаражи.АдресВыезда <> Неопределено
							Или сткГаражи.АдресВозвращения <> Неопределено
						Тогда
							
							тзМаршрут = ПолучитьМаршрут(текРейс);
							Если тзМаршрут.Количество() Тогда
								мсвПредшественников = Новый Массив;
								
								Если Истина
									И АвтоматическиПодбиратьГаражОтправления
									И сткГаражи.АдресВыезда <> Неопределено 
									И тзМаршрут.Найти(Перечисления.упТипыТочекМаршрута.ТочкаОтправления, "ТипТочки") = Неопределено
								Тогда
									
									текТочкаОтправления = тзМаршрут.Вставить(0);	
									текТочкаОтправления.Рейс          = текРейс;
									текТочкаОтправления.ТипТочки      = Перечисления.упТипыТочекМаршрута.ТочкаОтправления;
									текТочкаОтправления.Адрес 		  = сткГаражи.АдресВыезда;
									текТочкаОтправления.Гараж 		  = сткГаражи.ГаражВыезда;
									текТочкаОтправления.Идентификатор = Новый УникальныйИдентификатор;
									
									тзМаршрутДляРасчета = Новый ТаблицаЗначений;
									тзМаршрутДляРасчета.Колонки.Добавить("НомерСтроки"  , Новый ОписаниеТипов("Число"));
									тзМаршрутДляРасчета.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
									тзМаршрутДляРасчета.Колонки.Добавить("Адрес"        , Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
									Сч = 0;
									Пока Сч < 2 Цикл
										текСтрока = тзМаршрут[Сч];
										
										НоваяСтрока = тзМаршрутДляРасчета.Добавить();
										НоваяСтрока.НомерСтроки = Сч + 1;
										НоваяСтрока.Идентификатор = Строка(текСтрока.Идентификатор);
										НоваяСтрока.Адрес = текСтрока.Адрес;
										Сч = Сч + 1;
									КонецЦикла;		
									соотРасстояния = упМаршрутизация.ПолучитьРасстояниеИВремяМеждуТочками(тзМаршрутДляРасчета, 3600, Истина,, ОбъектРейс.ОграничениеМаршрутизатора);
									
									текСледующаяТочка = тзМаршрут[1];
									
									сткДанные = соотРасстояния.Получить(текТочкаОтправления.Идентификатор);
									ДеньУбытия = ДеньНедели(текСледующаяТочка.ПлановоеПрибытие - сткДанные.Время*3600);
									Если ДеньУбытия = 1 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяПонедельник;
									ИначеЕсли ДеньУбытия = 2 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяВторник;	
									ИначеЕсли ДеньУбытия = 3 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяСреда;	
									ИначеЕсли ДеньУбытия = 4 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяЧетверг;	
									ИначеЕсли ДеньУбытия = 5 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяПятница;	
									ИначеЕсли ДеньУбытия = 6 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяСуббота;	
									ИначеЕсли ДеньУбытия = 7 Тогда
										текСледующаяТочка.ВремяОтПредыдущейТочки = сткДанные.ВремяВоскресенье;	
									КонецЕсли; 		
									текСледующаяТочка.РасстояниеОтПредыдущейТочки = сткДанные.Расстояние;
									текСледующаяТочка.ПроездНеРассчитан = сткДанные.ПроездНеРассчитан;
									
									текТочкаОтправления.ПлановоеУбытие 		= текСледующаяТочка.ПлановоеПрибытие - текСледующаяТочка.ВремяОтПредыдущейТочки*3600;
									текТочкаОтправления.ПлановоеПрибытие 	= текТочкаОтправления.ПлановоеУбытие;
									текТочкаОтправления.НачалоРаботПлан 	= текТочкаОтправления.ПлановоеПрибытие;
									
									мсвПредшественников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(текТочкаОтправления.Идентификатор);
									Для Сч = 1 По тзМаршрут.Количество() - 1 Цикл
										текТочка = тзМаршрут[Сч];
										Если ЗначениеЗаполнено(текТочка.Предшественники) Тогда
											текТочка.Предшественники = СтрШаблон("%1,%2", текТочка.Предшественники, текТочкаОтправления.Идентификатор);
										Иначе
											текТочка.Предшественники = Строка(текТочкаОтправления.Идентификатор);
										КонецЕсли; 
										мсвПредшественников.Добавить(текТочка.Идентификатор);
									КонецЦикла; 
								КонецЕсли; 
								
								Если Истина
									И АвтоматическиПодбиратьГаражВозвращения
									И сткГаражи.АдресВозвращения <> Неопределено 
									И тзМаршрут.Найти(Перечисления.упТипыТочекМаршрута.ТочкаЗавершения, "ТипТочки") = Неопределено
								Тогда
									
									Если мсвПредшественников.Количество() = 0 Тогда
										мсвПредшественников = тзМаршрут.ВыгрузитьКолонку("Идентификатор");
									КонецЕсли;
									
									текТочкаЗавершения = тзМаршрут.Вставить(тзМаршрут.Количество());	
									текТочкаЗавершения.Рейс = текРейс;
									текТочкаЗавершения.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения;
									текТочкаЗавершения.Адрес = сткГаражи.АдресВозвращения;
									текТочкаЗавершения.Гараж = сткГаражи.ГаражВозвращения;
									текТочкаЗавершения.Идентификатор = Новый УникальныйИдентификатор;
									текТочкаЗавершения.Предшественники = СтрСоединить(мсвПредшественников, ",");
									
									тзМаршрутДляРасчета = Новый ТаблицаЗначений;
									тзМаршрутДляРасчета.Колонки.Добавить("НомерСтроки"  , Новый ОписаниеТипов("Число"));
									тзМаршрутДляРасчета.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
									тзМаршрутДляРасчета.Колонки.Добавить("Адрес"        , Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
									Сч = тзМаршрут.Количество() - 2;
									Пока Сч < тзМаршрут.Количество() Цикл
										текСтрока = тзМаршрут[Сч];
										
										НоваяСтрока = тзМаршрутДляРасчета.Добавить();
										НоваяСтрока.НомерСтроки = Сч + 1;
										НоваяСтрока.Идентификатор = Строка(текСтрока.Идентификатор);
										НоваяСтрока.Адрес = текСтрока.Адрес;
										Сч = Сч + 1;
									КонецЦикла;		
									соотРасстояния = упМаршрутизация.ПолучитьРасстояниеИВремяМеждуТочками(тзМаршрутДляРасчета, 3600, Истина,, ОбъектРейс.ОграничениеМаршрутизатора);
									
									текПредыдущаяТочка = тзМаршрут[Сч - 2];
									
									сткДанные = соотРасстояния.Получить(текПредыдущаяТочка.Идентификатор);
									ДеньУбытия = ДеньНедели(текПредыдущаяТочка.ПлановоеУбытие);
									Если ДеньУбытия = 1 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяПонедельник;
									ИначеЕсли ДеньУбытия = 2 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяВторник;	
									ИначеЕсли ДеньУбытия = 3 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяСреда;	
									ИначеЕсли ДеньУбытия = 4 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяЧетверг;	
									ИначеЕсли ДеньУбытия = 5 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяПятница;	
									ИначеЕсли ДеньУбытия = 6 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяСуббота;	
									ИначеЕсли ДеньУбытия = 7 Тогда
										текТочкаЗавершения.ВремяОтПредыдущейТочки = сткДанные.ВремяВоскресенье;	
									КонецЕсли; 		
									
									текТочкаЗавершения.РасстояниеОтПредыдущейТочки = сткДанные.Расстояние;
									текТочкаЗавершения.ПроездНеРассчитан 	= сткДанные.ПроездНеРассчитан;
									текТочкаЗавершения.ПлановоеПрибытие 	= текПредыдущаяТочка.ПлановоеУбытие + текТочкаЗавершения.ВремяОтПредыдущейТочки*3600;
									текТочкаЗавершения.НачалоРаботПлан 		= текТочкаЗавершения.ПлановоеПрибытие;
									текТочкаЗавершения.ПлановоеУбытие 		= текТочкаЗавершения.ПлановоеПрибытие;
								КонецЕсли; 
								
								Сч = 1;
								Для каждого текСтрока Из тзМаршрут Цикл
									текСтрока.СтрокаНомер = Сч;
									Сч = Сч + 1;
								КонецЦикла; 
								
								ОбъектРейс.ДополнительныеСвойства.Вставить("МаршрутРаботыПодготовлены", Истина);
								ОбъектРейс.ДополнительныеСвойства.Вставить("МодифицированМаршрут"     , Истина);
								ОбъектРейс.ДополнительныеСвойства.Вставить("МодифицированыРаботы"     , Ложь);
								ОбъектРейс.ДополнительныеСвойства.Вставить("Маршрут"                  , тзМаршрут);
							КонецЕсли; 
						КонецЕсли; 
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
			
			Попытка
				ОбъектРейс.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			КонецПопытки;
			
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры // НазначитьРеквизитыРейса()

// Возвращает структуру с количеством непройденных точек.
//
// Параметры:
//  Рейс - ДокументСсылка - Ссылка на элемент.
//  Регистратор - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПолучитьТаблицуНепройденныхТочек(Рейс, Регистратор) Экспорт

	сткРезультат = Новый Структура("НепройденныеТочки, КоличествоТочек", Неопределено, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Период,
	|	упМаршрутПоРейсу.Рейс,
	|	упМаршрутПоРейсу.Идентификатор,
	|	упМаршрутПоРейсу.Адрес,
	|	упМаршрутПоРейсу.Гараж,
	|	упМаршрутПоРейсу.ТипТочки,
	|	упМаршрутПоРейсу.СтрокаНомер,
	|	упМаршрутПоРейсу.Пройдена,
	|	упМаршрутПоРейсу.Отменена,
	|	упМаршрутПоРейсу.РасстояниеОтПредыдущейТочки,
	|	упМаршрутПоРейсу.ВремяОтПредыдущейТочки,
	|	упМаршрутПоРейсу.ПлановоеПрибытие,
	|	упМаршрутПоРейсу.ПлановоеУбытие,
	|	упМаршрутПоРейсу.ПрибытиеФакт,
	|	упМаршрутПоРейсу.УбытиеФакт,
	|	упМаршрутПоРейсу.НачалоРаботПлан,
	|	упМаршрутПоРейсу.НачалоРаботФакт,
	|	упМаршрутПоРейсу.Гараж КАК Гараж1,
	|	упМаршрутПоРейсу.Предшественники
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Рейс = &Рейс
	|	И НЕ упМаршрутПоРейсу.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Рейс,
	|	втМаршрут.Идентификатор,
	|	втМаршрут.Адрес,
	|	втМаршрут.Гараж,
	|	втМаршрут.ТипТочки,
	|	втМаршрут.СтрокаНомер,
	|	втМаршрут.Пройдена,
	|	втМаршрут.Отменена,
	|	втМаршрут.РасстояниеОтПредыдущейТочки,
	|	втМаршрут.ВремяОтПредыдущейТочки,
	|	втМаршрут.ПлановоеПрибытие,
	|	втМаршрут.ПлановоеУбытие,
	|	втМаршрут.ПрибытиеФакт,
	|	втМаршрут.УбытиеФакт,
	|	втМаршрут.НачалоРаботПлан,
	|	втМаршрут.НачалоРаботФакт,
	|	втМаршрут.Гараж КАК Гараж1,
	|	втМаршрут.Предшественники
	|ПОМЕСТИТЬ втТочки
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(втМаршрут.Период) КАК Период,
	|			втМаршрут.Рейс КАК Рейс,
	|			втМаршрут.Идентификатор КАК Идентификатор
	|		ИЗ
	|			втМаршрут КАК втМаршрут
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втМаршрут.Рейс,
	|			втМаршрут.Идентификатор) КАК ВложенныйЗапрос
	|		ПО втМаршрут.Период = ВложенныйЗапрос.Период
	|			И втМаршрут.Рейс = ВложенныйЗапрос.Рейс
	|			И втМаршрут.Идентификатор = ВложенныйЗапрос.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Идентификатор,
	|	КОЛИЧЕСТВО(упРаботыПоРейсуСрезПоследних.СтрокаНомер) КАК КоличествоРабот
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсуСрезПоследних.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(втТочки.Идентификатор) КАК КоличествоТочек
	|ИЗ
	|	втТочки КАК втТочки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТочки.Рейс,
	|	втТочки.Идентификатор,
	|	втТочки.Адрес,
	|	втТочки.Гараж,
	|	втТочки.ТипТочки,
	|	втТочки.СтрокаНомер КАК СтрокаНомер,
	|	ЕСТЬNULL(втРаботы.КоличествоРабот, 0) КАК Количестворабот,
	|	втТочки.Пройдена,
	|	втТочки.Отменена,
	|	втТочки.РасстояниеОтПредыдущейТочки,
	|	втТочки.ВремяОтПредыдущейТочки,
	|	втТочки.ПлановоеПрибытие,
	|	втТочки.ПлановоеУбытие,
	|	втТочки.ПрибытиеФакт,
	|	втТочки.УбытиеФакт,
	|	втТочки.НачалоРаботПлан,
	|	втТочки.НачалоРаботФакт,
	|	ЛОЖЬ КАК Изменена,
	|	втТочки.Гараж КАК Гараж1,
	|	втТочки.Предшественники
	|ИЗ
	|	втТочки КАК втТочки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК втРаботы
	|		ПО втТочки.Идентификатор = втРаботы.Идентификатор
	|ГДЕ
	|	НЕ втТочки.Пройдена
	|	И НЕ втТочки.Отменена
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтрокаНомер";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Ссылка", Регистратор);
	
	Результат = Запрос.ВыполнитьПакет();
	сткРезультат.НепройденныеТочки = Результат[4].Выгрузить();
	текВыборка = Результат[3].Выбрать();
	Если текВыборка.Следующий() Тогда
		сткРезультат.КоличествоТочек = текВыборка.КоличествоТочек;
	КонецЕсли;
		
	Возврат сткРезультат;
	
КонецФункции // ПолучитьТаблицуНепройденныйхТочек()

// Получает контактную информацию экипажа
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс 	 - рейс.
// 
// Возвращаемое значение:
//  Строка - контактная информация.
//
Функция ПолучитьКонтактнуюИнформациюЭкипажа(Знач Рейс) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    Рейс = Документы.упРейс.ПустаяСсылка();
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПеревозчикПоРейсуСрезПоследних.Водитель1 КАК Водитель1,
	               |	упПеревозчикПоРейсуСрезПоследних.Водитель2 КАК Водитель2
	               |ИЗ
	               |	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Водители = Запрос.Выполнить().Выгрузить()[0];
	Результат = "";
	Если ЗначениеЗаполнено(Водители.Водитель1) Тогда
		Результат = Результат + Водители.Водитель1 + " " + УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Водители.Водитель1, Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
	КонецЕсли; 
	Если ЗначениеЗаполнено(Водители.Водитель2) Тогда
		Если ЗначениеЗаполнено(Результат) Тогда
			Результат = Результат + ", ";
		КонецЕсли; 
		Результат = Результат + Водители.Водитель2 + " " + УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Водители.Водитель2, Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

// Проверяет наличие незавершенных на мобильном клиенте прохождений точек
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс 	 - рейс.
// 
// Возвращаемое значение:
//   Булево - признак наличия не завершенных на мобильном клиенте документов.
//
Функция ЕстьНаличиеНезавершенныхНаМобильномКлиентеДокументов(Рейс) Экспорт
	
	// Проверим наличие непроведенных прохождений по рейсу (возможно они созданы мобильным клиентом, 
	// и в дальнейшем могут привести к нарушению оперативной последовательности проведения документов).
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПрохождениеТочки.Ссылка
	|ИЗ
	|	Документ.упПрохождениеТочки КАК упПрохождениеТочки
	|ГДЕ
	|	НЕ упПрохождениеТочки.ПометкаУдаления
	|	И НЕ упПрохождениеТочки.Проведен
	|	И НЕ упПрохождениеТочки.СобытиеНачалаРаботНаТочке = ЗНАЧЕНИЕ(Документ.упСобытиеМониторинга.ПустаяСсылка)
	|	И упПрохождениеТочки.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьМаршрут(Рейс)
	
	тзМаршрут = упРейс.СоздатьТаблицуМаршрут();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Рейс,
	|	упМаршрутПоРейсу.Идентификатор,
	|	упМаршрутПоРейсу.Адрес,
	|	упМаршрутПоРейсу.ВременноеОкноНачало,
	|	упМаршрутПоРейсу.ВременноеОкноОкончание,
	|	упМаршрутПоРейсу.ВремяОжиданияВТочке,
	|	упМаршрутПоРейсу.ВремяОтПредыдущейТочки,
	|	упМаршрутПоРейсу.ВремяПростояПоВинеЗаказчикаЧасы,
	|	упМаршрутПоРейсу.ВремяПростояПоВинеПеревозчикаЧасы,
	|	упМаршрутПоРейсу.ВремяРабот,
	|	упМаршрутПоРейсу.Гараж,
	|	упМаршрутПоРейсу.НачалоРаботПлан,
	|	упМаршрутПоРейсу.НачалоРаботФакт,
	|	упМаршрутПоРейсу.Отменена,
	|	упМаршрутПоРейсу.ПлановоеПрибытие,
	|	упМаршрутПоРейсу.ПлановоеУбытие,
	|	упМаршрутПоРейсу.ПорядокУчастка,
	|	упМаршрутПоРейсу.Предшественники,
	|	упМаршрутПоРейсу.ПрибытиеФакт,
	|	упМаршрутПоРейсу.ПроездНеРассчитан,
	|	упМаршрутПоРейсу.Пройдена,
	|	упМаршрутПоРейсу.РасстояниеОтПредыдущейТочки,
	|	упМаршрутПоРейсу.РасстояниеОтПредыдущейТочкиФакт,
	|	упМаршрутПоРейсу.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсу.ТипТочки,
	|	упМаршрутПоРейсу.УбытиеФакт,
	|	упМаршрутПоРейсу.Проблема
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Рейс
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтрокаНомер";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(тзМаршрут.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;	
	
	Возврат тзМаршрут;
	
КонецФункции

	
#КонецОбласти 
