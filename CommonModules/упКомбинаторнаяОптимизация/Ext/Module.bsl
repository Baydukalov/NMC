
#Область ЗадачаКоммивояжера

// Функция решает задачу коммивояжера по заданному алгоритму.
//
// Параметры:
//   МассивТочек                - Массив - Исходный массив адресов.
//   ВариантОптимизацииМаршрута - ПеречислениеСсылка.упВариантыОптимизацииМаршрута - Вариант оптимизации маршрута,
//                                по расстоянию/по времени.
//   АдресСклада                - СправочникСсылка.упАдреса - Адрес исходной точки маршрута.
//   АдресВременногоХранилища   - Строка - Адрес временного хранилища.
//   Алгоритм                   - ПеречислениеСсылка.упМетодыРешенияЗК - Вариант алгоритма решения задачи коммивояжера.
//
// Возвращаемое значение:
//   Структура  - Ключи:
//     * МассивТочек - Массив - Отсортированный по порядку объезда исходный массив адресов (решение задачи коммивояжера).
//     * Расстояние  - Число  - Расстояние пути при обходе адресов в порядке, предложенном в решении задачи.
//     * Время       - Число  - Время обхода адресов в порядке, предложенном в решении задачи.
//
Функция ИзменитьПорядокТочек(ТаблицаТочек, ВариантОптимизацииМаршрута = Неопределено, АдресВременногоХранилища = Неопределено, МетодРешенияЗК = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(МетодРешенияЗК) = Тип("ПеречислениеСсылка.упМетодыРешенияЗК") Тогда
		Возврат упСЛК.СоздатьОбъект().ИзменитьПорядокТочек(ТаблицаТочек, ВариантОптимизацииМаршрута, АдресВременногоХранилища, МетодРешенияЗК, ДополнительныеПараметры);
	ИначеЕсли ТипЗнч(МетодРешенияЗК) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		Возврат упСЛК.СоздатьОбъектАлгоритмаОптимизации(МетодРешенияЗК).ИзменитьПорядокТочек(ТаблицаТочек, ВариантОптимизацииМаршрута, АдресВременногоХранилища, МетодРешенияЗК, ДополнительныеПараметры);
	КонецЕсли; 
	
КонецФункции // ИзменитьПорядокТочек()	

// Функция решает задачу коммивояжера по заданному алгоритму.
//
// Параметры:
//   Рейс                - ДокументСсылка.упРейс - Ссылка на документ рейс.
//   НастройкаОптимизации - Структура - Содержит настройки.
//   АдресВременногоХранилища   - Строка - Адрес временного хранилища.
//
// Возвращаемое значение:
//   Структура  - Ключи:
//     * МассивТочек - Массив - Отсортированный по порядку объезда исходный массив адресов (решение задачи коммивояжера).
//     * Расстояние  - Число  - Расстояние пути при обходе адресов в порядке, предложенном в решении задачи.
//     * Время       - Число  - Время обхода адресов в порядке, предложенном в решении задачи.
//
Функция ИзменитьПорядокТочекПоРейсу(Рейс, НастройкаОптимизации, АдресВременногоХранилища = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПространственныйВес = НастройкаОптимизации.ПространственныйВес;
	ВременнойВес = НастройкаОптимизации.ВременнойВес;
	ВесСрочности = НастройкаОптимизации.ВесСрочности;
	МетодРешенияЗК = НастройкаОптимизации.МетодРешенияЗК;
	ВариантОптимизацииМаршрута = НастройкаОптимизации.ВариантОптимизацииМаршрута;
	РежимКорректировки = НастройкаОптимизации.РежимКорректировки;
	
	ИндексПервойНеПройденнойТочки = 0;
	Маршрут = Неопределено;
	тзВершин = упКомбинаторнаяОптимизация.ПодготовитьТаблицуТочекДляИзмененияПорядка(Маршрут, Рейс, МетодРешенияЗК, ИндексПервойНеПройденнойТочки, РежимКорректировки);
	
	РезультатВычисления = Неопределено;
	Если НЕ тзВершин = Неопределено Тогда
		МассивТочек = тзВершин.ВыгрузитьКолонку("Идентификатор");
		сткДопПараметры = Неопределено;
		Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
			ВариантОптимизацииМаршрута = Неопределено;
			сткДопПараметры = Новый Структура;
			сткВесов = Новый Структура;
			сткВесов.Вставить("ПространственныйВес", ПространственныйВес);
			сткВесов.Вставить("ВременнойВес"       , ВременнойВес);
			сткВесов.Вставить("ВесСрочности"       , ВесСрочности);
			сткДопПараметры.Вставить("СтруктураВесов", сткВесов);
		КонецЕсли;
		РезультатВычисления = упКомбинаторнаяОптимизация.ИзменитьПорядокТочек(тзВершин, ВариантОптимизацииМаршрута, АдресВременногоХранилища, МетодРешенияЗК, сткДопПараметры);
		Если НЕ РезультатВычисления = Неопределено Тогда
			МассивОптимизированныхТочек = РезультатВычисления.МассивТочек;
			СпискиИдентичны = упСервисныеФункцииКлиентСервер.МассивыСИдентичнымПорядком(МассивТочек, МассивОптимизированныхТочек);
			Если СпискиИдентичны Тогда
				// задача не решена, порядок точек не изменен.
				РезультатВычисления = Неопределено;
			Иначе
				РезультатВычисления.Вставить("ИндексПервойНеПройденнойТочки", ИндексПервойНеПройденнойТочки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатВычисления;
	
КонецФункции // ИзменитьПорядокТочекПоРейсу()

// Выполним получение таблицы точек для обрбаотки.
//
// Параметры:
//  ТаблицаМаршрут  - ТаблицаЗначений - Маршрут рейса.
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  МетодРешенияЗК  - ПеречислениеСсылка.упМетодыРешенияЗК - Метод решения задачи комивояжорра.
//  ИндексПервойНеПройденнойТочки  - Число - Индекс строки первой не пройденной точки.
//  РежимКорректировки  - Булево - Режим изменения маршрута.
//  ТолькоАктивные  - Булево - Обработка активных.
//  ЕстьПометка  - Булево - В маршруте есть колонка пометка.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица точек.
//
Функция ПодготовитьТаблицуТочекДляИзмененияПорядка(ТаблицаМаршрут = Неопределено, Рейс, МетодРешенияЗК, ИндексПервойНеПройденнойТочки, РежимКорректировки, ТолькоАктивные=ЛОЖЬ, ЕстьПометка=ЛОЖЬ) Экспорт
	
	Маршрут = Новый ТаблицаЗначений;
	
	Если ТаблицаМаршрут = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упМаршрутПоРейсу.*
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсу
		|ГДЕ
		|	НЕ упМаршрутПоРейсу.Отменена
		|
		|УПОРЯДОЧИТЬ ПО
		|	упМаршрутПоРейсу.СтрокаНомер";
		Запрос.УстановитьПараметр("Рейс", Рейс);
		Результат = Запрос.ВыполнитьПакет();
		Если НЕ Результат[0].Пустой() Тогда
			Маршрут = Результат[0].Выгрузить(); 
		КонецЕсли;
	Иначе
		Маршрут = ТаблицаМаршрут;
	КонецЕсли;
	тзВершин = Неопределено;
	Если Маршрут.Количество() > 2 Тогда
		Если РежимКорректировки Тогда
			тзВершин = Новый ТаблицаЗначений;
			тзВершин.Колонки.Добавить("Идентификатор", 			Новый ОписаниеТипов("УникальныйИдентификатор"));
			тзВершин.Колонки.Добавить("Адрес", 					Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
			тзВершин.Колонки.Добавить("ТипТочки", 				Новый ОписаниеТипов("ПеречислениеСсылка.упТипыТочекМаршрута"));
			тзВершин.Колонки.Добавить("Предшественники",		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
			
			Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
				тзВершин.Колонки.Добавить("ВременноеОкноНачало", 	Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
				тзВершин.Колонки.Добавить("ВременноеОкноОкончание", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
				тзВершин.Колонки.Добавить("ВремяРабот", 			Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный)));
			КонецЕсли;
			
			ПредыдущаяСтрока = Неопределено;
			мсвИдентификаторы = Новый Массив;
			Для каждого СтрокаМаршрута Из Маршрут Цикл
				Пометка = Истина;
				Если ЕстьПометка Тогда
					Пометка = СтрокаМаршрута["Пометка"];
				КонецЕсли;
				Если НЕ СтрокаМаршрута.Пройдена
					И Пометка Тогда
					Если НЕ ПредыдущаяСтрока = Неопределено
						И ПредыдущаяСтрока.Пройдена Тогда
						НоваяСтрокаВершин = тзВершин.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаВершин, ПредыдущаяСтрока);
						НоваяСтрокаВершин.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления;
						ИндексПервойНеПройденнойТочки = Маршрут.Индекс(ПредыдущаяСтрока);
						мсвИдентификаторы.Добавить(Строка(ПредыдущаяСтрока.Идентификатор));
					КонецЕсли;
					НоваяСтрокаВершин = тзВершин.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаВершин, СтрокаМаршрута);
					мсвИдентификаторы.Добавить(Строка(СтрокаМаршрута.Идентификатор));
				КонецЕсли;
				ПредыдущаяСтрока = СтрокаМаршрута;
			КонецЦикла;
			
			// Убираются предшественники, которые не участвуют в оптимизации маршрута
			Для каждого Строка Из тзВершин Цикл
				мсвПредшественники = СтрРазделить(Строка.Предшественники,",");
				мсвПредшественникиНовый = Новый Массив;
				Для каждого Предшественник Из мсвПредшественники Цикл
					Если НЕ мсвИдентификаторы.Найти(Предшественник) = Неопределено Тогда
						мсвПредшественникиНовый.Добавить(Предшественник);
					КонецЕсли;
				КонецЦикла;
				Строка.Предшественники = СтрСоединить(мсвПредшественникиНовый, ",");
			КонецЦикла;
		Иначе
			СписокПолейТаблицы = "Идентификатор, Адрес, ТипТочки, Предшественники";
			Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
				СписокПолейТаблицы = "Идентификатор, Адрес, ВременноеОкноНачало, ВременноеОкноОкончание, ВремяРабот, ТипТочки, Предшественники";
			КонецЕсли;
			
			Если ТипЗнч(Маршрут) = ТИП("ТаблицаЗначений") Тогда
				тзВершин = Маршрут.Скопировать(, СписокПолейТаблицы);
			Иначе
				Отбор = Неопределено;
				Если ТолькоАктивные Тогда
					Отбор = Новый Структура("Пометка", Истина);
				КонецЕсли;
				тзВершин = Маршрут.Выгрузить(Отбор, СписокПолейТаблицы);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат тзВершин;
	
КонецФункции // ПодготовитьТаблицуТочекДляИзмененияПорядка()



#КонецОбласти 

#Область ЗадачаМаршрутизацииТранспорта

// Процедура решает задачу маршрутизации транспорта.
//
// Параметры:
//  МассивЗаданий - Массив - Массив ссылок на документы.
//	ТаблицаЗаданий - ТаблицаЗначений - Таблица заданий.
//	ТаблицаТранспорта - ТаблицаЗначений - ссылка на транспортные средства.
//  МассивРейсов	- Массив - Массив ссылок на рейсы.
//	ПараметрыПланирования - Структура - Параметры.
//	ВидПеревозки - ПеречислениеСсылка - Ссылка на элемент.
//	АдресВременногоХранилища - Строка - Путь временного хранилища.
//
Процедура РешитьЗадачуМаршрутизацииТранспорта(МетодРешенияЗМТ, МассивЗаданий = Неопределено, ТаблицаЗаданий = Неопределено, ТаблицаТранспорта, МассивРейсов, ПараметрыПланирования, ВидПеревозки, АдресВременногоХранилища, КлючВарианта) Экспорт
	
	Если ТипЗнч(МетодРешенияЗМТ) = Тип("ПеречислениеСсылка.упМетодыРешенияЗМТ") Тогда
		упСЛК.СоздатьОбъект().РешитьЗадачуМаршрутизацииТранспорта(МетодРешенияЗМТ, МассивЗаданий, ТаблицаЗаданий, ТаблицаТранспорта, МассивРейсов, ПараметрыПланирования, ВидПеревозки, АдресВременногоХранилища, КлючВарианта);
	ИначеЕсли ТипЗнч(МетодРешенияЗМТ) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		упСЛК.СоздатьОбъектАлгоритмаОптимизации(МетодРешенияЗМТ).РешитьЗадачуМаршрутизацииТранспорта(МетодРешенияЗМТ, МассивЗаданий, ТаблицаЗаданий, ТаблицаТранспорта, МассивРейсов, ПараметрыПланирования, ВидПеревозки, АдресВременногоХранилища, КлючВарианта); 
	КонецЕсли; 
	
КонецПроцедуры // РешитьЗадачуМаршрутизацииЗональноеПланирование()

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

// Задача возвращает текст запроса распределения заданий по периодическим рейсам.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Строка - Текст запроса.
//
Функция ПолучитьТекстЗапросаРаспределенияЗаданийПоПериодическимРейсам() Экспорт
	
	Возврат
	// 0. Получение заданий с точностью до грузовых мест, готовых к планированию в рейсы.
	"ВЫБРАТЬ
	|	ОстаткиКПланированию.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозку,
	|	ОстаткиКПланированию.Задание,
	|	ОстаткиКПланированию.ГрузовоеМесто,
	|	ОстаткиКПланированию.КоличествоКПланированиюОстаток КАК Количество
	|ПОМЕСТИТЬ втОстаткиГрузовыхМест
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(, Задание В (&МассивЗаданий)) КАК ОстаткиКПланированию
	|ГДЕ
	|	ОстаткиКПланированию.КоличествоКПланированиюОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1. Вычисление показателей загрузки по заданию.
	// TODO: учет вложенности грузов
	//|ВЫБРАТЬ
	//|	втОстатки.Задание,
	//|	СУММА(упГрузовыеМеста.Масса * втОстатки.Количество) КАК Масса,
	//|	СУММА(упГрузовыеМеста.Объем * втОстатки.Количество) КАК Объем,
	//|	СУММА(упГрузовыеМеста.КоличествоМест * втОстатки.Количество) КАК КоличествоМест,
	//|	СУММА(упГрузовыеМеста.КоличествоЗагрузочныхМетров * втОстатки.Количество) КАК КоличествоЗагрузочныхМетров
	//|ПОМЕСТИТЬ втОстатки
	//|ИЗ
	//|	втОстаткиГрузовыхМест КАК втОстатки
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	//|		ПО втОстатки.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	втОстатки.Задание
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	втОстатки.Задание,
	//|	упЗаданиеНаПеревозкуГруза.Масса,
	//|	упЗаданиеНаПеревозкуГруза.Объем,
	//|	упЗаданиеНаПеревозкуГруза.КоличествоМест,
	//|	упЗаданиеНаПеревозкуГруза.КоличествоЗагрузочныхМетров
	//|ИЗ
	//|	втОстаткиГрузовыхМест КАК втОстатки
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	//|		ПО (втОстатки.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	//|			И втОстатки.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|ВЫБРАТЬ
	|	втОстатки.Задание КАК Задание,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) * втОстатки.Количество) КАК Масса,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) * втОстатки.Количество) КАК Объем,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) * втОстатки.Количество) КАК КоличествоМест,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) * втОстатки.Количество) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	втОстаткиГрузовыхМест КАК втОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|				,
	|				ЗаявкаНаПеревозку В
	|					(ВЫБРАТЬ
	|						втОстатки.ЗаявкаНаПеревозку
	|					ИЗ
	|						втОстаткиГрузовыхМест КАК втОстатки
	|					ГДЕ
	|						НЕ втОстатки.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО втОстатки.ЗаявкаНаПеревозку = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И втОстатки.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|				,
	|				ЗаданиеНаПеревозку В
	|					(ВЫБРАТЬ
	|						втОстатки.Задание
	|					ИЗ
	|						втОстаткиГрузовыхМест КАК втОстатки)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО втОстатки.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И втОстатки.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|ГДЕ
	|	втОстатки.Задание.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится))
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.Задание
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОстатки.Задание,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) * упГрузовыеМеста.КоличествоГрузов)
	|ИЗ
	|	втОстаткиГрузовыхМест КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упГрузовыеМеста
	|		ПО втОстатки.Задание = упГрузовыеМеста.Ссылка
	|			И (втОстатки.Задание.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|				,
	|				ЗаявкаНаПеревозку В
	|					(ВЫБРАТЬ
	|						втОстатки.ЗаявкаНаПеревозку
	|					ИЗ
	|						втОстаткиГрузовыхМест КАК втОстатки
	|					ГДЕ
	|						НЕ втОстатки.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО (упГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку)
	|			И (упГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|				,
	|				ЗаданиеНаПеревозку В
	|					(ВЫБРАТЬ
	|						втОстатки.Задание
	|					ИЗ
	|						втОстаткиГрузовыхМест КАК втОстатки)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО (упГрузовыеМеста.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку)
	|			И (упГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто)
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.Задание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2. Получение данных по заданию.
	|ВЫБРАТЬ
	|	втОстатки.Задание,
	|	упЗаданиеНаПеревозкуГруза.Организация,
	|	упЗаданиеНаПеревозкуГруза.ВидПеревозки,
	|	упЗаданиеНаПеревозкуГруза.Заказчик,
	|	упЗаданиеНаПеревозкуГруза.Отправитель,
	|	упЗаданиеНаПеревозкуГруза.Получатель,
	|	упЗаданиеНаПеревозкуГруза.АдресОтправления,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения,
	|	упЗаданиеНаПеревозкуГруза.АдресОтправления.ГеографическаяЗона КАК ЗонаОтправления,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения.ГеографическаяЗона КАК ЗонаПолучения,
	|	упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаС,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59)
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаПо
	|	КОНЕЦ КАК ОтправлениеГрузаПо,
	|	упЗаданиеНаПеревозкуГруза.ПолучениеГрузаС,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.ПолучениеГрузаПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59)
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.ПолучениеГрузаПо
	|	КОНЕЦ КАК ПолучениеГрузаПо
	|ПОМЕСТИТЬ втДанныеЗадания
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ПО втОстатки.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3. Получение активных расписаний периодических рейсов.
	|ВЫБРАТЬ
	|	упРасписанияПериодическихРейсов.Ссылка,
	|	упРасписанияПериодическихРейсов.Перевозчик,
	|	упРасписанияПериодическихРейсов.Соглашение,
	|	упРасписанияПериодическихРейсов.ТипТС,
	|	упРасписанияПериодическихРейсов.ТранспортноеСредство,
	|	упРасписанияПериодическихРейсов.Организация,
	|	упРасписанияПериодическихРейсов.ВидПеревозки,
	|	упРасписанияПериодическихРейсов.Заказчик,
	|	упРасписанияПериодическихРейсов.Отправитель,
	|	упРасписанияПериодическихРейсов.Получатель,
	|	упРасписанияПериодическихРейсов.МаксимальноеКоличествоТочекВРейсе,
	|	упЗоныРасписаний.АдресОтправления,
	|	упЗоныРасписаний.АдресПолучения,
	|	упЗоныРасписаний.ЗонаОтправления,
	|	упЗоныРасписаний.ЗонаПолучения,
	|	упРасписанияПериодическихРейсов.ДатаНачалаДействия,
	|	ВЫБОР
	|		КОГДА упРасписанияПериодическихРейсов.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59)
	|		ИНАЧЕ упРасписанияПериодическихРейсов.ДатаОкончанияДействия
	|	КОНЕЦ КАК ДатаОкончанияДействия
	|ПОМЕСТИТЬ втРасписанияРейсов
	|ИЗ
	|	Справочник.упРасписанияПериодическихРейсов КАК упРасписанияПериодическихРейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упРасписанияПериодическихРейсов.Зоны КАК упЗоныРасписаний
	|		ПО упРасписанияПериодическихРейсов.Ссылка = упЗоныРасписаний.Ссылка
	|			И (упРасписанияПериодическихРейсов.Активно)
	|			И (НЕ упРасписанияПериодическихРейсов.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 4. Получение подходящих расписаний для каждого задания.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втДанныеЗадания.Задание КАК Задание,
	|	втДанныеЗадания.АдресОтправления,
	|	втДанныеЗадания.АдресПолучения,
	|   втДанныеЗадания.ОтправлениеГрузаС,
	|   втДанныеЗадания.ОтправлениеГрузаПо,
	|	втРасписанияРейсов.Ссылка КАК ПериодическоеРасписание,
	|	втРасписанияРейсов.Перевозчик,
	|	втРасписанияРейсов.Соглашение,
	|	втРасписанияРейсов.ТипТС,
	|	втРасписанияРейсов.ТранспортноеСредство,
	|	втРасписанияРейсов.МаксимальноеКоличествоТочекВРейсе
	|ПОМЕСТИТЬ втПодходящиеРасписанияБезКонтроля
	|ИЗ
	|	втРасписанияРейсов КАК втРасписанияРейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеЗадания КАК втДанныеЗадания
	|		ПО (&ТекущаяДата < втРасписанияРейсов.ДатаОкончанияДействия)
	|           И &ТекущаяДата < втДанныеЗадания.ОтправлениеГрузаПо
	|			И втРасписанияРейсов.ВидПеревозки = втДанныеЗадания.ВидПеревозки
	|			И (втРасписанияРейсов.Организация В (втДанныеЗадания.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)))
	|			И (втРасписанияРейсов.Заказчик В (втДанныеЗадания.Заказчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)))
	|			И (втРасписанияРейсов.Отправитель В (втДанныеЗадания.Отправитель, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)))
	|			И (втРасписанияРейсов.Получатель В (втДанныеЗадания.Получатель, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)))
	|			И (втРасписанияРейсов.АдресОтправления В (втДанныеЗадания.АдресОтправления, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)))
	|			И (втРасписанияРейсов.АдресПолучения В (втДанныеЗадания.АдресПолучения, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонОтправления
	|		ПО втРасписанияРейсов.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Родитель
	|			И (втДанныеЗадания.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Зона)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонПолучения
	|		ПО втРасписанияРейсов.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Родитель
	|			И (втДанныеЗадания.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Зона)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 5.  (5 - 7: для каждого экипажа вычисляются предельные показатели загрузки).
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПодходящиеРасписания.ТипТС,
	|	втПодходящиеРасписания.ТранспортноеСредство
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втПодходящиеРасписанияБезКонтроля КАК втПодходящиеРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 6. Получение прицепного состава.
	|ВЫБРАТЬ
	|	упПрицепы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	СУММА(упПрицепы.Прицеп.Грузоподъемность) КАК Грузоподъемность,
	|	СУММА(упПрицепы.Прицеп.Объем) КАК Объем,
	|	СУММА(упПрицепы.Прицеп.КоличествоМест) КАК КоличествоМест,
	|	СУММА(упПрицепы.Прицеп.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров 
	|ПОМЕСТИТЬ ХарактеристикиПрицепов
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(&ТекущаяДата, ТранспортноеСредство  В (ВЫБРАТЬ втТС.ТранспортноеСредство ИЗ втТС КАК втТС)) КАК упПрицепы
	|ГДЕ
	|	упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|
	|СГРУППИРОВАТЬ ПО
	|	упПрицепы.ТранспортноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 7.
	|ВЫБРАТЬ
	|	втТС.ТипТС КАК ТипТС,
	|	втТС.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упТранспортныеСредства.ОсновнойВодитель КАК Водитель1,
	|	упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(РСПрицепы.Грузоподъемность,0) КАК Грузоподъемность,
	|	упТранспортныеСредства.Объем +  ЕСТЬNULL(РСПрицепы.Объем,0)  КАК Объем,
	|	упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(РСПрицепы.КоличествоМест,0) КАК КоличествоМест,
	|	упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(РСПрицепы.КоличествоЗагрузочныхМетров,0)  КАК   КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втХарактеристикиТС
	|ИЗ
	|	втТС КАК втТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ПО втТС.ТранспортноеСредство = упТранспортныеСредства.Ссылка
	|			И (втТС.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	| 		ЛЕВОЕ СОЕДИНЕНИЕ ХарактеристикиПрицепов КАК РСПрицепы
	|		ПО РСПрицепы.ТранспортноеСредство = втТС.ТранспортноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТС.ТипТС,
	|	втТС.ТранспортноеСредство,
	|	ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка),
	|	упТипыТС.Грузоподъемность,
	|	упТипыТС.Объем,
	|	упТипыТС.КоличествоМест,
	|	упТипыТС.КоличествоЗагрузочныхМетров
	|ИЗ
	|	втТС КАК втТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК упТипыТС
	|		ПО втТС.ТипТС = упТипыТС.Ссылка
	|			И (втТС.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 8. К полученным ранее подходящим расписаниям применяется контроль загрузки.
	//     (суммарная загрузка по заданию не должна превышать предельные показатели загрузки по экипажу, указанному в периодическом расписании).
	|ВЫБРАТЬ
	|	втПодходящиеРасписания.Задание,
	|	втПодходящиеРасписания.АдресОтправления,
	|	втПодходящиеРасписания.АдресПолучения,
	|   втПодходящиеРасписания.ОтправлениеГрузаС,
	|   втПодходящиеРасписания.ОтправлениеГрузаПо,
	|	втПодходящиеРасписания.ПериодическоеРасписание,
	|	втПодходящиеРасписания.Перевозчик,
	|	втПодходящиеРасписания.Соглашение,
	|	втПодходящиеРасписания.ТипТС,
	|	втПодходящиеРасписания.ТранспортноеСредство,
	|	втПодходящиеРасписания.МаксимальноеКоличествоТочекВРейсе,
	|	втХарактеристикиТС.Водитель1
	|ПОМЕСТИТЬ втПодходящиеРасписания
	|ИЗ
	|	втПодходящиеРасписанияБезКонтроля КАК втПодходящиеРасписания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО втПодходящиеРасписания.Задание = втОстатки.Задание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втХарактеристикиТС КАК втХарактеристикиТС
	|		ПО втПодходящиеРасписания.ТипТС = втХарактеристикиТС.ТипТС
	|			И втПодходящиеРасписания.ТранспортноеСредство = втХарактеристикиТС.ТранспортноеСредство
	|			И (втОстатки.Масса <= втХарактеристикиТС.Грузоподъемность)
	|			И (втОстатки.Объем <= втХарактеристикиТС.Объем)
	|			И (втОстатки.КоличествоМест <= втХарактеристикиТС.КоличествоМест)
	|			И (втОстатки.КоличествоЗагрузочныхМетров <= втХарактеристикиТС.КоличествоЗагрузочныхМетров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 9. Получение расписания возможных периодических рейсов.
	|ВЫБРАТЬ
	|	упГрафикиПериодическихРейсов.Расписание КАК ПериодическоеРасписание,
	|	ДОБАВИТЬКДАТЕ(упГрафикиПериодическихРейсов.ДатаНачалаРейса, СЕКУНДА, ЧАС(упГрафикиПериодическихРейсов.ВремяНачалаРейса) * 3600 + МИНУТА(упГрафикиПериодическихРейсов.ВремяНачалаРейса) * 60 + СЕКУНДА(упГрафикиПериодическихРейсов.ВремяНачалаРейса)) КАК ДатаНачалаРейса,
	|	упГрафикиПериодическихРейсов.КоличествоТС
	|ПОМЕСТИТЬ втКалендарь
	|ИЗ
	|	РегистрСведений.упГрафикиПериодическихРейсов КАК упГрафикиПериодическихРейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			втПодходящиеРасписания.ПериодическоеРасписание КАК ПериодическоеРасписание
	|		ИЗ
	|			втПодходящиеРасписания КАК втПодходящиеРасписания) КАК втПодходящиеРасписания
	|		ПО упГрафикиПериодическихРейсов.Расписание = втПодходящиеРасписания.ПериодическоеРасписание
	|			И (ДОБАВИТЬКДАТЕ(упГрафикиПериодическихРейсов.ДатаНачалаРейса, СЕКУНДА, ЧАС(упГрафикиПериодическихРейсов.ВремяНачалаРейса) * 3600 + МИНУТА(упГрафикиПериодическихРейсов.ВремяНачалаРейса) * 60 + СЕКУНДА(упГрафикиПериодическихРейсов.ВремяНачалаРейса)) >= &ТекущаяДата)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 10. Получение рейсов, созданных по периодическим расписаниям ранее. 
	//       Реквизит "Расписание" в документе "упРейс" проиндексирован.
	|ВЫБРАТЬ
	|	втПодходящиеРасписания.Задание,
	|	упРейс.Ссылка КАК Рейс,
	|	втПодходящиеРасписания.АдресОтправления,
	|	втПодходящиеРасписания.АдресПолучения,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеНачалоВыполнения,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) КАК Масса,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
	|	втПодходящиеРасписания.ПериодическоеРасписание,
	|	втПодходящиеРасписания.Перевозчик,
	|	втПодходящиеРасписания.ТипТС,
	|	втПодходящиеРасписания.ТранспортноеСредство,
	|	втПодходящиеРасписания.МаксимальноеКоличествоТочекВРейсе,
	|	упСтатусыРейсовСрезПоследних.Статус
	|ПОМЕСТИТЬ втПериодическиеРейсы10
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, ) КАК упСтатусыРейсовСрезПоследних
	|		ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	|			И (упСтатусыРейсовСрезПоследних.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен))
	|       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|       ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПодходящиеРасписания КАК втПодходящиеРасписания
	|		ПО упРейс.Расписание = втПодходящиеРасписания.ПериодическоеРасписание
	|			И (ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) >= &ТекущаяДата)
	|			И (ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) < втПодходящиеРасписания.ОтправлениеГрузаПо)
	|			И (ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) >= втПодходящиеРасписания.ОтправлениеГрузаС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 11. Получение рейсов, в которые теоритически можно добавить новые задания.
	|ВЫБРАТЬ
	|	втПериодическиеРейсы.Задание,
	|	втПериодическиеРейсы.Рейс,
	|	втПериодическиеРейсы.АдресОтправления,
	|	втПериодическиеРейсы.АдресПолучения,
	|	втПериодическиеРейсы.Масса,
	|	втПериодическиеРейсы.Объем,
	|	втПериодическиеРейсы.КоличествоМест,
	|	втПериодическиеРейсы.КоличествоЗагрузочныхМетров,
	|	втПериодическиеРейсы.ПериодическоеРасписание,
	|	втПериодическиеРейсы.ПлановоеНачалоВыполнения,
	|	втПериодическиеРейсы.Перевозчик,
	|	втПериодическиеРейсы.ТипТС,
	|	втПериодическиеРейсы.ТранспортноеСредство,
	|	втПериодическиеРейсы.МаксимальноеКоличествоТочекВРейсе
	|ПОМЕСТИТЬ втАктивныеРейсы05
	|ИЗ
	|	втПериодическиеРейсы10 КАК втПериодическиеРейсы
	|ГДЕ
	|	втПериодическиеРейсы.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КПланированиюТС), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.СформированыЗаявкиНаТС), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.НазначеноТС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 12. Контроль превышения загрузки по активным рейсам.
	|ВЫБРАТЬ
	|	втАктивныеРейсы.Задание,
	|	втАктивныеРейсы.Рейс,
	|	втАктивныеРейсы.ТипТС,
	|	втАктивныеРейсы.ТранспортноеСредство,
	|	втАктивныеРейсы.ПериодическоеРасписание,
	|	втАктивныеРейсы.ПлановоеНачалоВыполнения,
	|	втАктивныеРейсы.АдресОтправления,
	|	втАктивныеРейсы.АдресПолучения,
	|	втАктивныеРейсы.МаксимальноеКоличествоТочекВРейсе
	|ПОМЕСТИТЬ втАктивныеРейсы10
	|ИЗ
	|	втАктивныеРейсы05 КАК втАктивныеРейсы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втХарактеристикиТС КАК втХарактеристикиТС
	|		ПО втАктивныеРейсы.ТипТС = втХарактеристикиТС.ТипТС
	|			И втАктивныеРейсы.ТранспортноеСредство = втХарактеристикиТС.ТранспортноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО втАктивныеРейсы.Задание = втОстатки.Задание
	|ГДЕ
	|	втАктивныеРейсы.Масса + втОстатки.Масса <= втХарактеристикиТС.Грузоподъемность
	|	И втАктивныеРейсы.Объем + втОстатки.Объем <= втХарактеристикиТС.Объем
	|	И втАктивныеРейсы.КоличествоМест + втОстатки.КоличествоМест <= втХарактеристикиТС.КоличествоМест
	|	И втАктивныеРейсы.КоличествоЗагрузочныхМетров + втОстатки.КоличествоЗагрузочныхМетров <= втХарактеристикиТС.КоличествоЗагрузочныхМетров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 13. Маршруты по активным рейсам.
	// 	   
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Рейс,
	|	упМаршрутПоРейсуСрезПоследних.Адрес,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер
	|ПОМЕСТИТЬ втМаршрутыПоРейсам
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					втАктивныеРейсы10.Рейс
	|				ИЗ
	|					втАктивныеРейсы10)) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ 
	|	упМаршрутПоРейсуСрезПоследних.Гараж = ЗНАЧЕНИЕ(Справочник.упГаражи.ПустаяСсылка)
	|	И упМаршрутПоРейсуСрезПоследних.ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 14. Подготовительная таблица для подсчета количества точек по активным рейсам.
	// 	   (Маршрут строится только по точкам с типом "По заданию". Для задания проверяется входят ли точки отправления/получения в маршрут.)
	|ВЫБРАТЬ
	|	втМаршрутыПоРейсам.Рейс,
	|	втАктивныеРейсы.Задание,
	|	СУММА(1) КАК КоличествоТочекВМаршруте,
	|	МАКСИМУМ(ВЫБОР КОГДА втМаршрутыПоРейсам.Адрес = втАктивныеРейсы.АдресОтправления ТОГДА втМаршрутыПоРейсам.СтрокаНомер ИНАЧЕ 0 КОНЕЦ) КАК НомерТочкиОтправленияДляЗадания,
	|	МАКСИМУМ(ВЫБОР КОГДА втМаршрутыПоРейсам.Адрес = втАктивныеРейсы.АдресПолучения ТОГДА втМаршрутыПоРейсам.СтрокаНомер ИНАЧЕ 0 КОНЕЦ) КАК НомерТочкиПолученияДляЗадания	
	|ПОМЕСТИТЬ втМаршрутыПоРейсамПоЗаданиям
	|ИЗ
	|	втМаршрутыПоРейсам КАК втМаршрутыПоРейсам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАктивныеРейсы10 КАК втАктивныеРейсы
	|	ПО втАктивныеРейсы.Рейс = втМаршрутыПоРейсам.Рейс 
	|СГРУППИРОВАТЬ ПО
	|	втМаршрутыПоРейсам.Рейс,
	|	втАктивныеРейсы.Задание
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 14. Количество точек по маршрутам.
	//	   (Если точки по заданию в маршрут не входят, то количество точек увеличивается на 2.
	//	   Если входит хотя бы одна, то количество точек увеличивается на 1.
	//	   Если входят обе, то проверяется их порядок, если порядок нарушен то количество точек увеличивается на 1.)
	|ВЫБРАТЬ
	|	втМаршрут.Рейс КАК Рейс,
	|	втМаршрут.Задание КАК Задание,
	|	втМаршрут.КоличествоТочекВМаршруте + ВЫБОР
	|		КОГДА втМаршрут.НомерТочкиОтправленияДляЗадания = 0
	|				И втМаршрут.НомерТочкиПолученияДляЗадания = 0
	|			ТОГДА 2
	|		КОГДА втМаршрут.НомерТочкиОтправленияДляЗадания = 0
	|				И НЕ втМаршрут.НомерТочкиПолученияДляЗадания = 0
	|			ТОГДА 1
	|		КОГДА НЕ втМаршрут.НомерТочкиОтправленияДляЗадания = 0
	|				И втМаршрут.НомерТочкиПолученияДляЗадания = 0
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА втМаршрут.НомерТочкиПолученияДляЗадания < втМаршрут.НомерТочкиОтправленияДляЗадания
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоТочекМаршрута
	|ПОМЕСТИТЬ втРейсыКоличествоТочек
	|ИЗ
	|	втМаршрутыПоРейсамПоЗаданиям КАК втМаршрут
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 15. Начало активных рейсов должно соответствовать возможному расписанию.
	|ВЫБРАТЬ
	|	втАктивныеРейсы.Задание,
	|	втАктивныеРейсы.Рейс,
	|	втАктивныеРейсы.ПериодическоеРасписание,
	|	втАктивныеРейсы.ТипТС,
	|	втАктивныеРейсы.ТранспортноеСредство,
	|	втАктивныеРейсы.АдресОтправления,
	|	втАктивныеРейсы.АдресПолучения,
	|	втКалендарь.ДатаНачалаРейса,
	|	втАктивныеРейсы.МаксимальноеКоличествоТочекВРейсе
	|ПОМЕСТИТЬ втАктивныеРейсы15
	|ИЗ
	|	втАктивныеРейсы10 КАК втАктивныеРейсы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКалендарь КАК втКалендарь
	|		ПО втАктивныеРейсы.ПериодическоеРасписание = втКалендарь.ПериодическоеРасписание
	|			И (НАЧАЛОПЕРИОДА(втАктивныеРейсы.ПлановоеНачалоВыполнения, ДЕНЬ) = НАЧАЛОПЕРИОДА(втКалендарь.ДатаНачалаРейса, ДЕНЬ))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРейсыКоличествоТочек КАК втРейсыКоличествоТочек
	|		ПО втАктивныеРейсы.Рейс = втРейсыКоличествоТочек.Рейс
	|			И втАктивныеРейсы.Задание = втРейсыКоличествоТочек.Задание
	|ГДЕ 
	|	втАктивныеРейсы.МаксимальноеКоличествоТочекВРейсе = 0 
	|		ИЛИ втАктивныеРейсы.МаксимальноеКоличествоТочекВРейсе >= ЕстьNULL(втРейсыКоличествоТочек.КоличествоТочекМаршрута,0)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 16. (16 - 17: возможные даты новых рейсов проверяются на наличие свободных ТС).
	|ВЫБРАТЬ
	|	втКалендарь.ПериодическоеРасписание,
	|	втКалендарь.ДатаНачалаРейса,
	|	втКалендарь.КоличествоТС,
	|	втПериодическиеРейсы.Рейс
	|ПОМЕСТИТЬ втГрафикНовыйРейсов05
	|ИЗ
	|	втКалендарь КАК втКалендарь
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			втПериодическиеРейсы10.ПериодическоеРасписание КАК ПериодическоеРасписание,
	|			втПериодическиеРейсы10.Рейс КАК Рейс,
	|			втПериодическиеРейсы10.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения
	|		ИЗ
	|			втПериодическиеРейсы10 КАК втПериодическиеРейсы10) КАК втПериодическиеРейсы
	|		ПО втКалендарь.ПериодическоеРасписание = втПериодическиеРейсы.ПериодическоеРасписание
	|			И (НАЧАЛОПЕРИОДА(втПериодическиеРейсы.ПлановоеНачалоВыполнения, ДЕНЬ) = НАЧАЛОПЕРИОДА(втКалендарь.ДатаНачалаРейса, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 17.
	|ВЫБРАТЬ
	|	втПодходящиеРасписания.Задание,
	|	втГрафикНовыйРейсов.ПериодическоеРасписание,
	|	втПодходящиеРасписания.Перевозчик,
	|	втПодходящиеРасписания.Соглашение,
	|	втПодходящиеРасписания.ТипТС,
	|	втПодходящиеРасписания.ТранспортноеСредство,
	|	втПодходящиеРасписания.Водитель1,
	|	втПодходящиеРасписания.АдресОтправления,
	|	втПодходящиеРасписания.АдресПолучения,
	|	втПодходящиеРасписания.МаксимальноеКоличествоТочекВРейсе,
	|	втГрафикНовыйРейсов.ДатаНачалаРейса КАК ПлановоеНачалоРейса,
	|	втГрафикНовыйРейсов.КоличествоТС - КОЛИЧЕСТВО(втГрафикНовыйРейсов.Рейс) КАК ОстатокТС
	|ПОМЕСТИТЬ втГрафикНовыйРейсов10
	|ИЗ
	|	втГрафикНовыйРейсов05 КАК втГрафикНовыйРейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПодходящиеРасписания КАК втПодходящиеРасписания
	|		ПО втГрафикНовыйРейсов.ПериодическоеРасписание = втПодходящиеРасписания.ПериодическоеРасписание
	|            И втПодходящиеРасписания.ОтправлениеГрузаПо > втГрафикНовыйРейсов.ДатаНачалаРейса
	|            И втПодходящиеРасписания.ОтправлениеГрузаС <= втГрафикНовыйРейсов.ДатаНачалаРейса
	|	
	|СГРУППИРОВАТЬ ПО
	|	втПодходящиеРасписания.Задание,
	|	втГрафикНовыйРейсов.ПериодическоеРасписание,
	|	втПодходящиеРасписания.Перевозчик,
	|	втПодходящиеРасписания.Соглашение,
	|	втПодходящиеРасписания.ТипТС,
	|	втПодходящиеРасписания.ТранспортноеСредство,
	|	втПодходящиеРасписания.Водитель1,
	|	втГрафикНовыйРейсов.ДатаНачалаРейса,
	|	втГрафикНовыйРейсов.КоличествоТС,
	|	втПодходящиеРасписания.АдресОтправления,
	|	втПодходящиеРасписания.АдресПолучения,
	|	втПодходящиеРасписания.МаксимальноеКоличествоТочекВРейсе
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(втГрафикНовыйРейсов.Рейс) < втГрафикНовыйРейсов.КоличествоТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 18. Получение итоговой выборки: добавление задания в существующий рейс приоритетнее создания нового рейса.
	|ВЫБРАТЬ
	|	втАктивныеРейсы.Задание,
	|	втАктивныеРейсы.Рейс,
	|	втАктивныеРейсы.ПериодическоеРасписание КАК Расписание,
	|	NULL КАК Перевозчик,
	|	NULL КАК Соглашение,
	|	втАктивныеРейсы.ТипТС,
	|	втАктивныеРейсы.ТранспортноеСредство,
	|	NULL КАК Водитель1,
	|	втАктивныеРейсы.ДатаНачалаРейса КАК ПлановоеНачалоРейса,
	|	0 КАК Порядок
	|ИЗ
	|	втАктивныеРейсы15 КАК втАктивныеРейсы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрафикНовыйРейсов.Задание,
	|	NULL,
	|	втГрафикНовыйРейсов.ПериодическоеРасписание,
	|	втГрафикНовыйРейсов.Перевозчик,
	|	втГрафикНовыйРейсов.Соглашение,
	|	втГрафикНовыйРейсов.ТипТС,
	|	втГрафикНовыйРейсов.ТранспортноеСредство,
	|	втГрафикНовыйРейсов.Водитель1,
	|	втГрафикНовыйРейсов.ПлановоеНачалоРейса,
	|	1
	|ИЗ
	|	втГрафикНовыйРейсов10 КАК втГрафикНовыйРейсов
	|
	|УПОРЯДОЧИТЬ ПО
	|	втАктивныеРейсы.Задание,
	|	Порядок,
	|	ПлановоеНачалоРейса";
	
КонецФункции

#КонецОбласти 
