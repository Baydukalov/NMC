
////////////////////////////////////////////////////////////////////////////////
// Серверные события формы отчета. Управление перевозками (вызов сервера, переопределяемый).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается для отчета "упАнализМарижинальностиПоЗаявкам".
//
// Параметры:
//  Расшифровка - ИдентификаторРасшифровкиКомпоновкиДанных - Данные расшифровки.
//  АдресРасшифровки - Строка - Адрес данных расшифровки компановки данных во временном хранилище.
// 
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция СформироватьДанныеРасшифровкиОтчетаАнализМаржинальностиПоЗаявкам(Расшифровка, АдресРасшифровки) Экспорт
	
	// Выбранные поля - не ресурсы.
	мсвПоляДетальнойЗаписи = Новый Массив;
	мсвПоляДетальнойЗаписи.Добавить("ЗаявкаНаПеревозкуГруза");
	мсвПоляДетальнойЗаписи.Добавить("Валюта");
	мсвПоляДетальнойЗаписи.Добавить("СтатьяДоходовРасходов");
	
	// Данные расшифровки.
	ДанныеРасшифровки 	= ПолучитьИзВременногоХранилища(АдресРасшифровки);			
	ОтборРасшифровки 	= ДанныеРасшифровки.Настройки.Отбор;
	
	// Настройки отбора основной схеме отчета "Движения".
	СхемаКДРасшифровки 		= Отчеты.упАнализМаржинальностиПоЗаявкамДвижения.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ИсточникДоступныхНастроекКомпоновкиДанных = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКДРасшифровки);
	
	КомпоновщикНастроекСКД	= Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекСКД.Инициализировать(ИсточникДоступныхНастроекКомпоновкиДанных);
	
	ФиксированныеНастройки 	= КомпоновщикНастроекСКД.ФиксированныеНастройки;
	ОтборФиксНастроек 		= ФиксированныеНастройки.Отбор;
	
	
	мсвПолейОтбора	= Новый Массив; // Массив, для проверки дублирующих полей отбора.
	
	// Настройки отчета по движениям заполняются настройками расшифровки отчета "Анализ маржинальности по заявкам".
	Для каждого ЭлементОтбора Из ОтборРасшифровки.Элементы Цикл
		мсвПолейОтбора.Добавить(Строка(ЭлементОтбора.ЛевоеЗначение));
		НовыйЭлементОтбора = ОтборФиксНастроек.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовыйЭлементОтбора.ЛевоеЗначение 	= ЭлементОтбора.ЛевоеЗначение;
		НовыйЭлементОтбора.ВидСравнения 	= ЭлементОтбора.ВидСравнения;
		НовыйЭлементОтбора.ПравоеЗначение 	= ЭлементОтбора.ПравоеЗначение;
	КонецЦикла;
	
	ВыбранноеПоле = ДанныеРасшифровки.Элементы[Расшифровка]; // Выбранное поле для расшифровки.
	
	// Собираются данные полей по детальной записи и верхних группировок.
	мсвПараметровОтбора = ДанныеРасшифровкиОтчетаАнализМаржинальностиПоЗаявкам(мсвПоляДетальнойЗаписи, ВыбранноеПоле, ДанныеРасшифровки.Элементы);
	
	// Собранные данные переносятся в настройки отбора отчета "Движения".
	Для каждого ЭлементОтбора Из мсвПараметровОтбора Цикл
		Если мсвПолейОтбора.Найти(ЭлементОтбора.ИмяПоля) = Неопределено Тогда
			мсвПолейОтбора.Добавить(ЭлементОтбора.ИмяПоля);
			НовыйЭлементОтбора = ОтборФиксНастроек.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных(ЭлементОтбора.ИмяПоля);
			НовыйЭлементОтбора.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
			НовыйЭлементОтбора.ПравоеЗначение 	= ЭлементОтбора.Значение;
		КонецЕсли; 		
	КонецЦикла;
	
	СтруктураРасшифровки = Новый Структура;	// Данные для параметров открытия отчета.
	СтруктураРасшифровки.Вставить("Настройки",ФиксированныеНастройки);
	СтруктураРасшифровки.Вставить("Период", '00010101');
	
	// Анализируются параметры данных отчета "Анализ маржинальности по заявкам".
	ПараметрыДанных = ДанныеРасшифровки.Настройки.ПараметрыДанных.Элементы;
	ПараметрПериод	= ПараметрыДанных.Найти("Период");
	Если НЕ ПараметрПериод = Неопределено Тогда
		СтруктураРасшифровки.Вставить("Период", ПараметрПериод.Значение);
	КонецЕсли;
	
	Возврат СтруктураРасшифровки;
	
КонецФункции

// Функция - формирует массив данными родительских группировок.
//			   Для макетов - таблиц процедура не применима, так как в ней 
//			   используется ограничение - у поля только один родитель.
//
// Параметры:
//  мсвПоляДетальнойЗаписи - Массив - Поля детальных записей.
//  Поле - ЭлементРасшифровкиКомпоновкиДанныхПоля - выбранный элемент расшифровки.
//  ЭлементыРКД - Элементы - Список элементов.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ДанныеРасшифровкиОтчетаАнализМаржинальностиПоЗаявкам(мсвПоляДетальнойЗаписи, Поле, ЭлементыРКД)
	
	мсвПараметровОтбора = Новый Массив;
	
	мсвРодителиПоля = Поле.ПолучитьРодителей();
	
	Если мсвРодителиПоля.Количество() > 0 Тогда
		
		ЭлементРодитель = мсвРодителиПоля[0];
		
		// Если выбран ресурс детальной записи.
		Если ТипЗнч(ЭлементРодитель) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
			НайтиЗначенияПолейДетальнойЗаписи(мсвПараметровОтбора, мсвПоляДетальнойЗаписи, Поле, ЭлементРодитель, ЭлементыРКД);
		КонецЕсли;				
		
		// Выбираются все значения расшифровок родительских группировок.
		ПолучитьЗначенияПолей(мсвПараметровОтбора, ЭлементыРКД, Поле.Идентификатор);
	КонецЕсли;
	
	Возврат мсвПараметровОтбора;
	
КонецФункции

// Поиск значений полей детальных записей.
//
// Параметры:
//  мсвПараметровОтбора - Массив - Параметры.
//  мсвПоляДетальнойЗаписи - Массив - Поля детальных записей.
//  Поле - ЭлементРасшифровкиКомпоновкиДанныхПоля - выбранный элемент расшифровки.
//  Родитель - ЭлементРасшифровкиКомпоновкиДанныхПоля - Родитель элемент расшифровки.
//  ЭлементыРКД - Элементы - Список элементов.
//
Процедура НайтиЗначенияПолейДетальнойЗаписи(мсвПараметровОтбора, мсвПоляДетальнойЗаписи, Поле, Родитель, ЭлементыРКД)
	
	ЗначениеПолейВыбранногоПоля = Поле.ПолучитьПоля();
	ИмяПоля = "";
	Если ЗначениеПолейВыбранногоПоля.Количество()>0 Тогда
		ИмяПоля = ЗначениеПолейВыбранногоПоля[0].Поле;
	КонецЕсли;
	
	ИдентификаторПоля = Поле.Идентификатор - 1;
	
	ПрекратитьПоиск = Ложь;
	
	// Попытка найти поле заявки на перевозку груза с меньшим идентификатором.
	Пока ИдентификаторПоля > 0 Цикл
		
		СоседнееПоле	= ЭлементыРКД.Получить(ИдентификаторПоля);
		
		Если ТипЗнч(СоседнееПоле) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
			ПрекратитьПоиск = Истина;
			Прервать;
		Иначе
			ЗначенияПолейСоседнегоПоля = СоседнееПоле.ПолучитьПоля();
			Для каждого ЗначениеСоседнегоПоля Из ЗначенияПолейСоседнегоПоля Цикл
				Если НЕ мсвПоляДетальнойЗаписи.Найти(ЗначениеСоседнегоПоля.Поле) = Неопределено Тогда
					мсвПараметровОтбора.Добавить(Новый Структура("ИмяПоля, Значение", ЗначениеСоседнегоПоля.Поле, ЗначениеСоседнегоПоля.Значение));
				ИначеЕсли ЗначениеСоседнегоПоля.Поле = ИмяПоля Тогда
					ПрекратитьПоиск = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ПрекратитьПоиск Тогда
			Прервать;
		КонецЕсли;
		
		ИдентификаторПоля = ИдентификаторПоля - 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Получает значения всех вышестоящих группировок.
//
// Параметры:
//	мсвПараметровОтбора - Массив - СписокПолей, Масив структур - Ключ имя поля,
//                                  значение - значение группировочного поля отчета.
//									- параметр в который будет помещен список найденных значений.
//	КоллекцияЭлементов - ЭлементыРасшифровкиКомпоновкиДанных - коллекция элементов,
//         расшифровки отчета (ДанныеРасшифровки.Элементы).
//	Идентификатор - ИдентификаторРасшифровкиКомпоновкиДанных - идентификатор расшифровываемого поля.
//
Процедура ПолучитьЗначенияПолей(мсвПараметровОтбора, КоллекцияЭлементов, Идентификатор) Экспорт
	
	Для Каждого Родитель Из  КоллекцияЭлементов[Идентификатор].ПолучитьРодителей() Цикл
		ПолучитьЗначенияПолей(мсвПараметровОтбора, КоллекцияЭлементов, Родитель.Идентификатор);
		ПолучитьЗначениеПоля(мсвПараметровОтбора, Родитель);
	КонецЦикла;
	
КонецПроцедуры

// Получает значения полей текущего элемента расшифровки.
//
// Параметры:
//		мсвПараметровОтбора - Массив - СписокПолей, Масив структур - Ключ имя поля,
//                                       значение - значение группировочного поля отчета.
//											- параметр в который будет помещен список найденных значений.
//		ВыбранноеПоле - ЭлементРасшифровкиКомпоновкиДанныхПоля - элемент расшифровки поля которого необходимо получить.
//
Процедура ПолучитьЗначениеПоля(мсвПараметровОтбора, ВыбранноеПоле) Экспорт
	
	Если ТипЗнч(ВыбранноеПоле) <> Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") тогда
		Для Каждого Поле из ВыбранноеПоле.ПолучитьПоля() Цикл
			Если ТипЗнч(Поле) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
				мсвПараметровОтбора.Добавить(Новый Структура("ИмяПоля, Значение", Поле.Поле, Поле.Значение));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти
