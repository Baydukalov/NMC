#Область ПрограммныйИнтерфейс

// Процедура - Установить доступность кнопок перемещения точек маршрута
//
// Параметры:
//  Элемент			 - Элемент	 - элемент управления.
//  Маршрут			 - ТабличнаяЧасть	 - табличная часть.
//  ЭлементКнопкаВверх	 - Элемент	 - кнопка вверх.
//  ЭлементКнопкаВниз	 - Элемент	 - кнопка вниз.
//
Процедура УстановитьДоступностьКнопокПеремещенияТочекМаршрута(Элемент, Маршрут, ЭлементКнопкаВверх, ЭлементКнопкаВниз) Экспорт
	
	СтрокаТочка = Элемент.ТекущиеДанные;
	
	Если Не СтрокаТочка = Неопределено Тогда
		РазрешеноПеремещатьВверх = Истина;
		РазрешеноПеремещатьВниз 	= Истина;
		КоличествоСтрок          = Маршрут.Количество(); 
		ЕстьТочкаЗавершения      = Маршрут[КоличествоСтрок - 1].ТипТочки	= ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения");
		ЕстьТочкаОтправления     = Маршрут[0].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления");
		
		Если Ложь
			Или СтрокаТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")
			Или СтрокаТочка.НомерСтроки = 1     // Первая точка.
			Или (Истина
				И ЕстьТочкаОтправления 
				И СтрокаТочка.НомерСтроки = 2) // Вторая точка и есть точка отправления(выезда из гаража).
		Тогда
			РазрешеноПеремещатьВверх 	= Ложь;
		КонецЕсли;
		
		Если Ложь
			Или СтрокаТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения")
			Или СтрокаТочка.НомерСтроки = Маршрут.Количество()    // Последняя точка.
			Или (Истина
				И ЕстьТочкаЗавершения 
				И СтрокаТочка.НомерСтроки = КоличествоСтрок - 1) // Предпоследняя точка и есть точка завершения(возвращение в гараж).
		Тогда
			РазрешеноПеремещатьВниз 	= Ложь;
		КонецЕсли;
		
		ЭлементКнопкаВверх.Доступность = РазрешеноПеремещатьВверх;
		ЭлементКнопкаВниз.Доступность  = РазрешеноПеремещатьВниз;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти