
#Если Не ВебКлиент Тогда

#Область ГеоКодированиеГеографическойЗоны

// Выполним обращение к сервису геокодирования.
//
// Параметры:
//  СтрокаПоиска  - Строка - Текст геокодирования.
//
// Возвращаемое значение:
//   Массив  - массив структур.
//
Функция ПолучитьПолигоныГеографическойЗоны(СтрокаПоиска) Экспорт	
	
	#Если НЕ ВебКлиент Тогда
		
		Структура = упГеокодированиеСервер.ПолучитьПараметрыГеоКодирования();
		
		Если НЕ Структура.Использовать Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования не используется.'"));			
			Возврат Неопределено; 
		КонецЕсли;
		
		АдресСервера = Структура.АдресСервера; // - http://nominatim.openstreetmap.org/search?q=.
		ТипГеокодера = Структура.Тип;
		ОграничениеОтвета = Формат(Структура.КоличествоОтветов,"ЧН=5; ЧГ=0");
		
		ПредставлениеСтрокиПоиска = НРег(СтрокаПоиска); // - приводим наименование геозоны в нижний регистр.
		
		ТексЗапросаКСервису = "";
		// Собираем строку запроса к сервису геокодирования.
		Если Структура.Тип = Структура.AxelotMaps Или Структура.Тип = Структура.Nominatim Тогда			
			ТексЗапросаКСервису = АдресСервера  + "search?q=" + ПредставлениеСтрокиПоиска + "&format=xml&accept-language=ru-RU&polygon_geojson=1&limit=" + ОграничениеОтвета;
		КонецЕсли;	
		
		Если ПустаяСтрока(ТексЗапросаКСервису) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Текст запроса к сервису геокодирования не составлен.'"));
			Возврат Неопределено; 
		КонецЕсли;
		
		#Если Сервер Тогда
			НастройкаСохранения = Новый Соответствие;
			НастройкаСохранения.Вставить("МестоХранения", "Сервер");
			НастройкаСохранения.Вставить("Путь",          Неопределено);
			
			НастройкиПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.СтруктураПараметровПолученияФайла();
			ФайлКоординат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла(ТексЗапросаКСервису,НастройкиПолучения,НастройкаСохранения);
		#Иначе
			ПараметрыПолучения = Новый Структура();
			ПараметрыПолучения.Вставить("ПутьДляСохранения",ПолучитьИмяВременногоФайла("xml"));
			
			ФайлКоординат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ТексЗапросаКСервису,ПараметрыПолучения,Ложь);
		#КонецЕсли
		
		Если ФайлКоординат.Статус Тогда
			ЧтениеXML = Новый ЧтениеXML;
			ЧтениеXML.ОткрытьФайл(ФайлКоординат.Путь);
			
			Попытка
				ОбъектыXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервер вернул неожиданный ответ.'"));
				Возврат Неопределено;
			КонецПопытки;	
			
			Попытка
				НайденныеОбъекты = ОбъектыXDTO.place; // сервиса nominatim
			Исключение
				НайденныеОбъекты = Неопределено;
			КонецПопытки;
			
			Если НЕ НайденныеОбъекты = Неопределено Тогда
				Если НЕ ТипЗнч(НайденныеОбъекты) = ТИП("СписокXDTO") Тогда
					НайденныеОбъекты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НайденныеОбъекты);
				КонецЕсли;	
				Массив = Новый Массив;
				Для каждого ТекущаяЗапись Из НайденныеОбъекты Цикл
					
					ТекстЧтенияJSON = ТекущаяЗапись.geojson;
					// Выполним чтение JSON формата.
					ЧтениеJSON = Новый ЧтениеJSON();
					ЧтениеJSON.УстановитьСтроку(ТекстЧтенияJSON);
					
					ОтветJSON	  = ПрочитатьJSON(ЧтениеJSON);
					сткКоординаты = ПолучитьКоординатыПолигоны(ОтветJSON);
					// Закроем данные JSON формата.
					ЧтениеJSON.Закрыть();
					
					Если НЕ сткКоординаты = Неопределено Тогда
						Структура = Новый Структура;
						Структура.Вставить("display_name",ТекущаяЗапись.display_name);						
						Структура.Вставить("Координаты",сткКоординаты.Координаты);
						Структура.Вставить("ИндексПолигона",сткКоординаты.ИндексПолигона);
						Структура.Вставить("ТипПолигона",сткКоординаты.ТипПолигона);
						Структура.Вставить("Широта",ТекущаяЗапись.lat);
						Структура.Вставить("Долгота",ТекущаяЗапись.lon);
						Структура.Вставить("osm_id",ТекущаяЗапись.osm_id);
						Структура.Вставить("place_id",ТекущаяЗапись.place_id);
						Структура.Вставить("place_rank",ТекущаяЗапись.place_rank);
						Структура.Вставить("type",ТекущаяЗапись.type);
						Массив.Добавить(Структура);
					КонецЕсли;
				КонецЦикла;
				НайденныеОбъекты = Массив;
			КонецЕсли;
			
		Иначе			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования недоступен.'"));			
			НайденныеОбъекты = Неопределено;
		КонецЕсли; 
		
		Возврат НайденныеОбъекты; 
		
	#Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования в веб-клиенте недоступен.'"));
		Возврат Неопределено; 
	#КонецЕсли
	
КонецФункции // ПолучитьПолигонГеографическойЗоны()

// Выполним поиск координат полигоны, если мульти полигон, тогда выбираем полигон.
//  с наибольшим количеством точек, обычно это первый полигон в коллекции.
//
// Параметры:
//  ОтветJSON  - Результат чтения из объекта JSON.
//
// Возвращаемое значение:
//   Массив   - Структур ключи долгота и широта, координаты полигона.
//
Функция ВыполнитьОбходОтветаJSON(ОтветJSON) Экспорт
	
	Результат = Неопределено;
	
	ТипОтвета = ОтветJSON.ТипПолигона;
	Если ТипОтвета = "MultiPolygon" Тогда
		мсвПолигоны = Новый Массив;		
		Если ТипЗнч(ОтветJSON.Координаты)=ТИП("Массив") Тогда			
			Для каждого ТекущийПолигон Из ОтветJSON.Координаты Цикл
				Если ТекущийПолигон.Количество() Тогда
					Массив = Новый Массив;
					ВыполнитьРекурсивныйПоискКоординат(ТекущийПолигон[0],Массив);
					Если Массив.Количество() Тогда
						мсвПолигоны.Добавить(Массив);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если мсвПолигоны.Количество() Тогда
				Результат = мсвПолигоны[0];
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипОтвета = "Polygon" Тогда
		Если ТипЗнч(ОтветJSON.Координаты)=ТИП("Массив") Тогда
			Массив = Новый Массив;
			ОбластьПолигона = ОтветJSON.Координаты[0];
			ВыполнитьРекурсивныйПоискКоординат(ОбластьПолигона,Массив);
			Если Массив.Количество() Тогда
				Результат = Массив;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВыполнитьОбходОтветаJSON()

// Выполним поиск координат полигоны, если мульти полигон, тогда выбираем полигон .
//  с наибольшим количеством точек, обычно это первый полигон в коллекции.
//
// Параметры:
//  ОтветJSON  - Результат чтения из объекта JSON.
//
// Возвращаемое значение:
//   Массив   - Структур ключи долгота и широта, координаты полигона.
//
Функция ПолучитьКоординатыПолигоны(ОтветJSON)
	
	Результат = Неопределено;
	
	ТипОтвета = ОтветJSON.type;
	Если ТипОтвета = "MultiPolygon" Тогда
		Если ТипЗнч(ОтветJSON.coordinates)=ТИП("Массив") Тогда
			Результат = Новый Структура();
			Результат.Вставить("Координаты",ОтветJSON.coordinates);
			Результат.Вставить("ИндексПолигона",0);
			Результат.Вставить("ТипПолигона",ТипОтвета);
		КонецЕсли;
	ИначеЕсли ТипОтвета = "Polygon" Тогда
		Если ТипЗнч(ОтветJSON.coordinates)=ТИП("Массив") Тогда
			Результат = Новый Структура();
			Результат.Вставить("Координаты",ОтветJSON.coordinates);
			Результат.Вставить("ИндексПолигона",0);
			Результат.Вставить("ТипПолигона",ТипОтвета);
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьКоординатыПолигоны()

// Выполним рекурсивный обход коллекции найдем все координаты.
//
// Параметры:
//  ОбъектПоиска  - Массив - Коллекция в которой производится поиск.
//  Координаты  - Массив - Помещаем в коллекцию результат.
//
// Возвращаемое значение:
//   Нет.
//
Функция ВыполнитьРекурсивныйПоискКоординат(ОбъектПоиска,Координаты)
	
	i_шаг = 0;
	
	Для каждого ТекущийОбъект Из ОбъектПоиска Цикл
		
		Если ТипЗнч(ТекущийОбъект)=ТИП("Массив") Тогда
			ВыполнитьРекурсивныйПоискКоординат(ТекущийОбъект,Координаты);
		Иначе
			Если i_шаг=0 Тогда
				Структура = Новый Структура("Долгота",ТекущийОбъект);
			ИначеЕсли i_шаг=1 Тогда
				Структура.Вставить("Широта",ТекущийОбъект);
				Координаты.Добавить(Структура);
			КонецЕсли;			
			i_шаг=i_шаг+1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции // ВыполнитьРекурсивныйПоискКоординат()

#КонецОбласти

#Область ГеоКодированиеАдреса

#Область ГеоКодированиеNominatim

// Функция возвращает структуру координат объекта поиска 
//
Функция ПолучитьСтруктуруОбъектаПоискаNominatim(ОбъектXDTO)
	
	стрОбъекта = Новый Структура("ПолноеПредставление", ОбъектXDTO.display_name);	
	
	стрОбъекта.Вставить("Долгота",ОбъектXDTO.lon);
	стрОбъекта.Вставить("Широта",ОбъектXDTO.lat);
	
	Возврат стрОбъекта; 
	
КонецФункции // ПолучитьСтруктуруОбъектаПоискаNominatim()

// Функция возвращает ОбъектXDTO содержащий свойство display_name
//
Функция ВернутьОбъектСвойствоDisplayName(СписокXDTO)
	
	НайденныйОбъект = Неопределено;
	
	Для каждого ОбъектXDTO Из СписокXDTO Цикл
		Результат = ОбъектXDTO.свойства().Получить("display_name");
		Если НЕ Результат = Неопределено Тогда
			НайденныйОбъект = ОбъектXDTO;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат НайденныйОбъект; 
	
КонецФункции // ВернутьОбъектСвойствоDisplayName()

#КонецОбласти

#Область ГеоКодированиеAxelotMaps

// Функция возвращает структуру координат объекта поиска 
//
Функция ПолучитьСтруктуруОбъектаПоискаAxelotMaps(ОбъектXDTO)
	
	стрОбъекта = Новый Структура("ПолноеПредставление", ОбъектXDTO.display_name);	
	
	стрОбъекта.Вставить("Долгота",ОбъектXDTO.lon);
	стрОбъекта.Вставить("Широта",ОбъектXDTO.lat);
	
	Возврат стрОбъекта; 
	
КонецФункции // ПолучитьСтруктуруОбъектаПоискаAxelotMaps()

#КонецОбласти

#Область ГеоКодированиеGoogle

// Функция возвращает стркутуру координат объекта поиска 
//
Функция ПолучитьСтруктуруОбъектаПоискаGoogle(ОбъектXDTO)
	
	стрОбъекта = Новый Структура("ПолноеПредставление", ОбъектXDTO.formatted_address);	
	
	стрОбъекта.Вставить("Долгота",ОбъектXDTO.geometry.location.lng);
	стрОбъекта.Вставить("Широта",ОбъектXDTO.geometry.location.lat);
	
	Возврат стрОбъекта; 
	
КонецФункции // ПолучитьСтруктуруОбъектаПоискаGoogle()

// Функция возвращает ОбъектXDTO содержащий свойство formatted_address
//
Функция ВернутьОбъектСвойствоFormattedAddress(СписокXDTO)
	
	НайденныйОбъект = Неопределено;
	
	Для каждого ОбъектXDTO Из СписокXDTO Цикл
		Результат = ОбъектXDTO.свойства().Получить("formatted_address");
		Если НЕ Результат = Неопределено Тогда
			НайденныйОбъект = ОбъектXDTO;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат НайденныйОбъект; 
	
КонецФункции // ВернутьОбъектСвойствоFormattedAddress()

#КонецОбласти

#Область ГеоКодированиеСитиГИД

// Функция возвращает ОбъектXDTO содержащий свойство foundText
//
Функция ВернутьОбъектСвойствоFoundText(СписокXDTO)
	
	НайденныйОбъект = Неопределено;
	
	Для каждого ОбъектXDTO Из СписокXDTO Цикл
		Результат = ОбъектXDTO.свойства().Получить("foundText");
		Если НЕ Результат = Неопределено Тогда
			НайденныйОбъект = ОбъектXDTO;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат НайденныйОбъект; 
	
КонецФункции // ВернутьОбъектСвойствоFoundText()

// Функция возвращает стркутуру координат объекта поиска 
//
Функция ПолучитьСтруктуруОбъектаПоискаСитиГИД(ОбъектXDTO)
	
	стрОбъекта = Новый Структура("ПолноеПредставление", ОбъектXDTO.foundText);	
	
	стрОбъекта.Вставить("Долгота",ОбъектXDTO.latitude);
	стрОбъекта.Вставить("Широта",ОбъектXDTO.latitude);
	
	Возврат стрОбъекта; 
	
КонецФункции // ПолучитьСтруктуруОбъектаПоискаСитиГИД()

#КонецОбласти

// Возвращает строку представление адреса из переданных координат.
//
Функция ПолучитьПредставлениеАдресаИзКоординат(Координаты) Экспорт
	
	Результат = Неопределено;
	
	#Если НЕ ВебКлиент Тогда
		
		Структура = упГеокодированиеСервер.ПолучитьТекстHTTPЗапросГеокодеру(Ложь,,Координаты);
		
		Если НЕ Структура.Использовать Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования не используется.'"));
			Возврат Неопределено; 
		КонецЕсли;
		
		ТекстЗапроса = "";
		
		Если Структура.Свойство("Текст") Тогда			
			ТекстЗапроса = Структура.Текст;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Текст HTTP запроса к геокодеру не сформирован.'"));
			Возврат Неопределено; 
		КонецЕсли;
		
		#Если Сервер Тогда
			НастройкаСохранения = Новый Соответствие;
			НастройкаСохранения.Вставить("МестоХранения", "Сервер");
			НастройкаСохранения.Вставить("Путь",          Неопределено);
			
			НастройкиПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.СтруктураПараметровПолученияФайла();
			ФайлКоординат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла(ТекстЗапроса,НастройкиПолучения,НастройкаСохранения);
		#Иначе
			ПараметрыПолучения = Новый Структура();
			ПараметрыПолучения.Вставить("ПутьДляСохранения",ПолучитьИмяВременногоФайла("xml"));
			//ПараметрыПолучения.Вставить("ЗащищенноеСоединение",Истина); 			
			ФайлКоординат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ТекстЗапроса,ПараметрыПолучения,Ложь);
		#КонецЕсли
		
		 упГеокодированиеСервер.ДобавитьРезультатОбращенияКСервисуГеоКодирования(Структура.Тип,Ложь,ФайлКоординат.Статус,ФайлКоординат);
		
		Если ФайлКоординат.Статус Тогда
			
			Если Структура.Тип = Структура.СитиГИД Тогда
				// Для геокодера СитиГИД данные приходят в формате JSON, XML-формат не предусмотрен
				ЧтениеJSON = Новый ЧтениеJSON();
				ЧтениеJSON.ОткрытьФайл(ФайлКоординат.Путь);
				ОбъектыXDTO = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON);
				ЧтениеJSON.Закрыть();
				УдалитьФайлы(ФайлКоординат.Путь);
				
				ТекущийРезультат = "";
				ТекущийОбъект = ОбъектыXDTO.свойства().Получить("settlement");
				Если НЕ ТекущийОбъект = Неопределено Тогда
					Если ТипЗнч(ОбъектыXDTO.settlement) = ТИП("Строка") Тогда
						ТекущийРезультат = ТекущийРезультат + ОбъектыXDTO.settlement + " ";
					КонецЕсли;
				КонецЕсли;
				
				ТекущийОбъект = ОбъектыXDTO.свойства().Получить("street");
				Если НЕ ТекущийОбъект = Неопределено Тогда
					Если ТипЗнч(ОбъектыXDTO.street) = ТИП("Строка") Тогда
						ТекущийРезультат = ТекущийРезультат + ОбъектыXDTO.street + " ";
					КонецЕсли;
				КонецЕсли;
				
				ТекущийОбъект = ОбъектыXDTO.свойства().Получить("house");
				Если НЕ ТекущийОбъект = Неопределено Тогда
					Если ТипЗнч(ОбъектыXDTO.house) = ТИП("Строка") Тогда
						ТекущийРезультат = ТекущийРезультат + ОбъектыXDTO.house;
					КонецЕсли;
				КонецЕсли;
				
				Результат = ТекущийРезультат;			
				
			Иначе
				
				ЧтениеXML = Новый ЧтениеXML;
				ЧтениеXML.ОткрытьФайл(ФайлКоординат.Путь);
				
				Если Структура.Тип = Структура.Nominatim Тогда
					
					Пока ЧтениеXML.Прочитать() Цикл
						Если ЧтениеXML.Имя = "result" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда				
							Пока ЧтениеXML.Прочитать() Цикл				
								Если ЧтениеXML.Имя = "#text" И ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
									Результат = ЧтениеXML.Значение;
									Прервать;
								КонецЕсли;				
							КонецЦикла;
						КонецЕсли;
					КонецЦикла;			
				ИначеЕсли Структура.Тип = Структура.Яндекс Тогда
					ОбъектыXDTO 		= ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);				
					Попытка
						НайденныеОбъекты 	= ОбъектыXDTO.GeoObjectCollection.featureMember;
					Исключение
						НайденныеОбъекты 	= Неопределено;
					КонецПопытки;
					
					Если ТипЗнч(НайденныеОбъекты) = Тип("СписокXDTO") Тогда
						ОбъектXDTO = ВернутьОбъектСвойствоDescription(НайденныеОбъекты);
						стрОбъекта = ПолучитьСтруктуруОбъектаПоиска(ОбъектXDTO);
					ИначеЕсли ТипЗнч(НайденныеОбъекты) = Тип("ОбъектXDTO") Тогда
						стрОбъекта = ПолучитьСтруктуруОбъектаПоиска(НайденныеОбъекты);
					Иначе
						стрОбъекта = Неопределено;
					КонецЕсли;
					Если НЕ стрОбъекта = Неопределено Тогда
						Результат = стрОбъекта.ПолноеПредставление;
					КонецЕсли;
				ИначеЕсли Структура.Тип = Структура.Google Тогда
					
					ОбъектыXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
					
					Попытка
						Если ОбъектыXDTO.status = "OK" Тогда
							НайденныеОбъекты 	= ОбъектыXDTO.result;
						Иначе
							НайденныеОбъекты = Неопределено;
							ТекстОшибки = СтрШаблон("Сервис геокодирования Google, передал статус обработки: %1",ОбъектыXDTO.status);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
						КонецЕсли;
					Исключение
						НайденныеОбъекты 	= Неопределено;
					КонецПопытки;
					
					Если ТипЗнч(НайденныеОбъекты) = Тип("СписокXDTO") Тогда
						ОбъектXDTO = ВернутьОбъектСвойствоFormattedAddress(НайденныеОбъекты);
						стрОбъекта = ПолучитьСтруктуруОбъектаПоискаGoogle(ОбъектXDTO);
					ИначеЕсли ТипЗнч(НайденныеОбъекты) = Тип("ОбъектXDTO") Тогда
						стрОбъекта = ПолучитьСтруктуруОбъектаПоискаGoogle(НайденныеОбъекты);
					Иначе
						стрОбъекта = Неопределено;
					КонецЕсли;
					
					Если НЕ стрОбъекта = Неопределено Тогда
						Результат = стрОбъекта.ПолноеПредставление;
					КонецЕсли;
					
				КонецЕсли;
				
				ЧтениеXML.Закрыть();
				
			КонецЕсли;
		Иначе 
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования недоступен.'"));
		КонецЕсли;
		
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции // ПолучитьПредставлениеАдресаИзКоординат()

// Преобразует значение координаты в строку.
//
// Параметры:
//	Координата - число - Географические координаты.
//
// Возвращаемое значение:
//	Строка - представление координаты в формате сервиса.
//
Функция ПредставлениеКоординаты(Координата) Экспорт
	
	Возврат Формат(Координата, "ЧЦ=9; ЧДЦ=6; ЧРД=.; ЧН=0; ЧГ=");
	
КонецФункции // ПредставлениеКоординаты()

// Выполним подключение к сервису геокодирования.
//  обратимся к обратному геокодированиию.
//
// Параметры:
//  ПараметрыПроверки  - Структура - Параментры подключения к сервисам маршрутизации:
//                       * Ключ - упТипГеокодера, тип сервиса геокодирования.
//                       * Ключ - упАдресСервераГеокодирования, строка, адрес сервиса.
//                       * Ключ - упАдресСервераГеокодированияNominatim, строка, адрес сервиса.
//                       * Ключ - упКлючСервисаГеоКодирования, строка,  подключение ключ API  к сервису.
//                       * Ключ - упПределОтветовГеосервиса, Число, количество ответов.
//
// Возвращаемое значение:
//   Структура   - Ключ "Статус" - Булево, Истина если соединение установлено.
//               - Ключ "Ошибка" - Строка, текст ошибки.
//
Функция ВыполнитьПровекруСервисаГеокодирования(ПараметрыПроверки) Экспорт
	
	Результат = Новый Структура("Ошибка,Статус","Не удалось выполнить подключение",Ложь);	
	
	НачальнаяШирота = 55.84121;
	НачальнаяДолгота = 37.64957;	
	
	Координаты =  Новый Структура("Широта,Долгота",НачальнаяШирота,НачальнаяДолгота);
	
	#Если НЕ ВебКлиент Тогда
		
		ПараметрыГеокодирования = упГеокодированиеСервер.ПолучитьПараметрыГеоКодирования();
		
		ТекущийКлюч = упМаршрутизация.ПолучитьТекстКлючаAPI(ПараметрыПроверки.упКлючСервисаГеоКодирования,ПараметрыПроверки.упТипГеокодера);
		
		Широта = ПредставлениеКоординаты(Координаты.Широта);
		Долгота = ПредставлениеКоординаты(Координаты.Долгота);
		
		ТекстЗапроса = "";
		АдресРесурса = "";
		КомандаСтр = "";
		ИмяФайла = ПолучитьИмяВременногоФайла("xml");
		СитиГИДПрямоеГеокодирование = Ложь;
		ЗащищенноеСоединение = Ложь;
		
		Порт = Неопределено;
		
		Если ПараметрыПроверки.упТипГеокодера = ПараметрыГеокодирования.Nominatim Тогда
			ТекстЗапроса = ПараметрыПроверки.упАдресСервераГеокодированияNominatim + "reverse?format=xml&lat=" + Широта + "&lon=" + Долгота + "&zoom=18";
			АдресРесурса = ПараметрыПроверки.упАдресСервераГеокодированияNominatim;
			КомандаСтр = "search?q=1&format=xml&lat=" + Широта + "&lon=" + Долгота;
			
			ЗащищенноеСоединение = Истина;
		ИначеЕсли ПараметрыПроверки.упТипГеокодера = ПараметрыГеокодирования.AxelotMaps Тогда
			ТекстЗапроса = "https://maps.axelot.ru/reverse?format=xml&lat=" + Широта + "&lon=" + Долгота + "&zoom=18";
		ИначеЕсли ПараметрыПроверки.упТипГеокодера = ПараметрыГеокодирования.Google Тогда
			ТекстЗапроса = "https://maps.googleapis.com/maps/api/geocode/xml?latlng=" + Широта + "," + Долгота + "&sensor=false&language=ru-RU" + ТекущийКлюч;
		ИначеЕсли ПараметрыПроверки.упТипГеокодера = ПараметрыГеокодирования.Яндекс Тогда
			ТекстЗапроса = "https://geocode-maps.yandex.ru/1.x/?geocode=" + Долгота + "," + Широта + "&format=xml&results=" + Формат(ПараметрыПроверки.упКлючСервисаГеоКодирования,"ЧН=1; ЧГ=0") + "&lang=ru_RU" + ТекущийКлюч;
		ИначеЕсли ПараметрыПроверки.упТипГеокодера = ПараметрыГеокодирования.СитиГИД Тогда
			
			СитиГИДПрямоеГеокодирование = Истина;
			
			сткПараметры = Новый Структура;
			сткПараметры.Вставить("accessKey",ПараметрыПроверки.упКлючСервисаГеоКодирования);
			сткПараметры.Вставить("address","Россия, 129226, Москва, ул.Докукина, дом 16, стр.3");
			
			СтруктураПараметров = Новый Структура;
			
			СтруктураПараметров.Вставить("Параметры",сткПараметры);
			СтруктураПараметров.Вставить("ФорматОтвета","JSON");
			
			ЗаголовокHTTP = Новый Соответствие();
			ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
			ЗаголовокHTTP.Вставить("url", "/api/geocode");	
			ЗаголовокHTTP.Вставить("type", "POST");
			ЗаголовокHTTP.Вставить("dataType", "text");
			
			СтруктураПараметров.Вставить("ЗаголовокHTTP",ЗаголовокHTTP);
			СтруктураПараметров.Вставить("Ресурс","/api/geocode");				
			СтруктураПараметров.Вставить("Порт",8081);
			СтруктураПараметров.Вставить("Текст","");
			СтруктураПараметров.Вставить("СерверГеокодированияПрямого_СитиГИД",ПараметрыПроверки.упАдресСервераГеокодированияСитиГИД);
			
		КонецЕсли;
		
		АдресРесурса = СтрЗаменить(НРег(АдресРесурса),"http://","");
		АдресРесурса = СтрЗаменить(НРег(АдресРесурса),"https://","");
		
		Если ПустаяСтрока(АдресРесурса) Тогда
			
			Если СитиГИДПрямоеГеокодирование Тогда
				ФайлКоординат = ВыполнитьОбращениеКСервисуПрямогоГеоКодирования(СтруктураПараметров);
			Иначе
				ФайлКоординат = ВернутьФайлОтГеоСервиса(ТекстЗапроса);
			КонецЕсли;
			
			упГеокодированиеСервер.ДобавитьРезультатОбращенияКСервисуГеоКодирования(ПараметрыПроверки.упТипГеокодера,СитиГИДПрямоеГеокодирование,ФайлКоординат.Статус,ФайлКоординат);
			
			Результат.Ошибка = ?(ФайлКоординат.Свойство("СообщениеОбОшибке"),ФайлКоординат.СообщениеОбОшибке,"");
			Результат.Статус = ФайлКоординат.Статус;
			
			Если ФайлКоординат.Свойство("Путь") Тогда
				
				УдалитьФайлы(ФайлКоординат.Путь);
				
			КонецЕсли;
			
		Иначе			
			Если ЗащищенноеСоединение = Истина Тогда
				ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
			Иначе
				ЗащищенноеСоединение = Неопределено;
			КонецЕсли;
			
			HttpКоманда = ТекстЗапроса;
			Соединение	= Новый HTTPСоединение(СокрЛП(АдресРесурса),,,,,, ЗащищенноеСоединение);
			
			ЗапросHttp	= Новый HTTPЗапрос(КомандаСтр);
			
			Попытка
				HTTPОтвет = Соединение.Получить(ЗапросHttp, ИмяФайла);
				Результат.Ошибка = "";
				Результат.Статус = Истина;
				
				Если HTTPОтвет.КодСостояния < 200 Или HTTPОтвет.КодСостояния >= 300 Тогда
					ПутьДляСохранения = ПолучитьИмяВременногоФайла();
					ФайлОтвета = Новый ЧтениеТекста(ПутьДляСохранения, КодировкаТекста.UTF8);
					ВызватьИсключение СтроковыеФункцииКлиентСервер.ИзвлечьТекстИзHTML(ФайлОтвета.Прочитать(5 * 1024));
				КонецЕсли;
				
				
			Исключение
				Результат.Ошибка = "Не удалось отправить Http запрос " + HttpКоманда + ". " + Символы.ПС + ОписаниеОшибки();
				Результат.Статус = Ложь;
			КонецПопытки;
			
			Если HTTPОтвет.КодСостояния >= 400 и HTTPОтвет.КодСостояния < 500  Тогда
				Результат.Ошибка = "Код ответа больше 4XX, ошибка запроса.  Код статуса: " + HTTPОтвет.КодСостояния +
				Символы.ПС + "  Подробнее смотри http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html#sec10.4";
				Результат.Статус = Ложь;
			КонецЕсли;
			
			Если HTTPОтвет.КодСостояния >= 500 и HTTPОтвет.КодСостояния < 600  Тогда
				Результат.Ошибка = "Код ответа больше 5XX, ошибка сервера. Код статуса: " + HTTPОтвет.КодСостояния +
				Символы.ПС + "  Подробнее смотри http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html#sec10.5";
				Результат.Статус = Ложь;
			КонецЕсли;
			
			упГеокодированиеСервер.ДобавитьРезультатОбращенияКСервисуГеоКодирования(ПараметрыПроверки.упТипГеокодера,СитиГИДПрямоеГеокодирование,Результат.Статус,ИмяФайла);
			
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ИмяФайла) Тогда
			
			УдалитьФайлы(ИмяФайла);
			
		КонецЕсли;		
	#КонецЕсли
	
	Если ПараметрыПроверки.упНормализоватьАдресаСервисомDaData Тогда
		упГеокодированиеСервер.НормализованныйАдрес("г. Лобня, Лейтенанта Бойко ул, дом № 104", ПараметрыПроверки.упСекретныйКлючСервисаDaData, ПараметрыПроверки.упAPIКлючСервисаDaData);
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции // ВыполнитьПровекруСервисаГеокодирования()
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция ищет координаты адреса по представлению.
//
// Параметры:
//  Адрес  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура   - Параметры объекта обрбаотки.
//
Функция НайтиКоординатыАдреса(Адрес) Экспорт
	
	ТипЗначения = ТипЗнч(Адрес);
	Если ТипЗначения = Тип("СправочникСсылка.упАдреса") Тогда 
		СтрокаПоиска = упГеокодированиеСервер.ПолучитьПредставлениеАдреса(Адрес);
		Записывать   = Истина;
	ИначеЕсли ТипЗначения = Тип("Структура") Тогда 
		СтрокаПоиска = Адрес;
		Записывать   = Ложь;
	Иначе
		СтрокаПоиска = Новый Структура("Представление", Строка(Адрес));
		Записывать   = Ложь;
	КонецЕсли;
	
	стрОбъекта = Неопределено;
	
	Структура = ОбработатьСтрокуПоискаАдреса(СтрокаПоиска);
	
	Если НЕ Структура = Неопределено Тогда
		НайденныеОбъекты = Структура.Объект;
		ТипЗначения = ТипЗнч(НайденныеОбъекты);
		Если ТипЗначения = Тип("СписокXDTO") Тогда
			Если Структура.Тип = Структура.Яндекс Тогда
				ОбъектXDTO = ВернутьОбъектСвойствоDescription(НайденныеОбъекты);
				стрОбъекта = ПолучитьСтруктуруОбъектаПоиска(ОбъектXDTO);
			ИначеЕсли Структура.Тип = Структура.Nominatim Тогда
				ОбъектXDTO = ВернутьОбъектСвойствоDisplayName(НайденныеОбъекты);
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаNominatim(ОбъектXDTO);
			ИначеЕсли Структура.Тип = Структура.AxelotMaps Тогда
				ОбъектXDTO = ВернутьОбъектСвойствоDisplayName(НайденныеОбъекты);
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаAxelotMaps(ОбъектXDTO);				
			ИначеЕсли Структура.Тип = Структура.Google Тогда
				ОбъектXDTO = ВернутьОбъектСвойствоFormattedAddress(НайденныеОбъекты);
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаGoogle(ОбъектXDTO);
			ИначеЕсли Структура.Тип = Структура.СитиГИД Тогда
				ОбъектXDTO = ВернутьОбъектСвойствоFoundText(НайденныеОбъекты);
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаСитиГИД(ОбъектXDTO);
			КонецЕсли;
		ИначеЕсли ТипЗначения = Тип("ОбъектXDTO") Тогда
			Если Структура.Тип = Структура.Яндекс Тогда
				стрОбъекта = ПолучитьСтруктуруОбъектаПоиска(НайденныеОбъекты);
			ИначеЕсли Структура.Тип = Структура.Nominatim Тогда
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаNominatim(НайденныеОбъекты);
			ИначеЕсли Структура.Тип = Структура.AxelotMaps Тогда
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаAxelotMaps(НайденныеОбъекты);
			ИначеЕсли Структура.Тип = Структура.Google Тогда
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаGoogle(НайденныеОбъекты);
			ИначеЕсли Структура.Тип = Структура.СитиГИД Тогда
				стрОбъекта = ПолучитьСтруктуруОбъектаПоискаСитиГИД(НайденныеОбъекты);
			КонецЕсли;
		ИначеЕсли ТипЗначения = Тип("Структура") Тогда
			стрОбъекта = НайденныеОбъекты;
		КонецЕсли; 
	КонецЕсли; 
		
	Если Записывать И НЕ стрОбъекта = Неопределено Тогда
		упГеокодированиеСервер.ЗаполнитьКоординатыАдреса(Адрес, стрОбъекта);
	КонецЕсли; 
	
	Возврат стрОбъекта;
	
КонецФункции

// Процедура ищет координаты адресов.
//
// Параметры:
//  мсвАдресов  - Массив - Ссылки на элементы.
//
// Возвращаемое значение:
//   Число   - Число обработанных объектов.
//
Функция НайтиКоординатыАдресов(мсвАдресов) Экспорт
	
	соотАдресов = упГеокодированиеСервер.ПолучитьПредставленияАдресов(мсвАдресов);
	
	Сч = 0;
	Для Каждого текЭлемент Из соотАдресов Цикл 
		Если ЗначениеЗаполнено(текЭлемент.Значение) Тогда
			
			текЗначение = НайтиКоординатыАдреса(текЭлемент.Значение);
			
			текКлюч = текЭлемент.Ключ;
			соотАдресов.Удалить(текЭлемент);
			соотАдресов.Вставить(текКлюч,текЗначение);
			
			Если НЕ текЗначение = Неопределено Тогда 
				Сч = Сч + 1;
			КонецЕсли;	
			
		Иначе
			текЭлемент.Значение = Неопределено;
		КонецЕсли;		
		
	КонецЦикла;	
	
	упГеокодированиеСервер.ЗаполнитьКоординатыАдресов(соотАдресов);
	
	Возврат Сч;
	
КонецФункции // НайтиКоординатыАдресов()

Функция ВернутьФайлОтГеоСервиса(ТекстЗапроса)
	
	#Если Сервер Тогда
		НастройкаСохранения = Новый Соответствие;
		НастройкаСохранения.Вставить("МестоХранения", "Сервер");
		НастройкаСохранения.Вставить("Путь",          Неопределено);
		
		НастройкиПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.СтруктураПараметровПолученияФайла();			
		ФайлКоординат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла(ТекстЗапроса,НастройкиПолучения,НастройкаСохранения);
	#Иначе
		ПараметрыПолучения = Новый Структура();
		ПараметрыПолучения.Вставить("ПутьДляСохранения",ПолучитьИмяВременногоФайла("xml"));
		//ПараметрыПолучения.Вставить("ЗащищенноеСоединение",Истина); 
		ФайлКоординат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ТекстЗапроса,ПараметрыПолучения,Ложь);
	#КонецЕсли
	
	Возврат ФайлКоординат;
	
КонецФункции // ВернутьФайлОтГеоСервиса()

Функция ВыполнитьОбращениеКСервисуПрямогоГеоКодирования(ПараметрыОбращения)
	
	ФайлЗапроса = ПолучитьИмяВременногоФайла("JSON");
	
	ЗаписьJSON = Новый ЗаписьJSON;
	
	ЗаписьJSON.ОткрытьФайл(ФайлЗапроса,,,Новый ПараметрыЗаписиJSON(,Символы.Таб));
	// формируем файл формата JSON для запроса к сервису.
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON();
	НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	ЗаписатьJSON(ЗаписьJSON,ПараметрыОбращения.Параметры,НастройкиСериализацииJSON);
	
	ЗаписьJSON.Закрыть();
	ЗаписатьJSON = Неопределено;
	
	ТекстФайл = Новый ТекстовыйДокумент;
	ТекстФайл.Прочитать(ФайлЗапроса,КодировкаТекста.UTF8);
	Тело = ТекстФайл.ПолучитьТекст();	
	
	Сервер		  = ПараметрыОбращения.СерверГеокодированияПрямого_СитиГИД;	
	ЗаголовокHTTP = ПараметрыОбращения.ЗаголовокHTTP;
	Ресурс		  = ПараметрыОбращения.Ресурс;
	Прокси        = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("http://" + Сервер);
	
	HTTP		  = Новый HTTPСоединение(Сервер,ПараметрыОбращения.Порт,,,Прокси);
	
	ЗапросHttp = Новый HTTPЗапрос(Ресурс, ЗаголовокHTTP);
	ЗапросHttp.УстановитьТелоИзСтроки(Тело,КодировкаТекста.UTF8,ИспользованиеByteOrderMark.НеИспользовать);
	
	текОшибки = Истина;
	СообщениеОбОшибке = "";
	ТекущийРезультат = Новый Структура();
	
	Попытка
		ОтветHttp = HTTP.ОтправитьДляОбработки(ЗапросHttp);
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		текОшибки = Ложь;
	КонецПопытки;
	
	Если текОшибки Тогда
		
		ТекстОтветJSON = СокрЛП(ОтветHttp.ПолучитьТелоКакСтроку());
		
		Если ПустаяСтрока(ТекстОтветJSON) Тогда
			СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера прямого геокодирования не получен. " + Строка(HTTP.Сервер);
			текОшибки = Ложь;			
		Иначе			
			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.УстановитьСтроку(ТекстОтветJSON);
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
			
			ЧтениеJSON = Неопределено;
			УдалитьФайлы(ФайлЗапроса);
			
			Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда
				
				СообщениеОбОшибке = ОтветJSON.result;
				текОшибки = Ложь;
				
				Если ОтветJSON.Свойство("address") ИЛИ ОтветJSON.Свойство("addresses") Тогда
					ОтветAddress = Неопределено;
					Address = Неопределено;
					Если ОтветJSON.Свойство("address") Тогда
						ОтветAddress = ОтветJSON.address;
					ИначеЕсли ОтветJSON.Свойство("addresses") Тогда
						ОтветAddress = ОтветJSON.addresses;
					КонецЕсли;
					
					Если ТипЗнч(ОтветAddress)=ТИП("Массив") Тогда
						Если ОтветAddress.Количество() Тогда
							Address = ОтветAddress[0];
						КонецЕсли;
					КонецЕсли;
					
					Если НЕ Address = Неопределено И Address.Свойство("location") Тогда
						СообщениеОбОшибке = "";
						текОшибки = Истина; // Нет ошибок.
						ТекущийРезультат.Вставить("Долгота",Address.location.lon);
						ТекущийРезультат.Вставить("Широта",Address.location.lat);						
						
						ПолноеПредставление = ?(ПустаяСтрока(Address.province),"",Address.province + ", "); // province — область или другое обозначение территории.
						ПолноеПредставление = ПолноеПредставление + ?(ПустаяСтрока(Address.region),"",Address.region + ", "); // region — регион или другое обозначение территории.
						ПолноеПредставление = ПолноеПредставление + ?(ПустаяСтрока(Address.city),"",Address.city + ", "); // city — город.
						ПолноеПредставление = ПолноеПредставление + ?(ПустаяСтрока(Address.street),"",Address.street + ", "); // street — улица.
						ПолноеПредставление = ПолноеПредставление + ?(ПустаяСтрока(Address.house),"",Address.house + "."); // house — номер дома.
						
						ТекущийРезультат.Вставить("ПолноеПредставление",ПолноеПредставление);
						ТекущийРезультат.Вставить("РегионСтрока",Address.province);
						ТекущийРезультат.Вставить("Город",Address.city);
						
					КонецЕсли;
				КонецЕсли;
			Иначе
				СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера прямого геокодирования не получен. " + Строка(HTTP.Сервер);
				текОшибки = Ложь;
			КонецЕсли;			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущийРезультат.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	ТекущийРезультат.Вставить("Статус", текОшибки);
	
	Возврат ТекущийРезультат;
	
КонецФункции // ВыполнитьОбращениеКСервисуПрямогоГеоКодирования()

// РАБОТА С http://geocode-maps.yandex.ru.

// Функция возвращает объектXDTO с сервиса геокодирования.
//
Функция ОбработатьСтрокуПоискаАдреса(СтрокаПоиска)
	
	#Если ВебКлиент Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования в веб-клиенте недоступен.'"));
		Возврат Неопределено;
		
	#Иначе		
		
		Структура = упГеокодированиеСервер.ПолучитьТекстHTTPЗапросГеокодеру(Истина,СтрокаПоиска);
		
		Если НЕ Структура.Использовать Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования не используется.'"));
			Возврат Неопределено; 
		КонецЕсли;
		
		ТекстЗапроса = "";
		СитиГИДПрямоеГеокодирование = Ложь;
		НайденныеОбъекты = Неопределено;
		
		Попытка
			
			Если Структура.Свойство("Текст") Тогда
				ТекстЗапроса = Структура.Текст;			
				Если Структура.Тип = Структура.СитиГИД И ПустаяСтрока(ТекстЗапроса) Тогда
					// Выполним обращение к сервису прямого геокодирования СитиГИД.
					СитиГИДПрямоеГеокодирование = Истина;
				КонецЕсли;			
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Текст HTTP запроса к геокодеру не сформирован.'"));
				Возврат Неопределено; 
			КонецЕсли;
			
			Если СитиГИДПрямоеГеокодирование Тогда
				ФайлКоординат = ВыполнитьОбращениеКСервисуПрямогоГеоКодирования(Структура);
			Иначе
				ФайлКоординат = ВернутьФайлОтГеоСервиса(ТекстЗапроса);
			КонецЕсли;
			
			упГеокодированиеСервер.ДобавитьРезультатОбращенияКСервисуГеоКодирования(Структура.Тип,Истина,ФайлКоординат.Статус,ФайлКоординат);
			
			Если ФайлКоординат.Статус Тогда			
				
				Если СитиГИДПрямоеГеокодирование Тогда	  
					// При прямом геокодировании СитиГИД данные приходят в ином формате
					// отличном от обратного, поэтому соостовляем структуру и заполняем её
					НайденныеОбъекты = Новый Структура("Долгота,Широта,ПолноеПредставление,Город,РегионСтрока",0,0,"","","");
					ЗаполнитьЗначенияСвойств(НайденныеОбъекты,ФайлКоординат);
				ИначеЕсли (Структура.Тип = Структура.СитиГИД)ИЛИ(Структура.Свойство("ФорматОтвета") И Структура.ФорматОтвета = "JSON") Тогда
					// Для геокодера СитиГИД данные приходят в формате JSON, XML-формат не предусмотрен.
					ЧтениеJSON = Новый ЧтениеJSON();
					ЧтениеJSON.ОткрытьФайл(ФайлКоординат.Путь);
					ОбъектыXDTO = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON);
					
					НайденныеОбъекты = ОбъектыXDTO;
					
					Если Структура.Тип = Структура.Nominatim Тогда
						ПолеСуществует = ОбъектыXDTO.свойства().Получить("matches");
						Если ПолеСуществует = Неопределено Тогда
							НайденныеОбъекты = Неопределено;
						Иначе
							НайденныеОбъекты = ОбъектыXDTO.matches.matches;
						КонецЕсли;
					ИначеЕсли Структура.Тип = Структура.AxelotMaps Тогда
						ПолеСуществует = ОбъектыXDTO.свойства().Получить("matches");
						Если ПолеСуществует = Неопределено Тогда
							НайденныеОбъекты = Неопределено;
						Иначе
							НайденныеОбъекты = ОбъектыXDTO.matches.matches;
						КонецЕсли;					
					КонецЕсли;
					
					ЧтениеJSON.Закрыть();
					
					УдалитьФайлы(ФайлКоординат.Путь);
					
				Иначе
					
					ЧтениеXML = Новый ЧтениеXML;
					ЧтениеXML.ОткрытьФайл(ФайлКоординат.Путь);
					
					ОбъектыXDTO 		= ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
					
					Если Структура.Тип = Структура.Яндекс Тогда
						
						Попытка
							НайденныеОбъекты 	= ОбъектыXDTO.GeoObjectCollection.featureMember;
						Исключение
							НайденныеОбъекты 	= Неопределено;
						КонецПопытки;
						
					ИначеЕсли Структура.Тип = Структура.Nominatim Тогда
						
						Попытка
							НайденныеОбъекты = ОбъектыXDTO.place;
						Исключение
							НайденныеОбъекты 	= Неопределено;
						КонецПопытки;				
						
					ИначеЕсли Структура.Тип = Структура.AxelotMaps Тогда
						
						Попытка
							НайденныеОбъекты = ОбъектыXDTO.place;
						Исключение
							НайденныеОбъекты 	= Неопределено;
						КонецПопытки;				
						
					ИначеЕсли Структура.Тип = Структура.Google Тогда
						Попытка
							Если ОбъектыXDTO.status = "OK" Тогда					
								НайденныеОбъекты 	= ОбъектыXDTO.result;
							Иначе
								НайденныеОбъекты = Неопределено;
								ТекстОшибки = СтрШаблон("Сервис геокодирования Google, передал статус обработки: %1",ОбъектыXDTO.status);
								ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
							КонецЕсли;
						Исключение
							НайденныеОбъекты = Неопределено;
						КонецПопытки;
					Иначе
						НайденныеОбъекты = Неопределено;
					КонецЕсли;
				КонецЕсли;
			Иначе 			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования недоступен.'"));
				Если ФайлКоординат.Свойство("СообщениеОбОшибке") И НЕ ПустаяСтрока(ФайлКоординат.СообщениеОбОшибке) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ФайлКоординат.СообщениеОбОшибке);
				КонецЕсли;
				НайденныеОбъекты 	= Неопределено;
			КонецЕсли;
			
			Если НайденныеОбъекты = Неопределено И (Структура.Тип = Структура.Nominatim ИЛИ Структура.Тип = Структура.AxelotMaps) И Структура.Свойство("Текст_Параметры") Тогда
				
				ТекстЗапроса = Структура.Текст_Параметры;
				
				ФайлКоординат = ВернутьФайлОтГеоСервиса(ТекстЗапроса);
				
				Если ФайлКоординат.Статус Тогда
					Если Структура.Свойство("ФорматОтвета") И Структура.ФорматОтвета = "JSON" Тогда
						ЧтениеJSON = Новый ЧтениеJSON();
						ЧтениеJSON.ОткрытьФайл(ФайлКоординат.Путь);
						ОбъектыXDTO = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON);
						
						ПолеСуществует = ОбъектыXDTO.свойства().Получить("matches");
						Если ПолеСуществует = Неопределено Тогда
							НайденныеОбъекты = Неопределено;
						Иначе
							НайденныеОбъекты = ОбъектыXDTO.matches.matches;
						КонецЕсли;
						
						ЧтениеJSON.Закрыть();
						
						УдалитьФайлы(ФайлКоординат.Путь);
						
					Иначе
						
						ЧтениеXML = Новый ЧтениеXML;
						ЧтениеXML.ОткрытьФайл(ФайлКоординат.Путь);
						
						ОбъектыXDTO 		= ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
						
						Попытка
							НайденныеОбъекты = ОбъектыXDTO.place;
						Исключение
							НайденныеОбъекты 	= Неопределено;
						КонецПопытки;
						
					КонецЕсли;
					
					Если НЕ НайденныеОбъекты = Неопределено Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Геокодирование выполнено с параметрами: " + Структура.ПарамертыПоиска + ". Результат поиска: " + НайденныеОбъекты.display_name);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
		Исключение
			#Если НаСервере Тогда
				ТекущиеДанные = ?(ТипЗнч(СтрокаПоиска)=ТИП("Структура"),СтрокаПоиска.Представление,СтрокаПоиска);
				ТекстСообщения = СтрШаблон("ПРи геокодировании адреса %1 произошла ошибка: %2",ТекущиеДанные,ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации("Геокодирование:", ПредопределенноеЗначение("УровеньЖурналаРегистрации.Ошибка"), , ТекущиеДанные, ТекстСообщения);
			#Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования не вернул результат, ошибка использования сервиса. Возможно сервис не доступен.'"));
			#КонецЕсли
		КонецПопытки;
		
		Структура.Вставить("Объект",НайденныеОбъекты);
		
		Возврат Структура;		
		
	#КонецЕсли
	
КонецФункции // ОбработатьСтрокуПоискаАдреса()

// Функция возвращает стркутуру координат объекта поиска.
//
Функция ПолучитьСтруктуруОбъектаПоиска(ОбъектXDTO)
	
	стрОбъекта = Новый Структура("ПолноеПредставление, Долгота, Широта", ОбъектXDTO.GeoObject.metaDataProperty.GeocoderMetaData.text);	
	РазобратьСтрокуКоординат(стрОбъекта, ОбъектXDTO.GeoObject.Point.pos);
	
	Возврат стрОбъекта; 
	
КонецФункции // ПолучитьСтруктуруОбъектаПоиска()

// Функция возвращает ОбъектXDTO содержащий свойство description.
//
Функция ВернутьОбъектСвойствоDescription(СписокXDTO)
	
	НайденныйОбъект = Неопределено;
	
	Для каждого ОбъектXDTO Из СписокXDTO Цикл
		Результат = ОбъектXDTO.свойства().Получить("GeoObject");
		Если Результат <> Неопределено Тогда
			Результат = ОбъектXDTO.GeoObject.свойства().Получить("description");
			Если Результат <> Неопределено Тогда				
				НайденныйОбъект = ОбъектXDTO;
				Прервать;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат НайденныйОбъект; 
	
КонецФункции // ВернутьОбъектСвойствоDescription()

// Процедура разбирает строку координат сервиса геокодирвания.
//
Процедура РазобратьСтрокуКоординат(стрКоординат, строкаКоординат)
	
	Попытка
		стрКоординат.Долгота = Число(Лев(строкаКоординат,  Найти(строкаКоординат, " ") - 1));
		стрКоординат.Широта	 = Число(Сред(строкаКоординат, Найти(строкаКоординат, " ") + 1));
	Исключение
	КонецПопытки;	
	
КонецПроцедуры// ()

#КонецОбласти 

#КонецЕсли