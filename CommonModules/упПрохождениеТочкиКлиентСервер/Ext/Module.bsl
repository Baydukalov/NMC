#Область ПрограммныйИнтерфейс

// Возвращает точку разгрузки по рейсу.
//
// Параметры:
//  КоллекцияТочекМаршрута		 - Коллекция	 - Коллекция точек.
//  Рейс						 - ДокументСсылка.упРейс	 - Ссылка на элемент.
//  АдресРазгрузки				 - СправочникСсылка.упАдреса	 - Ссылка на элемент.
//  ПлановоеПрибытие			 - Дата					 - План прибытия.
//  НачалоРаботПлан				 - Дата					 - период начала рбаот.
//  ПлановоеУбытие				 - Дата					 - план убытия.
//  Отказ						 - Булево					 - Фиксировать изменения.
//  Регистратор				 - ДокументСсылка			 - Ссылка на элемент.
//  ПорядковыйНомерТекущейТочки	 - Число					 - Номер.
//  ПредшественникиТочкиРазгрузки	 - Строка					 - Предшественники.
// 
// Возвращаемое значение:
//  СправочникСсылка.упАдреса - Результат выполнения.
//
Функция ДобавитьТочкуРазгрузкиПоРейсу(КоллекцияТочекМаршрута, Рейс, АдресРазгрузки, ПлановоеПрибытие, НачалоРаботПлан, ПлановоеУбытие, Отказ, Регистратор = Неопределено, ПорядковыйНомерТекущейТочки, ПредшественникиТочкиРазгрузки = Неопределено) Экспорт
	
	Если Отказ Тогда 
		Возврат Неопределено; 
	КонецЕсли;
		
	// Поиск точки с заданным адресом.
	сткТочкаРазгрузки = НайтиТочкуРазгрузки(КоллекцияТочекМаршрута, Рейс, АдресРазгрузки, Регистратор, ПорядковыйНомерТекущейТочки);
	ТочкаРазгрузки = сткТочкаРазгрузки.ТочкаРазгрузки;
	КоличествоТочек = сткТочкаРазгрузки.КоличествоТочек;
		
	// Если такой точки нет.
	Если ТочкаРазгрузки = Неопределено Тогда
		
		// Берется последняя точка.
		ПоследняяТочкаМаршрута = ПоследняяТочкаМаршрута(КоллекцияТочекМаршрута);
		ПоследняяТочкаМаршрутаТочкаЗавершения = Ложь;
		
		НомерСтрокиРазгрузки = 0;
		Если Не ПоследняяТочкаМаршрута = Неопределено Тогда
			Если ПоследняяТочкаМаршрута.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
				ПоследняяТочкаМаршрутаТочкаЗавершения = Истина;	
				НомерСтрокиРазгрузки = ПорядковыйНомерТочки(ПоследняяТочкаМаршрута);	
			Иначе
				КоличествоТочек = КоличествоТочек + 1;
				НомерСтрокиРазгрузки = КоличествоТочек;
			КонецЕсли;
		КонецЕсли;			
		
		ТочкаРазгрузки = ДобавитьТочкуРазгрузки(КоллекцияТочекМаршрута, Рейс, АдресРазгрузки, НомерСтрокиРазгрузки, ПлановоеПрибытие, НачалоРаботПлан, ПлановоеУбытие);
		ТочкаРазгрузки.Предшественники = ПредшественникиТочкиРазгрузки;
		
		Если ПоследняяТочкаМаршрутаТочкаЗавершения Тогда
			ИзменитьПорядокТочки(КоллекцияТочекМаршрута, ПоследняяТочкаМаршрута, КоличествоТочек + 1); // Сдвинуть последнюю точку в конец маршрута.
			Если Не ТипЗнч(КоллекцияТочекМаршрута) = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
				// дополним предшественников точки гаража новой точкой
				мсвПредшественников = СтрРазделить(ПоследняяТочкаМаршрута.Предшественники, ",", Ложь);
				мсвПредшественников.Добавить(Строка(ТочкаРазгрузки.Идентификатор));
				ПоследняяТочкаМаршрута.Предшественники = СтрСоединить(мсвПредшественников, ",");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
		
	Возврат ТочкаРазгрузки;
		
КонецФункции

// Возвращет номер индекс по типу точки.
//
// Параметры:
//  ТипТочки - ПеречислениеСсылка.упТипыТочекМаршрута - Тип точки.
//
// Возвращаемое значение:
//   Число - Номер индекса.
//
Функция ИндексКартинкиТочки(ТипТочки) Экспорт
	
	текРезультат = 0;
	
	#Если Клиент Тогда
		Если ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления") Тогда
			текРезультат = 0;
		ИначеЕсли ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию") Тогда	
			текРезультат = 1;
		ИначеЕсли ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.КонтрольнаяТочка") Тогда	
			текРезультат = 2;
		ИначеЕсли ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда		
			текРезультат = 3;
		ИначеЕсли ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ПроисшествиеВПути") Тогда					
			текРезультат = 4;
		КонецЕсли;
	#ИначеЕсли Сервер Тогда 	
		текРезультат = Перечисления.упТипыТочекМаршрута.Индекс(ТипТочки);				
	#КонецЕсли
	
	Возврат текРезультат;
		
КонецФункции

// Возвращет номер индекс по типу операции.
//
// Параметры:
//  ТипОперации - ПеречислениеСсылка.упТипыОпераций - Тип Операции.
//
// Возвращаемое значение:
//   Число - Номер индекса.
//
Функция ИндексКартинкиОперации(ТипОперации) Экспорт
	текРезультат = 0;
	
	#Если Клиент Тогда
		Если ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка") Тогда
			текРезультат = 0;
		ИначеЕсли ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка") Тогда	
			текРезультат = 1;
		ИначеЕсли ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда	
			текРезультат = 2;
		ИначеЕсли ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПолучениеДокументов") Тогда		
			текРезультат = 3;
		ИначеЕсли ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ДополнительнаяОперация") Тогда					
			текРезультат = 4;
		ИначеЕсли ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда					
			текРезультат = 5;
		ИначеЕсли ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей") Тогда					
			текРезультат = 6;
	    КонецЕсли;
	#ИначеЕсли Сервер Тогда 	
		текРезультат = Перечисления.упТипыОпераций.Индекс(ТипОперации);				
	#КонецЕсли
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти

#Область ОбработкаПрохожденияТочки

// Процедура - Обработать изменение работы.
//
// Параметры:
//  ДанныеФормыЭлемент     - ДанныеФормыЭлементДерева, ДанныеФормыЭлементКоллекции     - элемент работы в котором произошли изменения.
//  КоллекцияТочекМаршрута - ДанныеФормыКоллекцияЭлементовДерева, ДанныеФормыКоллекция - коллекция всех точек маршрута.
//  КоллекцияРабот         - ДанныеФормыКоллекция                                      - коллекция всех работ,
//                                                                                       задается если тип элемента формы ДанныеФормыЭлементКоллекции, 
//                                                                                       для дерева смысла не имеет.
//  СтараяПроблема         - СправочникСсылка.упПроблемы                               - проблема, которая была установлена в работе до изменения.
//  СтарыйАдресаРазгрузки  - СправочникСсылка.упАдреса                                 - адрес, который был установлен в работе до изменения.
//  сткВозврат             - Структура                                                 - струкутра, для восстановления работы разгрузки в случае 
//                                                                                       если отменили отмену работы, содержит свойства:
//     * ЗаданиеНаПеревозкуГруза - ДокументСсылка.упЗаданиеНаПеревозкуГруза - Задание.
//     * ГрузовоеМесто           - СправочникСсылка.упГрузовыеМеста         - Грузовое место.
//  ПорядковыНомерТекущейТочки - Число                                                 - Порядковый номер редактируемой точки.
//  АдресаПогрузкиПоРейсу  - ТаблицаЗначений                                           - Таблица адресов погрузки, для автоматического подбора адреса разгрузки, содержит колонки:
//     * Адрес         - СправочникСсылка.упАдреса                - Адрес погрузки.
//     * Задание       - ДокументСсылка.упЗаданиеНаПеревозкуГруза - Задание.
//     * ГрузовоеМесто - СправочникСсылка.упГрузовыеМеста         - Грузовое место.
//  СтараяСумма            - Число                                                     - Сумма инкассации для проверки.
//
Процедура ОбработатьИзменениеРаботы(ДанныеФормыЭлемент, 
	                               КоллекцияТочекМаршрута, 
							 КоллекцияРабот = Неопределено, 
							 СтараяПроблема, 
							 СтарыйАдресаРазгрузки, 
							 сткДополнительныеСвойства, 
							 ПорядковыНомерТекущейТочки = Неопределено, 
							 АдресаПогрузкиПоРейсу, 
							 ПорядковыйНомерНовойТочки = Неопределено, 
							 СтараяСумма = 0) Экспорт
	
	Если ДанныеФормыЭлемент <> Неопределено Тогда
		
		ЭтоДерево = ЭтоДерево(ДанныеФормыЭлемент);
		
		ГрузРазделен = Ложь;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеФормыЭлемент, "ГрузРазделен") Тогда
			ГрузРазделен = ДанныеФормыЭлемент.ГрузРазделен;	
		КонецЕсли;
						
		Если ЭтоДерево Тогда
			ЭтоРабота = ДанныеФормыЭлемент.ЭтоРабота;	
			ЭтоПолучениеИлиПередачаДокументов = ДанныеФормыЭлемент.ЭтоПолучениеПередачаДокументов;
		Иначе	
			ЭтоРабота = Истина;	
			ЭтоПолучениеИлиПередачаДокументов = Истина;
		КонецЕсли;

		Если ЭтоРабота ИЛИ ЭтоПолучениеИлиПередачаДокументов Тогда
			
			Если ЭтоДерево Тогда
				ТекущаяОперация = ДанныеФормыЭлемент.ПолучитьРодителя();
				ТипОперации = ТекущаяОперация.НазначениеСтроки;
			Иначе	
				ТипОперации = ДанныеФормыЭлемент.Операция;
			КонецЕсли;
			
		КонецЕсли; 
		
		СтараяПроблемаСправочник = ?(СтараяПроблема = Неопределено, ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка"), СтараяПроблема);
		СтарыйАдресаРазгрузкиСправочник = ?(СтарыйАдресаРазгрузки = Неопределено, ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка"), СтарыйАдресаРазгрузки);								
				
		ИзменилисьДанныеДляОтменыСтроки = Ложь
									Или ДанныеФормыЭлемент.Проблема <> СтараяПроблемаСправочник
									Или ДанныеФормыЭлемент.АдресРазгрузки <> СтарыйАдресаРазгрузкиСправочник
									Или ГрузРазделен;
											
		ОбработатьДанныеПриОтменеРазгрузки = Истина
									  И НЕ ГрузРазделен
									  И (Ложь
									     Или ОперацияРазгрузки(ТипОперации)
					 					Или ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов"));
										
		ЭтоДосрочноеУбытие = Истина
		                     И ДанныеФормыЭлемент.Отменена
						 И ДанныеФормыЭлемент.Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение");
		
		 
			 
		 Если Истина
			 И Не ЭтоДосрочноеУбытие
			 И Не ДанныеФормыЭлемент.ЗапрещеноСозданиеНовойТочкиМаршрута
			 И ИзменилисьДанныеДляОтменыСтроки 
			 И ОбработатьДанныеПриОтменеРазгрузки
			 И ЗначениеЗаполнено(ДанныеФормыЭлемент.Проблема) 
			 И Не ЗначениеЗаполнено(ДанныеФормыЭлемент.АдресРазгрузки) 
			 И Не ЗначениеЗаполнено(СтарыйАдресаРазгрузкиСправочник)
			 И (Ложь
			    Или ЭтоРабота 
			    Или ЭтоПолучениеИлиПередачаДокументов) 
		 Тогда
		 
		 	// Заполнить его адресом погрузки груза.
			 ДанныеФормыЭлемент.АдресРазгрузки = АдресПогрузкиГруза(ДанныеФормыЭлемент, АдресаПогрузкиПоРейсу);
	 
		 КонецЕсли;									
		 
		 Если Не ЭтоДосрочноеУбытие Тогда
			 // Обработать данные по текущей точке.
			 ОбработатьРаботуПоТочке(ДанныеФормыЭлемент, КоллекцияРабот, ЭтоДерево, сткДополнительныеСвойства);
		 КонецЕсли;
		 		 
		 Если ЭтоРабота ИЛИ ЭтоПолучениеИлиПередачаДокументов Тогда
			 
			 // Если изменились данные для отмены строки.
			 Если ИзменилисьДанныеДляОтменыСтроки  Тогда
				 
				 Если ОбработатьДанныеПриОтменеРазгрузки Тогда
					 
					 // Обработать данные по точкам, которые образовались в следствии отмены текущей работы.
					 ОбработатьПроблемуПоТочкеРазгрузки(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, КоллекцияРабот, СтараяПроблемаСправочник, СтарыйАдресаРазгрузкиСправочник, ПорядковыНомерТекущейТочки, ЭтоДерево, ПорядковыйНомерНовойТочки);		
					 
				 ИначеЕсли ОперацияПогрузки(ТипОперации) Тогда
					 
					 // Обработать данные по точкам, которые образовались в следствии отмены текущей работы.
					 ОбработатьПроблемуПоТочкеПогрузки(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, КоллекцияРабот, СтараяПроблемаСправочник, сткДополнительныеСвойства, ПорядковыНомерТекущейТочки, ЭтоДерево, ПорядковыйНомерНовойТочки);	
					 
				 КонецЕсли;
				 
			 КонецЕсли;
			 
			 // Если изменена сумма инкассации
			 Если Истина
				 И ТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей")
				 И НЕ ДанныеФормыЭлемент.Сумма = СтараяСумма 
			 Тогда
				 ИзменитьСуммуВТочкеРазгрузки(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, КоллекцияРабот, ЭтоДерево);
			 КонецЕсли;
			 
		 КонецЕсли;
			 		 
	КонецЕсли;
		
КонецПроцедуры

Процедура ИзменитьСуммуВТочкеРазгрузки(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, КоллекцияРабот = Неопределено, ЭтоДерево = Истина)
	
	Сумма			= ДанныеФормыЭлемент.Сумма;
	Если ЭтоДерево Тогда
		ГрузовоеМесто	= ДанныеФормыЭлемент.НазначениеСтроки;
		
		СтрокаОперации	= ДанныеФормыЭлемент.ПолучитьРодителя();
		СтрокиОперации  = СтрокаОперации.ПолучитьЭлементы();
		СтрокаЗадания	= СтрокаОперации.ПолучитьРодителя();
		СтрокаТочка		= СтрокаЗадания.ПолучитьРодителя();
		ЗаданиеНаПеревозкуГруза = СтрокаЗадания.НазначениеСтроки;
		ИзменитьСуммуВТочкеРазгрузкиДерево(КоллекцияТочекМаршрута, ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, Сумма);
	Иначе
		ГрузовоеМесто	= ДанныеФормыЭлемент.ГрузовоеМесто;
		ЗаданиеНаПеревозкуГруза = ДанныеФормыЭлемент.Задание;	
		ИзменитьСуммуВТочкеРазгрузкиКоллекция(КоллекцияРабот, ГрузовоеМесто, ЗаданиеНаПеревозкуГруза, Сумма);	
	КонецЕсли;
						
КонецПроцедуры

Процедура ИзменитьСуммуВТочкеРазгрузкиДерево(КоллекцияТочекМаршрута, ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, Сумма) 
	Для каждого Точка Из КоллекцияТочекМаршрута Цикл
		СтрокиЗаданийВТочке 			= Точка.ПолучитьЭлементы();
		СтрокаЗаданиеНаПеревозкуГруза	= НайтиЗаданиеВТочке(СтрокиЗаданийВТочке, ЗаданиеНаПеревозкуГруза);
		Если НЕ СтрокаЗаданиеНаПеревозкуГруза = Неопределено Тогда
			СтрокиОперацийПоЗаданию		= СтрокаЗаданиеНаПеревозкуГруза.ПолучитьЭлементы();
			СтрокаОперация				= НайтиОперациюВЗадании(СтрокиОперацийПоЗаданию, ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей"));
			Если НЕ СтрокаОперация = Неопределено Тогда
				сткСвойстваРаботы 		= Новый Структура("НазначениеСтроки, Сумма", ГрузовоеМесто, Сумма);
				СтрокиРаботы			= СтрокаОперация.ПолучитьЭлементы();
				ИзменитьРаботу(СтрокиРаботы, сткСвойстваРаботы);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ИзменитьСуммуВТочкеРазгрузкиКоллекция(КоллекцияРабот, ГрузовоеМесто, ЗаданиеНаПеревозкуГруза, Сумма)
	мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("ГрузовоеМесто, Задание, Операция", ГрузовоеМесто, ЗаданиеНаПеревозкуГруза, ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей")));
	Для каждого СтрокаРаботы Из мсвСтрокиРабот Цикл
		СтрокаРаботы.Сумма = Сумма;
	КонецЦикла;
КонецПроцедуры

Функция ЭтоДерево(ДанныеФормыЭлемент)
	
	#Если Клиент Тогда
		Возврат НЕ ТипЗнч(ДанныеФормыЭлемент) = Тип("ДанныеФормыЭлементКоллекции");
	#ИначеЕсли Сервер Тогда 	
		Возврат НЕ (ТипЗнч(ДанныеФормыЭлемент) = Тип("ДанныеФормыЭлементКоллекции") 
					ИЛИ  ТипЗнч(ДанныеФормыЭлемент) = Тип("ОбработкаТабличнаяЧастьСтрока.упКорректировкаРейса.Работы"));
	#КонецЕсли
КонецФункции

Функция АдресПогрузкиГруза(ДанныеФормыЭлемент, АдресаПогрузкиПоРейсу)
	
	текРезультат = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
	
	Если ЭтоДерево(ДанныеФормыЭлемент) Тогда
		ЭлементОперация 		= ДанныеФормыЭлемент.ПолучитьРодителя();
		ЗаданиеНаПеревозкуГруза = ЭлементОперация.ПолучитьРодителя().НазначениеСтроки;
		ГрузовоеМесто			= ДанныеФормыЭлемент.НазначениеСтроки;
	Иначе	
		ЗаданиеНаПеревозкуГруза = ДанныеФормыЭлемент.Задание;
		ГрузовоеМесто			= ДанныеФормыЭлемент.ГрузовоеМесто;
	КонецЕсли;
	
	сткОтбора = Новый Структура("Задание, ГрузовоеМесто", ЗаданиеНаПеревозкуГруза, ГрузовоеМесто);
	мсвСтроки = АдресаПогрузкиПоРейсу.НайтиСтроки(сткОтбора);
	Если мсвСтроки.Количество() > 0 Тогда
		текРезультат = мсвСтроки[0].Адрес;
	КонецЕсли;
	
	Возврат текРезультат
	
КонецФункции

// Процедура - Отменяет точку в коллекции, выполняются все необходимые действия при отмене точки маршрута.
//
// Параметры:
//  КоллекцияТочекМаршрута - ДанныеФормыКоллекцияЭлементовДерева, ДанныеФормыКоллекция - коллекция всех точек маршрута.
//  ЭлементТочкаМаршрута - Элемент - Элемент точки маршрута.
//  Проблема - Строка - Описание проблемы.
//  АдресаПогрузкиПоРейсу - ТаблицаЗначений - Таблица адресов погрузки, для автоматического подбора адреса разгрузки, с колонками:
//     * Адрес         - СправочникСсылка.упАдреса                - Адрес погрузки.
//     * Задание       - ДокументСсылка.упЗаданиеНаПеревозкуГруза - Задание на перевозку груза.
//     * ГрузовоеМесто - СправочникСсылка.упГрузовыеМеста         - Грузовое место.
//  ПорядковыйНомерТекущейТочки - Число - Порядковый номер редактируемой точки.
//
Процедура ОтменитьТочку(КоллекцияТочекМаршрута, ЭлементТочкаМаршрута, Проблема, АдресаПогрузкиПоРейсу, ПорядковыйНомерТекущейТочки, сткДополнительныеСвойства = Неопределено) Экспорт
	
	Если Проблема <> ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение") Тогда
		ЭлементТочкаМаршрута.НачалоРаботФакт = '0001.01.01';
		ЭлементТочкаМаршрута.УбытиеФакт = '0001.01.01';
	Иначе
		ЭлементТочкаМаршрута.ДосрочноеУбытие = Истина;
	КонецЕсли;
		
	Для каждого Задание Из ЭлементТочкаМаршрута.ПолучитьЭлементы() Цикл
		Для каждого Операция Из Задание.ПолучитьЭлементы() Цикл
			Для каждого Назначение Из Операция.ПолучитьЭлементы() Цикл
				Если НЕ ЗначениеЗаполнено(Назначение.Проблема) Тогда
					
					СтараяПроблема = ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
					СтарыйАдресРазгрузки = Назначение.АдресРазгрузки;
					Если сткДополнительныеСвойства = Неопределено Тогда
						сткДополнительныеСвойства = Новый Структура();
					КонецЕсли;
										
					Если Проблема <> ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение") Тогда
										
						Назначение.Проблема = Проблема;
						
						Если АдресаПогрузкиПоРейсу <> Неопределено Тогда
							Если ОперацияРазгрузки(Операция.НазначениеСтроки) Тогда
								
								мсвАдреса = АдресаПогрузкиПоРейсу.НайтиСтроки(Новый Структура("Задание, ГрузовоеМесто",Задание.НазначениеСтроки, Назначение.НазначениеСтроки));
								Если мсвАдреса.Количество() > 0 Тогда
									Назначение.АдресРазгрузки = мсвАдреса[0].Адрес;								
								КонецЕсли;
								
							ИначеЕсли Операция.НазначениеСтроки = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда
								
								мсвАдреса = АдресаПогрузкиПоРейсу.НайтиСтроки(Новый Структура("Задание",Задание.НазначениеСтроки));
								Если мсвАдреса.Количество() > 0 Тогда
									Назначение.АдресРазгрузки = мсвАдреса[0].Адрес;								
								КонецЕсли;
								
							КонецЕсли; 
						КонецЕсли;
					Иначе
						
						Если Не Назначение.Выполнена Тогда
							Если ОперацияРазгрузки(Операция.НазначениеСтроки)  Тогда
								Назначение.АдресРазгрузки = ЭлементТочкаМаршрута.АдресТочки;	
							КонецЕсли;
							Назначение.Проблема = Проблема;
						КонецЕсли;
						
					КонецЕсли;
										
					ОбработатьИзменениеРаботы(Назначение, КоллекцияТочекМаршрута, , СтараяПроблема, СтарыйАдресРазгрузки, сткДополнительныеСвойства, ПорядковыйНомерТекущейТочки, АдресаПогрузкиПоРейсу); 
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Параметры:
//	ДанныеФормыЭлемент - ДанныеФормыЭлемент;
//  КоллекцияТочекМаршрута - ДанныеФормыЭлементДерева;
//
Процедура ОбработатьПроблемуПоТочкеРазгрузки(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, КоллекцияРабот = Неопределено, СтараяПроблема, СтарыйАдресаРазгрузки, ПорядковыНомерТекущейТочки = Неопределено, ЭтоДерево = Истина, ПорядковыйНомерНовойТочки = Неопределено)
			
	Если ЭтоДерево Тогда
		АдресРазгрузки	= ДанныеФормыЭлемент.АдресРазгрузки;
		Проблема = ДанныеФормыЭлемент.Проблема;
		Количество = ДанныеФормыЭлемент.Количество;
		Сумма = ДанныеФормыЭлемент.Сумма;
		ГрузовоеМесто = ДанныеФормыЭлемент.НазначениеСтроки;
		Тара = ДанныеФормыЭлемент.Тара;
		СтрокаОперации	= ДанныеФормыЭлемент.ПолучитьРодителя();
		СтрокиОперации = СтрокаОперации.ПолучитьЭлементы();
		СтрокаЗадания = СтрокаОперации.ПолучитьРодителя();
		СтрокаТочка = СтрокаЗадания.ПолучитьРодителя();
		Если ПорядковыНомерТекущейТочки = Неопределено Тогда
			ПорядковыНомерТекущейТочки = КоллекцияТочекМаршрута.Индекс(СтрокаТочка) + 1;	
		КонецЕсли;
		КоличествоЭкземпляровДокумента = ДанныеФормыЭлемент.КоличествоЭкземпляровДокумента;
		ТипРегламентногоДокумента = ДанныеФормыЭлемент.ТипРегламентногоДокумента;

		Операция = СтрокаОперации.НазначениеСтроки;
		ЗаданиеНаПеревозкуГруза = СтрокаЗадания.НазначениеСтроки;
		Рейс = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеФормыЭлемент, "Рейс", ПредопределенноеЗначение("Документ.упРейс.ПустаяСсылка"));
		
	Иначе
		АдресРазгрузки	= ДанныеФормыЭлемент.АдресРазгрузки;
		Проблема = ДанныеФормыЭлемент.Проблема;
		Количество = ДанныеФормыЭлемент.КоличествоГрузов;
		Сумма = ДанныеФормыЭлемент.Сумма;
		ГрузовоеМесто = ДанныеФормыЭлемент.ГрузовоеМесто;
		Тара = ДанныеФормыЭлемент.Тара;
		Операция = ДанныеФормыЭлемент.Операция;
		ЗаданиеНаПеревозкуГруза = ДанныеФормыЭлемент.Задание;	
		
		КоличествоЭкземпляровДокумента = ДанныеФормыЭлемент.КоличествоЭкземпляровДокумента;
		ТипРегламентногоДокумента = ДанныеФормыЭлемент.ТипРегламентногоДокумента;
		
		мсвТочки = КоллекцияТочекМаршрута.НайтиСтроки(Новый Структура("Идентификатор", ДанныеФормыЭлемент.Идентификатор));
		Если мсвТочки.Количество() > 0 Тогда
			СтрокаТочка = мсвТочки[0];
		КонецЕсли;
		
		Если ПорядковыНомерТекущейТочки = Неопределено Тогда
			ПорядковыНомерТекущейТочки = СтрокаТочка.СтрокаНомер;		
		КонецЕсли;
		
		Рейс = ДанныеФормыЭлемент.Рейс;
						
	КонецЕсли;
		
	СтрокиТочки = КоллекцияТочекМаршрута;
	Отказ = Ложь;
	
	// Изменение работ и маршрута, связанных с добавляемой точкой при отмене разгрузки.
	// Признак запрета создания новой точки хранится в строках работ, заполняется
	// по умолчанию из соответствующего реквизита проблемы.
	Если Истина
		И НЕ ЗначениеЗаполнено(СтарыйАдресаРазгрузки) 
		И НЕ ЗначениеЗаполнено(СтараяПроблема) Тогда
		
		Если ЗначениеЗаполнено(АдресРазгрузки) Тогда
			
			// Найти(добавить) точку с новым адресом.
			ТочкаРазгрузки = ДобавитьНовуюТочкуРазгрузкиПоРейсу(СтрокиТочки, АдресРазгрузки, СтрокаТочка, Отказ, ПорядковыНомерТекущейТочки, ЭтоДерево, Проблема, Рейс);
			ПорядковыйНомерНовойТочки = СтрокиТочки.Индекс(ТочкаРазгрузки) + 1;
			
			// Добавить работу на новый адрес.
			ОбработатьНовуюТочкуРазгрузки(ТочкаРазгрузки, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, Проблема, Количество, ЭтоДерево, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);
			
		КонецЕсли;
		
	ИначеЕсли Истина
			И НЕ ЗначениеЗаполнено(Проблема)
			И НЕ ЗначениеЗаполнено(АдресРазгрузки) Тогда								
			
		Если ЗначениеЗаполнено(АдресРазгрузки) Тогда
			
			// Удалить работу со старого адреса.
			ОбработатьСтаруюТочкуРазгрузки(СтрокиТочки, СтрокаТочка, КоллекцияРабот, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, ЭтоДерево);	
			
		КонецЕсли;	
					
	ИначеЕсли Истина
			И СтараяПроблема = Проблема
			И НЕ СтарыйАдресаРазгрузки = АдресРазгрузки Тогда		
			
		Если ЗначениеЗаполнено(АдресРазгрузки) Тогда	
			// Найти(добавить) точку с новым адресом.
			ТочкаРазгрузки = ДобавитьНовуюТочкуРазгрузкиПоРейсу(СтрокиТочки, АдресРазгрузки, СтрокаТочка, Отказ, ПорядковыНомерТекущейТочки, ЭтоДерево, Проблема, Рейс);
			ПорядковыйНомерНовойТочки = СтрокиТочки.Индекс(ТочкаРазгрузки) + 1;
			
			// Добавить работу на новый адрес.
			ОбработатьНовуюТочкуРазгрузки(ТочкаРазгрузки, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, Проблема, Количество, ЭтоДерево, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтарыйАдресаРазгрузки) Тогда
			// Удалить работу со старого адреса.
			ОбработатьСтаруюТочкуРазгрузки(СтрокиТочки, СтрокаТочка, КоллекцияРабот, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, ЭтоДерево);	
		КонецЕсли;	
			
	ИначеЕсли Истина
			И НЕ СтараяПроблема = Проблема
			И СтарыйАдресаРазгрузки = АдресРазгрузки Тогда				
			
		Если ЗначениеЗаполнено(АдресРазгрузки) Тогда		
			
			// Найти(добавить) точку с новым адресом.
			ТочкаРазгрузки = ДобавитьНовуюТочкуРазгрузкиПоРейсу(СтрокиТочки, АдресРазгрузки, СтрокаТочка, Отказ, ПорядковыНомерТекущейТочки, ЭтоДерево, Проблема, Рейс);
			ПорядковыйНомерНовойТочки = СтрокиТочки.Индекс(ТочкаРазгрузки) + 1;
				
			// Заменить проблему в работе.
			ОбработатьНовуюТочкуРазгрузки(ТочкаРазгрузки, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, Проблема, Количество, ЭтоДерево, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);
			
		КонецЕсли;	
			
	ИначеЕсли Истина
			И НЕ СтараяПроблема = Проблема
			И НЕ СтарыйАдресаРазгрузки = АдресРазгрузки Тогда						
			
		Если ЗначениеЗаполнено(АдресРазгрузки) Тогда			
			// Найти(добавить) точку с новым адресом.
			ТочкаРазгрузки = ДобавитьНовуюТочкуРазгрузкиПоРейсу(СтрокиТочки, АдресРазгрузки, СтрокаТочка, Отказ, ПорядковыНомерТекущейТочки, ЭтоДерево, Проблема, Рейс);
			ПорядковыйНомерНовойТочки = СтрокиТочки.Индекс(ТочкаРазгрузки) + 1;
				
			// Добавить работу на новый адрес.
			ОбработатьНовуюТочкуРазгрузки(ТочкаРазгрузки, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, Проблема, Количество, ЭтоДерево, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтарыйАдресаРазгрузки) Тогда
			// Удалить работу со старого адреса.
			ОбработатьСтаруюТочкуРазгрузки(СтрокиТочки, СтрокаТочка, КоллекцияРабот, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, ЭтоДерево);	
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ЭтоДерево Тогда
		УстановитьОтметкуОтменыУРодителейРаботы(ДанныеФормыЭлемент);	
	КонецЕсли;
						
КонецПроцедуры

Функция ДобавитьНовуюТочкуРазгрузкиПоРейсу(СтрокиТочки, АдресРазгрузки, СтрокаТочка, Отказ, ПорядковыНомерТекущейТочки, ЭтоДерево, Проблема, Рейс = Неопределено)	
	
	Если Рейс = Неопределено Тогда
		Рейс = ПредопределенноеЗначение("Документ.упРейс.ПустаяСсылка");	
	КонецЕсли;
		
	ТочкаРазгрузки = Неопределено;
	
	Если Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение") Тогда
		
		ТочкаРазгрузки = ДобавитьТочкуДляРаботИзТочкиСДосрочнымУбытием(СтрокаТочка, СтрокиТочки, ЭтоДерево);
	
	Иначе
		мсвПредшественников = Новый Массив;
		мсвПредшественников.Добавить(СтрокаТочка.Предшественники);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТочка, "Идентификатор") Тогда
			мсвПредшественников.Добавить(Строка(СтрокаТочка.Идентификатор));
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТочка, "ИдентификаторТочки") Тогда	
			мсвПредшественников.Добавить(Строка(СтрокаТочка.ИдентификаторТочки));	
		КонецЕсли;
		
		ПредшественникиТочкиРазгрузки = СтрСоединить(мсвПредшественников, ",");
		
		// Найти(добавить) точку с новым адресом.
		Если ЭтоДерево Тогда
			ТочкаРазгрузки = ДобавитьТочкуРазгрузкиПоРейсу(СтрокиТочки,Рейс ,АдресРазгрузки, СтрокаТочка.ПрибытиеПлан, СтрокаТочка.НачалоРаботПлан, СтрокаТочка.УбытиеПлан, Отказ, ,ПорядковыНомерТекущейТочки, ПредшественникиТочкиРазгрузки);	
		Иначе	
			ТочкаРазгрузки = ДобавитьТочкуРазгрузкиПоРейсу(СтрокиТочки,Рейс ,АдресРазгрузки, СтрокаТочка.ПлановоеПрибытие, СтрокаТочка.ПлановоеПрибытие, СтрокаТочка.ПлановоеУбытие, Отказ, ,ПорядковыНомерТекущейТочки, ПредшественникиТочкиРазгрузки);	
		КонецЕсли;
	КонецЕсли;
		
	Возврат ТочкаРазгрузки;
	
КонецФункции

Процедура УстановитьОтметкуОтменыУРодителейРаботы(СтрокаРаботы)
	
	ЕстьПолеИндексКартинкиВыполнено = СтрокаРаботы.Свойство("ИндексКартинкиВыполнено");
	
	Если ЕстьПолеИндексКартинкиВыполнено Тогда
		Если СтрокаРаботы.Отменена Тогда
			СтрокаРаботы.ИндексКартинкиВыполнено = 3;
		Иначе
			СтрокаРаботы.ИндексКартинкиВыполнено = СтрокаРаботы.Выполнена;	
		КонецЕсли;
	КонецЕсли;

	ВсеРаботыОтменены 	= Истина;
	
	СтрокаОперации = СтрокаРаботы.ПолучитьРодителя();
	СтрокаЗадания = СтрокаОперации.ПолучитьРодителя();
	СтрокаТочка = СтрокаЗадания.ПолучитьРодителя();
	
	СтрокиРаботы = СтрокаОперации.ПолучитьЭлементы();
	ЕстьДосрочноеПрохождение = Ложь;
	
	Для каждого ТекСтрокаРабота Из СтрокиРаботы Цикл
		Если НЕ ТекСтрокаРабота.Отменена Тогда
			ВсеРаботыОтменены = Ложь;
			Прервать;
		КонецЕсли;
		ЕстьДосрочноеПрохождение = Ложь
                           Или ЕстьДосрочноеПрохождение 
					  Или ТекСтрокаРабота.Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение")
	КонецЦикла;
	
	СтрокаОперации.Отменена = ВсеРаботыОтменены;
	Если ЕстьПолеИндексКартинкиВыполнено Тогда
		Если СтрокаОперации.Отменена Тогда
			СтрокаОперации.ИндексКартинкиВыполнено = 3;
		Иначе
			СтрокаОперации.ИндексКартинкиВыполнено = СтрокаОперации.Выполнена;	
		КонецЕсли;
	КонецЕсли;
	
	ВсеОперацииОтменены = ВсеРаботыОтменены;
	
	Если ВсеРаботыОтменены Тогда
		Для каждого ТекСтрокаОперация Из СтрокаЗадания.ПолучитьЭлементы() Цикл
			Если НЕ ТекСтрокаОперация.Отменена Тогда
				ВсеОперацииОтменены = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
	СтрокаЗадания.Отменена = ВсеОперацииОтменены;
	Если ЕстьПолеИндексКартинкиВыполнено Тогда
		Если СтрокаЗадания.Отменена Тогда
			СтрокаЗадания.ИндексКартинкиВыполнено = 3;
		Иначе
			СтрокаЗадания.ИндексКартинкиВыполнено = СтрокаЗадания.Выполнена;	
		КонецЕсли;
	КонецЕсли;

	
	ВсеЗаданияОтменены = ВсеОперацииОтменены;
	
	Если ВсеОперацииОтменены Тогда
		Для каждого ТекСтрокаЗадание Из СтрокаТочка.ПолучитьЭлементы() Цикл
			Если НЕ текСтрокаЗадание.Отменена Тогда
				ВсеЗаданияОтменены = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	СтрокаТочка.Отменена = Истина
	                       И ВсеЗаданияОтменены
					   И Не ЕстьДосрочноеПрохождение;
	Если Истина
		И ВсеЗаданияОтменены
		И ЕстьДосрочноеПрохождение
	Тогда
		СтрокаТочка.Выполнена = Истина; 
	КонецЕсли;				   
	
	СтрокаТочка.ДосрочноеУбытие = ЕстьДосрочноеПрохождение;
					   
					   
	Если ЕстьПолеИндексКартинкиВыполнено Тогда
		Если ВсеЗаданияОтменены Тогда
			Если ЕстьДосрочноеПрохождение Тогда
				СтрокаТочка.ИндексКартинкиВыполнено = СтрокаТочка.Выполнена;	
			Иначе	
				СтрокаТочка.ИндексКартинкиВыполнено = 3;
			КонецЕсли;
		Иначе
			СтрокаТочка.ИндексКартинкиВыполнено = СтрокаТочка.Выполнена;	
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры

Процедура ОбработатьСтаруюТочкуРазгрузки(КоллекцияТочекМаршрута, СтрокаТочка, КоллекцияРабот, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, ЭтоДерево)
	
	Если ЭтоДерево Тогда
		ОбработатьСтаруюТочкуРазгрузкиДерево(КоллекцияТочекМаршрута, СтрокаТочка, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто);
	Иначе
		ОбработатьСтаруюТочкуРазгрузкиКоллекция(КоллекцияТочекМаршрута, СтрокаТочка, КоллекцияРабот, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьСтаруюТочкуРазгрузкиДерево(КоллекцияТочекМаршрута, СтрокаТочка, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто)
	
	ТочкаРазгрузки = НайтиТочкуПоАдресуДерево(КоллекцияТочекМаршрута,СтарыйАдресаРазгрузки, СтрокаТочка.ПорядокТочки); 
	
	СтрокиЗаданийВТочке = Неопределено;
	СтрокаЗаданиеНаПеревозкуГруза	= Неопределено;
	
	СтрокиОперацийПоЗаданию = Неопределено;
	СтрокаОперация = Неопределено;
	
	СтрокиРаботы = Неопределено;
	
	// Удалить работу со старого адреса.
	Если НЕ ТочкаРазгрузки = Неопределено Тогда
		
		СтрокиЗаданийВТочке = ТочкаРазгрузки.ПолучитьЭлементы();
		СтрокаЗаданиеНаПеревозкуГруза	= НайтиЗаданиеВТочке(СтрокиЗаданийВТочке, ЗаданиеНаПеревозкуГруза);
		Если НЕ СтрокаЗаданиеНаПеревозкуГруза = Неопределено Тогда
			СтрокиОперацийПоЗаданию = СтрокаЗаданиеНаПеревозкуГруза.ПолучитьЭлементы();
			СтрокаОперация = НайтиОперациюВЗадании(СтрокиОперацийПоЗаданию, Операция);
			Если НЕ СтрокаОперация = Неопределено Тогда
				сткСвойстваРаботы = Новый Структура("НазначениеСтроки", ГрузовоеМесто);
				СтрокиРаботы = СтрокаОперация.ПолучитьЭлементы();
				УдалитьРаботу(СтрокиРаботы, сткСвойстваРаботы);
			КонецЕсли;
		КонецЕсли;
		
		
		// Проверить есть ли еще основные работы в точке по старому адресу.
		Если НЕ СтрокиРаботы = Неопределено И СтрокиРаботы.Количество() = 0 Тогда
			
			// Из задания в точке удаляется операция разгрузки.
			УдалитьЭлементКоллекции(СтрокиОперацийПоЗаданию, СтрокаОперация);
			
			// Поиск операции погрузки в задании.
			СтрокаОперацииПогрузки = НайтиОперациюВЗадании(СтрокиОперацийПоЗаданию, ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка"));
			
			// Поиск операции выемки ценностей в задании
			СтрокаОперацииВыемкаЦенностей = НайтиОперациюВЗадании(СтрокиОперацийПоЗаданию, ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей"));
			
			// Если погрузка в задании не найдена.
			Если СтрокаОперацииПогрузки = Неопределено 
					И СтрокаОперацииВыемкаЦенностей = Неопределено Тогда
				
				// удалить задание из точки.
				УдалитьЭлементКоллекции(СтрокиЗаданийВТочке, СтрокаЗаданиеНаПеревозкуГруза);
				
				Если СтрокиЗаданийВТочке.Количество() = 0 И НЕ ЗначениеЗаполнено(ТочкаРазгрузки.Гараж) Тогда
					УдалитьЭлементКоллекции(КоллекцияТочекМаршрута, ТочкаРазгрузки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьСтаруюТочкуРазгрузкиКоллекция(КоллекцияТочекМаршрута, СтрокаТочка, КоллекцияРабот, СтарыйАдресаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто)
	
	ТочкаРазгрузки = НайтиТочкуПоАдресуКоллекция(КоллекцияТочекМаршрута, СтарыйАдресаРазгрузки, СтрокаТочка.СтрокаНомер); 
	
	// Удалить работу со старого адреса.
	Если НЕ ТочкаРазгрузки = Неопределено Тогда
		
		мсвСтрокиРаботПоАдресу = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор, Задание, Операция, ГрузовоеМесто", ТочкаРазгрузки.Идентификатор, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто));
		Для каждого СтрокаРабота  Из мсвСтрокиРаботПоАдресу Цикл
			КоллекцияРабот.Удалить(СтрокаРабота);	
		КонецЦикла;
		
		мсвСтрокиРаботПоАдресу = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор", ТочкаРазгрузки.Идентификатор));
		
		КоличествоРаботВТочке 	= мсвСтрокиРаботПоАдресу.Количество();
		КоличествоОсновныхРабот = 0;
		
		Для каждого СтрокаРабота Из мсвСтрокиРаботПоАдресу Цикл
			Если ОсновнаяОперация(СтрокаРабота.Операция) Тогда
				КоличествоОсновныхРабот = КоличествоОсновныхРабот + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоОсновныхРабот = 0 Тогда
			Для каждого СтрокаРабота  Из мсвСтрокиРаботПоАдресу Цикл
				КоллекцияРабот.Удалить(СтрокаРабота);	
			КонецЦикла;
			
			КоллекцияТочекМаршрута.Удалить(ТочкаРазгрузки);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьНовуюТочкуРазгрузки(ТочкаРазгрузки, КоллекцияРабот = Неопределено, ЗаданиеНаПеревозкуГруза, Операция, НазначениеСтроки, Проблема, Количество, ЭтоДерево = Истина, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара = Неопределено, Сумма, Рейс);
	
	Если ЭтоДерево Тогда
		ОбработатьНовуюТочкуРазгрузкиДерево(ТочкаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, НазначениеСтроки, Проблема, Количество, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Сумма, Рейс);
	Иначе	
		ОбработатьНовуюТочкуРазгрузкиКоллекция(ТочкаРазгрузки, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, НазначениеСтроки, Проблема, Количество, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьНовуюТочкуРазгрузкиДерево(ТочкаРазгрузки, ЗаданиеНаПеревозкуГруза, Операция, НазначениеСтроки, Проблема, Количество, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Сумма, Рейс)
	
	КоллекцияЗаданийТочки = ТочкаРазгрузки.ПолучитьЭлементы();
	
	СтрокиОперацийПоЗаданию = Неопределено;
	СтрокаОперация = Неопределено;
	
	СтрокиРаботы = Неопределено;
	
	СтрокаЗаданиеНаПеревозкуГруза	= НайтиЗаданиеВТочке(КоллекцияЗаданийТочки, ЗаданиеНаПеревозкуГруза);
	Если СтрокаЗаданиеНаПеревозкуГруза = Неопределено Тогда
		СтрокаЗаданиеНаПеревозкуГруза = КоллекцияЗаданийТочки.Добавить();
		СтрокаЗаданиеНаПеревозкуГруза.НазначениеСтроки = ЗаданиеНаПеревозкуГруза;
		СтрокаЗаданиеНаПеревозкуГруза.ИндексКартинки = -1;
	КонецЕсли;	
	
	СтрокиОперацийПоЗаданию = СтрокаЗаданиеНаПеревозкуГруза.ПолучитьЭлементы();
	СтрокаОперация = НайтиОперациюВЗадании(СтрокиОперацийПоЗаданию, Операция);
	Если СтрокаОперация = Неопределено Тогда
		СтрокаОперация = СтрокиОперацийПоЗаданию.Добавить();
		СтрокаОперация.НазначениеСтроки = Операция;
		Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка") Тогда
			СтрокаОперация.ИндексКартинки	= 6;
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка") Тогда
			СтрокаОперация.ИндексКартинки	= 5;
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей") Тогда
			СтрокаОперация.ИндексКартинки	= 11;
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") Тогда
			СтрокаОперация.ИндексКартинки	= 10;
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда
			СтрокаОперация.ИндексКартинки	= 7;
		КонецЕсли;
	КонецЕсли;
	
	сткСвойстваРаботы = Новый Структура();
	сткСвойстваРаботы.Вставить("Проблема", ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка"));
	сткСвойстваРаботы.Вставить("АдресРазгрузки", ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка"));
	сткСвойстваРаботы.Вставить("ЭтоАдрес", Ложь);
	сткСвойстваРаботы.Вставить("ЭтоРабота", Истина);
	сткСвойстваРаботы.Вставить("Выполнена", Ложь);
	сткСвойстваРаботы.Вставить("Количество", Количество);
	сткСвойстваРаботы.Вставить("Сумма", Сумма);
	сткСвойстваРаботы.Вставить("ИндексКартинки", -1);
	сткСвойстваРаботы.Вставить("Операция", СтрокаОперация.НазначениеСтроки);
	сткСвойстваРаботы.Вставить("НазначениеСтроки", НазначениеСтроки);
	сткСвойстваРаботы.Вставить("ПроблемаПричинаВозникновенияЭтойРаботы", Проблема);
	сткСвойстваРаботы.Вставить("ТипРегламентногоДокумента", ТипРегламентногоДокумента);
	сткСвойстваРаботы.Вставить("КоличествоЭкземпляровДокумента", КоличествоЭкземпляровДокумента);
	сткСвойстваРаботы.Вставить("Рейс", Рейс);
		
	СтрокиРаботы = СтрокаОперация.ПолучитьЭлементы();
	ИзменитьРаботу(СтрокиРаботы, сткСвойстваРаботы);
	
КонецПроцедуры

Процедура ОбработатьНовуюТочкуРазгрузкиКоллекция(ТочкаРазгрузки, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, НазначениеСтроки, Проблема, Количество, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс)
	
	сткОтбора = Новый Структура();
	сткОтбора.Вставить("Идентификатор", ТочкаРазгрузки.Идентификатор);
	мсвРаботыПоТочке = КоллекцияРабот.НайтиСтроки(сткОтбора);
	
	сткОтбора = Новый Структура();
	сткОтбора.Вставить("Идентификатор", ТочкаРазгрузки.Идентификатор);
	сткОтбора.Вставить("Задание", ЗаданиеНаПеревозкуГруза);
	сткОтбора.Вставить("Операция", Операция);
	Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда
		сткОтбора.Вставить("ТипРегламентногоДокумента", ТипРегламентногоДокумента);
	Иначе
		сткОтбора.Вставить("ГрузовоеМесто", НазначениеСтроки);
	КонецЕсли; 
		
	мсвСтрокиРаботы = КоллекцияРабот.НайтиСтроки(сткОтбора);
	
	Если мсвСтрокиРаботы.Количество() = 0 Тогда
		СтрокаРабота = КоллекцияРабот.Добавить();
		СтрокаРабота.Идентификатор = ТочкаРазгрузки.Идентификатор;
		СтрокаРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		СтрокаРабота.СтрокаНомер = мсвРаботыПоТочке.Количество() + 1;
		СтрокаРабота.КоличествоГрузов = Количество;
		СтрокаРабота.КоличествоЭкземпляровДокумента = КоличествоЭкземпляровДокумента;
		СтрокаРабота.Сумма = Сумма;
	Иначе
		СтрокаРабота = мсвСтрокиРаботы[0];
		СтрокаРабота.КоличествоГрузов = СтрокаРабота.КоличествоГрузов + Количество;
		СтрокаРабота.КоличествоЭкземпляровДокумента = СтрокаРабота.КоличествоЭкземпляровДокумента + КоличествоЭкземпляровДокумента;
		СтрокаРабота.Сумма = СтрокаРабота.Сумма + Сумма;
	КонецЕсли;
	
	СтрокаРабота.Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
	СтрокаРабота.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
	СтрокаРабота.Выполнена = Ложь;
	СтрокаРабота.Операция = Операция;
	СтрокаРабота.Задание = ЗаданиеНаПеревозкуГруза;
	СтрокаРабота.ГрузовоеМесто = НазначениеСтроки;
	СтрокаРабота.Тара = Тара;
	СтрокаРабота.ТипОперации	= ИндексКартинкиОперации(Операция);
	СтрокаРабота.ПроблемаПричинаВозникновенияЭтойРаботы = Проблема;
	СтрокаРабота.СтрокаДокумента = Истина;
	СтрокаРабота.ТипРегламентногоДокумента = ТипРегламентногоДокумента;
	СтрокаРабота.Рейс = Рейс;
		
КонецПроцедуры

Процедура ОбработатьПроблемуПоТочкеПогрузки(ЭлементДереваРабота, КоллекцияТочекМаршрута, КоллекцияРабот = Неопределено, ЗначениеПроблемыДоИзменения, сткДополнительныеСвойства, ПорядковыНомерТекущейТочки = Неопределено, ЭтоДерево = Истина, ПорядковыйНомерНовойТочки = Неопределено)
	
	Если ЭтоДерево Тогда
		ОбработатьПроблемуПоТочкеПогрузкиДерево(ЭлементДереваРабота, КоллекцияТочекМаршрута, КоллекцияРабот, ЗначениеПроблемыДоИзменения, сткДополнительныеСвойства, ПорядковыНомерТекущейТочки, ПорядковыйНомерНовойТочки);
	Иначе
		ОбработатьПроблемуПоТочкеПогрузкиКоллекция(ЭлементДереваРабота, КоллекцияТочекМаршрута, КоллекцияРабот, ЗначениеПроблемыДоИзменения, сткДополнительныеСвойства, ПорядковыНомерТекущейТочки, ПорядковыйНомерНовойТочки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьПроблемуПоТочкеПогрузкиДерево(ЭлементДереваРабота, КоллекцияТочекМаршрута, КоллекцияРабот = Неопределено, ЗначениеПроблемыДоИзменения, сткДополнительныеСвойства, ПорядковыНомерТекущейТочки = Неопределено, ПорядковыйНомерНовойТочки = Неопределено)
	
	Если ЭлементДереваРабота.Проблема <> ЗначениеПроблемыДоИзменения Тогда
		
		Проблема = ЭлементДереваРабота.Проблема;
		ГрузовоеМесто = ЭлементДереваРабота.НазначениеСтроки;
		ТипГруза = ЭлементДереваРабота.ТипГруза;
		
		СтрокаОперации	= ЭлементДереваРабота.ПолучитьРодителя();
		Операция = СтрокаОперации.НазначениеСтроки;
		СтрокаЗадания = СтрокаОперации.ПолучитьРодителя();
		ЗаданиеНаПеревозкуГруза = СтрокаЗадания.НазначениеСтроки;
		СтрокаТочка = СтрокаЗадания.ПолучитьРодителя();
			
		мсвЗависимыеТипыГрузов = упЗаданиеНаПеревозкуВызовСервера.ЗависимыеТипыГрузовПоЗаданию(ЗаданиеНаПеревозкуГруза, ТипГруза);
				
		// Если отменяется работа погрузки.
		Если ЗначениеЗаполнено(Проблема) Тогда
					
			ЭтоДосрочноеПрохождение = Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение");
			Если ЭтоДосрочноеПрохождение Тогда
				
				// Прохождение точки и выполнение работ откладываются на как можно поздний срок.
				ТочкаНовая = ДобавитьТочкуДляРаботИзТочкиСДосрочнымУбытием(СтрокаТочка, КоллекцияТочекМаршрута, Истина); 
				ПорядковыйНомерНовойТочки = КоллекцияТочекМаршрута.Индекс(ТочкаНовая) + 1;
				Количество = ЭлементДереваРабота.Количество;
				Сумма = ЭлементДереваРабота.Сумма; 
				КоличествоЭкземпляровДокумента = ЭлементДереваРабота.КоличествоЭкземпляровДокумента;
				ТипРегламентногоДокумента = ЭлементДереваРабота.ТипРегламентногоДокумента;
			     Тара = ЭлементДереваРабота.Тара;
				Рейс = ЭлементДереваРабота.Рейс;
				ОбработатьНовуюТочкуРазгрузки(ТочкаНовая, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, Проблема, Количество, Истина, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);
								
			Иначе	
			
				// Обходятся все точки маршрута.
				Для каждого СтрокаТочка Из КоллекцияТочекМаршрута Цикл
					
					Если СтрокаТочка.ТочкаПройдена Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокиЗаданийВТочке = СтрокаТочка.ПолучитьЭлементы();
					СтрокаЗаданиеНаПеревозкуГруза = НайтиЗаданиеВТочке(СтрокиЗаданийВТочке, ЗаданиеНаПеревозкуГруза);
					СтрокиРаботы = Неопределено;
					СтрокиОперацийПоЗаданию = Неопределено;
					ЕстьСтрокиРазгрузки = Ложь;
									
					// Если в точке есть работы с заданием на перевозку, которое было в погрузке.
					Если СтрокаЗаданиеНаПеревозкуГруза <> Неопределено Тогда
						
						СтрокиОперацийПоЗаданию = СтрокаЗаданиеНаПеревозкуГруза.ПолучитьЭлементы();
											
						// Проверить и удалить все обратные операции в этой точке.
						СтрокаОперация = НайтиОперациюВЗадании(СтрокиОперацийПоЗаданию, ОбратнаяОперация(Операция));
						Если СтрокаОперация <> Неопределено Тогда
							сткСвойстваРаботы = Новый Структура("НазначениеСтроки", ГрузовоеМесто);
							
							СтрокиРаботы = СтрокаОперация.ПолучитьЭлементы();
							СтрокаРабота = НайтиРаботу(СтрокиРаботы, сткСвойстваРаботы);
							
							// Нашли работу разгрузки.
							Если СтрокаРабота <> Неопределено Тогда
								СтрокаРабота.Проблема = Проблема;
								УдалитьРаботу(СтрокиРаботы, сткСвойстваРаботы);
							КонецЕсли;
							
							// Из задания в точке удаляется операция разгрузки.
							УдалитьЭлементКоллекции(СтрокиОперацийПоЗаданию, СтрокаОперация);
							
							ЕстьСтрокиРазгрузки = СтрокиРаботы.Количество() > 0;
							
						КонецЕсли;
						
						// Отменяются погрузки, удаляются разгрузки зависимых типов грузов.
						Если мсвЗависимыеТипыГрузов.Количество() > 0 Тогда
							мсвЗависимыеОперации = Новый Массив;
							Для каждого СтрокаОперация Из СтрокиОперацийПоЗаданию Цикл
								СтрокиРаботы = СтрокаОперация.ПолучитьЭлементы();
								мсвЗависимыеРаботы = Новый Массив;
								Для каждого СтрокаРабота Из СтрокиРаботы Цикл
									Если Истина
										И Не СтрокаРабота.Выполнена
										И мсвЗависимыеТипыГрузов.Найти(СтрокаРабота.ТипГруза) <> Неопределено
									Тогда
										СтрокаРабота.Проблема = Проблема;
										СтрокаРабота.Отменена = Истина;
										Если ОперацияРазгрузки(СтрокаОперация.НазначениеСтроки)  Тогда
											мсвЗависимыеРаботы.Добавить(СтрокаРабота);		
										КонецЕсли;
										мсвЗависимыеОперации.Добавить(СтрокаОперация);
									КонецЕсли;
								КонецЦикла;
								
								Для каждого СтрокаРабота Из мсвЗависимыеРаботы Цикл
									УдалитьЭлементКоллекции(СтрокиРаботы, СтрокаРабота);
								КонецЦикла;
							КонецЦикла;
							мсвЗависимыеОперации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗависимыеОперации);
							Для каждого СтрокаОперация Из мсвЗависимыеОперации Цикл
								СтрокаОперация.Отменена = Истина;
								Если ОперацияРазгрузки(СтрокаОперация.НазначениеСтроки)  Тогда
									УдалитьЭлементКоллекции(СтрокиОперацийПоЗаданию, СтрокаОперация);
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;	
					КонецЕсли;
					
					// Проверить есть ли еще основные работы в точке по старому адресу.
					Если Истина
						И СтрокиОперацийПоЗаданию <> Неопределено 
						И НЕ ЕстьСтрокиРазгрузки 
					Тогда
					
						ЕстьОсновныеОперацииПоЗаданию = Ложь;
					
						Для каждого СтрокаОперация Из СтрокиОперацийПоЗаданию Цикл
							Если ОсновнаяОперация(СтрокаОперация.НазначениеСтроки) Тогда
								ЕстьОсновныеОперацииПоЗаданию = Истина;
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						// Если погрузка в задании не найдена.
						Если НЕ ЕстьОсновныеОперацииПоЗаданию Тогда
							
							// удалить задание из точки.
							УдалитьЭлементКоллекции(СтрокиЗаданийВТочке, СтрокаЗаданиеНаПеревозкуГруза);
							
							Если НЕ СтрокаТочка.ПорядокТочки = ПорядковыНомерТекущейТочки Тогда
								СтрокаТочка.ФормироватьПрохождениеТочки = Ложь;
							КонецЕсли;
							
							СтрокаТочка.Отменена = Истина;
							
						КонецЕсли;
					КонецЕсли;
									
				КонецЦикла;
			КонецЕсли;
			
		// Отменена проблема в точке погрузки.
		Иначе
			
			// Данные, необходимые для восстановления точки разгрузки.
			Если сткДополнительныеСвойства = Неопределено Тогда
				сткДополнительныеСвойства = Новый Структура();	
			КонецЕсли;
			сткДополнительныеСвойства.Вставить("Задание", ЗаданиеНаПеревозкуГруза);
			сткДополнительныеСвойства.Вставить("ГрузовоеМесто", ГрузовоеМесто);
			
		КонецЕсли;
		
		УстановитьОтметкуОтменыУРодителейРаботы(ЭлементДереваРабота);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьПроблемуПоТочкеПогрузкиКоллекция(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, КоллекцияРабот = Неопределено, ЗначениеПроблемыДоИзменения, сткВозврат, ПорядковыНомерТекущейТочки = Неопределено, ПорядковыйНомерНовойТочки = Неопределено)
	
	ГрузРазделен = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеФормыЭлемент, "ГрузРазделен") Тогда
		ГрузРазделен = ДанныеФормыЭлемент.ГрузРазделен;	
	КонецЕсли;
	
	Если Ложь
		Или ЗначениеПроблемыДоИзменения <> ДанныеФормыЭлемент.Проблема 
		Или ГрузРазделен 
	Тогда
		
		Проблема = ДанныеФормыЭлемент.Проблема;
		ГрузовоеМесто = ДанныеФормыЭлемент.ГрузовоеМесто;
		ТипГруза = ДанныеФормыЭлемент.ТипГруза;
		Операция = ДанныеФормыЭлемент.Операция;
		ЗаданиеНаПеревозкуГруза = ДанныеФормыЭлемент.Задание;
		мсвЗависимыеТипыГрузов = упЗаданиеНаПеревозкуВызовСервера.ЗависимыеТипыГрузовПоЗаданию(ЗаданиеНаПеревозкуГруза, ТипГруза);
		
		мсвРаботыПоДосрочноПройденымТочкам = Новый Массив;		
		
		// Установлена проблема в точке погрузки.
		Если Ложь
			Или ЗначениеЗаполнено(Проблема) 
			Или ГрузРазделен 
		Тогда
		
		     Если Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение") Тогда
				
				ТочкаСРаботамиИзТочкиСДосрочнымУбытием = ДобавитьТочкуДляРаботИзТочкиСДосрочнымУбытием(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, Ложь);								
				
				// Работы в новую точку добавляются по аналогии с точкой разгрузки.
				ПорядковыйНомерНовойТочки = КоллекцияТочекМаршрута.Индекс(ТочкаСРаботамиИзТочкиСДосрочнымУбытием) + 1;
				Количество = ДанныеФормыЭлемент.КоличествоГрузов;
				Сумма = ДанныеФормыЭлемент.Сумма;
				КоличествоЭкземпляровДокумента = ДанныеФормыЭлемент.КоличествоЭкземпляровДокумента;
				ТипРегламентногоДокумента = ДанныеФормыЭлемент.ТипРегламентногоДокумента;
			     Тара = ДанныеФормыЭлемент.Тара;
				Рейс = ДанныеФормыЭлемент.Рейс;
				ОбработатьНовуюТочкуРазгрузки(ТочкаСРаботамиИзТочкиСДосрочнымУбытием, КоллекцияРабот, ЗаданиеНаПеревозкуГруза, Операция, ГрузовоеМесто, Проблема, Количество, Ложь, ТипРегламентногоДокумента, КоличествоЭкземпляровДокумента, Тара, Сумма, Рейс);
				
			Иначе
		
				мсвИдентификаторыТочек = Новый Массив;
				мсвИдентификаторыТочек.Добавить(ДанныеФормыЭлемент.Идентификатор);
				
				мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("Выполнена, Задание, ГрузовоеМесто, Операция", Ложь, ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, ОбратнаяОперация(Операция)));
				Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
					Если мсвИдентификаторыТочек.Найти(СтрокаРабота.Идентификатор) = Неопределено Тогда
						мсвИдентификаторыТочек.Добавить(СтрокаРабота.Идентификатор);
					КонецЕсли;
					СтрокаРабота.Отменена = Истина;
					СтрокаРабота.Проблема = Проблема;
					СтрокаРабота.ГрузРазделен = ГрузРазделен;
				КонецЦикла;
				
				Если мсвЗависимыеТипыГрузов.Количество() > 0 Тогда
					мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("Выполнена, Задание, Отменена", Ложь, ЗаданиеНаПеревозкуГруза, Ложь));
					Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
						Если мсвЗависимыеТипыГрузов.Найти(СтрокаРабота.ТипГруза) <> Неопределено Тогда
							СтрокаРабота.Отменена = Истина;
							СтрокаРабота.Проблема = Проблема;	
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
									
				КоличествоОсновныхРаботПоЗаданию = 0;
				
				// Проверить, остались ли основные работы в точках.
				Для каждого ЭлементИдентификатор Из мсвИдентификаторыТочек Цикл
					мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор, Задание", ЭлементИдентификатор, ЗаданиеНаПеревозкуГруза));
					
					КоличествоОсновныхРаботВТочке = 0;
					Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
						Если ОсновнаяОперация(СтрокаРабота.Операция) Тогда
							Если НЕ СтрокаРабота.Отменена Тогда
								КоличествоОсновныхРаботВТочке = КоличествоОсновныхРаботВТочке + 1;		
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
					Если КоличествоОсновныхРаботВТочке = 0 Тогда
						Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
							Если НЕ СтрокаРабота.Отменена Тогда
								СтрокаРабота.Отменена = Истина;
								СтрокаРабота.Выполнена = Ложь;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
					ВсеРаботыВТочкеОтменены = Истина;
					
					мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор, Отменена", ЭлементИдентификатор, Ложь));
										
					Если мсвСтрокиРабот.Количество() = 0 Тогда
						мсвСтрокиТочки  = КоллекцияТочекМаршрута.НайтиСтроки(Новый Структура("Идентификатор", ЭлементИдентификатор));
						СтрокаТочка = мсвСтрокиТочки[0];
						СтрокаТочка.Отменена = Истина;
						СтрокаТочка.Пройдена = Ложь;
					КонецЕсли;
					
					КоличествоОсновныхРаботПоЗаданию = КоличествоОсновныхРаботПоЗаданию + КоличествоОсновныхРаботВТочке;
					
				КонецЦикла;
							
				Если КоличествоОсновныхРаботПоЗаданию = 0 Тогда
					
					// Отменяются все неосновные работы по заданию.
					
					мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("Выполнена, Отменена, Задание", Ложь, Ложь, ЗаданиеНаПеревозкуГруза));
					
					мсвИдентификаторыТочек = Новый Массив;
					
					Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
						
						Если мсвИдентификаторыТочек.Найти(СтрокаРабота.Идентификатор) = Неопределено Тогда
							мсвИдентификаторыТочек.Добавить(СтрокаРабота.Идентификатор);
						КонецЕсли;
						
						СтрокаРабота.Отменена = Истина;
						СтрокаРабота.Выполнена = Ложь;
						
					КонецЦикла;
					
					// Анализируются точки в которых отменялись работы
					Для каждого ЭлементИдентификатор Из мсвИдентификаторыТочек Цикл
						
						мсвСтрокиРабот = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор, Отменена", ЭлементИдентификатор, Ложь));
						
						// Если в точке работ больге нет
						Если мсвСтрокиРабот.Количество() = 0 Тогда
							
							// Точка отменяется
							мсвСтрокиТочки  = КоллекцияТочекМаршрута.НайтиСтроки(Новый Структура("Идентификатор", ЭлементИдентификатор));
							СтрокаТочка = мсвСтрокиТочки[0];
							СтрокаТочка.Отменена = Истина;
							СтрокаТочка.Пройдена = Ложь;
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
							
				// Проверить, если все точки отменены, то отменить точки отправления и завершения.
				КоличествоТочек = КоллекцияТочекМаршрута.Количество();
				Если Истина
					И КоличествоТочек > 0 
					И (Ложь
						Или КоллекцияТочекМаршрута[0].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")
						Или КоллекцияТочекМаршрута[КоличествоТочек - 1].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения")) 
				Тогда
							
					ВсеТочкиМаршрутаОтменены = Истина;
					Для каждого ТочкаМаршрута Из КоллекцияТочекМаршрута Цикл
						Если Ложь
							Или ТочкаМаршрута.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")
							Или ТочкаМаршрута.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") 
						Тогда
							Продолжить;	
						КонецЕсли;
						
						Если Ложь
							Или НЕ ТочкаМаршрута.Отменена 
							Или ТочкаМаршрута.Пройдена 
						Тогда
							ВсеТочкиМаршрутаОтменены = Ложь;
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Если ВсеТочкиМаршрутаОтменены Тогда
						
						Если Истина 
							И НЕ КоллекцияТочекМаршрута[0].Отменена 
							И КоллекцияТочекМаршрута[0].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления") 
						Тогда
							КоллекцияТочекМаршрута[0].Отменена = Истина;		
						КонецЕсли;
						
						Если Истина
							И НЕ КоллекцияТочекМаршрута[КоличествоТочек - 1].Отменена 
							И КоллекцияТочекМаршрута[КоличествоТочек - 1].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") 
						Тогда
							КоллекцияТочекМаршрута[КоличествоТочек - 1].Отменена = Истина;		
						КонецЕсли;
						
					КонецЕсли;		
							
				КонецЕсли;
			
			КонецЕсли;
			
		// Отменена проблема в точке погрузки.
		Иначе
			
			мсвСтрокиТочки  = КоллекцияТочекМаршрута.НайтиСтроки(Новый Структура("Идентификатор", ДанныеФормыЭлемент.Идентификатор));
			Если мсвСтрокиТочки.Количество() > 0 Тогда
				СтрокаТочка = мсвСтрокиТочки[0];
				Если СтрокаТочка.Отменена Тогда
					СтрокаТочка.Отменена = Ложь;
					СтрокаТочка.Пройдена = Ложь;
					
					// Восстанавливаются не основные работы в точке.
					мсвСтрокиРаботВТочке = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор",СтрокаТочка.Идентификатор));
					Для каждого СтрокаРабота Из мсвСтрокиРаботВТочке Цикл
						Если ОсновнаяОперация(СтрокаРабота.Операция) Тогда
							Продолжить
						КонецЕсли;
						
						СтрокаРабота.Отменена = Ложь;
						СтрокаРабота.Проблема =  ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
						СтрокаРабота.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
					КонецЦикла;
					
					// Восстанавливаются точки отправления/завершения.
					КоличествоТочек = КоллекцияТочекМаршрута.Количество();
					
					Если Истина
						И КоллекцияТочекМаршрута[0].Отменена 
						И КоллекцияТочекМаршрута[0].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления") 
					Тогда
						КоллекцияТочекМаршрута[0].Отменена = Ложь;		
					КонецЕсли;
					
					Если Истина
						И КоллекцияТочекМаршрута[КоличествоТочек - 1].Отменена 
						И КоллекцияТочекМаршрута[КоличествоТочек - 1].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") 
					Тогда
						КоллекцияТочекМаршрута[КоличествоТочек - 1].Отменена = Ложь;		
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
						
			// Данные, необходимые для восстановления точки разгрузки.
			сткВозврат = Новый Структура();
			сткВозврат.Вставить("Задание", ЗаданиеНаПеревозкуГруза);
			сткВозврат.Вставить("ГрузовоеМесто", ГрузовоеМесто);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДобавитьТочкуДляРаботИзТочкиСДосрочнымУбытием(ДанныеФормыЭлемент, КоллекцияТочекМаршрута, ЭтоДерево)
	
	Если ЭтоДерево Тогда
		ТочкаТекущая = ДанныеФормыЭлемент;
		ИдентификаторТекущейТочки = ТочкаТекущая.ИдентификаторТочки;
	Иначе	
		ИдентификаторТекущейТочки = ДанныеФормыЭлемент.Идентификатор;
		ТочкаТекущая = НайтиЗначениеВКоллекции(КоллекцияТочекМаршрута, "Идентификатор", ИдентификаторТекущейТочки); 
	КонецЕсли;
		
	ТочкаНовая = НайтиЗначениеВКоллекции(КоллекцияТочекМаршрута, "ИдентификаторИсходнойТочки", ИдентификаторТекущейТочки); 
	
	Если ТочкаНовая = Неопределено Тогда
		ТочкаНовая = КоллекцияТочекМаршрута.Добавить();
		ЗаполнитьЗначенияСвойств(ТочкаНовая, ТочкаТекущая);
		ТочкаНовая.ИдентификаторИсходнойТочки = ИдентификаторТекущейТочки;
		ТочкаНовая.Отменена = Ложь;
		ИдентификаторНовойТочки = Новый УникальныйИдентификатор;
		Если ЭтоДерево Тогда
			ТочкаНовая.Выполнена = 0;
			ТочкаНовая.ПрибытиеПлан = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
			ТочкаНовая.УбытиеПлан = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
			ТочкаНовая.ИдентификаторТочки = ИдентификаторНовойТочки;
			ТочкаНовая.Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
		Иначе	
			ТочкаНовая.Пройдена = Ложь;
			ТочкаНовая.ПлановоеПрибытие = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
			ТочкаНовая.ПлановоеУбытие = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
			ТочкаНовая.Идентификатор = ИдентификаторНовойТочки;
		КонецЕсли;
		
		ТочкаНовая.НачалоРаботПлан = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
		ТочкаНовая.ПрибытиеФакт = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
		ТочкаНовая.НачалоРаботФакт = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
		ТочкаНовая.УбытиеФакт = упСервисныеФункцииКлиентСервер.НачалоОтсчета();
		
		// Во все точки имеющим в предшественниках точку-родителя, добавляется в предшественники новая точка
		ИндексПервойТочкиСПредшественником = Неопределено;
		
		мсвПредшественникиНовойТочки = ТочкаНовая.Предшественники;
		
		// Определяется порядок новой точки в маршруте.
		Для каждого СтрокаТочкаТекущая Из КоллекцияТочекМаршрута Цикл
			
			Если ТочкаПройдена(СтрокаТочкаТекущая, ЭтоДерево) Тогда
				Продолжить;
			КонецЕсли;
						
			мсвПредшественники = СтрРазделить(СтрокаТочкаТекущая.Предшественники,",");
			
			// Если для текущей точки, точка-родитель есть в предшественниках.
			Если мсвПредшественники.Найти(Строка(ИдентификаторТекущейТочки)) <> Неопределено Тогда
				
				// В предшественники добавляется новая точка.
				мсвПредшественники.Добавить(ИдентификаторНовойТочки);	
				
				// Если порядок новой точки в маршруте еще не определено.
				Если ИндексПервойТочкиСПредшественником = Неопределено Тогда
					ИндексПервойТочкиСПредшественником = КоллекцияТочекМаршрута.Индекс(СтрокаТочкаТекущая);
				КонецЕсли;
				
			// Если у новой точки приоритет больше чем у текущей
			// Приоритеты:
			//   0 - Точка отправления(вид адреса есть в списке точек отправления вида перевозки);
			//   1 - Точка создана при отмене по проблеме(не досрочной) работы разгрузки;
			//   2 - Все остальные точки.
			ИначеЕсли Истина
			          И Не ТочкаПройдена(СтрокаТочкаТекущая, ЭтоДерево)
					И Не СтрокаТочкаТекущая.Отменена
			          И ТочкаНовая.Приоритет > СтрокаТочкаТекущая.Приоритет Тогда	
				Если ИндексПервойТочкиСПредшественником = Неопределено Тогда
					ИндексПервойТочкиСПредшественником = КоллекцияТочекМаршрута.Индекс(СтрокаТочкаТекущая);
				КонецЕсли;
	          				
			КонецЕсли;
			
			// Если порядок для новой точки опеределен.
			Если ИндексПервойТочкиСПредшественником <> Неопределено Тогда
				// Все последующие точки сдвигаются, освобождая место для новой точки.
				Если ЭтоДерево Тогда
					СтрокаТочкаТекущая.ПорядокТочки = СтрокаТочкаТекущая.ПорядокТочки + 1;
				Иначе
					СтрокаТочкаТекущая.СтрокаНомер = СтрокаТочкаТекущая.СтрокаНомер + 1;
				КонецЕсли;
				
			КонецЕсли;
               			
			СтрокаТочкаТекущая.Предшественники = СтрСоединить(мсвПредшественники, ",");
			
		КонецЦикла;
		
		ИндексНовойТочки = КоллекцияТочекМаршрута.Индекс(ТочкаНовая);
		
		Если ИндексПервойТочкиСПредшественником <> Неопределено Тогда
			КоллекцияТочекМаршрута.Сдвинуть(ИндексНовойТочки, ИндексПервойТочкиСПредшественником - ИндексНовойТочки); 
			Если ЭтоДерево Тогда
				ТочкаНовая.ПорядокТочки = ИндексПервойТочкиСПредшественником + 1;
			Иначе
				ТочкаНовая.СтрокаНомер = ИндексПервойТочкиСПредшественником + 1;
			КонецЕсли;
			ПорядковыНомерТекущейТочки = ИндексПервойТочкиСПредшественником + 1;
		Иначе
			Если ЭтоДерево Тогда
				ТочкаНовая.ПорядокТочки = ИндексНовойТочки + 1;
			Иначе
				ТочкаНовая.СтрокаНомер = ИндексНовойТочки + 1;
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
		
	Возврат ТочкаНовая;
	
КонецФункции

Функция ТочкаПройдена(Точка, ЭтоДерево)
	Возврат Ложь
	        Или (ЭтоДерево И Точка.Выполнена = 1)
		   Или (Не ЭтоДерево И Точка.Пройдена)
КонецФункции

Функция НайтиТочкуПоАдресуДерево(КоллекцияТочекПоМаршрута, Адрес, СтрокаНомер)
	
	текРезультат = Неопределено;
	
	Для каждого ЭлементТочка Из КоллекцияТочекПоМаршрута Цикл
		Если ЭлементТочка.ТочкаПройдена ИЛИ ЭлементТочка.ПорядокТочки <= СтрокаНомер Тогда
			Продолжить;
		КонецЕсли;
		
			
		Если ЭлементТочка.АдресТочки = Адрес Тогда
			текРезультат = ЭлементТочка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат текРезультат;
	
КонецФункции

Функция НайтиТочкуПоАдресуКоллекция(КоллекцияТочекПоМаршрута, Адрес, СтрокаНомер)
	
	текРезультат = Неопределено;
	
	Для каждого ЭлементТочка Из КоллекцияТочекПоМаршрута Цикл
		
		Если ЭлементТочка.Пройдена ИЛИ ЭлементТочка.СтрокаНомер <= СтрокаНомер Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементТочка.Адрес = Адрес Тогда
			текРезультат = ЭлементТочка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат текРезультат;
	
КонецФункции

Функция НайтиЗаданиеВТочке(КоллекцияЗаданийВТочке, ЗаданиеНаПеревозкуГруза)
	Возврат НайтиЗначениеВКоллекции(КоллекцияЗаданийВТочке, "НазначениеСтроки", ЗаданиеНаПеревозкуГруза);
КонецФункции

Функция НайтиОперациюВЗадании(КоллекцияОперацийПоЗаданию, Операция)
	Возврат НайтиЗначениеВКоллекции(КоллекцияОперацийПоЗаданию, "НазначениеСтроки", Операция);
КонецФункции

Процедура УдалитьЭлементКоллекции(КоллекцияЭлементов, ЭлементКоллекции)
	Индекс = КоллекцияЭлементов.Индекс(ЭлементКоллекции);
	КоллекцияЭлементов.Удалить(Индекс);	
КонецПроцедуры

Функция НайтиРаботу(КоллекцияРаботПоОперации, сткСвойстваРаботы)
	текРезультат = Неопределено;
	Для каждого ТекущаяРабота Из КоллекцияРаботПоОперации Цикл
		Если ТекущаяРабота.НазначениеСтроки = сткСвойстваРаботы.НазначениеСтроки Тогда
			текРезультат = ТекущаяРабота;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат текРезультат;
КонецФункции

Процедура ИзменитьРаботу(КоллекцияРаботПоОперации, сткСвойстваРаботы)
	ИзменяемаяРабота = НайтиРаботу(КоллекцияРаботПоОперации, сткСвойстваРаботы);
	Если ИзменяемаяРабота = Неопределено Тогда
		ИзменяемаяРабота = КоллекцияРаботПоОперации.Добавить();
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ИзменяемаяРабота, сткСвойстваРаботы);
КонецПроцедуры

Процедура УдалитьРаботу(КоллекцияРаботПоОперации, сткСвойстваРаботы)
	ИзменяемаяРабота = НайтиРаботу(КоллекцияРаботПоОперации, сткСвойстваРаботы);
	Если НЕ ИзменяемаяРабота = Неопределено Тогда
		УдалитьЭлементКоллекции(КоллекцияРаботПоОперации, ИзменяемаяРабота);
	КонецЕсли;
КонецПроцедуры

// Установка пометки отмены работы.
//
// Параметры:
//  КоллекцияРаботПоОперации - Коллекция - Работы по операции.
//	сткСвойстваРаботы - Структура - Свойства работы.
//	ОтметкаОтмены - Булево - Отметка.
//
Процедура УстановитьОтметкуОтменыРаботы(КоллекцияРаботПоОперации, сткСвойстваРаботы, ОтметкаОтмены) Экспорт
	
	ИзменяемаяРабота = НайтиРаботу(КоллекцияРаботПоОперации, сткСвойстваРаботы);
	Если НЕ ИзменяемаяРабота = Неопределено Тогда
		ИзменяемаяРабота.Отмена	= ОтметкаОтмены;	
		УстановитьФлагРодителя(ИзменяемаяРабота);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Установить флаги.
//
// Параметры:
//  ЭлементДерева - Элемент - Элемент дерева.
//
Процедура УстановитьФлаги(ЭлементДерева) Экспорт
	
	Родитель = ЭлементДерева.ПолучитьРодителя();
	Потомки = ЭлементДерева.ПолучитьЭлементы();
	Выполнена = ЭлементДерева.Выполнена;
	ЕстьПолеИндексКартинкиВыполнено = ЭлементДерева.Свойство("ИндексКартинкиВыполнено");
	УстановитьФлагиПотомкам(Потомки, Выполнена, ЕстьПолеИндексКартинкиВыполнено);
	УстановитьФлагРодителя(Родитель);
	
КонецПроцедуры

// Процедура - Установить флаги потомка.
//
// Параметры:
//  ЭлементыДерева - Элемент - Элемент дерева.
//  Выполнена - Булево - Значение.
//  ЕстьПолеИндексКартинкиВыполнено - Булево - Флаг поле есть.
//
Процедура УстановитьФлагиПотомкам(ЭлементыДерева, Выполнена, ЕстьПолеИндексКартинкиВыполнено = Ложь) Экспорт
	
	Если ЭлементыДерева.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
			
	КоличествоНеВыполненных 	= 0;
	КоличествоВыполненных		= 0;
	
	Для каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если НЕ ЭлементДерева.Отменена 
			И НЕ ЗначениеЗаполнено(ЭлементДерева.Проблема) 
			И НЕ ЗначениеЗаполнено(ЭлементДерева.АдресРазгрузки) Тогда
			ЭлементДерева.Выполнена = Выполнена;	
			Если ЕстьПолеИндексКартинкиВыполнено Тогда
				ЭлементДерева.ИндексКартинкиВыполнено = ЭлементДерева.Выполнена;	
			КонецЕсли;

			Потомки	= ЭлементДерева.ПолучитьЭлементы();				
			УстановитьФлагиПотомкам(Потомки, Выполнена,ЕстьПолеИндексКартинкиВыполнено);	
		Иначе
			Если ЕстьПолеИндексКартинкиВыполнено Тогда
				ЭлементДерева.ИндексКартинкиВыполнено = 3;
			КонецЕсли;
		КонецЕсли;
		
		Родитель = ЭлементДерева.ПолучитьРодителя();	
		
		Если ЭлементДерева.Выполнена = 0 Тогда
			КоличествоНеВыполненных = КоличествоНеВыполненных + 1;	
		ИначеЕсли ЭлементДерева.Выполнена = 1 Тогда
			КоличествоВыполненных = КоличествоВыполненных + 1;	
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Родитель = Неопределено Тогда
		Если КоличествоНеВыполненных = ЭлементыДерева.Количество() Тогда
			Родитель.Выполнена = 0;
		ИначеЕсли КоличествоВыполненных = ЭлементыДерева.Количество() Тогда
			Родитель.Выполнена = 1;
		Иначе
			Родитель.Выполнена = 2;
		КонецЕсли;
		Если ЕстьПолеИндексКартинкиВыполнено Тогда
			Если Родитель.Отменена Тогда
				Родитель.ИндексКартинкиВыполнено = 3;
			Иначе
				Родитель.ИндексКартинкиВыполнено = Родитель.Выполнена;	
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// Процедура - Установить флаги родителя.
//
// Параметры:
//  ЭлементДерева - Элемент - Элемент дерева.
//
Процедура УстановитьФлагРодителя(ЭлементДерева) Экспорт
	
	Если ЭлементДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Потомки = ЭлементДерева.ПолучитьЭлементы();
	
	КоличествоВыполненных 	= 0;
	КоличествоНеВыполненных = 0;
	
	КоличествоОтмененных	= 0;
	
	Для каждого Потомок Из Потомки Цикл
		КоличествоВыполненных	= КоличествоВыполненных + ?(Потомок.Выполнена = 1, 1, 0);
		КоличествоНеВыполненных = КоличествоНеВыполненных + ?(Потомок.Выполнена = 0, 1, 0);
		КоличествоОтмененных	= КоличествоОтмененных + ?(Потомок.Отменена = 1, 1, 0);
	КонецЦикла;
	
	Если КоличествоВыполненных = Потомки.Количество() Тогда
		ЭлементДерева.Выполнена = 1;
	ИначеЕсли КоличествоНеВыполненных = Потомки.Количество() Тогда	
		ЭлементДерева.Выполнена = 0;
	Иначе
		ЭлементДерева.Выполнена = 2;
	КонецЕсли;
	
	Если КоличествоОтмененных = Потомки.Количество() Тогда
		ЭлементДерева.Отменена = 1;
	Иначе
		ЭлементДерева.Отменена = 0;	
	КонецЕсли;
	
	Если ЭлементДерева.Свойство("ИндексКартинкиВыполнено") Тогда
		Если ЭлементДерева.Отменена Тогда
			ЭлементДерева.ИндексКартинкиВыполнено = 3;
		Иначе
			ЭлементДерева.ИндексКартинкиВыполнено = ЭлементДерева.Выполнена;	
		КонецЕсли;
	КонецЕсли;
	
	Родитель 		= ЭлементДерева.ПолучитьРодителя();
	УстановитьФлагРодителя(Родитель);
	
КонецПроцедуры

// Процедура - Обработать работу по точке.
//
// Параметры:
//  ЭлементДерева              - Элемент - Элемент дерева.
//  КоллекцияРабот             - Коллекция	 - Коллекция элементов.
//  ЭтоДерево				 - Булево		 - Флаг дерева.
//  сткДополнительныеСвойства  - Структура	 - структура для возврата значений.
//
Процедура ОбработатьРаботуПоТочке(ЭлементДерева, КоллекцияРабот = Неопределено, ЭтоДерево = Истина, сткДополнительныеСвойства = Неопределено) Экспорт
	
	Если ЭтоДерево Тогда
		ОбработатьРаботуПоТочкеДерево(ЭлементДерева, сткДополнительныеСвойства);
	Иначе	
		ОбработатьРаботуПоТочкеКоллекция(ЭлементДерева, КоллекцияРабот);	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Обработать работу по точке в дереве.
//
// Параметры:
//  ЭлементДерева            - Элемент 	 - Элемент дерева.
//  стДополнительныеСвойства - Структура - структура для возврата значений.
//
Процедура ОбработатьРаботуПоТочкеДерево(ЭлементДерева, сткДополнительныеСвойства = Неопределено) Экспорт
			
	Если Истина
		И ЭлементДерева <> Неопределено 
		И (Ложь
		   Или ЭлементДерева.ЭтоРабота 
		   Или ЭлементДерева.ЭтоПолучениеПередачаДокументов)
	Тогда
					
		ГрузРазделен = Ложь;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементДерева, "ГрузРазделен") Тогда
			ГрузРазделен = ЭлементДерева.ГрузРазделен;	
		КонецЕсли;
		
		Если ЭлементДерева.ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
			ЭлементДерева.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		КонецЕсли;
		
		// Если есть проблема.
		ФлагОтменена = Ложь;
		Если Ложь
			Или ЗначениеЗаполнено(ЭлементДерева.Проблема) 
			Или ГрузРазделен 
		Тогда
			ФлагОтменена = Истина;
		Иначе
			Если ЗначениеЗаполнено(ЭлементДерева.АдресРазгрузки) Тогда
				ФлагОтменена = Истина;		
			Иначе	
				ФлагОтменена = Ложь;		
			КонецЕсли;
		КонецЕсли;
				
		ЭлементДерева.Отменена = ФлагОтменена; 
					
		// Если отменяется выполненная строка.
		Если ЭлементДерева.Выполнена И ЭлементДерева.Отменена Тогда
			ЭлементДерева.Выполнена  = 0;
		КонецЕсли;
		
		Родитель = ЭлементДерева.ПолучитьРодителя();// Родителям устанавливается флаг с учетом флага текущей строки.
		
		УстановитьПотребностьВОтменеТарыВТочке(ЭлементДерева, Родитель.ПолучитьЭлементы(), сткДополнительныеСвойства);
						
		УстановитьФлагРодителя(Родитель);
		
		// Если в точке нет больше основных операций (типа "погрузка", "разгрузка"), то отменяются все дополнительные работы.
		Если ОсновнаяОперация(ЭлементДерева.Операция) Тогда
			ОтменитьДополнительныеРаботыПоТочкеДерево(ЭлементДерева, ФлагОтменена);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

// Процедура - Обработать работу по точке коллекция.
//
// Параметры:
//  ДанныеФормыЭлемент - Элемент - Элемент формы.
//  КоллекцияРабот - Коллекция - Коллекция работ.
//
Процедура ОбработатьРаботуПоТочкеКоллекция(ДанныеФормыЭлемент, КоллекцияРабот) Экспорт

	Если НЕ ДанныеФормыЭлемент = Неопределено Тогда
		
		// Если есть проблема.
		ФлагОтменена = Ложь;
		
		Если ДанныеФормыЭлемент.ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
			ДанныеФормыЭлемент.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеФормыЭлемент.Проблема) Тогда
			ФлагОтменена = Истина;
		КонецЕсли;
		ДанныеФормыЭлемент.Отменена = ФлагОтменена;
		
		Если ДанныеФормыЭлемент.Выполнена И ДанныеФормыЭлемент.Отменена Тогда
			ДанныеФормыЭлемент.Выполнена = Ложь;
		КонецЕсли;
	     		
		// Если в точке нет больше основных операций (типа "погрузка", "разгрузка"), то отменяются все дополнительные работы.
		Если ОсновнаяОперация(ДанныеФормыЭлемент.Операция) Тогда

			ЗаданиеНаПеревозкуГруза = ДанныеФормыЭлемент.Задание;
			мсвСтрокиРаботыПоТекущемуЗаданиюВТочке = КоллекцияРабот.НайтиСтроки(Новый Структура("Идентификатор, Задание", ДанныеФормыЭлемент.Идентификатор, ЗаданиеНаПеревозкуГруза));
			
			ВсеРаботыПоЗаданиюВТочкеОтменены = Истина;
			
			// Ведется подсчет сколько всего основных работ и сколько из них отменено.
			Для каждого СтрокаРабота Из мсвСтрокиРаботыПоТекущемуЗаданиюВТочке Цикл
				Если ОсновнаяОперация(СтрокаРабота.Операция) Тогда
					Если НЕ СтрокаРабота.Отменена = ФлагОтменена Тогда
						ВсеРаботыПоЗаданиюВТочкеОтменены = Ложь;	
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
						
			// Если все основные работы отменены.
			Если ВсеРаботыПоЗаданиюВТочкеОтменены Тогда
				
				ТипОперацииПолучениеДокументов = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПолучениеДокументов");
				
				// Ведется поиск не основных работ.
				Для каждого СтрокаРабота Из мсвСтрокиРаботыПоТекущемуЗаданиюВТочке Цикл
					Если НЕ ОсновнаяОперация(СтрокаРабота.Операция) И Не СтрокаРабота.Выполнена Тогда
						СтрокаРабота.Отменена = ФлагОтменена;
						СтрокаРабота.Проблема = ДанныеФормыЭлемент.Проблема;
						Если Истина
							И СтрокаРабота.Операция <> ТипОперацииПолучениеДокументов
							И Не СтрокаРабота.ЗапрещеноСозданиеНовойТочкиМаршрута
						Тогда
							СтрокаРабота.АдресРазгрузки = ДанныеФормыЭлемент.АдресРазгрузки;	
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли; 
	
КонецПроцедуры

// Процедура - Установить потребность в отмене тары
//
// Параметры:
//  ЭлементДерева			 - 	 - 
//  КоллекцияРабот			 - 	 - 
//  сткДополнительныеСвойства	 - 	 - 
//
Процедура УстановитьПотребностьВОтменеТарыВТочке(ЭлементДерева, КоллекцияРабот, сткДополнительныеСвойства = Неопределено)
	
	ВидГрузаТара							= ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара");
	ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре	= Ложь;
	ЕстьНеОтмененнаяРаботаТара				= Ложь;
		
	Если сткДополнительныеСвойства = Неопределено Тогда
		сткДополнительныеСвойства = Новый Структура();
	КонецЕсли;
	
	Тара = ПредопределенноеЗначение("Справочник.упГрузовыеМеста.ПустаяСсылка");
	Если ЭлементДерева.ВидГруза = ВидГрузаТара Тогда
		Тара = ЭлементДерева.НазначениеСтроки;
	ИначеЕсли ЗначениеЗаполнено(ЭлементДерева.Тара) Тогда	
		Тара	= ЭлементДерева.Тара;
	КонецЕсли;
		
	Если Истина
		И ЭлементДерева.Отменена
		И ОсновнаяОперация(ЭлементДерева.Операция)
		И ЗначениеЗаполнено(ЭлементДерева.НазначениеСтроки)
		И ЗначениеЗаполнено(Тара)
	Тогда
		
		Для каждого ЭлементРабота Из КоллекцияРабот Цикл
			
			ТараРаботы = ПредопределенноеЗначение("Справочник.упГрузовыеМеста.ПустаяСсылка");
			Если ЭлементРабота.ВидГруза = ВидГрузаТара Тогда
				ТараРаботы = ЭлементРабота.НазначениеСтроки;
			ИначеЕсли ЗначениеЗаполнено(ЭлементРабота.Тара) Тогда	
				ТараРаботы = ЭлементРабота.Тара;
			КонецЕсли;
			
			Если Истина
				И НЕ ЭлементРабота.Отменена
				И ЗначениеЗаполнено(ЭлементРабота.НазначениеСтроки)
				И Тара = ТараРаботы
			Тогда	
				
				Если ЭлементРабота.ВидГруза = ВидГрузаТара Тогда
					ЕстьНеОтмененнаяРаботаТара	= Истина;	
				Иначе
					ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре = Истина;	
				КонецЕсли;
				
				Если ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре
					И ЕстьНеОтмененнаяРаботаТара Тогда
					Прервать;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	сткДополнительныеСвойства.Вставить("ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре", 	ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре);
	сткДополнительныеСвойства.Вставить("ЕстьНеОтмененнаяРаботаТара", 			ЕстьНеОтмененнаяРаботаТара);
			
КонецПроцедуры

// Процедура - Отменить работу по таре
//
// Параметры:
//  ЭлементДерева			 - 	 - 
//  КоллекцияРабот			 - 	 - 
//  сткДополнительныеСвойства	 - 	 - 
//  ЭтоДерево				 - 	 - 
//
Процедура ОтменитьРаботуПоТаре(ЭлементДерева, сткДополнительныеСвойства, ЭтоДерево = Истина, КоллекцияТочекМаршрута = Неопределено, СтараяПроблемаСправочник = Неопределено, СтарыйАдресаРазгрузкиСправочник = Неопределено) Экспорт
	
	Если ЭтоДерево Тогда
		Родитель = ЭлементДерева.ПолучитьРодителя();
		ОтменитьРаботыПоТареДерево(ЭлементДерева, Родитель.ПолучитьЭлементы(), сткДополнительныеСвойства, КоллекцияТочекМаршрута, СтараяПроблемаСправочник, СтарыйАдресаРазгрузкиСправочник);
	КонецЕсли;
		
КонецПроцедуры

// Процедура - Отменить работу по таре дерево
//
// Параметры:
//  ЭлементДерева			 - 	 - 
//  КоллекцияРабот			 - 	 - 
//  сткДополнительныеСвойства	 - 	 - 
//
Процедура ОтменитьРаботыПоТареДерево(ЭлементДерева, КоллекцияРабот, сткДополнительныеСвойства, КоллекцияТочекМаршрута, СтараяПроблемаСправочник, СтарыйАдресаРазгрузкиСправочник);
	
	ВидГрузаТара				= ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара");
	ОтменитьГрузовыеМестаВТаре 	= Ложь;
	ОтменитьТару			  	= Ложь;
	
	Если НЕ сткДополнительныеСвойства = Неопределено Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(сткДополнительныеСвойства, "ОтменитьГрузовыеМестаВТаре") Тогда
			ОтменитьГрузовыеМестаВТаре = сткДополнительныеСвойства.ОтменитьГрузовыеМестаВТаре;
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(сткДополнительныеСвойства, "ОтменитьТару") Тогда
			ОтменитьТару = сткДополнительныеСвойства.ОтменитьТару;
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И НЕ ОтменитьГрузовыеМестаВТаре
		И НЕ ОтменитьТару Тогда
		Возврат;
	КонецЕсли;
	
	Тара = ПредопределенноеЗначение("Справочник.упГрузовыеМеста.ПустаяСсылка");
	Если ЭлементДерева.ВидГруза = ВидГрузаТара Тогда
		Тара = ЭлементДерева.НазначениеСтроки;
	ИначеЕсли ЗначениеЗаполнено(ЭлементДерева.Тара) Тогда	
		Тара	= ЭлементДерева.Тара;
	КонецЕсли;

	Если Истина
		И ЭлементДерева.Отменена
		И ОсновнаяОперация(ЭлементДерева.Операция)
		И ЗначениеЗаполнено(ЭлементДерева.НазначениеСтроки)
		И ЗначениеЗаполнено(Тара)
		Тогда
		
		Для каждого ЭлементРабота Из КоллекцияРабот Цикл
			
			ТараРаботы = ПредопределенноеЗначение("Справочник.упГрузовыеМеста.ПустаяСсылка");
			Если ЭлементРабота.ВидГруза = ВидГрузаТара Тогда
				ТараРаботы = ЭлементРабота.НазначениеСтроки;
			ИначеЕсли ЗначениеЗаполнено(ЭлементРабота.Тара) Тогда	
				ТараРаботы = ЭлементРабота.Тара;
			КонецЕсли;
			
			Если Истина
				И НЕ ЭлементРабота.Отменена
				И ЗначениеЗаполнено(ЭлементРабота.НазначениеСтроки)
				И Тара = ТараРаботы
				Тогда	
				
				Если ЭлементРабота.ВидГруза = ВидГрузаТара Тогда
					Если ОтменитьТару Тогда
						ЭлементРабота.Отменена 	= Истина;
						ЭлементРабота.Выполнена	= Ложь;
						ЭлементРабота.Проблема	= ЭлементДерева.Проблема;
						ЭлементРабота.АдресРазгрузки	= ЭлементДерева.АдресРазгрузки;
						Если НЕ КоллекцияТочекМаршрута = Неопределено Тогда
							Если ОперацияПогрузки(ЭлементДерева.Операция) Тогда
								ОбработатьПроблемуПоТочкеПогрузки(ЭлементРабота, КоллекцияТочекМаршрута, КоллекцияРабот, СтараяПроблемаСправочник, сткДополнительныеСвойства, , Истина);		
							Иначе	
								ОбработатьПроблемуПоТочкеРазгрузки(ЭлементРабота, КоллекцияТочекМаршрута, КоллекцияРабот, СтараяПроблемаСправочник, СтарыйАдресаРазгрузкиСправочник, , Истина,);		
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				Иначе
					Если ОтменитьГрузовыеМестаВТаре Тогда
						ЭлементРабота.Отменена 	= Истина;
						ЭлементРабота.Выполнена	= Ложь;
						ЭлементРабота.Проблема	= ЭлементДерева.Проблема;
						ЭлементРабота.АдресРазгрузки	= ЭлементДерева.АдресРазгрузки;
						Если НЕ КоллекцияТочекМаршрута = Неопределено Тогда
							Если ОперацияПогрузки(ЭлементДерева.Операция) Тогда
								ОбработатьПроблемуПоТочкеПогрузки(ЭлементРабота, КоллекцияТочекМаршрута, КоллекцияРабот, СтараяПроблемаСправочник, сткДополнительныеСвойства, , Истина);		
							Иначе	
								ОбработатьПроблемуПоТочкеРазгрузки(ЭлементРабота, КоллекцияТочекМаршрута, КоллекцияРабот, СтараяПроблемаСправочник, СтарыйАдресаРазгрузкиСправочник, , Истина,);		
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	ОтменитьДополнительныеРаботыПоТочкеДерево(ЭлементДерева, Истина);							
	
	Родитель  = ЭлементДерева.ПолучитьРодителя();    // Родителям устанавливается флаг с учетом флага текущей строки.
	УстановитьФлагРодителя(Родитель);

КонецПроцедуры

Процедура ОтменитьДополнительныеРаботыПоТочкеДерево(ЭлементДерева, ФлагОтменена)
	РодительЗадание = ЭлементДерева.ПолучитьРодителя().ПолучитьРодителя(); 	// 	Добираемся до элемента - "точка".
	ЭлементыЗадание = РодительЗадание.ПолучитьЭлементы();					//	Выбираются все задания в точке.
	
	ВсегоКоличествоОсновныхРабот = 0;
	ВсегоКоличествоОтмененныхОсновныхРабот 	= 0;
	
	// Ведется подсчет сколько всего основных работ и сколько из них отменено.
	Для каждого ЭлементОперация Из ЭлементыЗадание Цикл
		Если ОсновнаяОперация(ЭлементОперация.НазначениеСтроки) Тогда
			ЭлементРаботы = ЭлементОперация.ПолучитьЭлементы();
			ВсегоКоличествоОсновныхРабот = ВсегоКоличествоОсновныхРабот + ЭлементРаботы.Количество();
			Для каждого ЭлементРабота Из ЭлементРаботы Цикл
				Если ЭлементРабота.Отменена = ФлагОтменена Тогда
					ВсегоКоличествоОтмененныхОсновныхРабот = ВсегоКоличествоОтмененныхОсновныхРабот + 1;	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Если все основные работы отменены.
	Если ВсегоКоличествоОсновныхРабот = ВсегоКоличествоОтмененныхОсновныхРабот Тогда
		
		ТипОперацииПолучениеДокументов = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПолучениеДокументов");
		
		// Ведется поиск не основных работ.
		Для каждого ЭлементОперация Из ЭлементыЗадание Цикл
			Если Истина
				И Не ОсновнаяОперация(ЭлементОперация.НазначениеСтроки)
				И Не ЭлементОперация.Выполнена Тогда
				
				// Они отменяются.
				ЭлементРаботы = ЭлементОперация.ПолучитьЭлементы();
				Для каждого ЭлементРабота Из ЭлементРаботы Цикл
					ЭлементРабота.Отменена = ФлагОтменена;
					ЭлементРабота.Выполнена = НЕ ФлагОтменена;
					Если ЗначениеЗаполнено(ЭлементДерева.Проблема) Тогда
						ЭлементРабота.Проблема = ЭлементДерева.Проблема;
						Если НЕ ЭлементРабота.Операция = ТипОперацииПолучениеДокументов Тогда
							ЭлементРабота.АдресРазгрузки = ЭлементДерева.АдресРазгрузки;
						КонецЕсли;
					КонецЕсли;
					Родитель = ЭлементРабота.ПолучитьРодителя();    // Родителям устанавливается флаг с учетом флага текущей строки.
					УстановитьФлагРодителя(Родитель);
					
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Процедура - Заполнить элементы дерева.
//
// Параметры:
//  ЭлементДереваПриемник - Элемент - Элемент дерева.
//  ЭлементДереваИсточник - Элемент - Элемент дерева.
//
Процедура ЗаполнитьЭлементДерева(ЭлементДереваПриемник, ЭлементДереваИсточник) Экспорт
	
	Точка				= ЭлементДереваИсточник;	
	
	НоваяСтрокаТочка 	= ЭлементДереваПриемник;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, Точка);
		
	СтрокиЗадания	= Точка.ПолучитьЭлементы();
	
	Для каждого СтрокаЗадание Из СтрокиЗадания Цикл
		
		НоваяСтрокаЗадание = НоваяСтрокаТочка.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗадание, СтрокаЗадание);
		
		СтрокиОперации = СтрокаЗадание.ПолучитьЭлементы();
		
		Для каждого СтрокаОперация Из СтрокиОперации Цикл
			
			НоваяСтрокаОперация = НоваяСтрокаЗадание.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаОперация, СтрокаОперация);
			СтрокиГрузовоеМесто = СтрокаОперация.ПолучитьЭлементы();
			
			Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовоеМесто Цикл
				НоваяСтрокаГрузовоеМесто = НоваяСтрокаОперация.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаГрузовоеМесто, СтрокаГрузовоеМесто);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла; 
		
КонецПроцедуры

// Функция - Основная операция.
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Булево - Истина, если операция является основной
//
Функция ОсновнаяОперация(Операция) Экспорт
	
	Результат = Ложь;
	
	Если Ложь
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка")
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка")
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей")
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") 
	Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Основная операция для вида грузов груз.
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Булево - Истина, если операция является основной
//
Функция ОсновнаяОперацияДляВидаГрузаГруз(Операция) Экспорт
	
	Результат = Ложь;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка")
			ИЛИ Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка") Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Основная операция для вида груз инкассируемая ценность.
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Булево - Истина, если операция является основной
//
Функция ОсновнаяОперацияДляВидГрузаИнкассируемаяЦенность(Операция) Экспорт
	
	Результат = Ложь;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей")
			ИЛИ Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Операция погрузки.
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Булево - Истина, если операция является операцией погрузки.
//
Функция ОперацияПогрузки(Операция) Экспорт
	
	Результат = Ложь;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка")
			ИЛИ Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей") Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Операция разгрузки
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Булево - Истина, если операция является операцией разгрузки.
//
Функция ОперацияРазгрузки(Операция) Экспорт
	
	Результат = Ложь;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка")
			ИЛИ Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Операция разгрузки
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Булево - Истина, если операция является операцией разгрузки.
//
Функция ОперацияРазрешеноОтложить(Операция) Экспорт
	
	Результат = Ложь;
	
	Если Ложь
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка")
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") 
		Или Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") 
	Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


// Функция - Список операций разгрузки
//
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  Массив - Перечисление.упТипыОперация.
//
Функция СписокОперацийРазгрузки() Экспорт
	
	мсвРезультат = Новый Массив;
	мсвРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка"));
	мсвРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей"));

	Возврат мсвРезультат;
	
КонецФункции

// Функция - Список операций погрузки
//
// Параметры:
//  Нет.
// 
// Возвращаемое значение:
//  Массив - Перечисление.упТипыОперация.
//
Функция СписокОперацийПогрузки() Экспорт
	
	мсвРезультат = Новый Массив;
	мсвРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка"));
	мсвРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей"));

	Возврат мсвРезультат;
	
КонецФункции

// Функция - Обратная операция.
//
// Параметры:
//  Операция - Перечисление.упТипыОперация	 - операция.
// 
// Возвращаемое значение:
//  Операция - Перечисление.упТипыОперация	 - операция.
//
Функция ОбратнаяОперация(Операция) Экспорт
	
	Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПустаяСсылка");
	
	Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка") Тогда
		Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка");
	ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка") Тогда
		Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка");
	ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей") Тогда
		Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей");
	ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") Тогда
		Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей");
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Функция - Операция погрузки по грузовому месту.
//
// Параметры:
//  ГрузовоеМесто	 - Справочник.упГрузовыеМеста - грузовое место.
// 
// Возвращаемое значение:
//   Операция - Перечисление.упТипыОперация	 - операция.
//
Функция ОперацияПогрузкиПоГрузовомуМесту(ГрузовоеМесто) Экспорт
	
	Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка");
	Если ЗначениеЗаполнено(ГрузовоеМесто) Тогда
		ВидГруза = упЗаданиеНаПеревозкуВызовСервера.ВидГруза(ГрузовоеМесто);
		Если ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.ИнкассируемаяЦенность") Тогда
			Результат = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей");	
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Функция - Действие отменяющее задание.
//
// Параметры:
//  ДействиеПриРаботе	 - Перечисление.упДействияПоРаботе - действие
// 
// Возвращаемое значение:
//   Булево - Истина, если передано действие которое отменяет задание.
//
Функция ДействиеОтменяющееЗадание(ДействиеПриРаботе) Экспорт
	
	Результат = Ложь;
	
	Если ДействиеПриРаботе = ПредопределенноеЗначение("Перечисление.упДействияПоРаботе.ОтменитьЗадание")
			ИЛИ ДействиеПриРаботе = ПредопределенноеЗначение("Перечисление.упДействияПоРаботе.ОтменитьЗаявку") Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура - Изменить признак прохождения точки
//
// Параметры:
//  ТекущиеДанные	 - 	 - 
//  Маршрут		 - 	 - 
//  Работы		 - 	 - 
//  Задания		 - 	 - 
//
Процедура ИзменитьПризнакПрохожденияТочки(ТекущиеДанные, Маршрут, Работы, Задания) Экспорт
	
	Выполнена = ТекущиеДанные.Пройдена;
	Отменена	= ТекущиеДанные.Отменена;
	ИдентификаторТочки = ТекущиеДанные.Идентификатор;
	
	// Изменить признак соответствующих работ
	сткОтбора = Новый Структура();
	сткОтбора.Вставить("Идентификатор", ИдентификаторТочки);
			
	мсвИдентификаторыТочекСОтмененнымиРаботами = Новый Массив;
	
	мсвСтрокиРабот = Работы.НайтиСтроки(сткОтбора);
	Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
		СтрокаРабота.Выполнена 	= Выполнена;
		СтрокаРабота.Отменена 	= Отменена;
		
		Если Отменена Тогда
			
			// Изменить признак выполнена у основных работ по заданию/грузовому месту работы
			сткОтборПоЗаданию = Новый Структура();
			сткОтборПоЗаданию.Вставить("Задание", 		СтрокаРабота.Задание);
			сткОтборПоЗаданию.Вставить("ГрузовоеМесто", СтрокаРабота.ГрузовоеМесто);
			
			мсвСтрокиРаботПоЗаданию	= Работы.НайтиСтроки(сткОтборПоЗаданию);
			
			Для каждого СтрокаРаботаПоЗаданию Из мсвСтрокиРаботПоЗаданию Цикл
				Если СтрокаРаботаПоЗаданию.Идентификатор = ИдентификаторТочки Тогда
					Продолжить;
				КонецЕсли;
				мсвИдентификаторыТочекСОтмененнымиРаботами.Добавить(СтрокаРаботаПоЗаданию.Идентификатор);
				СтрокаРаботаПоЗаданию.Выполнена 	= Выполнена;
				СтрокаРаботаПоЗаданию.Отменена 	= Отменена;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	мсвИдентификаторыТочекСОтмененнымиРаботами = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвИдентификаторыТочекСОтмененнымиРаботами);
	
	// Проверить в точках с отмененными работами
	Если мсвИдентификаторыТочекСОтмененнымиРаботами.Количество() > 0 Тогда
		
		Для каждого ИдентификаторТочкиСОтмененнымиРаботами Из мсвИдентификаторыТочекСОтмененнымиРаботами Цикл
			
			сткОтбора = Новый Структура();
			сткОтбора.Вставить("Идентификатор", ИдентификаторТочкиСОтмененнымиРаботами);
			сткОтбора.Вставить("Отменена", 		Ложь);
			
			ЕстьОсновныеРаботы 	= Ложь;
			ОтменитьТочку 		= Ложь;
				
			мсвСтрокиВТочке = Работы.НайтиСтроки(сткОтбора);
			
			Если мсвСтрокиВТочке.Количество() > 0 Тогда
				Для каждого СтрокаРабота Из мсвСтрокиВТочке Цикл
					Если упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(СтрокаРабота.Операция) Тогда
						ЕстьОсновныеРаботы = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ ЕстьОсновныеРаботы Тогда
					ОтменитьТочку 		= Истина;
					Для каждого СтрокаРабота Из мсвСтрокиВТочке Цикл
						СтрокаРабота.Отменена = Истина;
						СтрокаРабота.Выполнена = Ложь;
					КонецЦикла;
				КонецЕсли;
			Иначе	
				ОтменитьТочку = Истина;				
			КонецЕсли;
			
			Если ОтменитьТочку Тогда
				сткОтбора = Новый Структура();
				сткОтбора.Вставить("Идентификатор", ИдентификаторТочкиСОтмененнымиРаботами);
				мсвСтрокиТочка = Маршрут.НайтиСтроки(сткОтбора);
				Если мсвСтрокиТочка.Количество()>0 Тогда
					мсвСтрокиТочка[0].Отменена 	= Истина;
					мсвСтрокиТочка[0].Пройдена = Ложь;
				КонецЕсли;
			КонецЕсли;

		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаЗадание Из Задания Цикл
		сткОтбор = Новый Структура("Задание", СтрокаЗадание.Задание);
		Если ЗначениеЗаполнено(СтрокаЗадание.ГрузовоеМесто) Тогда
			сткОтбор.Вставить("ГрузовоеМесто", СтрокаЗадание.ГрузовоеМесто);	
		КонецЕсли;
		сткОтбор.Вставить("Отменена", Ложь);	
		мсвСтроки = Работы.НайтиСтроки(сткОтбор);
		СтрокаЗадание.Отменено = мсвСтроки.Количество() = 0;
	КонецЦикла;
		
КонецПроцедуры

// Процедура - Изменить признак выполнения работы
//
// Параметры:
//  ТекущиеДанные	 - 	 - 
//  Маршрут		 - 	 - 
//  Работы		 - 	 - 
//  Задания		 - 	 - 
//
Процедура ИзменитьПризнакВыполненияРаботы(ТекущиеДанные, Маршрут, Работы, Задания) Экспорт
	
	Выполнена 		= ТекущиеДанные.Выполнена;
	Отменена			= ТекущиеДанные.Отменена;
	Задание			= ТекущиеДанные.Задание;
	ГрузовоеМесто 		= ТекущиеДанные.ГрузовоеМесто;
	ИдентификаторТочки 	= ТекущиеДанные.Идентификатор;
	Операция 			= ТекущиеДанные.Операция;
	
	мсвИдентификаторыТочек = Новый Массив;
	мсвИдентификаторыТочек.Добавить(ИдентификаторТочки);
	
	Если ОсновнаяОперация(Операция) Тогда
		сткОтбор = Новый Структура("Задание", Задание);
		Если ЗначениеЗаполнено(ГрузовоеМесто) Тогда
			сткОтбор.Вставить("ГрузовоеМесто", ГрузовоеМесто);	
		КонецЕсли;
		
		мсвСтроки = Работы.НайтиСтроки(сткОтбор);
		Для каждого СтрокаРабота Из мсвСтроки Цикл
			мсвИдентификаторыТочек.Добавить(СтрокаРабота.Идентификатор);
			СтрокаРабота.Выполнена 	= Выполнена;
			СтрокаРабота.Отменена 	= Отменена;
		КонецЦикла;
	КонецЕсли;
	
	мсвИдентификаторыТочек = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвИдентификаторыТочек);
		
	Для каждого ИдентификаторТочки Из мсвИдентификаторыТочек Цикл
		
		// Проанализировать точки с отмененными работами, если все работы в них отменены, то отменить точку
		Если Отменена Тогда
 			сткОтбор = Новый Структура("Идентификатор", ИдентификаторТочки);
			сткОтбор.Вставить("Отменена", Ложь);	
			
			мсвСтроки = Работы.НайтиСтроки(сткОтбор);
			
			Если мсвСтроки.Количество() = 0 Тогда
				
				мсвТочки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТочки));
				Если мсвТочки.Количество() > 0 Тогда
					мсвТочки[0].Пройдена = Выполнена;
					мсвТочки[0].Отменена = Отменена;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Выполнена Тогда	
			
			мсвТочки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТочки));
			Если мсвТочки.Количество() > 0 Тогда
				мсвТочки[0].Пройдена = Выполнена;
				мсвТочки[0].Отменена = Отменена;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
		
	// Проанализировать задания/грузовые места - отменить соответствующие строки 
	Для каждого СтрокаЗадание Из Задания Цикл
		сткОтбор = Новый Структура("Задание", СтрокаЗадание.Задание);
		Если ЗначениеЗаполнено(СтрокаЗадание.ГрузовоеМесто) Тогда
			сткОтбор.Вставить("ГрузовоеМесто", СтрокаЗадание.ГрузовоеМесто);	
		КонецЕсли;
		сткОтбор.Вставить("Отменена", Ложь);	
		мсвСтроки = Работы.НайтиСтроки(сткОтбор);
		СтрокаЗадание.Отменено = мсвСтроки.Количество() = 0;
	КонецЦикла;
		
КонецПроцедуры

// Процедура - Изменить все точки пройдены работы выполнены
//
// Параметры:
//  Маршрут	 - 	 - 
//  Работы	 - 	 - 
//  Задания	 - 	 - 
//
Процедура ИзменитьВсеТочкиПройденыРаботыВыполнены(Маршрут, Работы, Задания) Экспорт
	
	Для каждого СтрокаТочка Из Маршрут Цикл
		СтрокаТочка.Пройдена = Истина;
		СтрокаТочка.Отменена = Ложь;
	КонецЦикла;
	
	Для каждого СтрокаРабота Из Работы Цикл
		СтрокаРабота.Выполнена 	= Истина;
		СтрокаРабота.Отменена 	= Ложь;
	КонецЦикла;

	Для каждого СтрокаЗадание Из Задания Цикл
		СтрокаЗадание.Отменено 	= Ложь;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - Изменить все точки/работы как не выполненные
//
// Параметры:
//  Маршрут	 - 	 - 
//  Работы	 - 	 - 
//  Задания	 - 	 - 
//
Процедура ИзменитьВсеТочкиНеПройденыНеВыполнены(Маршрут, Работы, Задания) Экспорт
	
	Для каждого СтрокаТочка Из Маршрут Цикл
		СтрокаТочка.Пройдена = Ложь;
		СтрокаТочка.Отменена = Ложь;
	КонецЦикла;
	
	Для каждого СтрокаРабота Из Работы Цикл
		СтрокаРабота.Выполнена 	= Ложь;
		СтрокаРабота.Отменена 	= Ложь;
	КонецЦикла;

	Для каждого СтрокаЗадание Из Задания Цикл
		СтрокаЗадание.Отменено 	= Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Поиск переданного элемента в коллекции.
//
// Параметры:
//  КоллекцияЗначений - Коллекция - Коллекция значений.
//  ИмяКолонки - Строка - Наименование колонки.
//  Значение - Строка - Значение любого типа.
//
// Возвращаемое значение:
//   Элемент - Элемент коллекции.
//
Функция НайтиЗначениеВКоллекции(КоллекцияЗначений, ИмяКолонки, Значение) Экспорт
	
	Результат = Неопределено;
	
	Для каждого Элемент Из КоллекцияЗначений Цикл
		Если Элемент[ИмяКолонки] = Значение Тогда
			Результат = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РежимКорректировки(СпособПрохожденияТочекМаршрута, Статус) Экспорт
	
	Результат = Истина
			  И СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ОтдельнымДокументом")
			  И (Ложь
			  	Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению")
				Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполняется"));
	
	Возврат Результат;
	
КонецФункции

Функция НайтиТочкуРазгрузки(КоллекцияТочекПоМаршрута, Рейс, АдресРазгрузки, Регистратор, ПорядковыйНомерТекущейТочки)
	
	сткВозврата = Новый Структура("ТочкаРазгрузки, КоличествоТочек", Неопределено, 0);
	
	ТочкаРазгрузки 	= Неопределено;
	КоличествоТочек	= 0;
	
	Если ТипЗнч(КоллекцияТочекПоМаршрута) = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		КоличествоТочек = КоллекцияТочекПоМаршрута.Количество();
		Для каждого ЭлементТочка Из КоллекцияТочекПоМаршрута Цикл
			
			Если ЭлементТочка.ТочкаПройдена
				 ИЛИ ЗначениеЗаполнено(ЭлементТочка.Гараж) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементТочка.АдресТочки = АдресРазгрузки И ПорядковыйНомерТекущейТочки - 1 < КоллекцияТочекПоМаршрута.Индекс(ЭлементТочка) Тогда
				ТочкаРазгрузки = ЭлементТочка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	ИначеЕсли ТипЗнч(КоллекцияТочекПоМаршрута) = Тип("ТаблицаЗначений") Тогда	
		
		ТочкаРазгрузки  = КоллекцияТочекПоМаршрута.Найти(АдресРазгрузки, "Адрес");
		КоллекцияТочекПоМаршрута.Сортировать("СтрокаНомер");
		КоличествоТочек = КоллекцияТочекПоМаршрута[КоллекцияТочекПоМаршрута.Количество() - 1].СтрокаНомер;
		
		Для каждого Точка Из КоллекцияТочекПоМаршрута Цикл
			Если Точка.Адрес = АдресРазгрузки 
				И Точка.СтрокаНомер > ПорядковыйНомерТекущейТочки 
				И НЕ ЗначениеЗаполнено(Точка.Гараж) Тогда
				ТочкаРазгрузки = Точка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	#КонецЕсли 	
	ИначеЕсли КоллекцияТочекПоМаршрута <> Неопределено Тогда
		
		КоличествоТочек = КоллекцияТочекПоМаршрута.Количество();
		Для каждого ЭлементТочка Из КоллекцияТочекПоМаршрута Цикл
			
			Если ЭлементТочка.Пройдена
				ИЛИ ЗначениеЗаполнено(ЭлементТочка.Гараж) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементТочка.Адрес = АдресРазгрузки И ПорядковыйНомерТекущейТочки - 1 < КоллекцияТочекПоМаршрута.Индекс(ЭлементТочка) Тогда
				ТочкаРазгрузки = ЭлементТочка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе	
		сткТочки	= упРейсВызовСервера.ПолучитьТаблицуНепройденныхТочек(Рейс, Регистратор);
		КоллекцияТочекПоМаршрута	= сткТочки.НепройденныеТочки;
		КоличествоТочек		 		= сткТочки.КоличествоТочек;
		Для каждого Точка Из КоллекцияТочекПоМаршрута Цикл
			Если Точка.Адрес = АдресРазгрузки 
				И Точка.СтрокаНомер > ПорядковыйНомерТекущейТочки 
				И НЕ ЗначениеЗаполнено(Точка.Гараж) Тогда
				ТочкаРазгрузки = Точка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		
	КонецЕсли;
	
	сткВозврата.Вставить("ТочкаРазгрузки", 	ТочкаРазгрузки);
	сткВозврата.Вставить("КоличествоТочек", КоличествоТочек);
	
	Возврат сткВозврата;
	
КонецФункции

Функция ПоследняяТочкаМаршрута(КоллекцияТочекМаршрута)
	
	текРезультат = Неопределено;
	
	Если ТипЗнч(КоллекцияТочекМаршрута) = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		
		йй = КоллекцияТочекМаршрута.Количество() - 1;
		Пока йй >=0 Цикл
			
			ТочкаМаршрута = КоллекцияТочекМаршрута[йй];
			Если НЕ ТочкаМаршрута.ТочкаПройдена И НЕ ТочкаМаршрута.ТочкаОтменена Тогда
				текРезультат = ТочкаМаршрута;
				Прервать;
			КонецЕсли;
			йй = йй - 1;
		КонецЦикла;
	Иначе
		Если КоллекцияТочекМаршрута.Количество() > 0 Тогда
			текРезультат = КоллекцияТочекМаршрута[КоллекцияТочекМаршрута.Количество() - 1];
		КонецЕсли;
	КонецЕсли;		
	
	Возврат текРезультат;
	
КонецФункции

Процедура ИзменитьПорядокТочки(КоллекцияТочекМаршрута, ПоследняяТочка, ПорядокТочки)
	
	Если ТипЗнч(ПоследняяТочка) = Тип("ДанныеФормыЭлементДерева") Тогда
		КоличествоТочек = КоллекцияТочекМаршрута.Количество();
		ПоследняяТочка.ПорядокТочки = КоличествоТочек;
		ИндексТочки = КоллекцияТочекМаршрута.Индекс(ПоследняяТочка);
		КоллекцияТочекМаршрута.Сдвинуть(ИндексТочки, КоличествоТочек - ИндексТочки - 1);
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда		
	ИначеЕсли ТипЗнч(КоллекцияТочекМаршрута) = Тип("ТаблицаЗначений") Тогда
		ПоследняяТочка.СтрокаНомер 	= ПорядокТочки;	
		ПоследняяТочка.Изменена		= Истина;
	#КонецЕсли	
	Иначе
		КоличествоТочек = КоллекцияТочекМаршрута.Количество();
		ПоследняяТочка.СтрокаНомер = КоличествоТочек;
		ИндексТочки = КоллекцияТочекМаршрута.Индекс(ПоследняяТочка);
		КоллекцияТочекМаршрута.Сдвинуть(ИндексТочки, КоличествоТочек - ИндексТочки - 1);
	КонецЕсли;		
		
КонецПроцедуры

Функция ДобавитьТочкуРазгрузки(КоллекцияТочекМаршрута, Рейс, АдресРазгрузки, ПорядокТочки, ПлановоеПрибытие, НачалоРаботПлан, ПлановоеУбытие)
	
	ТочкаРазгрузки = КоллекцияТочекМаршрута.Добавить();				
	ТочкаРазгрузки.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию");
	
	Если ТипЗнч(КоллекцияТочекМаршрута) = Тип("ДанныеФормыКоллекцияЭлементовДерева") Тогда
		
		ТочкаРазгрузки.АдресТочки = АдресРазгрузки;
		ТочкаРазгрузки.ТочкаПройдена = Ложь;
		ТочкаРазгрузки.ЭтоАдрес = Истина;
		ТочкаРазгрузки.ТочкаОтменена = Ложь;
		ТочкаРазгрузки.НазначениеСтроки = АдресРазгрузки;
		ТочкаРазгрузки.ПорядокТочки = ПорядокТочки;
		ТочкаРазгрузки.ПрибытиеФакт = ПлановоеПрибытие;
		ТочкаРазгрузки.НачалоРаботФакт = НачалоРаботПлан;
		ТочкаРазгрузки.УбытиеФакт = ПлановоеУбытие;
		ТочкаРазгрузки.ИндексКартинки	= 1;
		ТочкаРазгрузки.ФормироватьПрохождениеТочки = Истина;
		ТочкаРазгрузки.Приоритет = 2;
		
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда		
	ИначеЕсли ТипЗнч(КоллекцияТочекМаршрута) = Тип("ТаблицаЗначений") Тогда
		
		ТочкаРазгрузки.Адрес = АдресРазгрузки;
		ТочкаРазгрузки.Изменена = Истина;
		ТочкаРазгрузки.Пройдена = Ложь;
		ТочкаРазгрузки.Отменена = Ложь;
		ТочкаРазгрузки.Идентификатор	= Новый УникальныйИдентификатор;
		ТочкаРазгрузки.СтрокаНомер = ПорядокТочки;
		ТочкаРазгрузки.КоличествоРабот = 0;
		ТочкаРазгрузки.ПлановоеПрибытие = ПлановоеПрибытие;
		ТочкаРазгрузки.НачалоРаботПлан = НачалоРаботПлан;
		ТочкаРазгрузки.ПлановоеУбытие = ПлановоеУбытие;		
		ТочкаРазгрузки.Приоритет = 2;
	#КонецЕсли
	Иначе
	
		ТочкаРазгрузки.Адрес = АдресРазгрузки;
		ТочкаРазгрузки.Пройдена = Ложь;
		ТочкаРазгрузки.Отменена = Ложь;
		ТочкаРазгрузки.Идентификатор = Новый УникальныйИдентификатор;
		ТочкаРазгрузки.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию");
		ТочкаРазгрузки.ИндексКартинки = ИндексКартинкиТочки(ТочкаРазгрузки.ТипТочки);
		ТочкаРазгрузки.СтрокаНомер = ПорядокТочки;
		ТочкаРазгрузки.ПлановоеПрибытие = ПлановоеПрибытие;
		ТочкаРазгрузки.ПлановоеУбытие = ПлановоеУбытие;
		ТочкаРазгрузки.Приоритет = 2;
		Если ТипЗнч(ТочкаРазгрузки) = Тип("ОбработкаТабличнаяЧастьСтрока.упКорректировкаРейса.Маршрут") Тогда
			ТочкаРазгрузки.Рейс = Рейс;
		КонецЕсли;

	КонецЕсли;		
	
	Возврат ТочкаРазгрузки;
	
КонецФункции

Функция ПорядковыйНомерТочки(ТочкаМаршрута)
	
	текРезультат = Неопределено;
	
	Если ТипЗнч(ТочкаМаршрута) = Тип("ДанныеФормыЭлементДерева") Тогда
		текРезультат = ТочкаМаршрута.ПорядокТочки;
	Иначе
		текРезультат = ТочкаМаршрута.СтрокаНомер;
	КонецЕсли;		
	
	Возврат текРезультат
КонецФункции

#КонецОбласти
