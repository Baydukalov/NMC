
#Область ПрограммныйИнтерфейс

// Функция - Обновить обработчик события
//
// Параметры:
//  ФорматСообщения	 - Перечисление.сшпФорматыСообщений - Формат поступившего сообщения.
//  Пакет			 - ОбъектXDTO - объект сообщения.
// 
// Возвращаемое значение:
// Булево  - Признак успешности обновления обработчика события.
//
Функция ОбновитьОбработчикСобытия(ФорматСообщения, Пакет) Экспорт
	статусВозврата = Истина;
	Попытка
		xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, Пакет);
		сткПараметры = сшпОбщегоНазначения.ПолучитьПараметрыСообщенияСтруктурой(xdtoПакет);
		классОбъекта = сткПараметры.ObjectClass;
		Если ТипЗнч(классОбъекта) = Тип("Строка") тогда
			типИнтеграции = ?(сткПараметры.Integration = "Ingoing", Перечисления.сшпТипыИнтеграции.Входящая, Перечисления.сшпТипыИнтеграции.Исходящая); 
			
			НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
				
			//если именился класс или тип интеграции обработчика, то удалим старый обработчик
			запросШаблон = Новый Запрос("ВЫБРАТЬ
			                            |	тбРепозиторий.ИмяКлассаОбъекта,
			                            |	тбРепозиторий.ТипИнтеграции
			                            |ИЗ
			                            |	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
			                            |ГДЕ
			                            |	тбРепозиторий.ИдентификаторШаблона = &ИдентификаторШаблона
			                            |	И (тбРепозиторий.ИмяКлассаОбъекта <> &ИмяКлассаОбъекта
			                            |			ИЛИ тбРепозиторий.ТипИнтеграции <> &ТипИнтеграции)");
			запросШаблон.УстановитьПараметр("ИдентификаторШаблона", Новый УникальныйИдентификатор(сткПараметры.Id));
			запросШаблон.УстановитьПараметр("ИмяКлассаОбъекта", классОбъекта);
			запросШаблон.УстановитьПараметр("ТипИнтеграции", типИнтеграции);
			результатЗапрос = запросШаблон.Выполнить();
			Если НЕ результатЗапрос.Пустой() тогда
				блокировкаСтатусы = Новый БлокировкаДанных;
				элементБлокировка = блокировкаСтатусы.Добавить("РегистрСведений.сшпРепозиторийОбъектовИнтеграции");
				элементБлокировка.ИсточникДанных = результатЗапрос;
				элементБлокировка.ИспользоватьИзИсточникаДанных("ИмяКлассаОбъекта", "ИмяКлассаОбъекта");
				элементБлокировка.ИспользоватьИзИсточникаДанных("ТипИнтеграции", "ТипИнтеграции");
				элементБлокировка.Режим = РежимБлокировкиДанных.Исключительный;
				блокировкаСтатусы.Заблокировать();
				выборкаЗапрос = результатЗапрос.Выбрать();
				Пока выборкаЗапрос.Следующий() цикл
					записьОбработчик = РегистрыСведений.сшпРепозиторийОбъектовИнтеграции.СоздатьМенеджерЗаписи();
					записьОбработчик.ИмяКлассаОбъекта = выборкаЗапрос.ИмяКлассаОбъекта;
					записьОбработчик.ТипИнтеграции = выборкаЗапрос.ТипИнтеграции;
					записьОбработчик.Удалить();
					
					записьСтатус = РегистрыСведений.сшпСтатусыОбработчиков.СоздатьМенеджерЗаписи();
					записьСтатус.ИдентификаторОбработчика = Новый УникальныйИдентификатор(сткПараметры.Id);
					записьСтатус.Удалить();
					
					Если выборкаЗапрос.ТипИнтеграции = Перечисления.сшпТипыИнтеграции.Исходящая тогда
						сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпВерсияОбработчиков", Строка(Новый УникальныйИдентификатор));
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;
			
			текЗапись = РегистрыСведений.сшпРепозиторийОбъектовИнтеграции.СоздатьМенеджерЗаписи();
			текЗапись.ИмяКлассаОбъекта = классОбъекта;
			текЗапись.ТипИнтеграции = типИнтеграции;
			текЗапись.ПроцедураОбработки = Строка(xdtoПакет.Body) + Символы.ПС + "~Выход:";
			текЗапись.Версия = сткПараметры.Version; 
			текЗапись.ВерсияПеременных = сткПараметры.variableVersion; 
			текЗапись.ИдентификаторШаблона = Новый УникальныйИдентификатор(сткПараметры.Id);
			текЗапись.Наименование = сткПараметры.Name;
			стрМетодХранения = Неопределено;
			сткПараметры.Свойство("KeepObjectLink", стрМетодХранения);
			Если стрМетодХранения = Неопределено тогда
				текЗапись.МетодХранения = Перечисления.сшпМетодХранения.ПоСсылке;
			Иначе	
				текЗапись.МетодХранения = ?(стрМетодХранения = Истина, Перечисления.сшпМетодХранения.ПоСсылке, Перечисления.сшпМетодХранения.Сериализация);
			КонецЕсли;
			текЗапись.Записать(Истина);
			Если типИнтеграции = Перечисления.сшпТипыИнтеграции.Исходящая тогда
				сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпВерсияОбработчиков", Строка(Новый УникальныйИдентификатор));
			КонецЕсли;
			
			новыйСтатус = ?(сткПараметры.IsStarted = Истина, Перечисления.сшпСтатусыОбработчиков.Включен, Перечисления.сшпСтатусыОбработчиков.Отключен);
			сшпРаботаСДанными.УстановитьСтатусОбработчика(текЗапись.ИдентификаторШаблона, новыйСтатус);
			ОбновитьСтатусыСообщенийБезОбработчика(типИнтеграции, классОбъекта);
			
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации("Datareon. Обновление обработчика", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Если ТранзакцияАктивна() тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		статусВозврата = Ложь;
	КонецПопытки;
	Возврат статусВозврата;	
КонецФункции

// Функция - Удалить обработчик события
//
// Параметры:
//  ФорматСообщения	 - Перечисление.сшпФорматыСообщений - Формат поступившего сообщения.
//  Пакет			 - ОбъектXDTO - объект сообщения.
// 
// Возвращаемое значение:
// Булево  - Признак успешности выполнения операции.
//
Функция УдалитьОбработчикСобытия(ФорматСообщения, Пакет) Экспорт
	статусВозврата = Истина;
	Попытка
		xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, Пакет);
		ИдентификаторШаблона = xdtoПакет.Body;
		Если ТипЗнч(ИдентификаторШаблона) = Тип("Строка") тогда
			запросШаблон = Новый Запрос("ВЫБРАТЬ
			|	тбРепозиторий.ИмяКлассаОбъекта,
			|	тбРепозиторий.ТипИнтеграции
			|ИЗ
			|	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
			|ГДЕ
			|	тбРепозиторий.ИдентификаторШаблона = &ИдентификаторШаблона");
			запросШаблон.УстановитьПараметр("ИдентификаторШаблона", Новый УникальныйИдентификатор(ИдентификаторШаблона));
			результатЗапрос = запросШаблон.Выполнить();
			Если НЕ результатЗапрос.Пустой() тогда
				НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
				блокировкаСтатусы = Новый БлокировкаДанных;
				элементБлокировка = блокировкаСтатусы.Добавить("РегистрСведений.сшпРепозиторийОбъектовИнтеграции");
				элементБлокировка.ИсточникДанных = результатЗапрос;
				элементБлокировка.ИспользоватьИзИсточникаДанных("ИмяКлассаОбъекта", "ИмяКлассаОбъекта");
				элементБлокировка.ИспользоватьИзИсточникаДанных("ТипИнтеграции", "ТипИнтеграции");
				элементБлокировка.Режим = РежимБлокировкиДанных.Исключительный;
				блокировкаСтатусы.Заблокировать();
				выборкаЗапрос = результатЗапрос.Выбрать();
				Пока выборкаЗапрос.Следующий() цикл
					записьОбработчик = РегистрыСведений.сшпРепозиторийОбъектовИнтеграции.СоздатьМенеджерЗаписи();
					записьОбработчик.ИмяКлассаОбъекта = выборкаЗапрос.ИмяКлассаОбъекта;
					записьОбработчик.ТипИнтеграции = выборкаЗапрос.ТипИнтеграции;
					записьОбработчик.Удалить();
					
					записьСтатус = РегистрыСведений.сшпСтатусыОбработчиков.СоздатьМенеджерЗаписи();
					записьСтатус.ИдентификаторОбработчика = Новый УникальныйИдентификатор(ИдентификаторШаблона);
					записьСтатус.Удалить();
					
					Если выборкаЗапрос.ТипИнтеграции = Перечисления.сшпТипыИнтеграции.Исходящая тогда
						сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпВерсияОбработчиков", Строка(Новый УникальныйИдентификатор));
					КонецЕсли;	
				КонецЦикла;
				ЗафиксироватьТранзакцию();
			КонецЕсли;
		КонецЕсли;
	Исключение
		описаниеОшибка = ОписаниеОшибки();
		Если ТранзакцияАктивна() тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Datareon. Удаление обработчика", УровеньЖурналаРегистрации.Ошибка,, ИдентификаторШаблона, описаниеОшибка);
		статусВозврата = Ложь;
	КонецПопытки;
	Возврат статусВозврата;
КонецФункции

// Функция - Получить список версий обработчиков событий
// 
// Возвращаемое значение:
// Строка  - Список шаблонов с указанием текущей версии в формате XML.
//
Функция ПолучитьСписокВерсийОбработчиковСобытий() Экспорт
	xmlЗапись = Новый ЗаписьXML;
	xmlЗапись.УстановитьСтроку("UTF-8");
	запросШаблон = Новый Запрос("ВЫБРАТЬ
	|	тбРепозиторий.ИдентификаторШаблона,
	|	тбРепозиторий.Версия,
	|	тбРепозиторий.ВерсияПеременных,
	|	ВЫБОР КОГДА тбРепозиторий.МетодХранения = ЗНАЧЕНИЕ(Перечисление.сшпМетодХранения.ПоСсылке) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК МетодХранения,
	|	ВЫБОР КОГДА сшпСтатусыОбработчиков.Статус = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыОбработчиков.Включен) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК Статус
	|ИЗ
	|	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСтатусыОбработчиков КАК сшпСтатусыОбработчиков
	|		ПО тбРепозиторий.ИдентификаторШаблона = сшпСтатусыОбработчиков.ИдентификаторОбработчика");
	результатЗапрос = запросШаблон.Выполнить();
	Если НЕ результатЗапрос.Пустой() тогда
		xmlЗапись.ЗаписатьНачалоЭлемента("templates1C");
		выборкаЗапрос = результатЗапрос.Выбрать();
		Пока выборкаЗапрос.Следующий() цикл
			xmlЗапись.ЗаписатьНачалоЭлемента("template");
			xmlЗапись.ЗаписатьАтрибут("id", XMLСтрока(выборкаЗапрос.ИдентификаторШаблона));
			xmlЗапись.ЗаписатьАтрибут("version", выборкаЗапрос.Версия);
			xmlЗапись.ЗаписатьАтрибут("variableVersion", выборкаЗапрос.ВерсияПеременных);
			xmlЗапись.ЗаписатьАтрибут("KeepObjectLink", XMLСтрока(выборкаЗапрос.МетодХранения));
			xmlЗапись.ЗаписатьАтрибут("isStarted", XMLСтрока(выборкаЗапрос.Статус));
			xmlЗапись.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlЗапись.ЗаписатьКонецЭлемента();
	КонецЕсли;	
	РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-ActualTemplateList", xmlЗапись.Закрыть());
	
	Возврат сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(РезультатОбработки);
КонецФункции

// Функция - Получить список обработчиков событий
// 
// Возвращаемое значение:
// Строка  - массив шаблонов с текстом обработчиков в формате XML.
//
Функция ПолучитьСписокОбработчиковСобытий(ФорматСообщения, Пакет) Экспорт
	идентификаторШаблона = Неопределено;
	Попытка
		xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, Пакет);
		xdtoCommand = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, xdtoПакет.Body);
		Если НЕ xdtoCommand.template = Неопределено тогда
			идентификаторШаблона = Новый УникальныйИдентификатор(xdtoCommand.template.id);
		КонецЕсли;
	Исключение
	КонецПопытки;	
	xmlЗапись = Новый ЗаписьXML;
	xmlЗапись.УстановитьСтроку("UTF-8");
	запросШаблон = Новый Запрос("ВЫБРАТЬ
	|	тбРепозиторий.ИмяКлассаОбъекта,
	|	тбРепозиторий.ТипИнтеграции,
	|	тбРепозиторий.ПроцедураОбработки,
	|	тбРепозиторий.Версия,
	|	тбРепозиторий.ИдентификаторШаблона,
	|	ВЫБОР КОГДА тбРепозиторий.МетодХранения = ЗНАЧЕНИЕ(Перечисление.сшпМетодХранения.ПоСсылке) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК МетодХранения,
	|	ВЫБОР КОГДА сшпСтатусыОбработчиков.Статус = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыОбработчиков.Включен) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК Статус
	|ИЗ
	|	РегистрСведений.сшпРепозиторийОбъектовИнтеграции КАК тбРепозиторий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСтатусыОбработчиков КАК сшпСтатусыОбработчиков
	|		ПО тбРепозиторий.ИдентификаторШаблона = сшпСтатусыОбработчиков.ИдентификаторОбработчика");
	Если ЗначениеЗаполнено(идентификаторШаблона) тогда
		запросШаблон.Текст = запросШаблон.Текст + "
		|ГДЕ
		|	тбРепозиторий.ИдентификаторШаблона = &ИдентификаторШаблона";
		запросШаблон.УстановитьПараметр("ИдентификаторШаблона", идентификаторШаблона);
	конецЕсли;
	результатЗапрос = запросШаблон.Выполнить();
	Если НЕ результатЗапрос.Пустой() тогда
		xmlЗапись.ЗаписатьНачалоЭлемента("TemplateList");
		выборкаЗапрос = результатЗапрос.Выбрать();
		Пока выборкаЗапрос.Следующий() цикл
			xmlЗапись.ЗаписатьНачалоЭлемента("Template1C");
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, Строка(выборкаЗапрос.ИдентификаторШаблона), "Id"); 
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, выборкаЗапрос.Версия, "Version"); 
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, "Обработчик " + выборкаЗапрос.ИмяКлассаОбъекта, "Name"); 
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, ?(выборкаЗапрос.ТипИнтеграции = Перечисления.сшпТипыИнтеграции.Входящая,"Ingoing", "Outgoing"), "Integration"); 
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, выборкаЗапрос.ИмяКлассаОбъекта, "ObjectClass");
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, выборкаЗапрос.ПроцедураОбработки, "Code", Истина);
			сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, выборкаЗапрос.МетодХранения, "KeepObjectLink");
			xmlЗапись.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlЗапись.ЗаписатьКонецЭлемента();
	КонецЕсли;	
	РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-TemplateSyncMessage", xmlЗапись.Закрыть());
	Возврат сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(РезультатОбработки);
КонецФункции

// Функция - Получить номер версии подсистемы 1С
// 
// Возвращаемое значение:
// Булево  - признак успешно сформированного сообщения.
//
Функция ПолучитьВерсиюПодсистемы1С() Экспорт
	xmlЗапись = Новый ЗаписьXML;
	xmlЗапись.УстановитьСтроку("UTF-8");
	xmlЗапись.ЗаписатьНачалоЭлемента("info1C");
	xmlЗапись.ЗаписатьАтрибут("version", Строка(Формат(сшпКэшируемыеФункции.ВерсияПодсистемы(),"ЧГ=0")));
	СисИнфо = Новый СистемнаяИнформация;
	xmlЗапись.ЗаписатьАтрибут("platform", СисИнфо.ВерсияПриложения);
	xmlЗапись.ЗаписатьКонецЭлемента();

	РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-ActualVersion1C", xmlЗапись.Закрыть());
	
	Возврат сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(РезультатОбработки);
КонецФункции

// Функция - Получить состояние обработки данных по запросу GPS
// 
// Возвращаемое значение:
// ЗаписьXML  - состояние обработки.
//
Функция ПолучитьСостониеОбработки(ФорматСообщения, Пакет) Экспорт
	
	xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, Пакет);
	сткПараметры = сшпОбщегоНазначения.ПолучитьПараметрыСообщенияСтруктурой(xdtoПакет);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(1) КАК Количество
		|ИЗ
		|	РегистрСведений.сшпОчередьВходящихСообщений КАК сшпОчередьВходящихСообщений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК сшпСостояниеСообщений
		|		ПО сшпОчередьВходящихСообщений.ИдентификаторСообщения = сшпСостояниеСообщений.ИдентификаторСообщения
		|			И (сшпСостояниеСообщений.СтатусСообщения В (&СписокСтатусов))";
	
	Запрос.УстановитьПараметр("СписокСтатусов", сшпКэшируемыеФункции.ПоучитьСписокРабочихСтатусов());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СостоянияОбработки = Новый Структура;
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СостоянияОбработки.Вставить("InputUnprocessedCountMessages", ВыборкаДетальныеЗаписи.Количество);
	Иначе
		СостоянияОбработки.Вставить("InputUnprocessedCountMessages", 0);
	КонецЕсли;
	
	МаксимальноеКоличествоСообщений = 0;
	сшпОтключитьПотокОбработкиОчередиИсходящих = ПолучитьФункциональнуюОпцию("сшпОтключитьПотокОбработкиОчередиИсходящих");
	Если сткПараметры.Свойство("OutputUnprocessedCountMessagesMax", МаксимальноеКоличествоСообщений) Тогда 
		МинимальноеКоличествоСообщений = ?(сткПараметры.Свойство("OutputUnprocessedCountMessagesMin", МинимальноеКоличествоСообщений), сткПараметры.OutputUnprocessedCountMessagesMin, 0);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(1) КАК Количество
		|ИЗ
		|	РегистрСведений.сшпОчередьОтправляемыхСообщений КАК сшпОчередьОтправляемыхСообщений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК сшпСостояниеСообщений
		|		ПО сшпОчередьОтправляемыхСообщений.ИдентификаторСообщения = сшпСостояниеСообщений.ИдентификаторСообщения
		|			И (сшпСостояниеСообщений.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОжиданиеОтправки))";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Если Не сшпОтключитьПотокОбработкиОчередиИсходящих И ВыборкаДетальныеЗаписи.Количество > МаксимальноеКоличествоСообщений Тогда 
				ОстановитьОбработкуИсходящих(Истина);
			ИначеЕсли сшпОтключитьПотокОбработкиОчередиИсходящих И ВыборкаДетальныеЗаписи.Количество < МинимальноеКоличествоСообщений Тогда
				ОстановитьОбработкуИсходящих(Ложь);
			КонецЕсли;
		ИначеЕсли сшпОтключитьПотокОбработкиОчередиИсходящих Тогда 
			ОстановитьОбработкуИсходящих(Ложь);
		КонецЕсли;
	ИначеЕсли сшпОтключитьПотокОбработкиОчередиИсходящих Тогда 
		ОстановитьОбработкуИсходящих(Ложь);
	КонецЕсли;
	
	РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-ProcessingStatus1C");
	РезультатОбработки.Properties = СостоянияОбработки;
	
	Возврат сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(РезультатОбработки);
КонецФункции

Процедура ОстановитьОбработкуИсходящих(Остановить)
	Константы.сшпОтключитьПотокОбработкиОчередиИсходящих.Установить(Остановить);
КонецПроцедуры

// Функция - Получить параметры подключения к информационной базе
// 
// Возвращаемое значение:
// ЗаписьXML  - параметры подключения к информационной базе 1С.
//
Функция ПолучитьПараметрыПодключения() Экспорт
	СтрокаСоединения = СтрРазделить(СтрокаСоединенияИнформационнойБазы(), ";");
	Если СтрокаСоединения.Количество() < 2 Тогда 
		ИмяСервера = "";
		ИмяИБ = "";
	Иначе 
		ИмяСервера = Сред(СтрокаСоединения[0], 7, СтрДлина(СтрокаСоединения[0])-7);
		ИмяИБ = Сред(СтрокаСоединения[1], 6, СтрДлина(СтрокаСоединения[1])-6);
	КонецЕсли;
	
	xmlЗапись = Новый ЗаписьXML;
	xmlЗапись.УстановитьСтроку("UTF-8");
	xmlЗапись.ЗаписатьНачалоЭлемента("database");
	xmlЗапись.ЗаписатьАтрибут("name", ИмяИБ);
	xmlЗапись.ЗаписатьАтрибут("serverUri", ИмяСервера);
	xmlЗапись.ЗаписатьКонецЭлемента();

	РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-InfoBaseConnectionString1C", xmlЗапись.Закрыть());
	
	Возврат сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(РезультатОбработки);
КонецФункции

// Функция - Получить структуру конфигурации
// 
// Возвращаемое значение:
// Строка  - Описание структуры конфигурации в формате передачи сообщений (XML, FastInfoset, JSON).
//
Функция ПолучитьСтруктуруКонфигурации() Экспорт
	xmlКонфигурация = Новый ЗаписьXML;
	xmlКонфигурация.УстановитьСтроку("UTF-8");
	xmlКонфигурация.ЗаписатьНачалоЭлемента("Конфигурация");
	xmlКонфигурация.ЗаписатьАтрибут("Имя", Метаданные.Имя);
	xmlКонфигурация.ЗаписатьАтрибут("Версия", Метаданные.Версия);
	
	СоответствиеПредставленийТипов = Новый Соответствие;
	ДобавитьВДеревоСтрокиПоМетаданным(Метаданные, "Константы", xmlКонфигурация,, СоответствиеПредставленийТипов);

	МассивОбщихРеквизитов = Новый Массив;
	Для каждого элементМетаданных из Метаданные.ОбщиеРеквизиты цикл
		МассивТипов = элементМетаданных.Тип.Типы();
		ОписаниеТипа = "";
		Для Каждого ЭлементМассиваТипов Из МассивТипов Цикл
			ПредставлениеТипа = СоответствиеПредставленийТипов[ЭлементМассиваТипов];
			Если ПредставлениеТипа = Неопределено Тогда 
				ПредставлениеТипа = ПолучитьПредставлениеТипа(ЭлементМассиваТипов);
				СоответствиеПредставленийТипов.Вставить(ЭлементМассиваТипов, ПредставлениеТипа);
			КонецЕсли;
			ОписаниеТипа = ОписаниеТипа + ?(ЗначениеЗаполнено(ОписаниеТипа), ";", "") + ПредставлениеТипа;
		КонецЦикла;
		
		АвтоИспользование = элементМетаданных.АвтоИспользование = Метаданные.СвойстваОбъектов.АвтоИспользованиеОбщегоРеквизита.Использовать;
		СоставРеквизита = Новый Массив;
		Для Каждого ЭлементСостава Из элементМетаданных.Состав Цикл 
			Если АвтоИспользование И ЭлементСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Авто
				Или ЭлементСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда 
				СоставРеквизита.Добавить(ЭлементСостава.Метаданные.ПолноеИмя());
			КонецЕсли;
		КонецЦикла;
		МассивОбщихРеквизитов.Добавить(Новый Структура("Имя, Состав, ОписаниеТипа", элементМетаданных.Имя, СоставРеквизита, ОписаниеТипа)); 
	КонецЦикла;
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("Справочники");
	Для каждого элементМетаданных из Метаданные.Справочники цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("Документы");
	Для каждого элементМетаданных из Метаданные.Документы цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("Перечисления");
	Для каждого элементМетаданных из Метаданные.Перечисления цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "ЗначенияПеречисления", xmlКонфигурация, Ложь, СоответствиеПредставленийТипов);
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("РегистрыСведений");
	Для каждого элементМетаданных из Метаданные.РегистрыСведений цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Измерения", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Ресурсы", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("ПланыВидовХарактеристик");
	Для каждого элементМетаданных из Метаданные.ПланыВидовХарактеристик цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("ПланыВидовРасчета");
	Для каждого элементМетаданных из Метаданные.ПланыВидовРасчета цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("ПланыСчетов");
	Для каждого элементМетаданных из Метаданные.ПланыСчетов цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("РегистрыБухгалтерии");
	Для каждого элементМетаданных из Метаданные.РегистрыБухгалтерии цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Измерения", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Ресурсы", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("РегистрыНакопления");
	Для каждого элементМетаданных из Метаданные.РегистрыНакопления цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Измерения", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Ресурсы", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("РегистрыРасчета");
	Для каждого элементМетаданных из Метаданные.РегистрыРасчета цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Измерения", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Ресурсы", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("БизнесПроцессы");
	Для каждого элементМетаданных из Метаданные.БизнесПроцессы цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьНачалоЭлемента("Задачи");
	Для каждого элементМетаданных из Метаданные.Задачи цикл
		xmlКонфигурация.ЗаписатьНачалоЭлемента(элементМетаданных.Имя);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "СтандартныеРеквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоМетаданным(элементМетаданных, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
		ДобавитьВДеревоСтрокиПоОбщимРеквизитам(элементМетаданных, "ОбщиеРеквизиты", xmlКонфигурация, МассивОбщихРеквизитов);
		xmlКонфигурация.ЗаписатьНачалоЭлемента("ТабличныеЧасти");
		Для каждого табличнаяЧасть из элементМетаданных.ТабличныеЧасти цикл
			xmlКонфигурация.ЗаписатьНачалоЭлемента(табличнаяЧасть.Имя);
			ДобавитьВДеревоСтрокиПоМетаданным(табличнаяЧасть, "Реквизиты", xmlКонфигурация,, СоответствиеПредставленийТипов);
			xmlКонфигурация.ЗаписатьКонецЭлемента();
		КонецЦикла;
		xmlКонфигурация.ЗаписатьКонецЭлемента();
		
		xmlКонфигурация.ЗаписатьКонецЭлемента();
	КонецЦикла;
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	xmlКонфигурация.ЗаписатьКонецЭлемента();
	
	пакет = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-1c-Metadata", xmlКонфигурация.Закрыть());
	
	Возврат сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(пакет);
КонецФункции

// Процедура - Добавить в дерево строки по метаданным
//
// Параметры:
//  УзелМетаданных	 - Метаданные - узел метаданных изкоторого будут читаться объекты. 
//  ИмяОбъекта		 - Строка - имя объекта метаданных 
//  xmlУзел			 - ЗаписьXML - узел в которые будет выполняться добавление элементов.
//
Процедура ДобавитьВДеревоСтрокиПоМетаданным(УзелМетаданных, ИмяОбъекта, xmlУзел, ДобавлятьОписаниеТипа = Истина, СоответствиеПредставленийТипов)
	xmlУзел.ЗаписатьНачалоЭлемента(ИмяОбъекта);
	Попытка
		Для каждого элементМетаданных из УзелМетаданных[ИмяОбъекта] цикл
			Если ДобавлятьОписаниеТипа Тогда 
				ОписаниеТипа = "";
				МассивТипов = элементМетаданных.Тип.Типы();
				Для Каждого ЭлементМассиваТипов Из МассивТипов Цикл
					ПредставлениеТипа = СоответствиеПредставленийТипов[ЭлементМассиваТипов];
					Если ПредставлениеТипа = Неопределено Тогда 
						ПредставлениеТипа = ПолучитьПредставлениеТипа(ЭлементМассиваТипов);
						СоответствиеПредставленийТипов.Вставить(ЭлементМассиваТипов, ПредставлениеТипа);
					КонецЕсли;
					ОписаниеТипа = ОписаниеТипа + ?(ЗначениеЗаполнено(ОписаниеТипа), ";", "") + ПредставлениеТипа;
				КонецЦикла;
			
				сшпФункцииРаботыXML.СформироватьУзелXML(xmlУзел, Неопределено, элементМетаданных.Имя, , Новый Структура("Тип", ОписаниеТипа));
			Иначе
				сшпФункцииРаботыXML.СформироватьУзелXML(xmlУзел, Неопределено, элементМетаданных.Имя, ,);
			КонецЕсли;
		КонецЦикла;
	Исключение
	КонецПопытки;
	xmlУзел.ЗаписатьКонецЭлемента();
КонецПроцедуры

// Процедура - Добавить в дерево строки по метаданным
//
// Параметры:
//  УзелМетаданных	 - Метаданные - узел метаданных изкоторого будут читаться объекты. 
//  ИмяОбъекта		 - Строка - имя объекта метаданных 
//  xmlУзел			 - ЗаписьXML - узел в которые будет выполняться добавление элементов.
//
Процедура ДобавитьВДеревоСтрокиПоОбщимРеквизитам(УзелМетаданных, ИмяОбъекта, xmlУзел, МассивОбщихРеквизитов)
	xmlУзел.ЗаписатьНачалоЭлемента(ИмяОбъекта);
	ИмяУзла = УзелМетаданных.ПолноеИмя();
	Попытка
		Для каждого ЭлементСоответствия из МассивОбщихРеквизитов цикл
			Если ЭлементСоответствия.Состав.Найти(ИмяУзла) <> Неопределено Тогда 
				сшпФункцииРаботыXML.СформироватьУзелXML(xmlУзел, Неопределено, ЭлементСоответствия.Имя, , Новый Структура("Тип", ЭлементСоответствия.ОписаниеТипа));
			КонецЕсли;
		КонецЦикла;
	Исключение
	КонецПопытки;
	xmlУзел.ЗаписатьКонецЭлемента();
КонецПроцедуры

// Функция - ПолучитьПредставлениеТипа
функция ПолучитьПредставлениеТипа(ТипОписания) Экспорт 
	ОМ = Метаданные.НайтиПоТипу(ТипОписания);
	Если Не ОМ = Неопределено Тогда 
		ИмяОМ = ОМ.Имя;
		НайденныйТип = Метаданные.Перечисления.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "Перечисления." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.Справочники.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "Справочники." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.Документы.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "Документы." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.ПланыВидовХарактеристик.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "ПланыВидовХарактеристик." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.ПланыВидовРасчета.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "ПланыВидовРасчета." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.ПланыСчетов.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "ПланыСчетов." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.БизнесПроцессы.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "БизнесПроцессы." + ОМ.Имя;
		КонецЕсли;
		
		НайденныйТип = Метаданные.Задачи.Найти(ИмяОМ);
		Если НайденныйТип <> Неопределено Тогда 
			Возврат "Задачи." + ОМ.Имя;
		КонецЕсли;
	Иначе
		Возврат Строка(ТипОписания);
	КонецЕсли;
КонецФункции

// Процедура - Обновить статусы сообщений без обработчика
//
// Параметры:
//  ТипИнтеграции	 - Перечисление.сшпТипыИнтеграции - Тип интеграции для определения очереди в котоой надо обработать сообщения. 
//  Класс			 - Строка - Идентификатор класса для которого поступил обработчик и требуется изменить статус сообщений на "ОжиданиеОбработки".
//
Процедура ОбновитьСтатусыСообщенийБезОбработчика(ТипИнтеграции, Класс) Экспорт
	текстЗапрос = "";
	Если ТипИнтеграции = Перечисления.сшпТипыИнтеграции.Входящая тогда
		текстЗапрос = "ВЫБРАТЬ
		|	тбВходящая.ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.сшпОчередьВходящихСообщений КАК тбВходящая
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбВходящая.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбВходящая.КлассСообщения = &Класс)
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОтсутствуетОбработчик))";
	Иначе
		текстЗапрос = "ВЫБРАТЬ
		|	тбИсходящая.ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.сшпОчередьИсходящихСообщений КАК тбИсходящая
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбИсходящая.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбИсходящая.ОбъектСобытия = &Класс)
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОтсутствуетОбработчик))";
	КонецЕсли;	
	запросОбновление = Новый Запрос(текстЗапрос);
	запросОбновление.УстановитьПараметр("Класс", Класс);
	результатЗапрос = запросОбновление.Выполнить();
	Если НЕ результатЗапрос.Пустой() тогда
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Попытка
			блокировкаСтатусы = Новый БлокировкаДанных;
			элементБлокировка = блокировкаСтатусы.Добавить("РегистрСведений.сшпСостояниеСообщений");
			элементБлокировка.ИсточникДанных = результатЗапрос;
			элементБлокировка.ИспользоватьИзИсточникаДанных("ИдентификаторСообщения", "ИдентификаторСообщения");
			элементБлокировка.Режим = РежимБлокировкиДанных.Исключительный;
			блокировкаСтатусы.Заблокировать();
			выборкаЗапрос = результатЗапрос.Выбрать();
			Пока выборкаЗапрос.Следующий() цикл
				сшпРаботаСДанными.УстановитьСостояниеСообщения(выборкаЗапрос.ИдентификаторСообщения, Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки);
			КонецЦикла;
			ЗафиксироватьТранзакцию();
		Исключение
			ЗаписьЖурналаРегистрации("Datareon. Изменение статуса сообщения", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки());
			ОтменитьТранзакцию();
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Процедура - Отправить сообщение об ошибке
//
// Параметры:
//  Класс		- Строка - тип класса ошибки. 
//  Описание	- Строка - описание ошибки. 
//  свойства	- Структура - дополнительные свойства ошибки. 
//
Процедура ОтправитьСообщениеОбОшибке(класс, описание, свойства) Экспорт
	пакет = сшпОбщегоНазначения.СформироватьСтруктуруПакета("TER",класс, Описание);
	пакет.Properties = свойства;
	сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(пакет);
КонецПроцедуры	

// Функция - Управление состоянием обработчика
//
// Параметры:
//  формат	- Перечисление.сшпФорматыСообщений - Формат поступившего сообщения.
//  пакет	- ОбъектXDTO - объект сообщения.
// 
// Возвращаемое значение:
//  Булево  - Признак успешности обновления статуса обработчика события.
//
Функция УправлениеСостояниемОбработчика(формат, пакет) Экспорт
	статусВозврата = Истина;
	Попытка
		xdtoСообщение = сшпОбщегоНазначения.ПолучитьОбъектXDTO(формат, пакет);
		xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(формат, xdtoСообщение.Body);
		идентификатор = xdtoПакет.Id;
		статус = ?(Булево(xdtoПакет.IsStarted) = истина, Перечисления.сшпСтатусыОбработчиков.Включен, Перечисления.сшпСтатусыОбработчиков.Отключен);
		сшпРаботаСДанными.УстановитьСтатусОбработчика(новый УникальныйИдентификатор(идентификатор), статус);
	Исключение
		ЗаписьЖурналаРегистрации("Datareon. Изменение статуса обработчика", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		статусВозврата = Ложь;
	КонецПопытки;
	Возврат статусВозврата;	
КонецФункции

// Функция - Возврат пакетов в обработку
//
// Параметры:
//  ФорматСообщения	 - Перечисление.сшпФорматыСообщений - Формат поступившего сообщения.
//  Пакет			 - ОбъектXDTO - объект сообщения.
// 
// Возвращаемое значение:
//  Булево  - Признак успешности выполнения операции.
//
Функция ВозвратПакетовВОбработку(ФорматСообщения, Пакет) Экспорт
	статусВозврата = Истина;
	списокИдентификаторов = Новый СписокЗначений;
	Попытка
		xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, Пакет);
		xdtoCommand = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, xdtoПакет.Body);
		последовательность = xdtoCommand.Последовательность();
		количествоИдентификаторов = последовательность.Количество();
		Если количествоИдентификаторов > 0 тогда
			Для Индекс = 0 по количествоИдентификаторов - 1 цикл
				списокИдентификаторов.Добавить(Новый УникальныйИдентификатор(последовательность.ПолучитьЗначение(Индекс)));
			КонецЦикла;
		КонецЕсли;	
	Исключение
	КонецПопытки;
	Если списокИдентификаторов.Количество() тогда
		запросСтатусы = Новый Запрос("ВЫБРАТЬ
		|	сшпСостояниеСообщений.ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.сшпСостояниеСообщений КАК сшпСостояниеСообщений
		|ГДЕ
		|	сшпСостояниеСообщений.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОшибкаОбработки)
		|	И сшпСостояниеСообщений.ИдентификаторСообщения В(&СписокСообщений)");
		запросСтатусы.УстановитьПараметр("СписокСообщений", списокИдентификаторов);
		результатЗапрос = запросСтатусы.Выполнить();
		Если НЕ результатЗапрос.Пустой() тогда
			НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
			Попытка
				блокировкаСтатусы = Новый БлокировкаДанных;
				элементБлокировка = блокировкаСтатусы.Добавить("РегистрСведений.сшпСостояниеСообщений");
				элементБлокировка.ИсточникДанных = результатЗапрос;
				элементБлокировка.ИспользоватьИзИсточникаДанных("ИдентификаторСообщения", "ИдентификаторСообщения");
				элементБлокировка.Режим = РежимБлокировкиДанных.Исключительный;
				блокировкаСтатусы.Заблокировать();
				выборкаЗапрос = результатЗапрос.Выбрать();
				Пока выборкаЗапрос.Следующий() цикл
					сшпРаботаСДанными.УстановитьСостояниеСообщения(выборкаЗапрос.ИдентификаторСообщения, Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки);
				КонецЦикла;
				ЗафиксироватьТранзакцию();
			Исключение
				ЗаписьЖурналаРегистрации("Datareon. Изменение статуса сообщения", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки());
				ОтменитьТранзакцию();
				статусВозврата = Ложь;
			КонецПопытки;
			Если статусВозврата И сшпФункциональныеОпции.АвтоматическийСтартОбработчиков() тогда
				сшпОбщегоНазначения.ЗапуститьОбработчикОчереди("ОбработкаОчередиИсходящихСообщений");
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	Возврат статусВозврата;
КонецФункции	

// Функция - НайтиСообщения
// 
// Возвращаемое значение:
//  Булево  - Признак успешности выполнения операции.
//
Функция НайтиСообщения(ФорматСообщения, Пакет, текИдентификатор) Экспорт
	
	РезультВыполнения = Истина;
	Попытка
		
		статусВозврата = Истина;
		xdtoПакет = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, Пакет);
		сткПараметры = сшпОбщегоНазначения.ПолучитьПараметрыСообщенияСтруктурой(xdtoПакет);
		
		ТелоСообщения = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, xdtoПакет.Body);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТелоСообщения.Получить("text");
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НайденныеОбъекты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ НайденныеИндексированныеОбъекты
		|ИЗ
		|	НайденныеОбъекты КАК НайденныеОбъекты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 100
		|	сшпОчередьИсходящихСообщений.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	сшпОчередьИсходящихСообщений.ДатаРегистрации
		|ПОМЕСТИТЬ Сообщения
		|ИЗ
		|	РегистрСведений.сшпОчередьИсходящихСообщений КАК сшпОчередьИсходящихСообщений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НайденныеИндексированныеОбъекты КАК НайденныеИндексированныеОбъекты
		|		ПО сшпОчередьИсходящихСообщений.СсылкаНаОбъект = НайденныеИндексированныеОбъекты.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторСообщения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(сшпСостояниеОтправляемыхСообщений.СтатусСообщения, сшпСостояниеСообщений.СтатусСообщения) КАК Status,
		|	ЕСТЬNULL(сшпСостояниеОтправляемыхСообщений.ДатаИзменения, сшпСостояниеСообщений.ДатаИзменения) КАК StatusDate,
		|	ЕСТЬNULL(сшпОчередьОтправляемыхСообщений.ИдентификаторСообщения, Сообщения.ИдентификаторСообщения) КАК Id,
		|	Сообщения.ДатаРегистрации КАК RegisterDate,
		|	ЕСТЬNULL(сшпСостояниеОтправляемыхСообщений.ОписаниеОшибки, сшпСостояниеСообщений.ОписаниеОшибки) КАК Error
		|ИЗ
		|	Сообщения КАК Сообщения
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК сшпСостояниеСообщений
		|		ПО Сообщения.ИдентификаторСообщения = сшпСостояниеСообщений.ИдентификаторСообщения
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сшпОчередьОтправляемыхСообщений КАК сшпОчередьОтправляемыхСообщений
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК сшпСостояниеОтправляемыхСообщений
		|			ПО сшпОчередьОтправляемыхСообщений.ИдентификаторСообщения = сшпСостояниеОтправляемыхСообщений.ИдентификаторСообщения
		|		ПО Сообщения.ИдентификаторСообщения = сшпОчередьОтправляемыхСообщений.ИдентификаторБазовогоСообщения";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		
		xmlРезультат = Новый ЗаписьXML;
		xmlРезультат.УстановитьСтроку("UTF-8");
		xmlРезультат.ЗаписатьНачалоЭлемента("FindResult");
		xmlРезультат.ЗаписатьНачалоЭлемента("Messages");
		СтруктураАтрибутов = Новый Структура("Id, Status, StatusDate, RegisterDate, Error");
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СтруктураАтрибутов, ВыборкаДетальныеЗаписи);
			сшпФункцииРаботыXML.СформироватьУзелXML(xmlРезультат, Неопределено, "Message", , СтруктураАтрибутов);
		КонецЦикла;
		
		xmlРезультат.ЗаписатьКонецЭлемента();
		xmlРезультат.ЗаписатьКонецЭлемента();
		пакетРезультат = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-FindResult", xmlРезультат.Закрыть());
		пакетРезультат.CorrelationId = xdtoПакет.CorrelationId;
		пакетРезультат.ReplyTo = xdtoПакет.ReplyTo;  
		Если сткПараметры.Свойство("NodeConfigurationId") Тогда                     
			СвойстваСообщения = Новый Структура("NodeConfigurationId", сткПараметры.NodeConfigurationId);
			пакетРезультат.Properties = СвойстваСообщения;
		КонецЕсли;
		РезультВыполнения = сшпВзаимодействиеСАдаптером.ОтправитьСистемноеСообщение(пакетРезультат);
		
	Исключение
		ЗаписьЖурналаРегистрации("Datareon. Поиск сообщений", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		РезультВыполнения = Ложь;
	КонецПопытки;
	
	Возврат РезультВыполнения;
	
КонецФункции

#КонецОбласти
