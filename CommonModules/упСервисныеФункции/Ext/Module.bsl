
#Область ПрограммныйИнтерфейс

// Функция возвращает значение константы.
//
// Параметры:
//  Константа - строка - имя константы.
//
// Возвращаемое значение:
//  Произвольное - значение константы.
//
Функция ПолучитьЗначениеКонстанты(Константа) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("упКэшироватьЗначенияКонстант") Тогда 
		Результат = упСервисныеФункцииПовторноеИспользование.ПолучитьЗначениеКонстанты(Константа);
	Иначе
		МенеджерЗначения = Константы[Константа].СоздатьМенеджерЗначения();
		МенеджерЗначения.Прочитать();
		Результат = МенеджерЗначения.Значение;
		Если МетодРеализован(МенеджерЗначения, "ПолучитьРазрешенноеЗначение") Тогда
			Результат = МенеджерЗначения.ПолучитьРазрешенноеЗначение();
		КонецЕсли; 
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Процедура контролирует не зависла ли обработка заданий.
//
Процедура ПроверкаРаботыФоновыхЗаданий() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Константы.упДатаВремяИсполненияКонтрольногоЗадания.Установить(ТекущаяДата());
	УстановитьПривилегированныйРежим(Ложь);
	
	мсвНомеровСеансов = Новый Массив;
	
	Сеансы = ПолучитьСеансыИнформационнойБазы();
	Для Каждого текСеанс Из Сеансы Цикл 
		мсвНомеровСеансов.Добавить(текСеанс.НомерСеанса);
	КонецЦикла;
	
	МаксимальноеВремяЖизни = Константы.упМаксимальноеВремяФоновойОбработки.Получить();
	Если ЗначениеЗаполнено(МаксимальноеВремяЖизни) Тогда 
		ДатаНачалаОбработки = ТекущаяДата() - 150 - МаксимальноеВремяЖизни;
	Иначе
		ДатаНачалаОбработки = ТекущаяДата() - 150;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбПроизошедшиеСобытия.ДокументОснование,
	               |	тбПроизошедшиеСобытия.ТипСобытия,
	               |	тбПроизошедшиеСобытия.ДатаЗаписи,
	               |	тбПроизошедшиеСобытия.НомерСеанса
	               |ИЗ
	               |	РегистрСведений.упПроизошедшиеСобытия КАК тбПроизошедшиеСобытия
	               |ГДЕ
	               |	тбПроизошедшиеСобытия.СтатусОтработки = ЗНАЧЕНИЕ(ПеречислениЕ.упСтатусыОтработкиСобытий.Обрабатывается)
	               |	И НЕ тбПроизошедшиеСобытия.НомерСеанса = 0
	               |	И НЕ тбПроизошедшиеСобытия.НомерСеанса В (&НомерСеанса)
	               |	И тбПроизошедшиеСобытия.ДатаНачалаОбработки < &ДатаНачалаОбработки
	               |	И тбПроизошедшиеСобытия.ДатаНачалаОбработки > ДАТАВРЕМЯ(1, 1, 1)";
	Запрос.УстановитьПараметр("НомерСеанса",         мсвНомеровСеансов);
	Запрос.УстановитьПараметр("ДатаНачалаОбработки", ДатаНачалаОбработки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сеансы = ПолучитьСеансыИнформационнойБазы();                                 
	Для Каждого текСеанс Из Сеансы Цикл 
		Если мсвНомеровСеансов.Найти(текСеанс.НомерСеанса) = Неопределено Тогда 
			мсвНомеровСеансов.Добавить(текСеанс.НомерСеанса);
		КонецЕсли;
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл 
		Если мсвНомеровСеансов.Найти(Выборка.НомерСеанса) = Неопределено Тогда 
			текЗапись = РегистрыСведений.упПроизошедшиеСобытия.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(текЗапись, Выборка);
			текЗапись.Прочитать();
			Если текЗапись.СтатусОтработки = Перечисления.упСтатусыОтработкиСобытий.Обрабатывается И текЗапись.НомерСеанса = Выборка.НомерСеанса Тогда 
				ЗаписьЖурналаРегистрации("Корректировка фоновых задач", УровеньЖурналаРегистрации.Информация, Метаданные.РегистрыСведений.упПроизошедшиеСобытия,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Снят признак обработки события, документ - %1, событие - %2, номер сеанса - %3, дата начала обработки - %4'"), текЗапись.ДокументОснование, текЗапись.ТипСобытия, текЗапись.НомерСеанса, текЗапись.ДатаНачалаОбработки));
				
				текЗапись.СтатусОтработки     = Перечисления.упСтатусыОтработкиСобытий.Открыто;
				текЗапись.ДатаНачалаОбработки = '00010101';
				текЗапись.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	упОбрабатываемыеСобытияМониторинга.ТипСобытия,
	|	упОбрабатываемыеСобытияМониторинга.НомерСеанса
	|ИЗ
	|	РегистрСведений.упОбрабатываемыеСобытияМониторинга КАК упОбрабатываемыеСобытияМониторинга
	|ГДЕ
	|	упОбрабатываемыеСобытияМониторинга.СтатусОтработки = ЗНАЧЕНИЕ(Перечисление.упСтатусыОбработкиСобытийМониторинга.Обрабатывается)
	|	И НЕ упОбрабатываемыеСобытияМониторинга.НомерСеанса = 0
	|	И НЕ упОбрабатываемыеСобытияМониторинга.НомерСеанса В (&НомерСеанса)
	|	И упОбрабатываемыеСобытияМониторинга.ДатаНачалаОбработки < &ДатаНачалаОбработки
	|	И упОбрабатываемыеСобытияМониторинга.ДатаНачалаОбработки > ДАТАВРЕМЯ(1, 1, 1)
	|	И упОбрабатываемыеСобытияМониторинга.ДатаОтработки = ДАТАВРЕМЯ(1, 1, 1)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сеансы = ПолучитьСеансыИнформационнойБазы();
	Для Каждого текСеанс Из Сеансы Цикл 
		Если мсвНомеровСеансов.Найти(текСеанс.НомерСеанса) = Неопределено Тогда 
			мсвНомеровСеансов.Добавить(текСеанс.НомерСеанса);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("НомерСеанса",         мсвНомеровСеансов);      
	
	Пока Выборка.Следующий() Цикл 
		Если мсвНомеровСеансов.Найти(Выборка.НомерСеанса) = Неопределено Тогда 
			текЗапись = РегистрыСведений.упОбрабатываемыеСобытияМониторинга.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(текЗапись, Выборка);
			текЗапись.Прочитать();
			Если текЗапись.СтатусОтработки = Перечисления.упСтатусыОбработкиСобытийМониторинга.Обрабатывается И текЗапись.НомерСеанса = Выборка.НомерСеанса Тогда 
				ЗаписьЖурналаРегистрации("Корректировка фоновых задач", УровеньЖурналаРегистрации.Информация, Метаданные.РегистрыСведений.упОбрабатываемыеСобытияМониторинга,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Снят признак обработки типа события - %1, номер сеанса - %2, дата начала обработки - %3'"), текЗапись.ТипСобытия, текЗапись.НомерСеанса, текЗапись.ДатаНачалаОбработки));
				
				текЗапись.СтатусОтработки     = Перечисления.упСтатусыОбработкиСобытийМониторинга.ОшибкаОбработки;
				текЗапись.ДатаНачалаОбработки = '00010101';
				текЗапись.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры

// Возвращает структуру показателей загрузки.
//
// Параметры:
//  УчитыватьМассу - Булево - Флаг выполнения.
//  УчитыватьОбъем - Булево - Флаг выполнения.
//  УчитыватьКоличествоМест - Булево - Флаг выполнения.
//  УчитыватьЛинейныеРазмеры - Булево - Флаг выполнения.
//  ИмяРеквизита - Строка - Перечислены имена реквизитов.
//
// Возвращаемое значение:
//  Структура - Результат выполнения.
//
Функция ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу = Истина, УчитыватьОбъем = Истина, УчитыватьКоличествоМест = Истина, УчитыватьЛинейныеРазмеры = Истина, ИмяРеквизита = "Наименование") Экспорт
	
	сткПредставления = Новый Структура;
	Если УчитыватьМассу Тогда
		прМасса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияМассы"), ИмяРеквизита);
		Если Не ЗначениеЗаполнено(прМасса) Тогда
			прМасса = НСтр("ru = 'неопр.'");
		КонецЕсли; 
		сткПредставления.Вставить("Масса", прМасса);
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		прОбъем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияОбъема"), ИмяРеквизита);
		Если Не ЗначениеЗаполнено(прОбъем) Тогда
			прОбъем = НСтр("ru = 'неопр.'");
		КонецЕсли;   
		сткПредставления.Вставить("Объем", прОбъем);
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда
		прМесто = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияГрузовыхМест"), ИмяРеквизита);
		Если Не ЗначениеЗаполнено(прМесто) Тогда
			прМесто = НСтр("ru = 'неопр.'");
		КонецЕсли;   
		сткПредставления.Вставить("КоличествоМест", прМесто);
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		прЛинЕд = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияЛинейныхГабаритов"), ИмяРеквизита);
		Если Не ЗначениеЗаполнено(прЛинЕд) Тогда
			прЛинЕд = НСтр("ru = 'неопр.'");
		КонецЕсли;   
		сткПредставления.Вставить("КоличествоЗагрузочныхМетров", прЛинЕд);
	КонецЕсли;
	
	Возврат сткПредставления;
	
КонецФункции // ПолучитьПредставлениеПоказателейЗагрузки()	

// Удаление документа рейс.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документы.
//
Процедура УдалитьРейсНепосредственно(Рейс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Истина;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.упРейс");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Рейс);
		Блокировка.Заблокировать();
		
		Объект = Рейс.ПолучитьОбъект();
		Если Объект = Неопределено Тогда
			// объект уже удален.
			Возврат;
		КонецЕсли;
		Объект.ДополнительныеСвойства.Вставить("СШПНеОбрабатывать"); 
		Объект.Удалить();
		
		ПоискСсылок = Новый Массив;
	    ПоискСсылок.Добавить(Рейс);
	
	    ПрепятствующиеУдалению = НайтиПоСсылкам(ПоискСсылок);
		Если ПрепятствующиеУдалению.Количество() Тогда
			Результат = Ложь;
		КонецЕсли; 
		
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
	Если Результат Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;

КонецПроцедуры // УдалитьРейсНепосредственно()

// Удаление записей регистра упРасстоянияМеждуТочками. 
//
// Параметры:
//  ТаблицаКоординат - ТаблицаЗначений - Таблица данных.
//
Процедура УдалитьЗаписиРегистраРасстоянияМеждуТочками(ТаблицаКоординат = Неопределено) Экспорт
	
	Сч         = 0;
	Процент    = 0;
	Если ТаблицаКоординат = Неопределено Тогда // очищаем весь регистр
		Количество = упСервисныеФункции.ПолучитьРазмерРегистра(Метаданные.РегистрыСведений.упРасстоянияМеждуТочками);
		Пока Истина Цикл
			НачатьТранзакцию();
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 500
			|	упРасстоянияМеждуТочками.НачальнаяШирота,
			|	упРасстоянияМеждуТочками.НачальнаяДолгота,
			|	упРасстоянияМеждуТочками.КонечнаяШирота,
			|	упРасстоянияМеждуТочками.КонечнаяДолгота,
			|	упРасстоянияМеждуТочками.Ограничение
			|ИЗ
			|	РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками";
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					Запись = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
					Запись.НачальнаяШирота  = Выборка.НачальнаяШирота;
					Запись.НачальнаяДолгота = Выборка.НачальнаяДолгота;
					Запись.КонечнаяШирота   = Выборка.КонечнаяШирота;
					Запись.КонечнаяДолгота  = Выборка.КонечнаяДолгота;
					Запись.Ограничение = Выборка.Ограничение;
					Запись.Прочитать();
					Запись.Удалить();
					упСервисныеФункции.ОбновитьИндикатор(Количество, Сч, Процент);
				КонецЦикла;
			Иначе
				Прервать;
			КонецЕсли; 
			ЗафиксироватьТранзакцию();
		КонецЦикла; 
	Иначе	
		Количество = ТаблицаКоординат.Количество();
		Для каждого текСтрока Из ТаблицаКоординат Цикл
			НаборЗаписей = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьНаборЗаписей(); 
			НаборЗаписей.Отбор.НачальнаяШирота.Установить(текСтрока.НачальнаяШирота); 
			НаборЗаписей.Отбор.НачальнаяДолгота.Установить(текСтрока.НачальнаяДолгота); 
			НаборЗаписей.Отбор.КонечнаяШирота.Установить(текСтрока.КонечнаяШирота); 
			НаборЗаписей.Отбор.КонечнаяДолгота.Установить(текСтрока.КонечнаяДолгота); 
			НаборЗаписей.Отбор.Ограничение.Установить(текСтрока.Ограничение); 
			НаборЗаписей.Записать(); 
			упСервисныеФункции.ОбновитьИндикатор(Количество, Сч, Процент);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

// Процедура устанавливает заголовок формы.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//
Процедура УстановитьЗаголовокФормыИнформацияПроведения(Форма) Экспорт

	ОбъектДокумент = Форма.Объект;
	Если Не ОбъектДокумент.Ссылка.Пустая() Тогда
		Форма.Автозаголовок = Ложь;
		Если ОбъектДокумент.Проведен Тогда
			Форма.Заголовок = НСтр(СтрШаблон("ru = '%1 (проведен)'", Строка(ОбъектДокумент.Ссылка)));
		Иначе	
			Форма.Заголовок = Строка(ОбъектДокумент.Ссылка);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Выполнить проверку.
//
// Параметры:
//  Тип - Строка - имя типа.
//  ИсключаяСсылкиМетаданных - Булево - Исключить.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЛиТипСсылкиНаОбъектБД(Тип, ИсключаяСсылкиМетаданных = Истина) Экспорт 
	
	Результат = Ложь;
	ХмлТип = XMLТип(Тип);
	Если ХмлТип <> Неопределено Тогда
		Если Найти(ХмлТип.ИмяТипа, "Ref.") > 0 Тогда 
			Если Ложь
				Или Не ИсключаяСсылкиМетаданных
				Или (Истина
					И Найти(ХмлТип.ИмяТипа, "BusinessProcessRoutePointRef.") = 0
					И Найти(ХмлТип.ИмяТипа, "EnumRef.") = 0)
			Тогда
				Результат = Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	Возврат Результат;

КонецФункции

// Выполнить проверку фоновых заданий.
//
// Параметры:
//  ФоновоеЗадание - ФоновоеЗадание - Фоновое задание для выполнения асинхронных операций.
//
Процедура ПроверитьВывестиОшибкуФоновогоЗадания(ФоновоеЗадание) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки.
	    ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору();
	КонецЕсли;
	Если ФоновоеЗадание <> Неопределено И ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке));
	КонецЕсли; 
	
КонецПроцедуры

// Процедура возвращает результат исполнения СКД
//
// Параметры:
//  СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Объект схемы.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция ВыполнитьСКДвТЗ(СхемаКомпоновкиДанных, ВозможностьИспользованияВнешнихФункций = Ложь) Экспорт
		
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	// Отключение вывода общих итогов
	КомпоновщикНастроекКомпоновкиДанных.Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("ГоризонтальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	КомпоновщикНастроекКомпоновкиДанных.Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,,ВозможностьИспользованияВнешнихФункций);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат Результат;
	
КонецФункции

// Осуществляет вывод результата компоновки в коллекцию значений. По умолчанию в качестве коллекции используется новая таблица значений.
//
// Параметры:
//  СхемаКомпоновки					 - СхемаКомпоновкиДанных	 - СхемаКомпоновкиДанных
//  НастройкаКомпоновки				 - НастройкиКомпоновкиДанных	 - НастройкиКомпоновкиДанных
//  КоллекцияЗначений				 - ДеревоЗначений,
//  						             Массив, СписокЗначений,
//  						             ТаблицаЗначений	 - Если не указана, создается ТаблицаЗначений
//  ВнешниеНаборыДанных				 - Структура				 - внешние наборы
//  ТолькоСоздатьКолонки				 - Булево					 - только создавать колонки.
//  _СхемаКолонок					 - Структура				 - Если Неопределено, то не возвращается
//  МаксимальноеЧислоСтрокРезультата	 - Число					 - Для предотвращения получения слишком большого результата. 
//                                         Если порог превышен, то результат = Неопределено.
//  ОтключитьОбщиеИтоги				 - Булево					 - отключить общие итоги.
//  ПроверятьДоступностьПолей			 - Булево					 - проверять доступность полей.
// 
// Возвращаемое значение:
//  КоллекцияЗначений - результат в коллекции.
//
Функция СкомпоноватьВКоллекциюЗначенийПоСхеме(Знач СхемаКомпоновки, Знач НастройкаКомпоновки = Неопределено, КоллекцияЗначений = Неопределено,
	Знач ВнешниеНаборыДанных = Неопределено, Знач ТолькоСоздатьКолонки = Ложь, _СхемаКолонок = Неопределено, Знач МаксимальноеЧислоСтрокРезультата = 0,
	Знач ОтключитьОбщиеИтоги = Истина, ПроверятьДоступностьПолей = Ложь) Экспорт
	
	#Если _ Тогда
	    СхемаКомпоновки = Новый СхемаКомпоновкиДанных;
	#КонецЕсли
	Если НастройкаКомпоновки = Неопределено Тогда
		НастройкаКомпоновки = СхемаКомпоновки.НастройкиПоУмолчанию;
	КонецЕсли; 
	Если НастройкаКомпоновки.Структура.Количество() = 0 Тогда
		НайтиДобавитьЭлементСтруктурыГруппировкаКомпоновки(НастройкаКомпоновки.Структура);
	КонецЕсли;
	Если ОтключитьОбщиеИтоги Тогда
		НастройкаКомпоновки.ПараметрыВывода.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВертикальноеРасположениеОбщихИтогов"),
			РасположениеИтоговКомпоновкиДанных.Нет);
	КонецЕсли;
	Если Ложь
		Или КоллекцияЗначений = Неопределено 
		Или ТипЗнч(КоллекцияЗначений) = Тип("СписокЗначений")
		Или ТипЗнч(КоллекцияЗначений) = Тип("Массив")
	Тогда
		КоллекцияРезультата = Новый ТаблицаЗначений;
	Иначе
		КоллекцияРезультата = КоллекцияЗначений;
	КонецЕсли;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	//Попытка
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновки, НастройкаКомпоновки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"), ПроверятьДоступностьПолей);
	//Исключение
	//	ВызватьИсключение;
	//КонецПопытки;
	Если ТолькоСоздатьКолонки Тогда
		КоллекцияЗначений.Колонки.Очистить();
		ЯчейкиЗаголовка = МакетКомпоновки.Макеты[0].Макет.Ячейки;
		Для Каждого Ячейка Из ЯчейкиЗаголовка Цикл
			//КолонкаКоллекции = КоллекцияЗначений.Колонки.Найти(Ячейка.Имя);
			//Если КолонкаКоллекции = Неопределено Тогда
				КоллекцияЗначений.Колонки.Добавить(Ячейка.Имя, Ячейка.ТипЗначения, Ячейка.Заголовок,);
			//КонецЕсли;
		КонецЦикла; 
	Иначе
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, , Истина);
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВывода.УстановитьОбъект(КоллекцияРезультата);
		ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	КонецЕсли;
	//Исследовать(КоллекцияРезультата);
	Если ТипЗнч(КоллекцияЗначений) = Тип("СписокЗначений") Тогда
		Есть0 = КоллекцияРезультата.Колонки.Количество() > 0;
		Есть1 = КоллекцияРезультата.Колонки.Количество() > 1;
		Для Каждого СтрокаРезультата Из КоллекцияРезультата Цикл
			НовыйЭлемент = КоллекцияЗначений.Добавить();
			Если Есть0 Тогда
				НовыйЭлемент.Значение = СтрокаРезультата[0];
			КонецЕсли;
			Если Есть1 Тогда
				НовыйЭлемент.Представление = СтрокаРезультата[1];
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(КоллекцияЗначений) = Тип("Массив") Тогда
		Если КоллекцияРезультата.Колонки.Количество() > 0 Тогда
			Для Каждого СтрокаРезультата Из КоллекцияРезультата Цикл
				КоллекцияЗначений.Добавить(СтрокаРезультата[0]);
			КонецЦикла;
		КонецЕсли;
	Иначе
		КоллекцияЗначений = КоллекцияРезультата;
	КонецЕсли;
	Результат = КоллекцияЗначений;
	Возврат Результат;
	
КонецФункции

// Присвоить если не равно
//
// Параметры:
//  П1						 - Тип - первый параметр.
//  П2						 - Тип - второй параметр.
//  СравнениеСтрокБезУчетаРегистра	 - Булево - признак сравнения без учета регистра.
// 
// Возвращаемое значение:
//  Истина - если присвоили, Ложь - были равны
//
Функция ПрисвоитьЕслиНеРавно(П1, П2, СравнениеСтрокБезУчетаРегистра = Ложь) Экспорт 
	
	Если Истина
		И СравнениеСтрокБезУчетаРегистра 
		И ТипЗнч(П1) = Тип("Строка")
		И ТипЗнч(П2) = Тип("Строка")
	Тогда
		Если НРег(П1) <> НРег(П2) Тогда
			П1 = П2;
			Возврат Истина;
		КонецЕсли; 
	Иначе
		Если П1 <> П2 Тогда
			П1 = П2;
			Возврат Истина;
		КонецЕсли; 
	КонецЕсли; 
	Возврат Ложь;
	
КонецФункции

// Найти добавить элемент настроек компоновки по полю
//
// Параметры:
//  ЭлементыНастройки		 - ЭлементНастройки	 - элемент настройки.
//  Ключ					 - Строка	 - имя ключа.
//  ПроверятьУникальность	 - Булево	 - проверять уникальность.
//  ИспользованиеДляНового	 - Булево	 - использовать для нового.
//  Позиция				 - Число	 - позиция.
// 
// Возвращаемое значение:
//  ЭлементНастроек - элемент настроек.
//
Функция НайтиДобавитьЭлементНастроекКомпоновкиПоПолю(Знач ЭлементыНастройки, Знач Ключ = "", Знач ПроверятьУникальность = Истина,
	Знач ИспользованиеДляНового = Истина, Позиция = Неопределено) Экспорт
	
	Попытка
		ЭлементыНастройки = ЭлементыНастройки.Элементы;
	Исключение
	КонецПопытки;
	ИмяСвойстваПоля = "Поле";
	Если      ТипЗнч(ЭлементыНастройки) = Тип("КоллекцияЭлементовПорядкаКомпоновкиДанных") Тогда
		ТипЭлемента = Тип("ЭлементПорядкаКомпоновкиДанных");
	ИначеЕсли ТипЗнч(ЭлементыНастройки) = Тип("КоллекцияВыбранныхПолейКомпоновкиДанных") Тогда
		ТипЭлемента = Тип("ВыбранноеПолеКомпоновкиДанных");
	ИначеЕсли ТипЗнч(ЭлементыНастройки) = Тип("КоллекцияПолейГруппировкиКомпоновкиДанных") Тогда
		ТипЭлемента = Тип("ПолеГруппировкиКомпоновкиДанных");
	ИначеЕсли ТипЗнч(ЭлементыНастройки) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
		ТипЭлемента = Неопределено;
	ИначеЕсли ТипЗнч(ЭлементыНастройки) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		ТипЭлемента = Неопределено;
		ИмяСвойстваПоля = "Параметр";
	//ИначеЕсли ТипЗнч(ЭлементыНастройки) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
	//	ТипЭлемента = Тип("ЭлементОтбораКомпоновкиДанных");
	КонецЕсли;
	Если ТипЗнч(Ключ) = Тип("Строка") Тогда
		Если ИмяСвойстваПоля = "Поле" Тогда
			Ключ = Новый ПолеКомпоновкиДанных(Ключ);
		Иначе
			Ключ = Новый ПараметрКомпоновкиДанных(Ключ);
		КонецЕсли; 
	КонецЕсли;
	Если ПроверятьУникальность Тогда
		ЭлементНастроек = упСервисныеФункцииКлиентСервер.НайтиЭлементКоллекцииПоЗначениюСвойства(ЭлементыНастройки, ИмяСвойстваПоля, Ключ, ТипЭлемента);
	КонецЕсли;
	Если ЭлементНастроек = Неопределено Тогда
		Если ТипЭлемента <> Неопределено Тогда
			ЭлементНастроек = ЭлементыНастройки.Добавить(ТипЭлемента);
		Иначе
			ЭлементНастроек = ЭлементыНастройки.Добавить();
		КонецЕсли;
		Если ИмяСвойстваПоля = "Поле" Тогда
			ЭлементНастроек.Поле = Ключ;
		Иначе
			ЭлементНастроек.Параметр = Ключ;
		КонецЕсли; 
		ЭлементНастроек.Использование = ИспользованиеДляНового;
	КонецЕсли;
	Если Позиция <> Неопределено Тогда
		ЭлементыНастройки.Сдвинуть(ЭлементНастроек, Позиция - ЭлементыНастройки.Индекс(ЭлементНастроек));
	КонецЕсли; 
	Возврат ЭлементНастроек;
	
КонецФункции

// Найти добавить элемент отбора компоновки
//
// Параметры:
//  ЭлементыОтбора		 - ЭлементОтбора	 - элемент отбора.
//  Поле				 - Поле, Строка	 - поле.
//  Значение			 - Значение	 - значение.
//  Сравнение			 - ВидСравнения	 - вид сравнения.
//  ДоступныеПоляОтбора	 - ДоступныеПоляОтбора	 - доступные поля отбора.
//  ПроверятьУникальность - Булево	 - проверять уникальность.
//  Использование		 - Булево	 - использование.
//  Недоступный		 - Булево	 - недоступный.
// 
// Возвращаемое значение:
//   ЭлементОтбора - элемент отбора.
//
Функция НайтиДобавитьЭлементОтбораКомпоновки(Знач ЭлементыОтбора, Знач Поле = "", Знач Значение = Неопределено, Знач Сравнение = "", 
	Знач ДоступныеПоляОтбора =  Неопределено, Знач ПроверятьУникальность = Истина, Знач Использование = Истина, Знач Недоступный = Ложь) Экспорт
	
	Если ТипЗнч(ЭлементыОтбора) = Тип("НастройкиКомпоновкиДанных") Тогда
		ЭлементыОтбора = ЭлементыОтбора.Отбор;
	КонецЕсли;
	Если ТипЗнч(ЭлементыОтбора) = Тип("ОтборКомпоновкиДанных") Тогда
		ДоступныеПоляОтбора = ЭлементыОтбора.ДоступныеПоляОтбора;
		ЭлементыОтбора = ЭлементыОтбора.Элементы;
	ИначеЕсли ТипЗнч(ЭлементыОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		ЭлементыОтбора = ЭлементыОтбора.Элементы;
	Иначе
		ЭлементыОтбора = ЭлементыОтбора;
	КонецЕсли;
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	Если ПроверятьУникальность Тогда
		ЭлементОтбора = НайтиЭлементОтбораКомпоновки(ЭлементыОтбора, "" + Поле);
	КонецЕсли;
	Если ЭлементОтбора = Неопределено Тогда
		ЭлементОтбора = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Поле;
	КонецЕсли;
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.ЗагрузитьЗначения(Значение);
		Значение = СписокЗначений;
	КонецЕсли;
	
	// Вид сравнения
	Если ДоступныеПоляОтбора <> Неопределено Тогда
		ДоступноеПолеОтбора = ДоступныеПоляОтбора.НайтиПоле(Поле);
		Если ДоступноеПолеОтбора = Неопределено Тогда 
			Если ДоступныеПоляОтбора.Элементы.Количество() > 0 Тогда 
				ВызватьИсключение "Доступное поле с именем """ + Поле + """ не найдено";
			КонецЕсли; 
		Иначе
			ДоступныеВидыСравнения = ДоступноеПолеОтбора.ДоступныеВидыСравнения;
		КонецЕсли;
	КонецЕсли;
	Если Сравнение = "Авто" Тогда
		#Если Не ТонкийКлиент И Не ВебКлиент Тогда
			Сравнение = ОпределитьВидСравненияПоЗначению(Значение, Истина, ДоступныеВидыСравнения);
		#Иначе
			ВызватьИсключение "Вид сравнения ""Авто"" не поддерживается в веб и тонком клиентах";
		#КонецЕсли 
	ИначеЕсли ТипЗнч(Сравнение) = Тип("ВидСравненияКомпоновкиДанных") Тогда
	Иначе
		Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			Сравнение = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			Сравнение = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
	КонецЕсли;
	Если Истина
		И Сравнение = ВидСравненияКомпоновкиДанных.Равно
		И Значение = Неопределено
		И ДоступныеПоляОтбора <> Неопределено
	Тогда
		ДоступноеПолеОтбора = ДоступныеПоляОтбора.НайтиПоле(Поле);
		Если ДоступноеПолеОтбора <> Неопределено Тогда
			Значение = ДоступноеПолеОтбора.Тип.ПривестиЗначение(Значение);
			Если Истина
				И Значение = ""
				И ДоступноеПолеОтбора.Тип.КвалификаторыСтроки.Длина = 0
			Тогда
				Сравнение = ВидСравненияКомпоновкиДанных.Содержит;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПрисвоитьЕслиНеРавно(ЭлементОтбора.ВидСравнения, Сравнение);
	ПрисвоитьЕслиНеРавно(ЭлементОтбора.ПравоеЗначение, Значение);
	ПрисвоитьЕслиНеРавно(ЭлементОтбора.Использование, Использование);
	Если Недоступный Тогда
		ПрисвоитьЕслиНеРавно(ЭлементОтбора.РежимОтображения, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли; 
	Результат = ЭлементОтбора;
	Возврат Результат;
	
КонецФункции

// Найти элемент отбора компоновки
//
// Параметры:
//  Отбор							 - Отбор - отбор.
//  ИменаПолей						 - Строка	 - имена полей.
//  выхНайденныеЭлементы				 - Соответствие	 - найденные элементы.
//  ТолькоВключенныеНаРавенствоЗначению	 - Булево	 - только включенные на равенство по значению.
//  ВключатьПодчиненные				 - Булево	 - включать подчиненные.
// 
// Возвращаемое значение:
//  Соответствие - элементы отбора.
//
Функция НайтиЭлементОтбораКомпоновки(Знач Отбор, Знач ИменаПолей, выхНайденныеЭлементы = Неопределено, Знач ТолькоВключенныеНаРавенствоЗначению = Ложь,
	Знач ВключатьПодчиненные = Ложь) Экспорт
	
	Если ТипЗнч(Отбор) = Тип("ОтборКомпоновкиДанных") Тогда
		ЭлементыОтбора = Отбор.Элементы;
	Иначе
		ЭлементыОтбора = Отбор;
	КонецЕсли;
	Если ТипЗнч(ИменаПолей) = Тип("Строка") Тогда
		МассивИменПолей = РазбитьСтрокуРазделителем(ИменаПолей, ",", Истина);
	Иначе
		МассивИменПолей = ИменаПолей;
	КонецЕсли;
	МассивПолей = Новый Массив;
	Для Каждого ИмяПоля Из МассивИменПолей Цикл
		МассивПолей.Добавить(Новый ПолеКомпоновкиДанных(ИмяПоля));
	КонецЦикла;
	МассивПолейПуст = МассивПолей.Количество() = 0;
	Если выхНайденныеЭлементы = Неопределено Тогда
		выхНайденныеЭлементы = Новый Соответствие;
	КонецЕсли;
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если Истина
			И ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			И (Ложь
				Или Не ТолькоВключенныеНаРавенствоЗначению
				Или (Истина
					И ЭлементОтбора.Использование
					И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
					И ТипЗнч(ЭлементОтбора.ЛевоеЗначение)   = Тип("ПолеКомпоновкиДанных")
					И ТипЗнч(ЭлементОтбора.ПравоеЗначение) <> Тип("ПолеКомпоновкиДанных")))
		Тогда
			Если Ложь
				Или МассивПолейПуст
				Или МассивПолей.Найти(ЭлементОтбора.ЛевоеЗначение) <> Неопределено 
			Тогда
				выхНайденныеЭлементы.Вставить("" + ЭлементОтбора.ЛевоеЗначение, ЭлементОтбора);
			КонецЕсли;
		ИначеЕсли Истина
			И ВключатьПодчиненные
			И ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
		Тогда
			НайтиЭлементОтбораКомпоновки(ЭлементОтбора.Элементы, МассивИменПолей, выхНайденныеЭлементы, ТолькоВключенныеНаРавенствоЗначению);
		КонецЕсли;
	КонецЦикла;
	Если МассивИменПолей.Количество() = 1 Тогда
		Результат = выхНайденныеЭлементы[МассивИменПолей[0]];
	Иначе
		Результат = выхНайденныеЭлементы;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Функция разбивает строку разделителем.
//
// Параметры:
//  Стр						 - Строка	 - которую разбиваем.
//  Разделитель				 - Строка	 - ".",  символ-разделитель.
//  ОбрезатьНепечатныеСимволы		 - Булево	 - обрезать непечатные.
//  РазбитьТолькоПервымВхождением	 - Булево	 - только первым вхождением.
//  ОставлятьПустуюСтроку		 - Булево	 - оставлять пустую строку.
// 
// Возвращаемое значение:
//  Массив - фрагментов.
//
Функция РазбитьСтрокуРазделителем(Знач Стр, Разделитель = ".", ОбрезатьНепечатныеСимволы = Ложь, РазбитьТолькоПервымВхождением = Ложь, ОставлятьПустуюСтроку = Истина) Экспорт
	
	МассивСтрок = Новый Массив;
	Если Не ОставлятьПустуюСтроку И Не ЗначениеЗаполнено(Стр) Тогда
		Возврат МассивСтрок;
	КонецЕсли; 
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока Истина Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = СокрЛ(Сред(Стр,Поз));
			Если РазбитьТолькоПервымВхождением Тогда
				МассивСтрок.Добавить(Стр);
				Стр = "";
			КонецЕсли; 
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока Истина Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				Фрагмент = Стр;
				Если ОбрезатьНепечатныеСимволы Тогда
					Фрагмент = СокрЛП(Фрагмент);
				КонецЕсли;
				МассивСтрок.Добавить(Фрагмент);
				Возврат МассивСтрок;
			КонецЕсли;
			Фрагмент = Лев(Стр,Поз-1);
			Если ОбрезатьНепечатныеСимволы Тогда
				Фрагмент = СокрЛП(Фрагмент);
			КонецЕсли;
			МассивСтрок.Добавить(Фрагмент);
			Стр = Сред(Стр,Поз+ДлинаРазделителя);
			Если РазбитьТолькоПервымВхождением Тогда
				МассивСтрок.Добавить(Стр);
				Стр = "";
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;

	Возврат МассивСтрок;
		
КонецФункции // ПолучитьМассивИзСтрокиСРазделителем()

// Строки равны
//
// Параметры:
//  Строка1					 - Строка	 - первая строка.
//  Строка2					 - Строка	 - вторая строка.
//  СУчетомРегистра				 - Булево	 - с учетом регистра.
//  БезПравыхНепечатныхСимволов	 - Булево	 - без правых непечатных символов.
// 
// Возвращаемое значение:
//  Булево - результат сравнения.
//
Функция СтрокиРавны(Знач Строка1, Знач Строка2, СУчетомРегистра = Ложь, БезПравыхНепечатныхСимволов = Ложь) Экспорт
	
	Если Не СУчетомРегистра Тогда
		Строка1 = НРег(Строка1);
		Строка2 = НРег(Строка2);
	КонецЕсли; 
	Если БезПравыхНепечатныхСимволов Тогда
		Строка1 = СокрП(Строка1);
		Строка2 = СокрП(Строка2);
	КонецЕсли; 
	Результат = Строка1 = Строка2;
	Возврат Результат;
	
КонецФункции

// Определить вид сравнения по значению
//
// Параметры:
//  Значение				 - Значение	 - значение.
//  РежимКомпоновки			 - Булево	 - режим компоновки.
//  ДоступныеВидыСравнения	 - Массив	 - доступные виды.
// 
// Возвращаемое значение:
//  ВидСравнения - вид сравнения.
//
Функция ОпределитьВидСравненияПоЗначению(Знач Значение, Знач РежимКомпоновки = Ложь, Знач ДоступныеВидыСравнения = Неопределено) Экспорт
	
	ТипЗначения = ТипЗнч(Значение);
	Если РежимКомпоновки Тогда
		КоллекцияВидовСравнения = ВидСравненияКомпоновкиДанных;
	Иначе
		КоллекцияВидовСравнения = ВидСравнения;
	КонецЕсли;
	Если ТипЗначения = Тип("СписокЗначений") Тогда
		Результат = КоллекцияВидовСравнения.ВСписке;
	ИначеЕсли Истина
		И ТипЗначения = Тип("Строка")
		И ДоступныеВидыСравнения <> Неопределено
		И ДоступныеВидыСравнения.НайтиПоЗначению(КоллекцияВидовСравнения.Содержит) <> Неопределено
	Тогда
		Результат = КоллекцияВидовСравнения.Содержит;
	Иначе
		Результат = КоллекцияВидовСравнения.Равно;
		ОбъектМД = Метаданные.НайтиПоТипу(ТипЗначения);
		Если ОбъектМД <> Неопределено Тогда
			ТипТаблицы = СтрРазделить(ОбъектМД.ПолноеИмя(), ".")[0];
			Если ТипТаблицы = "Справочник" Тогда
				Если ОбъектМД.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
					Если Значение.ЭтоГруппа Тогда
						Результат = КоллекцияВидовСравнения.ВИерархии;
					КонецЕсли;
				Иначе // иерархия элементов
					Результат = КоллекцияВидовСравнения.ВИерархии;
				КонецЕсли;
			ИначеЕсли ТипТаблицы = "ПланВидовХарактеристик" Тогда
				Если Значение.ЭтоГруппа Тогда
					Результат = КоллекцияВидовСравнения.ВИерархии;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Установить значение параметра компоновки
//
// Параметры:
//  Параметры		 - ПараметрыДанных	 - параметры.
//  ИмяПараметра	 - Строка	 - имя.
//  Значение		 - Значение	 - значение.
//  Использование	 - Булево	 - использование.
//
Процедура УстановитьЗначениеПараметраКомпоновки(Параметры, ИмяПараметра, Значение, Использование = Истина) Экспорт

	#Если _ Тогда
		_Настройки = Новый НастройкиКомпоновкиДанных;
		Параметры = _Настройки.ПараметрыДанных;
	#КонецЕсли
	ЗначениеПараметра = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ЗначениеПараметра = Неопределено Тогда
		ЗаписьЖурналаРегистрации("Ошибка.СпутниковыМониторинг", УровеньЖурналаРегистрации.Ошибка,, ИмяПараметра, ИмяПараметра);
	Иначе
		ЗначениеПараметра.Использование = Использование;
		ЗначениеПараметра.Значение = Значение;
	КонецЕсли;

КонецПроцедуры // УстановитьЗначениеПараметраКомпоновки()

// Найти добавить элемент структуры группировка компоновки
//
// Параметры:
//  Группировки - КоллекцияГруппировок	 - коллекция группировок.
//  Поле		 - Поле				 - поле.
// 
// Возвращаемое значение:
//  Группировка - группировка
//
Функция НайтиДобавитьЭлементСтруктурыГруппировкаКомпоновки(Знач Группировки, Знач Поле = "") Экспорт
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	ЭлементСуществует = Ложь;
	Для Каждого Группировка Из Группировки Цикл
		Поля = Группировка.ПоляГруппировки.Элементы;
		Если Ложь
			Или (Истина
				И Поля.Количество() = 0
				И "" + Поле = "")
			Или (Истина
				И Поля.Количество() = 1
				И Поля[0].Поле = Поле)
		Тогда
			ЭлементСуществует = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если Не ЭлементСуществует Тогда
		Группировка = Группировки.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
		Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		Группировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
		Если "" + Поле <> "" Тогда
			ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
			ПолеГруппировки.Поле = Поле;
		КонецЕсли;
	КонецЕсли;
	Группировка.Использование = Истина;
	Возврат Группировка;
	
КонецФункции

// Получить схему компоновки по объекту метаданных
//
// Параметры:
//  ПолноеИмяИлиОбъектМД			 - Строка	 - имя объекта метаданных.
//  ИмяНабораДанных				 - Строка	 - "НаборДанных1", имя набора.
//  ДобавитьАвтополеКоличествоСтрок - Булево	 - Истина, добавить автополе.
// 
// Возвращаемое значение:
//  СхемаКомпоновкиДанных - схема
//
Функция СхемаКомпоновкиПоОбъектуМетаданных(Знач ПолноеИмяИлиОбъектМД, ИмяНабораДанных = "НаборДанных1", ДобавитьАвтополеКоличествоСтрок = Истина) Экспорт
	
	Если ТипЗнч(ПолноеИмяИлиОбъектМД) = Тип("Строка") Тогда
		ПолноеИмяМД = ПолноеИмяИлиОбъектМД;
	Иначе
		ПолноеИмяМД = ПолноеИмяИлиОбъектМД.ПолноеИмя();
	КонецЕсли; 
	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;
	ИмяИсточникаДанных = "ИсточникДанных1";
	ИсточникДанных = СхемаКомпоновкиДанных.ИсточникиДанных.Найти(ИмяИсточникаДанных);
	Если ИсточникДанных = Неопределено Тогда
		ИсточникДанных = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
		ИсточникДанных.Имя = ИмяИсточникаДанных;
		ИсточникДанных.ТипИсточникаДанных = "Local";
	КонецЕсли; 
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя = ИмяНабораДанных;
	НаборДанных.ИсточникДанных = ИсточникДанных.Имя;
	#Если _ Тогда
	    НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Добавить();
	#КонецЕсли
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	НаборДанных.Запрос = "ВЫБРАТЬ Т.* ИЗ " + ПолноеИмяМД + " КАК Т";
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

// Транспонировать таблицу значений (матричная операция)
//
// Параметры:
//  ТаблицаИсходная	 - ТаблицаЗначений	 - исходная таблица.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - транспонированная таблица.
//
Функция ТранспонироватьТаблицуЗначений(ТаблицаИсходная) Экспорт 
	
	ТаблицаПриемная = Новый ТаблицаЗначений;
	Массив = ТаблицаИсходная.ВыгрузитьКолонку(ТаблицаИсходная.Колонки[0]);
	Для Каждого ЗначениеЯчейки ИЗ Массив ЦИКЛ
		ТаблицаПриемная.Колонки.Добавить("Колонка" + (ТаблицаПриемная.Колонки.Количество()));
	Конеццикла;                                   
	Для каждого СтрокаИсходная из ТаблицаИсходная.Колонки Цикл
		ТаблицаПриемная.Добавить();
	КонецЦикла;
	СчетчикСтрок = -1;
	Для каждого СтрокаИсходная Из ТаблицаИсходная Цикл
		СчетчикСтрок = СчетчикСтрок + 1;
		МассивЗначенийКолонки = Новый Массив;
		Счетчик = -1;
		Для каждого КолонкаИсходная из ТаблицаИсходная.Колонки Цикл
			Счетчик = Счетчик + 1;
			ЗначениеИндекса = СтрокаИсходная[КолонкаИсходная.Имя];
			МассивЗначенийКолонки.Вставить(Счетчик, ЗначениеИндекса);
		КонецЦикла;
		ТаблицаПриемная.ЗагрузитьКолонку(МассивЗначенийКолонки, ТаблицаПриемная.Колонки[СчетчикСтрок]);
	КонецЦикла;
	Возврат ТаблицаПриемная;
	
КонецФункции

// Функция - Найти создать ссылочный объект по ключу
//
// Параметры:
//  ОбъектМетаданныхИлиТип	 - ОбъектМетаданныхИлиТип - ОбъектМетаданныхИлиТип
//  КлючОбъекта			 - КлючОбъекта			 - КлючОбъекта
//  СтрокаПорядка			 - СтрокаПорядка		 - СтрокаПорядка
//  СоздатьПриОтсутствии		 - СоздатьПриОтсутствии	 - СоздатьПриОтсутствии
//  СвойстваНового			 - СвойстваНового		 - СвойстваНового
//  выхКоличествоНайденных	 - выхКоличествоНайденных - выхКоличествоНайденных
//  ВыводитьСообщения		 - Булево				 - выводить сообщения.
// 
// Возвращаемое значение:
//  Ссылка - ссылка.
//
Функция НайтиСоздатьСсылочныйОбъектПоКлючу(Знач ОбъектМетаданныхИлиТип, Знач КлючОбъекта, Знач СтрокаПорядка = "", СоздатьПриОтсутствии = Ложь, СвойстваНового = Неопределено,
	выхКоличествоНайденных = 0, ВыводитьСообщения = Истина) Экспорт
	
	Если ТипЗнч(ОбъектМетаданныхИлиТип) = Тип("ОбъектМетаданных") Тогда
		ПолноеИмяМД = ОбъектМетаданныхИлиТип.ПолноеИмя();
		ПолноеИмяМДИлиСсылкаТипа = Новый (СтрЗаменить(ПолноеИмяМД, ".", "Ссылка."));
	ИначеЕсли ТипЗнч(ОбъектМетаданныхИлиТип) = Тип("Тип") Тогда
		ПолноеИмяМДИлиСсылкаТипа = Новый (ОбъектМетаданныхИлиТип);
		ПолноеИмяМД = Метаданные.НайтиПоТипу(ОбъектМетаданныхИлиТип).ПолноеИмя();
	Иначе
		ПолноеИмяМДИлиСсылкаТипа = ОбъектМетаданныхИлиТип;
		ПолноеИмяМД = Метаданные.НайтиПоТипу(ТипЗнч(ОбъектМетаданныхИлиТип)).ПолноеИмя();
	КонецЕсли; 
	Построитель = упСервисныеФункцииПовторноеИспользование.ПолучитьПостроительЗапросаОбъектаМетаданныхКэш(ПолноеИмяМДИлиСсылкаТипа);
	#Если _ Тогда
	    Построитель = Новый ПостроительЗапроса;
	#КонецЕсли
	//Если ТипЗнч(КлючОбъекта) = Тип("СправочникСсылка.НаборыЗначенийРеквизитовИис") Тогда
	//	Запрос = ПолучитьЗапрос(зпИменаСвойствСИхЗначениямиИзНабораЗначенийРеквизитов);
	//	Запрос.Параметры.НаборЗначенийРеквизитов = КлючОбъекта;
	//	Таблица = Запрос.Выполнить().Выгрузить();
	//	СтруктураКлюча = Новый Структура;
	//	Для Каждого СтрокаТаблицы Из Таблица Цикл
	//		СтруктураКлюча.Вставить(СтрокаТаблицы.ИсточникЗначения, СтрокаТаблицы.Значение);
	//	КонецЦикла;
	//Иначе
		СтруктураКлюча = КлючОбъекта;
	//КонецЕсли;
	УстановитьОтборПоСтруктуре(Построитель.Отбор, СтруктураКлюча);
	Если ЗначениеЗаполнено(СтрокаПорядка) Тогда
		Построитель.Порядок.Установить(СтрокаПорядка);
	КонецЕсли;
	ТаблицаРезультата = Построитель.Результат.Выгрузить();
	выхКоличествоНайденных = ТаблицаРезультата.Количество();
	Если выхКоличествоНайденных > 0 Тогда
		Результат = ТаблицаРезультата[0].Ссылка;
		Результат = Результат.ПолучитьОбъект(); //
	Иначе
		Результат = Неопределено;
	КонецЕсли; 
	Если Истина
		И СоздатьПриОтсутствии
		И Результат = Неопределено
	Тогда
		//НовыйОбъект = Менеджер.СоздатьЭлемент();
		НовыйОбъект = СоздатьСсылочныйОбъектПоМетаданным(ПолноеИмяМД);
		ЗаполнитьЗначенияСвойств(НовыйОбъект, КлючОбъекта); 
		Если СвойстваНового <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(НовыйОбъект, СвойстваНового);
		КонецЕсли; 
		Результат = НовыйОбъект;
		//
		//НовыйОбъект.Записать();
		//Результат = НовыйОбъект.Ссылка;
		////Если ВыводитьСообщения Тогда
		////	ОбщийИис.СообщитьПользователю("Создан элемент """ + НовыйОбъект + """ вида """ + ТипЗнч(ПолноеИмяМДИлиСсылкаТипа) + """", Результат);
		////КонецЕсли; 
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Функция - Создать ссылочный объект по метаданным
//
// Параметры:
//  ПолноеИмяМД						 - Строка	 - имя метаданных.
//  ЭтоГруппаДляНового					 - Булево	 - это группа.
//  ИдентификаторСсылки					 - УникальныйИдентификатор	 - идентификатор.
//  ЗаполнитьРеквизитыЗначениямиЗаполнения	 - Булево	 - заполнить значениями заполнения.
// 
// Возвращаемое значение:
//   Объект - объект.
//
Функция СоздатьСсылочныйОбъектПоМетаданным(ПолноеИмяМД, ЭтоГруппаДляНового = Ложь, ИдентификаторСсылки = Неопределено, ЗаполнитьРеквизитыЗначениямиЗаполнения = Истина) Экспорт

	Если ИдентификаторСсылки = Неопределено Тогда
		ИдентификаторСсылки = Новый УникальныйИдентификатор();
	КонецЕсли; 
	Если ЭтоГруппаДляНового Тогда
		Менеджер = Новый (СтрЗаменить(ПолноеИмяМД, ".", "Менеджер."));
		Объект = Менеджер.СоздатьГруппу();
	Иначе
		Объект = Новый (СтрЗаменить(ПолноеИмяМД, ".", "Объект."));
	КонецЕсли; 
	Объект = ЗаменитьИдентификаторОбъекта(Объект, ИдентификаторСсылки);
	Если ЗаполнитьРеквизитыЗначениямиЗаполнения Тогда
		//ЗаполнитьРеквизитыОбъектаЗначениямиЗаполнения(Объект);
		Объект.Заполнить(Неопределено);
	КонецЕсли; 
	Возврат Объект;

КонецФункции

// Функция - Заменить идентификатор объекта
//
// Параметры:
//  Объект			 - Объект	 - объект.
//  ИдентификаторСсылки	 - УникальныйИдентификатор	 - идентификатор.
// 
// Возвращаемое значение:
//  Объект - объект.
//
Функция ЗаменитьИдентификаторОбъекта(Объект, ИдентификаторСсылки = Неопределено) Экспорт 
	
	Если ИдентификаторСсылки = Неопределено Тогда
		ИдентификаторСсылки = Новый УникальныйИдентификатор;
	КонецЕсли; 
	Объект = СериализаторXDTO.ЗаписатьXDTO(Объект);
	Объект.Ref = ИдентификаторСсылки;
	//Объект.IsFolder = ЭтоГруппаДляНового;
	Объект = СериализаторXDTO.ПрочитатьXDTO(Объект);
	Возврат Объект;
	
КонецФункции

// Функция - Установить отбор по структуре
//
// Параметры:
//  Отбор						 - Отбор	 - отбор.
//  СтруктураОтбора				 - Структура	 - структура отбора.
//  Сравнение					 - ВидСравнения	 - вид сравнения.
//  ДобавлятьСсылкуВПутьКДанным	 - Булево	 - добавлять ссылку в путь к данным.
//  СброситьПередУстановкой		 - Булево	 - сбросить перед установкой.
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция УстановитьОтборПоСтруктуре(Знач Отбор, Знач СтруктураОтбора, Знач Сравнение = "", Знач ДобавлятьСсылкуВПутьКДанным = Ложь,
	Знач СброситьПередУстановкой = Истина) Экспорт
	
	Если СброситьПередУстановкой Тогда
		Отбор.Сбросить();
	КонецЕсли;
	Для Каждого КлючИЗначение Из СтруктураОтбора Цикл
		ПутьКДанным = КлючИЗначение.Ключ;
		Если ДобавлятьСсылкуВПутьКДанным Тогда
			ПутьКДанным = "Ссылка." + ПутьКДанным;
		КонецЕсли;
		НайтиДобавитьЭлементОтбора(Отбор, ПутьКДанным, Сравнение, КлючИЗначение.Значение, , , КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецФункции

// Функция - Найти добавить элемент отбора
//
// Параметры:
//  ОтборИлиОбъект	 - Отбор	 - отбор.
//  ПутьКДанным	 - Строка	 - путь к данным.
//  Сравнение		 - ВидСравнения	 - сравнение.
//  Значение		 - Значение	 - значение.
//  ЗначениеПо		 - Значение	 - значение по.
//  Использование	 - Булево	 - использование.
//  Имя			 - Строка	 - имя.
//  Представление	 - Строка	 - представление.
// 
// Возвращаемое значение:
//  ЭлементОтбора - элемент отбора.
//
Функция НайтиДобавитьЭлементОтбора(Знач ОтборИлиОбъект, Знач ПутьКДанным = "", Знач Сравнение = "", Знач Значение = Неопределено, Знач ЗначениеПо = Неопределено,
	Знач Использование = Истина, Знач Имя = "", Знач Представление = "") Экспорт
	
	Если ТипЗнч(ОтборИлиОбъект) = Тип("Отбор") Тогда
		Отбор = ОтборИлиОбъект;
	Иначе
		Отбор = ОтборИлиОбъект.Отбор;
	КонецЕсли;
	
	// Ищем или добавляем новый элемент отбора
	ЭлементОтбора = Неопределено;
	
	//Если Имя <> Неопределено Тогда
	//	ЭлементОтбора= Отбор.Найти(Имя);
	//КонецЕсли;
	Если Имя <> Неопределено Тогда
		Для Каждого ЭлОтбора Из Отбор Цикл
			Если Ложь
				Или ЭлОтбора.Имя = Имя
				Или ЭлОтбора.Представление = Имя
			Тогда
				ЭлементОтбора = ЭлОтбора;            
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ЭлементОтбора = Неопределено Тогда
		Попытка
			ЭлементОтбора = Отбор.Добавить(ПутьКДанным, Имя, Представление);
		Исключение
			ВызватьИсключение "Ошибка добавления элемента отбора """ + ПутьКДанным + """ построителя запроса: " + ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	УстановитьЭлементОтбора(ЭлементОтбора, Сравнение, Значение, ЗначениеПо, Использование);
	Результат = ЭлементОтбора;
	Возврат Результат;
	
КонецФункции

// Функция - Установить элемент отбора
//
// Параметры:
//  ЭлементОтбора				 - ЭлементОтбора	 - элемент отбора.
//  Сравнение					 - ВидСравнения	 - сравнение.
//  Значение					 - Значение	 - значение.
//  ЗначениеПо					 - Значение	 - значение по.
//  Использование				 - Строка	 - использование. 
//  ПриводитьТипДляНеопределено	 - Булево	 - приводить тип.
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция УстановитьЭлементОтбора(Знач ЭлементОтбора, Знач Сравнение = "", Знач Значение, Знач ЗначениеПо, Знач Использование = Истина,
	Знач ПриводитьТипДляНеопределено = Истина) Экспорт
	
	Если ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда 
		Значение = Новый Массив(Значение);
	КонецЕсли; 
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.ЗагрузитьЗначения(Значение);
		Значение = СписокЗначений;
	ИначеЕсли Истина
		И ПриводитьТипДляНеопределено
		И Значение = Неопределено
	Тогда
		Значение = ЭлементОтбора.ТипЗначения.ПривестиЗначение(Значение);
	КонецЕсли;
	
	// Вид сравнения
	//Если СтрокиРавныИис(Сравнение, "Авто") Тогда
	//	Сравнение = ОпределитьВидСравненияПоЗначению(Значение);
	//Иначе
	Если ТипЗнч(Сравнение) = Тип("ВидСравнения") Тогда
	//ИначеЕсли Истина
	//	И Сравнение <> Неопределено // для оптимизации
	//	И УФ(сСравнитьТипОбъектаБД, Сравнение, оСправочникСсылка_ЗначенияПеречислений2iS)
	//Тогда
	//	Сравнение = глКэш.ЗначенияСистемныхПеречислений.Найти(Сравнение, "Ссылка").Значение;
	ИначеЕсли Истина
		И Сравнение <> Неопределено 
		И Сравнение <> "" 
	Тогда // Добавлено 25.03.2012
		ВызватьИсключение "Неверный тип сравнения """ + ТипЗнч(Сравнение) + """";
	Иначе
		Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			Сравнение = ВидСравнения.ВСписке;
		Иначе
			Сравнение = ВидСравнения.Равно;
		КонецЕсли;
	КонецЕсли;
	// Еще надо сделать автоопределение Сожержит, как у компоновки
	
	ЭлементОтбора.ВидСравнения = Сравнение;
	Попытка
		Если ЗначениеПо <> Неопределено Тогда
			ЭлементОтбора.ЗначениеС  = Значение;
			ЭлементОтбора.ЗначениеПО = ЗначениеПо;
		Иначе
			ЭлементОтбора.Значение = Значение;
		КонецЕсли;
	Исключение
		ВызватьИсключение "Ошибка установки значения типа """ + ТипЗнч(Значение) + """ элементу отбора """ + ЭлементОтбора.Имя + """: " + ОписаниеОшибки();
	КонецПопытки; 
	Попытка
		ЭлементОтбора.Использование = Использование;
	Исключение
		// Сюда попадаем для элемента отбора набора записей с регистратором
	КонецПопытки;
	
КонецФункции

// Функция - Получить построитель запроса объекта метаданных
//
// Параметры:
//  ОпределительМетаданных	 - ОпределительМетаданных	 - тип или полное имя.
//  ВыбираемыеПоля			 - Строка	 - поля.
//  ЛиРазличные			 - Булево	 - различные.
//  Первые				 - Число	 - первые.
//  Автоупорядочивание		 - Булево	 - автоупорядочивание.
// 
// Возвращаемое значение:
//  ПостроительЗапроса - построитель запроса.
//
Функция ПолучитьПостроительЗапросаОбъектаМетаданных(Знач ОпределительМетаданных, ВыбираемыеПоля = "*", ЛиРазличные = Ложь,
	Первые = 0, Автоупорядочивание = Ложь) Экспорт
	
	Если ТипЗнч(ОпределительМетаданных) = Тип("Тип") Тогда
		ОпределительМетаданных = Метаданные.НайтиПоТипу(ОпределительМетаданных);
	ИначеЕсли ТипЗнч(ОпределительМетаданных) = Тип("Строка") Тогда
		ОпределительМетаданных = Метаданные.НайтиПоПолномуИмени(ОпределительМетаданных);
	КонецЕсли; 
	Построитель = Новый ПостроительЗапроса;
	ПолноеИмяМД = ОпределительМетаданных.ПолноеИмя();
	//ПолноеИмяТаблицы = ПолучитьИмяТаблицыИзМетаданных(ПолноеИмяМД);
	ПолноеИмяТаблицы = ПолноеИмяМД;
	Текст = "ВЫБРАТЬ ";
	Если Первые > 0 Тогда
		Текст = Текст + " ПЕРВЫЕ " + Формат(Первые, "ЧГ=") + " ";
	КонецЕсли; 
	Если ЛиРазличные Тогда
		Текст = Текст + "РАЗЛИЧНЫЕ ";
		ПолныйПостроитель = ПолучитьПостроительЗапросаОбъектаМетаданных(ОпределительМетаданных);
		#Если _ Тогда
			ПолныйПостроитель = Новый ПостроительЗапроса;
		#КонецЕсли
		ВыбранныеПоля = "";
		Для Каждого ДоступноеПоле Из ПолныйПостроитель.ДоступныеПоля Цикл
			Если Ложь
				Или ДоступноеПоле.ТипЗначения.СодержитТип(Тип("ХранилищеЗначения"))
				Или (Истина
					И ДоступноеПоле.ТипЗначения.СодержитТип(Тип("Строка"))
					И ДоступноеПоле.ТипЗначения.КвалификаторыСтроки.Длина = 0)
			Тогда
				Продолжить;
			КонецЕсли; 
			Если ВыбранныеПоля <> "" Тогда
				ВыбранныеПоля = ВыбранныеПоля + ", ";
			КонецЕсли; 
			ВыбранныеПоля = ВыбранныеПоля + "Т." + ДоступноеПоле.ПутьКДанным;
		КонецЦикла;
	Иначе
		ВыбранныеПоля = "Т.*";
	КонецЕсли; 
	Текст = Текст + "РАЗРЕШЕННЫЕ " + ВыбранныеПоля + " ИЗ " + ПолноеИмяТаблицы + " КАК Т";
	Если Автоупорядочивание Тогда
		Текст = Текст + "
		|АВТОУПОРЯДОЧИВАНИЕ"; 
	КонецЕсли; 
	Построитель.Текст = Текст;
	Построитель.ЗаполнитьНастройки();
	Возврат Построитель;
	
КонецФункции // ПолучитьПостроительЗапроса()

// Установка явной блокировки данных, вызывается из обработчика события "ОбработкаПроведения"
//
// Параметры:
//  БлокировкаДанных - БлокировкаДанных	 - Набор элементов блокировки данных
//  Ссылка		 - Ссылка	 - Ссылка на объект блокировки
//  Отказ			 - Булево	 - Признак отказа в установке блокировки
//
Процедура УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ) Экспорт

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецПопытки;
	
КонецПроцедуры // УстановитьУправляемуюБлокировку()

// Для Объект = Неопределено возвращает Ложь, работает только для русского и английского языков платформы
//
// Параметры:
//  Объект	 - Объект	 - объект.
//  ИмяМетода	 - Строка	 - имя метода объекта.
// 
// Возвращаемое значение:
//  Булево - если метод реализован.
//
Функция МетодРеализован(Объект, ИмяМетода) Экспорт
	
	Возврат Ложь;
	
	Если Объект = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Выражение = "Объект." + ИмяМетода + "(,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,)";
	Попытка
		Выполнить(Выражение);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Описание = Инфо.Описание;
		//СообщитьИис(Описание);
	КонецПопытки;
	Результат = Ложь
		Или Описание = "Слишком много фактических параметров" 
		Или Описание = "Too many actual parameters";
	Возврат Результат;
	
КонецФункции

// Получает строку путем повтора переданной строки заданное количество раз.
//
// Параметры:
//  СтрокаДляПовтора - Строка	 - строка.
//  ЧислоПовторов – Число - повторов.
// 
// Возвращаемое значение:
//  Строка - строка с повтором.
//
Функция ПолучитьСтрокуПовтором(СтрокаДляПовтора, ЧислоПовторов) Экспорт

	Результат = "";
	Для Счетчик = 1 По ЧислоПовторов Цикл
		Результат = Результат + СтрокаДляПовтора;
	КонецЦикла;
	Возврат Результат;

КонецФункции // ПолучитьСтрокуПовтором()

// Рекурсивно идет вверх до родителя нужного типа
//
// Параметры:
//  Элемент	 - Элемент	 - элемент.
//  ТипРодителя - Тип - Неопределено=Тип("УправляемаяФорма") Возвращаемое значение:
// 
// Возвращаемое значение:
//  Родитель - родитель.
//
Функция ПолучитьРодителяЭлементаУправляемойФормы(Элемент, ТипРодителя = Неопределено) Экспорт

	ТипУправляемаяФорма = Тип("УправляемаяФорма");
	Если ТипРодителя = Неопределено Тогда
		ТипРодителя = ТипУправляемаяФорма;
	КонецЕсли; 
	Родитель = Элемент.Родитель;
	Пока ТипЗнч(Родитель) <> ТипРодителя Цикл
		Если ТипЗнч(Родитель) = ТипУправляемаяФорма Тогда
			Родитель = Неопределено;
			Прервать;
		КонецЕсли;
		Родитель = Родитель.Родитель;
	КонецЦикла; 
	Возврат Родитель;

КонецФункции

// Процедура - Добавить условное оформление формы для поля стиля линии
//
// Параметры:
//  ПолеФормы		 - Поле - поле формы.
//  ПутьКДаннымСтиля - Строка	 - путь.
//
Процедура ДобавитьУсловноеОформлениеФормыДляПоляСтиляЛинии(ПолеФормы, ПутьКДаннымСтиля = Неопределено) Экспорт 
	
	ЭтаФорма = ПолучитьРодителяЭлементаУправляемойФормы(ПолеФормы);
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 100
	|	упСтилиЛиний.Ссылка,
	|	упСтилиЛиний.Цвет
	|ИЗ
	|	Справочник.упСтилиЛиний КАК упСтилиЛиний
	|";
	ТаблицаСтилейЛиний = Запрос.Выполнить().Выгрузить();
	Счетчик = 1;
	УсловноеОформление = ЭтаФорма.УсловноеОформление;
	#Если Сервер И Не Сервер Тогда
	    УсловноеОформление1 = Новый НастройкиКомпоновкиДанных;
		УсловноеОформление = УсловноеОформление1.УсловноеОформление;
	#КонецЕсли
	Для Каждого СтрокаЛинии Из ТаблицаСтилейЛиний Цикл
		ЦветЛинии = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(СтрокаЛинии.Цвет);
		Если ПутьКДаннымСтиля = Неопределено Тогда
			ПутьКДаннымСтиля = ПолеФормы.ПутьКДанным;
		КонецЕсли; 
		
		ЭлементУсловноеОформление = УсловноеОформление.Элементы.Добавить();
		ЭлементУсловноеОформление.Представление = "# Цвет стиля " + Счетчик + " для поля " + ПолеФормы.Имя;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭлементУсловноеОформление.Отбор, ПутьКДаннымСтиля, СтрокаЛинии.Ссылка);
		ПолеУсловногоОформления = ЭлементУсловноеОформление.Поля.Элементы.Добавить();
		ПолеУсловногоОформления.Поле = Новый ПолеКомпоновкиДанных(ПолеФормы.Имя);
		ПолеУсловногоОформления.Использование = Истина;
		ЭлементУсловноеОформление.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветЛинии);
		ЭлементУсловноеОформление.Использование = Истина;
		
		Счетчик = Счетчик + 1;
	КонецЦикла;

КонецПроцедуры

// Функция декодирует файл форматов .esm, .ddd
//
// Параметры:
//  Файл  - Файл - Двоичные данные
//
// Возвращаемое значение:
//   Структура
//		* ТекстОшибки - Строка - текст ошибки, возникшей при попытке декодировать файл
//		* ДанныеИдентификации - Структура - структура с полями:
//			** НомерКарты - Строка - номер карты.
//			** ОрганВыдавшийКарту - Строка - орган выдавший карту.
//			** ДатаВыпускаКарты - Дата -  в универсальном времени
//			** ДатаНачалаДействияКарты - Дата - в универсальном времени
//			** ДатаОкончанияДействияКарты - Дата - в универсальном времени
//			** ФамилияВодителя - Строка - фамилия.
//			** ИмяВодителя - Строка - имя.
//		* ТаблицаАктивностей - ТаблицаЗначений  - с колонками:
//			** Период - Дата - в универсальном времени
//			** СлотСчитывающегоУстройства - ПеречислениеСсылка.упТахографСлотыСчитывающегоУстройства - слот.
//			** СтатусУправления - ПеречислениеСсылка.упТахографСтатусыУправления - статус.
//			** ПоложениеКарточки - ПеречислениеСсылка.упТахографПоложенияКарточкиВСчитывающемУстройстве - положение карточки.
//			** РежимДеятельности - ПеречислениеСсылка.упТахографРежимыДеятельностиВодителей - режим.
//
Функция РасшифроватьФайлТахографа(Файл) Экспорт

	Возврат упСЛК.СоздатьОбъект().РасшифроватьФайлТахографа(Файл);

КонецФункции // РасшифроватьФайлТахографа()

// Процедура - Установить часовой пояс сеанса
//
// Параметры:
//  ИменаПараметровСеанса - Строка	 - имена параметров.
//  ЧасовойПояс		 - СправочникСсылка.упЧасовыеПояса 	 - часовой пояс.
//
Процедура УстановкаПараметровСеанса(ИменаПараметровСеанса, ЧасовойПояс = Неопределено) Экспорт
	
	Если Ложь
		Или Не ОбщегоНазначенияПовтИсп.РазделениеВключено() 
		Или Не ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей() 
	Тогда
	
		Если ИменаПараметровСеанса = Неопределено Тогда
			ПараметрыСеанса.упЧасовойПоясУстановлен  = Ложь;
			ИменаПараметровСеанса = Новый Массив;
			ИменаПараметровСеанса.Добавить("упВедетсяРаботаВРазныхЧасовыхПоясах");
		КонецЕсли;	
		
		Если Истина
			И Не ИменаПараметровСеанса.Найти("упЧасовойПояс") = Неопределено 
			И ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах")
		Тогда
		
			Если Ложь
				Или Не ПараметрыСеанса.упЧасовойПоясУстановлен 
				Или ЗначениеЗаполнено(ЧасовойПояс) 
	 		Тогда
				Если ЗначениеЗаполнено(ЧасовойПояс) Тогда
					ИдентификаторЧасовогоПояса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЧасовойПояс, "ИдентификаторЧасовогоПояса");
				Иначе	
					сткЧасовойПояс = ПолучитьЧасовойПоясПользователя();	
					ИдентификаторЧасовогоПояса = сткЧасовойПояс.ИдентификаторЧасовогоПояса;
					ЧасовойПояс = сткЧасовойПояс.ЧасовойПояс;
				КонецЕсли;
				УстановитьЧасовойПоясСеанса(ИдентификаторЧасовогоПояса);	
				ПараметрыСеанса.упЧасовойПояс	          = ЧасовойПояс;
				ПараметрыСеанса.упЧасовойПоясУстановлен = Истина;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Функция - Получить часовой пояс пользователя
// 
// Возвращаемое значение:
//  Структура - структура с полями:
//  * Наименование 				- Строка - наименование пояса
//  * ИдентификаторЧасовогоПояса 	- Строка - идентификатор пояса
//
Функция ПолучитьЧасовойПоясПользователя() Экспорт
	
	сткРезультат = Новый Структура("Наименование, ИдентификаторЧасовогоПояса, ЧасовойПояс", "", "", Справочники.упЧасовыеПояса.ПустаяСсылка());
	
	ЭтоВнешнийПользователь = ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя();
	Если НЕ ЭтоВнешнийПользователь Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упЧасовыеПоясаПользователейСрезПоследних.ЧасовойПояс КАК ЧасовойПояс
		|ПОМЕСТИТЬ втЧасовыеПояса
		|ИЗ
		|	РегистрСведений.упЧасовыеПоясаПользователей.СрезПоследних(&Период, Пользователь = &Пользователь) КАК упЧасовыеПоясаПользователейСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упСтруктураПредприятия.ЧасовойПояс
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтруктураПредприятия КАК упСтруктураПредприятия
		|		ПО Пользователи.Подразделение = упСтруктураПредприятия.Ссылка
		|ГДЕ
		|	Пользователи.Ссылка = &Пользователь
		|	И &ВедетсяУчетПодразделений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упЧасовыеПояса.Наименование,
		|	упЧасовыеПояса.ИдентификаторЧасовогоПояса,
		|	упЧасовыеПояса.Ссылка КАК ЧасовойПояс
		|ИЗ
		|	втЧасовыеПояса КАК втЧасовыеПояса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упЧасовыеПояса КАК упЧасовыеПояса
		|		ПО втЧасовыеПояса.ЧасовойПояс = упЧасовыеПояса.Ссылка";
		Запрос.УстановитьПараметр("Период", 					ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("ВедетсяУчетПодразделений", 	ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"));
		
		ТекущийПользователь =  ПользователиКлиентСервер.ТекущийПользователь();
		
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
		
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
		КонецЕсли; 
	КонецЕсли;
	Возврат сткРезультат;
	
КонецФункции

// Функция - Возвращает ссылку на текущий часовой пояс сеанса.
// 
// Возвращаемое значение:
//  СправочникСсылка.упЧасовыеПояса - часовой пояс сеанса.
//
Функция ТекущийЧасовойПояс() Экспорт
	Возврат ПараметрыСеанса.упЧасовойПояс;	
КонецФункции

// Функция - Часовой пояс вида перевозки
//
// Параметры:
//  ВидПеревозки - СправочникСсылка.упВидыПеревозок	 - вид перевозки.
// 
// Возвращаемое значение:
//  СправочникСсылка.упЧасовыеПояса - часовой пояс.
//
Функция ЧасовойПоясВидаПеревозки(ВидПеревозки = Неопределено) Экспорт
	
	Результат = ТекущийЧасовойПояс();
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ЧасовойПоясВидаПеревозки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ЧасовойПояс");
		Если ЗначениеЗаполнено(ЧасовойПоясВидаПеревозки) Тогда
			Результат = ЧасовойПоясВидаПеревозки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура - Добавить в заголовок формы часовой пояс.
//
// Параметры:
//  Форма		 - Форма - форма объекта, в заголовок которого нужно добавить описание часового пояса.
//  ЧасовойПояс	 - СправочникСсылка.упЧасовыеПояса - часовой пояс.
//
Процедура ДобавитьВЗаголовокЧасовойПояс(Форма, ЧасовойПояс) Экспорт
	Если ЗначениеЗаполнено(ЧасовойПояс) Тогда
		Форма.Заголовок = СформироватьЗаголовокСЧасовымПоясом(Форма.Заголовок, ЧасовойПояс);
	КонецЕсли;
КонецПроцедуры

// Функция - Сформировать заголовок с часовым поясом
//
// Параметры:
//  Заголовок	 - Строка	 - заголовок формы.
//  ЧасовойПояс	 - СправочникСсылка.упЧасовыеПояса - часовой пояс.
// 
// Возвращаемое значение:
//  Строка - заголовок
//
Функция СформироватьЗаголовокСЧасовымПоясом(Заголовок, ЧасовойПояс) Экспорт
	Результат = Заголовок;
	Если ЗначениеЗаполнено(ЧасовойПояс) Тогда
		Результат	= СтрШаблон("%1 / %2", Заголовок, ЧасовойПояс);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Функция - Сравнивает часовой пояс сеанча и часовой пояс документа.
//
// Параметры:
//  ЧасовойПоясДокумента - СправочникСсылка.упЧасовыеПояса - часовой пояс документа.
// 
// Возвращаемое значение:
//  Булево - Истина, если часовые пояса совпадают.
//
Функция СовпадаютЧасовойПоясПользователяИДокумента(ЧасовойПоясДокумента) Экспорт
	
	ЧасовойПоясСеанса = ПараметрыСеанса.упЧасовойПояс;
	Результат = упСервисныеФункцииПовторноеИспользование.СовпадаютЧасовыеПояса(ЧасовойПоясСеанса, ЧасовойПоясДокумента);
	
	Если НЕ Результат Тогда
		ТекстСообщения = Нстр("ru = 'Часовой пояс сеанса:%1 и документа:%2 не совпадают. Возможен только просмотр документа. %3'");
		ТестДоступнаРоль = "";
		Если Ложь
			Или Пользователи.ЭтоПолноправныйПользователь()
			Или РольДоступна("упРазрешенаРаботаВРазныхЧасовыхПоясах") Тогда
			ТестДоступнаРоль = Нстр("ru = 'Для изменения часового пояса сеанса воспользуйтесь командой ""Сервис и настройки""->""Сменить часовой пояс сеанса""'");
		КонецЕсли;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ЧасовойПоясСеанса, ЧасовойПоясДокумента, ТестДоступнаРоль);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Перевести время при смене часового пояса.
//
// Параметры:
//  ВремяВПоясеИсточник	 - УниверсальнаяДата - ДатаВремя которое необходимо перевести из пояса "источник" в пояс "приемник".
//  ЧасовойПоясИсточник	 - СправочникСсылка.упЧасовыеПояса - часовой пояс "источник"(из которого переводится время).
//  ЧасовойПоясПриемник	 - СправочникСсылка.упЧасовыеПояса - часовой пояс "приемник" (в который переводится время).
// 
// Возвращаемое значение:
//  ДатаВремя - время в часовом поясе-приемнике. 
//
Функция ПеревестиВремяПриСменеЧасовогоПояса(ВремяВПоясеИсточник, ЧасовойПоясИсточник, ЧасовойПоясПриемник) Экспорт
	
	мсвСсылок = Новый Массив;
	мсвСсылок.Добавить(ЧасовойПоясИсточник);
	мсвСсылок.Добавить(ЧасовойПоясПриемник);
	
	соотЧасовыеПояса = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвСсылок, "ИдентификаторЧасовогоПояса");
	
	Универсальная = УниверсальноеВремя(ВремяВПоясеИсточник, соотЧасовыеПояса.Получить(ЧасовойПоясИсточник));
	Возврат МестноеВремя(Универсальная, соотЧасовыеПояса.Получить(ЧасовойПоясПриемник));
КонецФункции

// Функция - ЭтоПослеОткрытияФормы.
//
// Параметры:
//  ЭтаФорма	 - Форма	 - форма.
// 
// Возвращаемое значение:
//  Булево - только просмотр.
//
Функция ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Экспорт
	
	Возврат Не ЭтаФорма.Параметры.Свойство("ТолькоПросмотр");

КонецФункции

// Процедура - Записать массив документов
//
// Параметры:
//  мсвДокументы	 - Массив	 - документы.
//  РежимЗаписи	 - РежимЗаписи	 - режим.
//  ПометкаУдаления	 - Булево	 - пометка удаления.
//  ТекстОшибки	 - Строка	 - описание ошибки.
//  Отказ			 - Булево	 - Истина, в случае ошибки.
//
Процедура ЗаписатьМассивДокументов(мсвДокументы, РежимЗаписи, ПометкаУдаления = Ложь, ТекстОшибки, Отказ) Экспорт
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);

	Для каждого ДокументТекущий Из мсвДокументы Цикл
		ДокументОбъект = ДокументТекущий.ПолучитьОбъект();
		ДокументОбъект.ПометкаУдаления = ПометкаУдаления;
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписи);
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			Отказ = Истина;
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,ДокументТекущий, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

// Функция - Приводит текущую дату сеанса к часовому поясу документа, если они не совпадают,
//			иначе возвращает текущю дату сеанса
//
// Параметры:
//  ЧасовойПоясДокумента	 - СправочникСсылка.упЧасовойПояс	 - часовой пояс документа.
// 
// Возвращаемое значение:
//  ДатаВремя - Приведенная дата сеанса.
//
Функция ПриведеннаяКЧасовомуПоясуДокументаДатаСеанса(ЧасовойПоясДокумента) Экспорт
	
	Результат = ТекущаяДатаСеанса();
	
	Если Истина
		И ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах")
		И НЕ СовпадаютЧасовойПоясПользователяИДокумента(ЧасовойПоясДокумента) Тогда
		ЧасовойПоясСеанса = упСервисныеФункции.ТекущийЧасовойПояс();
		Результат = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Результат, ЧасовойПоясСеанса, ЧасовойПоясДокумента);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получим КИ для переданого объекта.
//
// Параметры:
//  Ссылка	 - СправочникСсылка.упПартнеры, СправочникСсылка.упКонтрагенты, СправочникСсылка.Организации - Ссылка на носитель КИ.
// 
// Возвращаемое значение:
//  Строка - КИ.
//
Функция ПолучитьКонтактнуюИнформацию(Ссылка) Экспорт
	
	текРезультат = "";
	ВидАдрес   = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
	ВидФакс    = Справочники.ВидыКонтактнойИнформации.ФаксОрганизации;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упПартнеры") Тогда
		ВидАдрес   = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера;	    
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упКонтрагенты") Тогда
		ВидАдрес   = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;	    
		ВидФакс    = Справочники.ВидыКонтактнойИнформации.ФаксКонтрагенты;
	КонецЕсли;
	
	текАдрес	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидАдрес);
	Если ЗначениеЗаполнено(текАдрес) Тогда
		текРезультат = текРезультат  + текАдрес;	
	КонецЕсли;
		
	текТелефон	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидТелефон);
	Если ЗначениеЗаполнено(текТелефон) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текТелефон);
	КонецЕсли;

	текФакс		= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидФакс);
	Если ЗначениеЗаполнено(текФакс) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текФакс);
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции // ПолучитьКонтактнуюИнформацию()

// Конвертер даты и времени в UNIX формат
//  Определяется время-UNIX количеством секунд, прошедших с полуночи (00:00:00 UTC) 1 января 1970 года.
//
// Параметры:
//  ДатаВремя - Дата - Значение для перевода.
//
// Возвращаемое значение:
//   Число - количество секунд.
//
Функция ПеревестиДатуВремяВUNIXФормат(ДатаВремя) Экспорт
	Результат = 0;
	Если ЗначениеЗаполнено(ДатаВремя) Тогда
		Результат = ДатаВремя - Дата("19700101000000");
	КонецЕсли;
	Возврат Результат;
КонецФункции // ПеревестиДатуВремяВUNIXФормат()

// Процедура - Представления описания ошибки
//
// Параметры:
//  ИсточникОшибки			 - Объект	 - Объект при работе с которым возникла ошибка.
//  ТекстОшибкиДляПользователя - Строка	 - Текст ошибки для вывода пользователю.
//  ТекстОшибкиДляЖР		 - Строка	 - Текст ошибки для записи в журнал регистрации.
//
Процедура ПредставленияОписанияОшибки(Знач ИсточникОшибки = Неопределено, ТекстОшибкиДляПользователя = Неопределено, ТекстОшибкиДляЖР = Неопределено) Экспорт
	
	// Если есть описание ошибки в доп. свойствах объекта.
	Если Истина
		И ИсточникОшибки <> Неопределено
		И ТекстОшибкиДляПользователя <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникОшибки, "ДополнительныеСвойства")
	Тогда
		ТекстОшибкиДляПользователя = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИсточникОшибки.ДополнительныеСвойства, "ТекстОшибки", Неопределено);
	КонецЕсли;
	
	ТекстОшибкиДляЖР = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	Если Не ЗначениеЗаполнено(ТекстОшибкиДляЖР) Тогда 
		ТекстОшибкиДляЖР = ОписаниеОшибки();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстОшибкиДляПользователя) Тогда
		ТекстОшибкиДляПользователя = ТекстОшибкиДляЖР;
	КонецЕсли;
			
КонецПроцедуры

// Клиентские приложения, работающие под управление ОС Windows, для работы со справочной системой, синтакс-помощником,
// а также с полем HTML-документа, планировщиком, форматированным документом и редактором HTML-документа используют библиотеку WebKit начиная с платформы 8.3.14.
// 
Функция ИспользуетсяWebKit() Экспорт
	
	Результат = Ложь;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.14.0") > 0 Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область НастройкиФорм

// Процедура устанавливает для формы переданные найтроки.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//  ПараметрыНастройки - Структура - Параметры.
//
Процедура НастроитьФормуПоПараметрам(Форма, ПараметрыНастройки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упПараметрыКонтекстныхФункциональныхОпций.Ссылка
	|ИЗ
	|	Справочник.упПараметрыКонтекстныхФункциональныхОпций КАК упПараметрыКонтекстныхФункциональныхОпций
	|ГДЕ
	|	ИСТИНА";
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Отбор = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор;
	Для каждого текПараметр Из ПараметрыНастройки Цикл
		Отбор.Добавить(СтрШаблон("упПараметрыКонтекстныхФункциональныхОпций.%1 = %2", текПараметр.Ключ,  Формат(текПараметр.Значение, "БЛ=Ложь; БИ=Истина")));
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Настройка = Выборка.Ссылка;
	Иначе
		СправочникОбъект = Справочники.упПараметрыКонтекстныхФункциональныхОпций.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыНастройки);
		СправочникОбъект.Записать();
		Настройка = СправочникОбъект.Ссылка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Форма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("упНастройкаФормы", Настройка));
	
КонецПроцедуры

// Устанавливает значение параметра СКД.
//
// Параметры:
//  Настройки - Структура - Настройки.
//  Парамет - Структура - Параметр.
//  Значение - Структура- Значение.
//  Использование - Булево - Признак использования.
//
// Возвращаемое значение:
//  ЗначениеПараметра - Значение параметра.
//
Функция УстановитьПараметрСКД(Настройки, Параметр, Значение, Использование) Экспорт
	
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
	
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Использование = Использование;
		ЗначениеПараметра.Значение      = Значение;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции // УстановитьПараметрСКД()

// Устанавливает заголовок формы.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//
Процедура УстановитьЗаголовокФормы(Форма) Экспорт
	
	ЭтотОбъект = Форма.Объект;
	ОбъектСсылка = ЭтотОбъект.Ссылка;
	Форма.Автозаголовок  = Ложь;
	ЗаголовокПоУмолчанию = Истина;
	
	Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда	
		
		Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
			Форма.Заголовок = СтрШаблон("%1 (%2)", ЭтотОбъект.РегистрационныйНомер, ПолучитьПредставлениеОбъекта(ЭтотОбъект, ОбъектСсылка));				
			ЗаголовокПоУмолчанию = Ложь;	
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаголовокПоУмолчанию Тогда
		
		Если ОбъектСсылка.Пустая() Тогда
			ПредставлениеОбъекта = ПолучитьПредставлениеОбъекта(ЭтотОбъект, ОбъектСсылка);
			ТекстСоздание = НСтр("ru = ' (создание)'");
			Форма.Заголовок = СтрШаблон("%1 %2", ПредставлениеОбъекта, ТекстСоздание); 
		Иначе	
			ПредставлениеОбъекта = ПолучитьПредставлениеОбъекта(ЭтотОбъект, ОбъектСсылка);
			Форма.Заголовок = СтрШаблон("%1 (%2)", ЭтотОбъект.Наименование, ПредставлениеОбъекта); 	
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьПредставлениеОбъекта(ОбъектДокумент, ОбъектСсылка)
	Результат = "";
	Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.упТипыТС") Тогда
		
		Если ОбъектДокумент.ВидТранспорта = Перечисления.упВидыТранспорта.Контейнер Тогда
			Результат = Нстр("ru = 'Тип ISO-контейнера'");			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
		Если ОбъектДокумент.ВидТранспорта = Перечисления.упВидыТранспорта.Контейнер Тогда
			Результат = Нстр("ru = 'ISO-контейнер'");			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПрефиксыОбъектов

// Возвращает строку QRКода.
// 
// Параметры:
//  Документ - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьСтрокуДанныхQRКода(Документ) Экспорт
	
	сткПараметрыСервера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Справочники.упСервераСбораДанных.СерверМобильныхПриложений, "Сервер, Порт, ДобавлятьВQRКодДанныеАутентификацииНаМобильномКлиенте");
	
	ДанныеАутентификации = "";
	ШКДокумента          = "";
	
	Если сткПараметрыСервера.ДобавлятьВQRКодДанныеАутентификацииНаМобильномКлиенте Тогда
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.упРейс") Тогда
			ДанныеАутентификации = ПолучитьДанныеАутентификации(Документ);
		ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.упПутевойЛист") Тогда
			Рейс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Рейс");
			ДанныеАутентификации = ПолучитьДанныеАутентификации(Рейс);
			ШКДокумента = ПолучитьШКДокументаДляПечати(Рейс);
		КонецЕсли;	
	КонецЕсли;
	
	Если ШКДокумента = "" Тогда
		ШКДокумента = ПолучитьШКДокументаДляПечати(Документ);
	КонецЕсли;
					
	текРезультат = СтрШаблон("srv=tcpip://%1:%2&route=%3%4",сткПараметрыСервера.Сервер, Формат(сткПараметрыСервера.Порт, "ЧГ="), ШКДокумента, ДанныеАутентификации); 
	
	Возврат текРезультат;
	
КонецФункции

// Функция возвращает префикс штрихкода по объекту.
//
// Параметры:
//  Объект - ОбъектМетаданных - Объект конфигурации.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьПрефиксШтрихкода(Объект) Экспорт
	
	ИмяОбъекта = Объект.Метаданные().ПолноеИмя();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	упПрефиксыШтрихкодов.Префикс
	|ИЗ
	|	РегистрСведений.упПрефиксыШтрихкодов КАК упПрефиксыШтрихкодов
	|ГДЕ
	|	упПрефиксыШтрихкодов.ТипОбъекта = &ТипОбъекта";
	Запрос.УстановитьПараметр("ТипОбъекта", ИмяОбъекта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Префикс;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Функция возвращает штрихкод документа.
//
// Параметры:
//  Документ - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьШКДокументаДляПечати(Документ) Экспорт
	
	сткДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Номер, Дата");
	
	Возврат ПолучитьШКДокумента(ПолучитьПрефиксШтрихкода(Документ), сткДанныеДокумента.Дата, сткДанныеДокумента.Номер);
	
КонецФункции // ПолучитьШКДокументаДляПечати()

// Функция возвращает префикс штрихкода по имени макета.
//
// Параметры:
//  ИмяМакета - Строка - Наименование.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьПрефиксШтрихкодаПечатнойФормы(ИмяМакета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	упПрефиксыПечатныхФорм.Префикс
	|ИЗ
	|	РегистрСведений.упПрефиксыПечатныхФорм КАК упПрефиксыПечатныхФорм
	|ГДЕ
	|	упПрефиксыПечатныхФорм.ИмяМакета = &ИмяМакета";
	Запрос.УстановитьПараметр("ИмяМакета", ИмяМакета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Префикс;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Функция возвращает штрихкод документа.
//
// Параметры:
//  Документ - ДокументСсылка - Ссылка на элемент.
//  ИмяМакета - Строка - Наименование.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьШКПечатнойФормыДляПечати(Документ, ИмяМакета) Экспорт
	
	сткДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Номер, Дата");
	
	Возврат ПолучитьПрефиксШтрихкодаПечатнойФормы(ИмяМакета) + Формат(сткДанныеДокумента.Дата, "ДФ=ггММдд") + сткДанныеДокумента.Номер;
	
КонецФункции // ПолучитьШКДокументаДляПечати()

// Функция возвращает штрихкод документа.
//
// Параметры:
//  Префикс - Строка - префикс объекта.
//  Дата - Дата - Период.
//  Номер - Строка - Наименование.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьШКДокумента(Префикс, Дата, Номер) Экспорт
	
	Возврат Префикс + Формат(Дата, "ДФ=ггММдд") + Номер;
	
КонецФункции // ПолучитьШКДокументаДляПечати()

// Возвращает данные префикса для переданного штрихкода.
//
// Параметры:
//  ШК - Строка - Штрих код.
//
// Возвращаемое значение:
//   РезультатЗапроса  - Результат выполнения.
//
Функция ПолучитьДлинуПрефиксаДокумента(ШК) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	упПрефиксыШтрихкодов.Префикс,
	|	упПрефиксыШтрихкодов.ТипОбъекта,
	|	упПрефиксыШтрихкодов.ДлинаПрефикса
	|ИЗ
	|	РегистрСведений.упПрефиксыШтрихкодов КАК упПрефиксыШтрихкодов
	|ГДЕ
	|	ПОДСТРОКА(&ШК, 1, 3) = упПрефиксыШтрихкодов.Префикс
	|	И упПрефиксыШтрихкодов.ДлинаПрефикса = 3
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	упПрефиксыШтрихкодов.Префикс,
	|	упПрефиксыШтрихкодов.ТипОбъекта,
	|	упПрефиксыШтрихкодов.ДлинаПрефикса
	|ИЗ
	|	РегистрСведений.упПрефиксыШтрихкодов КАК упПрефиксыШтрихкодов
	|ГДЕ
	|	ПОДСТРОКА(&ШК, 1, 2) = упПрефиксыШтрихкодов.Префикс
	|	И упПрефиксыШтрихкодов.ДлинаПрефикса = 2
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	упПрефиксыШтрихкодов.Префикс,
	|	упПрефиксыШтрихкодов.ТипОбъекта,
	|	упПрефиксыШтрихкодов.ДлинаПрефикса
	|ИЗ
	|	РегистрСведений.упПрефиксыШтрихкодов КАК упПрефиксыШтрихкодов
	|ГДЕ
	|	ПОДСТРОКА(&ШК, 1, 1) = упПрефиксыШтрихкодов.Префикс
	|	И упПрефиксыШтрихкодов.ДлинаПрефикса = 1";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ШК", ШК);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Функция возвращает документа по ШК
//
// Параметры:
//  ШК - Строка - Штрих код.
//  ТипОбъекта - Строка - Описание типа объекта.
//
// Возвращаемое значение:
//   ДокументСсылка  - Результат выполнения.
//
Функция ПолучитьДокументПоШК(ШК, ТипОбъекта = Неопределено) Экспорт
	
	Выборка = ПолучитьДлинуПрефиксаДокумента(ШК).Выбрать();
	Если Выборка.Количество() Тогда 
		Пока Выборка.Следующий() Цикл 
			Попытка
				СтрокаДата  = "20" + Сред(ШК, Выборка.ДлинаПрефикса + 1, 6);
				ОписаниеТипаДата = Новый ОписаниеТипов("Дата");
				Дата = ОписаниеТипаДата.ПривестиЗначение(СтрокаДата);
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				Продолжить;
			КонецПопытки;
			
			Если ЗначениеЗаполнено(ТипОбъекта) И ТипОбъекта <> Выборка.ТипОбъекта Тогда
				Продолжить;
			КонецЕсли;
			
			Номер = Прав(ШК, СтрДлина(ШК) - Выборка.ДлинаПрефикса - 6);
			
			ТекстЗапроса = 
			"ВЫБРАТЬ 
			|	тбДокумент.Ссылка
			|ИЗ 
			|	" + Выборка.ТипОбъекта + " КАК тбДокумент 
			|ГДЕ 
			|	НАЧАЛОПЕРИОДА(тбДокумент.Дата, ДЕНЬ) = &Дата 
			|	И тбДокумент.Номер = &Номер";	
			Запрос = Новый Запрос(ТекстЗапроса);
			
			Запрос.УстановитьПараметр("Номер",       Номер);
			Запрос.УстановитьПараметр("Дата",        Дата);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДокумент = РезультатЗапроса.Выбрать();
			Если ВыборкаДокумент.Следующий() Тогда
				Возврат ВыборкаДокумент.Ссылка;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ЗначениеЗаполнено(ТипОбъекта) Тогда 
		Попытка
			СтрокаДата  = "20" + Лев(ШК, 6);
			ОписаниеТипаДата = Новый ОписаниеТипов("Дата");
			Дата = ОписаниеТипаДата.ПривестиЗначение(СтрокаДата);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат Неопределено;
		КонецПопытки;
		
		Номер = Прав(ШК, СтрДлина(ШК) - 6);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ 
		|	тбДокумент.Ссылка
		|ИЗ 
		|	" + ТипОбъекта + " КАК тбДокумент 
		|ГДЕ 
		|	НАЧАЛОПЕРИОДА(тбДокумент.Дата, ДЕНЬ) = &Дата 
		|	И тбДокумент.Номер = &Номер";	
		Запрос = Новый Запрос(ТекстЗапроса);
		
		Запрос.УстановитьПараметр("Номер",       Номер);
		Запрос.УстановитьПараметр("Дата",        Дата);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДокумент = РезультатЗапроса.Выбрать();
		Если ВыборкаДокумент.Следующий() Тогда
			Возврат ВыборкаДокумент.Ссылка;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Неопределено;	
	
КонецФункции // ПолучитьДокументПоШК()

// Изменяет значение константы "упИспользуютсяРазличныеВидыПеревозок" в зависимости от переданного источника.
//
// Параметры:
//  Источник - ДокументСсылка - Ссылка на элемент.
//  Отказ - Булево - флаг ошибки.
//
Процедура ПроверитьЗначениеОпцииИспользоватьРазличныеВидыПеревозокПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ Источник.ЭтоГруппа
		И НЕ ПолучитьФункциональнуюОпцию("упИспользуютсяРазличныеВидыПеревозок")
		И Справочники.упВидыПеревозок.КоличествоВидовПеревозки() > 1 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Константы.упИспользуютсяРазличныеВидыПеревозок.Установить(Истина);
		УстановитьПривилегированныйРежим(Ложь);
		
	ИначеЕсли НЕ Источник.ЭтоГруппа
		И ПолучитьФункциональнуюОпцию("упИспользуютсяРазличныеВидыПеревозок")
		И Справочники.упВидыПеревозок.КоличествоВидовПеревозки() < 2 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Константы.упИспользуютсяРазличныеВидыПеревозок.Установить(Ложь);
		УстановитьПривилегированныйРежим(Ложь);
	
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает префикс источника подписки в соответствии с префиксом организации. 
// Источник подписки должен содержать
// обязательный реквизит шапки "Организация", с типом "СправочникСсылка.Организации".
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача].
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки.
// Префикс - Строка - префикс объекта, который нужно изменить.
//
Процедура УстановитьПрефиксОрганизации(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.упПутевойЛист") Тогда
		Если ПолучитьФункциональнуюОпцию("упВестиНумерациюПутевыхЛистовВРазрезеОрганизаций") Тогда
			Если НЕ Источник.ДополнительныеСвойства.Свойство("НеУстанавливатьПрефиксОрганизации") Тогда
				ПрефиксацияОбъектовСобытия.УстановитьПрефиксОрганизации(Источник, СтандартнаяОбработка, Префикс);	
			КонецЕсли;
		КонецЕсли;
	Иначе	
		ПрефиксацияОбъектовСобытия.УстановитьПрефиксОрганизации(Источник, СтандартнаяОбработка, Префикс);	
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверку модифицированности Даты и Организации документа.
// Если дата не входит в предыдущий период или изменен реквизит Организация, то номер документа обнуляется.
// Это необходимо для назначения нового номера документу.
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки.
//  Отказ    - Булево - флаг отказа.
// 
Процедура ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.упПутевойЛист") Тогда
		Если ПолучитьФункциональнуюОпцию("упВестиНумерациюПутевыхЛистовВРазрезеОрганизаций") Тогда
			Если НЕ Источник.ДополнительныеСвойства.Свойство("НеУстанавливатьПрефиксОрганизации") Тогда
				ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения);
			КонецЕсли;
		КонецЕсли;
	Иначе	
		ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения);	
	КонецЕсли;
	
КонецПроцедуры

Функция СоответствиеРеквизитовФормы(ЭтаФорма, Знач ПутьКРодителю = "", Знач СоответствиеРеквизитов = Неопределено) Экспорт 
	
	Если СоответствиеРеквизитов = Неопределено Тогда
		СоответствиеРеквизитов = Новый Соответствие;
	КонецЕсли;
	Попытка
		РеквизитыФормы = ЭтаФорма.ПолучитьРеквизиты(ПутьКРодителю);
	Исключение
		Возврат СоответствиеРеквизитов;
	КонецПопытки;
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		ПолноеИмяРеквизита = РеквизитФормы.Имя;
		Если ЗначениеЗаполнено(РеквизитФормы.Путь) Тогда
			ПолноеИмяРеквизита = РеквизитФормы.Путь + "." + ПолноеИмяРеквизита; 
		КонецЕсли; 
		СоответствиеРеквизитов.Вставить(ПолноеИмяРеквизита, РеквизитФормы);
		СоответствиеРеквизитовФормы(ЭтаФорма, ПолноеИмяРеквизита, СоответствиеРеквизитов);
	КонецЦикла;
	Возврат СоответствиеРеквизитов;

КонецФункции

Процедура УдалитьУсловноеОформлениеФормыПоПрефиксу(ЭтаФорма, ПрефиксУдаляемыхЭлементов = "#") Экспорт
	
	МассивКУдалению = Новый Массив;
	Для Каждого ЭлементОформления Из ЭтаФорма.УсловноеОформление.Элементы Цикл
		Если Лев(ЭлементОформления.Представление, 1) = ПрефиксУдаляемыхЭлементов Тогда
			МассивКУдалению.Добавить(ЭлементОформления);
		КонецЕсли; 
	КонецЦикла;
	Для Каждого ЭлементОформления Из МассивКУдалению Цикл
		ЭтаФорма.УсловноеОформление.Элементы.Удалить(ЭлементОформления);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область УправлениеСвойствамиЗаявкиЗаданияНаПеревозку

// Вызывается при смене страницы из форм документов Заявка на перевозку или Задание на перевозку
//
Процедура ПриСменеСтраницыДополнительныеРеквизиты(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Если Не Элементы.Найти("ГруппаДополнительныеРеквизитыУниверсальная") = Неопределено Тогда
		Если Форма.Свойства_ИспользоватьСвойства Тогда
			УправлениеСвойствами.УдалитьСтарыеРеквизитыИЭлементы(Форма);
		КонецЕсли;  
		УдаляемаяГруппаФормы = Элементы["ГруппаДополнительныеРеквизитыУниверсальная"];
		Элементы.Удалить(УдаляемаяГруппаФормы);
	КонецЕсли;
	НоваяГруппаДополнительныеРеквизиты = Элементы.Добавить("ГруппаДополнительныеРеквизитыУниверсальная", Тип("ГруппаФормы"), Элементы.ГруппаДополнительныеРеквизиты);
	НоваяГруппаДополнительныеРеквизиты.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаДополнительныеРеквизиты.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	НоваяГруппаДополнительныеРеквизиты.ОтображатьЗаголовок = Ложь;
	НоваяГруппаДополнительныеРеквизиты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;	
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(Форма,,, Истина);
	
КонецПроцедуры

// Обновляет отображение дополнительных реквизитов на форме документа: либо по документу, либо по грузовому месту
//
Процедура ОбновитьДополнительныеРеквизитыПоГрузовомуМесту(Форма) Экспорт 
	
	Элементы = Форма.Элементы;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДополнительно Тогда
		     Возврат;
		КонецЕсли; 
		
		Если Не Элементы.Найти("ГруппаДополнительныеРеквизитыУниверсальная") = Неопределено Тогда
			УправлениеСвойствами.УдалитьСтарыеРеквизитыИЭлементы(Форма);
			УдаляемаяГруппаФормы = Элементы["ГруппаДополнительныеРеквизитыУниверсальная"];
			Элементы.Удалить(УдаляемаяГруппаФормы);
		КонецЕсли;
		
		Если Форма.Объект.ОбщийГруз Тогда
			Если ЗначениеЗаполнено(Форма.ГрузовоеМесто) Тогда
				Форма.ТекущееГрузовоеМесто = Форма.ГрузовоеМесто;
				НоваяГруппаДополнительныеРеквизиты = Элементы.Добавить("ГруппаДополнительныеРеквизитыУниверсальная", Тип("ГруппаФормы"), Элементы.ГруппаОписаниеГруза);
				НоваяГруппаДополнительныеРеквизиты.Вид = ВидГруппыФормы.Страница;
				НоваяГруппаДополнительныеРеквизиты.ОтображатьЗаголовок = Истина;
				НоваяГруппаДополнительныеРеквизиты.Заголовок = НСтр("ru = 'Дополнительные реквизиты'");
				
				УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(Форма, Форма.ТекущееГрузовоеМесто.ПолучитьОбъект(),, Истина);
			КонецЕсли; 
		ИначеЕсли ЗначениеЗаполнено(Форма.ТекущееГрузовоеМесто) Тогда
			НоваяГруппаДополнительныеРеквизиты = Элементы.Добавить("ГруппаДополнительныеРеквизитыУниверсальная", Тип("ГруппаФормы"), Элементы.ГруппаДополнительныеРеквизитыГрузовогоМеста);
			НоваяГруппаДополнительныеРеквизиты.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			НоваяГруппаДополнительныеРеквизиты.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			НоваяГруппаДополнительныеРеквизиты.ОтображатьЗаголовок = Истина;
			НоваяГруппаДополнительныеРеквизиты.Заголовок = НСтр(СтрШаблон("ru = 'Дополнительные реквизиты по грузовому месту %1'", Форма.ТекущееГрузовоеМесто.Код));
			
			УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(Форма, Форма.ТекущееГрузовоеМесто.ПолучитьОбъект(),, Истина);
		Иначе	
			НоваяГруппаДополнительныеРеквизиты = Элементы.Добавить("ГруппаДополнительныеРеквизитыУниверсальная", Тип("ГруппаФормы"), Элементы.ГруппаДополнительныеРеквизитыГрузовогоМеста);
			НоваяГруппаДополнительныеРеквизиты.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			НоваяГруппаДополнительныеРеквизиты.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
			НоваяГруппаДополнительныеРеквизиты.ОтображатьЗаголовок = Истина;
			НоваяГруппаДополнительныеРеквизиты.Заголовок = НСтр("ru = 'Дополнительные реквизиты'");
			
			НоваяНадпись = Элементы.Добавить("НадписьИнформативная", Тип("ДекорацияФормы"), НоваяГруппаДополнительныеРеквизиты);
			НоваяНадпись.Вид        = ВидДекорацииФормы.Надпись;
			НоваяНадпись.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
			НоваяНадпись.Заголовок  = НСтр("ru = 'Для возможности редактирования дополнительных реквизитов по грузовому месту необходимо записать документ.'");
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// Переносит изменения дополнительных реквизитов из формы в объект,
//  в случае редактирования дополнительных реквизитов по грузовому месту - с записью объекта
//
// Параметры:
//  Форма			 - Форма	 - форма.
//  ЗаписыватьОбъект - Булево	 - записывать.
//
Процедура ПеренестиЗначенияДополнительныхРеквизитовВОбъект(Форма, ЗаписыватьОбъект = Ложь) Экспорт
	
	Если ЗаписыватьОбъект Тогда
		ГрузовоеМестоОбъект = Форма.ТекущееГрузовоеМесто.ПолучитьОбъект();
		УправлениеСвойствами.ПеренестиЗначенияИзРеквизитовФормыВОбъект(Форма, ГрузовоеМестоОбъект);
		ГрузовоеМестоОбъект.ОбменДанными.Загрузка = Истина;
		ГрузовоеМестоОбъект.Записать();
	Иначе
		УправлениеСвойствами.ПеренестиЗначенияИзРеквизитовФормыВОбъект(Форма);
	КонецЕсли; 
	
КонецПроцедуры
	
#КонецОбласти 

#Область ДополнительныеОперацииВыполнения

// Возвращает признак выполняющегося задания.
//
// Параметры:
//  ИдентификаторЗадания - Строка - Идентификатор.
//
// Возвращаемое значение:
//   Булево  - Результат выполнения.
//
Функция ЗаданиеЕщеВыполняется(ИдентификаторЗадания) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	
	Возврат Задание <> Неопределено И Задание.Состояние = СостояниеФоновогоЗадания.Активно;
	
КонецФункции

// Выводим сообщения пользователю по фоновому заданию.
//
// Параметры:
//  ИдентификаторЗадания - УникальныйИдентификатор - Идентификатор.
//  ИдентификаторНазначения - УникальныйИдентификатор - Идентификатор.
//
Процедура ПолучитьИзДлительнойОперацииСообщенияПользователю(ИдентификаторЗадания, ИдентификаторНазначения = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		Возврат;
	КонецЕсли;
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Сообщения = ФоновоеЗадание.ПолучитьСообщенияПользователю(Истина);
	
	Если Сообщения = Неопределено Тогда
		// Вместо пустого массива возвращается Неопределено.
		Возврат;
	КонецЕсли;
	
	// Сообщения фонового задания могут повторяться из-за особенностей технической реализации.
	// заполнения списка Ошибки регламентной операции.
	// Также они могут содержать служебные сообщения.
	// См. ДополнитьИнформациюОбОшибкахСообщениямиПользователю().
	УникальныеТекстыСообщений = Новый Массив;
	УникальныеСообщения = Новый Массив;
	Для Каждого Сообщение Из Сообщения Цикл
		
		Если упСервисныеФункцииКлиентСервер.ЭтоСлужебноеСообщение(Сообщение) Тогда
			// Такие не показываем пользователю.
			Продолжить;
		КонецЕсли;
		
		Если УникальныеТекстыСообщений.Найти(Сообщение.Текст) = Неопределено Тогда
			УникальныеТекстыСообщений.Добавить(Сообщение.Текст);
			УникальныеСообщения.Добавить(Сообщение);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Сообщение Из УникальныеСообщения Цикл
		Если ИдентификаторНазначения <> Неопределено Тогда
			Сообщение.ИдентификаторНазначения = ИдентификаторНазначения;
		КонецЕсли;
		Сообщение.Сообщить();
	КонецЦикла;
	
КонецПроцедуры

Процедура упКонтрольРазрешенныхЗначенийКонстантПередЗаписью(Источник, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    Источник = Константы.АвтоматическиНастраиватьРазрешенияВПрофиляхБезопасности.СоздатьМенеджерЗначения();
	КонецЕсли;
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	Если МетодРеализован(Источник, "ПолучитьРазрешенноеЗначение") Тогда
		Источник.Значение = Источник.ПолучитьРазрешенноеЗначение();
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеАутентификации(Рейс)
	
	ДанныеАутентификации = "";
	
	сткЭкипаж = упРейс.ПолучитьЭкипажРейса(Рейс);
	текСотрудник = Неопределено;
	Если ЗначениеЗаполнено(сткЭкипаж.Водитель1) Тогда
		текСотрудник = сткЭкипаж.Водитель1;
	ИначеЕсли ЗначениеЗаполнено(сткЭкипаж.Водитель2) Тогда	
		текСотрудник = сткЭкипаж.Водитель2;
	ИначеЕсли ЗначениеЗаполнено(сткЭкипаж.Экспедитор) Тогда	
		текСотрудник = сткЭкипаж.Экспедитор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(текСотрудник) Тогда
		сткПараметрыАутентификации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текСотрудник, "Логин, Пароль");
		Если ЗначениеЗаполнено(сткПараметрыАутентификации.Логин) Тогда
			ДанныеАутентификации = СтрШаблон("&login=%1&password=%2", сткПараметрыАутентификации.Логин, сткПараметрыАутентификации.Пароль);  
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеАутентификации;
	
КонецФункции

Процедура ОбновитьИндикатор(Знач Количество, Сч, Процент) Экспорт 
	
	Сч = Сч + 1;
	Если Цел(100 * Сч / Количество) <> Процент Тогда 
		Процент = Цел(100 * Сч / Количество);
		Если ЛиЭтоФоновоеЗадание() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("###" + Формат(Процент, "ЧРД=.; ЧГ="));
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

Функция ЛиЭтоФоновоеЗадание()
	
	ТекущийСеанс = упСервисныеФункцииПовторноеИспользование.ТекущийСеанс();
	Если ТекущийСеанс = Неопределено Тогда
		Результат = Ложь;
	Иначе
		Результат = СтрСравнить(ТекущийСеанс.ИмяПриложения, "BackgroundJob") = 0;
	КонецЕсли; 
	Возврат Результат;

КонецФункции 

&НаСервере
// Устанавливает заголовки полей для поиска в форме
//
Процедура УстановитьЗаголовкиПолейДляПоиска(ЭтаФорма) Экспорт
	
	Элементы = ЭтаФорма.Элементы;
	
	// Преобразуем автозаголовки в статические заголовки для возможности поиска https://partners.v8.1c.ru/forum/topic/1074579
	СоответствиеРеквизитов = упСервисныеФункции.СоответствиеРеквизитовФормы(ЭтаФорма);
	Для Каждого ЭлементФормы Из Элементы Цикл
		СтруктураСвойств = Новый Структура("ПутьКДанным, Заголовок");
		ЗаполнитьЗначенияСвойств(СтруктураСвойств, ЭлементФормы); 
		Если Истина
			И ЗначениеЗаполнено(СтруктураСвойств.ПутьКДанным) 
			И Не ЗначениеЗаполнено(СтруктураСвойств.Заголовок) 
		Тогда
			ЭлементФормы.Заголовок = СоответствиеРеквизитов[ЭлементФормы.ПутьКДанным].Заголовок;
		КонецЕсли; 
	КонецЦикла;
	Для Каждого КнопкаФормы Из Элементы Цикл
		Если Истина
			И Типзнч(КнопкаФормы) = Тип("КнопкаФормы")
			И ЗначениеЗаполнено(КнопкаФормы.ИмяКоманды) 
			И Не ЗначениеЗаполнено(КнопкаФормы.Заголовок) 
		Тогда
			КомандаФормы = ЭтаФорма.Команды.Найти(КнопкаФормы.Имя);
			Если КомандаФормы <> Неопределено Тогда
				КнопкаФормы.Заголовок = КомандаФормы.Заголовок;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ПредставлениеВОтчете

// Выполняет замену символов в строке Символ переноса(Символы.ПС).
//
// Параметры:
//  ЗаменяемыеСимволы - Строка - строка символов, каждый из которых требует замены;
//  Строка            - Строка - исходная строка, в которой требуется замена символов;
// 
//  Возвращаемое значение:
//   Строка - строка после замены символов.
//
//  Примечание: функция предназначена для простых случаев, например, для замены латиницы на похожие кириллические
//  символы.
//
Функция ЗаменитьОдниСимволыПереносомПоСтрокам(ЗаменяемыеСимволы, Строка) Экспорт
	
	Результат = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(ЗаменяемыеСимволы, Строка, Символы.ПС);
	
	Возврат Результат;
	
КонецФункции // ЗаменитьОдниСимволыПереносомПоСтрокам()

// Выполняет замену символов в строке.
//
// Параметры:
//  СхемаМультимодальнойПеревозки - СправочникСсылка.упСхемыМультимодальныхПеревозок - Ссылка на цепь поставок;
// 
//  Возвращаемое значение:
//   Строка - строка перечислены перевалочные пункты, каждый пункт на новой строке.
//
//  Примечание: функция предназначена для простых случаев, например, для замены латиницы на похожие кириллические
//  символы.
//
Функция ПолучитьПеревалочныеПункты(СхемаМультимодальнойПеревозки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПРЕДСТАВЛЕНИЕ(ПромежуточныеТочкиМаршрута.Адрес) КАК Адрес,
	|	ПромежуточныеТочкиМаршрута.Представление КАК Представление,
	|	ПромежуточныеТочкиМаршрута.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
	|ГДЕ
	|	ПромежуточныеТочкиМаршрута.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СхемаМультимодальнойПеревозки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	МассивЗначений = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если НЕ ПустаяСтрока(Выборка.Адрес) Тогда
			МассивЗначений.Добавить(Выборка.Адрес);
		КонецЕсли;
	КонецЦикла;
	Результат = "";
	Если МассивЗначений.Количество() Тогда
		Результат = СтрСоединить(МассивЗначений, Символы.ПС);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции // ЗаменитьОдниСимволыПереносомПоСтрокам()

&НаСервере
// Возвращает количество записей в регистре
//
Функция ПолучитьРазмерРегистра(ОбъектМетаданных) Экспорт
	
	ЛитералыТипа = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".");
	ТипТаблицы = ЛитералыТипа[0];
	ИмяТаблицы = ЛитералыТипа[1];
	
	КоличествоЗаписей = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоЗаписей
	|ИЗ
	|	" + ТипТаблицы + "." + ИмяТаблицы + " КАК ТаблицаРегистра";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КоличествоЗаписей = Выборка.КоличествоЗаписей;
	КонецЕсли; 
	
	Возврат КоличествоЗаписей;
	
КонецФункции

#Область Планировщик

Функция УбратьНадписьЕщеИзЯчеекПланировщика(ЗначениеИзмерения, МаксимальноеКоличествоИнтерваловЗаПериод) Экспорт
	
	Результат = Строка(ЗначениеИзмерения);
	
	Если МаксимальноеКоличествоИнтерваловЗаПериод > 1 Тогда
		Для й = 0 По МаксимальноеКоличествоИнтерваловЗаПериод Цикл
			Результат = Результат + Символы.ПС;
		КонецЦикла;
		
	КонецЕсли;
					
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФункцииСвязанныеСДвижениямиДокумента

// Удалить записи переданного движения.
//
// Параметры:
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//
Процедура УдалитьДвижения(Движения) Экспорт
	
	Для каждого Движение Из Движения Цикл
		Движение.Записывать = Истина;
		Движение.Очистить();		
	КонецЦикла;
	
	Движения.Записать();
			
КонецПроцедуры

#КонецОбласти

// Функция - Возвращает таблицу значений, структура которой соответствует объекту метаданных.
//
// Параметры:
//  ИмяКоллекцииОбъектовМетаданных	 - Строка	 - Имя коллекции объектов метаданных, например "РегистрыНакопления".
//  ИмяОбъектаМетаданных		 - Строка	 - Имя объекта метаданных, например "упЗаданияКПланированию"
// 
// Возвращаемое значение:
//  ТаблицаЗначений - имеет структуру запрошенных метаданных.
//
Функция ТаблицаСтруктурыМетаданных(КоллекцияОбъектовМетаданных, МетаданныеОбъекта) Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Колонки = Результат.Колонки;
	
	Если Ложь
		Или КоллекцияОбъектовМетаданных = Метаданные.РегистрыНакопления
		Или КоллекцияОбъектовМетаданных = Метаданные.РегистрыСведений
	Тогда
			
		Для каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
			Колонки.Добавить(Измерение.Имя, Измерение.Тип);
		КонецЦикла;
		
		Для каждого Ресурс Из МетаданныеОбъекта.Ресурсы Цикл
			Колонки.Добавить(Ресурс.Имя, Ресурс.Тип);
		КонецЦикла;

		Для каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
			Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
		КонецЦикла;
				
	Иначе
		
		Результат = Неопределено;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
