
#Область ПрограммныйИнтерфейс

#Если Не ВебКлиент Тогда

// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//  текстСкрипта				 - строка	 - тектс на javascript.
//  Форма						 - УправляемаяФорма	 - Форма объекта.
//  УказателиНаСлужебныеЭлементы	 - Структура		 - служебные элементы.
//  ИмяЭлементаПолеHTML			 - Строка			 - имя поля.
//
Процедура ВыполнитьПроцедуруHTML(текстСкрипта, Форма, УказателиНаСлужебныеЭлементы = Неопределено, ИмяЭлементаПолеHTML = Неопределено) Экспорт	
	
	Форма.ВыполняетОбращениеКДокументу = Истина;
	
	Попытка		
		СлужебныеЭлементыHtml(УказателиНаСлужебныеЭлементы, Форма, ИмяЭлементаПолеHTML);
		УказателиНаСлужебныеЭлементы.ТекстСкрипта.value = текстСкрипта;
		УказателиНаСлужебныеЭлементы.ВычислительРезультата.click();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),, "HTMLДокумент");
	КонецПопытки;
	
	Форма.ВыполняетОбращениеКДокументу = Ложь;
	
КонецПроцедуры

// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//  текстСкрипта				 - строка	 - тектс на javascript.
//  Форма						 - УправляемаяФорма	 - Форма объекта.
//  УказателиНаСлужебныеЭлементы	 - Структура		 - служебные элементы.
//  ИмяЭлементаПолеHTML			 - Строка			 - имя поля.
// 
// Возвращаемое значение:
//  Строка - значение, которое возвращает javascript.
//
Функция ВыполнитьФункциюHTML(текстСкрипта, Форма, УказателиНаСлужебныеЭлементы = Неопределено, ИмяЭлементаПолеHTML = Неопределено) Экспорт
	
	ТекущийРезультат = "";
	Форма.ВыполняетОбращениеКДокументу = Истина;
	
	Попытка   
		СлужебныеЭлементыHtml(УказателиНаСлужебныеЭлементы, Форма, ИмяЭлементаПолеHTML);
		УказателиНаСлужебныеЭлементы.ТекстСкрипта.value = текстСкрипта;
		// При вызове метода "click()" поле HTML докуменат объекта генерирует событие "ПриНажатии()".
		// Поэтому идет перенапровление вызова этот момент надо пресекать.
		УказателиНаСлужебныеЭлементы.ВычислительРезультата.click();
		РезультатJSON = УказателиНаСлужебныеЭлементы.ПолучательРезультата.value;
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(РезультатJSON);
		ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		ТекущийРезультат =  ОтветJSON;
		
	Исключение		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),, "HTMLДокумент");
		Форма.ВыполняетОбращениеКДокументу = Ложь;
		Возврат "";
	КонецПопытки;
	
	Форма.ВыполняетОбращениеКДокументу = Ложь;
	
	Возврат ТекущийРезультат;
	
КонецФункции

Процедура СлужебныеЭлементыHtml(УказателиНаСлужебныеЭлементы, Знач Форма, Знач ИмяЭлементаПолеHTML = Неопределено)
	
	Если Не ЗначениеЗаполнено(ИмяЭлементаПолеHTML) Тогда
		ИмяЭлементаПолеHTML = "HTMLДокумент";
	КонецЕсли; 
	
	Если УказателиНаСлужебныеЭлементы = Неопределено Тогда
		УказателиНаСлужебныеЭлементы = Новый Структура;
		УказателиНаСлужебныеЭлементы.Вставить("ВычислительРезультата", Форма.Элементы[ИмяЭлементаПолеHTML].Документ.getElementById("ButtonExe"));
		Элемент = Форма.Элементы[ИмяЭлементаПолеHTML].Документ.getElementById("GetExtCommand");
		Elements = Элемент.getElementsByTagName("*");
		Для каждого tag Из Elements Цикл
			УказателиНаСлужебныеЭлементы.Вставить("ТекстСкрипта", tag);
			Прервать;
		КонецЦикла;
		Элемент = Форма.Элементы[ИмяЭлементаПолеHTML].Документ.getElementById("GetData");
		Elements = Элемент.getElementsByTagName("*");
		Для каждого tag Из Elements Цикл
			УказателиНаСлужебныеЭлементы.Вставить("ПолучательРезультата", tag);
			Прервать;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли

#КонецОбласти