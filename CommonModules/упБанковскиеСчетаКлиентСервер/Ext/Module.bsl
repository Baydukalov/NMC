#Область ПрограммныйИнтерфейс

// Процедура - Осуществляется подбор банка по переданному тексту.
//
// Параметры:
//  Текст				 - Строка	 		- текст для подбора.
//  ДанныеВыбора		 - СписокЗначений	- список заполняется данными соответствующими тексту.
//  СтандартнаяОбработка - Булево	 		- передается переменная по ссылке, если нестандартный подбор, тогда устанавливается в Истина.
//
Процедура ПодборБанка(Текст, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если Текст = "" Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Текст) 
			И СтрДлина(Текст) > 2 Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = упБанковскиеСчетаВызовСервера.ПодборБанкаПоНаименованию(Текст);					
	КонецЕсли;
		
КонецПроцедуры

// Функция - Формируется наименование банковского счета.
//
// Параметры:
//  НомерСчета			 - Строка - номер счета.
//  НаименованиеБанка	 - Строка - наименование банка.
//  НаименованиеВалюта	 - Строка - наименование валюты.
//  Валютный			 - Булево - Истина, если это валютный счет.
// 
// Возвращаемое значение:
//  Строка - наименование счета.
//
Функция НаименованиеБанковскогоСчета(НомерСчета, НаименованиеБанка, НаименованиеВалюта, Валютный) Экспорт
	
	Результат = Новый Массив;
	
	Если ЗначениеЗаполнено(НомерСчета) Тогда
		Результат.Добавить(Строка(НомерСчета));	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НаименованиеБанка) Тогда
		Результат.Добавить(Строка(НаименованиеБанка));
	КонецЕсли;	
	
	Если Валютный Тогда
		Результат.Добавить(Строка(НаименованиеВалюта));	
	КонецЕсли;
	
	Возврат СтрСоединить(Результат, ", ");
		
КонецФункции

#КонецОбласти
