
#Область ПрограммныйИнтерфейс

#Область УстановкаДоступностиКнопок

// Процедура управляет доступностью кнопок статуса в заявке на перевозку груза.
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//  ТребуетсяСогласование	- Булево - Флаг фиксировать изменения.
//
Процедура УстановитьКнопкиСтатусаЗаявкиНаПеревозкуГруза(Ссылка, Статус, Элементы, ТребуетсяСогласование, ИспользуютсяМультимодальныеПеревозки = Ложь) Экспорт

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
	ДополнительныеПараметры.Вставить("ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза", РольДоступна("упСогласованиеЗаявкиНаПеревозкуГруза"));
	ДополнительныеПараметры.Вставить("ДоступнаРольОтменаЗаявкиНаПеревозкуГруза", РольДоступна("упОтменаЗаявкиНаПеревозкуГруза"));
	ДополнительныеПараметры.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению", Ложь);
	ДополнительныеПараметры.Вставить("ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза", ТребуетсяСогласование);
	ДополнительныеПараметры.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", ИспользуютсяМультимодальныеПеревозки);
	ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
	
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза"),Статус,ДополнительныеПараметры);
	
	ТекущаяСтраница = Элементы.ГруппаКнопкаНовая;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена = Ложь;
	
	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая Тогда
			ТекущаяСтраница = Элементы.ГруппаКнопкаНовая;
			ДоступнаГруппаПроведение	= Истина;
		//ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок Тогда
		//	ТекущаяСтраница = Элементы.ГруппаКнопкаПодобранаЦепьПоставок;
		//	ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована Тогда	
			ТекущаяСтраница = Элементы.ГруппаКнопкаСогласована;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению Тогда				
			ТекущаяСтраница = Элементы.ГруппаКнопкаКВыполнению;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена Тогда				
			ДоступнаГруппаОтмена = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность = ДоступнаГруппаОтмена;
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса в задании на перевозку груза.
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//
Процедура УстановитьКнопкиСтатусаЗаданияНаПеревозкуГруза(Ссылка, Статус, Элементы) Экспорт

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЭтоПолноправныйПользователь",						Пользователи.ЭтоПолноправныйПользователь());
	ДополнительныеПараметры.Вставить("ДоступнаРольОтменаЗаданийНаПеревозкуГруза",		РольДоступна("упОтменаЗаданияНаПеревозкуГруза"));
	
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза"),Статус,ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовая;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;

	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Тогда
			ТекущаяСтраница 			= Элементы.ГруппаКнопокКПланированию;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаКВыполнению;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда				
			ДоступнаГруппаОтмена		= Истина;
		КонецЕсли;
		
	КонецЦикла;

	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность     = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность         = ДоступнаГруппаОтмена;
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса в заявке на транспортное средство.
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//  Тендер	- Булево - Это тендер.
//
Процедура УстановитьКнопкиСтатусаЗаявкиНаТС(Ссылка, Статус, Элементы, Тендер) Экспорт

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Тендер", Тендер);
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упЗаявкаНаТС"),Статус,ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаОтправить;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;

	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаТС.Отправлена Тогда
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаОтправить;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаПодтвердить;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Тогда	
			ДоступнаГруппаОтмена		= Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если (Статус = Перечисления.упСтатусыЗаявкиНаТС.Отправлена ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение) И Тендер Тогда 
		Если ТипЗнч(Пользователи.АвторизованныйПользователь()) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда 
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаПредложение;
			ДоступнаГруппаПроведение	= Истина;
		КонецЕсли;
	КонецЕсли;	

	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность     = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтклонить.Доступность      = ДоступнаГруппаОтмена;
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса в рейсе
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//  Параметры	- Структура - Параметры.
//
Процедура УстановитьКнопкиСтатусаРейса(Ссылка, Статус, Элементы, Параметры = Неопределено) Экспорт

	ПеревозкаТранспортнойКомпанией = Ложь;
	СпособПрохожденияТочекМаршрута = Неопределено;
	ЕстьОтмененныеРаботы = Неопределено;
	ЕстьОтложенныеРаботы = Неопределено;
	ЭтоОперацияСКонтейнером = Ложь;
	Если Параметры <> Неопределено Тогда
		ПеревозкаТранспортнойКомпанией = Параметры.ПеревозкаТранспортнойКомпанией;
		СпособПрохожденияТочекМаршрута = Параметры.СпособПрохожденияТочекМаршрута;
		ЕстьОтмененныеРаботы = Параметры.ЕстьОтмененныеРаботы;
		ЕстьОтложенныеРаботы = Параметры.ЕстьОтложенныеРаботы;
		ЭтоОперацияСКонтейнером = Параметры.ЭтоОперацияСКонтейнером;
		ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = Параметры.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера;
	КонецЕсли;
			
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПеревозкаТранспортнойКомпанией", ПеревозкаТранспортнойКомпанией);
	ДополнительныеПараметры.Вставить("ТребуетсяОформлениеПутевогоЛиста", упРейс.ТребуетсяОформлениеПутевогоЛиста(Ссылка));
	ДополнительныеПараметры.Вставить("СпособПрохожденияТочекМаршрута", СпособПрохожденияТочекМаршрута);
	ДополнительныеПараметры.Вставить("ЕстьОтмененныеРаботы", ЕстьОтмененныеРаботы);
	ДополнительныеПараметры.Вставить("ЕстьОтложенныеРаботы", ЕстьОтложенныеРаботы);
	ДополнительныеПараметры.Вставить("ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера", ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера);
	ДополнительныеПараметры.Вставить("ЭтоОперацияСКонтейнером", ЭтоОперацияСКонтейнером);
	ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
	
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упРейс"),Статус,ДополнительныеПараметры);
	
	ТекущаяСтраница = Элементы.ГруппаКнопкаНовая;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена = Ложь;
	
	Если ЭтоОперацияСКонтейнером Тогда
		Элементы.СтатусКПланированиюТС.Заголовок = ЗаголовокКнопкиСтатусКонтейнернойПеревозки(Перечисления.упСтатусыРейса.КПланированиюТС);
	КонецЕсли;

	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыРейса.КПланированиюТС Тогда
			
			ТекущаяСтраница = Элементы.ГруппаКнопкаКПланированиюТС;
			ДоступнаГруппаПроведение	= Истина;
						
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыРейса.КВыполнению Тогда	
			
			ТекущаяСтраница = Элементы.ГруппаКнопкаКВыполнению;
			ДоступнаГруппаПроведение	= Истина;
			
			Если ЭтоОперацияСКонтейнером Тогда
				Элементы.СтатусКВыполнению.Заголовок = ЗаголовокКнопкиСтатусКонтейнернойПеревозки(текДоступныйСтатус);
			КонецЕсли;
			
		ИначеЕсли Истина
			     И текДоступныйСтатус = Перечисления.упСтатусыРейса.Выполнен 
				И СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда	
			ТекущаяСтраница = Элементы.ГруппаКнопкаВыполнен;
			ДоступнаГруппаПроведение	= Истина;
			
			Если ЭтоОперацияСКонтейнером Тогда
				Элементы.СтатусВыполнен.Заголовок = ЗаголовокКнопкиСтатусКонтейнернойПеревозки(текДоступныйСтатус);
			КонецЕсли;
			
		ИначеЕсли Истина
			     И текДоступныйСтатус = Перечисления.упСтатусыРейса.ВыполненЧастично 
				И СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда	
			ТекущаяСтраница = Элементы.ГруппаКнопкаВыполненЧастично;
			ДоступнаГруппаПроведение	= Истина;
			
			Если ЭтоОперацияСКонтейнером Тогда
				Элементы.СтатусВыполненЧастично.Заголовок = ЗаголовокКнопкиСтатусКонтейнернойПеревозки(текДоступныйСтатус);
			КонецЕсли;
	
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыРейса.Завершен Тогда	
			Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
				ТекущаяСтраница = Элементы.ГруппаКнопкаЗавершен;
				ДоступнаГруппаПроведение	= Истина;
			КонецЕсли;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыРейса.Отменен Тогда				
			ДоступнаГруппаОтмена = Истина;
		
		КонецЕсли;
		
	КонецЦикла;

	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность = ДоступнаГруппаОтмена;
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса в путевом листе
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//
Процедура УстановитьКнопкиСтатусаПутевогоЛиста(Ссылка, Статус, Элементы) Экспорт
	
	ДополнительныеПараметры =Новый Структура();
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упПутевойЛист"),Статус,ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаВыдан;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;

	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаВыдан;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаВозвращен;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаЗакрыт;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыПутевогоЛиста.Отменен Тогда	
			ДоступнаГруппаОтмена		= Истина;
		КонецЕсли;
		
	КонецЦикла;

	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность     = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкиОтменить.Доступность      = ДоступнаГруппаОтмена;
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса запроса условий перевозки
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//
Процедура УстановитьКнопкиСтатусаЗапросаУсловийПеревозки(Ссылка, Статус, Элементы) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упЗапросУсловийПеревозки"), Статус, ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовая;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;

	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Обработан Тогда
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаОбработана;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен Тогда	
			ДоступнаГруппаОтмена		= Истина;
		КонецЕсли;
		
	КонецЦикла;

	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность     = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность         = ДоступнаГруппаОтмена;
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса при ремонте
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//
Процедура УстановитьКнопкиСтатусаРемонтаИТОТС(Ссылка, Статус, Элементы) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упРемонтИТОТС"), Статус, ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовый;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;
	
	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован Тогда
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаЗапланирован;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Начат Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаНачат;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаЗавершен;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Отменен Тогда				
			ДоступнаГруппаОтмена		= Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность     = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность         = ДоступнаГруппаОтмена;	
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса при заявке на ремонт
//
// Параметры:
//  Ссылка			 - ДокументСсылка	 - Ссылка на элемент.
//  Статус			 - ПеречислениеСсылка	 - Ссылка на элемент.
//  Элементы			 - ЭлементФормы		 - Элементы расположенные на форме.
//  ТребуетсяСогласование - Булево				 - признак согласования.
//
Процедура УстановитьКнопкиСтатусаЗаявкиРемонтаИТОТС(Ссылка, Статус, Элементы, ТребуетсяСогласование) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
	ДополнительныеПараметры.Вставить("ДоступнаРольСогласованиеЗаявкиНаРемонтИТО", РольДоступна("упСогласованиеЗаявкиНаРемонтИТО"));
	ДополнительныеПараметры.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению", Ложь);
	ДополнительныеПараметры.Вставить("ТребуетсяСогласованиеЗаявкиНаРемонт", ТребуетсяСогласование);
	ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
	
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС"), Статус, ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовая;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;
	
	Для каждого текДоступныйСтатус Из мсвСтатусы Цикл
		
		Если текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая Тогда
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовая;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована Тогда	
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаСогласована;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению Тогда				
			ТекущаяСтраница 			= Элементы.ГруппаКнопкаКВыполнению;
			ДоступнаГруппаПроведение	= Истина;
		ИначеЕсли текДоступныйСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена Тогда				
			ДоступнаГруппаОтмена		= Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность     = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность         = ДоступнаГруппаОтмена;
	
КонецПроцедуры // УстановитьКнопкиСтатусаЗаявкиРемонтаИТОТС()

// Процедура управляет доступностью кнопок статуса входящей претензии
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//
Процедура УстановитьКнопкиСтатусаВходящейПретензии(Ссылка, Статус, Элементы) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упВходящаяПретензия"), Статус, ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовый;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;
	
	КоличествоВариантов = мсвСтатусы.Количество();
	
	Если КоличествоВариантов = 2 Тогда
		
			ТекущаяСтраница 		      	= Элементы.ГруппаКнопкаВРаботе;
			ДоступнаГруппаПроведение = Истина;
			ДоступнаГруппаОтмена        = Истина;
			
	ИначеЕсли КоличествоВариантов = 4 Тогда	
		
			ТекущаяСтраница 		      	= Элементы.ГруппаКнопкаЗавершен;
			ДоступнаГруппаПроведение = Истина;
			ДоступнаГруппаОтмена        = Истина;
			
	КонецЕсли; 
	
	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность    	  = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность               = ДоступнаГруппаОтмена;	
	
КонецПроцедуры

// Процедура управляет доступностью кнопок статуса входящей претензии
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//	Элементы - ЭлементФормы - Элементы расположенные на форме.
//
Процедура УстановитьКнопкиСтатусаИсходящейПретензии(Ссылка, Статус, Элементы) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	мсвСтатусы = ПолучитьВозможныеНовыеСтатусы(Тип("ДокументСсылка.упИсходящаяПретензия"), Статус, ДополнительныеПараметры);
	
	ТекущаяСтраница 			= Элементы.ГруппаКнопкаНовый;
	ДоступнаГруппаПроведение	= Ложь;
	ДоступнаГруппаОтмена		= Ложь;
	
	КоличествоВариантов = мсвСтатусы.Количество();
	
	Если КоличествоВариантов = 2 Тогда
		
			ТекущаяСтраница 		      	= Элементы.ГруппаКнопкаВРаботе;
			ДоступнаГруппаПроведение = Истина;
			ДоступнаГруппаОтмена        = Истина;
			
	ИначеЕсли КоличествоВариантов = 4 Тогда	
		
			ТекущаяСтраница 		      	= Элементы.ГруппаКнопкаЗавершен;
			ДоступнаГруппаПроведение = Истина;
			ДоступнаГруппаОтмена        = Истина;
			
	КонецЕсли; 
	
	Элементы.ГруппаКнопкиПроведения.ТекущаяСтраница = ТекущаяСтраница;
	Элементы.ГруппаКнопкиПроведения.Доступность    	  = ДоступнаГруппаПроведение;
	Элементы.ГруппаКнопкаОтмены.Доступность               = ДоступнаГруппаОтмена;	
	
КонецПроцедуры

#КонецОбласти 

#Область РасчетСтатуса

// Возвращает статус заявки на ремонт.
//
// Параметры:
//  СписокЗаявокНаРемонт	 - Массив						 - Массив ссылок на документы.
//  СтатусРемонта		 - ПеречислениеСсылка.упСтатусыРемонтаИТОТС	 - статус ремонта.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция РассчитатьСтатусЗаявкиНаРемонт(СписокЗаявокНаРемонт, СтатусРемонта = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументРемонт.Ссылка КАК Ремонт,
	|	ДокументРемонт.ЗаявкаНаРемонтИТО КАК Заявка
	|ПОМЕСТИТЬ вт_ДокументРемонт
	|ИЗ
	|	Документ.упРемонтИТОТС КАК ДокументРемонт
	|ГДЕ
	|	ДокументРемонт.ЗаявкаНаРемонтИТО В(&Заявки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРемонтИТОТС.Ремонт КАК Ремонт,
	|	упРемонтИТОТС.Заявка КАК Заявка,
	|	ВЫБОР
	|		КОГДА &Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.ПустаяСсылка)
	|			ТОГДА упСтатусыРемонтовИТОТС.Статус
	|		ИНАЧЕ &Статус
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ вт_Ремонты
	|ИЗ
	|	вт_ДокументРемонт КАК упРемонтИТОТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРемонтовИТОТС.СрезПоследних(
	|				,
	|				Документ В
	|					(ВЫБРАТЬ
	|						вт_ДокументРемонт.Ремонт
	|					ИЗ
	|						вт_ДокументРемонт)) КАК упСтатусыРемонтовИТОТС
	|		ПО упРемонтИТОТС.Ремонт = упСтатусыРемонтовИТОТС.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА вт_Ремонты.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Новый)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт)
	|		КОГДА вт_Ремонты.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Запланирован)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт)
	|		КОГДА вт_Ремонты.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Начат)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте)
	|		КОГДА вт_Ремонты.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Завершен)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена)
	|		КОГДА вт_Ремонты.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Отменен)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению)
	|		ИНАЧЕ ЕСТЬNULL(упСтатусыЗаявокНаРемонтИТОТС.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Новая))
	|	КОНЕЦ КАК Статус,
	|	ЕСТЬNULL(упСтатусыЗаявокНаРемонтИТОТС.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Новая)) КАК ТекущийСтатус,
	|	упЗаявкаНаРемонтИТОТС.Ссылка КАК Заявка
	|ИЗ
	|	Документ.упЗаявкаНаРемонтИТОТС КАК упЗаявкаНаРемонтИТОТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаРемонтИТОТС.СрезПоследних КАК упСтатусыЗаявокНаРемонтИТОТС
	|		ПО упЗаявкаНаРемонтИТОТС.Ссылка = упСтатусыЗаявокНаРемонтИТОТС.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Ремонты КАК вт_Ремонты
	|		ПО упЗаявкаНаРемонтИТОТС.Ссылка = вт_Ремонты.Заявка
	|ГДЕ
	|	упЗаявкаНаРемонтИТОТС.Ссылка В(&Заявки)";
	Запрос.УстановитьПараметр("Заявки", СписокЗаявокНаРемонт);
	Запрос.УстановитьПараметр("Статус", ?(СтатусРемонта = Неопределено, Перечисления.упСтатусыРемонтаИТОТС.ПустаяСсылка(), СтатусРемонта));
	
	Возврат Запрос.Выполнить().Выгрузить();
			
КонецФункции // РассчитатьСтатусЗаявкиНаРемонт()

// Возвращает статус заявки на перевозку.
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза - Массив - заявки (ДокументСсылка.упЗаявкаНаПеревозкуГруза) для расчета статуса.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица заявок с новыми и текущими статусам.
//	   * Статус        - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - новый, рассчитанный статус заявки.
//	   * ТекущийСтатус - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - текущий статус заявки.
//	   * Заявка        - ДокументСсылка.упЗаявкаНаПеревозкуГруза            - заявка.
//
Функция РассчитатьСтатусЗаявкиНаПеревозкуПорядокИсполненияРейсом(мсвЗаявкиНаПеревозкуГруза) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(упЗаявкиКВыполнениюОстатки.КоличествоОстаток) КАК КоличествоКВыполнению,
	|	упЗаявкиКВыполнениюОстатки.Заявка КАК Заявка
	|ПОМЕСТИТЬ втЗаявкиКПланированию
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(, Заявка В (&Заявки)) КАК упЗаявкиКВыполнениюОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	упЗаявкиКВыполнениюОстатки.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК Заявка
	|ПОМЕСТИТЬ втЗаданияДокумент
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза В(&Заявки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Задание,
	|	упЗаданиеНаПеревозкуГруза.Заявка,
	|	упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус КАК Статус
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|			,
	|			Документ В
	|				(ВЫБРАТЬ
	|					втЗаданияДокумент.Задание
	|				ИЗ
	|					втЗаданияДокумент)) КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияДокумент КАК упЗаданиеНаПеревозкуГруза
	|		ПО упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ = упЗаданиеНаПеревозкуГруза.Задание
	|ГДЕ
	|	НЕ упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток) КАК КоличествоКПланированиюОстаток,
	|	СУММА(упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток) КАК КоличествоКВыполнениюОстаток,
	|	СУММА(упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток) КАК КоличествоНеПодтвержденоОстаток,
	|	СУММА(упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток) КАК КоличествоНеПереданноеКВыполнениюОстаток,
	|	упЗаданияКПланированиюОстатки.Задание.ЗаявкаНаПеревозкуГруза КАК Заявка
	|ПОМЕСТИТЬ втЗаданияКПланированию
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|			,
	|			Задание В
	|				(ВЫБРАТЬ
	|					втЗадания.Задание
	|				ИЗ
	|					втЗадания КАК втЗадания)) КАК упЗаданияКПланированиюОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	упЗаданияКПланированиюОстатки.Задание.ЗаявкаНаПеревозкуГруза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Отменена
	|				И ВЫБОР
	|					КОГДА упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ НЕ упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)
	|				КОНЕЦ
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Отменена,
	|	ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Выполнена
	|				ИЛИ ВЫБОР
	|					КОГДА упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)
	|				КОНЕЦ
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Выполнена,
	|	ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Выполнена
	|					И (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|						ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|					И (упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|						ИЛИ упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий))
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Отменена
	|					И (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|						ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|					И (НЕ упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|						И упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий))
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыполненаРазгрузка,
	|	ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Выполнена
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Отменена
	|					И упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий))
	|				И (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|				И упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыполненаПогрузка,
	|	ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|				И (упРаботыПоРейсуСрезПоследних.Отменена
	|					ИЛИ НЕ упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка))
	|				И НЕ упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтмененаРазгрузка,
	|	ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|				И упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Разгрузка,
	|	упРаботыПоРейсуСрезПоследних.Задание.ЗаявкаНаПеревозкуГруза КАК Заявка,
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРаботыПоРейсам
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Задание В
	|				(ВЫБРАТЬ
	|					втЗадания.Задание
	|				ИЗ
	|					втЗадания КАК втЗадания)) КАК упРаботыПоРейсуСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втРаботыПоРейсам.Отменена) КАК Отменена,
	|	СУММА(втРаботыПоРейсам.Выполнена) КАК Выполнена,
	|	СУММА(втРаботыПоРейсам.ОтмененаРазгрузка) КАК ОтмененаРазгрузка,
	|	СУММА(втРаботыПоРейсам.Разгрузка) КАК Разгрузка,
	|	СУММА(втРаботыПоРейсам.ВыполненаРазгрузка) КАК ВыполненаРазгрузка,
	|	СУММА(втРаботыПоРейсам.ВыполненаПогрузка) КАК ВыполненаПогрузка,
	|	КОЛИЧЕСТВО(*) КАК КоличествоРабот,
	|	втРаботыПоРейсам.Заявка
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втРаботыПоРейсам КАК втРаботыПоРейсам
	|
	|СГРУППИРОВАТЬ ПО
	|	втРаботыПоРейсам.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСостояниеПоЗаданиямОстатки.Задание.ЗаявкаНаПеревозкуГруза КАК Заявка,
	|	СУММА(ВЫБОР
	|			КОГДА упСостояниеПоЗаданиямОстатки.КоличествоОстаток > 0
	|				ТОГДА упСостояниеПоЗаданиямОстатки.КоличествоОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоОстаток,
	|	СУММА(упСостояниеПоЗаданиямОстатки.КоличествоОстаток) КАК ОбщийОстаток
	//|	СУММА(ВЫБОР
	//|			КОГДА упСостояниеПоЗаданиямОстатки.Местоположение ССЫЛКА Справочник.упТранспортныеСредства
	//|				ТОГДА упСостояниеПоЗаданиямОстатки.КоличествоОстаток
	//|			ИНАЧЕ 0
	//|		КОНЕЦ) КАК КоличествоВТС
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.упСостояниеПоЗаданиям.Остатки(
	|			,
	|			Задание В
	|				(ВЫБРАТЬ
	|					втЗадания.Задание
	|				ИЗ
	|					втЗадания КАК втЗадания)) КАК упСостояниеПоЗаданиямОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	упСостояниеПоЗаданиямОстатки.Задание.ЗаявкаНаПеревозкуГруза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(упПланПоЗаданиям.Количество) КАК КоличествоСкорректировано,
	|	упПланПоЗаданиям.Задание.ЗаявкаНаПеревозкуГруза КАК Заявка
	|ПОМЕСТИТЬ втКорректировки
	|ИЗ
	|	втЗадания КАК втЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПланПоЗаданиям КАК упПланПоЗаданиям
	|		ПО втЗадания.Задание = упПланПоЗаданиям.Задание
	|ГДЕ
	|	упПланПоЗаданиям.Количество < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	упПланПоЗаданиям.Задание.ЗаявкаНаПеревозкуГруза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботыПоРейсам.Заявка,
	|	МАКСИМУМ(СтатусыРейсов.Статус) КАК Статус
	|ПОМЕСТИТЬ втСтатусыРейсов
	|ИЗ
	|	РегистрСведений.упСтатусыРейсов.СрезПоследних(, ) КАК СтатусыРейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРаботыПоРейсам КАК втРаботыПоРейсам
	|		ПО СтатусыРейсов.Документ = втРаботыПоРейсам.Рейс
	|			И (СтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется))
	|
	|СГРУППИРОВАТЬ ПО
	|	втРаботыПоРейсам.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА тбЗаявкиКПланированию.КоличествоКВыполнению > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению)
	|		КОГДА ЕСТЬNULL(тбЗаданияКПланированию.КоличествоКВыполнениюОстаток, 0) = 0
	|				И ВЫБОР
	|					КОГДА упВидыПеревозок.ВариантФормированияМаршрута = ЗНАЧЕНИЕ(Перечисление.упВариантыФормированияМаршрута.Полный)
	|						ТОГДА ЕСТЬNULL(тбРаботы.ВыполненаРазгрузка, 0) > 0
	|					ИНАЧЕ ЕСТЬNULL(тбРаботы.ВыполненаПогрузка, 0) > 0
	|				КОНЕЦ
	|				И ЕСТЬNULL(тбРаботы.Выполнена, 0) + ЕСТЬNULL(тбРаботы.Отменена, 0) = ЕСТЬNULL(тбРаботы.КоличествоРабот, 0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена)
	|		КОГДА тбОстатки.ОбщийОстаток = 0
	|				И тбОстатки.КоличествоОстаток = 0
	|			ТОГДА ВЫБОР
	|					КОГДА тбКорректировки.КоличествоСкорректировано = 0
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично)
	|				КОНЕЦ
	//|		КОГДА тбОстатки.КоличествоВТС > 0
	|				КОГДА ЕСТЬNULL(тбЗаданияКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|					И ЕСТЬNULL(тбЗаданияКПланированию.КоличествоНеПодтвержденоОстаток, 0) = 0
	|					И ЕСТЬNULL(тбЗаданияКПланированию.КоличествоНеПереданноеКВыполнениюОстаток, 0) = 0
	|					И ЕСТЬNULL(тбРаботы.КоличествоРабот, 0) > 0
	|					И (ЕСТЬNULL(тбРаботы.Выполнена, 0) > 0
	|							И ЕСТЬNULL(тбРаботы.ВыполненаПогрузка, 0) > ЕСТЬNULL(тбРаботы.ВыполненаРазгрузка, 0)
	|						ИЛИ ЕСТЬNULL(тбРаботы.Отменена, 0) > 0)
	|					И НЕ ЕСТЬNULL(тбРаботы.Отменена, 0) + ЕСТЬNULL(тбРаботы.Выполнена, 0) = ЕСТЬNULL(тбРаботы.КоличествоРабот, 0)
	|					И втСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВПути)
	|		КОГДА ЕСТЬNULL(тбЗаданияКПланированию.КоличествоКВыполнениюОстаток, 0) > 0
	|				И ЕСТЬNULL(тбЗаданияКПланированию.КоличествоНеПереданноеКВыполнениюОстаток, 0) <= 0
	|					И втСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется)
	|		КОГДА ЕСТЬNULL(тбЗаданияКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(тбЗаданияКПланированию.КоличествоКВыполнениюОстаток, 0) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс)
	|		КОГДА ЕСТЬNULL(тбЗаданияКПланированию.КоличествоКПланированиюОстаток, 0) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания)
	|		КОГДА ЕСТЬNULL(тбЗаявкиКПланированию.КоличествоКВыполнению, 0) = 0
	|					И ЕСТЬNULL(тбРаботы.Отменена, 0) > 0
	|					И (ЕСТЬNULL(тбРаботы.Отменена, 0) = ЕСТЬNULL(тбРаботы.КоличествоРабот, 0)
	|						ИЛИ ЕСТЬNULL(тбРаботы.ОтмененаРазгрузка, 0) = ЕСТЬNULL(тбРаботы.Разгрузка, 0))
	|				ИЛИ упСтатусыЗаявокНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)
	|	КОНЕЦ КАК Статус,
	|	ЕСТЬNULL(упСтатусыЗаявокНаПеревозкуГруза.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) КАК ТекущийСтатус,
	|	упЗаявкаНаПеревозкуГруза.Ссылка КАК Заявка
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаявокНаПеревозкуГруза
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = упСтатусыЗаявокНаПеревозкуГруза.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗаявкиКПланированию КАК тбЗаявкиКПланированию
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = тбЗаявкиКПланированию.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗаданияКПланированию КАК тбЗаданияКПланированию
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = тбЗаданияКПланированию.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК тбРаботы
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = тбРаботы.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК тбОстатки
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = тбОстатки.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКорректировки КАК тбКорректировки
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = тбКорректировки.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтатусыРейсов КАК втСтатусыРейсов
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = втСтатусыРейсов.Заявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозок
	|		ПО упЗаявкаНаПеревозкуГруза.ВидПеревозки = упВидыПеревозок.Ссылка
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка В(&Заявки)";
	Запрос.УстановитьПараметр("Заявки", мсвЗаявкиНаПеревозкуГруза);
	
	Возврат Запрос.Выполнить().Выгрузить();
			
КонецФункции	

// Возвращает статус заявки на перевозку.
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза - Массив - заявки (ДокументСсылка.упЗаявкаНаПеревозкуГруза) для расчета статуса.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица заявок с новыми и текущими статусам.
//	   * Статус        - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - новый, рассчитанный статус заявки.
//	   * ТекущийСтатус - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - текущий статус заявки.
//	   * Заявка        - ДокументСсылка.упЗаявкаНаПеревозкуГруза            - заявка.
//
Функция РассчитатьСтатусЗаявкиНаПеревозкуПорядокИсполненияПутевымЛистом(мсвЗаявкиНаПеревозкуГруза) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =

	"
	|	//{: Текст собран конструктором из подсистемы ""Инструменты разработчика"" (http://devtool1c.ucoz.ru), который сохраняет звездочки и комментарии.
	|	//{Запрос: 0 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		упЗаявкаНаПеревозкуГрузаТ.Ссылка КАК Заявка,
	|		упЗаявкаНаПеревозкуГрузаТ.ВидТранспортнойУслуги КАК ВидТранспортнойУслуги,
	|		упЗаявкаНаПеревозкуГрузаТ.КоличествоРабочихСмен КАК КоличествоРабочихСмен,
	|		упЗаявкаНаПеревозкуГрузаТ.ВидПеревозки КАК ВидПеревозки
	|	ПОМЕСТИТЬ втЗаявки
	|	ИЗ
	|		Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГрузаТ
	|	ГДЕ упЗаявкаНаПеревозкуГрузаТ.Ссылка В  (&Заявки)
	|	;
	|	//{Запрос: 1 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		СУММА(упЗаявкиКВыполнениюОстатки.КоличествоОстаток) КАК КоличествоКВыполнению,
	|		упЗаявкиКВыполнениюОстатки.Заявка КАК Заявка
	|	ПОМЕСТИТЬ втЗаявкиКПланированию
	|	ИЗ
	|		РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|			,
	|			Заявка В (
	|				ВЫБРАТЬ
	|					втЗаявки.Заявка КАК Заявка
	|				ИЗ
	|					втЗаявки КАК втЗаявки)) КАК упЗаявкиКВыполнениюОстатки
	|	СГРУППИРОВАТЬ ПО
	|		упЗаявкиКВыполнениюОстатки.Заявка
	|	;
	|	//{Запрос: 2 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		втЗаявкиТ.Заявка КАК Заявка,
	|		СУММА(ВЫБОР
	|			КОГДА ЛОЖЬ
	|				Или втСтатусыПутевыхЛистов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Выдан)
	|				или втСтатусыПутевыхЛистов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Возвращен)
	|				или втСтатусыПутевыхЛистов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Закрыт)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоВыданныхПутевых,
	|		СУММА(ВЫБОР
	|			КОГДА ЛОЖЬ
	|				или втСтатусыПутевыхЛистов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Возвращен)
	|				или втСтатусыПутевыхЛистов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Закрыт)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоВозвращенныхПутевых
	|	ПОМЕСТИТЬ втПутевыеЛисты
	|	ИЗ
	|		Документ.упПутевойЛист.Заявки КАК втПутевыеЛисты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявки КАК втЗаявкиТ
	|		ПО втПутевыеЛисты.ЗаявкаНаПеревозкуГруза = втЗаявкиТ.Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних(
	|			,
	|			) КАК втСтатусыПутевыхЛистов
	|		ПО втСтатусыПутевыхЛистов.Документ = втПутевыеЛисты.Ссылка
	|	СГРУППИРОВАТЬ ПО
	|		втЗаявкиТ.Заявка
	|	;
	|	//{Запрос: 3 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ИСТИНА
	|				И ЕСТЬNULL(тбЗаявкиКПланированию.КоличествоКВыполнению, 0) > 0
	|				И ЕСТЬNULL(втПутевыеЛисты.КоличествоВыданныхПутевых, 0) = 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению)
	|			КОГДА ИСТИНА
	|				И ЕСТЬNULL(тбЗаявкиКПланированию.КоличествоКВыполнению, 0) = 0
	|				И ЕСТЬNULL(втПутевыеЛисты.КоличествоВыданныхПутевых, 0) > 0
	|				И ЕСТЬNULL(втПутевыеЛисты.КоличествоВыданныхПутевых, 0) = ЕСТЬNULL(втПутевыеЛисты.КоличествоВозвращенныхПутевых, 0)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена)
	|			КОГДА ИСТИНА
	|				И ЕСТЬNULL(втПутевыеЛисты.КоличествоВыданныхПутевых, 0) > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется)
	|			КОГДА упСтатусыЗаявокНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)
	|		КОНЕЦ КАК Статус,
	|		ЕСТЬNULL(упСтатусыЗаявокНаПеревозкуГруза.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) КАК ТекущийСтатус,
	|		втЗаявки.Заявка КАК Заявка
	|	ИЗ
	|		втЗаявки КАК втЗаявки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаявокНаПеревозкуГруза
	|		ПО втЗаявки.Заявка = упСтатусыЗаявокНаПеревозкуГруза.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗаявкиКПланированию КАК тбЗаявкиКПланированию
	|		ПО втЗаявки.Заявка = тбЗаявкиКПланированию.Заявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозок
	|		ПО втЗаявки.ВидПеревозки = упВидыПеревозок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПутевыеЛисты КАК втПутевыеЛисты
	|		ПО втЗаявки.Заявка = втПутевыеЛисты.Заявка
	|";

	Запрос.УстановитьПараметр("Заявки", мсвЗаявкиНаПеревозкуГруза);
	
	Возврат Запрос.Выполнить().Выгрузить();
			
КонецФункции	

// Возвращает статус заявки на перевозку.
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза - Массив - заявки (ДокументСсылка.упЗаявкаНаПеревозкуГруза) для расчета статуса.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица заявок с новыми и текущими статусам.
//	   * Статус        - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - новый, рассчитанный статус заявки.
//	   * ТекущийСтатус - ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - текущий статус заявки.
//	   * Заявка        - ДокументСсылка.упЗаявкаНаПеревозкуГруза            - заявка.
//
Функция РассчитатьСтатусЗаявкиНаПеревозку(мсвЗаявкиНаПеревозкуГруза) Экспорт
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("Статус",        Новый ОписаниеТипов("ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза"));
	тбзРезультат.Колонки.Добавить("ТекущийСтатус", Новый ОписаниеТипов("ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза"));
	тбзРезультат.Колонки.Добавить("Заявка",        Новый ОписаниеТипов("ДокументСсылка.упЗаявкаНаПеревозкуГруза"));
	
	ПорядокИсполненияЗаявок = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвЗаявкиНаПеревозкуГруза, "ПорядокИсполненияЗаявки");
	
	мсвЗаявкиИсполнениеРейсом        = Новый Массив;
	мсвЗаявкиИсполнениеПутевымЛистом = Новый Массив;
	
	Для каждого ЗаявкаНаПеревозкуГрузов Из мсвЗаявкиНаПеревозкуГруза Цикл
		Если ПорядокИсполненияЗаявок.Получить(ЗаявкаНаПеревозкуГрузов) = Перечисления.упПорядокИсполненияЗаявок.Рейсом Тогда
			мсвЗаявкиИсполнениеРейсом.Добавить(ЗаявкаНаПеревозкуГрузов);
		Иначе	
			мсвЗаявкиИсполнениеПутевымЛистом.Добавить(ЗаявкаНаПеревозкуГрузов);
		КонецЕсли;
	КонецЦикла;
	
	Если мсвЗаявкиИсполнениеРейсом.Количество() > 0 Тогда
		тбзСтатусы = РассчитатьСтатусЗаявкиНаПеревозкуПорядокИсполненияРейсом(мсвЗаявкиИсполнениеРейсом);
		Для каждого Строка Из тбзСтатусы Цикл
			НоваяСтрока = тбзРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	
	Если мсвЗаявкиИсполнениеПутевымЛистом.Количество() > 0 Тогда
		тбзСтатусы = РассчитатьСтатусЗаявкиНаПеревозкуПорядокИсполненияПутевымЛистом(мсвЗаявкиИсполнениеПутевымЛистом);
		Для каждого Строка Из тбзСтатусы Цикл
			НоваяСтрока = тбзРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат тбзРезультат;
			
КонецФункции	

// Возвращает таблицу значений статуса задания на перевозку.
//
// Параметры:
//  мсвЗаданияНаПеревозкуГруза - Массив - Массив ссылок на документы.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция РассчитатьСтатусЗаданияНаПеревозку(мсвЗаданияНаПеревозкуГруза) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданияКПланированиюОстатки.Задание КАК Задание,
	|	упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток КАК КоличествоКПланированиюОстаток,
	|	упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток КАК КоличествоНеПодтвержденоОстаток,
	|	упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток КАК КоличествоНеПереданноеКВыполнениюОстаток,
	|	упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнениюОстаток
	|ПОМЕСТИТЬ втКПланированию
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|		,
	|		Задание В (&Задание)) КАК упЗаданияКПланированиюОстатки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Отменена
	|				И ВЫБОР
	|				КОГДА упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ НЕ упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)
	|			КОНЕЦ
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Отменена,
	|	СУММА(ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Выполнена
	|				ИЛИ ВЫБОР
	|				КОГДА упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)
	|			КОНЕЦ
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Выполнена,
	|	СУММА(ВЫБОР
	|		КОГДА упРаботыПоРейсуСрезПоследних.Выполнена
	|				И (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|				И (упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|						ИЛИ (упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|							ИЛИ упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)))
	|				ИЛИ упРаботыПоРейсуСрезПоследних.Отменена
	|							И (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|								ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|							И (НЕ упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|									И упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий))
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ВыполненаРазгрузка,
	|	СУММА(ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Выполнена
	|					ИЛИ упРаботыПоРейсуСрезПоследних.Отменена
	|					И (НЕ упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|						И упРаботыПоРейсуСрезПоследних.Проблема.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)))
	|				И (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|							ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|						И упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ВыполненаПогрузка,
	|	СУММА(ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|				И (упРаботыПоРейсуСрезПоследних.Отменена
	|						ИЛИ НЕ упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка))
	|					И НЕ упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы.Действие = ЗНАЧЕНИЕ(Перечисление.упДействияПоРаботе.НетДействий)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ОтмененаРазгрузка,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ упРаботыПоРейсуСрезПоследних.Отменена
	|				И НЕ упРаботыПоРейсуСрезПоследних.Выполнена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоНевыполненныхРабот,
	|	СУММА(ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|				И упРаботыПоРейсуСрезПоследних.Проблема = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Разгрузка,
	|	СУММА(1) КАК КоличествоРабот,
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание
	|ПОМЕСТИТЬ втРаботыПодготовка
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Задание В (&Задание)) КАК упРаботыПоРейсуСрезПоследних
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсуСрезПоследних.Рейс,
	|	упРаботыПоРейсуСрезПоследних.Задание
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыРейсов.Документ КАК Рейс,
	|	втРаботыПодготовка.Задание КАК Задание,
	|	СтатусыРейсов.Статус КАК Статус,
	|	ВЫБОР
	|		КОГДА СтатусыРейсов.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен),
	|				ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен),
	|				ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично),
	|				ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен))
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ втСтатусыРейсовПодготовка
	|ИЗ
	|	РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|		,
	|		Документ В (
	|		ВЫБРАТЬ
	|		втРаботыПодготовка.Рейс КАК Рейс
	|		ИЗ
	|		втРаботыПодготовка КАК втРаботыПодготовка)) КАК СтатусыРейсов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРаботыПодготовка КАК втРаботыПодготовка
	|	ПО СтатусыРейсов.Документ = втРаботыПодготовка.Рейс
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусыРейсов.Задание КАК Задание,
	|	МАКСИМУМ(СтатусыРейсов.Статус) КАК Статус
	|ПОМЕСТИТЬ втСтатусыРейсов
	|ИЗ
	|	втСтатусыРейсовПодготовка КАК СтатусыРейсов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втСтатусыРейсовПодготовка.Задание КАК Задание,
	|		МАКСИМУМ(втСтатусыРейсовПодготовка.Приоритет) КАК Приоритет
	|	ИЗ
	|		втСтатусыРейсовПодготовка КАК втСтатусыРейсовПодготовка
	|	СГРУППИРОВАТЬ ПО
	|		втСтатусыРейсовПодготовка.Задание) КАК втПриоритет
	|	ПО ИСТИНА
	|		И СтатусыРейсов.Приоритет = втПриоритет.Приоритет
	|		И СтатусыРейсов.Задание = втПриоритет.Задание
	|СГРУППИРОВАТЬ ПО
	|	СтатусыРейсов.Задание
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втРаботы.Отменена) КАК Отменена,
	|	СУММА(втРаботы.Выполнена) КАК Выполнена,
	|	СУММА(втРаботы.ВыполненаПогрузка) КАК ВыполненаПогрузка,
	|	СУММА(втРаботы.ВыполненаРазгрузка) КАК ВыполненаРазгрузка,
	|	СУММА(втРаботы.ОтмененаРазгрузка) КАК ОтмененаРазгрузка,
	|	СУММА(втРаботы.Разгрузка) КАК Разгрузка,
	|	СУММА(втРаботы.КоличествоРабот) КАК КоличествоРабот,
	|	СУММА(втРаботы.КоличествоНевыполненныхРабот) КАК КоличествоНевыполненныхРабот,
	|	МИНИМУМ(втСтатусыРейсов.Статус) КАК СтатусРейса,
	|	втРаботы.Задание КАК Задание
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втРаботыПодготовка КАК втРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСтатусыРейсов КАК втСтатусыРейсов
	|	ПО втРаботы.Задание = втСтатусыРейсов.Задание
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Задание
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПодтвержденоОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПереданноеКВыполнениюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоКВыполнениюОстаток, 0) = 0
	|				И ВЫБОР
	|				КОГДА упВидыПеревозок.ВариантФормированияМаршрута = ЗНАЧЕНИЕ(Перечисление.упВариантыФормированияМаршрута.Полный)
	|					ТОГДА ЕСТЬNULL(втРаботы.ВыполненаРазгрузка, 0) > 0
	|				ИНАЧЕ ЕСТЬNULL(втРаботы.ВыполненаПогрузка, 0) > 0
	|			КОНЕЦ
	|				И ЕСТЬNULL(втРаботы.Выполнена, 0) + ЕСТЬNULL(втРаботы.Отменена, 0) = ЕСТЬNULL(втРаботы.КоличествоРабот, 0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполнено)
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПодтвержденоОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПереданноеКВыполнениюОстаток, 0) = 0
	|				И ЕСТЬNULL(втРаботы.КоличествоРабот, 0) > 0
	|				И (ЕСТЬNULL(втРаботы.Выполнена, 0) > 0
	|					ИЛИ ЕСТЬNULL(втРаботы.Отменена, 0) > 0)
	|				И НЕ ЕСТЬNULL(втРаботы.Отменена, 0) + ЕСТЬNULL(втРаботы.Выполнена, 0) = ЕСТЬNULL(втРаботы.КоличествоРабот, 0)
	// Изменения для случая если задание выполняется несколькими рейсами и
	// Рейс1 в статусе "Выполнен", Рейс2 в статусе "Квыполнению", задание оставалось в статусе "ВПути", а должно быть "Включено в рейс" 
	|				И втРаботы.СтатусРейса = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ВПути)
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПодтвержденоОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПереданноеКВыполнениюОстаток, 0) = 0
	|				И ЕСТЬNULL(втРаботы.КоличествоНевыполненныхРабот, 0) > 0
	|				И втРаботы.СтатусРейса = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполняется)
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПодтвержденоОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоНеПереданноеКВыполнениюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоКВыполнениюОстаток, 0) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению)
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоКВыполнениюОстаток, 0) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ВКлюченоВРейс)
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию)
	|		КОГДА ЕСТЬNULL(втКПланированию.КоличествоКПланированиюОстаток, 0) = 0
	|				И ЕСТЬNULL(втКПланированию.КоличествоКВыполнениюОстаток, 0) = 0
	|				И ЕСТЬNULL(втРаботы.Отменена, 0) > 0
	|				И (ЕСТЬNULL(втРаботы.Отменена, 0) = ЕСТЬNULL(втРаботы.КоличествоРабот, 0)
	|					ИЛИ ЕСТЬNULL(втРаботы.ОтмененаРазгрузка, 0) = ЕСТЬNULL(втРаботы.Разгрузка, 0))
	|				ИЛИ упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)
	|	КОНЕЦ КАК Статус,
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
	|	ЕСТЬNULL(упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)) КАК ТекущийСтатус
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКПланированию КАК втКПланированию
	|	ПО упЗаданиеНаПеревозкуГруза.Ссылка = втКПланированию.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК втРаботы
	|	ПО упЗаданиеНаПеревозкуГруза.Ссылка = втРаботы.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|		,
	|		Документ В (&Задание)) КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	|	ПО упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозок
	|	ПО упЗаданиеНаПеревозкуГруза.ВидПеревозки = упВидыПеревозок.Ссылка
	|ГДЕ упЗаданиеНаПеревозкуГруза.Ссылка В  (&Задание)
	|";

	Запрос.УстановитьПараметр("Задание", мсвЗаданияНаПеревозкуГруза);	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Возврат Запрос.Выполнить().Выгрузить();
					   
КонецФункции // РассчитатьСтатусЗаданияНаПеревозку()

// Возвращает структуру статуса рейса.
//
// Параметры:
//  Рейс - ДокументСсылка - ссылка на документ.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция РассчитатьСтатусРейса(Рейс) Экспорт

	сткРезультат = Новый Структура();
	сткРезультат.Вставить("Статус", Неопределено);
	сткРезультат.Вставить("ТекущийСтатус", Неопределено);
	сткРезультат.Вставить("ВыполненЧастично", Ложь);
	сткРезультат.Вставить("ОтложенаЧастьРабот", Ложь);
	сткРезультат.Вставить("Состояние", "");
	сткРезультат.Вставить("ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера", Ложь);
	сткРезультат.Вставить("ВидТранспортнойУслуги", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|		КОГДА (упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|				И упРаботыПоРейсуСрезПоследних.Отменена
	|					И упРаботыПоРейсуСрезПоследних.Проблема <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ДосрочноеПрохождение)
	|					И НЕ упРаботыПоРейсуСрезПоследних.ГрузРазделен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличестовОтмененныхПогрузок,
	|	СУММА(ВЫБОР
	|		КОГДА  Истина
	|		       И (Ложь
	|		          ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|			      ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|			   И упРаботыПоРейсуСрезПоследних.Отменена
	|			   И упРаботыПоРейсуСрезПоследних.Проблема <> ЗНАЧЕНИЕ(Справочник.упПроблемы.ДосрочноеПрохождение)
	|			   И НЕ упРаботыПоРейсуСрезПоследних.ГрузРазделен
	|			   И (Ложь
	|			      ИЛИ упРаботыПоРейсуСрезПоследних.Рейс.ВидТранспортнойУслуги <> ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами)
	|			      ИЛИ (Истина
	|			           И упРаботыПоРейсуСрезПоследних.Рейс.ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами)
	|			           И упРаботыПоРейсуСрезПоследних.АдресРазгрузки = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка)))
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличестовОтмененныхРазгрузок
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|		КОГДА упМаршрутПоРейсуСрезПоследних.Пройдена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоПройденныхТочек,
	|	СУММА(ВЫБОР
	|		КОГДА упМаршрутПоРейсуСрезПоследних.Отложена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтложенныхТочек,
	|	СУММА(ВЫБОР
	|		КОГДА упМаршрутПоРейсуСрезПоследних.Отменена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтмененныхТочек,
	|	СУММА(ВЫБОР
	|		КОГДА упМаршрутПоРейсуСрезПоследних.Отменена
	|				И НЕ упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|				И НЕ упМаршрутПоРейсуСрезПоследних.УбытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекИзмененныхПрохождением,
	|	КОЛИЧЕСТВО(упМаршрутПоРейсуСрезПоследних.Идентификатор) КАК КоличествоТочек,
	|	втРаботы.КоличестовОтмененныхПогрузок КАК КоличестовОтмененныхПогрузок,
	|	втРаботы.КоличестовОтмененныхРазгрузок КАК КоличестовОтмененныхРазгрузок,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс
	|ПОМЕСТИТЬ втТочки
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних,
	|	втРаботы КАК втРаботы
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.КоличестовОтмененныхПогрузок,
	|	втРаботы.КоличестовОтмененныхРазгрузок,
	|	упМаршрутПоРейсуСрезПоследних.Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен)
	|		КОГДА упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|				ИЛИ ЕСТЬNULL(втТочки.КоличествоОтмененныхТочек, 0) > 0
	|				И ЕСТЬNULL(втТочки.КоличествоОтмененныхТочек, 0) = ЕСТЬNULL(втТочки.КоличествоТочек, 0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|		КОГДА (упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен)
	|					ИЛИ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично))
	|				И упРейс.ВидПеревозки.ТребуетсяОформлениеПутевогоЛиста
	|					И упПеревозчикПоРейсуСрезПоследних.Перевозчик.ТребуетсяОформлениеПутевогоЛиста
	|					И упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Закрыт)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен)
	|		КОГДА НЕ втТочки.КоличествоПройденныхТочек ЕСТЬ NULL
	|				И (ЕСТЬNULL(втТочки.КоличестовОтмененныхПогрузок, 0) > 0
	|					ИЛИ ЕСТЬNULL(втТочки.КоличестовОтмененныхРазгрузок, 0) > 0
	|					ИЛИ ЕСТЬNULL(втТочки.КоличествоОтложенныхТочек, 0) > 0)
	|				И ЕСТЬNULL(втТочки.КоличествоПройденныхТочек, 0) + ЕСТЬNULL(втТочки.КоличествоОтмененныхТочек, 0) + ЕСТЬNULL(втТочки.КоличествоОтложенныхТочек, 0) = ЕСТЬNULL(втТочки.КоличествоТочек, 0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично)
	|		КОГДА НЕ втТочки.КоличествоПройденныхТочек ЕСТЬ NULL
	|				И ЕСТЬNULL(втТочки.КоличествоПройденныхТочек, 0) + ЕСТЬNULL(втТочки.КоличествоОтмененныхТочек, 0) = ЕСТЬNULL(втТочки.КоличествоТочек, 0)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен)
	|		КОГДА НЕ втТочки.КоличествоПройденныхТочек ЕСТЬ NULL
	|				И ЕСТЬNULL(втТочки.КоличествоПройденныхТочек, 0) > 0
	|				ИЛИ НЕ упРейс.ВидПеревозки.СпособПрохожденияТочекМаршрута = ЗНАЧЕНИЕ(Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе)
	|				И ЕСТЬNULL(втТочки.КоличествоТочекИзмененныхПрохождением, 0) > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|		КОГДА НЕ упСтатусыРейсовСрезПоследних.Статус ЕСТЬ NULL
	|				И (упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен)
	|					ИЛИ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично)
	|					ИЛИ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется)
	|					ИЛИ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению)
	|		КОГДА ЕСТЬNULL(упСтатусыРейсовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)) В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый),
	|				ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КПланированиюТС),
	|				ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.НазначеноТС))
	|				И упРейс.ПеревозкаТранспортнойКомпанией
	|				ИЛИ НЕ упПеревозчикПоРейсуСрезПоследних.Рейс ЕСТЬ NULL
	|					И упРейс.ВидПеревозки.ВыборТС = ЗНАЧЕНИЕ(Перечисление.упВариантыПодбораТС.НепосредственныйПодборВРейсе)
	|					ИЛИ упПеревозчикПоРейсуСрезПоследних.Регистратор ССЫЛКА Документ.упПодтверждениеЗаявкиНаТС
	|					ИЛИ НЕ упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство ЕСТЬ NULL
	|					И НЕ упИерархияПартнеров.Партнер ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.НазначеноТС)
	|		КОГДА ЕСТЬNULL(упСтатусыРейсовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)) = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)
	|				И упРейс.ВидПеревозки.АвтоматическиПередаватьРейсКПланированию
	|				ИЛИ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КПланированиюТС)
	|				ИЛИ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.НазначеноТС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КПланированиюТС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)
	|	КОНЕЦ КАК Статус,
	|	упСтатусыРейсовСрезПоследних.Статус КАК ТекущийСтатус,
	|	упСтатусыРейсовСрезПоследних.ВыполненЧастично КАК ВыполненЧастично,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТочки.КоличествоОтложенныхТочек, 0) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОтложенаЧастьРабот,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТочки.КоличествоОтложенныхТочек, 0) > 0
	|			ТОГДА ""Есть отложенные работы""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Состояние,
	|	ВЫБОР
	|		КОГДА упРейс.ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами)
	|				И упРейс.ВидПеревозки.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера,
	|	упРейс.ВидТранспортнойУслуги
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|		,
	|		Документ = &Рейс) КАК упСтатусыРейсовСрезПоследних
	|	ПО упСтатусыРейсовСрезПоследних.Документ = упРейс.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТочки КАК втТочки
	|	ПО упРейс.Ссылка = втТочки.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО ИСТИНА
	|		И упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка
	|		И НЕ (упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияПартнеров КАК упИерархияПартнеров
	|	ПО ИСТИНА
	|		И упРейс.Организация.Партнер = упИерархияПартнеров.Родитель
	|		И упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Партнер = упИерархияПартнеров.Партнер
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних(
	|			,
	|			) КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО ИСТИНА
	|			И упПутевойЛистРейсы.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ.Ссылка
	|			И НЕ (упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	|	ПО ИСТИНА
	|		И упРейс.Ссылка = упПутевойЛистРейсы.Рейс
	|		И НЕ (упПутевойЛистРейсы.Ссылка.ПометкаУдаления)
	|ГДЕ упРейс.Ссылка = &Рейс
	|";


	Запрос.УстановитьПараметр("Рейс", Рейс);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции // РассчитатьСтатусРейса()()

#КонецОбласти 

Функция ПолучитьВозможныеНовыеСтатусы(ТипДокумента, ТекущийСтатус, ДополнительныеПараметры)
	
	мсвСтатусы = Новый Массив;
	
	Если ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыЗаявкиНаПеревозкуГруза(ТекущийСтатус, ДополнительныеПараметры);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыЗаданияНаПеревозкуГруза(ТекущийСтатус, ДополнительныеПараметры);		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упРейс") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыРейса(ТекущийСтатус, ДополнительныеПараметры);		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда	
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыЗаявкиНаТС(ТекущийСтатус, ДополнительныеПараметры);		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упПутевойЛист") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыПутевогоЛиста(ТекущийСтатус);	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыЗапросаУсловийПеревозки(ТекущийСтатус);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упРемонтИТОТС") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыРемонтаИТОТС(ТекущийСтатус);	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упВходящаяПретензия") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыВходящейПретензии(ТекущийСтатус);	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упИсходящаяПретензия") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыИсходящейПретензии(ТекущийСтатус);	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда
		мсвСтатусы = ПолучитьВозможныеНовыеСтатусыЗаявкиРемонтаИТОТС(ТекущийСтатус, ДополнительныеПараметры);
	КонецЕсли;
		
	Возврат мсвСтатусы;
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыЗаявкиНаПеревозкуГруза(ТекущийСтатус, ДополнительныеПараметры)
	
	мсвСтатусы = Новый Массив;
	
	ЭтоПолноправныйПользователь = ДополнительныеПараметры.ЭтоПолноправныйПользователь;
	АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению = ДополнительныеПараметры.АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению;
	ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = ДополнительныеПараметры.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза;
	ИспользуютсяМультимодальныеПеревозки = ДополнительныеПараметры.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки;
	ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза = ДополнительныеПараметры.ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза;
	ДоступнаРольОтменаЗаявкиНаПеревозкуГруза = ДополнительныеПараметры.ДоступнаРольОтменаЗаявкиНаПеревозкуГруза;
	
	ЕстьНеОтмененныеЗаданияНаПеревозку = Ложь;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.Ссылка) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упЗаданиеНаПеревозкуГруза.Ссылка
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
		|		ПО (упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ = упЗаданиеНаПеревозкуГруза.Ссылка)
		|ГДЕ
		|	НЕ упЗаданиеНаПеревозкуГруза.ПометкаУдаления
		|	И НЕ ЕСТЬNULL(упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)) = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
		|	И упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДополнительныеПараметры.Ссылка);
		ЕстьНеОтмененныеЗаданияНаПеревозку = НЕ Запрос.Выполнить().Пустой();
	КонецЕсли;

	СтатусПустаяСсылка	= Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПустаяСсылка();
	
	Если ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена Тогда
		
		Если ЭтоПолноправныйПользователь Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая);
		КонецЕсли;	
		
		Если НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);	
		КонецЕсли;
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли Не ЗначениеЗаполнено(ТекущийСтатус) ИЛИ ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая Тогда	
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.Ссылка) Тогда
			Если ИспользуютсяМультимодальныеПеревозки Тогда
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок);
			КонецЕсли; 
			
			Если ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза  Тогда 
				Если ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза ИЛИ ДополнительныеПараметры.ЭтоПолноправныйПользователь Тогда 
					мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована);
				КонецЕсли;	
				Если АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению Тогда
					мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
				КонецЕсли;
			Иначе
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
			КонецЕсли;	
			
			Если НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);	
			КонецЕсли;
			
			мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		КонецЕсли; 
				
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок Тогда	
			
		Если ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза  Тогда 
			Если ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза ИЛИ ДополнительныеПараметры.ЭтоПолноправныйПользователь Тогда 
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована);
			КонецЕсли;	
			Если АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению Тогда
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
			КонецЕсли;
		Иначе
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
		КонецЕсли;	
				
		Если НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована Тогда	
		
		Если ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована);
		КонецЕсли;	
		Если НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована Тогда				
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
		Если НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);	
		КонецЕсли;
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению Тогда
		Если (ДоступнаРольОтменаЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания Тогда				
		Если (ДоступнаРольОтменаЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс Тогда				
		Если (ДоступнаРольОтменаЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется Тогда							
		Если (ДоступнаРольОтменаЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда  
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена Тогда	
		// Нет.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично Тогда							
		Если (ДоступнаРольОтменаЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда  
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена Тогда							
		// Нет.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отказ Тогда										
		// Нет.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути Тогда													
		Если (ДоступнаРольОтменаЗаявкиНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеЗаданияНаПеревозку Тогда  
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
		КонецЕсли;	
		
	КонецЕсли;		
	Возврат мсвСтатусы;	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыЗаданияНаПеревозкуГруза(ТекущийСтатус, ДополнительныеПараметры)
	
	мсвСтатусы = Новый Массив;
	
	ЭтоПолноправныйПользователь						= ДополнительныеПараметры.ЭтоПолноправныйПользователь;
	ДоступнаРольОтменаЗаданийНаПеревозкуГруза		= ДополнительныеПараметры.ДоступнаРольОтменаЗаданийНаПеревозкуГруза;
	СтатусПустаяСсылка								= Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ПустаяСсылка();
	
	Если  Не ЗначениеЗаполнено(ТекущийСтатус) ИЛИ ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое Тогда
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Тогда	
		// Пока поставлю комментарий, так как "К выполнению" задание должно переходить,
		// после того как перешли к Планированию ТС.		
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс Тогда		
		//Если ДоступнаРольОтменаЗаданийНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь Тогда
		//	мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
		//КонецЕсли;
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению Тогда		
		//Если ДоступнаРольОтменаЗаданийНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь Тогда
		//	мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
		//КонецЕсли;
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполняется Тогда		
		//Если ДоступнаРольОтменаЗаданийНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь Тогда
		//	мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
		//КонецЕсли;
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВПути Тогда		
		//Если ДоступнаРольОтменаЗаданийНаПеревозкуГруза ИЛИ ЭтоПолноправныйПользователь Тогда
		//	мсвСтатусы.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
		//КонецЕсли;
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполнено Тогда		
		// Нет.	
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда		
		// Нет.	
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отказ Тогда			
		// Нет.	
	КонецЕсли;		
	Возврат мсвСтатусы;	
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыРейса(ТекущийСтатус, ДополнительныеПараметры)
	
	мсвСтатусы = Новый Массив;
		
	ПеревозкаТранспортнойКомпанией = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ПеревозкаТранспортнойКомпанией", Ложь);	
	ТребуетсяОформлениеПутевогоЛиста = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ТребуетсяОформлениеПутевогоЛиста", Ложь);
	СпособПрохожденияТочекМаршрута = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "СпособПрохожденияТочекМаршрута", Неопределено);
	ЕстьОтмененныеРаботы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЕстьОтмененныеРаботы", Ложь);
	ЕстьОтложенныеРаботы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЕстьОтложенныеРаботы", Ложь);
	ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера", Ложь);
	ЭтоОперацияСКонтейнером = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЭтоОперацияСКонтейнером", Ложь);
	СтатусПустаяСсылка = Перечисления.упСтатусыРейса.ПустаяСсылка();
		
	Если Ложь
		Или Не ЗначениеЗаполнено(ТекущийСтатус) 
		Или ТекущийСтатус = Перечисления.упСтатусыРейса.Новый Тогда
		
		Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Или ПеревозкаТранспортнойКомпанией Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
		Иначе	
			мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.КПланированиюТС);	
		КонецЕсли;
		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Отменен);	
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
			
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыРейса.КПланированиюТС Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Отменен);	
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС Тогда	
		// Нет.
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыРейса.НазначеноТС Тогда
		
		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
		
		Если НЕ упРейс.ЕстьЕщеРейсыПоТСВСтатусеВыполняется(ДополнительныеПараметры.Ссылка) Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполняется);	
		КонецЕсли;
		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Отменен);	
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
				
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРейса.КВыполнению Тогда
		
		упОтменаРейсаВСтатусеКВыполнению = РольДоступна("упОтменаРейсаВСтатусеКВыполнению");
		РольДоступнаупРазработчик        = РольДоступна("упРазработчик");
		РольДоступнаПолныеПрава          = РольДоступна("ПолныеПрава");
		
		РазрешеноОтменятьРейс = Ложь
						    Или РольДоступнаПолныеПрава
						    Или РольДоступнаупРазработчик
						    Или упОтменаРейсаВСтатусеКВыполнению;
		
		Если РазрешеноОтменятьРейс Тогда
			 мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Отменен);
		КонецЕсли;
					
		Если РазрешеноВыполнятьРейс(ЭтоОперацияСКонтейнером, ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера, ДополнительныеПараметры.Ссылка) Тогда
			Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
				мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Завершен);
			Иначе	 
				Если СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
					Если ЕстьОтмененныеРаботы Тогда
						мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);		
					Иначе
						мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполнен);			
					КонецЕсли;
				Иначе
					мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполняется);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРейса.Выполняется Тогда	
		
		Если РазрешеноВыполнятьРейс(ЭтоОперацияСКонтейнером, ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера, ДополнительныеПараметры.Ссылка) Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполнен);	
		КонецЕсли;	
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРейса.Выполнен Тогда
		
		Если Истина
			И Не ТребуетсяОформлениеПутевогоЛиста
			И РазрешеноВыполнятьРейс(ЭтоОперацияСКонтейнером, ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера, ДополнительныеПараметры.Ссылка)
		Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Завершен);	
		КонецЕсли;
		
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРейса.ВыполненЧастично Тогда	
		
		Если Истина
			И Не ТребуетсяОформлениеПутевогоЛиста
			И Не ЕстьОтложенныеРаботы
			И РазрешеноВыполнятьРейс(ЭтоОперацияСКонтейнером, ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера, ДополнительныеПараметры.Ссылка)
		Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Завершен);	
		КонецЕсли;
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыРейса.Отменен Тогда	
		// Нет.
		
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыРейса.Завершен Тогда			
		// Нет.
		
	КонецЕсли;		
	
	Возврат мсвСтатусы;	
	
КонецФункции

Функция РазрешеноВыполнятьРейс(ЭтоОперацияСКонтейнером, ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера, Рейс)
	Возврат Ложь
             Или Не ЭтоОперацияСКонтейнером
             Или (Истина
                  И ЭтоОперацияСКонтейнером
			   И (Ложь
			      Или Не ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера
			      Или (Истина
			           И ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера
				      И упРейс.ЕстьВыполненнаяЗаявкаНаПеревозкуГрузаПоОперацииСКонтейнером(Рейс))));
	 
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыЗаявкиНаТС(ТекущийСтатус, ДополнительныеПараметры)
	
	мсвСтатусы = Новый Массив;
	Тендер = ДополнительныеПараметры.Тендер;
	СтатусПустаяСсылка = Перечисления.упСтатусыЗаявкиНаТС.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) ИЛИ ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаТС.Создана Тогда
		Если НЕ Тендер Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Подтверждена);	
		КонецЕсли;
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Отправлена);		
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Отклонена);
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаТС.Отправлена Тогда	
		Если НЕ Тендер Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Подтверждена);	
		КонецЕсли;
		
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Отклонена);
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение Тогда			
		Если НЕ Тендер Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Подтверждена);	
		КонецЕсли;
		
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаТС.Отклонена);
				
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда	
		// Нет.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Тогда			
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		// Нет.
	КонецЕсли;		
	
	Возврат мсвСтатусы;	
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыПутевогоЛиста(ТекущийСтатус)
	
	мсвСтатусы = Новый Массив;
	
	СтатусПустаяСсылка				 = Перечисления.упСтатусыРейса.ПустаяСсылка();
	
	Если  Не ЗначениеЗаполнено(ТекущийСтатус) ИЛИ ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Новый Тогда
		мсвСтатусы.Добавить(Перечисления.упСтатусыПутевогоЛиста.Выдан);	
		мсвСтатусы.Добавить(Перечисления.упСтатусыПутевогоЛиста.Отменен);	
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыПутевогоЛиста.Возвращен);	
		мсвСтатусы.Добавить(Перечисления.упСтатусыПутевогоЛиста.Отменен);	
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыПутевогоЛиста.Закрыт);	
		мсвСтатусы.Добавить(Перечисления.упСтатусыПутевогоЛиста.Отменен);	
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда	
		// Нет.
	ИначеЕсли  ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Отменен Тогда			
		// Нет.
	КонецЕсли;		
	
	Возврат мсвСтатусы;	
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыЗапросаУсловийПеревозки(ТекущийСтатус)
	
	мсвСтатусы = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Или ТекущийСтатус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Новый Тогда
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗапросаУсловийПеревозки.Обработан);	
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен);	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Обработан Тогда	
		// Нет.
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Закрыт Тогда	
		// Нет.	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен Тогда			
		// Нет.
	КонецЕсли;		
	
	Возврат мсвСтатусы;
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыРемонтаИТОТС(ТекущийСтатус)
	
	мсвСтатусы = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Или ТекущийСтатус = Перечисления.упСтатусыРемонтаИТОТС.Новый Тогда
		мсвСтатусы.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Запланирован);
		мсвСтатусы.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Начат);
		мсвСтатусы.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРемонтаИТОТС.Начат Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Завершен);
		мсвСтатусы.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда			
		// Нет.
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыРемонтаИТОТС.Отменен Тогда			
		// Нет.	
	КонецЕсли;		
	
	Возврат мсвСтатусы;
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыЗаявкиРемонтаИТОТС(ТекущийСтатус, ДополнительныеПараметры)
	
	мсвСтатусы = Новый Массив;
	
	ЭтоПолноправныйПользователь = ДополнительныеПараметры.ЭтоПолноправныйПользователь;
	АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению = ДополнительныеПараметры.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению;
	ТребуетсяСогласованиеЗаявкиНаРемонт = ДополнительныеПараметры.ТребуетсяСогласованиеЗаявкиНаРемонт;
	ДоступнаРольСогласованиеЗаявкиНаРемонтИТО = ДополнительныеПараметры.ДоступнаРольСогласованиеЗаявкиНаРемонтИТО; //упСогласованиеЗаявкиНаРемонтИТО
	СтатусРемонт = Перечисления.упСтатусыРемонтаИТОТС.ПустаяСсылка();
	Если ДополнительныеПараметры.Свойство("СтатусРемонт") Тогда
		СтатусРемонт = ДополнительныеПараметры.СтатусРемонт;
	КонецЕсли;
	
	ЕстьНеОтмененныеРемонты = Ложь;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.Ссылка) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	РемонтИТОТС.Ссылка
		|ИЗ
		|	Документ.упРемонтИТОТС КАК РемонтИТОТС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРемонтовИТОТС.СрезПоследних КАК упСтатусыРемонтовИТОТС
		|		ПО (упСтатусыРемонтовИТОТС.Документ = РемонтИТОТС.Ссылка)
		|ГДЕ
		|	НЕ РемонтИТОТС.ПометкаУдаления
		|	И НЕ ЕСТЬNULL(упСтатусыРемонтовИТОТС.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Новый)) = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Отменен)
		|	И РемонтИТОТС.ЗаявкаНаРемонтИТО = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДополнительныеПараметры.Ссылка);
		ЕстьНеОтмененныеРемонтИТОТС = НЕ Запрос.Выполнить().Пустой();
	КонецЕсли;
	
	СтатусПустаяСсылка	= Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Или ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая Тогда
		
		Если ТребуетсяСогласованиеЗаявкиНаРемонт  Тогда 
			Если ДоступнаРольСогласованиеЗаявкиНаРемонтИТО ИЛИ ЭтоПолноправныйПользователь Тогда 
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована);
			КонецЕсли;	
			Если АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению Тогда
				мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению);
			КонецЕсли;
		Иначе
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению);
		КонецЕсли;	
		
		Если НЕ ЕстьНеОтмененныеРемонты Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена);
		КонецЕсли;
		
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.НеСогласована Тогда	
		
		Если (ДоступнаРольСогласованиеЗаявкиНаРемонтИТО ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеРемонты Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована);
		КонецЕсли;	
		
		Если НЕ ЕстьНеОтмененныеРемонты Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена);
		КонецЕсли;
		
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована Тогда				
				
		мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению);
		
		Если НЕ ЕстьНеОтмененныеРемонты Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена);
		КонецЕсли;
		
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению Тогда
		
		Если (ДоступнаРольСогласованиеЗаявкиНаРемонтИТО ИЛИ ЭтоПолноправныйПользователь) И НЕ ЕстьНеОтмененныеРемонты Тогда 
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена);
		КонецЕсли;
		
		МассивСтатусовРемонта = Новый Массив;
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Новый);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Запланирован);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Начат);
		Если НЕ МассивСтатусовРемонта.Найти(СтатусРемонт) = Неопределено Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению);
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт);
		КонецЕсли;
		
		мсвСтатусы.Добавить(СтатусПустаяСсылка); // Разрешена отмена проведения.
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт Тогда				
		
		МассивСтатусовРемонта = Новый Массив;
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Новый);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Запланирован);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Начат);
		Если НЕ МассивСтатусовРемонта.Найти(СтатусРемонт) = Неопределено Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт);
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте);
		КонецЕсли;
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте Тогда				
		
		МассивСтатусовРемонта = Новый Массив;
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Начат);
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Завершен);
		Если НЕ МассивСтатусовРемонта.Найти(СтатусРемонт) = Неопределено Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте);
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена);
		КонецЕсли;
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена Тогда	
		
		МассивСтатусовРемонта = Новый Массив;
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Завершен);
		Если НЕ МассивСтатусовРемонта.Найти(СтатусРемонт) = Неопределено Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена);
		КонецЕсли;
		
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена Тогда							
		
		МассивСтатусовРемонта = Новый Массив;
		МассивСтатусовРемонта.Добавить(Перечисления.упСтатусыРемонтаИТОТС.Отменен);
		Если НЕ МассивСтатусовРемонта.Найти(СтатусРемонт) = Неопределено Тогда
			мсвСтатусы.Добавить(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена);
		КонецЕсли;
		
	КонецЕсли;		
	
	Возврат мсвСтатусы;
	
КонецФункции // ПолучитьВозможныеНовыеСтатусыЗаявкиРемонтаИТОТС()

Функция ПолучитьВозможныеНовыеСтатусыВходящейПретензии(ТекущийСтатус)
	
	мсвСтатусы = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Или ТекущийСтатус = Перечисления.упСтатусыВходящейПретензии.Новая Тогда
		мсвСтатусы.Добавить(Перечисления.упСтатусыВходящейПретензии.ВРаботе);
		мсвСтатусы.Добавить(Перечисления.упСтатусыВходящейПретензии.Отменена);	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыВходящейПретензии.ВРаботе Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыВходящейПретензии.Отклонена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыВходящейПретензии.Отменена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыВходящейПретензии.Удовлетворена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыВходящейПретензии.УдовлетворенаЧастично);	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыВходящейПретензии.Удовлетворена Тогда
		//Нет.
	ИначеЕсли Текущийстатус = Перечисления.упСтатусыВходящейПретензии.УдовлетворенаЧастично Тогда	
		//Нет.
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыВходящейПретензии.Отклонена Тогда			
		// Нет.
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыВходящейПретензии.Отменена Тогда			
		// Нет.	
	КонецЕсли;		
	
	Возврат мсвСтатусы;
	
КонецФункции

Функция ПолучитьВозможныеНовыеСтатусыИсходящейПретензии(ТекущийСтатус)
	
	мсвСтатусы = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Или ТекущийСтатус = Перечисления.упСтатусыИсходящейПретензии.Новая Тогда
		мсвСтатусы.Добавить(Перечисления.упСтатусыИсходящейПретензии.Отправлена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыИсходящейПретензии.Отменена);	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыИсходящейПретензии.Отправлена Тогда	
		мсвСтатусы.Добавить(Перечисления.упСтатусыИсходящейПретензии.Отклонена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыИсходящейПретензии.Отменена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыИсходящейПретензии.Удовлетворена);
		мсвСтатусы.Добавить(Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично);	
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыИсходящейПретензии.Удовлетворена Тогда
		//Нет.
	ИначеЕсли Текущийстатус = Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично Тогда	
		//Нет.
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыИсходящейПретензии.Отклонена Тогда			
		// Нет.
	ИначеЕсли ТекущийСтатус = Перечисления.упСтатусыИсходящейПретензии.Отменена Тогда			
		// Нет.	
	КонецЕсли;		
	
	Возврат мсвСтатусы;
	
КонецФункции

// Проверка возможности изменения статуса документа. 
//
// Параметры:
//  Документ - ДокументСсылка - Ссылка на элемент.
//	ТекущийСтатус - ПеречислениеСсылка - Ссылка на элемент.
//	НовыйСтатус - ПеречислениеССылка - Ссылка на элемент.
//  ДополнительныеПараметры	- Структура - Параметры.
//  Отказ	- Булево - Фиксировать изменения.
//
Процедура ПроверитьВозможностьУстановкиСтатуса(Документ, ТекущийСтатус, НовыйСтатус, ДополнительныеПараметры, Отказ) Экспорт
	
	текТипДокумента = ТипЗнч(Документ);
	
	Если Ложь
		Или текТипДокумента = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") 
		Или текТипДокумента = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза")
		Или текТипДокумента = Тип("ДокументСсылка.упРейс")
		Или текТипДокумента = Тип("ДокументСсылка.упЗаявкаНаТС")
		Или текТипДокумента = Тип("ДокументСсылка.упЗапросУсловийПеревозки")
		Или текТипДокумента = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда
		
		ДополнительныеПараметры.Вставить("Ссылка", Документ);
		мсвДоступныеСтатусы =  ПолучитьВозможныеНовыеСтатусы(текТипДокумента, ТекущийСтатус, ДополнительныеПараметры);
		
		Если мсвДоступныеСтатусы.Найти(НовыйСтатус) = Неопределено Тогда
			
			 ЭтоОтменаПроведения = Ложь;
			 Если ОбщегоНазначения.ЗначениеСсылочногоТипа(НовыйСтатус) Тогда
				 МенеджерПеречисления 	= ОбщегоНазначения.МенеджерОбъектаПоСсылке(НовыйСтатус);
				 ЭтоОтменаПроведения 	= НовыйСтатус = МенеджерПеречисления.ПустаяСсылка();
			 КонецЕсли;
			 
			 ТекущийСтатусСтрока = ПредставлениеСтатуса(ТекущийСтатус, Документ, ДополнительныеПараметры);
			 
			 Если ЭтоОтменаПроведения Тогда
				 ОписаниеОшибки = Нстр("ru = 'Для документа %1 в статусе ""%2"" запрещена отмена проведения.'");
				 ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Документ, ТекущийСтатусСтрока); 
			 Иначе
				 НовыйСтатусСтрока = ПредставлениеСтатуса(НовыйСтатус, Документ, ДополнительныеПараметры);
				 ОписаниеОшибки = Нстр("ru = 'Документ %1 в статусе ""%2"" нельзя переводить в статус ""%3"".'");
				 ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Документ, ТекущийСтатусСтрока, НовыйСтатусСтрока);
			 КонецЕсли;
			 
			 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, Документ,,, Отказ);
			 ЗаписьЖурналаРегистрации(Нстр("ru = 'Ошибка установки статуса'"), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		Иначе	
			текРезультат 	= Истина;	
		КонецЕсли;
	ИначеЕсли текТипДокумента = Тип("ДокументСсылка.упРемонтИТОТС") Тогда		
		Если НовыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Запланирован") Тогда 	
			Если ДополнительныеПараметры.Свойство("ИспользоватьЗаявкуНаРемонтИТОТС") 
				И ДополнительныеПараметры.Свойство("ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт") Тогда 	
				
				Если ДополнительныеПараметры.ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт
					И НЕ ДополнительныеПараметры.ИспользоватьЗаявкуНаРемонтИТОТС Тогда
					Если ДополнительныеПараметры.Свойство("ВидРемонта")Тогда
						ВидРемонта = ДополнительныеПараметры.ВидРемонта;
					Иначе
						ВидРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ВидРемонта");
					КонецЕсли;
					ОписаниеОшибки = Нстр("ru = 'Для вида ремонта ""%1"" запрещено планировать выполнение ремонтных работ без предварительного оформления заявки на ремонт (ТО)'");
					ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, ВидРемонта);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, Документ,,, Отказ);
					ЗаписьЖурналаРегистрации(Нстр("ru = 'Ошибка установки статуса'"), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура изменения статуса документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - Ссылка на элемент.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//  ОписаниеОшибки	- Строка - Описание ошибки.
//  Отказ	- Булево - Фиксировать изменения.
//
Процедура УстановитьСтатусДокументу(ДокументСсылка, Статус, ОписаниеОшибки, Отказ) Экспорт
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить(ОбщегоНазначения.ИмяТаблицыПоСсылке(ДокументСсылка));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументСсылка);
	
	ПространствоБлокировкиРегистр = "РегистрСведений.%1";
	ПространствоБлокировкиРегистр = СтрШаблон(ПространствоБлокировкиРегистр, ПолучитьИмяРегистра(ДокументСсылка));
	
	ЭлементБлокировки = БлокировкаДанных.Добавить(ПространствоБлокировкиРегистр);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ДокументСсылка);
			
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, ДокументСсылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
		
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	ДокументОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);	
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
			ОписаниеОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ОшибкаУстановкиСтатуса", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
	КонецПопытки;
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе	
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

// Используется при проведении документа, устанавливает параметры связанные со статусом в свойства.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Ссылка на элемент.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Отказ	- Булево - Фиксировать изменения.
//
Процедура УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ) Экспорт
	
	ЗаписыватьСтатус = Ложь;
	НовыйСтатус = Неопределено;
	ТекущийСтатус = ПолучитьТекущийСтатус(Ссылка, ЗаписыватьСтатус);

	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		НовыйСтатус = ДополнительныеСвойства.Статус;
		ЗаписыватьСтатус = Истина;
	Иначе
		НовыйСтатус = ТекущийСтатус;
	КонецЕсли;
	
	Если Истина
		И ТекущийСтатус <> НовыйСтатус
		И НЕ ДополнительныеСвойства.Свойство("НеПроводитьПроверкуНаВозможныеСтатусы")
	Тогда
		ПроверитьВозможностьУстановкиСтатуса(Ссылка, ТекущийСтатус, НовыйСтатус, ДополнительныеСвойства, Отказ);	
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ТекущийСтатус", ТекущийСтатус);
	ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	ДополнительныеСвойства.Вставить("ЗаписыватьСтатус", ЗаписыватьСтатус);

КонецПроцедуры // УстановкаСтатуса()

// Используется при проведении документа, устанавливает параметры связанные со статусом в свойства.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Ссылка на элемент.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Отказ	- Булево - Фиксировать изменения.
//
Процедура УстановкаСтатусаРейса(ДополнительныеСвойства, Ссылка, Отказ) Экспорт

	Статус = Неопределено;
	ЗаписыватьСтатус = Ложь;
	СтатусРавенТекущему = Ложь;
	Событие = Нстр("ru = 'Ошибка при проведении рейса'");
	
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		Статус = ДополнительныеСвойства.Статус;
		ЗаписыватьСтатус = Истина;
		
		ТекущийСтатус = ПолучитьТекущийСтатус(Ссылка, ЗаписыватьСтатус);
		ДополнительныеСвойства.Вставить("ТекущийСтатус", ТекущийСтатус);
		
		Если ТекущийСтатус = Статус Тогда
			ЗаписыватьСтатус = Ложь;
			СтатусРавенТекущему = Истина;
		Иначе	
			Если НЕ ДополнительныеСвойства.Свойство("НеПроводитьПроверкуНаВозможныеСтатусы") Тогда
				ПроверитьВозможностьУстановкиСтатуса(Ссылка, ТекущийСтатус, Статус, ДополнительныеСвойства, Отказ);	
								
				Если Истина
					И Не Отказ 
					И Статус = Перечисления.упСтатусыРейса.Завершен 
				Тогда
					мсвСтатусыДляПроверки = Новый Массив;
					мсвСтатусыДляПроверки.Добавить(Перечисления.упСтатусыРейса.Выполнен);
					мсвСтатусыДляПроверки.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);
					
					ДатаВыполнен = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Ссылка, мсвСтатусыДляПроверки);
					
					Если ДополнительныеСвойства.Дата <= ДатаВыполнен Тогда
						ТекстОшибки = НСтр("ru = 'Дата завершения %1 должна быть позже даты выполнения или частичного выполнения рейса %2!'");
						ТекстОшибки = СтрШаблон(ТекстОшибки, ДополнительныеСвойства.Дата, ДатаВыполнен);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
					КонецЕсли;
				КонецЕсли;
			
				Если Отказ Тогда
					Возврат;
				КонецЕсли;

			КонецЕсли;	
		КонецЕсли;
		
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = ПолучитьТекстЗапросаСтатуса(Ссылка);
		Запрос.УстановитьПараметр("Документ", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			Статус = Выборка.Статус;
			ДополнительныеСвойства.Вставить("ВыполненЧастично", Выборка.ВыполненЧастично);
		Иначе	
			Статус = РассчитатьСтатусРейса(Ссылка).Статус;
			ЗаписыватьСтатус = Истина;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ТекущийСтатус", Статус);
				
		Если Статус = Перечисления.упСтатусыРейса.Отменен Тогда 
			ОписаниеОшибки = Нстр("ru = 'Рейс в статусе ""Отменен"", перепроведение запрещено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,,,,Отказ);
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,Ссылка, ОписаниеОшибки);    
		КонецЕсли;	
		
	КонецЕсли;
	
	Если Не Отказ Тогда
		Если Ложь
			Или Статус = Перечисления.упСтатусыРейса.КВыполнению 
			Или Статус = Перечисления.упСтатусыРейса.Отменен 
			Или Статус = Перечисления.упСтатусыРейса.Выполнен 
			Или Статус = Перечисления.упСтатусыРейса.Завершен Тогда
			
			Если Не ДополнительныеСвойства.Свойство("РассчитыватьСтатус") Тогда
				ДополнительныеСвойства.Вставить("РассчитыватьСтатус", Ложь);
			КонецЕсли;
			
			Если Статус = Перечисления.упСтатусыРейса.КВыполнению  Тогда
				
				ВедетсяУчетДокументов = ПолучитьФункциональнуюОпцию("упВедетсяУчетДокументовТранспортныхСредствВодителей");
				
				Если ВедетсяУчетДокументов Тогда
					упРейс.ПроверитьНаличиеНеобходимыхДокументовПоРейсу(Ссылка, Отказ);
				КонецЕсли;
				
				ИспользуютсяМультимодальныеПеревозки = ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки");
				
				Если Истина
					И Не Отказ
					И ИспользуютсяМультимодальныеПеревозки 
				Тогда
					Если Не Документы.упРейс.ПоследовательностьВыполненияЭтаповМультимодальнойПеревозкиСоблюдена(Ссылка, ТекстОшибки) Тогда
						ОписаниеОшибки = НСтр("ru = 'Документу %1 не может быть назначен статус ""К выполнению""!'") + Символы.ПС + ТекстОшибки;	
						ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, Ссылка);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,,,,Отказ);
						ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,Ссылка, ОписаниеОшибки);    
					КонецЕсли;
					
					Если Истина
						И Не Отказ
						И Не Документы.упРейс.ВсеПрерыванияГрузовогоПотокаОбработаны(Ссылка, ТекстОшибки) 
					Тогда
						ОписаниеОшибки = НСтр("ru = 'Документу %1 не может быть назначен статус ""К выполнению""!'") + Символы.ПС + ТекстОшибки;	
						ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, Ссылка);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,,,,Отказ);
						ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,Ссылка, ОписаниеОшибки); 
					КонецЕсли;
					
				КонецЕсли;
								
				Если Истина
					И Не Отказ
					И Не ДополнительныеСвойства.ПеревозкаТранспортнойКомпанией 
					И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПутевыхЛистов") 
					И упРейс.ТребуетсяОформлениеПутевогоЛиста(Ссылка)
					И Не упРейс.ЕстьПутевойЛист(Ссылка) 
				Тогда
					ОписаниеОшибки = НСтр("ru = 'Начать исполнение рейса(%1) без ввода документа ""Путевой лист"" нельзя!'");
					ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, Ссылка);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,,,,Отказ);
					ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,Ссылка, ОписаниеОшибки);    
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли Статус = Перечисления.упСтатусыРейса.КПланированиюТС И ЗаписыватьСтатус Тогда 	
			ДополнительныеСвойства.Вставить("РассчитыватьСтатус", Ложь);
		ИначеЕсли Статус = Перечисления.упСтатусыРейса.Выполняется И ЗаписыватьСтатус Тогда	
			ДополнительныеСвойства.Вставить("РассчитыватьСтатус", Ложь);
		Иначе
			ДополнительныеСвойства.Вставить("РассчитыватьСтатус", 	Не СтатусРавенТекущему);	
		КонецЕсли;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ДобавитьСтатус", Истина);
	ДополнительныеСвойства.Вставить("Статус", Статус);
	ДополнительныеСвойства.Вставить("ЗаписыватьСтатус", ЗаписыватьСтатус);

КонецПроцедуры // УстановкаСтатуса()

// Функция возвращает текст запроса для получения статуса документа.
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Строка - Результат выполнения, текст запроса.
//
Функция ПолучитьТекстЗапросаСтатуса(Ссылка) Экспорт

	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс") Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	тбСтатусы.Статус,
		|	тбСтатусы.ВыполненЧастично,
		|	тбСтатусы.Состояние,
		|	тбСтатусы.ОтложенаЧастьРабот
		|ИЗ
		|	РегистрСведений.упСтатусыРейсов.СрезПоследних(, Документ = &Документ) КАК тбСтатусы";
		
	Иначе	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	тбСтатусы.Статус
		|ИЗ
		|	РегистрСведений.%1.СрезПоследних(, Документ = &Документ) КАК тбСтатусы";
				
		ИмяРегистра = ПолучитьИмяРегистра(Ссылка);
	
		ТекстЗапроса = СтрШаблон(ТекстЗапроса, ИмяРегистра);
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции // ПолучитьТекстЗапросаСтатуса()

// Возвращает строку с имененме регистра под документу.
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Строка - Наименование регистра.
//
Функция ПолучитьИмяРегистра(Ссылка) Экспорт

	ТипДокумента = ТипЗнч(Ссылка);
	
	ИмяРегистра = "";	
	Если ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		ИмяРегистра = "упСтатусыЗаявокНаПеревозкуГруза";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		ИмяРегистра = "упСтатусыЗаданийНаПеревозкуГруза";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упРейс") Тогда	
		ИмяРегистра = "упСтатусыРейсов";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда		
		ИмяРегистра = "упСтатусыЗаявокНаТС";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упПутевойЛист") Тогда		
		ИмяРегистра = "упСтатусыПутевыхЛистов";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда		
		ИмяРегистра = "упСтатусыЗапросовУсловийПеревозок";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упРемонтИТОТС") Тогда		
		ИмяРегистра = "упСтатусыРемонтовИТОТС";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упВходящаяПретензия") Тогда
		ИмяРегистра = "упСтатусыВходящихПретензий";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упИсходящаяПретензия") Тогда
		ИмяРегистра = "упСтатусыИсходящихПретензий";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда		
		ИмяРегистра = "упСтатусыЗаявокНаРемонтИТОТС";		
	КонецЕсли;
	
	Возврат ИмяРегистра;

КонецФункции // ПолучитьИмяРегистра()

// Возвращает новый статус для по типу документа.
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ПеречислениеСсылка - Ссылка на элемент.
//
Функция ПолучитьСтатусНовый(Ссылка) Экспорт

	Результат = Неопределено;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		Результат = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		Результат = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс") Тогда	
		Результат = Перечисления.упСтатусыРейса.Новый;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда		
		Результат = Перечисления.упСтатусыЗаявкиНаТС.Создана;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда		
		Результат = Перечисления.упСтатусыПутевогоЛиста.Новый;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда		
		Результат = Перечисления.упСтатусыЗапросаУсловийПеревозки.Новый;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда		
		Результат = Перечисления.упСтатусыРемонтаИТОТС.Новый;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упВходящаяПретензия") Тогда
		Результат =  Перечисления.упСтатусыВходящейПретензии.Новая;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упИсходящаяПретензия") Тогда
		Результат =  Перечисления.упСтатусыИсходящейПретензии.Новая;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда		
		Результат =  Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПолучитьСтатусНовый()

// Возвращает статус отменено для по типу документа.
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ПеречислениеСсылка - Ссылка на элемент.
//
Функция ПолучитьСтатусОтменено(Ссылка) Экспорт

	Результат = Неопределено;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		Результат = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		Результат = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс") Тогда	
		Результат = Перечисления.упСтатусыРейса.Отменен;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда		
		Результат = Перечисления.упСтатусыЗаявкиНаТС.Отклонена;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда		
		Результат = Перечисления.упСтатусыПутевогоЛиста.Отменен;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда		
		Результат = Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда		
		Результат = Перечисления.упСтатусыРемонтаИТОТС.Отменен;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упВходящаяПретензия") Тогда
		Результат = Перечисления.упСтатусыВходящейПретензии.Отменена;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упИсходящаяПретензия") Тогда
		Результат = Перечисления.упСтатусыИсходящейПретензии.Отменена;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда		
		Результат =  Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПолучитьСтатусОтменено()

// Возвращает текуший статус переданного документа.
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	ЗаписыватьСтатус - Булево - Выполнить запись.
//
// Возвращаемое значение:
//   ПеречислениеСсылка - Ссылка на элемент.
//
Функция ПолучитьТекущийСтатус(Ссылка, ЗаписыватьСтатус = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаСтатуса(Ссылка);
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Статус = Выборка.Статус;
	Иначе	
		Статус = ПолучитьСтатусНовый(Ссылка);
		ЗаписыватьСтатус = Истина;
	КонецЕсли;

	Возврат Статус;
	
КонецФункции // ПолучитьСтатус()

// Возвращает структуру с значением текущего статус документа "пРейс".
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПолучитьТекущийСтатусРейса(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаСтатуса(Ссылка);
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	ВыполненЧастично = Ложь;
	ОтложенаЧастьРабот = Ложь;
	Состояние = Неопределено;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Статус = Выборка.Статус;
		ВыполненЧастично = Выборка.ВыполненЧастично;
		ОтложенаЧастьРабот = Выборка.ОтложенаЧастьРабот;
		Состояние = Выборка.Состояние;
	Иначе	
		Статус = ПолучитьСтатусНовый(Ссылка);
	КонецЕсли; 

	сткВозврата = Новый Структура("Статус, ВыполненЧастично, ОтложенаЧастьРабот, Состояние", Статус, ВыполненЧастично, ОтложенаЧастьРабот, Состояние);
	
	Возврат сткВозврата;
	
КонецФункции // ПолучитьСтатус()

// Возвращает таблицу рейсов со статусами.
//
// Параметры:
//  мсвРейсы - Массив - массив рейсов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица рейсов со статусами.
//
Функция ПолучитьТекущиеСтатусыРейсов(мсвРейсы) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(упСтатусыРейсовСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый)) КАК Статус,
	               |	ЕСТЬNULL(упСтатусыРейсовСрезПоследних.ВыполненЧастично, ЛОЖЬ) КАК ВыполненЧастично,
	               |	упРейс.Ссылка КАК Рейс
	               |ИЗ
	               |	Документ.упРейс КАК упРейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, Документ В (&мсвРейсы)) КАК упСтатусыРейсовСрезПоследних
	               |		ПО (упСтатусыРейсовСрезПоследних.Документ = упРейс.Ссылка)
	               |ГДЕ
	               |	упРейс.Ссылка В(&мсвРейсы)";
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	тбзСтатусы = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзСтатусы;
	
КонецФункции // ПолучитьСтатус()

// Функция - проверяет возможность изменения статуса документа.
//
// Параметры:
//	Рейс - ДокументСсылка - Ссылка на элемент.
//	НовыйСтатус - ПеречислениеСсылка - Ссылка на элемент.
//	ОписаниеОшибки - Строка - Описание ошибки.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЕстьВозможностьУстановитьСтатусРейса(Рейс, НовыйСтатус, ОписаниеОшибки) Экспорт
	
	Результат = Ложь;
	
	сткСтатусРейса = ПолучитьТекущийСтатусРейса(Рейс);
	СтатусРейсаТекущий = сткСтатусРейса.Статус;
	ОтложенаЧастьРабот = сткСтатусРейса.ОтложенаЧастьРабот;
	Если НовыйСтатус = Перечисления.упСтатусыРейса.Завершен Тогда
		
		Результат = Ложь
					Или СтатусРейсаТекущий = Перечисления.упСтатусыРейса.Выполнен 
					Или (Истина
					     И Не ОтложенаЧастьРабот
						И СтатусРейсаТекущий = Перечисления.упСтатусыРейса.ВыполненЧастично)
					Или (Истина
						И ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") 
						И СтатусРейсаТекущий = Перечисления.упСтатусыРейса.КВыполнению);
		
		Если НЕ Результат Тогда
			Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
				ОписаниеОшибки = НСтр("ru = 'Переводить в статус ""Завершен"" можно только из статуса ""К выполнению""'");
			Иначе
				Если ОтложенаЧастьРабот Тогда
					ОписаниеОшибки = НСтр("ru = 'Переводить в статус ""Завершен"" можно только рейс без отложенных работ'");	
				Иначе	
					ОписаниеОшибки = НСтр("ru = 'Переводить в статус ""Завершен"" можно только из статуса ""Выполнен"" или ""Выполнен частично""'");	
				КонецЕсли;
				
			КонецЕсли; 
		КонецЕсли;
		
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Проверяет наличие отличных от заданного документа,
//  регистраторов изменявших статус.
//
// Параметры:
//  Ссылка			 - ДокументСсылка	 - ПроверяемыйДокумент.
//  Отказ				 - Булево			 - Фиксировать изменения.
//  ВыводитьСообщения	 - Булево			 - Истина, выводить сообщение.
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция ЕстьДругиеРегистраторыИзменявшиеСтатус(Ссылка, Отказ, ВыводитьСообщения = Истина) Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	тбСтатусы.Регистратор
	|ИЗ
	|	РегистрСведений.%1 КАК тбСтатусы
	|ГДЕ
	|	тбСтатусы.Документ = &Документ
	|	И НЕ тбСтатусы.Регистратор = &Документ";
	
		
	ИмяРегистра = ПолучитьИмяРегистра(Ссылка);
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ИмяРегистра);
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Отказ = НЕ Запрос.Выполнить().Пустой();
	
	Если Отказ И ВыводитьСообщения Тогда
		ИмяОбъекта = "РегистрСведений.%1";
		ИмяОбъекта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ИмяОбъекта, ИмяРегистра);
		Синоним = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяОбъекта).СоздатьНаборЗаписей().Метаданные().Синоним;
		
		ТекстСообщения = Нстр("ru = 'Сначала необходимо отменить проведение других документов изменяющих статус(см. Движения документа -> %1)'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Синоним);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);				
	КонецЕсли;
	
	Возврат Отказ;
				
КонецФункции

// Функция устанавливает новый статус документам.
//
// Параметры:
//  мсвДокументов - Массив - Массив ссылок.
//  Статус - ПеречислениеСсылка - Ссылка на элемент.
// 
// Возвращаемое значение:
//  Массив - ССылки на документы.
//
Функция УстановитьСтатусДокументам(мсвДокументов, Статус) Экспорт
	
	мсвУдачных = Новый Массив;
	
	Для Каждого текДокумент Из мсвДокументов Цикл 
		Отказ = Ложь;
		ОписаниеОшибки = "";
		УстановитьСтатусДокументу(текДокумент, Статус, ОписаниеОшибки, Отказ);
		
		Если НЕ Отказ Тогда
			мсвУдачных.Добавить(текДокумент);
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат мсвУдачных;
	
КонецФункции

// Функция - проверяет возможность изменения маршута.
//
// Параметры:
//  Рейс	 - ДокументСсылка.упРейс	 - рейс.
//  Период - Дата				 - период
//  Статус - ПеречислениеСсылка	 - Ссылка на элемент.
// 
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ДокументРейсМожетИзменятьМаршрут(Рейс, Период, Статус) Экспорт
	
	текРезультат = Истина;
		
	Если упРаботаСоСтатусамиКлиентСервер.РейсПереданКВыполнению(Статус) Тогда
		Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению") Тогда
			текРезультат = НЕ упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(Рейс, Период);
			Если НЕ текРезультат Тогда
				
			КонецЕсли;
		Иначе	
			текРезультат = Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат текРезультат;
	
КонецФункции

Функция СостояниеОперацииСКонтейнером(Статус) Экспорт
	
	Результат = Нстр("ru = 'Новый'");
	
	Если Статус = Перечисления.упСтатусыРейса.КПланированиюТС Тогда	
		Результат = Нстр("ru = 'К планированию'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС Тогда		
		Результат = Нстр("ru = 'К СформированыЗаявкиНаТС'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.НазначеноТС Тогда		
		Результат = Нстр("ru = 'Назначен контейнер'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда		
		Результат = Нстр("ru = 'К выполнению'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Выполняется Тогда		
		Результат = Нстр("ru = 'Контейнер сформирован'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Выполнен Тогда		
		Результат = Нстр("ru = 'Контейнер расформирован'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.ВыполненЧастично Тогда		
		Результат = Нстр("ru = 'Контейнер расформирован частично'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Отменен Тогда		
		Результат = Нстр("ru = 'Отменен'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Завершен Тогда		
		Результат = Нстр("ru = 'Завершен'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЗаголовокКнопкиСтатусКонтейнернойПеревозки(Статус)

	Если Статус = Перечисления.упСтатусыРейса.КПланированиюТС Тогда	
		Результат = Нстр("ru = 'К планированию'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.НазначеноТС Тогда		
		Результат = Нстр("ru = 'Назначить контейнер'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда		
		Результат = Нстр("ru = 'К выполнению'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Выполняется Тогда		
		Результат = Нстр("ru = 'Сформировать контейнер'");
	ИначеЕсли Статус = Перечисления.упСтатусыРейса.Выполнен Тогда		
		Результат = Нстр("ru = 'Расформировать контейнер'");
	Иначе
		Результат = Строка(Статус);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПредставлениеСтатуса(Статус, Документ, ДополнительныеПараметры)
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.упРейс")  Тогда
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЭтоОперацияСКонтейнером", Ложь) Тогда
			Результат = СостояниеОперацииСКонтейнером(Статус);
		Иначе	
			Результат = Строка(Статус);
		КонецЕсли;
	Иначе	
		Результат = Строка(Статус);	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти