#Область ПрограммныйИнтерфейс

// Функция проверяет есть ли задолженность по предоплате
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура ПроконтролироватьРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	// Если статус устанавливается по кнопке из документа или по событию
	Если ДополнительныеСвойства.ВестиУчетРасчетовСЗаказчиками
		И ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению 
		И НЕ ДополнительныеСвойства.ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению Тогда
		
		
		// Проконтролировать остаток предоплаты по заявке
		Если ДополнительныеСвойства.КонтролироватьПредоплату Тогда
			тбзЗадолженностьПоПредоплате = ЗадолженностьПоПредоплатеПоЗаявке(ДополнительныеСвойства.АналитическийРазрезПоПартнерам, Ссылка);
			мсвЗадолженность = Новый Массив;
			Для каждого Строка Из тбзЗадолженностьПоПредоплате Цикл
				мсвЗадолженность.Добавить(СтрШаблон("%1%2", Строка.Остаток, Строка.Валюта));
			КонецЦикла;
			СтрокаЗадолженность = СтрСоединить(мсвЗадолженность, "; ");
			
			Если мсвЗадолженность.Количество() > 0 Тогда
				ТекстСообщения = Нстр("ru = 'Нельзя переводить %1 в статус ""К выполнению"", так как предоплата внесена не полностью(Остаток:%2)'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Ссылка,СтрокаЗадолженность);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
		
		// Проконтролировать максимальную задолженность по соглашению
		Если ЗначениеЗаполнено(Реквизиты.Соглашение) Тогда
			сткСоглашение = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.Соглашение, "КонтролироватьДебиторскуюЗадолженность, МаксимальнаяЗадолженность, ГруппаТарифов");
			ВалютаСоглашения			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(сткСоглашение.ГруппаТарифов, "Валюта");
			МаксимальнаяЗадолженность	= сткСоглашение.МаксимальнаяЗадолженность;
			Если сткСоглашение.КонтролироватьДебиторскуюЗадолженность Тогда
				ЗадолженностьПоСоглашению	= ЗадолженностьПоСоглашению(ДополнительныеСвойства.АналитическийРазрезПоПартнерам, ВалютаСоглашения, Реквизиты.Дата, ДополнительныеСвойства.ВедетсяВалютныйУчет);	
				Если ЗадолженностьПоСоглашению > МаксимальнаяЗадолженность Тогда
					Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
						ВалютаПредставление = Нстр("ru = '; Валюта соглашения:%1'");
						ВалютаПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ВалютаПредставление, ВалютаСоглашения);
					Иначе	
						ВалютаПредставление = "";
					КонецЕсли;
					ТекстСообщения = Нстр("ru = 'Нельзя переводить %1 в статус ""К выполнению"", так как задолженность по соглашению превышает допустимую.(Задолженность:%2; Максимальная задолженность:%3%4)'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Ссылка,ЗадолженностьПоСоглашению, МаксимальнаяЗадолженность, ВалютаПредставление);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Функция проверяет есть ли задолженность по предоплате
//
// Параметры:
//  Ссылка - ДокументСсылка	 - Ссылка на элемент.
//  Отказ	 - Булево			 - Применить изменения.
//
Процедура ПроконтролироватьОстатокПредоплатыПоРейсу(Ссылка, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	// Если статус устанавливается по кнопке из документа или по событию
	тбзЗадолженностьПоПредоплате = ЗадолженностьПоПредоплатеПоРейсу(Ссылка);
	
	мсвЗадолженность = Новый Массив;
	Для каждого Строка Из тбзЗадолженностьПоПредоплате Цикл
		мсвЗадолженность.Добавить(НСтр(СтрШаблон("ru = 'Объект расчетов: %1, задолженность: %2, %3'", Строка.ОбъектРасчетов, Строка.КОплатеДоНачалаВыполненияРейсаОстаток, Строка.Валюта)));
	КонецЦикла;
	
	СтрокаЗадолженность = СтрСоединить(мсвЗадолженность, Символы.ПС);
			
	Если мсвЗадолженность.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нельзя переводить %1 в статус ""К выполнению"", так как предоплата внесена не полностью: %2%3)'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Ссылка, Символы.ПС, СтрокаЗадолженность);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;

КонецПроцедуры

// Функция возвращает задолженность по аналитическому разрезу по заявке.
//
// Параметры:
//	АналитическийРазрезПоПартнерам 	- СправочникСсылка.упАналитическиеРазрезыПоПартнерам - аналитический разрез.
//	Заявка 							- ДокументСсылка.упЗаявкиНаПеревозкуГрузов - заявка.
//
// Возвращаемое значение:
//	ТаблицаЗначений
//			* Валюта 	- СправочникСсылка.Валюты 	- валюта задолженности.
//			* Остаток	- Число						- задолженность.
//
Функция ЗадолженностьПоПредоплатеПоЗаявке(АналитическийРазрезПоПартнерам, Заявка)
	
	тбзРезультат = Неопределено;	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаРаботыСЗаявкойОстаток КАК Остаток,
	               |	упРасчетыСЗаказчикамиОстатки.Валюта
	               |ИЗ
	               |	РегистрНакопления.упРасчетыСЗаказчиками.Остатки(
	               |			,
	               |			АналитическийРазрезПоПартнерам = &АналитическийРазрезПоПартнерам
	               |				И ОбъектРасчетов = &ОбъектРасчетов) КАК упРасчетыСЗаказчикамиОстатки";
	Запрос.УстановитьПараметр("АналитическийРазрезПоПартнерам", АналитическийРазрезПоПартнерам);
	Запрос.УстановитьПараметр("ОбъектРасчетов", 				Заявка);
	тбзРезультат = Запрос.Выполнить().Выгрузить();

	Возврат тбзРезультат;

КонецФункции	

// Функция возвращает задолженность по аналитическому разрезу по заявке.
//
// Параметры:
//  АналитическийРазрезПоПартнерам	 - СправочникСсылка.упАналитическиеРазрезыПоПартнерам	 - аналитический разрез.
//  ВалютаСоглашения				 - СправочникСсылка.Валюты								 - валюта.
//  Дата					 		 - Дата													 - дата, для пересчета из упр. валюты в валюту соглашения.
// 
// Возвращаемое значение:
//  Число - задолженность.
//
Функция ЗадолженностьПоСоглашению(АналитическийРазрезПоПартнерам, ВалютаСоглашения, Дата, ВедетсяВалютныйУчет)
	
	Результат = 0;	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упРасчетыСЗаказчикамиОстатки.СуммаОстаток,
	|	упРасчетыСЗаказчикамиОстатки.Валюта
	|ИЗ
	|	РегистрНакопления.упРасчетыСЗаказчиками.Остатки(, АналитическийРазрезПоПартнерам = &АналитическийРазрезПоПартнерам) КАК упРасчетыСЗаказчикамиОстатки";
	Запрос.УстановитьПараметр("АналитическийРазрезПоПартнерам", АналитическийРазрезПоПартнерам);
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СуммаОстаток = Выборка.СуммаОстаток;
		Если ВедетсяВалютныйУчет И Не Выборка.Валюта = ВалютаСоглашения Тогда
			КурсВалютыСоглашения   	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаСоглашения, Дата);
			КурсВалютыОстатка	 	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(Выборка.Валюта, Дата);			
			СуммаОстаток	= РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СуммаОстаток,  Выборка.Валюта, ВалютаСоглашения, КурсВалютыОстатка.Курс, КурсВалютыСоглашения.Курс, КурсВалютыОстатка.Кратность, КурсВалютыСоглашения.Кратность);					
		КонецЕсли;
		Результат = Результат + СуммаОстаток;	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции	


// Функция возвращает задолженность по заявкам.
//
// Параметры:
//	Рейс - ДокументСсылка.упРейс - рейс.
//
// Возвращаемое значение:
//	ТаблицаЗначений
//			* Валюта - СправочникСсылка.Валюты - валюта задолженности.
//			* КОплатеДоНачалаВыполненияРейсаОстаток - Число - задолженность.
//
Функция ЗадолженностьПоПредоплатеПоРейсу(Рейс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	упАналитическиеРазрезыПоПартнерам.Ссылка КАК АналитическийРазрезПоПартнерам
	|ПОМЕСТИТЬ втАналитическиеРазрезыПоРейсу
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезыПоПартнерам КАК упАналитическиеРазрезыПоПартнерам
	|				ПО упЗаявкаНаПеревозкуГруза.Заказчик = упАналитическиеРазрезыПоПартнерам.Партнер
	|					И упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика = упАналитическиеРазрезыПоПартнерам.Контрагент
	|					И упЗаявкаНаПеревозкуГруза.Организация = упАналитическиеРазрезыПоПартнерам.Организация
	|			ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГруза.Ссылка
	|		ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|ГДЕ
	|	упРейсЗадания.Ссылка = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРасчетыСЗаказчикамиОстатки.ОбъектРасчетов,
	|	упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаВыполненияРейсаОстаток КАК КОплатеДоНачалаВыполненияРейсаОстаток,
	|	упРасчетыСЗаказчикамиОстатки.Валюта
	|ИЗ
	|	РегистрНакопления.упРасчетыСЗаказчиками.Остатки(
	|			,
	|			(АналитическийРазрезПоПартнерам, ОбъектРасчетов) В
	|				(ВЫБРАТЬ
	|					втАналитическиеРазрезыПоРейсу.АналитическийРазрезПоПартнерам,
	|					втАналитическиеРазрезыПоРейсу.ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втАналитическиеРазрезыПоРейсу КАК втАналитическиеРазрезыПоРейсу)) КАК упРасчетыСЗаказчикамиОстатки";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	тбзРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзРезультат;
	
КонецФункции	

#Область ФормированиеДвижений

// Процедура формирует записи по регистру "Расчеты с заказчиками" для документа фактические доходы.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Или НЕ ДополнительныеСвойства.ВестиУчетРасчетовСЗаказчиками Или Реквизиты.Аннулирован Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРасчетыСЗаказчиками	= Движения.упРасчетыСЗаказчиками;
	ДвиженияРасчетыСЗаказчиками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасшифровкаПлатежа.АналитическийРазрезПоПартнерам,
	|	РасшифровкаПлатежа.ОбъектРасчетов,
	|	РасшифровкаПлатежа.Валюта,
	|	РасшифровкаПлатежа.Сумма,
	|	РасшифровкаПлатежа.СтатьяДоходов,
	|	РасшифровкаПлатежа.СуммаВалюта
	|ПОМЕСТИТЬ втРасшифровкаПлатежа
	|ИЗ
	|	&тбзРасшифровкаПлатежа КАК РасшифровкаПлатежа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасшифровкаПлатежа.АналитическийРазрезПоПартнерам,
	|	втРасшифровкаПлатежа.ОбъектРасчетов,
	|	втРасшифровкаПлатежа.Валюта,
	|	втРасшифровкаПлатежа.СтатьяДоходов,
	|	СУММА(втРасшифровкаПлатежа.Сумма) КАК Сумма,
	|	СУММА(втРасшифровкаПлатежа.СуммаВалюта) КАК СуммаВалюта
	|ПОМЕСТИТЬ втРасшифровка
	|ИЗ
	|	втРасшифровкаПлатежа КАК втРасшифровкаПлатежа
	|
	|СГРУППИРОВАТЬ ПО
	|	втРасшифровкаПлатежа.СтатьяДоходов,
	|	втРасшифровкаПлатежа.ОбъектРасчетов,
	|	втРасшифровкаПлатежа.Валюта,
	|	втРасшифровкаПлатежа.АналитическийРазрезПоПартнерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасшифровка.АналитическийРазрезПоПартнерам,
	|	втРасшифровка.ОбъектРасчетов,
	|	втРасшифровка.СтатьяДоходов,
	|	втРасшифровка.Валюта,
	|	втРасшифровка.Сумма,
	|	втРасшифровка.СуммаВалюта,
	|	ЕСТЬNULL(упРасчетыСЗаказчикамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаРаботыСЗаявкойОстаток, 0) КАК КОплатеДоНачалаРаботыСЗаявкойОстаток,
	|	ЕСТЬNULL(упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаВыполненияРейсаОстаток, 0) КАК КОплатеДоНачалаВыполненияРейсаОстаток
	|ИЗ
	|	РегистрНакопления.упРасчетыСЗаказчиками.Остатки(
	|			,
	|			(АналитическийРазрезПоПартнерам, ОбъектРасчетов, СтатьяДоходов, Валюта) В
	|				(ВЫБРАТЬ
	|					втРасшифровкаПлатежа.АналитическийРазрезПоПартнерам,
	|					втРасшифровкаПлатежа.ОбъектРасчетов,
	|					втРасшифровкаПлатежа.СтатьяДоходов,
	|					втРасшифровкаПлатежа.Валюта
	|				ИЗ
	|					втРасшифровкаПлатежа КАК втРасшифровкаПлатежа)) КАК упРасчетыСЗаказчикамиОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРасшифровка КАК втРасшифровка
	|		ПО упРасчетыСЗаказчикамиОстатки.АналитическийРазрезПоПартнерам = втРасшифровка.АналитическийРазрезПоПартнерам
	|			И упРасчетыСЗаказчикамиОстатки.ОбъектРасчетов = втРасшифровка.ОбъектРасчетов
	|			И упРасчетыСЗаказчикамиОстатки.СтатьяДоходов = втРасшифровка.СтатьяДоходов
	|			И упРасчетыСЗаказчикамиОстатки.Валюта = втРасшифровка.Валюта";
	
	Если ТипЗнч(Реквизиты.РасшифровкаПлатежа) = Тип("ТаблицаЗначений")
			И НЕ ДополнительныеСвойства.ВедетсяВалютныйУчет 
			И Реквизиты.РасшифровкаПлатежа.Колонки.Найти("СуммаВалюта") = Неопределено Тогда
		Реквизиты.РасшифровкаПлатежа.Колонки.Добавить("СуммаВалюта", Новый ОписаниеТипов("Число",
																	 Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Любой)));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("тбзРасшифровкаПлатежа", Реквизиты.РасшифровкаПлатежа);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДвижениеРасчетыСЗаказчиками = ДвиженияРасчетыСЗаказчиками.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеРасчетыСЗаказчиками, Выборка);
		ДвижениеРасчетыСЗаказчиками.ВидДвижения				= ВидДвиженияНакопления.Расход;
		ДвижениеРасчетыСЗаказчиками.Период               	= Реквизиты.Дата;
		ДвижениеРасчетыСЗаказчиками.Регистратор          	= Ссылка;
		
		Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
			СуммаОстаток	= Выборка.СуммаВалюта;
		Иначе
			СуммаОстаток	= Выборка.Сумма;
		КонецЕсли;
		ДвижениеРасчетыСЗаказчиками.Сумма = СуммаОстаток;
		
		//Если СуммаОстаток > Выборка.СуммаОстаток Тогда
		//	ТекстСообщения = Нстр("ru = 'Сумма оплаты %1 (Аналитический разрез:%2;Объект расчета:%3;Статья доходов:%4;Валюта:%5) больше чем указано в фактических доходах:%6.'");
		//	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Сумма, Выборка.АналитическийРазрезПоПартнерам, Выборка.ОбъектРасчетов, Выборка.СтатьяДоходов, Выборка.Валюта, Выборка.СуммаОстаток);
		//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		//	Событие = Нстр("ru = 'Ошибка при проведении счета на оплату заказчику'");
		//	ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,,Ссылка, ТекстСообщения);    
		//КонецЕсли;
		
		СуммаПоЗаявке 	= 0;
		Если Выборка.КОплатеДоНачалаРаботыСЗаявкойОстаток > СуммаОстаток Тогда
			СуммаПоЗаявке = СуммаОстаток;
		Иначе	
			СуммаПоЗаявке = Выборка.КОплатеДоНачалаРаботыСЗаявкойОстаток;
		КонецЕсли;
		ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаРаботыСЗаявкой = СуммаПоЗаявке;
		СуммаОстаток = СуммаОстаток - СуммаПоЗаявке;
		
		СуммаПоРейсу = 0;
		Если Выборка.КОплатеДоНачалаВыполненияРейсаОстаток > СуммаОстаток Тогда
			СуммаПоРейсу = СуммаОстаток;
		Иначе	
			СуммаПоРейсу = Выборка.КОплатеДоНачалаВыполненияРейсаОстаток;
		КонецЕсли;
		ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаВыполненияРейса = СуммаПоРейсу;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
