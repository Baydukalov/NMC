#Область ПрограммныйИнтерфейс

// Процедура формирует движения по регистру "упСтатусыИсходящихПретензий".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объекта.
//  Отказ					 - Булево			 - флаг ошибки.
//
Процедура СформироватьДвиженияСтатусИсходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСтатуса = Движения.упСтатусыИсходящихПретензий;
	
	Статус				    = ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус	= ДополнительныеСвойства.ЗаписыватьСтатус;
	Если ЗаписыватьСтатус Тогда
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
		
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = ТекущаяДатаСеанса();
		ДвижениеСтатуса.Документ    = Ссылка;
		ДвижениеСтатуса.Статус      = Статус;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упПретензииПоЗаявкамНаПеревозкуГруза".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объекта.
//  Отказ					 - Булево			 - флаг ошибки.
//
Процедура СформироватьДвиженияПоЗаявкеИсходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПоЗаявкам = Движения.упПретензииПоЗаявкамНаПеревозкуГруза;
	ДвиженияПоЗаявкам.Записывать = Истина;
	
	Заявка	= Реквизиты.ЗаявкаНаПеревозкуГруза;
	Если ЗначениеЗаполнено(Заявка)  Тогда
		
		Движение = ДвиженияПоЗаявкам.Добавить();
		Движение.Период                             = ТекущаяДатаСеанса();
		Движение.ЗаявкаНаПеревозкуГруза   = Заявка;
		Движение.Рейс                                 = Реквизиты.Рейс;
		Движение.Претензия                         = Ссылка;
		Движение.ПричинаПретензии             = Реквизиты.ПричинаПретензии; 
		Движение.Партнер                            = Реквизиты.Партнер;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упПретензииПоРейсам".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объекта.
//  Отказ					 - Булево			 - флаг ошибки.
//
Процедура СформироватьДвиженияПоРейсуИсходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПоРейсам = Движения.упПретензииПоРейсам;
	ДвиженияПоРейсам.Записывать = Истина;
	
	Рейс	= Реквизиты.Рейс;
	Если ЗначениеЗаполнено(Рейс)  Тогда
		
		Движение = ДвиженияПоРейсам.Добавить();
		Движение.Период                             = ТекущаяДатаСеанса();
		Движение.Рейс                                 = Рейс;
		Движение.Претензия                         = Ссылка;
		Движение.ПричинаПретензии             = Реквизиты.ПричинаПретензии; 
		Движение.Партнер                            = Реквизиты.Партнер;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упДоходы".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты				 - Структура		 - Реквизиты объекта.
//  Отказ					 - Булево			 - флаг ошибки.
//
Процедура СформироватьДвиженияДоходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	ПропуститьЗаписьДвиженийДохода = Ложь;
	
	Если Истина
		И ДополнительныеСвойства.ТекущийСтатус <> Перечисления.упСтатусыИсходящейПретензии.Удовлетворена
		И ДополнительныеСвойства.ТекущийСтатус <> Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично 
		И ДополнительныеСвойства.Статус <> Перечисления.упСтатусыИсходящейПретензии.Удовлетворена 
		И ДополнительныеСвойства.Статус <> Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично 
	Тогда
		
		ПропуститьЗаписьДвиженийДохода = Истина;	
		
	КонецЕсли; 
	
	Если Отказ ИЛИ ПропуститьЗаписьДвиженийДохода Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияДоходы	= Движения.упДоходы;
	ДвиженияДоходы.Записывать = Истина;
	
	ДвиженияДоходыРасходыПредприятия = Движения.Найти("упДоходыРасходыПредприятия");
	ДвиженияДоходыРасходыПредприятия.Записывать = Истина;	
	
	Статус	= ДополнительныеСвойства.Статус;
	Если Ложь
		Или Статус = Перечисления.упСтатусыИсходящейПретензии.Удовлетворена
		Или Статус = Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично 
	Тогда 
		
		тбзДоходы 			= ДополнительныеСвойства.ПлановыеДоходы;
		ВедетсяВалютныйУчет = ДополнительныеСвойства.ВедетсяВалютныйУчет;
		
		Если ВедетсяВалютныйУчет Тогда
			сткСуммыШаблон	= Новый Структура("Сумма, СуммаБезНДС, СуммаВалюта, СуммаБезНДСВалюта",0,0,0,0);
		Иначе
			сткСуммыШаблон	= Новый Структура("Сумма, СуммаБезНДС",0,0);
		КонецЕсли;
		
		тбзДоходы.Колонки.Добавить("сткСуммы");
		
		Для каждого СтрокаДоход Из тбзДоходы Цикл
			сткСуммы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткСуммыШаблон);
			ЗаполнитьЗначенияСвойств(сткСуммы, СтрокаДоход);
			СтрокаДоход.сткСуммы = сткСуммы;
		КонецЦикла;
	
		ТекстОшибки = "";
		
		тбзРаспределенныеДоходы = РаспределениеДоходовПоРейсу(Реквизиты.Рейс, Реквизиты.ЗаявкаНаПеревозкуГруза, тбзДоходы, сткСуммыШаблон, ТекстОшибки, Отказ);		
		
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось распределить доходы. %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Событие = Нстр("ru = 'Ошибка при распределении доходов'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстОшибки);    
		КонецЕсли;

		тбзДоходыПредприятия = упРаспределениеРасходов.ТаблицаДоходыРасходыПредприятия();
		
		РеквизитыЗаявок = Новый Соответствие;
		
		Для каждого СтрокаДохода Из тбзРаспределенныеДоходы Цикл
			ДвижениеДоходы = ДвиженияДоходы.Добавить();
			ДвижениеДоходы.Период      = Реквизиты.Дата;
			ДвижениеДоходы.Регистратор = Ссылка;
			ДвижениеДоходы.ЗаявкаНаПеревозкуГруза = СтрокаДохода.ЗаявкаНаПеревозкуГруза;
			ДвижениеДоходы.СтатьяДоходов          = СтрокаДохода.СтатьяДоходов;
			ДвижениеДоходы.ХозяйственнаяОперация = Перечисления.упХозяйственныеОперации.ОтражениеДоходовПоПретензиям;
		
			сткСуммы = СтрокаДохода.СткСуммы;
			
			Если ВедетсяВалютныйУчет Тогда
				ДвижениеДоходы.Валюта		 		= СтрокаДохода.Валюта;
				ДвижениеДоходы.СуммаПланСНДС			= 0;
				ДвижениеДоходы.СуммаПланБезНДС		= 0;
				ДвижениеДоходы.СуммаПланВалютаСНДС		= 0;
				ДвижениеДоходы.СуммаПланВалютаБезНДС	= 0;
				ДвижениеДоходы.СуммаФактСНДС			= сткСуммы.Сумма;
				ДвижениеДоходы.СуммаФактБезНДС		= сткСуммы.СуммаБезНДС;
				ДвижениеДоходы.СуммаФактВалютаСНДС		= сткСуммы.СуммаВалюта;
				ДвижениеДоходы.СуммаФактВалютаБезНДС	= сткСуммы.СуммаБезНДСВалюта;
			Иначе
				ДвижениеДоходы.Валюта		 		= Справочники.Валюты.ПустаяСсылка();
				ДвижениеДоходы.СуммаПланСНДС			= 0;
				ДвижениеДоходы.СуммаПланБезНДС		= 0;	
				ДвижениеДоходы.СуммаПланВалютаСНДС		= 0;
				ДвижениеДоходы.СуммаПланВалютаБезНДС	= 0;
				ДвижениеДоходы.СуммаФактСНДС			= сткСуммы.Сумма;
				ДвижениеДоходы.СуммаФактБезНДС		= сткСуммы.СуммаБезНДС;
				ДвижениеДоходы.СуммаФактВалютаСНДС		= 0;
				ДвижениеДоходы.СуммаФактВалютаБезНДС	= 0;
			КонецЕсли;	
			
			
			// Заполняется строка доходов предприятия.
			НоваяСтрока  = тбзДоходыПредприятия.Добавить();
			НоваяСтрока.Организация = Реквизиты.Организация;
			НоваяСтрока.ХозяйственнаяОперация = Перечисления.упХозяйственныеОперации.ОтражениеДоходовПоПретензиям;
			НоваяСтрока.СтатьяДоходовРасходов = СтрокаДохода.СтатьяДоходов;
			
			ЗаявкаНаПеревозкуГруза  = СтрокаДохода.ЗаявкаНаПеревозкуГруза;
			
			Если ЗначениеЗаполнено(Реквизиты.Подразделение) Тогда
				НоваяСтрока.Подразделение = Реквизиты.Подразделение;
			Иначе
				сткРеквизиты = РеквизитыЗаявок.Получить(ЗаявкаНаПеревозкуГруза);
				Если сткРеквизиты <> Неопределено Тогда
					НоваяСтрока.Подразделение = сткРеквизиты.Подразделение;
				ИначеЕсли ЗначениеЗаполнено(СтрокаДохода.ЗаявкаНаПеревозкуГруза) Тогда
					сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаДохода.ЗаявкаНаПеревозкуГруза, "Подразделение, НаправлениеДеятельности");
					РеквизитыЗаявок.Вставить(СтрокаДохода.ЗаявкаНаПеревозкуГруза, сткРеквизиты);
					НоваяСтрока.Подразделение = сткРеквизиты.Подразделение;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Реквизиты.НаправлениеДеятельности) Тогда
				НоваяСтрока.НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
			Иначе
				сткРеквизиты = РеквизитыЗаявок.Получить(ЗаявкаНаПеревозкуГруза);
				Если сткРеквизиты <> Неопределено Тогда
					НоваяСтрока.НаправлениеДеятельности = сткРеквизиты.НаправлениеДеятельности;	
				ИначеЕсли ЗначениеЗаполнено(СтрокаДохода.ЗаявкаНаПеревозкуГруза) Тогда
					сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаДохода.ЗаявкаНаПеревозкуГруза, "Подразделение, НаправлениеДеятельности");
					РеквизитыЗаявок.Вставить(СтрокаДохода.ЗаявкаНаПеревозкуГруза, сткРеквизиты);
					НоваяСтрока.НаправлениеДеятельности = сткРеквизиты.НаправлениеДеятельности;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока.Доходы  = сткСуммы.Сумма;
			НоваяСтрока.Расходы = 0;
			
		КонецЦикла;
		
		тбзДоходыПредприятия.Свернуть("Организация, Подразделение, НаправлениеДеятельности, СтатьяДоходовРасходов, ХозяйственнаяОперация", "Доходы, Расходы");
		
		Для каждого СтрокаДоход Из тбзДоходыПредприятия Цикл
					
			ДвижениеДоход = ДвиженияДоходыРасходыПредприятия.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеДоход, СтрокаДоход);
			ДвижениеДоход.Период      = Реквизиты.Дата;
			ДвижениеДоход.Регистратор = Ссылка;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры	

// Распределяет суммы расходов по заявкам(принадлежащих рейсам) в соответствии с количественными
//  показателями и правилами, заданными для каждой статьи расходов.
//
// Параметры:
//  Рейс					 - 	 - 
//  ЗаявкаНаПеревозкуГрузов	 - 	 - 
//  ТаблицаДоходов			 - 	 - 
//  сткШаблон				 - Структура	 - Данные.
//  ТекстОшибки			 - Строка		 - текст ошибки.
//  Отказ					 - Булево		 - флаг ошибки.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция РаспределениеДоходовПоРейсу(Рейс, ЗаявкаНаПеревозкуГрузов, ТаблицаДоходов, сткШаблон, ТекстОшибки, Отказ)
	
	Если ТаблицаДоходов.Колонки.Найти("ЗаявкаНаПеревозкуГруза") = Неопределено Тогда
		ТаблицаДоходов.Колонки.Добавить("ЗаявкаНаПеревозкуГруза", Новый ОписаниеТипов("ДокументСсылка.упЗаявкаНаПеревозкуГруза"));
	КонецЕсли;
	
	тбзРаспределение = Новый ТаблицаЗначений;
	тбзРаспределение.Колонки.Добавить("ЗаявкаНаПеревозкуГруза");
	тбзРаспределение.Колонки.Добавить("СтатьяДоходов");
	тбзРаспределение.Колонки.Добавить("Валюта");
	тбзРаспределение.Колонки.Добавить("сткСуммы");
	
	Если ЗначениеЗаполнено(ЗаявкаНаПеревозкуГрузов) Тогда
		Для каждого СтрокаДоходы Из ТаблицаДоходов Цикл
			НоваяСтрока = тбзРаспределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДоходы,,"сткСуммы");
			НоваяСтрока.ЗаявкаНаПеревозкуГруза = ЗаявкаНаПеревозкуГрузов;
			сткСуммы	 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткШаблон);	
			ЗаполнитьЗначенияСвойств(сткСуммы, СтрокаДоходы.сткСуммы);
			НоваяСтрока.сткСуммы = сткСуммы;
		КонецЦикла;
	Иначе	
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаРаспределенияДоходов();
		мсвРейсы = Новый Массив();
		мсвРейсы.Добавить(Рейс);
		Запрос.УстановитьПараметр("мсвРейсы", Рейс);
		мсвРезультат = Запрос.Выполнить();
		
		//ИндексПоказателиЗаявки = 4; 
		тбзПоказателиЗаявки	= мсвРезультат.Выгрузить();
		
		тбзПоказателиЗаявки.Колонки.Добавить("Коэффициент");
		тбзПоказателиЗаявки.Колонки.Добавить("сткСуммы");
		
		Для каждого СтрокаДоходы Из ТаблицаДоходов Цикл
						
			РассчитатьКоэффициентыРаспределения(тбзПоказателиЗаявки);
			
			сткСуммыПоЗаявкам	 = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткШаблон);	// Накопительная сумма по мере обхода рейсов.
			ЗаявкиКоличествоСтрок = тбзПоказателиЗаявки.Количество();
			й_Шаг = 1; 
			
			сткСуммыПоСтатьеДоходов		= СтрокаДоходы.сткСуммы; 
			СтатьяДоходов 				= СтрокаДоходы.СтатьяДоходов;
			Валюта						= СтрокаДоходы.Валюта;
		
			Для каждого СтрокаЗаявки Из тбзПоказателиЗаявки Цикл
				
				сткСуммы = Неопределено;
				
				Если й_Шаг = ЗаявкиКоличествоСтрок Тогда
					сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(сткСуммыПоСтатьеДоходов, сткСуммыПоЗаявкам, Отказ); 			// Берется остаток.
				Иначе
					сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(сткСуммыПоСтатьеДоходов, СтрокаЗаявки.Коэффициент); 	// Сумма распределения умножается на коэффициент.
				КонецЕсли;
				
				
				Если НЕ упСервисныеФункцииКлиентСервер.СтруктураРавнаНулю(сткСуммы) Тогда
					СтрокаРаспределение = тбзРаспределение.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаЗаявки);
					СтрокаРаспределение.Валюта = Валюта;
					СтрокаРаспределение.СтатьяДоходов = СтатьяДоходов;
					СтрокаРаспределение.сткСуммы      = сткСуммы;
					сткСуммыПоЗаявкам = упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоЗаявкам, сткСуммы, Отказ);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;	
						
	КонецЕсли;
		
	// Сворачивается таблица распределений.
	тбзРаспределениеСвернутое = тбзРаспределение.Скопировать();
	тбзРаспределениеСвернутое.Свернуть("ЗаявкаНаПеревозкуГруза,СтатьяДоходов,Валюта","сткСуммы");
	
	Для каждого Строка Из тбзРаспределениеСвернутое Цикл
		
		сткПоиска = Новый Структура("ЗаявкаНаПеревозкуГруза,СтатьяДоходов,Валюта");
		ЗаполнитьЗначенияСвойств(сткПоиска, Строка);
		мсвСтрокРаспределения = тбзРаспределение.НайтиСтроки(сткПоиска);
		
		сткСумма = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткШаблон);
		
		Для каждого СтрокаРаспределение Из мсвСтрокРаспределения Цикл
			
			сткСумма	= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСумма, СтрокаРаспределение.сткСуммы, Отказ);
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Строка.сткСуммы = сткСумма;
		
	КонецЦикла;
		
	Возврат тбзРаспределениеСвернутое;
	
КонецФункции

// Процедура - расчета коэффициента распределения.
//
// Параметры:
//	ТаблицаДокументов - ТаблицаЗначений - Таблица данных.
//
Процедура РассчитатьКоэффициентыРаспределения(ТаблицаДокументов)
	
	КоличествоСтрок = ТаблицаДокументов.Количество();
	Для каждого Строка Из ТаблицаДокументов Цикл
		Строка.Коэффициент = 1 / КоличествоСтрок;
	КонецЦикла;
	
КонецПроцедуры	

Функция ТекстЗапросаРаспределенияДоходов()
		
	Текст = 
		"ВЫБРАТЬ
		|	тбРаботы.Рейс,
		|	тбРаботы.Отменена,
		|	тбРаботы.Задание,
		|	тбРаботы.Операция
		|ПОМЕСТИТЬ втРаботы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс В (&мсвРейсы)) КАК тбРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упРаботыПоРейсу.Рейс,
		|	упРаботыПоРейсу.Регистратор
		|ПОМЕСТИТЬ втРегистраторы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
		|ГДЕ
		|	упРаботыПоРейсу.Рейс В(&мсвРейсы)
		|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.Рейс,
		|	втРаботы.Задание,
		|	СУММА(ВЫБОР
		|			КОГДА втРаботы.Отменена
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоОтменена,
		|	СУММА(1) КАК КоличествоОпераций
		|ПОМЕСТИТЬ втОтмененныеЗадания
		|ИЗ
		|	втРаботы КАК втРаботы
		|ГДЕ
		|	(втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|			ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
		|
		|СГРУППИРОВАТЬ ПО
		|	втРаботы.Рейс,
		|	втРаботы.Задание
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|			КОГДА втРаботы.Отменена
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) = СУММА(1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейсЗадания.Ссылка КАК Рейс,
		|	упРейсЗадания.Задание
		|ПОМЕСТИТЬ втГрузовыеМестаПоЗаданиям
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
		|		ПО упРейсЗадания.Ссылка = втОтмененныеЗадания.Рейс
		|			И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
		|ГДЕ
		|	упРейсЗадания.Ссылка В(&мсвРейсы)
		|	И втОтмененныеЗадания.Задание ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втРегистраторы.Рейс,
		|	упКорректировкаРейсаЗадания.Задание
		|ИЗ
		|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
		|		ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
		|			И упКорректировкаРейсаЗадания.Ссылка.Рейс = втРегистраторы.Рейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
		|		ПО упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
		|			И упКорректировкаРейсаЗадания.Ссылка.Рейс = втОтмененныеЗадания.Рейс
		|ГДЕ
		|	втОтмененныеЗадания.Задание ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втГрузовыеМестаПоЗаданиям.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ИЗ
		|	втГрузовыеМестаПоЗаданиям КАК втГрузовыеМестаПоЗаданиям";
	
	Возврат Текст;			
	
КонецФункции

#КонецОбласти