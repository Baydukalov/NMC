#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

// Процедура - Форма документа при открытии
//
// Параметры:
//  ЭтаФорма	 - Форма	 - форма.
//  Отказ		 - Булево	 - Истина, в случае ошибки.
//
Процедура ФормаДокументаПриОткрытии(ЭтаФорма, Отказ) Экспорт

	ЭтаФорма.УстановитьПредставлениеСуммовыхПоказателей();
	
	ЭтаФорма.ЗагрузитьВзаиморасчетыСПартнерами();
		
КонецПроцедуры

// Процедура - После записи форма документа
//
// Параметры:
//  ЭтаФорма		 - Форма						 - форма.
//  Объект		 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  ПараметрыЗаписи	 - Структура							 - параметры записи.
//
Процедура ПослеЗаписиФормаДокумента(ЭтаФорма, Объект, ПараметрыЗаписи) Экспорт
	
	ВзаимодействияКлиент.КонтактПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи,"упЗаявкаНаПеревозкуГруза");
	
	ЭтаФорма.УстановитьПредставлениеСуммовыхПоказателей();
		
КонецПроцедуры

// Процедура - Обработка оповещения форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  ИмяСобытия	 - Строка								 - имя события.
//  Параметр	 - Параметр							 - Параметр сообщения.
//  Источник	 - Источник							 - Источник события.
//
Процедура ОбработкаОповещенияФормаДокумента(ЭтаФорма, Объект, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если Истина
		И ИмяСобытия = "СменаСтатуса" 
		И Источник = Объект.Ссылка 
	Тогда
		ЭтаФорма.ИзменилсяСтатус();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Процедура - Адрес отправления при изменении форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  Элемент	 - ЭлементФормы	 - элемент формы.
//
Процедура АдресОтправленияПриИзмененииФормаДокумента(ЭтаФорма, Объект, Элемент) Экспорт
	
	Если Истина
		И Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) 
		И ЗначениеЗаполнено(Объект.Отправитель) 
		И ЗначениеЗаполнено(Объект.АдресОтправления) 
	Тогда
		Объект.ГрафикРаботыАдресаОтправления = упЗаявкаФормаДокументаВызовСервера.ПолучитьГрафикРаботы(Объект.Отправитель, Объект.АдресОтправления);
	КонецЕсли; 
	
КонецПроцедуры

// Процедура - Отправитель при изменении форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  Элемент	 - ЭлементФормы	 - элемент формы.
//
Процедура ОтправительПриИзмененииФормаДокумента(ЭтаФорма, Объект, Элемент) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.КонтрагентОтправителя) Тогда
		Объект.КонтрагентОтправителя = упЗаявкаФормаДокументаВызовСервера.ПолучитьОсновногоКонтрагента(Объект.Отправитель);	
	КонецЕсли; 
	
	Если Истина
		И Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) 
		И ЗначениеЗаполнено(Объект.Отправитель) 
		И ЗначениеЗаполнено(Объект.АдресОтправления) 
	Тогда
		Объект.ГрафикРаботыАдресаОтправления = упЗаявкаФормаДокументаВызовСервера.ПолучитьГрафикРаботы(Объект.Отправитель, Объект.АдресОтправления);
	КонецЕсли; 
		
КонецПроцедуры

// Процедура - Заказчик при изменении форма документа
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//  Элемент	 - ЭлементФормы	 - элемент формы.
//
Процедура ЗаказчикПриИзмененииФормаДокумента(ЭтаФорма, Объект, Элемент) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Соглашение) Тогда
		Объект.Соглашение = упЗаявкаФормаДокументаВызовСервера.ПолучитьОсновноеСоглашение(Объект.Заказчик, Объект.Организация);
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Объект.КонтрагентЗаказчика) Тогда
		Объект.КонтрагентЗаказчика = упЗаявкаФормаДокументаВызовСервера.ПолучитьОсновногоКонтрагента(Объект.Заказчик);		 
	КонецЕсли; 
	
	ЭтаФорма.ЗагрузитьВзаиморасчетыСПартнерами();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Взаиморасчеты

// Процедура - Загрузить взаиморасчеты с партнерами
//
// Параметры:
//  ЭтаФорма	 - Форма							 - форма.
//  Объект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза	 - объект заявки.
//
Процедура ЗагрузитьВзаиморасчетыСПартнерами(ЭтаФорма, Объект) Экспорт
	ЭтаФорма.ВзаиморасчетыЗагрузить = Истина;
	ЭтаФорма.ПодключитьОбработчикОжиданияВзаиморасчеты = Ложь;
	Если Истина
		И ЭтаФорма.РазрешеноЗагружатьВзаиморасчеты 
		И НЕ ЭтаФорма.ВзаиморасчетыЗагружаются 
		И ЗначениеЗаполнено(Объект.Организация) 
		И ЗначениеЗаполнено(Объект.Заказчик)
	Тогда
		ЭтаФорма.ВзаиморасчетыЗагружаются    = Истина;
		ЭтаФорма.ВзаиморасчетыЗагрузить      = Ложь;
		ЭтаФорма.ВзаиморасчетыАдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
		ЭтаФорма.ПодключитьОбработчикОжиданияВзаиморасчеты = Истина;
		ЭтаФорма.Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Обновить;
		упЗаявкаФормаДокументаВызовСервера.ЗагрузитьВзаиморасчетыСПартнерамиНаСервере(Объект.Организация, Объект.Заказчик, ЭтаФорма.ВзаиморасчетыАдресХранилища);
	КонецЕсли;	
КонецПроцедуры

// Процедура - Сформировать таблицу взаиморасчетов
//
// Параметры:
//  ЭтаФорма	 - Форма			 - форма.
//  Результат	 - ТабличныйДокумент - для вывода результата.
//
Процедура СформироватьТаблицуВзаиморасчетов(ЭтаФорма, Результат) Экспорт
	упЗаявкаФормаДокументаВызовСервера.СформироватьТаблицуВзаиморасчетов(ЭтаФорма.СостояниеВзаиморасчетов, Результат);
КонецПроцедуры

// Процедура - Заполнить форму взаиморасчетами
//
// Параметры:
//  ЭтаФорма		 - Форма	 - форма.
//  Взаиморасчеты	 - Булево,ТабличныйДокумент	 - результат формирования.
//
Процедура ЗаполнитьФормуВзаиморасчетами(ЭтаФорма, Взаиморасчеты) Экспорт
	ЭтаФорма.ВзаиморасчетыЗагружаются = Ложь;
	Если НЕ ЭтаФорма.ВзаиморасчетыЗагрузить Тогда
		Если НЕ ТипЗнч(Взаиморасчеты) = Тип("Булево") Тогда
			упЗаявкаФормаДокументаКлиент.СформироватьТаблицуВзаиморасчетов(ЭтаФорма, Взаиморасчеты);
			ЭтаФорма.Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Успешно;
			ПоказатьОповещениеПользователя(НСтр("ru = 'Взаиморасчеты загружены.'"),,, БиблиотекаКартинок.Успешно32);
		Иначе
			ЭтаФорма.Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Остановить;	
			ПоказатьОповещениеПользователя(НСтр("ru = 'Не удалось загрузить взаиморасчеты.'"),, НСтр("ru = 'Подробности см. в Журнале регистрации.'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;		
	Иначе
		упЗаявкаФормаДокументаКлиент.ЗагрузитьВзаиморасчетыСПартнерами(ЭтаФорма);		
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#КонецОбласти
