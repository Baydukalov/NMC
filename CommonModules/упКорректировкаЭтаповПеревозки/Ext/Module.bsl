#Область ФормированиеДвижений

// Процедура формирует записи по регистру "упПлановыеЭтапыПеревозки".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияПлановыеЭтапыМультимодальнойПеревозки(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
		
	ДвиженияПлановыеЭтапы = Движения.упПлановыеЭтапыПеревозки;
	ДвиженияПлановыеЭтапы.Записывать = Истина;
	
	Если ДополнительныеСвойства.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
		Для каждого текСтрока Из Реквизиты.ИзмененияПлановПеревозки Цикл
			ДвижениеПлановыеЭтапы = ДвиженияПлановыеЭтапы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеПлановыеЭтапы, текСтрока);		
			ДвижениеПлановыеЭтапы.ЗаявкаНаПеревозкуГруза = Реквизиты.ЗаявкаНаПеревозкуГруза;
			ДвижениеПлановыеЭтапы.Период = Реквизиты.Дата;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упЗаявкиКВыполнению".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаявкиКВыполнению = Движения.упЗаявкиКВыполнению;
	ДвиженияЗаявкиКВыполнению.Записывать = Истина;
	
	Если Не ДополнительныеСвойства.ДвиженияЗаявкиКВыполнению = Неопределено Тогда
		Для каждого текСтрока Из ДополнительныеСвойства.ДвиженияЗаявкиКВыполнению Цикл
			ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеЗаявки, текСтрока);
			ДвижениеЗаявки.Период = Реквизиты.Дата;
			ДвижениеЗаявки.Заявка = Реквизиты.ЗаявкаНаПеревозкуГруза;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ВспомогательныеПроцедурыФункции

// Возвращает возможность редактирования этапов по заявке.
//
// Параметры:
//  ТекущийОбъект	 - 	 - 
//  Период			 - 	 - 
// 
// Возвращаемое значение:
//  Булево - 
//
Функция ЗапрещеноРедактироватьЭтапыМультимодальнойПеревозки(ТекущийОбъект) Экспорт
	
	Результат = Ложь;
	Период    = ТекущийОбъект.Дата;
	Ссылка    = ТекущийОбъект.Ссылка;
		  
	Если ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упЗаявкаНаПеревозкуГруза") Тогда
		ЗаявкаНаПеревозкуГруза = ТекущийОбъект.Ссылка;
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упКорректировкаЭтаповПеревозки") Тогда	
		ЗаявкаНаПеревозкуГруза = ТекущийОбъект.ЗаявкаНаПеревозкуГруза;
	КонецЕсли;
	            
	Результат = НЕ РазрешеноЭтапыМультимодальнойПеревозки(Период, Ссылка, ЗаявкаНаПеревозкуГруза);
	
	Если Результат Тогда
		ТекстСообщения = Нстр("ru = 'Корректировка этапов мультимодальной перевозки запрещена, так как есть изменения на дату %1 или  более поздние.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Период);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		Событие = Нстр("ru = 'Ошибка при корректировке этапов мультимодальной перевозки'");
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
	КонецЕсли;
	
	Возврат Результат;	
		
КонецФункции

Процедура ОтменитьЗаданияНаПеревозкуПоЭтапам(ТекущийОбъект, ТекстОшибки = "", Отказ) Экспорт
	
	тбзГрузовыеМеста = ТекущийОбъект.ИзмененияПлановПеревозки.Выгрузить(Новый Структура("ОбработкаПрерывания", Истина), "Тара, ГрузовоеМесто, НомерВЦепиПоставок, НомерВЦепиПоставокКонец");
	тбзГрузовыеМеста.Колонки.Добавить("Заявка", Новый ОписаниеТипов("ДокументСсылка.упЗаявкаНаПеревозкуГруза"));
	тбзГрузовыеМеста.ЗаполнитьЗначения(ТекущийОбъект.ЗаявкаНаПеревозкуГруза, "Заявка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|//{Запрос: 0, -7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.Заявка КАК Заявка,
	|	втГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	&ГрузовыеМеста КАК втГрузовыеМеста
	|;
	|//{Запрос: 1, -6 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявкаГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ЗаявкаГрузовыеМеста.Ссылка КАК Заявка,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявкам
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК ЗаявкаГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМеста
	|	ПО ИСТИНА
	|		И ЗаявкаГрузовыеМеста.ГрузовоеМесто = втГрузовыеМеста.ГрузовоеМесто
	|		И ЗаявкаГрузовыеМеста.Ссылка = втГрузовыеМеста.Заявка
	|ГДЕ ЗаявкаГрузовыеМеста.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|	ЗаявкаГрузовыеМеста.Ссылка КАК Заявка,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.Тара КАК Тара
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК ЗаявкаГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМеста
	|	ПО ЗаявкаГрузовыеМеста.Ссылка = втГрузовыеМеста.Заявка
	|ГДЕ ЗаявкаГрузовыеМеста.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|;
	|//{Запрос: 2, -5 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкиКВыполнению.Регистратор КАК ЗаданиеНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению КАК упЗаявкиКВыполнению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаПоЗаявкам КАК втГрузовыеМестаПоЗаявкам
	|	ПО ИСТИНА
	|		И упЗаявкиКВыполнению.Заявка = втГрузовыеМестаПоЗаявкам.Заявка
	|		И упЗаявкиКВыполнению.ГрузовоеМесто = втГрузовыеМестаПоЗаявкам.ГрузовоеМесто
	|		И упЗаявкиКВыполнению.НомерВЦепиПоставок = втГрузовыеМестаПоЗаявкам.НомерВЦепиПоставок
	|		И упЗаявкиКВыполнению.НомерВЦепиПоставокКонец = втГрузовыеМестаПоЗаявкам.НомерВЦепиПоставокКонец
	|		И упЗаявкиКВыполнению.Тара = втГрузовыеМестаПоЗаявкам.Тара
	|ГДЕ упЗаявкиКВыполнению.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза
	|;
	|//{Запрос: 3, -4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияТ.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
	|	ЗаданияРейса.Ссылка КАК Рейс,
	|	ЗаданияРейса.ГрузовоеМесто КАК ГрузовоеМесто,
	|	СтатусыРейсов.Статус КАК СтатусРейса,
	|	СтатусыЗаданий.Статус КАК СтатусЗадания
	|ПОМЕСТИТЬ втСтатусыЗаданий
	|ИЗ
	|	втЗадания КАК втЗаданияТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс.Задания КАК ЗаданияРейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|			,
	|			) КАК СтатусыРейсов
	|		ПО СтатусыРейсов.Документ = ЗаданияРейса.Ссылка
	|	ПО втЗаданияТ.ЗаданиеНаПеревозкуГруза = ЗаданияРейса.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаданий
	|	ПО СтатусыЗаданий.Документ = ЗаданияРейса.Задание
	|;
	|//{Запрос: 4, -3 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСтатусыЗаданийТ.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
	|	втСтатусыЗаданийТ.Рейс КАК Рейс,
	|	втСтатусыЗаданийТ.СтатусЗадания КАК СтатусЗадания,
	|	втСтатусыЗаданийТ.СтатусРейса КАК СтатусРейса
	|ИЗ
	|	втСтатусыЗаданий КАК втСтатусыЗаданийТ
	|ГДЕ втСтатусыЗаданийТ.СтатусЗадания В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ВПути),
	|		ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполнено),
	|		ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполняется),
	|		ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению))
	|;
	|//{Запрос: 5, -2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСтатусыЗаданийТ.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
	|	втСтатусыЗаданийТ.СтатусЗадания КАК СтатусЗадания,
	|	втСтатусыЗаданийТ.СтатусРейса КАК СтатусРейса
	|ИЗ
	|	втСтатусыЗаданий КАК втСтатусыЗаданийТ
	|;
	|//{Запрос: 6, -1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСтатусыЗаданийТ.Рейс КАК Рейс,
	|	втСтатусыЗаданийТ.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозку,
	|	втСтатусыЗаданийТ.СтатусРейса КАК СтатусРейса,
	|	втСтатусыЗаданийТ.ГрузовоеМесто КАК ГрузовоеМесто
	|ИЗ
	|	втСтатусыЗаданий КАК втСтатусыЗаданийТ
	|ГДЕ НЕ (втСтатусыЗаданийТ.Рейс ЕСТЬ NULL)
	|ИТОГИ
	|	МАКСИМУМ(СтатусРейса)
	|ПО
	|	Рейс
	|";
	
	Запрос.УстановитьПараметр("ГрузовыеМеста", тбзГрузовыеМеста);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексЗапросаПроверкаСтатуса = 4;
	ИндексЗапросаЗадания = 5;
	ИндексЗапросаРейсы = 6;
	
	Если РезультатЗапроса[ИндексЗапросаПроверкаСтатуса].Пустой() Тогда
			
		ВыборкаРейс = РезультатЗапроса[ИндексЗапросаРейсы].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРейс.Следующий() Цикл
			
			РейсСсылка = ВыборкаРейс.Рейс;
			ВыборкаЗадание = ВыборкаРейс.Выбрать();
			
			ТекстОшибки = "";
			
			тбзЗадания = Новый ТаблицаЗначений;
			тбзЗадания.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
			тбзЗадания.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
			
			Пока ВыборкаЗадание.Следующий() Цикл
				НоваяСтрока = тбзЗадания.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗадание);
			КонецЦикла;
			
			Документы.упРейс.УдалитьЗаданияИзРейса(, тбзЗадания, РейсСсылка, ТекстОшибки, Отказ); 
			
			//тбзОтмененныеГрузовыеМеста = Новый ТаблицаЗначений;
			//тбзОтмененныеГрузовыеМеста.Колонки.Добавить("ЗаданиеНаПеревозкуГруза", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
			//тбзОтмененныеГрузовыеМеста.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
			//
			//Пока ВыборкаЗадание.Следующий() Цикл
			//	НоваяСтрока = тбзОтмененныеГрузовыеМеста.Добавить();
			//	НоваяСтрока.ЗаданиеНаПеревозкуГруза = ВыборкаЗадание.ЗаданиеНаПеревозкуГруза;
			//КонецЦикла;
			
			//упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();
			//упКорректировкаРейсаОбъект.УдалитьЗаданияИзРейса(РейсСсылка, тбзОтмененныеГрузовыеМеста, ТекстОшибки, Отказ);
			
			Если Отказ Тогда
				ТекстОшибки = СтрШаблон(Нстр("ru = 'Не удалось исключить задания из %1. Произошла ошибка: %2'"), РейсСсылка, ТекстОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, РейсСсылка,,,Отказ);
				ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,РейсСсылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;

		Если Не Отказ Тогда
			
			ВыборкаЗадание = РезультатЗапроса[ИндексЗапросаЗадания].Выбрать();
			
			Пока ВыборкаЗадание.Следующий() Цикл
				ЗаданиеНаПеревозкуГруза = ВыборкаЗадание.ЗаданиеНаПеревозкуГруза;
				ЗаданиеНаПеревозкуГрузаОбъект = ЗаданиеНаПеревозкуГруза.ПолучитьОбъект();
				
				ЗаданиеНаПеревозкуГрузаОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
				Попытка
					ЗаданиеНаПеревозкуГрузаОбъект.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					ТекстОшибки = СтрШаблон(Нстр("ru = 'Не удалось отменить %1. Произошла ошибка: %2'"), ЗаданиеНаПеревозкуГруза, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЗаданиеНаПеревозкуГруза,,,Отказ);
					ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,ЗаданиеНаПеревозкуГруза, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				КонецПопытки;
				
				Если Отказ Тогда
					Прервать;
				КонецЕсли;

			КонецЦикла;
						
		КонецЕсли;

	Иначе	
		Выборка = РезультатЗапроса[ИндексЗапросаПроверкаСтатуса].Выбрать(); 
		мсвСообщенияОбОшибках = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ТекстОшибки = СтрШаблон(Нстр("ru = 'Не удалось отменить %1 в статусе ""%2""'"), Выборка.ЗаданиеНаПеревозкуГруза, Выборка.СтатусЗадания);	
			мсвСообщенияОбОшибках.Добавить(ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецЦикла;
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Ошибка", СтрСоединить(мсвСообщенияОбОшибках, Символы.ПС));
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти 

#Область СлужебныеПроцедурыФункции

// Возвращает возможность корректировки маршрута.
//
Функция РазрешеноЭтапыМультимодальнойПеревозки(Период, Ссылка, ЗаявкаНаПеревозкуГруза)
	
	Результат = Истина;
	
	Если Истина
		И ЗначениеЗаполнено(Период) 
		И ЗначениеЗаполнено(Ссылка)
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =  "
		|ВЫБРАТЬ
		|	упПлановыеЭтапыМультимодальнойПеревозкиТ.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.упПлановыеЭтапыПеревозки КАК упПлановыеЭтапыМультимодальнойПеревозкиТ
		|ГДЕ ИСТИНА
		|	И упПлановыеЭтапыМультимодальнойПеревозкиТ.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
		|	И упПлановыеЭтапыМультимодальнойПеревозкиТ.Период >= &Период
		|	И упПлановыеЭтапыМультимодальнойПеревозкиТ.Регистратор <> &Регистратор
		|";
		
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("Период", Период);
		
		Результат = Запрос.Выполнить().Пустой();
	КонецЕсли;
			
	Возврат Результат;
		
КонецФункции

#КонецОбласти 