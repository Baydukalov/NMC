#Область ПрограммныйИнтерфейс
// Процедура контролирует что не образовались отрицательные остатки 
// в регистре "упАгрегатыНаСкладах". При перепроведении и отмене проведения 
// документов приходов/перемещений.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура КонтрольОтрицательныхОстатковАгрегатовНаСкладах(ДополнительныеСвойства, Ссылка, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбАгрегаты.Склад КАК Склад,
	|	тбАгрегаты.Агрегат КАК Агрегат,
	|	тбАгрегаты.СтрокаДокумента КАК СтрокаДокумента
	|ПОМЕСТИТЬ тбАгрегаты
	|ИЗ
	|	&КонтрольАгрегатов КАК тбАгрегаты
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упАгрегатыНаСкладахОстатки.Склад КАК Склад,
	|	упАгрегатыНаСкладахОстатки.Агрегат КАК Агрегат,
	|	тбАгрегатыТ.СтрокаДокумента
	|ИЗ
	|	тбАгрегаты КАК тбАгрегатыТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|		,
	|		(Склад, Агрегат) В (
	|				ВЫБРАТЬ
	|				тбАгрегаты.Склад КАК Склад,
	|				тбАгрегаты.Агрегат КАК Агрегат
	|			ИЗ
	|				тбАгрегаты КАК тбАгрегаты)) КАК упАгрегатыНаСкладахОстатки
	|	ПО ИСТИНА
	|		И упАгрегатыНаСкладахОстатки.Агрегат = тбАгрегатыТ.Агрегат
	|		И упАгрегатыНаСкладахОстатки.Склад = тбАгрегатыТ.Склад
	|ГДЕ упАгрегатыНаСкладахОстатки.КоличествоОстаток < 0
	|";

	
	Запрос.УстановитьПараметр("КонтрольАгрегатов", ДополнительныеСвойства.КонтрольАгрегатов);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ВыводитьСообщениеОтрицательныйОстаток = Ложь
										Или (Истина
											И Выборка.СтрокаДокумента > 0
											И ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) 
										Или Выборка.СтрокаДокумента = 0;
		
		Если ВыводитьСообщениеОтрицательныйОстаток Тогда
			
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", будет иметь отрицательный остаток на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			Иначе
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Доп. оборудование: ""%1"", будет иметь отрицательный остаток на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			КонецЕсли;
			
		Иначе
			
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат ""%1"" отсутствует в наличии на складе ""%2""'", Выборка.Агрегат, Выборка.Склад));
			Иначе
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Доп. оборудование ""%1"" отсутствует в наличии на складе ""%2""'", Выборка.Агрегат, Выборка.Склад));
			КонецЕсли;

		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Контроль остатков'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),УровеньЖурналаРегистрации.Предупреждение,,,ТекстОшибки);
		
	КонецЦикла;
	
КонецПроцедуры	

// Процедура контролирует остатки регистра "упОстаткиМатериалов".
// при перепроведении и отмене проведения документов приходов/перемещений
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссыкла на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
Процедура КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
		
	ТаблицаДвижений = Новый ТаблицаЗначений;
	ТаблицаДвижений.Колонки.Добавить("Склад",Новый ОписаниеТипов("СправочникСсылка.упСклады"));
	ТаблицаДвижений.Колонки.Добавить("Запчасть",Новый ОписаниеТипов("СправочникСсылка.упЗапчасти"));
	Запрос = Новый Запрос;                                 
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиМатериалов.Склад,
	               |	упОстаткиМатериалов.Материал КАК Запчасть
	               |ИЗ
	               |	РегистрНакопления.упОстаткиМатериалов КАК упОстаткиМатериалов
	               |ГДЕ
	               |	упОстаткиМатериалов.Регистратор = &Регистратор";
	Запрос.УстановитьПараметр("Регистратор",Ссылка);			   
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	
	Если Не ТаблицаДвижений.Количество() = 0 Тогда
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.упОстаткиМатериалов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ТаблицаДвижений;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Материал", "Запчасть");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		Блокировка.Заблокировать();
	КонецЕсли;	
	
	упСервисныеФункции.УдалитьДвижения(Движения);
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПеремещениеМатериалов") Тогда
		// Формирование движений по регистру накопления "упОстаткиМатериалов": "расход"
		упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
		
		// Формирование движений по регистру накопления "упОстаткиМатериалов": "приход"
		Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
			СкладПолучатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"СкладПолучатель");
			Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиМатериалов Цикл
				текСтрока.ВидДвижения = ВидДвиженияНакопления.Приход;
				текСтрока.Склад       = СкладПолучатель;
			КонецЦикла; 
		КонецЕсли;
		упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	Иначе	
		упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	КонецЕсли;

	Движения.упОстаткиМатериалов.Записать();
	
	Для Каждого ДвижениеПоРегистру Из Движения.упОстаткиМатериалов Цикл
		НоваяСтрока = ТаблицаДвижений.Добавить();	
		НоваяСтрока.Склад = ДвижениеПоРегистру.Склад;
		НоваяСтрока.Запчасть = ДвижениеПоРегистру.Материал;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДвижений.Склад,
		|	ТаблицаДвижений.Запчасть
		|ПОМЕСТИТЬ ТаблицаДвижений
		|ИЗ
		|	&ТаблицаДвижений КАК ТаблицаДвижений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упОстаткиМатериаловОстатки.Склад,
		|	упОстаткиМатериаловОстатки.Материал КАК Запчасть,
		|	упОстаткиМатериаловОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.упОстаткиМатериалов.Остатки(
		|			&ТекущаяДата,
		|			Склад В
		|					(ВЫБРАТЬ
		|						ТаблицаДвижений.Склад
		|					ИЗ
		|						ТаблицаДвижений КАК ТаблицаДвижений)
		|				И Материал В
		|					(ВЫБРАТЬ
		|						ТаблицаДвижений.Запчасть
		|					ИЗ
		|						ТаблицаДвижений КАК ТаблицаДвижений)) КАК упОстаткиМатериаловОстатки
		|ГДЕ
		|	упОстаткиМатериаловОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("ТаблицаДвижений", ТаблицаДвижений);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ВыборкаНаТекущуюДату = РезультатЗапроса.Выбрать();
	Пока ВыборкаНаТекущуюДату.Следующий() Цикл
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Запчасть: ""%1"", будет иметь отрицательный остаток на складе ""%2"", на дату ""%3"".'", ВыборкаНаТекущуюДату.Запчасть, ВыборкаНаТекущуюДату.Склад,ТекущаяДата()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти