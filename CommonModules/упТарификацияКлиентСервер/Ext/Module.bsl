////////////////////////////////////////////////////////////////////////////////
// Подсистема "Тарификация", функции в основном используемые на клиенте.
//  
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ФункцииДляРаботыСПараметрамиРасчета

// Проверяет относится или нет параметр расчета к "ссылочным".
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЭтоСсылочныйПараметр(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	текРезультат = Истина;
	
	текСписокПараметров = ПолучитьСписокСсылочныхПараметров();
		
	Если текСписокПараметров.НайтиПоЗначению(Ссылка) = Неопределено Тогда
		текРезультат = Ложь;	
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции // ЭтоСсылочныйПараметр()

// Проверяет относится или нет параметр расчета к "числовым".
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЭтоЧисловойПараметр(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	текРезультат = Истина;
	
	текСписокПараметров = ПолучитьСписокЧисловыхПараметров();
		
	Если текСписокПараметров.НайтиПоЗначению(Ссылка) = Неопределено Тогда
		текРезультат = Ложь;	
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // упПараметрыРасчета()

// Проверяет относится или нет параметр расчета к "временным".
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЭтоВременнойПараметр(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	текРезультат = Истина;
	
	текСписокПараметров = ПолучитьСписокВременныхПараметров();
		
	Если текСписокПараметров.НайтиПоЗначению(Ссылка) = Неопределено Тогда
		текРезультат = Ложь;	
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // ЭтоВременнойПараметр()

// Проверяет относится или нет параметр расчета к "Булевым".
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЭтоБулевыйПараметр(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	текРезультат = Истина;
	
	текСписокПараметров = ПолучитьСписокБулевыхПараметров();
		
	Если текСписокПараметров.НайтиПоЗначению(Ссылка) = Неопределено Тогда
		текРезультат = Ложь;	
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // ЭтоВременнойПараметр()

// Проверяет относится или нет параметр расчета к "Географическим зонам".
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЭтоГеографическаяЗонаПараметр(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	текРезультат = Истина;
	
	текСписокПараметров = ПолучитьСписокГеографическиеЗоныПараметры();
		
	Если текСписокПараметров.НайтиПоЗначению(Ссылка) = Неопределено Тогда
		текРезультат = Ложь;	
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // ЭтоГеографическаяЗонаПараметр()

// Проверяет, является ли значением ссылочного показателя сам показатель
//	(обычно значением показателя является число, то есть он как-то рассчитывается).
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ЗначениемПараметраЕстьСамПараметр(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	текРезультат = Истина;
	
	текСписокПараметров = ПолучитьСписокСсылочныхПараметровСоЗначениемСсылка();
		
	Если текСписокПараметров.НайтиПоЗначению(Ссылка) = Неопределено Тогда
		текРезультат = Ложь;	
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции // ЭтоСсылочныйПараметр()

#КонецОбласти

#Область ИнициализацияСписковПараметров

// Возвращает список ссылочных параметров.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокСсылочныхПараметров()
	
	текРезультат = Новый СписокЗначений();
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипТС"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаПолучения"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаОтправления"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВидПеревозки"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Рейс"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТемпературныйРежим"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипГруза"));
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает список числовых параметров.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокЧисловыхПараметров()
	
	текРезультат = Новый СписокЗначений();
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПлановыеРасходыПоПеревозке"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПредполагаемаяСтоимостьПеревозки"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МассаГруза"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОбъемГруза"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОбъемныйВес"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоМестГруза"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоЗагрузочныхМетровГруза"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТочекПогрузки"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТочекРазгрузки"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТочекВМаршруте"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ГрузоподъемностьТС"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОбъемТС"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоМестТС"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоЗагрузочныхМетровТС"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Расстояние"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОценочнаяСтоимостьГруза"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПростояПоВинеЗаказчикаЧасы"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПростояПоВинеПеревозчикаЧасы"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Расстояние"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяРаботыЧас"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаТС"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ИзменениеРасходаТопливаТС"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТемператураВоздуха"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ФактическиеРасходыПоЗаявке"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПлановаяСуммаИнкассации"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаГорнаяМестность"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаГрузовой"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаДлительнаяЭксплуатация"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаЗимний"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаКлиматКонтроль"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаКондиционер"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаНаселенныйПункт"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаПростой"));			
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаСложностьДорог"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаСпециальноеОборудование"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоГорнаяМестность"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоГрузовой"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоДлительнаяЭксплуатация"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТопливаЗимний"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоКлиматКонтроль"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоКондиционер"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоНаселенныйПункт"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоПростой"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоСложностьДорог"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоСпециальноеОборудование"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Моточасы"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МоточасыСГрузом"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПробегСГрузом"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МассаПрицепногоСостава"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте"));	
 	
	Возврат текРезультат;
	
КонецФункции

// Возвращает список временных параметров.
//
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокВременныхПараметров() Экспорт
	
	текРезультат = Новый СписокЗначений();
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаПогрузкиПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаПогрузкиФактическое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияПогрузкиПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияПогрузкиФактическое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОтправленияСПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОтправленияПоПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПолученияСПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПолученияПоПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаРазгрузкиПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаРазгрузкиФактическое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияРазгрузкиПлановое"));
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияРазгрузкиФактическое"));
		
	Возврат текРезультат;
	
КонецФункции

// Возвращает список временных параметров.
//
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокБулевыхПараметров() Экспорт
	
	текРезультат = Новый СписокЗначений();
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоНормаРасходаТопливаНа100км"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоНормаРасходаТопливаНа1часРаботы"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоПлановыйРасчетТоплива"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоНормативныйРасчетТоплива"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоРасчетТопливаПоДопОборудованию"));	
	
	Возврат текРезультат;

КонецФункции

// Возвращает список параметров относящихся к географическим зонам.
//
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокГеографическиеЗоныПараметры() Экспорт
	
	текРезультат = Новый СписокЗначений();
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаОтправления"));	
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаПолучения"));	
	
	Возврат текРезультат;

КонецФункции

// Возвращает список таких ссылочных параметров, 
//	что при расчете показателя с этим параметром, 
//	значением является ссылка, а не число.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокСсылочныхПараметровСоЗначениемСсылка()
	
	текРезультат = Новый СписокЗначений();
	текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Рейс"));
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает тип параметра.
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  Булево - Результат выполнения.
//
Функция ПолучитьТипПараметра(Ссылка) Экспорт

	текРезультат = Неопределено;
	
	Если ЭтоСсылочныйПараметр(Ссылка) Тогда
		Если Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипТС") Тогда
			текРезультат = Тип("СправочникСсылка.упТипыТС");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаПолучения") 
					ИЛИ Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаОтправления")Тогда 
			текРезультат = Тип("СправочникСсылка.упГеографическиеЗоны");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВидПеревозки") Тогда
			текРезультат = Тип("СправочникСсылка.упВидыПеревозок");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Рейс") Тогда
			текРезультат = Тип("ДокументСсылка.упРейс");		
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТемпературныйРежим") Тогда
			текРезультат = Тип("СправочникСсылка.упТемпературныеРежимы");		
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипГруза") Тогда
			текРезультат = Тип("СправочникСсылка.упТипыГрузов");		
		КонецЕсли;
	ИначеЕсли ЭтоЧисловойПараметр(Ссылка) Тогда	
		текРезультат = Тип("Число");
	ИначеЕсли ЭтоВременнойПараметр(Ссылка) Тогда
		текРезультат = Тип("Дата");
	ИначеЕсли ЭтоБулевыйПараметр(Ссылка) Тогда
		текРезультат = Тип("Булево");
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // ПолучитьТипПараметра()

// Возвращает пустое значение переданного типа.
//
// Параметры:
//  Ссылка - ПеречислениеСсылка.упПараметрыРасчета - параметр расчета.
//
// Возвращаемое значение:
//  СправочникСсылка - Результат выполнения.
//
Функция ПолучитьПустоеЗначение(Ссылка) Экспорт
	
	текРезультат = Неопределено;
	
	Если ЭтоСсылочныйПараметр(Ссылка) Тогда
		Если Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипТС") Тогда
			текРезультат = ПредопределенноеЗначение("Справочник.упТипыТС.ПустаяСсылка");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаПолучения") 
					ИЛИ Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаОтправления")Тогда 
			текРезультат = ПредопределенноеЗначение("Справочник.упГеографическиеЗоны.ПустаяСсылка");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВидПеревозки") Тогда
			текРезультат = ПредопределенноеЗначение("Справочник.упВидыПеревозок.ПустаяСсылка");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипГруза") Тогда
			текРезультат = ПредопределенноеЗначение("Справочник.упТипыГрузов.ПустаяСсылка");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТемпературныйРежим") Тогда	
			текРезультат = ПредопределенноеЗначение("Справочник.упТемпературныеРежимы.ПустаяСсылка");
		ИначеЕсли Ссылка = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Рейс") Тогда	
			текРезультат = ПредопределенноеЗначение("Документ.упРейс.ПустаяСсылка");
		КонецЕсли;
	ИначеЕсли ЭтоЧисловойПараметр(Ссылка) Тогда	
		текРезультат = 0;
	ИначеЕсли ЭтоВременнойПараметр(Ссылка) Тогда
		текРезультат = '00010101';
	ИначеЕсли ЭтоБулевыйПараметр(Ссылка) Тогда
		текРезультат = Ложь;
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

// Возвращае массив доступных параметров.
//
// Параметры:
//  ТипРасчета - ПеречислениеСсылка - параметр расчета.
//  сткПараметрыУчета - Структура - Параметры.
//
// Возвращаемое значение:
//  Массив - Результат выполнения.
//
Функция ПолучитьСписокДоступныхПараметров(ТипРасчета, сткПараметрыУчета) Экспорт
		
	текРезультат = Новый Массив();
	
	Если ТипРасчета = ПредопределенноеЗначение("Перечисление.упТипРасчета.Тарификация") Тогда
		
		УчитыватьКоличествоЗагрузочныхМетров = сткПараметрыУчета.УчитыватьКоличествоЗагрузочныхМетров;
		УчитыватьКоличествоМест				 = сткПараметрыУчета.УчитыватьКоличествоМест;
		УчитыватьМассу						 = сткПараметрыУчета.УчитыватьМассу;
		КонтролироватьТемпературныеРежимы	 = сткПараметрыУчета.КонтролироватьТемпературныеРежимы;
		ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками	= сткПараметрыУчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками;
		ИспользуютсяУслугиИнкассации		 = сткПараметрыУчета.ИспользуютсяУслугиИнкассации;
		ИспользуетсяРейс					 = сткПараметрыУчета.ИспользуетсяРейс;
		
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВидПеревозки"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПлановыеРасходыПоПеревозке"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПредполагаемаяСтоимостьПеревозки"));	
		Если УчитыватьМассу Тогда                       
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МассаГруза"));		
		КонецЕсли;
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОбъемГруза"));
		Если УчитыватьМассу Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОбъемныйВес"));
		КонецЕсли;	
		Если УчитыватьКоличествоМест И ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоМестГруза"));		
		КонецЕсли;
		Если УчитыватьКоличествоЗагрузочныхМетров И ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоЗагрузочныхМетровГруза"));		
		КонецЕсли;
		Если УчитыватьМассу Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ГрузоподъемностьТС"));	
		КонецЕсли;		
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОбъемТС"));	
		Если УчитыватьКоличествоМест Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоМестТС"));	
		КонецЕсли;	
		Если УчитыватьКоличествоЗагрузочныхМетров Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоЗагрузочныхМетровТС"));	
		КонецЕсли;
		Если КонтролироватьТемпературныеРежимы Тогда 
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТемпературныйРежим"));
		КонецЕсли;	
		
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипГруза"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТочекПогрузки"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТочекРазгрузки"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТочекВМаршруте"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Расстояние"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяРаботыЧас"));
		Если ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ОценочнаяСтоимостьГруза"));		
		КонецЕсли;
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаПогрузкиПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаПогрузкиФактическое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияПогрузкиПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияПогрузкиФактическое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОтправленияСПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОтправленияПоПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПолученияСПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПолученияПоПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаРазгрузкиПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяНачалаРазгрузкиФактическое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияРазгрузкиПлановое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяОкончанияРазгрузкиФактическое"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТипТС"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаОтправления"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЗонаПолучения"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПростояПоВинеЗаказчикаЧасы"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяПростояПоВинеПеревозчикаЧасы"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ФактическиеРасходыПоЗаявке"));
		Если ИспользуютсяУслугиИнкассации Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПлановаяСуммаИнкассации"));	
		КонецЕсли;
		Если ИспользуетсяРейс Тогда
			текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Рейс"));		
		КонецЕсли;
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте"));								
	Иначе
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Расстояние"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ВремяРаботыЧас"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоОтопитель"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МассаГруза"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаТС"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаОтопитель"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ТемператураВоздуха"));
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ИзменениеРасходаТопливаТС"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоНормаРасходаТопливаНа100км"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоНормаРасходаТопливаНа1часРаботы"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоПлановыйРасчетТоплива"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоНормативныйРасчетТоплива"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ЭтоРасчетТопливаПоДопОборудованию"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаГорнаяМестность"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаГрузовой"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаДлительнаяЭксплуатация"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаЗимний"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаКлиматКонтроль"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаКондиционер"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаНаселенныйПункт"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаПростой"));			
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаСложностьДорог"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.НормаРасходаТопливаСпециальноеОборудование"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоГорнаяМестность"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоГрузовой"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоДлительнаяЭксплуатация"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоТопливаЗимний"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоКлиматКонтроль"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоКондиционер"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоНаселенныйПункт"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоПростой"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоСложностьДорог"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.КоличествоСпециальноеОборудование"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.Моточасы"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МоточасыСГрузом"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПробегСГрузом"));	
		текРезультат.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.МассаПрицепногоСостава"));	
	
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти

#Область ФункцииДляРаботыСПравиламиРасчета

// Функция - преобразует представление формулы в выражение готовое для выполнения, при этом выполняет следющие действия:
// 				1. удаляет незначащие разделители.
// 				2. заменяет идентификаторы показателей на приведенные.
// 				3. проверяет допустимость показателей и функций.
// 				4. заполняет список переменных.
//
// Параметры:
//  Формула - Строка - Текст формулы.
//  СписокПеременных			 - Массив	 - в эту переменную возвращается список идентификаторов задействованных в формуле.
//  СписокДопустимыхПеременных	 - Массив	 - передается список идентификаторов показателей.
//  СписокДопустимыхФункций		 - Массив	 - передается список идентификаторов функций.
//  Отказ						 - Булево - истина в случае возникновения ошибки.
//  сткИнформацияОбОшибке - Структура - Данные информации.
//
// Возвращаемое значение:
//  Строка - скомпилированная функция.
//
Функция СкомпилироватьФормулуПравилаРасчета(Формула, СписокПеременных, СписокДопустимыхПеременных, СписокДопустимыхФункций, Отказ, сткИнформацияОбОшибке = Неопределено) Экспорт
	
	текРезультат = "";
	
	// Инициализация.
	мсвРазделителей = Новый Массив;
	мсвРазделителей.Добавить(" ");
	мсвРазделителей.Добавить(Символы.ВК);
	мсвРазделителей.Добавить(Символы.ВТаб);
	мсвРазделителей.Добавить(Символы.НПП);
	мсвРазделителей.Добавить(Символы.ПС);
	мсвРазделителей.Добавить(Символы.ПФ);
	мсвРазделителей.Добавить(Символы.Таб);
			
	мсвОпераций = Новый Массив;
	мсвОпераций.Добавить("И");
	мсвОпераций.Добавить("ИЛИ");
	мсвОпераций.Добавить("НЕ");
	мсвОпераций.Добавить("OR");
	мсвОпераций.Добавить("AND");
	мсвОпераций.Добавить("NOT");
	мсвОпераций.Добавить("ИСТИНА");
	мсвОпераций.Добавить("TRUE");
	мсвОпераций.Добавить("ЛОЖЬ");
	мсвОпераций.Добавить("ИСТИНА");

	// Функции для которых не написана реализация проверки формулы
	мсвФункцииБезПроверки = ФункцииБезПроверки();
	
	текОператоры = ".,+-/*?=<>()'";
	текЦифры 	 = "0123456789";

	текПрефиксПеременной = ПолучитьПрефиксПеременной();
	СписокПеременных	 = Новый Массив();
	
	текФормула  = Формула;
	текЛексема	= "";
		
	// Формирование формулы.
	Пока СтрДлина(текФормула) > 0 Цикл
		
		текСимвол = Лев(текФормула,1);
		
		// Если символ является разделителем.
		Если НЕ мсвРазделителей.Найти(текСимвол) = Неопределено Тогда
			текФормула = Прав(текФормула, СтрДлина(текФормула)-1);
			текЛексема = "";
		Иначе	
			
			// Если оператор.
			Если Найти(текОператоры, текСимвол) > 0 Тогда
				текФормула = Прав(текФормула, СтрДлина(текФормула)-1);
				текЛексема = текСимвол;
				
			// Если числовое значение.
			ИначеЕсли Найти(текЦифры, текСимвол) > 0 Тогда	
				текЛексема = ИзвлечьЧисло(текФормула); 	
				
			// Функция, переменная или операция.
			Иначе
				ЭтоФункция = Ложь;
				текЛексема = ВРЕГ(ИзвлечьИдентификатор(текФормула, ЭтоФункция));
				
				Если мсвОпераций.Найти(текЛексема) = Неопределено Тогда
					Если ЭтоФункция Тогда
						Если СписокДопустимыхФункций.НайтиПоЗначению(текЛексема) = Неопределено Тогда
							ТекстСообщения = Нстр("ru = 'В правиле используется недопустимая функция ""%1""'");
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текЛексема);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Формула", "Объект", Отказ);
						Иначе	
							ДополнитьФункциюАргументами(текЛексема, текФормула, СписокПеременных);	
						КонецЕсли;
						
					Иначе
						Если НЕ СписокДопустимыхПеременных.НайтиПоЗначению(текЛексема) = Неопределено Тогда
							Если СписокПеременных.Найти(текЛексема) = Неопределено Тогда
								СписокПеременных.Добавить(текЛексема);	
							КонецЕсли;
							текЛексема = текПрефиксПеременной + текЛексема;	
						Иначе											
							ТекстСообщения = Нстр("ru = 'В правиле используется недопустимый показатель ""%1""'");
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текЛексема);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Формула", "Объект", Отказ);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		текРезультат = текРезультат + текЛексема;
	КонецЦикла;
	
	// Проверка формулы на корректность.
	Если НЕ Отказ Тогда
		сткИнформацияОбОшибке = Неопределено;
		упТарификацияВызовСервера.ПроверитьФормулу(Формула, текРезультат,СписокПеременных, мсвРазделителей,текПрефиксПеременной,Отказ, сткИнформацияОбОшибке);
		Если НЕ сткИнформацияОбОшибке = Неопределено Тогда
			ТекстСообщения = "{("+сткИнформацияОбОшибке.НомерСтроки+","+сткИнформацияОбОшибке.НомерСтолбца+")}"+сткИнформацияОбОшибке.ОписаниеОшибки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Формула", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
		
	Возврат текРезультат;
		
КонецФункции

Процедура ДополнитьФункциюАргументами(Лексема, Формула, СписокПеременных)
	сткАргументыФункции = ДополнительныеАргументыФункций();		
	ДополнительныеАргументыФункции = "";
	Если сткАргументыФункции.Свойство(Лексема, ДополнительныеАргументыФункции) Тогда
		
		мсвДополнительныеАргументыФункции = СтрРазделить(ДополнительныеАргументыФункции, ",");
		
		Если мсвДополнительныеАргументыФункции.Количество() > 0 Тогда
				
			текПрефиксПеременной = ПолучитьПрефиксПеременной();
			
			// Из формулы удаляется открывающаяся скобка после имени функции
			Формула = Прав(Формула, СтрДлина(Формула) - 1);
			
			// Лексема дополняется аргументами
			Лексема = Лексема + "(";
			
			Для каждого ДополнительныйАргумент Из мсвДополнительныеАргументыФункции Цикл
				Если ЗначениеЗаполнено(ДополнительныйАргумент) Тогда
					Лексема = Лексема + текПрефиксПеременной + ДополнительныйАргумент + ", ";
					Если СписокПеременных.Найти(ДополнительныйАргумент) = Неопределено Тогда
						СписокПеременных.Добавить(ДополнительныйАргумент);
					КонецЕсли;
				Иначе
					Лексема = Лексема + Лев(текПрефиксПеременной, СтрДлина(текПрефиксПеременной) - 1) + ", ";
				КонецЕсли;
			КонецЦикла;
			
			// Если аргументов у функции нет
			Если Лев(Формула,1) = ")" Тогда
				
				// Убирается лишняя запятая
				Лексема = Лев(Лексема, СтрДлина(Лексема) - 2);
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ФункцииБезПроверки()
	мсвРезультат = Новый Массив;
	мсвРезультат.Добавить("ПЛАНОВЫЙПРОБЕГВЗОНЕ");
	мсвРезультат.Добавить("ПЛАНОВЫЙПРОБЕГВНЕЗОНЫ");
	мсвРезультат.Добавить("ФАКТИЧЕСКИЙПРОБЕГВЗОНЕ");
	мсвРезультат.Добавить("ФАКТИЧЕСКИЙПРОБЕГВНЕЗОНЫ");
		
	Возврат мсвРезультат;
		
КонецФункции

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Функция - вырезает первое корректное числовое значение из строки слева.
//
// Параметры:
//  Формула - Строка - строковое представление формулы, возвращает формулу без найденной лексемы.
//
// Возвращаемое значение:
//   Строка - лексема.
//
Функция ИзвлечьЧисло(Формула)
	
	текДопустимыеСимволы 	= "0123456789";
	текРазделитель 			= ".";
	ЕстьРазделитель 		= Ложь;
	
	текРезультат = "";
	
	// Формирование формулы.
	Пока СтрДлина(Формула) > 0 Цикл
		
		текСимвол = Лев(Формула,1);
		
		Если  Найти(текДопустимыеСимволы, текСимвол) > 0 Тогда
			текРезультат 	= текРезультат + текСимвол;
			Формула 		= Прав(Формула, СтрДлина(Формула)-1);
			
		ИначеЕсли НЕ ЕстьРазделитель И текСимвол = текРазделитель Тогда 	
			ЕстьРазделитель = Истина;
			текРезультат 	= текРезультат + текСимвол;
			Формула 		= Прав(Формула, СтрДлина(Формула)-1);
			
		Иначе
			Прервать
		КонецЕсли;
	КонецЦикла;	
	
	Возврат текРезультат;
КонецФункции

// Функция - вырезает первый корректный идентификатор из строки слева.
//
// Параметры:
//  Формула	 		- Строка	 - строковое представление формулы, возвращает формулу без найденной лексемы.
//  ЭтоФункция	 	- Булево	 - Возвращает Истина, если найденная лексема есть функция.
//
// Возвращаемое значение:
//  Строка - лексема.
//
Функция ИзвлечьИдентификатор(Формула, ЭтоФункция)
	
	текНеДопустимыеСимволы = ".,+-/*?=<>() ";
	текПризнакФункции	   = "(";
	ЭтоФункция = Ложь;
	
	текРезультат = "";
	
	// Формирование формулы.
	Пока СтрДлина(Формула) > 0 Цикл
		
		текСимвол = Лев(Формула,1);
		
		Если  Найти(текНеДопустимыеСимволы, текСимвол) > 0 Тогда
			Если текСимвол = текПризнакФункции Тогда
				ЭтоФункция = Истина;
			КонецЕсли;
			Прервать;
			
		Иначе
			текРезультат	= текРезультат + текСимвол;
			Формула 		= Прав(Формула, СтрДлина(Формула)-1);
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат текРезультат;
КонецФункции

// Функция - Возвращает префикс переменной, используемый при расчете.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - префикс переменной.
//
Функция ПолучитьПрефиксПеременной() Экспорт
	Возврат "сткПеременные.";		
КонецФункции

Функция ДополнительныеАргументыФункций()
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ПЛАНОВЫЙПРОБЕГВЗОНЕ", "Рейс");
	сткРезультат.Вставить("ПЛАНОВЫЙПРОБЕГВНЕЗОНЫ","Рейс");
	сткРезультат.Вставить("ФАКТИЧЕСКИЙПРОБЕГВЗОНЕ","Рейс");
	сткРезультат.Вставить("ФАКТИЧЕСКИЙПРОБЕГВНЕЗОНЫ","Рейс");
	сткРезультат.Вставить("РАССЧИТАТЬТАРИФНУЮСЕТКУ","");
	Возврат сткРезультат;
КонецФункции

// Функция - Идентификатор показателя по наименованию.
//
// Параметры:
//  Наименование - Строка	 - наименование показателя.
// 
// Возвращаемое значение:
//  Строка - идентификатор показателя
//
Функция ИдентификаторПоказателяПоНаименованию(Наименование) Экспорт
	
	Разделители	=  " .,+,-,/,*,?,=,<,>,(,)%!@#$%&*""№:;{}[]?()\|/`~'^_";
		
	Идентификатор = "";
	БылСпецСимвол = Ложь;
	Для НомСимвола = 1 По СтрДлина(Наименование) Цикл
		Символ = Сред(Наименование, НомСимвола, 1);
		Если Найти(Разделители, Символ) <> 0 Тогда
			БылСпецСимвол = Истина;
		ИначеЕсли БылСпецСимвол Тогда
			БылСпецСимвол = Ложь;
			Идентификатор = Идентификатор + ВРег(Символ);
		Иначе
			Если НомСимвола = 1 Тогда 
				Идентификатор = Идентификатор + ВРег(Символ);
			Иначе
				Идентификатор = Идентификатор + Символ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Результат = СокрЛП(Идентификатор);
	Результат = упСервисныеФункцииКлиентСервер.ПолучитьИмяПоНаименованию(СокрЛП(Результат));

	Возврат Результат;
	
КонецФункции


#КонецОбласти
