
#Область ПрограммныйИнтерфейс

// Создание менеджера объектов.
//
// Возвращаемое значение:
//	Произвольный - Результат выполнения.
//
Функция МенеджерОбъектов() Экспорт 
	
	Возврат Неопределено;
	
	// Подключение компоненты
	Если НЕ ПодключитьВнешнююКомпоненту("ОбщийМакет.упКомпонентаЗащиты", "Licence", ТипВнешнейКомпоненты.Native) Тогда 
		ВызватьИсключение НСтр("ru = 'Ошибка подключения компоненты СЛК'"); 
	КонецЕсли; 
	
	// Создание объекта компоненты
	ОбъектКомпоненты = Новый("AddIn.Licence.LicenceExtension"); 
	
	// Запуск.
	Если НЕ ОбъектКомпоненты.Запуск(Серия(), Истина) Тогда 
		ВызватьИсключение ОбъектКомпоненты.ПолучитьОписаниеОшибки(); 
	КонецЕсли;
	
	// Получаем имя менеджера объектов
	Имя = ОбъектКомпоненты.ПолучитьМенеджерОбъектов();
	Если Имя = Неопределено Тогда
		ВызватьИсключение ОбъектКомпоненты.ПолучитьОписаниеОшибки();
	КонецЕсли;
	
	Защита = Неопределено;
	// Обход ошибки обычного приложения
	Если ТекущийРежимЗапуска() <> РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда
		Попытка
			Защита = Новый ("ОписаниеЗащитыОтОпасныхДействий");
			Защита.ПредупреждатьОбОпасныхДействиях = Ложь;
		Исключение
			// Исключение возможно на предыдущих версиях платформы без механизма
			// защиты от опасных действий
		КонецПопытки;
	КонецЕсли;
	
	// Создание менеджера объектов
	Попытка 
		Если Защита = Неопределено Тогда
			МенеджерОбъектов = ВнешниеОбработки.Создать(Имя, БезопасныйРежим());
		Иначе
			МенеджерОбъектов = ВнешниеОбработки.Создать(
			ВнешниеОбработки.Подключить(ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Имя)), , БезопасныйРежим(), Защита));
		КонецЕсли;
		// Настраиваем менеджер объектов, дополнительно передаем объект для отключения
		// предупреждений об опасных действиях
		МенеджерОбъектов.УстановитьОбъектКомпоненты(ОбъектКомпоненты, Защита);
	Исключение 
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Ошибка создания менеджера объектов СЛК: %1'"), ОписаниеОшибки());
	КонецПопытки;
	
	Возврат МенеджерОбъектов;
	
КонецФункции

// Создание объекта по его имени в файле данных
//
// Возвращаемое значение:
//	Произвольный - Результат выполнения.
//
Функция СоздатьОбъект() Экспорт
	
	текОбъект = Неопределено;
	Для каждого текМакет Из Метаданные.ОбщиеМакеты Цикл
		Если СтрНайти(текМакет.Имя, "упЗащищеннаяОбработка") = 1 Тогда
			текОбъект = упСЛК.МенеджерОбъектов().СоздатьОбъект(СтрШаблон("%1.ЗащищеннаяОбработка", текМакет.Имя));
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат текОбъект;
	
КонецФункции

// Создание объекта по его имени в файле данных
//
// Параметры:
//  АлгоритмОптимизации	 - АлгоритмОптимизации - АлгоритмОптимизации.
// 
// Возвращаемое значение:
//  Произвольный - Результат выполнения.
//
Функция СоздатьОбъектАлгоритмаОптимизации(АлгоритмОптимизации) Экспорт
	
	// Проверка корректности алгоритма оптимизации выполняется предварительно вызовом 
	// функции Справочники.упАлгоритмыОптимизации.ПроверитьКорректностьПоставляемогоАлгоритмаОптимизации.
	текОбъект = Неопределено;
	
	ВнешняяОбработка 	= АлгоритмОптимизации.ВнешняяОбработка;
	ИмяВнешнейОбработки = "";
	Если Истина
		И ЗначениеЗаполнено(ВнешняяОбработка)
		И ЗначениеЗаполнено(ВнешняяОбработка.ТекущаяВерсия) 
	Тогда
		ДвоичныеДанные 			  = РаботаСФайламиСлужебныйВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(ВнешняяОбработка.ТекущаяВерсия).Получить();
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		ИмяВнешнейОбработки       = ВнешниеОбработки.Подключить(АдресВоВременномХранилище,, АлгоритмОптимизации.БезопасныйРежимВнешнейОбработки);
	Иначе
		ВызватьИсключение НСтр(СтрШаблон("ru = 'У подключаемого алгоритма оптимизации %1 не загружена внешняя обработка с произвольным алгоритмом.'", АлгоритмОптимизации));
	КонецЕсли; 
	
	ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВнешнейОбработки, АлгоритмОптимизации.БезопасныйРежимВнешнейОбработки);
	СведенияОбОбработке = ВнешняяОбработка.СведенияОбОбработке();
	МенеджерОбъектов = упСЛК.МенеджерОбъектов();
	ПсевдонимФайла = Строка(Новый УникальныйИдентификатор());
	МенеджерОбъектов.ЗагрузитьИзМакета(ВнешняяОбработка.ПолучитьМакет(СведенияОбОбработке.ИмяМакетаЗащиты), ПсевдонимФайла);
	
	// Создание объекта
	текОбъект = МенеджерОбъектов.СоздатьОбъект(СтрШаблон("%1.%2", ПсевдонимФайла, СведенияОбОбработке.ИмяЗагруженнойВМакетОбработки)); 
	Возврат текОбъект;
	
КонецФункции

// Для отладки
//
// Параметры:
//  Имя	 - Строка	 - имя
// 
// Возвращаемое значение:
//  Строка - 1
//
Функция ПолучитьЗначениеПараметра(Имя) Экспорт 
	Возврат "1";
КонецФункции

// Функция - Адрес консоли
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция АдресКонсоли() Экспорт
	
	Возврат МенеджерОбъектов().ПолучитьАдресКонсоли();
	
КонецФункции

// Функция - Серия
// 
// Возвращаемое значение:
//  Нет - нет возвращаемого значения.
//
Функция Серия() Экспорт
	
	Возврат "537C";
	
КонецФункции

#КонецОбласти
