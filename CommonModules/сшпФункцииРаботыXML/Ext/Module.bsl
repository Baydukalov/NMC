
#Область ПрограммныйИнтерфейс

// Функция - Получить объект потокового чтения
//
// Параметры:
//	ЧитаемаяСтрока - данные, которые необходимо последовательно прочитать.
// 
// Возвращаемое значение:
//	ЧтениеXML - объект чтения XML данных.
//
Функция ПолучитьОбъектПотоковогоЧтения(ЧитаемаяСтрока) Экспорт
	текЧтение = Новый ЧтениеXML;
	текЧтение.УстановитьСтроку(ЧитаемаяСтрока);
	Возврат текЧтение;
КонецФункции

// Функция - Получить объект потоковой записи
//
// Параметры:
//	Кодировка - кодировка, в которой необходимо произвести запись данных.
//
// Возвращаемое значение:
//	ЗаписьXML - объект записи XML данных.
//
Функция ПолучитьОбъектПотоковойЗаписи(Кодировка = "") Экспорт
	текЗапись = Новый ЗаписьXML;
	текЗапись.УстановитьСтроку(Кодировка);
	Возврат текЗапись;
КонецФункции	

// Функция - Прочитать объект из потока
//
// Параметры:
//	Поток - объект чтения XML, которых нужно прочитать.
//	типобъекта - тип объекта для чтения.
// 
// Возвращаемое значение:
//	Результат чтения объекта.
//
Функция ПрочитатьОбъектИзПотока(Поток, типобъекта) Экспорт
	Возврат ФабрикаXDTO.ПрочитатьXML(Поток, типобъекта);
КонецФункции

// Функция - Записать объект поток
//
// Параметры:
//	Объект - объект, который необходимо записать.
//	Фабрика - XDTO-фабрика.
// 
// Возвращаемое значение:
//	Результат записи XDTO-объекта в объект записи XML.
//
Функция ЗаписатьОбъектВПоток(Объект, Фабрика = Неопределено) Экспорт
	xdtoФабрика = ?(Фабрика = Неопределено, ФабрикаXDTO, Фабрика);
	xmlЗапись = ПолучитьОбъектПотоковойЗаписи();
	xdtoФабрика.ЗаписатьXML(xmlЗапись, Объект);
	Возврат xmlЗапись.Закрыть();
КонецФункции

// Функция - Сериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо сериализовать.
// 
// Возвращаемое значение:
//	Результат сериализации объекта.	
//
Функция СериализоватьОбъект(Источник) Экспорт
	
	текОбъектЗаписи = сшпФункцииРаботыXML.ПолучитьОбъектПотоковойЗаписи();
	СериализаторXDTO.ЗаписатьXML(текОбъектЗаписи, Источник, НазначениеТипаXML.Явное);
	Возврат текОбъектЗаписи.Закрыть();
КонецФункции	

// Функция - Десериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо десериализовать.
// 
// Возвращаемое значение:
//	Результат десериализации объекта.	
//
Функция ДесериализоватьОбъект(Источник) Экспорт
	результатОбъект = Неопределено;
	потокЧтение = сшпФункцииРаботыXML.ПолучитьОбъектПотоковогоЧтения(Источник);
	результатОбъект = СериализаторXDTO.ПрочитатьXML(потокЧтение);
	потокЧтение.Закрыть();
	Возврат результатОбъект;
КонецФункции	

// Функция - Сформировать XML
//
// Параметры:
//	сткДанные - входящие данные для формирования.
//	ДобавитьОбъявление - признак необходимости добавления объявления в начало XML-документа.
// 
// Возвращаемое значение:
//	Запись XML.
//
Функция СформироватьXML(сткДанные, ДобавитьОбъявление = Ложь) Экспорт
	текВозврат = Неопределено;
	Если ЗначениеЗаполнено(сткДанные) и ТипЗнч(сткДанные) = Тип("Структура") тогда
		текЗапись = Новый ЗаписьXML;
		текЗапись.УстановитьСтроку("UTF-8");
		Если ДобавитьОбъявление тогда
			текЗапись.ЗаписатьОбъявлениеXML();
		КонецЕсли;
		СформироватьУзелXML(текЗапись, сткДанные, "classData", Истина);
		текВозврат = текЗапись.Закрыть();
	КонецЕсли;
	Возврат текВозврат;
КонецФункции

// Процедура - Сформировать элемент XML
//
// Параметры:
//	ЗаписьXML - текущий объект записи XML.
//	Данные - данные для записи.
//	Имя - имя элемента.
//	ЭтоДанные - признак записи в элемент XML данных или текста.
// 
// Возвращаемое значение:
//	Запись XML.
//
Процедура СформироватьЭлементXML(ЗаписьXML, Данные, Имя, ЭтоДанные = Ложь, ЗаписыватьПустоеЗначение = Ложь) Экспорт
	типЗначения = ТипЗнч(Данные);
	Если типЗначения = Тип("Структура") или типЗначения = Тип("Массив") тогда
		СформироватьУзелXML(ЗаписьXML, Данные, Имя);
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента(Имя);
		Если ЗначениеЗаполнено(Данные) Или ЗаписыватьПустоеЗначение тогда
			Если ЭтоДанные тогда
				ЗаписьXML.ЗаписатьСекциюCDATA(Данные);
			Иначе	
				ЗаписьXML.ЗаписатьТекст(XMLСтрока(Данные));
			КонецЕсли;
		Иначе
			ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
			ЗаписьXML.ЗаписатьАтрибут("xsi:nil", "true");
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
КонецПроцедуры

// Процедура - Сформировать элемент XML
//
// Параметры:
//	Запись - текущий объект записи XML.
//	Данные - данные для записи.
//	Имя - имя узла.
//	ДобавитьПространстваИмен - признак необходимости записи в узел информации о пространстве имён.
// 
// Возвращаемое значение:
//	Запись XML.
//
Процедура СформироватьУзелXML(Запись, Данные, Имя, ДобавитьПространстваИмен = Ложь, Атрибуты = Неопределено) Экспорт
	Запись.ЗаписатьНачалоЭлемента(Имя);
	Если ДобавитьПространстваИмен тогда
		Запись.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	КонецЕсли;	
	Если ТипЗнч(Атрибуты) = Тип("Структура") Тогда
		Для Каждого Атрибут Из Атрибуты Цикл 
			Запись.ЗаписатьАтрибут(Атрибут.Ключ, XMLСтрока(Атрибут.Значение));
		КонецЦикла;
	КонецЕсли;
	Если ТипЗнч(Данные) = Тип("Структура") тогда
		Для каждого текЭлемент из Данные цикл
			СформироватьЭлементXML(Запись, текЭлемент.Значение, текЭлемент.Ключ);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Данные) = Тип("Массив") тогда
		Для каждого текЭлемент из Данные цикл
			типЗначения = ТипЗнч(текЭлемент);
			Если типЗначения = Тип("Структура") тогда
				СформироватьУзелXML(Запись, текЭлемент, "row");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Запись.ЗаписатьКонецЭлемента();
КонецПроцедуры

#КонецОбласти
