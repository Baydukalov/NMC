
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Тарификация", функции для расчета формул правил расчета.
//  
// Инструкция по добавлению нового показателя расчета:
//   1. Добавляется новое значение перечисления "упПараметрыРасчета"
//   2. Добавляется новая картинка в библиотеку "упТипыПоказателей"
//   3. Добавить в макет "ОписаниеПараметровРасчета" горизонтальную
//      область с именем показателя, заполнить описание для каждого
//      объекта расчета(см. вертикальную область);
//   3. Добавить параметр в один из списков 
//      ОМ.упТарификацияКлиентСервере: 
//          *  "ПолучитьСписокЧисловыхПараметров", 
//          *  "ПолучитьСписокВременныхПараметров", 
//          *  "ПолучитьСписокБулевыхПараметров", 
//          *  "ПолучитьСписокСсылочныхПараметров"
//          *  "ПолучитьСписокСсылочныхПараметровСоЗначениемСсылка", 
//          *  "ПолучитьСписокГеографическиеЗоныПараметры".
//   4. Добавить параметр в список доступных параметров, в зависимости от типа
//      "Расчетный" или "ГСМ" в ОМ.упТарификацияКлиентСервере.ПолучитьСписокДоступныхПараметров
//   5. Инициализировать нужный параметр в функциях ОМ.упТарификацияВызовСервера 
//      с префиксом "ПодготовитьВходныеПараметры_"
//   6. Добавить расчет в процедуры ОМ.упТарификацияВызовСервера с префиксами
//      "ТаблицаДанныхДляРасчетаПоКаждойСтроке_" и "ТаблицаДанныхДляРасчетаПоДокументуВЦелом_"
//	
// Инструкция по добавлению новой функции:
//	1. Если функция нестандартная, типа ОЦЕНИТЬПО, то добавить функцию с нужным именем в блок "Функции используемые в формулах".
//	2. Добавить идентификатор функции в функцию ПолучитьСписокДопустимыхФункций().
//	3. Добавить описание функции в ПолучитьТаблицуДопустимыхФункций().
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура - Производит расчет правил по объекту.
//
// Параметры:
//  Объект - ДокументСсылка.упЗаявкаНаПеревозкуГруза - объект по которому производится расчет,
//			 ДокументСсылка.упРейс.							 						
//  ВходныеПараметры - Структура - содержит дополнительные параметры для расчета. 
//			Если содержит элемент с ключом "СпособРасчета", 
//			то производится попытка вычисления функции с именем равным значению этого элемента.
//  сткТрассировка - Структура - описание см. 
//	     ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки.
//
Процедура Рассчитать(Объект, ВходныеПараметры = Неопределено, сткТрассировка = Неопределено) Экспорт

	 УстановитьПривилегированныйРежим(Истина);
	 
	Если ВходныеПараметры = Неопределено Тогда
	 	ВходныеПараметры = Новый Структура();
	КонецЕсли;  
	ВходныеПараметры.Вставить("ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками", ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками"));
	
	упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"ВремяНачалоРасчета",,ТекущаяДатаСеанса(),1);

	текСпособРасчета = "";
	Если ВходныеПараметры.Свойство("СпособРасчета", текСпособРасчета) Тогда
		 Попытка
			 текРезультат = Вычислить(текСпособРасчета + "(Объект, ВходныеПараметры, сткТрассировка)");
		 Исключение	
			 ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			 Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			 КонецЕсли;
			 
			ТекстСообщения = Нстр("ru = 'Функции для способа расчета %1 не существует! %2'");
		     ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Объект, ТекстОшибки);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстОшибки,0);
		 КонецПопытки	
	 Иначе	 
		 текТипЗнч = ТипЗнч(Объект.Ссылка);
		 Если текТипЗнч = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") 
			 	ИЛИ текТипЗнч = Тип("ДокументСсылка.упФактическиеДоходы") 
				ИЛИ текТипЗнч = Тип("ДокументСсылка.упРейс") 
				ИЛИ текТипЗнч = Тип("ДокументСсылка.упЗаявкаНаТС") 
				ИЛИ текТипЗнч = Тип("ДокументСсылка.упПредложениеПеревозчика") 
				ИЛИ текТипЗнч = Тип("ДокументСсылка.упСтрахованиеГруза") 
				ИЛИ текТипЗнч = Тип("ДокументСсылка.упФактическиеРасходы")  Тогда 	
				
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"ОбъектРасчета",,Объект.Ссылка,1);	
				
			РассчитатьПоОбъекту(Объект, ВходныеПараметры, сткТрассировка);	
				 
		ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упПутевойЛист") Тогда
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"ОбъектРасчета",,Объект.Ссылка,1);	
			РассчитатьРасходТопливаПоПутевомуЛисту(Объект, ВходныеПараметры, сткТрассировка);
		Иначе
			 ТекстСообщения = Нстр("ru = 'Функции для заданного объекта с типом %1, не существует'");
			 ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текТипЗнч);
			 упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"Ошибка",,ТекстСообщения,0);
		КонецЕсли; 
	 КонецЕсли;
	 
	 упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,,"ВремяОкончаниеРасчета",,ТекущаяДатаСеанса(),1);
	 упСервисныеФункцииКлиентСервер.СформироватьРасшифровку(сткТрассировка);
	 	
КонецПроцедуры	

// Функция - Вычисляет формулу по заданному правилу расчета
//
// Параметры:
//  ПравилоРасчета	 - СправочникСсылка.упПравилаРасчетаТарифов	 - ссылка на правило расчета
//  Данные		 - Структура							 - структура с дополнительными данными для расчета
//  							                                 (ИдентификаторПеременной, Значение)
//  							                                 Например, "Организация" для фиксированого показателя
//  Отказ			 - Булево								 - Истина, в случае ошибки
//  сткТрассировка	 - Структура							 - описание см. ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки
//  СтрокаРасчета	 - Строка								 - строка табличной части объекта расчета, для передачи в расчет таких параметров
//  								                            как "задание", "рейс". 
//                                                                  Используется в основном для расчета расходов по рейсу и фактических расходов
// 
// Возвращаемое значение:
//  Число - результат расчета
//
Функция ВычислитьФормулуПравилаРасчета(ПравилоРасчета, Данные, Отказ, сткТрассировка = Неопределено, СтрокаРасчета = Неопределено) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ВычислитьФормулуПравилаРасчета(ПравилоРасчета, Данные, Отказ, сткТрассировка, СтрокаРасчета);

КонецФункции 

// Функция - Вычисляет заданную формулу используя значения переменных.
//
// Параметры:
//  Формула - Строка - скомпилированная формула.
//  сткЗначенияПеременных - Структура - (ИмяПеременной, Значение), значения переменных, задействованных в формуле.
//  Отказ - Булево - Истина, в случае неудачи.
//  ВыводитьСообщение - Булево - Истина, пользователю выводятся сообщения об ошибках.
//  текИнформацияОбОшибке - Строка - описание ошибки.
//  сткТрассировка - Структура - описание см. ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки.
//
// Возвращаемое значение:
//   Число - результат вычисления функции.
//
Функция ВычислитьФормулу(Формула, сткЗначенияПеременных, Отказ, ВыводитьСообщение = Истина, текИнформацияОбОшибке = "", сткТрассировка = Неопределено) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ВычислитьФормулу(Формула, сткЗначенияПеременных, Отказ, ВыводитьСообщение, текИнформацияОбОшибке, сткТрассировка);
	
КонецФункции

// Функция - Возвращает строковое представление значения показателя
//
// Параметры:
//  ЗначениеПоказателя	 - Произвольный	 - значение показателя
// 
// Возвращаемое значение:
//  Строка - строковое представление значения показателя
//
Функция СтрокаЗначениеПоказателя(ЗначениеПоказателя) Экспорт
	
	Результат = "";
	
	Если ТипЗнч(ЗначениеПоказателя) = Тип("Массив") Тогда
		Результат = СтрСоединить(ЗначениеПоказателя, ";");
	Иначе	
		Результат = Сокрлп(ЗначениеПоказателя);		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура - Проверяет формулу, вычисляя ее со случайными значениями, 
//				в случае возникновения ошибки, возвращает ее описание и местонахождение.
//
// Параметры:
//  Формула - Строка - Формула.
//  СкомпилированнаяФормула	- Строка - Скомпилированная формула.
//  мсвПеременных - Массив - Массив идентификаторов переменных, задействованных в формуле.
//  мсвРазделители - Массив - Массив разделителей, которые не используются в скомпилированной формуле.
//  ПрефиксПеременной - Строка - Префикс переменной, используется при вычислении фомрулы, не изменяется.
//  Отказ - Булево - Истина, в случае ошибки.
//  сткИнформацияОбОшибке - Структура - (НомерСтроки, НомерСтолбца, ОписаниеОшибки),
//		номер строки и столбца - соответствуют ошибке в формуле.
//
Процедура ПроверитьФормулу(Формула, СкомпилированнаяФормула, мсвПеременных, мсвРазделители, ПрефиксПеременной, Отказ, сткИнформацияОбОшибке = Неопределено) Экспорт
	
	упСЛК.СоздатьОбъект().ПроверитьФормулу(Формула, СкомпилированнаяФормула, мсвПеременных, мсвРазделители, ПрефиксПеременной, Отказ, сткИнформацияОбОшибке)

КонецПроцедуры

// Функция - Рассчитывает несколько показателей переданных в структуре.
//
// Параметры:
//  сткПоказатели - Структура - ИдентификаторПоказателя, СсылкаНаПоказатель.
//  Данные - Структура - (ИдентификаторПеременной, Значение),
//			   ИдентификаторПеременной - так как используется в расчете показателя (см. "РассчитатьПоказатель").
//  Отказ - Булево - Истина в случае ошибки.
//  сткТрассировка - Структура - описание см. ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки.
//
// Возвращаемое значение:
//  Структура - ИдентификаторПоказателя, РассчитанноеЗначение.
//
Функция РассчитатьПоказатели(сткПоказатели, Данные, Отказ, сткТрассировка = Неопределено) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().РассчитатьПоказатели(сткПоказатели, Данные, Отказ, сткТрассировка);
	
КонецФункции

// Функция - вычисляет показатели расчета.
//
// Параметры:
//  Показатель - СправочникСсылка.упПоказателиРасчета - ссылка на показатель.
//  Данные - Структура - исходные данные необходимые для расчета.
//	Отказ - Булево - если не удалось рассчитать то Истина, в противном случае ложь.
//  сткТрассировка - Структура - описание см. ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки.
//
// Возвращаемое значение:
//  Число - рассчитанное значение.
//
Функция РассчитатьПоказатель(Показатель, Данные, Отказ, сткТрассировка = Неопределено) Экспорт

	Возврат упСЛК.СоздатьОбъект().РассчитатьПоказатель(Показатель, Данные, Отказ, сткТрассировка);
	
КонецФункции

// Получает значение по тарифной сетке, где в первом столбце и колонке указаны значения показателей, 
// а на пересечении значения сетки
//
// Параметры:
//  ТарифнаяСетка		 - СправочникСсылка.упТарифныеСетки  - тарифная сетка.
//  Показатели			 - Показатели	 - Показатели
//  ПоказательКолонок	 - ПоказательКолонок	 - ПоказательКолонок
//  ПоказательСтрок		 - ПоказательСтрок	 - ПоказательСтрок
// 
// Возвращаемое значение:
//  Значение - результат по тарифной сетке.
//
Функция ПолучитьЗначениеПоТарифнойСетке(Знач ТарифнаяСетка, Знач Показатели, Знач ПоказательКолонок, Знач ПоказательСтрок) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТарифнаяСетка = Новый ТаблицаЗначений;
		ПоказательКолонок = Справочники.упПоказателиРасчета.ПустаяСсылка();
		ПоказательСтрок = Справочники.упПоказателиРасчета.ПустаяСсылка();
	КонецЕсли;
	ПроверочноеЗначениеКолонок = Показатели[СокрП(ПоказательКолонок.Код)];
	ПроверочноеЗначениеСтрок = Показатели[СокрП(ПоказательСтрок.Код)];
	ЧисловыеЗначенияСсылокКолонок = ПоказательКолонок.ЗначенияПараметра;
	Если ЧисловыеЗначенияСсылокКолонок.Количество() > 0 Тогда
		ТекстСообщения = ("ru = 'Показатель ""%1"" ссылочного типа нельзя использовать в тарифной сетке, т.к. в нем заполнена таблица ""Значения показателя""");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПоказательКолонок);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли; 
	ЧисловыеЗначенияСсылокСтрок = ПоказательСтрок.ЗначенияПараметра;
	Если ЧисловыеЗначенияСсылокСтрок.Количество() > 0 Тогда
		ТекстСообщения = ("ru = 'Показатель ""%1"" ссылочного типа нельзя использовать в тарифной сетке, т.к. в нем заполнена таблица ""Значения показателя""");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПоказательСтрок);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли; 
	
	// Ищем колонку
	СтрокаЗначенийПоказателя = ТарифнаяСетка[0];
	НайденныйИндексКолонки = Неопределено;
	ВГраница = ТарифнаяСетка.Колонки.Количество() - 1;
	
	мсвПроверяемыхЗначенийКолонок = ДополнитьМассивПроверяемыхЗначенийПоказателя(ПроверочноеЗначениеКолонок, ПоказательКолонок);
	
	Для каждого ПроверяемоеЗначениеКолонкиТекущее Из мсвПроверяемыхЗначенийКолонок Цикл
		
		Для Индекс = 0 По ВГраница - 1 Цикл
			Колонка = ТарифнаяСетка.Колонки[ВГраница - Индекс];
			ЗначениеПоказателя = СтрокаЗначенийПоказателя[Колонка.Имя];
			Если ЧисловыеЗначенияСсылокКолонок <> Неопределено Тогда 
				СтрокаЧисловогоЗначения = ЧисловыеЗначенияСсылокКолонок.Найти(ЗначениеПоказателя, "ЗначениеПараметра");
			КонецЕсли; 
			Если Ложь
				Или ЗначениеПоказателя = ПроверяемоеЗначениеКолонкиТекущее 
				Или (Истина
				И упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗнч(ЗначениеПоказателя))
				И СтрокаЧисловогоЗначения <> Неопределено
				И СтрокаЧисловогоЗначения.НомерСтроки = ПроверяемоеЗначениеКолонкиТекущее)
				Тогда 
				НайденныйИндексКолонки = ВГраница - Индекс;
				Прервать;
			ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Число") Тогда 
				Если ЗначениеПоказателя > ПроверяемоеЗначениеКолонкиТекущее Тогда 
					НайденныйИндексКолонки = ВГраница - Индекс;
				Иначе
					Прервать;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
		Если НЕ НайденныйИндексКолонки = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	// Ищем строку
	НайденныйИндексСтроки = Неопределено;
	ВГраница = ТарифнаяСетка.Количество() - 1;
	
	мсвПроверяемыхЗначенийСтрок	= ДополнитьМассивПроверяемыхЗначенийПоказателя(ПроверочноеЗначениеСтрок, ПоказательСтрок);
	
	Для каждого ПроверяемоеЗначениеСтрокиТекущее Из мсвПроверяемыхЗначенийСтрок Цикл
		
		Для Индекс = 0 По ВГраница - 1 Цикл
			ЗначениеПоказателя = ТарифнаяСетка[ВГраница - Индекс][0];
			Если ЧисловыеЗначенияСсылокСтрок <> Неопределено Тогда 
				СтрокаЧисловогоЗначения = ЧисловыеЗначенияСсылокСтрок.Найти(ЗначениеПоказателя, "ЗначениеПараметра");
			КонецЕсли; 
			Если Ложь
				Или ЗначениеПоказателя = ПроверяемоеЗначениеСтрокиТекущее 
				Или (Истина
				И упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗнч(ЗначениеПоказателя))
				И СтрокаЧисловогоЗначения <> Неопределено
				И СтрокаЧисловогоЗначения.НомерСтроки = ПроверяемоеЗначениеСтрокиТекущее)
				Тогда 
				НайденныйИндексСтроки = ВГраница - Индекс;
				Прервать;
			ИначеЕсли ТипЗнч(ЗначениеПоказателя) = Тип("Число") Тогда 
				Если ЗначениеПоказателя > ПроверяемоеЗначениеСтрокиТекущее Тогда 
					НайденныйИндексСтроки = ВГраница - Индекс;
				Иначе
					Прервать;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
		
		Если НЕ НайденныйИндексСтроки = Неопределено Тогда
			Прервать;
		КонецЕсли;

	КонецЦикла;
	
	Если НайденныйИндексСтроки = Неопределено Или НайденныйИндексКолонки = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Для значений ""%1"" показателя ""%2"" и ""%3"" показателя ""%4"" в тарифной сетке не найдено тарифа'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПроверочноеЗначениеСтрок, ПоказательСтрок,
			ПроверочноеЗначениеКолонок, ПоказательКолонок);
		ВызватьИсключение ТекстСообщения;
		Результат = Неопределено;
	Иначе 
		Результат = ТарифнаяСетка[НайденныйИндексСтроки][НайденныйИндексКолонки];
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Для возможности отладки внешние обработки могут создаваться через файловый кэш.
// Этот метод при необходимости обновляет ее в этом кэше и возвращает ее имя,
// используемое в дальнейшем при создании объектов этой внешней обработки.
//
// Параметры:
//  ПравилоРасчета	 - СправочникСсылка.упПравилаРасчета 	 - правило расчета.
//  _ДвоичныеДанные	 - ДвоичныеДанные	 -  не используется.
// 
// Возвращаемое значение:
//  ИмяВнешнейОбработки - Строка.
//
Функция ПроверитьОбновитьФайлВнешнейОбработкиПравила(Знач ПравилоРасчета, _ДвоичныеДанные = Неопределено) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    ПравилоРасчета = Справочники.упПравилаРасчета.ПустаяСсылка();
	КонецЕсли;
	ВнешняяОбработка 	= ПравилоРасчета.ВнешняяОбработка;
	ИмяВнешнейОбработки = "";
	Если Истина
		И ЗначениеЗаполнено(ВнешняяОбработка)
		И ЗначениеЗаполнено(ВнешняяОбработка.ТекущаяВерсия) 
	Тогда
		ДвоичныеДанные 			= РаботаСФайламиСлужебныйВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(ВнешняяОбработка.ТекущаяВерсия).Получить();
		ДатаИзмененияХранилища 	= МестноеВремя(ВнешняяОбработка.ТекущаяВерсияДатаМодификацииФайла);
		КраткоеИмяФайла = "" + ПравилоРасчета;
		КаталогВнешнихОбработокДляОтладки = Константы.упКаталогВнешнихОбработокДляОтладки.Получить();
		Если ЗначениеЗаполнено(КаталогВнешнихОбработокДляОтладки) Тогда
			ИмяВнешнейОбработки = КаталогВнешнихОбработокДляОтладки + "\" + КраткоеИмяФайла + ".epf";
			ФайлВнешнейОбработки = Новый Файл(ИмяВнешнейОбработки);
			Если Ложь
				Или Не ФайлВнешнейОбработки.Существует()
				Или ФайлВнешнейОбработки.ПолучитьВремяИзменения() < ДатаИзмененияХранилища 
			Тогда
				Попытка
					ДвоичныеДанные.Записать(ИмяВнешнейОбработки);
				Исключение
					ОписаниеОшибки = ОписаниеОшибки();
					ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
					ИмяВнешнейОбработки = "";
				КонецПопытки; 
				Если ЗначениеЗаполнено(ИмяВнешнейОбработки) Тогда
					ФайлВнешнейОбработки.УстановитьВремяИзменения(ДатаИзмененияХранилища);
				КонецЕсли; 
			КонецЕсли;
		Иначе
			АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			ИмяВнешнейОбработки = ВнешниеОбработки.Подключить(АдресВоВременномХранилище,, ПравилоРасчета.БезопасныйРежимВнешнейОбработки);
		КонецЕсли;
	Иначе
		ВызватьИсключение "У правила произвольного расчета не загружена внешняя обработка с произвольным алгоритмом.";
	КонецЕсли; 
	Возврат ИмяВнешнейОбработки;

КонецФункции

#КонецОбласти

#Область ФункцииПравилРасчета

// Возвращает таблицу показателей расчета.
//
// Параметры:
//  ТипРасчета - ПеречислениеСсылка - ссылка на тип.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьТаблицуПоказателей(ТипРасчета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПоказателиРасчета.Ссылка КАК Показатель,
	               |	упПоказателиРасчета.Код КАК Идентификатор,
	               |	упПоказателиРасчета.Наименование,
	               |	упПоказателиРасчета.ПараметрРасчета,
	               |	ЕСТЬNULL(упПоказателиРасчета.ПараметрРасчета.Порядок, -1) + 2 КАК ИндексКартинки
	               |ИЗ
	               |	Справочник.упПоказателиРасчета КАК упПоказателиРасчета
	               |ГДЕ
	               |	упПоказателиРасчета.ТипРасчета = &ТипРасчета
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	упПоказателиРасчета.Код";
	Запрос.УстановитьПараметр("ТипРасчета", ТипРасчета);			   
	РезультатЗапроса = Запрос.Выполнить();
		
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// Возвращает таблцу оценочной шкалы.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьТаблицуШкал() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОценочнаяШкала.Ссылка,
	               |	упОценочнаяШкала.Код КАК Шкала,
	               |	упОценочнаяШкала.Наименование
	               |ИЗ
	               |	Справочник.упОценочнаяШкала КАК упОценочнаяШкала
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Шкала";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// Возвращает таблцу тарифных сеток.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьТаблицуТарифныхСеток() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТарифныеСетки.Ссылка,
	               |	упТарифныеСетки.Код КАК ТарифнаяСетка,
	               |	упТарифныеСетки.Наименование
	               |ИЗ
	               |	Справочник.упТарифныеСетки КАК упТарифныеСетки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТарифнаяСетка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// Возвращает масси типовых функий.
//
// Параметры:
//  ЕстьШкалы - Булево - Выполнить.
//
// Возвращаемое значение:
//  Массив - Результат выполнения.
//
Функция ПолучитьСписокДопустимыхФункций(ЕстьШкалы = Истина) Экспорт
	
	мсвРезультат = Новый Массив();
	
	мсвРезультат.Добавить("ЦЕЛ");
	мсвРезультат.Добавить("INT");
	мсвРезультат.Добавить("ОКР");
	мсвРезультат.Добавить("ROUND");
	мсвРезультат.Добавить("МАКС");
	мсвРезультат.Добавить("MAX");
	мсвРезультат.Добавить("МИН");
	мсвРезультат.Добавить("MIN");
	Если ЕстьШкалы Тогда
		мсвРезультат.Добавить("ОЦЕНИТЬПО");		
	КонецЕсли;
	
	мсвРезультат.Добавить("РАССЧИТАТЬТАРИФНУЮСЕТКУ");
	
	мсвРезультат.Добавить("ПЛАНОВЫЙПРОБЕГВЗОНЕ");
	мсвРезультат.Добавить("ПЛАНОВЫЙПРОБЕГВНЕЗОНЫ");
	мсвРезультат.Добавить("ФАКТИЧЕСКИЙПРОБЕГВЗОНЕ");
	мсвРезультат.Добавить("ФАКТИЧЕСКИЙПРОБЕГВНЕЗОНЫ");
	
	мсвРезультат.Добавить("ГОД");
	мсвРезультат.Добавить("YEAR");
	мсвРезультат.Добавить("МЕСЯЦ");
	мсвРезультат.Добавить("MONTH");
	мсвРезультат.Добавить("ДЕНЬ");
	мсвРезультат.Добавить("DAY");
	мсвРезультат.Добавить("ЧАС");
	мсвРезультат.Добавить("HOUR");
	мсвРезультат.Добавить("МИНУТА");
	мсвРезультат.Добавить("MINUTE");
	мсвРезультат.Добавить("СЕКУНДА");
	мсвРезультат.Добавить("SECOND");
	
	мсвРезультат.Добавить("НАЧАЛОГОДА");
	мсвРезультат.Добавить("НАЧАЛОМЕСЯЦА");
	мсвРезультат.Добавить("НАЧАЛОКВАРТАЛА");
	мсвРезультат.Добавить("НАЧАЛОНЕДЕЛИ");
	мсвРезультат.Добавить("НАЧАЛОДНЯ");
	мсвРезультат.Добавить("НАЧАЛОЧАСА");
	мсвРезультат.Добавить("НАЧАЛОМИНУТЫ");
	
	мсвРезультат.Добавить("BEGOFYEAR");
	мсвРезультат.Добавить("BEGOFMONTH");
	мсвРезультат.Добавить("BEGOFQUARTER");
	мсвРезультат.Добавить("BEGOFWEEK");
	мсвРезультат.Добавить("BEGOFDAY");
	мсвРезультат.Добавить("BEGOFHOUR");
	мсвРезультат.Добавить("BEGOFMINUTE");
	
	
	мсвРезультат.Добавить("КОНЕЦГОДА");
	мсвРезультат.Добавить("КОНЕЦКВАРТАЛА");
	мсвРезультат.Добавить("КОНЕЦМЕСЯЦА");
	мсвРезультат.Добавить("КОНЕЦНЕДЕЛИ");
	мсвРезультат.Добавить("КОНЕЦДНЯ");
	мсвРезультат.Добавить("КОНЕЦЧАСА");
	мсвРезультат.Добавить("КОНЕЦМИНУТЫ");
	
	мсвРезультат.Добавить("ENDOFYEAR");
	мсвРезультат.Добавить("ENDOFMONTH");
	мсвРезультат.Добавить("ENDOFQUARTER");
	мсвРезультат.Добавить("ENDOFWEEK");
	мсвРезультат.Добавить("ENDOFDAY");
	мсвРезультат.Добавить("ENDOFHOUR");
	мсвРезультат.Добавить("ENDOFMINUTE");
	
	мсвРезультат.Добавить("НЕДЕЛЯГОДА");
	мсвРезультат.Добавить("WEEKOFYEAR");
	мсвРезультат.Добавить("ДЕНЬГОДА");
	мсвРезультат.Добавить("DAYOFYEAR");
	мсвРезультат.Добавить("ДЕНЬНЕДЕЛИ");
	мсвРезультат.Добавить("WEEKDAY");
	мсвРезультат.Добавить("ДОБАВИТЬМЕСЯЦ");
	мсвРезультат.Добавить("ADDMONTH");
	мсвРезультат.Добавить("ТЕКУЩАЯДАТА");
	мсвРезультат.Добавить("CURRENTDATE");
	

	Возврат мсвРезультат;
	
КонецФункции

// Возвращает таблицу допустимых функций.
//
// Параметры:
//  ЕстьШкалы - Булево - Выполнить.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьТаблицуДопустимыхФункций(ЕстьШкалы = Истина) Экспорт

	тбзРезультат = Новый ТаблицаЗначений;
	текТипСтрока = Новый ОписаниеТипов("Строка");
	
	тбзРезультат.Колонки.Добавить("Наименование", 	текТипСтрока);
	тбзРезультат.Колонки.Добавить("Синтаксис", 		текТипСтрока);
	тбзРезультат.Колонки.Добавить("Подсказка", 		текТипСтрока);
	тбзРезультат.Колонки.Добавить("Аргументы", 		Новый ОписаниеТипов("Массив"));
	тбзРезультат.Колонки.Добавить("Шаблон", 		текТипСтрока);
	тбзРезультат.Колонки.Добавить("КоличествоАргументов", 		Новый ОписаниеТипов("Число"));
	
	// Функции работы со значениями типа Число.
	
	// ЦЕЛ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ЦЕЛ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ЦЕЛ(<Число>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Вычисляет целую часть переданного числа, полностью отсекая дробную часть.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
		
	сткАргумент = Новый Структура("Число", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Цел() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// МАКС.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'МАКС'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'МАКС(<Число1>,...,<ЧислоN>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет максимальное значение из полученных параметров.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
	
	
	сткАргумент = Новый Структура("Число1", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Макс(,) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= -1;
	
	// МИН.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'МИН'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'МИН(<Число1>,...,<ЧислоN>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет минимальное значение из полученных параметров.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
	
	
	сткАргумент = Новый Структура("Число1", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Мин(,) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= -1;

	// ОКР.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ОКР'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ОКР(<Число>, <Разрядность>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Округляет исходное число до нужной разрядности.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
	
	
	сткАргумент = Новый Структура("Число", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Окр(,) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 2;

	
	// ОЦЕНИТЬПО.
	Если ЕстьШкалы Тогда
				
		текСтрока = тбзРезультат.Добавить();
		текСтрока.Наименование 	= Нстр("ru = 'ОЦЕНИТЬПО'"); 
		текСтрока.Синтаксис 	= Нстр("ru = 'ОЦЕНИТЬ(<Шкала>,<Число>)'"); 
		текСтрока.Подсказка 	= Нстр("ru = 'Определяет значение показателя по шкале.'"); 
		
		мсвАргументов = Новый Массив();
		
		мсвТипов = Новый Массив();
		мсвТипов.Добавить(Тип("СправочникСсылка.упОценочнаяШкала"));
		
		сткАргумент = Новый Структура("Шкала", Новый ОписаниеТипов(мсвТипов));
		
		мсвАргументов.Добавить(сткАргумент);
		
		мсвТипов = Новый Массив();
		мсвТипов.Добавить(Тип("Число"));
		
		
		сткАргумент = Новый Структура("Значение", Новый ОписаниеТипов(мсвТипов));
		
		мсвАргументов.Добавить(сткАргумент);
		
		текСтрока.Шаблон				= Нстр("ru = ' ОЦЕНИТЬПО(,) '"); 
		текСтрока.Аргументы				= мсвАргументов;
		текСтрока.КоличествоАргументов 	= 2;
		
	КонецЕсли;
	
	// РАССЧИТАТЬТАРИФНУЮСЕТКУ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'РАССЧИТАТЬТАРИФНУЮСЕТКУ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'РАССЧИТАТЬТАРИФНУЮСЕТКУ(<ТарифнаяСетка>, <Показатель 1, необязательный>, <Показатель 2, необязательный>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет значения показателя по тарифной сетке.'"); 
	
	мсвАргументов = Новый Массив();
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("СправочникСсылка.упТарифныеСетки"));
	
	сткАргумент = Новый Структура("ТарифнаяСетка", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
	мсвТипов.Добавить(Тип("СправочникСсылка.упВидыПеревозок"));
	мсвТипов.Добавить(Тип("СправочникСсылка.упГеографическиеЗоны"));
	мсвТипов.Добавить(Тип("СправочникСсылка.упТипыТС"));

	сткАргумент = Новый Структура("Значение", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
	мсвТипов.Добавить(Тип("СправочникСсылка.упВидыПеревозок"));
	мсвТипов.Добавить(Тип("СправочникСсылка.упГеографическиеЗоны"));
	мсвТипов.Добавить(Тип("СправочникСсылка.упТипыТС"));

	сткАргумент = Новый Структура("Значение", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' РАССЧИТАТЬТАРИФНУЮСЕТКУ(,,) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 3;
	
	// Функции для расчета планового/фактического пробега во/вне географических зон.
	
	// ПлановыйПробегВЗоне.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ПЛАНОВЫЙПРОБЕГВЗОНЕ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ПЛАНОВЫЙПРОБЕГВЗОНЕ(<ГеографическаяЗона>, <ВызватьМаршрутизацию>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет плановый пробег в зоне для рейса (в км). Может долго вычисляться.'"); 
	
	мсвАргументов = Новый Массив();
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("ДокументСсылка.упРейс"));
		
	сткАргумент = Новый Структура("Рейс", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("СправочникСсылка.упГеографическиеЗоны"));
	
	сткАргумент = Новый Структура("ГеографическаяЗона", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
			
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Булево"));
		
	сткАргумент = Новый Структура("ВызватьМаршрутизацию", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' ПЛАНОВЫЙПРОБЕГВЗОНЕ( , ИСТИНА) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 3;
	
	
	// ПлановыйПробегВнеЗоны.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ПЛАНОВЫЙПРОБЕГВНЕЗОНЫ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ПЛАНОВЫЙПРОБЕГВНЕЗОНЫ(<ГеографическаяЗона>, <ВызватьМаршрутизацию>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет плановый пробег вне зоны для рейса (в км). Может долго вычисляться.'"); 
	
	мсвАргументов = Новый Массив();
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("ДокументСсылка.упРейс"));
		
	сткАргумент = Новый Структура("Рейс", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("СправочникСсылка.упГеографическиеЗоны"));
	
	сткАргумент = Новый Структура("ГеографическаяЗона", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
			
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Булево"));
		
	сткАргумент = Новый Структура("ВызватьМаршрутизацию", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' ПЛАНОВЫЙПРОБЕГВНЕЗОНЫ( , ИСТИНА) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 3;
	
	// ФактическийПробегВЗоне.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ФАКТИЧЕСКИЙПРОБЕГВЗОНЕ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ФАКТИЧЕСКИЙПРОБЕГВЗОНЕ(<ГеографическаяЗона>, <ВызватьМаршрутизацию>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет фактический пробег в зоне для рейса (в км).'"); 
	
	мсвАргументов = Новый Массив();
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("ДокументСсылка.упРейс"));
		
	сткАргумент = Новый Структура("Рейс", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("СправочникСсылка.упГеографическиеЗоны"));
	
	сткАргумент = Новый Структура("ГеографическаяЗона", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
			
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Булево"));
		
	сткАргумент = Новый Структура("ВызватьМаршрутизацию", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' ФАКТИЧЕСКИЙПРОБЕГВЗОНЕ( , ИСТИНА) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 3;

	
	// ФактическийПробегВнеЗоны.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ФАКТИЧЕСКИЙПРОБЕГВНЕЗОНЫ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ФАКТИЧЕСКИЙПРОБЕГВНЕЗОНЫ(<ГеографическаяЗона>, <ВызватьМаршрутизацию>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет фактический пробег вне зоны для рейса (в км).'"); 
	
	мсвАргументов = Новый Массив();
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("ДокументСсылка.упРейс"));
		
	сткАргумент = Новый Структура("Рейс", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("СправочникСсылка.упГеографическиеЗоны"));
	
	сткАргумент = Новый Структура("ГеографическаяЗона", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
		
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Булево"));
		
	сткАргумент = Новый Структура("ВызватьМаршрутизацию", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' ФАКТИЧЕСКИЙПРОБЕГВНЕЗОНЫ( , ИСТИНА) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 3;
	
	// Функции работы со значениями типа Дата.
	
	// ГОД.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ГОД'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ГОД(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет год в указанной дате.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	
	текСтрока.Шаблон				= Нстр("ru = ' Год() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// Месяц.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'МЕСЯЦ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'МЕСЯЦ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет месяц в указанной дате.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Месяц() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// ДЕНЬ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ДЕНЬ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ДЕНЬ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет день в указанной дате.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' День() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// ЧАС.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ЧАС'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ЧАС(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет час в указанной дате.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	
	текСтрока.Шаблон				= Нстр("ru = ' Час() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// МИНУТА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'МИНУТА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'МИНУТА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет минуту в указанной дате.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Минута() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// СЕКУНДА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'СЕКУНДА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'СЕКУНДА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет секунда в указанной дате.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' Секунда() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// НАЧАЛОГОДА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОГОДА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОГОДА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала года для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоГода() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// НАЧАЛОКВАРТАЛА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОКВАРТАЛА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОКВАРТАЛА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала квартала для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоКвартала() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// НАЧАЛОМЕСЯЦА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОМЕСЯЦА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОМЕСЯЦА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала месяца для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоМесяца() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// НАЧАЛОНЕДЕЛИ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОНЕДЕЛИ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОНЕДЕЛИ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала недели для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоНедели() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// НАЧАЛОДНЯ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОДНЯ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОДНЯ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала дня для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоДня() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// НАЧАЛОЧАСА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОЧАСА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОЧАСА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала часа для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоЧаса() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// НАЧАЛОМИНУТЫ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НАЧАЛОМИНУТЫ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НАЧАЛОМИНУТЫ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время начала минуты для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НачалоМинуты() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// КОНЕЦГОДА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦГОДА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦГОДА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца года для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецГода() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// КОНЕЦКВАРТАЛА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦКВАРТАЛА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦКВАРТАЛА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца квартала для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецКвартала() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// КОНЕЦМЕСЯЦА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦМЕСЯЦА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦМЕСЯЦА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца месяца для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецМесяца() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// КОНЕЦНЕДЕЛИ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦНЕДЕЛИ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦНЕДЕЛИ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца недели для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецНедели() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// КОНЕЦДНЯ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦДНЯ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦДНЯ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца дня для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецДня() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// КОНЕЦЧАСА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦЧАСА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦЧАСА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца часа для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецЧаса() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// КОНЕЦМИНУТЫ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'КОНЕЦМИНУТЫ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'КОНЕЦМИНУТЫ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет дату и время конца минуты для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' КонецМинуты() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// НЕДЕЛЯГОДА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'НЕДЕЛЯГОДА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'НЕДЕЛЯГОДА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет номер недели в году для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' НеделяГода() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// ДЕНЬГОДА.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ДЕНЬГОДА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ДЕНЬГОДА(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет номер дня в году для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' ДеньГода() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;

	// ДЕНЬНЕДЕЛИ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ДЕНЬНЕДЕЛИ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ДЕНЬНЕДЕЛИ(<Дата>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет номер дня недели для указанной даты.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	текСтрока.Шаблон				= Нстр("ru = ' ДеньНедели() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 1;
	
	// ДОБАВИТЬМЕСЯЦ.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ДОБАВИТЬМЕСЯЦ'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ДОБАВИТЬМЕСЯЦ(<Дата>, <ЧислоМесяцев>)'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Добавляет (или вычитает) к указанной дате заданное число месяцев.'"); 
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Дата"));
	
		
	сткАргумент = Новый Структура("Дата", Новый ОписаниеТипов(мсвТипов));
	
	мсвАргументов = Новый Массив();
	мсвАргументов.Добавить(сткАргумент);
	
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(Тип("Число"));
	
	сткАргумент = Новый Структура("Число", Новый ОписаниеТипов(мсвТипов));
	мсвАргументов.Добавить(сткАргумент);

	
	текСтрока.Шаблон				= Нстр("ru = ' ДобавитьМесяц(,) '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 2;
	
	// ТекущаяДата.
	текСтрока = тбзРезультат.Добавить();
	текСтрока.Наименование 	= Нстр("ru = 'ТЕКУЩАЯДАТА'"); 
	текСтрока.Синтаксис 	= Нстр("ru = 'ТЕКУЩАЯДАТА()'"); 
	текСтрока.Подсказка 	= Нстр("ru = 'Определяет текущую (системную) дату на компьютере.'"); 
	
	мсвАргументов = Новый Массив();
	
	текСтрока.Шаблон				= Нстр("ru = ' ТекущаяДата() '"); 
	текСтрока.Аргументы				= мсвАргументов;
	текСтрока.КоличествоАргументов 	= 0;
	
	Возврат тбзРезультат;
КонецФункции	

// Функция - Возвращает список ссылок на переменные(показателей и шкал) по массиву идентификаторов.
//
// Параметры:
//  мсвИдентификаторов - Массив - массив кодов переменных, "Показатели расчета" или "Оценочная шкала".
//	ВыгружатьДерево - Булево - Истина, выгружается таблица. Ложь, выгружается дерево.
//
// Возвращаемое значение:
//  ДеревоЗначений - ТаблицаЗначений, Результа выполнения.
//
Функция ПолучитьПеременные(мсвИдентификаторов, ВыгружатьДерево = Истина) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ПолучитьПеременные(мсвИдентификаторов, ВыгружатьДерево);	
	
КонецФункции

// Возвращает строку типового примера.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - Результа выполнения.
//
Функция ПолучитьДемоПримерФормулы() Экспорт
	
	текРезультат = "Окр(2.5) + ДеньГода(ТекущаяДата())";
	
	ЕстьПоказатель 			= Ложь;
	ЕстьСсылочныйПоказатель = Ложь;
	ЕстьВременнойПоказатель = Ложь;
	ЕстьОценочнаяШкала		= Ложь;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти

#Область ФункцииРасчетаПоОбъектам

Процедура РассчитатьПоОбъекту(Объект, ВходныеПараметры, сткТрассировка)
	
	ПодготовитьВходныеПараметры(Объект, ВходныеПараметры, сткТрассировка);

	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	                  |	тбПравилаРасчета.ПравилоРасчета,
	                  |	тбПравилаРасчета.СтатьяДоходовРасходов,
	                  |	тбПравилаРасчета.Валюта,
	                  |	тбПравилаРасчета.ПравилоРасчета.РасчетПроводитсяПоКаждойСтроке КАК РасчетПроводитсяПоКаждойСтроке,
	                  |	тбПравилаРасчета.ПравилоРасчета.Формула КАК Формула
	                  |ПОМЕСТИТЬ втПравилаРасчета
	                  |ИЗ
	                  |	РегистрСведений.упПравилаРасчетаТарифов.СрезПоследних(
	                  |			&Период,
	                  |			СтатьяДоходовРасходов В (&СписокСтатей)
	                  |				И ГруппаТарифов = &ГруппаТарифов
	                  |				И Организация = &Организация) КАК тбПравилаРасчета
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                  |	ВЫРАЗИТЬ(упПравилаРасчетаПеременные.Значение КАК Справочник.упПоказателиРасчета).ПараметрРасчета КАК ПараметрРасчета,
	                  |	втПравилаРасчета.РасчетПроводитсяПоКаждойСтроке,
	                  |	втПравилаРасчета.ПравилоРасчета,
	                  |	втПравилаРасчета.СтатьяДоходовРасходов
	                  |ПОМЕСТИТЬ втПараметрыПоПравиламРасчета
	                  |ИЗ
	                  |	Справочник.упПравилаРасчета.Переменные КАК упПравилаРасчетаПеременные
	                  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПравилаРасчета КАК втПравилаРасчета
	                  |		ПО упПравилаРасчетаПеременные.Ссылка = втПравилаРасчета.ПравилоРасчета
	                  |ГДЕ
	                  |	упПравилаРасчетаПеременные.Значение ССЫЛКА Справочник.упПоказателиРасчета
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                  |	втПараметрыПоПравиламРасчета.ПараметрРасчета,
	                  |	втПараметрыПоПравиламРасчета.РасчетПроводитсяПоКаждойСтроке
	                  |ИЗ
	                  |	втПараметрыПоПравиламРасчета КАК втПараметрыПоПравиламРасчета
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	втПравилаРасчета.ПравилоРасчета,
	                  |	втПравилаРасчета.СтатьяДоходовРасходов,
	                  |	втПравилаРасчета.Валюта,
	                  |	втПравилаРасчета.РасчетПроводитсяПоКаждойСтроке,
	                  |	втПравилаРасчета.СтатьяДоходовРасходов.ТипСтатьиДоходовИРасходов КАК ТипСтатьиДоходовИРасходов,
	                  |	втПравилаРасчета.Формула
	                  |ИЗ
	                  |	втПравилаРасчета КАК втПравилаРасчета
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	втПараметрыПоПравиламРасчета.ПараметрРасчета КАК ПараметрРасчета,
	                  |	втПараметрыПоПравиламРасчета.РасчетПроводитсяПоКаждойСтроке КАК РасчетПроводитсяПоКаждойСтроке,
	                  |	втПараметрыПоПравиламРасчета.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов
	                  |ИЗ
	                  |	втПараметрыПоПравиламРасчета КАК втПараметрыПоПравиламРасчета
	                  |ИТОГИ
	                  |	МАКСИМУМ(РасчетПроводитсяПоКаждойСтроке)
	                  |ПО
	                  |	СтатьяДоходовРасходов";
	текЗапрос.УстановитьПараметр("СписокСтатей", 			ВходныеПараметры.СписокСтатей);
	текЗапрос.УстановитьПараметр("ГруппаТарифов", 			ВходныеПараметры.ГруппаТарифов);
	текЗапрос.УстановитьПараметр("Организация", 			ВходныеПараметры.Организация);
	текЗапрос.УстановитьПараметр("Период", 					ВходныеПараметры.Период);
	
	мсвРезультатЗапроса = текЗапрос.ВыполнитьПакет();
	
	// Используемые правила расчета.
	тбзПравилаРасчета	= мсвРезультатЗапроса[3].Выгрузить();
	
	тбзПараметрыРасчета = мсвРезультатЗапроса[2].Выгрузить();
	
	дрвПараметрыПоПравиламРасчета = мсвРезультатЗапроса[4].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	тбзПараметрыДляРасчетаПоДокументуЦеликом	= тбзПараметрыРасчета.Скопировать(Новый Структура("РасчетПроводитсяПоКаждойСтроке",Ложь));
	тбзПараметрыДляРасчетаПоКаждойСтроке		= тбзПараметрыРасчета.Скопировать(Новый Структура("РасчетПроводитсяПоКаждойСтроке",Истина));
	
	
	сткДанныеПоОбъектуРасчета 					= упСЛК.СоздатьОбъект().СформироватьСтруктуруДанных(Объект, ВходныеПараметры);
	
	мсвПараметрыДляРасчетаПоДокументуВЦелом		= тбзПараметрыДляРасчетаПоДокументуЦеликом.ВыгрузитьКолонку("ПараметрРасчета");
	мсвПараметрыДляРасчетаПоКаждойСтроке		= тбзПараметрыДляРасчетаПоКаждойСтроке.ВыгрузитьКолонку("ПараметрРасчета");
	
	// Добавляются все параметры используемые в формулах распределения
	ЕстьПравилоРаспределения = ВходныеПараметры.Свойство("ПравилоРаспределения");
	
	Если ЕстьПравилоРаспределения Тогда
		
		мсвПараметрыФормулыРаспределения = ПараметрыИспользуемыеВФормулеРаспределения();
		Для каждого ПараметрФормулы Из мсвПараметрыФормулыРаспределения Цикл
			
			Если мсвПараметрыДляРасчетаПоДокументуВЦелом.Количество() > 0 Тогда
				Если мсвПараметрыДляРасчетаПоДокументуВЦелом.Найти(ПараметрФормулы) = Неопределено Тогда
					мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(ПараметрФормулы);
				КонецЕсли;	
			КонецЕсли;
			
			Если мсвПараметрыДляРасчетаПоКаждойСтроке.Количество() > 0 Тогда
				Если мсвПараметрыДляРасчетаПоКаждойСтроке.Найти(ПараметрФормулы) = Неопределено Тогда
					мсвПараметрыДляРасчетаПоКаждойСтроке.Добавить(ПараметрФормулы);
				КонецЕсли;	
			КонецЕсли;

		КонецЦикла;
				
	КонецЕсли;
	
	ПодготовитьПараметрыРасчета(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
	
	// Данные для расчета по документу в целом.
	тбзДанныеДляРасчетаПоДокументуВЦелом 		= ТаблицаДанныхДляРасчетаПоДокументуВЦелом(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, дрвПараметрыПоПравиламРасчета);
				
	// Данные для расчета по каждой строке.
	тбзДанныеДляРасчетаПоКаждойСтроке 			= ТаблицаДанныхДляРасчетаПоКаждойСтроке(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоКаждойСтроке, дрвПараметрыПоПравиламРасчета);
	
	Сумма				= 0;
	СуммаНДС 			= 0;
	НомерСтрокиРасчета  = 0;
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		
	
	ИмяТабличнойЧасти 	= ВходныеПараметры.ИмяТабличнойЧастиСтатей;
	ИмяРеквизитаСтатьи	= ВходныеПараметры.ИмяРеквизитаСтатьи;
	ИмяРеквизитаСумма	= ВходныеПараметры.ИмяРеквизитаСумма;
	ИмяРеквизитаСуммаНДС= ВходныеПараметры.ИмяРеквизитаСуммаНДС;
	
	// Производится расчет каждой статьи таблицы статей доходов/расходов.
	Для каждого СтрокаРасчета Из Объект[ИмяТабличнойЧасти] Цикл
		
		Отказ = Ложь;
		СтрокаРасчета.Сумма	= 0;
		
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "НомерСтроки", Истина, НомерСтрокиРасчета,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "Наименование",,СтрокаРасчета[ИмяРеквизитаСтатьи],1);
		
		сткОтбора = Новый Структура("СтатьяДоходовРасходов", СтрокаРасчета[ИмяРеквизитаСтатьи]);
		Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(СтрокаРасчета.Валюта) Тогда
			сткОтбора.Вставить("Валюта", СтрокаРасчета.Валюта)
		КонецЕсли;
		
		мсвСтрокиСПравилом = тбзПравилаРасчета.НайтиСтроки(сткОтбора);
		
		СтрокаСПравилом = тбзПравилаРасчета.Найти(СтрокаРасчета[ИмяРеквизитаСтатьи], "СтатьяДоходовРасходов");
		Если мсвСтрокиСПравилом.Количество()>0 Тогда
			СтрокаСПравилом = мсвСтрокиСПравилом[0];
			
			ПравилоРасчета	= СтрокаСПравилом.ПравилоРасчета;
			
			ПравилоРаспределения		= Неопределено;
			
			Если ЕстьПравилоРаспределения Тогда
				ПравилоРаспределения = СтрокаРасчета.ПравилоРаспределения;
			КонецЕсли;
						
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "Правило",,ПравилоРасчета,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "РасчетПоСтроке",,СтрокаСПравилом.РасчетПроводитсяПоКаждойСтроке,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "Описание",,,2);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "Формула",,СтрокаСПравилом.Формула,1);
					
			Если ВедетсяВалютныйУчет Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаРасчета.Валюта) Тогда
					СтрокаРасчета.Валюта	= СтрокаСПравилом.Валюта;	
				КонецЕсли;
			Иначе
				СтрокаРасчета.Валюта	= Справочники.Валюты.ПустаяСсылка();
			КонецЕсли;	
			
			Если СтрокаСПравилом.РасчетПроводитсяПоКаждойСтроке Тогда
				мсвДанныеДляРасчета = ОтфильтроватьПоДопКолонкам(СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчетаПоКаждойСтроке, ); 
				ЦенаИтог = 0;
				й_Шаг = 1;
				Для каждого СтрокаДанные Из мсвДанныеДляРасчета Цикл
					сткДанныеПоСтроке = СтрокаДанные.СтруктураДанные;

					Если сткДанныеПоСтроке.Свойство("ОбъектРасчета") Тогда
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "НомерСтроки",Истина,й_Шаг,1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Имя",,сткДанныеПоСтроке.ОбъектРасчета.Имя,1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Значение",,сткДанныеПоСтроке.ОбъектРасчета.Значение,1);	
					Иначе	
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "НомерСтроки",Истина,1,1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Имя",,	" По документу в целом",1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Значение",,"",1);
	    				КонецЕсли;
					
					РезультатВычисления = ВычислитьФормулуПравилаРасчета(ПравилоРасчета, сткДанныеПоСтроке, Отказ, сткТрассировка, СтрокаРасчета);
					
					ЦенаИтог = ЦенаИтог + ?(НЕ РезультатВычисления = Неопределено, РезультатВычисления, 0);

					й_Шаг = й_Шаг + 1;
				КонецЦикла;
				СтрокаРасчета.Цена	= ЦенаИтог;		
			Иначе	
				Если ЕстьПравилоРаспределения Тогда
					
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "НомерСтроки",Истина,1,1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Имя",,	" По документу в целом",1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Значение",,"",1);
					
					СтрокаРасчета.Цена	= ВычислитьФормулуПравилаРасчетаУчитываяКоэффициентРаспределения(Объект, ПравилоРасчета, ПравилоРаспределения, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчетаПоДокументуВЦелом, Отказ, сткТрассировка);		
									
					
				Иначе	
					мсвДанныеДляРасчета = ОтфильтроватьПоДопКолонкам(СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчетаПоДокументуВЦелом, Ложь); 
					Если мсвДанныеДляРасчета.Количество() > 0  Тогда
						
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "НомерСтроки",Истина,1,1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Имя",,	" По документу в целом",1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Значение",,"",1);
						
						СтрокаРасчета.Цена	= ВычислитьФормулуПравилаРасчета(ПравилоРасчета, мсвДанныеДляРасчета[0].СтруктураДанные, Отказ, сткТрассировка, СтрокаРасчета);		
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья", "ИтогоПоСтатье",,СтрокаРасчета.Цена,1);
			
			Если СтрокаСПравилом.ТипСтатьиДоходовИРасходов = Перечисления.упТипыСтатейДоходовРасходов.СтатьяДоходов Тогда
				упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуПриИзмененииЗначения(СтрокаРасчета,"Цена");
			Иначе
				упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(СтрокаРасчета,"Цена");
			КонецЕсли;
			
		Иначе
			
			// Сообщение трассировки.
			ТекстСообщения = НСтр("ru='Для статьи ""%1"", организации ""%2"", группы тарифов ""%3"" не задано правило расчета на дату %4'");	
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, сокрлп(СтрокаРасчета[ИмяРеквизитаСтатьи]), 
																								сокрлп(ВходныеПараметры.Организация),
																								сокрлп(ВходныеПараметры.ГруппаТарифов),
																								сокрлп(ВходныеПараметры.Период));
            упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Статья","Ошибка",,ТекстСообщения,0,Новый Структура("Ссылка, Поле",ВходныеПараметры.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект."+ИмяТабличнойЧасти, НомерСтрокиРасчета, ИмяРеквизитаСтатьи)));
		КонецЕсли;
		
		Если ВедетсяВалютныйУчет Тогда
			 Сумма 		= Сумма + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(СтрокаРасчета.Валюта, СтрокаРасчета.Сумма, ВходныеПараметры.Период);
			 СуммаНДС 	= СуммаНДС + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(СтрокаРасчета.Валюта, СтрокаРасчета.СуммаНДС, ВходныеПараметры.Период);
		Иначе	
			 Сумма 		= Сумма + СтрокаРасчета.Сумма;
			 СуммаНДС 	= СуммаНДС + СтрокаРасчета.СуммаНДС;
		КонецЕсли;
		
		НомерСтрокиРасчета = НомерСтрокиРасчета + 1
		
	КонецЦикла;
	
	Объект[ИмяРеквизитаСумма]		= Сумма;
	Объект[ИмяРеквизитаСуммаНДС]	= СуммаНДС;
	
КонецПроцедуры

// Возвращает признак расчета по списку перевозчиков.
//
// Параметры:
//  Объект - ТаблицаЗначений - Таблица данных.
//	ВходныеПараметры - Структура - Параметры.
//	сткТрассировка - Структура - параметры трассировки.
//
// Возвращаемое значение:
//  Булево - Результа выполнения.
//
Функция РассчитатьПоСпискуПеревозчиков(Объект, ВходныеПараметры, сткТрассировка) Экспорт
	текРезультат = Истина;
	
	текЗапросПеревозчики = Новый Запрос;
	текЗапросПеревозчики.Текст = "ВЫБРАТЬ
	                             |	втПеревозчики.Перевозчик,
	                             |	втПеревозчики.Соглашение
	                             |ПОМЕСТИТЬ втСоглашения
	                             |ИЗ
	                             |	&тбзПеревозчики КАК втПеревозчики
	                             |;
	                             |
	                             |////////////////////////////////////////////////////////////////////////////////
	                             |ВЫБРАТЬ
	                             |	втСоглашения.Перевозчик,
	                             |	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов,
	                             |	упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка КАК Соглашение,
	                             |	упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка.ГруппаТарифов КАК ГруппаТарифов
	                             |ПОМЕСТИТЬ втСтатьиРасходовПоПеревозчикам
	                             |ИЗ
	                             |	Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК упСоглашенияСПеревозчикамиСтатьиРасходов
	                             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСоглашения КАК втСоглашения
	                             |		ПО упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка = втСоглашения.Соглашение
	                             |;
	                             |
	                             |////////////////////////////////////////////////////////////////////////////////
	                             |ВЫБРАТЬ
	                             |	втСтатьиРасходовПоПеревозчикам.Перевозчик КАК Перевозчик,
	                             |	втСтатьиРасходовПоПеревозчикам.СтатьяРасходов,
	                             |	втСтатьиРасходовПоПеревозчикам.Соглашение КАК Соглашение,
	                             |	втСтатьиРасходовПоПеревозчикам.ГруппаТарифов КАК ГруппаТарифов
	                             |ИЗ
	                             |	втСтатьиРасходовПоПеревозчикам КАК втСтатьиРасходовПоПеревозчикам
	                             |ИТОГИ ПО
	                             |	Перевозчик,
	                             |	Соглашение,
	                             |	ГруппаТарифов";
	текЗапросПеревозчики.УстановитьПараметр("тбзПеревозчики", Объект);								 
	
	текВыборкаПеревозчик	= текЗапросПеревозчики.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	текРейс = ВходныеПараметры.Рейс;
	сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текРейс, "Организация, Дата, ВидПеревозки");
	текОрганизация 		= сткРеквизитыРейса.Организация;
	текДата				= сткРеквизитыРейса.Дата;
	текВидПеревозки		= сткРеквизитыРейса.ВидПеревозки;
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ВедетсяВалютныйУчет Тогда
		ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	Иначе
		ВалютаУпрУчета = Справочники.Валюты.ПустаяСсылка();	
	КонецЕсли;
	
	Пока текВыборкаПеревозчик.Следующий() Цикл
		
		текПеревозчик = текВыборкаПеревозчик.Перевозчик;
		
		текВыборкаСоглашение = текВыборкаПеревозчик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
		Пока текВыборкаСоглашение.Следующий() Цикл
			
			текСоглашение = текВыборкаСоглашение.Соглашение;
			
			текВыборкаГруппаТарифов = текВыборкаСоглашение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока текВыборкаГруппаТарифов.Следующий() Цикл
				
				текГруппаТарифов = текВыборкаГруппаТарифов.ГруппаТарифов;
				
				// Формируется табличная часть объекта.
				тбзСтатьиРасходов = Новый ТаблицаЗначений;
				тбзСтатьиРасходов.Колонки.Добавить("СтатьяРасходов", 		Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
				тбзСтатьиРасходов.Колонки.Добавить("Задание", 				Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
				тбзСтатьиРасходов.Колонки.Добавить("Сумма", 				Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("Цена", 					Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("Количество", 			Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("Валюта",				Новый ОписаниеТипов("СправочникСсылка.Валюты"));
				тбзСтатьиРасходов.Колонки.Добавить("СтавкаНДС",				Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"));
				тбзСтатьиРасходов.Колонки.Добавить("СуммаНДС",				Новый ОписаниеТипов("Число"));
				
				текВыборкаСтатьяРасходов = текВыборкаГруппаТарифов.Выбрать();
				
				мсвСтатейРасходов = Новый Массив();
				Пока текВыборкаСтатьяРасходов.Следующий() Цикл
					
					мсвСтатейРасходов.Добавить(текВыборкаСтатьяРасходов.СтатьяРасходов);
					
					НоваяСтрока = тбзСтатьиРасходов.Добавить();
					НоваяСтрока.СтатьяРасходов 	=  текВыборкаСтатьяРасходов.СтатьяРасходов;
					НоваяСтрока.Валюта			= ВалютаУпрУчета;
					НоваяСтрока.Сумма			= 0;
					НоваяСтрока.Цена			= 0;
					НоваяСтрока.Количество		= 1;
					НоваяСтрока.СуммаНДС		= 0;
				
				КонецЦикла;
				
				// Формируется объект.
				сткОбъект = Новый Структура();
				сткОбъект.Вставить("Ссылка", 			текРейс);
				сткОбъект.Вставить("Организация", 		текОрганизация);
				сткОбъект.Вставить("Дата", 				текДата);
				сткОбъект.Вставить("СуммаРасходы", 		0);
				сткОбъект.Вставить("СуммаРасходыНДС", 	0);
				сткОбъект.Вставить("ПлановыеРасходы", 	тбзСтатьиРасходов);
				сткОбъект.Вставить("ВидПеревозки", 		текВидПеревозки);
								
				ВходныеПараметры = Новый Структура();
				ВходныеПараметры.Вставить("СписокСтатей", мсвСтатейРасходов);
				ВходныеПараметры.Вставить("ГруппаТарифов", текГруппаТарифов);
				
				// Объект рассчитывается.
				Рассчитать(сткОбъект, ВходныеПараметры);
							
				мсвСтрокПеревозчиков 				= Объект.НайтиСтроки(Новый Структура("Перевозчик, Соглашение",текПеревозчик, текСоглашение));
				Для каждого текСтрокаСПеревозчиком Из мсвСтрокПеревозчиков Цикл
					текСтрокаСПеревозчиком.Расходы	= сткОбъект.СуммаРасходы;
				КонецЦикла;
								
			КонецЦикла;
				
		КонецЦикла;
		
	КонецЦикла;

	Возврат текРезультат;
КонецФункции

// Функция - Рассчитывает расходы по в разрезе перевозчик, типТС рейс,
//			 заполняет таблицу тбзПеревозчики,
//			 Расходы по рейсу состоят из:
//				1. Расходы по доп. операциям по рейсу.
//				2. Расходы по соглашению с перевозчиком.
//				3. Расходы по типу ТС.
//
// Параметры:
//  Объект - ТаблицаЗначений - (Рейс:ДокументСсылка.упРейсы), Таблица данных.
//  ВходныеПараметры - Структура - Параметры.
//  сткТрассировка - Структура - описание см. ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки.
// 
// Возвращаемое значение:
//  Структура - Результа выполнения. Истина, в случае безошибочного расчета.
//
Функция РассчитатьРейсыПоПеревозчикам(Объект, ВходныеПараметры, сткТрассировка) Экспорт
	
	тбзРейсы 				= Объект;
	тбзРейсы.Колонки.Добавить("ПлановыеРасходыПоРейсам", 			Новый ОписаниеТипов("ТаблицаЗначений"));
	тбзРейсы.Колонки.Добавить("ПлановыеРасходыПоРейсамРасшифровка", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	тбзРасходыДляПодбора	= Неопределено;
	
	РезультатАдресВоВременномХранилище						= ВходныеПараметры.РезультатАдресВоВременномХранилище;
	ПризнакЗавершенияВыполненияАдресВоВременномХранилище	= ВходныеПараметры.ПризнакЗавершенияВыполненияАдресВоВременномХранилище;
	
	Если РезультатАдресВоВременномХранилище <> Неопределено И (Не ЭтоАдресВременногоХранилища(РезультатАдресВоВременномХранилище) Или Не ЭтоАдресВременногоХранилища(ПризнакЗавершенияВыполненияАдресВоВременномХранилище)) Тогда
		ПоместитьВоВременноеХранилище(Истина, ПризнакЗавершенияВыполненияАдресВоВременномХранилище);
		Возврат Ложь;
	КонецЕсли;
	
	тбзПеревозчикиТипыТС = ВходныеПараметры.тбзПеревозчики;
	
	тбзПеревозчикиТипыТС = Новый ТаблицаЗначений;
	тбзПеревозчикиТипыТС.Колонки.Добавить("Перевозчик", Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
	тбзПеревозчикиТипыТС.Колонки.Добавить("ТипТС", 		Новый ОписаниеТипов("СправочникСсылка.упТипыТС"));
	Для каждого Строка Из ВходныеПараметры.тбзПеревозчики Цикл
		НоваяСтрока = тбзПеревозчикиТипыТС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
			
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тбПеревозчикиТипыТС.Перевозчик,
	|	тбПеревозчикиТипыТС.ТипТС
	|ПОМЕСТИТЬ втПеревозчикиТипыТС
	|ИЗ
	|	&тбПеревозчикиТипыТС КАК тбПеревозчикиТипыТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРейсы.Рейс
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	&тбзРейсы КАК тбРейсы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПеревозчикиТипыТС.Перевозчик
	|ПОМЕСТИТЬ втПеревозчики
	|ИЗ
	|	втПеревозчикиТипыТС КАК втПеревозчикиТипыТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПеревозчикиТипыТС.ТипТС
	|ПОМЕСТИТЬ втТипыТС
	|ИЗ
	|	втПеревозчикиТипыТС КАК втПеревозчикиТипыТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСоглашения.Владелец КАК Перевозчик,
	|	МАКСИМУМ(тбСоглашения.Ссылка) КАК Соглашение
	|ПОМЕСТИТЬ втПеревозчикСоглашение
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками КАК тбСоглашения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
	|		ПО тбСоглашения.Владелец = втПеревозчики.Перевозчик
	|ГДЕ
	|	тбСоглашения.Владелец.Перевозчик
	|	И НЕ тбСоглашения.Ссылка = ЗНАЧЕНИЕ(Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСоглашения.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеревозчикСоглашение.Перевозчик,
	|	втПеревозчикСоглашение.Соглашение.ГруппаТарифов КАК ГруппаТарифов,
	|	втПеревозчикСоглашение.Соглашение
	|ПОМЕСТИТЬ втПеревозчикГруппаТарифов
	|ИЗ
	|	втПеревозчикСоглашение КАК втПеревозчикСоглашение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбТипыТС.ТипТС,
	|	упТипыТССтатьиРасходов.СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиРасходовТипыТС
	|ИЗ
	|	Справочник.упТипыТС.СтатьиРасходов КАК упТипыТССтатьиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТипыТС КАК тбТипыТС
	|		ПО упТипыТССтатьиРасходов.Ссылка = тбТипыТС.ТипТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка.Владелец КАК Перевозчик,
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов,
	|	втПеревозчикГруппаТарифов.ГруппаТарифов
	|ПОМЕСТИТЬ втСтатьиРасходовПеревозчик
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК упСоглашенияСПеревозчикамиСтатьиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчикГруппаТарифов КАК втПеревозчикГруппаТарифов
	|		ПО упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка = втПеревозчикГруппаТарифов.Соглашение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс,
	|	упРейсРаботы.Рейс.Ссылка.Организация КАК Организация,
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов КАК СтатьяРасходов,
	|	упРейсРаботы.КоличествоДопОпераций КАК Количество
	|ПОМЕСТИТЬ втСтатьиРасходовДопОперации
	|ИЗ
	|	втРейсы КАК втРейсы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу КАК упРейсРаботы
	|		ПО втРейсы.Рейс = упРейсРаботы.Регистратор
	|ГДЕ
	|	НЕ упРейсРаботы.ДополнительнаяОперация = ЗНАЧЕНИЕ(Справочник.упДополнительныеОперации.ПустаяСсылка)
	|	И упРейсРаботы.КоличествоДопОпераций > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс,
	|	втСтатьиРасходовПеревозчик.Перевозчик,
	|	втПеревозчикиТипыТС.ТипТС,
	|	втСтатьиРасходовПеревозчик.СтатьяРасходов,
	|	втСтатьиРасходовПеревозчик.ГруппаТарифов,
	|	1 КАК Количество,
	|	1 КАК ТипИсточникаСтатейРасходов,
	|	""По соглашениям перевозчика"" КАК ТипИсточникаСтатейРасходовОписание
	|ПОМЕСТИТЬ втСтатьиРасходовПоРейсам
	|ИЗ
	|	втСтатьиРасходовПеревозчик КАК втСтатьиРасходовПеревозчик
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчикиТипыТС КАК втПеревозчикиТипыТС
	|		ПО (втПеревозчикиТипыТС.Перевозчик = втСтатьиРасходовПеревозчик.Перевозчик),
	|	втРейсы КАК втРейсы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРейсы.Рейс,
	|	втПеревозчикиТипыТС.Перевозчик,
	|	втСтатьиРасходовТипыТС.ТипТС,
	|	втСтатьиРасходовТипыТС.СтатьяРасходов,
	|	втПеревозчикГруппаТарифов.ГруппаТарифов,
	|	1,
	|	2,
	|	""По типу ТС""
	|ИЗ
	|	втСтатьиРасходовТипыТС КАК втСтатьиРасходовТипыТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчикиТипыТС КАК втПеревозчикиТипыТС
	|		ПО втСтатьиРасходовТипыТС.ТипТС = втПеревозчикиТипыТС.ТипТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчикГруппаТарифов КАК втПеревозчикГруппаТарифов
	|		ПО (втПеревозчикГруппаТарифов.Перевозчик = втПеревозчикиТипыТС.Перевозчик),
	|	втРейсы КАК втРейсы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСтатьиРасходовДопОперации.Рейс,
	|	втПеревозчикиТипыТС.Перевозчик,
	|	втПеревозчикиТипыТС.ТипТС,
	|	втСтатьиРасходовДопОперации.СтатьяРасходов,
	|	втПеревозчикГруппаТарифов.ГруппаТарифов,
	|	втСтатьиРасходовДопОперации.Количество,
	|	3,
	|	""Дополнительные операции по рейсу""
	|ИЗ
	|	втСтатьиРасходовДопОперации КАК втСтатьиРасходовДопОперации,
	|	втПеревозчикиТипыТС КАК втПеревозчикиТипыТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчикГруппаТарифов КАК втПеревозчикГруппаТарифов
	|		ПО (втПеревозчикГруппаТарифов.Перевозчик = втПеревозчикиТипыТС.Перевозчик)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСтатьиРасходовПоРейсам.Рейс КАК Рейс,
	|	втСтатьиРасходовПоРейсам.ГруппаТарифов КАК ГруппаТарифов,
	|	втСтатьиРасходовПоРейсам.ТипТС КАК ТипТС,
	|	втСтатьиРасходовПоРейсам.СтатьяРасходов,
	|	упРейс.Организация КАК Организация,
	|	втСтатьиРасходовПоРейсам.Перевозчик КАК Перевозчик,
	|	втСтатьиРасходовПоРейсам.Количество,
	|	втСтатьиРасходовПоРейсам.ТипИсточникаСтатейРасходов,
	|	втСтатьиРасходовПоРейсам.ТипИсточникаСтатейРасходовОписание,
	|	упРейс.ВидПеревозки КАК ВидПеревозки
	|ИЗ
	|	втСтатьиРасходовПоРейсам КАК втСтатьиРасходовПоРейсам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|		ПО втСтатьиРасходовПоРейсам.Рейс = упРейс.Ссылка
	|ИТОГИ
	|	МАКСИМУМ(ГруппаТарифов),
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(ВидПеревозки)
	|ПО
	|	Рейс,
	|	Перевозчик,
	|	ТипТС";
	Запрос.УстановитьПараметр("тбзРейсы", тбзРейсы);
	Запрос.УстановитьПараметр("тбПеревозчикиТипыТС", тбзПеревозчикиТипыТС);
	
	МаксимальнаяПрибыль = 0;
	
	Для каждого текСтрока Из тбзРейсы Цикл
		Если текСтрока.ПлановыеРасходыПоРейсу > МаксимальнаяПрибыль Тогда
			МаксимальнаяПрибыль = текСтрока.ПлановыеРасходыПоРейсу;				
		КонецЕсли;
	КонецЦикла;
	
	ВыборкаРейс = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаРейс.Следующий() Цикл
		текРейс 		= ВыборкаРейс.Рейс;
		текОрганизация 	= ВыборкаРейс.Организация;
		текВидПеревозки	= ВыборкаРейс.ВидПеревозки;
		текПериод		= ТекущаяДатаСеанса();
		ВыборкаПеревозчик = ВыборкаРейс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		мсвСтрокиПоРейсу = тбзРейсы.НайтиСтроки(Новый Структура("Рейс",текРейс));
		Если мсвСтрокиПоРейсу.Количество() > 0 Тогда
			текСтрокаРейс = мсвСтрокиПоРейсу[0];
		Иначе
			Продолжить;			
		КонецЕсли;
		
		// Расшифровка по рейсу/перевозчику/типу ТС.
		тбзРасходыРасшифровка = Новый ТаблицаЗначений;
		тбзРасходыРасшифровка.Колонки.Добавить("Перевозчик", 								Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
		тбзРасходыРасшифровка.Колонки.Добавить("ТипТС", 									Новый ОписаниеТипов("СправочникСсылка.упТипыТС"));
		тбзРасходыРасшифровка.Колонки.Добавить("Сумма", 									Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(16,2)));
		тбзРасходыРасшифровка.Колонки.Добавить("СтатьяРасходов", 							Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
		тбзРасходыРасшифровка.Колонки.Добавить("ТипИсточникаСтатейРасходов", 				Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(1,0,ДопустимыйЗнак.Неотрицательный)));
		тбзРасходыРасшифровка.Колонки.Добавить("ТипИсточникаСтатейРасходовОписание", 		Новый ОписаниеТипов("Строка", ,,Новый КвалификаторыСтроки(100)));
		
		тбзРасходы = тбзРасходыРасшифровка.СкопироватьКолонки("Перевозчик, ТипТС, Сумма");
		тбзРасходы.Колонки.Добавить("Прибыль",	 		Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(16,2)));

		Пока ВыборкаПеревозчик.Следующий() Цикл
			текПеревозчик	 = ВыборкаПеревозчик.Перевозчик;
			текГруппаТарифов = ВыборкаПеревозчик.ГруппаТарифов;
			
			ВыборкаТипТС = ВыборкаПеревозчик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаТипТС.Следующий() Цикл
				текТипТС = ВыборкаТипТС.ТипТС;
								
				// Формируется табличная часть объекта.
				тбзСтатьиРасходов = Новый ТаблицаЗначений;
				тбзСтатьиРасходов.Колонки.Добавить("СтатьяРасходов", 		Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
				тбзСтатьиРасходов.Колонки.Добавить("Задание", 				Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
				тбзСтатьиРасходов.Колонки.Добавить("Сумма", 				Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("Цена", 					Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("Количество", 			Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("Валюта",				Новый ОписаниеТипов("СправочникСсылка.Валюты"));
				тбзСтатьиРасходов.Колонки.Добавить("СтавкаНДС",				Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"));
				тбзСтатьиРасходов.Колонки.Добавить("СуммаНДС",				Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("ТипИсточникаСтатейРасходов", 			Новый ОписаниеТипов("Число"));
				тбзСтатьиРасходов.Колонки.Добавить("ТипИсточникаСтатейРасходовОписание", 	Новый ОписаниеТипов("Строка"));
				
				ВыборкаСтатьяРасходов = ВыборкаТипТС.Выбрать();
				
				мсвСтатейРасходов = Новый Массив();
				Пока ВыборкаСтатьяРасходов.Следующий() Цикл
					
					мсвСтатейРасходов.Добавить(ВыборкаСтатьяРасходов.СтатьяРасходов);
					
					НоваяСтрока = тбзСтатьиРасходов.Добавить();
					НоваяСтрока.СтатьяРасходов 	= ВыборкаСтатьяРасходов.СтатьяРасходов;
					НоваяСтрока.Валюта			= Справочники.Валюты.ПустаяСсылка();
					НоваяСтрока.Сумма			= 0;
					НоваяСтрока.Цена			= 0;
					НоваяСтрока.Количество		= ВыборкаСтатьяРасходов.Количество;
					НоваяСтрока.СуммаНДС		= 0;
					НоваяСтрока.ТипИсточникаСтатейРасходов 			= ВыборкаСтатьяРасходов.ТипИсточникаСтатейРасходов;
					НоваяСтрока.ТипИсточникаСтатейРасходовОписание 	= ВыборкаСтатьяРасходов.ТипИсточникаСтатейРасходовОписание;
				
				КонецЦикла;
				
				// Формируется объект.
				сткОбъект = Новый Структура();
				сткОбъект.Вставить("Ссылка", 			текРейс);
				сткОбъект.Вставить("Организация", 		текОрганизация);
				сткОбъект.Вставить("Дата", 				текПериод);
				сткОбъект.Вставить("СуммаРасходы", 		0);
				сткОбъект.Вставить("СуммаРасходыНДС", 	0);
				сткОбъект.Вставить("ПлановыеРасходы", 	тбзСтатьиРасходов);
				сткОбъект.Вставить("ВидПеревозки", 		текВидПеревозки);
								
				ВходныеПараметры = Новый Структура();
				ВходныеПараметры.Вставить("СписокСтатей",	мсвСтатейРасходов);
				ВходныеПараметры.Вставить("ГруппаТарифов",	текГруппаТарифов);
				ВходныеПараметры.Вставить("ТипТС"        ,	текТипТС);

				
				// Объект рассчитывается.
				Рассчитать(сткОбъект, ВходныеПараметры);
												
				тбзПлановыеРасходы	= сткОбъект.ПлановыеРасходы;
				
				текСуммаРасходы = 0;
				Для каждого СтрокаРасход Из тбзПлановыеРасходы Цикл
					
					НоваяСтрокаРасходы = тбзРасходыРасшифровка.Добавить();
					НоваяСтрокаРасходы.Перевозчик 	= текПеревозчик;
					НоваяСтрокаРасходы.ТипТС		= текТипТС;
					ЗаполнитьЗначенияСвойств(НоваяСтрокаРасходы, СтрокаРасход);
					текСуммаРасходы = текСуммаРасходы + СтрокаРасход.Сумма;
										
				КонецЦикла;
				
				НоваяСтрока = тбзРасходы.Добавить();
				НоваяСтрока.Сумма 		= сткОбъект.СуммаРасходы;
				НоваяСтрока.Перевозчик 	= текПеревозчик;
				НоваяСтрока.ТипТС 		= текТипТС;
				
				текПрибыль 	= текСтрокаРейс.ПлановыеРасходыПоРейсу - сткОбъект.СуммаРасходы;
				текПрибыль	= ?(текПрибыль > 0, текПрибыль, 0);
				НоваяСтрока.Прибыль		= текПрибыль;
			КонецЦикла;
				
		КонецЦикла;
		
		текСтрокаРейс.ПлановыеРасходыПоРейсамРасшифровка = тбзРасходыРасшифровка;
		текСтрокаРейс.ПлановыеРасходыПоРейсам = тбзРасходы;
		
	КонецЦикла;

	сткВозврата = Новый Структура("Рейсы, РасходыДляПодбора, МаксимальнаяПрибыль", тбзРейсы, тбзРасходыДляПодбора, МаксимальнаяПрибыль);
	Если РезультатАдресВоВременномХранилище <> Неопределено Тогда
		ПоместитьВоВременноеХранилище(сткВозврата, 	РезультатАдресВоВременномХранилище);
		ПоместитьВоВременноеХранилище(Истина, 		ПризнакЗавершенияВыполненияАдресВоВременномХранилище);
	КонецЕсли;
		
	Возврат сткВозврата;

КонецФункции // РассчитатьРейсыПоПеревозчикам()
	
//Функция РассчитатьПлановыйРасходТопливаПоПутевомуЛисту(Объект, ВходныеПараметры, сткТрассировка)
//	
//	упСЛК.СоздатьОбъект().РассчитатьПлановыйРасходТопливаПоПутевомуЛисту(Объект, ВходныеПараметры, сткТрассировка);
//	
//КонецФункции


Функция РассчитатьРасходТопливаПоПутевомуЛисту(Объект, ВходныеПараметры, сткТрассировка) Экспорт
	
	//ПроверитьОграничениеПоДоступностиКомпонент(3);
	
	КоэффициентПересчетаМассы = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаМассы");
	ЭтоРасчетНормативногоРасходаТоплива = ВходныеПараметры.Свойство("РассчитатьНормативныйРасходТоплива");
	ЭтоРасчетПоДопОборудованию = ВходныеПараметры.Свойство("РассчитатьДополнительноеОборудование");
	
	сткЗапрос = СформироватьЗапросДляРасчетаРасходовПоПутевомуЛисту(Объект, ВходныеПараметры);
	мсвРезультат = сткЗапрос.Запрос.ВыполнитьПакет();

	сткДанные = СформироватьСтруктуруДанных(Объект, ВходныеПараметры);// данные для расчета расходов
	
	сткДанные.Вставить("ЭтоРасчетТопливаПоДопОборудованию", НЕ ЭтоРасчетНормативногоРасходаТоплива);
	сткДанные.Вставить("ЭтоНормативныйРасчетТоплива", ЭтоРасчетНормативногоРасходаТоплива);
	сткДанные.Вставить("ЭтоРасчетТопливаПоДопОборудованию", ЭтоРасчетПоДопОборудованию);
	сткДанные.Вставить("ЗимняяТемператураВоздуха", -50);

	упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "ТемператураВоздуха", , "" + сткДанные.ТемператураВоздуха + "°С",1);
	
	
	Если ЭтоРасчетПоДопОборудованию Тогда
									
		Коэффициент = 1; // коэффициент для корректировки расстояния между точками из маршрута (Перевода расстояния в пробег).
		ВремяРаботыЧас	= 0;
		Расстояние = 0;
		
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "ЗимняяТемператураВоздуха", , Нстр("ru = '""Не используется при расчете доп. оборудования""'"),1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "Коэффициент", , Коэффициент,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "Пробег", , СтрШаблон(Нстр("ru = '%1 км'"), Формат(сткДанные.Пробег, "ЧЦ=10; ЧДЦ=0")) ,1);
		
		сткДанные.Вставить("ВремяРаботыЧас", ВремяРаботыЧас);
		сткДанные.Вставить("Расстояние", 	  Расстояние);
		
		дрвГСМДопОборудование = мсвРезультат[сткЗапрос.ИндексЗапросаГСМДопОборудование].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		НормыРасходаДопОборудование = мсвРезультат[сткЗапрос.ИндексЗапросаНормыДопОборудование].Выгрузить();
		
		Отказ = Ложь;
		РасходТопливаНаДопОборудование = РассчитатьРасходТопливаНаДопОборудование(Объект, дрвГСМДопОборудование, НормыРасходаДопОборудование, , сткДанные, ВходныеПараметры, сткТрассировка, Отказ);
		
	Иначе	
	
		сткМаршрут	= СформироватьМаршрутПоРейсам(мсвРезультат, сткЗапрос.ИндексЗапросаПараметрыМаршрута); // содержит данные по маршруту рейсов
		
		// Если нет фактического и планового расстояние по точкам маршрута, то рассчитываем 
		// по плановым параметрам рейса, в качестве расстояния указывается пробег
		ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте = Ложь
										Или сткМаршрут.ФактическоеРасстояние > 0
										Или сткМаршрут.ПлановоеРасстояние > 0;
										
		Коэффициент = 1; // коэффициент для корректировки расстояния между точками из маршрута (Перевода расстояния в пробег).
		ВремяРаботыЧас	= 0;
		Расстояние = 0;
						
		Если ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте Тогда
			Если ЭтоРасчетНормативногоРасходаТоплива Тогда
				Коэффициент = КоэффициентОтношенияПробегаКРасстояниюПоМаршруту(сткМаршрут.ФактическоеРасстояние, сткМаршрут.ПлановоеРасстояние, сткДанные.Пробег);								
			КонецЕсли;
		Иначе
			Если ЭтоРасчетНормативногоРасходаТоплива Тогда
				ВыборкаПлановыеПараметрыРейса = мсвРезультат[сткЗапрос.ИндексЗапросаПлановыеПараметрыРейса].Выбрать();
				Если ВыборкаПлановыеПараметрыРейса.Следующий() Тогда
					ВремяРаботыЧас = ВыборкаПлановыеПараметрыРейса.ВремяВПути;
					МассаГруза 	= ВыборкаПлановыеПараметрыРейса.Масса;
					Если сткДанные.МассаГруза = 0
						И МассаГруза <> 0 Тогда
						сткДанные.МассаГруза = МассаГруза;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;						
			Расстояние = сткДанные.Пробег;
		КонецЕсли;
		
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "Коэффициент", , Коэффициент,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "ТранспортноеСредство", , сткДанные.ТранспортноеСредство,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "Пробег", , СтрШаблон(Нстр("ru = '%1 км'"), Формат(сткДанные.Пробег, "ЧЦ=10; ЧДЦ=0")) ,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "ПробегСГрузом", , СтрШаблон(Нстр("ru = '%1 км'"), Формат(сткДанные.ПробегСГрузом, "ЧЦ=10; ЧДЦ=0")) ,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "Моточасы", , СтрШаблон(Нстр("ru = '%1 ч'"), Формат(сткДанные.Моточасы, "ЧЦ=10; ЧДЦ=0")) ,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "МоточасыСГрузом", , СтрШаблон(Нстр("ru = '%1 ч'"), Формат(сткДанные.МоточасыСГрузом, "ЧЦ=10; ЧДЦ=0")) ,1);
		сткПредставления 	= упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(Истина, Ложь, Ложь, Ложь);
		ЕИ_Масса = сткПредставления.Масса;
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "МассаГруза", , СтрШаблон(Нстр("ru = '%1 %2'"), Формат(сткДанные.МассаГруза, "ЧЦ=12; ЧДЦ=3"), ЕИ_Масса) ,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "МассаПрицепногоСостава", , СтрШаблон(Нстр("ru = '%1 %2'"), Формат(сткДанные.МассаПрицепногоСостава, "ЧЦ=12; ЧДЦ=3"), ЕИ_Масса) ,1);
		
		сткДанные.Вставить("ВремяРаботыЧас", ВремяРаботыЧас);
		сткДанные.Вставить("Расстояние", Расстояние);
		сткДанные.Вставить("УчитыватьТемпературуВоздухаВПутевомЛисте", Ложь);
																
		ГСМ = мсвРезультат[сткЗапрос.ИндексЗапросаГСМ].Выгрузить();
		НормыРасхода = мсвРезультат[сткЗапрос.ИндексЗапросаНормы].Выгрузить();
		дрвПрицепы = мсвРезультат[сткЗапрос.ИндексЗапросаНормыПоПрицепам].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		дрвГСМДопОборудование = мсвРезультат[сткЗапрос.ИндексЗапросаГСМДопОборудование].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		НормыРасходаДопОборудование = мсвРезультат[сткЗапрос.ИндексЗапросаНормыДопОборудование].Выгрузить();
		
		ВыборкаЗимняяТемператураВоздуха = мсвРезультат[сткЗапрос.ИндексЗимняяТемператураВоздуха].Выбрать();
		Если ВыборкаЗимняяТемператураВоздуха.Следующий() Тогда
			сткДанные.Вставить("ЗимняяТемператураВоздуха", ВыборкаЗимняяТемператураВоздуха.ЗимняяТемператураВоздуха);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"", "ЗимняяТемператураВоздуха", ,"" + сткДанные.ЗимняяТемператураВоздуха + "°С",1);
		КонецЕсли;
						
		Для каждого СтрокаГСМ Из ГСМ Цикл
			
			ПроцентИспользования = ?(СтрокаГСМ.ПроцентИспользования = 0, 100, СтрокаГСМ.ПроцентИспользования);
			КоэффициентИспользования = ПроцентИспользования / 100; 
			
			ТекущийНомерСтроки = СтрокаГСМ.НомерСтроки;
			мсвСтрокиНормРасхода = НормыРасхода.НайтиСтроки(Новый Структура("ВидГСМ", СтрокаГСМ.ВидГСМ));
			ЗаполнитьЗначениямиНормРасхода(сткДанные, СтрокаГСМ, мсвСтрокиНормРасхода, ВходныеПараметры);
					
			ВидГСМ = СтрокаГСМ.ВидГСМ;
			ПравилоРасчета = СтрокаГСМ.ФормулаРасчета;
			ШкалаТемпературныхКоэффициентов = СтрокаГСМ.ШкалаТемпературныхКоэффициентов;
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "НомерСтроки", Истина, ТекущийНомерСтроки,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ТипНормыРасходаГСМ",, СтрокаГСМ.ТипНормыРасходаГСМ,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Наименование",, ВидГСМ,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Правило",, ПравилоРасчета,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ШкалаТемпературныхКоэффициентов",,ШкалаТемпературныхКоэффициентов,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Расход",, СтрокаГСМ.Расход,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ИзменениеРасхода",, СтрокаГСМ.ИзменениеРасхода,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Формула",,ПравилоРасчета.Формула,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ПроцентИспользования",,ПроцентИспользования,1);
	
			мсвСтрокиГСМПрицепы =  дрвПрицепы.Строки.НайтиСтроки(Новый Структура("ВидГСМ", ВидГСМ));
			СтрокаВидГСМПрицепы = Неопределено;
			Если мсвСтрокиГСМПрицепы.Количество() > 0 Тогда
				СтрокаВидГСМПрицепы = мсвСтрокиГСМПрицепы[0];
			КонецЕсли;
			
			РасходТоплива = 0;
			Отказ = Ложь;
							
			РасстояниеВсего = 0;
			ВремяРаботыЧасВсего = 0;
							
			Если ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте Тогда
				
				БазовыйРасходТоплива = 0;
				
				ТочкаОтправления = Неопределено;
				ДобавитьСообщениеТрассировки = Ложь;
				МассаГруза = 0;
				
				Для каждого ТочкаПрибытия Из сткМаршрут.Таблица Цикл
								
					Расстояние = 0;
					Если ЭтоРасчетНормативногоРасходаТоплива Тогда
						Если сткМаршрут.ФактическоеРасстояние > 0 Тогда
							Расстояние = ТочкаПрибытия.РасстояниеОтПредыдущейточкиФакт  * Коэффициент;					
						Иначе
							Расстояние = ТочкаПрибытия.РасстояниеОтПредыдущейточкиПлан  * Коэффициент;
						КонецЕсли;
					Иначе
						Расстояние = ТочкаПрибытия.РасстояниеОтПредыдущейточкиПлан;					
					КонецЕсли;
					РасстояниеВсего = РасстояниеВсего + Расстояние;
										
					ВремяРаботыЧас = ТочкаПрибытия.ВремяОтпредыдущейТочки;
					ВремяРаботыЧасВсего = ВремяРаботыЧасВсего + ВремяРаботыЧас;
					
					сткДанные.Вставить("МассаГруза", МассаГруза);
					сткДанные.Вставить("Расстояние", Расстояние);
					сткДанные.Вставить("ВремяРаботыЧас", ВремяРаботыЧас);
					
					Если ЗначениеЗаполнено(ПравилоРасчета) Тогда
												
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "НомерСтроки",Истина,?(ТочкаОтправления = Неопределено, 1, ТочкаОтправления.НомерСтроки + 1),1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "АдресОтправления", , ?(ТочкаОтправления = Неопределено,"",ТочкаОтправления.Адрес),1);
						упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "АдресПрибытия", , ТочкаПрибытия.Адрес,1);
						
					Иначе	
						ДобавитьСообщениеТрассировки = ТочкаОтправления <> Неопределено;
						Если ДобавитьСообщениеТрассировки Тогда
							упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "НомерСтроки", Истина, ТочкаОтправления.НомерСтроки,1);
							упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "АдресОтправления", , ТочкаОтправления.Адрес,1);
							упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "АдресПрибытия", , ТочкаПрибытия.Адрес,1);
						КонецЕсли;
					КонецЕсли;

					БазовыйРасходТоплива = БазовыйРасходТоплива + РассчитатьБазовыйРасходТоплива(ПравилоРасчета, сткДанные, КоэффициентПересчетаМассы, сткТрассировка, ДобавитьСообщениеТрассировки, ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте, КоэффициентИспользования, Отказ);
															
					Если Отказ Тогда
						Прервать;
					КонецЕсли;
					
					МассаГруза = МассаГруза + ТочкаПрибытия.Масса;
													
					ТочкаОтправления = ТочкаПрибытия;
					
				КонецЦикла;	
				
				сткДанные.Вставить("Расстояние", РасстояниеВсего);
				сткДанные.Вставить("ВремяРаботыЧас", ВремяРаботыЧасВсего);
								
			Иначе
				БазовыйРасходТоплива  = РассчитатьБазовыйРасходТоплива(ПравилоРасчета, сткДанные, КоэффициентПересчетаМассы, сткТрассировка, Истина, ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте, КоэффициентИспользования, Отказ);
			КонецЕсли;	
			
			Если НЕ ЗначениеЗаполнено(ПравилоРасчета) Тогда
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "БазовыйРасходТоплива",,БазовыйРасходТоплива,1);
			КонецЕсли;	
			
			РасходТопливаНаПрицепы = 0;
			
			Если СтрокаВидГСМПрицепы <> Неопределено Тогда
				РасходТопливаНаПрицепы	= РассчитатьРасходТопливаНаПрицепы(СтрокаВидГСМПрицепы.Строки, БазовыйРасходТоплива, сткДанные, сткТрассировка, КоэффициентИспользования, Отказ);	
			КонецЕсли;
			
			РасходТоплива = БазовыйРасходТоплива + РасходТопливаНаПрицепы;
			
			Если НЕ ЗначениеЗаполнено(ПравилоРасчета) Тогда
				НадбавкиРасходТоплива = РассчитатьРасходТопливаНаНадбавки(сткДанные,,мсвСтрокиНормРасхода, РасходТоплива, ВходныеПараметры, сткТрассировка, Отказ);
				РасходТоплива = РасходТоплива + НадбавкиРасходТоплива;
				Если ЗначениеЗаполнено(ШкалаТемпературныхКоэффициентов) Тогда
					РасходТоплива = РасходТоплива * ОЦЕНИТЬПО(ШкалаТемпературныхКоэффициентов, сткДанные.ТемператураВоздуха);
				КонецЕсли;
			КонецЕсли;
			
			РасходТопливаНаДопОборудование = РассчитатьРасходТопливаНаДопОборудование(Объект, дрвГСМДопОборудование, НормыРасходаДопОборудование, ВидГСМ, сткДанные, ВходныеПараметры, сткТрассировка, Отказ);
			РасходТоплива =  РасходТоплива + РасходТопливаНаДопОборудование;
					
			Если НЕ Отказ Тогда
				Если ЭтоРасчетНормативногоРасходаТоплива Тогда
					Объект.ГСМ[ТекущийНомерСтроки - 1].РасходТопливаПоНорме = РасходТоплива;
					Объект.ГСМ[ТекущийНомерСтроки - 1].РасходТопливаПрицепыПоНорме = РасходТопливаНаПрицепы;
				Иначе	
					Объект.ГСМ[ТекущийНомерСтроки - 1].РасходТопливаПлан = РасходТоплива;	
					Объект.ГСМ[ТекущийНомерСтроки - 1].РасходТопливаПрицепыПлан = РасходТопливаНаПрицепы;
				КонецЕсли;
				Если  НЕ ЗначениеЗаполнено(ПравилоРасчета) Тогда
					Для каждого Строка Из мсвСтрокиНормРасхода Цикл
						Если ЭтоРасчетНормативногоРасходаТоплива Тогда
							Объект.НормыРасходаГСМ[Строка.НомерСтроки - 1].РасходТопливаПоНорме = Строка.РасходТопливаПоНорме;
						Иначе
							Объект.НормыРасходаГСМ[Строка.НомерСтроки - 1].РасходТопливаПлан = Строка.РасходТопливаПлан;	
						КонецЕсли;	
					КонецЦикла;
				КонецЕсли;

				
				Для каждого Строка Из НормыРасходаДопОборудование Цикл
					Если ЭтоРасчетНормативногоРасходаТоплива Тогда
						Объект.НормыРасходаГСМДополнительноеОборудование[Строка.НомерСтроки - 1].РасходТопливаПоНорме = Строка.РасходТопливаПоНорме;
					Иначе
						Объект.НормыРасходаГСМДополнительноеОборудование[Строка.НомерСтроки - 1].РасходТопливаПлан = Строка.РасходТопливаПлан;	
					КонецЕсли;	
				КонецЦикла;
				
			КонецЕсли;
			
			ТекстСообщения = Нстр("ru = '%1 л.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Формат(РасходТоплива, "ЧЦ=12; ЧДЦ=3"));
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ИтогоПоВидуГСМ",,ТекстСообщения,1);
			
		КонецЦикла;
		
	КонецЕсли;
	
		
КонецФункции  // РассчитатьПлановыйРасходТопливаПоПутевомуЛисту()

Функция СформироватьЗапросДляРасчетаРасходовПоПутевомуЛисту(Объект, ВходныеПараметры)
	
	ЭтоРасчетНормативногоРасходаТоплива = ВходныеПараметры.Свойство("РассчитатьНормативныйРасходТоплива");
	ЭтоРасчетПоДопОборудованию = ВходныеПараметры.Свойство("РассчитатьДополнительноеОборудование");
	
	сткЗапрос = Новый Структура("Запрос", Неопределено);
	РасчетПоОбъекту = ТипЗнч(Объект) = Тип("ДокументОбъект.упПутевойЛист");
	
	Если ЭтоРасчетПоДопОборудованию Тогда
		
		Если РасчетПоОбъекту Тогда
			сткЗапрос.Вставить("ИндексЗапросаНормыДопОборудование", 6);
			сткЗапрос.Вставить("ИндексЗапросаГСМДопОборудование", 5);
		Иначе	
			сткЗапрос.Вставить("ИндексЗапросаНормыДопОборудование", 4);
			сткЗапрос.Вставить("ИндексЗапросаГСМДопОборудование", 5);
		КонецЕсли; 
		
		ТекстЗапроса 	 = "";		
		
		Если РасчетПоОбъекту Тогда
			
			ТекстЗапроса = 
			"
			|//{Запрос: 0 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГСМДопОборудование.ВидГСМ КАК ВидГСМ,
			|	ГСМДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ГСМДопОборудование.НормаРасхода КАК НормаРасхода,
			|	ГСМДопОборудование.НомерСтроки КАК НомерСтроки,
			|	ГСМДопОборудование.Расход КАК Расход,
			|	ГСМДопОборудование.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	ГСМДопОборудование.ФормулаРасчета КАК ФормулаРасчета,
			|	ГСМДопОборудование.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов
			|ПОМЕСТИТЬ втГСМДопОборудование
			|ИЗ
			|	&ГСМДопОборудования КАК ГСМДопОборудование
			|;
			|//{Запрос: 1 ////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование
			|ПОМЕСТИТЬ втДопОборудование
			|ИЗ
			|	втГСМДопОборудование КАК ДопОборудование
			|;
			|//{Запрос: 2 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	РежимыРаботы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	РежимыРаботы.Количество КАК Количество,
			|	РежимыРаботы.Расход КАК Расход,
			|	РежимыРаботы.РежимРаботы КАК РежимРаботы
			|ПОМЕСТИТЬ втРежимыДопОборудования
			|ИЗ
			|	&РежимыРаботыДопОборудования КАК РежимыРаботы
			|;
			|//{Запрос: 3 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДополнительноеОборудованиеМоточасы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ДополнительноеОборудованиеМоточасы.Моточасы КАК Моточасы
			|ПОМЕСТИТЬ втДопОборудованиеМоточасы
			|ИЗ
			|	&ДополнительноеОборудование КАК ДополнительноеОборудованиеМоточасы
			|;
			|//{Запрос: 4 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	НормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
			|	НормыРасходаГСМ.ВидЭксплуатационнойНормы КАК ВидЭксплуатационнойНормы,
			|	НормыРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	НормыРасходаГСМ.Количество КАК Количество,
			|	НормыРасходаГСМ.Расход КАК Расход,
			|	НормыРасходаГСМ.НомерСтроки КАК НомерСтроки,
			|	НормыРасходаГСМ.ТипРасхода КАК ТипРасхода,
			|	0 КАК РасходТопливаПоНорме,
			|	0 КАК РасходТопливаПлан
			|ПОМЕСТИТЬ втНормыРасходаГСМДопОборудования
			|ИЗ
			|	&НормыРасходаГСМДопОборудования КАК НормыРасходаГСМ
			|;
			|//{Запрос: 5 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.НомерСтроки КАК НомерСтроки,			
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	0 КАК ИзменениеРасхода,			
			|	втРежимыДопОборудованияТ.Количество КАК Количество,
			|	втРежимыДопОборудованияТ.Расход КАК Расход,
			|	втРежимыДопОборудованияТ.РежимРаботы КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРежимыДопОборудования КАК втРежимыДопОборудованияТ
			|	ПО втРежимыДопОборудованияТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.НомерСтроки КАК НомерСтроки,			
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	0 КАК ИзменениеРасхода,			
			|	втДопОборудованиеМоточасыТ.Моточасы КАК Количество,
			|	втГСМДопОборудованиеТ.Расход КАК Расход,
			|	NULL КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудованиеМоточасы КАК втДопОборудованиеМоточасыТ
			|	ПО втДопОборудованиеМоточасыТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|ИТОГИ
			|	МАКСИМУМ(НомерСтроки) КАК НомерСтроки,
			|	МАКСИМУМ(ФормулаРасчета) КАК ФормулаРасчета
			|ПО
			|	ВидГСМ,
			|	ДополнительноеОборудование
			|;
			|//{Запрос: 6 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	втНормыРасходаГСМДопОборудования КАК втНормыРасходаГСМДопОборудованияТ
			|";
			
		Иначе	
			
			ТекстЗапроса = 
			
			"
			|//{Запрос: 0 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГСМДопОборудование.Ссылка КАК ПутевойЛист,
			|	ГСМДопОборудование.ВидГСМ КАК ВидГСМ,
			|	ГСМДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ГСМДопОборудование.НормаРасхода КАК НормаРасхода,
			|	ГСМДопОборудование.НомерСтроки КАК НомерСтроки,
			|	ГСМДопОборудование.Расход КАК Расход,
			|	ГСМДопОборудование.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	ГСМДопОборудование.ФормулаРасчета КАК ФормулаРасчета,
			|	ГСМДопОборудование.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов
			|ПОМЕСТИТЬ втГСМДопОборудование
			|ИЗ
			|	Документ.упПутевойЛист.ГСМДополнительноеОборудование КАК ГСМДопОборудование
			|ГДЕ ИСТИНА
			|	И ГСМДопОборудование.Ссылка = &ПутевойЛист
			|	И ГСМДопОборудование.РасходИзСобственногоБака
			|;
			|//{Запрос: 1 ////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопОборудование.ПутевойЛист КАК ПутевойЛист,
			|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование
			|ПОМЕСТИТЬ втДопОборудование
			|ИЗ
			|	втГСМДопОборудование КАК ДопОборудование
			|;
			|//{Запрос: 2 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	РежимыРаботы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	РежимыРаботы.Количество КАК Количество,
			|	РежимыРаботы.Расход КАК Расход,
			|	РежимыРаботы.РежимРаботы КАК РежимРаботы
			|ПОМЕСТИТЬ втРежимыДопОборудования
			|ИЗ
			|	Документ.упПутевойЛист.НормыРасходаРежимовРаботы КАК РежимыРаботы
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудование КАК втДопОборудованиеТ
			|	ПО ИСТИНА
			|		И РежимыРаботы.ДополнительноеОборудование = втДопОборудованиеТ.ДополнительноеОборудование
			|		И РежимыРаботы.Ссылка = втДопОборудованиеТ.ПутевойЛист
			|;
			|//{Запрос: 3 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДополнительноеОборудованиеМоточасы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ДополнительноеОборудованиеМоточасы.Моточасы КАК Моточасы
			|ПОМЕСТИТЬ втДопОборудованиеМоточасы
			|ИЗ
			|	Документ.упПутевойЛист.ДополнительноеОборудование КАК ДополнительноеОборудованиеМоточасы
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудование КАК втДопОборудованиеТ
			|	ПО ИСТИНА
			|		И втДопОборудованиеТ.ДополнительноеОборудование = ДополнительноеОборудованиеМоточасы.ДополнительноеОборудование
			|		И втДопОборудованиеТ.ПутевойЛист = ДополнительноеОборудованиеМоточасы.Ссылка
			|ГДЕ НЕ (ДополнительноеОборудованиеМоточасы.ИспользуютсяРазличныеРежимыРаботы)
			|;
			|//{Запрос: 4 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	НормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
			|	НормыРасходаГСМ.ВидЭксплуатационнойНормы КАК ВидЭксплуатационнойНормы,
			|	НормыРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	НормыРасходаГСМ.Количество КАК Количество,
			|	НормыРасходаГСМ.Расход КАК Расход,
			|	НормыРасходаГСМ.НомерСтроки КАК НомерСтроки,
			|	НормыРасходаГСМ.ТипРасхода КАК ТипРасхода,
			|	0 КАК РасходТопливаПоНорме,
			|	0 КАК РасходТопливаПлан
			|ИЗ
			|	Документ.упПутевойЛист.НормыРасходаГСМДополнительноеОборудование КАК НормыРасходаГСМ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ПО ИСТИНА
			|		И втГСМДопОборудованиеТ.ВидГСМ = НормыРасходаГСМ.ВидГСМ
			|		И втГСМДопОборудованиеТ.ДополнительноеОборудование = НормыРасходаГСМ.ДополнительноеОборудование
			|		И втГСМДопОборудованиеТ.ПутевойЛист = НормыРасходаГСМ.Ссылка
			|;
			|//{Запрос: 5 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.НомерСтроки КАК НомерСтроки,
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	0 КАК ИзменениеРасхода,
			|	втРежимыДопОборудованияТ.Количество КАК Количество,
			|	втРежимыДопОборудованияТ.Расход КАК Расход,
			|	втРежимыДопОборудованияТ.РежимРаботы КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРежимыДопОборудования КАК втРежимыДопОборудованияТ
			|	ПО втРежимыДопОборудованияТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.НомерСтроки КАК НомерСтроки,
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	0 КАК ИзменениеРасхода,
			|	втДопОборудованиеМоточасыТ.Моточасы КАК Количество,
			|	втГСМДопОборудованиеТ.Расход КАК Расход,
			|	NULL КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудованиеМоточасы КАК втДопОборудованиеМоточасыТ
			|	ПО втДопОборудованиеМоточасыТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|ИТОГИ
			|	МАКСИМУМ(НомерСтроки) КАК НомерСтроки,
			|	МАКСИМУМ(ФормулаРасчета) КАК ФормулаРасчета
			|ПО
			|	ВидГСМ,
			|	ДополнительноеОборудование
			|";


		КонецЕсли;			

		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		мсвРейсы = Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс");
		Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
		Если НЕ РасчетПоОбъекту Тогда
			Запрос.УстановитьПараметр("ПутевойЛист", Объект.Ссылка);
		Иначе
			Запрос.УстановитьПараметр("ГСМДопОборудования", 			Объект.ГСМДополнительноеОборудование.Выгрузить(Новый Структура("РасходИзСобственногоБака",Истина),"НомерСтроки, ВидГСМ,ДополнительноеОборудование,НормаРасхода,Расход,ТипНормыРасходаГСМ,ФормулаРасчета,ШкалаТемпературныхКоэффициентов"));
			Запрос.УстановитьПараметр("НормыРасходаГСМДопОборудования", Объект.НормыРасходаГСМДополнительноеОборудование.Выгрузить(,"НомерСтроки,ВидЭксплуатационнойНормы,ВидГСМ,ДополнительноеОборудование,ТипРасхода,Расход,Количество"));
			Запрос.УстановитьПараметр("РежимыРаботыДопОборудования", 	Объект.НормыРасходаРежимовРаботы.Выгрузить(,"ДополнительноеОборудование,Количество,Расход,РежимРаботы"));
			Запрос.УстановитьПараметр("ДополнительноеОборудование", 	Объект.ДополнительноеОборудование.Выгрузить(Новый Структура("ИспользуютсяРазличныеРежимыРаботы",Ложь),"ДополнительноеОборудование,Моточасы"));
					
		КонецЕсли;
		
	Иначе	
			
		ТекстЗапроса = "";		
		
		Если РасчетПоОбъекту Тогда
			
			сткЗапрос.Вставить("ИндексЗапросаГСМ", 2);
			сткЗапрос.Вставить("ИндексЗапросаНормы", 3);
			сткЗапрос.Вставить("ИндексЗапросаНормыДопОборудование", 10);
			сткЗапрос.Вставить("ИндексЗапросаГСМДопОборудование", 9);
			
			ИндексНулевогоЗапроса = 11;//Нужен для того что бы можно было использовать индексы из текста запроса.
			
			ТекстЗапроса = 
			"
			|//{Запрос: 0 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГСМ.ВидГСМ КАК ВидГСМ,
			|	ГСМ.ПроцентИспользования КАК ПроцентИспользования,
			|	ГСМ.ИзменениеРасхода КАК ИзменениеРасхода,
			|	ГСМ.Расход КАК Расход,
			|	ГСМ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	ГСМ.ФормулаРасчета КАК ФормулаРасчета,
			|	ГСМ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	ГСМ.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте, 
			|	ГСМ.НомерСтроки КАК НомерСтроки
			|ПОМЕСТИТЬ втГСМ
			|ИЗ
			|	&ГСМ КАК ГСМ
			|;
			|//{Запрос: 1 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	НормыРасхода.ВидГСМ КАК ВидГСМ,
			|	НормыРасхода.ВидЭксплуатационнойНормы КАК ВидЭксплуатационнойНормы,
			|	НормыРасхода.Количество КАК Количество,
			|	НормыРасхода.НомерСтроки КАК НомерСтроки,
			|	НормыРасхода.Расход КАК Расход,
			|	НормыРасхода.ТипРасхода КАК ТипРасхода,
			|	0 КАК РасходТопливаПоНорме,
			|	0 КАК РасходТопливаПлан
			|ПОМЕСТИТЬ втНормыГСМ
			|ИЗ
			|	&НормыРасходаГСМ КАК НормыРасхода
			|;
			|//{Запрос: 2 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	втГСМ КАК втГСМТ
			|;
			|//{Запрос: 3 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	втНормыГСМ КАК втНормыГСМ
			|;
			|//{Запрос: 4 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГСМДопОборудование.ВидГСМ КАК ВидГСМ,
			|	ГСМДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ГСМДопОборудование.НормаРасхода КАК НормаРасхода,
			|	ГСМДопОборудование.Расход КАК Расход,
			|	ГСМДопОборудование.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	ГСМДопОборудование.ФормулаРасчета КАК ФормулаРасчета,
			|	ГСМДопОборудование.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов
			|ПОМЕСТИТЬ втГСМДопОборудование
			|ИЗ
			|	&ГСМДопОборудования КАК ГСМДопОборудование
			|;
			|//{Запрос: 5 ////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование
			|ПОМЕСТИТЬ втДопОборудование
			|ИЗ
			|	втГСМДопОборудование КАК ДопОборудование
			|;
			|//{Запрос: 6 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	РежимыРаботы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	РежимыРаботы.Количество КАК Количество,
			|	РежимыРаботы.Расход КАК Расход,
			|	РежимыРаботы.РежимРаботы КАК РежимРаботы
			|ПОМЕСТИТЬ втРежимыДопОборудования
			|ИЗ
			|	&РежимыРаботыДопОборудования КАК РежимыРаботы
			|;
			|//{Запрос: 7 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДополнительноеОборудованиеМоточасы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ДополнительноеОборудованиеМоточасы.Моточасы КАК Моточасы
			|ПОМЕСТИТЬ втДопОборудованиеМоточасы
			|ИЗ
			|	&ДополнительноеОборудование КАК ДополнительноеОборудованиеМоточасы
			|;
			|//{Запрос: 8 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	НормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
			|	НормыРасходаГСМ.ВидЭксплуатационнойНормы КАК ВидЭксплуатационнойНормы,
			|	НормыРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	НормыРасходаГСМ.Количество КАК Количество,
			|	НормыРасходаГСМ.Расход КАК Расход,
			|	НормыРасходаГСМ.НомерСтроки КАК НомерСтроки,
			|	НормыРасходаГСМ.ТипРасхода КАК ТипРасхода,
			|	0 КАК РасходТопливаПоНорме,
			|	0 КАК РасходТопливаПлан
			|ПОМЕСТИТЬ втНормыРасходаГСМДопОборудования
			|ИЗ
			|	&НормыРасходаГСМДопОборудования КАК НормыРасходаГСМ
			|;
			|//{Запрос: 9 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	втРежимыДопОборудованияТ.Количество КАК Количество,
			|	втРежимыДопОборудованияТ.Расход КАК Расход,
			|	0 КАК ИзменениеРасхода,			
			|	втРежимыДопОборудованияТ.РежимРаботы КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРежимыДопОборудования КАК втРежимыДопОборудованияТ
			|	ПО втРежимыДопОборудованияТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	втДопОборудованиеМоточасыТ.Моточасы КАК Количество,
			|	втГСМДопОборудованиеТ.Расход КАК Расход,
			|	0 КАК ИзменениеРасхода,			
			|	NULL КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудованиеМоточасы КАК втДопОборудованиеМоточасыТ
			|	ПО втДопОборудованиеМоточасыТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|УПОРЯДОЧИТЬ ПО
			|	ВидГСМ,
			|	ДополнительноеОборудование
			|ИТОГИ
			|ПО
			|	ВидГСМ,
			|	ДополнительноеОборудование
			|;
			|//{Запрос: 10 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	втНормыРасходаГСМДопОборудования КАК втНормыРасходаГСМДопОборудованияТ
			|";


			
		Иначе	
			
			сткЗапрос.Вставить("ИндексЗапросаНормы", 1);
			сткЗапрос.Вставить("ИндексЗапросаГСМ", 2);
			сткЗапрос.Вставить("ИндексЗапросаНормыДопОборудование", 7);
			сткЗапрос.Вставить("ИндексЗапросаГСМДопОборудование", 8);
			
			ИндексНулевогоЗапроса = 9;//Нужен для того что бы можно было использовать индексы из текста запроса.
			
			ТекстЗапроса = 
			"
			|//{Запрос: 0 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГСМ.ВидГСМ КАК ВидГСМ,
			|	ГСМ.ПроцентИспользования КАК ПроцентИспользования,
			|	ГСМ.ИзменениеРасхода КАК ИзменениеРасхода,
			|	ГСМ.Расход КАК Расход,
			|	ГСМ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	ГСМ.ФормулаРасчета КАК ФормулаРасчета,
			|	ГСМ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	ГСМ.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте,
			|	ГСМ.НомерСтроки КАК НомерСтроки
			|ПОМЕСТИТЬ втГСМ
			|ИЗ
			|	Документ.упПутевойЛист.ГСМ КАК ГСМ
			|ГДЕ ГСМ.Ссылка = &ПутевойЛист
			|;
			|//{Запрос: 1 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	НормыРасхода.ВидГСМ КАК ВидГСМ,
			|	НормыРасхода.ВидЭксплуатационнойНормы КАК ВидЭксплуатационнойНормы,
			|	НормыРасхода.Количество КАК Количество,
			|	НормыРасхода.НомерСтроки КАК НомерСтроки,
			|	НормыРасхода.Расход КАК Расход,
			|	НормыРасхода.ТипРасхода КАК ТипРасхода,
			|	0 КАК РасходТопливаПоНорме,
			|	0 КАК РасходТопливаПлан
			|ИЗ
			|	Документ.упПутевойЛист.НормыРасходаГСМ КАК НормыРасхода
			|ГДЕ НормыРасхода.Ссылка = &ПутевойЛист
			|;
			|//{Запрос: 2 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	*
			|ИЗ
			|	втГСМ КАК втГСМТ
			|;
			|//{Запрос: 3 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГСМДопОборудование.Ссылка КАК ПутевойЛист,
			|	ГСМДопОборудование.ВидГСМ КАК ВидГСМ,
			|	ГСМДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ГСМДопОборудование.НормаРасхода КАК НормаРасхода,
			|	ГСМДопОборудование.Расход КАК Расход,
			|	ГСМДопОборудование.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	ГСМДопОборудование.ФормулаРасчета КАК ФормулаРасчета,
			|	ГСМДопОборудование.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов
			|ПОМЕСТИТЬ втГСМДопОборудование
			|ИЗ
			|	Документ.упПутевойЛист.ГСМДополнительноеОборудование КАК ГСМДопОборудование
			|ГДЕ ИСТИНА
			|	И ГСМДопОборудование.Ссылка = &ПутевойЛист
			|	И НЕ (ГСМДопОборудование.РасходИзСобственногоБака)
			|;
			|//{Запрос: 4 ////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопОборудование.ПутевойЛист,
			|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование
			|ПОМЕСТИТЬ втДопОборудование
			|ИЗ
			|	втГСМДопОборудование КАК ДопОборудование
			|;
			|//{Запрос: 5 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	РежимыРаботы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	РежимыРаботы.Количество КАК Количество,
			|	РежимыРаботы.Расход КАК Расход,
			|	РежимыРаботы.РежимРаботы КАК РежимРаботы
			|ПОМЕСТИТЬ втРежимыДопОборудования
			|ИЗ
			|	Документ.упПутевойЛист.НормыРасходаРежимовРаботы КАК РежимыРаботы
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудование КАК втДопОборудованиеТ
			|	ПО РежимыРаботы.ДополнительноеОборудование = втДопОборудованиеТ.ДополнительноеОборудование
			|		И РежимыРаботы.Ссылка = втДопОборудованиеТ.ПутевойЛист
			|;
			|//{Запрос: 6 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДополнительноеОборудованиеМоточасы.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	ДополнительноеОборудованиеМоточасы.Моточасы КАК Моточасы
			|ПОМЕСТИТЬ втДопОборудованиеМоточасы
			|ИЗ
			|	Документ.упПутевойЛист.ДополнительноеОборудование КАК ДополнительноеОборудованиеМоточасы
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудование КАК втДопОборудованиеТ
			|	ПО втДопОборудованиеТ.ДополнительноеОборудование = ДополнительноеОборудованиеМоточасы.ДополнительноеОборудование
			|		И втДопОборудованиеТ.ПутевойЛист = ДополнительноеОборудованиеМоточасы.Ссылка
			|ГДЕ НЕ (ДополнительноеОборудованиеМоточасы.ИспользуютсяРазличныеРежимыРаботы)
			|;
			|//{Запрос: 7 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	НормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
			|	НормыРасходаГСМ.ВидЭксплуатационнойНормы КАК ВидЭксплуатационнойНормы,
			|	НормыРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	НормыРасходаГСМ.Количество КАК Количество,
			|	НормыРасходаГСМ.Расход КАК Расход,
			|	НормыРасходаГСМ.НомерСтроки КАК НомерСтроки,
			|	НормыРасходаГСМ.ТипРасхода КАК ТипРасхода,
			|	0 КАК РасходТопливаПоНорме,
			|	0 КАК РасходТопливаПлан
			|ИЗ
			|	Документ.упПутевойЛист.НормыРасходаГСМДополнительноеОборудование КАК НормыРасходаГСМ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ПО ИСТИНА
			|		И втГСМДопОборудованиеТ.ВидГСМ = НормыРасходаГСМ.ВидГСМ
			|		И втГСМДопОборудованиеТ.ДополнительноеОборудование = НормыРасходаГСМ.ДополнительноеОборудование
			|		И втГСМДопОборудованиеТ.ПутевойЛист = НормыРасходаГСМ.Ссылка
			|;
			|//{Запрос: 8 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	втРежимыДопОборудованияТ.Количество КАК Количество,
			|	втРежимыДопОборудованияТ.Расход КАК Расход,
			|	0 КАК ИзменениеРасхода,			
			|	втРежимыДопОборудованияТ.РежимРаботы КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРежимыДопОборудования КАК втРежимыДопОборудованияТ
			|	ПО втРежимыДопОборудованияТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
			|	втГСМДопОборудованиеТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
			|	втГСМДопОборудованиеТ.НормаРасхода КАК НормаРасхода,
			|	втГСМДопОборудованиеТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
			|	втГСМДопОборудованиеТ.ФормулаРасчета КАК ФормулаРасчета,
			|	втГСМДопОборудованиеТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
			|	втДопОборудованиеМоточасыТ.Моточасы КАК Количество,
			|	втГСМДопОборудованиеТ.Расход КАК Расход,
			|	0 КАК ИзменениеРасхода,			
			|	NULL КАК РежимРаботы,
			|	Ложь КАК УчитыватьТемпературуВоздухаВПутевомЛисте
			|ИЗ
			|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопОборудованиеМоточасы КАК втДопОборудованиеМоточасыТ
			|	ПО втДопОборудованиеМоточасыТ.ДополнительноеОборудование = втГСМДопОборудованиеТ.ДополнительноеОборудование
			|УПОРЯДОЧИТЬ ПО
			|	ВидГСМ,
			|	ДополнительноеОборудование
			|ИТОГИ
			|ПО
			|	ВидГСМ,
			|	ДополнительноеОборудование
			|";
			
		КонецЕсли;			

		сткЗапрос.Вставить("ИндексЗапросаГрузыВТочке", ИндексНулевогоЗапроса + 2);
		сткЗапрос.Вставить("ИндексЗапросаПараметрыМаршрута", ИндексНулевогоЗапроса + 5);
		сткЗапрос.Вставить("ИндексЗапросаНормыПоПрицепам", ИндексНулевогоЗапроса + 6);
		сткЗапрос.Вставить("ИндексЗимняяТемператураВоздуха", ИндексНулевогоЗапроса + 7);
		сткЗапрос.Вставить("ИндексЗапросаПлановыеПараметрыРейса", ИндексНулевогоЗапроса + 8);
				
		ТекстЗапроса = ТекстЗапроса 
			+ ОбщегоНазначения.РазделительПакетаЗапросов()
			+ "
			|//{Запрос: 0 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
			|	упРаботыПоРейсуСрезПоследних.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
			|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс,
			|	упРаботыПоРейсуСрезПоследних.Операция КАК Операция,
			|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
			|	упРаботыПоРейсуСрезПоследних.Масса КАК Масса,
			|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
			|	упРаботыПоРейсуСрезПоследних.Идентификатор КАК Идентификатор
			|ПОМЕСТИТЬ втРаботыПоРейсам
			|ИЗ
			|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
			|		,
			|		Рейс В (&мсвРейсы)) КАК упРаботыПоРейсуСрезПоследних
			|;
			|//{Запрос: 1 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
			|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
			|	упМаршрутПоРейсуСрезПоследних.ВремяОтПредыдущейТочки КАК ВремяОтПредыдущейТочки,
			|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
			|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
			|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочкиФакт КАК РасстояниеОтПредыдущейТочкиФакт,
			|	ВЫБОР
			|		КОГДА упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие
			|		ИНАЧЕ упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт
			|	КОНЕЦ КАК Прибытие,
			|	ВЫБОР
			|		КОГДА упМаршрутПоРейсуСрезПоследних.УбытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие
			|		ИНАЧЕ упМаршрутПоРейсуСрезПоследних.УбытиеФакт
			|	КОНЕЦ КАК Убытие,
			|	упМаршрутПоРейсуСрезПоследних.Адрес КАК Адрес
			|ПОМЕСТИТЬ втМаршрут
			|ИЗ
			|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
			|		,
			|		Рейс В (&мсвРейсы)) КАК упМаршрутПоРейсуСрезПоследних
			|ГДЕ НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
			|;
			|//{Запрос: 2 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРаботыПоРейсам.Идентификатор КАК Идентификатор,
			|	0 КАК Масса, // Поле масса определяется ниже, в зависимости от доступных компонент
			|	втРаботыПоРейсам.Рейс КАК Рейс
			|ПОМЕСТИТЬ втГрузыВТочке
			|ИЗ
			|	втРаботыПоРейсам КАК втРаботыПоРейсам
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
			|		,
			|		ЗаявкаНаПеревозку В (
			|			ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
			|			ИЗ
			|				втРаботыПоРейсам КАК втЗадания
			|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
			|	ПО ИСТИНА
			|		И втРаботыПоРейсам.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
			|		И втРаботыПоРейсам.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
			|		,
			|		ЗаданиеНаПеревозку В (
			|			ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				втЗадания.Задание КАК Задание
			|			ИЗ
			|				втРаботыПоРейсам КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
			|	ПО ИСТИНА
			|		И втРаботыПоРейсам.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
			|		И втРаботыПоРейсам.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
			|СГРУППИРОВАТЬ ПО
			|	втРаботыПоРейсам.Идентификатор,
			|	втРаботыПоРейсам.Рейс
			|;
			|//{Запрос: 3 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втМаршрут.Рейс КАК Рейс,
			|	МИНИМУМ(втМаршрут.Прибытие) КАК Прибытие
			|ПОМЕСТИТЬ втРейсыПрибытиеВПервуюТочку
			|ИЗ
			|	втМаршрут КАК втМаршрут
			|СГРУППИРОВАТЬ ПО
			|	втМаршрут.Рейс
			|;
			|//{Запрос: 4 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРейсыПрибытиеВПервуюТочку.Рейс КАК Рейс,
			|	ВЫБОР
			|		КОГДА втРейсыПрибытиеВПервуюТочку.Прибытие = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1))
			|		ИНАЧЕ втРейсыПрибытиеВПервуюТочку.Прибытие
			|	КОНЕЦ КАК ДатаНачала
			|ПОМЕСТИТЬ втРейсыНачало
			|ИЗ
			|	втРейсыПрибытиеВПервуюТочку КАК втРейсыПрибытиеВПервуюТочку
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
			|	ПО втРейсыПрибытиеВПервуюТочку.Рейс = упПлановыеПараметрыРейса.Рейс
			|;
			|//{Запрос: 5 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втМаршрут.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочкиПлан,
			|	втМаршрут.ВремяОтПредыдущейТочки КАК ВремяОтПредыдущейТочки,
			|	ЕСТЬNULL(втГрузыВТочке.Масса, 0) КАК Масса,
			|	втМаршрут.РасстояниеОтПредыдущейТочкиФакт КАК РасстояниеОтПредыдущейТочкиФакт,
			|	втМаршрут.Прибытие КАК Прибытие,
			|	втМаршрут.Убытие КАК Убытие,
			|	втМаршрут.Рейс КАК Рейс,
			|	втМаршрут.Адрес КАК Адрес,
			|	втМаршрут.Идентификатор КАК Идентификатор
			|ИЗ
			|	втМаршрут КАК втМаршрут
			|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузыВТочке КАК втГрузыВТочке
			|	ПО ИСТИНА
			|		И втМаршрут.Идентификатор = втГрузыВТочке.Идентификатор
			|		И втМаршрут.Рейс = втГрузыВТочке.Рейс
			|	ЛЕВОЕ СОЕДИНЕНИЕ втРейсыНачало КАК втРейсыНачало
			|	ПО втМаршрут.Рейс = втРейсыНачало.Рейс
			|УПОРЯДОЧИТЬ ПО
			|	втРейсыНачало.ДатаНачала,
			|	втМаршрут.СтрокаНомер
			|ИТОГИ
			|	СУММА(РасстояниеОтПредыдущейТочки),
			|	СУММА(РасстояниеОтПредыдущейТочкиФакт)
			|ПО
			|	Рейс
			|;
			|//{Запрос: 6 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	УпНормыРасходаГСМНаПрицепнойСостав_СрезПоследнихТ.Расход КАК Расход,
			|	УпНормыРасходаГСМНаПрицепнойСостав_СрезПоследнихТ.ТипРасхода КАК ТипРасхода,
			|	ВЫБОР
			|		КОГДА УпНормыРасходаГСМНаПрицепнойСостав_СрезПоследнихТ.Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)
			|			ТОГДА 2
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК Приоритет,
			|	УпПрицепыТранспортныхСредств_СрезПоследнихТ.Прицеп КАК Прицеп,
			|	УпНормыРасходаГСМНаПрицепнойСостав_СрезПоследнихТ.ВидГСМ КАК ВидГСМ
			|ИЗ
			|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
			|		&Период,
			|		ТранспортноеСредство = &ТранспортноеСредство) КАК УпПрицепыТранспортныхСредств_СрезПоследнихТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМНаПрицепнойСостав.СрезПоследних(
			|		&Период,
			|		Организация = &Организация
			|			И (Подразделение = &Подразделение
			|				ИЛИ Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка))
			|			И МодельТС = &МодельТС
			|				И ВидГСМ В (
			|				ВЫБРАТЬ
			|				втГСМ.ВидГСМ КАК ВидГСМ
			|			ИЗ
			|				втГСМ КАК втГСМ)) КАК УпНормыРасходаГСМНаПрицепнойСостав_СрезПоследнихТ
			|	ПО УпПрицепыТранспортныхСредств_СрезПоследнихТ.Прицеп.Модель = УпНормыРасходаГСМНаПрицепнойСостав_СрезПоследнихТ.МодельПрицепа
			|ГДЕ УпПрицепыТранспортныхСредств_СрезПоследнихТ.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
			|УПОРЯДОЧИТЬ ПО
			|	Приоритет
			|ИТОГИ
			|ПО
			|	ВидГСМ,
			|	Прицеп
			|;
			|//{Запрос: 7 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	упУчетнаяПолитикаСрезПоследних.ЗимняяТемператураВоздуха КАК ЗимняяТемператураВоздуха
			|ИЗ
			|	РегистрСведений.упУчетнаяПолитика.СрезПоследних(
			|		КОНЕЦПЕРИОДА(&Период, МЕСЯЦ),
			|		) КАК упУчетнаяПолитикаСрезПоследних
			|";

		
		Если ЭтоРасчетНормативногоРасходаТоплива Тогда
			ТекстЗапроса = ТекстЗапроса 
				+ ОбщегоНазначения.РазделительПакетаЗапросов() 
				+ "ВЫБРАТЬ
				  |	ЕСТЬNULL(СУММА(упПлановыеПараметрыРейса.ВремяВПути), 0) КАК ВремяВПути,
				  |	ЕСТЬNULL(СУММА(упПлановыеПараметрыРейса.Масса), 0) КАК Масса
				  |ИЗ
				  |	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
				  |ГДЕ
				  |	упПлановыеПараметрыРейса.Рейс В(&мсвРейсы)" 
				+ ОбщегоНазначения.РазделительПакетаЗапросов();
		КонецЕсли;			   
		
		// В запрос добавляется расчет массы груза в точке
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ПакетЗапросовСхемы = СхемаЗапроса.ПакетЗапросов;
		ЗапросГрузыВТочке = ПакетЗапросовСхемы[сткЗапрос.ИндексЗапросаГрузыВТочке];
		ОператорВыбратьСхемыЗапросов = ЗапросГрузыВТочке.Операторы[0];
		ВыбираемыеПоля = ОператорВыбратьСхемыЗапросов.ВыбираемыеПоля;
		
		Если ВходныеПараметры.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками  Тогда
			ПолеМасса = "СУММА(ВЫБОР
			|		КОГДА втРаботыПоРейсам.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
			|			ТОГДА 1
			|		ИНАЧЕ ВЫБОР
			|			КОГДА втРаботыПоРейсам.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
			|				ТОГДА - 1
			|			ИНАЧЕ 0
			|		КОНЕЦ
			|	КОНЕЦ * ВЫБОР
			|		КОГДА втРаботыПоРейсам.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
			|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, втРаботыПоРейсам.Задание.Масса))
			|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, втРаботыПоРейсам.ГрузовоеМесто.Масса))
			|	КОНЕЦ * втРаботыПоРейсам.КоличествоГрузов)";
		Иначе
			ПолеМасса = "СУММА(ВЫБОР
			|			КОГДА втРаботыПоРейсам.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
			|				ТОГДА 1
			|			ИНАЧЕ ВЫБОР
			|					КОГДА втРаботыПоРейсам.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
			|						ТОГДА -1
			|					ИНАЧЕ 0
			|				КОНЕЦ
			|		КОНЕЦ * втРаботыПоРейсам.Масса)";
		КонецЕсли;
		ВыбираемыеПоля.Установить(1, Новый ВыражениеСхемыЗапроса(ПолеМасса));
		
		Запрос = Новый Запрос;
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		мсвРейсы = Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс");
		Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
		Период = ?(ЗначениеЗаполнено(Объект.НачалоРейса), Объект.НачалоРейса, Документы.упРейс.ПолучитьВремяНачалаПоРейсам(мсвРейсы));
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
		Запрос.УстановитьПараметр("МодельТС", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТранспортноеСредство, "Модель"));
		
		Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
		Если НЕ РасчетПоОбъекту Тогда
			Запрос.УстановитьПараметр("ПутевойЛист", Объект.Ссылка);
		Иначе
			Запрос.УстановитьПараметр("ГСМ", Объект.ГСМ.Выгрузить(,"НомерСтроки, ПроцентИспользования, УчитыватьТемпературуВоздухаВПутевомЛисте,ШкалаТемпературныхКоэффициентов,ФормулаРасчета,ТипНормыРасходаГСМ,Расход,ИзменениеРасхода, ВидГСМ"));
			Запрос.УстановитьПараметр("НормыРасходаГСМ", Объект.НормыРасходаГСМ.Выгрузить(,"Количество,НомерСтроки,ТипРасхода,Расход,РасходТопливаПоНорме,ВидЭксплуатационнойНормы, ВидГСМ"));
			Запрос.УстановитьПараметр("ГСМДопОборудования", Объект.ГСМДополнительноеОборудование.Выгрузить(Новый Структура("РасходИзСобственногоБака",Ложь),"ВидГСМ,ДополнительноеОборудование,НормаРасхода,Расход,ТипНормыРасходаГСМ,ФормулаРасчета,ШкалаТемпературныхКоэффициентов"));
			Запрос.УстановитьПараметр("НормыРасходаГСМДопОборудования", Объект.НормыРасходаГСМДополнительноеОборудование.Выгрузить(,"НомерСтроки,ВидЭксплуатационнойНормы,ВидГСМ,ДополнительноеОборудование,ТипРасхода,Расход,Количество"));
			Запрос.УстановитьПараметр("РежимыРаботыДопОборудования", Объект.НормыРасходаРежимовРаботы.Выгрузить(,"ДополнительноеОборудование,Количество,Расход,РежимРаботы"));
			Запрос.УстановитьПараметр("ДополнительноеОборудование", Объект.ДополнительноеОборудование.Выгрузить(Новый Структура("ИспользуютсяРазличныеРежимыРаботы",Ложь),"ДополнительноеОборудование,Моточасы"));
					
		КонецЕсли;	
	
	КонецЕсли;
	
	сткЗапрос.Вставить("Запрос", Запрос);
	
	Возврат сткЗапрос;
	
КонецФункции

Функция СформироватьМаршрутПоРейсам(РезультатЗапросаДанныеДляРасчетаРасходов, ИндексЗапросаПараметрыМаршрута)
	
	сткМаршрут = Новый Структура();
	
	// Формирование таблицы маршрута по рейсам
	тбзМаршрут = Новый ТаблицаЗначений;
	тбзМаршрут.Колонки.Добавить("НомерСтроки",					Новый ОписаниеТипов("Число")); 
	тбзМаршрут.Колонки.Добавить("РасстояниеОтПредыдущейТочкиПлан",	Новый ОписаниеТипов("Число")); 
	тбзМаршрут.Колонки.Добавить("Адрес",						Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	тбзМаршрут.Колонки.Добавить("Идентификатор",					Новый ОписаниеТипов("УникальныйИдентификатор")); 
	тбзМаршрут.Колонки.Добавить("ВремяОтПредыдущейТочки",			Новый ОписаниеТипов("Число")); 
	тбзМаршрут.Колонки.Добавить("Масса",						Новый ОписаниеТипов("Число")); 
	тбзМаршрут.Колонки.Добавить("РасстояниеОтПредыдущейТочкиФакт",	Новый ОписаниеТипов("Число")); 
	тбзМаршрут.Колонки.Добавить("Прибытие", 					Новый ОписаниеТипов("Дата", , ,
															Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	тбзМаршрут.Колонки.Добавить("Убытие", 						Новый ОписаниеТипов("Дата", , ,
										  					Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
														
	сткМаршрут.Вставить("Таблица", тбзМаршрут);  													
														
	ФактРасстояниеПоРейсам 	= 0;
	ПланРасстояниеПоРейсам 	= 0;
	СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса = Неопределено;
	
	// Таблица для расчета расстояний между двумя точками
	тзМаршрутДляРасчета = Новый ТаблицаЗначений;
	тзМаршрутДляРасчета.Колонки.Добавить("НомерСтроки"  , Новый ОписаниеТипов("Число"));
	тзМаршрутДляРасчета.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	тзМаршрутДляРасчета.Колонки.Добавить("Адрес"        , Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	
	ВыборкаМаршрутПоРейсу = РезультатЗапросаДанныеДляРасчетаРасходов[ИндексЗапросаПараметрыМаршрута].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 	
	
	РасстояниеМеждуКрайнимиТочкамиРейсовИтого = 0;
	
	// Заполняется таблица маршрута по рейсам и подсчитывается плановое 
	// и фактическое расстояние по всему маршруту.
	// Фактическое и плановое расстояние между точками увеличивается на 
	// рассчитывемое расстояние между крайними точками рейсов.
	НомерСтрокиТекущий = 1;
	Пока ВыборкаМаршрутПоРейсу.Следующий() Цикл
		
		ВыборкаМаршрут	= ВыборкаМаршрутПоРейсу.Выбрать();
		ИндексПервойТочкиМаршрутаРейса 	= тбзМаршрут.Количество();
		АдресПервойТочкиМаршрутаРейса		= Справочники.упАдреса.ПустаяСсылка();
		
		Пока ВыборкаМаршрут.Следующий() Цикл
			НоваяСтрока = тбзМаршрут.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаМаршрут);
			НоваяСтрока.НомерСтроки = НомерСтрокиТекущий;
			НомерСтрокиТекущий = НомерСтрокиТекущий + 1;
		КонецЦикла;
		
		СтрокаПерваяТочкаМаршрута = Неопределено;
		
		Если тбзМаршрут.Количество() > ИндексПервойТочкиМаршрутаРейса Тогда
			СтрокаПерваяТочкаМаршрута = тбзМаршрут[ИндексПервойТочкиМаршрутаРейса];		
		КонецЕсли;
		
		РасстояниеМеждуКрайнимиТочкамиРейсов 	= 0;

		Если Истина
			И СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса <> Неопределено
			И СтрокаПерваяТочкаМаршрута <> Неопределено
			И СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса.Адрес <> СтрокаПерваяТочкаМаршрута.Адрес 
		Тогда
			тзМаршрутДляРасчета.Очистить();
			НоваяСтрокаМаршрутДляРасчета = тзМаршрутДляРасчета.Добавить();
			НоваяСтрокаМаршрутДляРасчета.НомерСтроки 	= 1;
			НоваяСтрокаМаршрутДляРасчета.Идентификатор 	= Строка(СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса.Идентификатор);
			НоваяСтрокаМаршрутДляРасчета.Адрес 		= СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса.Адрес;
			
			НоваяСтрокаМаршрутДляРасчета = тзМаршрутДляРасчета.Добавить();
			НоваяСтрокаМаршрутДляРасчета.НомерСтроки 	= 2;
			НоваяСтрокаМаршрутДляРасчета.Идентификатор 	= Строка(СтрокаПерваяТочкаМаршрута.Идентификатор);
			НоваяСтрокаМаршрутДляРасчета.Адрес 		= СтрокаПерваяТочкаМаршрута.Адрес;
			
			соотРасстояния = упМаршрутизация.ПолучитьРасстояниеИВремяМеждуТочками(тзМаршрутДляРасчета, 3600, Истина,, ВыборкаМаршрутПоРейсу.Рейс.ОграничениеМаршрутизатора);
			
			сткДанные = соотРасстояния.Получить(СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса.Идентификатор);
			ДеньУбытия = ДеньНедели(СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса.Убытие);
			Если ДеньУбытия = 1 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяПонедельник;
			ИначеЕсли ДеньУбытия = 2 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяВторник;	
			ИначеЕсли ДеньУбытия = 3 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяСреда;	
			ИначеЕсли ДеньУбытия = 4 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяЧетверг;	
			ИначеЕсли ДеньУбытия = 5 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяПятница;	
			ИначеЕсли ДеньУбытия = 6 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяСуббота;	
			ИначеЕсли ДеньУбытия = 7 Тогда
				СтрокаПерваяТочкаМаршрута.ВремяОтПредыдущейТочки = сткДанные.ВремяВоскресенье;	
			КонецЕсли; 		
			
			РасстояниеМеждуКрайнимиТочкамиРейсов 					= сткДанные.Расстояние;
			СтрокаПерваяТочкаМаршрута.РасстояниеОтПредыдущейТочкиПлан 	= РасстояниеМеждуКрайнимиТочкамиРейсов;
			
		КонецЕсли;
		
		Если тбзМаршрут.Количество() > 0 Тогда
			СтрокаПоследняяТочкаМаршрутаПредыдущегоРейса = тбзМаршрут[тбзМаршрут.Количество()-1];		
		КонецЕсли;

		РасстояниеМеждуКрайнимиТочкамиРейсовИтого = РасстояниеМеждуКрайнимиТочкамиРейсовИтого + РасстояниеМеждуКрайнимиТочкамиРейсов;
		ФактРасстояниеПоРейсам 	 = ФактРасстояниеПоРейсам + ВыборкаМаршрутПоРейсу.РасстояниеОтПредыдущейТочкиФакт;
		ПланРасстояниеПоРейсам 	 = ПланРасстояниеПоРейсам + ВыборкаМаршрутПоРейсу.РасстояниеОтПредыдущейТочкиПлан + РасстояниеМеждуКрайнимиТочкамиРейсов;
	
	КонецЦикла;
	
	Если ФактРасстояниеПоРейсам > 0  Тогда
		ФактРасстояниеПоРейсам = ФактРасстояниеПоРейсам + РасстояниеМеждуКрайнимиТочкамиРейсовИтого;
	КонецЕсли;
	
	сткМаршрут.Вставить("ФактическоеРасстояние", ФактРасстояниеПоРейсам);
	сткМаршрут.Вставить("ПлановоеРасстояние", 	ПланРасстояниеПоРейсам);
		
	Возврат сткМаршрут;
		
КонецФункции

Процедура ЗаполнитьЗначениямиНормРасхода(СтруктураДанных, СтрокаГСМ, мсвСтрокиНормРасхода, ВходныеПараметры)
	
	ЭтоРасчетПоДопОборудованию			= ВходныеПараметры.Свойство("РассчитатьДополнительноеОборудование");
	
	// Норма базового расхода топлива
	СтруктураДанных.Вставить("НормаРасходаТопливаТС", 			СтрокаГСМ.Расход);
	СтруктураДанных.Вставить("ИзменениеРасходаТопливаТС", 			СтрокаГСМ.ИзменениеРасхода);
	СтруктураДанных.Вставить("ЭтоНормаРасходаТопливаНа100км", 		СтрокаГСМ.ТипНормыРасходаГСМ = Перечисления.упТипыНормРасходаГСМ.НаСтоКилометров);
	СтруктураДанных.Вставить("ЭтоНормаРасходаТопливаНа1часРаботы", 	СтрокаГСМ.ТипНормыРасходаГСМ = Перечисления.упТипыНормРасходаГСМ.НаОдинЧасРаботы);
	СтруктураДанных.Вставить("ВидГСМ",							СтрокаГСМ.ВидГСМ);
	Если ЭтоРасчетПоДопОборудованию Тогда
		СтруктураДанных.Вставить("Моточасы",						СтрокаГСМ.Количество);	
	КонецЕсли;
	СтруктураДанных.Вставить("УчитыватьТемпературуВоздухаВПутевомЛисте", 	СтрокаГСМ.УчитыватьТемпературуВоздухаВПутевомЛисте);
	

	// Нормы расхода надбавок
	СтруктураДанных.Вставить("НормаРасходаТопливаГорнаяМестность", 		0);
	СтруктураДанных.Вставить("НормаРасходаТопливаГрузовой", 			0);
	СтруктураДанных.Вставить("НормаРасходаТопливаДлительнаяЭксплуатация", 0);
	СтруктураДанных.Вставить("НормаРасходаТопливаЗимний", 				0);
	СтруктураДанных.Вставить("НормаРасходаТопливаКлиматКонтроль", 		0);
	СтруктураДанных.Вставить("НормаРасходаТопливаКондиционер", 			0);
	СтруктураДанных.Вставить("НормаРасходаТопливаНаселенныйПункт", 		0);
	СтруктураДанных.Вставить("НормаРасходаТопливаПростой", 			0);
	СтруктураДанных.Вставить("НормаРасходаТопливаСложностьДорог", 		0);
	СтруктураДанных.Вставить("НормаРасходаТопливаСпециальноеОборудование",0);
	СтруктураДанных.Вставить("НормаРасходаТопливаОтопитель", 			0);
	
	СтруктураДанных.Вставить("КоличествоГорнаяМестность", 		 0);
	СтруктураДанных.Вставить("КоличествоГрузовой", 			 0);
	СтруктураДанных.Вставить("КоличествоДлительнаяЭксплуатация", 0);
	СтруктураДанных.Вставить("КоличествоТопливаЗимний", 		 0);
	СтруктураДанных.Вставить("КоличествоКлиматКонтроль", 		 0);
	СтруктураДанных.Вставить("КоличествоКондиционер", 		 0);
	СтруктураДанных.Вставить("КоличествоНаселенныйПункт", 		 0);
	СтруктураДанных.Вставить("КоличествоПростой", 			 0);
	СтруктураДанных.Вставить("КоличествоСложностьДорог", 		 0);
	СтруктураДанных.Вставить("КоличествоСпециальноеОборудование",0);
	СтруктураДанных.Вставить("КоличествоОтопитель",			 0);
	
	// Заполнение данных для расчета по правилу расчета
	Для каждого СтрокаНорма Из мсвСтрокиНормРасхода Цикл
		ИмяЗначения	= ОбщегоНазначения.ИмяЗначенияПеречисления(СтрокаНорма.ВидЭксплуатационнойНормы);
		СтруктураДанных.Вставить("НормаРасходаТоплива" + ИмяЗначения, СтрокаНорма.Расход);
		СтруктураДанных.Вставить("Количество" + ИмяЗначения, 	СтрокаНорма.Количество);
	КонецЦикла;
КонецПроцедуры

Функция КоэффициентОтношенияПробегаКРасстояниюПоМаршруту(ФактРасстояниеПоРейсам, ПланРасстояниеПоРейсам, ПробегИзПутевогоЛиста)
		
	// Если при прохождении указывалось фактически пройденное расстояние.
	Если ФактРасстояниеПоРейсам > 0  Тогда
		Если ПробегИзПутевогоЛиста > 0 Тогда
			
			// Фактическое расстояние распределяется пропорционально пробегу.
			Коэффициент = ПробегИзПутевогоЛиста / ФактРасстояниеПоРейсам;
			
		Иначе
			Коэффициент = 0;
		КонецЕсли;	
		
		// Если фактического нет, но есть плановое.
	ИначеЕсли ПланРасстояниеПоРейсам > 0 Тогда 	
		Если ПробегИзПутевогоЛиста > 0 Тогда
			
			// Плановое расстояние распределяется пропорционально пробегу
			Коэффициент = ПробегИзПутевогоЛиста / ПланРасстояниеПоРейсам;
			
		Иначе
			Коэффициент = 0;
		КонецЕсли;	
	КонецЕсли;

	Возврат Коэффициент;
	
КонецФункции

Функция РассчитатьБазовыйРасходТоплива(ПравилоРасчета, сткДанные, КоэффициентПересчетаМассыИзКгВЕдиницуИзмеренияМассыПоУмолчанию, сткТрассировка, ДобавитьСообщениеТрассировки, ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте, КоэффициентИспользования, Отказ)
	
	Результат = 0;
	
	Если ЗначениеЗаполнено(ПравилоРасчета) Тогда
		Результат	= ВычислитьФормулуПравилаРасчета(ПравилоРасчета, сткДанные, Отказ, сткТрассировка);	
	Иначе	
		НормаРасхода 		= сткДанные.НормаРасходаТопливаТС;
		// Изменение расхода - дополнительный к базовой норме расход топлива при движении автомобиля с грузом. 
		// Указывается на сколько литров изменится расход топлива при перевозки дополнительной 1 тонны груза на 100 км.
		ИзменениеРасхода	= сткДанные.ИзменениеРасходаТопливаТС;
		
		Если ЕстьДанныеПоРасстояниямМеждуТочкамиВМарщруте Тогда
						
			РасстояниеКм		= сткДанные.Расстояние;
			
			ВремяРаботыЧас		= сткДанные.ВремяРаботыЧас;
			
			КоличествоРасходаБазовый		= ?(сткДанные.ЭтоНормаРасходаТопливаНа100км, РасстояниеКм / 100, ВремяРаботыЧас);
			КоличествоРасходаИзменение 	= ?(сткДанные.ЭтоНормаРасходаТопливаНа100км, РасстояниеКм / 100, 0);
			
			МассаГруза		= сткДанные.МассаГруза;
			МассаГрузаКг		= МассаГруза * КоэффициентПересчетаМассыИзКгВЕдиницуИзмеренияМассыПоУмолчанию; 
			МассаГрузаТонна	= МассаГрузаКг / 1000;
			
			БазовыйРасход	= НормаРасхода * КоличествоРасходаБазовый;
			РасходНаТранспортнуюРаботу	= ИзменениеРасхода * МассаГрузаТонна * КоличествоРасходаИзменение;
			
			Результат = БазовыйРасход + РасходНаТранспортнуюРаботу;
			
			Если ДобавитьСообщениеТрассировки Тогда
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "МассаГруза", , 	МассаГрузаТонна,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "Расстояние", , 	РасстояниеКм,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "Время", , 		ВремяРаботыЧас,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "БазовыйРасход", , БазовыйРасход,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "РасходНаТранспортнуюРаботу", , РасходНаТранспортнуюРаботу,1);
			КонецЕсли;
		Иначе	
			
			// Базовый расход.
			РасстояниеКм				= сткДанные.Пробег;
			ВремяРаботыЧас				= сткДанные.Моточасы;
			КоличествоРасходаБазовый		= ?(сткДанные.ЭтоНормаРасходаТопливаНа100км, РасстояниеКм / 100, ВремяРаботыЧас);
			БазовыйРасход				= НормаРасхода * КоличествоРасходаБазовый;
			
			
			// Расход на транспортную работу.
			ПробегСГрузом 	= сткДанные.ПробегСГрузом;
			ПробегБезГруза = сткДанные.Пробег - ПробегСГрузом;
			
			МоточасыСГрузом	= сткДанные.МоточасыСГрузом;
			МоточасыБезГруза	= сткДанные.Моточасы - МоточасыСГрузом;
			
			
			// Расход на транспортную работу по перевозке груза и прицепного состава.
			КоличествоРасходаИзменение 	= ?(сткДанные.ЭтоНормаРасходаТопливаНа100км, ПробегСГрузом / 100, МоточасыСГрузом);
			МассаГруза		= сткДанные.МассаГруза + сткДанные.МассаПрицепногоСостава;
			МассаГрузаКг		= МассаГруза * КоэффициентПересчетаМассыИзКгВЕдиницуИзмеренияМассыПоУмолчанию; 
			МассаГрузаТонна	= МассаГрузаКг / 1000;
			
			РасходНаТранспортнуюРаботу	= ИзменениеРасхода * МассаГрузаТонна * КоличествоРасходаИзменение;
			
			// Расход на транспортную работу по перевозке прицепного состава.
			КоличествоРасходаИзменение 	= ?(сткДанные.ЭтоНормаРасходаТопливаНа100км, ПробегБезГруза / 100, МоточасыБезГруза);
			МассаГруза		= сткДанные.МассаПрицепногоСостава;
			МассаГрузаКг		= МассаГруза * КоэффициентПересчетаМассыИзКгВЕдиницуИзмеренияМассыПоУмолчанию; 
			МассаГрузаТонна	= МассаГрузаКг / 1000;
			
			РасходНаТранспортнуюРаботу	= РасходНаТранспортнуюРаботу + ИзменениеРасхода * МассаГрузаТонна * КоличествоРасходаИзменение;
		
			Результат = БазовыйРасход + РасходНаТранспортнуюРаботу;
			
			Если ДобавитьСообщениеТрассировки Тогда
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "МассаГруза", , 	МассаГрузаТонна,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "Расстояние", , 	РасстояниеКм,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "Время", , 		ВремяРаботыЧас,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "БазовыйРасход", , БазовыйРасход,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "РасходНаТранспортнуюРаботу", , РасходНаТранспортнуюРаботу,1);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДобавитьСообщениеТрассировки Тогда
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "РасходТоплива", , Результат,1);
	КонецЕсли;	
	
	Результат = Результат * КоэффициентИспользования;
	
	Если ДобавитьСообщениеТрассировки Тогда
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Маршрут", "РасходТопливаСУчетомКоэффициента", , Результат,1);
	КонецЕсли;	

	Возврат Результат;
	
КонецФункции

Функция РассчитатьРасходТопливаНаПрицепы(КоллекцияСтрокиНормГСМ, БазовыйРасходТоплива, сткДанные, сткТрассировка, КоэффициентИспользования, Отказ)
	
	Результат = 0;
	
	Для каждого СтрокаПрицеп Из КоллекцияСтрокиНормГСМ Цикл
		
		Для каждого СтрокаНорма Из СтрокаПрицеп.Строки Цикл
			
			РасходТопливаПоНорме = 0;
               Количество		 = 0;
			КоличествоНадпись	 = 0;
		
			Если СтрокаНорма.ТипРасхода = Перечисления.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода Тогда
				Количество = БазовыйРасходТоплива / 100;
				КоличествоНадпись	 = Количество;
			ИначеЕсли СтрокаНорма.ТипРасхода = Перечисления.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗа100Км Тогда	
				Количество = сткДанные.Расстояние / 100; 
				КоличествоНадпись	 = сткДанные.Расстояние;
			Иначе
				Количество = сткДанные.ВремяРаботыЧас; 
				КоличествоНадпись	 = Количество;
			КонецЕсли;
			
               РасходТопливаПоНорме 		= Количество * СтрокаНорма.Расход; 
			Если СтрокаНорма.ТипРасхода = Перечисления.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода Тогда
				РасходСУчетомКоэффициента = РасходТопливаПоНорме;
			Иначе	
				РасходСУчетомКоэффициента = КоэффициентИспользования * РасходТопливаПоНорме;
			КонецЕсли;
			Результат = Результат + РасходСУчетомКоэффициента;
			
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "Прицеп",Истина, СтрокаНорма.Прицеп,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "ТипРасхода",, СтрокаНорма.ТипРасхода,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "Расход",, СтрокаНорма.Расход,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "Количество",, КоличествоНадпись,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "РасходТопливаПоНорме",, РасходТопливаПоНорме,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "БазовыйРасходТоплива",, БазовыйРасходТоплива,1);
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"Прицеп", "РасходТопливаСУчетомКоэффициента",, РасходСУчетомКоэффициента,1);
			
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РассчитатьРасходТопливаНаДопОборудование(Объект, дрвГСМДопОборудование, НормыРасходаДопОборудование, ВидГСМ = Неопределено, сткДанные, ВходныеПараметры, сткТрассировка, Отказ)
	
	Результат = 0;
	
	ЭтоРасчетПоДопОборудованию			= ВходныеПараметры.Свойство("РассчитатьДополнительноеОборудование");
	ЭтоРасчетНормативногоРасходаТоплива 	= ВходныеПараметры.Свойство("РассчитатьНормативныйРасходТоплива");

	
	Если ЭтоРасчетПоДопОборудованию Тогда
		мсвСтрокиВидыГСМ = дрвГСМДопОборудование.Строки;		
	Иначе
		мсвСтрокиВидыГСМ = дрвГСМДопОборудование.Строки.НайтиСтроки(Новый Структура("ВидГСМ", ВидГСМ));	
	КонецЕсли;
	
	
	Для каждого СтрокаВидГСМ Из мсвСтрокиВидыГСМ Цикл
						
		ВидГСМ = СтрокаВидГСМ.ВидГСМ;
						
		Для каждого СтрокаДопОборудование Из СтрокаВидГСМ.Строки Цикл
			
			ДопОборудование 	 = СтрокаДопОборудование.ДополнительноеОборудование;
			ПравилоРасчета		 = СтрокаДопОборудование.ФормулаРасчета;
			мсвСтрокиНормРасхода = НормыРасходаДопОборудование.НайтиСтроки(Новый Структура("ВидГСМ, ДополнительноеОборудование", ВидГСМ, ДопОборудование));
			
			Если ЭтоРасчетПоДопОборудованию Тогда
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "НомерСтроки", Истина, 	СтрокаДопОборудование.НомерСтроки,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ТипНормыРасходаГСМ",,	СтрокаДопОборудование.ТипНормыРасходаГСМ,1);
				НадписьВидГСМ = СтрШаблон(Нстр("ru = '%1"" Доп. оборудованиене:""%2'"), ВидГСМ, ДопОборудование);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Наименование",,		НадписьВидГСМ,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Правило",,		ПравилоРасчета,1);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "Формула",,		ПравилоРасчета.Формула,1);

				Если ЗначениеЗаполнено(ПравилоРасчета) Тогда
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "НомерСтроки",Истина,1,1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Имя",,	" По документу в целом",1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "Значение",,"",1);
     			КонецЕсли;
			КонецЕсли;
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ДопОборудование", "ДопОборудование",Истина, ДопОборудование,1);

			ПравилоРасчета	 				= Неопределено;
			ШкалаТемпературныхКоэффициентов 	= Неопределено;
			
			РасходТопливаПоДопОборудованию	= 0;
			Для каждого Строка Из СтрокаДопОборудование.Строки Цикл
				ЗаполнитьЗначениямиНормРасхода(сткДанные, Строка, мсвСтрокиНормРасхода, ВходныеПараметры);
				РасходТоплива 	= 0;
				ПравилоРасчета	= Строка.ФормулаРасчета;
				ШкалаТемпературныхКоэффициентов = Строка.ШкалаТемпературныхКоэффициентов;
				Если ЗначениеЗаполнено(ПравилоРасчета) Тогда
					РасходТоплива	= ВычислитьФормулуПравилаРасчета(ПравилоРасчета, сткДанные, Отказ, сткТрассировка, СтрокаДопОборудование);	
				Иначе	
					РасходТоплива = Строка.Расход * Строка.Количество;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПравилоРасчета) Тогда
					
				Иначе	
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"РежимРаботыДопОборудования", "РежимРаботы",Истина, Строка.РежимРаботы,1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"РежимРаботыДопОборудования", "ТипНормыРасходаГСМ",, Строка.ТипНормыРасходаГСМ,1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"РежимРаботыДопОборудования", "Количество",, Строка.Количество,1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"РежимРаботыДопОборудования", "Расход",, Строка.Расход,1);
					упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"РежимРаботыДопОборудования", "РасходТоплива",, РасходТоплива,1);
				КонецЕсли;	

				РасходТопливаПоДопОборудованию =  РасходТопливаПоДопОборудованию + РасходТоплива;
			КонецЦикла;
			
			
			Если НЕ ЗначениеЗаполнено(ПравилоРасчета) Тогда
				НадбавкиРасходТоплива 	= РассчитатьРасходТопливаНаНадбавки(сткДанные, "НадбавкаДопОборудование", мсвСтрокиНормРасхода, РасходТопливаПоДопОборудованию, ВходныеПараметры, сткТрассировка, Отказ);
				РасходТопливаПоДопОборудованию = РасходТопливаПоДопОборудованию + НадбавкиРасходТоплива;
				Если ЗначениеЗаполнено(ШкалаТемпературныхКоэффициентов) Тогда
					РасходТопливаПоДопОборудованию = РасходТопливаПоДопОборудованию * ОЦЕНИТЬПО(ШкалаТемпературныхКоэффициентов, сткДанные.ТемператураВоздуха);
				КонецЕсли;
			КонецЕсли;
			
			Результат = Результат + РасходТопливаПоДопОборудованию;
			
			Если ЭтоРасчетПоДопОборудованию Тогда
				Если ЭтоРасчетНормативногоРасходаТоплива Тогда
					Объект.ГСМДополнительноеОборудование[СтрокаДопОборудование.НомерСтроки - 1].РасходТопливаПоНорме = РасходТопливаПоДопОборудованию;
				Иначе
					Объект.ГСМДополнительноеОборудование[СтрокаДопОборудование.НомерСтроки - 1].РасходТопливаПлан = РасходТопливаПоДопОборудованию;	
				КонецЕсли;	
				Если НЕ ЗначениеЗаполнено(ПравилоРасчета) Тогда
					Для каждого Строка Из мсвСтрокиНормРасхода Цикл
						Если ЭтоРасчетНормативногоРасходаТоплива Тогда
							Объект.НормыРасходаГСМДополнительноеОборудование[Строка.НомерСтроки - 1].РасходТопливаПоНорме = Строка.РасходТопливаПоНорме;
						Иначе
							Объект.НормыРасходаГСМДополнительноеОборудование[Строка.НомерСтроки - 1].РасходТопливаПлан = Строка.РасходТопливаПлан;	
						КонецЕсли;	
					КонецЦикла;
				КонецЕсли;
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ВидГСМ", "ИтогоПоВидуГСМ",,РасходТопливаПоДопОборудованию,1);
			КонецЕсли;

		КонецЦикла;
		
			
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РассчитатьРасходТопливаНаНадбавки(сткДанные, ИмяКоллекцииТрассировки = "Надбавка", мсвСтрокиНормРасхода, БазовыйРасходТоплива, ВходныеПараметры, сткТрассировка, Отказ)
	
	ЭтоРасчетНормативногоРасходаТоплива = ВходныеПараметры.Свойство("РассчитатьНормативныйРасходТоплива");

	Результат = 0;
	
	Для каждого СтрокаНадбавка Из мсвСтрокиНормРасхода Цикл
		
		Если Истина
			И сткДанные.УчитыватьТемпературуВоздухаВПутевомЛисте 
			И СтрокаНадбавка.ВидЭксплуатационнойНормы = ПредопределенноеЗначение("Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Зимний") 
		Тогда
			Если сткДанные.ТемператураВоздуха <= сткДанные.ЗимняяТемператураВоздуха Тогда
				РасходТоплива	= РассчитатьРасходПоНорме(БазовыйРасходТоплива, СтрокаНадбавка.ТипРасхода, СтрокаНадбавка.Расход, СтрокаНадбавка.Количество);
			Иначе	
				РасходТоплива	= 0;
			КонецЕсли;
		Иначе	
			РасходТоплива	= РассчитатьРасходПоНорме(БазовыйРасходТоплива, СтрокаНадбавка.ТипРасхода, СтрокаНадбавка.Расход, СтрокаНадбавка.Количество);
		КонецЕсли;

		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,ИмяКоллекцииТрассировки, "ВидЭксплуатационнойНормы",Истина, СтрокаНадбавка.ВидЭксплуатационнойНормы,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,ИмяКоллекцииТрассировки, "Расход",,СтрокаНадбавка.Расход,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,ИмяКоллекцииТрассировки, "Количество",,СтрокаНадбавка.Количество,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,ИмяКоллекцииТрассировки, "ТипРасхода",,СтрокаНадбавка.ТипРасхода,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,ИмяКоллекцииТрассировки, "РасходТоплива",,РасходТоплива,1);
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,ИмяКоллекцииТрассировки, "БазовыйРасходТоплива",, БазовыйРасходТоплива,1);
				
		Если ЭтоРасчетНормативногоРасходаТоплива Тогда
			СтрокаНадбавка.РасходТопливаПоНорме 	= РасходТоплива;
		Иначе
			СтрокаНадбавка.РасходТопливаПлан		= РасходТоплива;
		КонецЕсли;
		
		Результат = Результат + РасходТоплива;
	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РассчитатьРасходПоНорме(БазовыйРасход, ТипРасхода, Расход, Количество)
	
	Результат = 0;
	
	Если ТипРасхода = Перечисления.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода Тогда
		Результат = БазовыйРасход / 100 * Расход; 
	ИначеЕсли ТипРасхода = Перечисления.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗа100Км Тогда
		Результат = Количество / 100 * Расход;
	Иначе	
		Результат = Количество * Расход;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


#Область ФункцииФормул

Функция ОЦЕНИТЬПО(Шкала, ЗначениеПоказателя) Экспорт
	
	текРезультат = Неопределено;
	
	// Проверка типов
	Если ЗначениеЗаполнено(Шкала) Тогда
		
		Если НЕ ТипЗнч(Шкала) = Тип("СправочникСсылка.упОценочнаяШкала") Тогда
			ВызватьИсключение Нстр("ru = 'Не соответствие типов переданных значений, аргумент ""шкала""'");			
		КонецЕсли;
		
	Иначе	
		 ВызватьИсключение Нстр("ru = 'Передано пустое значение шкалы'");
	КонецЕсли;
	 
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	                  |	тбДиапазон.Значение
	                  |ИЗ
	                  |	Справочник.упОценочнаяШкала.Диапазоны КАК тбДиапазон
	                  |ГДЕ
	                  |	тбДиапазон.Ссылка = &Шкала
	                  |	И тбДиапазон.Начало <= &Значение
	                  |	И тбДиапазон.Окончание > &Значение";
	текЗапрос.УстановитьПараметр("Шкала", Шкала);
	текЗапрос.УстановитьПараметр("Значение", ЗначениеПоказателя);
	текВыборка 		= текЗапрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Значение;
	Иначе
		ТекстСообщения = Нстр("ru = 'Для заданного значения %1 в шкале ""%2"" не задан диапазон'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,ЗначениеПоказателя, СокрЛП(Шкала));
		ВызватьИсключение ТекстСообщения;			
	КонецЕсли;
	
	Возврат текРезультат
	
КонецФункции

Функция РАССЧИТАТЬТАРИФНУЮСЕТКУ(сткПараметрыРасчета, ТарифнаяСетка, Знач ЗначениеПоказателяТарифнойСеткиПоСтолбцам = Неопределено, Знач ЗначениеПоказателяТарифнойСеткиПоСтрокам = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	// Проверка типов
	Если ЗначениеЗаполнено(ТарифнаяСетка) Тогда
		
		Если НЕ ТипЗнч(ТарифнаяСетка) = Тип("СправочникСсылка.упТарифныеСетки") Тогда
			ВызватьИсключение Нстр("ru = 'Не соответствие типов переданных значений, аргумент ""тарифная сетка""'");			
		КонецЕсли;
		
	Иначе	
		 ВызватьИсключение Нстр("ru = 'Передано пустое значение тарифной сетки'");
	 КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТарифныеСетки.ПоказательТарифнойСеткиПоСтолбцам,
	               |	упТарифныеСетки.ПоказательТарифнойСеткиПоСтрокам,
	               |	упТарифныеСетки.ПоказательТарифнойСеткиПоСтолбцам.Код КАК ИдентификаторПоказателяТарифнойСеткиПоСтолбцам,
	               |	упТарифныеСетки.ПоказательТарифнойСеткиПоСтрокам.Код КАК ИдентификаторПоказателяТарифнойСеткиПоСтрокам,
	               |	упТарифныеСетки.ПоказательТарифнойСеткиПоСтолбцам.ПараметрРасчета КАК ПоСтолбцамПараметрРасчета,
	               |	упТарифныеСетки.ПоказательТарифнойСеткиПоСтрокам.ПараметрРасчета КАК ПоСтрокамПараметрРасчета
	               |ИЗ
	               |	Справочник.упТарифныеСетки КАК упТарифныеСетки
	               |ГДЕ
	               |	упТарифныеСетки.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ТарифнаяСетка);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ДанныеТарифнойСетки		= Справочники.упТарифныеСетки.СобратьДанныеТарифнойСетки(ТарифнаяСетка);
		ИдентификаторПоСтолбцам	= Сокрлп(Выборка.ИдентификаторПоказателяТарифнойСеткиПоСтолбцам);
		ИдентификаторПоСтрокам	= Сокрлп(Выборка.ИдентификаторПоказателяТарифнойСеткиПоСтрокам);
		ПоказательПоСтрокам		= Выборка.ПоказательТарифнойСеткиПоСтрокам;
		ПоказательПоСтолбцам	= Выборка.ПоказательТарифнойСеткиПоСтолбцам;
		
		Если ТипЗнч(ДанныеТарифнойСетки) = Тип("ТаблицаЗначений") Тогда
			
			ЗначениеПоказателяПоСтолбцам = сткПараметрыРасчета[ИдентификаторПоСтолбцам];			
			Если НЕ ЗначениеПоказателяТарифнойСеткиПоСтолбцам = Неопределено Тогда
				ТипЗначенияПоказателяПоСтолбцам = ТипЗнч(ЗначениеПоказателяТарифнойСеткиПоСтолбцам);
				НеобходимыйТипЗнч				= ТипЗнч(упТарификацияКлиентСервер.ПолучитьПустоеЗначение(Выборка.ПоСтолбцамПараметрРасчета));
				Если ТипЗначенияПоказателяПоСтолбцам = НеобходимыйТипЗнч Тогда
					ЗначениеПоказателяПоСтолбцам = ЗначениеПоказателяТарифнойСеткиПоСтолбцам;			
				Иначе
					ТекстИсключения = Нстр("ru = 'Не соответствие типов переданных значений, аргумент ""показатель 1""(Передается тип:%1; нужен:%2)'");
					ТекстИсключения = СтрШаблон(ТекстИсключения, ТипЗначенияПоказателяПоСтолбцам, НеобходимыйТипЗнч);
					ВызватьИсключение ТекстИсключения;			
				КонецЕсли;
			КонецЕсли;
			
			ЗначениеПоказателяПоСтрокам	= сткПараметрыРасчета[ИдентификаторПоСтрокам];			
			
			Если НЕ ЗначениеПоказателяТарифнойСеткиПоСтрокам = Неопределено Тогда
				ТипЗначенияПоказателяПоСтолбцам = ТипЗнч(ЗначениеПоказателяТарифнойСеткиПоСтрокам);
				НеобходимыйТипЗнч				= ТипЗнч(упТарификацияКлиентСервер.ПолучитьПустоеЗначение(Выборка.ПоСтрокамПараметрРасчета));
				Если ТипЗначенияПоказателяПоСтолбцам = НеобходимыйТипЗнч Тогда
					ЗначениеПоказателяПоСтрокам = ЗначениеПоказателяТарифнойСеткиПоСтрокам;			
				Иначе
					ТекстИсключения = Нстр("ru = 'Не соответствие типов переданных значений, аргумент ""показатель 2""(Передается тип:%1; нужен:%2)'");
					ТекстИсключения = СтрШаблон(ТекстИсключения, ТипЗначенияПоказателяПоСтолбцам, НеобходимыйТипЗнч);
					ВызватьИсключение ТекстИсключения;			
				КонецЕсли;
			КонецЕсли;
			
			ЗначенияПоказателей = Новый Структура;
			ЗначенияПоказателей.Вставить(ИдентификаторПоСтрокам, 	ЗначениеПоказателяПоСтрокам);
			ЗначенияПоказателей.Вставить(ИдентификаторПоСтолбцам, 	ЗначениеПоказателяПоСтолбцам);
			Результат = упТарификацияВызовСервера.ПолучитьЗначениеПоТарифнойСетке(ДанныеТарифнойСетки, ЗначенияПоказателей, ПоказательПоСтолбцам, ПоказательПоСтрокам);			
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция СформироватьСтруктуруДанных(Объект, ВходныеПараметры) Экспорт
	
	//ПроверитьОграничениеПоДоступностиКомпонент(1);
	
	сткРезультат = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ВходныеПараметры);
	
	Если НЕ ТипЗнч(Объект) = Тип("Структура") Тогда
		
		текРеквизиты	= Объект.Ссылка.Метаданные().Реквизиты;
		
		Для каждого текРеквизит Из текРеквизиты Цикл
			сткРезультат.Вставить(текРеквизит.Имя, Объект[текРеквизит.Имя]);
		КонецЦикла;

	КонецЕсли;
	Возврат сткРезультат;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ПодготовитьВходныеПараметры(Объект, ВходныеПараметры, сткТрассировка)
	
	Если НЕ ВходныеПараметры.Свойство("СписокСтатей") Тогда
		ВходныеПараметры.Вставить("СписокСтатей", Новый Массив);
	КонецЕсли;
	
	текТипЗнч = ТипЗнч(Объект.Ссылка);
	
	Если текТипЗнч = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда

		ПодготовитьВходныеПараметры_ЗаявкаНаПеревозкуГруза(Объект, ВходныеПараметры, сткТрассировка);
			
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упФактическиеДоходы") Тогда	 
		
		ПодготовитьВходныеПараметры_ФактическиеДоходы(Объект, ВходныеПараметры, сткТрассировка);
		 
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упРейс") Тогда
		
		Если ВходныеПараметры.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			ПодготовитьВходныеПараметры_Рейс(Объект, ВходныеПараметры, сткТрассировка);
		Иначе	
			ПодготовитьВходныеПараметры_Рейс_БезДетализации(Объект, ВходныеПараметры, сткТрассировка);	
		КонецЕсли;
				
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		
		ПодготовитьВходныеПараметры_ЗаявкаНаТС(Объект, ВходныеПараметры, сткТрассировка);
		 
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упСтрахованиеГруза") Тогда
		
		ПодготовитьВходныеПараметры_СтрахованиеГруза(Объект, ВходныеПараметры, сткТрассировка);
		
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда
		
		ПодготовитьВходныеПараметры_ПредложениеПеревозчика(Объект, ВходныеПараметры, сткТрассировка);
		
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упФактическиеРасходы") 
		ИЛИ текТипЗнч = Тип("Структура") И Объект.Свойство("ЭтоФактическиеРасходы")  Тогда	

		Если ВходныеПараметры.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			ПодготовитьВходныеПараметры_ФактическиеРасходы(Объект, ВходныеПараметры, сткТрассировка);
		Иначе
			ПодготовитьВходныеПараметры_ФактическиеРасходы_БезДетализации(Объект, ВходныеПараметры, сткТрассировка);		
		КонецЕсли;	
			
	КонецЕсли;		 
		
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	текТипЗнч = ТипЗнч(Объект.Ссылка);
	
	Если текТипЗнч = Тип("ДокументСсылка.упРейс") 
		ИЛИ текТипЗнч = Тип("ДокументСсылка.упПредложениеПеревозчика") 
		ИЛИ текТипЗнч = Тип("ДокументСсылка.упСтрахованиеГруза") Тогда
		
		Если сткДанныеПоОбъектуРасчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			ПодготовитьПараметрыРасчета_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
		Иначе
			ПодготовитьПараметрыРасчета_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);	
		КонецЕсли;	
	
	ИначеЕсли текТипЗнч = Тип("ДокументСсылка.упФактическиеДоходы")  Тогда
		
		ПодготовитьПараметрыРасчета_ФактическиеДоходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
		
	ИначеЕсли  текТипЗнч = Тип("ДокументСсылка.упФактическиеРасходы") Тогда	
		Если сткДанныеПоОбъектуРасчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			ПодготовитьПараметрыРасчета_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
	 	Иначе
			ПодготовитьПараметрыРасчета_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
		КонецЕсли;	
	ИначеЕсли	 текТипЗнч = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда	
		 ПодготовитьПараметрыРасчета_ЗаявкаНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
	КонецЕсли;	
	
	Если ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "ОбъемныйВес") Тогда
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "ГрузоподъемностьТС") Тогда
			мсвПараметрыДляРасчетаПоКаждойСтроке.Добавить(Перечисления.упПараметрыРасчета.ГрузоподъемностьТС);	
		КонецЕсли; 
	 	Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "ОбъемТС") Тогда
			мсвПараметрыДляРасчетаПоКаждойСтроке.Добавить(Перечисления.упПараметрыРасчета.ОбъемТС);	
		КонецЕсли; 
	КонецЕсли;
			
	Если ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "ОбъемныйВес") Тогда
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "ГрузоподъемностьТС") Тогда
			мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(Перечисления.упПараметрыРасчета.ГрузоподъемностьТС);	
		КонецЕсли; 
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "ОбъемТС") Тогда
			мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(Перечисления.упПараметрыРасчета.ОбъемТС);	
		КонецЕсли; 
	КонецЕсли;	

КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке, дрвПараметрыПоПравиламРасчета)
	
	тбзРезультат = Неопределено;
		
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		
		тбзРезультат = ТаблицаДанныхДляРасчетаПоКаждойСтроке_ЗаявкаНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке, дрвПараметрыПоПравиламРасчета);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упФактическиеДоходы") Тогда
		
		тбзРезультат = ТаблицаДанныхДляРасчетаПоКаждойСтроке_ФактическиеДоходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упРейс") 
			ИЛИ ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упЗаявкаНаТС")
			ИЛИ ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упПредложениеПеревозчика") 
			ИЛИ ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упСтрахованиеГруза") Тогда
			
		Если сткДанныеПоОбъектуРасчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда	
			тбзРезультат = ТаблицаДанныхДляРасчетаПоКаждойСтроке_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке);
		Иначе
			тбзРезультат = ТаблицаДанныхДляРасчетаПоКаждойСтроке_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке);	
		КонецЕсли;
				
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы")
			ИЛИ ТипЗнч(Объект) = Тип("Структура") И  Объект.Свойство("ЭтоФактическиеРасходы") Тогда
			
		Если сткДанныеПоОбъектуРасчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда	
			тбзРезультат = ТаблицаДанныхДляРасчетаПоКаждойСтроке_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке);	
		Иначе
			тбзРезультат = ТаблицаДанныхДляРасчетаПоКаждойСтроке_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДанныхДляРасчетаПоКаждойСтроке);		
		КонецЕсли;	
		
	КонецЕсли;
	Возврат тбзРезультат;
	
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, дрвПараметрыПоПравиламРасчета)
	
	тбзРезультат = Неопределено;
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		
		тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ЗаявкаНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, дрвПараметрыПоПравиламРасчета);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упФактическиеДоходы") Тогда
		
		тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеДоходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом);
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упРейс") 
		ИЛИ ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упЗаявкаНаТС")
		ИЛИ ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упПредложениеПеревозчика") 
		ИЛИ ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упСтрахованиеГруза") Тогда
		
		Если сткДанныеПоОбъектуРасчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом);
		Иначе
			тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом);
		КонецЕсли;
				
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы") Тогда
		
		Если сткДанныеПоОбъектуРасчета.ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом);
		Иначе
			тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом);
		КонецЕсли;
			
	КонецЕсли;
	
	
	Возврат тбзРезультат;
	
КонецФункции

// Функция - фильтрует таблицу по колонкам, 
//  имена колонок хранятся в свойстве структуры объекта, значения в строке расчета.
//
// Параметры:
//  СтрокаРасчета - Строка - хранятся значения для фильтрации.
//  сткДанныеПоОбъектуРасчета - Структура - в свойствах "ДополнительныеКолонкиПоСтроке",
//   или "ДополнительныеКолонкиПоДокументу", хранятся имена колонок для фильтрации.
//  тбзДанныеДляРасчета	- ТаблицаЗначений - таблица для наложения фильтра.
//  ЭтоРасчетПоСтроке - Булево - Флаг.
// 
// Возвращаемое значение:
//   Массив - ссылки на строки таблицы для расчета.
//
Функция ОтфильтроватьПоДопКолонкам(СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета, ЭтоРасчетПоСтроке = Истина)
	
	мсвРезультат = Новый Массив;
	текДополнительныеКолонки = "";
	
	Если ЭтоРасчетПоСтроке Тогда
		ИмяСвойстваДополнительныеКолонки = "ДополнительныеКолонкиПоСтроке";
	Иначе	
		ИмяСвойстваДополнительныеКолонки = "ДополнительныеКолонкиПоДокументу";	
	КонецЕсли;
	
	Если сткДанныеПоОбъектуРасчета.Свойство(ИмяСвойстваДополнительныеКолонки, текДополнительныеКолонки) И НЕ текДополнительныеКолонки = "" Тогда
		мсвДополнительныеКолонки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(текДополнительныеКолонки, ",");
		сткОтбора = Новый Структура();
		Для каждого ДополнительнаяКолонка Из мсвДополнительныеКолонки Цикл
			Если ЭтоРасчетПоСтроке Тогда
				Если ЗначениеЗаполнено(СтрокаРасчета[ДополнительнаяКолонка]) Тогда
					сткОтбора.Вставить(ДополнительнаяКолонка, СтрокаРасчета[ДополнительнаяКолонка]);			
				КонецЕсли;
			Иначе	
				сткОтбора.Вставить(ДополнительнаяКолонка, СтрокаРасчета[ДополнительнаяКолонка]);		
			КонецЕсли;
			
		КонецЦикла;
		
		Если сткОтбора.Количество() > 0 Тогда
			мсвРезультат = тбзДанныеДляРасчета.НайтиСтроки(сткОтбора);
		Иначе
			Для каждого Строка Из тбзДанныеДляРасчета Цикл
				мсвРезультат.Добавить(Строка);	
			КонецЦикла;	
		КонецЕсли;
	Иначе	
		Для каждого Строка Из тбзДанныеДляРасчета Цикл
			мсвРезультат.Добавить(Строка);	
		КонецЦикла;	
	КонецЕсли;
	
	Возврат мсвРезультат;
		
КонецФункции

// Функция - Вычисляет формулу по заданному правилу расчета учитывая коэффициент распределения.
//
// Параметры:
//  Объект - ТаблицаЗначений - Таблица данных.
//  ПравилоРасчета - Строка - Правило.
//  ПравилоРаспределения - Строка - Правило.
//  СтрокаРасчета - Строка - хранятся значения для фильтрации.
//  сткДанныеПоОбъектуРасчета - Структура - Данные.
//  тбзДанныеДляРасчета - ТаблицаЗначений - Таблица данных.
//  Отказ - Булево - Истина, в случае ошибки.
//  сткТрассировка - Структура - описание см. ОбщийМодуль.упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки.
// 
// Возвращаемое значение:
//  Число - результат расчета
//
Функция ВычислитьФормулуПравилаРасчетаУчитываяКоэффициентРаспределения(Объект, ПравилоРасчета, ПравилоРаспределения, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета, Отказ, сткТрассировка = Неопределено)
	
	СтрокаДанные 	= ПолучитьСтрокуДанныхДляРасчета(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета);	
	
	текРезультат = 0;
	
	Если НЕ СтрокаДанные = Неопределено Тогда
		
		Коэффициент		= ПолучитьКоэффициентРаспределения(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета, ПравилоРаспределения);
		
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "КоэффициентРаспределения",,Коэффициент,1);
		
		текРезультат = ВычислитьФормулуПравилаРасчета(ПравилоРасчета, СтрокаДанные.СтруктураДанные, Отказ, сткТрассировка, СтрокаРасчета) * Коэффициент;
		
		Если НЕ Отказ Тогда
			упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткТрассировка,"ОбъектРасчета", "ИтогоПоОбъектуСКоэффициентом",,текРезультат,1);
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат текРезультат;
	
КонецФункции 

Функция ПолучитьСтрокуДанныхДляРасчета(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета)
	
	текРезультат = Неопределено;
	
	текТипЗнч = ТипЗнч(Объект.Ссылка);
	
	Если текТипЗнч = Тип("ДокументСсылка.упФактическиеРасходы") 
		ИЛИ текТипЗнч = Тип("Структура") И Объект.Свойство("ЭтоФактическиеРасходы")  Тогда	
		текРезультат = ПолучитьСтрокуДанныхДляРасчета_ФактическиеРасходы(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета);
	КонецЕсли;		 
	
	Возврат текРезультат;
		
КонецФункции

Функция ПолучитьКоэффициентРаспределения(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета, ПравилоРаспределения)
	
	текРезультат = 1;
	
	текТипЗнч = ТипЗнч(Объект.Ссылка);
	
	Если текТипЗнч = Тип("ДокументСсылка.упФактическиеРасходы") 
		ИЛИ текТипЗнч = Тип("Структура") И Объект.Свойство("ЭтоФактическиеРасходы")  Тогда	
		текРезультат = ПолучитьКоэффициентРаспределения_ФактическиеРасходы(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета, ПравилоРаспределения);
	КонецЕсли;		 
	
	Возврат текРезультат;

	
КонецФункции

#Область РасчетПоЗаявкеНаПеревозкуГруза

Процедура ПодготовитьВходныеПараметры_ЗаявкаНаПеревозкуГруза(Объект, ВходныеПараметры, сткТрассировка)
	
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяДоходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "ПлановыеДоходы");
	ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));
	ВходныеПараметры.Вставить("ГруппаТарифов", Объект.Соглашение.ГруппаТарифов);
	ВходныеПараметры.Вставить("Организация", Объект.Организация);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", Объект.Ссылка);
	ВходныеПараметры.Вставить("Рейс", Неопределено);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаДоходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаДоходыНДС");
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое",	Объект.ОтправлениеГрузаС);
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", Объект.ОтправлениеГрузаПо);
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", Объект.ПолучениеГрузаС);
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", Объект.ПолучениеГрузаПо);
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", Объект.ОтправлениеГрузаС);
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", Объект.ОтправлениеГрузаС);
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", Объект.ПолучениеГрузаС);
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", Объект.ПолучениеГрузаС);
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", Объект.ОтправлениеГрузаПо);
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", Объект.ОтправлениеГрузаПо);
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", Объект.ПолучениеГрузаПо);
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", Объект.ПолучениеГрузаПо);
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", 0);
	ВходныеПараметры.Вставить("Расстояние",	0);
	ВходныеПараметры.Вставить("ВремяРаботыЧас", 0);
	ВходныеПараметры.Вставить("ВидПеревозки", Объект.ВидПеревозки);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	сткХарактеристикТС	= СтруктураХарактеристикиТС();
	ЗаполнитьПоСтруктуре(ВходныеПараметры, сткХарактеристикТС);
		
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета_ЗаявкаНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоСтроке", "СтатьяДоходов");
	сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоДокументу", "СтатьяДоходов");
	
КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке_ЗаявкаНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета, дрвПараметрыПоПравиламРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	тбзРезультат.Колонки.Добавить("ГрузовоеМесто");
	тбзРезультат.Колонки.Добавить("СтатьяДоходов");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =  
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	МАКСИМУМ(тбГрузовыеМеста.АдресОтправления) КАК АдресОтправления,
	|	МАКСИМУМ(тбГрузовыеМеста.АдресПолучения) КАК АдресПолучения,
	|	СУММА(тбГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, тбГрузовыеМеста.ГрузовоеМесто.Масса) * тбГрузовыеМеста.КоличествоГрузов) КАК МассаГруза,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, тбГрузовыеМеста.ГрузовоеМесто.Объем) * тбГрузовыеМеста.КоличествоГрузов) КАК ОбъемГруза,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, тбГрузовыеМеста.ГрузовоеМесто.КоличествоМест) * тбГрузовыеМеста.КоличествоГрузов) КАК КоличествоМестГруза,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, тбГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров) * тбГрузовыеМеста.КоличествоГрузов) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(тбГрузовыеМеста.ГрузовоеМесто.ОценочнаяСтоимость * тбГрузовыеМеста.КоличествоГрузов) КАК ОценочнаяСтоимостьГруза,
	|	МАКСИМУМ(тбГрузовыеМеста.ГрузовоеМесто.ТипГруза) КАК ТипГруза,
	|	МАКСИМУМ(тбГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим) КАК ТемпературныйРежим
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку = &Ссылка) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И тбГрузовыеМеста.Ссылка = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И тбГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|ГДЕ тбГрузовыеМеста.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	тбГрузовыеМеста.ГрузовоеМесто
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	|	ВЫБОР
	|		КОГДА упТранспортныеСредства.Грузоподъемность = 0
	|			ТОГДА упТранспортныеСредства.ТипТС.Грузоподъемность
	|		ИНАЧЕ упТранспортныеСредства.Грузоподъемность
	|	КОНЕЦ КАК ГрузоподъемностьТС,
	|	ВЫБОР
	|		КОГДА упТранспортныеСредства.Объем = 0
	|			ТОГДА упТранспортныеСредства.ТипТС.Объем
	|		ИНАЧЕ упТранспортныеСредства.Объем
	|	КОНЕЦ КАК ОбъемТС,
	|	ВЫБОР
	|		КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров = 0
	|			ТОГДА упТранспортныеСредства.ТипТС.КоличествоЗагрузочныхМетров
	|		ИНАЧЕ упТранспортныеСредства.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
	|	ВЫБОР
	|		КОГДА упТранспортныеСредства.КоличествоМест = 0
	|			ТОГДА упТранспортныеСредства.ТипТС.КоличествоМест
	|		ИНАЧЕ упТранспортныеСредства.КоличествоМест
	|	КОНЕЦ КАК КоличествоМестТС
	|ПОМЕСТИТЬ втТранспортныеСредства
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|ГДЕ НЕ (упТранспортныеСредства.ПометкаУдаления)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.МассаГруза КАК МассаГруза,
	|	втГрузовыеМеста.ОбъемГруза КАК ОбъемГруза,
	|	втГрузовыеМеста.КоличествоМестГруза КАК КоличествоМестГруза,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетровГруза КАК КоличествоЗагрузочныхМетровГруза,
	|	втГрузовыеМеста.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	0 КАК Расстояние,
	|	0 КАК ВремяРаботыЧас,
	|	втГрузовыеМеста.ТипГруза КАК ТипГруза,
	|	втГрузовыеМеста.ТемпературныйРежим КАК ТемпературныйРежим
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ЕСТЬNULL(втТипыТС.Ссылка, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)) КАК ТипТС
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК втТипыТС
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.МассаГруза <= втТипыТС.Грузоподъемность
	|		И втГрузовыеМеста.ОбъемГруза <= втТипыТС.Объем
	|		И втГрузовыеМеста.КоличествоМестГруза <= втТипыТС.КоличествоМест
	|		И втГрузовыеМеста.КоличествоЗагрузочныхМетровГруза <= втТипыТС.КоличествоЗагрузочныхМетров
	|УПОРЯДОЧИТЬ ПО
	|	втТипыТС.Грузоподъемность,
	|	втТипыТС.Объем,
	|	втТипыТС.КоличествоМест,
	|	втТипыТС.КоличествоЗагрузочныхМетров
	|ИТОГИ
	|ПО
	|	ГрузовоеМесто
	|";

	Запрос.УстановитьПараметр("Ссылка", сткДанныеПоОбъектуРасчета.Ссылка);  
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	тбзГрузовыеМеста = мсвРезультатЗапроса[2].Выгрузить();
	
	НуженТипТС = НуженТипТС(мсвПараметрыРасчета);
	тбзРасстоянияМеждуАдресами = Неопределено;
	
	Для каждого Строка Из тбзГрузовыеМеста Цикл
		ЗаполнитьРасстояниеИВремяПоГрузовомуМестуЗаявкиНаПеревозкуГруза(Строка, Объект, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами);	
	КонецЦикла;
	
	дрвТипыТСПоГрузовымМестам = мсвРезультатЗапроса[3].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);

	СтрокиСтатья = дрвПараметрыПоПравиламРасчета.Строки;
	Для каждого СтрокаСтатья Из СтрокиСтатья Цикл
		
		Если НЕ СтрокаСтатья.РасчетПроводитсяПоКаждойСтроке Тогда
			Продолжить;
		КонецЕсли;
		
		// Заполняются данные по грузовым местам.
		Для каждого СтрокаГрузовоеМесто Из тбзГрузовыеМеста Цикл
			НоваяСтрока = тбзРезультат.Добавить();
			НоваяСтрока.СтатьяДоходов = СтрокаСтатья.СтатьяДоходовРасходов;
			НоваяСтрока.ГрузовоеМесто = СтрокаГрузовоеМесто.ГрузовоеМесто;
			НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
			
			
			сткХарактеристикиГруза = СтруктураХаратеристикГруза();
			ЗаполнитьЗначенияСвойств(сткХарактеристикиГруза, СтрокаГрузовоеМесто);
			ЗаполнитьПоСтруктуре(НоваяСтрока.СтруктураДанные, сткХарактеристикиГруза);
			
			ТекстСообщения = Нстр("ru = '%1(Масса:%2;Объем:%3;Количество мест:%4;Количество загр. м.:%5)'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаГрузовоеМесто.ГрузовоеМесто, 
																									сткХарактеристикиГруза.МассаГруза,
																									сткХарактеристикиГруза.ОбъемГруза,
																									сткХарактеристикиГруза.КоличествоМестГруза,
																									сткХарактеристикиГруза.КоличествоЗагрузочныхМетровГруза,
																									сткХарактеристикиГруза.ОценочнаяСтоимостьГруза);
		
			НоваяСтрока.СтруктураДанные.Вставить("ОбъектРасчета",	Новый Структура("Имя, Значение",НСтр("ru = 'Грузовое место'"), ТекстСообщения));  
		КонецЦикла;
		
		// Заполняется ТипТС.
		Если НуженТипТС Тогда
			СтрокиГрузовыеМеста =  дрвТипыТСПоГрузовымМестам.Строки;
			Для каждого СтрокаГрузовоеМесто Из СтрокиГрузовыеМеста Цикл
				текГрузовоеМесто = СтрокаГрузовоеМесто.ГрузовоеМесто;
				СтрокиТипыТС = СтрокаГрузовоеМесто.Строки;
				Если СтрокиТипыТС.Количество()>0 Тогда
					СтрокаТипТС = СтрокиТипыТС[0];
					мсвСтрокиГрузовоеМесто = тбзРезультат.НайтиСтроки(Новый Структура("ГрузовоеМесто",текГрузовоеМесто));
					Если мсвСтрокиГрузовоеМесто.Количество() > 0 Тогда
						СтрокаГрузовоеМесто = мсвСтрокиГрузовоеМесто[0];
						СтрокаГрузовоеМесто.СтруктураДанные.Вставить("ТипТС", СтрокаТипТС.ТипТС);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

		// Заполняются характеристики ТС.
		// ****************************************************************************************************************
		// Детализация по правилам(статьям доходов) только из-за этих характеристик, потому что если в правиле используется,
		// две характеристики ТС, то ТС по этому правилу должно удовлетоврять обеим характеристикам.
		// В то же время для другого правила могут быть свои характеристики ТС.
		// Например: в Правиле1 есть параметры учета ГрузоподъемностьТС и ОбъемТС, а в Правиле2 только ГрузоподъемностьТС,
		// тогда ТС для Правила1 будет удовлетворять двум характеристикам, а ТС для Правила2 только одной.
		// ****************************************************************************************************************
			
		СтрокиПараметры = СтрокаСтатья.Строки;
		мсвНовыеПараметрыРасчета = Новый Массив;
		Для каждого СтрокаПараметр Из СтрокиПараметры Цикл
			мсвНовыеПараметрыРасчета.Добавить(СтрокаПараметр.ПараметрРасчета);
		КонецЦикла;
		
		НужныХарактеристикиТСПоПравилу = НужныХарактеристикиТС(мсвНовыеПараметрыРасчета);
		НуженОбъемныйВес = НуженОбъемныйВес(мсвНовыеПараметрыРасчета);
		
		Если НужныХарактеристикиТСПоПравилу Тогда
			
			СхемаЗапроса = Новый СхемаЗапроса;
			ТекстЗапроса = "ВЫБРАТЬ
			|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
			|	ЕСТЬNULL(втТранспортныеСредства.ГрузоподъемностьТС, 0) КАК ГрузоподъемностьТС,
			|	ЕСТЬNULL(втТранспортныеСредства.ОбъемТС, 0) КАК ОбъемТС,
			|	ЕСТЬNULL(втТранспортныеСредства.КоличествоЗагрузочныхМетровТС, 0) КАК КоличествоЗагрузочныхМетровТС,
			|	ЕСТЬNULL(втТранспортныеСредства.КоличествоМестТС, 0) КАК КоличествоМестТС
			|ИЗ
			|	втГрузовыеМеста КАК втГрузовыеМеста
			|		ЛЕВОЕ СОЕДИНЕНИЕ втТранспортныеСредства КАК втТранспортныеСредства
			|		ПО (НЕ &УчитыватьГрузоподъемностьТС
			|				ИЛИ втГрузовыеМеста.МассаГруза <= втТранспортныеСредства.ГрузоподъемностьТС)
			|			И (НЕ &УчитыватьОбъемТС
			|				ИЛИ втГрузовыеМеста.ОбъемГруза <= втТранспортныеСредства.ОбъемТС)
			|			И (НЕ &УчитыватьКоличествоМестТС
			|				ИЛИ втГрузовыеМеста.КоличествоМестГруза <= втТранспортныеСредства.КоличествоМестТС)
			|			И (НЕ &УчитыватьКоличествоЗагрузочныхМетровТС
			|				ИЛИ втГрузовыеМеста.КоличествоЗагрузочныхМетровГруза <= втТранспортныеСредства.КоличествоЗагрузочныхМетровТС)
			|ИТОГИ ПО
			|	ГрузовоеМесто";
			СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
			
			УчитыватьОбъемныйВес = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "ОбъемныйВес");
			УчитыватьГрузоподъемностьТС = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "ГрузоподъемностьТС") ИЛИ УчитыватьОбъемныйВес;
			УчитыватьОбъемТС = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "ОбъемТС") ИЛИ УчитыватьОбъемныйВес;
			УчитыватьКоличествоМестТС = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "КоличествоМестТС") ИЛИ УчитыватьОбъемныйВес;
			УчитыватьКоличествоЗагрузочныхМетровТС	= ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "КоличествоЗагрузочныхМетровТС") ИЛИ УчитыватьОбъемныйВес;
			
			ПакетЗапросовСхемы = СхемаЗапроса.ПакетЗапросов;
			ЗапросНаХарактеристикиТС = ПакетЗапросовСхемы[0];
			ВыраженияПорядкаСхемыЗапроса = ЗапросНаХарактеристикиТС.Порядок;
			
			Если УчитыватьГрузоподъемностьТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("ГрузоподъемностьТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Если УчитыватьОбъемТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("ОбъемТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Если УчитыватьКоличествоМестТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("КоличествоМестТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Если УчитыватьКоличествоЗагрузочныхМетровТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("КоличествоЗагрузочныхМетровТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
			Запрос.УстановитьПараметр("УчитыватьГрузоподъемностьТС", УчитыватьГрузоподъемностьТС);  
			Запрос.УстановитьПараметр("УчитыватьОбъемТС", УчитыватьОбъемТС);  
			Запрос.УстановитьПараметр("УчитыватьКоличествоМестТС", УчитыватьКоличествоМестТС);  
			Запрос.УстановитьПараметр("УчитыватьКоличествоЗагрузочныхМетровТС", УчитыватьКоличествоЗагрузочныхМетровТС);  
			
			ВыборкаГрузовоеМесто = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаГрузовоеМесто.Следующий() Цикл
				
				текГрузовоеМесто = ВыборкаГрузовоеМесто.ГрузовоеМесто;
				
				ВыборкаХарактеристикиТС = ВыборкаГрузовоеМесто.Выбрать();
				
				сткХарактеристикиТС = СтруктураХарактеристикиТС();
				Если ВыборкаХарактеристикиТС.Следующий() Тогда
					ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, ВыборкаХарактеристикиТС);
				КонецЕсли;
				
				мсвСтрокиГрузовоеМесто = тбзРезультат.НайтиСтроки(Новый Структура("ГрузовоеМесто, СтатьяДоходов",текГрузовоеМесто, СтрокаСтатья.СтатьяДоходовРасходов));
				Если мсвСтрокиГрузовоеМесто.Количество() > 0 Тогда
					СтрокаГрузовоеМесто = мсвСтрокиГрузовоеМесто[0];
					ЗаполнитьЗначенияСвойств(СтрокаГрузовоеМесто.СтруктураДанные, сткХарактеристикиТС,,"ТипТС");
					Если НуженОбъемныйВес Тогда
						ЗаполнитьОбъемныйВес(СтрокаГрузовоеМесто.СтруктураДанные);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тбзРезультат;
	
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ЗаявкаНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета, дрвПараметрыПоПравиламРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	тбзРезультат.Колонки.Добавить("СтатьяДоходов");
	
	НужныПараметрыГруза = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныРасстояния = НужныРасстояния(мсвПараметрыРасчета);
	НуженТипТС = НуженТипТС(мсвПараметрыРасчета);
	НужныФактическиеРасходы = НужныФактическиеРасходы(мсвПараметрыРасчета);
	НужнаПлановаяСуммаИнкассации = НужнаПлановаяСуммаИнкассации(мсвПараметрыРасчета);
	НуженТипГруза = НуженТипГруза(мсвПараметрыРасчета);
	НуженТемпературныйРежим = НуженТемпературныйРежим(мсвПараметрыРасчета);
		
	тбзРасстоянияМеждуАдресами = Неопределено;
	
	Если НужныРасстояния Тогда
		ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами);	
	КонецЕсли;

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	СУММА(тбЗаявки.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(тбЗаявки.КоличествоМест) КАК КоличествоМест,
	|	СУММА(тбЗаявки.Масса) КАК Масса,
	|	СУММА(тбЗаявки.Объем) КАК Объем
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, тбГрузовыеМеста.ГрузовоеМесто.Масса) * тбГрузовыеМеста.КоличествоГрузов КАК Масса,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, тбГрузовыеМеста.ГрузовоеМесто.Объем) * тбГрузовыеМеста.КоличествоГрузов КАК Объем,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, тбГрузовыеМеста.ГрузовоеМесто.КоличествоМест) * тбГрузовыеМеста.КоличествоГрузов КАК КоличествоМест,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, тбГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров) * тбГрузовыеМеста.КоличествоГрузов КАК КоличествоЗагрузочныхМетров
	|	ИЗ
	|		Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку = &ЗаявкаНаПеревозкуГруза) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И тбГрузовыеМеста.Ссылка = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И тбГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ГДЕ ИСТИНА
	|		И тбГрузовыеМеста.Ссылка = &ЗаявкаНаПеревозкуГруза
	|		И тбГрузовыеМеста.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, тбГрузовыеМеста.ГрузовоеМесто.Масса) * тбГрузовыеМеста.КоличествоГрузов КАК Масса,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, тбГрузовыеМеста.ГрузовоеМесто.Объем) * тбГрузовыеМеста.КоличествоГрузов КАК Объем,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, тбГрузовыеМеста.ГрузовоеМесто.КоличествоМест) * тбГрузовыеМеста.КоличествоГрузов КАК КоличествоМест,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, тбГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров) * тбГрузовыеМеста.КоличествоГрузов КАК КоличествоЗагрузочныхМетров
	|	ИЗ
	|		Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку = &ЗаявкаНаПеревозкуГруза) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И тбГрузовыеМеста.Ссылка = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|	ГДЕ ИСТИНА
	|		И тбГрузовыеМеста.Ссылка = &ЗаявкаНаПеревозкуГруза
	|		И тбГрузовыеМеста.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)) КАК тбЗаявки
	|";

	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", сткДанныеПоОбъектуРасчета.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		текМассаГруза = Выборка.Масса;
		текОбъем = Выборка.Объем;
		текКоличествоМест = Выборка.КоличествоМест;
		текКоличествоЗагрузочныхМетров = Выборка.КоличествоЗагрузочныхМетров;
	Иначе	
		текМассаГруза = Объект.Масса;
		текОбъем = Объект.Объем;
		текКоличествоМест = Объект.КоличествоМест;
		текКоличествоЗагрузочныхМетров = Объект.КоличествоЗагрузочныхМетров;
	КонецЕсли;
		
	текЗонаПолучения = Объект.ЗонаПолучения;
	текТипТС = Справочники.упТипыТС.ПустаяСсылка();
	сткХарактеристикиТС = СтруктураХарактеристикиТС();
	текОценочнаяСтоимость = Объект.ОценочнаяСтоимость;
	текФактическиеРасходыПоЗаявке	= 0;
	текПлановаяСуммаИнкассации = 0;
	текТипГруза = Справочники.упТипыГрузов.ПустаяСсылка();
	текТемпературныйРежим = Справочники.упТемпературныеРежимы.ПустаяСсылка();
	
	Если НужныФактическиеРасходы Тогда
		Запрос.Текст = "ВЫБРАТЬ
		                |	СУММА(упРасходыОбороты.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
		                |ИЗ
		                |	РегистрНакопления.упРасходы.Обороты(
		                |			,
		                |			,
		                |			,
		                |			ЗаданиеНаПеревозку В
		                |				(ВЫБРАТЬ
		                |					упЗаданиеНаПеревозкуГруза.Ссылка
		                |				ИЗ
		                |					Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		                |				ГДЕ
		                |					упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза)) КАК упРасходыОбороты";
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", сткДанныеПоОбъектуРасчета.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			текФактическиеРасходыПоЗаявке = Выборка.СуммаФактСНДСОборот;
		КонецЕсли;
	КонецЕсли;
	
	Если НужнаПлановаяСуммаИнкассации Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(упИнкассацияДенежныхСредствОбороты.СуммаПланОборот) КАК СуммаПланОборот
		               |ИЗ
		               |	РегистрНакопления.упИнкассацияДенежныхСредств.Обороты(, , , ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза) КАК упИнкассацияДенежныхСредствОбороты";
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", сткДанныеПоОбъектуРасчета.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			текПлановаяСуммаИнкассации = Выборка.СуммаПланОборот;
		КонецЕсли;			   
		
	КонецЕсли;

	Если НуженТипТС Тогда
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	втТипыТС.Ссылка КАК ТипТС
		               |ИЗ
		               |	Справочник.упТипыТС КАК втТипыТС
		               |ГДЕ
		               |	втТипыТС.Грузоподъемность >= &МассаГруза
		               |	И втТипыТС.Объем >= &ОбъемГруза
		               |	И втТипыТС.КоличествоМест >= &КоличествоМестГруза
		               |	И втТипыТС.КоличествоЗагрузочныхМетров >= &КоличествоЗагрузочныхМетровГруза
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	втТипыТС.Грузоподъемность,
		               |	втТипыТС.Объем,
		               |	втТипыТС.КоличествоМест,
		               |	втТипыТС.КоличествоЗагрузочныхМетров";
		
		Запрос.УстановитьПараметр("МассаГруза", текМассаГруза);
		Запрос.УстановитьПараметр("ОбъемГруза", текОбъем);
		Запрос.УстановитьПараметр("КоличествоМестГруза", текКоличествоМест);
		Запрос.УстановитьПараметр("КоличествоЗагрузочныхМетровГруза", текКоличествоЗагрузочныхМетров);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			текТипТС = Выборка.ТипТС;	
		КонецЕсли;
		
	КонецЕсли;

	СтрокиСтатья = дрвПараметрыПоПравиламРасчета.Строки;
	
	Для каждого СтрокаСтатья Из СтрокиСтатья Цикл
		
		Если СтрокаСтатья.РасчетПроводитсяПоКаждойСтроке Тогда
			Продолжить;
		КонецЕсли;
				
		// Заполняются характеристики ТС.
		// ****************************************************************************************************************
		// Детализация по правилам(статьям доходов) только из-за этих характеристик, потому что если в правиле используется,
		// две характеристики ТС, то ТС по этому правилу должно удовлетоврять обеим характеристикам.
		// В то же время для другого правила могут быть свои характеристики ТС.
		// Например: в Правиле1 есть параметры учета ГрузоподъемностьТС и ОбъемТС, а в Правиле2 только ГрузоподъемностьТС,
		// тогда ТС для Правила1 будет удовлетворять двум характеристикам, а ТС для Правила2 только одной.
		// ****************************************************************************************************************
		
		СтрокиПараметры = СтрокаСтатья.Строки;
		мсвНовыеПараметрыРасчета = Новый Массив;
		Для каждого СтрокаПараметр Из СтрокиПараметры Цикл
			мсвНовыеПараметрыРасчета.Добавить(СтрокаПараметр.ПараметрРасчета);
		КонецЦикла;
		
		НужныХарактеристикиТСПоПравилу = НужныХарактеристикиТС(мсвНовыеПараметрыРасчета);
		НуженОбъемныйВес = НуженОбъемныйВес(мсвНовыеПараметрыРасчета);
		
		Если НужныХарактеристикиТСПоПравилу Тогда
			
			СхемаЗапроса = Новый СхемаЗапроса;
			ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
			|	ВЫБОР
			|		КОГДА упТранспортныеСредства.Грузоподъемность = 0
			|			ТОГДА упТранспортныеСредства.ТипТС.Грузоподъемность
			|		ИНАЧЕ упТранспортныеСредства.Грузоподъемность
			|	КОНЕЦ КАК ГрузоподъемностьТС,
			|	ВЫБОР
			|		КОГДА упТранспортныеСредства.Объем = 0
			|			ТОГДА упТранспортныеСредства.ТипТС.Объем
			|		ИНАЧЕ упТранспортныеСредства.Объем
			|	КОНЕЦ КАК ОбъемТС,
			|	ВЫБОР
			|		КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров = 0
			|			ТОГДА упТранспортныеСредства.ТипТС.КоличествоЗагрузочныхМетров
			|		ИНАЧЕ упТранспортныеСредства.КоличествоЗагрузочныхМетров
			|	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
			|	ВЫБОР
			|		КОГДА упТранспортныеСредства.КоличествоМест = 0
			|			ТОГДА упТранспортныеСредства.ТипТС.КоличествоМест
			|		ИНАЧЕ упТранспортныеСредства.КоличествоМест
			|	КОНЕЦ КАК КоличествоМестТС
			|ИЗ
			|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
			|ГДЕ
			|	НЕ упТранспортныеСредства.ПометкаУдаления
			|	И (НЕ &УчитыватьГрузоподъемностьТС
			|			ИЛИ ВЫБОР
			|				КОГДА упТранспортныеСредства.Грузоподъемность = 0
			|					ТОГДА упТранспортныеСредства.ТипТС.Грузоподъемность
			|				ИНАЧЕ упТранспортныеСредства.Грузоподъемность
			|			КОНЕЦ >= &МассаГруза)
			|	И (НЕ &УчитыватьОбъемТС
			|			ИЛИ ВЫБОР
			|				КОГДА упТранспортныеСредства.Объем = 0
			|					ТОГДА упТранспортныеСредства.ТипТС.Объем
			|				ИНАЧЕ упТранспортныеСредства.Объем
			|			КОНЕЦ >= &ОбъемГруза)
			|	И (НЕ &УчитыватьКоличествоМестТС
			|			ИЛИ ВЫБОР
			|				КОГДА упТранспортныеСредства.КоличествоМест = 0
			|					ТОГДА упТранспортныеСредства.ТипТС.КоличествоМест
			|				ИНАЧЕ упТранспортныеСредства.КоличествоМест
			|			КОНЕЦ >= &КоличествоМестГруза)
			|	И (НЕ &УчитыватьКоличествоЗагрузочныхМетровТС
			|			ИЛИ ВЫБОР
			|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров = 0
			|					ТОГДА упТранспортныеСредства.ТипТС.КоличествоЗагрузочныхМетров
			|				ИНАЧЕ упТранспортныеСредства.КоличествоЗагрузочныхМетров
			|			КОНЕЦ >= &КоличествоЗагрузочныхМетровГруза)";
			СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
			
			УчитыватьОбъемныйВес = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "ОбъемныйВес");
			УчитыватьГрузоподъемностьТС = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "ГрузоподъемностьТС") ИЛИ УчитыватьОбъемныйВес;
			УчитыватьОбъемТС = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "ОбъемТС") ИЛИ УчитыватьОбъемныйВес;
			УчитыватьКоличествоМестТС = ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "КоличествоМестТС") ИЛИ УчитыватьОбъемныйВес;
			УчитыватьКоличествоЗагрузочныхМетровТС	= ЕстьРасчетПоПараметру(мсвНовыеПараметрыРасчета, "КоличествоЗагрузочныхМетровТС") ИЛИ УчитыватьОбъемныйВес;
			
			ПакетЗапросовСхемы = СхемаЗапроса.ПакетЗапросов;
			
			ЗапросНаХарактеристикиТС = ПакетЗапросовСхемы[0];
			
			ВыраженияПорядкаСхемыЗапроса = ЗапросНаХарактеристикиТС.Порядок;
			
			ПакетЗапросовСхемы = СхемаЗапроса.ПакетЗапросов;
			ЗапросНаХарактеристикиТС = ПакетЗапросовСхемы[0];
			ВыраженияПорядкаСхемыЗапроса = ЗапросНаХарактеристикиТС.Порядок;
			
			Если УчитыватьГрузоподъемностьТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("ГрузоподъемностьТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Если УчитыватьОбъемТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("ОбъемТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Если УчитыватьКоличествоМестТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("КоличествоМестТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Если УчитыватьКоличествоЗагрузочныхМетровТС Тогда
				ВыражениеПорядкаСхемыЗапроса = ВыраженияПорядкаСхемыЗапроса.Добавить("КоличествоЗагрузочныхМетровТС");
				ВыражениеПорядкаСхемыЗапроса.Направление = НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию;
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
			Запрос.УстановитьПараметр("УчитыватьГрузоподъемностьТС", УчитыватьГрузоподъемностьТС);  
			Запрос.УстановитьПараметр("УчитыватьОбъемТС", УчитыватьОбъемТС);  
			Запрос.УстановитьПараметр("УчитыватьКоличествоМестТС", УчитыватьКоличествоМестТС);  
			Запрос.УстановитьПараметр("УчитыватьКоличествоЗагрузочныхМетровТС", УчитыватьКоличествоЗагрузочныхМетровТС);  
			Запрос.УстановитьПараметр("МассаГруза", текМассаГруза);
			Запрос.УстановитьПараметр("ОбъемГруза", текОбъем);
			Запрос.УстановитьПараметр("КоличествоМестГруза", текКоличествоМест);
			Запрос.УстановитьПараметр("КоличествоЗагрузочныхМетровГруза", текКоличествоЗагрузочныхМетров);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, Выборка);
			КонецЕсли;
		КонецЕсли;
		
		Если НуженТипГруза
			ИЛИ НуженТемпературныйРежим Тогда
			
			Запрос.Текст = "ВЫБРАТЬ
			|	1 КАК Количество,
			|	тбГрузовыеМеста.ГрузовоеМесто.ТипГруза КАК ТипГруза,
			|	тбГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим КАК ТемпературныйРежим,
			|	ВЫБОР
			|		КОГДА тбГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
			|			ТОГДА 1
			|		КОГДА тбГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
			|			ТОГДА 2
			|		КОГДА тбГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
			|			ТОГДА 3
			|		ИНАЧЕ 4
			|	КОНЕЦ КАК ПриоритетТипаГруза
			|ПОМЕСТИТЬ втПараметрыГрузовыхМест
			|ИЗ
			|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузовыеМеста
			|ГДЕ
			|	тбГрузовыеМеста.Ссылка = &ЗаявкаНаПеревозкуГруза
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	втПараметрыГрузовыхМест.ТипГруза КАК ТипГруза,
			|	СУММА(втПараметрыГрузовыхМест.Количество) КАК Количество,
			|	МАКСИМУМ(втПараметрыГрузовыхМест.ПриоритетТипаГруза) КАК ПриоритетТипаГруза
			|ИЗ
			|	втПараметрыГрузовыхМест КАК втПараметрыГрузовыхМест
			|ГДЕ
			|	НЕ втПараметрыГрузовыхМест.ТипГруза = ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	втПараметрыГрузовыхМест.ТипГруза
			|
			|УПОРЯДОЧИТЬ ПО
			|	Количество УБЫВ,
			|	ПриоритетТипаГруза
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	втПараметрыГрузовыхМест.ТемпературныйРежим КАК ТемпературныйРежим,
			|	СУММА(втПараметрыГрузовыхМест.Количество) КАК Количество
			|ИЗ
			|	втПараметрыГрузовыхМест КАК втПараметрыГрузовыхМест
			|ГДЕ
			|	НЕ втПараметрыГрузовыхМест.ТемпературныйРежим = ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	втПараметрыГрузовыхМест.ТемпературныйРежим
			|
			|УПОРЯДОЧИТЬ ПО
			|	Количество УБЫВ";
			
			
			Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", сткДанныеПоОбъектуРасчета.Ссылка);
			мсвРезультат = Запрос.ВыполнитьПакет();					   
			
			ВыборкаТипыГрузов = мсвРезультат[1].Выбрать();
			Если ВыборкаТипыГрузов.Следующий() Тогда
				текТипГруза = ВыборкаТипыГрузов.ТипГруза;	
			КонецЕсли;
			
			ВыборкаТемпературныйРежим = мсвРезультат[2].Выбрать();
			Если ВыборкаТемпературныйРежим.Следующий() Тогда
				текТемпературныйРежим = ВыборкаТемпературныйРежим.ТемпературныйРежим;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.СтатьяДоходов = СтрокаСтатья.СтатьяДоходовРасходов;
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		НоваяСтрока.СтруктураДанные.Вставить("МассаГруза", текМассаГруза);	
		НоваяСтрока.СтруктураДанные.Вставить("ОбъемГруза", текОбъем);	
		НоваяСтрока.СтруктураДанные.Вставить("ЗонаПолучения", текЗонаПолучения);	
		НоваяСтрока.СтруктураДанные.Вставить("ТипТС", текТипТС);	
		НоваяСтрока.СтруктураДанные.Вставить("КоличествоМестГруза", текКоличествоМест);	
		НоваяСтрока.СтруктураДанные.Вставить("КоличествоЗагрузочныхМетровГруза", текКоличествоЗагрузочныхМетров);	
		НоваяСтрока.СтруктураДанные.Вставить("ОценочнаяСтоимостьГруза", текОценочнаяСтоимость);	
		НоваяСтрока.СтруктураДанные.Вставить("ФактическиеРасходыПоЗаявке", текФактическиеРасходыПоЗаявке);	
		НоваяСтрока.СтруктураДанные.Вставить("ПлановаяСуммаИнкассации", текПлановаяСуммаИнкассации);	
		НоваяСтрока.СтруктураДанные.Вставить("ТипГруза", текТипГруза);	
		НоваяСтрока.СтруктураДанные.Вставить("ТемпературныйРежим", текТемпературныйРежим);	
		
		Если НужныХарактеристикиТСПоПравилу Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, сткХарактеристикиТС,,"ТипТС");
			Если НуженОбъемныйВес Тогда
				ЗаполнитьОбъемныйВес(НоваяСтрока.СтруктураДанные);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

	Возврат тбзРезультат;	
		
КонецФункции

Процедура ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами)
	
	Если тбзРасстоянияМеждуАдресами = Неопределено Тогда
		тбзРасстоянияМеждуАдресами = Новый ТаблицаЗначений;
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресОтправления", 		Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресПолучения", 			Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("Расстояние", 				Новый ОписаниеТипов("Число"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("ВремяРаботыЧас", 			Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АдресОтправления) И ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		
		мсвАдреса	  = тбзРасстоянияМеждуАдресами.НайтиСтроки(Новый Структура("АдресОтправления, АдресПолучения",Объект.АдресОтправления, Объект.АдресПолучения));
		Если мсвАдреса.Количество() > 0 Тогда
			сткДанныеПоОбъектуРасчета.Вставить("Расстояние", 		мсвАдреса[0].Расстояние);
			сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", 	мсвАдреса[0].ВремяРаботыЧас);
		Иначе	
			ВремяПрохожденияТочкиОтправления 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресОтправления, "ВидАдреса.ВремяПрохожденияТочки");
			ВремяПрохожденияТочкиПолучения 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресПолучения, "ВидАдреса.ВремяПрохожденияТочки");
			сткРасстояния = ПолучитьРасстояниеИВремя(Объект.АдресОтправления, Объект.АдресПолучения, ВремяПрохожденияТочкиОтправления + ВремяПрохожденияТочкиПолучения);
			сткДанныеПоОбъектуРасчета.Вставить("Расстояние", 		сткРасстояния.Расстояние);
			сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", 	сткРасстояния.Время);
			
			НоваяСтрока = тбзРасстоянияМеждуАдресами.Добавить();
			НоваяСтрока.АдресОтправления 	= Объект.АдресОтправления;
			НоваяСтрока.АдресПолучения 		= Объект.АдресПолучения;
			НоваяСтрока.Расстояние 			= сткРасстояния.Расстояние;
			НоваяСтрока.ВремяРаботыЧас 		= сткРасстояния.Время;
		КонецЕсли;
			
	ИначеЕсли ЗначениеЗаполнено(Объект.ЗонаОтправления) И ЗначениеЗаполнено(Объект.ЗонаПолучения) Тогда	
		сткРасстояния = ПолучитьРасстояниеИВремяМеждуГеографическимиЗонами(Объект.ЗонаОтправления, Объект.ЗонаПолучения);	
		сткДанныеПоОбъектуРасчета.Вставить("Расстояние", 		сткРасстояния.Расстояние);
		сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", 	сткРасстояния.Время);
	Иначе
		сткДанныеПоОбъектуРасчета.Вставить("Расстояние", 		0);
		сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", 	0);
	КонецЕсли;
	
	сткДанныеПоОбъектуРасчета.Вставить("РасстоянияРассчитаны", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьРасстояниеИВремяПоГрузовомуМестуЗаявкиНаПеревозкуГруза(Строка, Объект, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами)
	
	Если тбзРасстоянияМеждуАдресами = Неопределено Тогда
		тбзРасстоянияМеждуАдресами = Новый ТаблицаЗначений;
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресОтправления", 		Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресПолучения", 			Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("Расстояние", 				Новый ОписаниеТипов("Число"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("ВремяРаботыЧас", 			Новый ОписаниеТипов("Число"));
		сткДанныеПоОбъектуРасчета.Вставить("РасстоянияРассчитаны", Ложь);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Строка.АдресОтправления) И НЕ ЗначениеЗаполнено(Строка.АдресПолучения) Тогда
		Если НЕ сткДанныеПоОбъектуРасчета.РасстоянияРассчитаны Тогда
			ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза(Объект, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами);
		КонецЕсли;
		Строка.Расстояние 		= сткДанныеПоОбъектуРасчета.Расстояние;
		Строка.ВремяРаботыЧас 	= сткДанныеПоОбъектуРасчета.ВремяРаботыЧас;
	Иначе
		текАдресОтправления = Строка.АдресОтправления;
		Если НЕ ЗначениеЗаполнено(текАдресОтправления) Тогда
			текАдресОтправления = Объект.АдресОтправления;	
		КонецЕсли;
		
		текАдресПолучения = Строка.АдресПолучения;
		Если НЕ ЗначениеЗаполнено(текАдресПолучения) Тогда
			текАдресПолучения = Объект.АдресПолучения;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текАдресОтправления) И ЗначениеЗаполнено(текАдресПолучения) Тогда
			
			мсвАдреса	  = тбзРасстоянияМеждуАдресами.НайтиСтроки(Новый Структура("АдресОтправления, АдресПолучения",текАдресОтправления, текАдресПолучения));
			Если мсвАдреса.Количество() > 0 Тогда
				Строка.Расстояние 		= мсвАдреса[0].Расстояние;
				Строка.ВремяРаботыЧас 	= мсвАдреса[0].ВремяРаботыЧас;
			Иначе	
				ВремяПрохожденияТочкиОтправления 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текАдресОтправления, "ВидАдреса.ВремяПрохожденияТочки");
				ВремяПрохожденияТочкиПолучения 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текАдресПолучения, "ВидАдреса.ВремяПрохожденияТочки");
				сткРасстояния = ПолучитьРасстояниеИВремя(текАдресОтправления, текАдресПолучения, ВремяПрохожденияТочкиОтправления + ВремяПрохожденияТочкиПолучения);
				
				Строка.Расстояние 		= сткРасстояния.Расстояние;
				Строка.ВремяРаботыЧас 	= сткРасстояния.Время;
				
				НоваяСтрока = тбзРасстоянияМеждуАдресами.Добавить();
				НоваяСтрока.АдресОтправления 	= текАдресОтправления;
				НоваяСтрока.АдресПолучения 		= текАдресПолучения;
				НоваяСтрока.Расстояние 			= сткРасстояния.Расстояние;
				НоваяСтрока.ВремяРаботыЧас 		= сткРасстояния.Время;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти	

#Область РасчетПоФактическимДоходам

Процедура ПодготовитьВходныеПараметры_ФактическиеДоходы(Объект, ВходныеПараметры, сткТрассировка)
			
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяДоходов");		
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "Доходы");

	ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));
	
	ГруппаТарифов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГруппаТарифов");				  
	
	ВходныеПараметры.Вставить("ГруппаТарифов", ГруппаТарифов);
	ВходныеПараметры.Вставить("Организация", Объект.Организация);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", Объект.Ссылка);
	ВходныеПараметры.Вставить("Рейс", Неопределено);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаДоходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаДоходыНДС");

	ВходныеПараметры.Вставить("ЗонаОтправления", Справочники.упГеографическиеЗоны.ПустаяСсылка());
	ВходныеПараметры.Вставить("ЗонаПолучения", Справочники.упГеографическиеЗоны.ПустаяСсылка());
	
	ВходныеПараметры.Вставить("ТипТС", Справочники.упТипыТС.ПустаяСсылка());		
	ВходныеПараметры.Вставить("ГрузоподъемностьТС", 0);		
	ВходныеПараметры.Вставить("ОбъемТС", 0);		
	ВходныеПараметры.Вставить("КоличествоМестТС", 0);		
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровТС", 0);		
	
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	
	ВходныеПараметры.Вставить("ВремяРаботыЧас", 0);
	ВходныеПараметры.Вставить("Расстояние", 0);
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", '00010101');
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", 0);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());

	
	ВходныеПараметры.Вставить("ВидПеревозки", Справочники.упВидыПеревозок.ПустаяСсылка());
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
		
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета_ФактическиеДоходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	тбзЗаявки = Новый ТаблицаЗначений;
	тбзЗаявки.Колонки.Добавить("ЗаявкаНаПеревозкуГруза", Новый ОписаниеТипов("ДокументСсылка.упЗаявкаНаПеревозкуГруза"));
	
	сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоСтроке", "ЗаявкаНаПеревозкуГруза");
	сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоДокументу", "ЗаявкаНаПеревозкуГруза");
	сткДанныеПоОбъектуРасчета.Вставить("СпособВводаФактическихДоходов", Объект.СпособВводаФактическихДоходов);
	
	Если Объект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоОднойЗаявке Тогда
		
		НоваяСтрока = тбзЗаявки.Добавить();
		НоваяСтрока.ЗаявкаНаПеревозкуГруза = Объект.ЗаявкаНаПеревозкуГруза;
		
	ИначеЕсли Объект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда

		тбзЗаявки = Объект.Доходы.Выгрузить(,"ЗаявкаНаПеревозкугруза");
		
	КонецЕсли;	
	
	// Формируются общие таблицы для получения значений параметров.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса_ФактическиеДоходы_ОбщиеДанные();
	Запрос.УстановитьПараметр("тбзЗаявки", тбзЗаявки);			   
	мсвРезультаты = Запрос.ВыполнитьПакет();
	сткДанныеПоОбъектуРасчета.Вставить("Запрос", Запрос);
				
КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке_ФактическиеДоходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("ЗаявкаНаПеревозкуГруза");
	тбзРезультат.Колонки.Добавить("СтруктураДанные");

	НужныПараметрыГруза = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныРасстояния = НужныРасстояния(мсвПараметрыРасчета);
	НужныПараметрыЗоны = НужныЗоны(мсвПараметрыРасчета);
	
	Запрос = сткДанныеПоОбъектуРасчета.Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	втГрузовыеМеста.КоличествоГрузов,
	               |	втГрузовыеМеста.МассаГруза,
	               |	втГрузовыеМеста.ОбъемГруза,
	               |	втГрузовыеМеста.КоличествоМестГруза,
	               |	втГрузовыеМеста.КоличествоЗагрузочныхМетровГруза,
	               |	втГрузовыеМеста.ОценочнаяСтоимостьГруза,
	               |	втГрузовыеМеста.ГрузоподъемностьТС,
	               |	втГрузовыеМеста.ОбъемТС,
	               |	втГрузовыеМеста.КоличествоЗагрузочныхМетровТС,
	               |	втГрузовыеМеста.КоличествоМестТС,
	               |	втГрузовыеМеста.ГрузовоеМесто,
	               |	втГрузовыеМеста.Рейс,
	               |	втГрузовыеМеста.ЗаявкаНаПеревозкуГруза,
	               |	втПоследниеТипыТСПоОбъектам.ТипТС,
	               |	втГрузовыеМеста.ВремяПростояПоВинеЗаказчикаЧасы,
	               |	втГрузовыеМеста.ВремяПростояПоВинеПеревозчикаЧасы,
	               |	тбзЗаявки.ЗонаОтправления,
	               |	тбзЗаявки.ЗонаПолучения,
	               |	тбзЗаявки.ОтправлениеГрузаС КАК ВремяОтправленияСПлановое,
	               |	тбзЗаявки.ОтправлениеГрузаПо КАК ВремяОтправленияПоПлановое,
	               |	тбзЗаявки.ПолучениеГрузаС КАК ВремяПолученияСПлановое,
	               |	тбзЗаявки.ПолучениеГрузаПо КАК ВремяПолученияПоПлановое,
	               |	тбзЗаявки.АдресОтправления,
	               |	тбзЗаявки.АдресПолучения,
	               |	тбзЗаявки.ВремяПрохожденияТочек,
	               |	тбзЗаявки.ВидПеревозки,
	               |	тбзГрузовыеМестаАдреса.АдресОтправления КАК АдресОтправленияГруза,
	               |	тбзГрузовыеМестаАдреса.АдресПолучения КАК АдресПолученияГруза,
	               |	ЕСТЬNULL(упВидыАдресовОтправления.ВремяПрохожденияТочки, 0) + ЕСТЬNULL(упВидыАдресовПолучения.ВремяПрохожденияТочки, 0) КАК ВремяПрохожденияТочекГруза,
	               |	втГрузовыеМеста.ТипГруза,
	               |	втГрузовыеМеста.ТемпературныйРежим
	               |ИЗ
	               |	втГрузовыеМеста КАК втГрузовыеМеста
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследниеТипыТСПоОбъектам КАК втПоследниеТипыТСПоОбъектам
	               |		ПО втГрузовыеМеста.ГрузовоеМесто = втПоследниеТипыТСПоОбъектам.ГрузовоеМесто
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРеквизитыЗаявок КАК тбзЗаявки
	               |		ПО втГрузовыеМеста.ЗаявкаНаПеревозкуГруза = тбзЗаявки.ЗаявкаНаПеревозкуГруза
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаАдреса КАК тбзГрузовыеМестаАдреса
	               |		ПО втГрузовыеМеста.ГрузовоеМесто = тбзГрузовыеМестаАдреса.ГрузовоеМесто
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресаОтправления
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыАдресов КАК упВидыАдресовОтправления
	               |			ПО упАдресаОтправления.ВидАдреса = упВидыАдресовОтправления.Ссылка
	               |		ПО (тбзГрузовыеМестаАдреса.АдресОтправления = упАдресаОтправления.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресаПолучения
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыАдресов КАК упВидыАдресовПолучения
	               |			ПО упАдресаПолучения.ВидАдреса = упВидыАдресовПолучения.Ссылка
	               |		ПО (тбзГрузовыеМестаАдреса.АдресПолучения = упАдресаПолучения.Ссылка)";
				   
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	тбзРасстояниеМеждуАдресами = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		текРейс 		 = Выборка.Рейс;
		текГрузовоеМесто = Выборка.ГрузовоеМесто;
		
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.ЗаявкаНаПеревозкуГруза = Выборка.ЗаявкаНаПеревозкуГруза;
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		НоваяСтрока.СтруктураДанные.Вставить("ОбъектРасчета", Новый Структура("Имя, Значение","Грузовое место",Выборка.ГрузовоеМесто));  
		НоваяСтрока.СтруктураДанные.Вставить("Рейс", текРейс);  
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, Выборка);
		
		Если НужныРасстояния Тогда
			ЗаполнитьРасстояниеИВремяПоГрузовомуМестуЗаявкиНаПеревозкуГруза_ФактическиеДоходы(Выборка, НоваяСтрока.СтруктураДанные, тбзРасстояниеМеждуАдресами);
		КонецЕсли;
		
		Если НуженОбъемныйВес Тогда
			ЗаполнитьОбъемныйВес(НоваяСтрока.СтруктураДанные);	
		КонецЕсли;
				
	КонецЦикла;
	
	Если НЕ сткДанныеПоОбъектуРасчета.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда
		Для каждого Строка Из тбзРезультат Цикл
			  Строка.ЗаявкаНаПеревозкуГруза = Документы.упЗаявкаНаПеревозкуГруза.ПустаяСсылка();
		КонецЦикла;
	КонецЕсли;	
	
	Возврат тбзРезультат;
	
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеДоходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("ЗаявкаНаПеревозкуГруза");
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	
	НужныПараметрыГруза = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныРасстояния = НужныРасстояния(мсвПараметрыРасчета);
	НужныФактическиеРасходы = НужныФактическиеРасходы(мсвПараметрыРасчета);
	НужнаПлановаяСуммаИнкассации = НужнаПлановаяСуммаИнкассации(мсвПараметрыРасчета);
	НужныРейсыПоЗаявке = НуженРейс(мсвПараметрыРасчета);

	Запрос = сткДанныеПоОбъектуРасчета.Запрос;
	Запрос.УстановитьПараметр("НужныФактическиеРасходы", НужныФактическиеРасходы);
	Запрос.УстановитьПараметр("НужнаПлановаяСуммаИнкассации", НужнаПлановаяСуммаИнкассации);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(упРасходыОбороты.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот,
	               |	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза
	               |ИЗ
	               |	РегистрНакопления.упРасходы.Обороты(
	               |			,
	               |			,
	               |			,
	               |			ЗаданиеНаПеревозку В
	               |				(ВЫБРАТЬ
	               |					тбЗадания.Задание
	               |				ИЗ
	               |					втЗаданияНаПеревозкуГруза КАК тбЗадания
	               |				ГДЕ
	               |					&НужныФактическиеРасходы)) КАК упРасходыОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	               |		ПО упРасходыОбороты.ЗаданиеНаПеревозку = упЗаданиеНаПеревозкуГруза.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(упИнкассацияДенежныхСредствОбороты.СуммаПланОборот) КАК СуммаПланОборот,
	               |	упИнкассацияДенежныхСредствОбороты.ЗаявкаНаПеревозкуГруза
	               |ИЗ
	               |	РегистрНакопления.упИнкассацияДенежныхСредств.Обороты(
	               |			,
	               |			,
	               |			,
	               |			ЗаявкаНаПеревозкуГруза В
	               |				(ВЫБРАТЬ
	               |					тбЗаявки.ЗаявкаНаПеревозкуГруза
	               |				ИЗ
	               |					втЗаявки КАК тбЗаявки
	               |				ГДЕ
	               |					&НужнаПлановаяСуммаИнкассации)) КАК упИнкассацияДенежныхСредствОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упИнкассацияДенежныхСредствОбороты.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	втТипыТС.ТипТС,
	               |	втТипыТС.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	               |ИЗ
	               |	втПоследниеТипыТСПоОбъектам КАК втТипыТС
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	втТипыТС.Убытие УБЫВ
	               |ИТОГИ ПО
	               |	ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(тбВремяПростояПоТочкам.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	               |	СУММА(тбВремяПростояПоТочкам.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	               |	тбВремяПростояПоТочкам.ЗаявкаНаПеревозкуГруза
	               |ПОМЕСТИТЬ втВремяПростояПоЗаявке
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		втМаршрутыПоРейсам.Идентификатор КАК Идентификатор,
	               |		МАКСИМУМ(втМаршрутыПоРейсам.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	               |		МАКСИМУМ(втМаршрутыПоРейсам.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	               |		втМаршрутыПоРейсам.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	               |	ИЗ
	               |		втМаршрутыПоРейсам КАК втМаршрутыПоРейсам
	               |	ГДЕ
	               |		НЕ втМаршрутыПоРейсам.Задание ЕСТЬ NULL
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		втМаршрутыПоРейсам.Идентификатор,
	               |		втМаршрутыПоРейсам.ЗаявкаНаПеревозкуГруза) КАК тбВремяПростояПоТочкам
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбВремяПростояПоТочкам.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(втТСпоРейсу.ГрузоподъемностьТС) / СУММА(втТСпоРейсу.КоличествоТС) КАК ГрузоподъемностьТС,
	               |	СУММА(втТСпоРейсу.ОбъемТС) / СУММА(втТСпоРейсу.КоличествоТС) КАК ОбъемТС,
	               |	СУММА(втТСпоРейсу.КоличествоЗагрузочныхМетровТС) / СУММА(втТСпоРейсу.КоличествоТС) КАК КоличествоЗагрузочныхМетровТС,
	               |	СУММА(втТСпоРейсу.КоличествоМестТС) / СУММА(втТСпоРейсу.КоличествоТС) КАК КоличествоМестТС,
	               |	втТСпоРейсу.ЗаявкаНаПеревозкуГруза
	               |ПОМЕСТИТЬ втХарактеристикиТС
	               |ИЗ
	               |	втТСпоРейсу КАК втТСпоРейсу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТСпоРейсу.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	1 КАК Количество,
	               |	втГрузовыеМеста.ТипГруза КАК ТипГруза,
	               |	втГрузовыеМеста.ТемпературныйРежим КАК ТемпературныйРежим,
	               |	втГрузовыеМеста.ПриоритетТипаГруза КАК ПриоритетТипаГруза,
	               |	втГрузовыеМеста.ЗаявкаНаПеревозкуГруза
	               |ПОМЕСТИТЬ втТипыГрузовТемпературныеРежимыПоЗаявкам
	               |ИЗ
	               |	втГрузовыеМеста КАК втГрузовыеМеста
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(втТипыГрузаПоЗаявкам.Количество) КАК Количество,
	               |	втТипыГрузаПоЗаявкам.ТипГруза,
	               |	втТипыГрузаПоЗаявкам.ПриоритетТипаГруза,
	               |	втТипыГрузаПоЗаявкам.ЗаявкаНаПеревозкуГруза
	               |ПОМЕСТИТЬ втКоличествоТиповГрузов
	               |ИЗ
	               |	втТипыГрузовТемпературныеРежимыПоЗаявкам КАК втТипыГрузаПоЗаявкам
	               |ГДЕ
	               |	НЕ втТипыГрузаПоЗаявкам.ТипГруза = ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТипыГрузаПоЗаявкам.ТипГруза,
	               |	втТипыГрузаПоЗаявкам.ПриоритетТипаГруза,
	               |	втТипыГрузаПоЗаявкам.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТипыГрузаПоЗаявкам.ТемпературныйРежим,
	               |	СУММА(втТипыГрузаПоЗаявкам.Количество) КАК Количество,
	               |	втТипыГрузаПоЗаявкам.ЗаявкаНаПеревозкуГруза
	               |ПОМЕСТИТЬ втКоличествоТемпературныхРежимов
	               |ИЗ
	               |	втТипыГрузовТемпературныеРежимыПоЗаявкам КАК втТипыГрузаПоЗаявкам
	               |ГДЕ
	               |	НЕ втТипыГрузаПоЗаявкам.ТемпературныйРежим = ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТипыГрузаПоЗаявкам.ТемпературныйРежим,
	               |	втТипыГрузаПоЗаявкам.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза,
	               |	МИНИМУМ(втКоличествоТиповГрузов.ПриоритетТипаГруза) КАК ПриоритетТипаГруза,
	               |	МАКСИМУМ(втКоличествоТиповГрузов.Количество) КАК Количество
	               |ПОМЕСТИТЬ втПараметрыТипаГрузаПоЗаявке
	               |ИЗ
	               |	втКоличествоТиповГрузов КАК втКоличествоТиповГрузов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			МАКСИМУМ(втКоличествоТиповГрузов.Количество) КАК Количество,
	               |			втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	               |		ИЗ
	               |			втКоличествоТиповГрузов КАК втКоличествоТиповГрузов
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза) КАК ВложенныйЗапрос
	               |		ПО втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза = ВложенныйЗапрос.ЗаявкаНаПеревозкуГруза
	               |			И втКоличествоТиповГрузов.Количество = ВложенныйЗапрос.Количество
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза,
	               |	втКоличествоТиповГрузов.ТипГруза
	               |ПОМЕСТИТЬ втТипыГрузовПоЗаявкам
	               |ИЗ
	               |	втКоличествоТиповГрузов КАК втКоличествоТиповГрузов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыТипаГрузаПоЗаявке КАК втПараметрыТипаГрузаПоЗаявке
	               |		ПО втКоличествоТиповГрузов.ЗаявкаНаПеревозкуГруза = втПараметрыТипаГрузаПоЗаявке.ЗаявкаНаПеревозкуГруза
	               |			И втКоличествоТиповГрузов.ПриоритетТипаГруза = втПараметрыТипаГрузаПоЗаявке.ПриоритетТипаГруза
	               |			И втКоличествоТиповГрузов.Количество = втПараметрыТипаГрузаПоЗаявке.Количество
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втКоличествоТемпературныхРежимов.ЗаявкаНаПеревозкуГруза,
	               |	МАКСИМУМ(втКоличествоТемпературныхРежимов.ТемпературныйРежим) КАК ТемпературныйРежим
	               |ПОМЕСТИТЬ втТемпературныеРежимыПоЗаявкам
	               |ИЗ
	               |	втКоличествоТемпературныхРежимов КАК втКоличествоТемпературныхРежимов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			втКоличествоТемпературныхРежимов.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	               |			МАКСИМУМ(втКоличествоТемпературныхРежимов.Количество) КАК Количество
	               |		ИЗ
	               |			втКоличествоТемпературныхРежимов КАК втКоличествоТемпературныхРежимов
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			втКоличествоТемпературныхРежимов.ЗаявкаНаПеревозкуГруза) КАК ВложенныйЗапрос
	               |		ПО втКоличествоТемпературныхРежимов.ЗаявкаНаПеревозкуГруза = ВложенныйЗапрос.ЗаявкаНаПеревозкуГруза
	               |			И втКоличествоТемпературныхРежимов.Количество = ВложенныйЗапрос.Количество
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втКоличествоТемпературныхРежимов.ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(тбХарактеристикиТС.КоличествоГрузов) КАК КоличествоГрузов,
	               |	СУММА(тбХарактеристикиТС.МассаГруза) КАК МассаГруза,
	               |	СУММА(тбХарактеристикиТС.ОбъемГруза) КАК ОбъемГруза,
	               |	СУММА(тбХарактеристикиТС.КоличествоМестГруза) КАК КоличествоМестГруза,
	               |	СУММА(тбХарактеристикиТС.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	               |	СУММА(тбХарактеристикиТС.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	               |	СУММА(тбХарактеристикиТС.ГрузоподъемностьТС) КАК ГрузоподъемностьТС,
	               |	СУММА(тбХарактеристикиТС.ОбъемТС) КАК ОбъемТС,
	               |	СУММА(тбХарактеристикиТС.КоличествоЗагрузочныхМетровТС) КАК КоличествоЗагрузочныхМетровТС,
	               |	СУММА(тбХарактеристикиТС.КоличествоМестТС) КАК КоличествоМестТС,
	               |	СУММА(тбХарактеристикиТС.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	               |	СУММА(тбХарактеристикиТС.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	               |	МИНИМУМ(тбХарактеристикиТС.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
	               |	МИНИМУМ(тбХарактеристикиТС.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
	               |	МИНИМУМ(тбХарактеристикиТС.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
	               |	МИНИМУМ(тбХарактеристикиТС.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
	               |	МАКСИМУМ(тбХарактеристикиТС.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
	               |	МАКСИМУМ(тбХарактеристикиТС.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
	               |	МАКСИМУМ(тбХарактеристикиТС.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
	               |	МАКСИМУМ(тбХарактеристикиТС.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
	               |	МАКСИМУМ(тбХарактеристикиТС.ТипГруза) КАК ТипГруза,
	               |	МАКСИМУМ(тбХарактеристикиТС.ТемпературныйРежим) КАК ТемпературныйРежим,
	               |	тбХарактеристикиТС.ЗаявкаНаПеревозкуГруза
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		СУММА(втГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	               |		СУММА(втГрузовыеМеста.МассаГруза) КАК МассаГруза,
	               |		СУММА(втГрузовыеМеста.ОбъемГруза) КАК ОбъемГруза,
	               |		СУММА(втГрузовыеМеста.КоличествоМестГруза) КАК КоличествоМестГруза,
	               |		СУММА(втГрузовыеМеста.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	               |		СУММА(втГрузовыеМеста.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	               |		СУММА(0) КАК ГрузоподъемностьТС,
	               |		СУММА(0) КАК ОбъемТС,
	               |		СУММА(0) КАК КоличествоЗагрузочныхМетровТС,
	               |		СУММА(0) КАК КоличествоМестТС,
	               |		СУММА(0) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	               |		СУММА(0) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	               |		МИНИМУМ(втГрузовыеМеста.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
	               |		МИНИМУМ(втГрузовыеМеста.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
	               |		МИНИМУМ(втГрузовыеМеста.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
	               |		МИНИМУМ(втГрузовыеМеста.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
	               |		МАКСИМУМ(втГрузовыеМеста.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
	               |		МАКСИМУМ(втГрузовыеМеста.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
	               |		МАКСИМУМ(втГрузовыеМеста.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
	               |		МАКСИМУМ(втГрузовыеМеста.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
	               |		МАКСИМУМ(ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)) КАК ТипГруза,
	               |		МАКСИМУМ(ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)) КАК ТемпературныйРежим,
	               |		втГрузовыеМеста.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	               |	ИЗ
	               |		втГрузовыеМеста КАК втГрузовыеМеста
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		втГрузовыеМеста.ЗаявкаНаПеревозкуГруза
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		втХарактеристикиТС.ГрузоподъемностьТС,
	               |		втХарактеристикиТС.ОбъемТС,
	               |		втХарактеристикиТС.КоличествоЗагрузочныхМетровТС,
	               |		втХарактеристикиТС.КоличествоМестТС,
	               |		0,
	               |		0,
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка),
	               |		ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка),
	               |		втХарактеристикиТС.ЗаявкаНаПеревозкуГруза
	               |	ИЗ
	               |		втХарактеристикиТС КАК втХарактеристикиТС
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		втВремяПростояПоЗаявке.ВремяПростояПоВинеЗаказчикаЧасы,
	               |		втВремяПростояПоЗаявке.ВремяПростояПоВинеПеревозчикаЧасы,
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка),
	               |		ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка),
	               |		втВремяПростояПоЗаявке.ЗаявкаНаПеревозкуГруза
	               |	ИЗ
	               |		втВремяПростояПоЗаявке КАК втВремяПростояПоЗаявке
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		втТипыГрузовПоЗаявкам.ТипГруза,
	               |		ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка),
	               |		втТипыГрузовПоЗаявкам.ЗаявкаНаПеревозкуГруза
	               |	ИЗ
	               |		втТипыГрузовПоЗаявкам КАК втТипыГрузовПоЗаявкам
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(3999, 12, 31),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ДАТАВРЕМЯ(1, 1, 1),
	               |		ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка),
	               |		втТемпературныеРежимыПоЗаявкам.ТемпературныйРежим,
	               |		втТемпературныеРежимыПоЗаявкам.ЗаявкаНаПеревозкуГруза
	               |	ИЗ
	               |		втТемпературныеРежимыПоЗаявкам КАК втТемпературныеРежимыПоЗаявкам) КАК тбХарактеристикиТС
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбХарактеристикиТС.ЗаявкаНаПеревозкуГруза
				   |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втГрузовыеМеста.Рейс,
	               |	втГрузовыеМеста.ГрузовоеМесто,
	               |	втГрузовыеМеста.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	               |ИЗ
	               |	втГрузовыеМеста КАК втГрузовыеМеста
	               |ИТОГИ ПО
	               |	ЗаявкаНаПеревозкуГруза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбзЗаявки.ЗаявкаНаПеревозкуГруза,
	               |	тбзЗаявки.ЗонаОтправления,
	               |	тбзЗаявки.ЗонаПолучения,
	               |	тбзЗаявки.ОтправлениеГрузаС КАК ВремяОтправленияСПлановое,
	               |	тбзЗаявки.ОтправлениеГрузаПо КАК ВремяОтправленияПоПлановое,
	               |	тбзЗаявки.ПолучениеГрузаС КАК ВремяПолученияСПлановое,
	               |	тбзЗаявки.ПолучениеГрузаПо КАК ВремяПолученияПоПлановое,
	               |	тбзЗаявки.АдресОтправления,
	               |	тбзЗаявки.АдресПолучения,
	               |	тбзЗаявки.ВремяПрохожденияТочек,
	               |	тбзЗаявки.ВидПеревозки
	               |ИЗ
	               |	втРеквизитыЗаявок КАК тбзЗаявки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗаданияНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	               |	втЗаданияНаПеревозкуГруза.Рейс КАК Рейс
	               |ИЗ
	               |	втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
	               |ИТОГИ ПО
	               |	ЗаявкаНаПеревозкуГруза";
	мсвРезультатЗапросов = Запрос.ВыполнитьПакет();
	
	ИндексЗапросаФактическиеРасходы = 0;
	ИндексЗапросаИнкассацияДС = 1;
	ИндексЗапросаТипТС = 2;
	ИндексЗапросаХарактеристикиГруза = 11;
	ИндексЗапросаГрузовыеМестаПоЗаявкам = 12;
	ИндексЗапросаРеквизитыЗаявок = 13;
	ИндексЗапросаРейсыПоЗаявке = 14;

	// Реквизиты заявок.
	Выборка = мсвРезультатЗапросов[ИндексЗапросаРеквизитыЗаявок].Выбрать();
	тбзРасстояниеМеждуАдресами = Неопределено;

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		НоваяСтрока.ЗаявкаНаПеревозкуГруза	= Выборка.ЗаявкаНаПеревозкуГруза;	
		ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, Выборка);
		Если НужныРасстояния Тогда
			ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза_ФактическиеДоходы(Выборка, НоваяСтрока.СтруктураДанные, тбзРасстояниеМеждуАдресами); 
		КонецЕсли;	
	КонецЦикла;
		
	// Фактические расходы.
	Выборка = мсвРезультатЗапросов[ИндексЗапросаФактическиеРасходы].Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвСтрокиЗаявок = тбзРезультат.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", Выборка.ЗаявкаНаПеревозкуГруза));
		Если мсвСтрокиЗаявок.Количество() > 0 Тогда
			мсвСтрокиЗаявок[0].СтруктураДанные.Вставить("ФактическиеРасходыПоЗаявке",	Выборка.СуммаФактСНДСОборот);	
		КонецЕсли;
	КонецЦикла;
	
	// Суммы инкассации.
	Выборка = мсвРезультатЗапросов[ИндексЗапросаИнкассацияДС].Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвСтрокиЗаявок = тбзРезультат.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", Выборка.ЗаявкаНаПеревозкуГруза));
		Если мсвСтрокиЗаявок.Количество() > 0 Тогда
			мсвСтрокиЗаявок[0].СтруктураДанные.Вставить("ПлановаяСуммаИнкассации",	Выборка.СуммаПланОборот);	
		КонецЕсли;
	КонецЦикла;
	
	// Тип ТС.
	ВыборкаЗаявка = мсвРезультатЗапросов[ИндексЗапросаТипТС].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаявка.Следующий() Цикл
		мсвСтрокиЗаявок = тбзРезультат.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", ВыборкаЗаявка.ЗаявкаНаПеревозкуГруза));
		Если мсвСтрокиЗаявок.Количество() > 0 Тогда
			ВыборкаТС = ВыборкаЗаявка.Выбрать();
			Если ВыборкаТС.Следующий()Тогда
				СтруктураДанные = мсвСтрокиЗаявок[0].СтруктураДанные;
				ЗаполнитьЗначенияСвойств(СтруктураДанные, ВыборкаТС);		
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Рейсы.
	Если НужныРейсыПоЗаявке Тогда
		ВыборкаЗаявка = мсвРезультатЗапросов[ИндексЗапросаРейсыПоЗаявке].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗаявка.Следующий() Цикл
			мсвРейсы = Новый Массив;
			ВыборкаРейс = ВыборкаЗаявка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаРейс.Следующий() Цикл
				мсвРейсы.Добавить(ВыборкаРейс.Рейс);				
			КонецЦикла;
			мсвСтрокиЗаявок = тбзРезультат.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", ВыборкаЗаявка.ЗаявкаНаПеревозкуГруза));
			Если мсвСтрокиЗаявок.Количество() > 0 Тогда
				Для каждого СтрокаЗаявка Из мсвСтрокиЗаявок Цикл
					СтрокаЗаявка.СтруктураДанные.Вставить("Рейс", мсвРейсы);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Характеристики груза.
	ВыборкаЗаявка	= мсвРезультатЗапросов[ИндексЗапросаХарактеристикиГруза].Выбрать();
	Пока ВыборкаЗаявка.Следующий() Цикл
		
		мсвСтрокиЗаявок = тбзРезультат.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", ВыборкаЗаявка.ЗаявкаНаПеревозкуГруза));
		Если мсвСтрокиЗаявок.Количество() > 0 Тогда
			СтруктураДанные = мсвСтрокиЗаявок[0].СтруктураДанные;
			
			Если НЕ ВыборкаЗаявка.МассаГруза = null Тогда
				ЗаполнитьЗначенияСвойств(СтруктураДанные, ВыборкаЗаявка);
			КонецЕсли;  
			
			Если НуженОбъемныйВес Тогда
				ЗаполнитьОбъемныйВес(СтруктураДанные);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ сткДанныеПоОбъектуРасчета.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда
		Для каждого Строка Из тбзРезультат Цикл
			  Строка.ЗаявкаНаПеревозкуГруза = Документы.упЗаявкаНаПеревозкуГруза.ПустаяСсылка();
		КонецЦикла;
	КонецЕсли;	
	
	Возврат тбзРезультат;	
		
КонецФункции

Функция ТекстЗапроса_ФактическиеДоходы_ОбщиеДанные()
	
	текРезультат = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тбзЗаявки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗаявки
	|ИЗ
	|	&тбзЗаявки КАК тбзЗаявки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	упЗаявкаНаПеревозкуГруза.ЗонаОтправления КАК ЗонаОтправления,
	|	упЗаявкаНаПеревозкуГруза.ЗонаПолучения КАК ЗонаПолучения,
	|	упЗаявкаНаПеревозкуГруза.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	упЗаявкаНаПеревозкуГруза.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	упЗаявкаНаПеревозкуГруза.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	упЗаявкаНаПеревозкуГруза.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	упЗаявкаНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	упЗаявкаНаПеревозкуГруза.АдресПолучения КАК АдресПолучения,
	|	ЕСТЬNULL(упВидыАдресовАдресОтправления.ВремяПрохожденияТочки, 0) + ЕСТЬNULL(упВидыАдресовПолучения.ВремяПрохожденияТочки, 0) КАК ВремяПрохожденияТочек,
	|	упЗаявкаНаПеревозкуГруза.ВидПеревозки КАК ВидПеревозки
	|ПОМЕСТИТЬ втРеквизитыЗаявок
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявки КАК втЗаявки
	|	ПО упЗаявкаНаПеревозкуГруза.Ссылка = втЗаявки.ЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресОтправления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыАдресов КАК упВидыАдресовАдресОтправления
	|		ПО упАдресОтправления.ВидАдреса = упВидыАдресовАдресОтправления.Ссылка
	|	ПО упЗаявкаНаПеревозкуГруза.АдресОтправления = упАдресОтправления.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресаПолучения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыАдресов КАК упВидыАдресовПолучения
	|		ПО упАдресаПолучения.ВидАдреса = упВидыАдресовПолучения.Ссылка
	|	ПО упЗаявкаНаПеревозкуГруза.АдресПолучения = упАдресаПолучения.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.Задание.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЗаявки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗаданияНаПеревозкуГруза
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявки КАК втЗаявки
	|	ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втЗаявки.ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗаданияНаПеревозкуГруза.Рейс КАК Рейс,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упПеревозчикПоРейсуСрезПоследних.ТипТС КАК ТипТС,
	|	втЗаданияНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втРейсыПоЗаданиям
	|ИЗ
	|	втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втЗаданияНаПеревозкуГруза.Рейс КАК Рейс
	|			ИЗ
	|				втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза)) КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО втЗаданияНаПеревозкуГруза.Рейс = упПеревозчикПоРейсуСрезПоследних.Рейс
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРейсыПоЗаданиям.Рейс КАК Рейс,
	|	втРейсыПоЗаданиям.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРейсыПоЗаданиям.ТипТС КАК ТипТС,
	|	ВЫБОР
	|		КОГДА втРейсыПоЗаданиям.ТранспортноеСредство.Грузоподъемность = 0
	|			ТОГДА втРейсыПоЗаданиям.ТипТС.Грузоподъемность
	|		ИНАЧЕ втРейсыПоЗаданиям.ТранспортноеСредство.Грузоподъемность
	|	КОНЕЦ КАК ГрузоподъемностьТС,
	|	ВЫБОР
	|		КОГДА втРейсыПоЗаданиям.ТранспортноеСредство.Объем = 0
	|			ТОГДА втРейсыПоЗаданиям.ТипТС.Объем
	|		ИНАЧЕ втРейсыПоЗаданиям.ТранспортноеСредство.Объем
	|	КОНЕЦ КАК ОбъемТС,
	|	ВЫБОР
	|		КОГДА втРейсыПоЗаданиям.ТранспортноеСредство.КоличествоЗагрузочныхМетров = 0
	|			ТОГДА втРейсыПоЗаданиям.ТипТС.КоличествоЗагрузочныхМетров
	|		ИНАЧЕ втРейсыПоЗаданиям.ТранспортноеСредство.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
	|	ВЫБОР
	|		КОГДА втРейсыПоЗаданиям.ТранспортноеСредство.КоличествоМест = 0
	|			ТОГДА втРейсыПоЗаданиям.ТипТС.КоличествоМест
	|		ИНАЧЕ втРейсыПоЗаданиям.ТранспортноеСредство.КоличествоМест
	|	КОНЕЦ КАК КоличествоМестТС,
	|	1 КАК КоличествоТС,
	|	втРейсыПоЗаданиям.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втТСпоРейсу
	|ИЗ
	|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиям
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботыПоРейсу.КоличествоГрузов КАК КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.Задание.Масса))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.ГрузовоеМесто.Масса)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК МассаГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.Задание.Объем))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.ГрузовоеМесто.Объем)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК ОбъемГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.Задание.КоличествоМест))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоМест)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоМестГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.Задание.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоЗагрузочныхМетров)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА тбРаботыПоРейсу.Задание.ОценочнаяСтоимость
	|		ИНАЧЕ тбРаботыПоРейсу.ГрузовоеМесто.ОценочнаяСтоимость * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК ОценочнаяСтоимостьГруза,
	|	тбРаботыПоРейсу.Операция КАК Операция,
	|	тбРаботыПоРейсу.Рейс КАК Рейс,
	|	тбРаботыПоРейсу.ГрузовоеМесто КАК ГрузовоеМесто,
	|	тбРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	тбРаботыПоРейсу.Задание КАК Задание,
	|	тбРаботыПоРейсу.Задание.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЗаданияНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
	|		ИНАЧЕ тбРаботыПоРейсу.ГрузовоеМесто.ТипГруза
	|	КОНЕЦ КАК ТипГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)
	|		ИНАЧЕ тбРаботыПоРейсу.ГрузовоеМесто.ТемпературныйРежим
	|	КОНЕЦ КАК ТемпературныйРежим,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)
	|			ТОГДА 4
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
	|			ТОГДА 1
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|			ТОГДА 2
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК ПриоритетТипаГруза
	|ПОМЕСТИТЬ втРаботыПоРейсу
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсыПоЗаданиям КАК втРейсы)) КАК тбРаботыПоРейсу
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Рейс = втЗаданияНаПеревозкуГруза.Рейс
	|		И тбРаботыПоРейсу.Задание = втЗаданияНаПеревозкуГруза.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗаданияНаПеревозкуГруза КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗаданияНаПеревозкуГруза КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|ГДЕ тбРаботыПоРейсу.Выполнена
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботыПоРейсу.ГрузовоеМесто КАК ГрузовоеМесто,
	|	МИНИМУМ(втРаботыПоРейсу.Задание.НомерВЦепиПоставок) КАК НомерПервогоЗвенаВЦепиПоставок,
	|	МАКСИМУМ(втРаботыПоРейсу.Задание.НомерВЦепиПоставок) КАК НомерПоследнегоЗвенаВЦепиПоставок
	|ПОМЕСТИТЬ втПредельныеНомераВЦепиПоставокПоГрузовомуМесту
	|ИЗ
	|	втРаботыПоРейсу КАК втРаботыПоРейсу
	|СГРУППИРОВАТЬ ПО
	|	втРаботыПоРейсу.ГрузовоеМесто
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботыПоРейсу.ГрузовоеМесто КАК Объект,
	|	втРаботыПоРейсу.Операция КАК Операция,
	|	втРейсыПоЗаданиям.ТипТС КАК ТипТС,
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт КАК Прибытие,
	|	упМаршрутПоРейсуСрезПоследних.УбытиеФакт КАК Убытие,
	|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочкиФакт КАК РасстояниеОтПредыдущейточки,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упМаршрутПоРейсуСрезПоследних.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	упМаршрутПоРейсуСрезПоследних.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	втРаботыПоРейсу.Задание КАК Задание,
	|	втРаботыПоРейсу.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПрибытиеПлан,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК УбытиеПлан,
	|	втРаботыПоРейсу.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втМаршрутыПоРейсам
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсыПоЗаданиям КАК втРейсы)) КАК упМаршрутПоРейсуСрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботыПоРейсу КАК втРаботыПоРейсу
	|	ПО упМаршрутПоРейсуСрезПоследних.Идентификатор = втРаботыПоРейсу.Идентификатор
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРейсыПоЗаданиям КАК втРейсыПоЗаданиям
	|	ПО упМаршрутПоРейсуСрезПоследних.Рейс = втРейсыПоЗаданиям.Рейс
	|ГДЕ ИСТИНА
	|	И НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
	|	И упМаршрутПоРейсуСрезПоследних.Пройдена
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрутыПоРейсам.Объект КАК Объект,
	|	СУММА(втМаршрутыПоРейсам.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(втМаршрутыПоРейсам.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы
	|ПОМЕСТИТЬ втВремяПростояПоГрузовымМестам
	|ИЗ
	|	втМаршрутыПоРейсам КАК втМаршрутыПоРейсам
	|ГДЕ НЕ (втМаршрутыПоРейсам.Объект ЕСТЬ NULL)
	|СГРУППИРОВАТЬ ПО
	|	втМаршрутыПоРейсам.Объект
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.ГрузовоеМесто КАК ГрузовоеМесто,
	|	МИНИМУМ(втТочкиПогрузки.Прибытие) КАК ВремяНачалаПогрузкиФактическое,
	|	МАКСИМУМ(втТочкиПогрузки.Убытие) КАК ВремяОкончанияПогрузкиФактическое,
	|	МИНИМУМ(втТочкиПогрузки.ПрибытиеПлан) КАК ВремяНачалаПогрузкиПлановое,
	|	МАКСИМУМ(втТочкиПогрузки.УбытиеПлан) КАК ВремяОкончанияПогрузкиПлановое,
	|	МИНИМУМ(втТочкиРазгрузки.Прибытие) КАК ВремяНачалаРазгрузкиФактическое,
	|	МАКСИМУМ(втТочкиРазгрузки.Убытие) КАК ВремяОкончанияРазгрузкиФактическое,
	|	МИНИМУМ(втТочкиРазгрузки.ПрибытиеПлан) КАК ВремяНачалаРазгрузкиПлановое,
	|	МАКСИМУМ(втТочкиРазгрузки.УбытиеПлан) КАК ВремяОкончанияРазгрузкиПлановое
	|ПОМЕСТИТЬ втГрузовыеМестаПогрузкаРазгрузка
	|ИЗ
	|	втПредельныеНомераВЦепиПоставокПоГрузовомуМесту КАК втПредельныеНомераВЦепиПоставокПоГрузовомуМесту
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМаршрутыПоРейсам КАК втТочкиПогрузки
	|	ПО ИСТИНА
	|		И втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.ГрузовоеМесто = втТочкиПогрузки.Объект
	|		И втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.НомерПервогоЗвенаВЦепиПоставок = втТочкиПогрузки.НомерВЦепиПоставок
	|		И (ЛОЖЬ
	|			ИЛИ втТочкиПогрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ИЛИ втТочкиПогрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМаршрутыПоРейсам КАК втТочкиРазгрузки
	|	ПО ИСТИНА
	|		И втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.ГрузовоеМесто = втТочкиРазгрузки.Объект
	|		И втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.НомерПервогоЗвенаВЦепиПоставок = втТочкиРазгрузки.НомерВЦепиПоставок
	|		И (ЛОЖЬ
	|			ИЛИ втТочкиРазгрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|			ИЛИ втТочкиРазгрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|СГРУППИРОВАТЬ ПО
	|	втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.ГрузовоеМесто
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(тбРаботыРазгрузки.КоличествоГрузов) КАК КоличествоГрузов,
	|	СУММА(тбРаботыРазгрузки.МассаГруза) КАК МассаГруза,
	|	СУММА(тбРаботыРазгрузки.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(тбРаботыРазгрузки.КоличествоМестГруза) КАК КоличествоМестГруза,
	|	СУММА(тбРаботыРазгрузки.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(тбРаботыРазгрузки.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	втТСпоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втТСпоРейсу.ТипТС КАК ТипТС,
	|	тбРаботыРазгрузки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	тбРаботыРазгрузки.Рейс КАК Рейс,
	|	втТСпоРейсу.ГрузоподъемностьТС КАК ГрузоподъемностьТС,
	|	втТСпоРейсу.ОбъемТС КАК ОбъемТС,
	|	втТСпоРейсу.КоличествоЗагрузочныхМетровТС КАК КоличествоЗагрузочныхМетровТС,
	|	втТСпоРейсу.КоличествоМестТС КАК КоличествоМестТС,
	|	СУММА(ЕСТЬNULL(втВремяПростояПоГрузовымМестам.ВремяПростояПоВинеЗаказчикаЧасы, 0)) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(ЕСТЬNULL(втВремяПростояПоГрузовымМестам.ВремяПростояПоВинеПеревозчикаЧасы, 0)) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаПогрузкиФактическое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияПогрузкиФактическое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаПогрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаПогрузкиПлановое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияПогрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияПогрузкиПлановое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаРазгрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаРазгрузкиФактическое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияРазгрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияРазгрузкиФактическое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаРазгрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаРазгрузкиПлановое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияРазгрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияРазгрузкиПлановое,
	|	тбРаботыРазгрузки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	тбРаботыРазгрузки.ТипГруза КАК ТипГруза,
	|	тбРаботыРазгрузки.ТемпературныйРежим КАК ТемпературныйРежим,
	|	тбРаботыРазгрузки.ПриоритетТипаГруза КАК ПриоритетТипаГруза
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втРаботыПоРейсу КАК тбРаботыРазгрузки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТСпоРейсу КАК втТСпоРейсу
	|	ПО ИСТИНА
	|		И тбРаботыРазгрузки.Рейс = втТСпоРейсу.Рейс
	|		И тбРаботыРазгрузки.ЗаявкаНаПеревозкуГруза = втТСпоРейсу.ЗаявкаНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредельныеНомераВЦепиПоставокПоГрузовомуМесту КАК втПредельныеНомераВЦепиПоставокПоГрузовомуМесту
	|	ПО ИСТИНА
	|		И тбРаботыРазгрузки.НомерВЦепиПоставок = втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.НомерПоследнегоЗвенаВЦепиПоставок
	|		И тбРаботыРазгрузки.ГрузовоеМесто = втПредельныеНомераВЦепиПоставокПоГрузовомуМесту.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВремяПростояПоГрузовымМестам КАК втВремяПростояПоГрузовымМестам
	|	ПО тбРаботыРазгрузки.ГрузовоеМесто = втВремяПростояПоГрузовымМестам.Объект
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаПогрузкаРазгрузка КАК втГрузовыеМестаПогрузкаРазгрузка
	|	ПО тбРаботыРазгрузки.ГрузовоеМесто = втГрузовыеМестаПогрузкаРазгрузка.ГрузовоеМесто
	|ГДЕ ЛОЖЬ
	|	ИЛИ тбРаботыРазгрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	ИЛИ тбРаботыРазгрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втТСпоРейсу.ТранспортноеСредство,
	|	втТСпоРейсу.ТипТС,
	|	тбРаботыРазгрузки.ГрузовоеМесто,
	|	тбРаботыРазгрузки.Рейс,
	|	втТСпоРейсу.ГрузоподъемностьТС,
	|	втТСпоРейсу.ОбъемТС,
	|	втТСпоРейсу.КоличествоЗагрузочныхМетровТС,
	|	втТСпоРейсу.КоличествоМестТС,
	|	втТСпоРейсу.КоличествоТС,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияПогрузкиФактическое,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаПогрузкиПлановое,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияПогрузкиПлановое,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаРазгрузкиФактическое,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияРазгрузкиФактическое,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаРазгрузкиПлановое,
	|	втГрузовыеМестаПогрузкаРазгрузка.ВремяОкончанияРазгрузкиПлановое,
	|	ЕСТЬNULL(втГрузовыеМестаПогрузкаРазгрузка.ВремяНачалаПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)),
	|	тбРаботыРазгрузки.ЗаявкаНаПеревозкуГруза,
	|	тбРаботыРазгрузки.ТипГруза,
	|	тбРаботыРазгрузки.ТемпературныйРежим,
	|	тбРаботыРазгрузки.ПриоритетТипаГруза
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втМаршрутыПоРейсам.Убытие) КАК Убытие,
	|	втМаршрутыПоРейсам.Объект КАК ГрузовоеМесто
	|ПОМЕСТИТЬ втТипыТСПоГрузам
	|ИЗ
	|	втМаршрутыПоРейсам КАК втМаршрутыПоРейсам
	|СГРУППИРОВАТЬ ПО
	|	втМаршрутыПоРейсам.Объект
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втМаршрутыПоРейсам.ТипТС) КАК ТипТС,
	|	втМаршрутыПоРейсам.Объект КАК ГрузовоеМесто,
	|	МАКСИМУМ(втТипыТСПоГрузам.Убытие) КАК Убытие,
	|	втМаршрутыПоРейсам.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втПоследниеТипыТСПоОбъектам
	|ИЗ
	|	втМаршрутыПоРейсам КАК втМаршрутыПоРейсам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТипыТСПоГрузам КАК втТипыТСПоГрузам
	|	ПО ИСТИНА
	|		И втМаршрутыПоРейсам.Объект = втТипыТСПоГрузам.ГрузовоеМесто
	|		И втМаршрутыПоРейсам.Убытие = втТипыТСПоГрузам.Убытие
	|СГРУППИРОВАТЬ ПО
	|	втМаршрутыПоРейсам.Объект,
	|	втМаршрутыПоРейсам.ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.АдресПолучения КАК АдресПолучения
	|ПОМЕСТИТЬ втГрузовыеМестаАдреса
	|ИЗ
	|	втЗаявки КАК втЗаявки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК упЗаявкаНаПеревозкуГрузаГрузовыеМеста
	|	ПО втЗаявки.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГрузаГрузовыеМеста.Ссылка
	|";

	
	Возврат текРезультат;	
	
КонецФункции

Процедура ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза_ФактическиеДоходы(сткЗаявка, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами)
	
	Если тбзРасстоянияМеждуАдресами = Неопределено Тогда
		тбзРасстоянияМеждуАдресами = Новый ТаблицаЗначений;
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресОтправления", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресПолучения", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("Расстояние", Новый ОписаниеТипов("Число"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("ВремяРаботыЧас", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(сткЗаявка.АдресОтправления) И ЗначениеЗаполнено(сткЗаявка.АдресПолучения) Тогда
		
		мсвАдреса	  = тбзРасстоянияМеждуАдресами.НайтиСтроки(Новый Структура("АдресОтправления, АдресПолучения",сткЗаявка.АдресОтправления, сткЗаявка.АдресПолучения));
		Если мсвАдреса.Количество() > 0 Тогда
			сткДанныеПоОбъектуРасчета.Вставить("Расстояние", мсвАдреса[0].Расстояние);
			сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", мсвАдреса[0].ВремяРаботыЧас);
		Иначе	
	    	сткРасстояния = ПолучитьРасстояниеИВремя(сткЗаявка.АдресОтправления, сткЗаявка.АдресПолучения, сткЗаявка.ВремяПрохожденияТочек);
			сткДанныеПоОбъектуРасчета.Вставить("Расстояние", сткРасстояния.Расстояние);
			сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", сткРасстояния.Время);
			
			НоваяСтрока = тбзРасстоянияМеждуАдресами.Добавить();
			НоваяСтрока.АдресОтправления = сткЗаявка.АдресОтправления;
			НоваяСтрока.АдресПолучения = сткЗаявка.АдресПолучения;
			НоваяСтрока.Расстояние = сткДанныеПоОбъектуРасчета.Расстояние;
			НоваяСтрока.ВремяРаботыЧас = сткДанныеПоОбъектуРасчета.ВремяРаботыЧас;
		КонецЕсли;
					
	ИначеЕсли ЗначениеЗаполнено(сткЗаявка.ЗонаОтправления) И ЗначениеЗаполнено(сткЗаявка.ЗонаПолучения) Тогда	
		сткРасстояния = ПолучитьРасстояниеИВремяМеждуГеографическимиЗонами(сткЗаявка.ЗонаОтправления, сткЗаявка.ЗонаПолучения);	
		сткДанныеПоОбъектуРасчета.Вставить("Расстояние", сткРасстояния.Расстояние);
		сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", сткРасстояния.Время);
	Иначе
		сткДанныеПоОбъектуРасчета.Вставить("Расстояние", 0);
		сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", 0);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРасстояниеИВремяПоГрузовомуМестуЗаявкиНаПеревозкуГруза_ФактическиеДоходы(Строка, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами)
	
	Если тбзРасстоянияМеждуАдресами = Неопределено Тогда
		тбзРасстоянияМеждуАдресами = Новый ТаблицаЗначений;
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресОтправления", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("АдресПолучения", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("Расстояние", Новый ОписаниеТипов("Число"));
		тбзРасстоянияМеждуАдресами.Колонки.Добавить("ВремяРаботыЧас", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	// Если в ТЧ.ГрузовыеМеста заявки не заполнены адреса
	Если НЕ ЗначениеЗаполнено(Строка.АдресОтправленияГруза) И НЕ ЗначениеЗаполнено(Строка.АдресПолученияГруза) Тогда
		
		// Адреса берутся из заявки
		ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза_ФактическиеДоходы(Строка, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами);
	
	Иначе
		текАдресОтправления = Строка.АдресОтправленияГруза;
		Если НЕ ЗначениеЗаполнено(текАдресОтправления) Тогда
			текАдресОтправления = Строка.АдресОтправления;	
		КонецЕсли;
		
		текАдресПолучения = Строка.АдресПолученияГруза;
		Если НЕ ЗначениеЗаполнено(текАдресПолучения) Тогда
			текАдресПолучения = Строка.АдресПолучения;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текАдресОтправления) И ЗначениеЗаполнено(текАдресПолучения) Тогда
			
			мсвАдреса	  = тбзРасстоянияМеждуАдресами.НайтиСтроки(Новый Структура("АдресОтправления, АдресПолучения",текАдресОтправления, текАдресПолучения));
			Если мсвАдреса.Количество() > 0 Тогда
				сткДанныеПоОбъектуРасчета.Вставить("Расстояние", мсвАдреса[0].Расстояние);
				сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", мсвАдреса[0].ВремяРаботыЧас);
			Иначе	
				сткРасстояния = ПолучитьРасстояниеИВремя(текАдресОтправления, текАдресПолучения, Строка.ВремяПрохожденияТочекГруза);
				сткДанныеПоОбъектуРасчета.Вставить("Расстояние", сткРасстояния.Расстояние);
				сткДанныеПоОбъектуРасчета.Вставить("ВремяРаботыЧас", сткРасстояния.Время);
				
				НоваяСтрока = тбзРасстоянияМеждуАдресами.Добавить();
				НоваяСтрока.АдресОтправления 	= Строка.АдресОтправления;
				НоваяСтрока.АдресПолучения = Строка.АдресПолучения;
				НоваяСтрока.Расстояние = сткДанныеПоОбъектуРасчета.Расстояние;
				НоваяСтрока.ВремяРаботыЧас = сткДанныеПоОбъектуРасчета.ВремяРаботыЧас;
			КонецЕсли;
		Иначе	
			Если НЕ сткДанныеПоОбъектуРасчета.РасстоянияРассчитаны Тогда
				ЗаполнитьРасстояниеИВремяПоЗаявкеНаПеревозкуГруза_ФактическиеДоходы(Строка, сткДанныеПоОбъектуРасчета, тбзРасстоянияМеждуАдресами);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти	

#Область РасчетПоРейсу

Процедура ПодготовитьВходныеПараметры_Рейс(Объект, ВходныеПараметры, сткТрассировка)
		
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "ПлановыеРасходы");
		
	Если ТипЗнч(Объект) =  Тип("ДанныеФормыСтруктура") Или ТипЗнч(Объект) = Тип("ДокументОбъект.упРейс") Тогда
		ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));		
	КонецЕсли;
	
	ПараметрыРейса = упРейс.ПараметрыРейса(Объект.Ссылка);
	
	Если Не ВходныеПараметры.Свойство("ГруппаТарифов") Тогда
		ВходныеПараметры.Вставить("ГруппаТарифов", ПараметрыРейса.ГруппаТарифов);	
	КонецЕсли;
	
	ВходныеПараметры.Вставить("Организация", Объект.Организация);
	ВходныеПараметры.Вставить("ВремяРаботыЧас", ПараметрыРейса.Время);
	ВходныеПараметры.Вставить("Расстояние", ПараметрыРейса.Расстояние);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", Объект.Ссылка);
	ВходныеПараметры.Вставить("Рейс", Объект.Ссылка);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ДополнительныеКолонкиПоСтроке", "Задание");
	
	ВходныеПараметры.Вставить("ТранспортноеСредство", ПараметрыРейса.ТранспортноеСредство);
	Если Не ВходныеПараметры.Свойство("ТипТС") Тогда
		ВходныеПараметры.Вставить("ТипТС", ПараметрыРейса.ТипТС);
	КонецЕсли; 
	ВходныеПараметры.Вставить("Перевозчик", ПараметрыРейса.Перевозчик);
	ВходныеПараметры.Вставить("ЗонаОтправления", ПараметрыРейса.ЗонаПогрузки);
	ВходныеПараметры.Вставить("ЗонаПолучения", ПараметрыРейса.ЗонаРазгрузки);
	ВходныеПараметры.Вставить("ВидПеревозки", Объект.ВидПеревозки);
	
	ВходныеПараметры.Вставить("ГрузоподъемностьТС", 0);		
	ВходныеПараметры.Вставить("ОбъемТС", 0);		
	ВходныеПараметры.Вставить("КоличествоМестТС", 0);		
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровТС", 0);		
	
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	                                                         
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", '00010101');
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", ПараметрыРейса.СуммаИнкассации);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());
				
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	Если ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "ГрузоподъемностьТС") Тогда
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "ГрузоподъемностьТС") Тогда
			мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(Перечисления.упПараметрыРасчета.ГрузоподъемностьТС);	
		КонецЕсли; 
	КонецЕсли;	
	
	Если ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "ОбъемТС") Тогда
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "ОбъемТС") Тогда
			мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(Перечисления.упПараметрыРасчета.ОбъемТС);	
		КонецЕсли; 
	КонецЕсли;	
		
	Если ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "КоличествоЗагрузочныхМетровТС") Тогда
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "КоличествоЗагрузочныхМетровТС") Тогда
			мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(Перечисления.упПараметрыРасчета.КоличествоЗагрузочныхМетровТС);	
		КонецЕсли; 
	КонецЕсли;	
		
	Если ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоКаждойСтроке, "КоличествоМестТС") Тогда
		Если НЕ ЕстьРасчетПоПараметру(мсвПараметрыДляРасчетаПоДокументуВЦелом, "КоличествоМестТС") Тогда
			мсвПараметрыДляРасчетаПоДокументуВЦелом.Добавить(Перечисления.упПараметрыРасчета.КоличествоМестТС);	
		КонецЕсли; 
	КонецЕсли;
		
КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	тбзРезультат.Колонки.Добавить("Задание");
	
	текРейсМасса = 0;
	текРейсОбъем = 0;
	текРейсКоличествоЗагрузочныхМетров = 0;
	текРейсКоличествоМест = 0;
	
	НужныПараметрыГруза = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныРасстояния = НужныРасстояния(мсвПараметрыРасчета);
	НужныВремянныеПараметры = НужныВремянныеПараметры(мсвПараметрыРасчета);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Не отмененные работы по рейсу
	|ВЫБРАТЬ
	|	тбРаботыПоРейсу.КоличествоГрузов КАК КоличествоГрузов,
	|	тбРаботыПоРейсу.Операция КАК Операция,
	|	тбРаботыПоРейсу.Рейс КАК Рейс,
	|	тбРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	тбРаботыПоРейсу.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	тбРаботыПоРейсу.ГрузовоеМесто КАК ГрузовоеМесто,
	|	тбРаботыПоРейсу.Задание КАК Задание,
	|	тбРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	тбРаботыПоРейсу.Отменена КАК Отменена
	|ПОМЕСТИТЬ втРаботыПоРейсуПоЗаданиям
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК тбРаботыПоРейсу
	|ГДЕ НЕ (тбРаботыПоРейсу.Отменена)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботыПоРейсу.Задание КАК Задание,
	|	тбРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	тбРаботыПоРейсу.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	тбРаботыПоРейсу.КоличествоГрузов КАК КоличествоГрузов,
	|	тбРаботыПоРейсу.Операция КАК Операция,
	|	тбРаботыПоРейсу.Отменена КАК Отменена,
	|	тбРаботыПоРейсу.Рейс КАК Рейс,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЭтоВидГрузаГруз,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.Задание.Масса))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.ГрузовоеМесто.Масса)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК МассаГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.Задание.Объем))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.ГрузовоеМесто.Объем)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК ОбъемГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.Задание.КоличествоМест))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоМест)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоМестГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.Задание.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоЗагрузочныхМетров)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА тбРаботыПоРейсу.Задание.ОценочнаяСтоимость
	|		ИНАЧЕ тбРаботыПоРейсу.ГрузовоеМесто.ОценочнаяСтоимость * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК ОценочнаяСтоимостьГруза
	|ПОМЕСТИТЬ втРаботыПоРейсу
	|ИЗ
	|	втРаботыПоРейсуПоЗаданиям КАК тбРаботыПоРейсу
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втРаботыПоРейсуПоЗаданиям КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втРаботыПоРейсуПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Не отмененные точки маршрута с выполняемыми в точке заданиями и операциями
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	втРаботыПоРейсу.Задание КАК Объект,
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт КАК Прибытие,
	|	упМаршрутПоРейсуСрезПоследних.УбытиеФакт КАК Убытие,
	|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейточки,
	|	втРаботыПоРейсу.Операция КАК Операция,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПрибытиеПлан,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК УбытиеПлан,
	|	упМаршрутПоРейсуСрезПоследних.Адрес.ГеографическаяЗона КАК АдресГеографическаяЗона
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботыПоРейсу КАК втРаботыПоРейсу
	|	ПО упМаршрутПоРейсуСрезПоследних.Идентификатор = втРаботыПоРейсу.Идентификатор
	|ГДЕ НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Прибытие / убытие в точке по заданиям.
	|ВЫБРАТЬ
	|	втМаршрут.Объект КАК Объект,
	|	ВЫБОР
	|		КОГДА втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|		КОГДА втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|		ИНАЧЕ втМаршрут.Операция
	|	КОНЕЦ КАК Операция,
	|	МИНИМУМ(втМаршрут.Прибытие) КАК ПрибытиеФакт,
	|	МАКСИМУМ(втМаршрут.Убытие) КАК УбытиеФакт,
	|	МИНИМУМ(втМаршрут.ПрибытиеПлан) КАК ПрибытиеПлан,
	|	МАКСИМУМ(втМаршрут.УбытиеПлан) КАК УбытиеПлан
	|ПОМЕСТИТЬ втВремянныеПараметрыЗаданий
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Объект,
	|	ВЫБОР
	|		КОГДА втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|		КОГДА втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|		ИНАЧЕ втМаршрут.Операция
	|	КОНЕЦ
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|// Количество точек погрузки/разгрузки по заданию/идентификатору
	|ВЫБРАТЬ
	|	втМаршрут.Объект КАК Объект,
	|	СУММА(ВЫБОР
	|		КОГДА втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаРазгрузки,
	|	втМаршрут.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втКоличествоТочек
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Объект,
	|	втМаршрут.Идентификатор
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|// Количество точек погрузки/разгрузки/всего по заданию
	|ВЫБРАТЬ
	|	втКоличествоТочек.Объект КАК Объект,
	|	СУММА(ВЫБОР
	|		КОГДА втКоличествоТочек.ЭтоТочкаПогрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втКоличествоТочек.ЭтоТочкаРазгрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекРазгрузки,
	|	СУММА(1) КАК КоличествоТочекМаршрута
	|ПОМЕСТИТЬ втКоличествоТочек2
	|ИЗ
	|	втКоличествоТочек КАК втКоличествоТочек
	|СГРУППИРОВАТЬ ПО
	|	втКоличествоТочек.Объект
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|// Зоны погрузки по заданиям.
	|ВЫБРАТЬ
	|	втМаршрут.Объект КАК Объект,
	|	МАКСИМУМ(втМаршрут.АдресГеографическаяЗона) КАК АдресГеографическаяЗона
	|ПОМЕСТИТЬ втТочкиПогрузки
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втМаршрут.Объект КАК Объект,
	|		МИНИМУМ(втМаршрут.СтрокаНомер) КАК СтрокаНомер
	|	ИЗ
	|		втМаршрут КАК втМаршрут
	|	ГДЕ втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	СГРУППИРОВАТЬ ПО
	|		втМаршрут.Объект) КАК ВложенныйЗапрос
	|	ПО ИСТИНА
	|		И втМаршрут.Объект = ВложенныйЗапрос.Объект
	|		И втМаршрут.СтрокаНомер = ВложенныйЗапрос.СтрокаНомер
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Объект
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|// Зоны разгрузки по заданиям.
	|ВЫБРАТЬ
	|	втМаршрут.Объект КАК Объект,
	|	МАКСИМУМ(втМаршрут.АдресГеографическаяЗона) КАК АдресГеографическаяЗона
	|ПОМЕСТИТЬ втТочкиРазгрузки
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втМаршрут.Объект КАК Объект,
	|		МИНИМУМ(втМаршрут.СтрокаНомер) КАК СтрокаНомер
	|	ИЗ
	|		втМаршрут КАК втМаршрут
	|	ГДЕ втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	СГРУППИРОВАТЬ ПО
	|		втМаршрут.Объект) КАК ВложенныйЗапрос
	|	ПО ИСТИНА
	|		И втМаршрут.Объект = ВложенныйЗапрос.Объект
	|		И втМаршрут.СтрокаНомер = ВложенныйЗапрос.СтрокаНомер
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Объект
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|// Параметры по заданиям.
	|ВЫБРАТЬ
	|	тбРаботыПоРейсу.Задание КАК Задание,
	|	СУММА(тбРаботыПоРейсу.МассаГруза) КАК МассаГруза,
	|	СУММА(тбРаботыПоРейсу.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(тбРаботыПоРейсу.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(тбРаботыПоРейсу.КоличествоМестГруза) КАК КоличествоМестГруза,
	|	СУММА(тбРаботыПоРейсу.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	МИНИМУМ(тбРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ОтправлениеГрузаС) КАК ВремяОтправленияСПлановое,
	|	МАКСИМУМ(тбРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ОтправлениеГрузаПо) КАК ВремяОтправленияПоПлановое,
	|	МИНИМУМ(тбРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ПолучениеГрузаС) КАК ВремяПолученияСПлановое,
	|	МАКСИМУМ(тбРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ПолучениеГрузаПо) КАК ВремяПолученияПоПлановое,
	|	ЕСТЬNULL(втПогрузкаВремянныеПараметрыЗаданий.ПрибытиеПлан, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаПогрузкиПлановое,
	|	ЕСТЬNULL(втПогрузкаВремянныеПараметрыЗаданий.ПрибытиеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаПогрузкиФактическое,
	|	ЕСТЬNULL(втПогрузкаВремянныеПараметрыЗаданий.УбытиеПлан, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияПогрузкиПлановое,
	|	ЕСТЬNULL(втПогрузкаВремянныеПараметрыЗаданий.УбытиеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияПогрузкиФактическое,
	|	ЕСТЬNULL(втРазгрузкаВремянныеПараметрыЗаданий.ПрибытиеПлан, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаРазгрузкиПлановое,
	|	ЕСТЬNULL(втРазгрузкаВремянныеПараметрыЗаданий.ПрибытиеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаРазгрузкиФактическое,
	|	ЕСТЬNULL(втРазгрузкаВремянныеПараметрыЗаданий.УбытиеПлан, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияРазгрузкиПлановое,
	|	ЕСТЬNULL(втРазгрузкаВремянныеПараметрыЗаданий.УбытиеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияРазгрузкиФактическое,
	|	МАКСИМУМ(ЕСТЬNULL(втКоличествоТочек2.КоличествоТочекПогрузки, 0)) КАК КоличествоТочекПогрузки,
	|	МАКСИМУМ(ЕСТЬNULL(втКоличествоТочек2.КоличествоТочекРазгрузки, 0)) КАК КоличествоТочекРазгрузки,
	|	МАКСИМУМ(ЕСТЬNULL(втКоличествоТочек2.КоличествоТочекМаршрута, 0)) КАК КоличествоТочекВМаршруте,
	|	ЕСТЬNULL(втТочкиПогрузки.АдресГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаОтправления,
	|	ЕСТЬNULL(втТочкиРазгрузки.АдресГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаПолучения,
	|	МАКСИМУМ(тбРаботыПоРейсу.ЭтоВидГрузаГруз) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ИЗ
	|	втРаботыПоРейсу КАК тбРаботыПоРейсу
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВремянныеПараметрыЗаданий КАК втПогрузкаВремянныеПараметрыЗаданий
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание = втПогрузкаВремянныеПараметрыЗаданий.Объект
	|		И (ЛОЖЬ
	|			ИЛИ втПогрузкаВремянныеПараметрыЗаданий.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|			ИЛИ втПогрузкаВремянныеПараметрыЗаданий.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВремянныеПараметрыЗаданий КАК втРазгрузкаВремянныеПараметрыЗаданий
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание = втРазгрузкаВремянныеПараметрыЗаданий.Объект
	|		И (ЛОЖЬ
	|			ИЛИ втРазгрузкаВремянныеПараметрыЗаданий.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|			ИЛИ втРазгрузкаВремянныеПараметрыЗаданий.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТочек2 КАК втКоличествоТочек2
	|	ПО тбРаботыПоРейсу.Задание = втКоличествоТочек2.Объект
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТочкиПогрузки КАК втТочкиПогрузки
	|	ПО тбРаботыПоРейсу.Задание = втТочкиПогрузки.Объект
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТочкиРазгрузки КАК втТочкиРазгрузки
	|	ПО тбРаботыПоРейсу.Задание = втТочкиРазгрузки.Объект
	|ГДЕ ЛОЖЬ
	|	ИЛИ тбРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ тбРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	тбРаботыПоРейсу.Задание,
	|	втПогрузкаВремянныеПараметрыЗаданий.ПрибытиеПлан,
	|	втПогрузкаВремянныеПараметрыЗаданий.ПрибытиеФакт,
	|	втПогрузкаВремянныеПараметрыЗаданий.УбытиеПлан,
	|	втПогрузкаВремянныеПараметрыЗаданий.УбытиеФакт,
	|	втРазгрузкаВремянныеПараметрыЗаданий.ПрибытиеПлан,
	|	втРазгрузкаВремянныеПараметрыЗаданий.ПрибытиеФакт,
	|	втРазгрузкаВремянныеПараметрыЗаданий.УбытиеПлан,
	|	втРазгрузкаВремянныеПараметрыЗаданий.УбытиеФакт,
	|	ЕСТЬNULL(втТочкиПогрузки.АдресГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)),
	|	ЕСТЬNULL(втТочкиРазгрузки.АдресГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка))
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|// Маршрут по рейсу для рассчета расстояний и времени по заданиям.
	|ВЫБРАТЬ
	|	втМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрут.Объект КАК Объект,
	|	втМаршрут.Идентификатор КАК Идентификатор,
	|	втМаршрут.Прибытие КАК Прибытие,
	|	втМаршрут.Убытие КАК Убытие,
	|	втМаршрут.РасстояниеОтПредыдущейточки КАК РасстояниеОтПредыдущейточки,
	|	втМаршрут.Операция КАК Операция,
	|	втМаршрут.ПрибытиеПлан КАК ПрибытиеПлан,
	|	втМаршрут.УбытиеПлан КАК УбытиеПлан
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|УПОРЯДОЧИТЬ ПО
	|	СтрокаНомер
	|ИТОГИ
	|	МАКСИМУМ(Прибытие),
	|	МАКСИМУМ(Убытие),
	|	МАКСИМУМ(РасстояниеОтПредыдущейточки),
	|	МАКСИМУМ(ПрибытиеПлан),
	|	МАКСИМУМ(УбытиеПлан)
	|ПО
	|	СтрокаНомер
	|";


	Запрос.УстановитьПараметр("Рейс", сткДанныеПоОбъектуРасчета.Ссылка);
	
	мсвРезультаты = Запрос.ВыполнитьПакет();
	
	ИндексЗапросаПараметрыЗаданий = 8;
	ИндексЗапросаРасчетРасстояний = 9;
	
	Если Ложь
		Или НужныРасстояния 
		Или НужныВремянныеПараметры 
	Тогда
		тбзРасстоянияПоЗаданиям = ТаблицаРасстоянийИЗатраченногоВремениДляКаждогоОбъекта(мсвРезультаты[ИндексЗапросаРасчетРасстояний].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам),,Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка());
	КонецЕсли;
	
	Выборка = мсвРезультаты[ИндексЗапросаПараметрыЗаданий].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.Задание = Выборка.Задание;
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		НоваяСтрока.СтруктураДанные.Вставить("ОбъектРасчета",	Новый Структура("Имя, Значение","Задание",Выборка.Задание));  
		ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, Выборка);
		
		Если Ложь
			Или НужныРасстояния 
			Или НужныВремянныеПараметры
		Тогда
			мсвСтроки = тбзРасстоянияПоЗаданиям.НайтиСтроки(Новый Структура("Объект", Выборка.Задание));
			Если мсвСтроки.Количество()>0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, мсвСтроки[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;  
	
	Если НужныХарактеристикиТС Тогда
		ЗаполнитьЗначенияПоказателейХарактеристикТС_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета);
	КонецЕсли;		

	Если НужныХарактеристикиТС Тогда
		Для каждого СтрокаРезультат Из тбзРезультат Цикл
			сткХарактеристикиТС = СтруктураХарактеристикиТС();
			ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, сткДанныеПоОбъектуРасчета);
			ЗаполнитьПоСтруктуре(СтрокаРезультат.СтруктураДанные, сткХарактеристикиТС);
			Если НуженОбъемныйВес Тогда
				ЗаполнитьОбъемныйВес(СтрокаРезультат.СтруктураДанные);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
	Возврат тбзРезультат;
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	
	НужныПараметрыГруза 	= НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС	= НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес		= НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныФактическиеРасходы	= НужныФактическиеРасходы(мсвПараметрыРасчета);
	НужныВремянныеПараметры  = НужныВремянныеПараметры(мсвПараметрыРасчета);
	НужныКоличествоТочек	= НужныКоличествоТочек(мсвПараметрыРасчета);
	НужныКоличествоЗаданий   = НужныКоличествоЗаданий(мсвПараметрыРасчета);
	
	Если Ложь
		Или НужныПараметрыГруза 
		Или НужныХарактеристикиТС 
		Или НужныВремянныеПараметры 
		Или НужныКоличествоТочек 
		Или НужныКоличествоЗаданий
	Тогда
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|// Не отмененные точки по рейсу.
		|ВЫБРАТЬ
		|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
		|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
		|	упМаршрутПоРейсуСрезПоследних.ВремяОтПредыдущейТочки КАК ВремяОтПредыдущейТочки,
		|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
		|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПрибытиеПлан,
		|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК УбытиеПлан,
		|	упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт КАК ПрибытиеФакт,
		|	упМаршрутПоРейсуСрезПоследних.УбытиеФакт КАК УбытиеФакт
		|ПОМЕСТИТЬ втМаршрут
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
		|ГДЕ НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Не отмененные работы по рейсу в разрезе заданий/операций
		|ВЫБРАТЬ
		|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упРаботыПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
		|	упРаботыПоРейсуСрезПоследних.Операция КАК Операция,
		|	упРаботыПоРейсуСрезПоследних.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
		|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА втМаршрут.ПрибытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		|	КОНЕЦ КАК ВремяНачалаПогрузкиПлановое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА втМаршрут.ПрибытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		|	КОНЕЦ КАК ВремяНачалаПогрузкиФактическое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА втМаршрут.УбытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ВремяОкончанияПогрузкиПлановое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА втМаршрут.УбытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ВремяОкончанияПогрузкиФактическое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|			ТОГДА втМаршрут.ПрибытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		|	КОНЕЦ КАК ВремяНачалаРазгрузкиПлановое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|			ТОГДА втМаршрут.ПрибытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		|	КОНЕЦ КАК ВремяНачалаРазгрузкиФактическое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|			ТОГДА втМаршрут.УбытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ВремяОкончанияРазгрузкиПлановое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|				ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|			ТОГДА втМаршрут.УбытиеПлан
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ВремяОкончанияРазгрузкиФактическое,
		|	ВЫБОР
		|		КОГДА упРаботыПоРейсуСрезПоследних.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
		|ПОМЕСТИТЬ втРаботыПоРейсуПоЗаданиям
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс
		|			И НЕ Отменена) КАК упРаботыПоРейсуСрезПоследних
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрут
		|	ПО упРаботыПоРейсуСрезПоследних.Идентификатор = втМаршрут.Идентификатор
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Не работы по рейсу с актуальными параметрами грузовых мест
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.Задание.Масса))
		|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.ГрузовоеМесто.Масса)) * тбРаботыПоРейсу.КоличествоГрузов
		|	КОНЕЦ) КАК МассаГруза,
		|	СУММА(ВЫБОР
		|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.Задание.Объем))
		|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.ГрузовоеМесто.Объем)) * тбРаботыПоРейсу.КоличествоГрузов
		|	КОНЕЦ) КАК ОбъемГруза,
		|	СУММА(ВЫБОР
		|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.Задание.КоличествоМест))
		|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоМест)) * тбРаботыПоРейсу.КоличествоГрузов
		|	КОНЕЦ) КАК КоличествоМестГруза,
		|	СУММА(ВЫБОР
		|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.Задание.КоличествоЗагрузочныхМетров))
		|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоЗагрузочныхМетров)) * тбРаботыПоРейсу.КоличествоГрузов
		|	КОНЕЦ) КАК КоличествоЗагрузочныхМетровГруза,
		|	СУММА(ВЫБОР
		|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА тбРаботыПоРейсу.Задание.ОценочнаяСтоимость
		|		ИНАЧЕ тбРаботыПоРейсу.ГрузовоеМесто.ОценочнаяСтоимость * тбРаботыПоРейсу.КоличествоГрузов
		|	КОНЕЦ) КАК ОценочнаяСтоимостьГруза,
		|	тбРаботыПоРейсу.Идентификатор КАК Идентификатор,
		|	тбРаботыПоРейсу.Операция КАК Операция,
		|	тбРаботыПоРейсу.Задание КАК Задание,
		|	МИНИМУМ(тбРаботыПоРейсу.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
		|	МИНИМУМ(тбРаботыПоРейсу.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
		|	МАКСИМУМ(тбРаботыПоРейсу.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
		|	МАКСИМУМ(тбРаботыПоРейсу.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
		|	МИНИМУМ(тбРаботыПоРейсу.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
		|	МИНИМУМ(тбРаботыПоРейсу.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
		|	МАКСИМУМ(тбРаботыПоРейсу.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
		|	МАКСИМУМ(тбРаботыПоРейсу.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
		|	МАКСИМУМ(тбРаботыПоРейсу.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
		|ПОМЕСТИТЬ втРаботыПоРейсу
		|ИЗ
		|	втРаботыПоРейсуПоЗаданиям КАК тбРаботыПоРейсу
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
		|		,
		|		ЗаявкаНаПеревозку В (
		|			ВЫБРАТЬ
		|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|			ИЗ
		|				втРаботыПоРейсуПоЗаданиям КАК втЗадания
		|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
		|	ПО ИСТИНА
		|		И тбРаботыПоРейсу.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
		|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
		|		,
		|		ЗаданиеНаПеревозку В (
		|			ВЫБРАТЬ
		|				втЗадания.Задание КАК Задание
		|			ИЗ
		|				втРаботыПоРейсуПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
		|	ПО ИСТИНА
		|		И тбРаботыПоРейсу.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
		|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
		|СГРУППИРОВАТЬ ПО
		|	тбРаботыПоРейсу.Идентификатор,
		|	тбРаботыПоРейсу.Операция,
		|	тбРаботыПоРейсу.Задание
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|// Характеристики точек по грузовым параметрам.
		|ВЫБРАТЬ
		|	втРаботыПоРейсу.Идентификатор КАК Идентификатор,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|			КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|					ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|				ТОГДА - 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ * втРаботыПоРейсу.МассаГруза) КАК МассаГруза,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|			КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|					ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|				ТОГДА - 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ * втРаботыПоРейсу.ОбъемГруза) КАК ОбъемГруза,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|			КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|					ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|				ТОГДА - 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ * втРаботыПоРейсу.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|			КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|					ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|				ТОГДА - 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ * втРаботыПоРейсу.КоличествоМестГруза) КАК КоличествоМестГруза,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЭтоТочкаПогрузки,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЭтоТочкаРазгрузки
		|ПОМЕСТИТЬ втГрузыВТочке
		|ИЗ
		|	втРаботыПоРейсу КАК втРаботыПоРейсу
		|СГРУППИРОВАТЬ ПО
		|	втРаботыПоРейсу.Идентификатор
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(тбЗадания.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
		|ПОМЕСТИТЬ втКоличествоЗаданий
		|ИЗ
		|	(ВЫБРАТЬ
		|		втРаботыПоРейсуТ.Задание КАК Задание,
		|		МАКСИМУМ(втРаботыПоРейсуТ.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
		|	ИЗ
		|		втРаботыПоРейсу КАК втРаботыПоРейсуТ
		|	СГРУППИРОВАТЬ ПО
		|		втРаботыПоРейсуТ.Задание) КАК тбЗадания
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|// Характеристики точек по грузовым параметрам + количество точек погрузки/разгрузки/всего.
		|ВЫБРАТЬ
		|	втМаршрут.РасстояниеОтПредыдущейТочки КАК РасстояниеОтПредыдущейТочки,
		|	втМаршрут.ВремяОтПредыдущейТочки КАК ВремяОтПредыдущейТочки,
		|	втМаршрут.Идентификатор КАК Идентификатор,
		|	ЕСТЬNULL(втГрузыВТочке.МассаГруза, 0) КАК МассаГруза,
		|	ЕСТЬNULL(втГрузыВТочке.ОбъемГруза, 0) КАК ОбъемГруза,
		|	ЕСТЬNULL(втГрузыВТочке.КоличествоЗагрузочныхМетровГруза, 0) КАК КоличествоЗагрузочныхМетровГруза,
		|	ЕСТЬNULL(втГрузыВТочке.КоличествоМестГруза, 0) КАК КоличествоМестГруза,
		|	ЕСТЬNULL(ВЫБОР
		|		КОГДА втГрузыВТочке.ЭтоТочкаПогрузки > 0
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ, 0) КАК КоличествоТочекПогрузки,
		|	ЕСТЬNULL(ВЫБОР
		|		КОГДА втГрузыВТочке.ЭтоТочкаРазгрузки > 0
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ, 0) КАК КоличествоТочекРазгрузки,
		|	1 КАК КоличествоТочекВМаршруте
		|ИЗ
		|	втМаршрут КАК втМаршрут
		|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузыВТочке КАК втГрузыВТочке
		|	ПО втМаршрут.Идентификатор = втГрузыВТочке.Идентификатор
		|УПОРЯДОЧИТЬ ПО
		|	втМаршрут.СтрокаНомер
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|// Временные характеристики заданий рейса, оценочная стоимость грузов.
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|		КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|				ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|			ТОГДА втРаботыПоРейсу.ОценочнаяСтоимостьГруза
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ОценочнаяСтоимостьГруза,
		|	МИНИМУМ(втРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ОтправлениеГрузаС) КАК ВремяОтправленияСПлановое,
		|	МАКСИМУМ(втРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ОтправлениеГрузаПо) КАК ВремяОтправленияПоПлановое,
		|	МИНИМУМ(втРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ПолучениеГрузаС) КАК ВремяПолученияСПлановое,
		|	МАКСИМУМ(втРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза.ПолучениеГрузаПо) КАК ВремяПолученияПоПлановое,
		|	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
		|	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
		|	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
		|	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
		|	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
		|	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
		|	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
		|	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
		|	МАКСИМУМ(втКоличествоЗаданий.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
		|ИЗ
		|	втРаботыПоРейсу КАК втРаботыПоРейсу,
		|	втКоличествоЗаданий КАК втКоличествоЗаданий
		|";

		
		Запрос.УстановитьПараметр("Рейс", сткДанныеПоОбъектуРасчета.Ссылка);
		
		мсвРезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		ИндексЗапросаХарактеристикиТочек = 5;
		ИндексЗапросаХаарктеристикиРейса = 6;
		
		Выборка = мсвРезультатыЗапроса[ИндексЗапросаХарактеристикиТочек].Выбрать();
		
		текМассаГруза = 0;
		МаксимальнаяМассаВТеченииРейса  = 0;
		
		текОбъемГруза = 0;
		МаксимальныйОбъемВТеченииРейса  = 0;
		
		текКоличествоМестГруза = 0;
		МаксимальноеКоличествоМестВТеченииРейса	= 0;
		
		текКоличествоЗагрузочныхМетровГруза = 0;
		МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса = 0;
		
		КоличествоТочекВМаршруте = 0;
		КоличествоТочекПогрузки  = 0;
		КоличествоТочекРазгрузки = 0;
				
		Пока Выборка.Следующий() Цикл
			
			текМассаГруза = текМассаГруза + Выборка.МассаГруза;
			
			Если текМассаГруза > МаксимальнаяМассаВТеченииРейса Тогда
				МаксимальнаяМассаВТеченииРейса = текМассаГруза;
			КонецЕсли;
			
			текОбъемГруза = текОбъемГруза + Выборка.ОбъемГруза;
			
			Если текОбъемГруза > МаксимальныйОбъемВТеченииРейса Тогда
				МаксимальныйОбъемВТеченииРейса = текОбъемГруза;
			КонецЕсли;
			
			текКоличествоМестГруза = текКоличествоМестГруза + Выборка.КоличествоМестГруза;
			
			Если текКоличествоМестГруза > МаксимальноеКоличествоМестВТеченииРейса Тогда
				МаксимальноеКоличествоМестВТеченииРейса = текКоличествоМестГруза;
			КонецЕсли;
			
			текКоличествоЗагрузочныхМетровГруза = текКоличествоЗагрузочныхМетровГруза + Выборка.КоличествоЗагрузочныхМетровГруза;
			
			Если текКоличествоЗагрузочныхМетровГруза > МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса Тогда
				МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса = текКоличествоЗагрузочныхМетровГруза;
			КонецЕсли;
			
			КоличествоТочекВМаршруте = КоличествоТочекВМаршруте + Выборка.КоличествоТочекВМаршруте;
			КоличествоТочекПогрузки  = КоличествоТочекПогрузки + Выборка.КоличествоТочекПогрузки;
			КоличествоТочекРазгрузки = КоличествоТочекРазгрузки + Выборка.КоличествоТочекРазгрузки;
			
		КонецЦикла;
		
		ВыборкаПараметрыРейса = мсвРезультатыЗапроса[ИндексЗапросаХаарктеристикиРейса].Выбрать();
		
		Если Истина
			И ВыборкаПараметрыРейса.Следующий() 
			И НЕ ВыборкаПараметрыРейса.ОценочнаяСтоимостьГруза = null 
		Тогда
			ЗаполнитьЗначенияСвойств(сткДанныеПоОбъектуРасчета, ВыборкаПараметрыРейса);
		КонецЕсли;
		
		сткДанныеПоОбъектуРасчета.Вставить("МассаГруза", МаксимальнаяМассаВТеченииРейса);
		сткДанныеПоОбъектуРасчета.Вставить("ОбъемГруза", МаксимальныйОбъемВТеченииРейса);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоМестГруза", МаксимальноеКоличествоМестВТеченииРейса);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоЗагрузочныхМетровГруза", МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоТочекВМаршруте", КоличествоТочекВМаршруте);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоТочекПогрузки", КоличествоТочекПогрузки);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоТочекРазгрузки", КоличествоТочекРазгрузки);
	КонецЕсли;
	
	Если НужныФактическиеРасходы Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(упРасходыОбороты.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
		               |ИЗ
		               |	РегистрНакопления.упРасходы.Обороты(
		               |			,
		               |			,
		               |			,
		               |			ЗаданиеНаПеревозку В
		               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |					упЗаданиеНаПеревозкуГруза.Ссылка
		               |				ИЗ
		               |					Документ.упРейс.Задания КАК упРейсЗадания
		               |						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		               |						ПО
		               |							упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза = упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза
		               |				ГДЕ
		               |					упРейсЗадания.Ссылка = &Рейс)) КАК упРасходыОбороты";
		Запрос.УстановитьПараметр("Рейс", сткДанныеПоОбъектуРасчета.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			сткДанныеПоОбъектуРасчета.Вставить("ФактическиеРасходыПоЗаявке", Выборка.СуммаФактСНДСОборот);
		КонецЕсли;
	КонецЕсли;
	
	Если НужныХарактеристикиТС Тогда
		ЗаполнитьЗначенияПоказателейХарактеристикТС_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета);
	КонецЕсли;		
	
	Если НуженОбъемныйВес Тогда
		ЗаполнитьОбъемныйВес(сткДанныеПоОбъектуРасчета);
	КонецЕсли;
	
	НоваяСтрока = тбзРезультат.Добавить();
	НоваяСтрока.СтруктураДанные = сткДанныеПоОбъектуРасчета; 
	
	Возврат тбзРезультат;	
		
КонецФункции

Процедура ЗаполнитьЗначенияПоказателейХарактеристикТС_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	текТранспортноеСредство = сткДанныеПоОбъектуРасчета.ТранспортноеСредство;
	текТипТС = сткДанныеПоОбъектуРасчета.ТипТС;
	текПеревозчик = сткДанныеПоОбъектуРасчета.Перевозчик;
	
	Если ЗначениеЗаполнено(текТранспортноеСредство) Тогда
		
		сткРеквизитыТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текТранспортноеСредство, "Грузоподъемность, Объем, КоличествоЗагрузочныхМетров, КоличествоМест, ТипТС.Грузоподъемность, ТипТС.Объем, ТипТС.КоличествоЗагрузочныхМетров, ТипТС.КоличествоМест");
		
		Если НЕ сткРеквизитыТС.Грузоподъемность = 0 Тогда
			сткДанныеПоОбъектуРасчета.Вставить("ГрузоподъемностьТС",сткРеквизитыТС.Грузоподъемность);		
		Иначе
			сткДанныеПоОбъектуРасчета.Вставить("ГрузоподъемностьТС",сткРеквизитыТС.ТипТСГрузоподъемность);		
		КонецЕсли;
		
		Если НЕ сткРеквизитыТС.Объем = 0 Тогда
			сткДанныеПоОбъектуРасчета.Вставить("ОбъемТС",сткРеквизитыТС.Объем);		
		Иначе
			сткДанныеПоОбъектуРасчета.Вставить("ОбъемТС",сткРеквизитыТС.ТипТСОбъем);		
		КонецЕсли;
		
		Если НЕ сткРеквизитыТС.КоличествоМест = 0 Тогда
			сткДанныеПоОбъектуРасчета.Вставить("КоличествоМестТС",сткРеквизитыТС.КоличествоМест);		
		Иначе
			сткДанныеПоОбъектуРасчета.Вставить("КоличествоМестТС",сткРеквизитыТС.ТипТСКоличествоМест);		
		КонецЕсли;
		
		
		Если НЕ сткРеквизитыТС.КоличествоЗагрузочныхМетров = 0 Тогда
			сткДанныеПоОбъектуРасчета.Вставить("КоличествоЗагрузочныхМетровТС",сткРеквизитыТС.КоличествоЗагрузочныхМетров);		
		Иначе
			сткДанныеПоОбъектуРасчета.Вставить("КоличествоЗагрузочныхМетровТС",сткРеквизитыТС.ТипТСКоличествоЗагрузочныхМетров);		
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(текТипТС) Тогда 
		сткРеквизитыТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текТипТС, "Грузоподъемность, Объем, КоличествоЗагрузочныхМетров, КоличествоМест");
				
		сткДанныеПоОбъектуРасчета.Вставить("ГрузоподъемностьТС", сткРеквизитыТС.Грузоподъемность);		
		сткДанныеПоОбъектуРасчета.Вставить("ОбъемТС", сткРеквизитыТС.Объем);		
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоМестТС", сткРеквизитыТС.КоличествоМест);		
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоЗагрузочныхМетровТС", сткРеквизитыТС.КоличествоЗагрузочныхМетров);		
	Иначе
		
		Условие = "";
		Порядок = "";
		Запрос = Новый Запрос;
		
		Если ЗначениеЗаполнено(текПеревозчик) Тогда
			Условие = "
			| И тбТС.Партнер = &Перевозчик";
			Запрос.УстановитьПараметр("Перевозчик", текПеревозчик);
		КонецЕсли;
		
		Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ГрузоподъемностьТС") Тогда
			Условие = Условие + "
			| И ВЫБОР
			|			КОГДА тбТС.Грузоподъемность = 0
			|				ТОГДА тбТС.ТипТС.Грузоподъемность
			|			ИНАЧЕ тбТС.Грузоподъемность
			|		КОНЕЦ >= &МассаГруза";
			Порядок	= Символы.ПС + "ГрузоподъемностьТС";
			Запрос.УстановитьПараметр("МассаГруза", сткДанныеПоОбъектуРасчета.МассаГруза);
		КонецЕсли;
		
		Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОбъемТС") Тогда
			Условие = Условие + "
			| И ВЫБОР
			|			КОГДА тбТС.Объем = 0
			|				ТОГДА тбТС.ТипТС.Объем
			|			ИНАЧЕ тбТС.Объем
			|		КОНЕЦ >= &ОбъемГруза";
			Порядок	= Порядок + ?(СтрДлина(Порядок) > 0, "," + Символы.ПС, "") + "ОбъемТС";
			Запрос.УстановитьПараметр("ОбъемГруза", сткДанныеПоОбъектуРасчета.ОбъемГруза);
		КонецЕсли;
		
		Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоЗагрузочныхМетровТС") Тогда
			Условие = Условие + "
			| И ВЫБОР
			|			КОГДА тбТС.КоличествоЗагрузочныхМетров = 0
			|				ТОГДА тбТС.ТипТС.КоличествоЗагрузочныхМетров
			|			ИНАЧЕ тбТС.КоличествоЗагрузочныхМетров
			|		КОНЕЦ >= &КоличествоЗагрузочныхМетровГруза";
			Порядок	= Порядок + ?(СтрДлина(Порядок) > 0, "," + Символы.ПС, "") + "КоличествоЗагрузочныхМетровТС";
			Запрос.УстановитьПараметр("КоличествоЗагрузочныхМетровГруза", сткДанныеПоОбъектуРасчета.КоличествоЗагрузочныхМетровГруза);
		КонецЕсли;
		
		Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоМестТС") Тогда
			Условие = Условие + "
			| И ВЫБОР
			|			КОГДА тбТС.КоличествоМест = 0
			|				ТОГДА тбТС.ТипТС.КоличествоМест
			|			ИНАЧЕ тбТС.КоличествоМест
			|		КОНЕЦ >= &КоличествоМестГруза";
			Порядок	= Порядок + ?(СтрДлина(Порядок) > 0, "," + Символы.ПС, "") + "КоличествоМестТС";
			Запрос.УстановитьПараметр("КоличествоМестГруза", сткДанныеПоОбъектуРасчета.КоличествоМестГруза);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	тбТС.Ссылка КАК ТранспортноеСредство,
		               |	ВЫБОР
		               |		КОГДА тбТС.Грузоподъемность = 0
		               |			ТОГДА тбТС.ТипТС.Грузоподъемность
		               |		ИНАЧЕ тбТС.Грузоподъемность
		               |	КОНЕЦ КАК ГрузоподъемностьТС,
		               |	ВЫБОР
		               |		КОГДА тбТС.Объем = 0
		               |			ТОГДА тбТС.ТипТС.Объем
		               |		ИНАЧЕ тбТС.Объем
		               |	КОНЕЦ КАК ОбъемТС,
		               |	ВЫБОР
		               |		КОГДА тбТС.КоличествоЗагрузочныхМетров = 0
		               |			ТОГДА тбТС.ТипТС.КоличествоЗагрузочныхМетров
		               |		ИНАЧЕ тбТС.КоличествоЗагрузочныхМетров
		               |	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
		               |	ВЫБОР
		               |		КОГДА тбТС.КоличествоМест = 0
		               |			ТОГДА тбТС.ТипТС.КоличествоМест
		               |		ИНАЧЕ тбТС.КоличествоМест
		               |	КОНЕЦ КАК КоличествоМестТС,
		               |	тбТС.ТипТС
		               |ИЗ
		               |	Справочник.упТранспортныеСредства КАК тбТС
		               |ГДЕ
		               |	НЕ тбТС.ПометкаУдаления" + Условие + ?(ЗначениеЗаполнено(Порядок),"
					   |УПОРЯДОЧИТЬ ПО " + Порядок, "");
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			сткДанныеПоОбъектуРасчета.Вставить("ТранспортноеСредство", Выборка.ТранспортноеСредство);
			сткДанныеПоОбъектуРасчета.Вставить("ТипТС", Выборка.ТипТС);
			сткХарактеристикиТС = СтруктураХарактеристикиТС();
			ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, Выборка);
			ЗаполнитьПоСтруктуре(сткДанныеПоОбъектуРасчета, сткХарактеристикиТС);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти	 

#Область РасчетПоРейсуБезДетализацииПоЗаданиям

Процедура ПодготовитьВходныеПараметры_Рейс_БезДетализации(Объект, ВходныеПараметры, сткТрассировка)
		
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "ПлановыеРасходы");
		
	Если ТипЗнч(Объект) =  Тип("ДанныеФормыСтруктура") Или ТипЗнч(Объект) = Тип("ДокументОбъект.упРейс") Тогда
		ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));		
	КонецЕсли;
	
	ПараметрыРейса = упРейс.ПараметрыРейса(Объект.Ссылка);
	
	Если Не ВходныеПараметры.Свойство("ГруппаТарифов") Тогда
		ВходныеПараметры.Вставить("ГруппаТарифов", ПараметрыРейса.ГруппаТарифов);	
	КонецЕсли;
	
	ВходныеПараметры.Вставить("Организация", Объект.Организация);
	ВходныеПараметры.Вставить("ВремяРаботыЧас", ПараметрыРейса.Время);
	ВходныеПараметры.Вставить("Расстояние", ПараметрыРейса.Расстояние);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", Объект.Ссылка);
	ВходныеПараметры.Вставить("Рейс", Объект.Ссылка);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ДополнительныеКолонкиПоСтроке", "");
	
	ВходныеПараметры.Вставить("ТранспортноеСредство", ПараметрыРейса.ТранспортноеСредство);
	Если Не ВходныеПараметры.Свойство("ТипТС") Тогда
		ВходныеПараметры.Вставить("ТипТС", ПараметрыРейса.ТипТС);
	КонецЕсли; 
	ВходныеПараметры.Вставить("Перевозчик", ПараметрыРейса.Перевозчик);
	ВходныеПараметры.Вставить("ЗонаОтправления", ПараметрыРейса.ЗонаПогрузки);
	ВходныеПараметры.Вставить("ЗонаПолучения", ПараметрыРейса.ЗонаРазгрузки);
	ВходныеПараметры.Вставить("ВидПеревозки", Объект.ВидПеревозки);
	
	ВходныеПараметры.Вставить("ГрузоподъемностьТС", 0);		
	ВходныеПараметры.Вставить("ОбъемТС", 0);		
	ВходныеПараметры.Вставить("КоличествоМестТС", 0);		
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровТС", 0);		
	
	ВходныеПараметры.Вставить("МассаГруза", ПараметрыРейса.Масса);	
	ВходныеПараметры.Вставить("ОбъемГруза", ПараметрыРейса.Объем);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", ПараметрыРейса.КоличествоМест);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", ПараметрыРейса.КоличествоЗагрузочныхМетров);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", '00010101');
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", ПараметрыРейса.СуммаИнкассации);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());
			
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	ПодготовитьПараметрыРасчета_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
	
КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета);
	
	Возврат тбзРезультат;
	
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом_Рейс_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	
	НужныПараметрыГруза = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныФактическиеРасходы = НужныФактическиеРасходы(мсвПараметрыРасчета);
	
	Если Ложь
		Или НужныПараметрыГруза
		Или НужныХарактеристикиТС 
	Тогда
				
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упМаршрутПоРейсуСрезПоследних.Идентификатор,
		               |	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочки,
		               |	упМаршрутПоРейсуСрезПоследних.ВремяОтПредыдущейТочки,
		               |	упМаршрутПоРейсуСрезПоследних.СтрокаНомер,
		               |	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПрибытиеПлан,
		               |	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК УбытиеПлан,
		               |	упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт,
		               |	упМаршрутПоРейсуСрезПоследних.УбытиеФакт
		               |ПОМЕСТИТЬ втМаршрут
		               |ИЗ
		               |	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
		               |ГДЕ
		               |	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упРаботыПоРейсуСрезПоследних.Идентификатор,
		               |	упРаботыПоРейсуСрезПоследних.Операция,
		               |	МИНИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		               |				ТОГДА втМаршрут.ПрибытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		               |		КОНЕЦ) КАК ВремяНачалаПогрузкиПлановое,
		               |	МИНИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)					   
		               |				ТОГДА втМаршрут.ПрибытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		               |		КОНЕЦ) КАК ВремяНачалаПогрузкиФактическое,
		               |	МАКСИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)					   
		               |				ТОГДА втМаршрут.УбытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		               |		КОНЕЦ) КАК ВремяОкончанияПогрузкиПлановое,
		               |	МАКСИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)					   
		               |				ТОГДА втМаршрут.УбытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		               |		КОНЕЦ) КАК ВремяОкончанияПогрузкиФактическое,
		               |	МИНИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)					   
		               |				ТОГДА втМаршрут.ПрибытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		               |		КОНЕЦ) КАК ВремяНачалаРазгрузкиПлановое,
		               |	МИНИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)					   					   
		               |				ТОГДА втМаршрут.ПрибытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		               |		КОНЕЦ) КАК ВремяНачалаРазгрузкиФактическое,
		               |	МАКСИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)					   					   
		               |				ТОГДА втМаршрут.УбытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		               |		КОНЕЦ) КАК ВремяОкончанияРазгрузкиПлановое,
		               |	МАКСИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
					   |					ИЛИ упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)					   					   
		               |				ТОГДА втМаршрут.УбытиеПлан
		               |			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		               |		КОНЕЦ) КАК ВремяОкончанияРазгрузкиФактическое
		               |ПОМЕСТИТЬ втРаботыПоРейсу
		               |ИЗ
		               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		               |			,
		               |			Рейс = &Рейс
		               |				И НЕ Отменена) КАК упРаботыПоРейсуСрезПоследних
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрут
		               |		ПО упРаботыПоРейсуСрезПоследних.Идентификатор = втМаршрут.Идентификатор
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упРаботыПоРейсуСрезПоследних.Идентификатор,
		               |	упРаботыПоРейсуСрезПоследних.Операция,
		               |	упРаботыПоРейсуСрезПоследних.Задание
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втРаботыПоРейсу.Идентификатор,
		               |	СУММА(ВЫБОР
		               |			КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
					   |					ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК ЭтоТочкаПогрузки,
		               |	СУММА(ВЫБОР
		               |			КОГДА втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
					   |					ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)					   
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК ЭтоТочкаРазгрузки
		               |ПОМЕСТИТЬ втГрузыВТочке
		               |ИЗ
		               |	втРаботыПоРейсу КАК втРаботыПоРейсу
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втРаботыПоРейсу.Идентификатор
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втМаршрут.РасстояниеОтПредыдущейТочки,
		               |	втМаршрут.ВремяОтПредыдущейТочки,
		               |	втМаршрут.Идентификатор,
		               |	ЕСТЬNULL(ВЫБОР
		               |			КОГДА втГрузыВТочке.ЭтоТочкаПогрузки > 0
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ, 0) КАК КоличествоТочекПогрузки,
		               |	ЕСТЬNULL(ВЫБОР
		               |			КОГДА втГрузыВТочке.ЭтоТочкаРазгрузки > 0
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ, 0) КАК КоличествоТочекРазгрузки,
		               |	1 КАК КоличествоТочекВМаршруте
		               |ИЗ
		               |	втМаршрут КАК втМаршрут
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втГрузыВТочке КАК втГрузыВТочке
		               |		ПО втМаршрут.Идентификатор = втГрузыВТочке.Идентификатор
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	втМаршрут.СтрокаНомер
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
		               |	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
		               |	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
		               |	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
		               |	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
		               |	МИНИМУМ(втРаботыПоРейсу.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
		               |	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
		               |	МАКСИМУМ(втРаботыПоРейсу.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое
		               |ИЗ
		               |	втРаботыПоРейсу КАК втРаботыПоРейсу";
		
		Запрос.УстановитьПараметр("Рейс", сткДанныеПоОбъектуРасчета.Ссылка);
		
		мсвРезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		Выборка = мсвРезультатыЗапроса[3].Выбрать();
		
		КоличествоТочекВМаршруте = 0;
		КоличествоТочекПогрузки 	= 0;
		КоличествоТочекРазгрузки = 0;
				
		Пока Выборка.Следующий() Цикл
			
			КоличествоТочекВМаршруте = КоличествоТочекВМаршруте + Выборка.КоличествоТочекВМаршруте;
			КоличествоТочекПогрузки = КоличествоТочекПогрузки + Выборка.КоличествоТочекПогрузки;
			КоличествоТочекРазгрузки = КоличествоТочекРазгрузки + Выборка.КоличествоТочекРазгрузки;
			
		КонецЦикла;
		
		ВыборкаПараметрыРейса = мсвРезультатыЗапроса[4].Выбрать();
		Если ВыборкаПараметрыРейса.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткДанныеПоОбъектуРасчета, ВыборкаПараметрыРейса);
		КонецЕсли;
		
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоТочекВМаршруте", КоличествоТочекВМаршруте);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоТочекПогрузки", КоличествоТочекПогрузки);
		сткДанныеПоОбъектуРасчета.Вставить("КоличествоТочекРазгрузки", КоличествоТочекРазгрузки);
	КонецЕсли;
	
	Если НужныХарактеристикиТС Тогда
		ЗаполнитьЗначенияПоказателейХарактеристикТС_Рейс(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета);
	КонецЕсли;	
	
	Если НужныФактическиеРасходы Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(упРасходыОбороты.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
		               |ИЗ
		               |	РегистрНакопления.упРасходы.Обороты(
		               |			,
		               |			,
		               |			,
		               |			ЗаданиеНаПеревозку В
		               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |					упЗаданиеНаПеревозкуГруза.Ссылка
		               |				ИЗ
		               |					Документ.упРейс.Задания КАК упРейсЗадания
		               |						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		               |						ПО
		               |							упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза = упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза
		               |				ГДЕ
		               |					упРейсЗадания.Ссылка = &Рейс)) КАК упРасходыОбороты";
		Запрос.УстановитьПараметр("Рейс", сткДанныеПоОбъектуРасчета.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			сткДанныеПоОбъектуРасчета.Вставить("ФактическиеРасходыПоЗаявке", Выборка.СуммаФактСНДСОборот);
		КонецЕсли;
	КонецЕсли;
	
	Если НуженОбъемныйВес Тогда
		ЗаполнитьОбъемныйВес(сткДанныеПоОбъектуРасчета);
	КонецЕсли;
	
	НоваяСтрока = тбзРезультат.Добавить();
	НоваяСтрока.СтруктураДанные = сткДанныеПоОбъектуРасчета; 
	
	Возврат тбзРезультат;	
		
КонецФункции

#КонецОбласти	

#Область РасчетПоСтрахованиюГруза

Процедура ПодготовитьВходныеПараметры_СтрахованиеГруза(Объект, ВходныеПараметры, сткТрассировка)
	
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "СтраховаяПремия");

	мсвСписокСтатей = ВходныеПараметры.СписокСтатей;
	Если Ложь
		Или ТипЗнч(Объект) =  Тип("ДанныеФормыСтруктура")
		Или ТипЗнч(Объект) = Тип("ДокументОбъект.упСтрахованиеГруза") Тогда
		ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));		
		текРейс = Объект["Рейс"];
	Иначе	
		текРейс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "Рейс");	
	КонецЕсли;
	
	// Так как перевозчик в рейсе может браться из подтверждения, то группа тарифов иногда передается.
	Если Не ВходныеПараметры.Свойство("ГруппаТарифов") Тогда
		ГруппаТарифов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГруппаТарифов");				  	
		ВходныеПараметры.Вставить("ГруппаТарифов", ГруппаТарифов);	
	КонецЕсли;
	
	ПараметрыРейса = упРейс.ПараметрыРейса(текРейс);
	сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текРейс, "Организация, ВидПеревозки");
	
	ВходныеПараметры.Вставить("Организация", сткРеквизитыРейса.Организация);
	ВходныеПараметры.Вставить("ВремяРаботыЧас", ПараметрыРейса.Время);
	ВходныеПараметры.Вставить("Расстояние", ПараметрыРейса.Расстояние);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", текРейс);
	ВходныеПараметры.Вставить("Рейс", текРейс);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ДополнительныеКолонкиПоСтроке", "Задание");
	
	ВходныеПараметры.Вставить("ТранспортноеСредство", ПараметрыРейса.ТранспортноеСредство);
	ВходныеПараметры.Вставить("ТипТС", ПараметрыРейса.ТипТС);
	ВходныеПараметры.Вставить("Перевозчик", ПараметрыРейса.Перевозчик);
	ВходныеПараметры.Вставить("ЗонаОтправления", ПараметрыРейса.ЗонаПогрузки);
	ВходныеПараметры.Вставить("ЗонаПолучения", ПараметрыРейса.ЗонаРазгрузки);
	ВходныеПараметры.Вставить("ВидПеревозки", сткРеквизитыРейса.ВидПеревозки);
	
	ВходныеПараметры.Вставить("ГрузоподъемностьТС", 0);		
	ВходныеПараметры.Вставить("ОбъемТС", 0);		
	ВходныеПараметры.Вставить("КоличествоМестТС", 0);		
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровТС", 0);		
	
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", '00010101');
	
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", ПараметрыРейса.СуммаИнкассации);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());
			
КонецПроцедуры

// ТаблицаДанныхДляРасчетаПоКаждойСтроке_СтрахованиеГруза 		- используется функция рейса.

// ТаблицаДанныхДляРасчетаПоДокументуВЦелом_СтрахованиеГруза 	- используется функция рейса.

#КонецОбласти	 

#Область РасчетПоФактическимРасходам

Процедура ПодготовитьВходныеПараметры_ФактическиеРасходы(Объект, ВходныеПараметры, сткТрассировка)

	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "Расходы");
	ГруппаТарифов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГруппаТарифов");				  
	ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));	
	
	ВходныеПараметры.Вставить("ПравилоРаспределения");
	
	ВходныеПараметры.Вставить("ГруппаТарифов", ГруппаТарифов);
	ВходныеПараметры.Вставить("Организация", Объект.Организация);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", Объект.Ссылка);
	ВходныеПараметры.Вставить("Рейс", Неопределено);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ХарактеристикиТС", Неопределено);
	ВходныеПараметры.Вставить("ВидПеревозки", Справочники.упВидыПеревозок.ПустаяСсылка());
		
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	
	ВходныеПараметры.Вставить("Расстояние", 0);	
	ВходныеПараметры.Вставить("ВремяРаботыЧас", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы",   0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", 0);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());
				
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
		
	мсвРейсы = Новый Массив;
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
	
		сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоСтроке", "Задание");
		сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоДокументу", "Задание");
		сткДанныеПоОбъектуРасчета.Вставить("Рейс", Объект.Рейс);
		
		мсвРейсы.Добавить(Объект.Рейс);
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда	
		
		сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоСтроке", "Рейс,Задание");
		сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоДокументу", "Рейс,Задание");
		
		Для каждого Строка Из Объект.Расходы Цикл
			Если мсвРейсы.Найти(Строка.Рейс) = Неопределено Тогда
				мсвРейсы.Добавить(Строка.Рейс);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		
		сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоСтроке", "Задание");
		сткДанныеПоОбъектуРасчета.Вставить("ДополнительныеКолонкиПоДокументу", "Задание");
		
		Для каждого Строка Из Объект.Рейсы Цикл
			Если мсвРейсы.Найти(Строка.Рейс) = Неопределено Тогда
				мсвРейсы.Добавить(Строка.Рейс);
			КонецЕсли;
		КонецЦикла;

		сткДанныеПоОбъектуРасчета.Вставить("Рейс", мсвРейсы);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса_ФактическиеРасходыОбщиеДанные();
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);

	мсвРезультаты = Запрос.ВыполнитьПакет();
	сткДанныеПоОбъектуРасчета.Вставить("Запрос", Запрос);
	
	ЗаполнитьЗначенияПоказателейЗоны_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
	
	ЗаполнитьЗначенияПоказателейХарактеристикТС_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);

КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	СпособВВодаРасходовПоНесколькимРейсам = Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам;
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	тбзРезультат.Колонки.Добавить("Задание");
	Если СпособВВодаРасходовПоНесколькимРейсам Тогда
		тбзРезультат.Колонки.Добавить("Рейс");
	КонецЕсли;

	НужныПараметрыГруза   = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);
	НуженОбъемныйВес      = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужныЗоны             = НужныЗоны(мсвПараметрыРасчета);
	НужныРасстояния       = НужныРасстояния(мсвПараметрыРасчета);
	НужныВремянныеПараметры  = НужныВремянныеПараметры(мсвПараметрыРасчета);
	НуженВидПеревозки        = НуженВидПеревозки(мсвПараметрыРасчета);
	
	Запрос = сткДанныеПоОбъектуРасчета.Запрос;
			
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Идентификатор КАК Идентификатор,
	|	втМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрут.Прибытие КАК Прибытие,
	|	втМаршрут.Убытие КАК Убытие,
	|	втМаршрут.ПрибытиеПлан КАК ПрибытиеПлан,
	|	втМаршрут.УбытиеПлан КАК УбытиеПлан,
	|	втМаршрут.РасстояниеОтПредыдущейточки КАК РасстояниеОтПредыдущейточки,
	|	втРаботы.Операция КАК Операция,
	|	втРаботы.Задание КАК Объект,
	|	втМаршрут.Рейс КАК Рейс
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК втРаботы
	|	ПО ИСТИНА
	|		И втМаршрут.Рейс = втРаботы.Рейс
	|		И втМаршрут.Идентификатор = втРаботы.Идентификатор
	|		И втРаботы.Выполнена
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.СтрокаНомер
	|ИТОГИ
	|	МАКСИМУМ(Прибытие),
	|	МАКСИМУМ(Убытие),
	|	МАКСИМУМ(ПрибытиеПлан),
	|	МАКСИМУМ(УбытиеПлан),
	|	МАКСИМУМ(РасстояниеОтПредыдущейточки)
	|ПО
	|	Рейс,
	|	СтрокаНомер
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеЗадания.Задание КАК Задание,
	|	втВыполненныеЗадания.МассаГруза КАК МассаГруза,
	|	втВыполненныеЗадания.ОбъемГруза КАК ОбъемГруза,
	|	втВыполненныеЗадания.КоличествоЗагрузочныхМетровГруза КАК КоличествоЗагрузочныхМетровГруза,
	|	втВыполненныеЗадания.КоличествоМестГруза КАК КоличествоМестГруза,
	|	втВыполненныеЗадания.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	втВыполненныеЗадания.ВидПеревозки КАК ВидПеревозки,
	|	втВыполненныеЗадания.Рейс КАК Рейс,
	|	ЕСТЬNULL(втПростойПоЗаданиямРейсам.ВремяПростояПоВинеЗаказчикаЧасы, 0) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	ЕСТЬNULL(втПростойПоЗаданиямРейсам.ВремяПростояПоВинеПеревозчикаЧасы, 0) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	втВыполненныеЗадания.ВремяОтправленияСПлановое КАК ВремяОтправленияСПлановое,
	|	втВыполненныеЗадания.ВремяОтправленияПоПлановое КАК ВремяОтправленияПоПлановое,
	|	втВыполненныеЗадания.ВремяПолученияСПлановое КАК ВремяПолученияСПлановое,
	|	втВыполненныеЗадания.ВремяПолученияПоПлановое КАК ВремяПолученияПоПлановое,
	|	ЕСТЬNULL(втКоличествоТочек.КоличествоТочекВМаршруте, 0) КАК КоличествоТочекВМаршруте,
	|	ЕСТЬNULL(втКоличествоТочек.КоличествоТочекПогрузки, 0) КАК КоличествоТочекПогрузки,
	|	ЕСТЬNULL(втКоличествоТочек.КоличествоТочекРазгрузки, 0) КАК КоличествоТочекРазгрузки,
	|	втВыполненныеЗадания.ЗонаОтправления КАК ЗонаОтправления,
	|	втВыполненныеЗадания.ЗонаПолучения КАК ЗонаПолучения,
	|	втВыполненныеЗадания.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ИЗ
	|	втВыполненныеЗадания КАК втВыполненныеЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПростойПоЗаданиямРейсам КАК втПростойПоЗаданиямРейсам
	|	ПО ИСТИНА
	|		И втВыполненныеЗадания.Рейс = втПростойПоЗаданиямРейсам.Рейс
	|		И втВыполненныеЗадания.Задание = втПростойПоЗаданиямРейсам.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТочекПоЗаданиям2 КАК втКоличествоТочек
	|	ПО ИСТИНА
	|		И втВыполненныеЗадания.Задание = втКоличествоТочек.Задание
	|		И втВыполненныеЗадания.Рейс = втКоличествоТочек.Рейс
	|";
				   
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Ложь
		Или НужныРасстояния 
		Или НужныВремянныеПараметры
	Тогда
		тбзРасстоянияПоРейсам  = ТаблицаРасстоянийИЗатраченногоВремениПоРейсам(мсвРезультатЗапроса[0].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам),,Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка());
	КонецЕсли;
    								
	Выборка = мсвРезультатЗапроса[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		НоваяСтрока.СтруктураДанные.Вставить("ОбъектРасчета",	Новый Структура("Имя, Значение","Задание",Выборка.Задание));  
				
		текЗадание = Выборка.Задание;
		НоваяСтрока.Задание = текЗадание;
		
		текРейс = Выборка.Рейс;
		Если СпособВВодаРасходовПоНесколькимРейсам Тогда
			НоваяСтрока.Рейс = текРейс;
			НоваяСтрока.СтруктураДанные.Вставить("Рейс", текРейс);  
		КонецЕсли; 
			
		ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, Выборка);
							
		Если НужныРасстояния ИЛИ НужныВремянныеПараметры Тогда
			Если СпособВВодаРасходовПоНесколькимРейсам Тогда
				мсвСтроки = тбзРасстоянияПоРейсам.НайтиСтроки(Новый Структура("Рейс, Объект", текРейс, текЗадание));
				Если мсвСтроки.Количество()>0 Тогда
					ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, мсвСтроки[0]);
				КонецЕсли;
			Иначе
				мсвСтроки = тбзРасстоянияПоРейсам.НайтиСтроки(Новый Структура("Объект", текЗадание));
				текРасстояние     = 0;
				текВремяРаботыЧас = 0;
				
				текВремяНачалаПогрузкиПлановое     = '39991231';
				текВремяНачалаПогрузкиФактическое  = '39991231';
				текВремяНачалаРазгрузкиПлановое    = '39991231';
				текВремяНачалаРазгрузкиФактическое = '39991231';
				текВремяОкончанияПогрузкиПлановое     = '00010101';
				текВремяОкончанияПогрузкиФактическое  = '00010101';
				текВремяОкончанияРазгрузкиПлановое    = '00010101';
				текВремяОкончанияРазгрузкиФактическое = '00010101';
				
				// Так как одно и то же задание может быть в нескольких рейсах, то просуммируем,
				//	расстояния и время по ним (это для обобщенных расходов по нескольким рейсам).
				Для каждого ЭлементСтрока Из мсвСтроки Цикл
					текРасстояние = текРасстояние + ЭлементСтрока.Расстояние;
					текВремяРаботыЧас = текВремяРаботыЧас + ЭлементСтрока.ВремяРаботыЧас;
					Если ЭлементСтрока.ВремяНачалаПогрузкиПлановое < текВремяНачалаПогрузкиПлановое Тогда
						текВремяНачалаПогрузкиПлановое = ЭлементСтрока.ВремяНачалаПогрузкиПлановое;
					КонецЕсли;
					Если ЭлементСтрока.ВремяНачалаПогрузкиФактическое < текВремяНачалаПогрузкиФактическое Тогда
						текВремяНачалаПогрузкиФактическое = ЭлементСтрока.ВремяНачалаПогрузкиФактическое;
					КонецЕсли;

					Если ЭлементСтрока.ВремяНачалаРазгрузкиПлановое < текВремяНачалаРазгрузкиПлановое Тогда
						текВремяНачалаРазгрузкиПлановое = ЭлементСтрока.ВремяНачалаРазгрузкиПлановое;
					КонецЕсли;

					Если ЭлементСтрока.ВремяНачалаРазгрузкиФактическое < текВремяНачалаРазгрузкиФактическое Тогда
						текВремяНачалаРазгрузкиФактическое = ЭлементСтрока.ВремяНачалаРазгрузкиФактическое;
					КонецЕсли;

					Если ЭлементСтрока.ВремяОкончанияПогрузкиПлановое > текВремяОкончанияПогрузкиПлановое Тогда
						текВремяОкончанияПогрузкиПлановое = ЭлементСтрока.ВремяОкончанияПогрузкиПлановое;
					КонецЕсли;
					Если ЭлементСтрока.ВремяОкончанияПогрузкиФактическое > текВремяОкончанияПогрузкиФактическое Тогда
						текВремяОкончанияПогрузкиФактическое = ЭлементСтрока.ВремяОкончанияПогрузкиФактическое;
					КонецЕсли;

					Если ЭлементСтрока.ВремяОкончанияРазгрузкиПлановое > текВремяОкончанияРазгрузкиПлановое Тогда
						текВремяОкончанияРазгрузкиПлановое = ЭлементСтрока.ВремяОкончанияРазгрузкиПлановое;
					КонецЕсли;

					Если ЭлементСтрока.ВремяОкончанияРазгрузкиФактическое > текВремяОкончанияРазгрузкиФактическое Тогда
						текВремяОкончанияРазгрузкиФактическое = ЭлементСтрока.ВремяОкончанияРазгрузкиФактическое;
					КонецЕсли;

				КонецЦикла;
				НоваяСтрока.СтруктураДанные.Вставить("Расстояние", текРасстояние);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяРаботыЧас", текВремяРаботыЧас);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяНачалаПогрузкиПлановое",        текВремяНачалаПогрузкиПлановое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяНачалаПогрузкиФактическое",     текВремяНачалаПогрузкиФактическое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяНачалаРазгрузкиПлановое",       текВремяНачалаРазгрузкиПлановое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяНачалаРазгрузкиФактическое",    текВремяНачалаРазгрузкиФактическое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяОкончанияПогрузкиПлановое",     текВремяОкончанияПогрузкиПлановое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяОкончанияПогрузкиФактическое",  текВремяОкончанияПогрузкиФактическое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяОкончанияРазгрузкиПлановое",    текВремяОкончанияРазгрузкиПлановое);
				НоваяСтрока.СтруктураДанные.Вставить("ВремяОкончанияРазгрузкиФактическое", текВремяОкончанияРазгрузкиФактическое);
			КонецЕсли;
		КонецЕсли;
			
		Если Ложь
			Или НужныХарактеристикиТС 
			Или НуженВидПеревозки 
		Тогда
			сткХарактеристикиТС	= сткДанныеПоОбъектуРасчета.ХарактеристикиТС.Получить(текРейс);
			Если НЕ сткХарактеристикиТС = Неопределено Тогда
				ЗаполнитьПоСтруктуре(НоваяСтрока.СтруктураДанные, сткХарактеристикиТС);
				Если НуженОбъемныйВес Тогда
					ЗаполнитьОбъемныйВес(НоваяСтрока.СтруктураДанные);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;  
			
	Возврат тбзРезультат;
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	СпособВВодаРасходовОбощенныеПоНесколькимРейсам	= Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам;
	СпособВводаРасходовПоНесколькимРейсам			= Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам;
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");
	тбзРезультат.Колонки.Добавить("Задание");
	тбзРезультат.Колонки.Добавить("Рейс");
		
	НужныПараметрыГруза   = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);		
	НуженОбъемныйВес      = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужнаОценочнаяСтоимость = НужнаОценочнаяСтоимость(мсвПараметрыРасчета);
	НужныЗоны               = НужныЗоны(мсвПараметрыРасчета);
	НужныРасстояния         = НужныРасстояния(мсвПараметрыРасчета);
	НужныФактическиеРасходы = НужныФактическиеРасходы(мсвПараметрыРасчета);
	НужнаПлановаяСуммаИнкассации = НужнаПлановаяСуммаИнкассации(мсвПараметрыРасчета);
	НуженВидПеревозки     = НуженВидПеревозки(мсвПараметрыРасчета);
	НужныКоличествоЗаданий = НужныКоличествоЗаданий(мсвПараметрыРасчета);
	тбзХарактеристикиПоЗаданиям = Новый ТаблицаЗначений;     
	мсвТипы = Новый Массив;
	мсвТипы.Добавить(Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	мсвТипы.Добавить(Тип("ДокументСсылка.упРейс"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("Задание", Новый ОписаниеТипов(мсвТипы));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("Рейс",    Новый ОписаниеТипов("ДокументСсылка.упРейс"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("МассаГруза", Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("МассаГрузаМаксимальная", Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ОбъемГруза",             Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ОбъемГрузаМаксимальный", Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("КоличествоЗагрузочныхМетровГруза",             Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("КоличествоЗагрузочныхМетровГрузаМаксимальное", Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("КоличествоМестГруза",               Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("КоличествоМестГрузаМаксимальное",   Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("Расстояние",                        Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ОценочнаяСтоимостьГруза",           Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ВремяРаботыЧас",                    Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ВремяПростояПоВинеЗаказчикаЧасы",   Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ВремяПростояПоВинеПеревозчикаЧасы", Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ФактическиеРасходыПоЗаявке",        Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("ПлановаяСуммаИнкассации",           Новый ОписаниеТипов("Число"));
	тбзХарактеристикиПоЗаданиям.Колонки.Добавить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", Новый ОписаниеТипов("Число"));
		
	Запрос = сткДанныеПоОбъектуРасчета.Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Идентификатор КАК Идентификатор,
	|	втМаршрут.Рейс КАК Рейс,
	|	втМаршрут.Прибытие КАК Прибытие,
	|	втМаршрут.Убытие КАК Убытие,
	|	втМаршрут.ПрибытиеПлан КАК ПрибытиеПлан,
	|	втМаршрут.УбытиеПлан КАК УбытиеПлан,
	|	втМаршрут.РасстояниеОтПредыдущейточки КАК РасстояниеОтПредыдущейточки,
	|	втМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	втРаботы.Задание КАК Объект,
	|	втРаботы.Операция КАК Операция
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК втРаботы
	|	ПО ИСТИНА
	|		И втМаршрут.Идентификатор = втРаботы.Идентификатор
	|		И втРаботы.Выполнена
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.СтрокаНомер
	|ИТОГИ
	|	МАКСИМУМ(Прибытие),
	|	МАКСИМУМ(Убытие),
	|	МАКСИМУМ(ПрибытиеПлан),
	|	МАКСИМУМ(УбытиеПлан),
	|	МАКСИМУМ(РасстояниеОтПредыдущейточки)
	|ПО
	|	Рейс,
	|	СтрокаНомер
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Идентификатор КАК Идентификатор,
	|	втМаршрут.Рейс КАК Рейс,
	|	ЕСТЬNULL(втЗаданияВТочке.МассаГруза, 0) КАК МассаГруза,
	|	ЕСТЬNULL(втЗаданияВТочке.ОбъемГруза, 0) КАК ОбъемГруза,
	|	ЕСТЬNULL(втЗаданияВТочке.КоличествоЗагрузочныхМетровГруза, 0) КАК КоличествоЗагрузочныхМетровГруза,
	|	ЕСТЬNULL(втЗаданияВТочке.КоличествоМестГруза, 0) КАК КоличествоМестГруза,
	|	втМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрут.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	втМаршрут.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	втЗаданияВТочке.Задание КАК Объект,
	|	втЗаданияВТочке.Операция КАК Операция
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЗаданияВТочке КАК втЗаданияВТочке
	|	ПО втМаршрут.Идентификатор = втЗаданияВТочке.Идентификатор
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.СтрокаНомер
	|ИТОГИ
	|	МАКСИМУМ(ВремяПростояПоВинеЗаказчикаЧасы),
	|	МАКСИМУМ(ВремяПростояПоВинеПеревозчикаЧасы)
	|ПО
	|	Рейс,
	|	СтрокаНомер
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втРаботыПоРейсу.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	втРаботыПоРейсу.Задание КАК Задание,
	|	втРаботыПоРейсу.Рейс КАК Рейс
	|ИЗ
	|	втВыполненныеРаботы КАК втРаботыПоРейсу
	|ГДЕ ЛОЖЬ
	|	ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ втРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботыПоРейсу.Задание,
	|	втРаботыПоРейсу.Рейс
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСуммыПоЗаявкам.Рейс КАК Рейс,
	|	втСуммыПоЗаявкам.Задание КАК Задание,
	|	втСуммыПоЗаявкам.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте,
	|	втСуммыПоЗаявкам.ПлановаяСуммаИнкассации КАК ПлановаяСуммаИнкассации,
	|	втСуммыПоЗаявкам.ФактическиеРасходыПоЗаявке КАК ФактическиеРасходыПоЗаявке
	|ИЗ
	|	втСуммыПоЗаявкам КАК втСуммыПоЗаявкам
	|";
				   
	
	мсвРезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	тбзРасстоянияПоРейсам  = ТаблицаРасстоянийИЗатраченногоВремениПоРейсам(мсвРезультатыЗапроса[0].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам), Истина, Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка());
	тбзСуммыПоЗаявкам      = мсвРезультатыЗапроса[3].Выгрузить();
	тбзОценочнаяСтоимость  = мсвРезультатыЗапроса[2].Выгрузить();
	
	ВыборкаРейс = мсвРезультатыЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРейс.Следующий() Цикл
		
		текРейс = ВыборкаРейс.Рейс;	
		
		текОценочнаяСтоимостьГрузаПоРейсу = 0;
		
		текМассаГруза = 0;
		МаксимальнаяМассаВТеченииРейса = 0;
		
		текОбъемГруза = 0;
		МаксимальныйОбъемВТеченииРейса = 0;
		
		текКоличествоМестГруза = 0;
		МаксимальноеКоличествоМестВТеченииРейса	= 0;
		
		текКоличествоЗагрузочныхМетровГруза = 0;
		МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса = 0;
		
		ВыборкаТочка = ВыборкаРейс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		текВремяПростояПоВинеЗаказчикаЧасыПоРейсу   = 0;
		текВремяПростояПоВинеПеревозчикаЧасыПоРейсу = 0;
					
		Пока ВыборкаТочка.Следующий() Цикл
			
			текВремяПростояПоВинеЗаказчикаЧасыПоРейсу	= текВремяПростояПоВинеЗаказчикаЧасыПоРейсу + ВыборкаТочка.ВремяПростояПоВинеЗаказчикаЧасы;
			текВремяПростояПоВинеПеревозчикаЧасыПоРейсу	= текВремяПростояПоВинеПеревозчикаЧасыПоРейсу + ВыборкаТочка.ВремяПростояПоВинеПеревозчикаЧасы;
			
			Выборка = ВыборкаТочка.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				текЗадание = Выборка.Объект;
				Если ЗначениеЗаполнено(текЗадание)  Тогда
								
					мсвСтрокиПоЗаданию = тбзХарактеристикиПоЗаданиям.НайтиСтроки(Новый Структура("Рейс, Задание",текРейс, текЗадание));
					Если мсвСтрокиПоЗаданию.Количество()>0 Тогда
						СтрокаПоЗаданию = мсвСтрокиПоЗаданию[0];
					Иначе   
						СтрокаПоЗаданию = тбзХарактеристикиПоЗаданиям.Добавить();
					КонецЕсли;
					
					СтрокаПоЗаданию.Рейс    = текРейс;
					СтрокаПоЗаданию.Задание = текЗадание;
					СтрокаПоЗаданию.ВремяПростояПоВинеЗаказчикаЧасы   = Выборка.ВремяПростояПоВинеЗаказчикаЧасы;
					СтрокаПоЗаданию.ВремяПростояПоВинеПеревозчикаЧасы = Выборка.ВремяПростояПоВинеПеревозчикаЧасы;
					
					текМассаГруза = текМассаГруза + Выборка.МассаГруза;
					
					Если текМассаГруза > МаксимальнаяМассаВТеченииРейса Тогда
						МаксимальнаяМассаВТеченииРейса = текМассаГруза;
					КонецЕсли;
					
					СтрокаПоЗаданию.МассаГруза = СтрокаПоЗаданию.МассаГруза + Выборка.МассаГруза;
					Если СтрокаПоЗаданию.МассаГруза > СтрокаПоЗаданию.МассаГрузаМаксимальная Тогда
						СтрокаПоЗаданию.МассаГрузаМаксимальная = СтрокаПоЗаданию.МассаГруза;
					КонецЕсли;
					
					текОбъемГруза = текОбъемГруза + Выборка.ОбъемГруза;
					
					Если текОбъемГруза > МаксимальныйОбъемВТеченииРейса Тогда
						МаксимальныйОбъемВТеченииРейса = текОбъемГруза;
					КонецЕсли;
					
					СтрокаПоЗаданию.ОбъемГруза = СтрокаПоЗаданию.ОбъемГруза + Выборка.ОбъемГруза ;
					Если СтрокаПоЗаданию.ОбъемГруза > СтрокаПоЗаданию.ОбъемГрузаМаксимальный Тогда
						СтрокаПоЗаданию.ОбъемГрузаМаксимальный = СтрокаПоЗаданию.ОбъемГруза;
					КонецЕсли;
					
					текКоличествоМестГруза = текКоличествоМестГруза + Выборка.КоличествоМестГруза;
					
					Если текКоличествоМестГруза > МаксимальноеКоличествоМестВТеченииРейса Тогда
						МаксимальноеКоличествоМестВТеченииРейса = текКоличествоМестГруза;
					КонецЕсли;
					
					СтрокаПоЗаданию.КоличествоМестГруза = СтрокаПоЗаданию.КоличествоМестГруза + Выборка.КоличествоМестГруза;
					Если СтрокаПоЗаданию.КоличествоМестГруза > СтрокаПоЗаданию.КоличествоМестГрузаМаксимальное Тогда
						СтрокаПоЗаданию.КоличествоМестГрузаМаксимальное = СтрокаПоЗаданию.КоличествоМестГруза;
					КонецЕсли;
					
					текКоличествоЗагрузочныхМетровГруза = текКоличествоЗагрузочныхМетровГруза + Выборка.КоличествоЗагрузочныхМетровГруза;
					
					Если текКоличествоЗагрузочныхМетровГруза > МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса Тогда
						МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса = текКоличествоЗагрузочныхМетровГруза;
					КонецЕсли;
					
					СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГруза 	= СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГруза + Выборка.КоличествоЗагрузочныхМетровГруза;
					Если СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГруза > СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГрузаМаксимальное Тогда
						СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГрузаМаксимальное = СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГруза;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
			
		КонецЦикла;
		
		тбзХарактеристикиПоЗаданиям.Свернуть("Рейс, Задание", "МассаГруза, 
												    |ОбъемГруза, 
												    |КоличествоМестГруза, 
												    |КоличествоЗагрузочныхМетровГруза, 
												    |Расстояние, 
												    |ВремяРаботыЧас, 
												    |МассаГрузаМаксимальная,
												    |ОбъемГрузаМаксимальный,
												    |КоличествоМестГрузаМаксимальное,
												    |КоличествоЗагрузочныхМетровГрузаМаксимальное,
												    |ОценочнаяСтоимостьГруза,
												    |ВремяПростояПоВинеЗаказчикаЧасы,
												    |ВремяПростояПоВинеПеревозчикаЧасы,
												    |ФактическиеРасходыПоЗаявке,
												    |ПлановаяСуммаИнкассации,
												    |КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте");
					
		Если НужнаОценочнаяСтоимость Тогда
			мсвСтрокиСтоимость	= тбзОценочнаяСтоимость.НайтиСтроки(Новый Структура("Рейс",текРейс));
			Если мсвСтрокиСтоимость.Количество()>0 Тогда
				Для каждого СтрокаСтоимость Из мсвСтрокиСтоимость Цикл
					текОценочнаяСтоимостьГруза = СтрокаСтоимость.ОценочнаяСтоимостьГруза;
					текЗадание = СтрокаСтоимость.Задание;
					мсвСтрокиХарактеристики = тбзХарактеристикиПоЗаданиям.НайтиСтроки(Новый Структура("Рейс, Задание", текРейс, текЗадание));
					Для каждого СтрокаХарактеристики Из мсвСтрокиХарактеристики Цикл
						СтрокаХарактеристики.ОценочнаяСтоимостьГруза	= текОценочнаяСтоимостьГруза;
					КонецЦикла;
					текОценочнаяСтоимостьГрузаПоРейсу = текОценочнаяСтоимостьГрузаПоРейсу + текОценочнаяСтоимостьГруза;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте = 0;
		
		Если НужныКоличествоЗаданий Тогда
			мсвСтрокиПоРейсу = тбзСуммыПоЗаявкам.НайтиСтроки(Новый Структура("Рейс", текРейс));
			
			Для каждого СтрокаЗадание Из мсвСтрокиПоРейсу Цикл
				
				мсвСтрокиПоЗаданиям = тбзХарактеристикиПоЗаданиям.НайтиСтроки(Новый Структура("Рейс, Задание", текРейс, СтрокаЗадание.Задание));
				
				Если мсвСтрокиПоЗаданиям.Количество() > 0 Тогда
					КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте = КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте + СтрокаЗадание.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте;
					мсвСтрокиПоЗаданиям[0].КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте = СтрокаЗадание.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;

		 		
		// Для хранения общих параметров по рейсу добавляется строка с пустым заданием.
		СтрокаПоЗаданию = тбзХарактеристикиПоЗаданиям.Добавить();
		СтрокаПоЗаданию.Рейс = текРейс;
		СтрокаПоЗаданию.Задание = Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка();
		СтрокаПоЗаданию.МассаГрузаМаксимальная            = МаксимальнаяМассаВТеченииРейса;
		СтрокаПоЗаданию.ОбъемГрузаМаксимальный            = МаксимальныйОбъемВТеченииРейса;
		СтрокаПоЗаданию.КоличествоМестГрузаМаксимальное   = МаксимальноеКоличествоЗагрузочныхМетровВТеченииРейса;
		СтрокаПоЗаданию.КоличествоЗагрузочныхМетровГрузаМаксимальное = МаксимальноеКоличествоМестВТеченииРейса;
		СтрокаПоЗаданию.ОценочнаяСтоимостьГруза           = текОценочнаяСтоимостьГрузаПоРейсу;
		СтрокаПоЗаданию.ВремяПростояПоВинеЗаказчикаЧасы   = текВремяПростояПоВинеЗаказчикаЧасыПоРейсу;
		СтрокаПоЗаданию.ВремяПростояПоВинеПеревозчикаЧасы = текВремяПростояПоВинеПеревозчикаЧасыПоРейсу;
		СтрокаПоЗаданию.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте = КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте;
						
	КонецЦикла;  
	
	
	Если Ложь
		Или НужныФактическиеРасходы 
		Или НужнаПлановаяСуммаИнкассации 
	Тогда
		
		Для каждого СтрокаЗаявка Из тбзСуммыПоЗаявкам Цикл
		
			мсвСтрокиПоЗаданиям = тбзХарактеристикиПоЗаданиям.НайтиСтроки(Новый Структура("Рейс, Задание", СтрокаЗаявка.Рейс, СтрокаЗаявка.Задание));
			
			Если мсвСтрокиПоЗаданиям.Количество() > 0 Тогда
				мсвСтрокиПоЗаданиям[0].ФактическиеРасходыПоЗаявке = СтрокаЗаявка.ФактическиеРасходыПоЗаявке;
				мсвСтрокиПоЗаданиям[0].ПлановаяСуммаИнкассации    = СтрокаЗаявка.ПлановаяСуммаИнкассации;
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЕсли;
	
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбХарактеристикиПоЗаданиям.Задание КАК Задание,
	|	тбХарактеристикиПоЗаданиям.Рейс КАК Рейс,
	|	тбХарактеристикиПоЗаданиям.МассаГрузаМаксимальная КАК МассаГрузаМаксимальная,
	|	тбХарактеристикиПоЗаданиям.ОбъемГрузаМаксимальный КАК ОбъемГрузаМаксимальный,
	|	тбХарактеристикиПоЗаданиям.КоличествоМестГрузаМаксимальное КАК КоличествоМестГрузаМаксимальное,
	|	тбХарактеристикиПоЗаданиям.КоличествоЗагрузочныхМетровГрузаМаксимальное КАК КоличествоЗагрузочныхМетровГрузаМаксимальное,
	|	тбХарактеристикиПоЗаданиям.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	тбХарактеристикиПоЗаданиям.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	тбХарактеристикиПоЗаданиям.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	тбХарактеристикиПоЗаданиям.ФактическиеРасходыПоЗаявке КАК ФактическиеРасходыПоЗаявке,
	|	тбХарактеристикиПоЗаданиям.ПлановаяСуммаИнкассации КАК ПлановаяСуммаИнкассации,
	|	тбХарактеристикиПоЗаданиям.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ПОМЕСТИТЬ втХарактеристикиПоЗаданиям
	|ИЗ
	|	&тбХарактеристикиПоЗаданиям КАК тбХарактеристикиПоЗаданиям
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРасстояния.Объект КАК Задание,
	|	тбРасстояния.Рейс КАК Рейс,
	|	тбРасстояния.Расстояние КАК Расстояние,
	|	тбРасстояния.ВремяРаботыЧас КАК ВремяРаботыЧас,
	|	тбРасстояния.ВремяНачалаПогрузкиПлановое КАК ВремяНачалаПогрузкиПлановое,
	|	тбРасстояния.ВремяНачалаПогрузкиФактическое КАК ВремяНачалаПогрузкиФактическое,
	|	тбРасстояния.ВремяНачалаРазгрузкиПлановое КАК ВремяНачалаРазгрузкиПлановое,
	|	тбРасстояния.ВремяНачалаРазгрузкиФактическое КАК ВремяНачалаРазгрузкиФактическое,
	|	тбРасстояния.ВремяОкончанияПогрузкиПлановое КАК ВремяОкончанияПогрузкиПлановое,
	|	тбРасстояния.ВремяОкончанияПогрузкиФактическое КАК ВремяОкончанияПогрузкиФактическое,
	|	тбРасстояния.ВремяОкончанияРазгрузкиПлановое КАК ВремяОкончанияРазгрузкиПлановое,
	|	тбРасстояния.ВремяОкончанияРазгрузкиФактическое КАК ВремяОкончанияРазгрузкиФактическое
	|ПОМЕСТИТЬ втРасстояния
	|ИЗ
	|	&тбРасстояния КАК тбРасстояния
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втХарактеристикиПоЗаданиям.Задание КАК Задание,
	|	втХарактеристикиПоЗаданиям.Рейс КАК Рейс,
	|	втХарактеристикиПоЗаданиям.МассаГрузаМаксимальная КАК МассаГруза,
	|	втХарактеристикиПоЗаданиям.ОбъемГрузаМаксимальный КАК ОбъемГруза,
	|	втХарактеристикиПоЗаданиям.КоличествоМестГрузаМаксимальное КАК КоличествоМестГруза,
	|	втХарактеристикиПоЗаданиям.КоличествоЗагрузочныхМетровГрузаМаксимальное КАК КоличествоЗагрузочныхМетровГруза,
	|	втХарактеристикиПоЗаданиям.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	втХарактеристикиПоЗаданиям.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	втХарактеристикиПоЗаданиям.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	втХарактеристикиПоЗаданиям.ФактическиеРасходыПоЗаявке КАК ФактическиеРасходыПоЗаявке,
	|	втХарактеристикиПоЗаданиям.ПлановаяСуммаИнкассации КАК ПлановаяСуммаИнкассации,
	|	втХарактеристикиПоЗаданиям.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте,
	|	ЕСТЬNULL(втРасстояния.Расстояние, 0) КАК Расстояние,
	|	ЕСТЬNULL(втРасстояния.ВремяРаботыЧас, 0) КАК ВремяРаботыЧас,
	|	ЕСТЬNULL(втРасстояния.ВремяНачалаПогрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаПогрузкиПлановое,
	|	ЕСТЬNULL(втРасстояния.ВремяНачалаПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаПогрузкиФактическое,
	|	ЕСТЬNULL(втРасстояния.ВремяНачалаРазгрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаРазгрузкиПлановое,
	|	ЕСТЬNULL(втРасстояния.ВремяНачалаРазгрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяНачалаРазгрузкиФактическое,
	|	ЕСТЬNULL(втРасстояния.ВремяОкончанияПогрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияПогрузкиПлановое,
	|	ЕСТЬNULL(втРасстояния.ВремяОкончанияПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияПогрузкиФактическое,
	|	ЕСТЬNULL(втРасстояния.ВремяОкончанияРазгрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияРазгрузкиПлановое,
	|	ЕСТЬNULL(втРасстояния.ВремяОкончанияРазгрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОкончанияРазгрузкиФактическое
	|ПОМЕСТИТЬ втРасстояниеИВремя
	|ИЗ
	|	втХарактеристикиПоЗаданиям КАК втХарактеристикиПоЗаданиям
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРасстояния КАК втРасстояния
	|	ПО ИСТИНА
	|		И втХарактеристикиПоЗаданиям.Задание = втРасстояния.Задание
	|		И втХарактеристикиПоЗаданиям.Рейс = втРасстояния.Рейс
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасстояниеИВремя.Задание КАК Задание,
	|	втРасстояниеИВремя.Рейс КАК Рейс,
	|	СУММА(втРасстояниеИВремя.МассаГруза) КАК МассаГруза,
	|	СУММА(втРасстояниеИВремя.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(втРасстояниеИВремя.КоличествоМестГруза) КАК КоличествоМестГруза,
	|	СУММА(втРасстояниеИВремя.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(втРасстояниеИВремя.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	СУММА(втРасстояниеИВремя.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(втРасстояниеИВремя.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	СУММА(втРасстояниеИВремя.ФактическиеРасходыПоЗаявке) КАК ФактическиеРасходыПоЗаявке,
	|	СУММА(втРасстояниеИВремя.ПлановаяСуммаИнкассации) КАК ПлановаяСуммаИнкассации,
	|	СУММА(втРасстояниеИВремя.Расстояние) КАК Расстояние,
	|	СУММА(втРасстояниеИВремя.ВремяРаботыЧас) КАК ВремяРаботыЧас,
	|	МИНИМУМ(втРасстояниеИВремя.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
	|	МИНИМУМ(втРасстояниеИВремя.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
	|	МИНИМУМ(втРасстояниеИВремя.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
	|	МИНИМУМ(втРасстояниеИВремя.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
	|	МАКСИМУМ(втРасстояниеИВремя.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
	|	МАКСИМУМ(втРасстояниеИВремя.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
	|	МАКСИМУМ(втРасстояниеИВремя.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
	|	МАКСИМУМ(втРасстояниеИВремя.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
	|	МИНИМУМ(ЕСТЬNULL(втВыполненныеЗадания.ВремяОтправленияСПлановое, втВремяПоРейсам.ВремяОтправленияСПлановое)) КАК ВремяОтправленияСПлановое,
	|	МАКСИМУМ(ЕСТЬNULL(втВыполненныеЗадания.ВремяОтправленияПоПлановое, втВремяПоРейсам.ВремяОтправленияПоПлановое)) КАК ВремяОтправленияПоПлановое,
	|	МИНИМУМ(ЕСТЬNULL(втВыполненныеЗадания.ВремяПолученияСПлановое, втВремяПоРейсам.ВремяПолученияСПлановое)) КАК ВремяПолученияСПлановое,
	|	МАКСИМУМ(ЕСТЬNULL(втВыполненныеЗадания.ВремяПолученияПоПлановое, втВремяПоРейсам.ВремяПолученияПоПлановое)) КАК ВремяПолученияПоПлановое,
	|	МАКСИМУМ(ЕСТЬNULL(втКоличествоТочек.КоличествоТочекВМаршруте, 0)) КАК КоличествоТочекВМаршруте,
	|	МАКСИМУМ(ЕСТЬNULL(втКоличествоТочек.КоличествоТочекПогрузки, 0)) КАК КоличествоТочекПогрузки,
	|	МАКСИМУМ(ЕСТЬNULL(втКоличествоТочек.КоличествоТочекРазгрузки, 0)) КАК КоличествоТочекРазгрузки,
	|	МАКСИМУМ(втРасстояниеИВремя.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ПОМЕСТИТЬ втЗаданияРейсы
	|ИЗ
	|	втРасстояниеИВремя КАК втРасстояниеИВремя
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВыполненныеЗадания КАК втВыполненныеЗадания
	|	ПО ИСТИНА
	|		И втВыполненныеЗадания.Задание = втРасстояниеИВремя.Задание
	|		И втВыполненныеЗадания.Рейс = втРасстояниеИВремя.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоРейсам КАК втВремяПоРейсам
	|	ПО втВремяПоРейсам.Рейс = втРасстояниеИВремя.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТочекПоЗаданиямДляДокументаВЦелом КАК втКоличествоТочек
	|	ПО ИСТИНА
	|		И втРасстояниеИВремя.Задание = втКоличествоТочек.Задание
	|		И втРасстояниеИВремя.Рейс = втКоличествоТочек.Рейс
	|СГРУППИРОВАТЬ ПО
	|	втРасстояниеИВремя.Задание,
	|	втРасстояниеИВремя.Рейс
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗадания.Задание КАК Задание,
	|	втЗадания.Рейс КАК Рейс,
	|	втЗадания.МассаГруза КАК МассаГруза,
	|	втЗадания.ОбъемГруза КАК ОбъемГруза,
	|	втЗадания.КоличествоМестГруза КАК КоличествоМестГруза,
	|	втЗадания.КоличествоЗагрузочныхМетровГруза КАК КоличествоЗагрузочныхМетровГруза,
	|	втЗадания.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	втЗадания.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	втЗадания.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	втЗадания.ФактическиеРасходыПоЗаявке КАК ФактическиеРасходыПоЗаявке,
	|	втЗадания.ПлановаяСуммаИнкассации КАК ПлановаяСуммаИнкассации,
	|	втЗадания.Расстояние КАК Расстояние,
	|	втЗадания.ВремяРаботыЧас КАК ВремяРаботыЧас,
	|	втЗадания.ВремяНачалаПогрузкиПлановое КАК ВремяНачалаПогрузкиПлановое,
	|	втЗадания.ВремяНачалаПогрузкиФактическое КАК ВремяНачалаПогрузкиФактическое,
	|	втЗадания.ВремяНачалаРазгрузкиПлановое КАК ВремяНачалаРазгрузкиПлановое,
	|	втЗадания.ВремяНачалаРазгрузкиФактическое КАК ВремяНачалаРазгрузкиФактическое,
	|	втЗадания.ВремяОкончанияПогрузкиПлановое КАК ВремяОкончанияПогрузкиПлановое,
	|	втЗадания.ВремяОкончанияПогрузкиФактическое КАК ВремяОкончанияПогрузкиФактическое,
	|	втЗадания.ВремяОкончанияРазгрузкиПлановое КАК ВремяОкончанияРазгрузкиПлановое,
	|	втЗадания.ВремяОкончанияРазгрузкиФактическое КАК ВремяОкончанияРазгрузкиФактическое,
	|	втЗадания.ВремяОтправленияСПлановое КАК ВремяОтправленияСПлановое,
	|	втЗадания.ВремяОтправленияПоПлановое КАК ВремяОтправленияПоПлановое,
	|	втЗадания.ВремяПолученияСПлановое КАК ВремяПолученияСПлановое,
	|	втЗадания.ВремяПолученияПоПлановое КАК ВремяПолученияПоПлановое,
	|	втЗадания.КоличествоТочекВМаршруте КАК КоличествоТочекВМаршруте,
	|	втЗадания.КоличествоТочекПогрузки КАК КоличествоТочекПогрузки,
	|	втЗадания.КоличествоТочекРазгрузки КАК КоличествоТочекРазгрузки,
	|	втЗадания.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ИЗ
	|	втЗаданияРейсы КАК втЗадания
	|";
	
	Запрос.УстановитьПараметр("тбХарактеристикиПоЗаданиям", тбзХарактеристикиПоЗаданиям);						 
	Запрос.УстановитьПараметр("тбРасстояния", тбзРасстоянияПоРейсам);						 
	
	тбзЗадания = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаЗадание Из тбзЗадания Цикл
		
		текРейс = СтрокаЗадание.Рейс;
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		Если СпособВводаРасходовПоНесколькимРейсам Тогда
			НоваяСтрока.СтруктураДанные.Вставить("Рейс", текРейс);	
		КонецЕсли;
		НоваяСтрока.Задание	= СтрокаЗадание.Задание;
		НоваяСтрока.Рейс    = текРейс;
		ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, СтрокаЗадание);
		
		Если НужныЗоны Тогда
			НоваяСтрока.СтруктураДанные.Вставить("ЗонаОтправления", сткДанныеПоОбъектуРасчета.ЗонаОтправления.Получить(текРейс));	
			НоваяСтрока.СтруктураДанные.Вставить("ЗонаПолучения",   сткДанныеПоОбъектуРасчета.ЗонаПолучения.Получить(текРейс));	
		КонецЕсли;	
		
		Если НужныХарактеристикиТС ИЛИ НуженВидПеревозки Тогда
			сткХарактеристикиТС = сткДанныеПоОбъектуРасчета.ХарактеристикиТС.Получить(текРейс);
			
			Если НЕ сткХарактеристикиТС = Неопределено Тогда
				ЗаполнитьПоСтруктуре(НоваяСтрока.СтруктураДанные, сткХарактеристикиТС);
				Если НуженОбъемныйВес Тогда
					ЗаполнитьОбъемныйВес(НоваяСтрока.СтруктураДанные);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;			
	КонецЦикла;
	
	Если СпособВВодаРасходовОбощенныеПоНесколькимРейсам Тогда
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	втЗадания.Задание,
		               |	СУММА(втЗадания.МассаГруза) КАК МассаГруза,
		               |	СУММА(втЗадания.ОбъемГруза) КАК ОбъемГруза,
		               |	СУММА(втЗадания.КоличествоМестГруза) КАК КоличествоМестГруза,
		               |	СУММА(втЗадания.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
		               |	СУММА(втЗадания.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
		               |	СУММА(втЗадания.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
		               |	СУММА(втЗадания.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы,
		               |	СУММА(втЗадания.ФактическиеРасходыПоЗаявке) КАК ФактическиеРасходыПоЗаявке,
		               |	СУММА(втЗадания.ПлановаяСуммаИнкассации) КАК ПлановаяСуммаИнкассации,
		               |	СУММА(втЗадания.Расстояние) КАК Расстояние,
		               |	СУММА(втЗадания.ВремяРаботыЧас) КАК ВремяРаботыЧас,
		               |	МИНИМУМ(втЗадания.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
		               |	МИНИМУМ(втЗадания.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
		               |	МИНИМУМ(втЗадания.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
		               |	МИНИМУМ(втЗадания.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
		               |	МАКСИМУМ(втЗадания.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
		               |	МАКСИМУМ(втЗадания.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
		               |	МАКСИМУМ(втЗадания.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
		               |	МАКСИМУМ(втЗадания.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
		               |	МИНИМУМ(втЗадания.ВремяОтправленияСПлановое) КАК ВремяОтправленияСПлановое,
		               |	МАКСИМУМ(втЗадания.ВремяОтправленияПоПлановое) КАК ВремяОтправленияПоПлановое,
		               |	МИНИМУМ(втЗадания.ВремяПолученияСПлановое) КАК ВремяПолученияСПлановое,
		               |	МАКСИМУМ(втЗадания.ВремяПолученияПоПлановое) КАК ВремяПолученияПоПлановое,
		               |	МАКСИМУМ(втЗадания.КоличествоТочекВМаршруте) КАК КоличествоТочекВМаршруте,
		               |	МАКСИМУМ(втЗадания.КоличествоТочекПогрузки) КАК КоличествоТочекПогрузки,
		               |	МАКСИМУМ(втЗадания.КоличествоТочекРазгрузки) КАК КоличествоТочекРазгрузки,
					|	МАКСИМУМ(втЗадания.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
		               |ИЗ
		               |	втЗаданияРейсы КАК втЗадания
		               |ГДЕ
		               |	втЗадания.Задание = ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втЗадания.Задание";
					   
		текВыборка = Запрос.Выполнить().Выбрать();			   
		Если текВыборка.Следующий() Тогда
			текРейс 	= Документы.упРейс.ПустаяСсылка();

			НоваяСтрока = тбзРезультат.Добавить();
			НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
			НоваяСтрока.Задание	= Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка();
			НоваяСтрока.Рейс	= текРейс;
			ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, текВыборка);
					
			Если НужныЗоны Тогда
				НоваяСтрока.СтруктураДанные.Вставить("ЗонаОтправления", сткДанныеПоОбъектуРасчета.ЗонаОтправления.Получить(текРейс));	
				НоваяСтрока.СтруктураДанные.Вставить("ЗонаПолучения",   сткДанныеПоОбъектуРасчета.ЗонаПолучения.Получить(текРейс));	
			КонецЕсли;	
			
			Если НужныХарактеристикиТС ИЛИ НуженВидПеревозки Тогда
				сткХарактеристикиТС = сткДанныеПоОбъектуРасчета.ХарактеристикиТС.Получить(текРейс);
				
				Если НЕ сткХарактеристикиТС = Неопределено Тогда
					ЗаполнитьПоСтруктуре(НоваяСтрока.СтруктураДанные, сткХарактеристикиТС);
					Если НуженОбъемныйВес Тогда
						ЗаполнитьОбъемныйВес(НоваяСтрока.СтруктураДанные);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;			
			
		КонецЕсли;
		
	КонецЕсли;
	
 	Возврат тбзРезультат;	
		
КонецФункции

Процедура ЗаполнитьЗначенияПоказателейХарактеристикТС_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	Если НЕ сткДанныеПоОбъектуРасчета.ХарактеристикиТС = Неопределено 
		И НЕ НужныХарактеристикиТС(мсвПараметрыДляРасчетаПоДокументуВЦелом) 
		И НЕ НужныХарактеристикиТС(мсвПараметрыДляРасчетаПоКаждойСтроке) 
		И НЕ НуженВидПеревозки(мсвПараметрыДляРасчетаПоДокументуВЦелом) 
		И НЕ НуженВидПеревозки(мсвПараметрыДляРасчетаПоКаждойСтроке) Тогда
		Возврат;
	КонецЕсли;
	
	СпособВводаРасходовОбобщенныеПоНесколькимРейсам	= Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам;
	текРезультат = Новый Соответствие;
	
	Запрос = сткДанныеПоОбъектуРасчета.Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	втТранспортныеСредстваПоРейсам.Рейс,
	|	втТранспортныеСредстваПоРейсам.ВидПеревозки,
	|	втТранспортныеСредстваПоРейсам.ГрузоподъемностьТС,
	|	втТранспортныеСредстваПоРейсам.ОбъемТС,
	|	втТранспортныеСредстваПоРейсам.КоличествоЗагрузочныхМетровТС,
	|	втТранспортныеСредстваПоРейсам.КоличествоМестТС,
	|	втТранспортныеСредстваПоРейсам.ТипТС
	|ИЗ
	|	втТранспортныеСредстваПоРейсам КАК втТранспортныеСредстваПоРейсам";
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		сткХарактеристикиТС 	= СтруктураХарактеристикиТС();
		ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, Выборка);
		сткХарактеристикиТС.Вставить("ВидПеревозки", Выборка.ВидПеревозки);
		текРезультат.Вставить(Выборка.Рейс, сткХарактеристикиТС);
	КонецЦикла;
	   
	Если СпособВводаРасходовОбобщенныеПоНесколькимРейсам Тогда
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	втТранспортныеСредстваПоРейсам.ТипТС КАК ТипТС,
		|	втТранспортныеСредстваПоРейсам.ВидПеревозки,
		|	ЕСТЬNULL(ДатыУбытияПоПоследнимТочкамРейсов.Убытие, ДАТАВРЕМЯ(1, 1, 1)) КАК Убытие
		|ИЗ
		|	втТранспортныеСредстваПоРейсам КАК втТранспортныеСредстваПоРейсам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыУбытияПоПоследнимТочкамРейсов КАК ДатыУбытияПоПоследнимТочкамРейсов
		|		ПО втТранспортныеСредстваПоРейсам.Рейс = ДатыУбытияПоПоследнимТочкамРейсов.Рейс
		|ГДЕ
		|	НЕ втТранспортныеСредстваПоРейсам.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Убытие УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(втТранспортныеСредстваПоРейсам.ГрузоподъемностьТС), 0) КАК ГрузоподъемностьТС,
		|	ЕСТЬNULL(СУММА(втТранспортныеСредстваПоРейсам.ОбъемТС), 0) КАК ОбъемТС,
		|	ЕСТЬNULL(СУММА(втТранспортныеСредстваПоРейсам.КоличествоЗагрузочныхМетровТС), 0) КАК КоличествоЗагрузочныхМетровТС,
		|	ЕСТЬNULL(СУММА(втТранспортныеСредстваПоРейсам.КоличествоМестТС), 0) КАК КоличествоМестТС
		|ИЗ
		|	втТранспортныеСредстваПоРейсам КАК втТранспортныеСредстваПоРейсам";
		
		сткХарактеристикиТС 	= СтруктураХарактеристикиТС();
		
		мсвРезультаты = Запрос.ВыполнитьПакет();			   
		
		мсвРезультаты = Запрос.ВыполнитьПакет();			   
		Выборка	= мсвРезультаты[0].Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, Выборка);
		КонецЕсли;
		
		Выборка	= мсвРезультаты[1].Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткХарактеристикиТС, Выборка);
		КонецЕсли;
		
		текРезультат.Вставить(Документы.упРейс.ПустаяСсылка(), сткХарактеристикиТС);
		
	КонецЕсли; 
	
	сткДанныеПоОбъектуРасчета.ХарактеристикиТС = текРезультат;
	
КонецПроцедуры

Процедура ЗаполнитьЗначенияПоказателейЗоны_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	Если НЕ НужныЗоны(мсвПараметрыДляРасчетаПоДокументуВЦелом) 
		И НЕ НужныЗоны(мсвПараметрыДляРасчетаПоКаждойСтроке) Тогда
		Возврат;
	КонецЕсли;
		
	Запрос = сткДанныеПоОбъектуРасчета.Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	втРейсыИзОбъекта.Рейс,
	|	втРейсыИзОбъекта.ЗонаОтправления,
	|	втРейсыИзОбъекта.ЗонаПолучения
	|ИЗ
	|	втРейсыИзОбъекта КАК втРейсыИзОбъекта";
	текВыборка = Запрос.Выполнить().Выбрать();
	
	соотЗоныОтправления = Новый Соответствие;
	соотЗоныПолучения	= Новый Соответствие;
	
	мсвЗоныОтправления 	= Новый Массив;
	мсвЗоныПолучения 	= Новый Массив;
	
	ОбщаяЗонаОтправления	= Справочники.упГеографическиеЗоны.ПустаяСсылка();
	ОбщаяЗонаПолучения	= Справочники.упГеографическиеЗоны.ПустаяСсылка();
		
	Пока текВыборка.Следующий() Цикл
		соотЗоныОтправления.Вставить(текВыборка.Рейс, текВыборка.ЗонаОтправления);	
		соотЗоныПолучения.Вставить(текВыборка.Рейс, текВыборка.ЗонаПолучения);	
		
		Если ЗначениеЗаполнено(текВыборка.ЗонаОтправления) Тогда
				мсвЗоныОтправления.Добавить(текВыборка.ЗонаОтправления);		
		КонецЕсли;
			
		Если ЗначениеЗаполнено(текВыборка.ЗонаПолучения) Тогда
			мсвЗоныПолучения.Добавить(текВыборка.ЗонаПолучения);	
		КонецЕсли;
	КонецЦикла;

	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		
		Если мсвЗоныОтправления.Количество()>0 Тогда
			ОбщаяЗонаОтправления = упМаршрутизация.ОбщаяГеографическаяЗона(мсвЗоныОтправления);	
		КонецЕсли;
		соотЗоныОтправления.Вставить(Документы.упРейс.ПустаяСсылка(), ОбщаяЗонаОтправления);	
				
		Если мсвЗоныПолучения.Количество()>0 Тогда
			ОбщаяЗонаПолучения = упМаршрутизация.ОбщаяГеографическаяЗона(мсвЗоныПолучения);	
		КонецЕсли;
		соотЗоныПолучения.Вставить(Документы.упРейс.ПустаяСсылка(), ОбщаяЗонаПолучения);	
	
	КонецЕсли;
	
	сткДанныеПоОбъектуРасчета.Вставить("ЗонаОтправления",	соотЗоныОтправления);
	сткДанныеПоОбъектуРасчета.Вставить("ЗонаПолучения",		соотЗоныПолучения);

КонецПроцедуры

Функция ТекстЗапроса_ФактическиеРасходыОбщиеДанные()
	
	текРезультат =
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Рейсы с зонами, тс и вид перевозки.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРейсыЗаданияИзОбъекта.Ссылка КАК Рейс,
	|	ЕСТЬNULL(ПлановыеПараметрыРейса.ЗонаПогрузки, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаОтправления,
	|	ЕСТЬNULL(ПлановыеПараметрыРейса.ЗонаРазгрузки, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаПолучения,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТипТС, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)) КАК ТипТС,
	|	ВЫБОР
	|		КОГДА втРейсыЗаданияИзОбъекта.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустыеГрузовыеМеста,
	|	втРейсыЗаданияИзОбъекта.ВидПеревозки КАК ВидПеревозки
	|ПОМЕСТИТЬ втРейсыИзОбъекта
	|ИЗ
	|	Документ.упРейс КАК втРейсыЗаданияИзОбъекта
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК ПлановыеПараметрыРейса
	|	ПО втРейсыЗаданияИзОбъекта.Ссылка = ПлановыеПараметрыРейса.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО втРейсыЗаданияИзОбъекта.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
	|ГДЕ втРейсыЗаданияИзОбъекта.Ссылка В  (&мсвРейсы)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Работы по рейсам.
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упРаботыПоРейсуСрезПоследних.Регистратор КАК Регистратор,
	|	упРаботыПоРейсуСрезПоследних.Отменена КАК Отменена,
	|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
	|	упРаботыПоРейсуСрезПоследних.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упРаботыПоРейсуСрезПоследних.Операция КАК Операция,
	|	упРаботыПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРейсыИзОбъекта.ЗонаОтправления КАК ЗонаОтправления,
	|	втРейсыИзОбъекта.ЗонаПолучения КАК ЗонаПолучения,
	|	упРаботыПоРейсуСрезПоследних.Выполнена КАК Выполнена,
	|	втРейсыИзОбъекта.ВидПеревозки КАК ВидПеревозки
	|ПОМЕСТИТЬ втРаботыПоРейсам
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК упРаботыПоРейсуСрезПоследних
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|	ПО втРейсыИзОбъекта.Рейс = упРаботыПоРейсуСрезПоследних.Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Работы по рейсам.
	|ВЫБРАТЬ
	|	тбРаботыПоРейсу.Рейс КАК Рейс,
	|	тбРаботыПоРейсу.Регистратор КАК Регистратор,
	|	тбРаботыПоРейсу.Отменена КАК Отменена,
	|	тбРаботыПоРейсу.Задание КАК Задание,
	|	тбРаботыПоРейсу.Операция КАК Операция,
	|	тбРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	тбРаботыПоРейсу.КоличествоГрузов КАК КоличествоГрузов,
	|	тбРаботыПоРейсу.ГрузовоеМесто КАК ГрузовоеМесто,
	|	тбРаботыПоРейсу.ЗонаОтправления КАК ЗонаОтправления,
	|	тбРаботыПоРейсу.ЗонаПолучения КАК ЗонаПолучения,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.Задание.Масса))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, тбРаботыПоРейсу.ГрузовоеМесто.Масса)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК МассаГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.Задание.Объем))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, тбРаботыПоРейсу.ГрузовоеМесто.Объем)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК ОбъемГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.Задание.КоличествоЗагрузочныхМетров))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоЗагрузочныхМетров)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.Задание.КоличествоМест))
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, тбРаботыПоРейсу.ГрузовоеМесто.КоличествоМест)) * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоМестГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА тбРаботыПоРейсу.Задание.ОценочнаяСтоимость
	|		ИНАЧЕ тбРаботыПоРейсу.ГрузовоеМесто.ОценочнаяСтоимость * тбРаботыПоРейсу.КоличествоГрузов
	|	КОНЕЦ КАК ОценочнаяСтоимостьГруза,
	|	тбРаботыПоРейсу.Выполнена КАК Выполнена,
	|	тбРаботыПоРейсу.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЭтоВидГрузаГруз
	|ПОМЕСТИТЬ втВсеРаботы
	|ИЗ
	|	втРаботыПоРейсам КАК тбРаботыПоРейсу
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втРаботыПоРейсам КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втРаботыПоРейсам КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Регистраторы по работам рейса.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРаботыПоРейсу.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втРегистраторы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ ИСТИНА
	|	И упРаботыПоРейсу.Рейс В  (&мсвРейсы)
	|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА втРейсыИзОбъекта.ПустыеГрузовыеМеста
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ КАК ГрузовоеМесто,
	|	СУММА(ВЫБОР
	|		КОГДА втРаботы.Отменена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтменена,
	|	СУММА(1) КАК КоличествоОпераций
	|ПОМЕСТИТЬ втОтмененныеЗадания
	|ИЗ
	|	втВсеРаботы КАК втРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|	ПО втРаботы.Рейс = втРейсыИзОбъекта.Рейс
	|ГДЕ ЛОЖЬ
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Задание,
	|	ВЫБОР
	|		КОГДА втРейсыИзОбъекта.ПустыеГрузовыеМеста
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.Ссылка КАК Рейс
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|ГДЕ ИСТИНА
	|	И упРейсЗадания.Ссылка В  (&мсвРейсы)
	|	И НЕ (ВЫБОР
	|			КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	|					И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упКорректировкаРейсаЗадания.Задание КАК Задание,
	|	упКорректировкаРейсаЗадания.Ссылка.Рейс КАК Рейс
	|ИЗ
	|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	|	ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|ГДЕ НЕ (ВЫБОР
	|		КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	|				И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗадания.Задание КАК Задание,
	|	втЗадания.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсыЗаданияИзОбъекта
	|ИЗ
	|	втЗадания КАК втЗадания
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРейсыИзОбъекта.Рейс КАК Рейс,
	|	втРейсыИзОбъекта.ВидПеревозки КАК ВидПеревозки,
	|	втРейсыИзОбъекта.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРейсыИзОбъекта.ТипТС КАК ТипТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.Грузоподъемность = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.Грузоподъемность
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.Грузоподъемность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ГрузоподъемностьТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.Объем = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.Объем
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.Объем
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбъемТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.КоличествоЗагрузочныхМетров = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.КоличествоЗагрузочныхМетров
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.КоличествоЗагрузочныхМетров
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.КоличествоМест = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.КоличествоМест
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.КоличествоМест
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоМестТС
	|ПОМЕСТИТЬ втТранспортныеСредстваПоРейсам
	|ИЗ
	|	втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботы.Рейс КАК Рейс,
	|	тбРаботы.Задание КАК Задание,
	|	тбРаботы.Операция КАК Операция,
	|	тбРаботы.Идентификатор КАК Идентификатор,
	|	тбРаботы.ЗонаОтправления КАК ЗонаОтправления,
	|	тбРаботы.ЗонаПолучения КАК ЗонаПолучения,
	|	тбРаботы.ВидПеревозки КАК ВидПеревозки,
	|	тбРаботы.МассаГруза КАК МассаГруза,
	|	тбРаботы.ОбъемГруза КАК ОбъемГруза,
	|	тбРаботы.КоличествоЗагрузочныхМетровГруза КАК КоличествоЗагрузочныхМетровГруза,
	|	тбРаботы.КоличествоМестГруза КАК КоличествоМестГруза,
	|	тбРаботы.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	тбРаботы.Выполнена КАК Выполнена,
	|	тбРаботы.ЭтоВидГрузаГруз КАК ЭтоВидГрузаГруз
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	втВсеРаботы КАК тбРаботы
	|ГДЕ НЕ (тбРаботы.Отменена)
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Рейс КАК Рейс,
	|	втРаботы.Задание КАК Задание,
	|	МАКСИМУМ(втРаботы.ВидПеревозки) КАК ВидПеревозки,
	|	СУММА(втРаботы.МассаГруза) КАК МассаГруза,
	|	СУММА(втРаботы.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(втРаботы.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(втРаботы.КоличествоМестГруза) КАК КоличествоМестГруза,
	|	СУММА(втРаботы.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	МАКСИМУМ(втРаботы.Задание.ЗаявкаНаПеревозкуГруза.ОтправлениеГрузаС) КАК ВремяОтправленияСПлановое,
	|	МАКСИМУМ(втРаботы.Задание.ЗаявкаНаПеревозкуГруза.ОтправлениеГрузаПо) КАК ВремяОтправленияПоПлановое,
	|	МАКСИМУМ(втРаботы.Задание.ЗаявкаНаПеревозкуГруза.ПолучениеГрузаС) КАК ВремяПолученияСПлановое,
	|	МАКСИМУМ(втРаботы.Задание.ЗаявкаНаПеревозкуГруза.ПолучениеГрузаПо) КАК ВремяПолученияПоПлановое,
	|	МАКСИМУМ(втРаботы.ЭтоВидГрузаГруз) КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ПОМЕСТИТЬ втВыполненныеЗаданияБезЗон
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыЗаданияИзОбъекта КАК втРейсыЗаданияИзОбъекта
	|	ПО ИСТИНА
	|		И втРаботы.Рейс = втРейсыЗаданияИзОбъекта.Рейс
	|		И втРаботы.Задание = втРейсыЗаданияИзОбъекта.Задание
	|ГДЕ ЛОЖЬ
	|		ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|		ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Рейс,
	|	втРаботы.Задание
	|ИМЕЮЩИЕ СУММА(ВЫБОР
	|			КОГДА втРаботы.Выполнена
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) = СУММА(1)
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВыполненныеЗадания.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсыСВыполненнымиРаботами
	|ИЗ
	|	втВыполненныеЗаданияБезЗон КАК втВыполненныеЗадания
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Рейс КАК Рейс,
	|	втРаботы.Задание КАК Задание,
	|	втРаботы.Операция КАК Операция,
	|	втРаботы.Идентификатор КАК Идентификатор,
	|	МАКСИМУМ(втРаботы.ЗонаОтправления) КАК ЗонаОтправления,
	|	МАКСИМУМ(втРаботы.ЗонаПолучения) КАК ЗонаПолучения,
	|	МАКСИМУМ(втРаботы.ВидПеревозки) КАК ВидПеревозки,
	|	СУММА(втРаботы.МассаГруза) КАК МассаГруза,
	|	СУММА(втРаботы.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(втРаботы.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(втРаботы.КоличествоМестГруза) КАК КоличествоМестГруза,
	|	СУММА(втРаботы.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза
	|ПОМЕСТИТЬ втВыполненныеРаботы
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВыполненныеЗаданияБезЗон КАК втВыполненныеЗадания
	|	ПО ИСТИНА
	|		И втРаботы.Рейс = втВыполненныеЗадания.Рейс
	|		И втРаботы.Задание = втВыполненныеЗадания.Задание
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Рейс,
	|	втРаботы.Задание,
	|	втРаботы.Операция,
	|	втРаботы.Идентификатор
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботы.Идентификатор КАК Идентификатор,
	|	тбРаботы.Рейс КАК Рейс,
	|	тбРаботы.Задание КАК Задание,
	|	тбРаботы.Операция КАК Операция,
	|	СУММА(ВЫБОР
	|		КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|			КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|				ТОГДА - 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ * тбРаботы.МассаГруза) КАК МассаГруза,
	|	СУММА(ВЫБОР
	|		КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|			КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|				ТОГДА - 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ * тбРаботы.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(ВЫБОР
	|		КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|			КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|				ТОГДА - 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ * тбРаботы.КоличествоЗагрузочныхМетровГруза) КАК КоличествоЗагрузочныхМетровГруза,
	|	СУММА(ВЫБОР
	|		КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|			КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|				ТОГДА - 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ * тбРаботы.КоличествоМестГруза) КАК КоличествоМестГруза
	|ПОМЕСТИТЬ втЗаданияВТочке
	|ИЗ
	|	втВыполненныеРаботы КАК тбРаботы
	|СГРУППИРОВАТЬ ПО
	|	тбРаботы.Идентификатор,
	|	тбРаботы.Рейс,
	|	тбРаботы.Задание,
	|	тбРаботы.Операция
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеРаботы.Задание КАК Задание,
	|	втВыполненныеРаботы.Рейс КАК Рейс,
	|	СУММА(ВЫБОР
	|		КОГДА втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаРазгрузки,
	|	втВыполненныеРаботы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втКоличествоТочекПоЗаданиям
	|ИЗ
	|	втВыполненныеРаботы КАК втВыполненныеРаботы
	|СГРУППИРОВАТЬ ПО
	|	втВыполненныеРаботы.Задание,
	|	втВыполненныеРаботы.Рейс,
	|	втВыполненныеРаботы.Идентификатор
	|;
	|//{Запрос: 14 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКоличествоТочек.Рейс КАК Рейс,
	|	втКоличествоТочек.Задание КАК Задание,
	|	СУММА(ВЫБОР
	|		КОГДА втКоличествоТочек.ЭтоТочкаПогрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втКоличествоТочек.ЭтоТочкаРазгрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекРазгрузки,
	|	СУММА(1) КАК КоличествоТочекВМаршруте
	|ПОМЕСТИТЬ втКоличествоТочекПоЗаданиям2
	|ИЗ
	|	втКоличествоТочекПоЗаданиям КАК втКоличествоТочек
	|СГРУППИРОВАТЬ ПО
	|	втКоличествоТочек.Рейс,
	|	втКоличествоТочек.Задание
	|;
	|//{Запрос: 15 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеРаботы.Рейс КАК Рейс,
	|	СУММА(ВЫБОР
	|		КОГДА втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаРазгрузки,
	|	втВыполненныеРаботы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втКоличествоТочекПоРейсу
	|ИЗ
	|	втВыполненныеРаботы КАК втВыполненныеРаботы
	|СГРУППИРОВАТЬ ПО
	|	втВыполненныеРаботы.Рейс,
	|	втВыполненныеРаботы.Идентификатор
	|;
	|//{Запрос: 16 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|		КОГДА втКоличествоТочек.ЭтоТочкаПогрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втКоличествоТочек.ЭтоТочкаРазгрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекРазгрузки,
	|	СУММА(1) КАК КоличествоТочекВМаршруте,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка) КАК Задание,
	|	втКоличествоТочек.Рейс КАК Рейс
	|ПОМЕСТИТЬ втКоличествоТочекПоЗаданиямДляДокументаВЦелом
	|ИЗ
	|	втКоличествоТочекПоРейсу КАК втКоличествоТочек
	|СГРУППИРОВАТЬ ПО
	|	втКоличествоТочек.Рейс
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	СУММА(втКоличествоТочекПоЗаданиям2.КоличествоТочекПогрузки) КАК КоличествоТочекПогрузки,
	|	СУММА(втКоличествоТочекПоЗаданиям2.КоличествоТочекРазгрузки) КАК КоличествоТочекРазгрузки,
	|	СУММА(втКоличествоТочекПоЗаданиям2.КоличествоТочекВМаршруте) КАК КоличествоТочекВМаршруте,
	|	втКоличествоТочекПоЗаданиям2.Задание КАК Задание,
	|	втКоличествоТочекПоЗаданиям2.Рейс КАК Рейс
	|ИЗ
	|	втКоличествоТочекПоЗаданиям2 КАК втКоличествоТочекПоЗаданиям2
	|СГРУППИРОВАТЬ ПО
	|	втКоличествоТочекПоЗаданиям2.Задание,
	|	втКоличествоТочекПоЗаданиям2.Рейс
	|;
	|//{Запрос: 17 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упМаршрутПоРейсуСрезПоследних.Адрес КАК Адрес,
	|	упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт КАК Прибытие,
	|	упМаршрутПоРейсуСрезПоследних.УбытиеФакт КАК Убытие,
	|	упМаршрутПоРейсуСрезПоследних.РасстояниеОтПредыдущейТочкиФакт КАК РасстояниеОтПредыдущейточки,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПрибытиеПлан,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК УбытиеПлан,
	|	упМаршрутПоРейсуСрезПоследних.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	упМаршрутПоРейсуСрезПоследних.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсыСВыполненнымиРаботами.Рейс КАК Рейс
	|			ИЗ
	|				втРейсыСВыполненнымиРаботами КАК втРейсыСВыполненнымиРаботами)) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ ИСТИНА
	|	И НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
	|	И упМаршрутПоРейсуСрезПоследних.Пройдена
	|;
	|//{Запрос: 18 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрут.Адрес.ГеографическаяЗона КАК АдресГеографическаяЗона,
	|	втЗаданияВТочке.Задание КАК Задание,
	|	втЗаданияВТочке.Операция КАК Операция,
	|	втМаршрут.Рейс КАК Рейс
	|ПОМЕСТИТЬ втМаршрутПоОперациям
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЗаданияВТочке КАК втЗаданияВТочке
	|	ПО втМаршрут.Идентификатор = втЗаданияВТочке.Идентификатор
	|;
	|//{Запрос: 19 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Задание КАК Задание,
	|	МАКСИМУМ(втМаршрут.АдресГеографическаяЗона) КАК АдресГеографическаяЗона,
	|	втМаршрут.Рейс КАК Рейс
	|ПОМЕСТИТЬ втТочкиПогрузки
	|ИЗ
	|	втМаршрутПоОперациям КАК втМаршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втМаршрут.Задание КАК Задание,
	|		МИНИМУМ(втМаршрут.СтрокаНомер) КАК СтрокаНомер
	|	ИЗ
	|		втМаршрутПоОперациям КАК втМаршрут
	|	ГДЕ втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	СГРУППИРОВАТЬ ПО
	|		втМаршрут.Задание) КАК ВложенныйЗапрос
	|	ПО ИСТИНА
	|		И втМаршрут.Задание = ВложенныйЗапрос.Задание
	|		И втМаршрут.СтрокаНомер = ВложенныйЗапрос.СтрокаНомер
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Задание,
	|	втМаршрут.Рейс
	|;
	|//{Запрос: 20 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втМаршрут.Задание КАК Задание,
	|	МАКСИМУМ(втМаршрут.АдресГеографическаяЗона) КАК АдресГеографическаяЗона,
	|	втМаршрут.Рейс КАК Рейс
	|ПОМЕСТИТЬ втТочкиРазгрузки
	|ИЗ
	|	втМаршрутПоОперациям КАК втМаршрут
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		втМаршрут.Задание КАК Задание,
	|		МИНИМУМ(втМаршрут.СтрокаНомер) КАК СтрокаНомер
	|	ИЗ
	|		втМаршрутПоОперациям КАК втМаршрут
	|	ГДЕ втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	СГРУППИРОВАТЬ ПО
	|		втМаршрут.Задание) КАК ВложенныйЗапрос
	|	ПО ИСТИНА
	|		И втМаршрут.Задание = ВложенныйЗапрос.Задание
	|		И втМаршрут.СтрокаНомер = ВложенныйЗапрос.СтрокаНомер
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Задание,
	|	втМаршрут.Рейс
	|;
	|//{Запрос: 21 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеЗадания.Рейс КАК Рейс,
	|	втВыполненныеЗадания.Задание КАК Задание,
	|	втВыполненныеЗадания.ВидПеревозки КАК ВидПеревозки,
	|	втВыполненныеЗадания.МассаГруза КАК МассаГруза,
	|	втВыполненныеЗадания.ОбъемГруза КАК ОбъемГруза,
	|	втВыполненныеЗадания.КоличествоЗагрузочныхМетровГруза КАК КоличествоЗагрузочныхМетровГруза,
	|	втВыполненныеЗадания.КоличествоМестГруза КАК КоличествоМестГруза,
	|	втВыполненныеЗадания.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	втВыполненныеЗадания.ВремяОтправленияСПлановое КАК ВремяОтправленияСПлановое,
	|	втВыполненныеЗадания.ВремяОтправленияПоПлановое КАК ВремяОтправленияПоПлановое,
	|	втВыполненныеЗадания.ВремяПолученияСПлановое КАК ВремяПолученияСПлановое,
	|	втВыполненныеЗадания.ВремяПолученияПоПлановое КАК ВремяПолученияПоПлановое,
	|	ЕСТЬNULL(втТочкиПогрузки.АдресГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаОтправления,
	|	ЕСТЬNULL(втТочкиРазгрузки.АдресГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаПолучения,
	|	втВыполненныеЗадания.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте
	|ПОМЕСТИТЬ втВыполненныеЗадания
	|ИЗ
	|	втВыполненныеЗаданияБезЗон КАК втВыполненныеЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТочкиПогрузки КАК втТочкиПогрузки
	|	ПО ИСТИНА
	|		И втВыполненныеЗадания.Задание = втТочкиПогрузки.Задание
	|		И втВыполненныеЗадания.Рейс = втТочкиПогрузки.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТочкиРазгрузки КАК втТочкиРазгрузки
	|	ПО ИСТИНА
	|		И втВыполненныеЗадания.Рейс = втТочкиРазгрузки.Рейс
	|		И втВыполненныеЗадания.Задание = втТочкиРазгрузки.Задание
	|;
	|//{Запрос: 22 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Рейс КАК Рейс,
	|	МАКСИМУМ(втМаршрут.Убытие) КАК Убытие
	|ПОМЕСТИТЬ ДатыУбытияПоПоследнимТочкамРейсов
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Рейс
	|;
	|//{Запрос: 23 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеРаботы.Задание КАК Задание,
	|	втВыполненныеРаботы.Рейс КАК Рейс,
	|	СУММА(втМаршрут.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(втМаршрут.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	МАКСИМУМ(втМаршрут.ПрибытиеПлан) КАК ПрибытиеПлан,
	|	МАКСИМУМ(втМаршрут.УбытиеПлан) КАК УбытиеПлан,
	|	МАКСИМУМ(втМаршрут.Прибытие) КАК Прибытие,
	|	МАКСИМУМ(втМаршрут.Убытие) КАК Убытие,
	|	втВыполненныеРаботы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втВремянныеПараметрыПоЗаданиямРейсам
	|ИЗ
	|	втВыполненныеРаботы КАК втВыполненныеРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрут
	|	ПО ИСТИНА
	|		И втВыполненныеРаботы.Рейс = втМаршрут.Рейс
	|		И втВыполненныеРаботы.Идентификатор = втМаршрут.Идентификатор
	|СГРУППИРОВАТЬ ПО
	|	втВыполненныеРаботы.Задание,
	|	втВыполненныеРаботы.Рейс,
	|	втВыполненныеРаботы.Идентификатор
	|;
	|//{Запрос: 24 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВремянныеПараметрыПоЗаданиямРейсам.Задание КАК Задание,
	|	втВремянныеПараметрыПоЗаданиямРейсам.Рейс КАК Рейс,
	|	СУММА(втВремянныеПараметрыПоЗаданиямРейсам.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(втВремянныеПараметрыПоЗаданиямРейсам.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы
	|ПОМЕСТИТЬ втПростойПоЗаданиямРейсам
	|ИЗ
	|	втВремянныеПараметрыПоЗаданиямРейсам КАК втВремянныеПараметрыПоЗаданиямРейсам
	|СГРУППИРОВАТЬ ПО
	|	втВремянныеПараметрыПоЗаданиямРейсам.Задание,
	|	втВремянныеПараметрыПоЗаданиямРейсам.Рейс
	|;
	|//{Запрос: 25 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеЗадания.Рейс КАК Рейс,
	|	МИНИМУМ(втВыполненныеЗадания.ВремяОтправленияСПлановое) КАК ВремяОтправленияСПлановое,
	|	МАКСИМУМ(втВыполненныеЗадания.ВремяОтправленияПоПлановое) КАК ВремяОтправленияПоПлановое,
	|	МИНИМУМ(втВыполненныеЗадания.ВремяПолученияСПлановое) КАК ВремяПолученияСПлановое,
	|	МАКСИМУМ(втВыполненныеЗадания.ВремяПолученияПоПлановое) КАК ВремяПолученияПоПлановое
	|ПОМЕСТИТЬ втВремяПоРейсам
	|ИЗ
	|	втВыполненныеЗадания КАК втВыполненныеЗадания
	|СГРУППИРОВАТЬ ПО
	|	втВыполненныеЗадания.Рейс
	|;
	|//{Запрос: 26 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втРейсыИзОбъекта.Рейс КАК Рейс
	|ПОМЕСТИТЬ втЗаявкиПоРейсам
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|	ПО упРейсЗадания.Ссылка = втРейсыИзОбъекта.Рейс
	|;
	|//{Запрос: 27 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗаявкиПоРейсам.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗаявки
	|ИЗ
	|	втЗаявкиПоРейсам КАК втЗаявкиПоРейсам
	|;
	|//{Запрос: 28 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(упИнкассацияДенежныхСредствОбороты.СуммаПланОборот) КАК СуммаПланОборот,
	|	упИнкассацияДенежныхСредствОбороты.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втСуммыПлановойИнкассацииПоЗаявкам
	|ИЗ
	|	РегистрНакопления.упИнкассацияДенежныхСредств.Обороты(
	|		,
	|		,
	|		,
	|		ЗаявкаНаПеревозкуГруза В (
	|			ВЫБРАТЬ
	|				втЗаявки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗаявки КАК втЗаявки)) КАК упИнкассацияДенежныхСредствОбороты
	|СГРУППИРОВАТЬ ПО
	|	упИнкассацияДенежныхСредствОбороты.ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 29 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	СУММА(упРасходыОбороты.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
	|ПОМЕСТИТЬ втСуммыФактическихРасходовПоЗаявкам
	|ИЗ
	|	втЗаявки КАК втЗаявки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упРасходы.Обороты(
	|		,
	|		,
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				упЗаданиеНаПеревозкуГруза.Ссылка КАК Ссылка
	|			ИЗ
	|				Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявки КАК втЗаявки
	|				ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втЗаявки.ЗаявкаНаПеревозкуГруза)) КАК упРасходыОбороты
	|	ПО втЗаявки.ЗаявкаНаПеревозкуГруза = упРасходыОбороты.ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза
	|СГРУППИРОВАТЬ ПО
	|	втЗаявки.ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 30 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеЗадания.Рейс КАК Рейс,
	|	втВыполненныеЗадания.Задание КАК Задание,
	|	втВыполненныеЗадания.КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте,
	|	ЕСТЬNULL(втСуммыПлановойИнкассацииПоЗаявкам.СуммаПланОборот, 0) КАК ПлановаяСуммаИнкассации,
	|	ЕСТЬNULL(втСуммыФактическихРасходовПоЗаявкам.СуммаФактСНДСОборот, 0) КАК ФактическиеРасходыПоЗаявке
	|ПОМЕСТИТЬ втСуммыПоЗаявкам
	|ИЗ
	|	втВыполненныеЗадания КАК втВыполненныеЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО втВыполненныеЗадания.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыПлановойИнкассацииПоЗаявкам КАК втСуммыПлановойИнкассацииПоЗаявкам
	|	ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втСуммыПлановойИнкассацииПоЗаявкам.ЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыФактическихРасходовПоЗаявкам КАК втСуммыФактическихРасходовПоЗаявкам
	|	ПО упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втСуммыФактическихРасходовПоЗаявкам.ЗаявкаНаПеревозкуГруза
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втЗаявкиПоРейсам.Рейс КАК Рейс,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка) КАК Задание,
	|	0 КАК КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте,
	|	СУММА(ЕСТЬNULL(втСуммыПлановойИнкассацииПоЗаявкам.СуммаПланОборот, 0)) КАК ПлановаяСуммаИнкассации,
	|	СУММА(ЕСТЬNULL(втСуммыФактическихРасходовПоЗаявкам.СуммаФактСНДСОборот, 0)) КАК ФактическиеРасходыПоЗаявке
	|ИЗ
	|	втЗаявкиПоРейсам КАК втЗаявкиПоРейсам
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыПлановойИнкассацииПоЗаявкам КАК втСуммыПлановойИнкассацииПоЗаявкам
	|	ПО втЗаявкиПоРейсам.ЗаявкаНаПеревозкуГруза = втСуммыПлановойИнкассацииПоЗаявкам.ЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыФактическихРасходовПоЗаявкам КАК втСуммыФактическихРасходовПоЗаявкам
	|	ПО втЗаявкиПоРейсам.ЗаявкаНаПеревозкуГруза = втСуммыФактическихРасходовПоЗаявкам.ЗаявкаНаПеревозкуГруза
	|СГРУППИРОВАТЬ ПО
	|	втЗаявкиПоРейсам.Рейс
	|";


	Возврат текРезультат;
	
КонецФункции

Функция ПолучитьСтрокуДанныхДляРасчета_ФактическиеРасходы(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета)
	
	текРезультат = Неопределено;
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		
		мсвСтроки = тбзДанныеДляРасчета.НайтиСтроки(Новый Структура("Задание", Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()));
		
		Если мсвСтроки.Количество() > 0 Тогда
			текРезультат = мсвСтроки[0];
		КонецЕсли;
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда	
		
		мсвСтроки = тбзДанныеДляРасчета.НайтиСтроки(Новый Структура("Рейс, Задание", СтрокаРасчета.Рейс, Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()));
		
		Если мсвСтроки.Количество() > 0 Тогда
			текРезультат = мсвСтроки[0];
		КонецЕсли;
		
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		
		Если ЗначениеЗаполнено(СтрокаРасчета.Задание) Тогда
			
			мсвСтрокиРейса = тбзДанныеДляРасчета.НайтиСтроки(Новый Структура("Задание", СтрокаРасчета.Задание));	
			
			Если мсвСтрокиРейса.Количество() > 0 Тогда
				текРейс = мсвСтрокиРейса[0].Рейс;
			КонецЕсли;
		
			мсвСтроки = тбзДанныеДляРасчета.НайтиСтроки(Новый Структура("Рейс, Задание", текРейс, Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()));	
			
			Если мсвСтроки.Количество() > 0 Тогда
				текРезультат = мсвСтроки[0];
			КонецЕсли;
		Иначе	

			мсвСтроки = тбзДанныеДляРасчета.НайтиСтроки(Новый Структура("Рейс, Задание", Документы.упРейс.ПустаяСсылка(), Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()));	
			
			Если мсвСтроки.Количество() > 0 Тогда
				текРезультат = мсвСтроки[0];
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

Функция ПолучитьКоэффициентРаспределения_ФактическиеРасходы(Объект, СтрокаРасчета, сткДанныеПоОбъектуРасчета, тбзДанныеДляРасчета, ПравилоРаспределения)
	
	текРезультат = 1;
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		
		Если ЗначениеЗаполнено(СтрокаРасчета.Задание) Тогда
			
			тбзРаспределения = ПолучитьТаблицуРаспределения();
			сткСоответствиеПоказателейРаспределенияПараметрамРасчета = СоответствиеПоказателейРаспределенияПараметрамРасчета();
			Для каждого Строка Из тбзДанныеДляРасчета Цикл
				
				Если ЗначениеЗаполнено(Строка.Задание) Тогда
					
					НоваяСтрока = тбзРаспределения.Добавить();
					НоваяСтрока.Ключ = Строка.Задание;
					
					Для каждого текПоказатель Из сткСоответствиеПоказателейРаспределенияПараметрамРасчета Цикл
						НоваяСтрока[текПоказатель.Ключ] = Строка.СтруктураДанные[текПоказатель.Значение];
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			
			сткКолонкиОшибки = Новый Структура("Ключ");
			
			сткРеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоРаспределения, "Формула, СпособРаспределения, Показатель");
			
			Если сткРеквизитыПравила.СпособРаспределения = Перечисления.упСпособыРаспределенияРасходов.ПропорциональноПараметру Тогда
				Формула = упРаспределениеРасходов.ПолучитьФормулуРаспределения(сткРеквизитыПравила.Показатель);
			Иначе
				Формула = сткРеквизитыПравила.Формула;			
			КонецЕсли;
			
			упРаспределениеРасходов.РассчитатьКоэффициентыРаспределения(Формула, тбзРаспределения, сткКолонкиОшибки);
						
			мсвСтроки = тбзРаспределения.НайтиСтроки(Новый Структура("Ключ", СтрокаРасчета.Задание));
			
			Если мсвСтроки.Количество() > 0 Тогда
				текРезультат = мсвСтроки[0].Коэффициент;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда	
	
				
		Если ЗначениеЗаполнено(СтрокаРасчета.Задание) Тогда
			
			тбзРаспределения = ПолучитьТаблицуРаспределения();
			сткСоответствиеПоказателейРаспределенияПараметрамРасчета = СоответствиеПоказателейРаспределенияПараметрамРасчета();
			Для каждого Строка Из тбзДанныеДляРасчета Цикл
				
				Если ЗначениеЗаполнено(Строка.Задание) Тогда
					
					Если НЕ СтрокаРасчета.Рейс = Строка.Рейс Тогда
						Продолжить;
					КонецЕсли;
					
					НоваяСтрока = тбзРаспределения.Добавить();
					НоваяСтрока.Ключ = Строка.Задание;
					
					Для каждого текПоказатель Из сткСоответствиеПоказателейРаспределенияПараметрамРасчета Цикл
						НоваяСтрока[текПоказатель.Ключ] = Строка.СтруктураДанные[текПоказатель.Значение];
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			
			сткКолонкиОшибки = Новый Структура("Ключ");
			
			сткРеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоРаспределения, "Формула, СпособРаспределения, Показатель");
			
			Если сткРеквизитыПравила.СпособРаспределения = Перечисления.упСпособыРаспределенияРасходов.ПропорциональноПараметру Тогда
				Формула = упРаспределениеРасходов.ПолучитьФормулуРаспределения(сткРеквизитыПравила.Показатель);
			Иначе
				Формула = сткРеквизитыПравила.Формула;			
			КонецЕсли;
			
			упРаспределениеРасходов.РассчитатьКоэффициентыРаспределения(Формула, тбзРаспределения, сткКолонкиОшибки);
						
			мсвСтроки = тбзРаспределения.НайтиСтроки(Новый Структура("Ключ", СтрокаРасчета.Задание));
			
			Если мсвСтроки.Количество() > 0 Тогда
				текРезультат = мсвСтроки[0].Коэффициент;
			КонецЕсли;
		КонецЕсли;
	
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		
		
		Если ЗначениеЗаполнено(СтрокаРасчета.Задание) Тогда
			
			мсвСтрокиРейса = тбзДанныеДляРасчета.НайтиСтроки(Новый Структура("Задание", СтрокаРасчета.Задание));	
			
			Если мсвСтрокиРейса.Количество() > 0 Тогда
				текРейс = мсвСтрокиРейса[0].Рейс;
			КонецЕсли;

			Если ЗначениеЗаполнено(текРейс) Тогда
							
				тбзРаспределения = ПолучитьТаблицуРаспределения();
				сткСоответствиеПоказателейРаспределенияПараметрамРасчета = СоответствиеПоказателейРаспределенияПараметрамРасчета();
				Для каждого Строка Из тбзДанныеДляРасчета Цикл
					
					Если ЗначениеЗаполнено(Строка.Задание) Тогда
						
						Если НЕ Строка.Рейс = текРейс Тогда
							Продолжить;
						КонецЕсли;
						
						НоваяСтрока = тбзРаспределения.Добавить();
						НоваяСтрока.Ключ = Строка.Задание;
						
						Для каждого текПоказатель Из сткСоответствиеПоказателейРаспределенияПараметрамРасчета Цикл
							НоваяСтрока[текПоказатель.Ключ] = Строка.СтруктураДанные[текПоказатель.Значение];
						КонецЦикла;
					КонецЕсли;
					
				КонецЦикла;
				
				сткКолонкиОшибки = Новый Структура("Ключ");
				
				сткРеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоРаспределения, "Формула, СпособРаспределения, Показатель");
				
				Если сткРеквизитыПравила.СпособРаспределения = Перечисления.упСпособыРаспределенияРасходов.ПропорциональноПараметру Тогда
					Формула = упРаспределениеРасходов.ПолучитьФормулуРаспределения(сткРеквизитыПравила.Показатель);
				Иначе
					Формула = сткРеквизитыПравила.Формула;			
				КонецЕсли;
				
				упРаспределениеРасходов.РассчитатьКоэффициентыРаспределения(Формула, тбзРаспределения, сткКолонкиОшибки);
				
				мсвСтроки = тбзРаспределения.НайтиСтроки(Новый Структура("Ключ", СтрокаРасчета.Задание));
				
				Если мсвСтроки.Количество() > 0 Тогда
					текРезультат = мсвСтроки[0].Коэффициент;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти	

#Область РасчетПоФактическимРасходамБезДетализацииПоЗаданиям

Процедура ПодготовитьВходныеПараметры_ФактическиеРасходы_БезДетализации(Объект, ВходныеПараметры, сткТрассировка)

	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "Расходы");
	ГруппаТарифов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГруппаТарифов");				  
	ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));	
	
	ВходныеПараметры.Вставить("ГруппаТарифов", ГруппаТарифов);
	ВходныеПараметры.Вставить("Организация", Объект.Организация);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", Объект.Ссылка);
	ВходныеПараметры.Вставить("Рейс", Объект.Ссылка);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ХарактеристикиТС", Неопределено);
	ВходныеПараметры.Вставить("ВидПеревозки", Справочники.упВидыПеревозок.ПустаяСсылка());
		
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("Расстояние", 0);	
	ВходныеПараметры.Вставить("ВремяРаботыЧас", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое", '00010101');
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", 0);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());

			
КонецПроцедуры

Процедура ПодготовитьПараметрыРасчета_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке)
	
	мсвРейсы = Новый Массив;
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		
		мсвРейсы.Добавить(Объект.Рейс);
		сткДанныеПоОбъектуРасчета.Вставить("Рейс", Объект.Рейс);
	
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда	
		
		Для каждого Строка Из Объект.Расходы Цикл
			Если мсвРейсы.Найти(Строка.Рейс) = Неопределено Тогда
				мсвРейсы.Добавить(Строка.Рейс);
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		Для каждого Строка Из Объект.Рейсы Цикл
			Если мсвРейсы.Найти(Строка.Рейс) = Неопределено Тогда
				мсвРейсы.Добавить(Строка.Рейс);
			КонецЕсли;
		КонецЦикла;
		сткДанныеПоОбъектуРасчета.Вставить("Рейс", мсвРейсы);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстЗапроса = ТекстЗапроса_ФактическиеРасходыОбщиеДанные_БезДетализации();
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда	
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ПакетЗапросовСхемы = СхемаЗапроса.ПакетЗапросов;
		ЗапросНаХарактеристикиРейса = ПакетЗапросовСхемы[11];
		ОператорВыбратьСхемыЗапросов = ЗапросНаХарактеристикиРейса.Операторы[0];
		Группировка = ОператорВыбратьСхемыЗапросов.Группировка;
		ВыбираемыеПоля = ОператорВыбратьСхемыЗапросов.ВыбираемыеПоля;
		
		Группировка.Добавить(Новый ВыражениеСхемыЗапроса("втРейсыИзОбъекта.Рейс"));
		ВыбираемыеПоля.Добавить("втРейсыИзОбъекта.Рейс");
		
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	    	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	мсвРезультат = Запрос.Выполнить();
	сткДанныеПоОбъектуРасчета.Вставить("Запрос", Запрос);
	сткДанныеПоОбъектуРасчета.Вставить("РезультатЗапроса", мсвРезультат);
	
	ЗаполнитьЗначенияПоказателейЗоны_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);
	
	ЗаполнитьЗначенияПоказателейХарактеристикТС_ФактическиеРасходы(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыДляРасчетаПоДокументуВЦелом, мсвПараметрыДляРасчетаПоКаждойСтроке);

КонецПроцедуры

Функция ТаблицаДанныхДляРасчетаПоКаждойСтроке_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	тбзРезультат = ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета);
			
	Возврат тбзРезультат;
КонецФункции

Функция ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ФактическиеРасходы_БезДетализации(Объект, сткДанныеПоОбъектуРасчета, мсвПараметрыРасчета)
	
	СпособВВодаРасходовПоНесколькимРейсам = Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам;
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("СтруктураДанные");

	Если СпособВВодаРасходовПоНесколькимРейсам Тогда
		тбзРезультат.Колонки.Добавить("Рейс");
	КонецЕсли;
		
	НужныПараметрыГруза = НужныПараметрыГруза(мсвПараметрыРасчета);
	НужныХарактеристикиТС = НужныХарактеристикиТС(мсвПараметрыРасчета);		
	НуженВидПеревозки = НуженВидПеревозки(мсвПараметрыРасчета);
	НуженОбъемныйВес = НуженОбъемныйВес(мсвПараметрыРасчета);
	НужнаОценочнаяСтоимость = НужнаОценочнаяСтоимость(мсвПараметрыРасчета);
	НужныЗоны = НужныЗоны(мсвПараметрыРасчета);
	НужныРасстояния = НужныРасстояния(мсвПараметрыРасчета);
	
	ВыборкаРейс = сткДанныеПоОбъектуРасчета.РезультатЗапроса.Выбрать();
	
	Пока ВыборкаРейс.Следующий() Цикл
			
		НоваяСтрока = тбзРезультат.Добавить();
		НоваяСтрока.СтруктураДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткДанныеПоОбъектуРасчета);
		Если СпособВВодаРасходовПоНесколькимРейсам Тогда
			текРейс = ВыборкаРейс.Рейс;
			НоваяСтрока.Рейс = текРейс;
			НоваяСтрока.СтруктураДанные.Вставить("Рейс", текРейс);
		КонецЕсли;	
		ЗаполнитьЗначенияСвойств(НоваяСтрока.СтруктураДанные, ВыборкаРейс);
	
		Если НужныЗоны Тогда
			Если СпособВВодаРасходовПоНесколькимРейсам Тогда
				НоваяСтрока.СтруктураДанные.Вставить("ЗонаОтправления", сткДанныеПоОбъектуРасчета.ЗонаОтправления.Получить(текРейс));	
				НоваяСтрока.СтруктураДанные.Вставить("ЗонаПолучения", сткДанныеПоОбъектуРасчета.ЗонаПолучения.Получить(текРейс));	
			Иначе
				НоваяСтрока.СтруктураДанные.Вставить("ЗонаОтправления", сткДанныеПоОбъектуРасчета.ЗонаОтправления);	
				НоваяСтрока.СтруктураДанные.Вставить("ЗонаПолучения", сткДанныеПоОбъектуРасчета.ЗонаПолучения);	
			КонецЕсли;						   
		КонецЕсли;
		
		Если НужныХарактеристикиТС ИЛИ НуженВидПеревозки Тогда
			Если СпособВВодаРасходовПоНесколькимРейсам Тогда
				сткХарактеристикиТС = сткДанныеПоОбъектуРасчета.ХарактеристикиТС.Получить(текРейс);
			Иначе
				сткХарактеристикиТС = сткДанныеПоОбъектуРасчета.ХарактеристикиТС;
			конецЕсли;
			Если НЕ сткХарактеристикиТС = Неопределено Тогда
				ЗаполнитьПоСтруктуре(НоваяСтрока.СтруктураДанные, сткХарактеристикиТС);
				Если НуженОбъемныйВес Тогда
					ЗаполнитьОбъемныйВес(НоваяСтрока.СтруктураДанные);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;

	Возврат тбзРезультат;	
		
КонецФункции

Функция ТекстЗапроса_ФактическиеРасходыОбщиеДанные_БезДетализации()
	
	текРезультат =
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Ссылка КАК Рейс,
	|	втРейсы.ВидПеревозки КАК ВидПеревозки,
	|	ЕСТЬNULL(ФактическиеПараметрыРейса.ЗонаПогрузки, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаОтправления,
	|	ЕСТЬNULL(ФактическиеПараметрыРейса.ЗонаРазгрузки, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаПолучения,
	|	ЕСТЬNULL(ФактическиеПараметрыРейса.Время, 0) КАК ВремяРаботыЧас,
	|	ЕСТЬNULL(ФактическиеПараметрыРейса.Расстояние, 0) КАК Расстояние,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТипТС, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)) КАК ТипТС,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОтправленияСПлановое,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяОтправленияПоПлановое,
	|	ЕСТЬNULL(ФактическиеПараметрыРейса.НачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяПолученияСПлановое,
	|	ЕСТЬNULL(ФактическиеПараметрыРейса.ОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяПолученияПоПлановое
	|ПОМЕСТИТЬ втРейсыИзОбъекта
	|ИЗ
	|	Документ.упРейс КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФактическиеПараметрыРейса
	|	ПО втРейсы.Ссылка = ФактическиеПараметрыРейса.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО втРейсы.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|	ПО втРейсы.Ссылка = упПлановыеПараметрыРейса.Рейс
	|ГДЕ втРейсы.Ссылка В  (&мсвРейсы)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРейсыИзОбъекта.Рейс КАК Рейс,
	|	втРейсыИзОбъекта.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРейсыИзОбъекта.ТипТС КАК ТипТС,
	|	втРейсыИзОбъекта.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.Грузоподъемность = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.Грузоподъемность
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.Грузоподъемность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ГрузоподъемностьТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.Объем = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.Объем
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.Объем
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбъемТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.КоличествоЗагрузочныхМетров = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.КоличествоЗагрузочныхМетров
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.КоличествоЗагрузочныхМетров
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
	|	ВЫБОР
	|		КОГДА НЕ втРейсыИзОбъекта.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|				И НЕ втРейсыИзОбъекта.ТранспортноеСредство.КоличествоМест = 0
	|			ТОГДА втРейсыИзОбъекта.ТранспортноеСредство.КоличествоМест
	|		КОГДА НЕ втРейсыИзОбъекта.ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|			ТОГДА втРейсыИзОбъекта.ТипТС.КоличествоМест
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоМестТС
	|ПОМЕСТИТЬ втТранспортныеСредстваПоРейсам
	|ИЗ
	|	втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упРаботыПоРейсуСрезПоследних.Операция КАК Операция,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упРаботыПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
	|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
	|	упРаботыПоРейсуСрезПоследних.Масса КАК Масса,
	|	упРаботыПоРейсуСрезПоследних.Объем КАК Объем,
	|	упРаботыПоРейсуСрезПоследних.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втРейсыИзОбъекта.ЗонаОтправления КАК ЗонаОтправления,
	|	втРейсыИзОбъекта.ЗонаПолучения КАК ЗонаПолучения,
	|	втРейсыИзОбъекта.ВидПеревозки КАК ВидПеревозки,
	|	упРаботыПоРейсуСрезПоследних.Отменена КАК Отменена,
	|	упРаботыПоРейсуСрезПоследних.Выполнена КАК Выполнена
	|ПОМЕСТИТЬ втВыполненныеРаботыПоЗаданиям
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК упРаботыПоРейсуСрезПоследних
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|	ПО втРейсыИзОбъекта.Рейс = упРаботыПоРейсуСрезПоследних.Рейс
	|ГДЕ упРаботыПоРейсуСрезПоследних.Выполнена
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботыПоРейсу.Рейс КАК Рейс,
	|	тбРаботыПоРейсу.Операция КАК Операция,
	|	тбРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	тбРаботыПоРейсу.Задание КАК Задание,
	|	тбРаботыПоРейсу.ЗонаОтправления КАК ЗонаОтправления,
	|	тбРаботыПоРейсу.ЗонаПолучения КАК ЗонаПолучения,
	|	тбРаботыПоРейсу.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса * тбРаботыПоРейсу.КоличествоГрузов, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса * тбРаботыПоРейсу.КоличествоГрузов, тбРаботыПоРейсу.Масса))
	|	КОНЕЦ КАК МассаГруза,
	|	ВЫБОР
	|		КОГДА тбРаботыПоРейсу.Отменена
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем * тбРаботыПоРейсу.КоличествоГрузов, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем * тбРаботыПоРейсу.КоличествоГрузов, тбРаботыПоРейсу.Объем))
	|	КОНЕЦ КАК ОбъемГруза,
	|	тбРаботыПоРейсу.Выполнена КАК Выполнена
	|ПОМЕСТИТЬ втВыполненныеРаботы
	|ИЗ
	|	втВыполненныеРаботыПоЗаданиям КАК тбРаботыПоРейсу
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втРаботыПоРейсуПоЗаданиям КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втРаботыПоРейсуПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И тбРаботыПоРейсу.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И тбРаботыПоРейсу.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбРаботы.Идентификатор КАК Идентификатор,
	|	тбРаботы.Рейс КАК Рейс,
	|	СУММА(ВЫБОР
	|		КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|			КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|				ТОГДА - 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ * тбРаботы.МассаГруза) КАК МассаГруза,
	|	СУММА(ВЫБОР
	|		КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|			КОГДА тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|					ИЛИ тбРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|				ТОГДА - 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ * тбРаботы.ОбъемГруза) КАК ОбъемГруза
	|ПОМЕСТИТЬ втРаботыВТочке
	|ИЗ
	|	втВыполненныеРаботы КАК тбРаботы
	|СГРУППИРОВАТЬ ПО
	|	тбРаботы.Идентификатор,
	|	тбРаботы.Рейс
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботыВТочке.Рейс КАК Рейс,
	|	МАКСИМУМ(втРаботыВТочке.МассаГруза) КАК МассаГруза,
	|	МАКСИМУМ(втРаботыВТочке.ОбъемГруза) КАК ОбъемГруза
	|ПОМЕСТИТЬ втМассаОбъемПоРейсу
	|ИЗ
	|	втРаботыВТочке КАК втРаботыВТочке
	|СГРУППИРОВАТЬ ПО
	|	втРаботыВТочке.Рейс
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВыполненныеРаботы.Рейс КАК Рейс,
	|	СУММА(ВЫБОР
	|		КОГДА втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|				ИЛИ втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|				ИЛИ втВыполненныеРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЭтоТочкаРазгрузки,
	|	втВыполненныеРаботы.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втТочкиПоРейсам
	|ИЗ
	|	втВыполненныеРаботы КАК втВыполненныеРаботы
	|СГРУППИРОВАТЬ ПО
	|	втВыполненныеРаботы.Рейс,
	|	втВыполненныеРаботы.Идентификатор
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТочкиПоРейсам.Рейс КАК Рейс,
	|	СУММА(ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаПогрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекПогрузки,
	|	СУММА(ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаРазгрузки > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоТочекРазгрузки,
	|	СУММА(1) КАК КоличествоТочекВМаршруте
	|ПОМЕСТИТЬ втКоличествоТочекПоРейсам
	|ИЗ
	|	втТочкиПоРейсам КАК втТочкиПоРейсам
	|СГРУППИРОВАТЬ ПО
	|	втТочкиПоРейсам.Рейс
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаПогрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие
	|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
	|	КОНЕЦ КАК ВремяНачалаПогрузкиПлановое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаПогрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ВремяОкончанияПогрузкиПлановое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаПогрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт
	|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
	|	КОНЕЦ КАК ВремяНачалаПогрузкиФактическое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаПогрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.УбытиеФакт
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ВремяОкончанияПогрузкиФактическое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаРазгрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие
	|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
	|	КОНЕЦ КАК ВремяНачалаРазгрузкиПлановое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаРазгрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ВремяОкончанияРазгрузкиПлановое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаРазгрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.ПрибытиеФакт
	|		ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
	|	КОНЕЦ КАК ВремяНачалаРазгрузкиФактическое,
	|	ВЫБОР
	|		КОГДА втТочкиПоРейсам.ЭтоТочкаРазгрузки > 0
	|			ТОГДА упМаршрутПоРейсуСрезПоследних.УбытиеФакт
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ВремяОкончанияРазгрузкиФактическое,
	|	упМаршрутПоРейсуСрезПоследних.УбытиеФакт КАК Убытие,
	|	упМаршрутПоРейсуСрезПоследних.ВремяПростояПоВинеЗаказчикаЧасы КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	упМаршрутПоРейсуСрезПоследних.ВремяПростояПоВинеПеревозчикаЧасы КАК ВремяПростояПоВинеПеревозчикаЧасы
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК упМаршрутПоРейсуСрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ втТочкиПоРейсам КАК втТочкиПоРейсам
	|	ПО ИСТИНА
	|		И упМаршрутПоРейсуСрезПоследних.Рейс = втТочкиПоРейсам.Рейс
	|		И упМаршрутПоРейсуСрезПоследних.Идентификатор = втТочкиПоРейсам.Идентификатор
	|ГДЕ ИСТИНА
	|	И НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
	|	И упМаршрутПоРейсуСрезПоследних.Пройдена
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Рейс КАК Рейс,
	|	МАКСИМУМ(втМаршрут.Убытие) КАК Убытие
	|ПОМЕСТИТЬ ДатыУбытияПоПоследнимТочкамРейсов
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Рейс
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.Рейс КАК Рейс,
	|	МИНИМУМ(втМаршрут.ВремяНачалаПогрузкиПлановое) КАК ВремяНачалаПогрузкиПлановое,
	|	МАКСИМУМ(втМаршрут.ВремяОкончанияПогрузкиПлановое) КАК ВремяОкончанияПогрузкиПлановое,
	|	МИНИМУМ(втМаршрут.ВремяНачалаПогрузкиФактическое) КАК ВремяНачалаПогрузкиФактическое,
	|	МАКСИМУМ(втМаршрут.ВремяОкончанияПогрузкиФактическое) КАК ВремяОкончанияПогрузкиФактическое,
	|	МИНИМУМ(втМаршрут.ВремяНачалаРазгрузкиПлановое) КАК ВремяНачалаРазгрузкиПлановое,
	|	МАКСИМУМ(втМаршрут.ВремяОкончанияРазгрузкиПлановое) КАК ВремяОкончанияРазгрузкиПлановое,
	|	МИНИМУМ(втМаршрут.ВремяНачалаРазгрузкиФактическое) КАК ВремяНачалаРазгрузкиФактическое,
	|	МАКСИМУМ(втМаршрут.ВремяОкончанияРазгрузкиФактическое) КАК ВремяОкончанияРазгрузкиФактическое,
	|	СУММА(втМаршрут.ВремяПростояПоВинеЗаказчикаЧасы) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(втМаршрут.ВремяПростояПоВинеПеревозчикаЧасы) КАК ВремяПростояПоВинеПеревозчикаЧасы
	|ПОМЕСТИТЬ втВремяПоРейсам
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Рейс
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втРейсыИзОбъекта.ВремяРаботыЧас) КАК ВремяРаботыЧас,
	|	СУММА(втРейсыИзОбъекта.Расстояние) КАК Расстояние,
	|	МИНИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяНачалаПогрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяНачалаПогрузкиПлановое,
	|	МАКСИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяОкончанияПогрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяОкончанияПогрузкиПлановое,
	|	МИНИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяНачалаПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяНачалаПогрузкиФактическое,
	|	МАКСИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяОкончанияПогрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяОкончанияПогрузкиФактическое,
	|	МИНИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяНачалаРазгрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяНачалаРазгрузкиПлановое,
	|	МАКСИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяОкончанияРазгрузкиПлановое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяОкончанияРазгрузкиПлановое,
	|	МИНИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяНачалаРазгрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяНачалаРазгрузкиФактическое,
	|	МАКСИМУМ(ЕСТЬNULL(втВремяПоРейсам.ВремяОкончанияРазгрузкиФактическое, ДАТАВРЕМЯ(1, 1, 1))) КАК ВремяОкончанияРазгрузкиФактическое,
	|	СУММА(ЕСТЬNULL(втВремяПоРейсам.ВремяПростояПоВинеЗаказчикаЧасы, 0)) КАК ВремяПростояПоВинеЗаказчикаЧасы,
	|	СУММА(ЕСТЬNULL(втВремяПоРейсам.ВремяПростояПоВинеПеревозчикаЧасы, 0)) КАК ВремяПростояПоВинеПеревозчикаЧасы,
	|	МИНИМУМ(втРейсыИзОбъекта.ВремяОтправленияСПлановое) КАК ВремяОтправленияСПлановое,
	|	МАКСИМУМ(втРейсыИзОбъекта.ВремяОтправленияПоПлановое) КАК ВремяОтправленияПоПлановое,
	|	МИНИМУМ(втРейсыИзОбъекта.ВремяПолученияСПлановое) КАК ВремяПолученияСПлановое,
	|	МАКСИМУМ(втРейсыИзОбъекта.ВремяПолученияПоПлановое) КАК ВремяПолученияПоПлановое,
	|	МАКСИМУМ(втМассаОбъемПоРейсу.МассаГруза) КАК МассаГруза,
	|	МАКСИМУМ(втМассаОбъемПоРейсу.ОбъемГруза) КАК ОбъемГруза,
	|	СУММА(ЕСТЬNULL(втКоличествоТочекПоРейсам.КоличествоТочекПогрузки, 0)) КАК КоличествоТочекПогрузки,
	|	СУММА(ЕСТЬNULL(втКоличествоТочекПоРейсам.КоличествоТочекРазгрузки, 0)) КАК КоличествоТочекРазгрузки,
	|	СУММА(ЕСТЬNULL(втКоличествоТочекПоРейсам.КоличествоТочекВМаршруте, 0)) КАК КоличествоТочекВМаршруте
	|ИЗ
	|	втРейсыИзОбъекта КАК втРейсыИзОбъекта
	|	ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоРейсам КАК втВремяПоРейсам
	|	ПО втРейсыИзОбъекта.Рейс = втВремяПоРейсам.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМассаОбъемПоРейсу КАК втМассаОбъемПоРейсу
	|	ПО втРейсыИзОбъекта.Рейс = втМассаОбъемПоРейсу.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТочекПоРейсам КАК втКоличествоТочекПоРейсам
	|	ПО втРейсыИзОбъекта.Рейс = втКоличествоТочекПоРейсам.Рейс
	|";

	Возврат текРезультат;
	
КонецФункции

#КонецОбласти	

#Область РасчетПоПредложениюПеревозчика

Процедура ПодготовитьВходныеПараметры_ЗаявкаНаТС(Объект, ВходныеПараметры, сткТрассировка)
	
	текРейс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "Рейс");
	
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "ПлановыеРасходы");

	мсвСписокСтатей = ВходныеПараметры.СписокСтатей;
	Если Ложь
		Или ТипЗнч(Объект) =  Тип("ДанныеФормыСтруктура")
		Или ТипЗнч(Объект) = Тип("ДокументОбъект.упЗаявкаНаТС") 
	Тогда
		ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));		
	КонецЕсли;
	
	// Так как перевозчик в рейсе может браться из подтверждения, то группа тарифов иногда передается.
	Если Не ВходныеПараметры.Свойство("ГруппаТарифов") Тогда
		ГруппаТарифов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГруппаТарифов");				  	
		ВходныеПараметры.Вставить("ГруппаТарифов", ГруппаТарифов);	
	КонецЕсли;
	
	ПараметрыРейса = упРейс.ПараметрыРейса(текРейс);
	
	сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текРейс, "Организация, ВидПеревозки");
	
	ВходныеПараметры.Вставить("Организация", сткРеквизитыРейса.Организация);
	ВходныеПараметры.Вставить("ВремяРаботыЧас", ПараметрыРейса.Время);
	ВходныеПараметры.Вставить("Расстояние", ПараметрыРейса.Расстояние);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", текРейс);
	ВходныеПараметры.Вставить("Рейс", текРейс);
	
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма", "СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС", "СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ДополнительныеКолонкиПоСтроке", "Задание");
	
	ВходныеПараметры.Вставить("ТранспортноеСредство", Объект.Ссылка.ТранспортноеСредство);
	ВходныеПараметры.Вставить("ТипТС", ПараметрыРейса.ТипТС);
	ВходныеПараметры.Вставить("Перевозчик", Объект.Ссылка.Перевозчик);
	ВходныеПараметры.Вставить("ЗонаОтправления", ПараметрыРейса.ЗонаПогрузки);
	ВходныеПараметры.Вставить("ЗонаПолучения", ПараметрыРейса.ЗонаРазгрузки);
	ВходныеПараметры.Вставить("ВидПеревозки", сткРеквизитыРейса.ВидПеревозки);
	
	ВходныеПараметры.Вставить("ГрузоподъемностьТС", 0);		
	ВходныеПараметры.Вставить("ОбъемТС", 0);		
	ВходныеПараметры.Вставить("КоличествоМестТС", 0);		
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровТС", 0);		
	
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", 0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое",'00010101');
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", ПараметрыРейса.СуммаИнкассации);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());

		
КонецПроцедуры

Процедура ПодготовитьВходныеПараметры_ПредложениеПеревозчика(Объект, ВходныеПараметры, сткТрассировка)
	 		
	текРейс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаявкаНаТС, "Рейс");
	
	ВходныеПараметры.Вставить("ИмяРеквизитаСтатьи", "СтатьяРасходов");
	ВходныеПараметры.Вставить("ИмяТабличнойЧастиСтатей", "ПлановыеРасходы");

	мсвСписокСтатей = ВходныеПараметры.СписокСтатей;
	Если Ложь
		Или ТипЗнч(Объект) =  Тип("ДанныеФормыСтруктура")
		Или ТипЗнч(Объект) = Тип("ДокументОбъект.упПредложениеПеревозчика")
	Тогда
		ВходныеПараметры.Вставить("СписокСтатей", Объект[ВходныеПараметры.ИмяТабличнойЧастиСтатей].Выгрузить().ВыгрузитьКолонку(ВходныеПараметры.ИмяРеквизитаСтатьи));		
	КонецЕсли;
	
	// Так как перевозчик в рейсе может браться из подтверждения, то группа тарифов иногда передается.
	Если Не ВходныеПараметры.Свойство("ГруппаТарифов") Тогда
		ГруппаТарифов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГруппаТарифов");				  	
		ВходныеПараметры.Вставить("ГруппаТарифов", ГруппаТарифов);	
	КонецЕсли;
	
	ПараметрыРейса = упРейс.ПараметрыРейса(текРейс);
	
	сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(текРейс, "Организация, ВидПеревозки");
	
	ВходныеПараметры.Вставить("Организация", сткРеквизитыРейса.Организация);
	ВходныеПараметры.Вставить("ВремяРаботыЧас", ПараметрыРейса.Время);
	ВходныеПараметры.Вставить("Расстояние", ПараметрыРейса.Расстояние);
	ВходныеПараметры.Вставить("Период", Объект.Дата);
	ВходныеПараметры.Вставить("Ссылка", текРейс);
	ВходныеПараметры.Вставить("Рейс", текРейс);
	ВходныеПараметры.Вставить("ИмяРеквизитаСумма","СуммаРасходы");
	ВходныеПараметры.Вставить("ИмяРеквизитаСуммаНДС","СуммаРасходыНДС");
	ВходныеПараметры.Вставить("ДополнительныеКолонкиПоСтроке",	"Задание");
	
	ВходныеПараметры.Вставить("ТранспортноеСредство", Объект.Ссылка.ТранспортноеСредство);
	ВходныеПараметры.Вставить("ТипТС", ПараметрыРейса.ТипТС);
	ВходныеПараметры.Вставить("Перевозчик", Объект.Ссылка.Перевозчик);
	ВходныеПараметры.Вставить("ЗонаОтправления", ПараметрыРейса.ЗонаПогрузки);
	ВходныеПараметры.Вставить("ЗонаПолучения", ПараметрыРейса.ЗонаРазгрузки);
	ВходныеПараметры.Вставить("ВидПеревозки", сткРеквизитыРейса.ВидПеревозки);
	
	ВходныеПараметры.Вставить("ГрузоподъемностьТС", 0);		
	ВходныеПараметры.Вставить("ОбъемТС", 0);		
	ВходныеПараметры.Вставить("КоличествоМестТС", 0);		
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровТС", 0);		
	
	ВходныеПараметры.Вставить("МассаГруза", 0);	
	ВходныеПараметры.Вставить("ОбъемГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоМестГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗагрузочныхМетровГруза", 0);	
	ВходныеПараметры.Вставить("ОценочнаяСтоимостьГруза", 0);	
	ВходныеПараметры.Вставить("КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте", 0);	
	
	ВходныеПараметры.Вставить("ВремяПростояПоВинеЗаказчикаЧасы",   0);
	ВходныеПараметры.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", 0);
	
	ВходныеПараметры.Вставить("ВремяОтправленияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОтправленияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияСПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяПолученияПоПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяНачалаРазгрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияПогрузкиФактическое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиПлановое", '00010101');
	ВходныеПараметры.Вставить("ВремяОкончанияРазгрузкиФактическое",'00010101');
	
	
	ВходныеПараметры.Вставить("КоличествоТочекВМаршруте", 0);
	ВходныеПараметры.Вставить("КоличествоТочекПогрузки", 0);
	ВходныеПараметры.Вставить("КоличествоТочекРазгрузки", 0);
	ВходныеПараметры.Вставить("ФактическиеРасходыПоЗаявке", 0);
	ВходныеПараметры.Вставить("ПлановаяСуммаИнкассации", ПараметрыРейса.СуммаИнкассации);
	ВходныеПараметры.Вставить("ТемпературныйРежим", Справочники.упТемпературныеРежимы.ПустаяСсылка());
	ВходныеПараметры.Вставить("ТипГруза", Справочники.упТипыГрузов.ПустаяСсылка());
	
КонецПроцедуры

// ТаблицаДанныхДляРасчетаПоКаждойСтроке_ПредложениеПеревозчика 	- используется функция рейса.

// ТаблицаДанныхДляРасчетаПоДокументуВЦелом_ПредложениеПеревозчика 	- используется функция рейса.

#КонецОбласти	

Функция ЕстьРасчетПоПараметру(мсвПараметрыРасчета, ИмяПараметра)
	
	Возврат НЕ мсвПараметрыРасчета.Найти(Перечисления.упПараметрыРасчета[ИмяПараметра]) = Неопределено; 
	
КонецФункции

Функция ОбъемныйВесГруза(сткДанные)
	текРезультат = 0;
	Если НЕ сткДанные.ОбъемГруза = 0 
		И НЕ сткДанные.ОбъемТС = 0 Тогда
		текРезультат = ?(сткДанные.МассаГруза / сткДанные.ОбъемГруза > сткДанные.ГрузоподъемностьТС / сткДанные.ОбъемТС, сткДанные.МассаГруза, сткДанные.ГрузоподъемностьТС * сткДанные.ОбъемГруза / сткДанные.ОбъемТС);
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужныПараметрыГруза(мсвПараметрыРасчета)
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "МассаГруза") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОбъемГруза")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоМестГруза")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоЗагрузочныхМетровГруза") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОценочнаяСтоимостьГруза") Тогда
		текРезультат = Истина;	
	КонецЕсли;	
	Возврат текРезультат;
КонецФункции

Функция НужныХарактеристикиТС(мсвПараметрыРасчета)
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ГрузоподъемностьТС")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОбъемТС")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоЗагрузочныхМетровТС")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоМестТС") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ТипТС") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОбъемныйВес") Тогда
		текРезультат	= Истина;
	КонецЕсли;	
	Возврат текРезультат;
КонецФункции

Функция НуженОбъемныйВес(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОбъемныйВес") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужнаОценочнаяСтоимость(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ОценочнаяСтоимостьГруза") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужныРасстояния(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "Расстояние")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяРаботыЧас") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужныЗоны(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ЗонаОтправления")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ЗонаПолучения") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НуженТипТС(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ТипТС") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НуженРейс(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "Рейс") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужныФактическиеРасходы(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ФактическиеРасходыПоЗаявке") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужнаПлановаяСуммаИнкассации(мсвПараметрыРасчета)
	текРезультат = Ложь;
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ПлановаяСуммаИнкассации") Тогда
		текРезультат = Истина;
	КонецЕсли;
	Возврат текРезультат;
КонецФункции

Функция НужныВремянныеПараметры(мсвПараметрыРасчета)
	
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяОтправленияСПлановое")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяОтправленияПоПлановое")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяПолученияСПлановое")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяПолученияПоПлановое") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяНачалаПогрузкиПлановое") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяНачалаПогрузкиФактическое")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяНачалаРазгрузкиПлановое") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяНачалаРазгрузкиФактическое") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяОкончанияПогрузкиПлановое")
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяОкончанияПогрузкиФактическое") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяОкончанияРазгрузкиПлановое") 
		ИЛИ ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВремяОкончанияРазгрузкиФактическое") Тогда
		текРезультат	= Истина;
	КонецЕсли;	
	Возврат текРезультат;
		
КонецФункции

Функция НужныКоличествоТочек(мсвПараметрыРасчета)
	
	текРезультат = Ложь;	
	Если Ложь
		Или ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоТочекВМаршруте")
		Или ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоТочекПогрузки")
		Или ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоТочекРазгрузки")
	Тогда
		текРезультат = Истина;
	КонецЕсли;	
	Возврат текРезультат;
		
КонецФункции

Функция НужныКоличествоЗаданий(мсвПараметрыРасчета)
	
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "КоличествоЗаданийСРаботамиПоПеревозкеГрузаВМаршруте") Тогда
		текРезультат	= Истина;
	КонецЕсли;	
	Возврат текРезультат;
		
КонецФункции

Функция НуженТипГруза(мсвПараметрыРасчета)
	
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ТипГруза") Тогда
		текРезультат	= Истина;
	КонецЕсли;	
	Возврат текРезультат;
		
КонецФункции

Функция НуженТемпературныйРежим(мсвПараметрыРасчета)
	
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ТемпературныйРежим") Тогда
		текРезультат	= Истина;
	КонецЕсли;	
	Возврат текРезультат;
		
КонецФункции

Функция СтруктураХаратеристикГруза()
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("МассаГруза", 						0);	
	сткРезультат.Вставить("ОбъемГруза", 						0);	
	сткРезультат.Вставить("КоличествоМестГруза", 				0);	
	сткРезультат.Вставить("КоличествоЗагрузочныхМетровГруза", 	0);	
	сткРезультат.Вставить("ОценочнаяСтоимостьГруза", 			0);	
	сткРезультат.Вставить("Расстояние", 						0);	
	сткРезультат.Вставить("ВремяРаботыЧас", 					0);	
	сткРезультат.Вставить("ТипГруза",							Справочники.упТипыГрузов.ПустаяСсылка());	
	сткРезультат.Вставить("ТемпературныйРежим", 				Справочники.упТемпературныеРежимы.ПустаяСсылка());	
	
	Возврат сткРезультат;
	
КонецФункции

Процедура ЗаполнитьПоСтруктуре(сткДляЗаполнения, сткДанные)
	
	Для каждого Элемент Из сткДанные Цикл
		сткДляЗаполнения.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
		
КонецПроцедуры

Функция СтруктураХарактеристикиТС()
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ГрузоподъемностьТС", 			0);
	сткРезультат.Вставить("ОбъемТС", 						0);
	сткРезультат.Вставить("КоличествоЗагрузочныхМетровТС", 	0);
	сткРезультат.Вставить("КоличествоМестТС", 				0);
	сткРезультат.Вставить("ТипТС",							Справочники.упТипыТС.ПустаяСсылка());
	
	Возврат сткРезультат;
	
КонецФункции

Процедура ЗаполнитьОбъемныйВес(сткДляЗаполнения)
	сткДляЗаполнения.Вставить("ОбъемныйВес", 	ОбъемныйВесГруза(сткДляЗаполнения));			
КонецПроцедуры

Функция ПолучитьРасстояниеИВремя(АдресОтправления, АдресПолучения, ВремяПрохожденияТочекОтправленияПолучения = 0)
	сткРасстояния = упМаршрутизация.ПолучитьРасстояниеИВремя(АдресОтправления, АдресПолучения);	
	Если сткРасстояния.Расстояние = Неопределено Тогда
		сткРасстояния.Расстояние = 0;
	КонецЕсли;
	Если сткРасстояния.Время = Неопределено Тогда
		сткРасстояния.Время = ВремяПрохожденияТочекОтправленияПолучения;
	Иначе
		сткРасстояния.Время = (сткРасстояния.Время + ВремяПрохожденияТочекОтправленияПолучения)/ 3600;
	КонецЕсли;

	Возврат сткРасстояния;
КонецФункции

Функция ПолучитьРасстояниеИВремяМеждуГеографическимиЗонами(ЗонаОтправления, ЗонаПолучения)
	сткРасстояния = упМаршрутизация.ПолучитьРасстояниеИВремяМеждуГеографическимиЗонами(ЗонаОтправления, ЗонаПолучения);	
	Если сткРасстояния.Расстояние = Неопределено Тогда
		сткРасстояния.Расстояние = 0;
	КонецЕсли;
	Если сткРасстояния.Время = Неопределено Тогда
		сткРасстояния.Время = 0;
	Иначе
		сткРасстояния.Время = сткРасстояния.Время / 3600;
	КонецЕсли;

	Возврат сткРасстояния;
КонецФункции

// Функция - Таблица расстояний и затраченного времени для каждого объекта.
//
// Параметры:
//  дрвДетализированныйМаршрут - Дерево - на первом уровне упорядоченные точки, на втором работы в точке по объекту.
//  ИмяОбъекта - Строка - имя колонки объекта по которому вычисляется,
//    расстояние и время, например, "Задание" или "Грузовое место".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - содержит список объектов с расстояниями и временем.
//
Функция ТаблицаРасстоянийИЗатраченногоВремениДляКаждогоОбъекта(дрвДетализированныйМаршрут, СчитатьОбщееРасстояние = Ложь, ОбъектПустаяСсылка)
	
	тбзРезультат = Новый ТаблицаЗначений;
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(ТипЗнч(ОбъектПустаяСсылка));
	тбзРезультат.Колонки.Добавить("Объект",			Новый ОписаниеТипов(мсвТипов));
	тбзРезультат.Колонки.Добавить("Расстояние",		Новый ОписаниеТипов("Число"));
	тбзРезультат.Колонки.Добавить("ВремяРаботыЧас",	Новый ОписаниеТипов("Число"));
	
	тбзРезультат.Колонки.Добавить("ВремяНачалаПогрузкиПлановое", 		Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяНачалаПогрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияПогрузкиПлановое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияПогрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	
	тбзРезультат.Колонки.Добавить("ВремяНачалаРазгрузкиПлановое", 		Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяНачалаРазгрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияРазгрузкиПлановое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияРазгрузкиФактическое", Новый ОписаниеТипов("Дата"));
	
	СтрокаТочкиМаршрута = дрвДетализированныйМаршрут.Строки;
	
	мсвОбъекты 				= Новый Массив; // По наличию в нем объекта определяем едет ли еще объект, то есть нужно ли по нему учитывать расстояние и время.
											 
	мсвУникальныеОбъекты    = Новый Массив; // Для каждого элемента массива рассчитывается расстояние и время в точке.
											// Добавлен для учета таких объектов как "задание", потому что по нему может быть несколько погрузок и разгрузок,
											// а учет должен вестись только для одного объекта.
											// Для "грузовых мест" достаточно бы было только мсвОбъекты.
	
	ВремяУбытияИзПредыдущейТочки	= Неопределено;
	
	РасстояниеВсего 		= 0;
	ВремяВсего				= 0;
	
	
	Для каждого СтрокаТочка Из СтрокаТочкиМаршрута Цикл
		ВремяПрибытия		= СтрокаТочка.Прибытие;
		ВремяУбытия			= СтрокаТочка.Убытие;
		ВремяПрибытияПлан	= СтрокаТочка.ПрибытиеПлан;
		ВремяУбытияПлан		= СтрокаТочка.УбытиеПлан;
		Расстояние			= СтрокаТочка.РасстояниеОтПредыдущейточки;
		ВремяВЭтойТочке 	= (ВремяУбытия - ВремяПрибытия)/3600;
		ВремяДоЭтойТочки	= ?(ВремяУбытияИзПредыдущейТочки = Неопределено, 0, (ВремяПрибытия - ВремяУбытияИзПредыдущейТочки)/3600);
		ВремяУбытияИзПредыдущейТочки = ВремяУбытия;
		
		РасстояниеВсего 	= РасстояниеВсего + Расстояние;
		ВремяВсего			= ВремяВсего + ВремяДоЭтойТочки + ВремяВЭтойТочке;
			
		Для каждого ЭлементОбъект Из мсвУникальныеОбъекты Цикл
			мсвСтроки = тбзРезультат.НайтиСтроки(Новый Структура("Объект", ЭлементОбъект));
			Если мсвСтроки.Количество()>0 Тогда
				мсвСтроки[0].Расстояние 	= мсвСтроки[0].Расстояние + Расстояние;
				мсвСтроки[0].ВремяРаботыЧас = мсвСтроки[0].ВремяРаботыЧас + ВремяДоЭтойТочки + ВремяВЭтойТочке;
			КонецЕсли;
		КонецЦикла;
			
		СтрокиПоОбъектам = СтрокаТочка.Строки;
		
		Для каждого СтрокаОбъект Из СтрокиПоОбъектам Цикл
			
			текОбъект 	= СтрокаОбъект.Объект;
			текОперация	= СтрокаОбъект.Операция;
			
			Если упПрохождениеТочкиКлиентСервер.ОперацияПогрузки(текОперация) Тогда
				Если мсвОбъекты.Найти(текОбъект) = Неопределено Тогда
					мсвУникальныеОбъекты.Добавить(текОбъект);
					
					НоваяСтрока = тбзРезультат.Добавить();
					НоваяСтрока.Объект 			= текОбъект;
					НоваяСтрока.Расстояние 		= 0;
					НоваяСтрока.ВремяРаботыЧас 	= ВремяВЭтойТочке;	
					НоваяСтрока.ВремяНачалаПогрузкиПлановое			= ВремяПрибытияПлан;
					НоваяСтрока.ВремяНачалаПогрузкиФактическое		= ВремяПрибытия;
					НоваяСтрока.ВремяОкончанияПогрузкиПлановое		= ВремяУбытияПлан;
					НоваяСтрока.ВремяОкончанияПогрузкиФактическое	= ВремяУбытия;
					
					мсвОбъекты.Добавить(текОбъект);
				КонецЕсли;
				//мсвОбъекты.Добавить(текОбъект);
				
			ИначеЕсли упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(текОперация) Тогда	
				
				//текИндекс = мсвОбъекты.Найти(текОбъект);
				//Если НЕ текИндекс = Неопределено Тогда
				//	мсвОбъекты.Удалить(текИндекс);
				//КонецЕсли;
				//
				//текИндекс = мсвОбъекты.Найти(текОбъект);
				//Если текИндекс = Неопределено Тогда
				текИндекс = мсвУникальныеОбъекты.Найти(текОбъект);	
					Если НЕ текИндекс = Неопределено Тогда
						мсвУникальныеОбъекты.Удалить(текИндекс);
						
						мсвСтроки = тбзРезультат.НайтиСтроки(Новый Структура("Объект", текОбъект));
						Если мсвСтроки.Количество()>0 Тогда
							//мсвСтроки[0].ВремяНачалаРазгрузкиПлановое 		= ВремяПрибытияПлан;
							//мсвСтроки[0].ВремяНачалаРазгрузкиФактическое 	= ВремяПрибытия;
							//мсвСтроки[0].ВремяОкончанияРазгрузкиПлановое 	= ВремяУбытияПлан;
							//мсвСтроки[0].ВремяОкончанияРазгрузкиФактическое = ВремяУбытия;
							
							НайденнаяСтрока = мсвСтроки[0];
							Если ЗначениеЗаполнено(ВремяПрибытияПлан) Тогда
								Если ЗначениеЗаполнено(НайденнаяСтрока.ВремяНачалаРазгрузкиПлановое) Тогда
									НайденнаяСтрока.ВремяНачалаРазгрузкиПлановое = Мин(ВремяПрибытияПлан, НайденнаяСтрока.ВремяНачалаРазгрузкиПлановое);
								Иначе	
									НайденнаяСтрока.ВремяНачалаРазгрузкиПлановое = ВремяПрибытияПлан;
								КонецЕсли; 
							КонецЕсли;
							
							Если ЗначениеЗаполнено(ВремяПрибытия) Тогда
								Если ЗначениеЗаполнено(НайденнаяСтрока.ВремяНачалаРазгрузкиФактическое) Тогда
									НайденнаяСтрока.ВремяНачалаРазгрузкиФактическое = Мин(ВремяПрибытия, НайденнаяСтрока.ВремяНачалаРазгрузкиФактическое);
								Иначе	
									НайденнаяСтрока.ВремяНачалаРазгрузкиФактическое = ВремяПрибытия;
								КонецЕсли; 
							КонецЕсли;
							Если ЗначениеЗаполнено(ВремяУбытияПлан) Тогда
								Если ЗначениеЗаполнено(НайденнаяСтрока.ВремяОкончанияРазгрузкиПлановое) Тогда
									НайденнаяСтрока.ВремяОкончанияРазгрузкиПлановое = Мин(ВремяУбытияПлан, НайденнаяСтрока.ВремяОкончанияРазгрузкиПлановое);
								Иначе	
									НайденнаяСтрока.ВремяОкончанияРазгрузкиПлановое = ВремяУбытияПлан;
								КонецЕсли; 
							КонецЕсли;
							Если ЗначениеЗаполнено(ВремяУбытия) Тогда
								Если ЗначениеЗаполнено(НайденнаяСтрока.ВремяОкончанияРазгрузкиФактическое) Тогда
									НайденнаяСтрока.ВремяОкончанияРазгрузкиФактическое = Мин(ВремяУбытия, НайденнаяСтрока.ВремяОкончанияРазгрузкиФактическое);
								Иначе	
									НайденнаяСтрока.ВремяОкончанияРазгрузкиФактическое = ВремяУбытия;
								КонецЕсли; 
							КонецЕсли;
							
					КонецЕсли;
				
					КонецЕсли;
				//КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если СчитатьОбщееРасстояние Тогда
		
		ВремяНачалаПогрузкиПлановое 		= '39991231';
		ВремяНачалаПогрузкиФактическое		= '39991231';
		ВремяОкончанияПогрузкиПлановое  	= '00010101';
		ВремяОкончанияПогрузкиФактическое	= '00010101';
		
		ВремяНачалаРазгрузкиПлановое		= '39991231';
		ВремяНачалаРазгрузкиФактическое		= '39991231';
		ВремяОкончанияРазгрузкиПлановое		= '00010101';
		ВремяОкончанияРазгрузкиФактическое	= '00010101';

		Для каждого СтрокаПоОбъекту Из тбзРезультат Цикл
			
			Если СтрокаПоОбъекту.ВремяНачалаПогрузкиПлановое < ВремяНачалаПогрузкиПлановое Тогда
				ВремяНачалаПогрузкиПлановое = СтрокаПоОбъекту.ВремяНачалаПогрузкиПлановое;	
			КонецЕсли;
			
			Если СтрокаПоОбъекту.ВремяНачалаПогрузкиФактическое < ВремяНачалаПогрузкиФактическое   Тогда
				ВремяНачалаПогрузкиФактическое = СтрокаПоОбъекту.ВремяНачалаПогрузкиФактическое;	
			КонецЕсли;

			Если СтрокаПоОбъекту.ВремяНачалаРазгрузкиПлановое < ВремяНачалаРазгрузкиПлановое   Тогда
				ВремяНачалаРазгрузкиПлановое = СтрокаПоОбъекту.ВремяНачалаРазгрузкиПлановое;	
			КонецЕсли;

			Если СтрокаПоОбъекту.ВремяНачалаРазгрузкиФактическое < ВремяНачалаРазгрузкиФактическое   Тогда
				ВремяНачалаРазгрузкиФактическое = СтрокаПоОбъекту.ВремяНачалаРазгрузкиФактическое;	
			КонецЕсли;

			Если СтрокаПоОбъекту.ВремяОкончанияПогрузкиПлановое > ВремяОкончанияПогрузкиПлановое Тогда
				ВремяОкончанияПогрузкиПлановое = СтрокаПоОбъекту.ВремяОкончанияПогрузкиПлановое;	
			КонецЕсли;

			Если СтрокаПоОбъекту.ВремяОкончанияПогрузкиФактическое > ВремяОкончанияПогрузкиФактическое   Тогда
				ВремяОкончанияПогрузкиФактическое = СтрокаПоОбъекту.ВремяОкончанияПогрузкиФактическое;	
			КонецЕсли;
			
			Если СтрокаПоОбъекту.ВремяОкончанияРазгрузкиПлановое > ВремяОкончанияРазгрузкиПлановое   Тогда
				ВремяОкончанияРазгрузкиПлановое = СтрокаПоОбъекту.ВремяОкончанияРазгрузкиПлановое;	
			КонецЕсли;
			
			Если СтрокаПоОбъекту.ВремяОкончанияРазгрузкиФактическое > ВремяОкончанияРазгрузкиФактическое   Тогда
				ВремяОкончанияРазгрузкиФактическое = СтрокаПоОбъекту.ВремяОкончанияРазгрузкиФактическое;	
			КонецЕсли;

		КонецЦикла;
		
		Строка = тбзРезультат.Добавить();
		Строка.Объект			= ОбъектПустаяСсылка;
		Строка.Расстояние		= РасстояниеВсего;
		Строка.ВремяРаботыЧас	= ВремяВсего;
		Строка.ВремяНачалаПогрузкиПлановое 			= ВремяНачалаПогрузкиПлановое;
		Строка.ВремяНачалаПогрузкиФактическое 		= ВремяНачалаПогрузкиФактическое;
		Строка.ВремяОкончанияПогрузкиПлановое 		= ВремяОкончанияПогрузкиПлановое;
		Строка.ВремяОкончанияПогрузкиФактическое 	= ВремяОкончанияПогрузкиФактическое;
		
		Строка.ВремяНачалаРазгрузкиПлановое 		= ВремяНачалаРазгрузкиПлановое;
		Строка.ВремяНачалаРазгрузкиФактическое 		= ВремяНачалаРазгрузкиФактическое;
		Строка.ВремяОкончанияРазгрузкиПлановое 		= ВремяОкончанияРазгрузкиПлановое;
		Строка.ВремяОкончанияРазгрузкиФактическое 	= ВремяОкончанияРазгрузкиФактическое;
		
	КонецЕсли;
	
	Возврат тбзРезультат;
	
КонецФункции

Функция ТаблицаРасстоянийИЗатраченногоВремениПоРейсам(дрвМаршрутыПоРейсам, СчитатьОбщееРасстояние = Ложь, ОбъектПустаяСсылка)

	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("Рейс", Новый ОписаниеТипов("ДокументСсылка.упРейс"));
	мсвТипов = Новый Массив();
	мсвТипов.Добавить(ТипЗнч(ОбъектПустаяСсылка));
	тбзРезультат.Колонки.Добавить("Объект",			Новый ОписаниеТипов(мсвТипов));
	тбзРезультат.Колонки.Добавить("Расстояние",		Новый ОписаниеТипов("Число"));
	тбзРезультат.Колонки.Добавить("ВремяРаботыЧас",	Новый ОписаниеТипов("Число"));
	
	тбзРезультат.Колонки.Добавить("ВремяНачалаПогрузкиПлановое", 		Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяНачалаПогрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияПогрузкиПлановое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияПогрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	
	тбзРезультат.Колонки.Добавить("ВремяНачалаРазгрузкиПлановое", 		Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяНачалаРазгрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияРазгрузкиПлановое", 	Новый ОписаниеТипов("Дата"));
	тбзРезультат.Колонки.Добавить("ВремяОкончанияРазгрузкиФактическое", 	Новый ОписаниеТипов("Дата"));
	
	СтрокиРейсы = дрвМаршрутыПоРейсам.Строки;
	Для каждого СтрокаРейс Из СтрокиРейсы Цикл
		
		тбзРасстоянияПоЗаданиям = ТаблицаРасстоянийИЗатраченногоВремениДляКаждогоОбъекта(СтрокаРейс, СчитатьОбщееРасстояние, ОбъектПустаяСсылка);	
		
		Для каждого СтрокаЗадание Из тбзРасстоянияПоЗаданиям Цикл
			НоваяСтрока = тбзРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗадание);
			НоваяСтрока.Рейс 			= СтрокаРейс.Рейс;
		КонецЦикла;
	КонецЦикла;
	
	Возврат тбзРезультат;
КонецФункции

// Возвращает структуру описания правил.
//
// Параметры:
//	ПравилоСсылка - СправочникСсылка - Ссылка на элемент.
//	ТипОбъектаРасчета - ПеречислениеСсылка - Ссылка на элемент.
//	РасчетПоКаждойСтроке - Булево - Выполнить.
//	ДополнительныеПараметры - Структура - Параметры.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция ОписаниеПравила(ПравилоСсылка, ТипОбъектаРасчета, РасчетПоКаждойСтроке, ДополнительныеПараметры = Неопределено) Экспорт 
	
	сткОписаниеПравила = Новый Структура("Описание, мсвПараметры","", Новый Массив);

	МакетОписания 		= Перечисления.упПараметрыРасчета.ПолучитьМакет("ОписаниеПараметровРасчета");
	КлючКолонкиОписания = КлючКолонкиОписания(ТипОбъектаРасчета, РасчетПоКаждойСтроке, ДополнительныеПараметры);
	
	КраткоеОписаниеРасчета = МакетОписания.Область("КраткоеОписаниеПравила|"+КлючКолонкиОписания).Текст;
	сткОписаниеПравила.Описание = МакетОписания.Область("КраткоеОписаниеПравила|"+КлючКолонкиОписания).Текст;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПравилаРасчетаПеременные.Идентификатор,
	               |	упПравилаРасчетаПеременные.Значение,
	               |	ВЫРАЗИТЬ(упПравилаРасчетаПеременные.Значение КАК Справочник.упПоказателиРасчета).ПараметрРасчета КАК ПарметрРасчета
	               |ИЗ
	               |	Справочник.упПравилаРасчета.Переменные КАК упПравилаРасчетаПеременные
	               |ГДЕ
	               |	упПравилаРасчетаПеременные.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ПравилоСсылка);
	текРезультатЗапроса = Запрос.Выполнить();
	Если НЕ текРезультатЗапроса.Пустой() Тогда
		текВыборка	= текРезультатЗапроса.Выбрать();
		Пока текВыборка.Следующий() И ЗначениеЗаполнено(текВыборка.ПарметрРасчета) Цикл
			
			текОписаниеПараметра = МакетОписания.Область(КлючСтрокиОписания(ОбщегоНазначения.ИмяЗначенияПеречисления(текВыборка.ПарметрРасчета))+"|"+КлючКолонкиОписания).Текст;
			сткОписаниеПравила.мсвПараметры.Добавить(Новый Структура("Параметр, ОписаниеПараметра",Сокрлп(текВыборка.Идентификатор), текОписаниеПараметра));
		КонецЦикла;
	КонецЕсли;
	
	Возврат сткОписаниеПравила;
	
КонецФункции

Функция КлючКолонкиОписания(ТипОбъектаРасчета, РасчетПоКаждойСтроке, ДополнительныеПараметры = Неопределено)
	текКлюч = "";
	ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	
	Если ТипОбъектаРасчета = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза")
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упЗаявкаНаПеревозкуГруза") Тогда
		текКлюч = "Заявка";
	ИначеЕсли ТипОбъектаРасчета = Тип("ДокументСсылка.упФактическиеДоходы") 
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упФактическиеДоходы") Тогда
		текКлюч = "ФактическиеДоходы";	
	ИначеЕсли ТипОбъектаРасчета = Тип("ДокументСсылка.упРейс")
		ИЛИ ТипОбъектаРасчета = Тип("ДокументСсылка.упПредложениеПеревозчика")
		ИЛИ ТипОбъектаРасчета = Тип("ДокументСсылка.упЗаявкаНаТС")
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упЗаявкаНаТС")
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упРейс") 
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упСтрахованиеГруза") 
		ИЛИ ТипОбъектаРасчета = Тип("ДокументСсылка.упСтрахованиеГруза") 
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упПредложениеПеревозчика") Тогда		
		текКлюч = "Рейс";	
		
		Если НЕ ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками И ТипОбъектаРасчета = Тип("ДокументОбъект.упРейс") Тогда
			текКлюч = текКлюч + "БезДетализации";	
		КонецЕсли;
		
	ИначеЕсли ТипОбъектаРасчета = Тип("ДокументСсылка.упФактическиеРасходы")
		ИЛИ ТипОбъектаРасчета = Тип("ДокументОбъект.упФактическиеРасходы") Тогда
		текКлюч = "ФактическиеРасходы";	
		
		Если НЕ ДополнительныеПараметры = Неопределено И ДополнительныеПараметры = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
			текКлюч = текКлюч + "ОбобщенныеРасходыПоНесколькимРейсам";
		КонецЕсли;
		
		Если НЕ ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			текКлюч = текКлюч + "БезДетализации";	
		КонецЕсли;
	ИначеЕсли ТипОбъектаРасчета = Тип("ДокументСсылка.упПутевойЛист") Тогда
		текКлюч = "ПутевойЛист";	
	КонецЕсли;
	
	Если РасчетПоКаждойСтроке И ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
		текКлюч = "ПоСтроке" + текКлюч;
	Иначе
		текКлюч = "ПоДокументу" + текКлюч;
	КонецЕсли;
	
	Возврат текКлюч;

КонецФункции

Функция КлючСтрокиОписания(ИдентификаторПараметра)
	
	сткИменаДляПодмены = Новый Структура();
	сткИменаДляПодмены.Вставить("Рейс", "ДокументРейс");
	
	Результат = Неопределено;
	
	Если НЕ сткИменаДляПодмены.Свойство(ИдентификаторПараметра, Результат) Тогда
		Результат = ИдентификаторПараметра;
	КонецЕсли;
	
	Возврат  Результат;
	
КонецФункции

#Область ФункцииОтносящиесяКПравиламРаспределения

// Возвращает типовую стуктуру соответствий показателей распределения.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Структура - Результат выполнения.
//
Функция СоответствиеПоказателейРаспределенияПараметрамРасчета() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("Расстояние", 	"Расстояние");
	сткРезультат.Вставить("Время",		 	"ВремяРаботыЧас");
	сткРезультат.Вставить("Масса",		 	"МассаГруза");
	сткРезультат.Вставить("Объем",		 	"ОбъемГруза");
	сткРезультат.Вставить("КоличествоМест",		 	"КоличествоМестГруза");
	сткРезультат.Вставить("КоличествоЗагрузочныхМетров",		 	"КоличествоЗагрузочныхМетровГруза");
	сткРезультат.Вставить("ПлановыеРасходыПоРейсу",		 	"ФактическиеРасходыПоЗаявке"); // Просто что бы было на что сослаться, а так нет соответствующего параметра.
	сткРезультат.Вставить("ОценочнаяСтоимостьГруза",		 	"ОценочнаяСтоимостьГруза");

	
	Возврат сткРезультат;
	
КонецФункции

// Возвращае типовую табилцу распределения.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьТаблицуРаспределения() Экспорт
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("Ключ");
	тбзРезультат.Колонки.Добавить("Расстояние", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("Время", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("Масса", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("Объем", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("КоличествоМест", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("КоличествоЗагрузочныхМетров", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("ПлановыеРасходыПоРейсу", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("ОценочнаяСтоимостьГруза", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРезультат.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	
	Возврат тбзРезультат;
	
КонецФункции

// Возвращает список параметров которые необходимо рассчитывать
//	если в строке есть правило распределения.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Массив(Строка) - список параметров.
//
Функция ПараметрыИспользуемыеВФормулеРаспределения()
	
	мсвРезультат = Новый Массив;
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.Расстояние);
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.ВремяРаботыЧас);
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.МассаГруза);
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.ОбъемГруза);
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.КоличествоМестГруза);
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.КоличествоЗагрузочныхМетровГруза);
	мсвРезультат.Добавить(Перечисления.упПараметрыРасчета.ОценочнаяСтоимостьГруза);

	Возврат мсвРезультат;
	
КонецФункции

Функция НуженВидПеревозки(мсвПараметрыРасчета)
	текРезультат = Ложь;	
	Если ЕстьРасчетПоПараметру(мсвПараметрыРасчета, "ВидПеревозки") Тогда
		текРезультат = Истина;	
	КонецЕсли;	
	Возврат текРезультат;
КонецФункции

#КонецОбласти

// Процедура - Проверяет уникальность кода показателя. Показатели расчета могут вводится в справочниках: показатели расчета,
//				тарифные сетки, оценочные шкалы. Для корректного использования их в формуле, необходимо соблюдать уникальность кодов(идентификаторов)
//				во всех трех справочниках.
//
// Параметры:
//  КодПоказателя			 - Строка	 - Код(идентификатор) показателя
//  ТипПроверяемогоОбъекта	 - Строка	 - Идентификатор типа объекта справочника, в который вводимого новый показатель.
//  Отказ					 - Булево	 - Истина, если существует показатель с таким же кодом
//
Процедура ПроверитьУникальностьКодаПоказателя(КодПоказателя, ТипПроверяемогоОбъекта, Отказ) Экспорт
	
	Если сокрлп(КодПоказателя) = "Организация" Тогда
		ТекстСообщения = Нстр("ru = 'Нельзя использовать показатель расчета с таким кодом'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "";
	
	Если НЕ ТипПроверяемогоОбъекта = "упПоказателиРасчета" Тогда
		ТекстЗапроса = ТекстЗапроса	+ "ВЫБРАТЬ
		                           	  |	упПоказателиРасчета.Ссылка
		                           	  |ИЗ
		                           	  |	Справочник.упПоказателиРасчета КАК упПоказателиРасчета
		                           	  |ГДЕ
		                           	  |	упПоказателиРасчета.Код = &Код";	
	КонецЕсли;
	
	Если НЕ ТипПроверяемогоОбъекта = "упОценочнаяШкала" Тогда
		
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
										|ОБЪЕДИНИТЬ ВСЕ
										|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса	+ "ВЫБРАТЬ
		                           	  |	упОценочнаяШкала.Ссылка
		                           	  |ИЗ
		                           	  |	Справочник.упОценочнаяШкала КАК упОценочнаяШкала
		                           	  |ГДЕ
		                           	  |	упОценочнаяШкала.Код = &Код";	
	КонецЕсли;
	
	Если НЕ ТипПроверяемогоОбъекта = "упТарифныеСетки" Тогда
		
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
										|ОБЪЕДИНИТЬ ВСЕ
										|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса	+ "ВЫБРАТЬ
		                           	  |	упТарифныеСетки.Ссылка
		                           	  |ИЗ
		                           	  |	Справочник.упТарифныеСетки КАК упТарифныеСетки
		                           	  |ГДЕ
		                           	  |	упТарифныеСетки.Код = &Код";	
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Код", КодПоказателя);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСообщения = Нстр("ru = 'В справочнике ""%1"" существует элемент с таким же кодом(%2)'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ТипЗнч(Выборка.Ссылка), Выборка.Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.Ссылка,	,,Отказ);
	КонецЕсли;
		
КонецПроцедуры

// Функция - Получить случайное значение ссылочного параметра.
//
// Параметры:
//  ПараметрРасчета	 - Перечисление.упПараметрыРасчета	 - ссылка на параметр расчета.
// 
// Возвращаемое значение:
//  Ссылка - значение ссылочного типа, если элементов в справочнике нет, то пустая ссылка.
//
Функция ПолучитьСлучайноеЗначениеСсылочногоПараметра(ПараметрРасчета) Экспорт
	
	Результат =	упТарификацияКлиентСервер.ПолучитьПустоеЗначение(ПараметрРасчета);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	тбСправочник.Ссылка
	|ИЗ
	|	%1 КАК тбСправочник
	|ГДЕ
	|	НЕ тбСправочник.ПометкаУдаления";
	
	ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Результат);
	Запрос.Текст  = СтрШаблон(Запрос.Текст, ИмяТаблицы);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли; 

	Возврат Результат;
		
КонецФункции

// Функция - Возвращает случайное значение показателя расчета.
//
// Параметры:
//  Показатель						 - Справочник.упПоказателиРасчета - показатель расчета.
//  ГенераторСлучайныхЧиселТекущий	 - ГенераторСлучайныхЧисел	 - генератор случайных чисел.
// 
// Возвращаемое значение:
//   Произвольное - случайное значение с типом соответствующим показателю.
//
Функция ПолучитьСлучайноеЗначениеПоказателяРасчета(Показатель, ГенераторСлучайныхЧиселТекущий) Экспорт
	
	Результат = 0;
	
	сткРеквизиты 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Показатель, "ПараметрРасчета, ТипПоказателя");
	ТипПоказателя	= сткРеквизиты.ТипПоказателя;
	ПараметрРасчета = сткРеквизиты.ПараметрРасчета;
	
	Если ТипПоказателя = ПредопределенноеЗначение("Перечисление.упТипыПоказателейРасчета.Фиксированный") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упЗначенияПоказателейРасчетаСрезПоследних.Значение
		|ИЗ
		|	РегистрСведений.упЗначенияПоказателейРасчета.СрезПоследних(
		|			,
		|			ПоказательРасчета = &ПоказательРасчета) КАК упЗначенияПоказателейРасчетаСрезПоследних";
		Запрос.УстановитьПараметр("ПоказательРасчета", 	Показатель);
				
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Значение;	
		КонецЕсли;

	Иначе
		
		Если упТарификацияКлиентСервер.ЭтоСсылочныйПараметр(ПараметрРасчета) Тогда
			
			Если упТарификацияВызовСервера.НеЗаменятьНаРазмер(Показатель) Тогда
				Результат = упТарификацияВызовСервера.ПолучитьСлучайноеЗначениеСсылочногоПараметра(ПараметрРасчета);	
			Иначе	
				Результат = ГенераторСлучайныхЧиселТекущий.СлучайноеЧисло();	
			КонецЕсли;
		ИначеЕсли упТарификацияКлиентСервер.ЭтоВременнойПараметр(ПараметрРасчета) Тогда	
			Результат 	= ДобавитьМесяц(ТекущаяДата(), ГенераторСлучайныхЧиселТекущий.СлучайноеЧисло(,100));
		Иначе
			Результат	= ГенераторСлучайныхЧиселТекущий.СлучайноеЧисло();			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Возвращает необходимость заменять на размер значение показателя расчета.
//
// Параметры:
//  ПоказательРасчета	 - Справочник.ПоказательРасчета	 - показатель расчета.
// 
// Возвращаемое значение:
//  Булево - Истина, если есть необходимость заменять на размер.
//
Функция НеЗаменятьНаРазмер(ПоказательРасчета) Экспорт
		
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоказательРасчета, "НеЗаменятьНаРазмер");
	
КонецФункции

// Функция - Дополняет значениями показателя, которые будут использоваться для поиска в тарифной сетке, 
//			 в том случае если не найдена ячейка по основному значению.
//
// Параметры:
//  ЗначениеПоказателя	 - Произвольный	 - основное значение показателя.
//  ПоказательСтрок		 - Справочник.упПоказателиРасчета -  показатель расчета, для которого подибраются значения.
// 
// Возвращаемое значение:
//   Массив - массив значений показателей.
//
Функция ДополнитьМассивПроверяемыхЗначенийПоказателя(ЗначениеПоказателя, ПоказательСтрок)
	
	Результат = Новый Массив;
	
	Результат.Добавить(ЗначениеПоказателя);
	
	ПараметрРасчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПоказательСтрок, "ПараметрРасчета");
	
	Если упТарификацияКлиентСервер.ЭтоГеографическаяЗонаПараметр(ПараметрРасчета) Тогда
		
		// Дополняются иерархией географических зон
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упИерархияГеографическихЗон.Родитель КАК ГеографическаяЗона 
		|ИЗ
		|	РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
		|ГДЕ
		|	упИерархияГеографическихЗон.Зона = &Зона
		|	И НЕ упИерархияГеографическихЗон.Родитель = &Зона
		|
		|УПОРЯДОЧИТЬ ПО
		|	Уровень УБЫВ";
		Запрос.УстановитьПараметр("Зона", ЗначениеПоказателя);
		Выборка	= Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.ГеографическаяЗона);
		КонецЦикла; 

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти