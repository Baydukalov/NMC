#Область ПрограммныйИнтерфейс

#Область РасчетВремениРаботПоГрузовымМестамИТипамГрузов

// Функция - Возвращает время работы по переданным параметрам.
//  Сначала ведется поиск по заданному типу груза, в случае неудачи.
//  ищется время с пустым типом груза.
//
// Параметры:
//  ВидАдреса					 - СправочникСсылка	 - Ссылка на элемент.
//  ТипОперации				 - ПеречислениеСсылка	 - Ссылка на элемент.
//  ГрузовоеМесто				 - СправочникСсылка		 - Ссылка на элемент.
//  ФормироватьПоГрузовымМестам	 - Булево				 - Флаг формирования.
//  КоличествоГрузов			 - Число				 - Значение количества.
//  Тара						 - СправочникСсылка.упГрузовыеМеста - ссылка на тару.
// 
// Возвращаемое значение:
//  Число - время в часах - количество часов.
//
Функция ПолучитьВремяРаботыПоГрузовомуМесту(ВидАдреса, ТипОперации, ГрузовоеМесто, ФормироватьПоГрузовымМестам, КоличествоГрузов, Тара) Экспорт
	
	текРезультат = 0;
	Если ЗначениеЗаполнено(ГрузовоеМесто) Тогда
		мсвГрузовыеМеста = Новый Массив();
		сткГрузовоеМесто = Новый Структура("ГрузовоеМесто, КоличествоГрузов, Тара", ГрузовоеМесто, КоличествоГрузов, Тара);
		мсвГрузовыеМеста.Добавить(сткГрузовоеМесто);
		
		мсвАдреса = Новый Массив();
		мсвАдреса.Добавить(Новый Структура("ВидАдреса, ТипОперации", ВидАдреса, ТипОперации));
		
		тбзВремя = ПолучитьВремяРаботыПоГрузовымМестам(мсвАдреса, мсвГрузовыеМеста, ФормироватьПоГрузовымМестам);
		
		Если НЕ тбзВремя = Неопределено и тбзВремя.Количество()>0 Тогда
			текРезультат = тбзВремя[0].ВремяРабот;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

// Функция - Возвращает время работы по переданным параметрам.
//
// Параметры:
//  мсвАдреса - Массив - массив ссылок на элементы.
//  мсвГрузовыеМеста - Массив - массив ссылок на элементы.
//  ФормироватьПоГрузовымМестам - Булево - Флаг формирования.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Данные в табличном виде.
//
Функция ПолучитьВремяРаботыПоГрузовымМестам(мсвАдреса, мсвГрузовыеМеста, ФормироватьПоГрузовымМестам) Экспорт
	
	тбзГрузовыеМеста	= Новый ТаблицаЗначений;
	тбзГрузовыеМеста.Колонки.Добавить("ГрузовоеМесто",		Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
	тбзГрузовыеМеста.Колонки.Добавить("Тара",	        	Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
	тбзГрузовыеМеста.Колонки.Добавить("ВидАдреса", 			Новый ОписаниеТипов("СправочникСсылка.упВидыАдресов"));
	тбзГрузовыеМеста.Колонки.Добавить("ТипОперации", 		Новый ОписаниеТипов("ПеречислениеСсылка.упТипыОпераций"));
	тбзГрузовыеМеста.Колонки.Добавить("КоличествоГрузов", 	Новый ОписаниеТипов("Число"));

	Для каждого сткГрузовоеМесто Из мсвГрузовыеМеста Цикл
		текГрузовоеМесто = сткГрузовоеМесто.ГрузовоеМесто;
		текТара          = сткГрузовоеМесто.Тара;
		текКоличество	 = сткГрузовоеМесто.КоличествоГрузов;
		Для каждого текВидАдреса Из мсвАдреса Цикл
			
			Если (НЕ ФормироватьПоГрузовымМестам
					ИЛИ НЕ ЗначениеЗаполнено(текГрузовоеМесто))
					И (текВидАдреса.ТипОперации = Перечисления.упТипыОпераций.ВыемкаЦенностей
						ИЛИ текВидАдреса.ТипОперации = Перечисления.упТипыОпераций.ПередачаЦенностей) Тогда
				Продолжить;
			КонецЕсли;
			
			текСтрока = тбзГрузовыеМеста.Добавить();
			Если ФормироватьПоГрузовымМестам Тогда
				текСтрока.ГрузовоеМесто	= текГрузовоеМесто;
				текСтрока.Тара      	= текТара;
				текСтрока.КоличествоГрузов	= текКоличество
			Иначе
				текСтрока.ГрузовоеМесто	= Справочники.упГрузовыеМеста.ПустаяСсылка();
				текСтрока.КоличествоГрузов	= 1;
			КонецЕсли;	
			текСтрока.ВидАдреса 	= текВидАдреса.ВидАдреса;
			текСтрока.ТипОперации	= текВидАдреса.ТипОперации;
		КонецЦикла;
	КонецЦикла;
	
	текРезультат = Неопределено;
	
	Если тбзГрузовыеМеста.Количество()>0 Тогда
				
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = ПолучитьТекстЗапросаДляРасчетаВремениВыполненияРабот();
		текЗапрос.УстановитьПараметр("ТипыГрузов", 		тбзГрузовыеМеста);
		
		текРезультат  = текЗапрос.Выполнить().Выгрузить();
	КонецЕсли;

	Возврат текРезультат;
	
КонецФункции

// Функция - Возвращает время работы по типам грузов.
//
// Параметры:
//  мсвАдреса					 - Массив	 - массив ссылок на элементы.
//  мсвТипыГрузовыхМест - Массив - массив ссылок на элементы.
//  тбзТипыГрузовыхМест			 - ТаблицаЗначений		 - типы грузовых мест.
//  ФормироватьПоГрузовымМестам	 - Булево	 - Флаг формирования.
//  мсвОпераций				 - Массив		 - массив операций.
// 
// Возвращаемое значение:
//  Число - Количество секунд.
//
Функция ПолучитьВремяРаботыПоТипамГрузов(мсвАдреса, тбзТипыГрузовыхМест, ФормироватьПоГрузовымМестам, мсвОпераций) Экспорт
	
	тбзТипыГрузов	= Новый ТаблицаЗначений;
	тбзТипыГрузов.Колонки.Добавить("ТипГруза", 		Новый ОписаниеТипов("СправочникСсылка.упТипыГрузов"));
	тбзТипыГрузов.Колонки.Добавить("ВидАдреса", 	Новый ОписаниеТипов("СправочникСсылка.упВидыАдресов"));
	тбзТипыГрузов.Колонки.Добавить("ТипОперации", 	Новый ОписаниеТипов("ПеречислениеСсылка.упТипыОпераций"));
	
	
	Для каждого текТипГруза Из тбзТипыГрузовыхМест Цикл
		Для каждого текОперация Из мсвОпераций Цикл
			
			Если упПрохождениеТочкиКлиентСервер.ОсновнаяОперацияДляВидаГрузаГруз(текОперация)
					И текТипГруза.ВидГруза = Перечисления.упВидыГрузов.ИнкассируемаяЦенность Тогда
					Продолжить;
				ИначеЕсли упПрохождениеТочкиКлиентСервер.ОсновнаяОперацияДляВидГрузаИнкассируемаяЦенность(текОперация)
					И текТипГруза.ВидГруза = Перечисления.упВидыГрузов.Груз Тогда
					Продолжить;
			КонецЕсли; 	
				
			Для каждого текВидАдреса Из мсвАдреса Цикл
				текСтрока = тбзТипыГрузов.Добавить();
				Если ФормироватьПоГрузовымМестам Тогда
					текСтрока.ТипГруза = текТипГруза.ТипГруза;
				КонецЕсли;
				текСтрока.ВидАдреса 	= текВидАдреса;
				текСтрока.ТипОперации	= текОперация;	
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	текРезультат = 0;
	
	Если тбзТипыГрузов.Количество()>0 Тогда
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = "ВЫБРАТЬ
		                  |	тбТипыГрузов.ВидАдреса,
		                  |	тбТипыГрузов.ТипОперации,
		                  |	тбТипыГрузов.ТипГруза
		                  |ПОМЕСТИТЬ втТипыГрузов
		                  |ИЗ
		                  |	&ТипыГрузов КАК тбТипыГрузов
		                  |;
		                  |
		                  |////////////////////////////////////////////////////////////////////////////////
		                  |ВЫБРАТЬ РАЗЛИЧНЫЕ
		                  |	втТипыГрузов.ТипОперации,
		                  |	втТипыГрузов.ВидАдреса
		                  |ПОМЕСТИТЬ втТипыТочекОпераций
		                  |ИЗ
		                  |	втТипыГрузов КАК втТипыГрузов
		                  |;
		                  |
		                  |////////////////////////////////////////////////////////////////////////////////
		                  |ВЫБРАТЬ
		                  |	тбВремяРабот.Время,
		                  |	тбВремяРабот.ТипГруза,
		                  |	тбВремяРабот.ТипОперации,
		                  |	тбВремяРабот.ВидТочки КАК ВидАдреса
		                  |ПОМЕСТИТЬ втВремяПоТипамГрузов
		                  |ИЗ
		                  |	РегистрСведений.упВремяВыполненияРабот КАК тбВремяРабот
		                  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТипыТочекОпераций КАК втТипыТочекОпераций
		                  |		ПО тбВремяРабот.ВидТочки = втТипыТочекОпераций.ВидАдреса
		                  |			И тбВремяРабот.ТипОперации = втТипыТочекОпераций.ТипОперации
		                  |;
		                  |
		                  |////////////////////////////////////////////////////////////////////////////////
		                  |ВЫБРАТЬ
		                  |	тбВремяРабот.Время,
		                  |	тбВремяРабот.ТипГруза,
		                  |	тбВремяРабот.ТипОперации,
		                  |	тбВремяРабот.ВидАдреса
		                  |ПОМЕСТИТЬ втВремяПоПустомуТипуГрузов
		                  |ИЗ
		                  |	втВремяПоТипамГрузов КАК тбВремяРабот
		                  |ГДЕ
		                  |	тбВремяРабот.ТипГруза = ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
		                  |;
		                  |
		                  |////////////////////////////////////////////////////////////////////////////////
		                  |ВЫБРАТЬ
		                  |	СУММА(ЕСТЬNULL(тбВремяРабот.Время, ЕСТЬNULL(тбВремяРаботПоПустомуТипуГруза.Время, 0))) / 3600 КАК ВремяРабот
		                  |ИЗ
		                  |	втТипыГрузов КАК тбТипыГрузов
		                  |		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоТипамГрузов КАК тбВремяРабот
		                  |		ПО тбТипыГрузов.ВидАдреса = тбВремяРабот.ВидАдреса
		                  |			И тбТипыГрузов.ТипОперации = тбВремяРабот.ТипОперации
		                  |			И тбТипыГрузов.ТипГруза = тбВремяРабот.ТипГруза
		                  |		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоПустомуТипуГрузов КАК тбВремяРаботПоПустомуТипуГруза
		                  |		ПО тбТипыГрузов.ВидАдреса = тбВремяРаботПоПустомуТипуГруза.ВидАдреса
		                  |			И тбТипыГрузов.ТипОперации = тбВремяРаботПоПустомуТипуГруза.ТипОперации";
		
		текЗапрос.УстановитьПараметр("ТипыГрузов", 		тбзТипыГрузов);
		текВыборка 	= текЗапрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если текВыборка.Следующий() Тогда
			текРезультат = текРезультат + текВыборка.ВремяРабот;
		КонецЕсли
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Функция - Возвращает текст запроса для расчета времени выполнения работ.
//
// Возвращаемое значение:
//  Строка - Текст запроса.
//
Функция ПолучитьТекстЗапросаДляРасчетаВремениВыполненияРабот() Экспорт
	
	текРезультат = 	
	"ВЫБРАТЬ
	|	тбТипыГрузов.ВидАдреса,
	|	тбТипыГрузов.ТипОперации,
	|	ВЫРАЗИТЬ(тбТипыГрузов.ГрузовоеМесто КАК Справочник.упГрузовыеМеста) КАК ГрузовоеМесто,
	|	тбТипыГрузов.Тара,
	|	тбТипыГрузов.КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	&ТипыГрузов КАК тбТипыГрузов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ВидАдреса,
	|	втГрузовыеМеста.ТипОперации,
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.Тара,
	|	втГрузовыеМеста.КоличествоГрузов,
	|	упГрузовыеМеста.ТипГруза,
	|	упГрузовыеМеста.Контейнер КАК ЭтоКонтейнер,
	|	ВЫБОР
	|		КОГДА упТипыГрузов.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоИнкассация
	|ПОМЕСТИТЬ втГрузовыеМестаТипыГрузов
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК упТипыГрузов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|			ПО (упГрузовыеМеста.ТипГруза = упТипыГрузов.Ссылка)
	|		ПО (втГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА (втГрузовыеМеста.ТипОперации = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|					ИЛИ втГрузовыеМеста.ТипОперации = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|					И упТипыГрузов.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)
	|				ТОГДА ИСТИНА
	|			КОГДА (втГрузовыеМеста.ТипОперации = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|					ИЛИ втГрузовыеМеста.ТипОперации = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка))
	|					И (упТипыГрузов.ВидГруза В (ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз), ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара))
	|						ИЛИ упТипыГрузов.ВидГруза ЕСТЬ NULL)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втТипыГрузов.ТипОперации,
	|	втТипыГрузов.ВидАдреса
	|ПОМЕСТИТЬ втТипыТочекОпераций
	|ИЗ
	|	втГрузовыеМестаТипыГрузов КАК втТипыГрузов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбВремяРабот.Время,
	|	тбВремяРабот.ТипГруза,
	|	тбВремяРабот.ТипОперации,
	|	тбВремяРабот.ВидТочки КАК ВидАдреса
	|ПОМЕСТИТЬ втВремяПоТипамГрузов
	|ИЗ
	|	РегистрСведений.упВремяВыполненияРабот КАК тбВремяРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТипыТочекОпераций КАК втТипыТочекОпераций
	|		ПО тбВремяРабот.ВидТочки = втТипыТочекОпераций.ВидАдреса
	|			И тбВремяРабот.ТипОперации = втТипыТочекОпераций.ТипОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбВремяРабот.Время,
	|	тбВремяРабот.ТипГруза,
	|	тбВремяРабот.ТипОперации,
	|	тбВремяРабот.ВидАдреса
	|ПОМЕСТИТЬ втВремяПоПустомуТипуГрузов
	|ИЗ
	|	втВремяПоТипамГрузов КАК тбВремяРабот
	|ГДЕ
	|	тбВремяРабот.ТипГруза = ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(тбВремяРабот.Время, ЕСТЬNULL(тбВремяРаботПоПустомуТипуГруза.Время, 0))) / 3600 КАК ВремяРабот,
	|	тбГрузовыеМеста.ТипОперации,
	|	тбГрузовыеМеста.ВидАдреса,
	|	тбГрузовыеМеста.ГрузовоеМесто,
	|	тбГрузовыеМеста.Тара,
	|    тбГрузовыеМеста.ЭтоКонтейнер
	|ПОМЕСТИТЬ втВремяПоГрузовымМестам
	|ИЗ
	|	втГрузовыеМестаТипыГрузов КАК тбГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоТипамГрузов КАК тбВремяРабот
	|		ПО тбГрузовыеМеста.ТипОперации = тбВремяРабот.ТипОперации
	|			И (ВЫБОР
	|				КОГДА тбГрузовыеМеста.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|					ТОГДА тбВремяРабот.ТипГруза = ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
	|				ИНАЧЕ тбГрузовыеМеста.ТипГруза = тбВремяРабот.ТипГруза
	|			КОНЕЦ)
	|			И тбГрузовыеМеста.ВидАдреса = тбВремяРабот.ВидАдреса
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоПустомуТипуГрузов КАК тбВремяРаботПоПустомуТипуГруза
	|		ПО тбГрузовыеМеста.ТипОперации = тбВремяРаботПоПустомуТипуГруза.ТипОперации
	|			И (тбВремяРаботПоПустомуТипуГруза.ВидАдреса = тбВремяРабот.ВидАдреса)
	|
	|СГРУППИРОВАТЬ ПО
	|	тбГрузовыеМеста.ТипОперации,
	|	тбГрузовыеМеста.ВидАдреса,
	|	тбГрузовыеМеста.ГрузовоеМесто,
	|	тбГрузовыеМеста.Тара,
	|    тбГрузовыеМеста.ЭтоКонтейнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбТипыГрузов.ГрузовоеМесто,
	|	тбТипыГрузов.Тара,
	|	тбТипыГрузов.ТипОперации,
	|	тбТипыГрузов.ЭтоИнкассация,
	|    тбТипыГрузов.ЭтоКонтейнер,
	|	ЕСТЬNULL(тбТипыГрузов.ГрузовоеМесто.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки, ЛОЖЬ) КАК ИнкассацияЗаГруз,
	|	ЕСТЬNULL(тбТипыГрузов.ГрузовоеМесто.Сумма, 0) КАК Сумма,
	|	СУММА(ЕСТЬNULL(тбВремя.ВремяРабот, 0) * тбТипыГрузов.КоличествоГрузов) КАК ВремяРабот,
	|	СУММА(тбТипыГрузов.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	|	втГрузовыеМестаТипыГрузов КАК тбТипыГрузов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоГрузовымМестам КАК тбВремя
	|		ПО тбТипыГрузов.ГрузовоеМесто = тбВремя.ГрузовоеМесто
	|			И тбТипыГрузов.ТипОперации = тбВремя.ТипОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	тбТипыГрузов.ГрузовоеМесто,
	|	тбТипыГрузов.Тара,
	|	тбТипыГрузов.ТипОперации,
	|	тбТипыГрузов.ЭтоИнкассация,
	|    тбТипыГрузов.ЭтоКонтейнер,
	|	тбТипыГрузов.ГрузовоеМесто.Сумма";
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти 

#Область РасчетВремениРаботПоДополнительнымОперациям

// Функция - Возвращает таблицу значений с временем выполнения операции.
//
// Параметры:
//  ЗаданиеНаПеревозкуГруза - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  ТаблицаЗначений - табличные данные.
//
Функция ПолучитьВремяПоДопОперациям(ЗаданиеНаПеревозкуГруза) Экспорт
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = 
	"ВЫБРАТЬ
	|	тбДопОперации.ДополнительнаяОперация,
	|	тбДопОперации.Количество * тбДопОперации.ДополнительнаяОперация.ВремяВыполненияОперации КАК ВремяРабот,
	|	тбДопОперации.ТипТочки КАК ТипОперации,
	|	тбДопОперации.Количество КАК КоличествоДопОпераций
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ДополнительныеОперации КАК тбДопОперации
	|ГДЕ
	|	тбДопОперации.Ссылка = &Ссылка";
	текЗапрос.УстановитьПараметр("Ссылка", ЗаданиеНаПеревозкуГруза);
	
	тбзРезультат = текЗапрос.Выполнить().Выгрузить(); 
	
	Возврат тбзРезультат
	
КонецФункции

// Функция - Возвращает таблицу значений с временем выполнения операции, с разрезом по переданной операции.
//
// Параметры:
//  ЗаданиеНаПервозкуГруза - ДокументСсылка - Ссылка на элемент.
//  ТипТочки - ПеречислениеСсылка - Ссылка на элемент.
//  ДополнительнаяОперация - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Число - время в часах - количество часов.
//
Функция ПолучитьВремяПоДопОперации(ЗаданиеНаПервозкуГруза, ТипТочки, ДополнительнаяОперация) Экспорт
	
	текРезультат = 0;
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	                  |	СУММА(тбДопОперации.Количество * тбДопОперации.ДополнительнаяОперация.ВремяВыполненияОперации) КАК ВремяРабот
	                  |ИЗ
	                  |	Документ.упЗаданиеНаПеревозкуГруза.ДополнительныеОперации КАК тбДопОперации
	                  |ГДЕ
	                  |	тбДопОперации.Ссылка = &Ссылка
	                  |	И тбДопОперации.ДополнительнаяОперация = &ДополнительнаяОперация
	                  |	И тбДопОперации.ТипТочки = &ТипТочки";
	текЗапрос.УстановитьПараметр("Ссылка", ЗаданиеНаПервозкуГруза);
	текЗапрос.УстановитьПараметр("ТипТочки", ТипТочки);
	текЗапрос.УстановитьПараметр("ДополнительнаяОперация", ДополнительнаяОперация);
		
	текВыборка = текЗапрос.Выполнить().Выбрать(); 
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.ВремяРабот;
	КонецЕсли;
	
	Возврат текРезультат
		
КонецФункции

// Процедура - Пересчета времени рагрузки.
//
// Параметры:
//  ЗаданиеОбъект - ДокументОбъект - Объект документа.
//  мсвТипыГрузовыхМест - Массив - Масив ссылок.
//
Процедура ПересчитатьВремяРазгрузки(ЗаданиеОбъект, тбзТипыГрузовыхМест) Экспорт
	
	Отказ = Ложь;
	ЗаданиеОбъект.ВремяРазгрузки = 0;
	
	текВидАдреса = ЗаданиеОбъект.АдресПолучения.ВидАдреса;
	Если НЕ ЗначениеЗаполнено(текВидАдреса) Тогда
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У адреса получателя не заполнен вид адреса'"),
				ЗаданиеОбъект.Ссылка,
				"",
				,
				Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
	
		мсвОпераций = Новый Массив();
		мсвОпераций.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка"));
		Если упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ЗаданиеОбъект.ВидПеревозки) Тогда
			мсвОпераций.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей"));	
		КонецЕсли;			
		
		мсвАдреса = Новый Массив();
		мсвАдреса.Добавить(текВидАдреса);
				
		ФормироватьПоГрузовымМестам = Истина;
		ЗаданиеОбъект.ВремяРазгрузки = упЗаданиеНаПеревозку.ПолучитьВремяРаботыПоТипамГрузов(мсвАдреса, тбзТипыГрузовыхМест, ФормироватьПоГрузовымМестам, мсвОпераций);
		
		// Добавить время по дополнительным операциям.
		Если ПолучитьФункциональнуюОпцию("упИспользуютсяДополнительныеОперации") Тогда
			Для каждого текСтрокаДопОперации Из ЗаданиеОбъект.ДополнительныеОперации Цикл
				Если НЕ мсвОпераций.Найти(текСтрокаДопОперации.ТипТочки) = Неопределено Тогда
					ЗаданиеОбъект.ВремяРазгрузки  = ЗаданиеОбъект.ВремяРазгрузки + текСтрокаДопОперации.ДополнительнаяОперация.ВремяВыполненияОперации  * текСтрокаДопОперации.Количество;		
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Пересчета времени погрузки.
//
// Параметры:
//  ЗаданиеОбъект - ДокументОбъект - Объект документа.
//  мсвТипыГрузовыхМест - Массив - Масив ссылок.
//
Процедура ПересчитатьВремяПогрузки(ЗаданиеОбъект, тбзТипыГрузовыхМест) Экспорт
	
	Отказ = Ложь;
	ЗаданиеОбъект.ВремяПогрузки = 0;	
	ВидАдреса = ЗаданиеОбъект.АдресОтправления.ВидАдреса;
	Если НЕ ЗначениеЗаполнено(ВидАдреса) Тогда
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У адреса отправителя не заполнен вид адреса'"),ЗаданиеОбъект.Ссылка,"",,Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		мсвОпераций = Новый Массив();
		мсвОпераций.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка"));
		Если упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ЗаданиеОбъект.ВидПеревозки) Тогда
			мсвОпераций.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей"));	
		КонецЕсли;			
		
		мсвАдреса = Новый Массив();
		мсвАдреса.Добавить(ВидАдреса);
				
		ФормироватьПоГрузовымМестам = Истина;
		ЗаданиеОбъект.ВремяПогрузки = упЗаданиеНаПеревозку.ПолучитьВремяРаботыПоТипамГрузов(мсвАдреса, тбзТипыГрузовыхМест, ФормироватьПоГрузовымМестам, мсвОпераций);
		
		// Добавить время по дополнительным операциям.
		Если ПолучитьФункциональнуюОпцию("упИспользуютсяДополнительныеОперации") Тогда
			Для каждого СтрокаДопОперации Из ЗаданиеОбъект.ДополнительныеОперации Цикл
				Если мсвОпераций.Найти(СтрокаДопОперации.ТипТочки) <> Неопределено Тогда
					ЗаданиеОбъект.ВремяПогрузки  = ЗаданиеОбъект.ВремяПогрузки + СтрокаДопОперации.ДополнительнаяОперация.ВремяВыполненияОперации  * СтрокаДопОперации.Количество;	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыКонтроля
	
// Процедура контролирует отсутствие отрицательных остатков в точках отправления и поступления.
//
// Параметры:
//  Ссылка - ДокументСсылка.упЗаданиеНаПеревозкуГруза - Ссылка на документ.
//  Отказ - Булево - Флаг фиксирования изменений.
//
Процедура ПроконтролироватьСостояниеЗадания(Ссылка, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тбСостояние.ГрузовоеМесто,
	|	тбСостояние.Местоположение,
	|	ВЫБОР
	|		КОГДА тбПланы.Количество ЕСТЬ NULL 
	|			ТОГДА тбСостояние.КоличествоОстаток
	|		ИНАЧЕ тбСостояние.КоличествоОстаток + ЕСТЬNULL(тбПланы.Количество, 0)
	|	КОНЕЦ КАК Отклонение
	|ИЗ
	|	РегистрНакопления.упСостояниеПоЗаданиям.Остатки(, Задание = &Задание) КАК тбСостояние
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПланПоЗаданиям.СрезПоследних(, Задание = &Задание) КАК тбПланы
	|		ПО тбСостояние.Задание = тбПланы.Задание
	|			И тбСостояние.Местоположение = тбПланы.Местоположение
	|			И тбСостояние.ГрузовоеМесто = тбПланы.ГрузовоеМесто
	|ГДЕ
	|	ВЫБОР
	|			КОГДА тбПланы.Количество ЕСТЬ NULL 
	|				ТОГДА тбСостояние.КоличествоОстаток
	|			ИНАЧЕ тбСостояние.КоличествоОстаток + ЕСТЬNULL(тбПланы.Количество, 0)
	|		КОНЕЦ < 0";
	Запрос.УстановитьПараметр("Задание", Ссылка);
	текВыборка = Запрос.Выполнить().Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		Если ЗначениеЗаполнено(текВыборка.ГрузовоеМесто)  Тогда
			ТекстСообщения = НСтр("ru = 'При проведении документа %1 по грузовому месту %2 в точке %3 формируется отрицательный остаток %4'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Ссылка, текВыборка.ГрузовоеМесто, текВыборка.Местоположение, текВыборка.Отклонение);
		Иначе	
			ТекстСообщения = НСтр("ru = 'При проведении документа %1 в точке %2 формируется отрицательный остаток %3'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Ссылка, текВыборка.Местоположение, текВыборка.Отклонение);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);
	КонецЦикла;
		
КонецПроцедуры // ПроконтролироватьЗадание()

// Процедура контролирует отсутствие отрицательных остатков по заданиям на перевозку в рейсе.
//
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаданиеНаПеревозкуГруза, ДокументСсылка.упРейс - Ссылка на документ.
//	ФормироватьПоГрузовымМестам - Булево - зависит от вида перевозки, 
//	    если истина, то регистр ведется в разрезе грузовых мест.
//  Отказ - Булево - Флаг фиксирования изменений.
//  ДополнительныеСвойства - Структура - Свойства.
//
Процедура ПроконтролироватьЗаданияКПланированию(Ссылка, ФормироватьПоГрузовымМестам, Отказ, ДополнительныеСвойства = Неопределено) Экспорт

	КонтролироватьЗадания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "КонтролироватьЗадания", Ложь);
	КонтролироватьСтаффировкуКонтейнера = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "КонтролироватьСтаффировкуКонтейнера", Ложь);
	
	Если Ложь
		Или Отказ
		Или (Истина
			И Не КонтролироватьЗадания
			И Не КонтролироватьСтаффировкуКонтейнера)
	Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ИндексЗапросаЗаданияРазныхЦепейПоставокВОдномРейсе = Неопределено;
	ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом = Неопределено;
	Событие = Нстр("ru = 'Ошибка при контроле движений документа'");
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс") Тогда
		Запрос.Текст =	
		"ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание,
		|	упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упРейсЗадания.Тара КАК Тара,
		|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
		|	упРейсЗадания.Задание.ЗапрещеноИсполнениеНесколькимиРейсами КАК ЗапрещеноИсполнениеНесколькимиРейсами
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|ГДЕ
		|	упРейсЗадания.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадания.Задание КАК Задание,
		|	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втЗадания.Тара КАК Тара
		|ИЗ
		|	втЗадания КАК втЗадания
		|ГДЕ
		|	ИСТИНА
		|	И НЕ втЗадания.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|	И НЕ втЗадания.Тара В
		|				(ВЫБРАТЬ
		|					втЗадания.ГрузовоеМесто КАК Тара
		|				ИЗ
		|					втЗадания КАК втЗадания)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбПланирование.Задание КАК Задание,
		|	тбПланирование.ГрузовоеМесто КАК ГрузовоеМесто,
		|	тбПланирование.КоличествоКПланированиюОстаток КАК КоличествоКПланированию,
		|	тбПланирование.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнению
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(
		|			,
		|			(Задание, ГрузовоеМесто) В
		|				(ВЫБРАТЬ
		|					втЗадания.Задание КАК Задание,
		|					втЗадания.ГрузовоеМесто КАК ГрузовоеМесто
		|				ИЗ
		|					втЗадания КАК втЗадания)) КАК тбПланирование
		|ГДЕ
		|	(ЛОЖЬ
		|			ИЛИ тбПланирование.КоличествоКПланированиюОстаток < 0
		|			ИЛИ тбПланирование.КоличествоКВыполнениюОстаток < 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втЗадания.Задание КАК Задание
		|ПОМЕСТИТЬ втЗаданияЗапрещеноИсполнениеПоНесколькимРейсам
		|ИЗ
		|	втЗадания КАК втЗадания
		|ГДЕ
		|	втЗадания.ЗапрещеноИсполнениеНесколькимиРейсами
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упРейс
		|			ТОГДА упЗаданияКПланированиюТ.Регистратор
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРейса
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упКорректировкаРейса).Рейс
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПрохождениеТочки
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПрохождениеТочки).Рейс
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПроисшествиеВПути
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПроисшествиеВПути).Рейс
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК Рейс,
		|	упЗаданияКПланированиюТ.Задание КАК Задание
		|ПОМЕСТИТЬ втРейсыПоЗаданиям
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию КАК упЗаданияКПланированиюТ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияЗапрещеноИсполнениеПоНесколькимРейсам КАК втЗадания
		|		ПО (втЗадания.Задание = упЗаданияКПланированиюТ.Задание)
		|ГДЕ
		|	ИСТИНА
		|	И НЕ упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза
		|	И НЕ упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРегистров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРейсыПоЗаданиямТ.Задание КАК Задание,
		|	СУММА(1) КАК КоличествоРазличныхРейсов
		|ПОМЕСТИТЬ втЗаданияСНесклькимиРейсами
		|ИЗ
		|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, ) КАК УпСтатусыРейсов_СрезПоследнихТ
		|		ПО втРейсыПоЗаданиямТ.Рейс = УпСтатусыРейсов_СрезПоследнихТ.Документ
		|ГДЕ
		|	УпСтатусыРейсов_СрезПоследнихТ.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
		|
		|СГРУППИРОВАТЬ ПО
		|	втРейсыПоЗаданиямТ.Задание
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРейсыПоЗаданиямТ.Задание КАК Задание,
		|	втРейсыПоЗаданиямТ.Рейс КАК Рейс
		|ИЗ
		|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияСНесклькимиРейсами КАК втЗаданияСНесклькимиРейсамиТ
		|		ПО (втЗаданияСНесклькимиРейсамиТ.Задание = втРейсыПоЗаданиямТ.Задание)
		|ИТОГИ ПО
		|	Задание";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
		ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом = 1;
		ИндексЗапросаОстаткиКПланированию = 2;
		ИндексЗапросаЗаданияПоНесколькимРейсам = 6;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрохождениеТочки") Тогда	
		Запрос.Текст =	"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.Задание КАК Задание,
		|	ВЫБОР
		|		КОГДА упРейсТ.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|		ИНАЧЕ втРаботы.ГрузовоеМесто
		|	КОНЕЦ КАК ГрузовоеМесто,
		|	ВЫБОР
		|		КОГДА упРейсТ.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|		ИНАЧЕ втРаботы.Тара
		|	КОНЕЦ КАК Тара,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботы.Отменена
		|				И НЕ втРаботы.ГрузРазделен
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК КоличествоОтменена,
		|	СУММА(1) КАК КоличествоОпераций,
		|	МАКСИМУМ(втРаботы.Проблема) КАК Проблема
		|ПОМЕСТИТЬ втОтмененныеЗадания
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Ссылка) КАК втРаботы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
		|	ПО упРейсТ.Ссылка = втРаботы.Рейс
		|ГДЕ ЛОЖЬ
		|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|СГРУППИРОВАТЬ ПО
		|	втРаботы.Задание,
		|	ВЫБОР
		|		КОГДА упРейсТ.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|		ИНАЧЕ втРаботы.ГрузовоеМесто
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА упРейсТ.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|		ИНАЧЕ втРаботы.Тара
		|	КОНЕЦ
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упРаботыПоРейсу.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ втРегистраторы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
		|ГДЕ ИСТИНА
		|	И упРаботыПоРейсу.Рейс = &Ссылка
		|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание,
		|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
		|	упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упРейсЗадания.Тара КАК Тара,
		|	упРейсЗадания.Задание.ЗапрещеноИсполнениеНесколькимиРейсами КАК ЗапрещеноИсполнениеНесколькимиРейсами,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
		|				И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Отменено
		|ПОМЕСТИТЬ втЗаданияПоДокументам
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
		|	ПО ИСТИНА
		|		И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
		|		И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
		|ГДЕ упРейсЗадания.Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	упКорректировкаРейсаЗадания.Задание КАК Задание,
		|	упКорректировкаРейсаЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
		|	упКорректировкаРейсаЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упКорректировкаРейсаЗадания.Тара КАК Тара,
		|	упКорректировкаРейсаЗадания.Задание.ЗапрещеноИсполнениеНесколькимиРейсами КАК ЗапрещеноИсполнениеНесколькимиРейсами,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
		|				И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Отменено
		|ИЗ
		|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
		|	ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
		|	ПО ИСТИНА
		|		И упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
		|		И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втЗаданияТ.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втЗаданияТ.Задание КАК Задание,
		|	втЗаданияТ.ЗаданиеЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
		|	втЗаданияТ.ЗапрещеноИсполнениеНесколькимиРейсами КАК ЗапрещеноИсполнениеНесколькимиРейсами,
		|	втЗаданияТ.Тара КАК Тара
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	втЗаданияПоДокументам КАК втЗаданияТ
		|ГДЕ НЕ (втЗаданияТ.Отменено)";
		
		Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.Рейс);
		
		ИндексПоследнегоЗапроса = 3;
		
		Если КонтролироватьЗадания Тогда
			
			Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "
			|//{Запрос: 4 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втЗадания.Задание КАК Задание,
			|	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
			|	втЗадания.Тара КАК Тара
			|ИЗ
			|	втЗаданияПоДокументам КАК втЗадания
			|ГДЕ ИСТИНА
			|	И НЕ (втЗадания.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
			|	И НЕ (втЗадания.Тара В (
			|			ВЫБРАТЬ
			|				втЗадания.ГрузовоеМесто КАК Тара
			|			ИЗ
			|				втЗадания КАК втЗадания))
			|;
			|//{Запрос: 5 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	тбПланирование.Задание КАК Задание,
			|	тбПланирование.ГрузовоеМесто КАК ГрузовоеМесто,
			|	тбПланирование.КоличествоКПланированиюОстаток КАК КоличествоКПланированию,
			|	тбПланирование.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнению
			|ИЗ
			|	РегистрНакопления.упЗаданияКПланированию.Остатки(
			|		,
			|		(Задание, ГрузовоеМесто) В (
			|				ВЫБРАТЬ
			|				втЗадания.Задание КАК Задание,
			|				втЗадания.ГрузовоеМесто КАК ГрузовоеМесто
			|			ИЗ
			|				втЗадания КАК втЗадания)) КАК тбПланирование
			|ГДЕ ЛОЖЬ
			|	ИЛИ тбПланирование.КоличествоКПланированиюОстаток < 0
			|	ИЛИ тбПланирование.КоличествоКВыполнениюОстаток < 0
			|;
			|//{Запрос: 6 ////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	втЗадания.Задание КАК Задание
			|ПОМЕСТИТЬ втЗаданияЗапрещеноИсполнениеПоНесколькимРейсам
			|ИЗ
			|	втЗаданияПоДокументам КАК втЗадания
			|ГДЕ втЗадания.ЗапрещеноИсполнениеНесколькимиРейсами
			|;
			|//{Запрос: 7 ////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВЫБОР
			|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упРейс
			|			ТОГДА упЗаданияКПланированиюТ.Регистратор
			|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРейса
			|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упКорректировкаРейса).Рейс
			|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПрохождениеТочки
			|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПрохождениеТочки).Рейс
			|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПроисшествиеВПути
			|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПроисшествиеВПути).Рейс
			|		ИНАЧЕ NULL
			|	КОНЕЦ КАК Рейс,
			|	упЗаданияКПланированиюТ.Задание КАК Задание
			|ПОМЕСТИТЬ втРейсыПоЗаданиям
			|ИЗ
			|	РегистрНакопления.упЗаданияКПланированию КАК упЗаданияКПланированиюТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияЗапрещеноИсполнениеПоНесколькимРейсам КАК втЗадания
			|	ПО втЗадания.Задание = упЗаданияКПланированиюТ.Задание
			|ГДЕ ИСТИНА
			|	И НЕ (упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза)
			|	И НЕ (упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРегистров)
			|;
			|//{Запрос: 8 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРейсыПоЗаданиямТ.Задание КАК Задание,
			|	СУММА(1) КАК КоличествоРазличныхРейсов
			|ПОМЕСТИТЬ втЗаданияСНесклькимиРейсами
			|ИЗ
			|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
			|		,
			|		) КАК УпСтатусыРейсов_СрезПоследнихТ
			|	ПО втРейсыПоЗаданиямТ.Рейс = УпСтатусыРейсов_СрезПоследнихТ.Документ
			|ГДЕ УпСтатусыРейсов_СрезПоследнихТ.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
			|СГРУППИРОВАТЬ ПО
			|	втРейсыПоЗаданиямТ.Задание
			|ИМЕЮЩИЕ СУММА(1) > 1
			|;
			|//{Запрос: 9 ////////////////////////////////////////
			|ВЫБРАТЬ
			|	втРейсыПоЗаданиямТ.Задание КАК Задание,
			|	втРейсыПоЗаданиямТ.Рейс КАК Рейс
			|ИЗ
			|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияСНесклькимиРейсами КАК втЗаданияСНесклькимиРейсамиТ
			|	ПО втЗаданияСНесклькимиРейсамиТ.Задание = втРейсыПоЗаданиямТ.Задание
			|ИТОГИ
			|ПО
			|	Задание";
			
			ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом = ИндексПоследнегоЗапроса + 1;
			ИндексЗапросаОстаткиКПланированию = ИндексПоследнегоЗапроса + 2;
			ИндексЗапросаЗаданияПоНесколькимРейсам = ИндексПоследнегоЗапроса + 6;
			ИндексПоследнегоЗапроса = ИндексЗапросаЗаданияПоНесколькимРейсам;
			
		КонецЕсли;
		
		Если КонтролироватьСтаффировкуКонтейнера Тогда
			Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + "
			|ВЫБРАТЬ
			|	упЗаданиеНаПеревозкуГрузаТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
			|	упЗаявкаНаПеревозкуГрузаТ.ДокументОснование КАК ОперацияСКонтейнером
			|ИЗ
			|	втЗадания КАК втЗаданияТ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГрузаТ
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
			|				,
			|				) КАК УпСтатусыРейсов_СрезПоследнихТ
			|			ПО упЗаявкаНаПеревозкуГрузаТ.ДокументОснование = УпСтатусыРейсов_СрезПоследнихТ.Документ
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейсТ
			|			ПО ИСТИНА
			|				И упРейсТ.Ссылка = упЗаявкаНаПеревозкуГрузаТ.ДокументОснование
			|				И упРейсТ.ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами)
			|		ПО ИСТИНА
			|			И упЗаявкаНаПеревозкуГрузаТ.Ссылка = упЗаданиеНаПеревозкуГрузаТ.ЗаявкаНаПеревозкуГруза
			|			И упЗаявкаНаПеревозкуГрузаТ.ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ПеревозкаГрузов)
			|	ПО ИСТИНА
			|			И упЗаданиеНаПеревозкуГрузаТ.Ссылка = втЗаданияТ.Задание		
			|ГДЕ ИСТИНА
			|	И упЗаявкаНаПеревозкуГрузаТ.ДокументОснование ССЫЛКА Документ.упРейс
			|	И НЕ (УпСтатусыРейсов_СрезПоследнихТ.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется),
			|			ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)))";
			ИндексЗапросаКонтрольСтаффировкиКонтейнера = ИндексПоследнегоЗапроса + 1;

		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упКорректировкаРейса") Тогда	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	тбзЗадания.Задание КАК Задание,
		|	тбзЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
		|	тбзЗадания.СтрокаДокумента КАК СтрокаДокумента,
		|	тбзЗадания.Тара КАК Тара
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	&тбзЗадания КАК тбзЗадания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадания.Задание КАК Задание,
		|	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втЗадания.Тара КАК Тара
		|ИЗ
		|	втЗадания КАК втЗадания
		|ГДЕ
		|	ИСТИНА
		|	И втЗадания.СтрокаДокумента
		|	И НЕ втЗадания.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|	И НЕ втЗадания.Тара В
		|				(ВЫБРАТЬ
		|					втЗадания.ГрузовоеМесто КАК Тара
		|				ИЗ
		|					втЗадания КАК втЗадания)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбПланирование.Задание КАК Задание,
		|	тбПланирование.ГрузовоеМесто КАК ГрузовоеМесто,
		|	тбПланирование.КоличествоКПланированиюОстаток КАК КоличествоКПланированию,
		|	тбПланирование.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнению
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(
		|			,
		|			(Задание, ГрузовоеМесто) В
		|				(ВЫБРАТЬ
		|					втЗадания.Задание КАК Задание,
		|					втЗадания.ГрузовоеМесто КАК ГрузовоеМесто
		|				ИЗ
		|					втЗадания КАК втЗадания
		|				ГДЕ
		|					втЗадания.СтрокаДокумента)) КАК тбПланирование
		|ГДЕ
		|	(ЛОЖЬ
		|			ИЛИ тбПланирование.КоличествоКПланированиюОстаток < 0
		|			ИЛИ тбПланирование.КоличествоКВыполнениюОстаток < 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втЗадания.Задание КАК Задание
		|ПОМЕСТИТЬ втЗаданияЗапрещеноИсполнениеПоНесколькимРейсам
		|ИЗ
		|	втЗадания КАК втЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
		|		ПО втЗадания.Задание = упЗаданиеНаПеревозкуГрузаТ.Ссылка
		|ГДЕ
		|	упЗаданиеНаПеревозкуГрузаТ.ЗапрещеноИсполнениеНесколькимиРейсами
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упРейс
		|			ТОГДА упЗаданияКПланированиюТ.Регистратор
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРейса
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упКорректировкаРейса).Рейс
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПрохождениеТочки
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПрохождениеТочки).Рейс
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПроисшествиеВПути
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПроисшествиеВПути).Рейс
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК Рейс,
		|	упЗаданияКПланированиюТ.Задание КАК Задание
		|ПОМЕСТИТЬ втРейсыПоЗаданиям
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию КАК упЗаданияКПланированиюТ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияЗапрещеноИсполнениеПоНесколькимРейсам КАК втЗадания
		|		ПО (втЗадания.Задание = упЗаданияКПланированиюТ.Задание)
		|ГДЕ
		|	ИСТИНА
		|	И НЕ упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза
		|	И НЕ упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРегистров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРейсыПоЗаданиямТ.Задание КАК Задание,
		|	СУММА(1) КАК КоличествоРазличныхРейсов
		|ПОМЕСТИТЬ втЗаданияСНесколькимиРейсами
		|ИЗ
		|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, ) КАК УпСтатусыРейсов_СрезПоследнихТ
		|		ПО втРейсыПоЗаданиямТ.Рейс = УпСтатусыРейсов_СрезПоследнихТ.Документ
		|ГДЕ
		|	УпСтатусыРейсов_СрезПоследнихТ.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
		|
		|СГРУППИРОВАТЬ ПО
		|	втРейсыПоЗаданиямТ.Задание
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРейсыПоЗаданиямТ.Задание КАК Задание,
		|	втРейсыПоЗаданиямТ.Рейс КАК Рейс
		|ИЗ
		|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияСНесколькимиРейсами КАК втЗаданияСНесколькимиРейсамиТ
		|		ПО (втЗаданияСНесколькимиРейсамиТ.Задание = втРейсыПоЗаданиямТ.Задание)
		|ИТОГИ ПО
		|	Задание";
		Запрос.УстановитьПараметр("тбзЗадания", ДополнительныеСвойства.КорректировкаРейса.Задания.Выгрузить());	
		ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом = 1;
		ИндексЗапросаОстаткиКПланированию = 2;
		ИндексЗапросаЗаданияПоНесколькимРейсам = 6;
		
	Иначе
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбПланирование.Задание КАК Задание,
		|	тбПланирование.ГрузовоеМесто КАК ГрузовоеМесто,
		|	тбПланирование.КоличествоКПланированиюОстаток КАК КоличествоКПланированию,
		|	тбПланирование.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнению
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(
		|		,
		|		Задание = &Ссылка) КАК тбПланирование
		|ГДЕ ЛОЖЬ
		|	ИЛИ тбПланирование.КоличествоКПланированиюОстаток < 0
		|	ИЛИ тбПланирование.КоличествоКВыполнениюОстаток < 0
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упРейс
		|			ТОГДА упЗаданияКПланированиюТ.Регистратор
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРейса
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упКорректировкаРейса).Рейс
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПрохождениеТочки
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПрохождениеТочки).Рейс
		|		КОГДА упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упПроисшествиеВПути
		|			ТОГДА ВЫРАЗИТЬ(упЗаданияКПланированиюТ.Регистратор КАК Документ.упПроисшествиеВПути).Рейс
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК Рейс,
		|	упЗаданияКПланированиюТ.Задание КАК Задание
		|ПОМЕСТИТЬ втРейсыПоЗаданиям
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию КАК упЗаданияКПланированиюТ
		|ГДЕ ИСТИНА
		|	И НЕ (упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза)
		|	И НЕ (упЗаданияКПланированиюТ.Регистратор ССЫЛКА Документ.упКорректировкаРегистров)
		|	И упЗаданияКПланированиюТ.Задание.ЗапрещеноИсполнениеНесколькимиРейсами
		|	И упЗаданияКПланированиюТ.Задание = &Ссылка
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРейсыПоЗаданиямТ.Задание КАК Задание,
		|	СУММА(1) КАК КоличествоРазличныхРейсов
		|ПОМЕСТИТЬ втЗаданияСНесколькимиРейсами
		|ИЗ
		|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
		|		,
		|		) КАК УпСтатусыРейсов_СрезПоследнихТ
		|	ПО втРейсыПоЗаданиямТ.Рейс = УпСтатусыРейсов_СрезПоследнихТ.Документ
		|ГДЕ УпСтатусыРейсов_СрезПоследнихТ.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
		|СГРУППИРОВАТЬ ПО
		|	втРейсыПоЗаданиямТ.Задание
		|ИМЕЮЩИЕ СУММА(1) > 1
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРейсыПоЗаданиямТ.Задание КАК Задание,
		|	втРейсыПоЗаданиямТ.Рейс КАК Рейс
		|ИЗ
		|	втРейсыПоЗаданиям КАК втРейсыПоЗаданиямТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияСНесколькимиРейсами КАК втЗаданияСНесколькимиРейсамиТ
		|	ПО втЗаданияСНесколькимиРейсамиТ.Задание = втРейсыПоЗаданиямТ.Задание
		|ИТОГИ
		|ПО
		|	Задание
		|";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ИндексЗапросаОстаткиКПланированию = 0;
		ИндексЗапросаЗаданияПоНесколькимРейсам = 3;
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если КонтролироватьЗадания Тогда
			
		Если Не РезультатЗапроса[ИндексЗапросаОстаткиКПланированию].Пустой() Тогда
			Выборка = РезультатЗапроса[ИндексЗапросаОстаткиКПланированию].Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.КоличествоКПланированию < 0 Тогда
					Если ФормироватьПоГрузовымМестам Тогда
						ТекстСообщения = НСтр("ru = 'По грузовому месту %1 нет плана для включения в рейс (уже оформлен рейс или нет плана по грузовому месту в %2).'");
						ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ГрузовоеМесто, Выборка.Задание);
					Иначе	
						ТекстСообщения = НСтр("ru = 'По %1 нет плана для включения в рейс (уже оформлен рейс или задание на перевозку не передано к планированию).'");
						ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Задание);
					КонецЕсли;
				Иначе	
					Если ФормироватьПоГрузовымМестам Тогда
						ТекстСообщения = НСтр("ru = 'По грузовому месту %1 нет плана для перевода задания (%2) к выполнению.'");
						ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ГрузовоеМесто, Выборка.Задание);
					Иначе	
						ТекстСообщения = НСтр("ru = 'По %1 нет плана для перевода задания к выполнению.'");
						ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Задание);
					КонецЕсли;
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,Метаданные.Документы.упПрохождениеТочки, Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);    
							
			КонецЦикла;
		КонецЕсли; 
		
		Если Не РезультатЗапроса[ИндексЗапросаЗаданияПоНесколькимРейсам].Пустой() Тогда
			ВыборкаЗадание = РезультатЗапроса[ИндексЗапросаЗаданияПоНесколькимРейсам].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаЗадание.Следующий() Цикл
				ЗаданиеТекущее = ВыборкаЗадание.Задание;
				мсвРейсы = Новый Массив;
				ВыборкаРейсы = ВыборкаЗадание.Выбрать();
				Пока ВыборкаРейсы.Следующий() Цикл
					// Если контроль осуществляется из рейса, то текущий рейс выводить в сообщение не надо.
					Если Ссылка <> ВыборкаРейсы.Рейс Тогда
						мсвРейсы.Добавить(ВыборкаРейсы.Рейс);
					КонецЕсли;
				КонецЦикла;
				ТекстСообщения = НСтр("ru = 'Запрещено выполнение задания %1 несколькими рейсами.(Задание выполнялось рейсами:%2)'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ВыборкаЗадание.Задание, СтрСоединить(мсвРейсы, "; "));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,Метаданные.Документы.упПрохождениеТочки, Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);    
			КонецЦикла;
		КонецЕсли;

		Если Истина
			И ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом <> Неопределено
			И Не РезультатЗапроса[ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом].Пустой()
		Тогда
			ВыборкаТара = РезультатЗапроса[ИндексЗапросаТараПеревозитсяВместеСГрузовымМестом].Выбрать();
			Пока ВыборкаТара.Следующий() Цикл
				ТекстСообщения = НСтр("ru = 'Не обнаружен план на перевозку тары %1 (грузовое место %2 (%3) доставляется в таре).'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ВыборкаТара.Тара, ВыборкаТара.ГрузовоеМесто, ВыборкаТара.Задание);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,, Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);    
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли;
	
	Если КонтролироватьСтаффировкуКонтейнера Тогда
		
		Если Не РезультатЗапроса[ИндексЗапросаКонтрольСтаффировкиКонтейнера].Пустой() Тогда
			Выборка = РезультатЗапроса[ИндексЗапросаКонтрольСтаффировкиКонтейнера].Выбрать();
			мсвОперации = Новый Массив;
			Если Выборка.Следующий() Тогда
				ТекстСообщения = Нстр("ru = '%1; %2'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ЗаявкаНаПеревозкуГруза, Выборка.ОперацияСКонтейнером);
				мсвОперации.Добавить(ТекстСообщения);
			КонецЕсли;
			ТекстСообщения = Нстр("ru = 'Запрещено начало выполнения рейса по перевозке груз-контейнера до выполнения стаффировки контейнеров.
			|(%1)'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, СтрСоединить(мсвОперации, Символы.ПС));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,, Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);    
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПроконтролироватьЗадание()

// Процедура - контролирует правильность заполнения временных интервалов отправления/получения груза.
//
// Параметры:
//  ОтправлениеГрузаС - Дата - Дата время.
//  ОтправлениеГрузаПо - Дата - Дата время.
//  ПолучениеГрузаС - Дата - Дата время.
//  ПолучениеГрузаПо - Дата - Дата время.
//  Отказ - Булево - Флаг фиксирования изменений.
//
Процедура ПроконтролироватьИнтервалОтправкиПолучения(ОтправлениеГрузаС, ОтправлениеГрузаПо, ПолучениеГрузаС, ПолучениеГрузаПо, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ОтправлениеГрузаС) И ЗначениеЗаполнено(ПолучениеГрузаПо) Тогда
		Если ОтправлениеГрузаС > ПолучениеГрузаПо Тогда
			ТекстСообщения = Нстр("ru = 'Верхняя граница получения груза меньше нижней границы отправления'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			,
			"Объект.ПолучениеГрузаПо",
			,
			Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтправлениеГрузаС) И ЗначениеЗаполнено(ОтправлениеГрузаПо) Тогда
		Если ОтправлениеГрузаПо < ОтправлениеГрузаС Тогда
			ТекстСообщения = Нстр("ru = 'Верхняя граница отправления груза меньше нижней границы отправления'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							"Объект.ОтправлениеГрузаПо",
							,
							Отказ);
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(ПолучениеГрузаС) И ЗначениеЗаполнено(ПолучениеГрузаПо) Тогда
		Если ПолучениеГрузаПо < ПолучениеГрузаС Тогда
			ТекстСообщения = Нстр("ru = 'Верхняя граница получения груза меньше нижней границы получения'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							"Объект.ПолучениеГрузаПо",
							,
							Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - контролирует правильность заполнения временных интервалов отправления/получения груза.
//
// Параметры:
//  ОтправлениеГрузаС	 - Дата	 - Дата время.
//  ОтправлениеГрузаПо	 - Дата	 - Дата время.
//  ПолучениеГрузаС		 - Дата	 - Дата время.
//  ПолучениеГрузаПо	 - Дата	 - Дата время.
//  ТекстОшибки		 - Строка		 - текст ошибки.
//  Отказ				 - Булево	 - Флаг фиксирования изменений.
//
Процедура ПроконтролироватьВременныеОкнаЗадания(ОтправлениеГрузаС, ОтправлениеГрузаПо, ПолучениеГрузаС, ПолучениеГрузаПо, ТекстОшибки, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ОтправлениеГрузаС) И ЗначениеЗаполнено(ПолучениеГрузаПо) Тогда
		Если ОтправлениеГрузаС > ПолучениеГрузаПо Тогда
			ТекстСообщения = Нстр("ru = 'Верхняя граница получения груза меньше нижней границы отправления'");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтправлениеГрузаС) И ЗначениеЗаполнено(ОтправлениеГрузаПо) Тогда
		Если ОтправлениеГрузаПо < ОтправлениеГрузаС Тогда
			ТекстСообщения = Нстр("ru = 'Верхняя граница отправления груза меньше нижней границы отправления'");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(ПолучениеГрузаС) И ЗначениеЗаполнено(ПолучениеГрузаПо) Тогда
		Если ПолучениеГрузаПо < ПолучениеГрузаС Тогда
			ТекстСообщения = Нстр("ru = 'Верхняя граница получения груза меньше нижней границы получения'");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


// Процедура контролирует входит ли задание в состав не отмененного рейса
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на документ.
//  Отказ - Булево - Флаг фиксирования изменений.
//
Процедура ПроконтролироватьВходитВСоставРейса(ДополнительныеСвойства, Ссылка, Отказ) Экспорт

	Запрос = Новый Запрос;
	СхемаЗапроса = Новый СхемаЗапроса;
	
	Текст = "ВЫБРАТЬ
	        |	ВЫБОР
	        |		КОГДА &ФормироватьПоГрузовымМестам
	        |			ТОГДА упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто
	        |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	        |	КОНЕЦ КАК ГрузовоеМесто,
	        |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов,
	        |	СУММА(ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток, 0)) КАК КоличествоКПланированиюОстаток,
	        |	СУММА(ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток, 0)) КАК КоличествоНеПодтвержденоОстаток,
	        |	СУММА(ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток, 0)) КАК КоличествоНеПереданноеКВыполнениюОстаток,
	        |	СУММА(ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток, 0)) КАК КоличествоКВыполнениюОстаток
	        |ИЗ
	        |	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упЗаданияКПланированию.Остатки(
	        |				,
	        |				(ГрузовоеМесто, Задание) В
	        |					(ВЫБРАТЬ
	        |						упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто,
	        |						упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК Задание
	        |					ИЗ
	        |						Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	        |					ГДЕ
	        |						упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = &Задание)) КАК упЗаданияКПланированиюОстатки
	        |		ПО упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упЗаданияКПланированиюОстатки.ГрузовоеМесто
	        |ГДЕ
	        |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = &Задание
	        |	И (НЕ упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов = ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток, 0)
	        |			ИЛИ НЕ упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов = ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток, 0)
	        |			ИЛИ НЕ упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов = ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток, 0)
	        |			ИЛИ НЕ упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов = ЕСТЬNULL(упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток, 0))
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов,
	        |	ВЫБОР
	        |		КОГДА &ФормироватьПоГрузовымМестам
	        |			ТОГДА упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто
	        |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	        |	КОНЕЦ
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ ПЕРВЫЕ 1
	        |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Ссылка) КАК Ссылка
	        |ИЗ
	        |	Документ.упРейс.Задания КАК упРейсЗадания
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	        |		ПО упРейсЗадания.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	        |ГДЕ
	        |	упРейсЗадания.Задание = &Задание
	        |	И НЕ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)";
	СхемаЗапроса.УстановитьТекстЗапроса(Текст);				   
	
	ИндексЗапросаМожноОтменятьЗадание = 0;
	ИндексЗапросаЕстьРейс = 1;

	Если НЕ ДополнительныеСвойства.ФормироватьПоГрузовымМестам Тогда
		ПакетЗапросовСхемы 			= СхемаЗапроса.ПакетЗапросов;
		ЗапросНаОстаткиПоЗаданиямКПланированию 	= ПакетЗапросовСхемы[ИндексЗапросаМожноОтменятьЗадание];
		ТекстУсловия = "Задание = &Задание";
		ЗапросНаОстаткиПоЗаданиямКПланированию.Операторы[0].Источники[1].Источник.Параметры[1].Выражение = Новый ВыражениеСхемыЗапроса(ТекстУсловия);     		
		ТекстУсловия = "Истина";
		ЗапросНаОстаткиПоЗаданиямКПланированию.Операторы[0].Источники[0].Соединения[0].Условие	= Новый ВыражениеСхемыЗапроса(ТекстУсловия)
	КонецЕсли;
		
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Задание", Ссылка);
	Запрос.УстановитьПараметр("ФормироватьПоГрузовымМестам", ДополнительныеСвойства.ФормироватьПоГрузовымМестам);
	
	мсвРезультат = Запрос.ВыполнитьПакет();
	
	МожноОтменятьЗадание	= мсвРезультат[ИндексЗапросаМожноОтменятьЗадание].Пустой();
		
	текВыборкаРейс	= мсвРезультат[ИндексЗапросаЕстьРейс].Выбрать();
	
	Если текВыборкаРейс.Следующий() Тогда
		Если МожноОтменятьЗадание Тогда
			
			// Если происходит отмена проведения.
			Если ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ПустаяСсылка() Тогда
				
				ОписаниеОшибки = Нстр("ru = 'По документу %1 в статусе ""%2"" существуют движения сформированные другими документами, отмена проведения запрещена(см. структуру подчинения).'");
			 	ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Ссылка, ДополнительныеСвойства.ТекущийСтатус); 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,,,,Отказ);
				
			КонецЕсли;
			
			// Ситуация когда задание было включено в рейс, но по какой то проблеме не было не доставлено.
			// В этом случае некорректно очищать движения по заданию.
			//   так как это нарушает хронологическую последовательность действий.
			ДополнительныеСвойства.Вставить("ДобавлятьДвиженияПриОтменеЗадания",	Истина);
		Иначе	
			ТекстСообщения = Нстр("ru = 'Задание на перевозку включено в документ %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текВыборкаРейс.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // ПроконтролироватьВходитВСоставРейса()

#КонецОбласти 

#Область ВспомогательныеПроцедурыФункции

// Функция - Получить временной интервал задания
//
// Параметры:
//  ТипТочки - ПеречислениеСсылка - Ссылка на элемент.
//  Операция - СправочникСсылка - Ссылка на элемент.
//  Задание - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Структура - Данные результата обработки.
//
Функция ПолучитьВременнойИнтервалЗадания(ТипТочки, Операция, Задание) Экспорт
	
	сткВременныеИнтервалы = ПолучитьВременныеИнтервалыЗадания(Задание);
	
	сткРезультат =Новый Структура("ВременноеОкноНачало, ВременноеОкноОкончание", '00010101', '00010101');
	
	Если НЕ сткВременныеИнтервалы = Неопределено Тогда
		
		Если упПрохождениеТочкиКлиентСервер.ОперацияПогрузки(Операция) Тогда
			сткРезультат.ВременноеОкноНачало 	= сткВременныеИнтервалы.Погрузка.Начало;
			сткРезультат.ВременноеОкноОкончание = сткВременныеИнтервалы.Погрузка.Окончание;
		ИначеЕсли упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(Операция) Тогда
			сткРезультат.ВременноеОкноНачало 	= сткВременныеИнтервалы.Разгрузка.Начало;
			сткРезультат.ВременноеОкноОкончание = сткВременныеИнтервалы.Разгрузка.Окончание;
		Иначе
			Если ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления") Тогда
				сткРезультат.ВременноеОкноНачало 	= сткВременныеИнтервалы.Погрузка.Начало;
				сткРезультат.ВременноеОкноОкончание = сткВременныеИнтервалы.Погрузка.Окончание;
			ИначеЕсли ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
				сткРезультат.ВременноеОкноНачало 	= сткВременныеИнтервалы.Разгрузка.Начало;
				сткРезультат.ВременноеОкноОкончание = сткВременныеИнтервалы.Разгрузка.Окончание;
			КонецЕсли;
		КонецЕсли;	
		
	КонецЕсли;
			
	Возврат сткРезультат;

КонецФункции // ПолучитьВременнойИнтервалЗадания()

// Функция - возвращает структуру  с временным интервалом по переданному задани.
//
// Параметры:
//  Задание - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//  Структура - Данные результата обработки.
//
Функция ПолучитьВременныеИнтервалыЗадания(Задание) Экспорт
	
	сткРезультат = Неопределено;
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	                  |	тбЗадание.ОтправлениеГрузаС,
	                  |	тбЗадание.ОтправлениеГрузаПо,
	                  |	тбЗадание.ПолучениеГрузаС,
	                  |	тбЗадание.ПолучениеГрузаПо
	                  |ИЗ
	                  |	Документ.упЗаданиеНаПеревозкуГруза КАК тбЗадание
	                  |ГДЕ
	                  |	тбЗадание.Ссылка = &Ссылка";
	текЗапрос.УстановитьПараметр("Ссылка", Задание);
	текВыборка = текЗапрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		
		сткРезультат = Новый Структура;
		
		сткОперация	= Новый Структура("Начало, Окончание", текВыборка.ОтправлениеГрузаС, текВыборка.ОтправлениеГрузаПо);
		сткРезультат.Вставить("Погрузка", сткОперация);
		
		сткОперация	= Новый Структура("Начало, Окончание", текВыборка.ПолучениеГрузаС, текВыборка.ПолучениеГрузаПо);
		сткРезультат.Вставить("Разгрузка", сткОперация);
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Функция - проверяет возможность перевода в статус к выполнению.
//
// Параметры:
//  ТекущийСтатус - ПеречислениеСсылка - Ссылка на элемент.
//  ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза - Булево - Флаг.
//
// Возвращаемое значение:
//  Структура - Данные результата обработки.
//
Функция РазрешеноПеревестиКВыполнению(ТекущийСтатус, ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза) Экспорт

	сткВозврата = Новый Структура("Результат, ТекстОшибки", Ложь, "");
	
	Если (Ложь
		Или ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована
		Или ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок
		Или (Истина
			И (Ложь 
			   Или ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
		        Или ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована)
		     И Не ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза)) 
	Тогда 
		сткВозврата.Вставить("Результат", Истина);
	Иначе
		Если ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза Тогда 
			сткВозврата.Вставить("ТекстОшибки", НСтр("ru = 'Статус заявки должен быть ""Согласована""'"));
		Иначе	
			сткВозврата.Вставить("ТекстОшибки", НСтр("ru = 'Статус заявки должен быть ""Новая""'"));
		КонецЕсли;	
	КонецЕсли;
	
	Возврат сткВозврата;
	
КонецФункции // РазрешеноПеревестиКВыполнению()

// Фукция -  возвращает таблицу значений с данными по заявкам на перевозку груза.
//
// Параметры:
//  мсвЗаданияНаПеревозкуГруза - Массив - Масив ссылок на документы.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Табличные данные.
//
Функция ПолучитьЗаявкиПоЗаданиям(мсвЗаданияНаПеревозкуГруза) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК Заявка
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка В(&Задания)";
	Запрос.УстановитьПараметр("Задания", мсвЗаданияНаПеревозкуГруза);
	
	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции // ПолучитьЗаявкиПоЗаданиям()

// Устанавливает заголовок формы задания на перевозку.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется процедура.
//
Процедура УстановитьЗаголовокФормыЗаданияНаПеревозку(Форма) Экспорт

	ОбъектЗадание = Форма.Объект;
	Если Не ОбъектЗадание.Ссылка.Пустая() Тогда
		Если ЗначениеЗаполнено(ОбъектЗадание.НомерВЦепиПоставок) Тогда
			Форма.Автозаголовок = Ложь;
			Форма.Заголовок = НСтр(СтрШаблон("ru = 'Задание на перевозку груза %1 от %2 (уч. в цепи поставок под номером ""%3"")'", ОбъектЗадание.Номер, ОбъектЗадание.Дата, ОбъектЗадание.НомерВЦепиПоставок));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвижений

// Процедура -  формирует движения по регистру "ДвиженияЗаявкиКВыполнению".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаявкиКВыполнению = Движения.упЗаявкиКВыполнению;
	ДвиженияЗаявкиКВыполнению.Записывать = Ложь;
	
	Статус = ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус = ДополнительныеСвойства.ЗаписыватьСтатус;
	Если ЗначениеЗаполнено(Реквизиты.ЗаявкаНаПеревозкуГруза) Тогда
		Если Истина
			И ЗаписыватьСтатус
			И Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию
		Тогда
			Если Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку Тогда 
				Для Каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
					ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
					ДвижениеЗаявки.Период = Реквизиты.Дата;
					ДвижениеЗаявки.Заявка = Реквизиты.ЗаявкаНаПеревозкуГруза;
					ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Расход;
					ДвижениеЗаявки.ГрузовоеМесто = текГруз.ГрузовоеМесто;
					ДвижениеЗаявки.Тара = текГруз.Тара;
					ДвижениеЗаявки.НомерВЦепиПоставок = Реквизиты.НомерВЦепиПоставок;
					ДвижениеЗаявки.НомерВЦепиПоставокКонец = Реквизиты.НомерВЦепиПоставокКонец;
					ДвижениеЗаявки.Количество = текГруз.КоличествоГрузов;
				КонецЦикла;	
			Иначе
				ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
				ДвижениеЗаявки.Период = Реквизиты.Дата;
				ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеЗаявки.Заявка = Реквизиты.ЗаявкаНаПеревозкуГруза;
				ДвижениеЗаявки.НомерВЦепиПоставок = Реквизиты.НомерВЦепиПоставок;
				ДвижениеЗаявки.НомерВЦепиПоставокКонец = Реквизиты.НомерВЦепиПоставокКонец;
				ДвижениеЗаявки.Количество = 1;
			КонецЕсли;
			ДвиженияЗаявкиКВыполнению.Записывать = Истина;		
			ДополнительныеСвойства.КонтролироватьЗаявки = Истина;										
			
		ИначеЕсли Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда
			ДвиженияЗаявкиКВыполнению.Записывать = Истина;		
			ДополнительныеСвойства.КонтролироватьЗаявки = Истина;										
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упСостояниеПоЗаданиям".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостояния = Движения.упСостояниеПоЗаданиям;
	ДвиженияСостояния.Записывать = Ложь;
	
	Статус = ДополнительныеСвойства.Статус;
	
	ФормироватьДвижения = Ложь
					  Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию
					  Или (Истина
					       И Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса 
					  	  И (Ложь
							Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс
							Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению));
	
	Если ФормироватьДвижения Тогда
		Если Ложь
			Или Реквизиты.ФормироватьПоГрузовымМестам 
			Или Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
			
			Для каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
				ДвижениеСостояния = ДвиженияСостояния.Добавить();
				ДвижениеСостояния.Задание = Ссылка;
				ДвижениеСостояния.Местоположение = Реквизиты.АдресОтправления;
				ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.ГрузовоеМесто = текГруз.ГрузовоеМесто;
				ДвижениеСостояния.Количество = текГруз.КоличествоГрузов;
				
				ДвижениеСостояния = ДвиженияСостояния.Добавить();
				ДвижениеСостояния.Задание = Ссылка;
				ДвижениеСостояния.Местоположение = Реквизиты.АдресПолучения;
				ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеСостояния.Период = Реквизиты.Дата;
				ДвижениеСостояния.ГрузовоеМесто = текГруз.ГрузовоеМесто;
				ДвижениеСостояния.Количество = текГруз.КоличествоГрузов;
				
			КонецЦикла;
			
		Иначе	
			ДвижениеСостояния = ДвиженияСостояния.Добавить();
			ДвижениеСостояния.Местоположение = Реквизиты.АдресОтправления;
			ДвижениеСостояния.Задание = Ссылка;
			ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Приход;
			ДвижениеСостояния.Период = Реквизиты.Дата;
			ДвижениеСостояния.Количество = 1;
			
			ДвижениеСостояния = ДвиженияСостояния.Добавить();
			ДвижениеСостояния.Местоположение = Реквизиты.АдресПолучения;
			ДвижениеСостояния.Задание = Ссылка;
			ДвижениеСостояния.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеСостояния.Период = Реквизиты.Дата;
			ДвижениеСостояния.Количество = 1;
		КонецЕсли;
		ДвиженияСостояния.Записывать = Истина;
		ДополнительныеСвойства.КонтролироватьСостояниеЗадания = Истина;										
	ИначеЕсли Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда
		ДвиженияСостояния.Записывать = Истина;	
		ДополнительныеСвойства.КонтролироватьСостояниеЗадания = Истина;										
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упПланПоЗаданиям".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПлан	= Движения.упПланПоЗаданиям;
	ДвиженияПлан.Записывать = Истина;

	Статус = ДополнительныеСвойства.Статус;
	
	Если Истина
		И Статус <> Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
		И Статус <> Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое 
	Тогда
		Если Ложь
			Или Реквизиты.ФормироватьПоГрузовымМестам 
			Или Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса 
		Тогда
			Для каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
				ДвижениеПлан = ДвиженияПлан.Добавить();
				ДвижениеПлан.Задание = Ссылка;
				ДвижениеПлан.Период = Реквизиты.Дата;
				ДвижениеПлан.ГрузовоеМесто = текГруз.ГрузовоеМесто;
				ДвижениеПлан.Местоположение = Реквизиты.АдресПолучения;
				ДвижениеПлан.Количество = текГруз.КоличествоГрузов;
			КонецЦикла;
		Иначе	
			ДвижениеПлан = ДвиженияПлан.Добавить();
			ДвижениеПлан.Задание = Ссылка;
			ДвижениеПлан.Местоположение = Реквизиты.АдресПолучения;
			ДвижениеПлан.Период = Реквизиты.Дата;
			ДвижениеПлан.Количество = 1;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упЗаданияКПланированию".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияЗаданиеКПланированию(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗадания = Движения.упЗаданияКПланированию;
	ДвиженияЗадания.Записывать = Ложь;
		
	Статус = ДополнительныеСвойства.Статус;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию 
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс 
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению Тогда
			
		Если Реквизиты.ФормироватьПоГрузовымМестам И Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Тогда
			Для каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
				ДвижениеЗадание = ДвиженияЗадания.Добавить();
				ДвижениеЗадание.Задание = Ссылка;
				ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеЗадание.Период = Реквизиты.Дата;
				ДвижениеЗадание.ГрузовоеМесто = текГруз.ГрузовоеМесто;
				ДвижениеЗадание.Тара = текГруз.Тара;
				ДвижениеЗадание.КоличествоКПланированию = текГруз.КоличествоГрузов;
				ДвижениеЗадание.КоличествоКВыполнению = текГруз.КоличествоГрузов;
				ДвижениеЗадание.КоличествоНеПодтверждено = текГруз.КоличествоГрузов;
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению	 = текГруз.КоличествоГрузов;
			КонецЦикла;
		Иначе	
			
			Если Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
				ДвижениеЗадание = ДвиженияЗадания.Добавить();
				ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеЗадание.Задание = Ссылка;
				ДвижениеЗадание.Период = Реквизиты.Дата;
				ДвижениеЗадание.КоличествоКПланированию = 1;
				ДвижениеЗадание.КоличествоКВыполнению = 0;
				ДвижениеЗадание.КоличествоНеПодтверждено = 1;
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = 1;
				
				Для каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
					ДвижениеЗадание = ДвиженияЗадания.Добавить();
					ДвижениеЗадание.Задание = Ссылка;
					ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Приход;
					ДвижениеЗадание.Период = Реквизиты.Дата;
					ДвижениеЗадание.ГрузовоеМесто = текГруз.ГрузовоеМесто;
					ДвижениеЗадание.Тара = текГруз.Тара;
					ДвижениеЗадание.КоличествоКПланированию = 0;
					ДвижениеЗадание.КоличествоКВыполнению = текГруз.КоличествоГрузов;
					ДвижениеЗадание.КоличествоНеПодтверждено = 0;
					ДвижениеЗадание.КоличествоНеПереданноеКВыполнению  = 0;
				КонецЦикла;
				
			ИначеЕсли Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Тогда
				ДвижениеЗадание = ДвиженияЗадания.Добавить();
				ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеЗадание.Задание = Ссылка;
				ДвижениеЗадание.Период = Реквизиты.Дата;
				ДвижениеЗадание.КоличествоКПланированию = 1;
				ДвижениеЗадание.КоличествоКВыполнению = 1;
				ДвижениеЗадание.КоличествоНеПодтверждено = 1;	
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению	 = 1;
				
			Иначе
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		ДвиженияЗадания.Записывать = Истина;		
		ДополнительныеСвойства.КонтролироватьЗадания	= Истина;										
	ИначеЕсли Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда 
		Если ДополнительныеСвойства.СохранятьДвиженияЗаданияКВыполнению Тогда
			ДвиженияЗадания.Прочитать();	
			Если ДополнительныеСвойства.ДобавлятьДвиженияПриОтменеЗадания Тогда
				Если Реквизиты.ФормироватьПоГрузовымМестам Тогда
					Для каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
						ДвижениеЗадание = ДвиженияЗадания.Добавить();
						ДвижениеЗадание.Задание = Ссылка;
						ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
						ДвижениеЗадание.Период = Реквизиты.Дата;
						ДвижениеЗадание.ГрузовоеМесто = текГруз.ГрузовоеМесто;
						ДвижениеЗадание.Тара = текГруз.Тара;
						ДвижениеЗадание.КоличествоКПланированию = текГруз.КоличествоГрузов;
						ДвижениеЗадание.КоличествоКВыполнению = текГруз.КоличествоГрузов;
						ДвижениеЗадание.КоличествоНеПодтверждено = текГруз.КоличествоГрузов;
						ДвижениеЗадание.КоличествоНеПереданноеКВыполнению	 = текГруз.КоличествоГрузов;
					КонецЦикла;
				Иначе
					Если Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
						ДвижениеЗадание = ДвиженияЗадания.Добавить();
						ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
						ДвижениеЗадание.Задание = Ссылка;
						ДвижениеЗадание.Период = Реквизиты.Дата;
						ДвижениеЗадание.КоличествоКПланированию = 1;
						ДвижениеЗадание.КоличествоКВыполнению = 0;
						ДвижениеЗадание.КоличествоНеПодтверждено = 1;
						ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = 1;
						
						Для каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
							ДвижениеЗадание = ДвиженияЗадания.Добавить();
							ДвижениеЗадание.Задание = Ссылка;
							ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
							ДвижениеЗадание.Период = Реквизиты.Дата;
							ДвижениеЗадание.ГрузовоеМесто = текГруз.ГрузовоеМесто;
							ДвижениеЗадание.Тара = текГруз.Тара;
							ДвижениеЗадание.КоличествоКПланированию = 0;
							ДвижениеЗадание.КоличествоКВыполнению = текГруз.КоличествоГрузов;
							ДвижениеЗадание.КоличествоНеПодтверждено = 0;
							ДвижениеЗадание.КоличествоНеПереданноеКВыполнению  = 0;
						КонецЦикла;
						
					Иначе
						ДвижениеЗадание = ДвиженияЗадания.Добавить();
						ДвижениеЗадание.Задание = Ссылка;
						ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
						ДвижениеЗадание.Период = Реквизиты.Дата;
						ДвижениеЗадание.ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
						ДвижениеЗадание.КоличествоКПланированию = 1;
						ДвижениеЗадание.КоличествоКВыполнению = 1;
						ДвижениеЗадание.КоличествоНеПодтверждено = 1;
						ДвижениеЗадание.КоличествоНеПереданноеКВыполнению	 = 1;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		ДвиженияЗадания.Записывать = Истина;		
		ДополнительныеСвойства.КонтролироватьЗадания	= Истина;										
	КонецЕсли;	

КонецПроцедуры

// Процедура - проведения документа "упРейс".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура ИнициироватьОбновлениеРаботПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    ДополнительныеСвойства = Новый Структура;
	КонецЕсли;
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	Рейс = Реквизиты.Рейс;
	Если ЗначениеЗаполнено(Рейс) Тогда
		РейсОбъект = Рейс.ПолучитьОбъект();
		Если Не ДополнительныеСвойства.Свойство("ЗаписьПриИнтеграции") Тогда
			Попытка
				РейсОбъект.Заблокировать();
			Исключение
				ТекстОшибки = НСтр("ru= 'Задание включено в рейс, который не удалось заблокировать (%Элемент%). %ОписаниеОшибки%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Рейс);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
				Возврат;
			КонецПопытки;
		КонецЕсли; 
		РейсОбъект.ДополнительныеСвойства.Вставить("ОбновитьРаботыПоЗаданию", Ссылка);
		Попытка
			РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда 
				ТекстСообщения = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упРейс,, ТекстСообщения);
			ТекстОшибки = СтрШаблон("%1%2%3", ТекстОшибки, Символы.ПС, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		КонецПопытки;
	КонецЕсли; 
	
КонецПроцедуры

// Функция - возвращает рейс, в который включено задание
//
// Параметры:
//  Ссылка				 - ДокументСсылка.упЗаданиеНаПеревозкуГруза 	 - Задание на перевозку груза
//  ДополнительныеСвойства	 - Структура	 - Структура
//  Реквизиты				 - Структура	 - Структура
// 
// Возвращаемое значение:
//  Рейс, в - который включено задание
//
Функция РейсЗадания(Знач Ссылка, Знач ДополнительныеСвойства, Знач Реквизиты) Экспорт 
	
	Рейс = Неопределено;
	Если ДополнительныеСвойства.Свойство("Статус") И ЗначениеЗаполнено(ДополнительныеСвойства.Статус) Тогда
		Cтатус = ДополнительныеСвойства.Статус;
	Иначе	
		Cтатус = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Ссылка); // возможно чтение без блокировки!	
	КонецЕсли; 
	
	Если Истина
		И (Cтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс Или Cтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению)
		И Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Регистратор КАК Рейс
		|ИЗ
		|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(, Документ = &Ссылка) КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
		|ГДЕ
		|	упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс), ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению))
		|	И упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Регистратор ССЫЛКА Документ.упРейс";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			Рейс = Выборка.Рейс;
		КонецЕсли; 
	КонецЕсли;
	Возврат Рейс;

КонецФункции

// Процедура - формирует движения по регистру "упСтатусыЗаданийНаПеревозкуГруза".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияСтатусЗадания(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСтатуса = Движения.упСтатусыЗаданийНаПеревозкуГруза;
			
	Статус				= ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус	= ДополнительныеСвойства.ЗаписыватьСтатус;
	Если ЗаписыватьСтатус Тогда
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
				
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = ТекущаяДатаСеанса();
		ДвижениеСтатуса.Документ    = Ссылка;
		ДвижениеСтатуса.Статус      = Статус;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упСтатусыЗаданийНаПеревозкуГруза" для нескольких заданий.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияСтатусыЗаданий(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	мсвЗаданийСИзмененнымСтатусом = Неопределено;
	ДополнительныеСвойства.Свойство("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус",мсвЗаданийСИзмененнымСтатусом);	
	
	ДвиженияСтатуса = Движения.упСтатусыЗаданийНаПеревозкуГруза;
	Если ДополнительныеСвойства.Свойство("ОчиститьЗаписиСтатусЗадания") Тогда
		ДвиженияСтатуса.Очистить();	
	Иначе	
		ДвиженияСтатуса.Прочитать();
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("мсвЗадания") Тогда
		мсвЗадания = ДополнительныеСвойства.мсвЗадания;
	Иначе	
		тбзЗадания = Реквизиты.Задания.Выгрузить(,"Задание");
		тбзЗадания.Свернуть("Задание");
		мсвЗадания = тбзЗадания.ВыгрузитьКолонку("Задание");
		Если мсвЗадания.Количество() = 0 Тогда
			Если Реквизиты.Свойство("Рейс") Тогда
				мсвЗадания = упРейс.СписокЗаданийПоРейсу(Реквизиты.Рейс);
			КонецЕсли;
		КонецЕсли;
					
	КонецЕсли;
	
	Если Истина
		И ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс")
		И ДополнительныеСвойства.Свойство("Статус")
		И (Ложь
	        Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.Новый
	        Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.КПланированиюТС
	        Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.НазначеноТС
		   Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.Отменен
		  )
	Тогда
	     // Если задание было удалено из рейса
		мсвЗаписиСтатусаДляУдаления = Новый Массив;
		Для каждого текЗапись Из ДвиженияСтатуса Цикл
			Если мсвЗадания.Найти(текЗапись.Документ) = Неопределено Тогда
				мсвЗаписиСтатусаДляУдаления.Добавить(текЗапись);
				Если мсвЗаданийСИзмененнымСтатусом.Найти(текЗапись.Документ) = Неопределено Тогда
					мсвЗаданийСИзмененнымСтатусом.Добавить(текЗапись.Документ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого текЗапись Из мсвЗаписиСтатусаДляУдаления Цикл
			ДвиженияСтатуса.Удалить(текЗапись);	
			ДвиженияСтатуса.Записывать = Истина;
		КонецЦикла;
	КонецЕсли;
	
	тбзСтатусыЗаданий = упРаботаСоСтатусами.РассчитатьСтатусЗаданияНаПеревозку(мсвЗадания);
	
	Период = ?(Реквизиты.Свойство("ДатаСтатуса"), Реквизиты.ДатаСтатуса, Реквизиты.Дата);
	Для каждого текСтрока Из тбзСтатусыЗаданий Цикл
				
		Если текСтрока.Статус = текСтрока.ТекущийСтатус Тогда
			Продолжить;
		КонецЕсли;
					
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = Период;
		ДвижениеСтатуса.Документ    = текСтрока.Задание;
		ДвижениеСтатуса.Статус      = текСтрока.Статус;
		
		Если НЕ мсвЗаданийСИзмененнымСтатусом = Неопределено Тогда
			мсвЗаданийСИзмененнымСтатусом.Добавить(текСтрока.Задание);		
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДвиженияСтатуса.Количество() > 0 Тогда
		Если НЕ мсвЗаданийСИзмененнымСтатусом = Неопределено Тогда
			ДополнительныеСвойства.ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус = мсвЗаданийСИзмененнымСтатусом;
		КонецЕсли;	
		ДвиженияСтатуса.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует записи по регистру "упАдресаПартнеров".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Реквизиты - Структура - Реквизиты.
//
Процедура ЗаписатьАдресаПартнеров(ДополнительныеСвойства, Реквизиты) Экспорт
	
	Если ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Тогда
		
		Отправитель = Реквизиты.Отправитель;
		АдресОтправления = Реквизиты.АдресОтправления;
		Если ЗначениеЗаполнено(Отправитель) И ЗначениеЗаполнено(АдресОтправления) Тогда
			ГрафикРаботы = Реквизиты.ГрафикРаботыАдресаОтправления;
			Попытка
				НачатьТранзакцию();
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упАдресаПартнеров");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Партнер", Отправитель);
				ЭлементБлокировки.УстановитьЗначение("Адрес", АдресОтправления);
				
				Блокировка.Заблокировать();
				
				НаборЗаписей = РегистрыСведений.упАдресаПартнеров.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Партнер.Установить(Отправитель);
				НаборЗаписей.Отбор.Адрес.Установить(АдресОтправления);
				НаборЗаписей.Прочитать();
				Если НаборЗаписей.Количество() И ЗначениеЗаполнено(НаборЗаписей[0].ГрафикРаботы) Тогда
					ГрафикРаботы = НаборЗаписей[0].ГрафикРаботы;
				КонецЕсли; 
				НаборЗаписей.Очистить();
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Партнер = Отправитель;
				НоваяЗапись.Адрес = АдресОтправления;
				НоваяЗапись.ДатаИзменения = ТекущаяДатаСеанса();
				НоваяЗапись.ГрафикРаботы = ГрафикРаботы;
				НаборЗаписей.Записать();
				
				ЗафиксироватьТранзакцию();
			Исключение
				Если ТранзакцияАктивна() Тогда 
					ОтменитьТранзакцию();
				КонецЕсли;
			КонецПопытки;
		КонецЕсли; 
		
		Получатель = Реквизиты.Получатель;
		АдресПолучения = Реквизиты.АдресПолучения;
		Если ЗначениеЗаполнено(Получатель) И ЗначениеЗаполнено(АдресПолучения) Тогда
			ГрафикРаботы = Реквизиты.ГрафикРаботыАдресаПолучения;
			Попытка
				НачатьТранзакцию();
				
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упАдресаПартнеров");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Партнер", Получатель);
				ЭлементБлокировки.УстановитьЗначение("Адрес", АдресПолучения);
				
				Блокировка.Заблокировать();
				
				НаборЗаписей = РегистрыСведений.упАдресаПартнеров.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Партнер.Установить(Получатель);
				НаборЗаписей.Отбор.Адрес.Установить(АдресПолучения);
				НаборЗаписей.Прочитать();
				Если НаборЗаписей.Количество() И ЗначениеЗаполнено(НаборЗаписей[0].ГрафикРаботы) Тогда
					ГрафикРаботы = НаборЗаписей[0].ГрафикРаботы;
				КонецЕсли; 
				НаборЗаписей.Очистить();
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Партнер = Получатель;
				НоваяЗапись.Адрес = АдресПолучения;
				НоваяЗапись.ДатаИзменения = ТекущаяДатаСеанса();
				НоваяЗапись.ГрафикРаботы = ГрафикРаботы;
				НаборЗаписей.Записать();
				
				ЗафиксироватьТранзакцию();
			Исключение
				Если ТранзакцияАктивна() Тогда 
					ОтменитьТранзакцию();
				КонецЕсли;
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упПараметрыЗаданияНаПеревозку".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПараметрыЗаданияНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаборЗаписей = Движения.упПараметрыЗаданияНаПеревозку;
	ДвиженияНаборЗаписей.Записывать = Истина;
		
	Статус = ДополнительныеСвойства.Статус;
	
	Если Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению Тогда
		
		ПараметрыНаПеревозку = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПараметрыНаПеревозку");
		Если ПараметрыНаПеревозку <> Неопределено Тогда
			Для каждого Строка Из ПараметрыНаПеревозку Цикл
				
				ДвижениеЗапись = ДвиженияНаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеЗапись, Строка);
				ДвижениеЗапись.ЗаданиеНаПеревозку = Ссылка;
				ДвижениеЗапись.Период = Реквизиты.Дата;
				
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли Истина
			И Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
			И ДополнительныеСвойства.СохранятьДвиженияЗаданияКВыполнению Тогда 
			
		ДвиженияНаборЗаписей.Прочитать();	
		
	КонецЕсли;	

КонецПроцедуры

// Процедура - формирует движения по регистру "упПараметрыЗаявкиНаПеревозку".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияПараметрыЗаявкиНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНаборЗаписей = Движения.упПараметрыЗаявкиНаПеревозку;
	ДвиженияНаборЗаписей.Записывать = Истина;
	
	Если ЗначениеЗаполнено(Реквизиты.ЗаявкаНаПеревозкуГруза) Тогда
				
		Статус = ДополнительныеСвойства.Статус;
		
		Если Ложь
			Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию 
			Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс 
			Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению Тогда
			
			ПараметрыНаПеревозку = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПараметрыНаПеревозку");
			
			Если ПараметрыНаПеревозку <> Неопределено Тогда
				Для каждого Строка Из ПараметрыНаПеревозку Цикл
					
					ДвижениеЗапись = ДвиженияНаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(ДвижениеЗапись, Строка);
					ДвижениеЗапись.ЗаявкаНаПеревозку = Реквизиты.ЗаявкаНаПеревозкуГруза;
					ДвижениеЗапись.Период = Реквизиты.Дата;
					
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли Истина
				И Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
				И ДополнительныеСвойства.СохранятьДвиженияЗаданияКВыполнению Тогда 
			
			ДвиженияНаборЗаписей.Прочитать();	
			
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

// Процедура - формирует записи по регистру "упУточнениеГрузовогоСостава".
//
// Параметры:
//  ЗаданиеНаПеревозкуГруза	 - 	 - 
//  ГрузовоеМесто			 - 	 - 
//  ИзмененныйСостав		 - 	 - 
//
Процедура УстановитьУточненныйСоставГрузовогоМестаПоЗаданию(ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, ИзмененныйСостав, Отказ = Ложь) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Запись = РегистрыСведений.упУточненияГрузовогоСостава.СоздатьМенеджерЗаписи();
	Запись.Задание = ЗаданиеНаПеревозкуГруза;
	Запись.ГрузовоеМесто = ГрузовоеМесто;
	Запись.Прочитать();
	Если НЕ Запись.Выбран() Тогда
		Запись.Задание = ЗаданиеНаПеревозкуГруза;	
		Запись.ГрузовоеМесто = ГрузовоеМесто;	
	КонецЕсли;
	Запись.ИзмененныйСостав = ИзмененныйСостав;	
	Запись.Записать();
	
КонецПроцедуры

Процедура ПрочитатьУточненныйСоставГрузовогоМестаПоЗаданию(ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, ИзмененныйСостав, Отказ = Ложь) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Запись = РегистрыСведений.упУточненияГрузовогоСостава.СоздатьМенеджерЗаписи();
	Запись.Задание = ЗаданиеНаПеревозкуГруза;
	Запись.ГрузовоеМесто = ГрузовоеМесто;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		ИзмененныйСостав = Запись.ИзмененныйСостав;
		Запись.Удалить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаданияВЦепиПоставок

// Определяет параметры задания на перевозку в цепи поставок
//
// Параметры:
//  Задание		 - ДокументСсылка.упЗаданиеНаПеревозкуГруза 	 - задание.
//  ГрузовоеМесто	 - СправочникСсылка.упГрузовыеМеста 	 - грузовое место.
// 
// Возвращаемое значение:
//  Структура - параметры.
//
Функция ПараметрыЗаданияВЦепиПоставок(Задание, ГрузовоеМесто) Экспорт 
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("НомерВЦепиПоставок", 0);
	сткВозврат.Вставить("КрайнийНомер"      , 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК Заявка,
	|	упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок
	|ПОМЕСТИТЬ втЗаявка
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка = &Задание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявка.НомерВЦепиПоставок,
	|	МАКСИМУМ(упЗаявкиКВыполнению.НомерВЦепиПоставок) КАК КрайнийНомер
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению КАК упЗаявкиКВыполнению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявка КАК втЗаявка
	|		ПО упЗаявкиКВыполнению.Заявка = втЗаявка.Заявка
	|			И (&ГрузовоеМесто В (упЗаявкиКВыполнению.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)))
	|			И (упЗаявкиКВыполнению.ВидДвижения = ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход))
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗаявка.НомерВЦепиПоставок";
	Запрос.УстановитьПараметр("Задание", Задание);
	Запрос.УстановитьПараметр("ГрузовоеМесто", ГрузовоеМесто);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
	КонецЕсли;
	
	Возврат сткВозврат; 
	
КонецФункции // ПараметрыЗаданияВЦепиПоставок()
	
#КонецОбласти

Процедура СформироватьТаблицыДвиженийДляСписанияПлановПоЗаданиям(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ) Экспорт
	
	тбзЗаданияКПланированию = Новый ТаблицаЗначений;
	тбзЗаданияКПланированию = упСервисныеФункции.ТаблицаСтруктурыМетаданных(Метаданные.РегистрыНакопления, Метаданные.РегистрыНакопления.упЗаданияКПланированию);
	тбзЗаданияКПланированию.Колонки.Добавить("ВидДвижения");
	
	тбзСостояниеПоЗаданиям = Новый ТаблицаЗначений;
	тбзСостояниеПоЗаданиям = упСервисныеФункции.ТаблицаСтруктурыМетаданных(Метаданные.РегистрыНакопления, Метаданные.РегистрыНакопления.упСостояниеПоЗаданиям);
	тбзСостояниеПоЗаданиям.Колонки.Удалить("Количество"); // Так как ресурс неотрицательный.
	тбзСостояниеПоЗаданиям.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(5)));
	
	тбзПланПоЗаданиям = Новый ТаблицаЗначений;
	тбзПланПоЗаданиям = упСервисныеФункции.ТаблицаСтруктурыМетаданных(Метаданные.РегистрыСведений, Метаданные.РегистрыСведений.упПланПоЗаданиям);
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданияКПланированиюОстатки.Задание КАК Задание,
	|	упЗаданияКПланированиюОстатки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упЗаданияКПланированиюОстатки.Тара КАК Тара,
	|	упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток КАК КоличествоКПланированию,
	|	ВЫБОР
	|		КОГДА упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток
	|			ТОГДА упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток
	|		ИНАЧЕ упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток
	|	КОНЕЦ КАК КоличествоНеПодтверждено,
	|	ВЫБОР
	|		КОГДА упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток
	|			ТОГДА упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток
	|		ИНАЧЕ упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток
	|	КОНЕЦ КАК КоличествоНеПереданноеКВыполнению,
	|	ВЫБОР
	|		КОГДА упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток
	|			ТОГДА упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток
	|		ИНАЧЕ упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток
	|	КОНЕЦ КАК КоличествоКВыполнению
	|ПОМЕСТИТЬ втОстаткиПоЗаданию
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|		&Период,
	|		Задание = &Задание) КАК упЗаданияКПланированиюОстатки
	|ГДЕ упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > 0
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиПоЗаданиюТ.*
	|ИЗ
	|	втОстаткиПоЗаданию КАК втОстаткиПоЗаданиюТ
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланПоЗаданиям.Выполнено КАК Выполнено,
	|	ПланПоЗаданиям.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ПланПоЗаданиям.Задание КАК Задание,
	|	ПланПоЗаданиям.Количество КАК Количество,
	|	ПланПоЗаданиям.Местоположение КАК Местоположение,
	|	ПланПоЗаданиям.Отменено КАК Отменено,
	|	ПланПоЗаданиям.ПричинаОтмены КАК ПричинаОтмены
	|ИЗ
	|	РегистрСведений.упПланПоЗаданиям.СрезПоследних(
	|		&Период,
	|		(ГрузовоеМесто, Задание) В (
	|				ВЫБРАТЬ
	|				втОстаткиПоЗаданию.ГрузовоеМесто КАК ГрузовоеМесто,
	|				втОстаткиПоЗаданию.Задание КАК Задание
	|			ИЗ
	|				втОстаткиПоЗаданию КАК втОстаткиПоЗаданию)) КАК ПланПоЗаданиям
	|ГДЕ ИСТИНА
	|	И ПланПоЗаданиям.Отменено = ЛОЖЬ
	|	И ПланПоЗаданиям.Выполнено = ЛОЖЬ
	|	И ПланПоЗаданиям.Количество > 0
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГрузаТ.АдресОтправления КАК АдресОтправления,
	|	упЗаданиеНаПеревозкуГрузаТ.АдресПолучения КАК АдресПолучения
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
	|ГДЕ упЗаданиеНаПеревозкуГрузаТ.Ссылка = &Задание
	|";

	Запрос.УстановитьПараметр("Задание", РеквизитыОбъекта.ЗаданиеНаПеревозку);
	Запрос.УстановитьПараметр("Период", Новый Граница(РеквизитыОбъекта.Дата, ВидГраницы.Исключая));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексЗапросаЗаданияКПланированию = 1;
	ИндексЗапросаПланПоЗаданиям = 2;
	ИндексЗапросаЗадание = 3;
	
	АдресОтправления = Справочники.упАдреса.ПустаяСсылка();
	АдресПолучения = Справочники.упАдреса.ПустаяСсылка();
	
	мсвЗадания = Новый Массив; // Для расчета статусов заданий.
	РезультатЗапросаЗаданияКПланированию = РезультатЗапроса[ИндексЗапросаЗаданияКПланированию];
	Если Не РезультатЗапросаЗаданияКПланированию.Пустой() Тогда
		
		РезультатЗапросаЗадание = РезультатЗапроса[ИндексЗапросаЗадание];
		
		ВыборкаЗадание = РезультатЗапросаЗадание.Выбрать();
		Если ВыборкаЗадание.Следующий() Тогда
			АдресОтправления = ВыборкаЗадание.АдресОтправления;
			АдресПолучения = ВыборкаЗадание.АдресПолучения;
		КонецЕсли;
		
		Выборка = РезультатЗапросаЗаданияКПланированию.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = тбзЗаданияКПланированию.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
			мсвЗадания.Добавить(Выборка.Задание);
			
			НоваяСтрока = тбзСостояниеПоЗаданиям.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Местоположение = АдресОтправления;
			НоваяСтрока.Количество = -Выборка.КоличествоКПланированию;

			НоваяСтрока = тбзСостояниеПоЗаданиям.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Местоположение = АдресПолучения;
			НоваяСтрока.Количество = Выборка.КоличествоКПланированию;
						
		КонецЦикла; 
		
	Иначе	
		ТекстОшибки = НСтр("ru = 'Для задания %1 нет остатков к планированию!'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РеквизитыОбъекта.ЗаданиеНаПеревозку);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации(СобытиеОшибкаСписаниеПлановПоЗаданиюНаПеревозку("ОшибкаБлокировки"), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		Выборка = РезультатЗапроса[ИндексЗапросаПланПоЗаданиям].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			мсвСтрокиЗаданий = тбзЗаданияКПланированию.НайтиСтроки(Новый Структура("Задание, ГрузовоеМесто", Выборка.Задание, Выборка.ГрузовоеМесто));
			Если мсвСтрокиЗаданий.Количество() > 0 Тогда
				СтрокаЗаданияКПланированию = мсвСтрокиЗаданий[0];
				НоваяСтрока = тбзПланПоЗаданиям.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Количество = ?(СтрокаЗаданияКПланированию.КоличествоКПланированию > НоваяСтрока.Количество,0, НоваяСтрока.Количество - СтрокаЗаданияКПланированию.КоличествоКПланированию);
				Если НоваяСтрока.Количество = 0 Тогда
					НоваяСтрока.Отменено = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;	
	
	ДополнительныеСвойства.Вставить("Задания", тбзЗаданияКПланированию);
	
	мсвЗадания = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗадания);
	ДополнительныеСвойства.Вставить("мсвЗадания", мсвЗадания);

	ДополнительныеСвойства.Вставить("ПланПоЗаданиям", тбзПланПоЗаданиям);
	
	ДополнительныеСвойства.Вставить("СостояниеПоЗаданиям", тбзСостояниеПоЗаданиям);
			
КонецПроцедуры

Функция ЗапрещеноРедактироватьПланПоЗаданиямНаПеревозкуГруза(ТекущийОбъект, Период = Неопределено) Экспорт
	
	Результат = Ложь;
	
	Если ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упСписаниеПлановПоЗаданиюНаПеревозку") Тогда
		Период = ТекущийОбъект.Дата;
		ЗаданиеНаПеревозку = ТекущийОбъект.ЗаданиеНаПеревозку;
		Ссылка = ТекущийОбъект.Ссылка;
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	упЗаданияКПланированиюОстатки.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию КАК упЗаданияКПланированиюОстатки
	|ГДЕ
	|	упЗаданияКПланированиюОстатки.Период >= &Период
	|	И упЗаданияКПланированиюОстатки.Регистратор <> &Регистратор
	|	И упЗаданияКПланированиюОстатки.Задание = &ЗаданиеНаПеревозку
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УпСостояниеПоЗаданиям_ОстаткиТ.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.упСостояниеПоЗаданиям КАК УпСостояниеПоЗаданиям_ОстаткиТ
	|ГДЕ
	|	УпСостояниеПоЗаданиям_ОстаткиТ.Период >= &Период
	|	И УпСостояниеПоЗаданиям_ОстаткиТ.Регистратор <> &Регистратор
	|	И УпСостояниеПоЗаданиям_ОстаткиТ.Задание = &ЗаданиеНаПеревозку
	|";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", ЗаданиеНаПеревозку);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = Нстр("ru = 'Запрещено изменять план по заданию, так как есть изменения на дату %1 или более поздние.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Период);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		Событие = Нстр("ru = 'Контроль последовательности проведения'");
		ЗаписьЖурналаРегистрации(СобытиеОшибкаСписаниеПлановПоЗаданиюНаПеревозку(Событие), УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция СобытиеОшибкаСписаниеПлановПоЗаданиюНаПеревозку(ПодСобытие = "") Экспорт
	
	Результат = СтрШаблон(НСтр("ru = 'Списание планов по заданию на перевозку%1'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ?(ЗначениеЗаполнено(ПодСобытие), "." + ПодСобытие, ""));
	
	Возврат Результат;
КонецФункции


#КонецОбласти