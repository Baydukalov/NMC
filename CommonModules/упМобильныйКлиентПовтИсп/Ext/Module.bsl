#Область ПрограммныйИнтерфейс

// Определяет правила исполнения задачи.
//
// Параметры:
//  ТипОперации - ПеречислениеСсылка - Ссылка на элемент.
//	Перевозчик - СправочникССылка - Ссылка на элемент.
//	Водитель - СправочникСсылка.упСотрудники - Ссылка на элемент.
//  ВидАдреса	- СправочникСсылка.упВидыАдресов - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПравилаВыполненияЗадач(ТипОперации, Перевозчик = Неопределено, Водитель = Неопределено, ВидАдреса = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ТипОперации КАК ТипОперации,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.Перевозчик КАК Перевозчик,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.Водитель КАК Водитель,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ВидАдреса КАК ВидАдреса,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ТребуетсяСканированиеШК КАК ТребуетсяСканированиеШК,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ТребуетсяФотография КАК ТребуетсяФотография,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ТребуетсяФотографияПриОтменеЗадачи КАК ТребуетсяФотографияПриОтменеЗадачи,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.СпособПрисоединенияВнешнихФайлов КАК СпособПрисоединенияВнешнихФайлов,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.РазрешенаУстановкаНовогоШК КАК РазрешенаУстановкаНовогоШК,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.СпособФормированияНомераРаботы КАК СпособФормированияНомераРаботы,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ВыводитьКомментарийПоЗаявке КАК ВыводитьКомментарийПоЗаявке,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ВыводитьНомерИсходногоДокумента КАК ВыводитьНомерИсходногоДокумента,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.СпособФормированияНомераДокумента КАК СпособФормированияНомераДокумента,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава КАК РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ТребуетсяОтчитыватьсяОЗадачеПоКаждойСтрокеТоварногоСостава КАК ТребуетсяОтчитыватьсяОЗадачеПоКаждойСтрокеТоварногоСостава,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ЗапрещеноОтчитыватьсяОРаботе КАК ЗапрещеноОтчитыватьсяОРаботе,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.НеобходимостьФотографииОпределяетсяНаУровнеПроблемы КАК НеобходимостьФотографииОпределяетсяНаУровнеПроблемы,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.РазрешеноПробитиеКассовогоЧека КАК РазрешеноПробитиеКассовогоЧека,
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ПробиватьКаждыйЧекОтдельнойКассовойСменой КАК ПробиватьКаждыйЧекОтдельнойКассовойСменой
	|ИЗ
	|	РегистрСведений.упПравилаВыполненияЗадачВМобильномКлиенте КАК упПравилаВыполненияЗадачВМобильномКлиенте
	|ГДЕ
	|	упПравилаВыполненияЗадачВМобильномКлиенте.ТипОперации = &ТипОперации
	|	И упПравилаВыполненияЗадачВМобильномКлиенте.Перевозчик В (&Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|	И упПравилаВыполненияЗадачВМобильномКлиенте.Водитель В (&Водитель, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	|	И упПравилаВыполненияЗадачВМобильномКлиенте.ВидАдреса В (&ВидАдреса, ЗНАЧЕНИЕ(Справочник.упВидыАдресов.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Водитель УБЫВ,
	|	Перевозчик УБЫВ,
	|	ВидАдреса УБЫВ";
	Запрос.УстановитьПараметр("ТипОперации", ТипОперации);
	Запрос.УстановитьПараметр("Перевозчик",  Перевозчик);
	Запрос.УстановитьПараметр("Водитель",    Водитель);
	Запрос.УстановитьПараметр("ВидАдреса",   ВидАдреса);
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("ТребуетсяСканированиеШК"                                   , Ложь);
	сткВозврат.Вставить("ТребуетсяФотография"                                       , Ложь);
	сткВозврат.Вставить("ТребуетсяФотографияПриОтменеЗадачи"                        , Ложь);
	сткВозврат.Вставить("РазрешенаУстановкаНовогоШК"                                , Ложь);
	сткВозврат.Вставить("ТребуетсяСканированиеШК"                                   , Ложь);
	сткВозврат.Вставить("СпособПрисоединенияВнешнихФайлов"                          , Перечисления.упСпособыПрисоединенияВнешнихФайлов.КПрохождениюТочки);
	сткВозврат.Вставить("СпособФормированияНомераРаботы"                            , Перечисления.упСпособыФормированияНомераРаботы.ЗаданиеНомер);
	сткВозврат.Вставить("ВыводитьКомментарийПоЗаявке"                               , Ложь);
	сткВозврат.Вставить("ВыводитьНомерИсходногоДокумента"                           , Ложь);
	сткВозврат.Вставить("СпособФормированияНомераДокумента"                         , Перечисления.упСпособыФормированияНомераРаботы.ЗаявкаНомерКИС);
	сткВозврат.Вставить("РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава"    , Ложь);
	сткВозврат.Вставить("ТребуетсяОтчитыватьсяОЗадачеПоКаждойСтрокеТоварногоСостава", Ложь);
	сткВозврат.Вставить("ЗапрещеноОтчитыватьсяОРаботе"                              , Ложь);
	сткВозврат.Вставить("НеобходимостьФотографииОпределяетсяНаУровнеПроблемы"       , Ложь);
	сткВозврат.Вставить("РазрешеноПробитиеКассовогоЧека"                            , Ложь);
	сткВозврат.Вставить("ПробиватьКаждыйЧекОтдельнойКассовойСменой"                 , Ложь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
		Если Не ЗначениеЗаполнено(сткВозврат.СпособПрисоединенияВнешнихФайлов) Тогда
			сткВозврат.СпособПрисоединенияВнешнихФайлов = Перечисления.упСпособыПрисоединенияВнешнихФайлов.КПрохождениюТочки;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(сткВозврат.СпособФормированияНомераРаботы) Тогда
			сткВозврат.СпособФормированияНомераРаботы = Перечисления.упСпособыФормированияНомераРаботы.ЗаданиеНомер;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(сткВозврат.СпособФормированияНомераДокумента) Тогда
			сткВозврат.СпособФормированияНомераДокумента = Перечисления.упСпособыФормированияНомераРаботы.ЗаявкаНомерКИС;
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции	

// Определяет правила прохождения точки.
//
// Параметры:
//	Перевозчик - СправочникССылка - Ссылка на элемент.
//	Водитель - СправочникСсылка.упСотрудники - Ссылка на элемент.
//  ВидАдреса	- СправочникСсылка.упВидыАдресов - Ссылка на элемент.
//  Адрес	- СправочникСсылка.упАдреса - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПравилаПрохожденияТочки(Перевозчик = Неопределено, Водитель = Неопределено, ВидАдреса = Неопределено, Адрес = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПравилаПрохожденияТочекВМобильномКлиенте.Перевозчик КАК Перевозчик,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.Водитель КАК Водитель,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ВидАдреса КАК ВидАдреса,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.Адрес КАК Адрес,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ДопустимоеОтклонение КАК ДопустимоеОтклонение,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ЗапрещеноОтчитыватьсяОРаботах КАК ЗапрещеноОтчитыватьсяОРаботах,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.СпособПрисоединенияВнешнихФайлов КАК СпособПрисоединенияВнешнихФайлов,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ГруппироватьЗадачиПередачиЦенностей КАК ГруппироватьЗадачиПередачиЦенностей,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ЗапрещеноОтменятьТочкиМаршрута КАК ЗапрещеноОтменятьТочкиМаршрута,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.РазрешеноГрупповоеВыполнениеРабот КАК РазрешеноГрупповоеВыполнениеРабот,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ГруппироватьЗадачиСГрузомВТаре КАК ГруппироватьЗадачиСГрузомВТаре,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.СпособГруппировкиРабот КАК СпособГруппировкиРабот,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ЗакрыватьКассовуюСменуПослеУбытияИзТочки КАК ЗакрыватьКассовуюСменуПослеУбытияИзТочки,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ЗапрещеноОтменятьРаботыВТочкеВозвратаНаСклад КАК ЗапрещеноОтменятьРаботыВТочкеВозвратаНаСклад,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.РазрешеноДосрочноеУбытиеИзТочкиМаршрута КАК РазрешеноДосрочноеУбытиеИзТочкиМаршрута,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ВидДоступаДействиеДосрочноеУбытиеИзТочкиМаршрута КАК ВидДоступаДействиеДосрочноеУбытиеИзТочкиМаршрута,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ЗапрашиватьРазрешениеНаНазначениеТочкиМаршрутаСледующей КАК ЗапрашиватьРазрешениеНаНазначениеТочкиМаршрутаСледующей,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ГруппаТиповСобытийМониторинга КАК ГруппаТиповСобытийМониторинга,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ВыводитьСтатусГотовностиТочки КАК ВыводитьСтатусГотовностиТочки,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ВариантФормВыводитьСтатусГотовностиТочки КАК ВариантФормВыводитьСтатусГотовностиТочки,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ВыводитьИнформациюОСкладскихВоротах КАК ВыводитьИнформациюОСкладскихВоротах,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.ВариантФормВыводитьИнформациюОСкладскихВоротах КАК ВариантФормВыводитьИнформациюОСкладскихВоротах,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.НастройкаОтветовДосрочноеУбытиеИзТочкиМаршрута КАК НастройкаОтветовДосрочноеУбытиеИзТочкиМаршрута,
	|	упПравилаПрохожденияТочекВМобильномКлиенте.НастройкаОтветовНазначениеТочкиМаршрутаСледующей КАК НастройкаОтветовНазначениеТочкиМаршрутаСледующей
	|ИЗ
	|	РегистрСведений.упПравилаПрохожденияТочекВМобильномКлиенте КАК упПравилаПрохожденияТочекВМобильномКлиенте
	|ГДЕ
	|	упПравилаПрохожденияТочекВМобильномКлиенте.Перевозчик В (&Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|	И упПравилаПрохожденияТочекВМобильномКлиенте.Водитель В (&Водитель, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	|	И упПравилаПрохожденияТочекВМобильномКлиенте.ВидАдреса В (&ВидАдреса, ЗНАЧЕНИЕ(Справочник.упВидыАдресов.ПустаяСсылка))
	|	И упПравилаПрохожденияТочекВМобильномКлиенте.Адрес В (&Адрес, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Адрес УБЫВ,
	|	ВидАдреса УБЫВ,
	|	Водитель УБЫВ,
	|	Перевозчик УБЫВ";
	Запрос.УстановитьПараметр("Адрес",       Адрес);
	Запрос.УстановитьПараметр("Перевозчик",  Перевозчик);
	Запрос.УстановитьПараметр("Водитель",    Водитель);
	Запрос.УстановитьПараметр("ВидАдреса",   ВидАдреса);
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("ОтклонениеДолгота"                                      , 0);
	сткВозврат.Вставить("ОтклонениеШирота"                                       , 0);
	сткВозврат.Вставить("ЗапрещеноОтчитыватьсяОРаботах"                          , Ложь);
	сткВозврат.Вставить("СпособПрисоединенияВнешнихФайлов"                       , Перечисления.упСпособыПрисоединенияВнешнихФайлов.КПрохождениюТочки);
	сткВозврат.Вставить("ГруппироватьЗадачиПередачиЦенностей"                    , Ложь);
	сткВозврат.Вставить("ЗапрещеноОтменятьТочкиМаршрута"                         , Ложь);
	сткВозврат.Вставить("РазрешеноГрупповоеВыполнениеРабот"                      , Ложь);
	сткВозврат.Вставить("ГруппироватьЗадачиСГрузомВТаре"                         , Ложь);
	сткВозврат.Вставить("СпособГруппировкиРабот"                                 , Перечисления.упСпособыГруппировкиРаботНаМобильномКлиенте.Все);
	сткВозврат.Вставить("ЗакрыватьКассовуюСменуПослеУбытияИзТочки"               , Ложь);
	сткВозврат.Вставить("ЗапрещеноОтменятьРаботыВТочкеВозвратаНаСклад"           , Ложь);
	сткВозврат.Вставить("РазрешеноДосрочноеУбытиеИзТочкиМаршрута"                , Ложь);
	сткВозврат.Вставить("ВидДоступаДействиеДосрочноеУбытиеИзТочкиМаршрута"       , Перечисления.упВидыДоступаДействийМобильныйКлиент.Всегда);
	сткВозврат.Вставить("ЗапрашиватьРазрешениеНаНазначениеТочкиМаршрутаСледующей", Ложь);
	сткВозврат.Вставить("ГруппаТиповСобытийМониторинга"                          , Справочники.упГруппыТиповСобытийМониторинга.ПустаяСсылка());
	сткВозврат.Вставить("ВыводитьСтатусГотовностиТочки"                          , Ложь);
	сткВозврат.Вставить("ВариантФормВыводитьСтатусГотовностиТочки"               , Перечисления.упВариантыФормТочкиМобильныйКлиент.ФормаТочкиМаршрута);
	сткВозврат.Вставить("ВыводитьИнформациюОСкладскихВоротах"                    , Ложь);
	сткВозврат.Вставить("ВариантФормВыводитьИнформациюОСкладскихВоротах"         , Перечисления.упВариантыФормТочкиМобильныйКлиент.ФормаТочкиМаршрута);
	сткВозврат.Вставить("НастройкаОтветовДосрочноеУбытиеИзТочкиМаршрута"          , Справочники.упНастройкиВопросаИОтветовПоСобытиямМониторинга.ПустаяСсылка());
	сткВозврат.Вставить("НастройкаОтветовНазначениеТочкиМаршрутаСледующей"        , Справочники.упНастройкиВопросаИОтветовПоСобытиямМониторинга.ПустаяСсылка());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
		Если ЗначениеЗаполнено(Выборка.ДопустимоеОтклонение) Тогда
			сткВозврат.ОтклонениеДолгота = Выборка.ДопустимоеОтклонение * упМобильныйКлиентПовтИсп.ГрадусовПоДолготеВ1м(Окр(Адрес.Широта, 1));
			сткВозврат.ОтклонениеШирота  = Выборка.ДопустимоеОтклонение * упМобильныйКлиентПовтИсп.ГрадусовПоШиротеВ1м();
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Выборка.СпособПрисоединенияВнешнихФайлов) Тогда
			сткВозврат.СпособПрисоединенияВнешнихФайлов = Перечисления.упСпособыПрисоединенияВнешнихФайлов.КПрохождениюТочки;
		КонецЕсли; 
		Если Не ЗначениеЗаполнено(Выборка.ВидДоступаДействиеДосрочноеУбытиеИзТочкиМаршрута) Тогда
			сткВозврат.ВидДоступаДействиеДосрочноеУбытиеИзТочкиМаршрута = Перечисления.упВидыДоступаДействийМобильныйКлиент.Всегда;
		КонецЕсли; 
		Если ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки") И ЗначениеЗаполнено(Выборка.СпособГруппировкиРабот) Тогда
			сткВозврат.СпособГруппировкиРабот              = Выборка.СпособГруппировкиРабот;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Выборка.ВариантФормВыводитьСтатусГотовностиТочки) Тогда
			сткВозврат.ВариантФормВыводитьСтатусГотовностиТочки = Перечисления.упВариантыФормТочкиМобильныйКлиент.ФормаТочкиМаршрута;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Выборка.ВариантФормВыводитьИнформациюОСкладскихВоротах) Тогда
			сткВозврат.ВариантФормВыводитьИнформациюОСкладскихВоротах = Перечисления.упВариантыФормТочкиМобильныйКлиент.ФормаТочкиМаршрута;
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции	

// Определяет правила исполнения рейса.
//
// Параметры:
//	Перевозчик - СправочникССылка - Ссылка на элемент.
//	Водитель - СправочникСсылка.упСотрудники - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПравилаВыполненияРейса(Перевозчик = Неопределено, Водитель = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПравилаВыполненияРейсаВМобильномКлиенте.Перевозчик КАК Перевозчик,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.Водитель КАК Водитель,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.РазрешеноИзменятьПорядокТочек КАК РазрешеноИзменятьПорядокТочек,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.РазрешеноОтменятьНовыеТочки КАК РазрешеноОтменятьНовыеТочки,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ТолькоОднаТочка КАК ТолькоОднаТочка,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ВремяОжиданияПодтвержденияНовыхТочек КАК ВремяОжиданияПодтвержденияНовыхТочек,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ПериодичностьОтправкиКоординат КАК ПериодичностьОтправкиКоординат,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.СпособОпределенияМестоположения КАК СпособОпределенияМестоположения,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ВидНавигатора КАК ВидНавигатора,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ПереводитьРейсВСтатусВыполняетсяПослеПрибытияВПервуюТочку КАК ПереводитьРейсВСтатусВыполняетсяПослеПрибытияВПервуюТочку,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.СпособВыводаИнформацииОПолучателе КАК СпособВыводаИнформацииОПолучателе,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ВедетсяРозничнаяПродажаВРейсе КАК ВедетсяРозничнаяПродажаВРейсе,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.РазрешенаНастройкаКассовогоОборудования КАК РазрешенаНастройкаКассовогоОборудования,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ЗакрыватьКассовуюСменуПослеВыполненияРейса КАК ЗакрыватьКассовуюСменуПослеВыполненияРейса,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ВидКассовойТехники КАК ВидКассовойТехники,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.КассаККМПоУмолчанию КАК КассаККМПоУмолчанию,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ЗапрещеноИзменениеПорядкаПогрузкиНаСкладах КАК ЗапрещеноИзменениеПорядкаПогрузкиНаСкладах,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ВидАдресаСклада КАК ВидАдресаСклада,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.РазрешеноОтложенноеВыполнениеЧастиРабот КАК РазрешеноОтложенноеВыполнениеЧастиРабот,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ВидДоступаДействиеОтложенноеВыполнениеЧастиРабот КАК ВидДоступаДействиеОтложенноеВыполнениеЧастиРабот,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ГруппаТиповСобытийМониторинга КАК ГруппаТиповСобытийМониторинга,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.НастройкаОтветовОтложенноеВыполнениеЧастиРабот КАК НастройкаОтветовОтложенноеВыполнениеЧастиРабот,
	|	ВЫБОР
	|		КОГДА упПравилаВыполненияРейсаВМобильномКлиенте.СтавкаНДСДляАвансов = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18)
	|		ИНАЧЕ упПравилаВыполненияРейсаВМобильномКлиенте.СтавкаНДСДляАвансов
	|	КОНЕЦ КАК СтавкаНДСДляАвансов,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.НоменклатураДляАвансов.Наименование КАК НоменклатураДляАвансовСтрока,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.НоменклатураДляАвансов КАК НоменклатураДляАвансов,
	|	упПравилаВыполненияРейсаВМобильномКлиенте.ИспользоватьАвтозаполнениеРасчетныхПолейПередПробитиемЧека КАК ИспользоватьАвтозаполнениеРасчетныхПолейПередПробитиемЧека
	|ИЗ
	|	РегистрСведений.упПравилаВыполненияРейсаВМобильномКлиенте КАК упПравилаВыполненияРейсаВМобильномКлиенте
	|ГДЕ
	|	упПравилаВыполненияРейсаВМобильномКлиенте.Перевозчик В (&Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|	И упПравилаВыполненияРейсаВМобильномКлиенте.Водитель В (&Водитель, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Водитель УБЫВ,
	|	Перевозчик УБЫВ";
	Запрос.УстановитьПараметр("Перевозчик",  Перевозчик);
	Запрос.УстановитьПараметр("Водитель",    Водитель);
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("РазрешеноИзменятьПорядокТочек"                            , Истина);
	сткВозврат.Вставить("РазрешеноОтменятьНовыеТочки"                              , Ложь);
	сткВозврат.Вставить("ТолькоОднаТочка"                                          , Ложь);
	сткВозврат.Вставить("ВремяОжиданияПодтвержденияНовыхТочек"                     , 300);
	сткВозврат.Вставить("ПериодичностьОтправкиКоординат"                           , 60);
	сткВозврат.Вставить("СпособОпределенияМестоположения"                          , Перечисления.упСпособыОпределенияМестоположения.ПоСпутникам);
	сткВозврат.Вставить("ВидНавигатора"                                            , Перечисления.упВидыНавигаторов.ВыбиратьОпционально);
	сткВозврат.Вставить("ПереводитьРейсВСтатусВыполняетсяПослеПрибытияВПервуюТочку", Ложь);
	сткВозврат.Вставить("СпособВыводаИнформацииОПолучателе"                        , Перечисления.упСпособыВыводаИнформацииОПолучателе.Получатель);
	сткВозврат.Вставить("ВедетсяРозничнаяПродажаВРейсе"                            , Ложь);
	сткВозврат.Вставить("РазрешенаНастройкаКассовогоОборудования"                  , Ложь);
	сткВозврат.Вставить("ЗакрыватьКассовуюСменуПослеВыполненияРейса"               , Ложь);
	сткВозврат.Вставить("ВидКассовойТехники"                                       , Перечисления.упВариантыВыбораВидовКассовойТехники.ПоВыбору);
	сткВозврат.Вставить("КассаККМПоУмолчанию"                                      , Справочники.упКассыККМ.ПустаяСсылка());
	сткВозврат.Вставить("ЗапрещеноИзменениеПорядкаПогрузкиНаСкладах"               , Ложь);
	сткВозврат.Вставить("ВидАдресаСклада"                                          , Справочники.упВидыАдресов.ПустаяСсылка());
	сткВозврат.Вставить("РазрешеноОтложенноеВыполнениеЧастиРабот"                  , Ложь);
	сткВозврат.Вставить("ВидДоступаДействиеОтложенноеВыполнениеЧастиРабот"         , Перечисления.упВидыДоступаДействийМобильныйКлиент.Всегда);
	сткВозврат.Вставить("ГруппаТиповСобытийМониторинга"                            , Справочники.упГруппыТиповСобытийМониторинга.ПустаяСсылка());
	сткВозврат.Вставить("НастройкаОтветовОтложенноеВыполнениеЧастиРабот"           , Справочники.упНастройкиВопросаИОтветовПоСобытиямМониторинга.ПустаяСсылка());
	сткВозврат.Вставить("НоменклатураДляАвансовСтрока"                             , НСтр("ru = 'Аванс'"));
	сткВозврат.Вставить("НоменклатураДляАвансов"                                   , Справочники.упНоменклатура.ПустаяСсылка());
	сткВозврат.Вставить("СтавкаНДСДляАвансов"                                      , Перечисления.упСтавкиНДС.НДС18);	
	сткВозврат.Вставить("ИспользоватьАвтозаполнениеРасчетныхПолейПередПробитиемЧека", Ложь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
		
		Если Не ЗначениеЗаполнено(сткВозврат.СпособОпределенияМестоположения) Тогда
			сткВозврат.СпособОпределенияМестоположения = Перечисления.упСпособыОпределенияМестоположения.ПоСпутникам;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткВозврат.ВидНавигатора) Тогда
			сткВозврат.ВидНавигатора = Перечисления.упВидыНавигаторов.ВыбиратьОпционально;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткВозврат.СпособВыводаИнформацииОПолучателе) Тогда
			сткВозврат.СпособВыводаИнформацииОПолучателе = Перечисления.упСпособыВыводаИнформацииОПолучателе.Получатель;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткВозврат.ВидКассовойТехники) Тогда
			сткВозврат.ВидКассовойТехники = Перечисления.упВариантыВыбораВидовКассовойТехники.ПоВыбору;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткВозврат.КассаККМПоУмолчанию) Тогда
			сткВозврат.КассаККМПоУмолчанию = Справочники.упКассыККМ.ПустаяСсылка();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткВозврат.ВидДоступаДействиеОтложенноеВыполнениеЧастиРабот) Тогда
			сткВозврат.ВидДоступаДействиеОтложенноеВыполнениеЧастиРабот = Перечисления.упВидыДоступаДействийМобильныйКлиент.Всегда;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(сткВозврат.НоменклатураДляАвансовСтрока) Тогда
			сткВозврат.НоменклатураДляАвансовСтрока = НСтр("ru = 'Аванс'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции	

// Определяет количество градусов в 1 метре по широте.
//
// Возвращаемое значение:
//   Число - градусы в метре.
//
Функция ГрадусовПоШиротеВ1м() Экспорт
	
	Возврат 0.0000089932;
	
КонецФункции	

// Определяет количество градусов в 1 метре по долготе.
//
// Параметры:
//	Широта - Число - Значение широты.
//
// Возвращаемое значение:
//   Число - градусы в метре.
//
Функция ГрадусовПоДолготеВ1м(Широта) Экспорт
	
	Возврат 0.0000089932 / Cos(Широта * 0.0174533);
	
КонецФункции	

// Определяет параметры задания на перевозку в цепи поставок
//
// Параметры:
//  Задание		 - ДокументСсылка.упЗаданиеНаПеревозкуГруза	 - задание.
//  ГрузовоеМесто	 - СправочникСсылка.упГрузовыеМеста 	 - грузовое место.
// 
// Возвращаемое значение:
//  Структура - параметры задания.
//
Функция ПараметрыЗаданияВЦепиПоставок(Задание, ГрузовоеМесто) Экспорт 
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("НомерВЦепиПоставок", 0);
	сткВозврат.Вставить("КрайнийНомер"      , 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК Заявка,
	|	упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок
	|ПОМЕСТИТЬ втЗаявка
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка = &Задание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявка.НомерВЦепиПоставок,
	|	МАКСИМУМ(упЗаявкиКВыполнению.НомерВЦепиПоставок) КАК КрайнийНомер
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению КАК упЗаявкиКВыполнению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявка КАК втЗаявка
	|		ПО упЗаявкиКВыполнению.Заявка = втЗаявка.Заявка
	|			И (&ГрузовоеМесто В (упЗаявкиКВыполнению.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)))
	|			И (упЗаявкиКВыполнению.ВидДвижения = ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход))
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗаявка.НомерВЦепиПоставок";
	Запрос.УстановитьПараметр("Задание", Задание);
	Запрос.УстановитьПараметр("ГрузовоеМесто", ГрузовоеМесто);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
	КонецЕсли;
	
	Возврат сткВозврат; 
	
КонецФункции 

#КонецОбласти