
#Область ПрограммныйИнтерфейс

// Процедура вызова регламентного задания.
//
Процедура ИнтеграцияССерверамиСбораДанных() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСервераСбораДанных.Ссылка,
	|	упСервераСбораДанных.Тип,
	|	упСервераСбораДанных.Сервер,
	|	упСервераСбораДанных.Порт,
	|	упСервераСбораДанных.ЗащищенноеСоединение,
	|	упСервераСбораДанных.Пользователь,
	|	упСервераСбораДанных.Пароль,
	|	упСервераСбораДанных.Токен КАК Токен,
	|	упСервераСбораДанных.СобытияМониторинга,
	|	упСервераСбораДанных.ПериодОтсрочкиПолученияДанныхПоСобытиям КАК Отсрочка,
	|	упСервераСбораДанных.Схема
	|ИЗ
	|	Справочник.упСервераСбораДанных КАК упСервераСбораДанных
	|ГДЕ
	|	упСервераСбораДанных.Активен
	|	И НЕ упСервераСбораДанных.Тип = ЗНАЧЕНИЕ(Перечисление.упТипСервераСбораДанных.AndroidКлиент)
	|	И НЕ упСервераСбораДанных.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Если Выборка.Тип = Перечисления.упТипСервераСбораДанных.ЦСМ Тогда 
			ПолучениеДанныхОт1С_ЦСМ(Выборка);
		КонецЕсли;
		Если Выборка.Тип = Перечисления.упТипСервераСбораДанных.AutoGRAPH Тогда 
			ПолучениеДанныхОтAutoGRAPH(Выборка);
		КонецЕсли;	
		Если Выборка.Тип = Перечисления.упТипСервераСбораДанных.СКАУТ Тогда 
			ПолучениеДанныхОтСКАУТ(Выборка);
		КонецЕсли;	
		Если Выборка.Тип = Перечисления.упТипСервераСбораДанных.Omnicomm Тогда 
			ПолучениеДанныхОтOmnicomm(Выборка);
		КонецЕсли;
		Если Выборка.Тип = Перечисления.упТипСервераСбораДанных.WialonPro Тогда 
			ПолучениеДанныхОтWialonPro(Выборка);
		КонецЕсли;
		Если Выборка.Тип = Перечисления.упТипСервераСбораДанных.WialonHosting Тогда 
			ПолучениеДанныхОтWialonHosting(Выборка);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры // ИнтеграцияССерверамиСбораДанных()

// Процедура вызова сервера 1С: ЦСМ.
//
// Параметры:
//  Сервер - СправочникСсылка.упСервераСбораДанных - Ссылка на элемент.
//
Процедура ПолучениеДанныхОт1С_ЦСМ(Сервер) Экспорт
	
	Если Сервер.Сервер = "" ИЛИ Сервер.Порт = 0 ИЛИ Сервер.Пользователь = "" Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не настроены параметры подключения к серверу сбора данных.'"));
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "ВЫБРАТЬ
	|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	упМобильныеУстройстваИТрекеры.Ссылка КАК Трекер
	|ПОМЕСТИТЬ вт_Трекеры
	|ИЗ
	|	Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упУстановленныеТрекеры.СрезПоследних КАК УстановленныеТрекерыСП
	|		ПО упМобильныеУстройстваИТрекеры.Ссылка = УстановленныеТрекерыСП.Трекер
	|ГДЕ
	|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
	|	И НЕ упМобильныеУстройстваИТрекеры.ИдентификаторУстройства = """"
	|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
	|	И УстановленныеТрекерыСП.Установлен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Трекеры.Трекер,
	|	вт_Трекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	МАКСИМУМ(ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК Период
	|ПОМЕСТИТЬ вт_Периоды
	|ИЗ
	|	вт_Трекеры КАК вт_Трекеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеТрекеров.СрезПоследних(
	|				,
	|				Трекер В
	|					(ВЫБРАТЬ
	|						вт_Трекеры.Трекер
	|					ИЗ
	|						вт_Трекеры КАК вт_Трекеры)) КАК ДанныеТрекеровСП
	|		ПО вт_Трекеры.Трекер = ДанныеТрекеровСП.Трекер
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Трекеры.ИдентификаторУстройства,
	|	вт_Трекеры.Трекер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Периоды.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	ЕСТЬNULL(ДанныеТрекеров.Счетчик, 0) КАК СчетчикАктуальности
	|ИЗ
	|	вт_Периоды КАК вт_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеТрекеров КАК ДанныеТрекеров
	|		ПО вт_Периоды.Период = ДанныеТрекеров.Период
	|			И вт_Периоды.Трекер = ДанныеТрекеров.Трекер";
	Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
	
	СтрокаЗапроса = "";
	
	Unixtime = Формат(НачалоДня(ТекущаяДата())-ДАТА("19700101000000"),"ЧН=0; ЧГ=0");
	МассивЗапросов = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивЗапросов.Добавить(Выборка.ИдентификаторУстройства);
		СчетчикАктуальности = "0";
		Если ЗначениеЗаполнено(Выборка.СчетчикАктуальности) Тогда
			СчетчикАктуальности = Формат(Выборка.СчетчикАктуальности, "ЧН=0; ЧГ=0");
		Иначе
			СчетчикАктуальности = ПолучитьЗначениеСчетчикаITOB(Выборка.ИдентификаторУстройства,Unixtime,Сервер);
		КонецЕсли;	
		МассивЗапросов.Добавить(СчетчикАктуальности);
	КонецЦикла;
	СтрокаЗапроса = СтрСоединить(МассивЗапросов,",");
		
	Адрес = "http://" + Сервер.Сервер + ":" + Формат(Сервер.Порт, "ЧН=0; ЧГ=0") + "/GetTerminalData?login=" + Сервер.Пользователь + "&pwd=" + Сервер.Пароль + "&QueryMethod=1&Output=xml&Pack=2&Query=" + СтрокаЗапроса;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".zip");
	Каталог  = Лев(ИмяФайла, СтрДлина(ИмяФайла)-4);
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("ПутьДляСохранения", ИмяФайла);
	
	Результат = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(Адрес, ПараметрыПолучения);
	Если НЕ Результат.Статус Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, Результат.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	ЧтениеZIP = Новый ЧтениеZipФайла();
	
	Попытка
		
		ЧтениеZIP.Открыть(ИмяФайла);
		ЧтениеZIP.ИзвлечьВсе(Каталог);
		ЧтениеZIP.Закрыть();
		
		НайденныеФайлы = НайтиФайлы(Каталог,"*.*");
		Для каждого НайденныйФайл Из НайденныеФайлы Цикл
			ОбработатьДанныеЦСМ(НайденныйФайл.ПолноеИмя, Сервер.Ссылка);					
			УдалитьФайлы(НайденныйФайл.ПолноеИмя);								
		КонецЦикла;
		
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;	
	
	УдалитьФайлы(ИмяФайла);
	УдалитьФайлы(Каталог);
	
КонецПроцедуры // ПолучениеДанныхОт1С_ЦСМ()

// Выполним обращение к сервису для получения значения счетчика.
//
// Параметры:
//  ИдентификаторУстройства  - Строка - Данные терминала.
//  Unixtime  - Строка - Моментом начала отсчёта.
//  СправочникСсылка.упСервераСбораДанных - Ссылка на элемент.
//
// Возвращаемое значение:
//   Строка - Крайнее значение счетчика.
//
Функция ПолучитьЗначениеСчетчикаITOB(ИдентификаторУстройства,Unixtime,Сервер)
	
	Счетчик = "0";
	
	СтрокаЗапроса = ИдентификаторУстройства + "," + Unixtime;	
	
	Адрес = "http://" + Сервер.Сервер + ":" + Формат(Сервер.Порт, "ЧН=0; ЧГ=0") + "/GetTerminalData?login=" + Сервер.Пользователь + "&pwd=" + Сервер.Пароль + "&QueryMethod=0&Output=xml&Pack=0&Query=" + СтрокаЗапроса;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".xml");
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("ПутьДляСохранения", ИмяФайла);
	
	Результат = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(Адрес, ПараметрыПолучения);
	
	ЧтениеXML = Новый ЧтениеXML;
	
	Попытка		
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.Имя="p" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Попытка
					Счетчик = ЧтениеXML.ПолучитьАтрибут("counter");	
					Прервать;
				Исключение
					ЗаписьЖурналаРегистрации(НСтр("ru = 'РазборСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					Продолжить;					
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
		ЧтениеXML.Закрыть();
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'РазборСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	УдалитьФайлы(ИмяФайла);
	
	Возврат Счетчик;

КонецФункции // ПолучитьЗначениеСчетчикаITOB()

// Разбор XML файла данных от трекеров 1С: ЦСМ.
//
// Параметры:
//  ИмяФайла - Строка - Полный путь к файлу.
//  Сервер - СправочникСсылка.упСервераСбораДанных - Ссылка на элемент.
//
Процедура ОбработатьДанныеЦСМ(ИмяФайла, Сервер)
	
	МассивОбъектовБлокировки = Новый Массив;
	
	ЧтениеXML = Новый ЧтениеXML;
	
	соотТрекеров = Новый Соответствие;
	
	ТаблицаЗначенийДатчиков = Новый ТаблицаЗначений;
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Трекер",Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"),"Трекер");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("КалибровочныйГрафик",Новый ОписаниеТипов("СправочникСсылка.упКалибровочныеГрафики"),"КалибровочныйГрафик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Датчик",Новый ОписаниеТипов("СправочникСсылка.упДатчики"),"Датчик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Рейс",Новый ОписаниеТипов("ДокументСсылка.упРейс"),"Рейс");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Значение",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Значение");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Назначение",Новый ОписаниеТипов("СправочникСсылка.упНазначенияДатчиков"),"Назначение");
	
	// Вычисление направления -->>.
	НачалоШирота  = Неопределено;
	НачалоДолгота = Неопределено;
	ПараметрыПроверкиНаправления = Новый Структура();
	// Вычисление направления <<--.
	НачатьТранзакцию();
	Попытка		
		
		ЧтениеXML.ОткрытьФайл(ИмяФайла);	
		
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.Имя="p" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				
				Попытка
					КодТерминала   = ЧтениеXML.ПолучитьАтрибут("code");
					Период         = МестноеВремя(Дата(ЧтениеXML.ПолучитьАтрибут("period")));
					Широта         = Число(ЧтениеXML.ПолучитьАтрибут("lat"));
					Долгота        = Число(ЧтениеXML.ПолучитьАтрибут("lon"));
					Высота         = Число(ЧтениеXML.ПолучитьАтрибут("altitude"));
					Скорость       = Число(ЧтениеXML.ПолучитьАтрибут("speed"));
					Счетчик        = Число(ЧтениеXML.ПолучитьАтрибут("counter"));	
				Исключение
					ЗаписьЖурналаРегистрации(НСтр("ru = 'РазборСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					Продолжить;					
				КонецПопытки;				
				
				ТекущийТерминал = упОбработкаДанныхТерминаловПовтИсп.НайтиТерминал(КодТерминала);
				Если Не ЗначениеЗаполнено(ТекущийТерминал) Тогда
					Продолжить;
				КонецЕсли;
				
				текДанныеТС = соотТрекеров[ТекущийТерминал];
				Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
					текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
					соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
				КонецЕсли;					
				
				Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда 
					Продолжить;
				КонецЕсли;
				
				Если МассивОбъектовБлокировки.Найти(текДанныеТС.ТранспортноеСредство) = Неопределено Тогда 
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеТрекеров");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", текДанныеТС.ТранспортноеСредство);
					БлокировкаДанных.Заблокировать();
					МассивОбъектовБлокировки.Добавить(текДанныеТС.ТранспортноеСредство);
				КонецЕсли;
				
				НовЗапись = РегистрыСведений.упДанныеТрекеров.СоздатьМенеджерЗаписи();
				НовЗапись.Период             	  = Период; 
				НовЗапись.ТранспортноеСредство 	  = текДанныеТС.ТранспортноеСредство; 
				НовЗапись.Трекер 				  = ТекущийТерминал;
				НовЗапись.Рейс                    = текДанныеТС.Рейс;
				НовЗапись.Широта                  = Широта;
				НовЗапись.Долгота                 = Долгота;
				НовЗапись.ПредставлениеКоординаты = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеКоординаты(НовЗапись.Широта, НовЗапись.Долгота);
				НовЗапись.Высота                  = Высота;
				НовЗапись.Скорость                = Скорость;
				НовЗапись.Счетчик                 = Счетчик;
				
				// Вычисление направления -->>.				
				ПараметрыПроверкиНаправления.Вставить("ТранспортноеСредство", НовЗапись.ТранспортноеСредство);
				ПараметрыПроверкиНаправления.Вставить("Широта1",  НачалоШирота);
				ПараметрыПроверкиНаправления.Вставить("Долгота1", НачалоДолгота);
				ПараметрыПроверкиНаправления.Вставить("Широта2",  НовЗапись.Широта);
				ПараметрыПроверкиНаправления.Вставить("Долгота2", НовЗапись.Долгота);				
				ПараметрыПроверкиНаправления.Вставить("Период",   НовЗапись.Период);
				ПараметрыПроверкиНаправления.Вставить("Трекер",   НовЗапись.Трекер);
				ПараметрыПроверкиНаправления.Вставить("Рейс",     НовЗапись.Рейс);
				// От ЦСМ не поступает НаправлениеДвижения выполним расчет и заполним значение.
				НовЗапись.НаправлениеДвижения = упИнтеграцияССерверамиСбораДанных.ПолучитьНаправлениеДвиженияДляКоординат(ПараметрыПроверкиНаправления,0);				
				// Вычисление направления <<--.
				
				НовЗапись.Записать();
				
				НачалоШирота  = НовЗапись.Широта;
				НачалоДолгота = НовЗапись.Долгота;
				
				РезультатЧтения = Истина;
				Пока РезультатЧтения И НЕ (ЧтениеXML.Имя = "p" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента) Цикл
					Если ЧтениеXML.Имя="sensor" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Попытка						
							Код    = Число(ЧтениеXML.ПолучитьАтрибут("code"));
							Данные = Число(ЧтениеXML.ПолучитьАтрибут("data"));							
						Исключение
							ЗаписьЖурналаРегистрации(НСтр("ru = 'РазборСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
							Продолжить;
						КонецПопытки;						
						
						Если ЗначениеЗаполнено(Код) Тогда
							Датчик = упОбработкаДанныхТерминаловПовтИсп.НайтиДатчик(Код, ТекущийТерминал.Модель);
							
							НовЗаписьДатчики = ТаблицаЗначенийДатчиков.Добавить();				
							НовЗаписьДатчики.Период   				= НовЗапись.Период;
							НовЗаписьДатчики.Трекер 				= НовЗапись.Трекер;
							НовЗаписьДатчики.КалибровочныйГрафик    = Датчик.КалибровочныйГрафик;
							НовЗаписьДатчики.Датчик   				= Датчик.Датчик;				
							НовЗаписьДатчики.ТранспортноеСредство   = НовЗапись.ТранспортноеСредство;				
							НовЗаписьДатчики.Рейс                   = НовЗапись.Рейс;				
							НовЗаписьДатчики.Значение 				= Данные;
							НовЗаписьДатчики.Назначение				= Датчик.Назначение;
						КонецЕсли;
					КонецЕсли;	
					
					РезультатЧтения = ЧтениеXML.Прочитать();	
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;	
		
		ЧтениеXML.Закрыть();
		
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации(НСтр("ru = 'РазборСпутниковыхДанных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если ТаблицаЗначенийДатчиков.Количество() Тогда
		упИнтеграцияССерверамиСбораДанных.ВыполнитьОбработкуЗначенийДатчиков(ТаблицаЗначенийДатчиков); 		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьДанныеЦСМ()

// Для полученной таблицы выполним обработку значений и запишем в базу результат.
//
// Параметры:
//  ТаблицаДанных  - ТаблицаЗначений - Таблица с данными для обработки.
//
Процедура ВыполнитьОбработкуЗначенийДатчиков(ТаблицаДанных) Экспорт
	
	Выборка = упСЛК.СоздатьОбъект().ВыполнитьОбработкуЗначенийДатчиков(ТаблицаДанных);
	Пока Выборка.Следующий() Цикл 
		Если Выборка.ТипЗначения = Перечисления.упТипыЗначений.Дискретный Тогда 
			ВыполнитьЗаписьДанныеДатчика(Выборка, 1);
		ИначеЕсли Выборка.ТипЗначения = Перечисления.упТипыЗначений.Непрерывный Тогда 
			ВыполнитьЗаписьДанныеДатчика(Выборка, 2);
		Иначе	
			ВыполнитьЗаписьДанныеДатчика(Выборка, 3);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры // ВыполнитьОбработкуЗначенийДатчиков()

// Для полученной таблицы выполним обработку значений и запишем в базу результат.
//
// Параметры:
//  ТаблицаДанных  - ТаблицаЗначений - Таблица с данными для обработки.
//
Процедура ВыполнитьОбработкуЗначенийДатчиковНабор(ТаблицаДанных) Экспорт
	
	Выборка = упСЛК.СоздатьОбъект().ВыполнитьОбработкуЗначенийДатчиков(ТаблицаДанных);
	Если Выборка.Количество() Тогда 
		Попытка
			ТекущееТранспортноеСредство=Неопределено;
			НаборЗаписей = РегистрыСведений.упДанныеДатчиков.СоздатьНаборЗаписей();
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			ЗначениеДатчика = ПредопределенноеЗначение("Справочник.упЗначенияДатчиков.ПустаяСсылка");
			Пока Выборка.Следующий() Цикл
				Если НЕ ТекущееТранспортноеСредство=Выборка.ТранспортноеСредство Тогда
					ТекущееТранспортноеСредство=Выборка.ТранспортноеСредство;
					Если НаборЗаписей.Количество() Тогда
						Попытка
							НаборЗаписей.Записать(Ложь);
						Исключение
							Если ТранзакцияАктивна() Тогда
								ОтменитьТранзакцию();
							КонецЕсли;
							ЗаписьЖурналаРегистрации("ЗаписьДанныхДатчика", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеДатчиков, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						КонецПопытки;
						Если ТранзакцияАктивна() Тогда
							ЗафиксироватьТранзакцию();
						КонецЕсли;
						НаборЗаписей.Очистить();
					КонецЕсли;
					Если НЕ ТранзакцияАктивна() Тогда
						НачатьТранзакцию();
						// ТС.
						БлокировкаДанных = Новый БлокировкаДанных;
						ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеДатчиков");
						ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
						ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТекущееТранспортноеСредство);
						БлокировкаДанных.Заблокировать();
					КонецЕсли;
					НаборЗаписей.Отбор.ТранспортноеСредство.Установить(ТекущееТранспортноеСредство, Истина);
				КонецЕсли;
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Период   			 = Выборка.Период;
				НоваяЗапись.Трекер 				 = Выборка.Трекер;
				НоваяЗапись.Датчик   			 = Выборка.Датчик;				
				НоваяЗапись.ТранспортноеСредство = Выборка.ТранспортноеСредство;	
				НоваяЗапись.Рейс                 = Выборка.Рейс;	
				НоваяЗапись.Вход				 = Выборка.Значение;
				НоваяЗапись.Значение 			 = Выборка.Значение;
				НоваяЗапись.РасшифровкаЗначения  = ЗначениеДатчика;
				Если Выборка.ТипЗначения = Перечисления.упТипыЗначений.Дискретный Тогда 
					НоваяЗапись.РасшифровкаЗначения = Выборка.Расшифровка;
				ИначеЕсли Выборка.ТипЗначения = Перечисления.упТипыЗначений.Непрерывный Тогда 
					НоваяЗапись.Значение = Выборка.Преобразование;
				КонецЕсли;
			КонецЦикла;
			Если НаборЗаписей.Количество() Тогда
				Попытка
					НаборЗаписей.Записать(Ложь);
				Исключение
					Если ТранзакцияАктивна() Тогда
						ОтменитьТранзакцию();
					КонецЕсли;
					ЗаписьЖурналаРегистрации("ЗаписьДанныхДатчика", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеДатчиков, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецПопытки;
			КонецЕсли;
			Если ТранзакцияАктивна() Тогда
				ЗафиксироватьТранзакцию();
			КонецЕсли;
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ЗаписьДанныхДатчика", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеДатчиков, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЕсли;
		
КонецПроцедуры // ВыполнитьОбработкуЗначенийДатчиковНабор()

// Записывает в базу данные от терминалов.
//
// Параметры:
//  Период - Дата - Период запроса.
//	ТекущийТерминал - СправочникСсылка.упМобильныеУстройстваИТрекеры - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ДанныеПоТекущемуТС(Период, ТекущийТерминал)
	
	Результат = Новый Структура("ТранспортноеСредство, Рейс, НачалоВыполнения, ОкончаниеВыполнения");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	УстановленныеТрекерыСП.Период КАК Период,
	|	УстановленныеТрекерыСП.Трекер КАК Трекер,
	|	УстановленныеТрекерыСП.ТранспортноеСредство КАК ТранспортноеСредство
	|ПОМЕСТИТЬ втУстановленныйТрекер
	|ИЗ
	|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(&Период, Трекер = &Трекер) КАК УстановленныеТрекерыСП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУстановленныйТрекер.Период КАК Период,
	|	втУстановленныйТрекер.Трекер КАК Трекер,
	|	втУстановленныйТрекер.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втУстановленныйТрекер.Период КАК НачалоПериода,
	|	МИНИМУМ(ЕСТЬNULL(упУстановленныеТрекеры.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК КонецПериода
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втУстановленныйТрекер КАК втУстановленныйТрекер
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУстановленныеТрекеры КАК упУстановленныеТрекеры
	|		ПО втУстановленныйТрекер.Период <= упУстановленныеТрекеры.Период
	|			И втУстановленныйТрекер.ТранспортноеСредство <> упУстановленныеТрекеры.ТранспортноеСредство
	|			И втУстановленныйТрекер.Трекер = упУстановленныеТрекеры.Трекер
	|ГДЕ
	|	ВЫБОР
	|			КОГДА упУстановленныеТрекеры.Период ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ упУстановленныеТрекеры.Период >= &Период
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	втУстановленныйТрекер.Период,
	|	втУстановленныйТрекер.Трекер,
	|	втУстановленныйТрекер.ТранспортноеСредство,
	|	втУстановленныйТрекер.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТС.Трекер КАК Трекер,
	|	втТС.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Рейс, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)) КАК Рейс,
	|	ЕСТЬNULL(упФактическиеПараметрыРейса.НачалоВыполнения, втТС.Период) КАК НачалоВыполнения,
	|	ЕСТЬNULL(упФактическиеПараметрыРейса.ОкончаниеВыполнения, втТС.КонецПериода) КАК ОкончаниеВыполнения
	|ПОМЕСТИТЬ вт_ДаныеТрекера
	|ИЗ
	|	втТС КАК втТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(&Период, ) КАК упПеревозчикПоРейсуСрезПоследних
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	|			ПО упПеревозчикПоРейсуСрезПоследних.Рейс = упФактическиеПараметрыРейса.Рейс
	|				И (ВЫБОР
	|					КОГДА упФактическиеПараметрыРейса.НачалоВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ упФактическиеПараметрыРейса.НачалоВыполнения <= &Период
	|				КОНЕЦ)
	|				И (ВЫБОР
	|					КОГДА упФактическиеПараметрыРейса.ОкончаниеВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ упФактическиеПараметрыРейса.ОкончаниеВыполнения >= &Период
	|				КОНЕЦ)
	|		ПО втТС.ТранспортноеСредство = упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП.Рейс КАК Рейс,
	|	ПеревозчикПоРейсуСП.ТранспортноеСредство КАК ТС,
	|	упСтатусыРейсов.Период КАК Начало,
	|	ЕСТЬNULL(ФПР_1.ОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК Окончание
	|ПОМЕСТИТЬ вт_РейсыТС
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов КАК упСтатусыРейсов
	|		ПО ПеревозчикПоРейсуСП.Рейс = упСтатусыРейсов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФПР_1
	|		ПО ПеревозчикПоРейсуСП.Рейс = ФПР_1.Рейс
	|ГДЕ
	|	упСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению)
	|	И ПеревозчикПоРейсуСП.ТранспортноеСредство В
	|			(ВЫБРАТЬ
	|				втТС.ТранспортноеСредство КАК ТранспортноеСредство
	|			ИЗ
	|				втТС КАК втТС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП.Рейс,
	|	ПеревозчикПоРейсуСП.ТранспортноеСредство,
	|	ЕСТЬNULL(ФПР_2.НачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)),
	|	СтатусыРейсов.Период
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов КАК СтатусыРейсов
	|		ПО ПеревозчикПоРейсуСП.Рейс = СтатусыРейсов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФПР_2
	|		ПО ПеревозчикПоРейсуСП.Рейс = ФПР_2.Рейс
	|ГДЕ
	|	СтатусыРейсов.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен))
	|	И ПеревозчикПоРейсуСП.ТранспортноеСредство В
	|			(ВЫБРАТЬ
	|				втТС.ТранспортноеСредство КАК ТранспортноеСредство
	|			ИЗ
	|				втТС КАК втТС)
	|;
	|    
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_РейсыТС.Рейс КАК Рейс,
	|	вт_РейсыТС.ТС КАК ТС,
	|	вт_РейсыТС.Начало КАК Начало,
	|	ЕСТЬNULL(упСтатусыРейсов.Период, вт_РейсыТС.Окончание) КАК Окончание
	|ПОМЕСТИТЬ вт_Рейс_Длительность
	|ИЗ
	|	вт_РейсыТС КАК вт_РейсыТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов КАК упСтатусыРейсов
	|		ПО вт_РейсыТС.Рейс = упСтатусыРейсов.Документ
	|ГДЕ
	|	упСтатусыРейсов.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполняется), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Выполнен), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.ВыполненЧастично), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Завершен))
	|	И ВЫБОР КОГДА НЕ вт_РейсыТС.Начало = ДАТАВРЕМЯ(1, 1, 1) И НЕ вт_РейсыТС.Окончание = ДАТАВРЕМЯ(1, 1, 1) ТОГДА (&Период МЕЖДУ вт_РейсыТС.Начало И вт_РейсыТС.Окончание И &ПериодК МЕЖДУ вт_РейсыТС.Начало И вт_РейсыТС.Окончание) ИНАЧЕ ИСТИНА КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Рейс_Длительность.Рейс КАК Рейс,
	|	вт_Рейс_Длительность.ТС КАК ТС,
	|	МАКСИМУМ(вт_Рейс_Длительность.Начало) КАК Начало,
	|	МАКСИМУМ(вт_Рейс_Длительность.Окончание) КАК Окончание
	|ПОМЕСТИТЬ вт_Рейс_Гр
	|ИЗ
	|	вт_Рейс_Длительность КАК вт_Рейс_Длительность
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Рейс_Длительность.Рейс,
	|	вт_Рейс_Длительность.ТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДаныеТрекера.Трекер КАК Трекер,
	|	вт_ДаныеТрекера.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ВЫБОР
	|		КОГДА вт_ДаныеТрекера.Рейс = ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА вт_Рейс_Гр.Рейс ЕСТЬ NULL
	|						ТОГДА ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)
	|					ИНАЧЕ вт_Рейс_Гр.Рейс
	|				КОНЕЦ
	|		ИНАЧЕ вт_ДаныеТрекера.Рейс
	|	КОНЕЦ КАК Рейс,
	|	&Период КАК НачалоВыполнения,
	|	ВЫБОР
	|		КОГДА вт_ДаныеТрекера.ОкончаниеВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ПериодК
	|		ИНАЧЕ ВЫБОР
	|				КОГДА вт_ДаныеТрекера.ОкончаниеВыполнения <= &ПериодК
	|					ТОГДА вт_ДаныеТрекера.ОкончаниеВыполнения
	|				ИНАЧЕ &ПериодК
	|			КОНЕЦ
	|	КОНЕЦ КАК ОкончаниеВыполнения,
	|	ЕСТЬNULL(вт_Рейс_Гр.Начало, ДАТАВРЕМЯ(1, 1, 1)) КАК Начало
	|ИЗ
	|	вт_ДаныеТрекера КАК вт_ДаныеТрекера
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Рейс_Гр КАК вт_Рейс_Гр
	|		ПО вт_ДаныеТрекера.ТранспортноеСредство = вт_Рейс_Гр.ТС
	|			И (ВЫБОР
	|				КОГДА вт_Рейс_Гр.Окончание = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА вт_Рейс_Гр.Начало <= &Период
	|				ИНАЧЕ &Период МЕЖДУ вт_Рейс_Гр.Начало И вт_Рейс_Гр.Окончание
	|			КОНЕЦ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начало УБЫВ";
	Запрос.УстановитьПараметр("ПериодК", КонецЧаса(Период));
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Трекер", ТекущийТерминал);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции // ДанныеПоТекущемуТС()

// Выполним запись строки данных в регистр упДанныеДатчиков.
//
// Параметры:
//  ТекущаяСтрока  - Строка таблицы значений Строка выборки - Строка заполнений.
//  ВариантЗаписи  - Число - вариант записи ресурсов в регистре.
//				по умолчанию - входящее значение и расчетное равны.
//				ВариантЗаписи = 1 - входящее дискретное.
//				ВариантЗаписи = 2 - входящее непрерывное.
//				ВариантЗаписи = 3 - входящее неизвестно.
//
Процедура ВыполнитьЗаписьДанныеДатчика(ТекущаяСтрока, ВариантЗаписи = 0)
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) ИЛИ НЕ ЗначениеЗаполнено(ТекущаяСтрока.Датчик) Тогда
		// Откажемся от записи если нет значения измерения.
		Возврат;
	КонецЕсли;
	
	Попытка
		
		НачатьТранзакцию();
		
		БлокировкаДанных = Новый БлокировкаДанных;
		// ТС.
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеДатчиков");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТекущаяСтрока.ТранспортноеСредство);
		// Датчик.
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеДатчиков");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Датчик", ТекущаяСтрока.Датчик);
		БлокировкаДанных.Заблокировать();
		
		НоваяЗапись = РегистрыСведений.упДанныеДатчиков.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период   			 = ТекущаяСтрока.Период;
		НоваяЗапись.Трекер 				 = ТекущаяСтрока.Трекер;
		НоваяЗапись.Датчик   			 = ТекущаяСтрока.Датчик;				
		НоваяЗапись.ТранспортноеСредство = ТекущаяСтрока.ТранспортноеСредство;	
		НоваяЗапись.Рейс                 = ТекущаяСтрока.Рейс;	
		НоваяЗапись.Вход				 = ТекущаяСтрока.Значение;
		НоваяЗапись.Значение 			 = ТекущаяСтрока.Значение;
		НоваяЗапись.РасшифровкаЗначения  = ПредопределенноеЗначение("Справочник.упЗначенияДатчиков.ПустаяСсылка");		
		
		Если ВариантЗаписи = 1 Тогда
			НоваяЗапись.РасшифровкаЗначения = ТекущаяСтрока.Расшифровка;
		ИначеЕсли ВариантЗаписи = 2 Тогда
			НоваяЗапись.Значение = ТекущаяСтрока.Преобразование;
		КонецЕсли;
		
		НоваяЗапись.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ЗаписьДанныхДатчика", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеДатчиков, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;	
	
КонецПроцедуры // ВыполнитьЗаписьДанныеДатчика()

#КонецОбласти

#Область ОпределениеНаправленияДвижения

// Выполним вычисление направления движения.
//
// Параметры:
//  ПараметрыОбращения  - Структура - Координаты для вычисления направления.
//  Направление  - Число - Направление поступившее от сервиса.
//
// Возвращаемое значение:
//   Число - Направление вычисленное.
//
Функция ПолучитьНаправлениеДвиженияДляКоординат(ПараметрыОбращения,Направление=0) Экспорт
	
	ОпределятьНаправление = Ложь;
	
	Если ПараметрыОбращения.Свойство("ОпределятьНаправление") Тогда
		ОпределятьНаправление = ПараметрыОбращения.ОпределятьНаправление;
	Иначе
		ОпределятьНаправление = упСервисныеФункции.ПолучитьЗначениеКонстанты("упОпределятьАвтоматическиНаправлениеДвиженияТранспортногоСредства");
		ПараметрыОбращения.Вставить("ОпределятьНаправление",ОпределятьНаправление)
	КонецЕсли;
	
	Результат = Направление;
	
	Если ОпределятьНаправление И НЕ ЗначениеЗаполнено(Направление)Тогда
		
		Широта1  = ПараметрыОбращения.Широта1;
		Долгота1 = ПараметрыОбращения.Долгота1;
		Широта2  = ПараметрыОбращения.Широта2;
		Долгота2 = ПараметрыОбращения.Долгота2;
		
		Если Широта1 = Неопределено ИЛИ Долгота1 = Неопределено Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	упДанныеТрекеров.Период КАК Период,
			|	упДанныеТрекеров.Широта,
			|	упДанныеТрекеров.Долгота
			|ИЗ
			|	РегистрСведений.упДанныеТрекеров КАК упДанныеТрекеров
			|ГДЕ
			|	упДанныеТрекеров.Период <= &Период
			|	И упДанныеТрекеров.ТранспортноеСредство = &ТС
			|	И упДанныеТрекеров.Трекер = &Трекер
			|	И упДанныеТрекеров.Рейс = &Рейс
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период УБЫВ";
			
			Запрос.УстановитьПараметр("Период", ПараметрыОбращения.Период);
			Запрос.УстановитьПараметр("Рейс",   ПараметрыОбращения.Рейс);
			Запрос.УстановитьПараметр("Трекер", ПараметрыОбращения.Трекер);
			Запрос.УстановитьПараметр("ТС",     ПараметрыОбращения.ТранспортноеСредство);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				Широта1 = Выборка.Широта;
				Долгота1 = Выборка.Долгота;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ(Широта1 = Неопределено ИЛИ Долгота1 = Неопределено ИЛИ Широта2 = Неопределено ИЛИ Долгота2 = Неопределено) Тогда
			// Чтобы отсечь значения незаполнеными данными.
			Результат = упРаботаСКартамиКлиентСервер.ВычислитьНаправлениеДвижения(Широта1,Долгота1,Широта2,Долгота2);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьНаправлениеДвиженияДляКоординат()

// Выполним проверку и заполним направление движения.
//
// Параметры:
//  ТаблицаТреков  - ТаблицаЗначений - Содержит список отсортированных по периоду точек координат.
//
Процедура ПроверитьЗаполнениеДанныхНаправленияДвижения(ТаблицаТреков) Экспорт
	
	ОпределятьНаправление = упСервисныеФункции.ПолучитьЗначениеКонстанты("упОпределятьАвтоматическиНаправлениеДвиженияТранспортногоСредства");
	
	Если НЕ ОпределятьНаправление Тогда
		Возврат;
	КонецЕсли;	
	
	Если ТаблицаТреков.Колонки.Найти("НаправлениеДвижения")=Неопределено Тогда
		ТаблицаТреков.Колонки.Добавить("НаправлениеДвижения",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "НаправлениеДвижения");		
	КонецЕсли;

	ПредыдущаяСтрока = Неопределено;
	
	Для каждого тбзСтрока Из ТаблицаТреков Цикл
		
		Если НЕ ПредыдущаяСтрока = Неопределено И НЕ ЗначениеЗаполнено(ПредыдущаяСтрока.НаправлениеДвижения) Тогда			
			тбзСтрока.НаправлениеДвижения = упРаботаСКартамиКлиентСервер.ВычислитьНаправлениеДвижения(ПредыдущаяСтрока.Широта,ПредыдущаяСтрока.Долгота,тбзСтрока.Широта,тбзСтрока.Долгота);
		КонецЕсли;
		
		ПредыдущаяСтрока = тбзСтрока;
		
	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеДанныхНаправленияДвижения()

#КонецОбласти

#Область ОбработкаAutoGRAPH

// Выполним обращение к сервису АвтоГРАФ получим таблицу всех устройств.
//
// Параметры:
//  АдресСервиса  - Строка - Адрес сервиса.
//  Порт  - Число - Номер порта для подключения к сервису.
//  Схема  - Строка - Наименование схемы.
//  ИмяФайла  - Строка - путь к временному файлу.
//  Токен  - Строка - токен подключения.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица устройств.
//
Функция ПолучитьСписокУстройствAutoGRAPH(АдресСервиса,Порт,Схема,ИмяФайла,Токен)
	
	Результат = Неопределено;
	Если НЕ ВыполнитьКомандуNetAutoGRAPH("EnumDevices/" + Схема, ИмяФайла, АдресСервиса, Порт, Токен) Тогда
		Возврат Результат;
	КонецЕсли;
	
	МассивПриборов = ПрочитатьФайлJSONAutoGRAPH(ИмяФайла, "EnumDevices/" + Схема);
	
	Если НЕ МассивПриборов = Неопределено Тогда	
		
		тбзРодители = Новый ТаблицаЗначений; // Таблица родительских элементов.
		тбзРодители.Колонки.Добавить("ID",,"ID");
		тбзРодители.Колонки.Добавить("ParentID",,"ParentID");	
		тбзРодители.Колонки.Добавить("Name",,"Name");
		Для каждого текЭлемент Из МассивПриборов.Groups Цикл	
			НоваяСтрока = тбзРодители.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,текЭлемент);	
		КонецЦикла;
		
		ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
		
		ТаблицаЗначений.Колонки.Добавить("Идентификатор",,"Идентификатор"); // ID.
		ТаблицаЗначений.Колонки.Добавить("НомерУстройства",,"НомерУстройства"); // Serial.
		ТаблицаЗначений.Колонки.Добавить("Имя",,"Имя"); // Name.
		ТаблицаЗначений.Колонки.Добавить("ParentID",,"ParentID"); // ParentID.
		ТаблицаЗначений.Колонки.Добавить("Родитель",,"Родитель");
		
		Для каждого текЭлемент Из МассивПриборов.Items Цикл	
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.Идентификатор = текЭлемент.ID;
			НоваяСтрока.НомерУстройства = Формат(текЭлемент.Serial,"ЧГ=0");
			НоваяСтрока.Имя = текЭлемент.Name;
			НоваяСтрока.ParentID = текЭлемент.ParentID;
			НайденныеСтроки = тбзРодители.НайтиСтроки(Новый Структура("ID",текЭлемент.ParentID));
			Если НайденныеСтроки.Количество() Тогда
				НоваяСтрока.Родитель = НайденныеСтроки[0].Name;
			КонецЕсли;
			Для каждого текЭлементСвойство Из текЭлемент.Properties Цикл
				НайденнаяКолонка = ТаблицаЗначений.Колонки.Найти(текЭлементСвойство.Name);
				Если НайденнаяКолонка = Неопределено Тогда
					ТаблицаЗначений.Колонки.Добавить(текЭлементСвойство.Name,,текЭлементСвойство.Name);
				КонецЕсли;
				НоваяСтрока[текЭлементСвойство.Name] = текЭлементСвойство.Value;
			КонецЦикла;
		КонецЦикла;	
		
		Если ТаблицаЗначений.Количество() Тогда
			Результат = ТаблицаЗначений.Скопировать();
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокУстройствAutoGRAPH()

// Выполним обращение к сервису AutoGRAPH.NET_Service.
//
// Параметры:
//  Сервер  - Выборка запроса - Элемент выборки.
//
Процедура ПолучениеДанныхОтAutoGRAPH(Сервер)	
	
	ИнформационнаяБазаФайловая = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая()=0,ЛОЖЬ,ИСТИНА);
	
	//ВыполнятьЛогированиеПоступившихДанных = Истина;
	ВыполнятьЛогированиеПоступившихДанных = Ложь; // не создаем логи скрыто от пользователя.
	
	Если НЕ ИнформационнаяБазаФайловая Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода","упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанных");	 
		Отбор.Вставить("Состояние",СостояниеФоновогоЗадания.Активно);
		Отбор.Вставить("Наименование","ПолучениеДанныхОтAutoGRAPH");
		МассивФЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		Если МассивФЗаданий.Количество() Тогда
			// С предыдущего запуска выполняются фоновые задания.
			// Придется ждать пока они не будут завершины.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Сервер.Сервер = "" ИЛИ Сервер.Порт = 0 Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтAutoGRAPH'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не настроены параметры подключения к серверу сбора данных.'"));
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".json");	
	ИмяКаталога = "";
	ИмяКаталога_ТекущейСессии = "";
	
	Если ВыполнятьЛогированиеПоступившихДанных Тогда
		
		стрСоед = СтрокаСоединенияИнформационнойБазы();
		
		Массив = СтрРазделить(стрСоед,";",Ложь);
		
		ТекущееИмяБазы = "";
		Для Каждого текЭлемент Из Массив Цикл
			
			Если СтрНайти(текЭлемент,"Ref=") > 0 Тогда
				ТекущееИмяБазы = СтрЗаменить(текЭлемент,"Ref=","");
				ТекущееИмяБазы = СтрЗаменить(ТекущееИмяБазы,Символ(34),"");
			КонецЕсли;
			
		КонецЦикла;
		
		ИмяФайла = ПолучитьИмяВременногоФайла(".json");
		
		ИмяКаталога = КаталогВременныхФайлов() + "Log_AutoGRAPH" + ?(ПустаяСтрока(ТекущееИмяБазы),"","_base_" + ТекущееИмяБазы);
		
		КаталогНаДиске = Новый Файл(ИмяКаталога);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ИмяКаталога);
		КонецЕсли; 
		
		ИмяКаталога_ТекущейСессии = ИмяКаталога + "\" + Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMddHHmmss");
		
		СоздатьКаталог(ИмяКаталога_ТекущейСессии);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтAutoGRAPH'"), УровеньЖурналаРегистрации.Информация,,, "Католог логирования на серевере: " + ИмяКаталога_ТекущейСессии);
		
	КонецЕсли;
	
	Токен = "";
	
	Если НЕ ПустаяСтрока(Сервер.Пользователь) Тогда 
		Токен = ПолучитьТокенAutoGRAPH(ИмяФайла, Сервер.Пользователь, Сервер.Пароль, Сервер.Сервер, Сервер.Порт);
		Если ПустаяСтрока(Токен) Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	тбзУстройства = ПолучитьСписокУстройствAutoGRAPH(Сервер.Сервер,Сервер.Порт,Сервер.Схема,ИмяФайла,Токен);
	
	Если тбзУстройства = Неопределено Тогда 
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтAutoGRAPH'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не удалось получить список устройств AutoGRAPH.'"));
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "ВЫБРАТЬ
	|	УстановленныеТрекерыСП.Трекер
	|ПОМЕСТИТЬ втУстановленныеТрекеры
	|ИЗ
	|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(, ) КАК УстановленныеТрекерыСП
	|ГДЕ
	|	УстановленныеТрекерыСП.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМобильныеУстройстваИТрекеры.Ссылка КАК Трекер,
	|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	упМобильныеУстройстваИТрекеры.Модель КАК Модель,
	|	упМобильныеУстройстваИТрекеры.СерверСбораДанных.ДатаЗагрузки КАК ДатаЗагрузки
	|ПОМЕСТИТЬ втТрекеры
	|ИЗ
	|	Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
	|ГДЕ
	|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
	|	И упМобильныеУстройстваИТрекеры.ИдентификаторУстройства <> """"
	|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
	|	И упМобильныеУстройстваИТрекеры.Ссылка В
	|			(ВЫБРАТЬ
	|				втУстановленныеТрекеры.Трекер
	|			ИЗ
	|				втУстановленныеТрекеры КАК втУстановленныеТрекеры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДанныеДатчиковСП.Период) КАК Период,
	|	ДанныеДатчиковСП.Трекер
	|ПОМЕСТИТЬ вт_ДатчикПериод
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|			,
	|			Трекер В
	|				(ВЫБРАТЬ
	|					втТрекеры.Трекер
	|				ИЗ
	|					втТрекеры КАК втТрекеры)) КАК ДанныеДатчиковСП
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДатчиковСП.Трекер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА втТрекеры.ДатаЗагрузки
	|		ИНАЧЕ ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК Период,
	|	втТрекеры.Трекер КАК Трекер,
	|	втТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	ЕСТЬNULL(ДанныеТрекеровСП.Счетчик, 0) КАК СчетчикАктульности,
	|	ВЫБОР
	|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА втТрекеры.ДатаЗагрузки
	|		ИНАЧЕ ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ПериодД
	|ИЗ
	|	втТрекеры КАК втТрекеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеТрекеров.СрезПоследних КАК ДанныеТрекеровСП
	|		ПО втТрекеры.Трекер = ДанныеТрекеровСП.Трекер
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДатчикПериод КАК вт_ДатчикПериод
	|		ПО втТрекеры.Трекер = вт_ДатчикПериод.Трекер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМоделиТрекеровДатчики.НомерДатчика КАК Датчик
	|ИЗ
	|	втТрекеры КАК втТрекеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТрекеров.Датчики КАК упМоделиТрекеровДатчики
	|		ПО втТрекеры.Модель = упМоделиТрекеровДатчики.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	упМоделиТрекеровДатчики.НомерДатчика";
	Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
	
	ПакетЗапросов  = Запрос.ВыполнитьПакет();	
	
	Выборка = ПакетЗапросов[3].Выбрать();
	тбзДатчики = ПакетЗапросов[4].Выгрузить();
	
	// ПоляТабличныхДанных = "/temp,MotohoursByI1";.
	ПоляТабличныхДанных = "";
	КрайнийМндекс = тбзДатчики.Количество()-1;
	Для каждого тбзСтрока Из тбзДатчики Цикл
		РазделительПолей = ",";
		Если тбзДатчики.Индекс(тбзСтрока) >= КрайнийМндекс Тогда
			РазделительПолей = "";
		КонецЕсли;
		ПоляТабличныхДанных = ПоляТабличныхДанных + ?(ПустаяСтрока(ПоляТабличныхДанных),"/","") + СокрЛП(тбзСтрока.Датчик) + РазделительПолей;
	КонецЦикла;
	
	МассивОбъектов = Новый Массив;
		
	Пока Выборка.Следующий() Цикл
			
		ИмяФайла = ПолучитьИмяВременногоФайла(".json");
		
		Если НЕ ПустаяСтрока(ИмяКаталога_ТекущейСессии) Тогда
			// Каталог для записи логов.
			ИмяКаталога_Трека = ИмяКаталога_ТекущейСессии + "\" + Строка(Выборка.Трекер);
			СоздатьКаталог(ИмяКаталога_Трека);		
		КонецЕсли;
		
		Отбор = Новый Структура();
		Отбор.Вставить("НомерУстройства",Выборка.ИдентификаторУстройства);
		НайденныеСтроки = тбзУстройства.НайтиСтроки(Отбор);
		стрПараметры = "";
		Если НайденныеСтроки.Количество() Тогда
			ТекущаяУстройство = НайденныеСтроки[0];
			НачалоПериода = Формат(Выборка.Период+1,"ДФ=yyyyMMdd-HHmm");
			НачалоПериода_Датчика = Формат(Выборка.ПериодД+1,"ДФ=yyyyMMdd-HHmm");
			КонецПериода =  Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMdd-HHmm");
			
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство.Идентификатор,Выборка.Трекер);
			
			СтруктураОбъектов.Вставить("КаталогЛогирования",ИмяКаталога_ТекущейСессии);
			СтруктураОбъектов.Вставить("КаталогЛог",ИмяКаталога);
			СтруктураОбъектов.Вставить("КаталогЛогТрек",ИмяКаталога_Трека);
			СтруктураОбъектов.Вставить("НачалоПериода",НачалоПериода);
			СтруктураОбъектов.Вставить("НачалоПериода_Датчика",НачалоПериода_Датчика);
			СтруктураОбъектов.Вставить("КонецПериода",КонецПериода);
			
			Если НЕ ПустаяСтрока(ПоляТабличныхДанных) Тогда
				Если ВыполнитьКомандуNetAutoGRAPH("GetTripTables/" + Сервер.Схема + "/" + ТекущаяУстройство.Идентификатор + "/" + НачалоПериода_Датчика + "/" + КонецПериода + ПоляТабличныхДанных + "/0", ИмяФайла, Сервер.Сервер, Сервер.Порт, Токен) Тогда
					мсвТабличныеДанные = ПрочитатьФайлJSONAutoGRAPH(ИмяФайла, "GetTripTables/" + Сервер.Схема);
					СтруктураОбъектов.Вставить("Датчики",мсвТабличныеДанные);
					мсвТабличныеДанные = Неопределено;
					
					Если НЕ ПустаяСтрока(ИмяКаталога_Трека) Тогда
						ИмяФайлаПеремещения = ИмяКаталога_Трека + "\GetTripTables_ДанныеДатчика_" + Строка(Выборка.Трекер) + "_" + НачалоПериода_Датчика + "_" + КонецПериода + ".json";					
						ПереместитьФайл(ИмяФайла,ИмяФайлаПеремещения);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			Если ВыполнитьКомандуNetAutoGRAPH("GetTrack/" + Сервер.Схема + "/" + ТекущаяУстройство.Идентификатор + "/" + НачалоПериода + "/" + КонецПериода + "/0", ИмяФайла, Сервер.Сервер, Сервер.Порт, Токен) Тогда
				мсвТреки = ПрочитатьФайлJSONAutoGRAPH(ИмяФайла, "GetTrack/" + Сервер.Схема);
				СтруктураОбъектов.Вставить("Рейсы",мсвТреки);
				мсвТреки = Неопределено;
				
				Если НЕ ПустаяСтрока(ИмяКаталога_Трека) Тогда
					ИмяФайлаПеремещения = ИмяКаталога_Трека + "\GetTrack_ДанныеТрекера_" + Строка(Выборка.Трекер) + "_" + НачалоПериода + "_" + КонецПериода + ".json";					
					ПереместитьФайл(ИмяФайла,ИмяФайлаПеремещения);
				КонецЕсли;
				
			КонецЕсли;
			
			МассивОбъектов.Добавить(СтруктураОбъектов);
			
			УдалитьФайлы(ИмяФайла);			
			
		КонецЕсли;	
	КонецЦикла;		
	
	УдалитьФайлы(ИмяФайла);	
	
	КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковЗагрузкиССервераСбораДанных");
	Если КоличествоПотоков = 0 ИЛИ ИнформационнаяБазаФайловая Тогда 
		КоличествоПотоков = 1;
	КонецЕсли;
	
	Если ИнформационнаяБазаФайловая Тогда
		СоздатьПотокОбработкиЗагрузкиДанных(МассивОбъектов);
	Иначе
		МассивПотоков = Новый Массив;
		Если КоличествоПотоков = 1 Тогда
			МассивПотоков.Добавить(МассивОбъектов);
		Иначе
			ОбъектовВПотоке = Окр(МассивОбъектов.Количество() / КоличествоПотоков,0,РежимОкругления.Окр15как10);
			ТекущееКоличество = 0;
			ОбъектыПотока = Новый Массив;
			Для Каждого текЭлемент Из МассивОбъектов Цикл
				ОбъектыПотока.Добавить(текЭлемент);
				ТекущееКоличество = ТекущееКоличество + 1;
				Если ТекущееКоличество >= ОбъектовВПотоке Тогда
					ТекущееКоличество = 0;
					МассивПотоков.Добавить(ОбъектыПотока);
					ОбъектыПотока = Новый Массив;
				КонецЕсли;
			КонецЦикла;
			// Допишем крайний поток ТС которые остались.
			Если ОбъектыПотока.Количество() Тогда
				ТекущийМассив = МассивПотоков[МассивПотоков.ВГраница()];
				Для Каждого текЭлемент Из ОбъектыПотока Цикл
					ТекущийМассив.Добавить(текЭлемент);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого текЭлемент Из МассивПотоков Цикл
			Если текЭлемент.Количество() Тогда 
				мсвПараметров = Новый Массив(1);
				мсвПараметров[0] = текЭлемент;
				упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанных", мсвПараметров,,,"ПолучениеДанныхОтAutoGRAPH");
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры // ПолучениеДанныхОтAutoGRAPH()

// Выполним создание разных потоков обработки.
//
// Параметры:
//  ДанныеОбработки  - Массив - Данные потока.
//
Процедура СоздатьПотокОбработкиЗагрузкиДанных(ДанныеОбработки) Экспорт
	
	Для Каждого текЭлемент Из ДанныеОбработки Цикл
		ВыполнитьОбработкуЗагруженныхДанных(текЭлемент);
	КонецЦикла;
	
КонецПроцедуры // СоздатьПотокОбработкиЗагрузкиДанных()

// Выполним обработку каждого объекта.
//
//
// Параметры:
//  ДанныеОбработки - Массив - Текщий элемент данных.
//
Процедура ВыполнитьОбработкуЗагруженныхДанных(ДанныеОбработки)
	
	ТекущийТерминал = ДанныеОбработки.Трекер;
	ТаблицаЗначенийДатчиков = Неопределено;
	ТекущиеПараметрыЛогирования = Неопределено;
	
	НачалоПериодаОбработки = ТекущаяДатаСеанса();
	
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда		
		
		ТекущиеПараметрыЛогирования = Новый Структура();
		ТекущиеПараметрыЛогирования.Вставить("КаталогЛогТрек",ДанныеОбработки.КаталогЛогТрек);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода",ДанныеОбработки.НачалоПериода);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода_Датчика",ДанныеОбработки.НачалоПериода_Датчика);
		ТекущиеПараметрыЛогирования.Вставить("КонецПериода",ДанныеОбработки.КонецПериода);
		
	КонецЕсли;

	Если ДанныеОбработки.Свойство("Датчики") Тогда
		ТаблицаЗначенийДатчиков = РазобратьТабличныеДанныеAutoGRAPH(ДанныеОбработки.Датчики,ТекущийТерминал,ТекущиеПараметрыЛогирования);
	КонецЕсли;
	Если ДанныеОбработки.Свойство("Рейсы") Тогда
		РазобратьТрекиAutoGRAPH(ДанныеОбработки.Рейсы,ТекущийТерминал,ТаблицаЗначенийДатчиков,ТекущиеПараметрыЛогирования);
	КонецЕсли;
	
	Если НЕ ТаблицаЗначенийДатчиков = Неопределено И ТаблицаЗначенийДатчиков.Количество() Тогда
		
		соотТрекеров = Новый Соответствие;
		
		Для каждого ТекущаяСтрока Из ТаблицаЗначенийДатчиков Цикл
			Если ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			текДанныеТС = соотТрекеров[ТекущийТерминал];
			Период = ТекущаяСтрока.Период;
			Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
				текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
				соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			ТекущаяСтрока.ТранспортноеСредство = текДанныеТС.ТранспортноеСредство;
			ТекущаяСтрока.Рейс                 = текДанныеТС.Рейс;
		КонецЦикла;
		
		ОтборТС = Новый Структура("ТранспортноеСредство", ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка"));
		
		НайднныеСтроки = ТаблицаЗначенийДатчиков.НайтиСтроки(ОтборТС);
		Если НайднныеСтроки.Количество() Тогда
			Для каждого ТекущаяСтрока Из НайднныеСтроки Цикл
				ТаблицаЗначенийДатчиков.Удалить(ТекущаяСтрока);
			КонецЦикла;
		КонецЕсли;
		
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначенийДатчиков);
			Запрос.Выполнить();
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Период КАК ДАТА) КАК Период,
			|	ВЫРАЗИТЬ(вртДанных.Трекер КАК Справочник.упМобильныеУстройстваИТрекеры) КАК Трекер,
			|	ВЫРАЗИТЬ(вртДанных.КалибровочныйГрафик КАК Справочник.упКалибровочныеГрафики) КАК КалибровочныйГрафик,
			|	ВЫРАЗИТЬ(вртДанных.Датчик КАК Справочник.упДатчики) КАК Датчик,
			|	ВЫРАЗИТЬ(вртДанных.ТранспортноеСредство КАК Справочник.упТранспортныеСредства) КАК ТранспортноеСредство,
			|	ВЫРАЗИТЬ(вртДанных.Рейс КАК Документ.упРейс) КАК Рейс,
			|	ВЫРАЗИТЬ(вртДанных.Значение КАК ЧИСЛО(10, 3)) КАК Значение,
			|	ВЫРАЗИТЬ(вртДанных.Назначение КАК Справочник.упНазначенияДатчиков) КАК Назначение
			|ПОМЕСТИТЬ втДанные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втДанные.Период,
			|	втДанные.Трекер КАК Трекер,
			|	втДанные.КалибровочныйГрафик,
			|	втДанные.Датчик КАК Датчик,
			|	втДанные.ТранспортноеСредство КАК ТранспортноеСредство,
			|	втДанные.Рейс,
			|	втДанные.Значение,
			|	втДанные.Назначение
			|ИЗ
			|	втДанные КАК втДанные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков КАК упДанныеДатчиков
			|		ПО втДанные.Период = упДанныеДатчиков.Период
			|			И втДанные.Трекер = упДанныеДатчиков.Трекер
			|			И втДанные.Датчик = упДанныеДатчиков.Датчик
			|			И втДанные.ТранспортноеСредство = упДанныеДатчиков.ТранспортноеСредство
			|			И втДанные.Значение = упДанныеДатчиков.Значение
			|ГДЕ
			|	упДанныеДатчиков.ТранспортноеСредство ЕСТЬ NULL И (НЕ втДанные.Датчик=ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка) И НЕ втДанные.ТранспортноеСредство=ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Трекер, ТранспортноеСредство, Датчик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ";
			
			Выборка = Запрос.Выполнить();
			
			тбзКопия = Неопределено;
			
			Если НЕ Выборка.Пустой() Тогда
				тбзКопия = Выборка.Выгрузить();
			КонецЕсли;
			
			Запрос.МенеджерВременныхТаблиц.Закрыть();
			
			Если ТипЗнч(тбзКопия) = ТИП("ТаблицаЗначений") И тбзКопия.Количество() Тогда
				упИнтеграцияССерверамиСбораДанных.ВыполнитьОбработкуЗначенийДатчиковНабор(тбзКопия);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда
		Если НЕ ПустаяСтрока(ДанныеОбработки.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ДанныеОбработки.КаталогЛогТрек + "\Данные продолжительности записи данных в базу.txt";
			
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			
			ТекстовыйДокумент.ДобавитьСтроку("Начало периода записи: " + Строка(НачалоПериодаОбработки));
			ТекстовыйДокумент.ДобавитьСтроку("Конец периода записи: " + Строка(ТекущаяДатаСеанса()));
			
			ТекстовыйДокумент.Записать(ИмяФайлаЗаписи);
			ТекстовыйДокумент = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбработкуЗагруженныхДанных()

// Получим дату из строки входящи=х данных.
//
// Параметры:
//  ДатаJSON - Строка - Строковое представление даты.
//
// Возвращаемое значение:
//   Дата - Результат выполнения.
//
Функция ПолучитьДатуИзСтроки(ДатаJSON)
	
	ТекущаяДата = Неопределено;
	
	Если СтрДлина(ДатаJSON) > 21 Тогда
		СтрДата  = Сред(ДатаJSON, 7, 13);		
		ТекущаяДата = ПрочитатьДатуJSON("new Date("+СтрДата+")", ФорматДатыJSON.JavaScript);
	Иначе
		ТекущаяДата = ПрочитатьДатуJSON(ДатаJSON, ФорматДатыJSON.Microsoft);
	КонецЕсли;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекстОшибки = "НЕ смог получить дату из строки: " + Строка(ДатаJSON);
		ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);		
	КонецЕсли;
	
	Возврат ТекущаяДата;	
	
КонецФункции // ПолучитьДатуИзСтроки()

// Выполнить обход полученных данных.
//
// Параметры:
//  ТабличныеДанные  - Массив - Список ТС с данными.
//  ТекущийТерминал  - Справочник.упМобильныеУстройстваИТрекеры - Ссылка на терминал.
//  ПараметрыЛогирования - Структура - Параметры для записи лога получения данных.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица содержит обработанные данные для записи в РС.
//
Функция РазобратьТабличныеДанныеAutoGRAPH(ТабличныеДанные, ТекущийТерминал,ПараметрыЛогирования=Неопределено)
		
	ТаблицаЗначенийДатчиков = Новый ТаблицаЗначений;
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Трекер",Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"),"Трекер");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("КалибровочныйГрафик",Новый ОписаниеТипов("СправочникСсылка.упКалибровочныеГрафики"),"КалибровочныйГрафик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Датчик",Новый ОписаниеТипов("СправочникСсылка.упДатчики"),"Датчик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Рейс",Новый ОписаниеТипов("ДокументСсылка.упРейс"),"Рейс");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Значение",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Значение");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Назначение",Новый ОписаниеТипов("СправочникСсылка.упНазначенияДатчиков"),"Назначение");
	
	соотДатчиков = Новый Соответствие;
	
	Для каждого ТекущиеДанные Из ТабличныеДанные Цикл		
		мсвТреки = ТекущиеДанные.Value.Trips;
		Для каждого текТрек Из мсвТреки Цикл
			мсвДаты = текТрек.DT;
			
			текИндекс = 0;
			Для каждого текДата Из мсвДаты Цикл
				Период = ПолучитьДатуИзСтроки(текДата);
				
				Структура = Новый Структура;
				
				мсвЗначениеПолей = текТрек.Values;
				Для каждого текПоле Из мсвЗначениеПолей Цикл
					ИмяДатчика = текПоле.Name;
					Структура.Вставить(ИмяДатчика,текПоле.Values[текИндекс]);					
					текДанныеДатчика = соотДатчиков[ИмяДатчика];
					Если текДанныеДатчика = Неопределено Тогда					
						сткДанныеДатчика = Новый Структура("Представление",текПоле.Caption);
						сткДанныеДатчика.Вставить("Единица",текПоле.Unit);
						соотДатчиков.Вставить(ИмяДатчика, сткДанныеДатчика);
					КонецЕсли;					
				КонецЦикла;
				
				Для каждого текЭлемент Из Структура Цикл
					Код = текЭлемент.Ключ;
					Если ЗначениеЗаполнено(Код) Тогда
						текДанныеДатчика = соотДатчиков[Код];
						Датчик = Неопределено;
						Если текДанныеДатчика.Свойство("Датчик") Тогда
							Датчик = текДанныеДатчика.Датчик;
						Иначе							
							Представление = ?(ПустаяСтрока(текДанныеДатчика.Представление),"",текДанныеДатчика.Представление + ", ")
							+ Код + ?(ПустаяСтрока(текДанныеДатчика.Единица),"",", (" + текДанныеДатчика.Единица + ")");
							Датчик = упОбработкаДанныхТерминаловПовтИсп.НайтиДатчик(Код, ТекущийТерминал.Модель,Представление);
							текДанныеДатчика.Вставить("Датчик",Датчик);
						КонецЕсли;						
						
						НовЗаписьДатчики = ТаблицаЗначенийДатчиков.Добавить();
						НовЗаписьДатчики.Период   				= Период;
						НовЗаписьДатчики.Трекер 				= ТекущийТерминал;
						НовЗаписьДатчики.КалибровочныйГрафик    = Датчик.КалибровочныйГрафик;
						НовЗаписьДатчики.Датчик   				= Датчик.Датчик;				
						
						НовЗаписьДатчики.Значение 				= текЭлемент.Значение;
						НовЗаписьДатчики.Назначение				= Датчик.Назначение;
						
					КонецЕсли;
				КонецЦикла;
				текИндекс = текИндекс + 1;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаЗначенийДатчиков.Сортировать("Период");
	
	Если НЕ ПараметрыЛогирования = Неопределено Тогда
		Если НЕ ПустаяСтрока(ПараметрыЛогирования.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ПараметрыЛогирования.КаталогЛогТрек + "\ТаблицаЗначенийДатчиков_" + Строка(ТекущийТерминал) + "_" + ПараметрыЛогирования.НачалоПериода_Датчика + "_" + ПараметрыЛогирования.КонецПериода + ".txt";		
			ВыполнитьЗаписьТаблицыЗначенийВФайл(ИмяФайлаЗаписи,ТаблицаЗначенийДатчиков);		
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаЗначенийДатчиков;
	
КонецФункции // РазобратьТабличныеДанныеAutoGRAPH()

// Выполнить обход полученных данных.
//
// Параметры:
//  СписокТС  - Массив - Список ТС с данными.
//  ТекущийТерминал  - Справочник.упМобильныеУстройстваИТрекеры - Ссылка на терминал.
//  ДанныеДатчиков  - ТаблицаЗначений - Таблица с данными датчиков.
//  ПараметрыЛогирования - Структура - Параметры для записи лога получения данных.
//
Процедура РазобратьТрекиAutoGRAPH(СписокТС, ТекущийТерминал, ДанныеДатчиков = Неопределено,ПараметрыЛогирования=Неопределено)
	
	ТаблицаЗначенийТреков = Новый ТаблицаЗначений;
	ТаблицаЗначенийТреков.Колонки.Добавить("Период"                  ,Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Период");
	ТаблицаЗначенийТреков.Колонки.Добавить("ТранспортноеСредство"    ,Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"), "ТранспортноеСредство");
	ТаблицаЗначенийТреков.Колонки.Добавить("Широта"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ТаблицаЗначенийТреков.Колонки.Добавить("Долгота"                 ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	ТаблицаЗначенийТреков.Колонки.Добавить("ПредставлениеКоординаты" ,Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийТреков.Колонки.Добавить("Скорость"                ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "Скорость");
	ТаблицаЗначенийТреков.Колонки.Добавить("Трекер"                  ,Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"), "Трекер");
	ТаблицаЗначенийТреков.Колонки.Добавить("Рейс"                    ,Новый ОписаниеТипов("ДокументСсылка.упРейс"), "Рейс");
	
	соотТрекеров = Новый Соответствие;
	соотТрекеровПериод = Новый Соответствие;
	ВыполнитьЗаписьДанныхДатчиков = НЕ ДанныеДатчиков = Неопределено;
	
	Для каждого текТС Из СписокТС Цикл
		мсвТреки = текТС.Value;
		Для каждого текТрек Из мсвТреки Цикл		// обходим рейсы.
			мсвДаты = текТрек.DT;
			текИндекс = 0;                    
			Для каждого текДата Из мсвДаты Цикл	// отрезки рейса.
				Период = ПолучитьДатуИзСтроки(текДата);
				ПроверкаПериода = соотТрекеровПериод.Получить(Период);
				Если НЕ ПроверкаПериода=Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Долгота = текТрек.Lng[текИндекс];
				Широта = текТрек.Lat[текИндекс];
				Скорость = текТрек.Speed[текИндекс];
				текИндекс = текИндекс + 1;
				текДанныеТС = соотТрекеров[ТекущийТерминал];
				Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
					текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
					соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
					Продолжить;
				КонецЕсли;
				НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
				НовЗаписьТрекера.Период   			     = Период;
				НовЗаписьТрекера.Широта                  = Широта;
				НовЗаписьТрекера.Долгота                 = Долгота;
				НовЗаписьТрекера.ПредставлениеКоординаты = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеКоординаты(НовЗаписьТрекера.Широта, НовЗаписьТрекера.Долгота);
				НовЗаписьТрекера.Скорость                = Скорость;
				НовЗаписьТрекера.ТранспортноеСредство 	 = текДанныеТС.ТранспортноеСредство; 
				НовЗаписьТрекера.Трекер 				 = ТекущийТерминал;
				НовЗаписьТрекера.Рейс                    = текДанныеТС.Рейс;
				Структура = Новый Структура("ТранспортноеСредство, Рейс", текДанныеТС.ТранспортноеСредство, текДанныеТС.Рейс);
				соотТрекеровПериод.Вставить(Период, Структура);
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаЗначенийТреков.Сортировать("Период");
	
	Если ВыполнитьЗаписьДанныхДатчиков Тогда
		Для каждого ТекущаяСтрока Из ДанныеДатчиков Цикл
			ВыполнитьКопированиеДанных = Истина;
			ДанныеТСРейса = соотТрекеровПериод.Получить(ТекущаяСтрока.Период);
			Если ДанныеТСРейса=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) Тогда
				ТекущаяСтрока.ТранспортноеСредство = ДанныеТСРейса.ТранспортноеСредство;
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеТСРейса.Рейс) Тогда
				ТекущаяСтрока.Рейс = ДанныеТСРейса.Рейс;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ПараметрыЛогирования = Неопределено Тогда
		Если НЕ ПустаяСтрока(ПараметрыЛогирования.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ПараметрыЛогирования.КаталогЛогТрек + "\ТаблицаЗначенийТреков_" + Строка(ТекущийТерминал) + "_" + ПараметрыЛогирования.НачалоПериода + "_" + ПараметрыЛогирования.КонецПериода + ".txt";		
			ВыполнитьЗаписьТаблицыЗначенийВФайл(ИмяФайлаЗаписи,ТаблицаЗначенийТреков);		
		КонецЕсли;
	КонецЕсли;
	
	Период = ДАТА("00010101000000");
	
	ПроверитьЗаполнениеДанныхНаправленияДвижения(ТаблицаЗначенийТреков);
	
	Попытка
		ТекущееТранспортноеСредство=Неопределено;
		НаборЗаписей = РегистрыСведений.упДанныеТрекеров.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		Для каждого тбзСтрока Из ТаблицаЗначенийТреков Цикл     
			Если НЕ ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство Тогда
				ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство;
				Если НаборЗаписей.Количество() Тогда
					Попытка
						НаборЗаписей.Записать(Ложь);
					Исключение
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
						КонецЕсли;
						ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;
					Если ТранзакцияАктивна() Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;
					НаборЗаписей.Очистить();
				КонецЕсли;
				Если НЕ ТранзакцияАктивна() Тогда
					НачатьТранзакцию();
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеТрекеров");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТекущееТранспортноеСредство);
					БлокировкаДанных.Заблокировать();
				КонецЕсли;
				НаборЗаписей.Отбор.ТранспортноеСредство.Установить(ТекущееТранспортноеСредство, Истина);
			КонецЕсли;
			НовЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗапись,тбзСтрока);
		КонецЦикла;
		Если НаборЗаписей.Количество() Тогда
			Попытка
				НаборЗаписей.Записать(Ложь);
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ТаблицаЗначенийТреков = Неопределено;

КонецПроцедуры // РазобратьТрекиAutoGRAPH()

// Аутентификация выполняется вызовом метода Login, который возвращает токен.
//
// Параметры:
//  ИмяФайла  - Строка - Путь к файлу.
//  Логин  - Строка - Имя пользователя сервиса.
//  Пароль  - Строка - Пароль пользователя сервиса.
//  АдресРесурса  - Строка - адрес доступа к сервису.
//  Порт  - Число - Номер порта.
//
// Возвращаемое значение:
//   Строка - Представление токена доступа.
//
Функция ПолучитьТокенAutoGRAPH(ИмяФайла, Логин, Пароль, АдресРесурса, Порт) Экспорт
	
	Запись = Новый ЗаписьJSON;
	Запись.ОткрытьФайл(ИмяФайла, "UTF-8");
	Данные = Новый Структура;
	Данные.Вставить("UserName", Логин);
	Данные.Вставить("Password", Пароль);
	ЗаписатьJSON(Запись, Данные);
	Запись.Закрыть();
	
	ТекстФайл = Новый ТекстовыйДокумент;
	ТекстФайл.Прочитать(ИмяФайла, "UTF-8");
	Тело = ТекстФайл.ПолучитьТекст();
	
	// Вызов (внимание! используется метод POST ):
	Соединение	= Новый HTTPСоединение(СокрЛП(АдресРесурса), Порт);
	ЗаголHttp	= Новый Соответствие;
	ЗаголHttp.Вставить("Content-Type", "application/json");
	ЗапросHttp	= Новый HTTPЗапрос("/Login", ЗаголHttp);
	ЗапросHttp.УстановитьТелоИзСтроки(Тело, "UTF-8", ИспользованиеByteOrderMark.НеИспользовать);
	Попытка
		ОтветHttp	= Соединение.ОтправитьДляОбработки(ЗапросHttp);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось отправить POST Http-запрос Login. " + Символы.ПС + ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат "";
	КонецПопытки;
	// Попытаемся прочесть токен.
	ТокенСтр = ОтветHttp.ПолучитьТелоКакСтроку("UTF-8");
	ТокенСтр = СтрЗаменить(ТокенСтр, """", "");
	Возврат ТокенСтр;
	
КонецФункции // ПолучитьТокенAutoGRAPH()

// Определяет возможность обращения к сервису для получения данных.
//
// Параметры:
//  КомандаСтр  - Строка - Команда обращения к сервису.
//  ИмяФайла  - Строка - Путь к файлу.
//  АдресРесурса  - Строка - адрес доступа к сервису.
//  Порт  - Число - Номер порта.
//  Токен  - Строка - Номер порта.
//
// Возвращаемое значение:
//   Булево - Флаг выполнения команды.
//
Функция ВыполнитьКомандуNetAutoGRAPH(КомандаСтр, ИмяФайла, АдресРесурса, Порт, Токен = "") Экспорт
	
	HttpКоманда = "http://" + СокрЛП(АдресРесурса) + ":" + Формат(Порт, "ЧДЦ=; ЧГ=0") + "/" + КомандаСтр;	
	Соединение	= Новый HTTPСоединение(СокрЛП(АдресРесурса), Порт);
	// Если задана аутентификация устанавливается заголовок AG-TOKEN.
	Если НЕ ПустаяСтрока(Токен) Тогда 
		ЗаголHttp	= Новый Соответствие;
		ЗаголHttp.Вставить("Content-Type", "application/json");
		ЗаголHttp.Вставить("AG-TOKEN", Токен);
		ЗапросHttp	= Новый HTTPЗапрос("/" + КомандаСтр, ЗаголHttp);
	Иначе 
		ЗапросHttp	= Новый HTTPЗапрос(КомандаСтр);
	КонецЕсли;
	Попытка
	 	ОтветHttp	= Соединение.Получить(ЗапросHttp, ИмяФайла);
	Исключение		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось отправить Http запрос " + HttpКоманда + ". " + Символы.ПС + ОписаниеОшибки();
		Сообщение.Сообщить();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ВыполнитьКомандуNetAutoGRAPH'"), УровеньЖурналаРегистрации.Ошибка,,, Сообщение.Текст);
		Возврат Ложь;
	КонецПопытки;
	
	// Анализируем фатальные ошибки.
	// В большинстве случаев нужно остановить работу и показать пользователю сообщение об ошибке.
	// Включив в него HTTP-статус.
	
	// Ошибки 4XX говорят о неправильном запросе - в широком смысле.
	// Может быть неправильный адрес, ошибка аутентификации, плохой формат запроса.
	// Подробнее смотри http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html#sec10.4.
	Если ОтветHttp.КодСостояния >= 400 и ОтветHttp.КодСостояния < 500  Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Код статуса больше 4XX, ошибка запроса.  Код статуса: " + ОтветHttp.КодСостояния +
		Символы.ПС + "  Подробнее смотри http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html#sec10.4";
		Сообщение.Сообщить();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ВыполнитьКомандуNetAutoGRAPH'"), УровеньЖурналаРегистрации.Ошибка,,, Сообщение.Текст);
		Возврат Ложь;
	КонецЕсли;
	
	// Ошибки 5XX говорят о проблемах на сервере (возможно, прокси-сервер).
	// Это может быть программная ошибка, нехватка памяти, ошибка конфигурации и т.д.
	// Подробнее смотри http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html#sec10.5.
	Если ОтветHttp.КодСостояния >= 500 и ОтветHttp.КодСостояния < 600  Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Код статуса больше 5XX, ошибка сервера. Код статуса: " + ОтветHttp.КодСостояния +
		Символы.ПС + "  Подробнее смотри http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html#sec10.5";
		Сообщение.Сообщить();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ВыполнитьКомандуNetAutoGRAPH'"), УровеньЖурналаРегистрации.Ошибка,,, Сообщение.Текст);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ВыполнитьКомандуNetAutoGRAPH()

// Читает данные ответа с сервера.
//
// Параметры:
//  ИмяФайла  - Строка - Путь к файлу.
//  HttpКоманда  - Строка - Команда сервиса.
//
// Возвращаемое значение:
//   Массив - Результат чтения данных.
//
Функция ПрочитатьФайлJSONAutoGRAPH(ИмяФайла, HttpКоманда) Экспорт
	
	Перем Файл, Данные, Тхт;
	
	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда 
		Сообщение = Новый СообщениеПользователю;
		// Проверим что это json.
		Тхт		= Новый ТекстовыйДокумент;
		Тхт.Прочитать(ИмяФайла);
		Если Тхт.КоличествоСтрок() = 0 Тогда 
			Сообщение.Текст = "Файл ответа не содержит строк";
			Сообщение.Сообщить();
			Возврат Неопределено;
		КонецЕсли;
		СтрокаАнализа = Тхт.ПолучитьСтроку(1);
		Если (Найти(СтрокаАнализа, "xml version=") > 0) ИЛИ (Найти(СтрокаАнализа, "<html>") > 0) Тогда 
			Сообщение.Текст = "Ответ не получен! Возможно строка запроса: " + HttpКоманда + ", содержит не корректную команду";
			Сообщение.Сообщить();
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Чтение = Новый ЧтениеJSON;
	Чтение.ОткрытьФайл(ИмяФайла);
	Данные = ПрочитатьJSON(Чтение, Ложь);
	Чтение.Закрыть();
	
	Возврат Данные;
	
КонецФункции // ПрочитатьФайлJSONAutoGRAPH()

// Выполним обращения к сервису для получения данных по схемам.
//
// Параметры:
//  Сервер  - Строка - Адрес сервиса.
//  Порт  - Число - Номер порта сервиса.
//  Логин  - Строка - Пользователь под которым можно обратится к сервису.
//  Пароль  - Строка - Пароль пользователя.
//
// Возвращаемое значение:
//   Массив   - Список вариантов наименований схем.
//
Функция ВернутьСхемыСервисаAutoGRAPH(Сервер,Порт,Логин,Пароль) Экспорт
	
	Результат = Неопределено;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".json");

	Токен = "";
	
	Если НЕ ПустаяСтрока(Логин) Тогда 
		Токен = ПолучитьТокенAutoGRAPH(ИмяФайла, Логин, Пароль, Сервер, Порт);
		Если ПустаяСтрока(Токен) Тогда 
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ВыполнитьКомандуNetAutoGRAPH("EnumSchemas", ИмяФайла, Сервер, Порт, Токен) Тогда
		Возврат Результат;
	КонецЕсли;
	
	МассивСхем = ПрочитатьФайлJSONAutoGRAPH(ИмяФайла, "EnumSchemas");
	
	Если ТипЗнч(МассивСхем) = ТИП("Массив") Тогда
		Результат = Новый Массив;
		Для каждого текЭлемент Из МассивСхем Цикл			
			Результат.Добавить(текЭлемент.ID);			
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВернутьСхемыСервисаAutoGRAPH()

// Выполним сохранение данных таблице в файл.
//
// Параметры:
//  ИмяФайла - Строка - Полуный путь к файлу в который требуется выполнить запись.
//  ТаблицаДанных - ТаблицаЗначений - Таблица данных для записи.
//
Процедура ВыполнитьЗаписьТаблицыЗначенийВФайл(ИмяФайла,ТаблицаДанных)
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	ТекстовыйДокумент.ДобавитьСтроку("Количество строк в таблице: " + Формат(ТаблицаДанных.Количество(),"ЧГ=0"));
	
	СтроковоеЗначениеТаблицы = "";
	
	МассивК = Новый Массив;
	
	Для каждого ТекущаяКолонка Из ТаблицаДанных.Колонки Цикл	
		МассивК.Добавить(ТекущаяКолонка.Имя);
		СтроковоеЗначениеТаблицы = СтроковоеЗначениеТаблицы + ТекущаяКолонка.Имя + Символы.Таб;
	КонецЦикла;
	
	ТекстовыйДокумент.ДобавитьСтроку(СтроковоеЗначениеТаблицы);
	
	Для каждого ТекущаяСтрока Из ТаблицаДанных Цикл
		СтроковоеЗначениеТаблицы = "";
		Для каждого ТекущаяКолонка Из МассивК Цикл	
			СтроковоеЗначениеТаблицы = СтроковоеЗначениеТаблицы + Строка(ТекущаяСтрока[ТекущаяКолонка]) + Символы.Таб + "|";
		КонецЦикла;
		
		Если НЕ ПустаяСтрока(СтроковоеЗначениеТаблицы) Тогда
			ТекстовыйДокумент.ДобавитьСтроку(СтроковоеЗначениеТаблицы);
		КонецЕсли;
		
	КонецЦикла;
	
	СтроковоеЗначениеТаблицы = Неопределено;
	
	ТекстовыйДокумент.Записать(ИмяФайла);
	ТекстовыйДокумент = Неопределено;
	
КонецПроцедуры // ВыполнитьЗаписьТаблицыЗначенийВФайл()

#КонецОбласти

#Область ОбработкаСКАУТ

// Выполним обращение к сервису СКАУТ получим таблицу всех устройств.
//
// Параметры:
//  АдресСервиса  - Строка - Адрес сервиса.
//  Порт  - Число - Номер порта для подключения к сервису.
//  Токен  - Строка - токен подключения.
//  СерверСбораДанных  - СправочникСсылка.упСервераСбораДанных - Ссылка Сервер сбора данных трекера.
//  ПроверятьТрекер  - Булево - Если признак установлен, будет выполнена проверка трекера.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьСписокУстройствScoutSOAP(АдресСервиса,Порт,Токен, СерверСбораДанных = Неопределено, ПроверятьТрекер = Ложь) Экспорт
	
	ПространствоИмен = "http://tempuri.org/";
	ТекстПодключения = "http://%1:%2/spic/units/soap?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресСервиса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "SpicUnitsService";
	ИмяТочкиПодключения = "BasicHttpBinding_ISpicSoapUnitsService";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		Прокси = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;
	
	Прокси.Пользователь = Токен;
	// Получить количество доступных объектов мониторинга.
	UnitsCount = Прокси.GetAllUnitsCount();
	
	ПространствоИмен = "http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Infrastructure.Data";
	SpicObjectsChunkRequest  = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип(ПространствоИмен, "SpicObjectsChunkRequest"));
	SpicObjectsChunkRequest.Count = UnitsCount;
	SpicObjectsChunkRequest.Offset = 0;
	
	units = Прокси.GetAllUnitsPaged(SpicObjectsChunkRequest);
	
	// При успешном выполнении units будет содержать все доступные ОМ.
	
	ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
	
	ТаблицаЗначений.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов("Строка"),"Уникальный идентификатор");
	ТаблицаЗначений.Колонки.Добавить("РегистрационныйНомер",Новый ОписаниеТипов("Строка"),"Государственный регистрационный номер"); 
	ТаблицаЗначений.Колонки.Добавить("ЧасовойПояс",Новый ОписаниеТипов("Строка"),"Часовой пояс");
	ТаблицаЗначений.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка"),"Название объекта мониторинга");
	ТаблицаЗначений.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"Транспортное средство");
		
	Если НЕ units.Свойства().Получить("Units") = Неопределено Тогда
		SpicUnit = Units.Units.ПолучитьСписок(Units.Units.Свойства().Получить("SpicUnit"));
		Если НЕ SpicUnit = Неопределено Тогда
			Количество = SpicUnit.Количество();
			Для Сч = 0 По Количество - 1 Цикл 				
				ДанныеПоТС = SpicUnit.Получить(Сч);
				Идентификатор = Формат(ДанныеПоТС.UnitId,"ЧГ=0");
				НоваяСтрока = ТаблицаЗначений.Добавить();
				НоваяСтрока.Идентификатор = Формат(ДанныеПоТС.UnitId,"ЧГ=0");
				НоваяСтрока.РегистрационныйНомер = ДанныеПоТС.StateNumber;
				НоваяСтрока.ЧасовойПояс = ДанныеПоТС.OlsonId;
				НоваяСтрока.Наименование = ДанныеПоТС.Name;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Результат = Неопределено;
	Прокси = Неопределено;units = Неопределено;
	Если ТаблицаЗначений.Количество() Тогда
		Результат = ТаблицаЗначений.Скопировать();
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокУстройствScoutSOAP()

// Выполним обращение к сервису СКАУТ.
//
// Параметры:
//  Сервер  - Выборка запроса - Элемент выборки.
//
Процедура ПолучениеДанныхОтСКАУТ(Сервер)	
	
	ИнформационнаяБазаФайловая = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая()=0,ЛОЖЬ,ИСТИНА);
	
	//ВыполнятьЛогированиеПоступившихДанных = Истина;
	ВыполнятьЛогированиеПоступившихДанных = Ложь; // не создаем логи скрыто от пользователя.
	
	Если НЕ ИнформационнаяБазаФайловая Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода","упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ");	 
		Отбор.Вставить("Состояние",СостояниеФоновогоЗадания.Активно);
		Отбор.Вставить("Наименование","ПолучениеДанныхОтScoutSOAP");
		МассивФЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		Если МассивФЗаданий.Количество() Тогда
			// С предыдущего запуска выполняются фоновые задания.
			// Придется ждать пока они не будут завершины.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Сервер.Сервер = "" ИЛИ Сервер.Порт = 0 Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтScoutSOAP'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не настроены параметры подключения к серверу сбора данных.'"));
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".txt");	
	ИмяКаталога = "";
	ИмяКаталога_ТекущейСессии = "";
	ИмяКаталога_Трека = "";
	
	Если ВыполнятьЛогированиеПоступившихДанных Тогда
		
		стрСоед = СтрокаСоединенияИнформационнойБазы();
		
		Массив = СтрРазделить(стрСоед,";",Ложь);
		
		ТекущееИмяБазы = "";
		Для Каждого текЭлемент Из Массив Цикл
			
			Если СтрНайти(текЭлемент,"Ref=") > 0 Тогда
				ТекущееИмяБазы = СтрЗаменить(текЭлемент,"Ref=","");
				ТекущееИмяБазы = СтрЗаменить(ТекущееИмяБазы,Символ(34),"");
			КонецЕсли;
			
		КонецЦикла;
		
		ИмяФайла = ПолучитьИмяВременногоФайла(".txt");
		
		ИмяКаталога = КаталогВременныхФайлов() + "Log_ScoutSOAP" + ?(ПустаяСтрока(ТекущееИмяБазы),"","_base_" + ТекущееИмяБазы);
		//ИмяКаталога = "\\1cSERVER\C$\1S\Storages\AXELOT\otladka\" + "Log_ScoutSOAP" + ?(ПустаяСтрока(ТекущееИмяБазы),"","_base_" + ТекущееИмяБазы);
		
		КаталогНаДиске = Новый Файл(ИмяКаталога);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ИмяКаталога);
		КонецЕсли; 
		
		ИмяКаталога_ТекущейСессии = ИмяКаталога + "\" + Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMddHHmmss");
		
		СоздатьКаталог(ИмяКаталога_ТекущейСессии);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтScoutSOAP'"), УровеньЖурналаРегистрации.Информация,,, "Каталог логирования на сервере: " + ИмяКаталога_ТекущейСессии);
		
	КонецЕсли;
	
	Токен = "";
	ПроксиАвторизация = Неопределено;
	Отказ = Ложь;
	СобытияМониторинга = Сервер.СобытияМониторинга;
	Отсрочка = Сервер.Отсрочка;
	Попытка
		Если НЕ ПустаяСтрока(Сервер.Пользователь) Тогда 
			Токен = ПолучитьТокенScoutSOAP(ПроксиАвторизация, Сервер.Пользователь, Сервер.Пароль, Сервер.Сервер, Сервер.Порт);
			Если ПустаяСтрока(Токен) Тогда 
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		тбзУстройства = ПолучитьСписокУстройствScoutSOAP(Сервер.Сервер, Сервер.Порт, Токен, Сервер.Ссылка, Истина);
		
		Если тбзУстройства = Неопределено Тогда
			ПроксиАвторизация.Logout();
			ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтScoutSOAP'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не удалось получить список устройств SpicUnitsService.'"));
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
		Запрос.УстановитьПараметр("ТЗ", тбзУстройства);
		Запрос.Выполнить();
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВЫРАЗИТЬ(вртДанных.Идентификатор КАК СТРОКА(50)) КАК Идентификатор,
		|	ВЫРАЗИТЬ(вртДанных.РегистрационныйНомер КАК СТРОКА(50)) КАК РегистрационныйНомер
		|ПОМЕСТИТЬ втДанные
		|ИЗ
		|	ТЗ КАК вртДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДанные.Идентификатор КАК Идентификатор,
		|	втДанные.РегистрационныйНомер КАК РегистрационныйНомер,
		|	ЕСТЬNULL(упТранспортныеСредства.Ссылка, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
		|	ЕСТЬNULL(упМобильныеУстройстваИТрекеры.Ссылка, ЗНАЧЕНИЕ(Справочник.упМобильныеУстройстваИТрекеры.ПустаяСсылка)) КАК Трекер
		|ИЗ
		|	втДанные КАК втДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|		ПО втДанные.РегистрационныйНомер = упТранспортныеСредства.РегистрационныйНомер
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
		|		ПО (втДанные.Идентификатор = упМобильныеУстройстваИТрекеры.ИдентификаторУстройства)
		|ГДЕ
		|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
		|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТЗ";
		Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл			
			Если ЗначениеЗаполнено(Выборка.ТранспортноеСредство)
				И НЕ ЗначениеЗаполнено(Выборка.Трекер) Тогда
				НовыйТрекер = Справочники.упМобильныеУстройстваИТрекеры.СоздатьЭлемент();
				НовыйТрекер.Наименование = СтрШаблон("%1_СКАУТ_%2", Выборка.Идентификатор, Выборка.РегистрационныйНомер);
				НовыйТрекер.ИдентификаторУстройства = Выборка.Идентификатор;
				НовыйТрекер.СерверСбораДанных = Сервер;
				Попытка
					НовыйТрекер.Записать();
				Исключение
					Продолжить;
				КонецПопытки;
				НовЗапись = РегистрыСведений.упУстановленныеТрекеры.СоздатьМенеджерЗаписи();
				НовЗапись.Период = ТекущаяДатаСеанса();
				НовЗапись.Трекер = НовыйТрекер.Ссылка;
				НовЗапись.ТранспортноеСредство = Выборка.ТранспортноеСредство;
				НовЗапись.Установлен = Истина;
				Попытка
					НовЗапись.Записать(Истина);			
				Исключение
					ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.СКАУТ", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упУстановленныеТрекеры, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
		Запрос.МенеджерВременныхТаблиц.Закрыть();
		
		МассивТС = тбзУстройства.ВыгрузитьКолонку("Идентификатор");
		Запрос = Новый Запрос;	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УстановленныеТрекерыСП.Трекер КАК Трекер,
		|	УстановленныеТрекерыСП.ТранспортноеСредство КАК ТранспортноеСредство,
		|	УстановленныеТрекерыСП.Период КАК Период
		|ПОМЕСТИТЬ втУстановленныеТрекеры
		|ИЗ
		|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(, ) КАК УстановленныеТрекерыСП
		|ГДЕ
		|	НЕ УстановленныеТрекерыСП.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
		|	И УстановленныеТрекерыСП.Установлен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упМобильныеУстройстваИТрекеры.Ссылка КАК Трекер,
		|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
		|	упМобильныеУстройстваИТрекеры.Модель КАК Модель,
		|	ВЫБОР
		|		КОГДА упМобильныеУстройстваИТрекеры.СерверСбораДанных.ДатаЗагрузки > втУстановленныеТрекеры.Период
		|			ТОГДА упМобильныеУстройстваИТрекеры.СерверСбораДанных.ДатаЗагрузки
		|		ИНАЧЕ втУстановленныеТрекеры.Период
		|	КОНЕЦ КАК ДатаЗагрузки,
		|	втУстановленныеТрекеры.ТранспортноеСредство КАК ТранспортноеСредство,
		|	упМобильныеУстройстваИТрекеры.СерверСбораДанных.ИнтервалПолученияДанных КАК Интервал,
		|	втУстановленныеТрекеры.ТранспортноеСредство.ДатчикУровняТоплива КАК ДатчикТоплива
		|ПОМЕСТИТЬ втТрекеры
		|ИЗ
		|	Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
		|		ЛЕВОЕ СОЕДИНЕНИЕ втУстановленныеТрекеры КАК втУстановленныеТрекеры
		|		ПО упМобильныеУстройстваИТрекеры.Ссылка = втУстановленныеТрекеры.Трекер
		|ГДЕ
		|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
		|	И упМобильныеУстройстваИТрекеры.ИдентификаторУстройства В(&ИдентификаторыУстройств)
		|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
		|	И упМобильныеУстройстваИТрекеры.Ссылка В
		|			(ВЫБРАТЬ
		|				втУстановленныеТрекеры.Трекер
		|			ИЗ
		|				втУстановленныеТрекеры КАК втУстановленныеТрекеры)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДанныеДатчиковСМ.Дата) КАК Период,
		|	ДанныеДатчиковСМ.Трекер КАК Трекер
		|ПОМЕСТИТЬ вт_ДатчикПериод
		|ИЗ
		|	Документ.упСобытиеМониторинга КАК ДанныеДатчиковСМ
		|ГДЕ
		|	ДанныеДатчиковСМ.Трекер В
		|				(ВЫБРАТЬ
		|					втТрекеры.Трекер
		|				ИЗ
		|					втТрекеры КАК втТрекеры)
		|	И ДанныеДатчиковСМ.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДатчиковСМ.Трекер
		//|ВЫБРАТЬ
		//|	МАКСИМУМ(ДанныеДатчиковСП.Период) КАК Период,
		//|	ДанныеДатчиковСП.Трекер КАК Трекер
		//|ПОМЕСТИТЬ вт_ДатчикПериод
		//|ИЗ
		//|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
		//|			,
		//|			Трекер В
		//|				(ВЫБРАТЬ
		//|					втТрекеры.Трекер
		//|				ИЗ
		//|					втТрекеры КАК втТрекеры)) КАК ДанныеДатчиковСП
		//|
		//|СГРУППИРОВАТЬ ПО
		//|	ДанныеДатчиковСП.Трекер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
		|			ТОГДА втТрекеры.ДатаЗагрузки
		|		ИНАЧЕ ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК Период,
		|	втТрекеры.Трекер КАК Трекер,
		|	втТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
		|	ЕСТЬNULL(ДанныеТрекеровСП.Счетчик, 0) КАК СчетчикАктульности,
		|	ВЫБОР
		|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
		|			ТОГДА втТрекеры.ДатаЗагрузки
		|		ИНАЧЕ ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК ПериодД,
		|	втТрекеры.Интервал КАК Интервал,
		|	втТрекеры.ДатчикТоплива КАК ДатчикТоплива
		|ИЗ
		|	втТрекеры КАК втТрекеры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеТрекеров.СрезПоследних КАК ДанныеТрекеровСП
		|		ПО втТрекеры.Трекер = ДанныеТрекеровСП.Трекер
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДатчикПериод КАК вт_ДатчикПериод
		|		ПО втТрекеры.Трекер = вт_ДатчикПериод.Трекер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТрекеры.Трекер КАК Трекер,
		|	упМоделиТрекеровДатчики.Датчик КАК Датчик,
		|	упМоделиТрекеровДатчики.КалибровочныйГрафик КАК КалибровочныйГрафик,
		|	ВЫБОР
		|		КОГДА упМоделиТрекеровДатчики.Датчик.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Топливо
		|ИЗ
		|	втТрекеры КАК втТрекеры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТрекеров.Датчики КАК упМоделиТрекеровДатчики
		|		ПО втТрекеры.Модель = упМоделиТрекеровДатчики.Ссылка";
		
		Запрос.УстановитьПараметр("ИдентификаторыУстройств", МассивТС);
		Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
		ПакетЗапросов  = Запрос.ВыполнитьПакет();
		Выборка = ПакетЗапросов[3].Выбрать();
		тбзДатчики = ПакетЗапросов[4].Выгрузить();
		
		МассивОбъектов = Новый Массив;
		ДатаЗапуска = ТекущаяДатаСеанса();
		КоличествоСекундЗагрузки = 86400; // 24*60*60 сутки.
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ПустаяСтрока(ИмяКаталога_ТекущейСессии) Тогда
				// Каталог для записи логов.
				ИмяКаталога_Трека = ИмяКаталога_ТекущейСессии + "\" + Строка(Выборка.Трекер);
				СоздатьКаталог(ИмяКаталога_Трека);		
			КонецЕсли;
			
			ТекущаяУстройство = Выборка.ИдентификаторУстройства;
			СтруктураПериода = ПолучитьИнтервалЗапросаДанных(Выборка.Период, Выборка.ПериодД, ДатаЗапуска, КоличествоСекундЗагрузки, Отсрочка);
			
			НачалоПериода = СтруктураПериода.НачалоПериода;
			НачалоПериода_Датчика = СтруктураПериода.НачалоПериода_Датчика;
			КонецПериода =  СтруктураПериода.КонецПериода;
			КонецПериода_Датчика = СтруктураПериода.КонецПериода_Датчика;
			
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство,Выборка.Трекер);
			//СтруктураОбъектов.Вставить("КаталогЛогирования",ИмяКаталога_ТекущейСессии);
			//СтруктураОбъектов.Вставить("КаталогЛог",ИмяКаталога);
			//СтруктураОбъектов.Вставить("КаталогЛогТрек",ИмяКаталога_Трека);
			//СтруктураОбъектов.Вставить("НачалоПериода",НачалоПериода);
			//СтруктураОбъектов.Вставить("НачалоПериода_Датчика",НачалоПериода_Датчика);
			//СтруктураОбъектов.Вставить("КонецПериода",КонецПериода);
			
			Попытка
				Данные = ПолучитьОнлайнДанныеScoutSOAP(ТекущаяУстройство, НачалоПериода, КонецПериода,Сервер.Сервер,Сервер.Порт,Токен);
			Исключение				
				Данные = Неопределено;				
				СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
			КонецПопытки;
			Если Данные = Неопределено Тогда
				Если (ДатаЗапуска - НачалоПериода) > 3600 Тогда
					Данные = Новый СписокЗначений;
					ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековСКАУТ();
					НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
					НовЗаписьТрекера.Период   			 = КонецПериода;
					НовЗаписьТрекера.Широта              = 0;
					НовЗаписьТрекера.Долгота             = 0;
					НовЗаписьТрекера.Скорость            = 0;
					НовЗаписьТрекера.Высота              = 0;
					НовЗаписьТрекера.НаправлениеДвижения = 0;
					Данные.Добавить(ТаблицаЗначенийТреков);
				КонецЕсли;
			КонецЕсли;
			
			СтруктураОбъектов.Вставить("Навигация",Данные);
			
			// Получение данных датчиков
			//Интервал = ?(Выборка.Интервал=0, 10, Выборка.Интервал);
			ЕстьДатчикТоплива = Ложь;
			ДатчикТоплива = Выборка.ДатчикТоплива;
			КалибровочныйГрафик = Справочники.упКалибровочныеГрафики.ПустаяСсылка();
			Если ЗначениеЗаполнено(Выборка.ДатчикТоплива) Тогда 
				ЕстьДатчикТоплива = Истина;
			Иначе
				Отбор = Новый Структура();
				Отбор.Вставить("Топливо", Истина);
				Отбор.Вставить("Трекер", Выборка.Трекер);
				НайденныеСтроки = тбзДатчики.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() Тогда
					ЕстьДатчикТоплива = Истина;
					ДатчикТоплива = НайденныеСтроки[0].Датчик;
					КалибровочныйГрафик = НайденныеСтроки[0].КалибровочныйГрафик;
				КонецЕсли;
			КонецЕсли;
			
			Если (СобытияМониторинга И ЕстьДатчикТоплива)
				И (НЕ НачалоДня(НачалоПериода_Датчика) = НачалоДня(ДатаЗапуска)) Тогда
				ДанныеИнтервала = Неопределено;
				ЕстьОшибки = Ложь;
				Попытка
					// попытаемся получить данные за весь период.
					ДанныеИнтервала = ПолучитьДанныеТопливаЗаПериодScoutSOAP(ТекущаяУстройство, НачалоПериода_Датчика, КонецПериода_Датчика,Сервер.Сервер,Сервер.Порт,Токен, ЕстьОшибки);
				Исключение
					ДанныеИнтервала = Неопределено;
					СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
				КонецПопытки;
				Если НЕ ДанныеИнтервала = Неопределено Тогда
					ПериодСобытияМониторинга = Новый Структура();
					ПериодСобытияМониторинга.Вставить("НачалоПериода", НачалоПериода_Датчика);
					ПериодСобытияМониторинга.Вставить("КонецПериода", КонецПериода_Датчика);
					Назначение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДатчикТоплива, "Назначение");
					СтруктураОбъектов.Вставить("СобытияМониторинга",ДанныеИнтервала);
					СтруктураОбъектов.Вставить("Назначение",Назначение);
					СтруктураОбъектов.Вставить("ДатчикТоплива",ДатчикТоплива);
					СтруктураОбъектов.Вставить("КалибровочныйГрафик",КалибровочныйГрафик);
					СтруктураОбъектов.Вставить("ПериодСобытияМониторинга",ПериодСобытияМониторинга);
				КонецЕсли;
			КонецЕсли;
			МассивОбъектов.Добавить(СтруктураОбъектов);
		КонецЦикла;
		ПроксиАвторизация.Logout();
	Исключение
		Отказ = Истина;
		ОписаниеТекущейОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтScoutSOAP'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ОписаниеТекущейОшибки);
		Если НЕ ПроксиАвторизация = Неопределено Тогда 
			ПроксиАвторизация.Logout();
		КонецЕсли;
	КонецПопытки;
	Токен = "";
	ПроксиАвторизация = Неопределено;
	Если НЕ Отказ Тогда
		КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковЗагрузкиССервераСбораДанных");
		Если КоличествоПотоков = 0 ИЛИ ИнформационнаяБазаФайловая Тогда 
			КоличествоПотоков = 1;
		КонецЕсли;
		
		Если ИнформационнаяБазаФайловая Тогда
			СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ(МассивОбъектов);
		Иначе
			МассивПотоков = Новый Массив;
			Если КоличествоПотоков = 1 Тогда
				МассивПотоков.Добавить(МассивОбъектов);
			Иначе
				ОбъектовВПотоке = Окр(МассивОбъектов.Количество() / КоличествоПотоков,0,РежимОкругления.Окр15как10);
				ТекущееКоличество = 0;
				ОбъектыПотока = Новый Массив;
				Для Каждого текЭлемент Из МассивОбъектов Цикл
					ОбъектыПотока.Добавить(текЭлемент);
					ТекущееКоличество = ТекущееКоличество + 1;
					Если ТекущееКоличество >= ОбъектовВПотоке Тогда
						ТекущееКоличество = 0;
						МассивПотоков.Добавить(ОбъектыПотока);
						ОбъектыПотока = Новый Массив;
					КонецЕсли;
				КонецЦикла;
				// Допишем крайний поток ТС которые остались.
				Если ОбъектыПотока.Количество() Тогда
					ТекущийМассив = МассивПотоков[МассивПотоков.ВГраница()];
					Для Каждого текЭлемент Из ОбъектыПотока Цикл
						ТекущийМассив.Добавить(текЭлемент);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого текЭлемент Из МассивПотоков Цикл
				Если текЭлемент.Количество() Тогда
					мсвПараметров = Новый Массив(1);
					мсвПараметров[0] = текЭлемент;
					//упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ(текЭлемент);
					упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ", мсвПараметров,,,"ПолучениеДанныхОтScoutSOAP");
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПолучениеДанныхОтСКАУТ()

Функция СоздатьТаблицуДанныхТрековСКАУТ()
	
	ТаблицаЗначенийТреков = Новый ТаблицаЗначений;
	ТаблицаЗначенийТреков.Колонки.Добавить("Период"                  ,Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Период");
	ТаблицаЗначенийТреков.Колонки.Добавить("Высота"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8,3)), "Высота");
	ТаблицаЗначенийТреков.Колонки.Добавить("НаправлениеДвижения"     ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "НаправлениеДвижения");		
	ТаблицаЗначенийТреков.Колонки.Добавить("Широта"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ТаблицаЗначенийТреков.Колонки.Добавить("Долгота"                 ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	ТаблицаЗначенийТреков.Колонки.Добавить("Скорость"                ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "Скорость");
	
	Возврат ТаблицаЗначенийТреков;
	
КонецФункции // СоздатьТаблицуДанныхТрековСКАУТ()

Функция СоздатьТаблицуДатчиковСКАУТ() Экспорт
	
	ТаблицаЗначенийДатчиков = Новый ТаблицаЗначений;
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Трекер",Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"),"Трекер");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("КалибровочныйГрафик",Новый ОписаниеТипов("СправочникСсылка.упКалибровочныеГрафики"),"КалибровочныйГрафик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Датчик",Новый ОписаниеТипов("СправочникСсылка.упДатчики"),"Датчик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Рейс",Новый ОписаниеТипов("ДокументСсылка.упРейс"),"Рейс");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Значение",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Значение");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Назначение",Новый ОписаниеТипов("СправочникСсылка.упНазначенияДатчиков"),"Назначение");
	
	Возврат ТаблицаЗначенийДатчиков;
	
КонецФункции // СоздатьТаблицуДатчиковСКАУТ()

Функция СоздатьТаблицуДанныхЗаправкиСливыСКАУТ()
	
	ТаблицаЗначенийЗаправкиСливы = Новый ТаблицаЗначений;
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("НачалоПериода", Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "НачалоПериода");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("КонецПериода", Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "КонецПериода");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("ТипСобытия", Новый ОписаниеТипов("СправочникСсылка.упТипыСобытийМониторинга"), "ТипСобытия");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("НачОстаток", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12,3)), "НачОстаток");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("КонОстаток", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12,3)), "КонОстаток");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Широта" , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Долгота", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	
	Возврат ТаблицаЗначенийЗаправкиСливы;
	
КонецФункции // СоздатьТаблицуДанныхЗаправкиСливыСКАУТ()

Процедура ДобавитьСтрокаДанныхДатчика(ТаблицаДатчика, Период, Трекер, КалибровочныйГрафик, ДатчикТоплива, Значение, Назначение, ТранспортноеСредство, Рейс) Экспорт
	
	НовЗаписьДатчики = ТаблицаДатчика.Добавить();
	НовЗаписьДатчики.Период   			 = Период;
	НовЗаписьДатчики.Трекер 			 = Трекер;
	НовЗаписьДатчики.КалибровочныйГрафик = КалибровочныйГрафик;
	НовЗаписьДатчики.Датчик   			 = ДатчикТоплива;
	НовЗаписьДатчики.Значение 			 = Значение;
	НовЗаписьДатчики.Назначение			 = Назначение;
	НовЗаписьДатчики.ТранспортноеСредство = ТранспортноеСредство;
	НовЗаписьДатчики.Рейс			     = Рейс;
	
КонецПроцедуры // ДобавитьСтрокаДанныхДатчика()

// Выполним создание разных потоков обработки.
//
// Параметры:
//  ДанныеОбработки  - Массив - Данные потока.
//
Процедура СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ(ДанныеОбработки) Экспорт
	
	Для Каждого текЭлемент Из ДанныеОбработки Цикл
		ВыполнитьОбработкуЗагруженныхДанныхСКАУТ(текЭлемент);
	КонецЦикла;
	
КонецПроцедуры // СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ()

// Выполним обработку каждого объекта.
//
//
// Параметры:
//  ДанныеОбработки - Массив - Текщий элемент данных.
//
Процедура ВыполнитьОбработкуЗагруженныхДанныхСКАУТ(ДанныеОбработки)
	
	ТекущийТерминал = ДанныеОбработки.Трекер;
	ТаблицаЗначенийДатчиков = Неопределено;
	ТекущиеПараметрыЛогирования = Неопределено;
	НачалоПериодаОбработки = ТекущаяДатаСеанса();
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда
		ТекущиеПараметрыЛогирования = Новый Структура();
		ТекущиеПараметрыЛогирования.Вставить("КаталогЛогТрек",ДанныеОбработки.КаталогЛогТрек);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода",ДанныеОбработки.НачалоПериода);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода_Датчика",ДанныеОбработки.НачалоПериода_Датчика);
		ТекущиеПараметрыЛогирования.Вставить("КонецПериода",ДанныеОбработки.КонецПериода);
	КонецЕсли;
	Если ДанныеОбработки.Свойство("Датчики") Тогда
		ТаблицаЗначенийДатчиков = ДанныеОбработки.Датчики;
	КонецЕсли;
	Если ДанныеОбработки.Свойство("Навигация") И НЕ ДанныеОбработки.Навигация = Неопределено Тогда
		РазобратьТрекиScoutSOAP(ДанныеОбработки.Навигация,ТекущийТерминал,ТаблицаЗначенийДатчиков,ТекущиеПараметрыЛогирования);
	КонецЕсли;
	Если ДанныеОбработки.Свойство("СобытияМониторинга") Тогда
		СобытияМониторинга = ДанныеОбработки.СобытияМониторинга;
		ДатчикТоплива = ДанныеОбработки.ДатчикТоплива;
		Назначение = ДанныеОбработки.Назначение;
		КалибровочныйГрафик = ДанныеОбработки.КалибровочныйГрафик;
		НачалоПериода = ДанныеОбработки.ПериодСобытияМониторинга.НачалоПериода;
		КонецПериода = ДанныеОбработки.ПериодСобытияМониторинга.КонецПериода;
		ТаблицаЗначенийДатчиков = РазобратьСобытияМониторингаScoutSOAP(СобытияМониторинга, ТекущийТерминал, ДатчикТоплива, Назначение, КалибровочныйГрафик, НачалоПериода, КонецПериода);
	КонецЕсли;
	Если НЕ ТаблицаЗначенийДатчиков = Неопределено И ТаблицаЗначенийДатчиков.Количество() Тогда
		соотТрекеров = Новый Соответствие;
		ПустаяДата = Дата("00010101000000");
		Для каждого ТекущаяСтрока Из ТаблицаЗначенийДатчиков Цикл
			Если ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			текДанныеТС = соотТрекеров[ТекущийТерминал];
			Период = ТекущаяСтрока.Период;
			Если текДанныеТС = Неопределено
				ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство)
				ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения)
				ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения 
				ИЛИ текДанныеТС.ОкончаниеВыполнения = ПустаяДата) Тогда 
				текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
				соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			ТекущаяСтрока.ТранспортноеСредство = текДанныеТС.ТранспортноеСредство;
			ТекущаяСтрока.Рейс                 = текДанныеТС.Рейс;
		КонецЦикла;
		ОтборТС = Новый Структура("ТранспортноеСредство", ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка"));
		НайднныеСтроки = ТаблицаЗначенийДатчиков.НайтиСтроки(ОтборТС);
		Если НайднныеСтроки.Количество() Тогда
			Для каждого ТекущаяСтрока Из НайднныеСтроки Цикл
				ТаблицаЗначенийДатчиков.Удалить(ТекущаяСтрока);
			КонецЦикла;
		КонецЕсли;
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначенийДатчиков);
			Запрос.Выполнить();
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Период КАК ДАТА) КАК Период,
			|	ВЫРАЗИТЬ(вртДанных.Трекер КАК Справочник.упМобильныеУстройстваИТрекеры) КАК Трекер,
			|	ВЫРАЗИТЬ(вртДанных.КалибровочныйГрафик КАК Справочник.упКалибровочныеГрафики) КАК КалибровочныйГрафик,
			|	ВЫРАЗИТЬ(вртДанных.Датчик КАК Справочник.упДатчики) КАК Датчик,
			|	ВЫРАЗИТЬ(вртДанных.ТранспортноеСредство КАК Справочник.упТранспортныеСредства) КАК ТранспортноеСредство,
			|	ВЫРАЗИТЬ(вртДанных.Рейс КАК Документ.упРейс) КАК Рейс,
			|	ВЫРАЗИТЬ(вртДанных.Значение КАК ЧИСЛО(10, 3)) КАК Значение,
			|	ВЫРАЗИТЬ(вртДанных.Назначение КАК Справочник.упНазначенияДатчиков) КАК Назначение
			|ПОМЕСТИТЬ втДанные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втДанные.Период,
			|	втДанные.Трекер КАК Трекер,
			|	втДанные.КалибровочныйГрафик,
			|	втДанные.Датчик КАК Датчик,
			|	втДанные.ТранспортноеСредство КАК ТранспортноеСредство,
			|	втДанные.Рейс,
			|	втДанные.Значение,
			|	втДанные.Назначение
			|ИЗ
			|	втДанные КАК втДанные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков КАК упДанныеДатчиков
			|		ПО втДанные.Период = упДанныеДатчиков.Период
			|			И втДанные.Трекер = упДанныеДатчиков.Трекер
			|			И втДанные.Датчик = упДанныеДатчиков.Датчик
			|			И втДанные.ТранспортноеСредство = упДанныеДатчиков.ТранспортноеСредство
			|			И втДанные.Значение = упДанныеДатчиков.Значение
			|ГДЕ
			|	упДанныеДатчиков.ТранспортноеСредство ЕСТЬ NULL И (НЕ втДанные.Датчик=ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка) И НЕ втДанные.ТранспортноеСредство=ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Трекер, ТранспортноеСредство, Датчик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ";
			Выборка = Запрос.Выполнить();
			тбзКопия = Неопределено;
			Если НЕ Выборка.Пустой() Тогда
				тбзКопия = Выборка.Выгрузить();
			КонецЕсли;
			Запрос.МенеджерВременныхТаблиц.Закрыть();
			Если ТипЗнч(тбзКопия) = ТИП("ТаблицаЗначений") И тбзКопия.Количество() Тогда
				упИнтеграцияССерверамиСбораДанных.ВыполнитьОбработкуЗначенийДатчиковНабор(тбзКопия);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда
		Если НЕ ПустаяСтрока(ДанныеОбработки.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ДанныеОбработки.КаталогЛогТрек + "\Данные продолжительности записи данных в базу.txt";
			
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			
			ТекстовыйДокумент.ДобавитьСтроку("Начало периода записи: " + Строка(НачалоПериодаОбработки));
			ТекстовыйДокумент.ДобавитьСтроку("Конец периода записи: " + Строка(ТекущаяДатаСеанса()));
			
			ТекстовыйДокумент.Записать(ИмяФайлаЗаписи);
			ТекстовыйДокумент = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбработкуЗагруженныхДанныхСКАУТ()

Функция РазобратьСобытияМониторингаScoutSOAP(СобытияМониторинга, ТекущийТерминал, ДатчикТоплива, Назначение, КалибровочныйГрафик, НачалоПериода, КонецПериода)

	ТаблицаЗначенийДатчиков = СоздатьТаблицуДатчиковСКАУТ();
	Комментарий = СтрШаблон("# Данные получены от ScoutSOAP FuelingDefuelingStatisticsService за период с %1 по %2", НачалоПериода, КонецПериода);
	ТаблицаЗначенийЗаправкиСливы = Новый ТаблицаЗначений;
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Дата");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("КонечнаяДата", Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "КонечнаяДата");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("ТипСобытия", Новый ОписаниеТипов("СправочникСсылка.упТипыСобытийМониторинга"), "ТипСобытия");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("НачОстаток", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12,3)), "НачОстаток");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("КонОстаток", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(12,3)), "КонОстаток");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Широта" , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Долгота", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Трекер", Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"), "Трекер");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
	ТаблицаЗначенийЗаправкиСливы.Колонки.Добавить("Рейс", Новый ОписаниеТипов("ДокументСсылка.упРейс"), "Рейс");
	
	соотТрекеров = Новый Соответствие;	
	
	Для каждого ОтрезокДанных Из СобытияМониторинга Цикл
		ТаблицаЗначений = ОтрезокДанных.Значение;
		Для каждого ТекущаяСтрокаЗаправкиСливы Из ТаблицаЗначений Цикл
			Период = ТекущаяСтрокаЗаправкиСливы.НачалоПериода;
			текДанныеТС = соотТрекеров[ТекущийТерминал];
			Если текДанныеТС = Неопределено 
				ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) 
				ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) 
				ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения 
				ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
				текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
				соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекущаяСтрокаЗаправкиСливы.ТипСобытия) Тогда
				НоваяЗаписьЗаправкиСливы = ТаблицаЗначенийЗаправкиСливы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗаписьЗаправкиСливы, ТекущаяСтрокаЗаправкиСливы);
				НоваяЗаписьЗаправкиСливы.Дата = ТекущаяСтрокаЗаправкиСливы.НачалоПериода;
				НоваяЗаписьЗаправкиСливы.КонечнаяДата = ТекущаяСтрокаЗаправкиСливы.КонецПериода;
				НоваяЗаписьЗаправкиСливы.ТранспортноеСредство 	 = текДанныеТС.ТранспортноеСредство;
				НоваяЗаписьЗаправкиСливы.Трекер 				 = ТекущийТерминал;
				НоваяЗаписьЗаправкиСливы.Рейс                    = текДанныеТС.Рейс;
			КонецЕсли;
			ДобавитьСтрокаДанныхДатчика(ТаблицаЗначенийДатчиков, ТекущаяСтрокаЗаправкиСливы.НачалоПериода, ТекущийТерминал, КалибровочныйГрафик, ДатчикТоплива, ТекущаяСтрокаЗаправкиСливы.НачОстаток, Назначение, текДанныеТС.ТранспортноеСредство, текДанныеТС.Рейс);
			ДобавитьСтрокаДанныхДатчика(ТаблицаЗначенийДатчиков, ТекущаяСтрокаЗаправкиСливы.КонецПериода, ТекущийТерминал, КалибровочныйГрафик, ДатчикТоплива, ТекущаяСтрокаЗаправкиСливы.КонОстаток, Назначение, текДанныеТС.ТранспортноеСредство, текДанныеТС.Рейс);
			
		КонецЦикла;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		КопияТС = ТаблицаЗначенийЗаправкиСливы.Скопировать(,"ТранспортноеСредство");
		Если КопияТС.Количество() Тогда
			КопияТипСобытия = ТаблицаЗначенийЗаправкиСливы.Скопировать(,"ТипСобытия");
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДатыСобытий");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = КопияТС;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТранспортноеСредство", "ТранспортноеСредство");
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДатыСобытий");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = КопияТипСобытия;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипСобытия", "ТипСобытия");
			БлокировкаДанных.Заблокировать();
		КонецЕсли;
		
		Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначенийЗаправкиСливы);
			Запрос.Выполнить();
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Дата КАК ДАТА) КАК Дата,
			|	ВЫРАЗИТЬ(вртДанных.КонечнаяДата КАК ДАТА) КАК КонечнаяДата,
			|	ВЫРАЗИТЬ(вртДанных.ТипСобытия КАК Справочник.упТипыСобытийМониторинга) КАК ТипСобытия,
			|	ВЫРАЗИТЬ(вртДанных.НачОстаток КАК ЧИСЛО(12, 3)) КАК НачОстаток,
			|	ВЫРАЗИТЬ(вртДанных.КонОстаток КАК ЧИСЛО(12, 3)) КАК КонОстаток,
			|	ВЫРАЗИТЬ(вртДанных.Широта КАК ЧИСЛО(9, 6)) КАК Широта,
			|	ВЫРАЗИТЬ(вртДанных.Долгота КАК ЧИСЛО(9, 6)) КАК Долгота,
			|	ВЫРАЗИТЬ(вртДанных.Трекер КАК Справочник.упМобильныеУстройстваИТрекеры) КАК Трекер,
			|	ВЫРАЗИТЬ(вртДанных.ТранспортноеСредство КАК Справочник.упТранспортныеСредства) КАК ТранспортноеСредство,
			|	ВЫРАЗИТЬ(вртДанных.Рейс КАК Документ.упРейс) КАК Рейс
			|ПОМЕСТИТЬ вт_Данные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	вт_Данные.Дата КАК Дата,
			|	вт_Данные.КонечнаяДата КАК КонечнаяДата,
			|	вт_Данные.ТипСобытия КАК ТипСобытия,
			|	вт_Данные.НачОстаток КАК НачОстаток,
			|	вт_Данные.КонОстаток КАК КонОстаток,
			|	вт_Данные.Широта КАК Широта,
			|	вт_Данные.Долгота КАК Долгота,
			|	вт_Данные.Трекер КАК Трекер,
			|	вт_Данные.ТранспортноеСредство КАК ТранспортноеСредство,
			|	вт_Данные.Рейс КАК Рейс
			|ИЗ
			|	вт_Данные КАК вт_Данные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДатыСобытий КАК упДатыСобытий
			|		ПО вт_Данные.Дата = упДатыСобытий.Период
			|			И вт_Данные.ТипСобытия = упДатыСобытий.ТипСобытия
			|			И вт_Данные.ТранспортноеСредство = упДатыСобытий.ТранспортноеСредство
			|ГДЕ
			|	упДатыСобытий.Событие ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ"; 
			РезультатЗапроса = Запрос.Выполнить();
			Если НЕ РезультатЗапроса.Пустой() Тогда
				ДатаЗаписи = ТекущаяДатаСеанса();
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					НовоеСобытие = Документы.упСобытиеМониторинга.СоздатьДокумент();
					ЗаполнитьЗначенияСвойств(НовоеСобытие, Выборка);
					НовоеСобытие.ДатаЗаписи = ДатаЗаписи;
					НовоеСобытие.Комментарий = Комментарий;
					НовоеСобытие.Записать(РежимЗаписиДокумента.Проведение);
				КонецЦикла;
			КонецЕсли;
			Запрос.МенеджерВременныхТаблиц.Закрыть();
		
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ЗаписьДанныхСобытияМониторинга.СКАУТ", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упСобытиеМониторинга, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат ТаблицаЗначенийДатчиков;

КонецФункции // РазобратьСобытияМониторингаScoutSOAP()

// Выполнить обход полученных данных.
//
// Параметры:
//  СписокТС  - Массив - Список ТС с данными.
//  ТекущийТерминал  - Справочник.упМобильныеУстройстваИТрекеры - Ссылка на терминал.
//  ДанныеДатчиков  - ТаблицаЗначений - Таблица с данными датчиков.
//  ПараметрыЛогирования - Структура - Параметры для записи лога получения данных.
//
Процедура РазобратьТрекиScoutSOAP(СписокТС, ТекущийТерминал, ДанныеДатчиков = Неопределено,ПараметрыЛогирования=Неопределено)
	
	ТаблицаЗначенийТреков = Новый ТаблицаЗначений;
	ТаблицаЗначенийТреков.Колонки.Добавить("Период"                  ,Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Период");
	ТаблицаЗначенийТреков.Колонки.Добавить("ТранспортноеСредство"    ,Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"), "ТранспортноеСредство");
	ТаблицаЗначенийТреков.Колонки.Добавить("Высота"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8,3)), "Высота");
	ТаблицаЗначенийТреков.Колонки.Добавить("НаправлениеДвижения"     ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "НаправлениеДвижения");		
	ТаблицаЗначенийТреков.Колонки.Добавить("Широта"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ТаблицаЗначенийТреков.Колонки.Добавить("Долгота"                 ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	ТаблицаЗначенийТреков.Колонки.Добавить("ПредставлениеКоординаты" ,Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийТреков.Колонки.Добавить("Скорость"                ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "Скорость");
	ТаблицаЗначенийТреков.Колонки.Добавить("Трекер"                  ,Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"), "Трекер");
	ТаблицаЗначенийТреков.Колонки.Добавить("Рейс"                    ,Новый ОписаниеТипов("ДокументСсылка.упРейс"), "Рейс");
	
	соотТрекеров = Новый Соответствие;
	соотТрекеровПериод = Новый Соответствие;
	ВыполнитьЗаписьДанныхДатчиков = НЕ ДанныеДатчиков = Неопределено;
	Для каждого ОтрезокДанных Из СписокТС Цикл
		ТаблицаЗначений = ОтрезокДанных.Значение;
		Для каждого ТекущаяСтрокаТрека Из ТаблицаЗначений Цикл
			Период = ТекущаяСтрокаТрека.Период;
			ПроверкаПериода = соотТрекеровПериод.Получить(Период);
			Если НЕ ПроверкаПериода=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			текДанныеТС = соотТрекеров[ТекущийТерминал];
			Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
				текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
				соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗаписьТрекера, ТекущаяСтрокаТрека);
			НовЗаписьТрекера.ПредставлениеКоординаты = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеКоординаты(НовЗаписьТрекера.Широта, НовЗаписьТрекера.Долгота);
			НовЗаписьТрекера.ТранспортноеСредство 	 = текДанныеТС.ТранспортноеСредство; 
			НовЗаписьТрекера.Трекер 				 = ТекущийТерминал;
			НовЗаписьТрекера.Рейс                    = текДанныеТС.Рейс;
			соотТрекеровПериод.Вставить(Период, Истина);
		КонецЦикла;
	КонецЦикла;
	соотТрекеровПериод.Очистить();
	ТаблицаЗначенийТреков.Сортировать("Период");
	
	Если НЕ ПараметрыЛогирования = Неопределено Тогда
		Если НЕ ПустаяСтрока(ПараметрыЛогирования.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ПараметрыЛогирования.КаталогЛогТрек + "\ТаблицаЗначенийТреков_" + Строка(ТекущийТерминал) + "_" + ПараметрыЛогирования.НачалоПериода + "_" + ПараметрыЛогирования.КонецПериода + ".txt";		
			//ВыполнитьЗаписьТаблицыЗначенийВФайл(ИмяФайлаЗаписи,ТаблицаЗначенийТреков);		
		КонецЕсли;
	КонецЕсли;
	
	Период = ДАТА("00010101000000");
	
	//ПроверитьЗаполнениеДанныхНаправленияДвижения(ТаблицаЗначенийТреков);
	Попытка
		ТекущееТранспортноеСредство=Неопределено;
		НаборЗаписей = РегистрыСведений.упДанныеТрекеров.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		Для каждого тбзСтрока Из ТаблицаЗначенийТреков Цикл
			Если НЕ ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство Тогда
				ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство;
				Если НаборЗаписей.Количество() Тогда
					Попытка
						НаборЗаписей.Записать(Ложь);
					Исключение
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
						КонецЕсли;
						ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.СКАУТ", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;
					Если ТранзакцияАктивна() Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;
					НаборЗаписей.Очистить();
				КонецЕсли;
				Если НЕ ТранзакцияАктивна() Тогда
					НачатьТранзакцию();
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеТрекеров");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТекущееТранспортноеСредство);
					БлокировкаДанных.Заблокировать();
				КонецЕсли;
				НаборЗаписей.Отбор.ТранспортноеСредство.Установить(ТекущееТранспортноеСредство, Истина);
			КонецЕсли;
			НовЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗапись,тбзСтрока);
		КонецЦикла;
		Если НаборЗаписей.Количество() Тогда
			Попытка
				НаборЗаписей.Записать(Ложь);
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.СКАУТ", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.СКАУТ", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ТаблицаЗначенийТреков = Неопределено;

КонецПроцедуры // РазобратьТрекиScoutSOAP()

// Аутентификация выполняется вызовом метода Login, который возвращает токен.
//
// Параметры:
//  WSПрокси  - WSПрокси - Клиентский прокси для вызова веб-сервиса.
//  Логин  - Строка - Имя пользователя сервиса.
//  Пароль  - Строка - Пароль пользователя сервиса.
//  АдресРесурса  - Строка - адрес доступа к сервису.
//  Порт  - Число - Номер порта.
//
// Возвращаемое значение:
//   Строка - Представление токена доступа.
//
Функция ПолучитьТокенScoutSOAP(WSПрокси, Логин, Пароль, АдресРесурса, Порт) Экспорт
	
	ПространствоИмен = "http://tempuri.org/";
	ТекстПодключения = "http://%1:%2/spic/auth/soap?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресРесурса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "SpicAuthorizationService";
	ИмяТочкиПодключения = "BasicHttpBinding_ISpicAuthorizationSoapService";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		ПроксиАвторизация = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;
	WSПрокси = ПроксиАвторизация;
	ПространствоИмен = "http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Authorization";
	SpicAuthorizationRequest = ПроксиАвторизация.ФабрикаXDTO.Создать(ПроксиАвторизация.ФабрикаXDTO.Тип(ПространствоИмен, "SpicAuthorizationRequest"));
	
	SpicAuthorizationRequest.CultureName = "ru-ru";
	SpicAuthorizationRequest.Login = Логин;
	SpicAuthorizationRequest.Password = Пароль;
	SpicAuthorizationRequest.TimeStampUtc = ТекущаяДата();
	SpicAuthorizationRequest.TimeZoneOlsonId = "Europe/Moscow";
	SpicAuthorizationRequest.UiCultureName= "ru-ru";
	
	Попытка
		SpicAuthorizationResponse = ПроксиАвторизация.Login(SpicAuthorizationRequest);
		SessionId = SpicAuthorizationResponse.SessionId;
		ПроксиАвторизация.Пользователь = SessionId;
	Исключение
		ТекстСообщения = СтрШаблон("Авторизация: Не удалось вызвать Login %1", ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка, , ,ТекстСообщения);
		ПроксиАвторизация= Неопределено;
		Возврат "";  
	КонецПопытки;
	
	Возврат SessionId;
		
КонецФункции // ПолучитьТокенScoutSOAP()

// Выполним обращение к сервису СКАУТ получим таблицу всех устройств.
//
// Параметры:
//  ИдентификаторТС - Строка - Идентификатор устройства на сервере.
//  НачалоПериода - Дата - Начало периода получения данных.
//  КонецПериода - Дата - Конец периода получения данных.
//  АдресСервиса - Строка - Адрес сервиса.
//  Порт - Число - Номер порта для подключения к сервису.
//  Токен - Строка - токен подключения.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьОнлайнДанныеScoutSOAP(ИдентификаторТС, НачалоПериода, КонецПериода, АдресСервиса, Порт, Токен)
	
	ПространствоИмен = "http://tempuri.org/";
	ТекстПодключения = "http://%1:%2/spic/StatisticsController/soap?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресСервиса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "SpicStatisticsControllerService";
	ИмяТочкиПодключения = "BasicHttpBinding_ISpicSoapStatisticsControllerService";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		Прокси = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;
	
	Прокси.Пользователь = Токен;
		
    ПространствоИмен = "http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Statistics.Controller";
	
	DateTimeRange = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Infrastructure.Data","SpicDateTimeRange"));
	DateTimeRange.Begin = НачалоПериода;
	DateTimeRange.End = КонецПериода;

	ObjectIdentity = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Infrastructure.Data","SpicObjectIdentity"));
	// Параметр ObjectTypeId может принимать следующие значения:
	// •	0F1E3A4A-88F5-4166-9BE8-76033DD85D08 (транспортное средство)
	// •	0783BE26-6398-480C-A88F-871438A01C36 (терминал)
	// •	54E3C5C5-7EFE-49B9-AE0E-F8C44D52FA36 (профиль терминала)
	ObjectIdentity.ObjectId = ИдентификаторТС;
	ObjectIdentity.ObjectTypeId = "0F1E3A4A-88F5-4166-9BE8-76033DD85D08";
	
	SpicStatisticsSessionRequest = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип(ПространствоИмен,"SpicStatisticsSessionRequest"));				
	SpicStatisticsSessionRequest.Period = DateTimeRange;     
	SpicStatisticsSessionRequest.TargetObject = ObjectIdentity;
		
	SpicStatisticsSessionResponse = Прокси.StartStatisticsSession(SpicStatisticsSessionRequest).Session;
	
	ПространствоИмен = "http://tempuri.org/";
	ТекстПодключения = "http://%1:%2/spic/NavigationValidation/soap?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресСервиса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "SpicNavigationValidationStatisticsService";
	ИмяТочкиПодключения = "BasicHttpBinding_ISpicSoapNavigationValidationStatisticsService";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		Прокси_NavigationValidation = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;
	
    Прокси_NavigationValidation.Пользователь = Токен;
	ПространствоИменSession = "http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Statistics.Controller";
	SpicStatisticsSession = Прокси_NavigationValidation.ФабрикаXDTO.Создать(Прокси_NavigationValidation.ФабрикаXDTO.Тип(ПространствоИменSession,"SpicStatisticsSession"));
	SpicStatisticsSession.StatisticsSessionId = SpicStatisticsSessionResponse.StatisticsSessionId; 
	// добавляем запрос на построение статистики
    Прокси_NavigationValidation.AddStatisticsRequest(SpicStatisticsSession);
	
	statisticsArray = Неопределено;
	ВремяНачалоЗапуска = ТекущаяДатаСеанса();
	// запускаем построение
	Прокси.StartBuild(SpicStatisticsSessionResponse);
	ВремяЗавершенияЗапуска = ТекущаяДатаСеанса();
	Если (ВремяЗавершенияЗапуска-ВремяНачалоЗапуска)< 10 Тогда
		// Когда время больше 10 сек значит сервис завис и не стоит ждать ответа.
		Сч = 0;
		statisticsArray = Новый СписокЗначений;
		Пока Истина Цикл
			Для Счч = 0 По 10 Цикл
				statisticsResponse = Прокси_NavigationValidation.GetStatistics(SpicStatisticsSession);
				Если  statisticsResponse.ChunkInfo.Status.Value = "Ok" Тогда //  Processing
					ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековСКАУТ();
					ОбъектXDTO = statisticsResponse.Statistics;
					Если НЕ ОбъектXDTO.Свойства().Получить("Points") = Неопределено Тогда
						SpicNavigationPoint = ОбъектXDTO.Points.ПолучитьСписок(ОбъектXDTO.Points.Свойства().Получить("SpicNavigationPoint"));
						Если НЕ SpicNavigationPoint = Неопределено Тогда
							Количество = SpicNavigationPoint.Количество();
							Для Сч = 0 По Количество - 1 Цикл 
								ДанныеПоТС = SpicNavigationPoint.Получить(Сч);
								Если НЕ ДанныеПоТС.IsNavigationValid Тогда
									Продолжить;
								КонецЕсли;
								НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
								
								НовЗаписьТрекера.Период   			 = ДанныеПоТС.Timestamp;
								НовЗаписьТрекера.Широта              = ДанныеПоТС.Navigation.Location.Latitude;
								НовЗаписьТрекера.Долгота             = ДанныеПоТС.Navigation.Location.Longitude;
								НовЗаписьТрекера.Скорость            = ДанныеПоТС.Navigation.Speed;
								НовЗаписьТрекера.Высота              = ДанныеПоТС.Navigation.AltitudeMeters;
								НовЗаписьТрекера.НаправлениеДвижения = ДанныеПоТС.Navigation.Angle;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
					Если ТаблицаЗначенийТреков.Количество() Тогда
						statisticsArray.Добавить(ТаблицаЗначенийТреков);
					КонецЕсли;
					Прервать;
				ИначеЕсли  statisticsResponse.ChunkInfo.Status.Value = "Error" Тогда 
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если statisticsResponse.ChunkInfo.Status.Value = "Ok" Тогда
				Сч = 0;
			Иначе
				Сч = Сч + 1;
			КонецЕсли;
			
			Если Сч >=10 Тогда
				// Когда не может выполнить расчет более 10 раз подряд.
				Прервать;
			КонецЕсли;
			
			Прокси.BuildNextChunk(SpicStatisticsSessionResponse);
			Если statisticsResponse.ChunkInfo.IsFinalChunk Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Прокси.StopStatisticsSession(SpicStatisticsSessionResponse);
	Прокси = Неопределено;Прокси_NavigationValidation = Неопределено;
	Если ТипЗнч(statisticsArray) = ТИП("СписокЗначений") Тогда
		Если statisticsArray.Количество()=0 Тогда
			statisticsArray = Неопределено;
		КонецЕсли;
	КонецЕсли;
	Возврат  statisticsArray;
	
КонецФункции // ПолучитьОнлайнДанныеScoutSOAP()

// Выполним обращение к сервису СКАУТ получим значение уровня топлива на начало укзанного перода.
//
// Параметры:
//  ИдентификаторТС  - Строка - идентификатор трекера.
//  НачалоПериода  - Дата - Начало периода.
//  КонецПериода  - Дата - Конец периода.
//  АдресСервиса  - Строка - Адрес сервиса.
//  Порт  - Число - Номер порта для подключения к сервису.
//  Токен  - Строка - токен подключения.
//  Ошибка  - Булево - Флаг указывает что есть ошибка обращения к сервису.
//
// Возвращаемое значение:
//   Значение уровня топлива - Число.
//
Функция ПолучитьДанныеТопливаЗаПериодScoutSOAP(ИдентификаторТС, НачалоПериода, КонецПериода, АдресСервиса, Порт, Токен, Ошибка) Экспорт
	
	ПространствоИмен = "http://tempuri.org/";
	ТекстПодключения = "http://%1:%2/spic/StatisticsController/soap?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресСервиса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "SpicStatisticsControllerService";
	ИмяТочкиПодключения = "BasicHttpBinding_ISpicSoapStatisticsControllerService";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		Прокси = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		Ошибка = Истина;
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;
	
	Прокси.Пользователь = Токен;
		
	ПространствоИмен = "http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Statistics.Controller";
	
	DateTimeRange = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Infrastructure.Data","SpicDateTimeRange"));
	DateTimeRange.Begin = НачалоПериода;
	DateTimeRange.End = КонецПериода;
	
	ObjectIdentity = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Infrastructure.Data","SpicObjectIdentity"));
	//Параметр ObjectTypeId может принимать следующие значения:
	//•	0F1E3A4A-88F5-4166-9BE8-76033DD85D08 (транспортное средство)
	//•	0783BE26-6398-480C-A88F-871438A01C36 (терминал)
	//•	54E3C5C5-7EFE-49B9-AE0E-F8C44D52FA36 (профиль терминала)
	ObjectIdentity.ObjectId = ИдентификаторТС;
	ObjectIdentity.ObjectTypeId = "0F1E3A4A-88F5-4166-9BE8-76033DD85D08";
	
	SpicStatisticsSessionRequest = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип(ПространствоИмен,"SpicStatisticsSessionRequest"));				
	SpicStatisticsSessionRequest.Period = DateTimeRange;     
	SpicStatisticsSessionRequest.TargetObject = ObjectIdentity;
	
	SpicStatisticsSessionResponse = Прокси.StartStatisticsSession(SpicStatisticsSessionRequest).Session;
	
	ПространствоИмен = "http://tempuri.org/";
	ТекстПодключения = "http://%1:%2/spic/fdstat/soap?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресСервиса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "SpicFuelingDefuelingStatisticsService";
	ИмяТочкиПодключения = "BasicHttpBinding_ISpicSoapFuelingDefuelingStatisticsService";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		Прокси_Fdstat = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		Ошибка = Истина;
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтScoutSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;	
		
    Прокси_Fdstat.Пользователь = Токен;
	ПространствоИменSession = "http://schemas.datacontract.org/2004/07/Scout.Plugins.Spic.Server.Statistics.Controller";
	SpicStatisticsSession = Прокси_Fdstat.ФабрикаXDTO.Создать(Прокси_Fdstat.ФабрикаXDTO.Тип(ПространствоИменSession,"SpicStatisticsSession"));
	SpicStatisticsSession.StatisticsSessionId = SpicStatisticsSessionResponse.StatisticsSessionId; 
	// добавляем запрос на построение статистики
	Прокси_Fdstat.AddStatisticsRequest(SpicStatisticsSession);
	statisticsArray = Неопределено;
	ВремяНачалаЗапуска = ТекущаяДатаСеанса();
	// запускаем построение
	Прокси.StartBuild(SpicStatisticsSessionResponse);
	ВремяЗавершенияЗапуска = ТекущаяДатаСеанса();
	Если  (ВремяЗавершенияЗапуска-ВремяНачалаЗапуска)>10 Тогда
		//выплнение расчетов выполняет очень долго не будем получать данные.
		Ошибка = Истина;
	Иначе
		Сч = 0;
		statisticsArray = Новый СписокЗначений;
		ВыполнениеЗависло = Ложь;
		ИдентификаторЧасовогоПояса = ПолучитьЧасовойПоясИнформационнойБазы();
		Пока Истина Цикл
			Для Счч = 0 По 10 Цикл
				Если  Счч>=10 Тогда
					ВыполнениеЗависло = Истина;
				КонецЕсли;
				statisticsResponse = Прокси_Fdstat.GetStatistics(SpicStatisticsSession);
				Если  statisticsResponse.ChunkInfo.Status.Value = "Ok" Тогда //  Processing
					ТаблицаЗначенийЗаправкиСливы = СоздатьТаблицуДанныхЗаправкиСливыСКАУТ();
					// Укажем состояние уровня на период
					НоваяЗаписьЗаправкиСливы = ТаблицаЗначенийЗаправкиСливы.Добавить();
					НоваяЗаписьЗаправкиСливы.НачалоПериода = МестноеВремя(statisticsResponse.ChunkInfo.Period.Begin, ИдентификаторЧасовогоПояса);
					НоваяЗаписьЗаправкиСливы.КонецПериода = МестноеВремя(statisticsResponse.ChunkInfo.Period.End, ИдентификаторЧасовогоПояса);
					НоваяЗаписьЗаправкиСливы.Широта = 0;
					НоваяЗаписьЗаправкиСливы.Долгота = 0;
					НоваяЗаписьЗаправкиСливы.НачОстаток = statisticsResponse.Statistics.BeginFuelVolumeL;
					НоваяЗаписьЗаправкиСливы.КонОстаток = statisticsResponse.Statistics.EndFuelVolumeL;
					НоваяЗаписьЗаправкиСливы.ТипСобытия = Справочники.упТипыСобытийМониторинга.ПустаяСсылка();
					Если statisticsResponse.Statistics.FuelingCount > 0
						ИЛИ statisticsResponse.Statistics.DefuelingCount > 0 Тогда // заправка, слив
						СобытияЗаправкиСливы = statisticsResponse.Statistics.Events.SpicFuelingDefuelingStatisticsEvent;
						Для каждого ТекущееСобытие Из СобытияЗаправкиСливы Цикл
							НоваяЗаписьЗаправкиСливы = ТаблицаЗначенийЗаправкиСливы.Добавить();
							НоваяЗаписьЗаправкиСливы.НачалоПериода = МестноеВремя(ТекущееСобытие.Period.Begin, ИдентификаторЧасовогоПояса);
							НоваяЗаписьЗаправкиСливы.КонецПериода = МестноеВремя(ТекущееСобытие.Period.End, ИдентификаторЧасовогоПояса);
							НоваяЗаписьЗаправкиСливы.Широта = ТекущееСобытие.Location.Latitude;
							НоваяЗаписьЗаправкиСливы.Долгота = ТекущееСобытие.Location.Longitude;
							НоваяЗаписьЗаправкиСливы.НачОстаток = ТекущееСобытие.BeginFuelVolumeL;
							НоваяЗаписьЗаправкиСливы.КонОстаток = ТекущееСобытие.EndFuelVolumeL;
							НоваяЗаписьЗаправкиСливы.ТипСобытия = Справочники.упТипыСобытийМониторинга.ПустаяСсылка();
							Если  ТекущееСобытие.EventType.Value = "Fueling" Тогда
								НоваяЗаписьЗаправкиСливы.ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗаправкаТоплива;
							ИначеЕсли  ТекущееСобытие.EventType.Value = "Defueling" Тогда
								НоваяЗаписьЗаправкиСливы.ТипСобытия = Справочники.упТипыСобытийМониторинга.СливТоплива;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					Если ТаблицаЗначенийЗаправкиСливы.Количество() Тогда
						statisticsArray.Добавить(ТаблицаЗначенийЗаправкиСливы);
					КонецЕсли;
					Прервать;
				ИначеЕсли  statisticsResponse.ChunkInfo.Status.Value = "Error" Тогда
					Ошибка = Истина;
				КонецЕсли;
			КонецЦикла;
			Если ВыполнениеЗависло Тогда
				Прервать;
			КонецЕсли;
			Если statisticsResponse.ChunkInfo.Status.Value = "Ok" Тогда
				Сч = 0;
			Иначе
				Сч = Сч + 1;
			КонецЕсли;
			Если Сч >=10 Тогда
				//Когда не может выполнить расчет более 3 раз подряд.
				Прервать;
			КонецЕсли;
			
			Прокси.BuildNextChunk(SpicStatisticsSessionResponse);
			Если statisticsResponse.ChunkInfo.IsFinalChunk Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Прокси.StopStatisticsSession(SpicStatisticsSessionResponse);
	Прокси = Неопределено; Прокси_Fdstat = Неопределено;
	Если ТипЗнч(statisticsArray) = ТИП("СписокЗначений") Тогда
		Если statisticsArray.Количество()=0 Тогда
			statisticsArray = Неопределено;
		КонецЕсли;
	КонецЕсли;
    Возврат  statisticsArray;
	
КонецФункции // ПолучитьДанныеТопливаЗаПериодScoutSOAP()

// Выполним обращение к сервису СКАУТ получим значение уровня топлива для списка ТС.
//
// Параметры:
//  СписокТС  - Массив - Список СправочникСсылка.упТранспортныеСредства.
//  Отказ  - Булево - Признак выполнения обращения.
//
// Возвращаемое значение:
//   Строка   - Описание ошибки получения данных.
//
Функция ПолучитьДанныеТопливаТранспортныхСредствScoutSOAP(СписокТС, Отказ) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ДатаОбращения = ТекущаяДатаСеанса();
	ТекстОшибки = "";
	ОписаниеОшибки = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивТранспортныхСредств", СписокТС);
	Запрос.Текст = "ВЫБРАТЬ
	|	упСервераСбораДанных.Тип КАК Тип,
	|	упСервераСбораДанных.Ссылка КАК Ссылка,
	|	упСервераСбораДанных.Сервер КАК Сервер,
	|	упСервераСбораДанных.Порт КАК Порт,
	|	упСервераСбораДанных.Пользователь КАК Пользователь,
	|	упСервераСбораДанных.Пароль КАК Пароль,
	|	упСервераСбораДанных.СобытияМониторинга КАК СобытияМониторинга,
	|	упСервераСбораДанных.ПериодОтсрочкиПолученияДанныхПоСобытиям КАК Отсрочка,
	|	упСервераСбораДанных.Схема КАК Схема
	|ИЗ
	|	Справочник.упСервераСбораДанных КАК упСервераСбораДанных
	|ГДЕ
	|	упСервераСбораДанных.Тип = ЗНАЧЕНИЕ(Перечисление.упТипСервераСбораДанных.СКАУТ)
	|	И НЕ упСервераСбораДанных.ПометкаУдаления
	|	И упСервераСбораДанных.Активен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упТранспортныеСредства.Ссылка КАК ТС,
	|	упТранспортныеСредства.ДатчикУровняТоплива КАК Датчик,
	|	упТранспортныеСредства.ДатчикУровняТоплива.Назначение КАК Назначение
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|ГДЕ
	|	упТранспортныеСредства.Ссылка В(&МассивТранспортныхСредств)
	|	И НЕ упТранспортныеСредства.ДатчикУровняТоплива = ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упТранспортныеСредства.Ссылка КАК ТС,
	|	упУстановленныеТрекерыСрезПоследних.Трекер КАК Трекер,
	|	упМоделиТрекеровДатчики.Датчик КАК Датчик,
	|	упМоделиТрекеровДатчики.КалибровочныйГрафик КАК КалибровочныйГрафик,
	|	упУстановленныеТрекерыСрезПоследних.Трекер.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка) КАК Рейс
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУстановленныеТрекеры.СрезПоследних КАК упУстановленныеТрекерыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упМоделиТрекеров.Датчики КАК упМоделиТрекеровДатчики
	|			ПО упУстановленныеТрекерыСрезПоследних.Трекер.Модель = упМоделиТрекеровДатчики.Ссылка
	|		ПО упТранспортныеСредства.Ссылка = упУстановленныеТрекерыСрезПоследних.ТранспортноеСредство
	|ГДЕ
	|	упТранспортныеСредства.Ссылка В(&МассивТранспортныхСредств)";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	Если ПакетЗапросов[0].Пустой() Тогда
		Отказ = Истина;	
		ТекстОшибки = НСтр("ru = 'Запрос значения уровня топлива поддерживает только сервис СКАУТ.'");
		ОписаниеОшибки.Добавить(ТекстОшибки);
	КонецЕсли;
	Если НЕ ПакетЗапросов[0].Пустой() И ПакетЗапросов[1].Пустой() Тогда
		Отказ = Истина;	
		ТекстОшибки = НСтр("ru = 'На выбранные транспортные средства не установлены датчики уровня топлива.'");
		ОписаниеОшибки.Добавить(ТекстОшибки);
	КонецЕсли;
		
	Если ОписаниеОшибки.Количество()=0 Тогда
		ВыборкаСерверов = ПакетЗапросов[0].Выбрать();
		Выборка = ПакетЗапросов[1].Выбрать();
		ТаблицаТрекеровТС = ПакетЗапросов[2].Выгрузить();
		ТаблицаЗначенийДатчиков = упИнтеграцияССерверамиСбораДанных.СоздатьТаблицуДатчиковСКАУТ();
		Пока ВыборкаСерверов.Следующий() Цикл
			Токен = "";
			ПроксиАвторизация = Неопределено;
			Попытка
				Если ПустаяСтрока(ВыборкаСерверов.Пользователь) Тогда 
					ТекстСообщения = СтрШаблон("Для сервера сбора данных %1 не указан Пользователь", Строка(ВыборкаСерверов.Ссылка));
					ОписаниеОшибки.Добавить(ТекстСообщения);
					Продолжить;
				Иначе
					Токен = упИнтеграцияССерверамиСбораДанных.ПолучитьТокенScoutSOAP(ПроксиАвторизация, ВыборкаСерверов.Пользователь, ВыборкаСерверов.Пароль, ВыборкаСерверов.Сервер, ВыборкаСерверов.Порт);
					Если ПустаяСтрока(Токен) Тогда 
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			Исключение
				Токен = "";
				СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОписаниеОшибки.Добавить(СтрШаблон("ПолучениеДанныхОтScoutSOAP Токен: %1", СтрокаСообщенияОбОшибке));
				Продолжить;
			КонецПопытки;
			Если ПустаяСтрока(Токен) Тогда 
				ТекстСообщения = СтрШаблон("Для сервера сбора данных %1 Токен сессии подключения не получен, обращение не выполнено", Строка(ВыборкаСерверов.Ссылка));
				ОписаниеОшибки.Добавить(ТекстСообщения);
				Продолжить;
			КонецЕсли;
			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл
				НачалоПериода = ДатаОбращения;
				КонецПериода = ДатаОбращения;
				ДатчикТоплива = Выборка.Датчик;
				Назначение = Выборка.Назначение;
				Трекер = Неопределено;
				КалибровочныйГрафик = Справочники.упКалибровочныеГрафики.ПустаяСсылка();
				Рейс = Документы.упРейс.ПустаяСсылка();
				ТекущаяУстройство = "";
				Отбор = Новый Структура();
				Отбор.Вставить("ТС", Выборка.ТС);
				Отбор.Вставить("Датчик", Выборка.Датчик);
				НайденныеСтроки = ТаблицаТрекеровТС.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() Тогда
					КалибровочныйГрафик = НайденныеСтроки[0].КалибровочныйГрафик;
					Трекер = НайденныеСтроки[0].Трекер;
					ТекущаяУстройство = НайденныеСтроки[0].ИдентификаторУстройства;
					Рейс = НайденныеСтроки[0].Рейс;
				Иначе
					Продолжить;
				КонецЕсли;
				ДанныеИнтервала = Неопределено;
				ЕстьОшибки = Ложь;
				Попытка
					ДанныеИнтервала = упИнтеграцияССерверамиСбораДанных.ПолучитьДанныеТопливаЗаПериодScoutSOAP(ТекущаяУстройство, НачалоПериода, КонецПериода, ВыборкаСерверов.Сервер, ВыборкаСерверов.Порт, Токен, ЕстьОшибки);
				Исключение
					ДанныеИнтервала = Неопределено;
					СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ОписаниеОшибки.Добавить(СтрШаблон("ПолучениеДанныхОтScoutSOAP: %1", СтрокаСообщенияОбОшибке));
				КонецПопытки;
				Если НЕ ДанныеИнтервала = Неопределено Тогда
					Для каждого ОтрезокДанных Из ДанныеИнтервала Цикл
						ТаблицаЗначений = ОтрезокДанных.Значение;
						Для каждого ТекущаяСтрокаЗаправкиСливы Из ТаблицаЗначений Цикл
							упИнтеграцияССерверамиСбораДанных.ДобавитьСтрокаДанныхДатчика(ТаблицаЗначенийДатчиков, ТекущаяСтрокаЗаправкиСливы.НачалоПериода, Трекер, КалибровочныйГрафик, ДатчикТоплива, ТекущаяСтрокаЗаправкиСливы.НачОстаток, Назначение, Выборка.ТС, Рейс);
							Если НЕ ТекущаяСтрокаЗаправкиСливы.НачалоПериода = ТекущаяСтрокаЗаправкиСливы.КонецПериода Тогда
								упИнтеграцияССерверамиСбораДанных.ДобавитьСтрокаДанныхДатчика(ТаблицаЗначенийДатчиков, ТекущаяСтрокаЗаправкиСливы.КонецПериода, Трекер, КалибровочныйГрафик, ДатчикТоплива, ТекущаяСтрокаЗаправкиСливы.КонОстаток, Назначение, Выборка.ТС, Рейс);
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла; 
			Если НЕ ПроксиАвторизация = Неопределено Тогда 
				ПроксиАвторизация.Logout();
			КонецЕсли;
		КонецЦикла; 
		Токен = "";
		ПроксиАвторизация = Неопределено;
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство,Трекер);
			СтруктураОбъектов.Вставить("Датчики",ТаблицаЗначенийДатчиков.Скопировать());
			МассивДанных = Новый Массив;
			МассивДанных.Добавить(СтруктураОбъектов);
			упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхСКАУТ(МассивДанных);
		КонецЕсли;
	КонецЕсли; 
	
	Если ОписаниеОшибки.Количество() Тогда
		Отказ = Истина;
		ТекстОшибки = СтрСоединить(ОписаниеОшибки, "; ");
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			Если СписокТС.Количество()=1 Тогда
				ТекущийУровеньТоплива = ТаблицаЗначенийДатчиков[0].Значение;
				ТекстОшибки = СтрШаблон("Значение уровня топлива актуализировано. Текущее значение по данным датчиков: %1 л.", Формат(ТекущийУровеньТоплива,"ЧРД=.; ЧН=0; ЧГ=0"));
			Иначе
				ТекстОшибки = НСтр("ru = 'Значение уровня топлива актуализировано.'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстОшибки;

КонецФункции // ПолучитьДанныеТопливаТранспортныхСредствScoutSOAP()

#КонецОбласти

#Область ОбработкаOmnicomm

// Аутентификация выполняется вызовом метода Login, который возвращает токен.
//
// Параметры:
//  WSПрокси  - WSПрокси - Клиентский прокси для вызова веб-сервиса.
//  Логин  - Строка - Имя пользователя сервиса.
//  Пароль  - Строка - Пароль пользователя сервиса.
//  АдресРесурса  - Строка - адрес доступа к сервису.
//  Порт  - Число - Номер порта.
//
// Возвращаемое значение:
//   Строка - Представление токена доступа.
//
Функция ПолучитьТокенOmnicommSOAP(WSПрокси, Логин, Пароль, АдресРесурса, Порт) Экспорт
	
	ПространствоИмен = "http://omnicomm.ru/analyticalserver";
	ТекстПодключения = "http://%1:%2/AnalyticalServer/ws?wsdl";
	МестоположениеWSDL = СтрШаблон(ТекстПодключения,АдресРесурса,Формат(Порт,"ЧГ=0"));
	
	WSИмяСервиса = "AnalyticalServer";
	ИмяТочкиПодключения = "AnalyticalServerPort";
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = МестоположениеWSDL;
	ПараметрыПодключения.URIПространстваИмен = ПространствоИмен;
	ПараметрыПодключения.ИмяСервиса = WSИмяСервиса;
	ПараметрыПодключения.ИмяТочкиПодключения = ИмяТочкиПодключения;
		
	Попытка
		ПроксиАвторизация = ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	Исключение
		СообщениеПользователю = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтOmnicommSOAP:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
		Возврат Неопределено;
	КонецПопытки;
	WSПрокси = ПроксиАвторизация;
	SessionId = "";	
	Попытка
		signInResponse = ПроксиАвторизация.signIn(Логин, Пароль);
		Если НЕ signInResponse.Свойства().Получить("return") = Неопределено Тогда
			SessionId = signInResponse.return.sessionId;			
		КонецЕсли;
	Исключение
		ТекстСообщения = СтрШаблон("Авторизация: Не удалось вызвать signIn %1", ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("ПолучениеДанныхОтOmnicommSOAP:", УровеньЖурналаРегистрации.Ошибка, , ,ТекстСообщения);
		ПроксиАвторизация= Неопределено;
		Возврат "";  
	КонецПопытки;
	
	Возврат SessionId;
		
КонецФункции // ПолучитьТокенOmnicommSOAP()

// Выполним обращение к сервису Omnicomm.
//
// Параметры:
//  Сервер  - Выборка запроса - Элемент выборки.
//
Процедура ПолучениеДанныхОтOmnicomm(Сервер)	
	
	ИнформационнаяБазаФайловая = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая()=0,ЛОЖЬ,ИСТИНА);
	
	//ВыполнятьЛогированиеПоступившихДанных = Истина;
	ВыполнятьЛогированиеПоступившихДанных = Ложь; // не создаем логи скрыто от пользователя.
	
	Если НЕ ИнформационнаяБазаФайловая Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода","упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхOmnicomm");	 
		Отбор.Вставить("Состояние",СостояниеФоновогоЗадания.Активно);
		Отбор.Вставить("Наименование","ПолучениеДанныхОтOMNICOMMSOAP");
		МассивФЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		Если МассивФЗаданий.Количество() Тогда
			// С предыдущего запуска выполняются фоновые задания.
			// Придется ждать пока они не будут завершины.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Сервер.Сервер = "" ИЛИ Сервер.Порт = 0 Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтOmnicommSOAP'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не настроены параметры подключения к серверу сбора данных.'"));
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".txt");	
	ИмяКаталога = "";
	ИмяКаталога_ТекущейСессии = "";
	
	Если ВыполнятьЛогированиеПоступившихДанных Тогда
		
		стрСоед = СтрокаСоединенияИнформационнойБазы();
		
		Массив = СтрРазделить(стрСоед,";",Ложь);
		
		ТекущееИмяБазы = "";
		Для Каждого текЭлемент Из Массив Цикл
			
			Если СтрНайти(текЭлемент,"Ref=") > 0 Тогда
				ТекущееИмяБазы = СтрЗаменить(текЭлемент,"Ref=","");
				ТекущееИмяБазы = СтрЗаменить(ТекущееИмяБазы,Символ(34),"");
			КонецЕсли;
			
		КонецЦикла;
		
		ИмяФайла = ПолучитьИмяВременногоФайла(".txt");
		
		ИмяКаталога = КаталогВременныхФайлов() + "Log_OmnicommSOAP" + ?(ПустаяСтрока(ТекущееИмяБазы),"","_base_" + ТекущееИмяБазы);
		
		КаталогНаДиске = Новый Файл(ИмяКаталога);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ИмяКаталога);
		КонецЕсли; 
		
		ИмяКаталога_ТекущейСессии = ИмяКаталога + "\" + Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMddHHmmss");
		
		СоздатьКаталог(ИмяКаталога_ТекущейСессии);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтOmnicommSOAP'"), УровеньЖурналаРегистрации.Информация,,, "Католог логирования на серевере: " + ИмяКаталога_ТекущейСессии);
		
	КонецЕсли;
	
	Токен = "";
	ПроксиАвторизация = Неопределено;
	Отказ = Ложь;
	
	Попытка
		Если НЕ ПустаяСтрока(Сервер.Пользователь) Тогда 
			Токен = ПолучитьТокенOmnicommSOAP(ПроксиАвторизация, Сервер.Пользователь, Сервер.Пароль, Сервер.Сервер, Сервер.Порт);
			Если ПустаяСтрока(Токен) Тогда 
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		тбзУстройства = ПолучитьСписокУстройствOmnicommSOAP(ПроксиАвторизация, Токен, Сервер.Ссылка, Истина);
		
		Если тбзУстройства = Неопределено Тогда 
			Если НЕ ПроксиАвторизация = Неопределено Тогда 
				ПроксиАвторизация.Logout();
			КонецЕсли;
			ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтOmnicommSOAP'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не удалось получить список устройств getObjectSet.'"));
			Возврат;			
		КонецЕсли;
		
		Если НЕ тбзУстройства = Неопределено Тогда 
			// На данном этапе нет реквизита по которому можно найти ТС.
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", тбзУстройства);
			Запрос.Выполнить();
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Идентификатор КАК СТРОКА(50)) КАК Идентификатор,
			|	ВЫРАЗИТЬ(вртДанных.Наименование КАК СТРОКА(50)) КАК Наименование
			|ПОМЕСТИТЬ втДанные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втДанные.Идентификатор КАК Идентификатор,
			|	втДанные.Наименование КАК Наименование,
			|	ЕСТЬNULL(упТранспортныеСредства.Ссылка, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
			|	ЕСТЬNULL(упМобильныеУстройстваИТрекеры.Ссылка, ЗНАЧЕНИЕ(Справочник.упМобильныеУстройстваИТрекеры.ПустаяСсылка)) КАК Трекер
			|ИЗ
			|	втДанные КАК втДанные
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
			|		ПО втДанные.Наименование = упТранспортныеСредства.Наименование
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
			|		ПО (втДанные.Идентификатор = упМобильныеУстройстваИТрекеры.ИдентификаторУстройства)
			|ГДЕ
			|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
			|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ";
			Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл			
				Если ЗначениеЗаполнено(Выборка.ТранспортноеСредство)
					И НЕ ЗначениеЗаполнено(Выборка.Трекер) Тогда
					НовыйТрекер = Справочники.упМобильныеУстройстваИТрекеры.СоздатьЭлемент();
					НовыйТрекер.Наименование = СтрШаблон("%1_СКАУТ_%2", Выборка.Идентификатор, Выборка.РегистрационныйНомер);
					НовыйТрекер.ИдентификаторУстройства = Выборка.Идентификатор;
					НовыйТрекер.СерверСбораДанных = Сервер;
					Попытка
						НовыйТрекер.Записать();
					Исключение
						Продолжить;
					КонецПопытки;
					НовЗапись = РегистрыСведений.упУстановленныеТрекеры.СоздатьМенеджерЗаписи();
					НовЗапись.Период = ТекущаяДатаСеанса();
					НовЗапись.Трекер = НовыйТрекер.Ссылка;
					НовЗапись.ТранспортноеСредство = Выборка.ТранспортноеСредство;
					НовЗапись.Установлен = Истина;
					Попытка
						НовЗапись.Записать(Истина);			
					Исключение
						ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.СКАУТ", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упУстановленныеТрекеры, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;
				КонецЕсли;
			КонецЦикла;
			Запрос.МенеджерВременныхТаблиц.Закрыть();
		КонецЕсли;
		
		МассивТС = тбзУстройства.ВыгрузитьКолонку("Идентификатор");
		
		Запрос = Новый Запрос;	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УстановленныеТрекерыСП.Трекер КАК Трекер
		|ПОМЕСТИТЬ втУстановленныеТрекеры
		|ИЗ
		|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(, ) КАК УстановленныеТрекерыСП
		|ГДЕ
		|	НЕ УстановленныеТрекерыСП.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упМобильныеУстройстваИТрекеры.Ссылка КАК Трекер,
		|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
		|	упМобильныеУстройстваИТрекеры.Модель КАК Модель,
		|	упМобильныеУстройстваИТрекеры.СерверСбораДанных.ДатаЗагрузки КАК ДатаЗагрузки
		|ПОМЕСТИТЬ втТрекеры
		|ИЗ
		|	Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
		|ГДЕ
		|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
		|	И упМобильныеУстройстваИТрекеры.ИдентификаторУстройства В(&ИдентификаторыУстройств)
		|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
		|	И упМобильныеУстройстваИТрекеры.Ссылка В
		|			(ВЫБРАТЬ
		|				втУстановленныеТрекеры.Трекер
		|			ИЗ
		|				втУстановленныеТрекеры КАК втУстановленныеТрекеры)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДанныеДатчиковСП.Период) КАК Период,
		|	ДанныеДатчиковСП.Трекер КАК Трекер
		|ПОМЕСТИТЬ вт_ДатчикПериод
		|ИЗ
		|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
		|			,
		|			Трекер В
		|				(ВЫБРАТЬ
		|					втТрекеры.Трекер
		|				ИЗ
		|					втТрекеры КАК втТрекеры)) КАК ДанныеДатчиковСП
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДатчиковСП.Трекер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
		|			ТОГДА втТрекеры.ДатаЗагрузки
		|		ИНАЧЕ ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК Период,
		|	втТрекеры.Трекер КАК Трекер,
		|	втТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
		|	ЕСТЬNULL(ДанныеТрекеровСП.Счетчик, 0) КАК СчетчикАктульности,
		|	ВЫБОР
		|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
		|			ТОГДА втТрекеры.ДатаЗагрузки
		|		ИНАЧЕ ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК ПериодД
		|ИЗ
		|	втТрекеры КАК втТрекеры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеТрекеров.СрезПоследних КАК ДанныеТрекеровСП
		|		ПО втТрекеры.Трекер = ДанныеТрекеровСП.Трекер
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДатчикПериод КАК вт_ДатчикПериод
		|		ПО втТрекеры.Трекер = вт_ДатчикПериод.Трекер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упМоделиТрекеровДатчики.Датчик КАК Датчик,
		|	упМоделиТрекеровДатчики.КалибровочныйГрафик КАК КалибровочныйГрафик
		|ИЗ
		|	втТрекеры КАК втТрекеры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТрекеров.Датчики КАК упМоделиТрекеровДатчики
		|		ПО втТрекеры.Модель = упМоделиТрекеровДатчики.Ссылка
		|ГДЕ
		|	упМоделиТрекеровДатчики.Датчик.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
		|
		|СГРУППИРОВАТЬ ПО
		|	упМоделиТрекеровДатчики.Датчик,
		|	упМоделиТрекеровДатчики.КалибровочныйГрафик";
		
		Запрос.УстановитьПараметр("ИдентификаторыУстройств", МассивТС);
		Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
		ПакетЗапросов  = Запрос.ВыполнитьПакет();
		Выборка = ПакетЗапросов[3].Выбрать();
		тбзДатчики = ПакетЗапросов[4].Выгрузить();
		
		
		ДатчикПоТопливу = Неопределено;		
		КалибровочныйГрафик = Неопределено;		
		Для каждого тбзСтрока Из тбзДатчики Цикл
			ДатчикПоТопливу = тбзСтрока.Датчик;
			КалибровочныйГрафик = тбзСтрока.КалибровочныйГрафик;
			Прервать;
		КонецЦикла;
		
		МассивОбъектов = Новый Массив;
		Unixtime = ДАТА("19700101000000");
		Пока Выборка.Следующий() Цикл
			ТекущаяУстройство = Выборка.ИдентификаторУстройства;
			НачалоПериода = Формат(Выборка.Период+1-Unixtime,"ЧН=0; ЧГ=0");
			НачалоПериода_Датчика = Формат(Выборка.Период+1-Unixtime,"ЧН=0; ЧГ=0");
			КонецПериода =  Формат(ТекущаяДатаСеанса()+1-Unixtime,"ЧН=0; ЧГ=0");
			Попытка
				Данные = ПолучитьОнлайнДанныеOmnicommSOAP(ТекущаяУстройство, НачалоПериода, КонецПериода, ПроксиАвторизация, Токен);
			Исключение
				Данные = Новый СписокЗначений;
			КонецПопытки;
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство,Выборка.Трекер);
			СтруктураОбъектов.Вставить("Навигация",Данные);
			
			Если НЕ ДатчикПоТопливу = Неопределено Тогда
				Попытка
					Данные = ПолучитьДанныеПоТопливуOmnicommSOAP(Выборка.Трекер, ДатчикПоТопливу, КалибровочныйГрафик, ТекущаяУстройство, НачалоПериода, КонецПериода, ПроксиАвторизация, Токен);
				Исключение
					Данные = Неопределено;
				КонецПопытки;
				Если НЕ Данные = Неопределено Тогда
					СтруктураОбъектов.Вставить("Датчики",Данные);
				КонецЕсли;
			КонецЕсли;
			
			МассивОбъектов.Добавить(СтруктураОбъектов);
		КонецЦикла;
		
	Исключение
		Отказ = Истина;
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтOmnicommSOAP'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ОписаниеОшибки());		
		Если НЕ ПроксиАвторизация = Неопределено Тогда 
			ПроксиАвторизация.Logout();
		КонецЕсли;
	КонецПопытки;
	Токен = "";
	
	Если НЕ Отказ Тогда
		КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковЗагрузкиССервераСбораДанных");
		Если КоличествоПотоков = 0 ИЛИ ИнформационнаяБазаФайловая Тогда 
			КоличествоПотоков = 1;
		КонецЕсли;
		
		Если ИнформационнаяБазаФайловая Тогда
			СоздатьПотокОбработкиЗагрузкиДанныхOMNICOMM(МассивОбъектов);
		Иначе
			МассивПотоков = Новый Массив;
			Если КоличествоПотоков = 1 Тогда
				МассивПотоков.Добавить(МассивОбъектов);
			Иначе
				ОбъектовВПотоке = Окр(МассивОбъектов.Количество() / КоличествоПотоков,0,РежимОкругления.Окр15как10);
				ТекущееКоличество = 0;
				ОбъектыПотока = Новый Массив;
				Для Каждого текЭлемент Из МассивОбъектов Цикл
					ОбъектыПотока.Добавить(текЭлемент);
					ТекущееКоличество = ТекущееКоличество + 1;
					Если ТекущееКоличество >= ОбъектовВПотоке Тогда
						ТекущееКоличество = 0;
						МассивПотоков.Добавить(ОбъектыПотока);
						ОбъектыПотока = Новый Массив;
					КонецЕсли;
				КонецЦикла;
				// Допишем крайний поток ТС которые остались.
				Если ОбъектыПотока.Количество() Тогда
					ТекущийМассив = МассивПотоков[МассивПотоков.ВГраница()];
					Для Каждого текЭлемент Из ОбъектыПотока Цикл
						ТекущийМассив.Добавить(текЭлемент);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого текЭлемент Из МассивПотоков Цикл
				Если текЭлемент.Количество() Тогда 
					мсвПараметров = Новый Массив(1);
					мсвПараметров[0] = текЭлемент;
					упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхOMNICOMM", мсвПараметров,,,"ПолучениеДанныхОтOMNICOMMSOAP");
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПолучениеДанныхОтOmnicomm()

// Выполним обращение к сервису Omnicomm получим таблицу всех устройств.
//
// Параметры:
//  WSПрокси  - WSПрокси - Клиентский прокси для вызова веб-сервиса.
//  Токен  - Строка - токен подключения.
//  СерверСбораДанных  - СправочникСсылка.упСервераСбораДанных - Ссылка Сервер сбора данных трекера.
//  ПроверятьТрекер  - Булево - Если признак установлен, будет выполнена проверка трекера.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьСписокУстройствOmnicommSOAP(WSПрокси, Токен, СерверСбораДанных = Неопределено, ПроверятьТрекер = Ложь) Экспорт
	
	ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
	
	ТаблицаЗначений.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов("Строка"),"Уникальный идентификатор");
	ТаблицаЗначений.Колонки.Добавить("ТипТС",Новый ОписаниеТипов("Строка"),"Тип транспортного средства"); 
	ТаблицаЗначений.Колонки.Добавить("Телефон",Новый ОписаниеТипов("Строка"),"Телефон");
	ТаблицаЗначений.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка"),"Название объекта мониторинга");
	ТаблицаЗначений.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"Транспортное средство");
	
	objects = WSПрокси.getObjectSet(Токен);
		
	Если НЕ objects.Свойства().Получить("return") = Неопределено Тогда
		Для каждого ТекущийОбъектXDTO Из objects.return.objects Цикл
			
			Идентификатор = Формат(ТекущийОбъектXDTO.id,"ЧГ=0");
			
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.Идентификатор = Идентификатор;
			НоваяСтрока.ТипТС = ТекущийОбъектXDTO.objectType;
			НоваяСтрока.Телефон = ТекущийОбъектXDTO.phone;
			НоваяСтрока.Наименование = ТекущийОбъектXDTO.objectName;
		КонецЦикла;		
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если ТаблицаЗначений.Количество() Тогда
		Результат = ТаблицаЗначений.Скопировать();
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокУстройствOmnicommSOAP()

// Выполним обращение к сервису OMNICOMM получим таблицу всех устройств.
//
// Параметры:
//  ИдентификаторТС - Строка - Идентификатор устройства на сервере.
//  НачалоПериода - Дата - Начало периода получения данных.
//  КонецПериода - Дата - Конец периода получения данных.
//  WSПрокси  - WSПрокси - Клиентский прокси для вызова веб-сервиса.
//  Токен - Строка - токен подключения.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьДанныеПоТопливуOmnicommSOAP(Трекер, Датчик, КалибровочныйГрафик, ИдентификаторТС, НачалоПериода, КонецПериода, WSПрокси, Токен)
	
	ТаблицаЗначенийДатчиков = Новый ТаблицаЗначений;
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Трекер",Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"),"Трекер");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("КалибровочныйГрафик",Новый ОписаниеТипов("СправочникСсылка.упКалибровочныеГрафики"),"КалибровочныйГрафик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Датчик",Новый ОписаниеТипов("СправочникСсылка.упДатчики"),"Датчик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Рейс",Новый ОписаниеТипов("ДокументСсылка.упРейс"),"Рейс");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Значение",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Значение");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Назначение",Новый ОписаниеТипов("СправочникСсылка.упНазначенияДатчиков"),"Назначение");
	
	objects = WSПрокси.getSmoothedFuel(Токен, Число(ИдентификаторТС), НачалоПериода, КонецПериода);
	
	Unixtime = ДАТА("19700101000000");
	Если НЕ objects.Свойства().Получить("return") = Неопределено Тогда
		Если objects.return.status = Истина Тогда
			      
			НаборДанных = objects.return.fuel;
			
			Для каждого ТекущийОбъектXDTO Из НаборДанных Цикл
				//Период = МестноеВремя(Unixtime + ТекущийОбъектXDTO.timeStamp);
				Период = Unixtime + ТекущийОбъектXDTO.timeStamp;
								
				НовЗаписьДатчики = ТаблицаЗначенийДатчиков.Добавить();
				НовЗаписьДатчики.Период   				= Период;
				НовЗаписьДатчики.Трекер 				= Трекер;
				НовЗаписьДатчики.КалибровочныйГрафик    = КалибровочныйГрафик;
				НовЗаписьДатчики.Датчик   				= Датчик;				
				
				НовЗаписьДатчики.Значение 				= ТекущийОбъектXDTO.smoothedFuel;
				НовЗаписьДатчики.Назначение				= Датчик.Назначение;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаЗначенийДатчиков.Сортировать("Период");
	
	Возврат ТаблицаЗначенийДатчиков;
	
КонецФункции // ПолучитьДанныеПоТопливуOmnicommSOAP()

// Выполним обращение к сервису OMNICOMM получим таблицу всех устройств.
//
// Параметры:
//  ИдентификаторТС - Строка - Идентификатор устройства на сервере.
//  НачалоПериода - Дата - Начало периода получения данных.
//  КонецПериода - Дата - Конец периода получения данных.
//  WSПрокси  - WSПрокси - Клиентский прокси для вызова веб-сервиса.
//  Токен - Строка - токен подключения.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьОнлайнДанныеOmnicommSOAP(ИдентификаторТС, НачалоПериода, КонецПериода, WSПрокси, Токен)
	
	objects = WSПрокси.getTrack(Токен, Число(ИдентификаторТС), НачалоПериода, КонецПериода);
	
	ДанныеТрека = Новый СписокЗначений;
	
	Если НЕ objects.Свойства().Получить("return") = Неопределено Тогда
		Если objects.return.status = Истина Тогда
			ТаблицаЗначенийТреков = Новый ТаблицаЗначений;
			ТаблицаЗначенийТреков.Колонки.Добавить("Период"                  ,Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Период");
			ТаблицаЗначенийТреков.Колонки.Добавить("Высота"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8,3)), "Высота");
			ТаблицаЗначенийТреков.Колонки.Добавить("НаправлениеДвижения"     ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "НаправлениеДвижения");		
			ТаблицаЗначенийТреков.Колонки.Добавить("Широта"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
			ТаблицаЗначенийТреков.Колонки.Добавить("Долгота"                 ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
			ТаблицаЗначенийТреков.Колонки.Добавить("Скорость"                ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "Скорость");
			НаборТочекТрека = objects.return.trackEvents;
			Unixtime = ДАТА("19700101000000");
			Для каждого ТекущийОбъектXDTO Из НаборТочекТрека Цикл
				
				//Период = МестноеВремя(Unixtime + ТекущийОбъектXDTO.timeStamp);
				Период = Unixtime + ТекущийОбъектXDTO.timeStamp;
				Координаты = СтрРазделить(ТекущийОбъектXDTO.gpsPos, ";");
				
				Долгота = Координаты[1];
				Широта = Координаты[0];
				Скорость = ТекущийОбъектXDTO.speed;
				НаправлениеДвижения = ТекущийОбъектXDTO.gpsDir;
				
				НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
				НовЗаписьТрекера.Высота = 0;
				НовЗаписьТрекера.Период   			 = Период;
				НовЗаписьТрекера.Широта              = Широта;
				НовЗаписьТрекера.Долгота             = Долгота;
				НовЗаписьТрекера.Скорость            = Скорость;
				НовЗаписьТрекера.НаправлениеДвижения = НаправлениеДвижения;
				
			КонецЦикла;
			
			ДанныеТрека.Добавить(ТаблицаЗначенийТреков);
			
		КонецЕсли;
	КонецЕсли;

    Возврат  ДанныеТрека;
	
КонецФункции // ПолучитьОнлайнДанныеOmnicommSOAP()

// Выполним создание разных потоков обработки.
//
// Параметры:
//  ДанныеОбработки  - Массив - Данные потока.
//
Процедура СоздатьПотокОбработкиЗагрузкиДанныхOMNICOMM(ДанныеОбработки) Экспорт
	
	Для Каждого текЭлемент Из ДанныеОбработки Цикл
		ВыполнитьОбработкуЗагруженныхДанныхOMNICOMM(текЭлемент);
	КонецЦикла;
	
КонецПроцедуры // СоздатьПотокОбработкиЗагрузкиДанныхOMNICOMM()

// Выполним обработку каждого объекта.
//
//
// Параметры:
//  ДанныеОбработки - Массив - Текщий элемент данных.
//
Процедура ВыполнитьОбработкуЗагруженныхДанныхOMNICOMM(ДанныеОбработки)
	
	ТекущийТерминал = ДанныеОбработки.Трекер;
	ТаблицаЗначенийДатчиков = Неопределено;
	ТекущиеПараметрыЛогирования = Неопределено;
	
	НачалоПериодаОбработки = ТекущаяДатаСеанса();
	
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда		
		
		ТекущиеПараметрыЛогирования = Новый Структура();
		ТекущиеПараметрыЛогирования.Вставить("КаталогЛогТрек",ДанныеОбработки.КаталогЛогТрек);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода",ДанныеОбработки.НачалоПериода);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода_Датчика",ДанныеОбработки.НачалоПериода_Датчика);
		ТекущиеПараметрыЛогирования.Вставить("КонецПериода",ДанныеОбработки.КонецПериода);
		
	КонецЕсли;

	Если ДанныеОбработки.Свойство("Датчики") Тогда
		ТаблицаЗначенийДатчиков = ДанныеОбработки.Датчики;
	КонецЕсли;
	
	Если ДанныеОбработки.Свойство("Навигация") И НЕ ДанныеОбработки.Навигация = Неопределено Тогда
		РазобратьТрекиOMNICOMMSOAP(ДанныеОбработки.Навигация,ТекущийТерминал,ТаблицаЗначенийДатчиков,ТекущиеПараметрыЛогирования);
	КонецЕсли;
	
	Если НЕ ТаблицаЗначенийДатчиков = Неопределено И ТаблицаЗначенийДатчиков.Количество() Тогда
		соотТрекеров = Новый Соответствие;
		Для каждого ТекущаяСтрока Из ТаблицаЗначенийДатчиков Цикл
			Если ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			текДанныеТС = соотТрекеров[ТекущийТерминал];
			Период = ТекущаяСтрока.Период;
			Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
				текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
				соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			ТекущаяСтрока.ТранспортноеСредство = текДанныеТС.ТранспортноеСредство;
			ТекущаяСтрока.Рейс                 = текДанныеТС.Рейс;
		КонецЦикла;
		
		ОтборТС = Новый Структура("ТранспортноеСредство", ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка"));
		
		НайднныеСтроки = ТаблицаЗначенийДатчиков.НайтиСтроки(ОтборТС);
		Если НайднныеСтроки.Количество() Тогда
			Для каждого ТекущаяСтрока Из НайднныеСтроки Цикл
				ТаблицаЗначенийДатчиков.Удалить(ТекущаяСтрока);
			КонецЦикла;
		КонецЕсли;
		
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначенийДатчиков);
			Запрос.Выполнить();
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Период КАК ДАТА) КАК Период,
			|	ВЫРАЗИТЬ(вртДанных.Трекер КАК Справочник.упМобильныеУстройстваИТрекеры) КАК Трекер,
			|	ВЫРАЗИТЬ(вртДанных.КалибровочныйГрафик КАК Справочник.упКалибровочныеГрафики) КАК КалибровочныйГрафик,
			|	ВЫРАЗИТЬ(вртДанных.Датчик КАК Справочник.упДатчики) КАК Датчик,
			|	ВЫРАЗИТЬ(вртДанных.ТранспортноеСредство КАК Справочник.упТранспортныеСредства) КАК ТранспортноеСредство,
			|	ВЫРАЗИТЬ(вртДанных.Рейс КАК Документ.упРейс) КАК Рейс,
			|	ВЫРАЗИТЬ(вртДанных.Значение КАК ЧИСЛО(10, 3)) КАК Значение,
			|	ВЫРАЗИТЬ(вртДанных.Назначение КАК Справочник.упНазначенияДатчиков) КАК Назначение
			|ПОМЕСТИТЬ втДанные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втДанные.Период,
			|	втДанные.Трекер КАК Трекер,
			|	втДанные.КалибровочныйГрафик,
			|	втДанные.Датчик КАК Датчик,
			|	втДанные.ТранспортноеСредство КАК ТранспортноеСредство,
			|	втДанные.Рейс,
			|	втДанные.Значение,
			|	втДанные.Назначение
			|ИЗ
			|	втДанные КАК втДанные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков КАК упДанныеДатчиков
			|		ПО втДанные.Период = упДанныеДатчиков.Период
			|			И втДанные.Трекер = упДанныеДатчиков.Трекер
			|			И втДанные.Датчик = упДанныеДатчиков.Датчик
			|			И втДанные.ТранспортноеСредство = упДанныеДатчиков.ТранспортноеСредство
			|			И втДанные.Значение = упДанныеДатчиков.Значение
			|ГДЕ
			|	упДанныеДатчиков.ТранспортноеСредство ЕСТЬ NULL И (НЕ втДанные.Датчик=ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка) И НЕ втДанные.ТранспортноеСредство=ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Трекер, ТранспортноеСредство, Датчик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ";
			
			Выборка = Запрос.Выполнить();
			
			тбзКопия = Неопределено;
			
			Если НЕ Выборка.Пустой() Тогда
				тбзКопия = Выборка.Выгрузить();
			КонецЕсли;
			
			Запрос.МенеджерВременныхТаблиц.Закрыть();
			
			Если ТипЗнч(тбзКопия) = ТИП("ТаблицаЗначений") И тбзКопия.Количество() Тогда
				упИнтеграцияССерверамиСбораДанных.ВыполнитьОбработкуЗначенийДатчиковНабор(тбзКопия);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда
		Если НЕ ПустаяСтрока(ДанныеОбработки.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ДанныеОбработки.КаталогЛогТрек + "\Данные продолжительности записи данных в базу.txt";
			
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			
			ТекстовыйДокумент.ДобавитьСтроку("Начало периода записи: " + Строка(НачалоПериодаОбработки));
			ТекстовыйДокумент.ДобавитьСтроку("Конец периода записи: " + Строка(ТекущаяДатаСеанса()));
			
			ТекстовыйДокумент.Записать(ИмяФайлаЗаписи);
			ТекстовыйДокумент = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбработкуЗагруженныхДанныхOMNICOMM()

// Выполнить обход полученных данных.
//
// Параметры:
//  СписокТС  - Массив - Список ТС с данными.
//  ТекущийТерминал  - Справочник.упМобильныеУстройстваИТрекеры - Ссылка на терминал.
//  ДанныеДатчиков  - ТаблицаЗначений - Таблица с данными датчиков.
//  ПараметрыЛогирования - Структура - Параметры для записи лога получения данных.
//
Процедура РазобратьТрекиOMNICOMMSOAP(СписокТС, ТекущийТерминал, ДанныеДатчиков = Неопределено,ПараметрыЛогирования=Неопределено)
	
	ТаблицаЗначенийТреков = Новый ТаблицаЗначений;
	ТаблицаЗначенийТреков.Колонки.Добавить("Период"                  ,Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Период");
	ТаблицаЗначенийТреков.Колонки.Добавить("ТранспортноеСредство"    ,Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"), "ТранспортноеСредство");
	ТаблицаЗначенийТреков.Колонки.Добавить("Высота"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8,3)), "Высота");
	ТаблицаЗначенийТреков.Колонки.Добавить("НаправлениеДвижения"     ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "НаправлениеДвижения");		
	ТаблицаЗначенийТреков.Колонки.Добавить("Широта"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ТаблицаЗначенийТреков.Колонки.Добавить("Долгота"                 ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	ТаблицаЗначенийТреков.Колонки.Добавить("ПредставлениеКоординаты" ,Новый ОписаниеТипов("Строка"));
	ТаблицаЗначенийТреков.Колонки.Добавить("Скорость"                ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "Скорость");
	ТаблицаЗначенийТреков.Колонки.Добавить("Трекер"                  ,Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"), "Трекер");
	ТаблицаЗначенийТреков.Колонки.Добавить("Рейс"                    ,Новый ОписаниеТипов("ДокументСсылка.упРейс"), "Рейс");
	
	соотТрекеров = Новый Соответствие;
	соотТрекеровПериод = Новый Соответствие;
	ВыполнитьЗаписьДанныхДатчиков = НЕ ДанныеДатчиков = Неопределено;
	Для каждого ОтрезокДанных Из СписокТС Цикл
		ТаблицаЗначений = ОтрезокДанных.Значение;
		Для каждого ТекущаяСтрокаТрека Из ТаблицаЗначений Цикл
			Период = ТекущаяСтрокаТрека.Период;
			ПроверкаПериода = соотТрекеровПериод.Получить(Период);
			Если НЕ ПроверкаПериода=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			текДанныеТС = соотТрекеров[ТекущийТерминал];
			Если текДанныеТС = Неопределено ИЛИ НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) ИЛИ НЕ (Период >= текДанныеТС.НачалоВыполнения) ИЛИ НЕ (Период <= текДанныеТС.ОкончаниеВыполнения ИЛИ текДанныеТС.ОкончаниеВыполнения = '00010101') Тогда 
				текДанныеТС = ДанныеПоТекущемуТС(Период, ТекущийТерминал);
				соотТрекеров.Вставить(ТекущийТерминал, текДанныеТС);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗаписьТрекера, ТекущаяСтрокаТрека);
			НовЗаписьТрекера.ПредставлениеКоординаты = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеКоординаты(НовЗаписьТрекера.Широта, НовЗаписьТрекера.Долгота);
			НовЗаписьТрекера.ТранспортноеСредство 	 = текДанныеТС.ТранспортноеСредство; 
			НовЗаписьТрекера.Трекер 				 = ТекущийТерминал;
			НовЗаписьТрекера.Рейс                    = текДанныеТС.Рейс;
			Структура = Новый Структура("ТранспортноеСредство, Рейс", текДанныеТС.ТранспортноеСредство, текДанныеТС.Рейс);
			соотТрекеровПериод.Вставить(Период, Структура);
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаЗначенийТреков.Сортировать("Период");
	
	Если ВыполнитьЗаписьДанныхДатчиков Тогда
		Для каждого ТекущаяСтрока Из ДанныеДатчиков Цикл
			ВыполнитьКопированиеДанных = Истина;
			ДанныеТСРейса = соотТрекеровПериод.Получить(ТекущаяСтрока.Период);
			Если ДанныеТСРейса=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) Тогда
				ТекущаяСтрока.ТранспортноеСредство = ДанныеТСРейса.ТранспортноеСредство;
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеТСРейса.Рейс) Тогда
				ТекущаяСтрока.Рейс = ДанныеТСРейса.Рейс;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ПараметрыЛогирования = Неопределено Тогда
		Если НЕ ПустаяСтрока(ПараметрыЛогирования.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ПараметрыЛогирования.КаталогЛогТрек + "\ТаблицаЗначенийТреков_" + Строка(ТекущийТерминал) + "_" + ПараметрыЛогирования.НачалоПериода + "_" + ПараметрыЛогирования.КонецПериода + ".txt";		
			ВыполнитьЗаписьТаблицыЗначенийВФайл(ИмяФайлаЗаписи,ТаблицаЗначенийТреков);		
		КонецЕсли;
	КонецЕсли;
	
	Период = ДАТА("00010101000000");
	
	Попытка
		ТекущееТранспортноеСредство=Неопределено;
		НаборЗаписей = РегистрыСведений.упДанныеТрекеров.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		Для каждого тбзСтрока Из ТаблицаЗначенийТреков Цикл
			Если НЕ ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство Тогда
				ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство;
				Если НаборЗаписей.Количество() Тогда
					Попытка
						НаборЗаписей.Записать(Ложь);
					Исключение
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
						КонецЕсли;
						ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.OMNICOMM", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;
					Если ТранзакцияАктивна() Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;
					НаборЗаписей.Очистить();
				КонецЕсли;
				Если НЕ ТранзакцияАктивна() Тогда
					НачатьТранзакцию();
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеТрекеров");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТекущееТранспортноеСредство);
					БлокировкаДанных.Заблокировать();
				КонецЕсли;
				НаборЗаписей.Отбор.ТранспортноеСредство.Установить(ТекущееТранспортноеСредство, Истина);
			КонецЕсли;
			НовЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗапись,тбзСтрока);
		КонецЦикла;
		Если НаборЗаписей.Количество() Тогда
			Попытка
				НаборЗаписей.Записать(Ложь);
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.OMNICOMM", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.OMNICOMM", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ТаблицаЗначенийТреков = Неопределено;

КонецПроцедуры // РазобратьТрекиOMNICOMMSOAP()

#КонецОбласти

#Область ОбработкаWialonPro

// Функция - Сформировать JSON
//
// Параметры:
//	сткДанные - входящие данные для формирования.
// 
// Возвращаемое значение:
//	Запись JSON.
//
Функция СформироватьJSON(Данные) Экспорт
	//ИмяФайла = ПолучитьИмяВременногоФайла(".json");
	//
	//Запись = Новый ЗаписьJSON;
	//Запись.ОткрытьФайл(ИмяФайла, "UTF-8");
	//ЗаписатьJSON(Запись, ПараметрыЗапроса);
	//Запись.Закрыть();
	//
	//ТекстФайл = Новый ТекстовыйДокумент;
	//ТекстФайл.Прочитать(ИмяФайла, "UTF-8");
	//Тело = ТекстФайл.ПолучитьТекст();
	//УдалитьФайлы(ИмяФайла); // Удалим файл с телом параметров запроса.
	текВозврат = "";
	ТипДанных = ТипЗнч(Данные);
	текЗапись = Новый ЗаписьJSON;
	ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет, " ", Истина);
	текЗапись.УстановитьСтроку(ПараметрыJSON);
	Если ТипДанных = Тип("Структура") тогда
		СформироватьОбъектJSON(текЗапись, Данные);
	ИначеЕсли ТипДанных = Тип("Массив") тогда
		СформироватьМассивJSON(текЗапись, Данные);
	Иначе	
		текЗапись.ЗаписатьИмяСвойства();
		текЗапись.ЗаписатьЗначение(Данные);
	КонецЕсли;
	текВозврат = текЗапись.Закрыть();
	Возврат текВозврат;
КонецФункции

Процедура СформироватьЭлементJSON(Запись, Данные, Имя = "")
	Если ТипЗнч(Данные) = Тип("Структура") тогда
		СформироватьОбъектJSON(Запись, Данные, Имя);
	ИначеЕсли ТипЗнч(Данные) = Тип("Массив") тогда
		СформироватьМассивJSON(Запись, Данные, Имя);
	Иначе	
		Запись.ЗаписатьИмяСвойства(Имя);
		Запись.ЗаписатьЗначение(Данные);
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьОбъектJSON(Запись, Данные, Имя = "")
	Если ТипЗнч(Данные) = Тип("Структура") тогда
		Если ЗначениеЗаполнено(Имя) тогда
			Запись.ЗаписатьИмяСвойства(Имя);
		КонецЕсли;	
		Запись.ЗаписатьНачалоОбъекта();
		Для каждого текЭлемент из Данные цикл
			СформироватьЭлементJSON(Запись, текЭлемент.Значение, текЭлемент.Ключ);
		КонецЦикла;
		Запись.ЗаписатьКонецОбъекта();
	КонецЕсли;
КонецПроцедуры

Процедура СформироватьМассивJSON(Запись, Данные, Имя = "")
	Если ТипЗнч(Данные) = Тип("Массив") тогда
		Запись.ЗаписатьНачалоМассива();
		Для каждого текЭлемент из Данные цикл
			СформироватьЭлементJSON(Запись, текЭлемент);
		КонецЦикла;
		Запись.ЗаписатьКонецМассива();			
	КонецЕсли;
КонецПроцедуры

// Отправим POST запрос к сервису WialonPro.
//
// Параметры:
//  Сервер  - Строка - Адрес сервиса.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  Сервис  - Строка - Имя сервиса обращения.
//  ПараметрыЗапроса  - Произвольный - Параметры запроса к сервису.
//  ТекстЗапроса  - Строка - Текст команды сервиса.
//  ssid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   Произвольный   - Результат запроса.
//
Функция HTTP_POSTЗапросКСервисуWialonPro(Сервер, Порт=0, ЗащищенноеСоединение=Ложь, Сервис=Неопределено, ПараметрыЗапроса, ТекстЗапроса, ssid=Неопределено, ПрочитатьВСоответствие=Ложь)

	Тело = СформироватьJSON(ПараметрыЗапроса);
	ЗаголHttp = Новый Соответствие;
	ЗаголHttp.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	ПараметрыПолучения = Новый Структура();
	ПараметрыПолучения.Вставить("Порт", Порт);
	ПараметрыПолучения.Вставить("Заголовки", ЗаголHttp);
	ПараметрыПолучения.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение); //
	
	URLСтрока = СтрШаблон("http://%1%2/ajax.html?svc=%3&params=%4%5", Сервер, ?(Сервис=Неопределено, "", Сервис), ТекстЗапроса, Тело, ?(ssid=Неопределено, "", "&ssid="+ssid));
	РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
	
	ОтветJSON=Неопределено;
	Если РезультатПолученияФайлов.Статус Тогда
		ФайлРезультата = РезультатПолученияФайлов.Путь;
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.ОткрытьФайл(ФайлРезультата);
		Попытка
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие);
		Исключение
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON, Истина);
		КонецПопытки;
		ЧтениеJSON.Закрыть();		
		УдалитьФайлы(ФайлРезультата); // Удалим файл с результатом запроса.
	КонецЕсли;

	Возврат ОтветJSON;

КонецФункции // HTTP_POSTЗапросКСервисуWialonPro()

// Для выполнения любой операции в WialonPro Pro вам понадобится авторизироваться и получить ID сессии.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Логин  - Строка - Имя пользователя сервиса.
//  Пароль  - Строка - Пароль пользователя сервиса.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  ПолучитьУстройств  - Булево - Получим устройства пользователя.
//
// Возвращаемое значение:
//   Строка - ID сессии.
//
Функция ПолучитьУникальныйИдентификаторСессииWialonPro(Сервер, Логин, Пароль, Порт, ЗащищенноеСоединение, ПолучитьУстройств=Истина) Экспорт
	СтруктураВозврата=Неопределено;
	Данные = Новый Структура;
	Данные.Вставить("user", Логин);
	Данные.Вставить("password", Пароль);
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonPro(Сервер, Порт, ЗащищенноеСоединение, "", Данные, "core/login");
	Если НЕ ОтветJSON=Неопределено Тогда
		ssid = ОтветJSON.ssid;
		userId = ОтветJSON.uid;
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("ssid",ssid);
		СтруктураВозврата.Вставить("userId",userId);
		Если ПолучитьУстройств=Истина Тогда
			МассивСимволов = Новый Массив;
			МассивСимволов.Добавить("[");
			МассивСимволов.Добавить("]");
			МассивСимволов.Добавить("""");
			itemsId = ОтветJSON.user.pup.monu;
			Для каждого ЭтотСимвол Из МассивСимволов Цикл
				itemsId = СтрЗаменить(itemsId, ЭтотСимвол, "");
			КонецЦикла;
			МассивОбъектов = СтрРазделить(itemsId,",");
			СтруктураВозврата.Вставить("itemsId",МассивОбъектов);
		КонецЕсли;
	КонецЕсли;

	Возврат СтруктураВозврата;
		
КонецФункции // ПолучитьУникальныйИдентификаторСессииWialonPro()

// Для того, чтобы корректно выйти из системы WialonPro Pro.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  ssid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   Структура - Результат завершения подключения.
//
Функция ЗавершитьСессиюПодключенияWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid) Экспорт
	РезультатЗавершения = Неопределено;
	Данные = Новый Структура;
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonPro(Сервер, Порт, ЗащищенноеСоединение, "", Данные, "core/logout", ssid);
	Если НЕ ОтветJSON=Неопределено Тогда
		Если НЕ ОтветJSON.error=0 Тогда
			Соответствие = Новый Соответствие;
			Соответствие.Вставить(0, "успешный выход");
			Соответствие.Вставить(1, "недействительная сессия");
			Соответствие.Вставить(2, "неверное имя сервиса");
			Соответствие.Вставить(3, "неверный результат");
			Соответствие.Вставить(4, "пользователь не был авторизован");
			РезультатЗавершения = Новый Структура();
			РезультатЗавершения.Вставить("Ошибка", Истина);
			РезультатЗавершения.Вставить("ТекстОшибки", Соответствие.Получить(ОтветJSON.error));
		КонецЕсли;
	КонецЕсли;
	
	ssid = "";
	
	Возврат РезультатЗавершения;
		
КонецФункции // ЗавершитьСессиюПодключенияWialonPro()

// Выполним обращение к сервису WialonPro получим таблицу всех устройств.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  ssid  - Строка - ID сессии.
//  ListItems  - Массив - Список ID объектов.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьСписокУстройствWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid, ListItems) Экспорт
	
	ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
	
	ТаблицаЗначений.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов("Строка"),"Уникальный идентификатор");
	ТаблицаЗначений.Колонки.Добавить("ТипТС",Новый ОписаниеТипов("Строка"),"Тип транспортного средства"); 
	ТаблицаЗначений.Колонки.Добавить("Телефон",Новый ОписаниеТипов("Строка"),"Телефон");
	ТаблицаЗначений.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка"),"Название объекта мониторинга");
	ТаблицаЗначений.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"Транспортное средство");
	
	Если НЕ ListItems.Количество() Тогда		
		Возврат ТаблицаЗначений;
	КонецЕсли;
	
	МассивЗапросов = Новый Массив;
	Для каждого ЭтотОбъектСервиса Из ListItems Цикл
		//Имя объекта
		Структура = Новый Структура();
		Структура.Вставить("svc", "core/search_item");
		Данные = Новый Структура();
		Данные.Вставить("itemId", ЭтотОбъектСервиса);
		Данные.Вставить("flags", "1");
		Структура.Вставить("params", Данные);
		МассивЗапросов.Добавить(Структура);
		// Телефон объекта
		Структура = Новый Структура();
		Структура.Вставить("svc", "core/search_item");
		Данные = Новый Структура();
		Данные.Вставить("itemId", ЭтотОбъектСервиса);
		Данные.Вставить("flags", "256");
		Структура.Вставить("params", Данные);
		МассивЗапросов.Добавить(Структура);
	КонецЦикла;
	
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonPro(Сервер, Порт, ЗащищенноеСоединение, "", МассивЗапросов, "core/batch", ssid);
	
	Если НЕ ОтветJSON=Неопределено Тогда
		Если ТипЗнч(ОтветJSON)=ТИП("Массив") Тогда
			Для каждого ТекущаяСтрокаОтвета Из ОтветJSON Цикл
				Если ТекущаяСтрокаОтвета.Свойство("nm") Тогда
					НоваяСтрока = ТаблицаЗначений.Добавить();
					НоваяСтрока.Идентификатор = ТекущаяСтрокаОтвета.id;
					НоваяСтрока.Наименование = ТекущаяСтрокаОтвета.nm;
				ИначеЕсли ТекущаяСтрокаОтвета.Свойство("ph") Тогда
					НоваяСтрока.Телефон = ТекущаяСтрокаОтвета.ph;
					НоваяСтрока.ТипТС = ТекущаяСтрокаОтвета.uid;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
		ОтветJSON = Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если ТаблицаЗначений.Количество() Тогда
		Результат = ТаблицаЗначений.Скопировать();
		ТаблицаЗначений.Очистить();
		ТаблицаЗначений = Неопределено;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокУстройствWialonPro()

// Выполним обращение к сервису WialonPro.
//
// Параметры:
//  Сервер  - Выборка запроса - Элемент выборки.
//
Процедура ПолучениеДанныхОтWialonPro(Сервер)	
	
	ИнформационнаяБазаФайловая = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая()=0,ЛОЖЬ,ИСТИНА);
	
	//ВыполнятьЛогированиеПоступившихДанных = Истина;
	ВыполнятьЛогированиеПоступившихДанных = Ложь; // не создаем логи скрыто от пользователя.
	
	Если НЕ ИнформационнаяБазаФайловая Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода","упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхWialonPro");	 
		Отбор.Вставить("Состояние",СостояниеФоновогоЗадания.Активно);
		Отбор.Вставить("Наименование","ПолучениеДанныхОтWialonPro");
		МассивФЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		Если МассивФЗаданий.Количество() Тогда
			// С предыдущего запуска выполняются фоновые задания.
			// Придется ждать пока они не будут завершины.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Сервер.Сервер = "" ИЛИ Сервер.Порт = 0 Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonPro'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не настроены параметры подключения к серверу сбора данных.'"));
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".txt");	
	ИмяКаталога = "";
	ИмяКаталога_ТекущейСессии = "";
	
	Если ВыполнятьЛогированиеПоступившихДанных Тогда
		
		стрСоед = СтрокаСоединенияИнформационнойБазы();
		
		Массив = СтрРазделить(стрСоед,";",Ложь);
		
		ТекущееИмяБазы = "";
		Для Каждого текЭлемент Из Массив Цикл
			
			Если СтрНайти(текЭлемент,"Ref=") > 0 Тогда
				ТекущееИмяБазы = СтрЗаменить(текЭлемент,"Ref=","");
				ТекущееИмяБазы = СтрЗаменить(ТекущееИмяБазы,Символ(34),"");
			КонецЕсли;
			
		КонецЦикла;
		
		ИмяФайла = ПолучитьИмяВременногоФайла(".txt");
		
		ИмяКаталога = КаталогВременныхФайлов() + "Log_WialonPro" + ?(ПустаяСтрока(ТекущееИмяБазы),"","_base_" + ТекущееИмяБазы);
		
		КаталогНаДиске = Новый Файл(ИмяКаталога);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ИмяКаталога);
		КонецЕсли; 
		
		ИмяКаталога_ТекущейСессии = ИмяКаталога + "\" + Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMddHHmmss");
		
		СоздатьКаталог(ИмяКаталога_ТекущейСессии);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonPro'"), УровеньЖурналаРегистрации.Информация,,, "Католог логирования на серевере: " + ИмяКаталога_ТекущейСессии);
		
	КонецЕсли;
	
	ssid = "";
	Отсрочка = Сервер.Отсрочка;
	СобытияМониторинга = Сервер.СобытияМониторинга;
	Отказ = Ложь;
	
	Попытка
		Если НЕ ПустаяСтрока(Сервер.Пользователь) Тогда 
			РезультатОбращения = ПолучитьУникальныйИдентификаторСессииWialonPro(Сервер.Сервер, Сервер.Пользователь, Сервер.Пароль, Сервер.Порт, Сервер.ЗащищенноеСоединение, Ложь);
			Если РезультатОбращения=Неопределено Тогда 
				Возврат;
			КонецЕсли;
			ssid = РезультатОбращения.ssid;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		//Запрос.УстановитьПараметр("ИдентификаторыУстройств", МассивТС);
		Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
		Запрос.Текст = ПолучитьТекстЗапросаWialon();
		
		ПакетЗапросов  = Запрос.ВыполнитьПакет();
		Выборка = ПакетЗапросов[3].Выбрать();
		тбзДатчики = ПакетЗапросов[7].Выгрузить();
		
		МассивОбъектов = Новый Массив;
		ДатаЗапуска = ТекущаяДатаСеанса();
		КоличествоСекундЗагрузки = 86400; // 24*60*60 сутки.
		Пока Выборка.Следующий() Цикл
			
			ТекущаяУстройство = Выборка.ИдентификаторУстройства;
			СтруктураПериода = ПолучитьИнтервалЗапросаДанных(Выборка.Период, Выборка.ПериодД, ДатаЗапуска, КоличествоСекундЗагрузки, Отсрочка);
			
			НачалоПериода = СтруктураПериода.НачалоПериода;
			НачалоПериода_Датчика = СтруктураПериода.НачалоПериода_Датчика;
			КонецПериода =  СтруктураПериода.КонецПериода;
			КонецПериода_Датчика = СтруктураПериода.КонецПериода_Датчика;
			
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство,Выборка.Трекер);
			
			ОтборДатчиков = Новый Структура();
			ОтборДатчиков.Вставить("Трекер", Выборка.Трекер);
			СоответствиеДатчиков = Неопределено;
			НайденныеДатчики = тбзДатчики.НайтиСтроки(ОтборДатчиков);
			Если НайденныеДатчики.Количество() Тогда
				СоответствиеДатчиков = Новый Соответствие;
				Для каждого ТекущийДатчик Из НайденныеДатчики Цикл
					Если ПустаяСтрока(ТекущийДатчик.НомерДатчика) Тогда
						Продолжить;
					КонецЕсли;
					СтруктураДатчика = Новый Структура();
					СтруктураДатчика.Вставить("Датчик", ТекущийДатчик.Датчик);
					СтруктураДатчика.Вставить("Назначение", ТекущийДатчик.Назначение);
					СтруктураДатчика.Вставить("КалибровочныйГрафик", ТекущийДатчик.КалибровочныйГрафик);
					СтруктураДатчика.Вставить("Трекер", Выборка.Трекер);
					СтруктураДатчика.Вставить("ТранспортноеСредство", ТекущийДатчик.ТранспортноеСредство);
					СоответствиеДатчиков.Вставить(ТекущийДатчик.НомерДатчика, СтруктураДатчика);
				КонецЦикла;
			КонецЕсли;
			
			Попытка
				ДанныеОтвета = ПолучитьОнлайнДанныеWialonPro(ТекущаяУстройство, СоответствиеДатчиков, НачалоПериода, КонецПериода, Сервер.Сервер, Сервер.Порт, Сервер.ЗащищенноеСоединение, ssid);
			Исключение				
				Данные = Неопределено;
				СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации("ПолучениеДанныхОтWialonPro:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
			КонецПопытки;
			
			Если ТипЗнч(ДанныеОтвета) = Тип("Структура") Тогда
				Данные = ДанныеОтвета.Рейсы;
				СтруктураОбъектов.Вставить("Датчики", ДанныеОтвета.Датчики);
			Иначе
				Данные = ДанныеОтвета;
			КонецЕсли;
			
			Если Данные = Неопределено Тогда
				Данные = Новый СписокЗначений;
				ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековWialonPro();
				НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
				НовЗаписьТрекера.Период   			 = КонецПериода;
				НовЗаписьТрекера.Широта              = 0;
				НовЗаписьТрекера.Долгота             = 0;
				НовЗаписьТрекера.Скорость            = 0;
				НовЗаписьТрекера.Высота              = 0;
				НовЗаписьТрекера.НаправлениеДвижения = 0;
				Данные.Добавить(ТаблицаЗначенийТреков);
			КонецЕсли;
			СтруктураОбъектов.Вставить("Навигация",Данные);
			МассивОбъектов.Добавить(СтруктураОбъектов);
		КонецЦикла;
		РезультатЗавершения = ЗавершитьСессиюПодключенияWialonPro(Сервер.Сервер, Сервер.Порт, Сервер.ЗащищенноеСоединение, ssid);
		Если НЕ РезультатЗавершения=Неопределено Тогда 
			ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер.Ссылка, РезультатЗавершения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonPro'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ТекстОшибки);
		КонецЕсли;
	Исключение
		Отказ = Истина;
		ОписаниеТекущейОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonPro'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ОписаниеТекущейОшибки);
		Если НЕ ПустаяСтрока(ssid) Тогда 
			РезультатЗавершения = ЗавершитьСессиюПодключенияWialonPro(Сервер.Сервер, Сервер.Порт, Сервер.ЗащищенноеСоединение, ssid);
			Если НЕ РезультатЗавершения=Неопределено Тогда 
				ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер.Ссылка, РезультатЗавершения.ТекстОшибки);
				ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonPro'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецПопытки;
	
	Если НЕ Отказ Тогда
		КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковЗагрузкиССервераСбораДанных");
		Если КоличествоПотоков = 0 ИЛИ ИнформационнаяБазаФайловая Тогда 
			КоличествоПотоков = 1;
		КонецЕсли;
		
		Если ИнформационнаяБазаФайловая Тогда
			СоздатьПотокОбработкиЗагрузкиДанныхWialonPro(МассивОбъектов);
		Иначе
			МассивПотоков = Новый Массив;
			Если КоличествоПотоков = 1 Тогда
				МассивПотоков.Добавить(МассивОбъектов);
			Иначе
				ОбъектовВПотоке = Окр(МассивОбъектов.Количество() / КоличествоПотоков,0,РежимОкругления.Окр15как10);
				ТекущееКоличество = 0;
				ОбъектыПотока = Новый Массив;
				Для Каждого текЭлемент Из МассивОбъектов Цикл
					ОбъектыПотока.Добавить(текЭлемент);
					ТекущееКоличество = ТекущееКоличество + 1;
					Если ТекущееКоличество >= ОбъектовВПотоке Тогда
						ТекущееКоличество = 0;
						МассивПотоков.Добавить(ОбъектыПотока);
						ОбъектыПотока = Новый Массив;
					КонецЕсли;
				КонецЦикла;
				// Допишем крайний поток ТС которые остались.
				Если ОбъектыПотока.Количество() Тогда
					ТекущийМассив = МассивПотоков[МассивПотоков.ВГраница()];
					Для Каждого текЭлемент Из ОбъектыПотока Цикл
						ТекущийМассив.Добавить(текЭлемент);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого текЭлемент Из МассивПотоков Цикл
				Если текЭлемент.Количество() Тогда 
					мсвПараметров = Новый Массив(1);
					мсвПараметров[0] = текЭлемент;
					упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхWialonPro", мсвПараметров,,,"ПолучениеДанныхОтWialonPro");
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПолучениеДанныхОтWialonPro()

// Выполним создание разных потоков обработки.
//
// Параметры:
//  ДанныеОбработки  - Массив - Данные потока.
//
Процедура СоздатьПотокОбработкиЗагрузкиДанныхWialonPro(ДанныеОбработки) Экспорт
	
	Для Каждого текЭлемент Из ДанныеОбработки Цикл
		ВыполнитьОбработкуЗагруженныхДанныхWialonPro(текЭлемент);
	КонецЦикла;
	
КонецПроцедуры // СоздатьПотокОбработкиЗагрузкиДанныхWialonPro()

// Выполним обработку каждого объекта.
//
//
// Параметры:
//  ДанныеОбработки - Массив - Текщий элемент данных.
//
Процедура ВыполнитьОбработкуЗагруженныхДанныхWialonPro(ДанныеОбработки)
	
	ТекущийТерминал = ДанныеОбработки.Трекер;
	ТаблицаЗначенийДатчиков = Неопределено;
	ТекущиеПараметрыЛогирования = Неопределено;
	НачалоПериодаОбработки = ТекущаяДатаСеанса();
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда
		ТекущиеПараметрыЛогирования = Новый Структура();
		ТекущиеПараметрыЛогирования.Вставить("КаталогЛогТрек",ДанныеОбработки.КаталогЛогТрек);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода",ДанныеОбработки.НачалоПериода);
		ТекущиеПараметрыЛогирования.Вставить("НачалоПериода_Датчика",ДанныеОбработки.НачалоПериода_Датчика);
		ТекущиеПараметрыЛогирования.Вставить("КонецПериода",ДанныеОбработки.КонецПериода);
	КонецЕсли;
	Если ДанныеОбработки.Свойство("Датчики") Тогда
		ТаблицаЗначенийДатчиков = ДанныеОбработки.Датчики;
	КонецЕсли;
	Если ДанныеОбработки.Свойство("Навигация") И НЕ ДанныеОбработки.Навигация = Неопределено Тогда
		РазобратьТрекиWialonPro(ДанныеОбработки.Навигация,ТекущийТерминал,ТаблицаЗначенийДатчиков,ТекущиеПараметрыЛогирования);
	КонецЕсли;
	//Если ДанныеОбработки.Свойство("СобытияМониторинга") Тогда
	//	СобытияМониторинга = ДанныеОбработки.СобытияМониторинга;
	//	ДатчикТоплива = ДанныеОбработки.ДатчикТоплива;
	//	Назначение = ДанныеОбработки.Назначение;
	//	КалибровочныйГрафик = ДанныеОбработки.КалибровочныйГрафик;
	//	НачалоПериода = ДанныеОбработки.ПериодСобытияМониторинга.НачалоПериода;
	//	КонецПериода = ДанныеОбработки.ПериодСобытияМониторинга.КонецПериода;
	//	ТаблицаЗначенийДатчиков = РазобратьСобытияМониторингаWialonPro(СобытияМониторинга, ТекущийТерминал, ДатчикТоплива, Назначение, КалибровочныйГрафик, НачалоПериода, КонецПериода);
	//КонецЕсли;
	Если НЕ ТаблицаЗначенийДатчиков = Неопределено И ТаблицаЗначенийДатчиков.Количество() Тогда
		соотТрекеров = Новый Соответствие;
		ПустаяДата = Дата("00010101000000");
		ОтборТС = Новый Структура("ТранспортноеСредство", ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка"));
		НайднныеСтроки = ТаблицаЗначенийДатчиков.НайтиСтроки(ОтборТС);
		Если НайднныеСтроки.Количество() Тогда
			Для каждого ТекущаяСтрока Из НайднныеСтроки Цикл
				ТаблицаЗначенийДатчиков.Удалить(ТекущаяСтрока);
			КонецЦикла;
		КонецЕсли;
		Если ТаблицаЗначенийДатчиков.Количество() Тогда
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначенийДатчиков);
			Запрос.Выполнить();
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Период КАК ДАТА) КАК Период,
			|	ВЫРАЗИТЬ(вртДанных.Трекер КАК Справочник.упМобильныеУстройстваИТрекеры) КАК Трекер,
			|	ВЫРАЗИТЬ(вртДанных.КалибровочныйГрафик КАК Справочник.упКалибровочныеГрафики) КАК КалибровочныйГрафик,
			|	ВЫРАЗИТЬ(вртДанных.Датчик КАК Справочник.упДатчики) КАК Датчик,
			|	ВЫРАЗИТЬ(вртДанных.ТранспортноеСредство КАК Справочник.упТранспортныеСредства) КАК ТранспортноеСредство,
			|	ВЫРАЗИТЬ(вртДанных.Рейс КАК Документ.упРейс) КАК Рейс,
			|	ВЫРАЗИТЬ(вртДанных.Значение КАК ЧИСЛО(10, 3)) КАК Значение,
			|	ВЫРАЗИТЬ(вртДанных.Назначение КАК Справочник.упНазначенияДатчиков) КАК Назначение
			|ПОМЕСТИТЬ втДанные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втДанные.Период КАК Период,
			|	втДанные.Трекер КАК Трекер,
			|	втДанные.КалибровочныйГрафик КАК КалибровочныйГрафик,
			|	втДанные.Датчик КАК Датчик,
			|	втДанные.ТранспортноеСредство КАК ТранспортноеСредство,
			|	втДанные.Рейс КАК Рейс,
			|	втДанные.Значение КАК Значение,
			|	втДанные.Назначение КАК Назначение
			|ИЗ
			|	втДанные КАК втДанные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков КАК упДанныеДатчиков
			|		ПО втДанные.Период = упДанныеДатчиков.Период
			|			И втДанные.Трекер = упДанныеДатчиков.Трекер
			|			И втДанные.Датчик = упДанныеДатчиков.Датчик
			|			И втДанные.ТранспортноеСредство = упДанныеДатчиков.ТранспортноеСредство
			|			И втДанные.Значение = упДанныеДатчиков.Значение
			|ГДЕ
			|	упДанныеДатчиков.ТранспортноеСредство ЕСТЬ NULL И (НЕ втДанные.Датчик=ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка) И НЕ втДанные.ТранспортноеСредство=ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Трекер, ТранспортноеСредство, Датчик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ";
			Выборка = Запрос.Выполнить();
			тбзКопия = Неопределено;
			Если НЕ Выборка.Пустой() Тогда
				тбзКопия = Выборка.Выгрузить();
			КонецЕсли;
			Запрос.МенеджерВременныхТаблиц.Закрыть();
			Если ТипЗнч(тбзКопия) = ТИП("ТаблицаЗначений") И тбзКопия.Количество() Тогда
				упИнтеграцияССерверамиСбораДанных.ВыполнитьОбработкуЗначенийДатчиковНабор(тбзКопия);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОбработки.Свойство("КаталогЛогТрек") Тогда
		Если НЕ ПустаяСтрока(ДанныеОбработки.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ДанныеОбработки.КаталогЛогТрек + "\Данные продолжительности записи данных в базу.txt";
			
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			
			ТекстовыйДокумент.ДобавитьСтроку("Начало периода записи: " + Строка(НачалоПериодаОбработки));
			ТекстовыйДокумент.ДобавитьСтроку("Конец периода записи: " + Строка(ТекущаяДатаСеанса()));
			
			ТекстовыйДокумент.Записать(ИмяФайлаЗаписи);
			ТекстовыйДокумент = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбработкуЗагруженныхДанныхWialonPro()

// Выполним обращение к сервису WialonPro получим данные по периодам.
//
// Параметры:
//  ИдентификаторТС - Строка - Идентификатор устройства на сервере.
//  СписокДатчиков - Соотвествие - Список Датчиков Устройства.
//  НачалоПериода - Дата - Начало периода получения данных.
//  КонецПериода - Дата - Конец периода получения данных.
//  АдресСервиса - Строка - Адрес сервиса.
//  Порт - Число - Номер порта для подключения к сервису.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  ssid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьОнлайнДанныеWialonPro(ИдентификаторТС, СписокДатчиков, НачалоПериода, КонецПериода, АдресСервиса, Порт, ЗащищенноеСоединение, ssid)
	
	Данные = Новый Структура;
	Данные.Вставить("itemId", ИдентификаторТС);
	Данные.Вставить("type", "1");
	Данные.Вставить("ival1", ПеревестиВUnixtime(НачалоПериода));
	Данные.Вставить("ival2", ПеревестиВUnixtime(КонецПериода));
	Данные.Вставить("flags", "0");
	Данные.Вставить("flagsMask", "0");
	Данные.Вставить("loadLocations", "0");
	Данные.Вставить("loadCount", "0xffffffff");
	//Чтобы загрузить, а затем работать с сообщениями в сессии есть встроенный объект – загрузчик сообщений.	
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonPro(АдресСервиса, Порт, ЗащищенноеСоединение, "", Данные, "messages/load_interval", ssid);
	
	ЗаполнитьДатчики = Ложь;
	ЗаписиДатчиков = Неопределено;
	Если НЕ СписокДатчиков = Неопределено И НЕ ОтветJSON=Неопределено Тогда
		Если ОтветJSON.Свойство("count") Тогда
			ЗаполнитьДатчики = Истина;
			Данные = Новый Структура;
			Данные.Вставить("lname", "");
			Данные.Вставить("rname", "");
			Данные.Вставить("indexFrom", "0");
			Данные.Вставить("indexTo", Формат(ОтветJSON.count-1, "ЧН=0; ЧГ=0"));
			Данные.Вставить("unitId", ИдентификаторТС);
			Данные.Вставить("sensorId", "0");
			// Чтобы получить значения датчиков.
			ОтветJSON_Датчики = HTTP_POSTЗапросКСервисуWialonPro(АдресСервиса, Порт, ЗащищенноеСоединение, "", Данные, "unit/calc_sensors", ssid, Истина);
			Если ТипЗнч(ОтветJSON_Датчики)=ТИП("Массив") Тогда
				ЗаписиДатчиков = Новый Соответствие;
				Сч=0;
				Для каждого ТекущаяСтрокаОтвета Из ОтветJSON_Датчики Цикл
					ЗаписиДатчиков.Вставить(Сч, ТекущаяСтрокаОтвета);
					Сч=Сч+1;
				КонецЦикла;
				ОтветJSON_Датчики.Очистить();
				ОтветJSON_Датчики = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//Если возникла необходимость загрузить сообщения за другой интервал, то перед выполнением запроса нужно очистить загрузчик сообщений: 
	Данные = Новый Структура;
	ОтветJSON_Сообщений = HTTP_POSTЗапросКСервисуWialonPro(АдресСервиса, Порт, ЗащищенноеСоединение, "", Данные, "messages/unload", ssid);
	
	СписокСообщений = Новый СписокЗначений;
	Если НЕ ОтветJSON=Неопределено Тогда
		ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековWialonPro();
		Если ОтветJSON.Свойство("count") Тогда
			Если ОтветJSON.count Тогда
				Unixtime = ДАТА("19700101000000");
				ИдентификаторЧасовогоПояса = ПолучитьЧасовойПоясИнформационнойБазы();
				МассивСообщений = ОтветJSON.messages;
				Сч=0;
				НеизвестноЗначение = НеизвестноЗначениеWialon();
				Если ЗаполнитьДатчики Тогда
					ТаблицаЗначенийДатчиков = СоздатьТаблицуДанныхДатчиковWialonPro();
				КонецЕсли;
				Интервал = 60; // Интервал получения данных по датчику, секунд.
				ПредыдущийПериод = ДАТА("00010101000000");
				Для каждого ТекущаяСтрокаОтвета Из МассивСообщений Цикл
					Сч=Сч+1;
					
					Если ТекущаяСтрокаОтвета.Свойство("y") Тогда
						Широта = ТекущаяСтрокаОтвета.y; //"y":<double>,/* широта */
					Иначе
						Продолжить;
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(Широта) Тогда
						Продолжить;
					КонецЕсли;
					
					Попытка
						ПериодGMT = Unixtime + ТекущаяСтрокаОтвета.t; //"t":<uint>,/* время UTC */
						Период = МестноеВремя(ПериодGMT, ИдентификаторЧасовогоПояса);
					Исключение
						Продолжить;
					КонецПопытки;
					
					Если ЗаполнитьДатчики Тогда
						Если (Период-ПредыдущийПериод)>=Интервал Тогда
							ПредыдущийПериод = Период;
							ТекущаяЗаписьДатчиков = ЗаписиДатчиков.Получить(Сч-1);
							Если НЕ ТекущаяЗаписьДатчиков=Неопределено Тогда
								Для каждого ТекущийДатчик Из СписокДатчиков Цикл
									ТекущееЗначениеДатчика = ТекущаяЗаписьДатчиков.Получить(ТекущийДатчик.Ключ);
									Если НЕ ТекущееЗначениеДатчика=Неопределено Тогда
										Если ТекущееЗначениеДатчика=НеизвестноЗначение Тогда
											Продолжить;
										КонецЕсли;
										НоваяЗаписьДатчика = ТаблицаЗначенийДатчиков.Добавить();
										ЗаполнитьЗначенияСвойств(НоваяЗаписьДатчика, ТекущийДатчик.Значение);
										НоваяЗаписьДатчика.Период = Период;
										НоваяЗаписьДатчика.Значение = ТекущееЗначениеДатчика;
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
					НовЗаписьТрекера.Период   			 = Период;
					НовЗаписьТрекера.Широта              = Широта;
					НовЗаписьТрекера.Долгота             = ТекущаяСтрокаОтвета.x; //"x":<double>,/* долгота */
					НовЗаписьТрекера.Скорость            = ТекущаяСтрокаОтвета.s; //"s":<int>,/* скорость */
					НовЗаписьТрекера.Высота              = ТекущаяСтрокаОтвета.z; //"z":<int>,/* высота над уровнем моря */
					НовЗаписьТрекера.НаправлениеДвижения = ТекущаяСтрокаОтвета.c; //"c":<int>,/* курс */
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		ОтветJSON = Неопределено;
		Если ТаблицаЗначенийТреков.Количество() Тогда
			СписокСообщений.Добавить(ТаблицаЗначенийТреков);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СписокСообщений) = ТИП("СписокЗначений") Тогда
		Если СписокСообщений.Количество()=0 Тогда
			СписокСообщений = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнитьДатчики Тогда
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("Рейсы", СписокСообщений);
		Если НЕ ТаблицаЗначенийДатчиков.Количество() Тогда
			Для каждого ТекущаяЗаписьДатчика Из СписокДатчиков Цикл
				НоваяЗаписьДатчика = ТаблицаЗначенийДатчиков.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗаписьДатчика, ТекущаяЗаписьДатчика);
				НоваяЗаписьДатчика.Период = КонецПериода;
				НоваяЗаписьДатчика.Значение = Неопределено;
			КонецЦикла;
		КонецЕсли;
		СтруктураВозврата.Вставить("Датчики", ТаблицаЗначенийДатчиков);
		Возврат  СтруктураВозврата;
	Иначе	
		Возврат  СписокСообщений;
	КонецЕсли;
	
КонецФункции // ПолучитьОнлайнДанныеWialonPro()

Функция СоздатьТаблицуДанныхДатчиковWialonPro()
	
	ТаблицаЗначенийДатчиков = Новый ТаблицаЗначений;
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Трекер",Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"),"Трекер");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("КалибровочныйГрафик",Новый ОписаниеТипов("СправочникСсылка.упКалибровочныеГрафики"),"КалибровочныйГрафик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Датчик",Новый ОписаниеТипов("СправочникСсылка.упДатчики"),"Датчик");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Рейс",Новый ОписаниеТипов("ДокументСсылка.упРейс"),"Рейс");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Значение",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Значение");
	ТаблицаЗначенийДатчиков.Колонки.Добавить("Назначение",Новый ОписаниеТипов("СправочникСсылка.упНазначенияДатчиков"),"Назначение");
	
	Возврат ТаблицаЗначенийДатчиков.Скопировать();
	
КонецФункции // СоздатьТаблицуДанныхДатчиковWialonPro()

Функция СоздатьТаблицуДанныхТрековWialonPro()
	
	ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековСКАУТ();
	
	Возврат ТаблицаЗначенийТреков.Скопировать();
	
КонецФункции // СоздатьТаблицуДанныхТрековWialonPro()

// Переведем В Unix формат.
//
// Параметры:
//  Период - Дата - Дата перевода.
//
// Возвращаемое значение:
//   Число   - В Unix формат.
//
Функция ПеревестиВUnixtime(Период)
	ИдентификаторЧасовогоПояса = ПолучитьЧасовойПоясИнформационнойБазы();
	Универсальная = УниверсальноеВремя(Период, ИдентификаторЧасовогоПояса);
	Возврат Формат(упСервисныеФункции.ПеревестиДатуВремяВUNIXФормат(Универсальная),"ЧН=0; ЧГ=0");
КонецФункции // ПеревестиВUnixtime()

Функция ПолучитьДанныеПотрекеруWialonProЗаПериод(НачалоПериода, КонецПериода, ТекущийТерминал)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("Трекер", ТекущийТерминал);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уТрек.Период КАК Период,
		|	уТрек.Трекер КАК Трекер,
		|	уТрек.ТранспортноеСредство КАК ТранспортноеСредство
		|ПОМЕСТИТЬ вт_Трекеры
		|ИЗ
		|	РегистрСведений.упУстановленныеТрекеры КАК уТрек
		|ГДЕ
		|	уТрек.Трекер = &Трекер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т1.Трекер КАК Трекер,
		|	Т1.Период КАК НачалоПериода,
		|	ЕСТЬNULL(Т2.Период, &КонецПериода) КАК КонецПериода,
		|	Т1.ТранспортноеСредство КАК ТранспортноеСредство
		|ПОМЕСТИТЬ вт_УТ
		|ИЗ
		|	вт_Трекеры КАК Т1
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Трекеры КАК Т2
		|		ПО Т1.Трекер = Т2.Трекер И Т1.Период < Т2.Период
		|ГДЕ
		|	ВЫБОР КОГДА Т2.ТранспортноеСредство ЕСТЬ NULL ТОГДА ИСТИНА
		|			ИНАЧЕ НЕ Т1.ТранспортноеСредство = Т2.ТранспортноеСредство
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПРСП.Рейс КАК Рейс,
		|	ПРСП.ТранспортноеСредство КАК ТранспортноеСредство
		|ПОМЕСТИТЬ вт_Рейсы
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПРСП
		|ГДЕ
		|	ПРСП.ТранспортноеСредство В (ВЫБРАТЬ вт_УТ.ТранспортноеСредство КАК ТС ИЗ вт_УТ КАК вт_УТ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Рейсы.Рейс КАК Рейс,
		|	вт_Рейсы.ТранспортноеСредство КАК ТранспортноеСредство,
		|	упСтатусыРейсов.Период КАК Период,
		|	упФактическиеПараметрыРейса.НачалоВыполнения КАК НачалоВыполнения
		|ПОМЕСТИТЬ вт_ПР
		|ИЗ
		|	вт_Рейсы КАК вт_Рейсы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов КАК упСтатусыРейсов
		|		ПО вт_Рейсы.Рейс = упСтатусыРейсов.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
		|		ПО вт_Рейсы.Рейс = упФактическиеПараметрыРейса.Рейс
		|ГДЕ
		|	НЕ упСтатусыРейсов.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.НазначеноТС), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.СформированыЗаявкиНаТС), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КПланированиюТС), ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Новый))
		|	И упФактическиеПараметрыРейса.НачалоВыполнения ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т3.Рейс КАК Рейс,
		|	Т3.ТранспортноеСредство КАК ТранспортноеСредство,
		|	МИНИМУМ(Т3.Период) КАК НачалоПериода,
		|	МАКСИМУМ(Т4.Период) КАК КонецПериода,
		|	1 КАК Рейтинг
		|ПОМЕСТИТЬ вт_Интер
		|ИЗ
		|	вт_ПР КАК Т3
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ПР КАК Т4
		|		ПО Т3.Рейс = Т4.Рейс И Т3.Период < Т4.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	Т3.Рейс, Т3.ТранспортноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_Рейсы.Рейс,
		|	вт_Рейсы.ТранспортноеСредство,
		|	ФПР2.НачалоВыполнения,
		|	ФПР2.ОкончаниеВыполнения,
		|	2
		|ИЗ
		|	вт_Рейсы КАК вт_Рейсы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФПР2
		|		ПО вт_Рейсы.Рейс = ФПР2.Рейс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Интер.Рейс КАК Рейс,
		|	вт_Интер.ТранспортноеСредство КАК ТранспортноеСредство,
		|	вт_Интер.НачалоПериода КАК НачалоПериода,
		|	ВЫБОР КОГДА вт_Интер.Рейтинг = 1 ТОГДА ДОБАВИТЬКДАТЕ(&КонецПериода, СЕКУНДА, 1) ИНАЧЕ вт_Интер.КонецПериода КОНЕЦ КАК КонецПериода,
		|	вт_Интер.Рейтинг КАК Рейтинг
		|ПОМЕСТИТЬ вт_ДопП
		|ИЗ
		|	вт_Интер КАК вт_Интер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_УТ.Трекер КАК Трекер,
		|	вт_УТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ЕСТЬNULL(вт_ДопП.НачалоПериода, &НачалоПериода) КАК НачалоВыполнения,
		|	ЕСТЬNULL(вт_ДопП.КонецПериода, &КонецПериода) КАК ОкончаниеВыполнения,
		|	ЕСТЬNULL(вт_ДопП.Рейс, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)) КАК Рейс
		|ИЗ
		|	вт_УТ КАК вт_УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДопП КАК вт_ДопП
		|		ПО вт_УТ.ТранспортноеСредство = вт_ДопП.ТранспортноеСредство
		|			И (ВЫБОР КОГДА &НачалоПериода >= вт_ДопП.НачалоПериода И &КонецПериода < вт_ДопП.КонецПериода ТОГДА ИСТИНА
		|				КОГДА вт_ДопП.НачалоПериода > &НачалоПериода И вт_ДопП.НачалоПериода < &КонецПериода И вт_ДопП.КонецПериода > &КонецПериода ТОГДА ИСТИНА
		|				КОГДА &НачалоПериода > вт_ДопП.НачалоПериода И &НачалоПериода < вт_ДопП.КонецПериода И вт_ДопП.КонецПериода < &КонецПериода ТОГДА ИСТИНА
		|				КОГДА &НачалоПериода < вт_ДопП.НачалоПериода И &КонецПериода > вт_ДопП.КонецПериода ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ)
		|
		|УПОРЯДОЧИТЬ ПО НачалоВыполнения";
	                      
	ТаблицаПериодов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаПериодов;
КонецФункции // ПолучитьДанныеПотрекеруWialonProЗаПериод()

// Выполнить обход полученных данных.
//
// Параметры:
//  СписокТС  - Массив - Список ТС с данными.
//  ТекущийТерминал  - Справочник.упМобильныеУстройстваИТрекеры - Ссылка на терминал.
//  ДанныеДатчиков  - ТаблицаЗначений - Таблица с данными датчиков.
//  ПараметрыЛогирования - Структура - Параметры для записи лога получения данных.
//
Процедура РазобратьТрекиWialonPro(СписокТС, ТекущийТерминал, ДанныеДатчиков = Неопределено,ПараметрыЛогирования=Неопределено)
	
	ЗаписиДанныхТрекеров = Новый ТаблицаЗначений;
	ЗаписиДанныхТрекеров.Колонки.Добавить("Период"                  ,Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)), "Период");
	ЗаписиДанныхТрекеров.Колонки.Добавить("ТранспортноеСредство"    ,Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"), "ТранспортноеСредство");
	ЗаписиДанныхТрекеров.Колонки.Добавить("Высота"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8,3)), "Высота");
	ЗаписиДанныхТрекеров.Колонки.Добавить("НаправлениеДвижения"     ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "НаправлениеДвижения");		
	ЗаписиДанныхТрекеров.Колонки.Добавить("Широта"                  ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Широта");
	ЗаписиДанныхТрекеров.Колонки.Добавить("Долгота"                 ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(9,6)), "Долгота");
	ЗаписиДанныхТрекеров.Колонки.Добавить("ПредставлениеКоординаты" ,Новый ОписаниеТипов("Строка"));
	ЗаписиДанныхТрекеров.Колонки.Добавить("Скорость"                ,Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)), "Скорость");
	ЗаписиДанныхТрекеров.Колонки.Добавить("Трекер"                  ,Новый ОписаниеТипов("СправочникСсылка.упМобильныеУстройстваИТрекеры"), "Трекер");
	ЗаписиДанныхТрекеров.Колонки.Добавить("Рейс"                    ,Новый ОписаниеТипов("ДокументСсылка.упРейс"), "Рейс");
	
	соотТрекеров = Новый Соответствие;
	
	ВыполнитьЗаписьДанныхДатчиков = НЕ ДанныеДатчиков = Неопределено;
	Для каждого ОтрезокДанных Из СписокТС Цикл
		ТаблицаЗначений = ОтрезокДанных.Значение;
		КоличествоСтрок = ТаблицаЗначений.Количество();
		Если КоличествоСтрок Тогда
			НачалоПериода = ТаблицаЗначений[0].Период;
			КонецПериода = ТаблицаЗначений[КоличествоСтрок-1].Период;
			ТаблицаПериодов = ПолучитьДанныеПотрекеруWialonProЗаПериод(НачалоПериода, КонецПериода, ТекущийТерминал);
		КонецЕсли;
		Для каждого ТекущаяСтрокаТрека Из ТаблицаЗначений Цикл
			Период = ТекущаяСтрокаТрека.Период;
			ПроверкаПериода = соотТрекеров.Получить(Период);
			Если НЕ ПроверкаПериода=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			текДанныеТС = Новый Структура("ТранспортноеСредство, Рейс, НачалоВыполнения, ОкончаниеВыполнения");
			Для каждого ТекущийПериод Из ТаблицаПериодов Цикл
				Если Период >= ТекущийПериод.НачалоВыполнения И Период <= ТекущийПериод.ОкончаниеВыполнения Тогда
					ЗаполнитьЗначенияСвойств(текДанныеТС, ТекущийПериод);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если НЕ ЗначениеЗаполнено(текДанныеТС.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			НовЗаписьТрекера = ЗаписиДанныхТрекеров.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗаписьТрекера, ТекущаяСтрокаТрека);
			НовЗаписьТрекера.ПредставлениеКоординаты = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеКоординаты(НовЗаписьТрекера.Широта, НовЗаписьТрекера.Долгота);
			НовЗаписьТрекера.ТранспортноеСредство 	 = текДанныеТС.ТранспортноеСредство; 
			НовЗаписьТрекера.Трекер 				 = ТекущийТерминал;
			НовЗаписьТрекера.Рейс                    = текДанныеТС.Рейс;
			соотТрекеров.Вставить(Период, текДанныеТС);
		КонецЦикла;
	КонецЦикла;
	
	Если ВыполнитьЗаписьДанныхДатчиков Тогда
		Для каждого ТекущаяСтрока Из ДанныеДатчиков Цикл
			ВыполнитьКопированиеДанных = Истина;
			ДанныеТСРейса = соотТрекеров.Получить(ТекущаяСтрока.Период);
			Если ДанныеТСРейса=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТранспортноеСредство) Тогда
				ТекущаяСтрока.ТранспортноеСредство = ДанныеТСРейса.ТранспортноеСредство;
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеТСРейса.Рейс) Тогда
				ТекущаяСтрока.Рейс = ДанныеТСРейса.Рейс;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаписиДанныхТрекеров.Сортировать("Период");
	
	Если НЕ ПараметрыЛогирования = Неопределено Тогда
		Если НЕ ПустаяСтрока(ПараметрыЛогирования.КаталогЛогТрек) Тогда
			ИмяФайлаЗаписи = ПараметрыЛогирования.КаталогЛогТрек + "\ЗаписиДанныхТрекеров_" + Строка(ТекущийТерминал) + "_" + ПараметрыЛогирования.НачалоПериода + "_" + ПараметрыЛогирования.КонецПериода + ".txt";		
			//ВыполнитьЗаписьТаблицыЗначенийВФайл(ИмяФайлаЗаписи,ЗаписиДанныхТрекеров);		
		КонецЕсли;
	КонецЕсли;
	
	Период = ДАТА("00010101000000");
	Попытка
		ТекущееТранспортноеСредство=Неопределено;
		НаборЗаписей = РегистрыСведений.упДанныеТрекеров.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		Для каждого тбзСтрока Из ЗаписиДанныхТрекеров Цикл
			Если НЕ ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство Тогда
				ТекущееТранспортноеСредство=тбзСтрока.ТранспортноеСредство;
				Если НаборЗаписей.Количество() Тогда
					Попытка
						НаборЗаписей.Записать(Ложь);
					Исключение
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
						КонецЕсли;
						ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.WialonPro", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;
					Если ТранзакцияАктивна() Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;
					НаборЗаписей.Очистить();
				КонецЕсли;
				Если НЕ ТранзакцияАктивна() Тогда
					НачатьТранзакцию();
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеТрекеров");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТекущееТранспортноеСредство);
					БлокировкаДанных.Заблокировать();
				КонецЕсли;
				НаборЗаписей.Отбор.ТранспортноеСредство.Установить(ТекущееТранспортноеСредство, Истина);
			КонецЕсли;
			НовЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗапись,тбзСтрока);
		КонецЦикла;
		Если НаборЗаписей.Количество() Тогда
			Попытка
				НаборЗаписей.Записать(Ложь);
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.WialonPro", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ЗаписьДанныхТрекера.WialonPro", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.упДанныеТрекеров, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ЗаписиДанныхТрекеров = Неопределено;

КонецПроцедуры // РазобратьТрекиWialonPro()

// Выполним обращение к сервису WialonPro получим таблицу всех датчиков устройств.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  ssid  - Строка - ID сессии.
//  ListItems  - Массив - Список ID объектов.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьСписокДатчиковУстройствWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid, ListItems) Экспорт
	
	ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
	
	ТаблицаЗначений.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов("Строка"),"Уникальный идентификатор");
	ТаблицаЗначений.Колонки.Добавить("Тип",Новый ОписаниеТипов("Строка"),"Тип датчика"); 
	ТаблицаЗначений.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка"),"Название объекта мониторинга");
	ТаблицаЗначений.Колонки.Добавить("Трекер",Новый ОписаниеТипов("Строка"),"Трекер");
	ТаблицаЗначений.Колонки.Добавить("ЕдИзм",Новый ОписаниеТипов("Строка"),"Единица измерения");
	
	Если НЕ ListItems.Количество() Тогда		
		Возврат ТаблицаЗначений;
	КонецЕсли;
	
	МассивЗапросов = Новый Массив;
	Для каждого ЭтотОбъектСервиса Из ListItems Цикл
		//Датчики объекта
		Структура = Новый Структура();
		Структура.Вставить("svc", "core/search_item");
		Данные = Новый Структура();
		Данные.Вставить("itemId", ЭтотОбъектСервиса);
		Данные.Вставить("flags", "2048");
		Структура.Вставить("params", Данные);
		МассивЗапросов.Добавить(Структура);
	КонецЦикла;
	
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonPro(Сервер, Порт, ЗащищенноеСоединение, "", МассивЗапросов, "core/batch", ssid, Истина);
	
	Если НЕ ОтветJSON=Неопределено Тогда
		Если ТипЗнч(ОтветJSON)=ТИП("Массив") Тогда
			Сч = 0;
			Для каждого ТекущаяСтрокаОтвета Из ОтветJSON Цикл
				Трекер = ListItems[Сч];
				sens = ТекущаяСтрокаОтвета.Получить("sens");
				Для каждого ТекущийДатчик Из sens Цикл
					НоваяСтрока = ТаблицаЗначений.Добавить();
					НоваяСтрока.Трекер = Трекер;
					НоваяСтрока.Идентификатор = ТекущийДатчик.Ключ;
					ПоляДатчика = ТекущийДатчик.Значение;
					НоваяСтрока.Наименование = ПоляДатчика.Получить("nm");
					НоваяСтрока.Тип = ПоляДатчика.Получить("tp");
					НоваяСтрока.ЕдИзм = ПоляДатчика.Получить("me");
				КонецЦикла;
				Сч = Сч+1;
			КонецЦикла;
		КонецЕсли;
		ОтветJSON = Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если ТаблицаЗначений.Количество() Тогда
		Результат = ТаблицаЗначений.Скопировать();
		ТаблицаЗначений.Очистить();
		ТаблицаЗначений = Неопределено;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокДатчиковУстройствWialonPro()

Функция НеизвестноЗначениеWialon()

	НеизвестноЗначение = -348201.3876; // если -348201.3876 -- значение неизвестно.
	
	Возврат НеизвестноЗначение;

КонецФункции // НеизвестноЗначениеWialon()

#КонецОбласти

#Область ОбработкаWialonHosting

// Отправим POST запрос к сервису WialonPro.
//
// Параметры:
//  Сервер  - Строка - Адрес сервиса.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  Сервис  - Строка - Имя сервиса обращения.
//  ПараметрыЗапроса  - Произвольный - Параметры запроса к сервису.
//  ТекстЗапроса  - Строка - Текст команды сервиса.
//  sid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   Произвольный   - Результат запроса.
//
Функция HTTP_POSTЗапросКСервисуWialonHosting(Сервер, Порт=0, ЗащищенноеСоединение=Ложь, Сервис=Неопределено, ПараметрыЗапроса, ТекстЗапроса, sid=Неопределено, ПрочитатьВСоответствие=Ложь)

	Тело = СформироватьJSON(ПараметрыЗапроса);
	ЗаголHttp = Новый Соответствие;
	ЗаголHttp.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	ПараметрыПолучения = Новый Структура();
	ПараметрыПолучения.Вставить("Порт", Порт);
	ПараметрыПолучения.Вставить("Заголовки", ЗаголHttp);
	ПараметрыПолучения.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение); //
	
	URLСтрока = СтрШаблон("http://%1%2/ajax.html?svc=%3&params=%4%5", Сервер, ?(Сервис=Неопределено, "", "/" + Сервис), ТекстЗапроса, Тело, ?(sid=Неопределено, "", "&sid="+sid));
	РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
	
	ОтветJSON=Неопределено;
	Если РезультатПолученияФайлов.Статус Тогда
		ФайлРезультата = РезультатПолученияФайлов.Путь;
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.ОткрытьФайл(ФайлРезультата);
		Попытка
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие);
		Исключение
			Попытка
				ОтветJSON = ПрочитатьJSON(ЧтениеJSON, Истина);
			Исключение
				ТекстФайл = Новый ТекстовыйДокумент;
				ТекстФайл.Прочитать(ФайлРезультата, "UTF-8");
				ТекстJSON = ТекстФайл.ПолучитьТекст();
				ЧтениеJSON = Новый ЧтениеJSON();
				ЧтениеJSON.УстановитьСтроку(ТекстJSON);
				Попытка
					ОтветJSON = ПрочитатьJSON(ЧтениеJSON, Истина);
				Исключение
					ОтветJSON=Неопределено;
				КонецПопытки;
			КонецПопытки;
		КонецПопытки;
		ЧтениеJSON.Закрыть();		
		УдалитьФайлы(ФайлРезультата); // Удалим файл с результатом запроса.
	КонецЕсли;

	Возврат ОтветJSON;

КонецФункции // HTTP_POSTЗапросКСервисуWialonHosting()

// Для выполнения любой операции в WialonHosting вам понадобится авторизироваться и получить ID сессии.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Токен  - Строка - Токен пользователя.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//
// Возвращаемое значение:
//   Строка - ID сессии.
//
Функция ПолучитьУникальныйИдентификаторСессииWialonHosting(Сервер, Токен, Порт, ЗащищенноеСоединение) Экспорт
	
	СтруктураВозврата=Неопределено;
	Данные = Новый Структура;
	Данные.Вставить("token", Токен);
	Данные.Вставить("fl", "0x1");
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonHosting(Сервер, Порт, ЗащищенноеСоединение, "wialon", Данные, "token/login");
	Если НЕ ОтветJSON=Неопределено Тогда
		Если ОтветJSON.Свойство("eid") Тогда
			sid = ОтветJSON.eid;
			СтруктураВозврата = Новый Структура();
			СтруктураВозврата.Вставить("sid",sid);
		ИначеЕсли ОтветJSON.Свойство("error") Тогда
			Если НЕ ОтветJSON.error=0 Тогда
				Соответствие = ВозможныеОшибкиWialon();
				ВариантОшибки = Соответствие.Получить(ОтветJSON.error);
				РезультатЗавершения = Новый Структура();
				РезультатЗавершения.Вставить("Ошибка", Истина);
				МассивОшибок = Новый Массив;
				Если НЕ ВариантОшибки=Неопределено Тогда
					МассивОшибок.Добавить(ВариантОшибки);
				КонецЕсли;
				Если ОтветJSON.Свойство("reason")И НЕ ПустаяСтрока(ОтветJSON.reason) Тогда
					МассивОшибок.Добавить(ОтветJSON.reason);
				КонецЕсли;
				ТекстОшибки = СтрСоединить(МассивОшибок, "; ");
				РезультатЗавершения.Вставить("ТекстОшибки", ТекстОшибки);
				ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonHosting'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, , СтрШаблон(НСтр("ru = '%1.'"), ТекстОшибки));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат СтруктураВозврата;
		
КонецФункции // ПолучитьУникальныйИдентификаторСессииWialonHosting()

Функция ВозможныеОшибкиWialon()
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(0, "Удачное выполнение операции (например при логауте это будет успешный выход)");
	Соответствие.Вставить(1, "Недействительная сессия");
	Соответствие.Вставить(2, "Неверное имя сервиса");
	Соответствие.Вставить(3, "Неверный результат");
	Соответствие.Вставить(4, "Неверный ввод, пользователь не был авторизован");
	Соответствие.Вставить(5, "Ошибка выполнения запроса");
	Соответствие.Вставить(6, "Неизвестная ошибка, Сервер недоступен");
	Соответствие.Вставить(7, "Доступ запрещен");
	Соответствие.Вставить(8, "Неверный пароль или имя пользователя");
	Соответствие.Вставить(9, "Сервер авторизации недоступен, пожалуйста попробуйте повторить запрос позже");
	Соответствие.Вставить(10, "Превышен лимит одновременных запросов");
	Соответствие.Вставить(11, "Ошибка во время выполнения запроса на сброс пароля");
	Соответствие.Вставить(14, "Ошибка биллинга");
	Соответствие.Вставить(1001, "Нет сообщений для выбранного интервала");
	Соответствие.Вставить(1002, "Элемент с таким уникальным свойством уже существует или Невозможно создать элемент в связи с ограничениями биллинга");
	Соответствие.Вставить(1003, "Только один запрос разрешается в данный момент времени");
	Соответствие.Вставить(1004, "Превышено ограничение по числу сообщений");
	Соответствие.Вставить(1005, "Ограничение по времени выполнения было превышено");
	Соответствие.Вставить(1006, "Превышение лимита попыток ввода кода двухфакторной авторизации");
	Соответствие.Вставить(1011, "Время сессии истекло либо ваш IP изменился");
	Соответствие.Вставить(2014, "Текущий пользователь не может быть выбран при создании учетной записи");
	Соответствие.Вставить(2015, "Удаление датчика запрещено по причине использования в другом датчике или дополнительных свойствах объекта");
	
	Возврат Соответствие;
	
КонецФункции // ВозможныеОшибкиWialon()

// Для того, чтобы корректно выйти из системы WialonPro Pro.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  sid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   Структура - Результат завершения подключения.
//
Функция ЗавершитьСессиюПодключенияWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid) Экспорт
	
	РезультатЗавершения = Неопределено;
	Данные = Новый Структура;
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonHosting(Сервер, Порт, ЗащищенноеСоединение, "wialon", Данные, "core/logout", sid);
	Если НЕ ОтветJSON=Неопределено Тогда
		Если НЕ ОтветJSON.error=0 Тогда
			Соответствие = ВозможныеОшибкиWialon();
			РезультатЗавершения = Новый Структура();
			РезультатЗавершения.Вставить("Ошибка", Истина);
			РезультатЗавершения.Вставить("ТекстОшибки", Соответствие.Получить(ОтветJSON.error));
		КонецЕсли;
	КонецЕсли;
	
	sid = "";
	
	Возврат РезультатЗавершения;
		
КонецФункции // ЗавершитьСессиюПодключенияWialonHosting()

// Выполним обращение к сервису WialonHosting получим таблицу всех устройств.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  sid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьСписокДатчиковУстройствWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid, ListItems) Экспорт
	
	ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
	
	ТаблицаЗначений.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов("Строка"),"Уникальный идентификатор");
	ТаблицаЗначений.Колонки.Добавить("Тип",Новый ОписаниеТипов("Строка"),"Тип датчика"); 
	ТаблицаЗначений.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка"),"Название объекта мониторинга");
	ТаблицаЗначений.Колонки.Добавить("Трекер",Новый ОписаниеТипов("Строка"),"Трекер");
	ТаблицаЗначений.Колонки.Добавить("ЕдИзм",Новый ОписаниеТипов("Строка"),"Единица измерения");
	
	Если НЕ ListItems.Количество() Тогда		
		Возврат ТаблицаЗначений;
	КонецЕсли;
	// описание датчиков.
	// https://sdk.wialon.com/wiki/ru/pro/remoteapi/apiref/unit/update_sensor
	// описание ключей поиска.
	//https://sdk.wialon.com/wiki/ru/sidebar/remoteapi/apiref/format/unit
	МассивЗапросов = Новый Массив;
	Для каждого ЭтотОбъектСервиса Из ListItems Цикл
		//Датчики объекта
		Структура = Новый Структура();
		Структура.Вставить("svc", "core/search_item");
		Данные = Новый Структура();
		Данные.Вставить("id", ЭтотОбъектСервиса);
		Данные.Вставить("flags", "4096");
		Структура.Вставить("params", Данные);
		МассивЗапросов.Добавить(Структура);
	КонецЦикла;
	
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonHosting(Сервер, Порт, ЗащищенноеСоединение, "wialon", МассивЗапросов, "core/batch", sid, Истина);
	
	Если НЕ ОтветJSON=Неопределено Тогда
		Если ТипЗнч(ОтветJSON)=ТИП("Массив") Тогда
			Сч = 0;
			Для каждого ТекущаяСтрокаОтвета Из ОтветJSON Цикл
				Трекер = ListItems[Сч];
				item = ТекущаяСтрокаОтвета.Получить("item");
				sens = item.Получить("sens");
				Для каждого ТекущийДатчик Из sens Цикл
					ПоляДатчика = ТекущийДатчик.Значение;
					НоваяСтрока = ТаблицаЗначений.Добавить();
					НоваяСтрока.Трекер = Трекер;
					НоваяСтрока.Идентификатор = ПоляДатчика.Получить("id");
					НоваяСтрока.Наименование = ПоляДатчика.Получить("n");
					НоваяСтрока.Тип = ПоляДатчика.Получить("t");
					НоваяСтрока.ЕдИзм = ПоляДатчика.Получить("m");
				КонецЦикла;
				Сч = Сч+1;
			КонецЦикла;
		КонецЕсли;
		ОтветJSON = Неопределено;
	КонецЕсли;
		
	Результат = Неопределено;
	
	Если ТаблицаЗначений.Количество() Тогда
		Результат = ТаблицаЗначений.Скопировать();
		ТаблицаЗначений.Очистить();
		ТаблицаЗначений = Неопределено;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокДатчиковУстройствWialonHosting()

// Выполним обращение к сервису WialonHosting получим таблицу всех устройств.
//
// Параметры:
//  Сервер  - Строка - Имя сервера.
//  Порт  - Число - Номер порта.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  sid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьСписокУстройствWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid) Экспорт
	
	ТаблицаЗначений = Новый ТаблицаЗначений; // Таблица устройств.
	
	ТаблицаЗначений.Колонки.Добавить("Идентификатор",Новый ОписаниеТипов("Строка"),"Уникальный идентификатор");
	ТаблицаЗначений.Колонки.Добавить("ТипТС",Новый ОписаниеТипов("Строка"),"Тип транспортного средства"); 
	ТаблицаЗначений.Колонки.Добавить("Телефон",Новый ОписаниеТипов("Строка"),"Телефон");
	ТаблицаЗначений.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка"),"Название объекта мониторинга");
	ТаблицаЗначений.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"Транспортное средство");
	
	Данные = Новый Структура();
	Данные.Вставить("itemsType", "avl_unit");
	Данные.Вставить("propName", "sys_name");
	Данные.Вставить("propValueMask", "*");
	Данные.Вставить("sortType", "sys_name");
	Структура = Новый Структура();
	Структура.Вставить("spec", Данные);
	Структура.Вставить("force", "1");
	Структура.Вставить("flags", "1");
	Структура.Вставить("from", "0");
	Структура.Вставить("to", "0");
	
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonHosting(Сервер, Порт, ЗащищенноеСоединение, "wialon", Структура, "core/search_items", sid);
	
	Если НЕ ОтветJSON=Неопределено Тогда
		Если ОтветJSON.Свойство("items") Тогда
			//ОтветJSON.totalItemsCount
			Если ТипЗнч(ОтветJSON.items)=ТИП("Массив") Тогда
				Для каждого ТекущаяСтрокаОтвета Из ОтветJSON.items Цикл
					Если ТекущаяСтрокаОтвета.Свойство("nm") Тогда
						НоваяСтрока = ТаблицаЗначений.Добавить();
						НоваяСтрока.Идентификатор = Формат(ТекущаяСтрокаОтвета.id, "ЧН=0; ЧГ=0");
						НоваяСтрока.Наименование = ТекущаяСтрокаОтвета.nm;
					ИначеЕсли ТекущаяСтрокаОтвета.Свойство("ph") Тогда
						НоваяСтрока.Телефон = ТекущаяСтрокаОтвета.ph;
						НоваяСтрока.ТипТС = ТекущаяСтрокаОтвета.uid;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
		КонецЕсли;
		ОтветJSON = Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если ТаблицаЗначений.Количество() Тогда
		Результат = ТаблицаЗначений.Скопировать();
		ТаблицаЗначений.Очистить();
		ТаблицаЗначений = Неопределено;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьСписокУстройствWialonHosting()

// Рассчитаем периоды получения данных.
//
// Параметры:
//  ПериодНТ  - Дата - Начальный период трекера.
//  ПериодНД  - Дата - Начальный период датчика.
//  ДатаЗапуска  - Дата - дата запуска операции.
//  КоличествоСекундЗагрузки  - Число - количество секунд проверки получения данных.
//  Отсрочка  - Число - количество секунд отсрочки получения данных.
//
// Возвращаемое значение:
//   Структура - полученные даты после расчета.
//
Функция ПолучитьИнтервалЗапросаДанных(ПериодНТ, ПериодНД, ДатаЗапуска, КоличествоСекундЗагрузки, Отсрочка = 0)
	
	СтруктураВозврата = Новый Структура();
	
	НачалоПериода = ПериодНТ+1;
	НачалоПериода_Датчика = ПериодНД;
	
	КонецПериода =  ДатаЗапуска;
	КонецПериода_Датчика = ДатаЗапуска;
	
	РазностьДат = КонецПериода-НачалоПериода;
	
	Если РазностьДат > КоличествоСекундЗагрузки Тогда
		КонецПериода =  КонецДня(НачалоПериода + КоличествоСекундЗагрузки) + 3; //  +3 три сегунды чтобы переиод взять за следующий день.
		Если КонецПериода > ДатаЗапуска Тогда
			КонецПериода = ДатаЗапуска;
		КонецЕсли;
	КонецЕсли;
	
	РазностьДат = КонецПериода_Датчика-НачалоПериода_Датчика;
	КонецПериода_ДатчикаПредварительно = КонецДня(НачалоПериода_Датчика + КоличествоСекундЗагрузки) + 3;
	Если (РазностьДат > КоличествоСекундЗагрузки)И(КонецПериода_ДатчикаПредварительно < ДатаЗапуска) Тогда
		КонецПериода_Датчика =  КонецПериода_ДатчикаПредварительно;
	Иначе
		Если НЕ Отсрочка = 0 Тогда
			// Используем отсрочку.
			КонецПериода_Датчика =  КонецПериода_Датчика - (Отсрочка*60*60);
		КонецЕсли;
	КонецЕсли;
	
	ИдентификаторЧасовогоПояса = ПолучитьЧасовойПоясИнформационнойБазы();
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("НачалоПериода", УниверсальноеВремя(НачалоПериода, ИдентификаторЧасовогоПояса));
	СтруктураВозврата.Вставить("КонецПериода", УниверсальноеВремя(КонецПериода, ИдентификаторЧасовогоПояса));
	СтруктураВозврата.Вставить("НачалоПериода_Датчика", УниверсальноеВремя(НачалоПериода_Датчика, ИдентификаторЧасовогоПояса));
	СтруктураВозврата.Вставить("КонецПериода_Датчика", УниверсальноеВремя(КонецПериода_Датчика, ИдентификаторЧасовогоПояса));
	
	Возврат СтруктураВозврата;
	
КонецФункции // ПолучитьИнтервалЗапросаДанных()

// Выполним обращение к сервису WialonHosting.
//
// Параметры:
//  Сервер  - Выборка запроса - Элемент выборки.
//
Процедура ПолучениеДанныхОтWialonHosting(Сервер)	
	
	ИнформационнаяБазаФайловая = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая()=0,ЛОЖЬ,ИСТИНА);
	
	//ВыполнятьЛогированиеПоступившихДанных = Истина;
	ВыполнятьЛогированиеПоступившихДанных = Ложь; // не создаем логи скрыто от пользователя.
	
	Если НЕ ИнформационнаяБазаФайловая Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода","упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхWialonHosting");	 
		Отбор.Вставить("Состояние",СостояниеФоновогоЗадания.Активно);
		Отбор.Вставить("Наименование","ПолучениеДанныхОтWialonHosting");
		МассивФЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		Если МассивФЗаданий.Количество() Тогда
			// С предыдущего запуска выполняются фоновые задания.
			// Придется ждать пока они не будут завершины.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Сервер.Сервер = "" ИЛИ Сервер.Порт = 0 Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonHosting'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.упСервераСбораДанных, Сервер.Ссылка, НСтр("ru = 'Не настроены параметры подключения к серверу сбора данных.'"));
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ПолучитьИмяВременногоФайла(".txt");	
	ИмяКаталога = "";
	ИмяКаталога_ТекущейСессии = "";
	
	Если ВыполнятьЛогированиеПоступившихДанных Тогда
		
		стрСоед = СтрокаСоединенияИнформационнойБазы();
		
		Массив = СтрРазделить(стрСоед,";",Ложь);
		
		ТекущееИмяБазы = "";
		Для Каждого текЭлемент Из Массив Цикл
			
			Если СтрНайти(текЭлемент,"Ref=") > 0 Тогда
				ТекущееИмяБазы = СтрЗаменить(текЭлемент,"Ref=","");
				ТекущееИмяБазы = СтрЗаменить(ТекущееИмяБазы,Символ(34),"");
			КонецЕсли;
			
		КонецЦикла;
		
		ИмяФайла = ПолучитьИмяВременногоФайла(".txt");
		
		ИмяКаталога = КаталогВременныхФайлов() + "Log_WialonHosting" + ?(ПустаяСтрока(ТекущееИмяБазы),"","_base_" + ТекущееИмяБазы);
		
		КаталогНаДиске = Новый Файл(ИмяКаталога);
		Если НЕ КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ИмяКаталога);
		КонецЕсли; 
		
		ИмяКаталога_ТекущейСессии = ИмяКаталога + "\" + Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMddHHmmss");
		
		СоздатьКаталог(ИмяКаталога_ТекущейСессии);
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonHosting'"), УровеньЖурналаРегистрации.Информация,,, "Католог логирования на серевере: " + ИмяКаталога_ТекущейСессии);
		
	КонецЕсли;
	
	sid = "";
	Отсрочка = Сервер.Отсрочка;
	СобытияМониторинга = Сервер.СобытияМониторинга;
	Отказ = Ложь;
	
	Попытка
		Если НЕ ПустаяСтрока(Сервер.Токен) Тогда 
			РезультатОбращения = ПолучитьУникальныйИдентификаторСессииWialonHosting(Сервер.Сервер, Сервер.Токен, Сервер.Порт, Сервер.ЗащищенноеСоединение);
			Если РезультатОбращения=Неопределено Тогда 
				Возврат;
			КонецЕсли;
			sid = РезультатОбращения.sid;
		КонецЕсли;
		
		Запрос = Новый Запрос;	
		//Запрос.УстановитьПараметр("ИдентификаторыУстройств", МассивТС);
		Запрос.УстановитьПараметр("СерверСбораДанных", Сервер.Ссылка);
		Запрос.Текст = ПолучитьТекстЗапросаWialon();
		
		ПакетЗапросов  = Запрос.ВыполнитьПакет();
		Выборка = ПакетЗапросов[3].Выбрать();
		тбзДатчики = ПакетЗапросов[7].Выгрузить();
		
		МассивОбъектов = Новый Массив;
		ДатаЗапуска = ТекущаяДатаСеанса();
		КоличествоСекундЗагрузки = 86400; // 24*60*60 сутки.
		Пока Выборка.Следующий() Цикл
			
			ТекущаяУстройство = Выборка.ИдентификаторУстройства;
			СтруктураПериода = ПолучитьИнтервалЗапросаДанных(Выборка.Период, Выборка.ПериодД, ДатаЗапуска, КоличествоСекундЗагрузки, Отсрочка);
			
			НачалоПериода = СтруктураПериода.НачалоПериода;
			НачалоПериода_Датчика = СтруктураПериода.НачалоПериода_Датчика;
			КонецПериода =  СтруктураПериода.КонецПериода;
			КонецПериода_Датчика = СтруктураПериода.КонецПериода_Датчика;
						
			СтруктураОбъектов = Новый Структура("Идентификатор,Трекер",ТекущаяУстройство,Выборка.Трекер);
			
			ОтборДатчиков = Новый Структура();
			ОтборДатчиков.Вставить("Трекер", Выборка.Трекер);
			СоответствиеДатчиков = Неопределено;
			НайденныеДатчики = тбзДатчики.НайтиСтроки(ОтборДатчиков);
			Если НайденныеДатчики.Количество() Тогда
				СоответствиеДатчиков = Новый Соответствие;
				Для каждого ТекущийДатчик Из НайденныеДатчики Цикл
					Если ПустаяСтрока(ТекущийДатчик.НомерДатчика) Тогда
						Продолжить;
					КонецЕсли;
					СтруктураДатчика = Новый Структура();
					СтруктураДатчика.Вставить("Датчик", ТекущийДатчик.Датчик);
					СтруктураДатчика.Вставить("Назначение", ТекущийДатчик.Назначение);
					СтруктураДатчика.Вставить("КалибровочныйГрафик", ТекущийДатчик.КалибровочныйГрафик);
					СтруктураДатчика.Вставить("Трекер", Выборка.Трекер);
					СтруктураДатчика.Вставить("ТранспортноеСредство", ТекущийДатчик.ТранспортноеСредство);
					СоответствиеДатчиков.Вставить(ТекущийДатчик.НомерДатчика, СтруктураДатчика);
				КонецЦикла;
			КонецЕсли;
			
			Попытка
				ДанныеОтвета = ПолучитьОнлайнДанныеWialonHosting(ТекущаяУстройство, СоответствиеДатчиков, НачалоПериода, КонецПериода, Сервер.Сервер, Сервер.Порт, Сервер.ЗащищенноеСоединение, sid);
			Исключение				
				Данные = Неопределено;
				СтрокаСообщенияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации("ПолучениеДанныхОтWialonHosting:", УровеньЖурналаРегистрации.Ошибка,,, СтрокаСообщенияОбОшибке);
			КонецПопытки;
			
			Если ТипЗнч(ДанныеОтвета) = Тип("Структура") Тогда
				Данные = ДанныеОтвета.Рейсы;
				СтруктураОбъектов.Вставить("Датчики", ДанныеОтвета.Датчики);
			Иначе
				Данные = ДанныеОтвета;
			КонецЕсли;
			
			Если Данные = Неопределено Тогда
				Данные = Новый СписокЗначений;
				ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековWialonPro();
				НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
				
				НовЗаписьТрекера.Период   			 = КонецПериода;
				НовЗаписьТрекера.Широта              = 0;
				НовЗаписьТрекера.Долгота             = 0;
				НовЗаписьТрекера.Скорость            = 0;
				НовЗаписьТрекера.Высота              = 0;
				НовЗаписьТрекера.НаправлениеДвижения = 0;
				
				Данные.Добавить(ТаблицаЗначенийТреков);
			КонецЕсли;
			
			СтруктураОбъектов.Вставить("Навигация",Данные);
					
			МассивОбъектов.Добавить(СтруктураОбъектов);
			
		КонецЦикла;
		РезультатЗавершения = ЗавершитьСессиюПодключенияWialonHosting(Сервер.Сервер, Сервер.Порт, Сервер.ЗащищенноеСоединение, sid);
		Если НЕ РезультатЗавершения=Неопределено Тогда 
			ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер.Ссылка, РезультатЗавершения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonHosting'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ТекстОшибки);
		КонецЕсли;
	Исключение
		Отказ = Истина;
		ОписаниеТекущейОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonHosting'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ОписаниеТекущейОшибки);
		Если НЕ ПустаяСтрока(sid) Тогда 
			РезультатЗавершения = ЗавершитьСессиюПодключенияWialonHosting(Сервер.Сервер, Сервер.Порт, Сервер.ЗащищенноеСоединение, sid);
			Если НЕ РезультатЗавершения=Неопределено Тогда 
				ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер.Ссылка, РезультатЗавершения.ТекстОшибки);
				ЗаписьЖурналаРегистрации(НСтр("ru = 'ПолучениеДанныхОтWialonHosting'"), УровеньЖурналаРегистрации.Ошибка,,Сервер.Ссылка, ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецПопытки;
	
	Если НЕ Отказ Тогда
		КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковЗагрузкиССервераСбораДанных");
		Если КоличествоПотоков = 0 ИЛИ ИнформационнаяБазаФайловая Тогда 
			КоличествоПотоков = 1;
		КонецЕсли;
		
		Если ИнформационнаяБазаФайловая Тогда
			СоздатьПотокОбработкиЗагрузкиДанныхWialonHosting(МассивОбъектов);
		Иначе
			МассивПотоков = Новый Массив;
			Если КоличествоПотоков = 1 Тогда
				МассивПотоков.Добавить(МассивОбъектов);
			Иначе
				ОбъектовВПотоке = Окр(МассивОбъектов.Количество() / КоличествоПотоков,0,РежимОкругления.Окр15как10);
				ТекущееКоличество = 0;
				ОбъектыПотока = Новый Массив;
				Для Каждого текЭлемент Из МассивОбъектов Цикл
					ОбъектыПотока.Добавить(текЭлемент);
					ТекущееКоличество = ТекущееКоличество + 1;
					Если ТекущееКоличество >= ОбъектовВПотоке Тогда
						ТекущееКоличество = 0;
						МассивПотоков.Добавить(ОбъектыПотока);
						ОбъектыПотока = Новый Массив;
					КонецЕсли;
				КонецЦикла;
				// Допишем крайний поток ТС которые остались.
				Если ОбъектыПотока.Количество() Тогда
					ТекущийМассив = МассивПотоков[МассивПотоков.ВГраница()];
					Для Каждого текЭлемент Из ОбъектыПотока Цикл
						ТекущийМассив.Добавить(текЭлемент);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого текЭлемент Из МассивПотоков Цикл
				Если текЭлемент.Количество() Тогда 
					мсвПараметров = Новый Массив(1);
					мсвПараметров[0] = текЭлемент;
					упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упИнтеграцияССерверамиСбораДанных.СоздатьПотокОбработкиЗагрузкиДанныхWialonHosting", мсвПараметров,,,"ПолучениеДанныхОтWialonHosting");
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПолучениеДанныхОтWialonHosting()

// Выполним обращение к сервису WialonHosting получим данные по периодам.
//
// Параметры:
//  ИдентификаторТС - Строка - Идентификатор устройства на сервере.
//  СписокДатчиков - Соотвествие - Список Датчиков Устройства.
//  НачалоПериода - Дата - Начало периода получения данных.
//  КонецПериода - Дата - Конец периода получения данных.
//  АдресСервиса - Строка - Адрес сервиса.
//  Порт - Число - Номер порта для подключения к сервису.
//  ЗащищенноеСоединение  - Булево - Признак защищенного соединения.
//  ssid  - Строка - ID сессии.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица устройств.
//
Функция ПолучитьОнлайнДанныеWialonHosting(ИдентификаторТС, СписокДатчиков, НачалоПериода, КонецПериода, АдресСервиса, Порт, ЗащищенноеСоединение, sid)
	
	Данные = Новый Структура;
	Данные.Вставить("itemId", ИдентификаторТС);
	Данные.Вставить("timeFrom", ПеревестиВUnixtime(НачалоПериода));
	Данные.Вставить("timeTo", ПеревестиВUnixtime(КонецПериода));
	Данные.Вставить("flags", "1");
	Данные.Вставить("flagsMask", "65281");
	Данные.Вставить("loadCount", "0xffffffff");
	//Чтобы загрузить, а затем работать с сообщениями в сессии есть встроенный объект – загрузчик сообщений.	
	ОтветJSON = HTTP_POSTЗапросКСервисуWialonHosting(АдресСервиса, Порт, ЗащищенноеСоединение, "wialon", Данные, "messages/load_interval", sid);
	
	ЗаполнитьДатчики = Ложь;
	ЗаписиДатчиков = Неопределено;
	ЕстьДанныеПоТрекам = Ложь;
	ЭтоСтруктура = Ложь;
	ЭтоСоответствие = Ложь;
	Если НЕ СписокДатчиков = Неопределено И НЕ ОтветJSON=Неопределено Тогда
		count=0;
		Если ТипЗнч(ОтветJSON)=ТИП("Структура") Тогда
			ЭтоСтруктура = Истина;
			Если ОтветJSON.Свойство("count")Тогда
				ЕстьДанныеПоТрекам = ?(ОтветJSON.count=0, Ложь, Истина);
				count=ОтветJSON.count;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ОтветJSON)=ТИП("Соответствие") Тогда
			ЭтоСоответствие = Истина;
			count_Соответствие = ОтветJSON.Получить("count");
			Если НЕ count_Соответствие = Неопределено Тогда
				ЕстьДанныеПоТрекам = ?(count_Соответствие=0, Ложь, Истина);
				count=count_Соответствие;
			КонецЕсли;
		КонецЕсли;
		Если ЕстьДанныеПоТрекам Тогда
			ЗаполнитьДатчики = Истина;
			Данные = Новый Структура;
			Данные.Вставить("source", "");
			Данные.Вставить("indexFrom", "0");
			Данные.Вставить("indexTo", Формат(count, "ЧН=0; ЧГ=0"));
			Данные.Вставить("unitId", ИдентификаторТС);
			Данные.Вставить("sensorId", "0");
			// Чтобы получить значения датчиков.
			ОтветJSON_Датчики = HTTP_POSTЗапросКСервисуWialonHosting(АдресСервиса, Порт, ЗащищенноеСоединение, "wialon", Данные, "unit/calc_sensors", sid, Истина);
			Если ТипЗнч(ОтветJSON_Датчики)=ТИП("Массив") Тогда
				ЗаписиДатчиков = Новый Соответствие;
				Сч=0;
				Для каждого ТекущаяСтрокаОтвета Из ОтветJSON_Датчики Цикл
					ЗаписиДатчиков.Вставить(Сч, ТекущаяСтрокаОтвета);
					Сч=Сч+1;
				КонецЦикла;
				ОтветJSON_Датчики.Очистить();
				ОтветJSON_Датчики = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//Если возникла необходимость загрузить сообщения за другой интервал, то перед выполнением запроса нужно очистить загрузчик сообщений: 
	Данные = Новый Структура;
	ОтветJSON_Сообщений = HTTP_POSTЗапросКСервисуWialonHosting(АдресСервиса, Порт, ЗащищенноеСоединение, "wialon", Данные, "messages/unload", sid);
	СписокСообщений = Новый СписокЗначений;
	Если ЕстьДанныеПоТрекам Тогда
		ТаблицаЗначенийТреков = СоздатьТаблицуДанныхТрековWialonPro();
		Unixtime = ДАТА("19700101000000");
		ИдентификаторЧасовогоПояса = ПолучитьЧасовойПоясИнформационнойБазы();
		МассивСообщений = Новый Массив;
		Если ЭтоСтруктура Тогда
			МассивСообщений = ОтветJSON.messages;
		ИначеЕсли ЭтоСоответствие Тогда
			МассивСообщений = ОтветJSON.Получить("messages");
		КонецЕсли;
		Сч=0;
		НеизвестноЗначение = НеизвестноЗначениеWialon();
		Если ЗаполнитьДатчики Тогда
			ТаблицаЗначенийДатчиков = СоздатьТаблицуДанныхДатчиковWialonPro();
		КонецЕсли;
		Интервал = 60; // Интервал получения данных по датчику, секунд.
		ПредыдущийПериод = ДАТА("00010101000000");
		Для каждого ТекущаяСтрокаОтвета Из МассивСообщений Цикл
			Сч=Сч+1;
			Позиция = Новый Структура;
			Если ЭтоСтруктура Тогда
				Позиция = ТекущаяСтрокаОтвета.pos;
			ИначеЕсли ЭтоСоответствие Тогда
				Позиция_Соответствие = ТекущаяСтрокаОтвета.Получить("pos");
				y = Позиция_Соответствие.Получить("y");
				Если НЕ y=Неопределено Тогда
					Позиция.Вставить("y",y);
					Позиция.Вставить("x",Позиция_Соответствие.Получить("x"));
					Позиция.Вставить("s",Позиция_Соответствие.Получить("s"));
					Позиция.Вставить("z",Позиция_Соответствие.Получить("z"));
					Позиция.Вставить("c",Позиция_Соответствие.Получить("c"));
				КонецЕсли;
			КонецЕсли;
			Если Позиция.Свойство("y") Тогда
				Широта = Позиция.y; //"y":<double>,/* широта */
			Иначе
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Широта) Тогда
				Продолжить;
			КонецЕсли;
			Попытка
				Если ЭтоСтруктура Тогда
					ПериодGMT = Unixtime + ТекущаяСтрокаОтвета.t; //"t":<uint>,/* время UTC */
				ИначеЕсли ЭтоСоответствие Тогда
					ПериодGMT_Соответствие = ТекущаяСтрокаОтвета.Получить("t");
					Если ПериодGMT_Соответствие=Неопределено Тогда
						Продолжить;
					Иначе
						ПериодGMT = Unixtime + ПериодGMT_Соответствие;
					КонецЕсли;
				КонецЕсли;
				Период = МестноеВремя(ПериодGMT, ИдентификаторЧасовогоПояса);
			Исключение
				Продолжить;
			КонецПопытки;
			Если ЗаполнитьДатчики Тогда
				Если (Период-ПредыдущийПериод)>=Интервал Тогда
					ПредыдущийПериод = Период;
					ТекущаяЗаписьДатчиков = ЗаписиДатчиков.Получить(Сч-1);
					Если НЕ ТекущаяЗаписьДатчиков=Неопределено Тогда
						Для каждого ТекущийДатчик Из СписокДатчиков Цикл
							ТекущееЗначениеДатчика = ТекущаяЗаписьДатчиков.Получить(ТекущийДатчик.Ключ);
							Если НЕ ТекущееЗначениеДатчика=Неопределено Тогда
								Если ТекущееЗначениеДатчика=НеизвестноЗначение Тогда
									Продолжить;
								КонецЕсли;
								НоваяЗаписьДатчика = ТаблицаЗначенийДатчиков.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяЗаписьДатчика, ТекущийДатчик.Значение);
								НоваяЗаписьДатчика.Период = Период;
								НоваяЗаписьДатчика.Значение = ТекущееЗначениеДатчика;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			НовЗаписьТрекера = ТаблицаЗначенийТреков.Добавить();
			НовЗаписьТрекера.Период   			 = Период;
			НовЗаписьТрекера.Широта              = Широта;
			НовЗаписьТрекера.Долгота             = Позиция.x; //"x":<double>,/* долгота */
			НовЗаписьТрекера.Скорость            = Позиция.s; //"s":<int>,/* скорость */
			НовЗаписьТрекера.Высота              = Позиция.z; //"z":<int>,/* высота над уровнем моря */
			НовЗаписьТрекера.НаправлениеДвижения = Позиция.c; //"c":<int>,/* курс */
		КонецЦикла;
		ОтветJSON = Неопределено;
		Если ТаблицаЗначенийТреков.Количество() Тогда
			СписокСообщений.Добавить(ТаблицаЗначенийТреков);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СписокСообщений) = ТИП("СписокЗначений") Тогда
		Если СписокСообщений.Количество()=0 Тогда
			СписокСообщений = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнитьДатчики Тогда
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("Рейсы", СписокСообщений);
		Если НЕ ТаблицаЗначенийДатчиков.Количество() Тогда
			Для каждого ТекущаяЗаписьДатчика Из СписокДатчиков Цикл
				НоваяЗаписьДатчика = ТаблицаЗначенийДатчиков.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗаписьДатчика, ТекущаяЗаписьДатчика);
				НоваяЗаписьДатчика.Период = КонецПериода;
				НоваяЗаписьДатчика.Значение = Неопределено;
			КонецЦикла;
		КонецЕсли;
		СтруктураВозврата.Вставить("Датчики", ТаблицаЗначенийДатчиков);
		Возврат  СтруктураВозврата;
	Иначе	
		Возврат  СписокСообщений;
	КонецЕсли;
	
КонецФункции // ПолучитьОнлайнДанныеWialonHosting()

// Выполним создание разных потоков обработки.
//
// Параметры:
//  ДанныеОбработки  - Массив - Данные потока.
//
Процедура СоздатьПотокОбработкиЗагрузкиДанныхWialonHosting(ДанныеОбработки) Экспорт
	
	Для Каждого текЭлемент Из ДанныеОбработки Цикл
		ВыполнитьОбработкуЗагруженныхДанныхWialonPro(текЭлемент);
	КонецЦикла;
	
КонецПроцедуры // СоздатьПотокОбработкиЗагрузкиДанныхWialonHosting()

Функция ПолучитьТекстЗапросаWialon()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	УстановленныеТрекерыСП.Трекер КАК Трекер,
	|	УстановленныеТрекерыСП.ТранспортноеСредство КАК ТранспортноеСредство,
	|	УстановленныеТрекерыСП.Период КАК Период
	|ПОМЕСТИТЬ втУстановленныеТрекеры
	|ИЗ
	|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(, ) КАК УстановленныеТрекерыСП
	|ГДЕ
	|	НЕ УстановленныеТрекерыСП.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|	И УстановленныеТрекерыСП.Установлен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМобильныеУстройстваИТрекеры.Ссылка КАК Трекер,
	|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	упМобильныеУстройстваИТрекеры.Модель КАК Модель,
	|	ВЫБОР
	|		КОГДА упМобильныеУстройстваИТрекеры.СерверСбораДанных.ДатаЗагрузки > втУстановленныеТрекеры.Период
	|			ТОГДА упМобильныеУстройстваИТрекеры.СерверСбораДанных.ДатаЗагрузки
	|		ИНАЧЕ втУстановленныеТрекеры.Период
	|	КОНЕЦ КАК ДатаЗагрузки,
	|	втУстановленныеТрекеры.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упМобильныеУстройстваИТрекеры.СерверСбораДанных.ИнтервалПолученияДанных КАК Интервал,
	|	втУстановленныеТрекеры.ТранспортноеСредство.ДатчикУровняТоплива КАК ДатчикТоплива
	|ПОМЕСТИТЬ втТрекеры
	|ИЗ
	|	Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ втУстановленныеТрекеры КАК втУстановленныеТрекеры
	|		ПО упМобильныеУстройстваИТрекеры.Ссылка = втУстановленныеТрекеры.Трекер
	|ГДЕ
	|	НЕ упМобильныеУстройстваИТрекеры.ПометкаУдаления
	//|	И упМобильныеУстройстваИТрекеры.ИдентификаторУстройства В(&ИдентификаторыУстройств)
	|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных
	|	И упМобильныеУстройстваИТрекеры.Ссылка В
	|			(ВЫБРАТЬ
	|				втУстановленныеТрекеры.Трекер
	|			ИЗ
	|				втУстановленныеТрекеры КАК втУстановленныеТрекеры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ДанныеДатчиковСП.Период) КАК Период,
	|	ДанныеДатчиковСП.Трекер КАК Трекер
	|ПОМЕСТИТЬ вт_ДатчикПериод
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|			,
	|			Трекер В
	|				(ВЫБРАТЬ
	|					втТрекеры.Трекер
	|				ИЗ
	|					втТрекеры КАК втТрекеры)) КАК ДанныеДатчиковСП
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДатчиковСП.Трекер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА втТрекеры.ДатаЗагрузки
	|		ИНАЧЕ ЕСТЬNULL(ДанныеТрекеровСП.Период, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК Период,
	|	втТрекеры.Трекер КАК Трекер,
	|	втТрекеры.ИдентификаторУстройства КАК ИдентификаторУстройства,
	|	ЕСТЬNULL(ДанныеТрекеровСП.Счетчик, 0) КАК СчетчикАктульности,
	|	ВЫБОР
	|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА втТрекеры.ДатаЗагрузки
	|		ИНАЧЕ ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ПериодД,
	|	втТрекеры.Интервал КАК Интервал,
	//|	втТрекеры.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втТрекеры.ДатчикТоплива КАК ДатчикТоплива
	//|	втТрекеры.Модель КАК Модель
	|ИЗ
	|	втТрекеры КАК втТрекеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеТрекеров.СрезПоследних КАК ДанныеТрекеровСП
	|		ПО втТрекеры.Трекер = ДанныеТрекеровСП.Трекер
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДатчикПериод КАК вт_ДатчикПериод
	|		ПО втТрекеры.Трекер = вт_ДатчикПериод.Трекер
	|УПОРЯДОЧИТЬ ПО Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТрекеры.Трекер КАК Трекер,
	|	втТрекеры.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ВЫБОР
	|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА втТрекеры.ДатаЗагрузки
	|		ИНАЧЕ ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ вт_УстДатчик
	|ИЗ
	|	втТрекеры КАК втТрекеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДатчикПериод КАК вт_ДатчикПериод
	|		ПО втТрекеры.Трекер = вт_ДатчикПериод.Трекер
	|
	|СГРУППИРОВАТЬ ПО
	|	втТрекеры.Трекер,
	|	втТрекеры.ТранспортноеСредство,
	|	ВЫБОР
	|		КОГДА втТрекеры.ДатаЗагрузки >= ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА втТрекеры.ДатаЗагрузки
	|		ИНАЧЕ ЕСТЬNULL(вт_ДатчикПериод.Период, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДатчикиТранспортныхСредств.Период КАК Период,
	|	упДатчикиТранспортныхСредств.Датчик КАК Датчик,
	|	упДатчикиТранспортныхСредств.ТранспортноеСредство КАК ТранспортноеСредство
	|ПОМЕСТИТЬ вт_УстДатчики
	|ИЗ
	|	РегистрСведений.упДатчикиТранспортныхСредств КАК упДатчикиТранспортныхСредств
	|ГДЕ
	|	упДатчикиТранспортныхСредств.ТранспортноеСредство В
	|			(ВЫБРАТЬ
	|				втУстановленныеТрекеры.ТранспортноеСредство КАК ТранспортноеСредство
	|			ИЗ
	|				втУстановленныеТрекеры КАК втУстановленныеТрекеры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т1.Период КАК НачалоПериода,
	|	МИНИМУМ(ЕСТЬNULL(Т2.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК КонецПериода,
	|	Т1.Датчик КАК Датчик,
	|	Т1.ТранспортноеСредство КАК ТранспортноеСредство
	|ПОМЕСТИТЬ вт_ПериодыДатчиков
	|ИЗ
	|	вт_УстДатчики КАК Т1
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_УстДатчики КАК Т2
	|		ПО Т1.Датчик = Т2.Датчик
	|			И Т1.Период < Т2.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	Т1.Датчик,
	|	Т1.ТранспортноеСредство,
	|	Т1.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_УстДатчик.Период КАК Период,
	|	упДатчики.ИдентификаторУстройства КАК НомерДатчика,
	|	ЕСТЬNULL(упДатчики.Назначение, ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.ПустаяСсылка)) КАК Назначение,
	|	вт_ПериодыДатчиков.Датчик КАК Датчик,
	|	ЗНАЧЕНИЕ(Справочник.упКалибровочныеГрафики.ПустаяСсылка) КАК КалибровочныйГрафик,
	|	ВЫБОР КОГДА упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо) ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК Топливо,
	|	вт_УстДатчик.ТранспортноеСредство КАК ТранспортноеСредство,
	|	вт_УстДатчик.Трекер КАК Трекер
	|ИЗ
	|	вт_УстДатчик КАК вт_УстДатчик
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ПериодыДатчиков КАК вт_ПериодыДатчиков
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упДатчики КАК упДатчики ПО (вт_ПериодыДатчиков.Датчик = упДатчики.Ссылка)
	|		ПО (вт_УстДатчик.ТранспортноеСредство = вт_ПериодыДатчиков.ТранспортноеСредство)
	|			И (ВЫБОР КОГДА вт_ПериодыДатчиков.КонецПериода = ДАТАВРЕМЯ(1, 1, 1) ТОГДА вт_УстДатчик.Период >= вт_ПериодыДатчиков.НачалоПериода
	|				ИНАЧЕ вт_УстДатчик.Период >= вт_ПериодыДатчиков.НачалоПериода И вт_УстДатчик.Период < вт_ПериодыДатчиков.КонецПериода КОНЕЦ)
	|
	|УПОРЯДОЧИТЬ ПО Период, Трекер";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ПолучитьТекстЗапросаWialon()


#КонецОбласти


