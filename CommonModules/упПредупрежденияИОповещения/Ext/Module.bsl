
#Область ПрограммныйИнтерфейс

// Создание предупреждение по документам с истекающим сроком действия.
//
Процедура ФормированиеПредупреждений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упДокументТранспортногоСредстваВодителя.Ссылка
	|ПОМЕСТИТЬ втЗаканчивающиесяДокументы
	|ИЗ
	|	Документ.упДокументТранспортногоСредстваВодителя КАК упДокументТранспортногоСредстваВодителя
	|ГДЕ
	|	ЕСТЬNULL(упДокументТранспортногоСредстваВодителя.ТипДокумента.ПредупреждатьОбОкончанииСрокаДействия, ЛОЖЬ)
	|	И упДокументТранспортногоСредстваВодителя.ДействуетПо <= ДОБАВИТЬКДАТЕ(&ТекущаяДата, ДЕНЬ, упДокументТранспортногоСредстваВодителя.ТипДокумента.ИнтервалПредупреждения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаканчивающиесяДокументы.Ссылка
	|ИЗ
	|	втЗаканчивающиесяДокументы КАК втЗаканчивающиесяДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упДокументТранспортногоСредстваВодителя КАК упДокументТранспортногоСредстваВодителя
	|		ПО втЗаканчивающиесяДокументы.Ссылка = упДокументТранспортногоСредстваВодителя.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ БизнесПроцесс.упПродлениеДокументаТСВодителя КАК упПродлениеДокументаТСВодителя
	|		ПО втЗаканчивающиесяДокументы.Ссылка = упПродлениеДокументаТСВодителя.Предмет
	|ГДЕ
	|	упДокументТранспортногоСредстваВодителя.Ссылка ЕСТЬ NULL
	|	И упПродлениеДокументаТСВодителя.Ссылка ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НовыйПроцесс = БизнесПроцессы.упПродлениеДокументаТСВодителя.СоздатьБизнесПроцесс();
		НовыйПроцесс.Заполнить(Выборка.Ссылка);
		НовыйПроцесс.Записать();
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти