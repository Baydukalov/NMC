
#Область ПрограммныйИнтерфейс

#Область ВыработкаАгрегатов

// Формирование выработки по агрегатам.
//
// Параметры:
//  ДополнительныеСвойства  - Структура - Свойства.
//  Ссылка  - ДокументСсылка - Ссылка на объект.
//  Движения  - Движения - Движения объекта.
//  Реквизиты  - Структура - Реквизиты объекта.
//  Отказ  - Булево - Флаг фиксации изменений.
//
Процедура РассчитатьВыработкуАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Или Не (ДополнительныеСвойства.Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{: Текст собран конструктором из подсистемы ""Инструменты разработчика"" (http://devtool1c.ucoz.ru), который сохраняет звездочки и комментарии.
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрицепыСП.Прицеп КАК Прицеп
	|ПОМЕСТИТЬ втПрицепы
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
	|		&Дата,
	|		ТранспортноеСредство = &ТранспортноеСредство
	|			И Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)) КАК ПрицепыСП
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	АгрегатыТССП.ТранспортноеСредство КАК ТС,
	|	АгрегатыТССП.Агрегат КАК Агрегат,
	|	АгрегатыТССП.Агрегат.Тип.Вид КАК ВидАгрегата,
	|	АгрегатыТССП.ХодоваяШина КАК ХодоваяШина,
	|	АгрегатыТССП.Агрегат.Тип.ИзмерятьНормуВМотоЧасах КАК ИзмерятьНормуВМотоЧасах,
	|	АгрегатыТССП.Агрегат.Тип.ЭксплуатационнаяНормаКм КАК ЭксплуатационнаяНормаКм,
	|	АгрегатыТССП.Агрегат.Тип.ЭксплуатационнаяНормаМотоЧасы КАК ЭксплуатационнаяНормаМотоЧасы,
	|	АгрегатыТССП.Агрегат.Тип.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втАгрегатыПодготовка
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Дата,
	|		ТранспортноеСредство = &ТранспортноеСредство) КАК АгрегатыТССП
	|ГДЕ ИСТИНА
	|	И АгрегатыТССП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И НЕ (АгрегатыТССП.Агрегат.Тип.ФиксироватьСрокПолезногоИспользования)
	|	И АгрегатыТССП.Агрегат ССЫЛКА Справочник.упШиныУзлыИАгрегаты
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	АгрегатыТССП.ТранспортноеСредство КАК ТС,
	|	АгрегатыТССП.Агрегат КАК Агрегат,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыАгрегатов.Прочее) КАК ВидАгрегата,
	|	ЛОЖЬ КАК ХодоваяШина,
	|	ИСТИНА КАК ИзмерятьНормуВМотоЧасах,
	|	0 КАК ЭксплуатационнаяНормаКм,
	|	ДопОборудование.Моточасы КАК ЭксплуатационнаяНормаМотоЧасы,
	|	ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Дата,
	|		ТранспортноеСредство = &ТранспортноеСредство) КАК АгрегатыТССП
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.ДополнительноеОборудование КАК ДопОборудование
	|	ПО ИСТИНА
	|		И АгрегатыТССП.Агрегат = ДопОборудование.ДополнительноеОборудование
	|		И ДопОборудование.Ссылка = &ПутевойЛист
	|ГДЕ ИСТИНА
	|	И АгрегатыТССП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И АгрегатыТССП.Агрегат ССЫЛКА Справочник.упДополнительноеОборудование
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	АгрегатыПрицеповСП.ТранспортноеСредство КАК ТС,
	|	АгрегатыПрицеповСП.Агрегат КАК Агрегат,
	|	АгрегатыПрицеповСП.Агрегат.Тип.Вид КАК ВидАгрегата,
	|	АгрегатыПрицеповСП.ХодоваяШина КАК ХодоваяШина,
	|	АгрегатыПрицеповСП.Агрегат.Тип.ИзмерятьНормуВМотоЧасах КАК ИзмерятьНормуВМотоЧасах,
	|	АгрегатыПрицеповСП.Агрегат.Тип.ЭксплуатационнаяНормаКм КАК ЭксплуатационнаяНормаКм,
	|	АгрегатыПрицеповСП.Агрегат.Тип.ЭксплуатационнаяНормаМотоЧасы КАК ЭксплуатационнаяНормаМотоЧасы,
	|	АгрегатыПрицеповСП.Агрегат.Тип.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Дата,
	|		ТранспортноеСредство В (
	|			ВЫБРАТЬ
	|				втПрицепы.Прицеп КАК Прицеп
	|			ИЗ
	|				втПрицепы КАК втПрицепы)) КАК АгрегатыПрицеповСП
	|ГДЕ ИСТИНА
	|	И АгрегатыПрицеповСП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И НЕ (АгрегатыПрицеповСП.Агрегат.Тип.ФиксироватьСрокПолезногоИспользования)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАгрегаты.ТС КАК ТС,
	|	втАгрегаты.Агрегат КАК Агрегат,
	|	втАгрегаты.ИзмерятьНормуВМотоЧасах КАК ИзмерятьНормуВМотоЧасах,
	|	втАгрегаты.ЭксплуатационнаяНормаКм КАК ЭксплуатационнаяНормаКм,
	|	втАгрегаты.ЭксплуатационнаяНормаМотоЧасы КАК ЭксплуатационнаяНормаМотоЧасы,
	|	втАгрегаты.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втАгрегаты
	|ИЗ
	|	втАгрегатыПодготовка КАК втАгрегаты
	|ГДЕ ИСТИНА
	|	И втАгрегаты.ВидАгрегата = ЗНАЧЕНИЕ(Перечисление.упВидыАгрегатов.Шины)
	|	И втАгрегаты.ХодоваяШина
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втАгрегаты.ТС КАК ТС,
	|	втАгрегаты.Агрегат КАК Агрегат,
	|	втАгрегаты.ИзмерятьНормуВМотоЧасах КАК ИзмерятьНормуВМотоЧасах,
	|	втАгрегаты.ЭксплуатационнаяНормаКм КАК ЭксплуатационнаяНормаКм,
	|	втАгрегаты.ЭксплуатационнаяНормаМотоЧасы КАК ЭксплуатационнаяНормаМотоЧасы,
	|	втАгрегаты.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	втАгрегатыПодготовка КАК втАгрегаты
	|ГДЕ НЕ (втАгрегаты.ВидАгрегата = ЗНАЧЕНИЕ(Перечисление.упВидыАгрегатов.Шины))
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упВыработкаАгрегатовОстатки.Агрегат КАК Агрегат,
	|	упВыработкаАгрегатовОстатки.ПробегКмОстаток КАК ПробегКмОстаток,
	|	упВыработкаАгрегатовОстатки.ПробегМотоЧасыОстаток КАК ПробегМотоЧасыОстаток,
	|	упВыработкаАгрегатовОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.упВыработкаАгрегатов.Остатки(
	|		&Дата,
	|		Агрегат В (
	|			ВЫБРАТЬ
	|				втАгрегаты.Агрегат КАК Агрегат
	|			ИЗ
	|				втАгрегаты КАК втАгрегаты)) КАК упВыработкаАгрегатовОстатки
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втАгрегаты.ТС КАК ТранспортноеСредство,
	|	втОстатки.Агрегат КАК Агрегат,
	|	ВЫБОР
	|		КОГДА втОстатки.ПробегКмОстаток > &Пробег
	|			ТОГДА &Пробег
	|		ИНАЧЕ втОстатки.ПробегКмОстаток
	|	КОНЕЦ КАК ПробегКм,
	|	ВЫБОР
	|		КОГДА втОстатки.ПробегМотоЧасыОстаток > &ПробегМоточасы
	|			ТОГДА &ПробегМоточасы
	|		ИНАЧЕ втОстатки.ПробегМотоЧасыОстаток
	|	КОНЕЦ КАК ПробегМотоЧасы,
	|	ВЫБОР
	|		КОГДА втАгрегаты.ИзмерятьНормуВМотоЧасах
	|			И втАгрегаты.ЭксплуатационнаяНормаМотоЧасы > 0
	|			ТОГДА 0.9 * втОстатки.Агрегат.ПервоначальнаяСтоимость * &ПробегМоточасы / (1000 * втАгрегаты.ЭксплуатационнаяНормаМотоЧасы)
	|		КОГДА НЕ втАгрегаты.ИзмерятьНормуВМотоЧасах
	|			И втАгрегаты.ЭксплуатационнаяНормаКм > 0
	|			ТОГДА 0.9 * втОстатки.Агрегат.ПервоначальнаяСтоимость * &Пробег / втАгрегаты.ЭксплуатационнаяНормаКм
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|	втАгрегаты.СтатьяРасходов КАК СтатьяРасходов,
	|	втАгрегаты.СтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения
	|ИЗ
	|	втАгрегаты КАК втАгрегаты
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|	ПО втАгрегаты.Агрегат = втОстатки.Агрегат
	|ГДЕ НЕ (втАгрегаты.Агрегат ССЫЛКА Справочник.упДополнительноеОборудование)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втАгрегаты.ТС КАК ТранспортноеСредство,
	|	втОстатки.Агрегат КАК Агрегат,
	|	0 КАК ПробегКм,
	|	ВЫБОР
	|		КОГДА втОстатки.ПробегМотоЧасыОстаток > втАгрегаты.ЭксплуатационнаяНормаМотоЧасы
	|			ТОГДА втАгрегаты.ЭксплуатационнаяНормаМотоЧасы
	|		ИНАЧЕ втОстатки.ПробегМотоЧасыОстаток
	|	КОНЕЦ КАК ПробегМотоЧасы,
	|	ВЫБОР
	|		КОГДА втОстатки.ПробегМотоЧасыОстаток > 0
	|			ТОГДА втОстатки.СуммаОстаток / втОстатки.ПробегМотоЧасыОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ * ВЫБОР
	|		КОГДА втОстатки.ПробегМотоЧасыОстаток > втАгрегаты.ЭксплуатационнаяНормаМотоЧасы
	|			ТОГДА втАгрегаты.ЭксплуатационнаяНормаМотоЧасы
	|		ИНАЧЕ втОстатки.ПробегМотоЧасыОстаток
	|	КОНЕЦ КАК Сумма,
	|	втАгрегаты.СтатьяРасходов КАК СтатьяРасходов,
	|	втАгрегаты.СтатьяРасходов.ПравилаРаспределения КАК ПравилоРаспределения
	|ИЗ
	|	втАгрегаты КАК втАгрегаты
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|	ПО втАгрегаты.Агрегат = втОстатки.Агрегат
	|ГДЕ втАгрегаты.Агрегат ССЫЛКА Справочник.упДополнительноеОборудование
	|";

	Запрос.УстановитьПараметр("ТранспортноеСредство", Реквизиты.ТранспортноеСредство);
	Запрос.УстановитьПараметр("Дата", 				Реквизиты.ГраницаИсключая);
	Запрос.УстановитьПараметр("Пробег", 			Реквизиты.Пробег);
	Запрос.УстановитьПараметр("ПробегМоточасы", 		Реквизиты.Моточасы);
	Запрос.УстановитьПараметр("ПутевойЛист", 		Ссылка);
	Запрос.УстановитьПараметр("ДополнительноеОборудование"      , Реквизиты.ДополнительноеОборудование.ВыгрузитьКолонку("ДополнительноеОборудование"));
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		//ДополнительныеСвойства.Вставить("ОстаткиАгрегатов", Результат.Выгрузить());
		ДополнительныеСвойства.Вставить("ВыработкаАгрегатов", Результат.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирования движения по регистру "упВыработкаАгрегатов".
//
// Параметры:
//  ДополнительныеСвойства  - Структура - Свойства.
//  Ссылка  - ДокументСсылка - Ссылка на объект.
//  Движения  - Движения - Движения объекта.
//  Реквизиты  - Структура - Реквизиты объекта.
//  Отказ  - Булево - Флаг фиксации изменений.
//
Процедура СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияВыработкаАгрегатов = Движения.упВыработкаАгрегатов;
	ДвиженияВыработкаАгрегатов.Записывать = Истина;
	
	ИзмененаВыработкаАгрегата = Неопределено;
	
	Если Истина
		И Реквизиты.Свойство("ИзмененаВыработкаАгрегата", ИзмененаВыработкаАгрегата)
		И ИзмененаВыработкаАгрегата 
	Тогда
		Движение = ДвиженияВыработкаАгрегатов.Добавить();
		Движение.Период = Реквизиты.Дата;
		Если Реквизиты.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
			Движение.Агрегат = Реквизиты.ДополнительноеОборудованиеРемонта;
		ИначеЕсли Реквизиты.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
			Движение.Агрегат = Реквизиты.Агрегат;
		КонецЕсли;
		Движение.ПробегКм = Реквизиты.ПробегКм;
		Движение.ПробегМотоЧасы = Реквизиты.ПробегМотоЧасы;
		Движение.Сумма = Реквизиты.СуммаВыработка;
	КонецЕсли;
	
	ТаблицаДвижений = Неопределено;
	ТаблицаИзДопСвойств = Неопределено;
	Если ДополнительныеСвойства.Свойство("ВыработкаАгрегатов") Тогда 
		ТаблицаДвижений = ДополнительныеСвойства.ВыработкаАгрегатов;
	ИначеЕсли ДополнительныеСвойства.Свойство("АгрегатыНаТС") Тогда	
		ТаблицаДвижений = ДополнительныеСвойства.АгрегатыНаТС;	
	КонецЕсли;
	
	Если НЕ ТаблицаДвижений = Неопределено Тогда
		Для каждого текСтрока Из ТаблицаДвижений Цикл
			Если Ложь
				Или ЗначениеЗаполнено(текСтрока.ПробегКм) 
				Или ЗначениеЗаполнено(текСтрока.ПробегМоточасы) 
				Или ЗначениеЗаполнено(текСтрока.Сумма)
			Тогда
				Движение = ДвиженияВыработкаАгрегатов.Добавить();
				Движение.Период = Реквизиты.Дата;
				ЗаполнитьЗначенияСвойств(Движение, текСтрока);
			КонецЕсли; 
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Нет.

#КонецОбласти
