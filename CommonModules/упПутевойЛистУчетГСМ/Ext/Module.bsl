#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Процедура формирует движения по регистру "упСтатусыПутевыхЛистов".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияСтатусПутевогоЛиста(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДатаСтатуса = ТекущаяДатаСеанса();
	Реквизиты.Вставить("ДатаСтатуса", ДатаСтатуса);
	
	ДвиженияСтатуса = Движения.упСтатусыПутевыхЛистов;
			
	Статус = ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус = ДополнительныеСвойства.ЗаписыватьСтатус;
	Если ЗаписыватьСтатус Тогда
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
				
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период = ДатаСтатуса;
		ДвижениеСтатуса.Документ = Ссылка;
		ДвижениеСтатуса.Статус = Статус;
		
		Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") И Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда
			ОписаниеОшибки = "";
			мсвРейсы = Реквизиты.Рейсы.ВыгрузитьКолонку("Рейс");
			тбзСтатусыРейсов = упРаботаСоСтатусами.ПолучитьТекущиеСтатусыРейсов(мсвРейсы);
			Для каждого СтатусРейсаСтрока Из тбзСтатусыРейсов Цикл
				Если СтатусРейсаСтрока.Статус = Перечисления.упСтатусыРейса.НазначеноТС Тогда
					РейсТекущий = СтатусРейсаСтрока.Рейс;
					упРейс.УстановитьСтатусРейса(РейсТекущий, Перечисления.упСтатусыРейса.КВыполнению, ОписаниеОшибки, Отказ);	
					Если Отказ Тогда
						ЗаписьЖурналаРегистрации("ОшибкаУстановкиСтатуса", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
						ТекстСообщения = Нстр("ru = 'Произошла ошибка при попытке перевести статус документа %1'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, РейсТекущий);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,РейсТекущий,,,Отказ);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,РейсТекущий,,,Отказ);
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда
			ОписаниеОшибки = "";
			мсвРейсы = Реквизиты.Рейсы.ВыгрузитьКолонку("Рейс");
			тбзСтатусыРейсов = упРаботаСоСтатусами.ПолучитьТекущиеСтатусыРейсов(мсвРейсы);
			Для каждого СтатусРейсаСтрока Из тбзСтатусыРейсов Цикл
				Если СтатусРейсаСтрока.Статус <> Перечисления.упСтатусыРейса.Завершен Тогда
					РейсТекущий = СтатусРейсаСтрока.Рейс;
					
					Если Не Документы.упРейс.ПроверитьНаличиеНедополученныхДокументов(РейсТекущий,, ОписаниеОшибки) Тогда
						Отказ = Истина;
					КонецЕсли;
					
					Если Не Отказ Тогда
						упРейс.УстановитьСтатусРейса(РейсТекущий, Перечисления.упСтатусыРейса.Завершен, ОписаниеОшибки, Отказ, ДополнительныеСвойства);					
					КонецЕсли; 
					
					Если Отказ Тогда
						ЗаписьЖурналаРегистрации("ОшибкаУстановкиСтатуса", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
						ТекстСообщения = Нстр("ru = 'Произошла ошибка при попытке перевести статус документа %1'");
						ТекстСообщения = СтрШаблон(ТекстСообщения, РейсТекущий);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,РейсТекущий,,,Отказ);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,РейсТекущий,,,Отказ);
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упОстаткиГСМ".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвижениеОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
			
	ИзменитьОстатки = Ложь;
	
	ИспользуютсяЛимитыРасходаГСМ = Ложь;
	
	Если Реквизиты.Свойство("ИспользуютсяЛимитыРасходаГСМ") Тогда
		Если Реквизиты.ИспользуютсяЛимитыРасходаГСМ Тогда
			ИспользуютсяЛимитыРасходаГСМ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда
		Статус	= ДополнительныеСвойства.Статус;
		ИзменитьОстатки = Ложь
						Или Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен
						Или Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаправкаСливГСМ") Тогда	
		ИзменитьОстатки = Истина;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упКорректировкаОстатковГСМ") Тогда	
		ИзменитьОстатки = Истина;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРегламентнаяОперация") Тогда	
		ИзменитьОстатки = Истина;
	КонецЕсли;
		
	Если ИзменитьОстатки Тогда
		
		ДвиженияОстаткиГСМ = Движения.упОстаткиГСМ;
		ДвиженияОстаткиГСМ.Записывать	= Истина;
		
		Если ИспользуютсяЛимитыРасходаГСМ Тогда
			ДвиженияЛимитыРасходаГСМ = Движения.упЛимитыРасходаГСМ;
			ДвиженияЛимитыРасходаГСМ.Записывать = Истина;
		КонецЕсли;
	
		тбзОстаткиГСМ = ДополнительныеСвойства.тбзОстаткиГСМ;
		
		Если тбзОстаткиГСМ.Количество() > 0 Тогда
			Для каждого СтрокаОстаткиГСМ Из тбзОстаткиГСМ Цикл
				ДвижениеОстаткиГСМ	= ДвиженияОстаткиГСМ.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеОстаткиГСМ, СтрокаОстаткиГСМ);
				
				Если ИспользуютсяЛимитыРасходаГСМ И СтрокаОстаткиГСМ.ВидДвиженияТоплива = Перечисления.упВидыДвиженияТоплива.Расход Тогда 
					ДвижениеЛимитыРасходаГСМ = ДвиженияЛимитыРасходаГСМ.Добавить();
					ЗаполнитьЗначенияСвойств(ДвижениеЛимитыРасходаГСМ, СтрокаОстаткиГСМ);
					ДвижениеЛимитыРасходаГСМ.Организация = Реквизиты.Организация;
					ДвижениеЛимитыРасходаГСМ.Подразделение = Реквизиты.Подразделение;
					ДвижениеЛимитыРасходаГСМ.Лимит = 0;
					ДвижениеЛимитыРасходаГСМ.Расход = СтрокаОстаткиГСМ.Количество;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

// Возращает типовую таблицу значений для остатков ГСМ.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция СтруктураТаблицыОстаткиГСМ() Экспорт
	
	тбзОстаткиГСМ = Новый ТаблицаЗначений();
	тбзОстаткиГСМ.Колонки.Добавить("Период");
	тбзОстаткиГСМ.Колонки.Добавить("ТранспортноеСредство");
	тбзОстаткиГСМ.Колонки.Добавить("ВидГСМ");
	тбзОстаткиГСМ.Колонки.Добавить("ТопливнаяКарта");
	тбзОстаткиГСМ.Колонки.Добавить("Количество");
	тбзОстаткиГСМ.Колонки.Добавить("Стоимость");
	тбзОстаткиГСМ.Колонки.Добавить("СтоимостьНДС");
	тбзОстаткиГСМ.Колонки.Добавить("ВидДвижения");
	тбзОстаткиГСМ.Колонки.Добавить("ВидДвиженияТоплива");

	Возврат тбзОстаткиГСМ;
	
КонецФункции

// Процедура формирует движения по регистру "упПробегТС".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвижениеПробег(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
			
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ИзменитьПробег        = Ложь;
	ИндексПробегТС        = Неопределено;
	ИндексПробегНаработка = Неопределено;
	ПериодДвижения = Реквизиты.Дата;
	
	ЭтоПутевойЛист	= ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист");
		
	Если ЭтоПутевойЛист Тогда
		Статус	     = ДополнительныеСвойства.Статус;
		ИзменитьПробег = Ложь
						Или Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен 
						Или Статус =Перечисления.упСтатусыПутевогоЛиста.Закрыт;
						
		ПериодДвижения = ДополнительныеСвойства.ПериодДвиженияГСМ;				
		Движения.упПробегТС.Записывать = Ложь;
		ИндексПробегТС        = 6;
		ИндексПробегНаработка = 7;
			
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упКорректировкаПробегаТС") Тогда	
		ИзменитьПробег        = Истина;
		ИндексПробегТС        = 2;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упКорректировкаНаработкиОборудования") Тогда	
		ИзменитьПробег        = Истина;
		ИндексПробегНаработка = 2;
		
	КонецЕсли;
		
	Если ИзменитьПробег Тогда
		
		Запрос = Новый Запрос;				
		Запрос.Текст = ТекстЗапросаФормированияДвиженийПоПробегу(Ссылка);						
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Если ЭтоПутевойЛист Тогда
			Граница = Новый Граница(Новый МоментВремени(ПериодДвижения , Реквизиты.Ссылка), ВидГраницы.Включая);
			Запрос.УстановитьПараметр("Период", Граница);	
		КонецЕсли;
				
		мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
		
		Если Истина
			И ИндексПробегТС <> Неопределено
			И НЕ мсвРезультатЗапроса[ИндексПробегТС].Пустой() Тогда
			
			ДвиженияПробегТС			= Движения.упПробегТС;
			ДвиженияПробегТС.Записывать	= Истина;
			
			Выборка = мсвРезультатЗапроса[ИндексПробегТС].Выбрать();
			Пока Выборка.Следующий() Цикл
				ДобавитьДвиженияПробег(ДвиженияПробегТС, Выборка, ПериодДвижения, Истина); 	
			КонецЦикла;
				
		КонецЕсли;
		
		Если Истина
			И ИндексПробегНаработка <> Неопределено
			И НЕ мсвРезультатЗапроса[ИндексПробегНаработка].Пустой() Тогда
			
			ДвиженияНаработкаОборудования = Движения.упНаработкаОборудования;
			ДвиженияНаработкаОборудования.Записывать = Истина;
			
			Выборка = мсвРезультатЗапроса[ИндексПробегНаработка].Выбрать();
			Пока Выборка.Следующий() Цикл
				ДобавитьДвиженияПробег(ДвиженияНаработкаОборудования, Выборка, Реквизиты.Дата, Ложь); 	
			КонецЦикла;
				
		КонецЕсли;
	
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьДвиженияПробег(ДвиженияКоллекция, Выборка, Период, ЭтоПробегТС = Истина)
	
	// Если выполняется корректировка начальных остатков, то пробег и моточасы
	// вводятся разными строками, потому что виды движений у них могут различатся.
	Если Выборка.ВидДвиженияПробег = Перечисления.упВидДвиженияПробег.Корректировка Тогда
		
		Если Выборка.Пробег > 0 Тогда
			ДвижениеПробег	= ДвиженияКоллекция.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеПробег, Выборка);
			ДвижениеПробег.Период      = Период;
			ДвижениеПробег.ВидДвижения = Выборка.ВидДвижения_Пробег;
			ДвижениеПробег.Моточасы    = 0;
			Если ЭтоПробегТС Тогда
				ДвижениеПробег.Одометр = 0;		
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.Моточасы > 0 Тогда
			ДвижениеПробег	= ДвиженияКоллекция.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеПробег, Выборка);
			ДвижениеПробег.Период       = Период;
			ДвижениеПробег.ВидДвижения  = Выборка.ВидДвижения_Моточасы;
			ДвижениеПробег.Пробег       = 0;
			Если ЭтоПробегТС Тогда
				ДвижениеПробег.Одометр = 0;		
			КонецЕсли;
		КонецЕсли;
		
		Если Истина
			И ЭтоПробегТС
			И Выборка.Одометр > 0
		Тогда
			ДвижениеПробег	= ДвиженияКоллекция.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеПробег, Выборка);
			ДвижениеПробег.Период      = Период;
			ДвижениеПробег.ВидДвижения = Выборка.ВидДвижения_Одометр;
			ДвижениеПробег.Пробег      = 0;
			ДвижениеПробег.Моточасы    = 0;
			
		КонецЕсли;
		
	Иначе	
		ДвижениеПробег	= ДвиженияКоллекция.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеПробег, Выборка);
		ДвижениеПробег.Период      = Период;
		ДвижениеПробег.ВидДвижения = Выборка.ВидДвижения_Пробег;
	КонецЕсли;
	
КонецПроцедуры


// Процедура формирует движения по регистру "упРасходы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	упРаспределениеРасходов.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
КонецПроцедуры

// Процедура формирует движения по регистру "упОстаткиГСМ" И "упОстаткиМатериалов".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияСливыГСМ(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	ИспользуютсяЛимитыРасходаГСМ = Ложь;
	Если Реквизиты.Свойство("ИспользуютсяЛимитыРасходаГСМ") Тогда
		Если Реквизиты.ИспользуютсяЛимитыРасходаГСМ Тогда
			ИспользуютсяЛимитыРасходаГСМ = Истина;
		КонецЕсли;
	КонецЕсли;

	ДвиженияОстаткиГСМ				= Движения.упОстаткиГСМ;
	ДвиженияОстаткиГСМ.Записывать	= Истина;
	
	ДвиженияОстаткиМатериалов		= Движения.упОстаткиМатериалов;
	ДвиженияОстаткиМатериалов.Записывать = Истина;
	
	Если ИспользуютсяЛимитыРасходаГСМ Тогда 
		ДвиженияЛимитыРасходаГСМ = Движения.упЛимитыРасходаГСМ;
		ДвиженияЛимитыРасходаГСМ.Записывать = Истина;
	КонецЕсли;

	Статус	= ДополнительныеСвойства.Статус;
	
	Если Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда
		
		МетодОценки 		= упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов");
		ЗаправкиСливыГСМ 				= Реквизиты.ЗаправкиСливыГСМ;
		ЗаправкиСливыГСМДопОборудование	= Реквизиты.ЗаправкиСливыГСМДополнительноеОборудование;
		
		мсвСтрокиСливы				= ЗаправкиСливыГСМ.НайтиСтроки(Новый Структура("ВидДвижения",Перечисления.упВидыДвиженияТоплива.Слив));
		мсвСтрокиСливыДопОборудование = ЗаправкиСливыГСМДопОборудование.НайтиСтроки(Новый Структура("ВидДвижения",Перечисления.упВидыДвиженияТоплива.Слив));
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвСтрокиСливы, мсвСтрокиСливыДопОборудование);
		
		Для каждого СтрокаСлив Из мсвСтрокиСливы Цикл
			
			Если ЗначениеЗаполнено(СтрокаСлив.ПриемникСливаГСМ) Тогда
				
				Если ТипЗнч(СтрокаСлив.ПриемникСливаГСМ) = Тип("СправочникСсылка.упСклады") Тогда
					
					ДвижениеОстаткиМатериалов = ДвиженияОстаткиМатериалов.Добавить();
					ДвижениеОстаткиМатериалов.Период = Реквизиты.Дата;
					ДвижениеОстаткиМатериалов.ВидДвижения	= ВидДвиженияНакопления.Приход;
					ДвижениеОстаткиМатериалов.Материал		= СтрокаСлив.ГСМ;
					ДвижениеОстаткиМатериалов.Склад		= СтрокаСлив.ПриемникСливаГСМ;
					ДвижениеОстаткиМатериалов.Партия		= ?(МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО, 
															Реквизиты.Ссылка, 
															Документы.упПоступлениеМатериалов.ПустаяСсылка());
					ДвижениеОстаткиМатериалов.Количество	= СтрокаСлив.Количество;
					Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
						ДвижениеОстаткиМатериалов.СуммаБезНДС	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(СтрокаСлив.Валюта, СтрокаСлив.Стоимость, Реквизиты.Дата);
						ДвижениеОстаткиМатериалов.СуммаСНДС	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(СтрокаСлив.Валюта, СтрокаСлив.Стоимость + СтрокаСлив.СтоимостьНДС, Реквизиты.Дата);
					Иначе
						ДвижениеОстаткиМатериалов.СуммаБезНДС	= СтрокаСлив.Стоимость;
						ДвижениеОстаткиМатериалов.СуммаСНДС	= СтрокаСлив.Стоимость + СтрокаСлив.СтоимостьНДС;
					КонецЕсли;
				Иначе
									
					ДвижениеОстаткиГСМ	= ДвиженияОстаткиГСМ.Добавить();
					ДвижениеОстаткиГСМ.Период 		= Реквизиты.Дата;
					ДвижениеОстаткиГСМ.ВидДвижения	= ВидДвиженияНакопления.Приход;
					ДвижениеОстаткиГСМ.ТранспортноеСредство	= СтрокаСлив.ПриемникСливаГСМ;
					ДвижениеОстаткиГСМ.ВидГСМ		= СтрокаСлив.ВидГСМ;
					ДвижениеОстаткиГСМ.Количество		= СтрокаСлив.Количество;
					Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
						ДвижениеОстаткиГСМ.Стоимость		= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(СтрокаСлив.Валюта, СтрокаСлив.Стоимость, Реквизиты.Дата);
						ДвижениеОстаткиГСМ.СтоимостьНДС	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(СтрокаСлив.Валюта, СтрокаСлив.СтоимостьНДС, Реквизиты.Дата);
					Иначе
						ДвижениеОстаткиГСМ.Стоимость		= СтрокаСлив.Стоимость;
						ДвижениеОстаткиГСМ.СтоимостьНДС	= СтрокаСлив.СтоимостьНДС;
					КонецЕсли;
				
				КонецЕсли;
				
			КонецЕсли;
			
			Если Истина
				И ИспользуютсяЛимитыРасходаГСМ 
				И НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаСлив, "ДополнительноеОборудование") 
			Тогда 
				ДвижениеЛимитыРасходаГСМ = ДвиженияЛимитыРасходаГСМ.Добавить();
				ДвижениеЛимитыРасходаГСМ.Период 		= Реквизиты.Дата;
				ДвижениеЛимитыРасходаГСМ.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
				ДвижениеЛимитыРасходаГСМ.ВидГСМ		= СтрокаСлив.ВидГСМ;
				ДвижениеЛимитыРасходаГСМ.Организация 	= Реквизиты.Организация;
				ДвижениеЛимитыРасходаГСМ.Подразделение 	= Реквизиты.Подразделение;
				ДвижениеЛимитыРасходаГСМ.Лимит 		= 0;
				ДвижениеЛимитыРасходаГСМ.Расход		= СтрокаСлив.Количество;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;	

	
КонецПроцедуры

// Процедура формирует движения по регистру "упРасходы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьПробегПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус			= ДополнительныеСвойства.Статус;
	
	ДвиженияРасходы	= Движения.упРасходы;
	ДвиженияРасходы.Записывать = Истина;

	Если Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен 
		ИЛИ Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда
		
		КоличествоРейсов = Реквизиты.Рейсы.Количество();
		Для каждого СтрокаРейс Из Реквизиты.Рейсы Цикл
			упРейс.УстановитьФактическиеПараметрыРейса(Новый Структура("Пробег", ?(КоличествоРейсов = 1, Реквизиты.Пробег, СтрокаРейс.Пробег)), СтрокаРейс.Рейс);		
		КонецЦикла;
	КонецЕсли;	

КонецПроцедуры

// Процедура формирует записи по регистру "упНормыРасходаГСМ".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеНормыРасходаГСМ(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияНормыРасходаГСМ	= Движения.упНормыРасходаГСМ;
	ДвиженияНормыРасходаГСМ.Записывать = Ложь;
	
	ДатаНачалаДействия		= Реквизиты.ДатаНачалаДействия;
	Организация			= Реквизиты.Организация;
	Подразделение			= Реквизиты.Подразделение;
	НормыТранспортныхСредств	= Реквизиты.НормыТранспортныхСредств;
	НормыДополнительногоОборудования	= Реквизиты.НормыДополнительногоОборудования;
	МетаданныеДокумент		= Ссылка.Метаданные();
	ИмяОбъектаНормРасхода	= "ТранспортноеСредство";
	ИмяТабличнойЧасти		= "НормыТранспортныхСредств";
	
	Если НормыТранспортныхСредств.Количество() > 0 Тогда
		
		ДвиженияНормыРасходаГСМ.Записывать = Истина;
		
		Для каждого Строка Из НормыТранспортныхСредств Цикл
			ДвижениеНормыРасходаГСМ					= ДвиженияНормыРасходаГСМ.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеНормыРасходаГСМ, Строка);
			ДвижениеНормыРасходаГСМ.Период 			= ДатаНачалаДействия;
			ДвижениеНормыРасходаГСМ.Организация 		= Организация;
			ДвижениеНормыРасходаГСМ.Подразделение 		= Подразделение;
			ДвижениеНормыРасходаГСМ.ОбъектНормРасхода 	= Строка.ТранспортноеСредство;
		КонецЦикла;
	КонецЕсли;
	
	Если НормыДополнительногоОборудования.Количество() > 0 Тогда
		
		ДвиженияНормыРасходаГСМ.Записывать = Истина;
		
		Для каждого Строка Из НормыДополнительногоОборудования Цикл
			ДвижениеНормыРасходаГСМ					= ДвиженияНормыРасходаГСМ.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеНормыРасходаГСМ, Строка);
			ДвижениеНормыРасходаГСМ.Период 			= ДатаНачалаДействия;
			ДвижениеНормыРасходаГСМ.Организация 		= Организация;
			ДвижениеНормыРасходаГСМ.Подразделение 		= Подразделение;
			ДвижениеНормыРасходаГСМ.ОбъектНормРасхода 	= Строка.ДополнительноеОборудование;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упНормыРасходаРежимовРаботы".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеНормыРасходаРежимовРаботы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачалаДействия = Реквизиты.ДатаНачалаДействия;
	Организация	= Реквизиты.Организация;
	Подразделение = Реквизиты.Подразделение;
	
	Если ДополнительныеСвойства.Свойство("НормыРежимовРаботы") Тогда
		
		НормыРежимовРаботы = ДополнительныеСвойства.НормыРежимовРаботы;
		
		ДвиженияРежимовРаботы	= Движения.упНормыРасходаРежимовРаботы;
		ДвиженияРежимовРаботы.Записывать = Истина;
		
		Для каждого Строка Из НормыРежимовРаботы Цикл
			ДвижениеРежимовРаботы	= ДвиженияРежимовРаботы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеРежимовРаботы, Строка);
			ДвижениеРежимовРаботы.Период 		= ДатаНачалаДействия;
			ДвижениеРежимовРаботы.Организация	= Организация;
			ДвижениеРежимовРаботы.Подразделение = Подразделение;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвижениеНормыРасходаРежимовРаботы()

// Процедура формирует записи по регистру "упЗаявкиКВыполнению".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаявкиКВыполнению = Движения.упЗаявкиКВыполнению;
	ДвиженияЗаявкиКВыполнению.Записывать = Истина;
	
	Статус = ДополнительныеСвойства.Статус;
	Если Ложь
		Или Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан
		Или Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен
		Или Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт
	Тогда 
	
		Для каждого Строка Из Реквизиты.Заявки Цикл
			ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
			ДвижениеЗаявки.Период = Реквизиты.Дата;
			ДвижениеЗаявки.Регистратор = Ссылка;
			ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеЗаявки.Заявка = Строка.ЗаявкаНаПеревозкуГруза;
			ДвижениеЗаявки.Количество = Строка.Количество;	
			ДвижениеЗаявки.НомерВЦепиПоставок = 0;
			ДвижениеЗаявки.НомерВЦепиПоставокКонец = 0;
		КонецЦикла;
		ДополнительныеСвойства.КонтролироватьЗаявки = Истина;										
		
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

// Проверка, можно ли формировать путевой на основании списка рейсов.
//
// Параметры:
//  мсвРейсы			 - Массив							 - Массив рейсов.
//  ПутевойЛист		 - ДокументсСсылка.упПутевойЛист		 - если по рейсам уже есть путевой лист, то он вернется через этот параметр.
//  ТранспортноеСредство	 - СправочникСсылка.ТранспортныеСредства - возвращется транспортное средство по рейсам.
//  Отказ				 - Булево							 - Ложь, если формировать нельзя.
//
Процедура ПроверитьВозможностьФормированияПутевогоНаОснованииРейсов(мсвРейсы, ПутевойЛист = Неопределено, ТранспортноеСредство, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРейс.Ссылка КАК Рейс,
	               |	упСтатусыРейсовСрезПоследних.Статус КАК СтатусРейса,
	               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	               |	упРейс.Организация,
	               |	ЕСТЬNULL(упПутевойЛистРейсы.Ссылка, ЗНАЧЕНИЕ(Документ.упПутевойЛист.ПустаяСсылка)) КАК ПутевойЛист
	               |ИЗ
	               |	Документ.упРейс КАК упРейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |		ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	               |		ПО (упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	               |			ПО (НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	               |				И упПутевойЛистРейсы.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ
	               |		ПО (упПутевойЛистРейсы.Рейс = упРейс.Ссылка)
	               |			И (НЕ упПутевойЛистРейсы.Ссылка.ПометкаУдаления)
	               |ГДЕ
	               |	упРейс.Ссылка В(&мсвРейсы)";
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	Выборка	= Запрос.Выполнить().Выбрать();
	Организация 			= Справочники.Организации.ПустаяСсылка();
	ТранспортноеСредство	= Справочники.упТранспортныеСредства.ПустаяСсылка();
	ПутевойЛист				= Документы.упПутевойЛист.ПустаяСсылка();
	РейсСПутевымЛистом		= Документы.упРейс.ПустаяСсылка();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			ТранспортноеСредство = Выборка.ТранспортноеСредство;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Организация = Выборка.Организация;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(ПутевойЛист) Тогда
			ПутевойЛист = Выборка.ПутевойЛист;
			РейсСПутевымЛистом = Выборка.Рейс;
		КонецЕсли;	
		
		Если НЕ Выборка.ПутевойЛист = ПутевойЛист Тогда
			ОписаниеОшибки = Нстр("ru = 'По рейсам уже были сформированы путевые листы %1(%2), %3(%4)'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, РейсСПутевымЛистом, ПутевойЛист, Выборка.Рейс, Выборка.ПутевойЛист);
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	
		Если НЕ Отказ 
				И НЕ Выборка.Организация = Организация Тогда
			ОписаниеОшибки = Нстр("ru = 'Организация у выбранных рейсов должна быть одинаковая'");
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Отказ = Истина;
			Прервать;
		КонецЕсли;

		Если НЕ Отказ 
				И НЕ Выборка.ТранспортноеСредство = ТранспортноеСредство Тогда
			ОписаниеОшибки = Нстр("ru = 'Для выбранных рейсов должно быть назначено одно и то же транспортное средство'");
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		Если Выборка.СтатусРейса = Перечисления.упСтатусыРейса.Новый
			Или Выборка.СтатусРейса = Перечисления.упСтатусыРейса.КПланированиюТС
			Или Выборка.СтатусРейса = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС	Тогда
			Отказ = Истина;
			ОписаниеОшибки = НСтр(СтрШаблон("ru = 'На основании рейса %1 в статусе ""%2"" не может быть сформирован путевой лист'", Выборка.Рейс, Выборка.СтатусРейса));
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Проверка, можно ли формировать путевой на основании списка заявок на перевозку груза.
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза	 - Массив	 - заявки на перевозку груза (ДокументСсылка.упЗаявкаНаПеревозкуГруза).
//  Отказ					 - Булево	 - Ложь, если формировать нельзя.
//
Процедура ПроверитьВозможностьФормированияПутевогоНаОснованииЗаявокНаПеревозкуГрузов(мсвЗаявкиНаПеревозкуГруза, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозку,
	               |	ЕСТЬNULL(упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) КАК Статус,
	               |	ЕСТЬNULL(упЗаявкиКВыполнениюОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	               |ИЗ
	               |	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упЗаявкиКВыполнению.Остатки(, Заявка В (&Заявки)) КАК упЗаявкиКВыполнениюОстатки
	               |		ПО (упЗаявкиКВыполнениюОстатки.Заявка = упЗаявкаНаПеревозкуГруза.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних
	               |		ПО (упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Документ = упЗаявкаНаПеревозкуГруза.Ссылка)
	               |ГДЕ
	               |	упЗаявкаНаПеревозкуГруза.Ссылка В(&Заявки)";
	Запрос.УстановитьПараметр("Заявки", мсвЗаявкиНаПеревозкуГруза);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
				
		Если Ложь
			Или Выборка.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
			Или Выборка.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована
		Тогда	
			ТекстСообщения = Нстр("ru = 'Запрещено формировать путевые листы по заявке в статусе %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Статус);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.ЗаявкаНаПеревозку,,,Отказ);	
			Продолжить;
		КонецЕсли;
		
		Если Выборка.КоличествоОстаток <= 0 Тогда
			ТекстСообщения = Нстр("ru = 'По %1 уже закрыты все рабочие смены.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ЗаявкаНаПеревозку);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.ЗаявкаНаПеревозку,,,Отказ);	
		КонецЕсли;
		
	КонецЦикла;	
		
КонецПроцедуры

// Функция - Проверить возможность добавления рейсов в путевой лист
//
// Параметры:
//  мсвРейсы	 - Массив	 - массив рейсов.
//  Отказ		 - Булево	 - Истина, если проверка не прошла.
// 
// Возвращаемое значение:
//  Структура - структура отбора для путевых листов
//
Функция ПроверитьВозможностьДобавленияРейсовВПутевойЛист(мсвРейсы, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРейс.Ссылка КАК Рейс,
	               |	упСтатусыРейсовСрезПоследних.Статус КАК СтатусРейса,
	               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	               |	упРейс.Организация,
	               |	ЕСТЬNULL(упПутевойЛистРейсы.Ссылка, ЗНАЧЕНИЕ(Документ.упПутевойЛист.ПустаяСсылка)) КАК ПутевойЛист
	               |ИЗ
	               |	Документ.упРейс КАК упРейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |		ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	               |		ПО (упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	               |			ПО (НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен))
	               |				И упПутевойЛистРейсы.Ссылка = упСтатусыПутевыхЛистовСрезПоследних.Документ
	               |		ПО (упПутевойЛистРейсы.Рейс = упРейс.Ссылка)
	               |			И (НЕ упПутевойЛистРейсы.Ссылка.ПометкаУдаления)
	               |ГДЕ
	               |	упРейс.Ссылка В(&мсвРейсы)";
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
		
	Организация 				= Справочники.Организации.ПустаяСсылка();
	РейсОрганизация				= Документы.упРейс.ПустаяСсылка();
	ТранспортноеСредство		= Справочники.упТранспортныеСредства.ПустаяСсылка();
	РейсТранспортноеСредство	= Документы.упРейс.ПустаяСсылка();
	
	ВыборкаРейс = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаРейс.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Организация 	= ВыборкаРейс.Организация;
			РейсОрганизация = ВыборкаРейс.Рейс;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			ТранспортноеСредство 		= ВыборкаРейс.ТранспортноеСредство;
			РейсТранспортноеСредство 	= ВыборкаРейс.Рейс;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаРейс.ПутевойЛист) Тогда
			ОписаниеОшибки = Нстр("ru = 'По рейсу %1 уже сформирован путевой лист %2'");
			ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, ВыборкаРейс.Рейс, ВыборкаРейс.ПутевойЛист);
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		Если НЕ ВыборкаРейс.ТранспортноеСредство = ТранспортноеСредство Тогда
			ОписаниеОшибки = Нстр("ru = 'Транспортные средства в рейсе %1(%2) и %3(%4) различаются'");
			ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, ВыборкаРейс.Рейс, ВыборкаРейс.ТранспортноеСредство, РейсТранспортноеСредство, ТранспортноеСредство);
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		Если НЕ ВыборкаРейс.Организация = Организация Тогда
			ОписаниеОшибки = Нстр("ru = 'Организации в рейсе %1(%2) и %3(%4) различаются'");
			ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, ВыборкаРейс.Рейс, ВыборкаРейс.Организация, РейсОрганизация, Организация);
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		Если ВыборкаРейс.СтатусРейса = Перечисления.упСтатусыРейса.Новый
			Или ВыборкаРейс.СтатусРейса = Перечисления.упСтатусыРейса.КПланированиюТС
			Или ВыборкаРейс.СтатусРейса = Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС	Тогда
			Отказ = Истина;
			ОписаниеОшибки = НСтр(СтрШаблон("ru = 'Рейса %1 в статусе ""%2"" не может быть включен путевой лист'", ВыборкаРейс.Рейс, ВыборкаРейс.СтатусРейса));
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
			Прервать;
		КонецЕсли;

	КонецЦикла;

	сткРезультат = Новый Структура();
	сткРезультат.Вставить("Организация", Организация);
	сткРезультат.Вставить("ТранспортноеСредство", ТранспортноеСредство);
	
	Возврат сткРезультат;
	
КонецФункции

// Процедура - Проверить наличие путевых листов по рейсу
//
// Параметры:
//  мсвРейсы	 - Массив						 - массив рейсов.
//  ПутевойЛист	 - ДокументСсылка.упПутевойЛист	 - ссылка на путевой лист.
//  Отказ		 - Булево						 - Истина, при проверке произошла ошибка.
//
Процедура ПроверитьНаличиеПутевыхЛистовПоРейсу(мсвРейсы, ПутевойЛист, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПутевойЛистРейсы.Ссылка КАК ПутевойЛист,
	|	упПутевойЛистРейсы.Рейс
	|ИЗ
	|	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛистРейсы.Ссылка)
	|ГДЕ
	|	упПутевойЛистРейсы.Рейс В(&мсвРейсы)
	|	И НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)
	|	И НЕ упПутевойЛистРейсы.Ссылка = &ПутевойЛист
	|	И НЕ упПутевойЛистРейсы.Ссылка.ПометкаУдаления
	|	И упПутевойЛистРейсы.Ссылка.Проведен";
	Запрос.УстановитьПараметр("мсвРейсы", 		мсвРейсы);
	Запрос.УстановитьПараметр("ПутевойЛист", 	ПутевойЛист);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		ПутевойЛистСсылка = текВыборка.ПутевойЛист;
		ТекстОшибки = НСтр("ru = 'По %1 существует проведенный путевой лист %2'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, текВыборка.Рейс, ПутевойЛистСсылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,ПутевойЛистСсылка,,,Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Проверить установку доп оборудования на ТС
//
// Параметры:
//  Объект			 - Объект								 - объект
//  тбзДопОборудование	 - ТаблицаЗначений						 - доп. оборудование.
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства	 - транспортное средство.
//  Период			 - Дата								 - период запроса.
//  Отказ				 - Булево								 - истина, в слуаче ошибки.
//
Процедура ПроверитьУстановкуДопОборудованияНаТС(Объект, тбзДопОборудование, ТранспортноеСредство, Период, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДопОборудование.НомерСтроки КАК НомерСтроки,
		|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
		|	ДопОборудование.ТранспортноеСредство КАК ТранспортноеСредство
		|ПОМЕСТИТЬ втДополнительноеОборудование
		|ИЗ
		|	&ДопОборудование КАК ДопОборудование
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПрицепы.Прицеп КАК ТранспортноеСредство
		|ПОМЕСТИТЬ втТранспортныеСредстваПутевогоЛиста
		|ИЗ
		|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
		|		&Период,
		|		ТранспортноеСредство = &ТранспортноеСредство) КАК упПрицепы
		|ГДЕ упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	&ТранспортноеСредство КАК ТранспортноеСредство
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ 
		|	ДопОборудование.НомерСтроки КАК НомерСтроки,
		|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
		|	ДопОборудование.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ВЫБОР
		|		КОГДА АгрегатыТС.Агрегат ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК АгрегатНеУстановленНаТС,
		|	ВЫБОР
		|		КОГДА ТранспортныеСредства.ТранспортноеСредство ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТСНеЯвляетсяПрицепом
		|ИЗ
		|	втДополнительноеОборудование КАК ДопОборудование
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
		|		&Период,
		|		) КАК АгрегатыТС
		|	ПО ИСТИНА
		|		И АгрегатыТС.Агрегат = ДопОборудование.ДополнительноеОборудование
		|		И АгрегатыТС.ТранспортноеСредство = ДопОборудование.ТранспортноеСредство
		|		И АгрегатыТС.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
		|	ЛЕВОЕ СОЕДИНЕНИЕ втТранспортныеСредстваПутевогоЛиста КАК ТранспортныеСредства
		|	ПО ДопОборудование.ТранспортноеСредство = ТранспортныеСредства.ТранспортноеСредство
		|ГДЕ ЛОЖЬ
		|	ИЛИ АгрегатыТС.Агрегат ЕСТЬ NULL
		|	ИЛИ ТранспортныеСредства.ТранспортноеСредство ЕСТЬ NULL
		|";
	  	
	Запрос.УстановитьПараметр("ДопОборудование", тбзДопОборудование);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.ТСНеЯвляетсяПрицепом Тогда
			ТекстОшибки = НСтр("ru = 'Транспортное средство ""%1"" не является прицепом для ""%2""'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Выборка.ТранспортноеСредство, ТранспортноеСредство);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ДополнительноеОборудование[" + Формат(Выборка.НомерСтроки-1,"ЧН=0; ЧГ=0") + "].ТранспортноеСредство","Объект");								
		ИначеЕсли Выборка.АгрегатНеУстановленНаТС Тогда	
			ТекстОшибки = НСтр("ru = 'На начало рейса дополнительное оборудование ""%1"" не установлено на ""%2""'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Выборка.ДополнительноеОборудование, Выборка.ТранспортноеСредство);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ДополнительноеОборудование[" + Формат(Выборка.НомерСтроки-1,"ЧН=0; ЧГ=0") + "].ДополнительноеОборудование","Объект");								
		КонецЕсли;
	КонецЕсли;
					
КонецПроцедуры

// Контроль остатков ГСМ.
//
// Параметры:
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура ПроконтролироватьОтрицательныеОстатки(ВидГСМ, ТранспортноеСредство, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	упОстаткиГСМОстатки.ТранспортноеСредство,
	               	|	упОстаткиГСМОстатки.ВидГСМ,
	               	|	упОстаткиГСМОстатки.КоличествоОстаток,
	               	|	упОстаткиГСМОстатки.СтоимостьОстаток,
					|	упОстаткиГСМОстатки.СтоимостьНДСОстаток
	               	|ИЗ
	               	|	РегистрНакопления.упОстаткиГСМ.Остатки(
	               	|			,
	               	|			ВидГСМ = &ВидГСМ
	               	|				И ТранспортноеСредство = &ТранспортноеСредство) КАК упОстаткиГСМОстатки
	               	|ГДЕ
	               	|	упОстаткиГСМОстатки.КоличествоОстаток < 0
	               	|	ИЛИ упОстаткиГСМОстатки.СтоимостьОстаток < 0
					|	ИЛИ упОстаткиГСМОстатки.СтоимостьНДСОстаток < 0";
					
	Запрос.УстановитьПараметр("ВидГСМ", 				ВидГСМ);
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	
	текВыборка = Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		ТекстСообщения = НСтр("ru = 'Остаток ГСМ:""%1"" по ТС:""%2"" меньше нуля(Количество:%3; Стоимость с НДС:%4; Стоимость НДС:%5)'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВидГСМ, ТранспортноеСредство, текВыборка.КоличествоОстаток, текВыборка.СтоимостьОстаток, текВыборка.СтоимостьНДСОстаток);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
			
КонецПроцедуры // ПроконтролироватьОтрицательныеОстатки()

// Процедура - Проконтролировать перелив бака.
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	РеквизитыОбъекта - Структура - Перечислены реквизиты документа.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура ПроконтролироватьПереливБака(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "  
	|	ВЫБРАТЬ
	|		ВидыГСМ_ТС.ТранспортноеСредство КАК ТранспортноеСредство,
	|		ВидыГСМ_ТС.ВидГСМ КАК ВидГСМ,
	|		ВидыГСМ_ТС.ОбъемБака КАК ОбъемБака
	|	ПОМЕСТИТЬ втВидыГСМПоТС
	|	ИЗ
	|		РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(,ТранспортноеСредство = &ТранспортноеСредство) КАК ВидыГСМ_ТС
	|	ГДЕ ВидыГСМ_ТС.ОбъемБака > 0
	|		И ВидыГСМ_ТС.Используется
	|";

	
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|ОБЪЕДИНИТЬ ВСЕ
		|";

		
		Запрос.Текст = Запрос.Текст +
		"
		|// Слив топлива в путевом листе
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаправкиСливы_ПЛ.ПриемникСливаГСМ КАК ТранспортноеСредство,
		|	ЗаправкиСливы_ПЛ.ВидГСМ КАК ВидГСМ,
		|	ВидыГСМ_ТС.ОбъемБака КАК ОбъемБака
		|ИЗ
		|	РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних() КАК ВидыГСМ_ТС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.ЗаправкиСливыГСМ КАК ЗаправкиСливы_ПЛ
		|	ПО ИСТИНА
		|		И ВидыГСМ_ТС.ТранспортноеСредство = ЗаправкиСливы_ПЛ.ПриемникСливаГСМ
		|		И ВидыГСМ_ТС.ВидГСМ = ВидыГСМ_ТС.ВидГСМ
		|		И ВидыГСМ_ТС.ОбъемБака > 0
		|		И ЗаправкиСливы_ПЛ.Ссылка = &ДокументСсылка
		|		И ЗаправкиСливы_ПЛ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		|		И ВидыГСМ_ТС.Используется
		|";
		
		
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаправкаСливГСМ") Тогда	
		
		Запрос.Текст = Запрос.Текст + "
		|ОБЪЕДИНИТЬ ВСЕ
		|";
		
		Запрос.Текст = Запрос.Текст + 		
		"
		|// Слив топлива в документе Заправки и сливы
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаправкиСливы.Склад КАК ТранспортноеСредство,
		|	ЗаправкиСливы.ВидГСМ КАК ВидГСМ,
		|	ВидыГСМ_ТС.ОбъемБака КАК ОбъемБака
		|ИЗ
		|	РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних() КАК ВидыГСМ_ТС
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаправкаСливГСМ КАК ЗаправкиСливы
		|	ПО ИСТИНА
		|		И ВидыГСМ_ТС.ТранспортноеСредство = ЗаправкиСливы.ТранспортноеСредство
		|		И ВидыГСМ_ТС.ВидГСМ = ЗаправкиСливы.ВидГСМ
		|		И ЗаправкиСливы.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Слив)
		|		И ВидыГСМ_ТС.ОбъемБака > 0
		|		И ЗаправкиСливы.Ссылка = &ДокументСсылка
		|		И ВидыГСМ_ТС.Используется
		|";

		
		
		
	КонецЕсли;	
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов();
				
	Запрос.Текст = Запрос.Текст + 
	"
	|//{Запрос: 1 ////////////////////////////////////////
	|		ВЫБРАТЬ
	|			упОстаткиГСМОстатки.ТранспортноеСредство КАК ТранспортноеСредство,
	|			упОстаткиГСМОстатки.ВидГСМ КАК ВидГСМ,
	|			упОстаткиГСМОстатки.КоличествоОстаток - втВидыГСМПоТС.ОбъемБака КАК Перелив
	|		ИЗ
	|			РегистрНакопления.упОстаткиГСМ.Остатки(
	|				,
	|				(ТранспортноеСредство, ВидГСМ) В (
	|						ВЫБРАТЬ
	|						втВидыГСМПоТС.ТранспортноеСредство КАК ТранспортноеСредство,
	|						втВидыГСМПоТС.ВидГСМ КАК ВидГСМ
	|					ИЗ
	|						втВидыГСМПоТС КАК втВидыГСМПоТС)) КАК упОстаткиГСМОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыГСМПоТС КАК втВидыГСМПоТС
	|			ПО ИСТИНА
	|				И упОстаткиГСМОстатки.ТранспортноеСредство = втВидыГСМПоТС.ТранспортноеСредство
	|				И упОстаткиГСМОстатки.ВидГСМ = втВидыГСМПоТС.ВидГСМ
	|		ГДЕ упОстаткиГСМОстатки.КоличествоОстаток - втВидыГСМПоТС.ОбъемБака > 0
	|";

	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("ТранспортноеСредство", РеквизитыОбъекта.ТранспортноеСредство);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = Нстр("ru = 'Перелив бака %1 на %2 л %3'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.ТранспортноеСредство, Выборка.Перелив, Выборка.ВидГСМ);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,,,Отказ);				
	КонецЦикла;
		
КонецПроцедуры

// Функция проводит сравнение переданного документа с последним документов регистра "упОстаткиГСМ".
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата - Дата - Период.
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЭтоПоследнийДокументИзменявшийОстаткиГСМ(Ссылка, Дата, ТранспортноеСредство, ВидГСМ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМ.Количество КАК Количество,
	               |	упОстаткиГСМ.Стоимость КАК Стоимость,
				|	упОстаткиГСМ.СтоимостьНДС КАК СтоимостьНДС
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ КАК упОстаткиГСМ
	               |ГДЕ
	               |	упОстаткиГСМ.МоментВремени > &МоментВремени
	               |	И упОстаткиГСМ.ТранспортноеСредство = &ТранспортноеСредство
	               |	И упОстаткиГСМ.ВидГСМ = &ВидГСМ";
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Дата, Ссылка));
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("ВидГСМ", ВидГСМ);
	Результат = Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

// Функция проводит сравнение переданного документа с последним документов регистра "упОстаткиГСМ" с учетом таблицы ГСМ.
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата - Дата - Период.
//	тбзГСМ - ТаблицаЗначений - Таблица данных.
//  ДатаПоследнегоДвиженияГСМ - Дата - Период.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЭтоПоследнийДокументИзменявшийОстаткиГСМПоТаблицеГСМ(Ссылка, Дата, тбзГСМ, ДатаПоследнегоДвиженияГСМ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзГСМ.ВидГСМ,
	               |	тбзГСМ.ТранспортноеСредство
	               |ПОМЕСТИТЬ втГСМПоПутевомуЛисту
	               |ИЗ
	               |	&тбзГСМ КАК тбзГСМ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упОстаткиГСМ.Количество КАК Количество,
	               |	упОстаткиГСМ.Стоимость КАК Стоимость,
	               |	упОстаткиГСМ.СтоимостьНДС КАК СтоимостьНДС,
	               |	упОстаткиГСМ.Период
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ КАК упОстаткиГСМ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГСМПоПутевомуЛисту КАК втГСМПоПутевомуЛисту
	               |		ПО упОстаткиГСМ.ТранспортноеСредство = втГСМПоПутевомуЛисту.ТранспортноеСредство
	               |			И упОстаткиГСМ.ВидГСМ = втГСМПоПутевомуЛисту.ВидГСМ
	               |ГДЕ
	               |	упОстаткиГСМ.МоментВремени > &МоментВремени
	               |	И НЕ упОстаткиГСМ.Регистратор = &Ссылка";

	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Дата, Ссылка));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("тбзГСМ", тбзГСМ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ДатаПоследнегоДвиженияГСМ = Дата;
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Период > ДатаПоследнегоДвиженияГСМ Тогда
			ДатаПоследнегоДвиженияГСМ = Выборка.Период;
		КонецЕсли;
		
	КонецЦикла;
		
	Результат = РезультатЗапроса.Пустой();
		
	Возврат Результат;

	
КонецФункции // ЭтоПоследнийДокументИзменявшийОстаткиГСМПоТаблицеГСМ()

// Функция проводит сравнение переданного документа и последним путевым листом,
//	делавшим движения по регистру "упОстаткиГСМ".
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата - Дата - Период.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ(Ссылка, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛистГСМ.ВидГСМ КАК ВидГСМ,
	|	ВЫБОР
	|		КОГДА упПутевойЛистГСМ.ДопОборудование = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
	|			ТОГДА упПутевойЛистГСМ.Ссылка.ТранспортноеСредство
	|		ИНАЧЕ упПутевойЛистГСМ.ДопОборудование
	|	КОНЕЦ КАК ТранспортноеСредство
	|ПОМЕСТИТЬ втГСМПоПутевомуЛисту
	|ИЗ
	|	Документ.упПутевойЛист.ГСМ КАК упПутевойЛистГСМ
	|ГДЕ упПутевойЛистГСМ.Ссылка = &Ссылка
	|		
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбДополнительноеОборудование.ВидГСМ КАК ВидГСМ,
	|	тбДополнительноеОборудование.ДополнительноеОборудование
	|ИЗ
	|	Документ.упПутевойЛист.ГСМДополнительноеОборудование КАК тбДополнительноеОборудование
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упОстаткиГСМ.Количество КАК Количество,
	|	упОстаткиГСМ.Стоимость КАК Стоимость,
	|	упОстаткиГСМ.СтоимостьНДС КАК СтоимостьНДС
	|ИЗ
	|	РегистрНакопления.упОстаткиГСМ КАК упОстаткиГСМ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГСМПоПутевомуЛисту КАК втГСМПоПутевомуЛисту
	|	ПО ИСТИНА
	|		И упОстаткиГСМ.ТранспортноеСредство = втГСМПоПутевомуЛисту.ТранспортноеСредство
	|		И упОстаткиГСМ.ВидГСМ = втГСМПоПутевомуЛисту.ВидГСМ
	|ГДЕ ИСТИНА
	|	И упОстаткиГСМ.МоментВремени > &МоментВремени
	|	И НЕ (упОстаткиГСМ.Регистратор = &Ссылка)
	|		
	|";

	Запрос.УстановитьПараметр("МоментВремени", 	Новый МоментВремени(Дата, Ссылка));
	Запрос.УстановитьПараметр("Ссылка", 		Ссылка);
	
	текРезультат	= Запрос.Выполнить().Пустой();
	
	Возврат текРезультат;
	
КонецФункции

// Функция проверят наличие документов движения регистра "упОстаткиГСМ" от указанного момента времени. 
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата - Дата - Период.
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция СписокПоследующихДокументовИзменявшихОстатки(Ссылка, Дата, ТранспортноеСредство, ВидГСМ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМ.Регистратор
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ КАК упОстаткиГСМ
	               |ГДЕ
	               |	упОстаткиГСМ.ТранспортноеСредство = &ТранспортноеСредство
	               |	И упОстаткиГСМ.ВидГСМ = &ВидГСМ
	               |	И упОстаткиГСМ.МоментВремени > &МоментВремени";
	Запрос.УстановитьПараметр("МоментВремени", Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("ВидГСМ", ВидГСМ);
	текРезультат	= Запрос.Выполнить().Пустой();
	
	Возврат текРезультат;
	
КонецФункции

// Функция проводит сравнение переданного документа с последним документов регистра "упПробегТС".
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата - Дата - Период.
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЭтоПоследнийДокументИзменявшийПробег(Ссылка, Дата, ТранспортноеСредство) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	упПробегТС.Пробег КАК Пробег,
	|	упПробегТС.Моточасы КАК Моточасы
	|ИЗ
	|	РегистрНакопления.упПробегТС КАК упПробегТС
	|ГДЕ ИСТИНА
	|	И упПробегТС.МоментВремени > &МоментВремени
	|	И НЕ (упПробегТС.Регистратор = &Регистратор)
	|	И упПробегТС.ТранспортноеСредство В  (&ТранспортноеСредство)
	|		
	|";

	мсвТранспортныеСредства = Неопределено;
	
	Если НЕ ТипЗнч(ТранспортноеСредство) = Тип("Массив") Тогда
		мсвТранспортныеСредства = Новый Массив;
		мсвТранспортныеСредства.Добавить(ТранспортноеСредство);
	Иначе
		мсвТранспортныеСредства = ТранспортноеСредство;
	КонецЕсли;

	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Дата, Ссылка));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ТранспортноеСредство", мсвТранспортныеСредства);
	
	Результат	= Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

// Функция проводит сравнение переданного документа с последним документов регистра "упНаработкаОборудования".
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата   - Дата                         - Период.
//	ДополнительноеОборудование - СправочникСсылка.упДополнительноеОборудование  - дополнительное оборудование
//						  - СправочникСсылка.упШиныУзлыИАгрегаты           - агрегат.
//                              - Массив                                         - оборудование 
//                                (СправочникСсылка.упДополнительноеОборудование, СправочникСсылка.упШиныУзлыИАгрегаты).
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЭтоПоследнийДокументИзменявшийНаработку(Ссылка, Дата, ДополнительноеОборудование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	упНаработка.Пробег КАК Пробег,
	|	упНаработка.Регистратор,
	|	упНаработка.Моточасы КАК Моточасы
	|ИЗ
	|	РегистрНакопления.упНаработкаОборудования КАК упНаработка
	|ГДЕ ИСТИНА
	|	И упНаработка.МоментВремени > &МоментВремени
	|	И НЕ (упНаработка.Регистратор = &Регистратор)
	|	И упНаработка.Оборудование В  (&ДополнительноеОборудование)
	|		
	|";

	мсвДополнительноеОборудование = Неопределено;
	
	Если НЕ ТипЗнч(ДополнительноеОборудование) = Тип("Массив") Тогда
		мсвДополнительноеОборудование = Новый Массив;
		мсвДополнительноеОборудование.Добавить(ДополнительноеОборудование);
	Иначе
		мсвДополнительноеОборудование = ДополнительноеОборудование;
	КонецЕсли;

	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Дата, Ссылка));
	Запрос.УстановитьПараметр("Регистратор",   Ссылка);
	Запрос.УстановитьПараметр("ДополнительноеОборудование", мсвДополнительноеОборудование);
	
	Результат	= Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

// Функция проверяет, является ли переданный путевой лист последним по дате для заданного ТС
//
// Параметры:
//	Ссылка - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Дата   - Дата                         - Период.
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства  - транспортное средство.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ЭтоПоследнийПутевойЛистПоТС(Ссылка, Дата, ТранспортноеСредство) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	упПутевойЛист.Ссылка КАК ПутевойЛист
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|	ПО упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка
	|ГДЕ ИСТИНА
	|	И упСтатусыПутевыхЛистовСрезПоследних.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)
	|	И упПутевойЛист.Ссылка <> &ПутевойЛист
	|	И упПутевойЛист.Дата > &Дата
	|	И НЕ (упПутевойЛист.ПометкаУдаления)
	|	И упПутевойЛист.Проведен
	|	И упПутевойЛист.ТранспортноеСредство = &ТранспортноеСредство
	|";
	
	Запрос.УстановитьПараметр("ПутевойЛист", Ссылка);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Результат	= Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает значение показателя топлива ТС по датчикам.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//
// Возвращаемое значение:
//   Число - Результат выполнения.
//
Функция УровеньТоплива(ТранспортноеСредство, ВидГСМ) Экспорт
	
	текРезультат = 0;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упДатчики.Ссылка КАК Датчик
	               |ПОМЕСТИТЬ втДатчики
	               |ИЗ
	               |	Справочник.упДатчики КАК упДатчики
	               |ГДЕ
	               |	упДатчики.ВидГСМ = &ВидГСМ
	               |	И упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Значение, упОстаткиГСМОстатки.КоличествоОстаток) КАК УровеньТоплива
	               |ИЗ
	               |	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				И Датчик В
	               |					(ВЫБРАТЬ
	               |						втДатчики.Датчик
	               |					ИЗ
	               |						втДатчики КАК втДатчики)) КАК упДанныеДатчиковСрезПоследних
	               |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиГСМ.Остатки(
	               |				,
	               |				ТранспортноеСредство = &ТранспортноеСредство
	               |					И ВидГСМ = &ВидГСМ) КАК упОстаткиГСМОстатки
	               |		ПО (ИСТИНА)";
				   
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	Запрос.УстановитьПараметр("ВидГСМ", 				ВидГСМ);
	
	текВыборкаУровень	= Запрос.Выполнить().Выбрать();
	Если текВыборкаУровень.Следующий() Тогда
		текРезультат = текВыборкаУровень.УровеньТоплива;
	КонецЕсли;
		
	Возврат текРезультат;
	
КонецФункции

// Функция возвращает таблицу значний показателей топлива по всем видам ГСМ под датчикам.
//
// Параметры:
//  ТранспортноеСредство		 - СправочникСсылка.упТранспортныеСредства	 - Ссылка на элемент.
//  Период				 - Дата								 - период.
//  Организация			 - СправочникСсылка.Организации			 - организация
//  Подразделение			 - СправочникСсылка.упСтруктураПредприятия	 - подразделение.
//  НачалоПутевогоЛиста		 - Дата								 - начало путевого.
//  ОкончаниеПутевогоЛиста	 - Дата								 - окончание путевого.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция УровеньТопливаТСПоВсемВидамГСМ(ТранспортноеСредство, Период, Организация, Подразделение, НачалоПутевогоЛиста, ОкончаниеПутевогоЛиста) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Виды ГСМ из табличной части ""Виды ГСМ"" транспортного средства,
	|// если табличная часть не заполнена, то у ТС вид гсм есть null
	|ВЫБРАТЬ
	|	ТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	|	тчВидыГСМ.ВидГСМ КАК ВидГСМ,
	|	тчВидыГСМ.ПроцентИспользования КАК ПроцентИспользования,
	|	ТранспортныеСредства.Модель КАК Модель
	|ПОМЕСТИТЬ втВидыГСМ_ТС
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК ТранспортныеСредства
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК тчВидыГСМ
	|	ПО ТранспортныеСредства.Ссылка = тчВидыГСМ.ТранспортноеСредство
	|ГДЕ ТранспортныеСредства.Ссылка = &ТранспортноеСредство
	|	И тчВидыГСМ.Используется
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Нормы расхода ГСМ из справочника ""Нормы расхода"", определяется
	|// по модели ТС.
	|// 1. Если у ТС не была заполнена ТЧ.ВидыГСМ, то виды берутся из норм по модели.
	|ВЫБРАТЬ
	|	втВидыГСМ_ТСТ.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМ_ТСТ.ПроцентИспользования КАК ПроцентИспользования,
	|	МоделиТС_НормыРасхода.НормаРасхода КАК НормаРасхода,
	|	МоделиТС_НормыРасхода.НормаРасхода.ИзменениеРасхода КАК ИзменениеРасхода,
	|	МоделиТС_НормыРасхода.НормаРасхода.Расход КАК Расход,
	|	МоделиТС_НормыРасхода.НормаРасхода.ФормулаРасчета КАК ФормулаРасчета,
	|	МоделиТС_НормыРасхода.НормаРасхода.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	МоделиТС_НормыРасхода.НормаРасхода.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	МоделиТС_НормыРасхода.НормаРасхода.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте,
	|	МоделиТС_НормыРасхода.ДатаУстановки КАК ДатаУстановки
	|ПОМЕСТИТЬ втВидыГСМ_ПоНормам
	|ИЗ
	|	втВидыГСМ_ТС КАК втВидыГСМ_ТСТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упМоделиТС.НормыРасходаГСМ КАК МоделиТС_НормыРасхода
	|	ПО ИСТИНА
	|		И МоделиТС_НормыРасхода.Ссылка = втВидыГСМ_ТСТ.Модель
	|		И МоделиТС_НормыРасхода.ВидГСМ = втВидыГСМ_ТСТ.ВидГСМ
	|		И ВЫБОР
	|				КОГДА &ОкончаниеПутевогоЛиста = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ВЫБОР
	|						КОГДА &НачалоПутевогоЛиста = ДАТАВРЕМЯ(1, 1, 1)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ МоделиТС_НормыРасхода.ДатаУстановки <= &НачалоПутевогоЛиста
	|					КОНЕЦ
	|				ИНАЧЕ МоделиТС_НормыРасхода.ДатаУстановки <= &ОкончаниеПутевогоЛиста
	|			КОНЕЦ
	|ГДЕ НЕ (втВидыГСМ_ТСТ.ВидГСМ ЕСТЬ NULL)
	|ОБЪЕДИНИТЬ ВСЕ
	|// 2. Если у ТС была заполнена ТЧ.ВидыГСМ, то виды берутся из ТЧ.видыГСМ, а нормы из модели.
	|ВЫБРАТЬ
	|	МоделиТС_НормыРасхода.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМ_ТСТ.ПроцентИспользования КАК ПроцентИспользования,
	|	МоделиТС_НормыРасхода.НормаРасхода КАК НормаРасхода,
	|	МоделиТС_НормыРасхода.НормаРасхода.ИзменениеРасхода КАК ИзменениеРасхода,
	|	МоделиТС_НормыРасхода.НормаРасхода.Расход КАК Расход,
	|	МоделиТС_НормыРасхода.НормаРасхода.ФормулаРасчета КАК ФормулаРасчета,
	|	МоделиТС_НормыРасхода.НормаРасхода.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	МоделиТС_НормыРасхода.НормаРасхода.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	МоделиТС_НормыРасхода.НормаРасхода.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте,
	|	МоделиТС_НормыРасхода.ДатаУстановки КАК ДатаУстановки
	|ИЗ
	|	втВидыГСМ_ТС КАК втВидыГСМ_ТСТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТС.НормыРасходаГСМ КАК МоделиТС_НормыРасхода
	|	ПО ИСТИНА
	|		И МоделиТС_НормыРасхода.Ссылка = втВидыГСМ_ТСТ.Модель
	|		И ВЫБОР
	|				КОГДА &ОкончаниеПутевогоЛиста = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ВЫБОР
	|						КОГДА &НачалоПутевогоЛиста = ДАТАВРЕМЯ(1, 1, 1)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ МоделиТС_НормыРасхода.ДатаУстановки <= &НачалоПутевогоЛиста
	|					КОНЕЦ
	|				ИНАЧЕ МоделиТС_НормыРасхода.ДатаУстановки <= &ОкончаниеПутевогоЛиста
	|			КОНЕЦ
	|ГДЕ втВидыГСМ_ТСТ.ВидГСМ ЕСТЬ NULL
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВидыГСМТ.ВидГСМ КАК ВидГСМ,
	|	МАКСИМУМ(втВидыГСМТ.ДатаУстановки) КАК ДатаУстановки
	|ПОМЕСТИТЬ втДатыУстановкиНормПоВидамГСМ
	|ИЗ
	|	втВидыГСМ_ПоНормам КАК втВидыГСМТ
	|СГРУППИРОВАТЬ ПО
	|	втВидыГСМТ.ВидГСМ
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВидыГСМ_ПоНормам.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМ_ПоНормам.ПроцентИспользования КАК ПроцентИспользования,
	|	втВидыГСМ_ПоНормам.ДатаУстановки КАК ДатаУстановки,
	|	втВидыГСМ_ПоНормам.ИзменениеРасхода КАК ИзменениеРасхода,
	|	втВидыГСМ_ПоНормам.НормаРасхода КАК НормаРасхода,
	|	втВидыГСМ_ПоНормам.Расход КАК Расход,
	|	втВидыГСМ_ПоНормам.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	втВидыГСМ_ПоНормам.ФормулаРасчета КАК ФормулаРасчета,
	|	втВидыГСМ_ПоНормам.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	втВидыГСМ_ПоНормам.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте
	|ПОМЕСТИТЬ втВидыГСМ
	|ИЗ
	|	втВидыГСМ_ПоНормам КАК втВидыГСМ_ПоНормам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДатыУстановкиНормПоВидамГСМ КАК втДатыУстановкиНормПоВидамГСМ
	|	ПО ИСТИНА
	|		И втВидыГСМ_ПоНормам.ВидГСМ = втДатыУстановкиНормПоВидамГСМ.ВидГСМ
	|		И втВидыГСМ_ПоНормам.ДатаУстановки = втДатыУстановкиНормПоВидамГСМ.ДатаУстановки
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втВидыГСМ_ПоНормам.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМ_ПоНормам.ПроцентИспользования КАК ПроцентИспользования,
	|	втВидыГСМ_ПоНормам.ДатаУстановки КАК ДатаУстановки,
	|	втВидыГСМ_ПоНормам.ИзменениеРасхода КАК ИзменениеРасхода,
	|	втВидыГСМ_ПоНормам.НормаРасхода КАК НормаРасхода,
	|	втВидыГСМ_ПоНормам.Расход КАК Расход,
	|	втВидыГСМ_ПоНормам.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	втВидыГСМ_ПоНормам.ФормулаРасчета КАК ФормулаРасчета,
	|	втВидыГСМ_ПоНормам.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	втВидыГСМ_ПоНормам.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте
	|ИЗ
	|	втВидыГСМ_ПоНормам КАК втВидыГСМ_ПоНормам
	|ГДЕ втВидыГСМ_ПоНормам.НормаРасхода ЕСТЬ NULL
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|// Топливные датчики по нужным видам ГСМ.
	|ВЫБРАТЬ
	|	упДатчики.Ссылка КАК Датчик
	|ПОМЕСТИТЬ втДатчики
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыГСМ КАК втВидыГСМ
	|	ПО упДатчики.ВидГСМ = втВидыГСМ.ВидГСМ
	|ГДЕ упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|// Определяются остатки для ТС по датчикам и данным путевых листов.
	|// Приоритет имеет датчик, если его нет, то берутся данные путевых листов.
	|ВЫБРАТЬ
	|	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Значение, упОстаткиГСМОстатки.КоличествоОстаток) КАК УровеньТоплива,
	|	упОстаткиГСМОстатки.ВидГСМ КАК ВидГСМ,
	|	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Датчик, ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка)) КАК Датчик
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|		,
	|		ТранспортноеСредство = &ТранспортноеСредство
	|			И Датчик В (
	|			ВЫБРАТЬ
	|				втДатчики.Датчик КАК Датчик
	|			ИЗ
	|				втДатчики КАК втДатчики)) КАК упДанныеДатчиковСрезПоследних
	|	ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиГСМ.Остатки(
	|		,
	|		ТранспортноеСредство = &ТранспортноеСредство
	|			И ВидГСМ В (
	|			ВЫБРАТЬ
	|				втВидыГСМ.ВидГСМ КАК ВидГСМ
	|			ИЗ
	|				втВидыГСМ КАК втВидыГСМ)) КАК упОстаткиГСМОстатки
	|	ПО ИСТИНА
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|// Итоговая таблица включает в себя виды гсм, остатки топлива и показатели норм.
	|// 1. Виды гсм для которых норм в моделях не определено, их значения устанавливаются
	|//    документом ""Установка норм ГСМ""
	|ВЫБРАТЬ
	|	втВидыГСМ.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМ.ПроцентИспользования КАК ПроцентИспользования,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ИзменениеРасхода КАК ИзменениеРасхода,
	|	ЕСТЬNULL(втОстатки.УровеньТоплива, 0) КАК УровеньТоплива,
	|	NULL КАК НормаРасхода,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.Расход КАК Расход,
	|	втОстатки.Датчик КАК Датчик,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ФормулаРасчета КАК ФормулаРасчета,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте,
	|	ВЫБОР
	|		КОГДА УпНормыРасходаГСМ_СрезПоследнихТ.Подразделение = &Подразделение
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	втВидыГСМ КАК втВидыГСМ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|	ПО втВидыГСМ.ВидГСМ = втОстатки.ВидГСМ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|			И (Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)
	|				ИЛИ Подразделение = &Подразделение)
	|			И ОбъектНормРасхода = &ТранспортноеСредство) КАК УпНормыРасходаГСМ_СрезПоследнихТ
	|	ПО УпНормыРасходаГСМ_СрезПоследнихТ.ВидГСМ = втВидыГСМ.ВидГСМ
	|ГДЕ втВидыГСМ.НормаРасхода ЕСТЬ NULL
	|ОБЪЕДИНИТЬ ВСЕ
	|// 2. Виды гсм, для которых в модели были определены нормы расхода топлива.
	|ВЫБРАТЬ
	|	втВидыГСМТ.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМТ.ПроцентИспользования КАК ПроцентИспользования,
	|	втВидыГСМТ.ИзменениеРасхода КАК ИзменениеРасхода,
	|	ЕСТЬNULL(втОстаткиТ.УровеньТоплива, 0) КАК УровеньТоплива,
	|	втВидыГСМТ.НормаРасхода КАК НормаРасхода,
	|	втВидыГСМТ.Расход КАК Расход,
	|	втОстаткиТ.Датчик КАК Датчик,
	|	втВидыГСМТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	втВидыГСМТ.ФормулаРасчета КАК ФормулаРасчета,
	|	втВидыГСМТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	втВидыГСМТ.УчитыватьТемпературуВоздухаВПутевомЛисте КАК УчитыватьТемпературуВоздухаВПутевомЛисте,
	|	1 КАК Приоритет
	|ИЗ
	|	втВидыГСМ КАК втВидыГСМТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстаткиТ
	|	ПО втВидыГСМТ.ВидГСМ = втОстаткиТ.ВидГСМ
	|ГДЕ НЕ (втВидыГСМТ.НормаРасхода ЕСТЬ NULL)
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|";

	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("НачалоПутевогоЛиста", 	?(ЗначениеЗаполнено(НачалоПутевогоЛиста),НачалоПутевогоЛиста,Период));
	Запрос.УстановитьПараметр("ОкончаниеПутевогоЛиста", ОкончаниеПутевогоЛиста);
	
	тбзРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзРезультат;
		
КонецФункции

// Выполним получение данных уровня топлива ТС.
//
// Параметры:
//  Дата  - Дата - Дата на которую выполнить поиск даных.
//  ТС  - СправочникСсылка.упТранспортныеСредства - Ссылка на ТС.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//
// Возвращаемое значение:
//   Число   - Показание топлива.
//
Функция ПолучитьПоказанияУровняТоплива(Дата, ТС, ВидГСМ = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановленныеТрекерыСП.Трекер КАК Трекер
	|ПОМЕСТИТЬ втТрекеры
	|ИЗ
	|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК УстановленныеТрекерыСП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДатчики.Ссылка
	|ПОМЕСТИТЬ втДатчики
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|ГДЕ
	|	упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
	|	И упДатчики.ВидГСМ = &ВидГСМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДатчиковСП.Период КАК Период,
	|	ДанныеДатчиковСП.Значение
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|			&Период,
	|			Датчик В
	|					(ВЫБРАТЬ
	|						втДатчики.Ссылка
	|					ИЗ
	|						втДатчики КАК втДатчики)
	|				И ТранспортноеСредство = &ТранспортноеСредство) КАК ДанныеДатчиковСП
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("Период", Новый Граница(Дата,ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТС);
	Запрос.УстановитьПараметр("ВидГСМ", ?(ВидГСМ = Неопределено,Справочники.упВидыГСМ.ПустаяСсылка(),ВидГСМ));
	
	Результат = Неопределено;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Выборка.Значение;	
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьПоказанияУровняТоплива()

// Выполним получение данных одометра ТС.
//
// Параметры:
//  Дата  - Дата - Дата на которую выполнить поиск даных.
//  ТС  - СправочникСсылка.упТранспортныеСредства - Ссылка на ТС.
//
// Возвращаемое значение:
//   Число   - Показание одометра.
//
Функция ПолучитьПоказанияОдометра(Дата, ТС) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановленныеТрекерыСП.Трекер КАК Трекер
	|ПОМЕСТИТЬ втТрекеры
	|ИЗ
	|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК УстановленныеТрекерыСП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДатчики.Ссылка
	|ПОМЕСТИТЬ втДатчики
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|ГДЕ
	|	упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Одометр)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДатчиковСП.Период КАК Период,
	|	ДанныеДатчиковСП.Значение
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|			&Период,
	|			Датчик В
	|					(ВЫБРАТЬ
	|						втДатчики.Ссылка
	|					ИЗ
	|						втДатчики КАК втДатчики)
	|				И ТранспортноеСредство = &ТранспортноеСредство) КАК ДанныеДатчиковСП
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("Период", Новый Граница(Дата,ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТС);
	
	Результат = Неопределено;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Выборка.Значение;	
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьПоказанияОдометра()

// Функция возвращает результат пробега по транспортному средству.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   Число   - Показание пробега.
//
Функция ПробегМоточасы(ТранспортноеСредство) Экспорт
	
	сткРезультат = Новый Структура("Пробег, Одометр, Моточасы",0,0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				|	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Значение, упПробегТСОстатки.ОдометрОстаток) КАК Одометр,
	               |	упПробегТСОстатки.МоточасыОстаток КАК Моточасы, 
				|	упПробегТСОстатки.ПробегОстаток КАК Пробег,
				|	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Значение, упПробегТСОстатки.ПробегОстаток) КАК ПробегПоДатчику
	               |ИЗ
	               |	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				И Датчик В
	               |					(ВЫБРАТЬ
	               |						упДатчики.Ссылка
	               |					ИЗ
	               |						Справочник.упДатчики КАК упДатчики
	               |					ГДЕ
	               |						упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Одометр))) КАК упДанныеДатчиковСрезПоследних
	               |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.упПробегТС.Остатки(, ТранспортноеСредство = &ТранспортноеСредство) КАК упПробегТСОстатки
	               |		ПО (ИСТИНА)";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);			   
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
		ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств = ПолучитьФункциональнуюОпцию("упВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств");
		Если НЕ ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств Тогда
			сткРезультат.Пробег = Выборка.ПробегПоДатчику;	
		КонецЕсли;
	КонецЕсли; 
				   
	Возврат сткРезультат;
	
КонецФункции

// Функция возвращает показатели топилва по остаткам с учетом вида ГСМ.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция УровеньТопливаПоРегиструОстатков(ТранспортноеСредство, ВидГСМ) Экспорт
	
	сткРезультат = Новый Структура("Количество, Стоимость, СтоимостьНДС", 0, 0);
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМОстатки.КоличествоОстаток,
	               |	упОстаткиГСМОстатки.СтоимостьОстаток,
				|	упОстаткиГСМОстатки.СтоимостьНДСОстаток
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ.Остатки(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				И ВидГСМ = &ВидГСМ) КАК упОстаткиГСМОстатки";
				   
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("ВидГСМ", ВидГСМ);
	
	текВыборкаУровень	= Запрос.Выполнить().Выбрать();
	Если текВыборкаУровень.Следующий() Тогда
		сткРезультат.Количество 	= текВыборкаУровень.КоличествоОстаток;
		сткРезультат.Стоимость = текВыборкаУровень.СтоимостьОстаток;
		сткРезультат.СтоимостьНДС = текВыборкаУровень.СтоимостьНДСОстаток;
	КонецЕсли;
		
	Возврат сткРезультат;
	
КонецФункции // УровеньТопливаПоРегиструОстатков()

// Функция возвращает показатели топилва по остаткам с учетом ТС.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция УровеньТопливаПоРегиструОстатковПоТС(ТранспортноеСредство) Экспорт
	
	тбзРезультат = Неопределено;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМОстатки.КоличествоОстаток,
	               |	упОстаткиГСМОстатки.СтоимостьОстаток,
	               |	упОстаткиГСМОстатки.СтоимостьНДСОстаток,				   
	               |	упОстаткиГСМОстатки.ВидГСМ
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ.Остатки(, ТранспортноеСредство = &ТранспортноеСредство) КАК упОстаткиГСМОстатки";
				   
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
		
	тбзРезультат	= Запрос.Выполнить().Выгрузить();
		
	Возврат тбзРезультат;
	
КонецФункции

// Функция возвращает значение пробега по остаткам регистра.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   Число   - Показание пробега.
//
Функция ПробегМоточасыПоРегиструОстатков(ТранспортноеСредство) Экспорт
	
	сткРезультат = Новый Структура("Пробег, Одометр, Моточасы",0,0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПробегТСОстатки.ПробегОстаток КАК Пробег,
	               |	упПробегТСОстатки.МоточасыОстаток КАК Моточасы,
	               |	упПробегТСОстатки.ОдометрОстаток КАК Одометр
	               |ИЗ
	               |	РегистрНакопления.упПробегТС.Остатки(, ТранспортноеСредство = &ТранспортноеСредство) КАК упПробегТСОстатки";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Выборка	= Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Функция возвращет структуру с показателями пробега и уровня топилва.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПробегУровеньТоплива(ТранспортноеСредство) Экспорт
		
	сткРезультат = Новый Структура("Пробег, УровеньТоплива, ВидГСМ", 0, 0, Справочники.упВидыГСМ.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Значение, упПробегТСОстатки.ПробегОстаток) КАК Пробег
	               |ИЗ
	               |	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				И Датчик В
	               |					(ВЫБРАТЬ
	               |						упДатчики.Ссылка
	               |					ИЗ
	               |						Справочник.упДатчики КАК упДатчики
	               |					ГДЕ
	               |						упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Одометр))) КАК упДанныеДатчиковСрезПоследних
	               |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.упПробегТС.Остатки(, ТранспортноеСредство = &ТранспортноеСредство) КАК упПробегТСОстатки
	               |		ПО (ИСТИНА)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упМоделиТСНормыРасходаГСМ.ВидГСМ
	               |ПОМЕСТИТЬ втВидГСМ
	               |ИЗ
	               |	Справочник.упМоделиТС.НормыРасходаГСМ КАК упМоделиТСНормыРасходаГСМ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ПО упМоделиТСНормыРасходаГСМ.Ссылка = упТранспортныеСредства.Модель
	               |ГДЕ
	               |	упМоделиТСНормыРасходаГСМ.Основной
	               |	И упТранспортныеСредства.Ссылка = &ТранспортноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упДатчики.Ссылка КАК Датчик
	               |ПОМЕСТИТЬ втДатчики
	               |ИЗ
	               |	Справочник.упДатчики КАК упДатчики
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидГСМ КАК втВидГСМ
	               |		ПО упДатчики.ВидГСМ = втВидГСМ.ВидГСМ
	               |ГДЕ
	               |	упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Значение, упОстаткиГСМОстатки.КоличествоОстаток) КАК УровеньТоплива,
	               |	ЕСТЬNULL(упДанныеДатчиковСрезПоследних.Датчик.ВидГСМ, упОстаткиГСМОстатки.ВидГСМ) КАК ВидГСМ
	               |ИЗ
	               |	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				И Датчик В
	               |					(ВЫБРАТЬ
	               |						втДатчики.Датчик
	               |					ИЗ
	               |						втДатчики КАК втДатчики)) КАК упДанныеДатчиковСрезПоследних
	               |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиГСМ.Остатки(
	               |				,
	               |				ТранспортноеСредство = &ТранспортноеСредство
	               |					И ВидГСМ В
	               |						(ВЫБРАТЬ
	               |							втВидГСМ.ВидГСМ
	               |						ИЗ
	               |							втВидГСМ КАК втВидГСМ)) КАК упОстаткиГСМОстатки
	               |		ПО (ИСТИНА)";
				   
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	мсвРезультат = Запрос.ВыполнитьПакет();
	
	текВыборкаПробег	= мсвРезультат[0].Выбрать();
	Если текВыборкаПробег.Следующий() Тогда
		сткРезультат.Пробег = текВыборкаПробег.Пробег;
	КонецЕсли;
	
	текВыборкаУровень	= мсвРезультат[3].Выбрать();
	Если текВыборкаУровень.Следующий() Тогда
		сткРезультат.УровеньТоплива = текВыборкаУровень.УровеньТоплива;
		сткРезультат.ВидГСМ			= текВыборкаУровень.ВидГСМ;
	КонецЕсли;
		
	Возврат сткРезультат;
	
КонецФункции

Функция ТекстЗапросаФормированияДвиженийПоПробегу(Ссылка)
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда
		Результат = "
		|//{Запрос: 0 ////////////////////////////////////////
		|// Пробеги по транспортному средству.
		|ВЫБРАТЬ
		|	ПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ПутевойЛист.ПробегНачало КАК ПробегНачало,
		|	ПутевойЛист.МоточасыНачало КАК МоточасыНачало,
		|	ПутевойЛист.Пробег КАК Пробег,
		|	ПутевойЛист.Моточасы КАК Моточасы,
		|	ПутевойЛист.Одометр КАК Одометр,
		|	ПутевойЛист.ОдометрНачало КАК ОдометрНачало
		|ПОМЕСТИТЬ втТСПробегиПоПутевомуЛисту
		|ИЗ
		|	Документ.упПутевойЛист КАК ПутевойЛист
		|ГДЕ ПутевойЛист.Ссылка = &Ссылка
		|		
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Наработка по доп. оборудованию.
		|ВЫБРАТЬ
		|	ДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
		|	0 КАК ПробегНачало,
		|	ДопОборудование.МоточасыНачало КАК МоточасыНачало,
		|	0 КАК Пробег,
		|	0 КАК Одометр,
		|	0 КАК ОдометрНачало,
		|	ДопОборудование.Моточасы КАК Моточасы
		|ПОМЕСТИТЬ втДопОборудованиеПробегиПутевойЛист
		|ИЗ
		|	Документ.упПутевойЛист.ДополнительноеОборудование КАК ДопОборудование
		|ГДЕ ДопОборудование.Ссылка = &Ссылка
		|		
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Пробеги по транспортному средству для получения остатков по пробегу.
		|// Для прицепов корректировка остатков не производится.
		|ВЫБРАТЬ
		|	втТСПробегиПоПутевомуЛисту.Моточасы КАК Моточасы,
		|	втТСПробегиПоПутевомуЛисту.МоточасыНачало КАК МоточасыНачало,
		|	втТСПробегиПоПутевомуЛисту.Пробег КАК Пробег,
		|	втТСПробегиПоПутевомуЛисту.ПробегНачало КАК ПробегНачало,
		|	втТСПробегиПоПутевомуЛисту.Одометр КАК Одометр,
		|	втТСПробегиПоПутевомуЛисту.ОдометрНачало КАК ОдометрНачало,
		|	втТСПробегиПоПутевомуЛисту.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втТСПробегиПоПутевомуЛисту.ПробегНачало - ЕСТЬNULL(ОстаткиПробегТС.ПробегОстаток, 0) КАК ДельтаПробег,
		|	втТСПробегиПоПутевомуЛисту.ОдометрНачало - ЕСТЬNULL(ОстаткиПробегТС.ОдометрОстаток, 0) КАК ДельтаОдометр,
		|	втТСПробегиПоПутевомуЛисту.МоточасыНачало - ЕСТЬNULL(ОстаткиПробегТС.МоточасыОстаток, 0) КАК ДельтаМоточасы
		|ПОМЕСТИТЬ втПробегиПоТСПутевомуЛисту
		|ИЗ
		|	втТСПробегиПоПутевомуЛисту КАК втТСПробегиПоПутевомуЛисту
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упПробегТС.Остатки(
		|		,
		|		ТранспортноеСредство В (
		|			ВЫБРАТЬ
		|				втТранспортныеСредства.ТранспортноеСредство КАК ТранспортноеСредство
		|			ИЗ
		|				втТСПробегиПоПутевомуЛисту КАК втТранспортныеСредства)) КАК ОстаткиПробегТС
		|	ПО втТСПробегиПоПутевомуЛисту.ТранспортноеСредство = ОстаткиПробегТС.ТранспортноеСредство
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|// Наработка по доп. оборудованияю для получения остатков по пробегу.
		|// Для прицепов корректировка остатков не производится.
		|ВЫБРАТЬ
		|	втДопОборудованиеПробегиПутевойЛист.Моточасы КАК Моточасы,
		|	втДопОборудованиеПробегиПутевойЛист.МоточасыНачало КАК МоточасыНачало,
		|	втДопОборудованиеПробегиПутевойЛист.Пробег КАК Пробег,
		|	втДопОборудованиеПробегиПутевойЛист.ПробегНачало КАК ПробегНачало,
		|	втДопОборудованиеПробегиПутевойЛист.ДополнительноеОборудование КАК Оборудование,
		|	втДопОборудованиеПробегиПутевойЛист.ПробегНачало - ЕСТЬNULL(ОстаткиНаработкаОборудования.ПробегОстаток, 0) КАК ДельтаПробег,
		|	втДопОборудованиеПробегиПутевойЛист.МоточасыНачало - ЕСТЬNULL(ОстаткиНаработкаОборудования.МоточасыОстаток, 0) КАК ДельтаМоточасы
		|ПОМЕСТИТЬ втНаработкаПоОборудованиюПоПутевомуЛисту
		|ИЗ
		|	втДопОборудованиеПробегиПутевойЛист КАК втДопОборудованиеПробегиПутевойЛист
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упНаработкаОборудования.Остатки(
		|		,
		|		Оборудование В (
		|			ВЫБРАТЬ
		|				втДопОборудованиеПробегиПутевойЛист.ДополнительноеОборудование КАК ДополнительноеОборудование
		|			ИЗ
		|				втДопОборудованиеПробегиПутевойЛист КАК втДопОборудованиеПробегиПутевойЛист)) КАК ОстаткиНаработкаОборудования
		|	ПО втДопОборудованиеПробегиПутевойЛист.ДополнительноеОборудование = ОстаткиНаработкаОборудования.Оборудование
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|// Пробеги по прицепам по транспортного средства.
		|ВЫБРАТЬ
		|	Прицепы.Прицеп КАК Прицеп,
		|	втТСПробегиПоПутевомуЛисту.Пробег КАК Пробег
		|ПОМЕСТИТЬ втПробегиПоПрицепам
		|ИЗ
		|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
		|		&Период,
		|		ТранспортноеСредство В (
		|			ВЫБРАТЬ
		|				втТСПробегиПоПутевомуЛисту.ТранспортноеСредство КАК ТранспортноеСредство
		|			ИЗ
		|				втТСПробегиПоПутевомуЛисту КАК втТСПробегиПоПутевомуЛисту)
		|			И Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)) КАК Прицепы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТСПробегиПоПутевомуЛисту КАК втТСПробегиПоПутевомуЛисту
		|	ПО Прицепы.ТранспортноеСредство = втТСПробегиПоПутевомуЛисту.ТранспортноеСредство
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|// Пробеги по агрегатам установленным на прицеп.
		|ВЫБРАТЬ
		|	Агрегаты.Агрегат КАК Агрегат,
		|	втТСПробегиПоПутевомуЛисту.Пробег КАК Пробег,
		|	втТСПробегиПоПутевомуЛисту.Моточасы КАК Моточасы
		|ПОМЕСТИТЬ втПробегиПоАгрегатамНаПрицепах
		|ИЗ
		|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
		|		&Период,
		|		ТранспортноеСредство В (
		|			ВЫБРАТЬ
		|				втТСПробегиПоПутевомуЛисту.ТранспортноеСредство КАК ТранспортноеСредство
		|			ИЗ
		|				втТСПробегиПоПутевомуЛисту КАК втТСПробегиПоПутевомуЛисту
		|			ОБЪЕДИНИТЬ ВСЕ
		|			ВЫБРАТЬ
		|				втПробегиПоПрицепам.Прицеп
		|			ИЗ
		|				втПробегиПоПрицепам КАК втПробегиПоПрицепам)
		|			И Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
		|			И Агрегат ССЫЛКА Справочник.упШиныУзлыИАгрегаты) КАК Агрегаты
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТСПробегиПоПутевомуЛисту КАК втТСПробегиПоПутевомуЛисту
		|	ПО Агрегаты.ТранспортноеСредство = втТСПробегиПоПутевомуЛисту.ТранспортноеСредство
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|// Формируются таблицы движений по пробегу и моточасам транспортного средства.
		|//1. Таблица корректировки начальных остатков,
		|//	 остатки корректируются в том случае, если введенные в путевой лист
		|//	 начальные остатки отличаются от остатков по ТС в регистре.
		|ВЫБРАТЬ
		|	втДельтаПробегТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаПробег > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаПробег > 0
		|			ТОГДА втДельтаПробегТ.ДельтаПробег
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаПробег
		|	КОНЕЦ КАК Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаОдометр > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Одометр,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаОдометр > 0
		|			ТОГДА втДельтаПробегТ.ДельтаОдометр
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаОдометр
		|	КОНЕЦ КАК Одометр,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаМоточасы > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Моточасы,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаМоточасы > 0
		|			ТОГДА втДельтаПробегТ.ДельтаМоточасы
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаМоточасы
		|	КОНЕЦ КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Корректировка) КАК ВидДвиженияПробег
		|ИЗ
		|	втПробегиПоТСПутевомуЛисту КАК втДельтаПробегТ
		|ГДЕ ЛОЖЬ
		|	ИЛИ втДельтаПробегТ.ДельтаМоточасы <> 0
		|	ИЛИ втДельтаПробегТ.ДельтаПробег <> 0
		|	ИЛИ втДельтаПробегТ.ДельтаОдометр <> 0
		|		
		|ОБЪЕДИНИТЬ ВСЕ
		|//2. Таблица пробега и моточасов по путевому листу
		|ВЫБРАТЬ
		|	втПробегиПоПутевомуЛистуТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Пробег,
		|	втПробегиПоПутевомуЛистуТ.Пробег КАК Пробег,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Одометр,
		|	втПробегиПоПутевомуЛистуТ.Одометр КАК Одометр,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Моточасы,
		|	втПробегиПоПутевомуЛистуТ.Моточасы КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Приход) КАК ВидДвиженияПробег
		|ИЗ
		|	втПробегиПоТСПутевомуЛисту КАК втПробегиПоПутевомуЛистуТ
		|ГДЕ ЛОЖЬ
		|	ИЛИ втПробегиПоПутевомуЛистуТ.Пробег <> 0
		|	ИЛИ втПробегиПоПутевомуЛистуТ.Одометр <> 0
		|	ИЛИ втПробегиПоПутевомуЛистуТ.Моточасы <> 0
		|		
		|ОБЪЕДИНИТЬ ВСЕ
		|//3. Таблица пробега и моточасов по прицепу
		|ВЫБРАТЬ
		|	втПробегиПоПрицепамТ.Прицеп КАК ТранспортноеСредство,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Пробег,
		|	втПробегиПоПрицепамТ.Пробег КАК Пробег,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Одометр,
		|	0 КАК Одометр,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Моточасы,
		|	0 КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Приход) КАК ВидДвиженияПробег
		|ИЗ
		|	втПробегиПоПрицепам КАК втПробегиПоПрицепамТ
		|;
		|//{Запрос: 7 ////////////////////////////////////////
		|// Формируются таблицы движений по пробегу и моточасам оборудования.
		|//1. Таблица корректировки начальных остатков,
		|//	 остатки корректируются в том случае, если введенные в путевой лист
		|//	 начальные остатки отличаются от остатков по оборудованию в регистре.
		|ВЫБРАТЬ
		|	втДельтаНаработка.Оборудование КАК Оборудование,
		|	ВЫБОР
		|		КОГДА втДельтаНаработка.ДельтаПробег > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаНаработка.ДельтаПробег > 0
		|			ТОГДА втДельтаНаработка.ДельтаПробег
		|		ИНАЧЕ - втДельтаНаработка.ДельтаПробег
		|	КОНЕЦ КАК Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаНаработка.ДельтаМоточасы > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Моточасы,
		|	ВЫБОР
		|		КОГДА втДельтаНаработка.ДельтаМоточасы > 0
		|			ТОГДА втДельтаНаработка.ДельтаМоточасы
		|		ИНАЧЕ - втДельтаНаработка.ДельтаМоточасы
		|	КОНЕЦ КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Корректировка) КАК ВидДвиженияПробег
		|ИЗ
		|	втНаработкаПоОборудованиюПоПутевомуЛисту КАК втДельтаНаработка
		|ГДЕ ЛОЖЬ
		|	ИЛИ втДельтаНаработка.ДельтаМоточасы <> 0
		|	ИЛИ втДельтаНаработка.ДельтаПробег <> 0
		|		
		|ОБЪЕДИНИТЬ ВСЕ
		|//2. Таблица пробега и моточасов по путевому листу
		|ВЫБРАТЬ
		|	втНаработкаПоПутевомуЛисту.Оборудование КАК Оборудование,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Пробег,
		|	втНаработкаПоПутевомуЛисту.Пробег КАК Пробег,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Моточасы,
		|	втНаработкаПоПутевомуЛисту.Моточасы КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Приход) КАК ВидДвиженияПробег
		|ИЗ
		|	втНаработкаПоОборудованиюПоПутевомуЛисту КАК втНаработкаПоПутевомуЛисту
		|ГДЕ ЛОЖЬ
		|	ИЛИ втНаработкаПоПутевомуЛисту.Пробег <> 0
		|	ИЛИ втНаработкаПоПутевомуЛисту.Моточасы <> 0
		|		
		|ОБЪЕДИНИТЬ ВСЕ
		|//3. Таблица пробега и моточасов по агрегатам на прицепе
		|ВЫБРАТЬ
		|	втПробегиПоАгрегатамНаПрицепах.Агрегат КАК Оборудование,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Пробег,
		|	втПробегиПоАгрегатамНаПрицепах.Пробег КАК Пробег,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения_Моточасы,
		|	втПробегиПоАгрегатамНаПрицепах.Моточасы КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Приход) КАК ВидДвиженияПробег
		|ИЗ
		|	втПробегиПоАгрегатамНаПрицепах КАК втПробегиПоАгрегатамНаПрицепах
		|";

	
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упКорректировкаПробегаТС") Тогда
		
		Результат = "
		|//{Запрос: 0 ////////////////////////////////////////
		|// Пробеги по транспортному средству из корректировки.
		|ВЫБРАТЬ
		|	КорректировкаПробегаТС.ТранспортноеСредство КАК ТранспортноеСредство,
		|	КорректировкаПробегаТС.Пробег КАК ПробегНачало,
		|	КорректировкаПробегаТС.Одометр КАК ОдометрНачало,
		|	КорректировкаПробегаТС.Моточасы КАК МоточасыНачало
		|ПОМЕСТИТЬ втТСПробегиПоКорректировке
		|ИЗ
		|	Документ.упКорректировкаПробегаТС КАК КорректировкаПробегаТС
		|ГДЕ КорректировкаПробегаТС.Ссылка = &Ссылка
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Отклонение введенных в корректировке данных от остатков регистра.
		|ВЫБРАТЬ
		|	втТСПробегиПоКорректировке.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втТСПробегиПоКорректировке.ПробегНачало - ЕСТЬNULL(ОстаткиПробегТС.ПробегОстаток, 0) КАК ДельтаПробег,
		|	втТСПробегиПоКорректировке.ОдометрНачало - ЕСТЬNULL(ОстаткиПробегТС.ОдометрОстаток, 0) КАК ДельтаОдометр,
		|	втТСПробегиПоКорректировке.МоточасыНачало - ЕСТЬNULL(ОстаткиПробегТС.МоточасыОстаток, 0) КАК ДельтаМоточасы
		|ПОМЕСТИТЬ втДельтаПробег
		|ИЗ
		|	втТСПробегиПоКорректировке КАК втТСПробегиПоКорректировке
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упПробегТС.Остатки(
		|		,
		|		ТранспортноеСредство В (
		|			ВЫБРАТЬ
		|				втТранспортныеСредства.ТранспортноеСредство КАК ТранспортноеСредство
		|			ИЗ
		|				втТСПробегиПоКорректировке КАК втТранспортныеСредства)) КАК ОстаткиПробегТС
		|	ПО втТСПробегиПоКорректировке.ТранспортноеСредство = ОстаткиПробегТС.ТранспортноеСредство
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Формируется таблица движений по пробегу и моточасм.
		|// Таблица корректировки начальных остатков.
		|// Остатки корректируются в том случае, если введенные в документ
		|// значения отличаются от остатков по ТС в регистре.
		|ВЫБРАТЬ
		|	втДельтаПробегТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаПробег > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаПробег > 0
		|			ТОГДА втДельтаПробегТ.ДельтаПробег
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаПробег
		|	КОНЕЦ КАК Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаОдометр > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Одометр,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаОдометр > 0
		|			ТОГДА втДельтаПробегТ.ДельтаОдометр
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаОдометр
		|	КОНЕЦ КАК Одометр,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаМоточасы > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Моточасы,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаМоточасы > 0
		|			ТОГДА втДельтаПробегТ.ДельтаМоточасы
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаМоточасы
		|	КОНЕЦ КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Корректировка) КАК ВидДвиженияПробег
		|ИЗ
		|	втДельтаПробег КАК втДельтаПробегТ
		|ГДЕ ЛОЖЬ
		|	ИЛИ втДельтаПробегТ.ДельтаМоточасы <> 0
		|	ИЛИ втДельтаПробегТ.ДельтаПробег <> 0
		|	ИЛИ втДельтаПробегТ.ДельтаОдометр <> 0
		|";
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упКорректировкаНаработкиОборудования") Тогда
		
		Результат = "
		|//{Запрос: 0 ////////////////////////////////////////
		|// Пробеги по оборудованию средству из корректировки.
		|ВЫБРАТЬ
		|	КорректировкаНаработки.Оборудование КАК Оборудование,
		|	КорректировкаНаработки.Пробег КАК ПробегНачало,
		|	КорректировкаНаработки.Моточасы КАК МоточасыНачало
		|ПОМЕСТИТЬ втПробегиПоКорректировке
		|ИЗ
		|	Документ.упКорректировкаНаработкиОборудования.Оборудование КАК КорректировкаНаработки
		|ГДЕ КорректировкаНаработки.Ссылка = &Ссылка
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Отклонение введенных в корректировке данных от остатков регистра.
		|ВЫБРАТЬ
		|	втПробегиПоКорректировке.Оборудование КАК Оборудование,
		|	втПробегиПоКорректировке.ПробегНачало - ЕСТЬNULL(ОстаткиПробег.ПробегОстаток, 0) КАК ДельтаПробег,
		|	втПробегиПоКорректировке.МоточасыНачало - ЕСТЬNULL(ОстаткиПробег.МоточасыОстаток, 0) КАК ДельтаМоточасы
		|ПОМЕСТИТЬ втДельтаПробег
		|ИЗ
		|	втПробегиПоКорректировке КАК втПробегиПоКорректировке
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упНаработкаОборудования.Остатки(
		|		,
		|		Оборудование В (
		|		ВЫБРАТЬ
		|		втОборудование.Оборудование КАК Оборудование
		|		ИЗ
		|		втПробегиПоКорректировке КАК втОборудование)) КАК ОстаткиПробег
		|	ПО втПробегиПоКорректировке.Оборудование = ОстаткиПробег.Оборудование
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Формируется таблица движений по пробегу и моточасм.
		|// Таблица корректировки начальных остатков.
		|// Остатки корректируются в том случае, если введенные в документ
		|// значения отличаются от остатков по оборудованию в регистре.
		|ВЫБРАТЬ
		|	втДельтаПробегТ.Оборудование КАК Оборудование,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаПробег > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаПробег > 0
		|			ТОГДА втДельтаПробегТ.ДельтаПробег
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаПробег
		|	КОНЕЦ КАК Пробег,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаМоточасы > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения_Моточасы,
		|	ВЫБОР
		|		КОГДА втДельтаПробегТ.ДельтаМоточасы > 0
		|			ТОГДА втДельтаПробегТ.ДельтаМоточасы
		|		ИНАЧЕ - втДельтаПробегТ.ДельтаМоточасы
		|	КОНЕЦ КАК Моточасы,
		|	ЗНАЧЕНИЕ(Перечисление.упВидДвиженияПробег.Корректировка) КАК ВидДвиженияПробег
		|ИЗ
		|	втДельтаПробег КАК втДельтаПробегТ
		|ГДЕ ЛОЖЬ
		|	ИЛИ втДельтаПробегТ.ДельтаМоточасы <> 0
		|	ИЛИ втДельтаПробегТ.ДельтаПробег <> 0
		|";

	КонецЕсли;
	
	Возврат Результат;
			
КонецФункции

// Функция возвращате структуру с разницей показателей ГСМ.
//
// Параметры:
//	ТекущееКоличество - Число - Количество.
//	ТекущаяСтоимость - Число - Значение стоимости.
//	ТекущаяСтоимостьНДС - Число - Стоимость.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//	Период - Дата - Период.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ДельтаГСМ(ТекущееКоличество, ТекущаяСтоимость, ТекущаяСтоимостьНДС, ВидГСМ, ТранспортноеСредство, Период) Экспорт
	
	тбзОстаткиГСМ = СтруктураТаблицыОстаткиГСМ();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упОстаткиГСМОстатки.КоличествоОстаток,
	               |	упОстаткиГСМОстатки.СтоимостьОстаток,
	               |	упОстаткиГСМОстатки.СтоимостьНДСОстаток
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГСМ.Остатки(
	               |			&Период,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				И ВидГСМ = &ВидГСМ) КАК упОстаткиГСМОстатки";
	Запрос.УстановитьПараметр("ТранспортноеСредство", 	ТранспортноеСредство);
	Запрос.УстановитьПараметр("ВидГСМ", 				ВидГСМ);
	Запрос.УстановитьПараметр("Период", 				Новый Граница(Период, ВидГраницы.Исключая));
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
	
		ДельтаКоличество 	= ТекущееКоличество - текВыборка.КоличествоОстаток;
		ДельтаСтоимость		= ТекущаяСтоимость - текВыборка.СтоимостьОстаток;
		ДельтаСтоимостьНДС	= ТекущаяСтоимостьНДС - текВыборка.СтоимостьНДСОстаток;
		
		ВидДвиженияКоличество 	= ?(ДельтаКоличество >= 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
		ДельтаКоличество 		= ?(ДельтаКоличество >= 0, ДельтаКоличество, -ДельтаКоличество);
		
		ВидДвиженияСтоимость 	= ?(ДельтаСтоимость >= 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
		ДельтаСтоимость 		= ?(ДельтаСтоимость >= 0, ДельтаСтоимость, -ДельтаСтоимость);
		
		ВидДвиженияСтоимостьНДС	= ?(ДельтаСтоимостьНДС >= 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
		ДельтаСтоимостьНДС		= ?(ДельтаСтоимостьНДС >= 0, ДельтаСтоимостьНДС, -ДельтаСтоимостьНДС);
		
		Если ВидДвиженияКоличество = ВидДвиженияСтоимость И ВидДвиженияСтоимость = ВидДвиженияСтоимостьНДС Тогда
			Если НЕ ДельтаКоличество = 0 
				ИЛИ НЕ ДельтаСтоимость = 0 Тогда
				СтрокаОстаткиГСМ = тбзОстаткиГСМ.Добавить();
				СтрокаОстаткиГСМ.Период					= Период;
				СтрокаОстаткиГСМ.ТранспортноеСредство 	= ТранспортноеСредство;
				СтрокаОстаткиГСМ.ВидГСМ 				= ВидГСМ;
				СтрокаОстаткиГСМ.ВидДвижения 			= ВидДвиженияКоличество;
				СтрокаОстаткиГСМ.ВидДвиженияТоплива 	= Перечисления.упВидыДвиженияТоплива.Корректировка;
				СтрокаОстаткиГСМ.Количество				= ДельтаКоличество;
				СтрокаОстаткиГСМ.Стоимость				= ДельтаСтоимость;
				СтрокаОстаткиГСМ.СтоимостьНДС			= ДельтаСтоимостьНДС;
			КонецЕсли;
		Иначе
			Если НЕ ДельтаКоличество = 0 Тогда
				СтрокаОстаткиГСМ = тбзОстаткиГСМ.Добавить();
				СтрокаОстаткиГСМ.Период					= Период;
				СтрокаОстаткиГСМ.ТранспортноеСредство 	= ТранспортноеСредство;
				СтрокаОстаткиГСМ.ВидГСМ 				= ВидГСМ;
				СтрокаОстаткиГСМ.ВидДвижения 			= ВидДвиженияКоличество;
				СтрокаОстаткиГСМ.ВидДвиженияТоплива 	= Перечисления.упВидыДвиженияТоплива.Корректировка;
				СтрокаОстаткиГСМ.Количество				= ДельтаКоличество;
				СтрокаОстаткиГСМ.Стоимость				= 0;
				СтрокаОстаткиГСМ.СтоимостьНДС			= 0;
			КонецЕсли;		
			
			Если НЕ ДельтаСтоимость = 0 Тогда
				СтрокаОстаткиГСМ = тбзОстаткиГСМ.Добавить();
				СтрокаОстаткиГСМ.Период					= Период;
				СтрокаОстаткиГСМ.ТранспортноеСредство 	= ТранспортноеСредство;
				СтрокаОстаткиГСМ.ВидГСМ 				= ВидГСМ;
				СтрокаОстаткиГСМ.ВидДвижения 			= ВидДвиженияСтоимость;
				СтрокаОстаткиГСМ.ВидДвиженияТоплива 	= Перечисления.упВидыДвиженияТоплива.Корректировка;
				СтрокаОстаткиГСМ.Количество				= 0;
				СтрокаОстаткиГСМ.Стоимость				= ДельтаСтоимость;
				СтрокаОстаткиГСМ.СтоимостьНДС			= 0;
			КонецЕсли;	
			
			Если НЕ ДельтаСтоимостьНДС = 0 Тогда
				СтрокаОстаткиГСМ = тбзОстаткиГСМ.Добавить();
				СтрокаОстаткиГСМ.Период					= Период;
				СтрокаОстаткиГСМ.ТранспортноеСредство 	= ТранспортноеСредство;
				СтрокаОстаткиГСМ.ВидГСМ 				= ВидГСМ;
				СтрокаОстаткиГСМ.ВидДвижения 			= ВидДвиженияСтоимость;
				СтрокаОстаткиГСМ.ВидДвиженияТоплива 	= Перечисления.упВидыДвиженияТоплива.Корректировка;
				СтрокаОстаткиГСМ.Количество				= 0;
				СтрокаОстаткиГСМ.Стоимость				= 0;
				СтрокаОстаткиГСМ.СтоимостьНДС			= ДельтаСтоимостьНДС;
			КонецЕсли;	
		КонецЕсли;
		
		
	КонецЕсли;

	Возврат тбзОстаткиГСМ;
	
КонецФункции

// Функция создает и заполняет новый путевой лист по рейсу.
//
// Параметры:
//  мсвРейсыДляПутевого	 - 						 - 
//  Гараж				 - СправочникСсылка.упГаражи	 - Ссылка на элемент.
//  Статус			 - ПеречислениеСсылка.упСтатусыПутевогоЛиста	 - Ссылка на элемент.
//  Отказ				 - Булево								 - Фиксировать изменения.
// 
// Возвращаемое значение:
//  ДокументСсылка.упПутевойЛист - Результат выполнения.
//
Функция СформироватьПутевойЛистПоРейсам(мсвРейсыДляПутевого, Гараж = Неопределено, Статус = Неопределено, Отказ) Экспорт
	
	мсвРейсы = Новый Массив;
	
	Если НЕ мсвРейсыДляПутевого = Неопределено 
			И мсвРейсыДляПутевого.Количество() > 0 Тогда
		РазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов 	= ПолучитьФункциональнуюОпцию("упРазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов");
		Если РазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов Тогда
			Для каждого ЭлементРейс Из мсвРейсыДляПутевого Цикл
				мсвРейсы.Добавить(ЭлементРейс);	
			КонецЦикла;
		Иначе
			мсвРейсы.Добавить(мсвРейсыДляПутевого[0]);
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;

	ПутевойЛист 		 = Неопределено;
	ТранспортноеСредство = Неопределено;
	
	Если НЕ Отказ Тогда
		ПроверитьВозможностьФормированияПутевогоНаОснованииРейсов(мсвРейсы, ПутевойЛист, ТранспортноеСредство, Отказ);
	КонецЕсли;	
		
	Если НЕ Отказ Тогда
		
		Если НЕ ЗначениеЗаполнено(ПутевойЛист) Тогда
			 					
			ПутевойЛистОбъект = Документы.упПутевойЛист.СоздатьДокумент();	
			ПутевойЛистОбъект.Дата  = ТекущаяДатаСеанса();
			ПутевойЛистОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
			ПутевойЛистОбъект.Гараж = Гараж;
			ПутевойЛистОбъект.ТранспортноеСредство = ТранспортноеСредство;
			ПутевойЛистОбъект.Заполнить(мсвРейсы);
			Если НЕ Статус = Неопределено Тогда
				Если Статус = Перечисления.упСтатусыПутевогоЛиста.Новый 
					ИЛИ Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда
					ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);	
				Иначе
					Отказ = Истина;
					ОписаниеОшибки = Нстр("ru = 'Нельзя переводить путевой лист из статуса ""Новый"" в статус ""%1""'");
					ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Статус);
					ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				Попытка
					ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
					ПутевойЛист = ПутевойЛистОбъект.Ссылка;
				Исключение
					Отказ = Истина;
					ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
						ОписаниеОшибки = ОписаниеОшибки();
					КонецЕсли;
					ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
				КонецПопытки;
			КонецЕсли;

		Иначе
			Если НЕ Статус = Неопределено Тогда
				УстановитьСтатусПутевогоЛиста(ПутевойЛист, Статус,ОписаниеОшибки, Отказ)  	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПутевойЛист;
	
КонецФункции

// Функция создает и заполняет новый путевой лист по заявкам на перевозку грузов.
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза	 - Массив				 - заявки на перевозку груза (ДокументСсылка.упЗаявкаНаПеревозкуГруза).
//  Гараж					 - СправочникСсылка.упГаражи	 - Ссылка на элемент.
//  Статус				 - ПеречислениеСсылка.упСтатусыПутевогоЛиста	 - Ссылка на элемент.
//  Отказ					 - Булево								 - Фиксировать изменения.
// 
// Возвращаемое значение:
//  ДокументСсылка.упПутевойЛист - Результат выполнения.
//
Функция СформироватьПутевойЛистПоЗаявкамНаПеревозкуГрузов(мсвЗаявкиНаПеревозкуГруза, Гараж = Неопределено, Статус = Неопределено, Отказ) Экспорт
	
	ПутевойЛист 		 = Неопределено;
	ТранспортноеСредство = Неопределено;
	
	Если НЕ Отказ Тогда
		ПроверитьВозможностьФормированияПутевогоНаОснованииЗаявокНаПеревозкуГрузов(мсвЗаявкиНаПеревозкуГруза, Отказ);
	КонецЕсли;	
		
	Если НЕ Отказ Тогда
		
		Если НЕ ЗначениеЗаполнено(ПутевойЛист) Тогда
			 					
			ПутевойЛистОбъект = Документы.упПутевойЛист.СоздатьДокумент();	
			ПутевойЛистОбъект.Дата  = ТекущаяДатаСеанса();
			ПутевойЛистОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
			ПутевойЛистОбъект.Заполнить(мсвЗаявкиНаПеревозкуГруза);
			Если НЕ Статус = Неопределено Тогда
				Если Ложь
					Или Статус = Перечисления.упСтатусыПутевогоЛиста.Новый 
					Или Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан 
				Тогда
					ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);	
				Иначе
					Отказ = Истина;
					ОписаниеОшибки = Нстр("ru = 'Нельзя переводить путевой лист из статуса ""Новый"" в статус ""%1""'");
					ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Статус);
					ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				Попытка
					ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
					ПутевойЛист = ПутевойЛистОбъект.Ссылка;
				Исключение
					Отказ = Истина;
					ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
						ОписаниеОшибки = ОписаниеОшибки();
					КонецЕсли;
					ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
				КонецПопытки;
			КонецЕсли;

		Иначе
			Если НЕ Статус = Неопределено Тогда
				УстановитьСтатусПутевогоЛиста(ПутевойЛист, Статус,ОписаниеОшибки, Отказ)  	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПутевойЛист;
	
КонецФункции

// Функция - Сформировать путевой лист по транспортному средству
//
// Параметры:
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства	 - транспортное средство.
//  Дата				 - ДатаВремя							 - дата путевого листа.
//  Гараж				 - СправочникСсылка.упГаражи				 - гараж
//  Отказ				 - Булево								 - истина, в случае возникновении ошибки.
// 
// Возвращаемое значение:
//  ДокументСсылка.упПутевойЛист - путевой лист.
//
Функция СформироватьПутевойЛистПоТранспортномуСредству(ТранспортноеСредство, Дата = Неопределено, Гараж, Отказ) Экспорт

	ПутевойЛистОбъект = Документы.упПутевойЛист.СоздатьДокумент();	
	ПутевойЛистОбъект.Дата = ?(ЗначениеЗаполнено(Дата), Дата ,ТекущаяДатаСеанса());
	ПутевойЛистОбъект.Заполнить(ТранспортноеСредство);
	ПутевойЛистОбъект.Гараж = Гараж;
	ПутевойЛистОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыПутевогоЛиста.Новый);	
	
	Если НЕ Отказ Тогда
		Попытка
			ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			ПутевойЛист = ПутевойЛистОбъект.Ссылка;
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОшибкаФормированияПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		КонецПопытки;
	КонецЕсли;
	
	Возврат ПутевойЛист;
	
КонецФункции

// Процедура создает и заполняет новый путевой лист по рейсу.
//
// Параметры:
//  мсвРейсы	 - Массив	 - рейсы.
//  ПутевойЛист - ДокументСсылка.упПутевойЛист 	 - путевой лист.
//  Отказ		 - Булево	 - Фиксировать изменения.
//
Процедура ДобавитьРейсыВПутевойЛист(мсвРейсы, ПутевойЛист, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ПутевойЛист) Тогда
		
		ПутевойЛистОбъект = ПутевойЛист.ПолучитьОбъект();	
		
		Рейсы	= ПутевойЛистОбъект.Рейсы;
		НоваяСтрока = Рейсы.Добавить();
		Для каждого ЭлементРейс Из мсвРейсы Цикл
			НоваяСтрока.Рейс = ЭлементРейс;
		КонецЦикла;
		
		Попытка
			ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			ПутевойЛист = ПутевойЛистОбъект.Ссылка;
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОшибкаДобавленияРейсаВПутевойЛист", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Функция создает новый документ "упПутевойЛист" для рейса.
//
// Параметры:
//  мсвРейсы		 - Массив						 - массив рейсов.
//  Статус		 - ПеречислениеСсылка.упСтатусыПутевогоЛиста	 - устанавливаемый статус путевого листа.
//  ОписаниеОшибки	 - Строка								 - Описание ошибки.
//  Отказ			 - Булево								 - Истина, при создании произошла ошибка.
// 
// Возвращаемое значение:
//  ДокументСсылка.упПутевойЛист - Результат выполнения.
//
Функция НовыйПутевойЛистПоРейсам(мсвРейсы, Статус = Неопределено, ОписаниеОшибки, Отказ) Экспорт
	
	ПутевойЛист = Неопределено;
	
	ПутевойЛистОбъект = Документы.упПутевойЛист.СоздатьДокумент();	
	ПутевойЛистОбъект.Дата = ТекущаяДатаСеанса();
	ПутевойЛистОбъект.Заполнить(мсвРейсы);
	ПутевойЛистОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	Если НЕ Статус = Неопределено Тогда
		Если Статус = Перечисления.упСтатусыПутевогоЛиста.Новый 
			ИЛИ Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда
			ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);	
		Иначе
			Отказ = Истина;
			ОписаниеОшибки = Нстр("ru = 'Нельзя переводить путевой лист из статуса ""Новый"" в статус ""%1""'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Статус);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Попытка
			ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			ПутевойЛист = ПутевойЛистОбъект.Ссылка;
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ПутевойЛист;
	
КонецФункции

// Процедура устанавилвает статут путевого листа.
//
// Параметры:
//  ПутевойЛист - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	Статус - ПеречислениеСсылка.упСтатусыПутевогоЛиста - Ссылка на элемент.
//  ОписаниеОшибки - Строка - Представление ошибки.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура УстановитьСтатусПутевогоЛиста(ПутевойЛист, Статус, ОписаниеОшибки, Отказ) Экспорт
	
	упРаботаСоСтатусами.УстановитьСтатусДокументу(ПутевойЛист, Статус, ОписаниеОшибки, Отказ); 
		
КонецПроцедуры

// Функция возвращает путевой лист по рейсам.
//
// Параметры:
//  мсвРейсов	 - Массив				 - Массив рейсов.
//  Гараж		 - СправочникСсылка.упГаражи	 - Гараж по которому выдается путевой лист.
//  Отказ		 - Булево					 - Ложь, путевой лист не выдан.
// 
// Возвращаемое значение:
//  ДокументСсылка.упПутевойЛист - Результат выполнения.
//
Функция ВыдатьПутевойЛистПоРейсам(мсвРейсов, Гараж = Неопределено, Отказ) Экспорт
	
	ПутевойЛист = СформироватьПутевойЛистПоРейсам(мсвРейсов, Гараж, Перечисления.упСтатусыПутевогоЛиста.Выдан, Отказ);
	
	Возврат ПутевойЛист;
	
КонецФункции

// Процедура устанавливает статус путевого листа в "Возвращен". 
//
// Параметры:
//  ПутевойЛист - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//  ОписаниеОшибки - Строка - Представление ошибки.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура ВернутьПутевойЛист(ПутевойЛист, ОписаниеОшибки, Отказ) Экспорт
	
	УстановитьСтатусПутевогоЛиста(ПутевойЛист, Перечисления.упСтатусыПутевогоЛиста.Возвращен, ОписаниеОшибки, Отказ);	
	
КонецПроцедуры

// Дозаполнение документа путевой лист.
//
// Параметры:
//  ПутевойЛист - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//  ВидГСМ - СправочникСсылка.упВидыГСМ - вид гсм.
//  Количество - Число - Количество.
//  Валюта - СправочникСсылка.Валюты - Валюты.
//	Стоимость - Число - Значение стоимости.
//	СтавкаНДС - ПеречислениеСсылка.упСтавкиНДС - НДС.
//	СтоимостьНДС - Число - Стоимость.
//  ОписаниеОшибки - Строка - Представление ошибки.
//	Отказ - Булево - Фиксировать изменения.
//	ГСМ - СправочникСсылка.упГСМ - Ссылка на элемент.
//	АЗС - СправочникСсылка.упКонтрагенты - Ссылка на элемент.
//	ТопливнаяКарта - СправочникСсылка.упТопливныеКарты - Ссылка на элемент.
//	ДатаЗаправкиСлива - Дата - Период.
//
Процедура ЗаправитьТранспортноеСредство(ПутевойЛист, ВидГСМ, Количество, Валюта, Стоимость, СтавкаНДС, СтоимостьНДС, ОписаниеОшибки, Отказ, ГСМ = Неопределено, АЗС = Неопределено, ТопливнаяКарта = Неопределено, ДатаЗаправкиСлива = Неопределено) Экспорт
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыПутевыхЛистов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ПутевойЛист);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упПутевойЛист");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", ПутевойЛист);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		ОписаниеОшибки =Нстр("ru = 'Не удалось наложить блокировки'");
		Возврат;
	КонецПопытки;
	
	Если НЕ Отказ Тогда
		СтатусПутевогоЛиста = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ПутевойЛист);
		Отказ = НЕ РазрешенаЗаправка(ПутевойЛист, СтатусПутевогоЛиста);
		Если Отказ Тогда
			Событие		= НСтр("ru = 'ЗаписьПутевогоЛиста'");
			ТекстОшибки = НСтр("ru = 'Нельзя заправлять документ в статусе ""%1""'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтатусПутевогоЛиста);
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПутевойЛист,ПутевойЛист,ТекстОшибки,РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецЕсли;
	КонецЕсли;
			
	Если НЕ Отказ Тогда
		ПутевойЛистОбъект 	= ПутевойЛист.ПолучитьОбъект();
		
		СтрокаЗаправки = ПутевойЛистОбъект.ЗаправкиСливыГСМ.Добавить();
		Если ЗначениеЗаполнено(ДатаЗаправкиСлива) Тогда
			СтрокаЗаправки.ДатаЗаправкиСлива	= ДатаЗаправкиСлива;	
		КонецЕсли;
		
		СтрокаЗаправки.ВидГСМ 			= ВидГСМ;
		СтрокаЗаправки.ГСМ 				= ?(ГСМ = Неопределено, Справочники.упГСМ.ПустаяСсылка(), ГСМ);
		СтрокаЗаправки.ВидДвижения 		= Перечисления.упВидыДвиженияТоплива.Заправка; 
		СтрокаЗаправки.ДопОборудование 	= Справочники.упДополнительноеОборудование.ПустаяСсылка(); 
		СтрокаЗаправки.Количество		= Количество;
		Если НЕ Количество = 0 Тогда
			СтрокаЗаправки.Цена				= Стоимость / Количество;
		Иначе
			СтрокаЗаправки.Цена				= 0;
		КонецЕсли;
		СтрокаЗаправки.Стоимость		= Стоимость;
		СтрокаЗаправки.СтавкаНДС		= СтавкаНДС;
		СтрокаЗаправки.Валюта			= Валюта;
		СтрокаЗаправки.СтоимостьНДС		= СтоимостьНДС;
		СтрокаЗаправки.АЗС				= ?(АЗС = Неопределено, Справочники.упКонтрагенты.ПустаяСсылка(), АЗС);
		СтрокаЗаправки.ТопливнаяКарта	= ?(ТопливнаяКарта = Неопределено, Справочники.упТопливныеКарты.ПустаяСсылка(), ТопливнаяКарта);
	
		ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыПутевогоЛиста.Возвращен);
		ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("ПроверитьВозможностьУстановкиСтатуса", Истина);
		Попытка
			ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			Событие		= "ru = 'ЗаписьПутевогоЛиста'";
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПутевойЛист,ПутевойЛист,ОписаниеОшибки,РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;	

	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();	
	КонецЕсли;

КонецПроцедуры

// Функция возвращает возможность заправки ТС. 
//
// Параметры:
//  ПутевойЛист - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	СтатусПутевогоЛиста - ПеречислениеСсылка.упСтатусыПутевогоЛиста - Ссылка на элемент.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция РазрешенаЗаправка(ПутевойЛист, СтатусПутевогоЛиста) Экспорт
	
	текРезультат = Ложь;
	Если СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Новый
		ИЛИ СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда
		текРезультат = Истина;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Процедура изменения начальных остатков по путевому листу.
//
// Параметры:
//  ПутевойЛист - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//	ГСМ - СправочникСсылка.упГСМ - Ссылка на элемент.
//	ПробегНачало - Дата - Период.
//	Статус - ПеречислениеСсылка.упСтатусыПутевогоЛиста - Ссылка на элемент.
//  ОписаниеОшибки - Строка - Представление ошибки.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура ЗаполнитьНачальныеОстатки(ПутевойЛист, ГСМ, ПробегНачало, Статус = Неопределено, ОписаниеОшибки, Отказ) Экспорт
	
	Если ТипЗнч(ГСМ) = Тип("ТаблицаЗначений") Тогда
		
		ПутевойЛистОбъект = ПутевойЛист.ПолучитьОбъект();	
		
		// Пробег.
		ПутевойЛистОбъект.ПробегНачало 	= ПробегНачало;
		ПутевойЛистОбъект.Пробег		= ПутевойЛистОбъект.ПробегОкончание - ПутевойЛистОбъект.ПробегНачало;
				
		// ГСМ.
		ПутевойЛистОбъект.ГСМ.Очистить();
		ПутевойЛистОбъект.ГСМ.Загрузить(ГСМ);
		
		Если НЕ Отказ Тогда
			Попытка
				Если ЗначениеЗаполнено(Статус) Тогда
					ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);
				КонецЕсли;
				ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
					ОписаниеОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("НачальныеОстаткиПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка, , ПутевойЛист, ОписаниеОшибки);
				ОписаниеОшибки = Нстр("ru = 'Не удалось изменить начальные остатки по путевому листу'");
				
			КонецПопытки;
		КонецЕсли;
	Иначе	
		Отказ = Истина;
		ОписаниеОшибки = Нстр("ru = 'Не удалось изменить начальные остатки по путевому листу'");
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения конечных остатков путевого листа.
//
// Параметры:
//  ПутевойЛист	 - ДокументСсылка.упПутевойЛист	 - Ссылка на элемент.
//  ГСМ			 - СправочникСсылка.упГСМ		 - Ссылка на элемент.
//  Заправки		 - ТаблицаЗначений				 - Таблица данных.
//  ПробегОкончание	 - Дата						 - Период.
//  Статус		 - ПеречислениеСсылка.упСтатусыПутевогоЛиста	 - Ссылка на элемент.
//  ОписаниеОшибки	 - Строка								 - Представление ошибки.
//  Отказ			 - Булево								 - Фиксировать изменения.
//
Процедура ЗаполнитьКонечныеОстатки(ПутевойЛист, ГСМ, Заправки, ПробегОкончание, Статус = Неопределено, ОписаниеОшибки, Отказ) Экспорт
	
	ПутевойЛистОбъект 					= ПутевойЛист.ПолучитьОбъект();	
	ПутевойЛистОбъект.ПробегОкончание 	= ПробегОкончание;
	ПутевойЛистОбъект.Пробег			= ПутевойЛистОбъект.ПробегОкончание - ПутевойЛист.ПробегНачало;
	
	Если ПутевойЛистОбъект.ПробегНачало >= ПутевойЛистОбъект.ПробегОкончание Тогда
		ОписаниеОшибки = НСтр("ru = 'Конечный пробег должен быть больше начального'");
		Отказ = Истина;	
	КонецЕсли;
		
	Если ТипЗнч(ГСМ) = Тип("ТаблицаЗначений") Тогда

		ГСМОбъекта = ПутевойЛистОбъект.ГСМ;
		
		Для каждого СтрокаГСМ Из ГСМ Цикл
			мсвСтрокиГСМ = ГСМОбъекта.НайтиСтроки(Новый Структура("ВидГСМ",СтрокаГСМ.ВидГСМ));
			мсвСтрокиГСМ[0].КонОстаток = СтрокаГСМ.КонОстаток;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(Заправки) = Тип("ТаблицаЗначений") Тогда
		
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		Если ВедетсяВалютныйУчет Тогда
			ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета(); 
		КонецЕсли;
		
		ЗаправкиОбъекта = ПутевойЛистОбъект.ЗаправкиСливыГСМ;
		ЗаправкиОбъекта.Очистить();
		Для каждого СтрокаЗаправка Из Заправки Цикл
			НоваяСтрокаЗаправка = ЗаправкиОбъекта.Добавить();
			Если ВедетсяВалютныйУчет Тогда
				НоваяСтрокаЗаправка.Валюта = ВалютаРеглУчета;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗаправка, СтрокаЗаправка);
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Попытка
			Если ЗначениеЗаполнено(Статус) Тогда
				ПутевойЛистОбъект.ДополнительныеСвойства.Вставить("Статус", Статус);
			КонецЕсли;
			ПутевойЛистОбъект.Записать(РежимЗаписиДокумента.Проведение);	
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("НачальныеОстаткиПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка, , ПутевойЛист, ОписаниеОшибки);
			ОписаниеОшибки = Нстр("ru = 'Не удалось изменить начальные остатки по путевому листу'");
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Процедура закрытия путевого листа.
//
// Параметры:
//  ПутевойЛист - ДокументСсылка.упПутевойЛист - Ссылка на элемент.
//  ОписаниеОшибки - Строка - Представление ошибки.
//	Отказ - Булево - Фиксировать изменения.
//
Процедура ЗакрытьПутевойЛист(ПутевойЛист, ОписаниеОшибки, Отказ) Экспорт
	
	УстановитьСтатусПутевогоЛиста(ПутевойЛист, Перечисления.упСтатусыПутевогоЛиста.Закрыт, ОписаниеОшибки, Отказ);	
	
КонецПроцедуры

// Возвращает печатную форму путевого листа.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   ТабличныйДокумент - Объект печати.
//
Функция ПолучитьТипПечатнойФормы(ТранспортноеСредство) Экспорт

	сткРеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТранспортноеСредство, "ПечатнаяФормаПутевогоЛиста, Модель.ПечатнаяФормаПутевогоЛиста");

	текРезультат = сткРеквизитыОбъекта.ПечатнаяФормаПутевогоЛиста;
	
	Если НЕ ЗначениеЗаполнено(текРезультат) Тогда
		текРезультат = сткРеквизитыОбъекта.МодельПечатнаяФормаПутевогоЛиста;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(текРезультат) Тогда
		текРезультат = Неопределено;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Возвращает таблицу ссылок документов путевой лист.
//
// Параметры:
//	мсвПутевыеЛисты - Массив - Ссылки на элемент.
//
// Возвращаемое значение:
//   ДеревоЗначений - Результат выполнения запроса.
//
Функция ТипыПечатныхФормПутевыхЛистовПоУмолчанию(мсвПутевыеЛисты) Экспорт

	дрвРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПутевойЛист.Ссылка,
	               |	ВЫБОР
	               |		КОГДА НЕ упТранспортныеСредства.ПечатнаяФормаПутевогоЛиста ЕСТЬ NULL 
	               |				И (упТранспортныеСредства.ПечатнаяФормаПутевогоЛиста ССЫЛКА Справочник.ДополнительныеОтчетыИОбработки
	               |						И НЕ упТранспортныеСредства.ПечатнаяФормаПутевогоЛиста = ЗНАЧЕНИЕ(Справочник.ДополнительныеОтчетыИОбработки.ПустаяСсылка)
	               |					ИЛИ упТранспортныеСредства.ПечатнаяФормаПутевогоЛиста ССЫЛКА Перечисление.упТипыПечатныхФормПутевогоЛиста
	               |						И НЕ упТранспортныеСредства.ПечатнаяФормаПутевогоЛиста = ЗНАЧЕНИЕ(Перечисление.упТипыПечатныхФормПутевогоЛиста.ПустаяСсылка))
	               |			ТОГДА упТранспортныеСредства.ПечатнаяФормаПутевогоЛиста
	               |		КОГДА НЕ упМоделиТС.ПечатнаяФормаПутевогоЛиста ЕСТЬ NULL 
	               |			ТОГДА упМоделиТС.ПечатнаяФормаПутевогоЛиста
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДополнительныеОтчетыИОбработки.ПустаяСсылка)
	               |	КОНЕЦ КАК ПечатнаяФормаПутевогоЛиста
	               |ИЗ
	               |	Документ.упПутевойЛист КАК упПутевойЛист
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упМоделиТС КАК упМоделиТС
	               |			ПО упТранспортныеСредства.Модель = упМоделиТС.Ссылка
	               |		ПО упПутевойЛист.ТранспортноеСредство = упТранспортныеСредства.Ссылка
	               |ГДЕ
	               |	упПутевойЛист.Ссылка В(&мсвПутевыеЛисты)
	               |ИТОГИ ПО
	               |	ПечатнаяФормаПутевогоЛиста";
	Запрос.УстановитьПараметр("мсвПутевыеЛисты", мсвПутевыеЛисты);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	дрвРезультат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Возврат дрвРезультат;
	
КонецФункции

// Функция - Печатные форм путевых листов по умолчанию
//
// Параметры:
//  мсвПутевыхЛистов - Массив	 - массив путевых листов.
// 
// Возвращаемое значение:
//  Массив - массив печатных форм по заданным путевым листам.
//
Функция ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов) Экспорт
	
	мсвРезультат = Новый Массив;
	
	дрвПечатныеФормы = ТипыПечатныхФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов);
	
	Для каждого СтрокаПечатнаяФорма Из дрвПечатныеФормы.Строки Цикл
		ПечатнаяФормаПутевогоЛиста = СтрокаПечатнаяФорма.ПечатнаяФормаПутевогоЛиста;
		
		ИдентификаторПечатнойФормы = Неопределено;
		ИмяМакета 		= "";
		Если Истина
			И ЗначениеЗаполнено(ПечатнаяФормаПутевогоЛиста)
			И ТипЗнч(ПечатнаяФормаПутевогоЛиста) = Тип("ПеречислениеСсылка.упТипыПечатныхФормПутевогоЛиста") 
		Тогда
			ИдентификаторПечатнойФормы = ОбщегоНазначения.ИмяЗначенияПеречисления(ПечатнаяФормаПутевогоЛиста);
			ИмяОбъектаМетаданных       = "Документ.упПутевойЛист";
			ИмяМакета                  = ИдентификаторПечатнойФормы;	
		КонецЕсли;
		
		Если ИдентификаторПечатнойФормы = Неопределено Тогда
			ИмяОбъектаМетаданных = "Документ.упПутевойЛист";
			ИмяМакета            = упСервисныеФункцииВызовСервера.ПолучитьИмяМакетаВнешнейПечатнойФормы(ПечатнаяФормаПутевогоЛиста, ИмяОбъектаМетаданных);	
		КонецЕсли;
					
		мсвСсылок 	= Новый Массив;
		Для каждого СтрокаПутевойЛист Из СтрокаПечатнаяФорма.Строки Цикл
			мсвСсылок.Добавить(СтрокаПутевойЛист.Ссылка);
		КонецЦикла;
		
		сткПечатнаяФорма = Новый Структура();
		сткПечатнаяФорма.Вставить("ИдентификаторПечатнойФормы", ИдентификаторПечатнойФормы);
		сткПечатнаяФорма.Вставить("ПечатнаяФормаПутевогоЛиста", ПечатнаяФормаПутевогоЛиста);
		сткПечатнаяФорма.Вставить("ИмяМакета",                  ИмяМакета);
		сткПечатнаяФорма.Вставить("мсвПутевыхЛистов",           мсвСсылок);
		мсвРезультат.Добавить(сткПечатнаяФорма);
	КонецЦикла;
	
	Возврат мсвРезультат;
	
КонецФункции

// Функция возвращает таблицу всех видов ГСМ по транспортному средству.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   Массив - ВидГСМ ссылки на элемент справочника. Результат выполнения.
//
Функция ВидыГСМПоТранспортномуСредству(ТранспортноеСредство) Экспорт
	
	Запрос = Новый Запрос;
	Если ТипЗнч(ТранспортноеСредство) = Тип("СправочникСсылка.упТранспортныеСредства")  Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	упМоделиТСНормыРасходаГСМ.ВидГСМ
		               |ИЗ
		               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТС.НормыРасходаГСМ КАК упМоделиТСНормыРасходаГСМ
		               |		ПО упТранспортныеСредства.Модель = упМоделиТСНормыРасходаГСМ.Ссылка
		               |ГДЕ
		               |	упТранспортныеСредства.Ссылка = &Ссылка";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.ВидГСМ
		               |ИЗ
		               |	Справочник.упДополнительноеОборудование.ВидыГСМ КАК упДополнительноеОборудованиеНормыРасходаГСМ
		               |ГДЕ
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.Ссылка = &Ссылка";
		
	КонецЕсли;			   
	Запрос.УстановитьПараметр("Ссылка", ТранспортноеСредство);
	мсвРезультат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидГСМ");
		
	Возврат мсвРезультат;
		
КонецФункции

// Функция - Вернуть доступные виды ГСМ
//
// Параметры:
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства - транспортное средство.
// 
// Возвращаемое значение:
//   Массив(СправочникСсылка.упВидыГСМ) - массив видов ГСМ.
//
Функция ВернутьДоступныеВидыГСМ(ТранспортноеСредство) Экспорт
	мсвДоступныхВидовГСМ = Новый Массив;
	
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВидыГСМ.ВидГСМ КАК ВидГСМ
		|ИЗ
		|	РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(
		|		,
		|		ТранспортноеСредство = &ТранспортноеСредство) КАК ВидыГСМ
		|ГДЕ ВидыГСМ.Используется
		|";
		
		Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	упМоделиТСНормыРасходаГСМ.ВидГСМ КАК ВидГСМ
			|ИЗ
			|	Справочник.упМоделиТС.НормыРасходаГСМ КАК упМоделиТСНормыРасходаГСМ
			|ГДЕ
			|	упМоделиТСНормыРасходаГСМ.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	упМоделиТСНормыРасходаГСМ.ВидГСМ";
			
			Запрос.УстановитьПараметр("Ссылка", ТранспортноеСредство.Модель);
			Результат = Запрос.Выполнить();
		КонецЕсли;
		
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				мсвДоступныхВидовГСМ.Добавить(Выборка.ВидГСМ);
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;

	Возврат мсвДоступныхВидовГСМ;
	
КонецФункции // ВернутьДоступныеВидыГСМНаСервере()

// Функция возвращает основной вид ГСМ по транспортному средству.
//
// Параметры:
//	ТранспортноеСредство - СправочникСсылка.упТранспортныеСредства - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упВидыГСМ - вид гсм.
//
Функция ОсновнойВидГСМПоТранспортномуСредству(ТранспортноеСредство) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Если ТипЗнч(ТранспортноеСредство) = Тип("СправочникСсылка.упТранспортныеСредства")  Тогда
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВидыГСМ.ВидГСМ КАК ВидГСМ,
		|	1 КАК Порядок
		|ИЗ
		|	РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(
		|		,ТранспортноеСредство = &Ссылка
		|		) КАК ВидыГСМ
		|ГДЕ ИСТИНА
		|	И ВидыГСМ.Используется
		|	И ВидыГСМ.Основной
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упМоделиТСНормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
		|	2 КАК Порядок
		|ИЗ
		|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТС.НормыРасходаГСМ КАК упМоделиТСНормыРасходаГСМ
		|	ПО упТранспортныеСредства.Модель = упМоделиТСНормыРасходаГСМ.Ссылка
		|ГДЕ ИСТИНА
		|	И упТранспортныеСредства.Ссылка = &Ссылка
		|	И упМоделиТСНормыРасходаГСМ.Основной
		|УПОРЯДОЧИТЬ ПО
		|	Порядок
		|";

	Иначе
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.ВидГСМ
		               |ИЗ
		               |	Справочник.упДополнительноеОборудование.ВидыГСМ КАК упДополнительноеОборудованиеНормыРасходаГСМ
		               |ГДЕ
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.Ссылка = &Ссылка
		               |	И упДополнительноеОборудованиеНормыРасходаГСМ.Основной";
		
	КонецЕсли;			   
	Запрос.УстановитьПараметр("Ссылка", ТранспортноеСредство);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ВидГСМ;	
	КонецЕсли;
			
	Возврат Результат;
		
КонецФункции

// Функция возвращает основные виды ГСМ по транспортным средствам.
//
// Параметры:
//  мсвТранспортныеСредства	 - Массив	 -  транспортные средства.
// 
// Возвращаемое значение:
//  Соответствие - (Транспортное средство, вид гсм)
//
Функция ОсновныеВидыГСМПоТранспортномСредствам(мсвТранспортныеСредства) Экспорт
	
	Результат = Новый Соответствие;
	
	Если мсвТранспортныеСредства.Количество() = 0 Тогда
		Возврат Результат;	
	КонецЕсли;
	
	ТранспортноеСредство = мсвТранспортныеСредства[0];
	
	Запрос = Новый Запрос;
	Если ТипЗнч(ТранспортноеСредство) = Тип("СправочникСсылка.упТранспортныеСредства")  Тогда
		Запрос.Текст = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыГСМ.ВидГСМ КАК ВидГСМ,
		|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
		|	упТранспортныеСредства.Модель КАК Модель
		|ПОМЕСТИТЬ втОсновныеВидыГСМ
		|ИЗ
		|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(
		|		,
		|		ТранспортноеСредство В (&мсвТранспортныеСредства)) КАК ВидыГСМ
		|	ПО ИСТИНА
		|		И ВидыГСМ.ТранспортноеСредство = упТранспортныеСредства.Ссылка
		|		И ВидыГСМ.Используется
		|		И ВидыГСМ.Основной
		|ГДЕ упТранспортныеСредства.Ссылка В  (&мсвТранспортныеСредства)
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбОсновныеВидыГСМ.ВидГСМ КАК ВидГСМ,
		|	тбОсновныеВидыГСМ.ТранспортноеСредство КАК Ссылка
		|ИЗ
		|	втОсновныеВидыГСМ КАК тбОсновныеВидыГСМ
		|ГДЕ НЕ тбОсновныеВидыГСМ.ТранспортноеСредство ЕСТЬ NULL
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упМоделиТСНормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
		|	тбОсновныеВидыГСМ.ТранспортноеСредство КАК Ссылка
		|ИЗ
		|	втОсновныеВидыГСМ КАК тбОсновныеВидыГСМ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упМоделиТС.НормыРасходаГСМ КАК упМоделиТСНормыРасходаГСМ
		|	ПО ИСТИНА
		|		И тбОсновныеВидыГСМ.Модель = упМоделиТСНормыРасходаГСМ.Ссылка
		|		И тбОсновныеВидыГСМ.ТранспортноеСредство ЕСТЬ NULL
		|ГДЕ упМоделиТСНормыРасходаГСМ.Основной
		|";

	Иначе
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.ВидГСМ КАК ВидГСМ,
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.упДополнительноеОборудование.ВидыГСМ КАК упДополнительноеОборудованиеНормыРасходаГСМ
		               |ГДЕ
		               |	упДополнительноеОборудованиеНормыРасходаГСМ.Ссылка В(&мсвТранспортныеСредства)
		               |	И упДополнительноеОборудованиеНормыРасходаГСМ.Основной";
		
	КонецЕсли;			   
	Запрос.УстановитьПараметр("мсвТранспортныеСредства", мсвТранспортныеСредства);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Ссылка, Выборка.ВидГСМ);
	КонецЦикла;
			
	Возврат Результат;
		
КонецФункции

// Функция возвращает масси путевых листов по переданным рейсам.
//
// Параметры:
//	мсвРейсы - Массив - Ссылки на документ рейс.
//
// Возвращаемое значение:
//   Массив - Массив путевых листов.
//
Функция ПутевыеЛистыПоРейсам(мсвРейсы) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПутевойЛистРейсы.Ссылка
	|ИЗ
	|	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛистРейсы.Ссылка)
	|ГДЕ
	|	упПутевойЛистРейсы.Рейс В(&мсвРейсы)
	|	И НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)
	|	И НЕ упПутевойЛистРейсы.Ссылка.ПометкаУдаления";
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвРезультат.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат мсвРезультат;
	
КонецФункции

// Функция - Сотрудники по путевому листу.
//
// Параметры:
//  Гараж - СправочникСсылка.упГаражи - гараж.
// 
// Возвращаемое значение:
//  Структура - (Механик,Врач) Результат выполнения.
//
Функция СотрудникиПоПутевомуЛисту(Гараж) Экспорт
	
	сткРезультат = Новый Структура("Механик, Врач", Справочники.упСотрудники.ПустаяСсылка(), Справочники.упСотрудники.ПустаяСсылка());
	 	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	упГаражиСотрудники.Сотрудник КАК Сотрудник,
	               |	упГаражиСотрудники.Сотрудник.ТипСотрудника КАК ТипСотрудника
	               |ИЗ
	               |	Справочник.упГаражи.Сотрудники КАК упГаражиСотрудники
	               |ГДЕ
	               |	упГаражиСотрудники.Ссылка = &Гараж
	               |	И (упГаражиСотрудники.Сотрудник.ТипСотрудника = ЗНАЧЕНИЕ(Перечисление.упТипыСотрудников.Механик)
	               |			ИЛИ упГаражиСотрудники.Сотрудник.ТипСотрудника = ЗНАЧЕНИЕ(Перечисление.упТипыСотрудников.Врач))
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудник)
	               |ПО
	               |	ТипСотрудника";
	Запрос.УстановитьПараметр("Гараж", Гараж);
	текВыборкаКоличество	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока текВыборкаКоличество.Следующий() Цикл
		
		Если текВыборкаКоличество.Сотрудник = 1 Тогда
			
			текВыборкаСотрудник = текВыборкаКоличество.Выбрать();
			
			Если текВыборкаСотрудник.Следующий() Тогда
				
				Если текВыборкаСотрудник.ТипСотрудника = Перечисления.упТипыСотрудников.Механик Тогда
					сткРезультат.Вставить("Механик", текВыборкаСотрудник.Сотрудник);
				Иначе	
					сткРезультат.Вставить("Врач", текВыборкаСотрудник.Сотрудник);	
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЦикла;
		
	Возврат сткРезультат;
			
КонецФункции

// Функция - Рейс принадлежит путевому листу
//
// Параметры:
//  Рейс		 - ДокументСсылка.упРейс	 - рейс
//  ПутевойЛист	 - ДокументСсылка.упПутевойЛист	 - путевой лист
// 
// Возвращаемое значение:
//  Булево - Истина, если рейс принадлежит путевому листу
//
Функция РейсПринадлежитПутевомуЛисту(Рейс, ПутевойЛист) Экспорт
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упПутевойЛистРейсы.Ссылка
	|ИЗ
	|	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	|ГДЕ
	|	упПутевойЛистРейсы.Ссылка = &Ссылка
	|	И упПутевойЛистРейсы.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Ссылка", ПутевойЛист);
	Запрос.УстановитьПараметр("Рейс", 	Рейс);
	Результат = НЕ Запрос.Выполнить().Пустой();
	
	Возврат Результат;
		
КонецФункции

// Функция - Есть остатки по пробегу ТС.
// 
// Возвращаемое значение:
//   Булево - Истина, в случае если остатки в ренистре присутствуют.
//
Функция ЕстьОстаткиПоПробегуТС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упПробегТСОстатки.ТранспортноеСредство КАК ТранспортноеСредство
	|ИЗ
	|	РегистрНакопления.упПробегТС.Остатки КАК упПробегТСОстатки";
	РезультатЗапроса	= Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
		
КонецФункции

#Область НормыРасходаГСМ

// Функция - Возвращает параметры нормы расхода ГСМ.
//
// Параметры:
//  Период			 - Дата							 - дата получения нормы.
//  Организация		 - СправочникСсылка.Организации		 - организация.
//  Подразделение		 - СправочникСсылка.упСтруктураПредприятия	 - подразделение.
//  ОбъектНормРасхода	 - СправочникСсылка.упТранспортноеСредство	 - ТС.
//  ВидГСМ			 - СправочникСсылка.упВидыГСМ				 - вид ГСМ.
//  НормаРасхода		 - СправочникСсылка.упНормыРасходаГСМ		 - норма расхода.
// 
// Возвращаемое значение:
//  Структура - содержит параметры найденной нормы
//  * ТипНормыРасходаГСМ - ПеречислениеСсылка.упТипыНормРасходаГСМ - тип нормы расхода.
//  * Расход	- Число - расход топлива за заданную в типе нормы единицу.
//  * ИзменениеРасхода - Число  - расход на 100 тонн км.
//  * ФормулаРасчета - СправочникСсылка.упПравилаРасчета - правило расчета нормы.
//  * ШкалаТемпературныхКоэффициентов - СправочникСсылка.упОценочнаяШкала - шкала температурных коэффициентов корректировки топлива.
//
Функция ПараметрыНормыРасходаГСМ(Период, Организация, Подразделение, ОбъектНормРасхода, ВидГСМ, НормаРасхода) Экспорт
	
	Результат = Новый Структура();
	
	Если ЗначениеЗаполнено(НормаРасхода) Тогда
		
		Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НормаРасхода, "ТипНормыРасходаГСМ, Расход, ИзменениеРасхода, ФормулаРасчета, ШкалаТемпературныхКоэффициентов");
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упНормыРасходаГСМСрезПоследних.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
		|	упНормыРасходаГСМСрезПоследних.Расход КАК Расход,
		|	упНормыРасходаГСМСрезПоследних.ИзменениеРасхода КАК ИзменениеРасхода,
		|	упНормыРасходаГСМСрезПоследних.ФормулаРасчета КАК ФормулаРасчета,
		|	упНормыРасходаГСМСрезПоследних.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов
		|ИЗ
		|	РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение
		|				И ОбъектНормРасхода = &ОбъектНормРасхода
		|				И ВидГСМ = &ВидГСМ) КАК упНормыРасходаГСМСрезПоследних";
		Запрос.УстановитьПараметр("Период", 		Период);
		Запрос.УстановитьПараметр("Организация", 	Организация);
		Запрос.УстановитьПараметр("Подразделение", 	Подразделение);
		Запрос.УстановитьПараметр("ОбъектНормРасхода", ОбъектНормРасхода);
		Запрос.УстановитьПараметр("ВидГСМ", 		ВидГСМ);
		
		РезультатЗапроса = Запрос.Выполнить();
		Для каждого КолонкаЗапроса Из РезультатЗапроса.Колонки Цикл
			Результат.Вставить(КолонкаЗапроса.Имя);
		КонецЦикла;
		
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
		КонецЦикла; 
	КонецЕсли;

	Возврат Результат;
		
КонецФункции

#Область НормыРасходаГСМ

// Функция - Возвращает параметры расчета надбавок для нормы расхода видов ГСМ.
//
// Параметры:
//  Период			 - Дата	 							- дата получения нормы.
//  Организация		 - СправочникСсылка.Организации	 		- организация.
//  Подразделение		 - СправочникСсылка.упСтруктураПредприятия	- подразделение.
//  ОбъектНормРасхода	 - СправочникСсылка.упТранспортноеСредство	- ТС.
//  ВидыГСМ			 - ТаблицаЗначений - таблица с колонками.
//	* ВидГСМ 		- СправочникСсылка.упВидыГСМ 			- вид ГСМ.
//	* НормаРасхода - СправочникСсылка.упНормыРасходаГСМ 	- норма расхода.
//                  
// Возвращаемое значение:
//  Структура - ТаблицаЗначений - таблица с колонками:
//	* ВидГСМ 					- СправочникСсылка.упВидыГСМ 							- вид гсм.
//	* ВидЭксплуатационнойНормы	- ПеречислениеСсылка.упВидыЭксплуатационныхНормРасходаГСМ 	- вид нормы.
//	* ТипРасхода 				- ПеречислениеСсылка.упСпособыРасчетаНадбавокГСМ  		- способ расчета надбавки(% от расхода, л/час, л/км).
//	* Расход 					- Число 											- количественный показатель расхода.
//	
//
Функция НадбавкиНормыРасходаГСМ(Период, Организация, Подразделение, ОбъектНормРасхода, ВидыГСМ) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"
	|//{: Текст собран конструктором из подсистемы ""Инструменты разработчика"" (http://devtool1c.ucoz.ru), который сохраняет звездочки и комментарии.
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГСМ.ВидГСМ КАК ВидГСМ,
	|	втГСМ.НормаРасхода КАК НормаРасхода
	|ПОМЕСТИТЬ втГСМ
	|ИЗ
	|	&тбзГСМ КАК втГСМ
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	упНормыРасходаГСМСрезПоследних.РасходЗимний КАК РасходЗимний,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаГорнаяМестность КАК ТипРасходаГорнаяМестность,
	|	упНормыРасходаГСМСрезПоследних.РасходГорнаяМестность КАК РасходГорнаяМестность,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаСложностьДорог КАК ТипРасходаСложностьДорог,
	|	упНормыРасходаГСМСрезПоследних.РасходСложностьДорог КАК РасходСложностьДорог,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаНаселенныйПункт КАК ТипРасходаНаселенныйПункт,
	|	упНормыРасходаГСМСрезПоследних.РасходНаселенныйПункт КАК РасходНаселенныйПункт,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаГрузовой КАК ТипРасходаГрузовой,
	|	упНормыРасходаГСМСрезПоследних.РасходГрузовой КАК РасходГрузовой,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаКондиционер КАК ТипРасходаКондиционер,
	|	упНормыРасходаГСМСрезПоследних.РасходКондиционер КАК РасходКондиционер,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаКлиматКонтроль КАК ТипРасходаКлиматКонтроль,
	|	упНормыРасходаГСМСрезПоследних.РасходКлиматКонтроль КАК РасходКлиматКонтроль,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаОтопитель КАК ТипРасходаОтопитель,
	|	упНормыРасходаГСМСрезПоследних.РасходОтопитель КАК РасходОтопитель,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаСпециальноеОборудование КАК ТипРасходаСпециальноеОборудование,
	|	упНормыРасходаГСМСрезПоследних.РасходСпециальноеОборудование КАК РасходСпециальноеОборудование,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаПростой КАК ТипРасходаПростой,
	|	упНормыРасходаГСМСрезПоследних.РасходПростой КАК РасходПростой,
	|	втГСМТ.ВидГСМ КАК ВидГСМ,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	упНормыРасходаГСМСрезПоследних.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ПОМЕСТИТЬ втНадбавкиПоВидуГСМ
	|ИЗ
	|	втГСМ КАК втГСМТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Подразделение = &Подразделение
	|		И ОбъектНормРасхода = &ОбъектНормРасхода) КАК упНормыРасходаГСМСрезПоследних
	|	ПО ИСТИНА
	|		И втГСМТ.ВидГСМ = упНормыРасходаГСМСрезПоследних.ВидГСМ
	|		И втГСМТ.НормаРасхода = ЗНАЧЕНИЕ(Справочник.упНормыРасходаГСМ.ПустаяСсылка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упНормыРасходаГСМТ.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	упНормыРасходаГСМТ.РасходЗимний КАК РасходЗимний,
	|	упНормыРасходаГСМТ.ТипРасходаГорнаяМестность КАК ТипРасходаГорнаяМестность,
	|	упНормыРасходаГСМТ.РасходГорнаяМестность КАК РасходГорнаяМестность,
	|	упНормыРасходаГСМТ.ТипРасходаСложностьДорог КАК ТипРасходаСложностьДорог,
	|	упНормыРасходаГСМТ.РасходСложностьДорог КАК РасходСложностьДорог,
	|	упНормыРасходаГСМТ.ТипРасходаНаселенныйПункт КАК ТипРасходаНаселенныйПункт,
	|	упНормыРасходаГСМТ.РасходНаселенныйПункт КАК РасходНаселенныйПункт,
	|	упНормыРасходаГСМТ.ТипРасходаГрузовой КАК ТипРасходаГрузовой,
	|	упНормыРасходаГСМТ.РасходГрузовой КАК РасходГрузовой,
	|	упНормыРасходаГСМТ.ТипРасходаКондиционер КАК ТипРасходаКондиционер,
	|	упНормыРасходаГСМТ.РасходКондиционер КАК РасходКондиционер,
	|	упНормыРасходаГСМТ.ТипРасходаКлиматКонтроль КАК ТипРасходаКлиматКонтроль,
	|	упНормыРасходаГСМТ.РасходКлиматКонтроль КАК РасходКлиматКонтроль,
	|	упНормыРасходаГСМТ.ТипРасходаОтопитель КАК ТипРасходаОтопитель,
	|	упНормыРасходаГСМТ.РасходОтопитель КАК РасходОтопитель,
	|	упНормыРасходаГСМТ.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	упНормыРасходаГСМТ.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	упНормыРасходаГСМТ.ТипРасходаСпециальноеОборудование КАК ТипРасходаСпециальноеОборудование,
	|	упНормыРасходаГСМТ.РасходСпециальноеОборудование КАК РасходСпециальноеОборудование,
	|	упНормыРасходаГСМТ.ТипРасходаПростой КАК ТипРасходаПростой,
	|	упНормыРасходаГСМТ.РасходПростой КАК РасходПростой,
	|	втГСМТ.ВидГСМ КАК ВидГСМ,
	|	упНормыРасходаГСМТ.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	упНормыРасходаГСМТ.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ИЗ
	|	втГСМ КАК втГСМТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упНормыРасходаГСМ КАК упНормыРасходаГСМТ
	|	ПО втГСМТ.НормаРасхода = упНормыРасходаГСМТ.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	упНормыРасходаГСМСрезПоследних.РасходЗимний КАК РасходЗимний,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаГорнаяМестность КАК ТипРасходаГорнаяМестность,
	|	упНормыРасходаГСМСрезПоследних.РасходГорнаяМестность КАК РасходГорнаяМестность,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаСложностьДорог КАК ТипРасходаСложностьДорог,
	|	упНормыРасходаГСМСрезПоследних.РасходСложностьДорог КАК РасходСложностьДорог,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаНаселенныйПункт КАК ТипРасходаНаселенныйПункт,
	|	упНормыРасходаГСМСрезПоследних.РасходНаселенныйПункт КАК РасходНаселенныйПункт,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаГрузовой КАК ТипРасходаГрузовой,
	|	упНормыРасходаГСМСрезПоследних.РасходГрузовой КАК РасходГрузовой,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаКондиционер КАК ТипРасходаКондиционер,
	|	упНормыРасходаГСМСрезПоследних.РасходКондиционер КАК РасходКондиционер,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаКлиматКонтроль КАК ТипРасходаКлиматКонтроль,
	|	упНормыРасходаГСМСрезПоследних.РасходКлиматКонтроль КАК РасходКлиматКонтроль,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаОтопитель КАК ТипРасходаОтопитель,
	|	упНормыРасходаГСМСрезПоследних.РасходОтопитель КАК РасходОтопитель,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаСпециальноеОборудование КАК ТипРасходаСпециальноеОборудование,
	|	упНормыРасходаГСМСрезПоследних.РасходСпециальноеОборудование КАК РасходСпециальноеОборудование,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаПростой КАК ТипРасходаПростой,
	|	упНормыРасходаГСМСрезПоследних.РасходПростой КАК РасходПростой,
	|	упНормыРасходаГСМСрезПоследних.ВидГСМ КАК ВидГСМ,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	упНормыРасходаГСМСрезПоследних.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ПОМЕСТИТЬ втНадбавкиПоНормеРасходаГСМ
	|ИЗ
	|	втНадбавкиПоВидуГСМ КАК втНадбавкиПоВидуГСМ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)
	|		И ОбъектНормРасхода = &ОбъектНормРасхода) КАК упНормыРасходаГСМСрезПоследних
	|	ПО втНадбавкиПоВидуГСМ.ВидГСМ = упНормыРасходаГСМСрезПоследних.ВидГСМ
	|ГДЕ ИСТИНА
	|	И втНадбавкиПоВидуГСМ.ТипРасходаЗимний ЕСТЬ NULL
	|	И &Подразделение <> ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	втНадбавкиПоВидуГСМ.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	втНадбавкиПоВидуГСМ.РасходЗимний КАК РасходЗимний,
	|	втНадбавкиПоВидуГСМ.ТипРасходаГорнаяМестность КАК ТипРасходаГорнаяМестность,
	|	втНадбавкиПоВидуГСМ.РасходГорнаяМестность КАК РасходГорнаяМестность,
	|	втНадбавкиПоВидуГСМ.ТипРасходаСложностьДорог КАК ТипРасходаСложностьДорог,
	|	втНадбавкиПоВидуГСМ.РасходСложностьДорог КАК РасходСложностьДорог,
	|	втНадбавкиПоВидуГСМ.ТипРасходаНаселенныйПункт КАК ТипРасходаНаселенныйПункт,
	|	втНадбавкиПоВидуГСМ.РасходНаселенныйПункт КАК РасходНаселенныйПункт,
	|	втНадбавкиПоВидуГСМ.ТипРасходаГрузовой КАК ТипРасходаГрузовой,
	|	втНадбавкиПоВидуГСМ.РасходГрузовой КАК РасходГрузовой,
	|	втНадбавкиПоВидуГСМ.ТипРасходаКондиционер КАК ТипРасходаКондиционер,
	|	втНадбавкиПоВидуГСМ.РасходКондиционер КАК РасходКондиционер,
	|	втНадбавкиПоВидуГСМ.ТипРасходаКлиматКонтроль КАК ТипРасходаКлиматКонтроль,
	|	втНадбавкиПоВидуГСМ.РасходКлиматКонтроль КАК РасходКлиматКонтроль,
	|	втНадбавкиПоВидуГСМ.ТипРасходаОтопитель КАК ТипРасходаОтопитель,
	|	втНадбавкиПоВидуГСМ.РасходОтопитель КАК РасходОтопитель,
	|	втНадбавкиПоВидуГСМ.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	втНадбавкиПоВидуГСМ.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	втНадбавкиПоВидуГСМ.ТипРасходаСпециальноеОборудование КАК ТипРасходаСпециальноеОборудование,
	|	втНадбавкиПоВидуГСМ.РасходСпециальноеОборудование КАК РасходСпециальноеОборудование,
	|	втНадбавкиПоВидуГСМ.ТипРасходаПростой КАК ТипРасходаПростой,
	|	втНадбавкиПоВидуГСМ.РасходПростой КАК РасходПростой,
	|	втНадбавкиПоВидуГСМ.ВидГСМ КАК ВидГСМ,
	|	втНадбавкиПоВидуГСМ.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	втНадбавкиПоВидуГСМ.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ИЗ
	|	втНадбавкиПоВидуГСМ КАК втНадбавкиПоВидуГСМ
	|ГДЕ НЕ (втНадбавкиПоВидуГСМ.ТипРасходаЗимний ЕСТЬ NULL)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Зимний) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаЗимний КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходЗимний КАК Расход,
	|	0 КАК Порядок
	|ПОМЕСТИТЬ втНадбавки
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходЗимний <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.ГорнаяМестность) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаГорнаяМестность КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходГорнаяМестность КАК Расход,
	|	1 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходГорнаяМестность <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.СложностьДорог) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаСложностьДорог КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходСложностьДорог КАК Расход,
	|	3 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходСложностьДорог <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.НаселенныйПункт) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаНаселенныйПункт КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходНаселенныйПункт КАК Расход,
	|	4 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходНаселенныйПункт <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Грузовой) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаГрузовой КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходГрузовой КАК Расход,
	|	5 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходГрузовой <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Кондиционер) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаКондиционер КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходКондиционер КАК Расход,
	|	6 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходКондиционер <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.КлиматКонтроль) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаКлиматКонтроль КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходКлиматКонтроль КАК Расход,
	|	7 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходКлиматКонтроль <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Отопитель) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаОтопитель КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходОтопитель КАК Расход,
	|	8 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходОтопитель <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.ДлительнаяЭксплуатация) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаДлительнаяЭксплуатация КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходДлительнаяЭксплуатация КАК Расход,
	|	9 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходДлительнаяЭксплуатация <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.СпециальноеОборудование) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаСпециальноеОборудование КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходСпециальноеОборудование КАК Расход,
	|	10 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходСпециальноеОборудование <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Простой) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаПростой КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходПростой КАК Расход,
	|	11 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходПростой <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.РабочаяОперация) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаРабочаяОперация КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходРабочаяОперация КАК Расход,
	|	12 КАК Порядок
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходРабочаяОперация <> 0
	|
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втНадбавки КАК втНадбавкиТ
	|УПОРЯДОЧИТЬ ПО
	|	ВидГСМ,
	|	Порядок
	|";


	Запрос.УстановитьПараметр("Период", 		Период);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Подразделение", 	Подразделение);
	Запрос.УстановитьПараметр("ОбъектНормРасхода", ОбъектНормРасхода);
	Запрос.УстановитьПараметр("тбзГСМ", 		ВидыГСМ);
	
	тбзРезультат = Запрос.Выполнить().Выгрузить();
		
	Возврат тбзРезультат;
		
КонецФункции

// Функция - Возвращает параметры расчета надбавок для нормы расхода видов ГСМ.
//
// Параметры:
//  Период		 - Дата							 - дата получения нормы.
//  Организация	 - СправочникСсылка.Организации		 - организация.
//  Подразделение	 - СправочникСсылка.упСтруктураПредприятия	 - подразделение.
//  ГСМ			 - СправочникСсылка.упГСМ	 - гсм. 									 -
// 
// Возвращаемое значение:
//  Структура - ТаблицаЗначений - таблица с колонками:
//  * ВидГСМ 					- СправочникСсылка.упВидыГСМ 							- вид гсм.
//  * ВидЭксплуатационнойНормы	- ПеречислениеСсылка.упВидыЭксплуатационныхНормРасходаГСМ 	- вид нормы.
//  * ТипРасхода 				- ПеречислениеСсылка.упСпособыРасчетаНадбавокГСМ  		- способ расчета надбавки(% от расхода, л/час, л/км).
//  * Расход 					- Число 											- количественный показатель расхода.
//
Функция НадбавкиНормыРасходаГСМПоДопОборудованию(Период, Организация, Подразделение, ГСМ) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГСМ.ВидГСМ КАК ВидГСМ,
	|	втГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование
	|ПОМЕСТИТЬ втГСМ
	|ИЗ
	|	&тбзГСМ КАК втГСМ
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	упНормыРасходаГСМСрезПоследних.РасходЗимний КАК РасходЗимний,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	втГСМТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втГСМТ.ВидГСМ КАК ВидГСМ,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	упНормыРасходаГСМСрезПоследних.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ПОМЕСТИТЬ втНадбавкиПоВидуГСМ
	|ИЗ
	|	втГСМ КАК втГСМТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Подразделение = &Подразделение) КАК упНормыРасходаГСМСрезПоследних
	|	ПО ИСТИНА
	|		И втГСМТ.ВидГСМ = упНормыРасходаГСМСрезПоследних.ВидГСМ
	|		И втГСМТ.ДополнительноеОборудование = упНормыРасходаГСМСрезПоследних.ОбъектНормРасхода
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	упНормыРасходаГСМСрезПоследних.РасходЗимний КАК РасходЗимний,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	упНормыРасходаГСМСрезПоследних.ВидГСМ КАК ВидГСМ,
	|	втНадбавкиПоВидуГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	упНормыРасходаГСМСрезПоследних.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	упНормыРасходаГСМСрезПоследних.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ПОМЕСТИТЬ втНадбавкиПоНормеРасходаГСМ
	|ИЗ
	|	втНадбавкиПоВидуГСМ КАК втНадбавкиПоВидуГСМ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)) КАК упНормыРасходаГСМСрезПоследних
	|	ПО ИСТИНА
	|		И втНадбавкиПоВидуГСМ.ВидГСМ = упНормыРасходаГСМСрезПоследних.ВидГСМ
	|		И втНадбавкиПоВидуГСМ.ДополнительноеОборудование = упНормыРасходаГСМСрезПоследних.ОбъектНормРасхода
	|ГДЕ ИСТИНА
	|	И втНадбавкиПоВидуГСМ.ТипРасходаЗимний ЕСТЬ NULL
	|	И &Подразделение <> ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	втНадбавкиПоВидуГСМ.ТипРасходаЗимний КАК ТипРасходаЗимний,
	|	втНадбавкиПоВидуГСМ.РасходЗимний КАК РасходЗимний,
	|	втНадбавкиПоВидуГСМ.ТипРасходаДлительнаяЭксплуатация КАК ТипРасходаДлительнаяЭксплуатация,
	|	втНадбавкиПоВидуГСМ.РасходДлительнаяЭксплуатация КАК РасходДлительнаяЭксплуатация,
	|	втНадбавкиПоВидуГСМ.ВидГСМ КАК ВидГСМ,
	|	втНадбавкиПоВидуГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втНадбавкиПоВидуГСМ.ТипРасходаРабочаяОперация КАК ТипРасходаРабочаяОперация,
	|	втНадбавкиПоВидуГСМ.РасходРабочаяОперация КАК РасходРабочаяОперация
	|ИЗ
	|	втНадбавкиПоВидуГСМ КАК втНадбавкиПоВидуГСМ
	|ГДЕ НЕ (втНадбавкиПоВидуГСМ.ТипРасходаЗимний ЕСТЬ NULL)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.Зимний) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаЗимний КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходЗимний КАК Расход,
	|	0 КАК Порядок,
	|	втНадбавкиПоНормеРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование
	|ПОМЕСТИТЬ втНадбавки
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходЗимний <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.ДлительнаяЭксплуатация) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаДлительнаяЭксплуатация КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходДлительнаяЭксплуатация КАК Расход,
	|	9 КАК Порядок,
	|	втНадбавкиПоНормеРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходДлительнаяЭксплуатация <> 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНадбавкиПоНормеРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	ЗНАЧЕНИЕ(Перечисление.упВидыЭксплуатационныхНормРасходаГСМ.РабочаяОперация) КАК ВидЭксплуатационнойНормы,
	|	втНадбавкиПоНормеРасходаГСМ.ТипРасходаРабочаяОперация КАК ТипРасхода,
	|	втНадбавкиПоНормеРасходаГСМ.РасходРабочаяОперация КАК Расход,
	|	12 КАК Порядок,
	|	втНадбавкиПоНормеРасходаГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование
	|ИЗ
	|	втНадбавкиПоНормеРасходаГСМ КАК втНадбавкиПоНормеРасходаГСМ
	|ГДЕ втНадбавкиПоНормеРасходаГСМ.РасходРабочаяОперация <> 0
	|
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втНадбавки КАК втНадбавкиТ
	|УПОРЯДОЧИТЬ ПО
	|	ВидГСМ,
	|	Порядок
	|";

	Запрос.УстановитьПараметр("Период", 		Период);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Подразделение", 	Подразделение);
	Запрос.УстановитьПараметр("тбзГСМ", 		ГСМ);
	
	тбзРезультат = Запрос.Выполнить().Выгрузить();
		
	Возврат тбзРезультат;
		
КонецФункции

#КонецОбласти

// Функция - Масса прицепного состава транспортного средства
//
// Параметры:
//  Период			 - Дата	 							- дата на которую определяется состав прицепов.
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства	- транспортное средство по которому определяется состав прицепов.
// 
// Возвращаемое значение:
//  Число - масса прицепного состава
//
Функция МассаПрицепногоСоставаТранспортногоСредства(Период, ТранспортноеСредство) Экспорт

	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|		КОГДА упПрицепы.Прицеп.СнаряженнаяМасса <> 0
	|			ТОГДА упПрицепы.Прицеп.СнаряженнаяМасса
	|		КОГДА упПрицепы.Прицеп.ТипТС.СнаряженнаяМасса <> 0
	|			ТОГДА упПрицепы.Прицеп.ТипТС.СнаряженнаяМасса
	|		ИНАЧЕ 0
	|	КОНЕЦ), 0) КАК СнаряженнаяМасса
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		ТранспортноеСредство = &ТранспортноеСредство) КАК упПрицепы
	|ГДЕ упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат = Выборка.СнаряженнаяМасса;	
	КонецЦикла; 
	
	Возврат Результат;
			
КонецФункции

#КонецОбласти

#КонецОбласти

