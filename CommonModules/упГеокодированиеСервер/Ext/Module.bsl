
#Область ГеоКодированиеГеографическойЗоны

// Функция выполняет получение параметры обращения к геосервису.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Структура - Парамерты результата обработки.
//
Функция ПолучитьПараметрыГеоКодирования() Экспорт
	
	Структура = Новый Структура;
	
	упИспользуетсяГеокодированиеАдресов   = упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользуетсяГеокодированиеАдресов");
	упТипГеокодера						  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипГеокодера");
	упАдресСервераГеокодированияNominatim = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераГеокодированияNominatim");
	упАдресСервераГеокодирования		  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераГеокодирования");
	упПределОтветовГеосервиса			  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПределОтветовГеосервиса");
	упКлючСервисаГеоКодирования			  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючСервисаГеоКодирования");
	упГеокодироватьБезАдресныхСокращений  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упГеокодироватьБезАдресныхСокращений");
	упАдресСервераГеокодированияСитиГИД   = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераГеокодированияСитиГИД");
	
	Структура.Вставить("Использовать",  упИспользуетсяГеокодированиеАдресов);
	Структура.Вставить("АдрСокращения", упГеокодироватьБезАдресныхСокращений); // выполнять упрощения текстового представления адреса
	Структура.Вставить("Ключ",		    упКлючСервисаГеоКодирования);
	Структура.Вставить("Тип",		    упТипГеокодера);
	Структура.Вставить("Nominatim",	    ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Nominatim"));
	Структура.Вставить("AxelotMaps",    ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.AxelotMaps"));
	Структура.Вставить("Google",	    ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Google"));
	Структура.Вставить("Яндекс",	    ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Яндекс"));
	Структура.Вставить("СитиГИД",	    ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.СитиГИД"));
	Структура.Вставить("ПустаяСсылка",  ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.ПустаяСсылка"));
	
	
	АдресСервера = "";
	Если упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.AxelotMaps") Тогда
		АдресСервера = ПолучитьАдресСервераAxelotMaps();
	ИначеЕсли упТипГеокодера = ПредопределенноеЗначение("Перечисление.упТипыГеокодеров.Nominatim") Тогда
		АдресСервера = упАдресСервераГеокодированияNominatim;
	КонецЕсли;
	
	Структура.Вставить("АдресСервера",	       АдресСервера);
	Структура.Вставить("СерверГеокодирования", упАдресСервераГеокодирования);
	Структура.Вставить("КоличествоОтветов",    упПределОтветовГеосервиса);
	Структура.Вставить("СерверГеокодированияПрямого_СитиГИД", упАдресСервераГеокодированияСитиГИД);
	
	ТекущийКлюч = упМаршрутизация.ПолучитьТекстКлючаAPI(упКлючСервисаГеоКодирования,упТипГеокодера);
	
	Структура.Вставить("ТекстКлючаAPI", ТекущийКлюч);
	
	Возврат Структура;
	
КонецФункции // ПолучитьПараметрыГеоКодирования()

// Функция производит обращение к сервису геокодирования.
//
// Параметры:
//  СтрокаПоиска - Строка - Текстовое представление поиска.
//
// Возвращаемое значение:
//   Массив - результат обработки.
//
Функция ПолучитьПолигоныГеографическойЗоны(СтрокаПоиска) Экспорт
	
	Возврат упГеокодированиеКлиентСервер.ПолучитьПолигоныГеографическойЗоны(СтрокаПоиска);
	
КонецФункции // ПолучитьПолигоныГеографическойЗоны()

#КонецОбласти

#Область ГеоКодированиеАдреса

// Получим параметры для формирования HTTP запроса к сервису геокодирования.
//
// Параметры:
//  ПрямоеГеокодирование  - Булево - вариант геокодирования прямое (Истина) или обратное (Ложь).
//  СтрокаПоиска  - Строка - Для прямого геокодирования, укажите текст поиска.
//  Координаты  - Структура - Для обратного геокодирования, укажите структуру с ключами Широта, Долгота.
//
// Возвращаемое значение:
//   Структура   - Собираем данные для формирования запроса.
//
Функция ПолучитьТекстHTTPЗапросГеокодеру(ПрямоеГеокодирование,СтрокаПоиска = Неопределено,Координаты = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = упГеокодированиеСервер.ПолучитьПараметрыГеоКодирования();
	
	сткПоиска = СтрокаПоиска;
	Если ПрямоеГеокодирование Тогда
		стрПоиска = сткПоиска.Представление;		
	КонецЕсли; 
	
	Если Результат.Использовать Тогда
		
		ТипГеокодера = Результат.Тип;
		Ключ = Результат.Ключ;
		АдресСервера = Результат.АдресСервера;
		ТекущийКлюч = Результат.ТекстКлючаAPI;
		
		Если НЕ ПрямоеГеокодирование И НЕ Координаты = Неопределено Тогда			
			Широта = упГеокодированиеКлиентСервер.ПредставлениеКоординаты(Координаты.Широта);
			Долгота = упГеокодированиеКлиентСервер.ПредставлениеКоординаты(Координаты.Долгота);
		КонецЕсли;		
		
		Если ТипГеокодера = Результат.Яндекс Тогда
			Если ПрямоеГеокодирование Тогда				
				ТекстЗапроса = "https://geocode-maps.yandex.ru/1.x/?geocode=" + стрПоиска + "&format=xml&results=" + Формат(Результат.КоличествоОтветов,"ЧН=1; ЧГ=0") + "&lang=ru_RU" + ТекущийКлюч;
				Результат.Вставить("Текст",ТекстЗапроса);
			ИначеЕсли НЕ Координаты = Неопределено Тогда
				ТекстЗапроса = "https://geocode-maps.yandex.ru/1.x/?geocode=" + Долгота + "," + Широта + "&format=xml&results=" + Формат(Результат.КоличествоОтветов,"ЧН=1; ЧГ=0") + "&lang=ru_RU" + ТекущийКлюч;
				Результат.Вставить("Текст",ТекстЗапроса);
			КонецЕсли;
		ИначеЕсли НЕ ПустаяСтрока(АдресСервера) И ТипГеокодера = Результат.Nominatim Тогда
			Если ПрямоеГеокодирование Тогда
				
				Если Результат.АдрСокращения Тогда
					Если СтрокаПоиска.Свойство("Страна") И НЕ ПустаяСтрока(СтрокаПоиска.Страна)Тогда
						// исключить наименование страны, представление может не совпадать.
						стрПоиска = СтрЗаменить(НРег(стрПоиска),НРег(СтрокаПоиска.Страна),"")
					КонецЕсли;				
					стрПоиска = ВыполнитьОбработкуСтрокиПоиска(стрПоиска);
				КонецЕсли;	
				
				ТекстЗапроса = АдресСервера + "search?q=" + стрПоиска + "&format=xml&addressdetails=0&limit=" + Формат(Результат.КоличествоОтветов,"ЧН=1; ЧГ=0") + "&accept-language=ru_RU";
				Если ТипЗнч(СтрокаПоиска) = Тип("Структура") Тогда
					ПарамертыПоиска = "";
					Если СтрокаПоиска.Свойство("Дом") И НЕ ПустаяСтрока(СтрокаПоиска.Дом) И СтрокаПоиска.Свойство("Улица") И НЕ ПустаяСтрока(СтрокаПоиска.Улица) Тогда
						Если СтрокаПоиска.Свойство("Строение") И ПустаяСтрока(СтрокаПоиска.Строение)Тогда
							ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "street=" + СокрЛП(СтрокаПоиска.Дом) + " " + СокрЛП(СтрокаПоиска.Улица);
						ИначеЕсли СтрокаПоиска.Свойство("Строение") Тогда
							ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "street=" + СокрЛП(СтрокаПоиска.Дом) + " " + СокрЛП(СтрокаПоиска.Строение) + " " + СокрЛП(СтрокаПоиска.Улица);
						КонецЕсли;
					ИначеЕсли СтрокаПоиска.Свойство("Улица") И НЕ ПустаяСтрока(СтрокаПоиска.Улица) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "street=" + СокрЛП(СтрокаПоиска.Улица);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("Город") И НЕ ПустаяСтрока(СтрокаПоиска.Город) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "city=" + СокрЛП(СтрокаПоиска.Город);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("Район") И НЕ ПустаяСтрока(СтрокаПоиска.Район) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "county=" + СокрЛП(СтрокаПоиска.Район);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("Регион") И НЕ ПустаяСтрока(СтрокаПоиска.Регион) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "state=" + СокрЛП(СтрокаПоиска.Регион);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("КодАльфа2") И НЕ ПустаяСтрока(СтрокаПоиска.КодАльфа2) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "countrycodes=" + СокрЛП(СтрокаПоиска.КодАльфа2);
					КонецЕсли;					
					
					Если НЕ ПустаяСтрока(ПарамертыПоиска) Тогда
						ТекстЗапроса_Параметры = АдресСервера + "search?" + ПарамертыПоиска + "&format=xml&addressdetails=0&limit=" + Формат(Результат.КоличествоОтветов,"ЧН=1; ЧГ=0") + "&accept-language=ru_RU";
						Результат.Вставить("Текст_Параметры",ТекстЗапроса_Параметры);
						Результат.Вставить("ПарамертыПоиска",ПарамертыПоиска);
					КонецЕсли;				
				КонецЕсли;
				
				Результат.Вставить("Текст",ТекстЗапроса);
				
				Если СтрЧислоВхождений(НРег(ТекстЗапроса),НРег("nominatim")) Тогда
					Результат.Вставить("ФорматОтвета","XML");
				Иначе
					Результат.Вставить("ФорматОтвета","JSON");					
				КонецЕсли;
				
			ИначеЕсли НЕ Координаты = Неопределено Тогда
				ТекстЗапроса = АдресСервера + "reverse?format=xml&lat=" + Широта + "&lon=" + Долгота + "&zoom=18";
				Результат.Вставить("Текст",ТекстЗапроса);
			КонецЕсли;
		ИначеЕсли ТипГеокодера = Результат.AxelotMaps Тогда
			АдресСервера = ПолучитьАдресСервераAxelotMaps();
			Если ПрямоеГеокодирование Тогда
				
				Если Результат.АдрСокращения Тогда
					Если СтрокаПоиска.Свойство("Страна") И НЕ ПустаяСтрока(СтрокаПоиска.Страна)Тогда
						// исключить наименование страны, представление может не совпадать.
						стрПоиска = СтрЗаменить(НРег(стрПоиска),НРег(СтрокаПоиска.Страна),"")
					КонецЕсли;				
					стрПоиска = ВыполнитьОбработкуСтрокиПоиска(стрПоиска);
				КонецЕсли;	
				
				ТекстЗапроса = АдресСервера + "search?q=" + стрПоиска + "&format=xml&addressdetails=0&limit=" + Формат(Результат.КоличествоОтветов,"ЧН=1; ЧГ=0") + "&accept-language=ru_RU";
				Если ТипЗнч(СтрокаПоиска) = Тип("Структура") Тогда
					ПарамертыПоиска = "";
					Если СтрокаПоиска.Свойство("Дом") И НЕ ПустаяСтрока(СтрокаПоиска.Дом) И СтрокаПоиска.Свойство("Улица") И НЕ ПустаяСтрока(СтрокаПоиска.Улица) Тогда
						Если СтрокаПоиска.Свойство("Строение") И ПустаяСтрока(СтрокаПоиска.Строение)Тогда
							ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "street=" + СокрЛП(СтрокаПоиска.Дом) + " " + СокрЛП(СтрокаПоиска.Улица);
						ИначеЕсли СтрокаПоиска.Свойство("Строение") Тогда
							ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "street=" + СокрЛП(СтрокаПоиска.Дом) + " " + СокрЛП(СтрокаПоиска.Строение) + " " + СокрЛП(СтрокаПоиска.Улица);
						КонецЕсли;
					ИначеЕсли СтрокаПоиска.Свойство("Улица") И НЕ ПустаяСтрока(СтрокаПоиска.Улица) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "street=" + СокрЛП(СтрокаПоиска.Улица);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("Город") И НЕ ПустаяСтрока(СтрокаПоиска.Город) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "city=" + СокрЛП(СтрокаПоиска.Город);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("Район") И НЕ ПустаяСтрока(СтрокаПоиска.Район) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "county=" + СокрЛП(СтрокаПоиска.Район);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("Регион") И НЕ ПустаяСтрока(СтрокаПоиска.Регион) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "state=" + СокрЛП(СтрокаПоиска.Регион);
					КонецЕсли;
					Если СтрокаПоиска.Свойство("КодАльфа2") И НЕ ПустаяСтрока(СтрокаПоиска.КодАльфа2) Тогда
						ПарамертыПоиска = ПарамертыПоиска + ?(ПустаяСтрока(ПарамертыПоиска),"","&") + "countrycodes=" + СокрЛП(СтрокаПоиска.КодАльфа2);
					КонецЕсли;					
					
					Если НЕ ПустаяСтрока(ПарамертыПоиска) Тогда
						ТекстЗапроса_Параметры = АдресСервера + "search?" + ПарамертыПоиска + "&format=xml&addressdetails=0&limit=" + Формат(Результат.КоличествоОтветов,"ЧН=1; ЧГ=0") + "&accept-language=ru_RU";
						Результат.Вставить("Текст_Параметры",ТекстЗапроса_Параметры);
						Результат.Вставить("ПарамертыПоиска",ПарамертыПоиска);
					КонецЕсли;				
				КонецЕсли;
				
				Результат.Вставить("Текст",ТекстЗапроса);
				Результат.Вставить("ФорматОтвета","XML");
				
			ИначеЕсли НЕ Координаты = Неопределено Тогда
				ТекстЗапроса = АдресСервера + "reverse?format=xml&lat=" + Широта + "&lon=" + Долгота + "&zoom=18";
				Результат.Вставить("Текст",ТекстЗапроса);
			КонецЕсли;
			
		ИначеЕсли ТипГеокодера = Результат.Google Тогда
			Если ПрямоеГеокодирование Тогда
				ТекстЗапроса = "https://maps.google.com/maps/api/geocode/xml?address=" + стрПоиска + "&language=ru-RU" + ТекущийКлюч;
				Результат.Вставить("Текст",ТекстЗапроса);
			ИначеЕсли НЕ Координаты = Неопределено Тогда
				ТекстЗапроса = "https://maps.googleapis.com/maps/api/geocode/xml?latlng=" + Широта + "," + Долгота + "&language=ru-RU" + ТекущийКлюч;
				Результат.Вставить("Текст",ТекстЗапроса);
			КонецЕсли;
		ИначеЕсли ТипГеокодера = Результат.СитиГИД Тогда			
			
			Если ПрямоеГеокодирование Тогда			
				
				сткПараметры = Новый Структура;
				сткПараметры.Вставить("accessKey",Ключ);
				сткПараметры.Вставить("address",стрПоиска);
				
				Результат.Вставить("Параметры",сткПараметры);
				Результат.Вставить("ФорматОтвета","JSON");
				
				ЗаголовокHTTP = Новый Соответствие();
				ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
				ЗаголовокHTTP.Вставить("url", "/api/geocode");	
				ЗаголовокHTTP.Вставить("type", "POST");
				ЗаголовокHTTP.Вставить("dataType", "text");
				
				Результат.Вставить("ЗаголовокHTTP",ЗаголовокHTTP);
				Результат.Вставить("Ресурс","/api/geocode");				
				Результат.Вставить("Порт",8081);
				Результат.Вставить("Текст","");
				
			ИначеЕсли НЕ Координаты = Неопределено Тогда
				ТекстЗапроса = Результат.СерверГеокодирования + "/api/GetNearestAddress?lat=" + Широта + "&lon=" + Долгота + "&searchRadius=0.1" + ТекущийКлюч;
				Результат.Вставить("Текст",ТекстЗапроса);
			КонецЕсли;
				
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сервис геокодирования не используется.'"));		
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции // ПолучитьТекстHTTPЗапросГеокодеру()

// Получить строка типов адресных сокращений.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Строка   - текст типов адресных сокращений.
//
Функция ПолучитьТекстАдресныхСокращений()
	
	стрСокращения = "ао;аобл;г;гор;край;обл;об;респ;рес;округ;окр;чувашия;р-н;у;тер;ао;п;г;пгт;рп;кп;дп;с/с;с/а;с/о;волость;п/о;тер;с/п;с/мо;массив;п;аал;аул;волость;высел;г;д;дп;ж/д_будка;ж/д_казарм;ж/д_оп;ж/д_пост;ж/д_рзд;ж/д_ст;заимка;казарма;кп;м;мкр;нп;остров;п;п/р;п/ст;пгт;починок;п/о;промзона;рзд;рп;с;сл;ст;ст-ца;у;х;городок;тер;ж/д_платф;кв-л;арбан;снт;лпх;погост;кордон;автодорога;жилрайон;жилзона;массив;аллея;б-р;въезд;дор;жт;заезд;кв-л;км;кольцо;линия;наб;остров;парк;пер;переезд;пл;пл-ка;проезд;пр-кт;просек;проселок;проулок;сад;сквер;стр;тер;тракт;туп;ул;уч-к;ш;аал;аул;высел;городок;д;ж/д_будка;ж/д_казарм;ж/д_оп;ж/д_пост;ж/д_рзд;ж/д_ст;казарма;м;мкр;нп;платф;п;п/о;п/р;п/ст;полустанок;починок;рзд;с;сл;ст;х;ж/д_платф;арбан;спуск;канал;гск;снт;лпх;проток;коса;вал;ферма;мост;ряды;а/я;берег;просека;протока;бугор;зона;днп;н/п;ф/х;дом;дом;корпус;корп;строение;стр;владение;влд;влад";
	стрСокращения = Нрег(стрСокращения);
	
	Возврат стрСокращения;	
	
КонецФункции // ПолучитьТекстАдресныхСокращений()

// Выполним упрощение исходной строки отфильтруем сокращения.
//
// Параметры:
//  СтрокаПоиска  - Строка - Текст строки упрощения.
//
// Возвращаемое значение:
//   Строка   - текст упрощенной строки.
//
Функция ВыполнитьОбработкуСтрокиПоиска(СтрокаПоиска)
		
	стрСокращения = ПолучитьТекстАдресныхСокращений();
	мсвСокращения = СтрРазделить(стрСокращения,";");
	Результат = СтрокаПоиска;
	
	стрОбработки = Нрег(СтрокаПоиска);
	// Перечислены те элементы которые не надо помещать в результирующий ответ.
	
	стрСимволы = "№;.;"; // знаки препинаний.
	
	мсвСимволы = СтрРазделить(стрСимволы,";");
	
	Для каждого  текСимвол Из мсвСимволы Цикл
		// убрать знаки препинания.
		стрОбработки = СтрЗаменить(стрОбработки,текСимвол," ");
	КонецЦикла;
	// Разобъем исходную строку на массив подстрок,
	// для проверки каждого по отдельности,
	// является ли эта подстрока сокращением.
	мсвАдрес = СтрРазделить(стрОбработки," ");
	
	текРезультат = "";
	
	Для каждого  текЭлемент Из мсвАдрес Цикл
		
		текСтрока = СокрЛП(текЭлемент);
		
		Если ПустаяСтрока(текСтрока) Тогда
			Продолжить;
		КонецЕсли;
		
		стрЧисло = СтрЗаменить(текСтрока,",","");
		Если СтрЧислоВхождений(стрСокращения,стрЧисло) Тогда
			// Есть сокращение выполним сравнение для каждого,
			// элемента масива значений сокращений,
			// если есть совпадение сокращений тогда не,
			// добавляем его в результирующую строку.
			СокращениеНайдено = Ложь;
			Для каждого  текСокращение Из мсвСокращения Цикл
				Если СокрЛП(текСокращение) = стрЧисло Тогда
					СокращениеНайдено = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если СокращениеНайдено Тогда
				текСтрока = СтрЗаменить(текСтрока,текСокращение,"");
			КонецЕсли;			
		КонецЕсли;
		
		Если СтрДлина(текСтрока)=6 Тогда
			// Уберем индекс он не корректно обрабатывается.
			Индекс = 0;
			Попытка
				Индекс = Число(текСтрока);
			Исключение
				Индекс = 0;
			КонецПопытки;
			Если НЕ Индекс = 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		текСтрока = СокрЛП(текСтрока);
		
		текРезультат = текРезультат + текСтрока + ?(ПустаяСтрока(текСтрока) И  Прав(текРезультат,1)=" ",""," ");
	
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(текРезультат) Тогда
		Результат = СокрЛП(текРезультат);		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ВыполнитьОбработкуСтрокиПоиска()

// Добавим счетчик попытки получить данные геосервиса.
//
// Параметры:
//  ТипГеокодера  - ПеречислениеСсылка.упТипыГеокодеров - Тип сервитса геокодирования.
//  ВидОперации  - Булево - Вид операции, Истина прямое, Ложь обратное.
//  УдачноеПодключение  - Булево - Если данные получены от геокодера.
//  ПараметрыОтвета  - Структура - Параметры ответа от сервиса геокодирования.
//
Процедура ДобавитьРезультатОбращенияКСервисуГеоКодирования(ТипГеокодера,ВидОперации,УдачноеПодключение,ПараметрыОтвета = Неопределено) Экспорт
	
	Если ТипЗнч(ПараметрыОтвета)=ТИП("Структура") И ПараметрыОтвета.Свойство("СообщениеОбОшибке")Тогда
		СтрокаПоиска = НРег("Ошибка работы с Интернет");
		Если СтрНайти(НРег(ПараметрыОтвета.СообщениеОбОшибке),СтрокаПоиска) > 0 Тогда
			// Ошибка подключения к интернету.
			// Такой вариант не обрабатываем.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийВидОперации = Перечисления.упВидыОперацийГеокодирования.Обратное;
	
	Если ВидОперации Тогда
		ТекущийВидОперации = Перечисления.упВидыОперацийГеокодирования.Прямое;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущаяДатаОбращения = НачалоДня(ТекущаяДатаСеанса());
	
	Запись = РегистрыСведений.упСтатистикаОбращенийКСервисамГеокодирования.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДатаОбращения;
	Запись.ТипГеокодера = ТипГеокодера;
	Запись.ВидОперации = ТекущийВидОперации;
	Запись.НомерСеанса = НомерСеансаИнформационнойБазы();
	Запись.Прочитать();
	
	Если НЕ Запись.Выбран() Тогда
		Запись.Период = ТекущаяДатаОбращения;
		Запись.ТипГеокодера = ТипГеокодера;
		Запись.ВидОперации = ТекущийВидОперации;
		Запись.НомерСеанса = НомерСеансаИнформационнойБазы();
	КонецЕсли;
	
	Если УдачноеПодключение Тогда
		Запись.КоличествоУспешно = Запись.КоличествоУспешно + 1;
	Иначе
		Запись.КоличествоНеудачно = Запись.КоличествоНеудачно + 1;
	КонецЕсли;
	
	Запись.Записать();

КонецПроцедуры // ДобавитьРезультатОбращенияКСервисуГеоКодирования()

Функция НормализованныйАдрес(Знач Наименование, СекретныйКлючСервисаDaData = "", APIКлючСервисаDaData = "") Экспорт 
	
	Если Не ЗначениеЗаполнено(СекретныйКлючСервисаDaData) Тогда
		СекретныйКлючСервисаDaData = упСервисныеФункции.ПолучитьЗначениеКонстанты("упСекретныйКлючСервисаDaData");
		Если Не ЗначениеЗаполнено(СекретныйКлючСервисаDaData) Тогда
			ТекстСообщения = Нстр(СтрШаблон("ru = 'Не заполнена константа ""%1""'", Метаданные.Константы.упСекретныйКлючСервисаDaData.Представление()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат "";
		КонецЕсли; 
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(APIКлючСервисаDaData) Тогда
		APIКлючСервисаDaData = упСервисныеФункции.ПолучитьЗначениеКонстанты("упAPIКлючСервисаDaData");
		Если Не ЗначениеЗаполнено(APIКлючСервисаDaData) Тогда
			ТекстСообщения = Нстр(СтрШаблон("ru = 'Не заполнена константа ""%1""'", Метаданные.Константы.упAPIКлючСервисаDaData.Представление()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат "";
		КонецЕсли; 
	КонецЕсли; 
	заголовки = Новый Соответствие; 
	заголовки.Вставить("Content-Type" , "application/json"); 
	заголовки.Вставить("Authorization" , "Token " + APIКлючСервисаDaData); 
	заголовки.Вставить("X-Secret" , СекретныйКлючСервисаDaData); 
	запрос = Новый HTTPЗапрос("/api/v2/clean/address", Заголовки); 
	запрос.УстановитьТелоИзСтроки("[""" + Наименование + """]", КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать); 
	
	Попытка
		соединение = Новый HTTPСоединение("dadata.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено),); 
		ответ = Соединение.ОтправитьДляОбработки(запрос); 
		ответСервера = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8); 
		чтение = Новый ЧтениеJSON; 
		чтение.УстановитьСтроку(ответСервера); 
		Результат = ПрочитатьJSON(чтение);
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			ВызватьИсключение Результат.detail;
		КонецЕсли; 
		данные = Результат[0]; 
		чтение.Закрыть(); 
		Наименование = данные.result; 
	Исключение
		ТекстСообщения = Нстр("ru = 'Ошибка вызова сервиса нормализации адреса (Dadata): " + ОписаниеОшибки() + "'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Наименование = "";
	КонецПопытки;
	Возврат Наименование;

КонецФункции

Процедура НормализоватьАдресВЭлементеСправочника(СсылкаАдреса) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    СсылкаАдреса = Справочники.упАдреса.ПустаяСсылка();
	КонецЕсли;
	ОбъектАдрес = СсылкаАдреса.ПолучитьОбъект();
	Попытка
		ОбъектАдрес.Заблокировать();
	Исключение
		ТекстСообщения = Нстр("ru = 'Ошибка обработка объекта """ + СсылкаАдреса + """: " + ОписаниеОшибки() + "'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	НормализованноеНаименование = упГеокодированиеСервер.НормализованныйАдрес(ОбъектАдрес.Наименование);
	Если ЗначениеЗаполнено(НормализованноеНаименование) Тогда
		ОбъектАдрес.НормализованноеНаименование = НормализованноеНаименование;
	КонецЕсли;
	ОбъектАдрес.Записать();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция ищет координаты адреса по представлению.
//
// Параметры:
//  Адрес - СправочникСсылка - Ссылка на объект.
//
// Возвращаемое значение:
//   Структура   - Параметры объекта обрбаотки.
//
Функция НайтиКоординатыАдреса(Адрес) Экспорт
	
	Возврат упГеокодированиеКлиентСервер.НайтиКоординатыАдреса(Адрес);
	
КонецФункции

// Процедура ищет координаты адресов.
//
// Параметры:
//  мсвАдресов  - Массив - Ссылки на элементы.
//
// Возвращаемое значение:
//   Число   - Число обработанных объектов.
//
Функция НайтиКоординатыАдресов(мсвАдресов) Экспорт
	
	Возврат упГеокодированиеКлиентСервер.НайтиКоординатыАдресов(мсвАдресов);
	
КонецФункции

// Функция возвращает наименование адреса.
//
// Параметры:
//  Адрес - СправочникСсылка - Ссылка на объект.
//
// Возвращаемое значение:
//   Структура   - Параметры объекта обрбаотки.
//
Функция ПолучитьПредставлениеАдреса(Адрес) Экспорт
	
	сткДнные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Адрес, "Наименование,ПолноеНаименование,Город,Дом,НаселенныйПункт,Район,Регион,Страна,Строение,ТипСтроения,Улица,Индекс");
	
	Структура = Новый Структура("Город,Дом,НаселенныйПункт,Район,Строение,Улица,Индекс");
	ЗаполнитьЗначенияСвойств(Структура,сткДнные);
	Структура.Вставить("Регион",СокрЛП(сткДнные.Регион));
	Структура.Вставить("Страна",СокрЛП(сткДнные.Страна));
	Структура.Вставить("КодАльфа2",?(ЗначениеЗаполнено(сткДнные.Страна),сткДнные.Страна.КодАльфа2,""));
	
	
	Строение = "";
	Если ЗначениеЗаполнено(сткДнные.ТипСтроения) Тогда
		Строение = Лев(НРег(сткДнные.ТипСтроения),1) + Строка(сткДнные.Строение);
	КонецЕсли;	
	
	Структура.Вставить("Строение",Строение);
	
	Если ЗначениеЗаполнено(сткДнные.ПолноеНаименование) Тогда
		Структура.Вставить("Представление",сткДнные.ПолноеНаименование);
	Иначе
		Структура.Вставить("Представление",сткДнные.ПолноеНаименование);
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции	

// Функция возвращает наименование адреса
//
// Параметры:
//  мсвАдресов  - Массив - Ссылки на элементы.
//
// Возвращаемое значение:
//   Соответствие   - Результат выполнения.
//
Функция ПолучитьПредставленияАдресов(мсвАдресов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упАдреса.Ссылка,
	|	упАдреса.Наименование,
	|	упАдреса.ПолноеНаименование,
	|	упАдреса.Страна.Представление КАК Страна,
	|	упАдреса.Страна.КодАльфа2 КАК КодАльфа2,
	|	упАдреса.Регион.Представление КАК Регион,
	|	упАдреса.НаселенныйПункт,
	|	упАдреса.Город,
	|	упАдреса.Район,
	|	упАдреса.Улица,
	|	упАдреса.Дом,
	|	упАдреса.Строение,
	|	упАдреса.Индекс,
	|	упАдреса.ТипСтроения
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Ссылка В(&Ссылка)";
	Запрос.УстановитьПараметр("Ссылка", мсвАдресов);
	
	соотАдресов = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Структура = Новый Структура("Город,Дом,НаселенныйПункт,Район,Страна,Улица,Индекс");	
		ЗаполнитьЗначенияСвойств(Структура,Выборка);
		Структура.Вставить("Регион",Выборка.Регион);
		Структура.Вставить("Страна",СокрЛП(Выборка.Страна));
		Структура.Вставить("КодАльфа2",СокрЛП(Выборка.КодАльфа2));
		Строение = "";
		Если ЗначениеЗаполнено(Выборка.ТипСтроения) Тогда
			Строение = Лев(НРег(Выборка.ТипСтроения),1) + Строка(Выборка.Строение);
		КонецЕсли;		
		Структура.Вставить("Строение",Строение);
		
		Если ЗначениеЗаполнено(Выборка.ПолноеНаименование) Тогда
			Структура.Вставить("Представление",Выборка.ПолноеНаименование);
		Иначе
			Структура.Вставить("Представление",Выборка.Наименование);
		КонецЕсли;
		соотАдресов.Вставить(Выборка.Ссылка, Структура);
	КонецЦикла;
	
	Возврат соотАдресов;
		
КонецФункции	

// Процедура записывает найденные координаты
//
// Параметры:
//  Адрес - СправочникСсылка - Ссылка на объект.
//  КоординатыАдреса - Структура - Долгота, Широта.
//
Процедура ЗаполнитьКоординатыАдреса(Адрес, КоординатыАдреса) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГеографическийАдресОбъект = Адрес.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств(ГеографическийАдресОбъект, КоординатыАдреса);
	
	ГеографическийАдресОбъект.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Процедура записывает найденные координаты
//
// Параметры:
//  соотАдресов  - Соответствие - Данные обработки.
//
Процедура ЗаполнитьКоординатыАдресов(соотАдресов) Экспорт
	
	Для Каждого текЭлемент Из соотАдресов Цикл 
		Если текЭлемент.Значение <> Неопределено Тогда
			ЗаполнитьКоординатыАдреса(текЭлемент.Ключ, текЭлемент.Значение);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

// Возвращает адрес сервера Axelot Maps 
//
Функция ПолучитьАдресСервераAxelotMaps() Экспорт
	
	Возврат "https://maps.axelot.ru/";
	
КонецФункции


#КонецОбласти 