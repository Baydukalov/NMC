#Область ПрограммныйИнтерфейс

#Область Пересчет

// Пересчет суммы НДС в строке.
//
// Параметры:
//  Строка - Строка - Строка табличной части, Строка данных.
//
Процедура ПересчитатьСуммуНДС(Строка) Экспорт
	
	Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") Тогда 
		Строка.СуммаНДС    = Строка.Сумма * 9 / 50;
		Строка.Сумма	   = Строка.Сумма + Строка.СуммаНДС;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
		Строка.СуммаНДС    = Строка.Сумма * 9 / 59;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20") Тогда 
		Строка.СуммаНДС    = Строка.Сумма / 5;
		Строка.Сумма	   = Строка.Сумма + Строка.СуммаНДС;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
		Строка.СуммаНДС    = Строка.Сумма / 6;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10") Тогда 
		Строка.СуммаНДС    = Строка.Сумма / 10;
		Строка.Сумма	   = Строка.Сумма + Строка.СуммаНДС;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
		Строка.СуммаНДС    = Строка.Сумма / 11;
	Иначе
		Строка.СуммаНДС    = 0;
	КонецЕсли;	
		
КонецПроцедуры

// Пересчет суммы без НДС в строке.
//
// Параметры:
//  Строка - Строка - Строка табличной части, Строка данных.
//
Процедура ПересчитатьСуммуБезНДС(Строка) Экспорт
	
	Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") Тогда 
		Строка.СуммаБезНДС    = Строка.СуммаСНДС * 50 / 59;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
		Строка.СуммаБезНДС    = Строка.СуммаСНДС;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10") Тогда 
		Строка.СуммаБезНДС    = Строка.СуммаСНДС * 10 / 11;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
		Строка.СуммаБезНДС    = Строка.СуммаСНДС;
	Иначе
		Строка.СуммаБезНДС    = Строка.СуммаСНДС;
	КонецЕсли;	
		
КонецПроцедуры

// Пересчет суммы скидки в строке.
//
// Параметры:
//  Строка - Строка - Строка табличной части, Строка данных.
//
Процедура ПересчитатьСуммуСкидки(Строка) Экспорт
	
	Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") 
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
		Строка.СуммаНДС    = Строка.Сумма * 9 / 59;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20")
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
		Строка.СуммаНДС    = Строка.Сумма / 6;	
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10")
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
		Строка.СуммаНДС    = Строка.Сумма / 11;
	Иначе
		Строка.СуммаНДС    = 0;
	КонецЕсли;	
	
	СуммаПлан 		= Строка.Количество * Строка.Цена;
	СуммаСоСкидкой 	= 0;
	
	Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18")
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20")
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10")
	Тогда
		СуммаСоСкидкой = Строка.Сумма - Строка.СуммаНДС;
		
		СуммаСоСкидкой = Строка.Сумма;
	ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118")
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120")
		ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
		
		СуммаСоСкидкой = Строка.Сумма;
	КонецЕсли;
	
	Если СуммаПлан > СуммаСоСкидкой Тогда
		Строка.СкидкаСумма = СуммаПлан - СуммаСоСкидкой;
		Если НЕ СуммаПлан = 0 Тогда
			Строка.СкидкаПроцент	= (Строка.СкидкаСумма / (СуммаПлан)) * 100;
		Иначе
			Строка.СкидкаПроцент	= 100;
		КонецЕсли;
	Иначе
		Строка.СкидкаСумма 		= 0;
		Строка.СкидкаПроцент 	= 0;
	КонецЕсли;	
	
КонецПроцедуры // ПересчитатьСуммуСкидки()

// Пересчет значаений строки в зависимости от изменяемого поля.
//
// Параметры:
//  Строка - Строка - Строка табличной части, Строка данных.
//  ИмяПоля - Строка - Наименование поля табличной части.
//  СтруктураДанных - Структура - Параметры.
//  ТекстОшибки - Строка - Представление ошибки.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура ПересчитатьСтрокуПриИзмененииЗначения(Строка, ИмяПоля, СтруктураДанных = Неопределено, ТекстОшибки = "", Отказ = Ложь) Экспорт
	
	Если ИмяПоля = "Цена" 
			ИЛИ ИмяПоля = "Количество"
			ИЛИ ИмяПоля = "СкидкаПроцент" 
			ИЛИ ИмяПоля = "СтавкаНДС" Тогда
			
		Строка.Сумма		= Строка.Количество * Строка.Цена;
		Строка.СкидкаСумма	= Строка.Сумма * (Строка.СкидкаПроцент / 100);
		Строка.Сумма		= Строка.Сумма - Строка.СкидкаСумма;	
		
		ПересчитатьСуммуНДС(Строка);
		
	ИначеЕсли ИмяПоля = "СкидкаСумма" Тогда		
		
		Строка.Сумма = Строка.Количество * Строка.Цена;
		
		Если Строка.СкидкаСумма > Строка.Сумма Тогда
			ТекстОшибки = НСтр("ru='Сумма скидки:%1 превышает сумму:%2'");	
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Строка.СкидкаСумма, Строка.Сумма);
			
			Если НЕ СтруктураДанных = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				СтруктураДанных.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ПлановыеДоходы", Строка.НомерСтроки, "СкидкаСумма"));	
			КонецЕсли;
			Строка.Сумма = 0;
			Строка.СкидкаПроцент	= 100;
			
			Отказ 		= Истина;	
		КонецЕсли;

		Если НЕ Строка.Сумма = 0 Тогда
			Строка.СкидкаПроцент	= (Строка.СкидкаСумма / Строка.Сумма) * 100;
			Строка.Сумма = Строка.Сумма - Строка.СкидкаСумма;	
		Иначе
			Строка.СкидкаПроцент	= 100;
		КонецЕсли;
		
		ПересчитатьСуммуНДС(Строка);	
				
	ИначеЕсли ИмяПоля = "Сумма" Тогда			
		
		ПересчитатьСуммуСкидки(Строка);
					
	КонецЕсли;
	
	Если НЕ СтруктураДанных = Неопределено И НЕ Отказ Тогда
		ПроверитьПроцентСкидкиПоСоглашению(Строка, СтруктураДанных, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Пересчет значаений строки расходов в зависимости от изменяемого поля.
//
// Параметры:
//  Строка - Строка - Строка табличной части, Строка данных.
//  ИмяПоля - Строка - Наименование поля табличной части.
//  СтруктураДанных - Структура - Параметры.
//  ТекстОшибки - Строка - Представление ошибки.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура ПересчитатьСтрокуРасходовПриИзмененииЗначения(Строка, ИмяПоля, СтруктураДанных = Неопределено, ТекстОшибки = "", Отказ = Ложь) Экспорт
	
	ЕстьСвойствоКоличество = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Строка, "Количество");
	
	Если Ложь
		Или ИмяПоля = "Цена" 
		Или ИмяПоля = "Количество" 
		Или ИмяПоля = "СтавкаНДС" Тогда
			
		Если ЕстьСвойствоКоличество Тогда
			Строка.Сумма		= Строка.Количество * Строка.Цена;	
		КонецЕсли;	
				
		ПересчитатьСуммуНДС(Строка);	
				
	ИначеЕсли ИмяПоля = "Сумма" ИЛИ ИмяПоля = "Стоимость" Тогда			
		
		СуммаСтоимость = Строка.Сумма ;
		
		Если Ложь 
			Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") 
			Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
			
			Строка.СуммаНДС    = Строка.Сумма * 9 / 59;
			
		ИначеЕсли Ложь
			Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20")
			Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда
			
			Строка.СуммаНДС    = Строка.Сумма / 6;
			
		ИначеЕсли Ложь
			Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10")
			Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда
			
			Строка.СуммаНДС    = Строка.Сумма / 11;
			
		Иначе
			Строка.СуммаНДС    = 0;
		КонецЕсли;	
		
		Если ЕстьСвойствоКоличество Тогда
			
			Если Ложь
				Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18")
				Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20")
				Или Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10")
			Тогда
				
				СуммаСтоимость =  (Строка.Сумма - Строка.СуммаНДС) / Строка.Количество;
			КонецЕсли;
			
			Если Строка.Количество > 0 Тогда
				Строка.Цена =  СуммаСтоимость / Строка.Количество;
			Иначе
				Строка.Цена =  0;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
		
КонецПроцедуры

// Проверка скидки по строке со скидкой установленной в соглашении.
//
// Параметры:
//  ТекущаяСтрока - Строка - Строка табличной части, Строка данных.
//  СтруктураДанных - Структура - Параметры.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура ПроверитьПроцентСкидкиПоСоглашению(ТекущаяСтрока, СтруктураДанных, Отказ) Экспорт
	
	Перем Соглашение;
		
	Если СтруктураДанных.Свойство("Соглашение", Соглашение) Тогда
		
		МаксимальныйПроцентСкидки	= упСервисныеФункцииВызовСервера.ПолучитьПроцентСкидкиПоСоглашению(Соглашение);
		
		Если ТекущаяСтрока.СкидкаПроцент > МаксимальныйПроцентСкидки Тогда
			Если упСервисныеФункцииВызовСервера.РазрешеноУвеличиватьСкидкуВышеМаксимальной() Тогда
				ТекстОшибки = НСтр("ru='Процент скидки превышает максимальный процент по соглашению(%1%)'");	
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, сокрлп(МаксимальныйПроцентСкидки));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, СтруктураДанных.Ссылка);	
			Иначе	
				Отказ		= Истина;
				ТекстОшибки = НСтр("ru='Процент скидки превышает максимальный процент по соглашению(%1%), будет установлен максимальный'");	
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, сокрлп(МаксимальныйПроцентСкидки));
				ТекущаяСтрока.СкидкаПроцент = МаксимальныйПроцентСкидки;	
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					СтруктураДанных.Ссылка,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ПлановыеДоходы", ТекущаяСтрока.НомерСтроки, "СкидкаПроцент"));
	КонецЕсли;
	
	
КонецПроцедуры

// Процедура пересчета итоговых сумм по строке.
//
// Параметры:
//  Объект - Строка - Строка табличной части, Строка данных.
//  ИмяТабличнойЧасти - Строка - Наименование табличной части.
//  сткСоответствиеРеквизитов - Соответствие - Структура соостветствия реквизитов.
//  ВедетсяВалютныйУчет - Булево - Флаг ведется учет валют.
//  ПараметрыОтбора - Структура - Параметры.
//
Процедура ПересчитатьИтогиПоСтрокам(Объект, ИмяТабличнойЧасти, сткСоответствиеРеквизитов, ВедетсяВалютныйУчет, ПараметрыОтбора = Неопределено) Экспорт
	
	Если ПараметрыОтбора = Неопределено Тогда
		Если НЕ ВедетсяВалютныйУчет Тогда
			Для каждого сткРеквизит Из сткСоответствиеРеквизитов Цикл
				Объект[сткРеквизит.Ключ] = Объект[ИмяТабличнойЧасти].Итог(сткРеквизит.Значение);
			КонецЦикла;
		Иначе
			Для каждого сткРеквизит Из сткСоответствиеРеквизитов Цикл
				Объект[сткРеквизит.Ключ] = 0;
			КонецЦикла;
			
			Для каждого Строка Из Объект[ИмяТабличнойЧасти] Цикл
				Для каждого сткРеквизит Из сткСоответствиеРеквизитов Цикл
					Если ЗначениеЗаполнено(Строка.Валюта) Тогда
						Объект[сткРеквизит.Ключ] = Объект[сткРеквизит.Ключ] + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка[сткРеквизит.Значение], Объект.Дата);
					Иначе
						Объект[сткРеквизит.Ключ] = Объект[сткРеквизит.Ключ] + Строка[сткРеквизит.Значение];
					КонецЕсли;	
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Для каждого сткРеквизит Из сткСоответствиеРеквизитов Цикл
			Объект[сткРеквизит.Ключ] = 0;
		КонецЦикла;
		
		мсвСтроки  = Объект[ИмяТабличнойЧасти].НайтиСтроки(ПараметрыОтбора);
		
		Если мсвСтроки.Количество() > 0 Тогда
			Для каждого Строка Из мсвСтроки Цикл
				Для каждого сткРеквизит Из сткСоответствиеРеквизитов Цикл
					Если ЗначениеЗаполнено(Строка.Валюта) Тогда
						Объект[сткРеквизит.Ключ] = Объект[сткРеквизит.Ключ] + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка[сткРеквизит.Значение], Объект.Дата);
					Иначе
						Объект[сткРеквизит.Ключ] = Объект[сткРеквизит.Ключ] + Строка[сткРеквизит.Значение];
					КонецЕсли;	
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// Процедура пересчета "Сумма", "СуммаНДС", "СуммаБезНДС" переданной строки.
//
// Параметры:
//  Строка - Строка - Строка табличной части, строка товарного состава.
//
Процедура ТоварныйСоставГрузаРассчитатьСуммы(Строка) Экспорт
	
	Сумма = Строка.Цена * Строка.Количество;
	Если Сумма Тогда 
		Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") Тогда 
			Строка.СуммаНДС    = Сумма * 0.18;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма + Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
			Строка.СуммаНДС    = Сумма * 9 / 59;
			Строка.СуммаБезНДС = Сумма / 1.18;
			Строка.Сумма       = Сумма;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20") Тогда 
			Строка.СуммаНДС    = Сумма / 5;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма + Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
			Строка.СуммаНДС    = Сумма / 6;
			Строка.СуммаБезНДС = Сумма / 1.2;
			Строка.Сумма       = Сумма;	
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10") Тогда 
			Строка.СуммаНДС    = Сумма / 10;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма + Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
			Строка.СуммаНДС    = Сумма / 11;
			Строка.СуммаБезНДС = Сумма / 1.1;
			Строка.Сумма       = Сумма;
		Иначе
			Строка.СуммаНДС    = 0;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма;
		КонецЕсли;	
	Иначе
		Строка.СуммаНДС    = 0;
		Строка.СуммаБезНДС = 0;
		Строка.Сумма       = 0;
	КонецЕсли;	
		
КонецПроцедуры // ТоварныйСоставГрузаРассчитатьСуммы()

#КонецОбласти

#КонецОбласти 