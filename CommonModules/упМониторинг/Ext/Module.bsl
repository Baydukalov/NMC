
#Область ПрограммныйИнтерфейс

// Выполним удаление не актуальных данных спутникового мониторинга.
//
Процедура УдалениеНеактуальныхДанныхСпутниковогоМониторинга() Экспорт
	
	ПериодХраненияСпутниковыхДанных = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПериодХраненияСпутниковыхДанных"); // в месяцах
	Если ПериодХраненияСпутниковыхДанных = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	АктуальнаяДата = ДобавитьМесяц(ТекущаяДатаСеанса(), -ПериодХраненияСпутниковыхДанных);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 200
	|	упДанныеТрекеров.Период,
	|	упДанныеТрекеров.ТранспортноеСредство
	|ИЗ
	|	РегистрСведений.упДанныеТрекеров КАК упДанныеТрекеров
	|ГДЕ
	|	упДанныеТрекеров.Период < &АктуальнаяДата";
	Запрос.УстановитьПараметр("АктуальнаяДата", АктуальнаяДата);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.упДанныеТрекеров.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.ТранспортноеСредство.Установить(Выборка.ТранспортноеСредство);
			НаборЗаписей.Записать();
		КонецЦикла; 
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 200
	|	упДанныеДатчиков.Период,
	|	упДанныеДатчиков.ТранспортноеСредство,
	|	упДанныеДатчиков.Датчик
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков КАК упДанныеДатчиков
	|ГДЕ
	|	упДанныеДатчиков.Период < &АктуальнаяДата";
	Запрос.УстановитьПараметр("АктуальнаяДата", АктуальнаяДата);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.упДанныеДатчиков.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.ТранспортноеСредство.Установить(Выборка.ТранспортноеСредство);
			НаборЗаписей.Отбор.Датчик.Установить(Выборка.Датчик);
			НаборЗаписей.Записать();
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

// Процедура обрабатывает записи регистра "упОбрабатываемыеСобытияМониторинга".
//
Процедура ПоискПроизошедшихСобытий() Экспорт
	
	ИнформационнаяБазаФайловая = ?(ОбщегоНазначения.ИнформационнаяБазаФайловая()=0,ЛОЖЬ,ИСТИНА);
	
	КоличествоПотоков = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковОбработкиСобытий");
	Если КоличествоПотоков = 0 ИЛИ ИнформационнаяБазаФайловая Тогда 
		КоличествоПотоков = 1;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упТипыСобытийМониторингаТранспортныеСредства.Ссылка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ упТипыСобытийМониторингаТранспортныеСредства.ТранспортноеСредство) КАК КоличествоТС
	|ПОМЕСТИТЬ втТСтипСоб
	|ИЗ
	|	Справочник.упТипыСобытийМониторинга.ТранспортныеСредства КАК упТипыСобытийМониторингаТранспортныеСредства
	|ГДЕ
	|	упТипыСобытийМониторингаТранспортныеСредства.Ссылка.Активно
	|
	|СГРУППИРОВАТЬ ПО
	|	упТипыСобытийМониторингаТранспортныеСредства.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упТипыСобытийМониторинга.Ссылка,
	//|	упТипыСобытийМониторинга.ТранспортныеСредства.(
	//|		ТранспортноеСредство
	//|	),
	|	упТипыСобытийМониторинга.Зона,
	|	упТипыСобытийМониторинга.ВидСобытия,
	|	ЕСТЬNULL(втТСтипСоб.КоличествоТС, 0) КАК КоличествоТС,
	|	упТипыСобытийМониторинга.ЧастотаОповещения,
	|	упТипыСобытийМониторинга.ВремяОстановки,
	|	упТипыСобытийМониторинга.Погрешность,
	|	упТипыСобытийМониторинга.ПогрешностьВремя,
	|	упТипыСобытийМониторинга.Датчик,
	|	ВЫБОР
	|		КОГДА упТипыСобытийМониторинга.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.упВидыСобытийМониторинга.ЗначениеДатчика)
	|				ИЛИ упТипыСобытийМониторинга.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.упВидыСобытийМониторинга.ИзмененияЗначенияДатчика)
	|			ТОГДА упТипыСобытийМониторинга.Датчик.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.ПустаяСсылка)
	|	КОНЕЦ КАК НазначениеДатчика,
	|	упТипыСобытийМониторинга.ВидСравнения,
	|	упТипыСобытийМониторинга.ЗначениеДатчика,
	|	упТипыСобытийМониторинга.Дельта,
	|	упТипыСобытийМониторинга.ТипИзменения,
	|	упТипыСобытийМониторинга.ПериодИзменения,
	|	упТипыСобытийМониторинга.Расстояние,
	|	упДельтаОтклоненияОтТочкиМаршрута.Значение КАК ДельтаОтклоненияОтТочкиМаршрутаМетры,
	|	упТипыСобытийМониторинга.НеФиксироватьСобытиеВТочкеМаршрута,
	|	упТипыСобытийМониторинга.СоздаватьПрохождениеТочки,
	|	упШиротаДляРасчетаКоличестваМетровВГрадусе.Значение КАК ШиротаДляРасчета
	|ИЗ
	|	Справочник.упТипыСобытийМониторинга КАК упТипыСобытийМониторинга
	|		ПОЛНОЕ СОЕДИНЕНИЕ Константа.упДельтаОтклоненияОтТочкиМаршрута КАК упДельтаОтклоненияОтТочкиМаршрута
	|		ПО (ИСТИНА)
	|		ПОЛНОЕ СОЕДИНЕНИЕ Константа.упШиротаДляРасчетаКоличестваМетровВГрадусе КАК упШиротаДляРасчетаКоличестваМетровВГрадусе
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТСтипСоб КАК втТСтипСоб
	|		ПО упТипыСобытийМониторинга.Ссылка = втТСтипСоб.Ссылка
	|ГДЕ
	|	упТипыСобытийМониторинга.Активно
	|	И ВЫБОР
	|			КОГДА упТипыСобытийМониторинга.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.упВидыСобытийМониторинга.ЗначениеДатчика)
	|					ИЛИ упТипыСобытийМониторинга.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.упВидыСобытийМониторинга.ИзмененияЗначенияДатчика)
	|				ТОГДА упТипыСобытийМониторинга.Датчик <> ЗНАЧЕНИЕ(Справочник.упДатчики.ПустаяСсылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И (&ИспользуетсяАндроидКлиент
	|			ИЛИ упТипыСобытийМониторинга.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.упВидыСобытийМониторинга.СобытиеAndroidКлиента))
	|
	|УПОРЯДОЧИТЬ ПО
	|	упТипыСобытийМониторинга.ВидСобытия.Порядок";
	Запрос.УстановитьПараметр("ИспользуетсяАндроидКлиент", ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент"));
	текРезультат = Запрос.Выполнить();
	
	текДата = ТекущаяДатаСеанса();
	ПустаяДата = Дата("00010101000000");
	
	мсвПотоков = Новый Массив(КоличествоПотоков);
	
	тбзТипыСобытий = Новый ТаблицаЗначений;
	тбзТипыСобытий.Колонки.Добавить("Ссылка");
	тбзТипыСобытий.Колонки.Добавить("ВидСобытия");
	тбзТипыСобытий.Колонки.Добавить("Зона");
	тбзТипыСобытий.Колонки.Добавить("КоличествоТС");
	
	тбзТипыСобытий.Колонки.Добавить("ЧастотаОповещения");
	//тбзТипыСобытий.Колонки.Добавить("ТранспортныеСредства");
	тбзТипыСобытий.Колонки.Добавить("ВремяОстановки");
	тбзТипыСобытий.Колонки.Добавить("Погрешность");
	тбзТипыСобытий.Колонки.Добавить("ПогрешностьВремя");
	
	тбзТипыСобытий.Колонки.Добавить("Датчик");
	тбзТипыСобытий.Колонки.Добавить("НазначениеДатчика");
	тбзТипыСобытий.Колонки.Добавить("ВидСравнения");
	тбзТипыСобытий.Колонки.Добавить("ЗначениеДатчика");
	
	тбзТипыСобытий.Колонки.Добавить("Дельта");
	тбзТипыСобытий.Колонки.Добавить("ТипИзменения");
	тбзТипыСобытий.Колонки.Добавить("ПериодИзменения");
	
	тбзТипыСобытий.Колонки.Добавить("Расстояние");		
	
	тбзТипыСобытий.Колонки.Добавить("ДельтаОтклоненияОтТочкиМаршрутаМетры");
	тбзТипыСобытий.Колонки.Добавить("НеФиксироватьСобытиеВТочкеМаршрута");
	тбзТипыСобытий.Колонки.Добавить("СоздаватьПрохождениеТочки");	
	тбзТипыСобытий.Колонки.Добавить("ШиротаДляРасчета");
	
	Если КоличествоПотоков = 1 Тогда
		мсвПотоков[0] = тбзТипыСобытий;
	Иначе
		КрайнийИндекс = мсвПотоков.ВГраница();
		Для Индекс = 0 по КрайнийИндекс Цикл
			мсвПотоков[Индекс] = тбзТипыСобытий.Скопировать();
		КонецЦикла;
	КонецЕсли;
	
	текВыборка = текРезультат.Выбрать();
	
	Сч = 0;
	
	Пока текВыборка.Следующий() Цикл 
		
		Попытка
			НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упОбрабатываемыеСобытияМониторинга");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("ТипСобытия", текВыборка.Ссылка);
			
			Блокировка.Заблокировать();
			
			текЗапись = РегистрыСведений.упОбрабатываемыеСобытияМониторинга.СоздатьМенеджерЗаписи();
			текЗапись.ТипСобытия = текВыборка.Ссылка;
			текЗапись.Прочитать();
			
			Если текЗапись.Выбран() Тогда
				Если текЗапись.СтатусОтработки = Перечисления.упСтатусыОбработкиСобытийМониторинга.Обрабатывается Тогда 
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			текЗапись.ТипСобытия		  = текВыборка.Ссылка;
			текЗапись.ДатаНачалаОбработки = текДата;
			текЗапись.СтатусОтработки	  = Перечисления.упСтатусыОбработкиСобытийМониторинга.Обрабатывается;
			текЗапись.ДатаОтработки		  = ПустаяДата;
			текЗапись.Записать();
			
			НовоеСобытие = мсвПотоков[Сч].Добавить();
			ЗаполнитьЗначенияСвойств(НовоеСобытие, текВыборка);
			
			Сч = Сч + 1;
			Если Сч = КоличествоПотоков Тогда 
				Сч = 0;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			Если ТранзакцияАктивна() Тогда 
				ОтменитьТранзакцию();
			КонецЕсли;
			// Ничего страшного - обработаем в следующий раз.
		КонецПопытки;
		
	КонецЦикла;	
	
	Если ИнформационнаяБазаФайловая Тогда
		Если мсвПотоков.Количество() Тогда
			СоздатьПотокОбработкиМониторингаСобытий(мсвПотоков[0]);
		КонецЕсли;
	Иначе
		Для Каждого текЭлемент Из мсвПотоков Цикл
			Если текЭлемент.Количество() Тогда 
				мсвПараметров = Новый Массив(1);
				мсвПараметров[0] = текЭлемент;
				упУправлениеФоновымиЗадачами.ВыполнитьФоновоеЗадание("упМониторинг.СоздатьПотокОбработкиМониторингаСобытий", мсвПараметров,,,"ПоискПроизошедшихСобытийМониторинга");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//СоздатьПотокОбработкиМониторингаСобытий(мсвПотоков[0]);
	
КонецПроцедуры // ПоискПроизошедшихСобытий()

// Выполним создание разных потоков обработки.
//
// Параметры:
//  тбзТипыСобытий - ТаблицЗаначений - Таблица с данными для обработки.
//
Процедура СоздатьПотокОбработкиМониторингаСобытий(тбзТипыСобытий) Экспорт
	
	Сеанс = НомерСеансаИнформационнойБазы();
	Дата  = ТекущаяДатаСеанса();
	Для Каждого текТипСобытия Из тбзТипыСобытий Цикл 
		текЗапись = РегистрыСведений.упОбрабатываемыеСобытияМониторинга.СоздатьМенеджерЗаписи();
		текЗапись.ТипСобытия = текТипСобытия.Ссылка;
		текЗапись.Прочитать();          
		Если текЗапись.Выбран() Тогда
			текЗапись.ТипСобытия		  = текТипСобытия.Ссылка;
			текЗапись.СтатусОтработки	  = Перечисления.упСтатусыОбработкиСобытийМониторинга.Обрабатывается;
			текЗапись.НомерСеанса		  = Сеанс;
			текЗапись.ДатаНачалаОбработки = Дата;
			текЗапись.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого текТипСобытия Из тбзТипыСобытий Цикл 
		ОбработатьТипыСобытий(текТипСобытия);
	КонецЦикла;
	
КонецПроцедуры

// Выполним обработку каждого типа события.
//
// Параметры:
//  ВыборкаТипыСобытий  - Выборка запроса - Выборка.
//
Процедура ОбработатьТипыСобытий(ВыборкаТипыСобытий)		
	
	упСЛК.СоздатьОбъект().ОбработатьТипыСобытий(ВыборкаТипыСобытий);
	
КонецПроцедуры

#Область ВычислитьРасстояниеПоДанным

// Выполнить подсчет расстояния по данным трекера ТС за укзанный период.
//
// Параметры:
//  НачалоПериода  - Дата - Начало периода вычисления.
//  ОкончаниеПериода  - Дата - Окончание периода вычисления.
//  ТранспортноеСредство  - СправочникСсылка.упТранспортныеСредства - Ссылка на ТС для которового проводим вычисление.
//
// Возвращаемое значение:
//   Число - Значение в метрах пройденного расстояния.
//
Функция упВычислитьРасстояниеПоДаннымТрекеров(НачалоПериода, ОкончаниеПериода, ТранспортноеСредство)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ (ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(ОкончаниеПериода) И ЗначениеЗаполнено(ТранспортноеСредство)) Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упДанныеТрекеров.Период КАК Период,
	|	упДанныеТрекеров.Широта,
	|	упДанныеТрекеров.Долгота
	|ИЗ
	|	РегистрСведений.упДанныеТрекеров КАК упДанныеТрекеров
	|ГДЕ
	|	упДанныеТрекеров.ТранспортноеСредство = &ТС
	|	И упДанныеТрекеров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Запрос.УстановитьПараметр("КонецПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ТС", ТранспортноеСредство);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПредыдущаяТочка = Неопределено;
	Пока Выборка.Следующий() Цикл
		Если НЕ ПредыдущаяТочка = Неопределено Тогда
			ТекущаяДлинна = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(ПредыдущаяТочка.Широта,ПредыдущаяТочка.Долгота,Выборка.Широта,Выборка.Долгота);
			Попытка
				Результат = Результат + ТекущаяДлинна;
			Исключение
				// Продолжим вычисление.
			КонецПопытки;
		КонецЕсли;
		ПредыдущаяТочка = Новый Структура("Широта,Долгота",0,0);
		ЗаполнитьЗначенияСвойств(ПредыдущаяТочка,Выборка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // упВычислитьРасстояниеПоДаннымТрекеров()

// Выполнить подсчет расстояния по данным датчиков ТС за укзанный период.
//
// Параметры:
//  НачалоПериода  - Дата - Начало периода вычисления.
//  ОкончаниеПериода  - Дата - Окончание периода вычисления.
//  ТранспортноеСредство  - СправочникСсылка.упТранспортныеСредства - Ссылка на ТС для которового проводим вычисление.
//
// Возвращаемое значение:
//   Число - Значение в метрах пройденного расстояния.
//
Функция упВычислитьРасстояниеПоДаннымДатчиков(НачалоПериода, ОкончаниеПериода, ТранспортноеСредство)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ (ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(ОкончаниеПериода) И ЗначениеЗаполнено(ТранспортноеСредство)) Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упДатчики.Ссылка
	|ПОМЕСТИТЬ вт_Датчики
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|ГДЕ
	|	упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Одометр)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Датчики.Ссылка,
	|	упДанныеДатчиковСрезПервых.Период КАК ПериодН,
	|	упДанныеДатчиковСрезПоследних.Период КАК ПериодК,
	|	упДанныеДатчиковСрезПервых.Значение КАК ЗначениеН,
	|	упДанныеДатчиковСрезПоследних.Значение КАК ЗначениеК,
	|	ВЫБОР
	|		КОГДА упДанныеДатчиковСрезПоследних.Значение ЕСТЬ NULL
	|				ИЛИ упДанныеДатчиковСрезПервых.Значение ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ упДанныеДатчиковСрезПоследних.Значение - упДанныеДатчиковСрезПервых.Значение
	|	КОНЕЦ КАК Разность
	|ПОМЕСТИТЬ вт_Данные
	|ИЗ
	|	вт_Датчики КАК вт_Датчики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков.СрезПервых(&НачалоПериода, ТранспортноеСредство = &ТС) КАК упДанныеДатчиковСрезПервых
	|		ПО вт_Датчики.Ссылка = упДанныеДатчиковСрезПервых.Датчик
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков.СрезПоследних(&КонецПериода, ТранспортноеСредство = &ТС) КАК упДанныеДатчиковСрезПоследних
	|		ПО вт_Датчики.Ссылка = упДанныеДатчиковСрезПоследних.Датчик
	|ГДЕ
	|	ВЫБОР
	|			КОГДА упДанныеДатчиковСрезПервых.ТранспортноеСредство ЕСТЬ NULL
	|					И упДанныеДатчиковСрезПоследних.ТранспортноеСредство ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Данные.Ссылка КАК Датчик,
	|	МАКСИМУМ(вт_Данные.Разность) КАК Разность
	|ИЗ
	|	вт_Данные КАК вт_Данные
	|ГДЕ
	|	ВЫБОР
	|			КОГДА вт_Данные.Разность > 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Данные.Ссылка";
	
	Запрос.УстановитьПараметр("КонецПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ТС", ТранспортноеСредство);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Разность;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // упВычислитьРасстояниеПоДаннымДатчиков()


#КонецОбласти

#КонецОбласти
