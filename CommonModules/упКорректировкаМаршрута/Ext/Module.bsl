#Область ФормированиеДвижений

// Процедура формирует движения по регистру "упМаршрутПоРейсу".
//
Процедура СформироватьДвижениеМаршрутПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	 	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияМаршрутПоРейсу = Движения.упМаршрутПоРейсу;
	ДвиженияМаршрутПоРейсу.Записывать = Ложь;
	
	Маршрут = ДополнительныеСвойства.Маршрут;
	
	Если ЗначениеЗаполнено(Маршрут) Тогда
		
		ДвиженияМаршрутПоРейсу.Записывать = Истина;
		
		Для каждого СтрокаМаршрут Из Маршрут Цикл
			Движение = ДвиженияМаршрутПоРейсу.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаМаршрут);
			Если Не ЗначениеЗаполнено(Движение.Рейс) Тогда
				Движение.Рейс = Реквизиты.Рейс;
			КонецЕсли;
			Движение.Период = Реквизиты.Дата;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упРаботыПоРейсу".
//
Процедура СформироватьДвижениеРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРаботыПоРейсу = Движения.упРаботыПоРейсу;
	ДвиженияРаботыПоРейсу.Записывать = Ложь;
	
	Работы = ДополнительныеСвойства.Работы;
	
	Если ЗначениеЗаполнено(Работы) Тогда
		
		ДвиженияРаботыПоРейсу.Записывать = Истина;
		
		Для каждого СтрокаРабота Из Работы Цикл
			Движение = ДвиженияРаботыПоРейсу.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаРабота);
			Если Не ЗначениеЗаполнено(Движение.Рейс) Тогда
				Движение.Рейс = Реквизиты.Рейс;
			КонецЕсли;
			Движение.Период = Реквизиты.Дата;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упЗаданияКПланированию".
//
Процедура СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаданияНаПеревозку = Движения.упЗаданияКПланированию;
	ДвиженияЗаданияНаПеревозку.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзЗадания.Задание КАК Задание,
	               |	тбзЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	               |	тбзЗадания.КоличествоКПланированию КАК КоличествоКПланированию,
	               |	тбзЗадания.КоличествоНеПодтверждено КАК КоличествоНеПодтверждено,
	               |	тбзЗадания.КоличествоНеПереданноеКВыполнению КАК КоличествоНеПереданноеКВыполнению,
	               |	тбзЗадания.КоличествоКВыполнению КАК КоличествоКВыполнению
	               |ПОМЕСТИТЬ втЗадания
	               |ИЗ
	               |	&тбзЗадания КАК тбзЗадания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗадания.Задание КАК Задание,
	               |	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	               |	СУММА(втЗадания.КоличествоКПланированию) КАК КоличествоКПланированию,
	               |	СУММА(втЗадания.КоличествоНеПодтверждено) КАК КоличествоНеПодтверждено,
	               |	СУММА(втЗадания.КоличествоНеПереданноеКВыполнению) КАК КоличествоНеПереданноеКВыполнению,
	               |	СУММА(втЗадания.КоличествоКВыполнению) КАК КоличествоКВыполнению
	               |ПОМЕСТИТЬ втГрузовыеМеста
	               |ИЗ
	               |	втЗадания КАК втЗадания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втЗадания.Задание,
	               |	втЗадания.ГрузовоеМесто
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	втЗадания.Задание
	               |ПОМЕСТИТЬ втРазличныеЗадания
	               |ИЗ
	               |	втЗадания КАК втЗадания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упЗаданияКПланированиюОбороты.Задание КАК Задание,
	               |	упЗаданияКПланированиюОбороты.ГрузовоеМесто КАК ГрузовоеМесто,
	               |	упЗаданияКПланированиюОбороты.КоличествоКПланированиюПриход КАК КоличествоКПланированию,
	               |	упЗаданияКПланированиюОбороты.КоличествоНеПодтвержденоПриход КАК КоличествоНеПодтверждено,
	               |	упЗаданияКПланированиюОбороты.КоличествоНеПереданноеКВыполнениюПриход КАК КоличествоНеПереданноеКВыполнению,
	               |	упЗаданияКПланированиюОбороты.КоличествоКВыполнениюПриход КАК КоличествоКВыполнению,
	               |	упЗаданияКПланированиюОбороты.Регистратор
	               |ПОМЕСТИТЬ втДвиженияПоЗаданию
	               |ИЗ
	               |	РегистрНакопления.упЗаданияКПланированию.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			(Задание, ГрузовоеМесто) В
	               |				(ВЫБРАТЬ
	               |					втГрузовыеМеста.Задание,
	               |					втГрузовыеМеста.ГрузовоеМесто
	               |				ИЗ
	               |					втГрузовыеМеста КАК втГрузовыеМеста)) КАК упЗаданияКПланированиюОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРазличныеЗадания КАК втРазличныеЗадания
	               |		ПО упЗаданияКПланированиюОбороты.Регистратор = втРазличныеЗадания.Задание
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДвиженияПоЗаданию.Задание,
	               |	втДвиженияПоЗаданию.ГрузовоеМесто,
	               |	втДвиженияПоЗаданию.КоличествоКПланированию,
	               |	втДвиженияПоЗаданию.КоличествоНеПодтверждено,
	               |	втДвиженияПоЗаданию.КоличествоНеПереданноеКВыполнению,
	               |	втДвиженияПоЗаданию.КоличествоКВыполнению
	               |ПОМЕСТИТЬ втОстаткиПослеКорректировки
	               |ИЗ
	               |	втДвиженияПоЗаданию КАК втДвиженияПоЗаданию
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	втГрузовыеМеста.Задание,
	               |	втГрузовыеМеста.ГрузовоеМесто,
	               |	втГрузовыеМеста.КоличествоКПланированию,
	               |	втГрузовыеМеста.КоличествоНеПодтверждено,
	               |	втГрузовыеМеста.КоличествоНеПереданноеКВыполнению,
	               |	втГрузовыеМеста.КоличествоКВыполнению
	               |ИЗ
	               |	втГрузовыеМеста КАК втГрузовыеМеста
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбЗадания.Задание,
	               |	тбЗадания.ГрузовоеМесто,
	               |	СУММА(тбЗадания.КоличествоКПланированиюОстаток) КАК КоличествоКПланированию,
	               |	СУММА(тбЗадания.КоличествоНеПодтвержденоОстаток) КАК КоличествоНеПодтверждено,
	               |	СУММА(тбЗадания.КоличествоНеПереданноеКВыполнениюОстаток) КАК КоличествоНеПереданноеКВыполнению,
	               |	СУММА(тбЗадания.КоличествоКВыполнениюОстаток) КАК КоличествоКВыполнению
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		упЗаданияКПланированиюОстатки.Задание КАК Задание,
	               |		упЗаданияКПланированиюОстатки.ГрузовоеМесто КАК ГрузовоеМесто,
	               |		-упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток КАК КоличествоКПланированиюОстаток,
	               |		-упЗаданияКПланированиюОстатки.КоличествоНеПодтвержденоОстаток КАК КоличествоНеПодтвержденоОстаток,
	               |		-упЗаданияКПланированиюОстатки.КоличествоНеПереданноеКВыполнениюОстаток КАК КоличествоНеПереданноеКВыполнениюОстаток,
	               |		-упЗаданияКПланированиюОстатки.КоличествоКВыполнениюОстаток КАК КоличествоКВыполнениюОстаток
	               |	ИЗ
	               |		РегистрНакопления.упЗаданияКПланированию.Остатки(
	               |				,
	               |				(Задание, ГрузовоеМесто) В
	               |					(ВЫБРАТЬ
	               |						втЗадания.Задание,
	               |						втЗадания.ГрузовоеМесто
	               |					ИЗ
	               |						втЗадания КАК втЗадания)) КАК упЗаданияКПланированиюОстатки
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втЗадания.Задание,
	               |		втЗадания.ГрузовоеМесто,
	               |		втЗадания.КоличествоКПланированию,
	               |		втЗадания.КоличествоНеПодтверждено,
	               |		втЗадания.КоличествоНеПереданноеКВыполнению,
	               |		втЗадания.КоличествоКВыполнению
	               |	ИЗ
	               |		втОстаткиПослеКорректировки КАК втЗадания) КАК тбЗадания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбЗадания.Задание,
	               |	тбЗадания.ГрузовоеМесто
	               |
	               |ИМЕЮЩИЕ
	               |	(НЕ СУММА(тбЗадания.КоличествоКПланированиюОстаток) = 0
	               |		ИЛИ НЕ СУММА(тбЗадания.КоличествоНеПодтвержденоОстаток) = 0
	               |		ИЛИ НЕ СУММА(тбЗадания.КоличествоНеПереданноеКВыполнениюОстаток) = 0
	               |		ИЛИ НЕ СУММА(тбЗадания.КоличествоКВыполнениюОстаток) = 0)";
				   
	Запрос.УстановитьПараметр("тбзЗадания", ДополнительныеСвойства.Задания);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Истина);				
		текВыборка = РезультатЗапроса.Выбрать();
		
		Пока текВыборка.Следующий() Цикл
			
			КоличествоКПланированию = ?(текВыборка.КоличествоКПланированию > 0, текВыборка.КоличествоКПланированию, 0);
			КоличествоНеПодтверждено = ?(текВыборка.КоличествоНеПодтверждено > 0, текВыборка.КоличествоНеПодтверждено, 0);
			КоличествоНеПереданноеКВыполнению = ?(текВыборка.КоличествоНеПереданноеКВыполнению > 0, текВыборка.КоличествоНеПереданноеКВыполнению, 0);
			КоличествоКВыполнению = ?(текВыборка.КоличествоКВыполнению > 0, текВыборка.КоличествоКВыполнению, 0);
			
			Если НЕ КоличествоКПланированию = 0 
				ИЛИ НЕ КоличествоНеПодтверждено = 0 
				ИЛИ НЕ КоличествоНеПереданноеКВыполнению = 0 
				ИЛИ НЕ КоличествоКВыполнению = 0 Тогда
				ДвижениеЗадание = ДвиженияЗаданияНаПеревозку.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеЗадание, текВыборка);
				ДвижениеЗадание.Период = Реквизиты.Дата;
				ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеЗадание.КоличествоКПланированию = КоличествоКПланированию;
				ДвижениеЗадание.КоличествоНеПодтверждено = КоличествоНеПодтверждено;
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = КоличествоНеПереданноеКВыполнению;
				ДвижениеЗадание.КоличествоКВыполнению = КоличествоКВыполнению;
			КонецЕсли;
			
			
			КоличествоКПланированию = ?(текВыборка.КоличествоКПланированию < 0, -текВыборка.КоличествоКПланированию, 0);
			КоличествоНеПодтверждено = ?(текВыборка.КоличествоНеПодтверждено < 0, -текВыборка.КоличествоНеПодтверждено, 0);
			КоличествоНеПереданноеКВыполнению = ?(текВыборка.КоличествоНеПереданноеКВыполнению < 0, -текВыборка.КоличествоНеПереданноеКВыполнению, 0);
			КоличествоКВыполнению = ?(текВыборка.КоличествоКВыполнению < 0, -текВыборка.КоличествоКВыполнению, 0);
			
			Если НЕ КоличествоКПланированию = 0 
				ИЛИ НЕ КоличествоНеПодтверждено = 0 
				ИЛИ НЕ КоличествоНеПереданноеКВыполнению = 0 
				ИЛИ НЕ КоличествоКВыполнению = 0 Тогда
				ДвижениеЗадание = ДвиженияЗаданияНаПеревозку.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеЗадание, текВыборка);
				ДвижениеЗадание.Период = Реквизиты.Дата;
				ДвижениеЗадание.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеЗадание.КоличествоКПланированию = КоличествоКПланированию;
				ДвижениеЗадание.КоличествоНеПодтверждено = КоличествоНеПодтверждено;
				ДвижениеЗадание.КоличествоНеПереданноеКВыполнению = КоличествоНеПереданноеКВыполнению;
				ДвижениеЗадание.КоличествоКВыполнению = КоличествоКВыполнению;
			КонецЕсли;
			
		КонецЦикла;	
	КонецЕсли;

КонецПроцедуры	

// Процедура - формирует движения по регистру "упПеревозчикПоРейсу".
//
Процедура СформироватьДвижениеПеревозчикПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияПеревозчикПоРейсу = Движения.упПеревозчикПоРейсу;
	
	Если ДополнительныеСвойства.Свойство("ИзмененЭкипаж") Тогда
		
		ДвиженияПеревозчикПоРейсу.Записывать = Истина;
		
		сткЭкипаж = ДополнительныеСвойства.ИзмененЭкипаж;
		Если ЗначениеЗаполнено(сткЭкипаж.ТипТС) Или ЗначениеЗаполнено(сткЭкипаж.Перевозчик) Тогда
			Движение = ДвиженияПеревозчикПоРейсу.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, сткЭкипаж);
			Движение.Рейс   = Реквизиты.Рейс;
			Движение.Период = Реквизиты.Дата;													
		КонецЕсли; 
		
	КонецЕсли;	
	
КонецПроцедуры

// Процедура формирует записи по регистру "упСостояниеПоЗаданиям".
//
Процедура СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСостояниеПоЗаданиям	= Движения.упСостояниеПоЗаданиям;
	ДвиженияСостояниеПоЗаданиям.Записывать	= Истина;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзЗадания.Задание КАК Задание,
	               |	тбзЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	               |	тбзЗадания.Местоположение КАК Местоположение,
	               |	тбзЗадания.Количество КАК Количество
	               |ПОМЕСТИТЬ втЗаданияАдреса
	               |ИЗ
	               |	&тбзСостояниеЗаданийПоАдресам КАК тбзЗадания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбзЗадания.Задание,
	               |	тбзЗадания.ГрузовоеМесто,
	               |	тбзЗадания.Местоположение,
	               |	тбзЗадания.Количество
	               |ПОМЕСТИТЬ втЗаданияТС
	               |ИЗ
	               |	&тбзСостояниеЗаданийПоТС КАК тбзЗадания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗаданияАдреса.Задание,
	               |	втЗаданияАдреса.ГрузовоеМесто,
	               |	втЗаданияАдреса.Местоположение,
	               |	втЗаданияАдреса.Количество
	               |ПОМЕСТИТЬ втЗадания
	               |ИЗ
	               |	втЗаданияАдреса КАК втЗаданияАдреса
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	втЗаданияТС.Задание,
	               |	втЗаданияТС.ГрузовоеМесто,
	               |	втЗаданияТС.Местоположение,
	               |	втЗаданияТС.Количество
	               |ИЗ
	               |	втЗаданияТС КАК втЗаданияТС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗадания.Задание КАК Задание,
	               |	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	               |	втЗадания.Местоположение КАК Местоположение,
	               |	СУММА(втЗадания.Количество) КАК Количество
	               |ПОМЕСТИТЬ втГрузовыеМеста
	               |ИЗ
	               |	втЗадания КАК втЗадания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втЗадания.Задание,
	               |	втЗадания.ГрузовоеМесто,
	               |	втЗадания.Местоположение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	втЗадания.Задание
	               |ПОМЕСТИТЬ втРазличныеЗадания
	               |ИЗ
	               |	втЗадания КАК втЗадания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упСостояниеПоЗаданиям.Задание КАК Задание,
	               |	упСостояниеПоЗаданиям.ГрузовоеМесто КАК ГрузовоеМесто,
	               |	упСостояниеПоЗаданиям.Местоположение КАК Местоположение,
	               |	упСостояниеПоЗаданиям.Регистратор,
	               |	упСостояниеПоЗаданиям.КоличествоОборот КАК Количество
	               |ПОМЕСТИТЬ втДвиженияПоСостояниюЗадания
	               |ИЗ
	               |	РегистрНакопления.упСостояниеПоЗаданиям.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			(Задание, ГрузовоеМесто) В
	               |				(ВЫБРАТЬ
	               |					втГрузовыеМеста.Задание,
	               |					втГрузовыеМеста.ГрузовоеМесто
	               |				ИЗ
	               |					втГрузовыеМеста КАК втГрузовыеМеста)) КАК упСостояниеПоЗаданиям
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРазличныеЗадания КАК втРазличныеЗадания
	               |		ПО упСостояниеПоЗаданиям.Регистратор = втРазличныеЗадания.Задание
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДвиженияПоСостояниюЗадания.Задание,
	               |	втДвиженияПоСостояниюЗадания.ГрузовоеМесто,
	               |	втДвиженияПоСостояниюЗадания.Местоположение,
	               |	втДвиженияПоСостояниюЗадания.Количество
	               |ПОМЕСТИТЬ втОстаткиПослеКорректировки
	               |ИЗ
	               |	втДвиженияПоСостояниюЗадания КАК втДвиженияПоСостояниюЗадания
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	втГрузовыеМеста.Задание,
	               |	втГрузовыеМеста.ГрузовоеМесто,
	               |	втГрузовыеМеста.Местоположение,
	               |	втГрузовыеМеста.Количество
	               |ИЗ
	               |	втГрузовыеМеста КАК втГрузовыеМеста
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбЗадания.Задание,
	               |	тбЗадания.ГрузовоеМесто,
	               |	тбЗадания.Местоположение,
	               |	СУММА(тбЗадания.Количество) КАК Количество
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		упСостояниеПоЗаданиям.Задание КАК Задание,
	               |		упСостояниеПоЗаданиям.ГрузовоеМесто КАК ГрузовоеМесто,
	               |		упСостояниеПоЗаданиям.Местоположение КАК Местоположение,
	               |		-упСостояниеПоЗаданиям.КоличествоОстаток КАК Количество
	               |	ИЗ
	               |		РегистрНакопления.упСостояниеПоЗаданиям.Остатки(
	               |				,
	               |				(Задание, ГрузовоеМесто) В
	               |					(ВЫБРАТЬ
	               |						втЗадания.Задание,
	               |						втЗадания.ГрузовоеМесто
	               |					ИЗ
	               |						втЗадания КАК втЗадания)) КАК упСостояниеПоЗаданиям
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втЗадания.Задание,
	               |		втЗадания.ГрузовоеМесто,
	               |		втЗадания.Местоположение,
	               |		втЗадания.Количество
	               |	ИЗ
	               |		втОстаткиПослеКорректировки КАК втЗадания) КАК тбЗадания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбЗадания.Задание,
	               |	тбЗадания.ГрузовоеМесто,
	               |	тбЗадания.Местоположение
	               |
	               |ИМЕЮЩИЕ
	               |	НЕ СУММА(тбЗадания.Количество) = 0";
				   
	Запрос.УстановитьПараметр("тбзСостояниеЗаданийПоАдресам", ДополнительныеСвойства.СостояниеЗаданийПоАдресам);
	Запрос.УстановитьПараметр("тбзСостояниеЗаданийПоТС", ДополнительныеСвойства.СостояниеЗаданийПоТС);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		текВыборка = РезультатЗапроса.Выбрать();
		
		Пока текВыборка.Следующий() Цикл
			
			Количество	= ?(текВыборка.Количество> 0,	текВыборка.Количество, 0);
		
			Если НЕ Количество = 0 Тогда
				ДвижениеСостояние		= ДвиженияСостояниеПоЗаданиям.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеСостояние, текВыборка);
				ДвижениеСостояние.Период      = Реквизиты.Дата;
				ДвижениеСостояние.ВидДвижения = ВидДвиженияНакопления.Приход;
				ДвижениеСостояние.Количество  = Количество;
			КонецЕсли;
			
			Количество = ?(текВыборка.Количество < 0, -текВыборка.Количество, 0);
		
			Если НЕ Количество = 0 Тогда
				ДвижениеСостояние		= ДвиженияСостояниеПоЗаданиям.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеСостояние, текВыборка);
				ДвижениеСостояние.Период      = Реквизиты.Дата;
				ДвижениеСостояние.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеСостояние.Количество  = Количество;
			КонецЕсли;
			
		КонецЦикла;	
	КонецЕсли;

КонецПроцедуры

// Процедура - формирует движения по регистру "упПланПоЗаданиям".
//
Процедура СформироватьДвижениеПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;

	ДвиженияПланПоЗаданиям = Движения.упПланПоЗаданиям;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеСвойства, "ПланПоЗаданиям") Тогда
		
		ПланПоЗаданиям = ДополнительныеСвойства.ПланПоЗаданиям;
		
		Если ЗначениеЗаполнено(ПланПоЗаданиям) Тогда
			
			ДвиженияПланПоЗаданиям.Записывать = Истина;
			
			Для каждого СтрокаПлан Из ПланПоЗаданиям Цикл
				Движение = ДвиженияПланПоЗаданиям.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, СтрокаПлан);
				Движение.Период = Реквизиты.Дата;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Сформировать движения по регистру "упЗанятостьТранспорта"
//
Процедура СформироватьДвиженияПлановаяЗанятостьТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	Если Ложь Тогда // Для контекстной подсказки
		Ссылка = Документы.упКорректировкаРейса.ПустаяСсылка();
		РеквизитыОбъекта = Ссылка.ПолучитьОбъект();
		Движения = РеквизитыОбъекта.Движения;
	КонецЕсли;
	Если Истина
		И ДополнительныеСвойства.Свойство("ИзмененЭкипаж")
		И ДополнительныеСвойства.ИзмененЭкипаж.ТранспортноеСредство <> ДополнительныеСвойства.СтарыйЭкипаж.ТранспортноеСредство
		И ЗначениеЗаполнено(ДополнительныеСвойства.СтарыйЭкипаж.ТранспортноеСредство) 
	Тогда
		ПлановаяЗанятостьТС = Движения.упЗанятостьТранспорта;
		ПлановаяЗанятостьТС.Записывать = Истина;
		СрезПоследних = упУчетТранспортныхСредств.СрезПоследнихЗанятостьТранспорта(Ссылка.МоментВремени(), РеквизитыОбъекта.Рейс, ДополнительныеСвойства.СтарыйЭкипаж.ТранспортноеСредство);
		Если Ложь Тогда // Для контекстной подсказки
			СрезПоследних = Новый ТаблицаЗначений;
		КонецЕсли;
		СрезПоследних.ЗаполнитьЗначения(Ссылка.Дата, "Период");
		СрезПоследних.ЗаполнитьЗначения(Ложь, "ПланАктуален");
		ПлановаяЗанятостьТС.Загрузить(СрезПоследних);
	КонецЕсли; 
	упРейс.СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

// Процедура - Восстановить значение плановое окончание выполнения рейса
//
Процедура СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	ТекущийСтатус = ДополнительныеСвойства.Статус;
	
	Если Истина
		И ДополнительныеСвойства.СтатусРейсаДоКорректировки = Перечисления.упСтатусыРейса.КВыполнению
		И (Ложь
		   Или ТекущийСтатус = Перечисления.упСтатусыРейса.КВыполнению
		   Или ТекущийСтатус = Перечисления.упСтатусыРейса.Отменен) Тогда
		
		// Махинация со статусом необходима если рейс корректировкой был переведен из статуса "К выполнению", в другой статус, например "Отменен".
		ДополнительныеСвойства.Статус = ДополнительныеСвойства.СтатусРейсаДоКорректировки;
		ТекущиеРаботы = ДополнительныеСвойства.Работы; 
		ДополнительныеСвойства.Вставить("Работы", ДополнительныеСвойства.РаботыПоРейсу.Скопировать(Новый Структура("Отменена",Ложь)));
		ТекущийМаршрут = ДополнительныеСвойства.Маршрут;
		ДополнительныеСвойства.Вставить("Маршрут", ДополнительныеСвойства.МаршрутПоРейсу.Скопировать(Новый Структура("Отменена",Ложь)));
		
		упРейс.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, РеквизитыОбъекта.Рейс, Движения, РеквизитыОбъекта, Отказ);
		
		ДополнительныеСвойства.Работы = ТекущиеРаботы;		
		ДополнительныеСвойства.Маршрут = ТекущийМаршрут;		
		ДополнительныеСвойства.Статус = ТекущийСтатус;
			
	Иначе	
		
		Рейс = РеквизитыОбъекта.Рейс;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
		|ГДЕ
		|	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
		|
		|УПОРЯДОЧИТЬ ПО
		|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер УБЫВ";
		Запрос.УстановитьПараметр("Рейс", Рейс);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			Запись = РегистрыСведений.упПлановыеПараметрыРейса.СоздатьМенеджерЗаписи();
			Запись.Рейс = Рейс;
			Запись.Прочитать();
			
			Если Истина
				И Запись.Выбран() 
				И Запись.ПлановоеОкончаниеВыполнения <> Выборка.ПлановоеУбытие
			Тогда
				Запись.ПлановоеОкончаниеВыполнения = Выборка.ПлановоеУбытие;
				Запись.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Процедура -  формирует движения по регистру "ДвиженияЗаявкиКВыполнению".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Набор движений.
//  Реквизиты - Структура - Реквизиты.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаявкиКВыполнению = Движения.упЗаявкиКВыполнению;
	ДвиженияЗаявкиКВыполнению.Записывать = Истина;
	тбзЗаявки = ДополнительныеСвойства.Заявки;

	Для каждого текСтрока Из тбзЗаявки Цикл
		ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
		ДвижениеЗаявки.Период = Реквизиты.Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеЗаявки, текСтрока);
	КонецЦикла;	
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упНепереданныеДокументы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - Зафиксировать изменения.
//
Процедура СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияНепереданныеДокументы	         = Движения.упНепереданныеДокументы;
	ДвиженияНепереданныеДокументы.Записывать = Ложь;
		
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
		ДвиженияНепереданныеДокументы.Записывать = Истина;
		Возврат;
	КонецЕсли; 
	
	ДвиженияНепереданныеДокументы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозку,
	               |	&Рейс КАК Рейс,
	               |	упЗаданиеНаПеревозкуГруза.Заказчик
	               |ПОМЕСТИТЬ втЗадания
	               |ИЗ
	               |	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	               |ГДЕ
	               |	упЗаданиеНаПеревозкуГруза.Ссылка В(&мсвНеОтмененныеЗадания)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗадания.ЗаданиеНаПеревозку,
	               |	втЗадания.Рейс,
	               |	упПакетДокументов.ТипРегламентногоДокумента,
	               |	упПакетДокументов.КоличествоПолучитьОтУчастника КАК КоличествоПолучить,
	               |	упПакетДокументов.КоличествоПередатьУчастнику КАК КоличествоПередать
	               |ПОМЕСТИТЬ втДокументы
	               |ИЗ
	               |	втЗадания КАК втЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПакетыРегламентныхДокументов.ПакетДокументов КАК упПакетДокументов
	               |		ПО втЗадания.Заказчик.ПакетРегламентныхДокументов = упПакетДокументов.Ссылка
	               |			И (упПакетДокументов.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Заказчик))
	               |			И (упПакетДокументов.ВозвратДокументовПослеИсполненияРейса)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДокументы.ЗаданиеНаПеревозку,
	               |	втДокументы.Рейс,
	               |	втДокументы.ТипРегламентногоДокумента,
	               |	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов) КАК Операция,
	               |	втДокументы.КоличествоПолучить КАК Количество
	               |ПОМЕСТИТЬ втДокументыПоЗаданиям
	               |ИЗ
	               |	втДокументы КАК втДокументы
	               |ГДЕ
	               |	втДокументы.КоличествоПолучить > 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	втДокументы.ЗаданиеНаПеревозку,
	               |	втДокументы.Рейс,
	               |	втДокументы.ТипРегламентногоДокумента,
	               |	ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов),
	               |	втДокументы.КоличествоПередать
	               |ИЗ
	               |	втДокументы КАК втДокументы
	               |ГДЕ
	               |	втДокументы.КоличествоПередать > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбЗадания.Рейс,
	               |	тбЗадания.ЗаданиеНаПеревозку,
	               |	тбЗадания.ТипРегламентногоДокумента,
	               |	тбЗадания.Операция,
	               |	СУММА(тбЗадания.Количество) КАК Количество
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		упНепереданныеДокументыОстатки.Рейс КАК Рейс,
	               |		упНепереданныеДокументыОстатки.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	               |		упНепереданныеДокументыОстатки.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
	               |		упНепереданныеДокументыОстатки.Операция КАК Операция,
	               |		-упНепереданныеДокументыОстатки.КоличествоОстаток КАК Количество
	               |	ИЗ
	               |		РегистрНакопления.упНепереданныеДокументы.Остатки(, Рейс = &Рейс) КАК упНепереданныеДокументыОстатки
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втЗадания.Рейс,
	               |		втЗадания.ЗаданиеНаПеревозку,
	               |		втЗадания.ТипРегламентногоДокумента,
	               |		втЗадания.Операция,
	               |		втЗадания.Количество
	               |	ИЗ
	               |		втДокументыПоЗаданиям КАК втЗадания) КАК тбЗадания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбЗадания.Рейс,
	               |	тбЗадания.ЗаданиеНаПеревозку,
	               |	тбЗадания.ТипРегламентногоДокумента,
	               |	тбЗадания.Операция
	               |
	               |ИМЕЮЩИЕ
	               |	НЕ СУММА(тбЗадания.Количество) = 0";
				   
	Запрос.УстановитьПараметр("мсвНеОтмененныеЗадания", ДополнительныеСвойства.мсвНеОтмененныеЗадания);
	Запрос.УстановитьПараметр("Рейс", 		Реквизиты.Рейс);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ДвижениеНепереданныеДокументы = ДвиженияНепереданныеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеНепереданныеДокументы, Выборка);
			ДвижениеНепереданныеДокументы.Период       	= Реквизиты.Дата;
			ДвижениеНепереданныеДокументы.ВидДвижения		= ?(Выборка.Количество > 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
			ДвижениеНепереданныеДокументы.Количество		= ?(Выборка.Количество > 0, Выборка.Количество, -Выборка.Количество);
		КонецЦикла;	
	
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упИзмененияРейсов".
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на документ.
//  Отказ - Булево - Фиксировать изменения.
//
Процедура СформироватьДвиженияИзмененияРейсов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") И Не ДополнительныеСвойства.Свойство("МобильныйКлиентНеОбрабатывать") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упУстановленныеТрекерыСрезПоследних.Трекер
		|ИЗ
		|	РегистрСведений.упУстановленныеТрекеры.СрезПоследних КАК упУстановленныеТрекерыСрезПоследних
		|ГДЕ
		|	упУстановленныеТрекерыСрезПоследних.Рейс = &Рейс";
		Запрос.УстановитьПараметр("Рейс", Реквизиты.Рейс);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ИзменениеРейса = РегистрыСведений.упИзмененияРейсов.СоздатьМенеджерЗаписи();
			ИзменениеРейса.Рейс = Реквизиты.Рейс;
			ИзменениеРейса.Прочитать();
			Если ИзменениеРейса.Выбран() И ИзменениеРейса.ТребуетсяОбновление Тогда
				Возврат;
			Иначе	
				Если Ложь
					Или ДополнительныеСвойства.Свойство("ОбновитьМаршрутНаМК")
					Или ИзмененМаршрутДляМобильногоКлиента(Ссылка)
					Или ИзмененыРаботыДляМобильногоКлиента(Ссылка)
					Или 
						(Истина
						И ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.Рейс, Новый Структура("РазрешеноОтложенноеВыполнение", "ВидПеревозки.РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу")).РазрешеноОтложенноеВыполнение
						И РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу(Реквизиты.Рейс))
				Тогда
					ИзменениеРейса.Рейс = Реквизиты.Рейс;
					ИзменениеРейса.ТребуетсяОбновление = Истина;
					
					Если ДополнительныеСвойства.Свойство("УведомлениеМобильныйКлиент") Тогда
						ИзменениеРейса.Заголовок        = ДополнительныеСвойства.УведомлениеМобильныйКлиент.Заголовок;
						ИзменениеРейса.ТекстУведомления = ДополнительныеСвойства.УведомлениеМобильныйКлиент.ТекстУведомления;
					КонецЕсли; 
					ИзменениеРейса.Записать();
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// Процедура формирует движения по регистру "упРазделениеГрузовыхМест".
//
Процедура СформироватьДвижениеРазделениеГрузовыхМест(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
		
	РазделениеГрузовыхМест = Неопределено;
		
	Если ДополнительныеСвойства.Свойство("РазделениеГрузовыхМест", РазделениеГрузовыхМест) Тогда
		
		ДвиженияРазделениеГрузовыхМест	= Движения.упРазделениеГрузовыхМест;
		ДвиженияРазделениеГрузовыхМест.Записывать = Ложь;
		
		Если ЗначениеЗаполнено(РазделениеГрузовыхМест) Тогда
			ДвиженияРазделениеГрузовыхМест.Записывать = Истина;
			
			Для каждого Строка Из РазделениеГрузовыхМест Цикл
				Движение				= ДвиженияРазделениеГрузовыхМест.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Строка);
				Движение.Период			= Реквизиты.Дата;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упОстаткиГрузовыхМест".
//
Процедура СформироватьДвижениеОстаткиГрузовыхМест(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
		
	ОстаткиГрузовыхМест = Неопределено;
		
	Если ДополнительныеСвойства.Свойство("ОстаткиГрузовыхМест", ОстаткиГрузовыхМест) Тогда
		
		ДвиженияОстаткиГрузовыхМест	= Движения.упОстаткиГрузовыхМест;
		ДвиженияОстаткиГрузовыхМест.Записывать = Ложь;
		
		Если ЗначениеЗаполнено(ОстаткиГрузовыхМест) Тогда
			ДвиженияОстаткиГрузовыхМест.Записывать = Истина;
			
			Для каждого Строка Из ОстаткиГрузовыхМест Цикл
				Движение				= ДвиженияОстаткиГрузовыхМест.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Строка);
				Если Строка.Количество < 0 Тогда
					Движение.Количество  = -Строка.Количество;
					Движение.ВидДвижения	= ВидДвиженияНакопления.Расход;
				Иначе	
					Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
				КонецЕсли;
				Движение.Период			= Реквизиты.Дата;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует путевой лист по рейсу корректировки.
//
Процедура СформироватьПутевойЛистПоРейсуКорректировки(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	мсвРейсы =  Новый Массив;
	мсвРейсы.Добавить(Реквизиты.Рейс);
	ПутевойЛист = упПутевойЛистУчетГСМ.СформироватьПутевойЛистПоРейсам(мсвРейсы, , ,Отказ);
	
	Если Отказ Тогда
		ТекстСообщения = Нстр("ru = 'Не удалось сформировать путевой лист по рейсу:%1, подробности в журнале регистрации'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Реквизиты.Рейс);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		Событие = Нстр("ru = 'Ошибка при корректировке маршрута рейса'");
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,, , ТекстСообщения);    
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует движения по регистру "упПрерыванияГрузовыхПотоков".
//
Процедура СформироватьДвижениеПрерыванияГрузовыхПотоков(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияПрерыванияГрузовыхПотоков = Движения.упПрерыванияГрузовыхПотоков;
	ДвиженияПрерыванияГрузовыхПотоков.Записывать = Истина;
	
	ПрерыванияГрузовыхПотоков = Неопределено;
	
	Если Истина
		И ДополнительныеСвойства.Свойство("ПрерыванияГрузовыхПотоков", ПрерыванияГрузовыхПотоков)
		И ЗначениеЗаполнено(ПрерыванияГрузовыхПотоков) Тогда
		
		РейсСсылка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "Рейс", Документы.упРейс.ПустаяСсылка());
		
		Для каждого СтрокаПрерывание Из ПрерыванияГрузовыхПотоков Цикл
			Движение = ДвиженияПрерыванияГрузовыхПотоков.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаПрерывание);
			Движение.Рейс = РейсСсылка;
			Движение.Период = Реквизиты.Дата;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - формирует движения по регистру "упПримененныеПравилаВключенияКонтрольныхТочек".
//
//
Процедура СформироватьДвиженияПримененныеПравилаВключенияКонтрольныхТочек(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ИмяСобытия = Нстр("ru = 'Подбор контрольных точек'");
	
	ПримененныеПравилаВключенияКонтрольныхТочек = Неопределено;
	Если ДополнительныеСвойства.Свойство("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек) Тогда
		Если Истина
			И ТипЗнч(ПримененныеПравилаВключенияКонтрольныхТочек) = Тип("СписокЗначений")
			И ПримененныеПравилаВключенияКонтрольныхТочек.Количество() > 0
		Тогда
		     мсвПравила = ПримененныеПравилаВключенияКонтрольныхТочек.ВыгрузитьЗначения();
		     мсвПравила = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПравила);
			Для каждого ПравилоТекущее Из мсвПравила Цикл
				РейсСсылка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "Рейс", Документы.упРейс.ПустаяСсылка());
		
				Запись = РегистрыСведений.упПримененныеПравилаВключенияКонтрольныхТочек.СоздатьМенеджерЗаписи();
				Запись.Рейс = РейсСсылка;
				Запись.ПравилоВключенияКонтрольныхТочек = ПравилоТекущее;
				Запись.Прочитать();
				Если Не Запись.Выбран() Тогда
					Запись.Рейс = РейсСсылка;
					Запись.ПравилоВключенияКонтрольныхТочек = ПравилоТекущее;
					Запись.ДатаПрименения = Реквизиты.Дата;
					Попытка
						Запись.Записать();
					Исключение
						ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
							ТекстОшибки = ОписаниеОшибки();
						КонецЕсли;
						Отказ = Истина;
						ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					КонецПопытки;

				КонецЕсли;
				Если Отказ Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ВспомогательныеПроцедурыФункции

// Возвращает возможность редактирования маршрута или рейса.
//
// Параметры:
//  ТекущийОбъект	 - 	 - 
//  Период			 - 	 - 
// 
// Возвращаемое значение:
//  Булево - 
//
Функция ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ТекущийОбъект, Период = Неопределено) Экспорт
	
	Результат = Ложь;
	Проверить = Ложь;
	Задания = Неопределено;
	Рейс = Неопределено;
	Ссылка = Неопределено;
	
	Если ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упПрохождениеТочки") Тогда
		Проверить = Истина;
		Период = ?(ЗначениеЗаполнено(ТекущийОбъект.ОкончаниеВыполнения), ТекущийОбъект.ОкончаниеВыполнения, ТекущийОбъект.Дата);
		Рейс = ТекущийОбъект.Рейс;
		Ссылка = ТекущийОбъект.Ссылка;
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументСсылка.упРейс") Тогда	
		Проверить = Истина;
		Рейс = ТекущийОбъект;
		Ссылка = ТекущийОбъект;
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упКорректировкаРейса") Тогда	
		Проверить = Истина;
		Задания = ТекущийОбъект.Задания.Выгрузить(, "Задание");
		Рейс = ТекущийОбъект.Рейс;
		Ссылка = ТекущийОбъект.Ссылка;
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упПроисшествиеВПути") Тогда			
		Если ТекущийОбъект.Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад
			ИЛИ ТекущийОбъект.Действие = Перечисления.упДействияПриПроисшествииВПути.ПродолжениеРейса Тогда
			Проверить = Истина;
			Период = ?(ЗначениеЗаполнено(ТекущийОбъект.ОкончаниеВыполнения), ТекущийОбъект.ОкончаниеВыполнения, ТекущийОбъект.Дата);
			Рейс = ТекущийОбъект.Рейс;
			Ссылка = ТекущийОбъект.Ссылка;
		КонецЕсли;		
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упДвижениеДокументовРейса") Тогда				
		Если НЕ ТекущийОбъект.ВозвратДокументовПослеИсполненияРейса
			И ТекущийОбъект.Работы.Количество() > 0 Тогда
			Проверить = Истина;
			Рейс = ТекущийОбъект.Рейс;
			Ссылка = ТекущийОбъект.Ссылка;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.упРегистрацияОтложенныхРаботПоРейсу") Тогда	
		Проверить = Истина;
		Рейс = ТекущийОбъект.Рейс;
		Ссылка = ТекущийОбъект.Ссылка;
	КонецЕсли;
	
	Если Проверить Тогда
		Результат = НЕ РазрешеноКорректироватьМаршрут(?(Период = Неопределено, ТекущийОбъект.Дата, Период), Ссылка, Рейс, Задания);
		//текРезультат = НЕ РазрешеноКорректироватьМаршрут(ТекущийОбъект.Дата, ТекущийОбъект.Ссылка, ТекущийОбъект.Рейс);
	КонецЕсли;
	
	Если Результат Тогда
		ТекстСообщения = Нстр("ru = 'Корректировка данных маршрута и работ по текущему рейсу запрещена, так как есть изменения на дату %1 или  более поздние.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Период);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		Событие = Нстр("ru = 'Ошибка при корректировке маршрута рейса'");
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
	КонецЕсли;
	
	Возврат Результат;	
		
КонецФункции

// Функция - Прерывание грузового потока документом обработано
//
// Параметры:
//  ТекущийОбъектСсылка	 - ДокументСсылка.упРейс, ДокументСсылка.упПрохождениеТочки - проверяемый документ.
// 
// Возвращаемое значение:
//  Истина - если обработаны все прерывания созданные документом.
//
Функция ПрерываниеГрузовогоПотокаДокументомОбработано(ТекущийОбъектСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.ЗаявкаНаПеревозку КАК ЗаявкаНаПеревозку
	|ИЗ
	|	РегистрСведений.упПрерыванияГрузовыхПотоков.СрезПоследних(
	|		,
	|		(ГрузовоеМесто, ЗаявкаНаПеревозку,
	|			НомерВЦепиПоставокКонец) В (
	|				ВЫБРАТЬ
	|				упПрерыванияГрузовыхПотоков.ГрузовоеМесто КАК ГрузовоеМесто,
	|				упПрерыванияГрузовыхПотоков.ЗаявкаНаПеревозку КАК ЗаявкаНаПеревозку,
	|				упПрерыванияГрузовыхПотоков.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|			ИЗ
	|				РегистрСведений.упПрерыванияГрузовыхПотоков КАК упПрерыванияГрузовыхПотоков
	|			ГДЕ упПрерыванияГрузовыхПотоков.Регистратор = &Регистратор)) КАК УпПрерыванияГрузовыхПотоков_СрезПоследнихТ
	|ГДЕ УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.Обработано
	|";

	Запрос.УстановитьПараметр("Регистратор", ТекущийОбъектСсылка);
	Результат = Запрос.Выполнить();

	Возврат Результат.Пустой();
	
КонецФункции

// Функция - Проверяет есть ли задания по которым совершались движения после проведения корректировки рейса. 
//			 Например, были отменены или по ним были введены новые рейсы.
//
// Параметры:
//  КорректировкаРейса	 - ДокументСсылка.упКорректировкаРейса	 - корректировка рейса.
//  Рейс				 - ДокументСсылка.упРейс				 - рейс.
//  Дата				 - ДатаВремя	 						 - дата документа корректировка.
// 
// Возвращаемое значение:
//  Истина - по заданиям были движения после того как была проведена корректировка.
//
Функция ЗаданияНаПеревозкуБылиИзмененыПослеПроведенияКорректировки(КорректировкаРейса, Рейс, Дата) Экспорт
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упКорректировкаРейсаЗадания.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза КАК упСтатусыЗаданийНаПеревозкуГруза
	               |		ПО упКорректировкаРейсаЗадания.Задание = упСтатусыЗаданийНаПеревозкуГруза.Документ
	               |ГДЕ
	               |	упКорректировкаРейсаЗадания.Ссылка = &КорректировкаРейса
	               |	И упСтатусыЗаданийНаПеревозкуГруза.Период > &Дата
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	упРейсЗадания.Ссылка
	               |ИЗ
	               |	Документ.упРейс.Задания КАК упРейсЗадания
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза КАК упСтатусыЗаданийНаПеревозкуГруза
	               |		ПО упРейсЗадания.Задание = упСтатусыЗаданийНаПеревозкуГруза.Документ
	               |ГДЕ
	               |	упРейсЗадания.Ссылка = &Рейс
	               |	И упСтатусыЗаданийНаПеревозкуГруза.Период > &Дата";
	Запрос.УстановитьПараметр("КорректировкаРейса", КорректировкаРейса);
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Дата", Дата);
	Результат	= НЕ Запрос.Выполнить().Пустой();
	Возврат Результат;
	
КонецФункции

// Функция - Возвращает товарный состав грузового места
//
// Параметры:
//  ГрузовоеМесто	 - СправочникСсылка.упГрузовыеМеста	 - грузовое место
// 
// Возвращаемое значение:
//  ТаблицаЗначений - товарный состав грузового места.
//
Функция ЗаполнитьТоварныйСоставГрузовыхМест(ГрузовоеМесто) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГрузовоеМесто", ГрузовоеМесто);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	упГрузовыеМестаТоварныйСоставГруза.Ссылка КАК ГрузовоеМесто,
	               |	упГрузовыеМестаТоварныйСоставГруза.НомерСтроки,
	               |	упГрузовыеМестаТоварныйСоставГруза.Номенклатура,
	               |	упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры,
	               |	упГрузовыеМестаТоварныйСоставГруза.Количество,
	               |	упГрузовыеМестаТоварныйСоставГруза.Цена,
	               |	упГрузовыеМестаТоварныйСоставГруза.СтавкаНДС,
	               |	упГрузовыеМестаТоварныйСоставГруза.СуммаНДС,
	               |	упГрузовыеМестаТоварныйСоставГруза.СуммаБезНДС,
	               |	упГрузовыеМестаТоварныйСоставГруза.Сумма,
	               |	упГрузовыеМестаТоварныйСоставГруза.АналитическийРазрез,
	               |	ЛОЖЬ КАК НовыйТовар
	               |ИЗ
	               |	Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК упГрузовыеМестаТоварныйСоставГруза
	               |ГДЕ
	               |	упГрузовыеМестаТоварныйСоставГруза.Ссылка = &ГрузовоеМесто";
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
			
КонецФункции

// Процедура контролирует отрицательные остатки по складу
//
// Параметры:
//	Реквизиты - Структура - структура.
//	Отказ - Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ПроконтролироватьОтрицательныеОстаткиГрузовыхМестПоСкладу(Реквизиты, Ссылка, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрохождениеТочки") Тогда
		тбзГрузовыеМеста = Реквизиты.Работы.Выгрузить(,"ГрузовоеМесто, Тара, СкладскаяЯчейка");
	Иначе
		тбзГрузовыеМеста = Реквизиты.ОстаткиГрузовыхМест;
	КонецЕсли;
	тбзГрузовыеМеста.Свернуть("ГрузовоеМесто, Тара, СкладскаяЯчейка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	втГрузовыеМеста.ГрузовоеМесто,
	               |	втГрузовыеМеста.Тара,
	               |	втГрузовыеМеста.СкладскаяЯчейка
	               |ПОМЕСТИТЬ втГрузовыеМеста
	               |ИЗ
	               |	&тбГрузовыеМеста КАК втГрузовыеМеста
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упОстаткиГрузовыхМестОстатки.ГрузовоеМесто,
	               |	упОстаткиГрузовыхМестОстатки.Тара,
	               |	упОстаткиГрузовыхМестОстатки.СкладскаяЯчейка,
	               |	упОстаткиГрузовыхМестОстатки.КоличествоОстаток
	               |ИЗ
	               |	РегистрНакопления.упОстаткиГрузовыхМест.Остатки(
	               |			,
	               |			(ГрузовоеМесто, Тара, СкладскаяЯчейка) В
	               |				(ВЫБРАТЬ
	               |					втГрузовыеМеста.ГрузовоеМесто,
	               |					втГрузовыеМеста.Тара,
	               |					втГрузовыеМеста.СкладскаяЯчейка
	               |				ИЗ
	               |					втГрузовыеМеста КАК втГрузовыеМеста)) КАК упОстаткиГрузовыхМестОстатки
	               |ГДЕ
	               |	упОстаткиГрузовыхМестОстатки.КоличествоОстаток < 0";
	Запрос.УстановитьПараметр("тбГрузовыеМеста", тбзГрузовыеМеста);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Отказ = НЕ РезультатЗапроса.Пустой();
	
	Если Отказ Тогда
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Тара	= ?(ЗначениеЗаполнено(Выборка.Тара), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = ' в таре ""%1""'"), Выборка.Тара),"");
			ТекстКоличествоГрузов = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(-Выборка.КоличествоОстаток, НСтр("ru = 'единицы,единиц,единиц'"), Истина);
			ОписаниеОшибки	= НСтр("ru = 'Для проведения операции, в складской ячейке ""%1"" не хватает %2 груза %3%4.'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Выборка.СкладскаяЯчейка, ТекстКоличествоГрузов, Выборка.ГрузовоеМесто, Тара);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки);
			
			Событие = Нстр("ru = 'Ошибка при проведении пересчета грузов'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ОписаниеОшибки);    
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры	

// Выполняется упорядочивание порядка точке маршрута в соответсвии с типовым шаблоном.
//
// Параметры:
//  Маршрут			  - ТаблицаЗначений / ДанныеФормыКоллекция - маршрут рейса
//  МаршрутСледования - СправочникСсылка.упТиповыеМаршруты
//
Процедура УпорядочитьТочкиМаршрутаПоШаблону(Маршрут, МаршрутСледования) Экспорт
	
	Если Не ЗначениеЗаполнено(МаршрутСледования) Или Маршрут.Количество() < 3 Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упТиповыеМаршрутыМаршрут.НомерСтроки КАК НомерСтроки,
	|	упТиповыеМаршрутыМаршрут.Адрес КАК Адрес
	|ИЗ
	|	Справочник.упТиповыеМаршруты.Маршрут КАК упТиповыеМаршрутыМаршрут
	|ГДЕ
	|	упТиповыеМаршрутыМаршрут.Ссылка = &МаршрутСледования
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("МаршрутСледования", МаршрутСледования);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		тзАдресов = РезультатЗапроса.Выгрузить();
		
		КопияМаршрута = Маршрут.Скопировать();
		УспешноеУпорядочивание = Истина;
		
		Сч = 0; // возврат
		
		Для каждого Точка Из тзАдресов Цикл
			НайденныеСтроки = Маршрут.НайтиСтроки(Новый Структура("Адрес", Точка.Адрес));
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				Если Не упРейс.ПроверитьСдвинутьСтрокуМаршрутаСУчетомПредшественников(Маршрут, НайденнаяСтрока, Сч) Тогда 
					УспешноеУпорядочивание = Ложь;
				КонецЕсли; 
				Сч = Сч + 1;
				Прервать; // в случае планирования с нескольких складов в тзАдресов будут присутствовать склады, для которых могут быть возвраты в рамках одного маршрута
			КонецЦикла;
		КонецЦикла; 
		
		Для ИндексТочки = Сч По Маршрут.Количество() - 1 Цикл
			НайденнаяСтрока = Маршрут[ИндексТочки];
			Если Не упРейс.ПроверитьСдвинутьСтрокуМаршрутаСУчетомПредшественников(Маршрут, НайденнаяСтрока, ИндексТочки) Тогда 
				УспешноеУпорядочивание = Ложь;
			КонецЕсли; 
		КонецЦикла;
		
		Если Не УспешноеУпорядочивание Тогда
			Маршрут = КопияМаршрута;
		КонецЕсли; 
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Возвращает возможность корректировки маршрута.
//
Функция РазрешеноКорректироватьМаршрут(Период, Ссылка, Рейс, Задания = Неопределено) Экспорт
	
	текРезультат = Истина;
	
	Если Ложь
		Или ЗначениеЗаполнено(Период) 
		Или ЗначениеЗаполнено(Ссылка)
	Тогда
		Запрос = Новый Запрос;
		
		Если Задания <> Неопределено Тогда
			
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
						|	тбЗадания.Задание
						|ПОМЕСТИТЬ втЗадания
						|ИЗ
						|	&Задания КАК тбЗадания
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	тбЗадания.Задание
						|ИЗ
						|	втЗадания КАК тбЗадания
						|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза КАК упСтатусыЗаданийНаПеревозкуГруза
						|		ПО тбЗадания.Задание = упСтатусыЗаданийНаПеревозкуГруза.Документ
						|ГДЕ
						|	упСтатусыЗаданийНаПеревозкуГруза.Период > &Период";
			Запрос.Текст = Запрос.Текст + "
						   |
						   |ОБЪЕДИНИТЬ ВСЕ
						   |
						   |";
			
			Запрос.УстановитьПараметр("Задания", Задания);
			
		КонецЕсли;
		
		Запрос.Текст =  Запрос.Текст + "ВЫБРАТЬ
		|	упМаршрутПоРейсу.Регистратор
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
		|ГДЕ
		|	упМаршрутПоРейсу.Рейс = &Рейс
		|	И упМаршрутПоРейсу.Активность
		|	И НЕ упМаршрутПоРейсу.Регистратор = &Регистратор
		|	И упМаршрутПоРейсу.Активность
		|	И упМаршрутПоРейсу.Период >= &Период
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упРаботыПоРейсу.Регистратор
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
		|ГДЕ
		|	упРаботыПоРейсу.Рейс = &Рейс
		|	И НЕ упРаботыПоРейсу.Регистратор = &Регистратор
		|	И упРаботыПоРейсу.Период >= &Период";
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("Рейс", Рейс);
		Запрос.УстановитьПараметр("Период", Период);
		
		текРезультат = Запрос.Выполнить().Пустой();
	КонецЕсли;
			
	Возврат текРезультат;
		
КонецФункции

Функция ИзмененМаршрутДляМобильногоКлиента(Документ)
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Период КАК Период,
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсу.Отменена КАК Отменена,
	|	упМаршрутПоРейсу.Отложена КАК Отложена,
	|	упМаршрутПоРейсу.СтрокаНомер КАК ПорядокПрохождения
	|ПОМЕСТИТЬ втТекущиеЗаписи
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|	МАКСИМУМ(упМаршрутПоРейсу.Отменена) КАК Отменена,
	|	МАКСИМУМ(упМаршрутПоРейсу.Отложена) КАК Отложена,
	|	МАКСИМУМ(упМаршрутПоРейсу.СтрокаНомер) КАК ПорядокПрохождения
	|ПОМЕСТИТЬ втПредшествующиеЗаписи
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			упМаршрутПоРейсу.Рейс КАК Рейс,
	|			упМаршрутПоРейсу.Идентификатор КАК Идентификатор,
	|			МАКСИМУМ(упМаршрутПоРейсу.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТекущиеЗаписи КАК втТекущиеЗаписи
	|				ПО упМаршрутПоРейсу.Рейс = втТекущиеЗаписи.Рейс
	|					И упМаршрутПоРейсу.Идентификатор = втТекущиеЗаписи.Идентификатор
	|					И упМаршрутПоРейсу.Период < втТекущиеЗаписи.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			упМаршрутПоРейсу.Рейс,
	|			упМаршрутПоРейсу.Идентификатор) КАК ВложенныйЗапрос
	|		ПО упМаршрутПоРейсу.Период = ВложенныйЗапрос.Период
	|			И упМаршрутПоРейсу.Рейс = ВложенныйЗапрос.Рейс
	|			И упМаршрутПоРейсу.Идентификатор = ВложенныйЗапрос.Идентификатор
	|
	|СГРУППИРОВАТЬ ПО
	|	упМаршрутПоРейсу.Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПрохождениеТочки.Ссылка
	|ПОМЕСТИТЬ втПрохожденияБезМК
	|ИЗ
	|	Документ.упПрохождениеТочки КАК упПрохождениеТочки
	|ГДЕ
	|	упПрохождениеТочки.Ссылка = &Ссылка
	|	И упПрохождениеТочки.СобытиеНачалаРаботНаТочке = ЗНАЧЕНИЕ(Документ.упСобытиеМониторинга.ПустаяСсылка)
	|	И упПрохождениеТочки.СобытиеПрибытияНаТочку = ЗНАЧЕНИЕ(Документ.упСобытиеМониторинга.ПустаяСсылка)
	|	И упПрохождениеТочки.СобытиеУбытияСТочки = ЗНАЧЕНИЕ(Документ.упСобытиеМониторинга.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеЗаписи.Рейс,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втПредшествующиеЗаписи.Идентификатор ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДобавленыТочки,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втТекущиеЗаписи.Отменена
	|					И НЕ ЕСТЬNULL(втПредшествующиеЗаписи.Отменена, ИСТИНА)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтмененыТочки,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втТекущиеЗаписи.Отложена
	|					И НЕ ЕСТЬNULL(втПредшествующиеЗаписи.Отложена, ИСТИНА)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтложеныТочки,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ втПредшествующиеЗаписи.ПорядокПрохождения ЕСТЬ NULL
	|					И втТекущиеЗаписи.ПорядокПрохождения <> втПредшествующиеЗаписи.ПорядокПрохождения
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ИзмененПорядокТочек,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втПрохожденияБезМК.Ссылка ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК НовыеПрохожденияБезМК
	|ИЗ
	|	втТекущиеЗаписи КАК втТекущиеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредшествующиеЗаписи КАК втПредшествующиеЗаписи
	|		ПО втТекущиеЗаписи.Идентификатор = втПредшествующиеЗаписи.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПрохожденияБезМК КАК втПрохожденияБезМК
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТекущиеЗаписи.Рейс";
	Запрос.УстановитьПараметр("Ссылка", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = (Выборка.ДобавленыТочки Или Выборка.ОтмененыТочки Или Выборка.ОтложеныТочки Или Выборка.ИзмененПорядокТочек Или Выборка.НовыеПрохожденияБезМК);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ИзмененМаршрутДляМобильногоКлиента()

Функция ИзмененыРаботыДляМобильногоКлиента(Документ)
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРаботыПоРейсу.Период КАК Период,
	|	упРаботыПоРейсу.Рейс КАК Рейс,
	|	упРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	упРаботыПоРейсу.Сумма,
	|	упРаботыПоРейсу.Отменена КАК Отменена,
	|	упРаботыПоРейсу.Отложена КАК Отложена,
	|	упРаботыПоРейсу.ГрузРазделен
	|ПОМЕСТИТЬ втТекущиеЗаписи
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	МАКСИМУМ(упРаботыПоРейсу.Сумма) КАК Сумма,
	|	МАКСИМУМ(упРаботыПоРейсу.Отменена) КАК Отменена,
	|	МАКСИМУМ(упРаботыПоРейсу.Отложена) КАК Отложена
	|ПОМЕСТИТЬ втПредшествующиеЗаписи
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			упРаботыПоРейсу.Рейс КАК Рейс,
	|			упРаботыПоРейсу.Идентификатор КАК Идентификатор,
	|			упРаботыПоРейсу.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|			МАКСИМУМ(упРаботыПоРейсу.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТекущиеЗаписи КАК втТекущиеЗаписи
	|				ПО упРаботыПоРейсу.Рейс = втТекущиеЗаписи.Рейс
	|					И упРаботыПоРейсу.Идентификатор = втТекущиеЗаписи.Идентификатор
	|					И упРаботыПоРейсу.ИдентификаторРаботы = втТекущиеЗаписи.ИдентификаторРаботы
	|					И упРаботыПоРейсу.Период < втТекущиеЗаписи.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			упРаботыПоРейсу.Рейс,
	|			упРаботыПоРейсу.Идентификатор,
	|			упРаботыПоРейсу.ИдентификаторРаботы) КАК ВложенныйЗапрос
	|		ПО упРаботыПоРейсу.Период = ВложенныйЗапрос.Период
	|			И упРаботыПоРейсу.Рейс = ВложенныйЗапрос.Рейс
	|			И упРаботыПоРейсу.Идентификатор = ВложенныйЗапрос.Идентификатор
	|			И упРаботыПоРейсу.ИдентификаторРаботы = ВложенныйЗапрос.ИдентификаторРаботы
	|
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсу.Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеЗаписи.Рейс,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втПредшествующиеЗаписи.ИдентификаторРаботы ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДобавленыРаботы,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втТекущиеЗаписи.Отменена
	|					И НЕ ЕСТЬNULL(втПредшествующиеЗаписи.Отменена, ИСТИНА)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтмененыРаботы,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втТекущиеЗаписи.Отложена
	|					И НЕ ЕСТЬNULL(втПредшествующиеЗаписи.Отложена, ИСТИНА)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтложеныРаботы,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втТекущиеЗаписи.ГрузРазделен
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ГрузРазделен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА втТекущиеЗаписи.Сумма <> ЕСТЬNULL(втПредшествующиеЗаписи.Сумма, 0)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ИзмененаСуммаИнкассации
	|ИЗ
	|	втТекущиеЗаписи КАК втТекущиеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредшествующиеЗаписи КАК втПредшествующиеЗаписи
	|		ПО втТекущиеЗаписи.Идентификатор = втПредшествующиеЗаписи.Идентификатор
	|			И втТекущиеЗаписи.ИдентификаторРаботы = втПредшествующиеЗаписи.ИдентификаторРаботы
	|
	|СГРУППИРОВАТЬ ПО
	|	втТекущиеЗаписи.Рейс";
	Запрос.УстановитьПараметр("Ссылка", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = (Выборка.ДобавленыРаботы Или Выборка.ОтмененыРаботы Или Выборка.ОтложеныРаботы Или Выборка.ИзмененаСуммаИнкассации Или Выборка.ГрузРазделен);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ИзмененыРаботыДляМобильногоКлиента()

Функция РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу(Рейс, ДополнительныеСвойства = Неопределено) Экспорт
	
	Результат = Ложь;
	
	МассивНедоступныхОпераций = Новый Массив;
	МассивНедоступныхОпераций.Добавить(Перечисления.упТипыОпераций.Погрузка);
	МассивНедоступныхОпераций.Добавить(Перечисления.упТипыОпераций.ВыемкаЦенностей);
	МассивНедоступныхОпераций.Добавить(Перечисления.упТипыОпераций.ПолучениеДокументов);
	МассивНедоступныхОпераций.Добавить(Перечисления.упТипыОпераций.ДополнительнаяОперация);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
	|ГДЕ
	|	НЕ упРаботыПоРейсуСрезПоследних.Отменена
	|	И НЕ упРаботыПоРейсуСрезПоследних.Выполнена
	|	И НЕ упРаботыПоРейсуСрезПоследних.Отложена
	|	И упРаботыПоРейсуСрезПоследних.Операция В(&МассивНедоступныхОпераций)";
	Запрос.УстановитьПараметр("МассивНедоступныхОпераций", МассивНедоступныхОпераций);
	Запрос.УстановитьПараметр("Рейс", Рейс);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Результат = Истина; 
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

#Область ДобавлениеЗаданийВМаршрут

// Выполним добавление работ по заданию.
//
// Параметры:
//  Маршрут									 - Структура			 - содрежит в себе описание маршрута в зависимости от реализации.
//  Работы									 - ТаблицаЗначений		 - Таблица работ.
//  АдресОтправления						 - СправочникСсылка.упАдрес	 - ссылка на адрес.
//  АдресПолучения							 - СправочникСсылка.упАдрес	 - ссылка на адрес.
//  Задание									 - ДокументСсылка.упЗаданиеНаПеревозкуГруза	 - Ссылка на документ.
//  мсвГрузовыхМест							 - Массив									 - Массив грузовых мест.
//  ПолучитьДопРаботы						 - Булево									 - Флаг получение рбаот.
//  СтруктураРаботПоЗаданию					 - Структура								 - Работы по заданию.
//  ВидЗоныДляФормированияПредставленияРейса - ПеречислениеСсылка						 - Сылка на элемент.
//  РаспределитьПоНепройденнымТочкам		 - Булево									 - Флаг выполнения распределения.
//  ВариантФормированияМаршрута				 - 											 - 
//  СтруктураТары							 - 											 - 
//  СтруктураСвязанных						 - 											 - 
//  ЭтоВозвратВБазовыйАдрес					 - 											 - 
//  ЧасовойПоясРейса						 - СправочникСсылка.упЧасовыеПояса			 - Часовой пояс в котором выполняется рейс, все временные окна заданий будут преобразованы к нему.
// 
// Возвращаемое значение:
//  Структура - Результат выполнения.
//
Функция ДобавитьРаботыПоЗаданиюВМаршрут(Рейс, Маршрут, Работы = Неопределено, АдресОтправления, АдресПолучения, Задание, мсвГрузовыхМест = Неопределено, ПолучитьДопРаботы, СтруктураРаботПоЗаданию = Неопределено,
	ВидЗоныДляФормированияПредставленияРейса = Неопределено, РаспределитьПоНепройденнымТочкам = Ложь, ВариантФормированияМаршрута = Неопределено, СтруктураТары = Неопределено,
	СтруктураСвязанных = Неопределено, ЭтоВозвратВБазовыйАдрес = Неопределено, ЧасовойПоясРейса = Неопределено, Знач соотВидовАдресов = Неопределено) Экспорт
	
	// Если Ложь Тогда // Для контекстной подсказки, в контексте клиенте недоступно
	//    Задание = Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка();
	//	АдресОтправления = Справочники.упВидыАдресов.ПустаяСсылка();
	//	АдресПолучения = Справочники.упВидыАдресов.ПустаяСсылка();
	//	Маршрут = РегистрыСведений.упМаршрутПоРейсу.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	// КонецЕсли;
	
	мсвВидыТочекОтправленияВидаПеревозки = Справочники.упВидыПеревозок.ВидыТочекОтправления(Задание.ВидПеревозки);
	
	Если ЭтоВозвратВБазовыйАдрес = Неопределено Тогда
		ЭтоВозвратВБазовыйАдрес = Истина
			И мсвВидыТочекОтправленияВидаПеревозки.Найти(АдресПолучения.ВидАдреса) <> Неопределено
			И мсвВидыТочекОтправленияВидаПеревозки.Найти(АдресОтправления.ВидАдреса) = Неопределено;
	КонецЕсли;
	ДобавлениеИзФормы = НЕ ВидЗоныДляФормированияПредставленияРейса = Неопределено;
	сткРезультат = Новый Структура("ТочкаПогрузки, ТочкаРазгрузки");
	мсвКонтрольныхТочек = Новый Массив;
	
	ВариантМаршрутаПолный = ВариантФормированияМаршрутаПолный(ВариантФормированияМаршрута);
	
	// Инициализируем массивы для хранения порядковых номера точек в маршруте,
	//   посещение которых перед текущей точкой является обязательным.
	мсвПредшественниковТочкиПогрузки = Новый Массив;
	АдресаПредшественниковПогрузки = Новый Массив;
	
	Если ВариантМаршрутаПолный Тогда
		мсвПредшественниковТочкиРазгрузки = Новый Массив;
		мсвПотомковТочкиПогрузки = Новый Массив;
		мсвПотомковТочкиРазгрузки = Новый Массив;
	КонецЕсли;
	
	ИндексВставкиПервойТочкиСклада = 0;
	ПерваяНепройденнаяТочка = Неопределено;
	ПройденныеТочки = Маршрут.НайтиСтроки(Новый Структура("Пройдена", Истина));
	Если ПройденныеТочки.Количество() > 0 Тогда
		ИндексВставкиПервойТочкиСклада = Маршрут.Индекс(ПройденныеТочки[ПройденныеТочки.ВГраница()]) + 1;
	Иначе
		ИндексВставкиПервойТочкиСклада = 0;
	КонецЕсли;
	Если Маршрут.Количество() > ИндексВставкиПервойТочкиСклада И Маршрут[ИндексВставкиПервойТочкиСклада].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления") Тогда
		ИдентификаторТочкиГаража = "" + Маршрут[ИндексВставкиПервойТочкиСклада].Идентификатор;
		мсвПредшественниковТочкиПогрузки.Добавить(ИдентификаторТочкиГаража);
		АдресаПредшественниковПогрузки.Добавить(Маршрут[ИндексВставкиПервойТочкиСклада].Адрес);
		ИндексВставкиПервойТочкиСклада = ИндексВставкиПервойТочкиСклада + 1;
		Если ВариантМаршрутаПолный Тогда
			мсвПредшественниковТочкиРазгрузки.Добавить(ИдентификаторТочкиГаража);
		КонецЕсли;
	КонецЕсли; 
	Если Маршрут.Количество() > ИндексВставкиПервойТочкиСклада И Маршрут[Маршрут.Количество() - 1].ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
		ИдентификаторТочкиГаража = "" + Маршрут[Маршрут.Количество() - 1].Идентификатор;
		мсвПотомковТочкиРазгрузки.Добавить(ИдентификаторТочкиГаража);
		Если ВариантМаршрутаПолный Тогда
			мсвПотомковТочкиПогрузки.Добавить(ИдентификаторТочкиГаража);
		КонецЕсли;
	КонецЕсли; 
	Если ЭтоВозвратВБазовыйАдрес И Не АдресПолучения.ВидАдреса.НеИспользоватьКакБазовыйАдресОтправления Тогда
		ТочкиСкладов = Маршрут.НайтиСтроки(Новый Структура("Адрес, ТипТочки, Пройдена, Отменена", АдресПолучения, ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию"), Ложь, Ложь));
		Если ПройденныеТочки.Количество() = 0 И ТочкиСкладов.Количество() = 0 Тогда 
			ТочкиСкладов.Добавить(ДобавитьТочкуВМаршрут(Маршрут, АдресПолучения, ВидЗоныДляФормированияПредставленияРейса, ИндексВставкиПервойТочкиСклада, ДобавлениеИзФормы));
		КонецЕсли; 
		Если ТочкиСкладов.Количество() > 0 Тогда
			мсвПредшественниковТочкиПогрузки.Добавить("" + ТочкиСкладов[0].Идентификатор);
			АдресаПредшественниковПогрузки.Добавить(АдресПолучения);
		КонецЕсли; 
	ИначеЕсли ЭтоВозвратВБазовыйАдрес Тогда
		ТочкиСкладов = Маршрут.НайтиСтроки(Новый Структура("Адрес, ТипТочки, Пройдена, Отменена", АдресПолучения, ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию"), Ложь, Ложь));
		Если ТочкиСкладов.Количество() > 0 И ТочкиСкладов[0] = Маршрут[ИндексВставкиПервойТочкиСклада] Тогда
			//мсвПредшественниковТочкиПогрузки.Добавить("" + ТочкиСкладов[0].Идентификатор);
			АдресаПредшественниковПогрузки.Добавить(АдресПолучения);
		КонецЕсли; 
	// Закомментировано 01.06.2017, т.к. дальше могут добавляться противоречивые ограничения по таре или связанным грузам
	//ИначеЕсли Не ЭтоВозвратВБазовыйАдрес Тогда
	//	Если Истина
	//		И Маршрут.Количество() > ИндексВставкиПервойТочкиСклада 
	//		И Маршрут[ИндексВставкиПервойТочкиСклада].Адрес <> АдресОтправления
	//		И ПройденныеТочки.Количество() = 0
	//	Тогда
	//		мсвПотомковТочкиПогрузки.Добавить("" + Маршрут[ИндексВставкиПервойТочкиСклада].Идентификатор);
	//		//АдресаПотомковПогрузки.Добавить(АдресПолучения);
	//	КонецЕсли; 
	КонецЕсли; 
	Если ВариантМаршрутаПолный Тогда
		Если СтруктураТары = Неопределено Тогда
			Если СтруктураСвязанных <> Неопределено Тогда
				мсвДетей     = СтруктураСвязанных.Дети;
				мсвРодителей = СтруктураСвязанных.Родители;
				
				Если мсвДетей.Количество() > 0 Тогда
					// Обязательные условия:
					//  * погрузка родительского задания должна быть раньше погрузки и разгрузки зависимого задания
					//  * разгрузка родительского задания должна быть раньше погрузки и разгрузки зависимого задания
					Для каждого текРабота Из Работы Цикл
						Если ЗначениеЗаполнено(текРабота.Задание) И мсвДетей.Найти(текРабота.Задание) <> Неопределено Тогда
							Если упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(текРабота.Операция) Тогда
								мсвПотомковТочкиПогрузки.Добавить("" + текРабота.Идентификатор);
								мсвПотомковТочкиРазгрузки.Добавить("" + текРабота.Идентификатор);
							КонецЕсли; 
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли;
				
				Если мсвРодителей.Количество() > 0 Тогда
					ВМаршрутДобавленРодитель = Ложь;
					// Обязательные условия:
					//  * погрузка зависимого задания должна быть позже погрузки и разгрузки родительского задания
					//  * разгрузка зависимого задания должна быть позже погрузки и разгрузки родительского задания
					Для каждого текРабота Из Работы Цикл
						Если ЗначениеЗаполнено(текРабота.Задание) И мсвРодителей.Найти(текРабота.Задание) <> Неопределено Тогда
							Если упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(текРабота.Операция) Тогда
								мсвПредшественниковТочкиПогрузки.Добавить("" + текРабота.Идентификатор);
								мсвПредшественниковТочкиРазгрузки.Добавить("" + текРабота.Идентификатор);
								ВМаршрутДобавленРодитель = Истина;
							КонецЕсли; 
						КонецЕсли; 
					КонецЦикла;
					
					Если Не ВМаршрутДобавленРодитель Тогда
						Для каждого Родитель Из мсвРодителей Цикл
							АдресаПредшественниковПогрузки.Добавить(упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Родитель, "АдресОтправления"));
						КонецЦикла;
					КонецЕсли; 
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			мсвТары  = СтруктураТары.Тара;
			мсвВТаре = СтруктураТары.ВТаре;
			
			ЗаявкаНаПеревозкуГруза = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задание, "ЗаявкаНаПеревозкуГруза");
			Если мсвТары.Количество() Тогда
				// Обязательные условия:
				//  * погрузка тары должна быть раньше (или одновременно) погрузки грузового места в эту тару
				//  * разгрузка тары должна быть позже (или одновременно) разгрузки грузового места из этой тары
				Для каждого текРабота Из Работы Цикл
					Если ЗаявкаНаПеревозкуГруза <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текРабота.Задание, "ЗаявкаНаПеревозкуГруза") Тогда
						Продолжить;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(текРабота.Тара) И Не мсвТары.Найти(текРабота.Тара) = Неопределено Тогда
						мсвПредшественниковТочкиРазгрузки.Добавить("" + текРабота.Идентификатор);
						мсвПотомковТочкиПогрузки.Добавить("" + текРабота.Идентификатор);
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
			
			Если мсвВТаре.Количество() Тогда
				// Обязательные условия:
				//  * погрузка грузового места в тару должна быть позже (или одновременно) погрузки тары
				//  * разгрузка грузового места должна быть позже (или одновременно) погрузки тары
				//  * разгрузка тары должна быть после (или одновременно) разгрузки грузового места их этой тары
				Для каждого текРабота Из Работы Цикл
					Если ЗаявкаНаПеревозкуГруза <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текРабота.Задание, "ЗаявкаНаПеревозкуГруза") Тогда
						Продолжить;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(текРабота.ГрузовоеМесто) И Не мсвВТаре.Найти(текРабота.ГрузовоеМесто) = Неопределено Тогда
						Если упПрохождениеТочкиКлиентСервер.ОперацияПогрузки(текРабота.Операция) Тогда
							мсвПредшественниковТочкиПогрузки.Добавить("" + текРабота.Идентификатор);
							мсвПредшественниковТочкиРазгрузки.Добавить("" + текРабота.Идентификатор);
						КонецЕсли; 
						
						Если упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(текРабота.Операция) Тогда
							мсвПотомковТочкиПогрузки.Добавить("" + текРабота.Идентификатор);
							мсвПотомковТочкиРазгрузки.Добавить("" + текРабота.Идентификатор);
						КонецЕсли; 
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
			
		КонецЕсли; 
		
		Если СтруктураТары <> Неопределено Или СтруктураСвязанных <> Неопределено Тогда
			мсвПредшественниковТочкиПогрузки  = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПредшественниковТочкиПогрузки);
			мсвПредшественниковТочкиРазгрузки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПредшественниковТочкиРазгрузки);
			мсвПотомковТочкиПогрузки          = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПотомковТочкиПогрузки);
			мсвПотомковТочкиРазгрузки         = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвПотомковТочкиРазгрузки);
		КонецЕсли;
		АдресаПредшественниковПогрузки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(АдресаПредшественниковПогрузки);
		
		сткТочкиПогрузкиРазгрузки = ПодобратьТочкиДляЗадания(Маршрут, Работы, АдресОтправления, АдресПолучения, 
																мсвПредшественниковТочкиПогрузки, мсвПотомковТочкиПогрузки,
																мсвПредшественниковТочкиРазгрузки, мсвПотомковТочкиРазгрузки, РаспределитьПоНепройденнымТочкам, АдресаПредшественниковПогрузки);
		текТочкаПогрузки		  = сткТочкиПогрузкиРазгрузки.ТочкаПогрузки;
		текТочкаРазгрузки		  = сткТочкиПогрузкиРазгрузки.ТочкаРазгрузки;
		текТочкаРазгрузкаПодобранная = сткТочкиПогрузкиРазгрузки.ТочкаРазгрузкиПодобранная;
			
		// Если точки погрузки и разгрузки не найдены.
		Если текТочкаПогрузки = Неопределено И текТочкаРазгрузки = Неопределено Тогда
			
			// Добавить точки перед точкой завершения или в конец.
			текКоличествоСтрок = Маршрут.Количество();
			Если текКоличествоСтрок > 0 Тогда
				
				текИндексСтрокиПогрузки = Неопределено; 
				Если Не мсвПредшественниковТочкиПогрузки.Количество() Тогда
					Для каждого текТочкаМаршрута Из Маршрут Цикл
						Если Не (текТочкаМаршрута.Пройдена Или текТочкаМаршрута.Отменена) Тогда
							текИндексСтрокиПогрузки = Маршрут.Индекс(текТочкаМаршрута);
							Прервать;
						КонецЕсли; 
					КонецЦикла;				
				КонецЕсли; 
				
				Если текИндексСтрокиПогрузки = Неопределено Тогда
					//текИндексСтрокиПогрузки	= текКоличествоСтрок - 1;
					текИндексСтрокиПогрузки	= текКоличествоСтрок;
					текПоследняяТочка = Маршрут[текИндексСтрокиПогрузки - 1];
					Если текПоследняяТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
						текИндексСтрокиПогрузки = текИндексСтрокиПогрузки - 1;
					КонецЕсли;
					
					Если мсвПотомковТочкиПогрузки.Количество() Тогда // определим, куда вставить новую точку: рашьше посещения потомков
						Для каждого текИдентификаторПотомка Из мсвПотомковТочкиПогрузки Цикл
							текИндексСтрокиПогрузки = Мин(текИндексСтрокиПогрузки, Маршрут.Индекс(Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПотомка), "Идентификатор")));
						КонецЦикла; 
					КонецЕсли; 
				КонецЕсли; 
				
				текИндексСтрокиРазгрузки = текИндексСтрокиПогрузки;
				Если мсвПредшественниковТочкиРазгрузки.Количество() Тогда
					Для каждого текИдентификаторПредшественника Из мсвПредшественниковТочкиРазгрузки Цикл
						текИндексСтрокиРазгрузки = Макс(текИндексСтрокиРазгрузки, Маршрут.Индекс(Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПредшественника), "Идентификатор")) + 1);
					КонецЦикла; 
				КонецЕсли; 
				
				Если Маршрут.Количество() > 1  Тогда
					Если соотВидовАдресов = Неопределено Тогда
						мсвАдресов = Новый Массив;
						Для каждого текТочка Из Маршрут Цикл
							мсвАдресов.Добавить(текТочка.Адрес);
						КонецЦикла;
						соотВидовАдресов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(мсвАдресов, "ВидАдреса");	
					КонецЕсли;
					
					Для каждого текТочка Из Маршрут Цикл
						сткЗначения = соотВидовАдресов.Получить(текТочка.Адрес);
						Если Не сткЗначения = Неопределено Тогда
							Если Не мсвВидыТочекОтправленияВидаПеревозки.Найти(сткЗначения.ВидАдреса) = Неопределено Тогда
								текИндекс = Маршрут.Индекс(текТочка);
								текИндексСтрокиРазгрузки = Макс(текИндексСтрокиРазгрузки, ?(текИндексСтрокиПогрузки > текИндекс, текИндекс, текИндекс + 1));	
							КонецЕсли;
							соотВидовАдресов.Удалить(текТочка.Адрес);	
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли; 
				
				текИндексСтрокиРазгрузки = текИндексСтрокиРазгрузки + 1;
				
				//текПоследняяТочка = Маршрут[текИндексСтрокиПогрузки];
				//Если Не текПоследняяТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
				//	текИндексСтрокиПогрузки 	= Неопределено;
				//	текИндексСтрокиРазгрузки 	= Неопределено;
				//КонецЕсли;
			Иначе	
				текИндексСтрокиПогрузки 	= Неопределено;
				текИндексСтрокиРазгрузки 	= Неопределено;
			КонецЕсли;
			
			текТочкаПогрузки 			= ДобавитьТочкуВМаршрут(Маршрут, АдресОтправления, ВидЗоныДляФормированияПредставленияРейса, текИндексСтрокиПогрузки, ДобавлениеИзФормы);
			текТочкаРазгрузки 			= ДобавитьТочкуВМаршрут(Маршрут, АдресПолучения, ВидЗоныДляФормированияПредставленияРейса, текИндексСтрокиРазгрузки, ДобавлениеИзФормы);
		ИначеЕсли текТочкаПогрузки <> Неопределено И текТочкаРазгрузки = Неопределено Тогда
			// Если найдена только точка погрузки.
			
			// Добавить точку разгрузки в конец пула разгрузки после точки погрузки.
			Если текТочкаРазгрузкаПодобранная = Неопределено Тогда
				текИндексСтроки = Маршрут.Индекс(текТочкаПогрузки);
			Иначе	
				текИндексСтроки = Маршрут.Индекс(текТочкаРазгрузкаПодобранная);
			КонецЕсли;
			
			ДобавитьПредшественниковКСуществующим(мсвПредшественниковТочкиПогрузки, текТочкаПогрузки);
			текИндексСтроки = текИндексСтроки + 1;
			Если мсвПредшественниковТочкиРазгрузки.Количество() Тогда
				Для каждого текИдентификаторПредшественника Из мсвПредшественниковТочкиРазгрузки Цикл
					текИндексСтроки = Макс(текИндексСтроки, Маршрут.Индекс(Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПредшественника), "Идентификатор")) + 1);
				КонецЦикла; 
			КонецЕсли;
			
			Если мсвПредшественниковТочкиРазгрузки.Количество() > 1 Тогда
				// Если в маршруте есть точка склада и точка возврата на склад, 
				// вставляем новую точку разгрузки перед возвратом на склад.
				НайденНовыйИндекс = Ложь;                
				ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Адрес", АдресОтправления));
				Если ИскомыеСтроки.Количество() > 1 Тогда
					Для каждого НайденнаяСтрока Из ИскомыеСтроки Цикл
						Если НайденнаяСтрока <> текТочкаПогрузки Тогда
							текИндексСтроки = Макс(текИндексСтроки, Маршрут.Индекс(НайденнаяСтрока));
							НайденНовыйИндекс = Истина;
							Прервать;
						КонецЕсли;                                                                      
					КонецЦикла;
				КонецЕсли;
				Если НайденНовыйИндекс Тогда
					текИндексСтрокиРазгрузки = текИндексСтроки;
				Иначе  
					текИндексСтрокиРазгрузки = Маршрут.Количество();
				КонецЕсли;
			Иначе
				Если мсвВидыТочекОтправленияВидаПеревозки.Найти(АдресПолучения.ВидАдреса) = Неопределено Тогда
					текИндексСтрокиРазгрузки = текИндексСтроки; // 20180221 - изменяем алгоритм вставки новой точки разгрузки: вставляем ее не в конец маршрута, а как можно ближе к точке погрузки
					// решение выбрано из-за проблемы дублирования точек сдачи инкассации, если сдача производится в отдельной точке и используется механизм связанных грузов 
				Иначе
					КоличествоТочек = Маршрут.Количество();
					текПоследняяТочка = Маршрут[КоличествоТочек - 1];
					Если текПоследняяТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
						текИндексСтрокиРазгрузки 	= КоличествоТочек - 1;
					Иначе
						текИндексСтрокиРазгрузки 	= КоличествоТочек;
					КонецЕсли;	
				КонецЕсли; 
			КонецЕсли;
			
			Если мсвПотомковТочкиРазгрузки.Количество() Тогда // определим, куда вставить новую точку: рашьше посещения потомков
				Для каждого текИдентификаторПотомка Из мсвПотомковТочкиРазгрузки Цикл
					текИндексСтрокиРазгрузки = Мин(текИндексСтрокиРазгрузки, Маршрут.Индекс(Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПотомка), "Идентификатор")));
				КонецЦикла; 
			КонецЕсли;
			
			текИндексСтроки = Макс(текИндексСтроки, текИндексСтрокиРазгрузки);
			
			текТочкаРазгрузки = ДобавитьТочкуВМаршрут(Маршрут, АдресПолучения, ВидЗоныДляФормированияПредставленияРейса, текИндексСтроки, ДобавлениеИзФормы);
		ИначеЕсли текТочкаРазгрузки <> Неопределено И текТочкаПогрузки = Неопределено Тогда
			// Если найдена только точка разгрузки.
			
			// Добавить точку погрузки перед точкой разгрузки.
			текИндексСтроки = Маршрут.Индекс(текТочкаРазгрузки);
			
			ДобавитьПредшественниковКСуществующим(мсвПредшественниковТочкиРазгрузки, текТочкаРазгрузки);
			Если мсвПотомковТочкиПогрузки.Количество() Тогда // определим, куда вставить новую точку: рашьше посещения потомков
				Для каждого текИдентификаторПотомка Из мсвПотомковТочкиПогрузки Цикл
					текИндексСтроки = Мин(текИндексСтроки, Маршрут.Индекс(Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПотомка), "Идентификатор")));
				КонецЦикла; 
			КонецЕсли; 
			
			Если Маршрут.Количество() > 1  Тогда
				Если соотВидовАдресов = Неопределено Тогда
					мсвАдресов = Новый Массив;
					Для каждого текТочка Из Маршрут Цикл
						мсвАдресов.Добавить(текТочка.Адрес);
					КонецЦикла;
					соотВидовАдресов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(мсвАдресов, "ВидАдреса");	
				КонецЕсли;
				
				Для каждого текТочка Из Маршрут Цикл
					текИндекс = Маршрут.Индекс(текТочка);
					Если текИндекс > текИндексСтроки Тогда
					    Прервать;
					КонецЕсли; 
					сткЗначения = соотВидовАдресов.Получить(текТочка.Адрес);
					Если Не сткЗначения = Неопределено Тогда
						Если Не мсвВидыТочекОтправленияВидаПеревозки.Найти(сткЗначения.ВидАдреса) = Неопределено Тогда
							текИндексСтроки = Мин(текИндексСтроки, текИндекс + 1);	
						КонецЕсли;
						соотВидовАдресов.Удалить(текТочка.Адрес);	
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
			
			текТочкаПогрузки = ДобавитьТочкуВМаршрут(Маршрут, АдресОтправления, ВидЗоныДляФормированияПредставленияРейса, текИндексСтроки, ДобавлениеИзФормы);
		Иначе
			ДобавитьПредшественниковКСуществующим(мсвПредшественниковТочкиПогрузки, текТочкаПогрузки);
			ДобавитьПредшественниковКСуществующим(мсвПредшественниковТочкиРазгрузки, текТочкаРазгрузки);
		КонецЕсли;
		Если мсвПредшественниковТочкиРазгрузки.Найти("" + текТочкаПогрузки.Идентификатор) = Неопределено Тогда
			//Для Каждого ПредшественникПогрузки Из мсвПредшественниковТочкиПогрузки Цикл
			//	мсвПредшественниковТочкиРазгрузки.Добавить(ПредшественникПогрузки);
			//КонецЦикла;
			мсвПредшественниковТочкиРазгрузки.Добавить("" + текТочкаПогрузки.Идентификатор);
		КонецЕсли; 
	Иначе	
		сткТочкиПогрузкиРазгрузки = ПодобратьТочкуПогрузки(Маршрут, Работы, АдресОтправления, РаспределитьПоНепройденнымТочкам);
		текТочкаПогрузки = сткТочкиПогрузкиРазгрузки.ТочкаПогрузки;
		// Если точка погрузки не найдена.
		Если текТочкаПогрузки = Неопределено Тогда
			
			// Добавить точки перед точкой завершения или в конец.
			текКоличествоСтрок = Маршрут.Количество();
			Если текКоличествоСтрок > 0 Тогда
				текИндексСтрокиПогрузки	= текКоличествоСтрок - 1;
				текПоследняяТочка = Маршрут[текИндексСтрокиПогрузки];
				Если НЕ текПоследняяТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
					текИндексСтрокиПогрузки = Неопределено;
				КонецЕсли;
			Иначе	
				текИндексСтрокиПогрузки = Неопределено;
			КонецЕсли;
			
			текТочкаПогрузки = ДобавитьТочкуВМаршрут(Маршрут, АдресОтправления, ВидЗоныДляФормированияПредставленияРейса, текИндексСтрокиПогрузки, ДобавлениеИзФормы);
		КонецЕсли;
	КонецЕсли;	
	
	Если СтруктураРаботПоЗаданию = Неопределено Тогда
		СтруктураРаботПоЗаданию = упЗаданиеНаПеревозкуВызовСервера.ПолучитьРаботыПоЗаданию(Задание, текТочкаПогрузки.Адрес, ?(ВариантМаршрутаПолный, текТочкаРазгрузки.Адрес, Неопределено), мсвГрузовыхМест, ПолучитьДопРаботы, ВариантФормированияМаршрута, ЧасовойПоясРейса); 	
		
		ОсновныеРаботы = СтруктураРаботПоЗаданию.ОсновныеРаботы;
		ОсновныеРаботыИнкассацияЗаГруз = Новый Массив;
		Для каждого текРабота Из ОсновныеРаботы Цикл
			Если текРабота.ИнкассацияЗаГруз Тогда
				ОсновныеРаботыИнкассацияЗаГруз.Добавить(текРабота);
			КонецЕсли;
		КонецЦикла;
		
		Если ОсновныеРаботыИнкассацияЗаГруз.Количество() Тогда
			ОсновныеРаботы = ОбщегоНазначенияКлиентСервер.СократитьМассив(ОсновныеРаботы, ОсновныеРаботыИнкассацияЗаГруз);
			
			СтруктураРаботИнкассацияЗаГруз = Новый Структура("ОсновныеРаботы, ДополнительныеРаботы, ПолучениеПередачаДокументов, ВременноеОкноНачало, ВременноеОкноОкончание");
			СтруктураРаботИнкассацияЗаГруз.ОсновныеРаботы = ОсновныеРаботыИнкассацияЗаГруз;
			СтруктураРаботИнкассацияЗаГруз.ДополнительныеРаботы = Новый Массив;
			СтруктураРаботИнкассацияЗаГруз.ПолучениеПередачаДокументов = Новый Массив;
			
			ДобавитьРаботыПоЗаданиюВМаршрут(Рейс, Маршрут, Работы, АдресПолучения, АдресОтправления, Задание,,, СтруктураРаботИнкассацияЗаГруз, ВидЗоныДляФормированияПредставленияРейса, РаспределитьПоНепройденнымТочкам, ВариантФормированияМаршрута,,,,ЧасовойПоясРейса);  
			СтруктураРаботПоЗаданию.ОсновныеРаботы = ОсновныеРаботы;
		КонецЕсли;
		
		ФормироватьРаботыСДокументами = Истина;
		Если ПолучитьФункциональнуюОпцию("упИспользуютсяКонтейнерныеПеревозки") Тогда
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидТранспортнойУслуги") = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
				ФормироватьРаботыСДокументами = Ложь;
			КонецЕсли;
		КонецЕсли; 
		
		Если ФормироватьРаботыСДокументами И Работы.НайтиСтроки(Новый Структура("Задание", Задание)).Количество() = 0 Тогда
			сткВозврат = упЗаданиеНаПеревозкуВызовСервера.ПолучитьДанныеПолучениеПередачаДокументов(Задание);
			Если сткВозврат.ПолучениеПередачаДокументов.Количество() Тогда
				СтруктураРаботПоЗаданию.ПолучениеПередачаДокументов = сткВозврат.ПолучениеПередачаДокументов;
			КонецЕсли;
			Если сткВозврат.КонтрольныеТочкиВозвратаДокументов.Количество() Тогда
				мсвКонтрольныхТочек = сткВозврат.КонтрольныеТочкиВозвратаДокументов;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
	ДобавитьРаботыПоЗаданиюВТочку(Работы, текТочкаПогрузки, Задание, ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей"), 	СтруктураРаботПоЗаданию, ДобавлениеИзФормы);
	ДобавитьРаботыПоЗаданиюВТочку(Работы, текТочкаПогрузки, Задание, ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка"), 			СтруктураРаботПоЗаданию, ДобавлениеИзФормы);
	Если ВариантМаршрутаПолный Тогда
		ДобавитьРаботыПоЗаданиюВТочку(Работы, текТочкаРазгрузки, Задание, ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей"),СтруктураРаботПоЗаданию, ДобавлениеИзФормы);
		ДобавитьРаботыПоЗаданиюВТочку(Работы, текТочкаРазгрузки, Задание, ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка"), 		СтруктураРаботПоЗаданию, ДобавлениеИзФормы);	
	КонецЕсли;
	
	Если мсвКонтрольныхТочек.Количество() Тогда
		текИндексСтрокиРазгрузки = Маршрут.Индекс(текТочкаРазгрузки);
		Для каждого текКонтрольнаяТочка Из мсвКонтрольныхТочек Цикл
			АдресВозвратаДокументов = текКонтрольнаяТочка.АдресВозвратаДокументов;
			
			НоваяРабота = Неопределено;
			Если ВариантМаршрутаПолный Тогда
				Сч = текИндексСтрокиРазгрузки + 1;
				Пока Сч < Маршрут.Количество() Цикл
					текТочка = Маршрут[Сч];
					Если текТочка.Адрес = АдресВозвратаДокументов Тогда
						НоваяРабота = Работы.Добавить();
						НоваяРабота.Задание = Задание;
						ЗаполнитьЗначенияСвойств(НоваяРабота, текКонтрольнаяТочка);
						НоваяРабота.Идентификатор = текТочка.Идентификатор;
						НоваяРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
						
						мсвПредшественников = СтрРазделить(текТочка.Предшественники, ",");
						мсвПредшественников.Добавить("" + текТочкаПогрузки.Идентификатор);
						мсвПредшественников.Добавить("" + текТочкаРазгрузки.Идентификатор);
						текТочка.Предшественники = ПривестиМассивПредшественников(мсвПредшественников, текТочка.Идентификатор);
						
						Прервать;
					КонецЕсли; 
					
					Сч = Сч + 1;
				КонецЦикла;
			КонецЕсли;
			Если НоваяРабота = Неопределено Тогда
				Сч = Маршрут.Количество() - 1;
				Пока Сч >= 1 Цикл
					текТочка = Маршрут[Сч];
					Если текТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
						Сч = Сч - 1;
					Иначе
						НоваяКонтрольнаяТочка = Маршрут.Вставить(Сч + 1);
						НоваяКонтрольнаяТочка.Адрес = АдресВозвратаДокументов;
						НоваяКонтрольнаяТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.КонтрольнаяТочка");
						НоваяКонтрольнаяТочка.Идентификатор = Новый УникальныйИдентификатор;
						НоваяКонтрольнаяТочка.СтрокаНомер = Сч + 2;
						Если ДобавлениеИзФормы Тогда
							НоваяКонтрольнаяТочка.ИндексКартинки = 2;
							НоваяКонтрольнаяТочка.СтрокаДокумента = Истина;		
						КонецЕсли;

						НоваяРабота = Работы.Добавить();
						НоваяРабота.Задание = Задание;
						ЗаполнитьЗначенияСвойств(НоваяРабота, текКонтрольнаяТочка);
						НоваяРабота.Идентификатор = НоваяКонтрольнаяТочка.Идентификатор;
						НоваяРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
						
						мсвПредшественников = Новый Массив;
						мсвПредшественников.Добавить("" + текТочкаПогрузки.Идентификатор);
						мсвПредшественников.Добавить("" + текТочкаРазгрузки.Идентификатор);
						НоваяКонтрольнаяТочка.Предшественники = СтрСоединить(мсвПредшественников, ",");
						Прервать;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
	текТочкаПогрузки.Предшественники = ПривестиМассивПредшественников(мсвПредшественниковТочкиПогрузки, текТочкаПогрузки.Идентификатор);
	сткРезультат.ТочкаПогрузки = текТочкаПогрузки;
	
	Если ВариантМаршрутаПолный Тогда
		текТочкаРазгрузки.Предшественники = ПривестиМассивПредшественников(мсвПредшественниковТочкиРазгрузки, текТочкаРазгрузки.Идентификатор);
		сткРезультат.ТочкаРазгрузки = текТочкаРазгрузки;
		
		Если мсвПотомковТочкиПогрузки.Количество()  Тогда
			Для каждого текИдентификаторПотомка Из мсвПотомковТочкиПогрузки Цикл
				текТочкаМаршрута = Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПотомка), "Идентификатор");
				Если Ложь
					Или текТочкаМаршрута = текТочкаПогрузки 
					Или текТочкаМаршрута = текТочкаРазгрузки 
				Тогда
					Продолжить;
				КонецЕсли; 
				Если ЗначениеЗаполнено(текТочкаМаршрута.Предшественники) Тогда
					//текТочкаМаршрута.Предшественники = текТочкаМаршрута.Предшественники + "," + текТочкаПогрузки.Идентификатор;
					мсвПредшественниковТочки = СтрРазделить(текТочкаМаршрута.Предшественники, ",");
					Если мсвПредшественниковТочки.Найти("" + текТочкаПогрузки.Идентификатор) = Неопределено Тогда
						текТочкаМаршрута.Предшественники = текТочкаМаршрута.Предшественники + "," + текТочкаПогрузки.Идентификатор;
					КонецЕсли;
				Иначе
					текТочкаМаршрута.Предшественники = "" + текТочкаПогрузки.Идентификатор;
				КонецЕсли; 
			КонецЦикла; 	
		КонецЕсли; 
		
		Если мсвПотомковТочкиРазгрузки.Количество() Тогда
			Для каждого текИдентификаторПотомка Из мсвПотомковТочкиРазгрузки Цикл
				текТочкаМаршрута = Маршрут.Найти(Новый УникальныйИдентификатор(текИдентификаторПотомка), "Идентификатор");
				Если Ложь
					Или текТочкаМаршрута = текТочкаПогрузки 
					Или текТочкаМаршрута = текТочкаРазгрузки 
				Тогда
					Продолжить;
				КонецЕсли;
				Если ЗначениеЗаполнено(текТочкаМаршрута.Предшественники) Тогда
					//текТочкаМаршрута.Предшественники = текТочкаМаршрута.Предшественники + "," + текТочкаРазгрузки.Идентификатор;
					мсвПредшественниковТочки = СтрРазделить(текТочкаМаршрута.Предшественники, ",");
					Если мсвПредшественниковТочки.Найти("" + текТочкаРазгрузки.Идентификатор) = Неопределено Тогда
						текТочкаМаршрута.Предшественники = текТочкаМаршрута.Предшественники + "," + текТочкаРазгрузки.Идентификатор;
					КонецЕсли;
				Иначе
					текТочкаМаршрута.Предшественники = "" + текТочкаРазгрузки.Идентификатор;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
		
	Возврат сткРезультат;
		
КонецФункции

Процедура ДобавитьПредшественниковКСуществующим(ПредшественникиТочки, Знач Точка)
	
	НовыеПредшественники = СтрРазделить(Точка.Предшественники, ",", Ложь);
	Для Каждого ПредшественникКандидат Из ПредшественникиТочки Цикл
		Если НовыеПредшественники.Найти(ПредшественникКандидат) = Неопределено Тогда
			НовыеПредшественники.Добавить(ПредшественникКандидат);
		КонецЕсли; 
	КонецЦикла; 
	ПредшественникиТочки = НовыеПредшественники;

КонецПроцедуры // ДобавитьРаботыПоЗаданиюВМаршрут()

// Функция - Определяет является ли вариант формирования маршрута полным.
//
// Параметры:
//  ВариантФормированияМаршрута	 - Перечисление.упВариантыФормированияМаршрута	 - вариант формирования маршрута.
// 
// Возвращаемое значение:
//  Булево - Истина, это полный вариант формирования.
//
Функция ВариантФормированияМаршрутаПолный(ВариантФормированияМаршрута) Экспорт
	Возврат ВариантФормированияМаршрута = ПредопределенноеЗначение("Перечисление.упВариантыФормированияМаршрута.Полный") 
								ИЛИ НЕ ЗначениеЗаполнено(ВариантФормированияМаршрута);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные функции, задействованные в механизме добавления задания в маршрут .
// 

// Подбирает для задания точки погрузки и разгрузки в маршруте.
//
// Параметры:
//  Маршрут - Структура - содрежит в себе описание маршрута в зависимости от реализации, 
//					(должно присутствовать обязательное поле - "Режим").
//	Задание - ДокументСсылка.упЗаданиеНаПеревозкуГруза - Ссылка на документ.
//	АдресОтправления - СправочникСсылка.упАдрес - ссылка на адрес.
//  АдресПолучатели - СправочникСсылка.упАдрес 	- ссылка на адрес.
//  РаспределитьПоНепройденнымТочкам - Булево - Флаг распределения.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПодобратьТочкиДляЗадания(ЗНАЧ Маршрут, ЗНАЧ Работы, ЗНАЧ АдресОтправления, ЗНАЧ АдресПолучения, ЗНАЧ ПредшественникиПогрузки, ЗНАЧ ПотомкиПогрузки, ЗНАЧ ПредшественникиРазгрузки,
	ЗНАЧ ПотомкиРазгрузки, ЗНАЧ РаспределитьПоНепройденнымТочкам = Ложь, Знач АдресаПредшественниковПогрузки = Неопределено)

	сткРезультат = Новый Структура("ТочкаПогрузки, ТочкаРазгрузки, ТочкаРазгрузкиПодобранная");
	Точки = Маршрут;
	Если Точки = Неопределено Тогда
		Возврат сткРезультат; // Исключительная ситуация, ошибка в реализации, такого быть не должно.
	КонецЕсли;
	Если АдресаПредшественниковПогрузки = Неопределено Тогда
		АдресаПредшественниковПогрузки = Новый Массив;
	КонецЕсли; 
	
	АдресаПредшественниковПогрузкиКопия = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(АдресаПредшественниковПогрузки);
	НевозможныеПредшественникиПогрузки = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПредшественникиПогрузки);
	
	НачальныйИндексТочкиРазгрузки = 0;
	// Поиск точки погрузки по совпадению адреса.
	ПодходящаяТочкаПогрузки = Неопределено;
	ТочкаПервогоПотомка = Неопределено;
	Для каждого ТекущаяТочка Из Точки Цикл
		ИндексПредшественника = НевозможныеПредшественникиПогрузки.Найти("" + ТекущаяТочка.Идентификатор);
		Если ИндексПредшественника <> Неопределено Тогда
			НевозможныеПредшественникиПогрузки.Удалить(ИндексПредшественника);
		КонецЕсли;
		Если Ложь
			Или РаспределитьПоНепройденнымТочкам И ТекущаяТочка.Пройдена 
			Или ЗначениеЗаполнено(ТекущаяТочка.Гараж)
		Тогда
			АдресПредшественникаПогрузки = АдресаПредшественниковПогрузкиКопия.Найти(ТекущаяТочка.Адрес);
			Если АдресПредшественникаПогрузки <> Неопределено Тогда // на случай если адрес встречается дважды
				АдресаПредшественниковПогрузкиКопия.Удалить(АдресПредшественникаПогрузки);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		Если ПодходящаяТочкаПогрузки = Неопределено И ТекущаяТочка.Адрес = АдресОтправления Тогда
			ПодходящаяТочкаПогрузки = ТекущаяТочка;
		КонецЕсли;
		Если ПотомкиПогрузки.Найти("" + ТекущаяТочка.Идентификатор) <> Неопределено Тогда
			Прервать;
		КонецЕсли; 
		
		АдресПредшественникаПогрузки = АдресаПредшественниковПогрузкиКопия.Найти(ТекущаяТочка.Адрес);
		Если Истина
			И ПодходящаяТочкаПогрузки <> Неопределено
			И (Ложь
				Или ПредшественникиПогрузки.Найти("" + ТекущаяТочка.Идентификатор) <> Неопределено
				Или АдресПредшественникаПогрузки <> Неопределено)
			И ТекущаяТочка.Адрес <> АдресОтправления
			//И ПредшественникиПогрузки.Найти("" + ТекущаяТочка.Идентификатор) = Неопределено
		Тогда
			ПодходящаяТочкаПогрузки = Неопределено;
		КонецЕсли;
				
		Если АдресПредшественникаПогрузки <> Неопределено Тогда // на случай если адрес встречается дважды
			АдресаПредшественниковПогрузкиКопия.Удалить(АдресПредшественникаПогрузки);
		КонецЕсли;
	КонецЦикла;
	Если НевозможныеПредшественникиПогрузки.Количество() > 0 Тогда
		ВызватьИсключение "Противоречивые условия размещения точки маршрута"; // ДиагностикаУП
	КонецЕсли; 
	Если ПодходящаяТочкаПогрузки <> Неопределено Тогда
		НачальныйИндексТочкиРазгрузки = Точки.Индекс(ПодходящаяТочкаПогрузки);
	ИначеЕсли Ложь
		//Или ПредшественникиРазгрузки.Количество() > 0
		Или ПредшественникиПогрузки.Количество() > 0 // Возможно это стоит раскомментировать
		Или АдресаПредшественниковПогрузки.Количество() > 0
	Тогда  
		Если АдресаПредшественниковПогрузки.Найти(АдресПолучения) <> Неопределено Тогда
			// Это петлевая цепочка адресов
			СтрокаПервогоПосещенияАдресаОтправления = Точки.Найти(АдресПолучения, "Адрес");
		КонецЕсли; 
		Если СтрокаПервогоПосещенияАдресаОтправления <> Неопределено Тогда
			НачальныйИндексТочкиРазгрузки = Точки.Индекс(СтрокаПервогоПосещенияАдресаОтправления) + 1;
			// Данное условие закомментировано по причине: при наличии заданий на перевозку груза с разными точками разгрузки, 
			// но общей точкой сдачи инкассации, точка сдачи инкассации дублируется.
		//Иначе
		//	НачальныйИндексТочкиРазгрузки = Точки.Количество(); // чтобы не подбирать точку разгрузки
		КонецЕсли; 
	КонецЕсли; 
	
	// Поиск точки разгрузки по совпадению адреса.
	// Переменные для определения точки разгрузки, если она не найдена по адресу.
	ТочкаРазгрузкиПодобранная = Неопределено;// Заполняется если невозможно найти точку разгрузки по адресу.
	ПодходящаяТочкаРазгрузки = Неопределено; // Больше не используем
	Для ИндексТочки = НачальныйИндексТочкиРазгрузки По Точки.Количество() - 1 Цикл
		ТекущаяТочка = Точки[ИндексТочки];
		Если Ложь
			Или РаспределитьПоНепройденнымТочкам И ТекущаяТочка.Пройдена 
			Или ЗначениеЗаполнено(ТекущаяТочка.Гараж)
		Тогда
			Продолжить;
		КонецЕсли;
		Если Истина
			И ПодходящаяТочкаПогрузки <> ТекущаяТочка 
			И (Ложь
				Или ПодходящаяТочкаРазгрузки = Неопределено 
				Или ПодходящаяТочкаПогрузки = Неопределено)
			И ТекущаяТочка.Адрес = АдресПолучения 
			И ПредшественникиПогрузки.Найти("" + ТекущаяТочка.Идентификатор) = Неопределено
		Тогда
			ПодходящаяТочкаРазгрузки = ТекущаяТочка;
		КонецЕсли;
		Если ПотомкиРазгрузки.Найти("" + ТекущаяТочка.Идентификатор) <> Неопределено Тогда
			Прервать;
		КонецЕсли; 
		Если Истина
			И ПодходящаяТочкаРазгрузки <> Неопределено
			И ПредшественникиРазгрузки.Найти("" + ТекущаяТочка.Идентификатор) <> Неопределено 
			И ПодходящаяТочкаРазгрузки.Идентификатор <> ТекущаяТочка.Идентификатор
		Тогда
			ПодходящаяТочкаПогрузки = Неопределено;
			ТочкаРазгрузкиПодобранная = Неопределено; // Больше не используем
		КонецЕсли; 
	КонецЦикла;

	сткРезультат.ТочкаПогрузки				= ПодходящаяТочкаПогрузки;
	сткРезультат.ТочкаРазгрузки				= ПодходящаяТочкаРазгрузки; 
	сткРезультат.ТочкаРазгрузкиПодобранная	= ТочкаРазгрузкиПодобранная; // Больше не используем
	 		
	Возврат сткРезультат;

КонецФункции // ПодобратьТочкиПогрузкиРазгрузки()

// Подбирает для задания точку погрузки в маршруте.
//
// Параметры:
//  Маршрут - ТаблицаЗначений - перечислены точки маршрута.
//	Работы	- ТаблицаЗначений - перечислены работы маршрута
//	АдресОтправления - СправочникСсылка.упАдрес - ссылка на адрес.
//  РаспределитьПоНепройденнымТочкам - Булево - Флаг распределения.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПодобратьТочкуПогрузки(Маршрут, Работы, АдресОтправления, РаспределитьПоНепройденнымТочкам = Ложь)

	сткРезультат = Новый Структура("ТочкаПогрузки");
			
	текТочки = Маршрут;
	
	Если текТочки = Неопределено Тогда
		Возврат сткРезультат; // Исключительная ситуация, ошибка в реализации, такого быть не должно.
	КонецЕсли;
	
	текТочкаПогрузки 	= Неопределено;

	// Поиск точек погрузки по совпадению адреса.
	Для каждого текТочка Из текТочки Цикл
		
		Если РаспределитьПоНепройденнымТочкам И текТочка.Пройдена Тогда
			Продолжить;
		КонецЕсли;
				
		Если текТочка.Адрес = АдресОтправления Тогда
			текТочкаПогрузки = текТочка;
			Прервать;
		КонецЕсли;

	КонецЦикла;
		
	сткРезультат.ТочкаПогрузки = текТочкаПогрузки;
	 		
	Возврат сткРезультат;

КонецФункции // ПодобратьТочкуПогрузки()

Функция ДобавитьТочкуВМаршрут(Точки, Адрес, ВидЗоныДляФормированияПредставленияРейса, Индекс = Неопределено, ДобавлениеИзФормы = Ложь)

	текРезультат = Неопределено;
	
	Если Не Индекс = Неопределено  Тогда
		текРезультат = Точки.Вставить(Индекс);
	Иначе	
		текРезультат = Точки.Добавить();
		Индекс = Точки.Индекс(текРезультат);
	КонецЕсли;
	
	текРезультат.Адрес = Адрес;
	
	текРезультат.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию");
	Если ДобавлениеИзФормы Тогда
		текРезультат.ИндексКартинки = 1;
		текРезультат.СтрокаДокумента = Истина;		
	КонецЕсли;
	текРезультат.Идентификатор = Новый УникальныйИдентификатор;
	
	текКоличествоТочек = Точки.Количество();
	
	Для й = Индекс По текКоличествоТочек - 1 Цикл
		Точки[й].СтрокаНомер = й + 1;
	КонецЦикла;
	
	Если текКоличествоТочек > 0 Тогда
		текПоследняяТочка = Точки[текКоличествоТочек-1];
		Если текПоследняяТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения") Тогда
			мсвПредшественников	= СтрРазделить(текПоследняяТочка.Предшественники, ",");
			мсвПредшественников.Добавить("" + текРезультат.Идентификатор);
			Точки[текКоличествоТочек-1].Предшественники = СтрСоединить(мсвПредшественников, ",");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат текРезультат;

КонецФункции // ДобавитьТочку()

Функция ДобавитьРаботыПоЗаданиюВТочку(Работы, Точка, Задание, Операция, СтруктураРаботПоЗаданию, ДобавлениеИзФормы = Ложь)
	
	текРезультат = Истина;

	мсвОсновныеРаботы 		       = СтруктураРаботПоЗаданию.ОсновныеРаботы;
	мсвДополнительныеРаботы        = СтруктураРаботПоЗаданию.ДополнительныеРаботы;
	мсвПолучениеПередачаДокументов = СтруктураРаботПоЗаданию.ПолучениеПередачаДокументов;
	мсвРаботыПоТочке 			   = Работы.НайтиСтроки(Новый Структура("Идентификатор", Точка.Идентификатор));
	КоличествоСтрокПоТочке 		   = мсвРаботыПоТочке.Количество();
		
	Если НЕ мсвОсновныеРаботы = Неопределено Тогда
		Для каждого текОсновнаяРабота Из мсвОсновныеРаботы Цикл
			
			Если НЕ текОсновнаяРабота.Операция = Операция Тогда
				Продолжить;		
			КонецЕсли;
			
			ДобавитьНовуюРаботу = Истина;
			
			Если НЕ Точка.Пройдена Тогда
				
				мсвРаботы = Работы.НайтиСтроки(Новый Структура("Идентификатор, Операция, Задание, ГрузовоеМесто, Тара"
																		, Точка.Идентификатор
																		, Операция
																		, Задание
																		, текОсновнаяРабота.ГрузовоеМесто
																		, текОсновнаяРабота.Тара));
				Если мсвРаботы.Количество() > 0 Тогда
					текРабота 			= мсвРаботы[0];	
					ДобавитьНовуюРаботу = Ложь;
				КонецЕсли;														
						
			КонецЕсли;
			
			Если ДобавитьНовуюРаботу Тогда
				текРабота = Работы.Добавить();
				КоличествоСтрокПоТочке = КоличествоСтрокПоТочке + 1;
				
				ЗаполнитьЗначенияСвойств(текРабота, текОсновнаяРабота);
				текРабота.Идентификатор = Точка.Идентификатор;
				текРабота.Выполнена		= Точка.Пройдена;
				текРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
				текРабота.Задание 		= Задание;
				текРабота.Операция 		= Операция;
				Если ДобавлениеИзФормы Тогда
					Если Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Погрузка") Тогда
						текРабота.ТипОперации = 0;
					ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.Разгрузка") Тогда
						текРабота.ТипОперации = 1;
					ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей") Тогда
						текРабота.ТипОперации = 5;
					ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаЦенностей") Тогда
						текРабота.ТипОперации = 6;
					КонецЕсли;
				КонецЕсли; 
				
				Если ДобавлениеИзФормы Тогда
					текРабота.СтрокаДокумента = Истина;
					
					// Добавим строку в конец списка.
					текРабота.СтрокаНомер 	= КоличествоСтрокПоТочке;
					
				КонецЕсли;
			Иначе	
				текРабота.КоличествоГрузов 	=  текРабота.КоличествоГрузов + текОсновнаяРабота.КоличествоГрузов;
				текРабота.ВремяРабот 		=  текРабота.ВремяРабот + текОсновнаяРабота.ВремяРабот;
			КонецЕсли;
			
			Если Точка.ВременноеОкноНачало = '00010101' Или Точка.ВременноеОкноНачало > текРабота.ВременноеОкноНачало Тогда
				Точка.ВременноеОкноНачало = текРабота.ВременноеОкноНачало;
			КонецЕсли;
			
			Если Точка.ВременноеОкноОкончание = '00010101' Или Точка.ВременноеОкноОкончание < текРабота.ВременноеОкноОкончание Тогда
				Точка.ВременноеОкноОкончание = текРабота.ВременноеОкноОкончание;
			КонецЕсли;
			
			Если Точка.Отменена Тогда
				Точка.Отменена = Ложь;
			КонецЕсли; 
						
		КонецЦикла;
	КонецЕсли;
	
	текТипОперацииДопОперация = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ДополнительнаяОперация");
	
	Для каждого текДопРабота Из мсвДополнительныеРаботы Цикл
		
		Если Не текДопРабота.ВлияющаяОперация = Операция Тогда
			Продолжить;		
		КонецЕсли;
		
		текРабота = Работы.Добавить();
		КоличествоСтрокПоТочке = КоличествоСтрокПоТочке + 1;

		ЗаполнитьЗначенияСвойств(текРабота, текДопРабота);
		текРабота.Идентификатор = Точка.Идентификатор;
		текРабота.Выполнена		= Точка.Пройдена;
		текРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		текРабота.Задание 		= Задание;
		текРабота.Операция		= текТипОперацииДопОперация;
		Если ДобавлениеИзФормы Тогда
			текРабота.ТипОперации   = 4;
		КонецЕсли;
		
		Если ДобавлениеИзФормы Тогда
			текРабота.СтрокаДокумента = Истина;
			
			// Добавим строку в конец списка.
			текРабота.СтрокаНомер 	= КоличествоСтрокПоТочке;
		КонецЕсли;
	
	КонецЦикла;
	
	Для каждого текПередачаДокументов Из мсвПолучениеПередачаДокументов Цикл
		
		Если Не текПередачаДокументов.ВлияющаяОперация = Операция Тогда
			Продолжить;		
		КонецЕсли;
		
		текРабота = Работы.Добавить();
		КоличествоСтрокПоТочке = КоличествоСтрокПоТочке + 1;
		
		ЗаполнитьЗначенияСвойств(текРабота, текПередачаДокументов);
		текРабота.Идентификатор = Точка.Идентификатор;
		текРабота.Выполнена		= Точка.Пройдена;
		текРабота.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		Если ДобавлениеИзФормы Тогда
			Если текРабота.Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда
				текРабота.ТипОперации   = 2;
			Иначе				
				текРабота.ТипОперации   = 3;
			КонецЕсли; 
		КонецЕсли;
		
		Если ДобавлениеИзФормы Тогда
			текРабота.СтрокаДокумента = Истина;
			
			// Добавим строку в конец списка.
			текРабота.СтрокаНомер 	= КоличествоСтрокПоТочке;
		КонецЕсли;
		
	КонецЦикла;
    	
	Возврат текРезультат;

КонецФункции // ДобавитьРаботуВТочку()

Функция ПривестиМассивПредшественников(МассивПредшественников, СобственныйИдентификаторТочки = "")
	
	Если МассивПредшественников.Количество() > 1 Тогда
		//МассивПредшественников = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивПредшественников);
		МассивПредшественников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивПредшественников);  // Так защищаемся от периодически появляющихся в коде повторных добавлениях предшественников
	КонецЕсли; 
	ИндексСебя = МассивПредшественников.Найти("" + СобственныйИдентификаторТочки);
	Если ИндексСебя <> Неопределено Тогда
		МассивПредшественников.Удалить(ИндексСебя);
	КонецЕсли; 
	
	Возврат СтрСоединить(МассивПредшественников, ",");
	
КонецФункции
	
#КонецОбласти 

Функция СобытиеОшибкаРегистрацияОтложенныхРаботПоРейсу(ПодСобытие = "") Экспорт
	
	Результат = СтрШаблон(НСтр("ru = 'Регистрация отложенных работ по рейсу%1'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ?(ЗначениеЗаполнено(ПодСобытие), "." + ПодСобытие, ""));
	Возврат Результат;
	
КонецФункции

#КонецОбласти 