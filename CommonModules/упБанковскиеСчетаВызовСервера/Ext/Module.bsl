#Область ПрограммныйИнтерфейс

// Функция - Подбор банка по наименованию
//
// Параметры:
//  Наименование - Строка	 - наименование банка
// 
// Возвращаемое значение:
//  СправочникСсылка.КлассификаторБанковРФ - ссылка на банк
//
Функция ПодборБанкаПоНаименованию(Наименование) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КлассификаторБанковРФ.Ссылка,
	               |	КлассификаторБанковРФ.Наименование
	               |ИЗ
	               |	Справочник.КлассификаторБанковРФ КАК КлассификаторБанковРФ
	               |ГДЕ
	               |	КлассификаторБанковРФ.Наименование ПОДОБНО &Наименование";
	Запрос.УстановитьПараметр("Наименование", "%" + Наименование + "%");
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка, упСервисныеФункцииКлиентСервер.ФорматированнаяСтрокаДляАвтоПодбора(Выборка.Наименование, Наименование));
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

// Процедура - Установить счет по умолчанию
//
// Параметры:
//  Владелец - СправочникСсылка	 - ссылка на владельца банковского счета
//  Счет	 - СправочникСсылка	 - ссылка на счет 
//  Отказ	 - Булево - Истина, если произошла ошибка
//
Процедура УстановитьСчетПоУмолчанию(Владелец, Счет, Отказ) Экспорт
	
	Объект = Владелец.ПолучитьОбъект();
	Объект.ОсновнойБанковскийСчет = Счет;
	Попытка
		Объект.Записать();	
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Владелец,,,Отказ);
		ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,Владелец, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецПопытки;
			
КонецПроцедуры

// Функция - Индексы банков чья деятельность прекращена
//
// Параметры:
//  мсвБанки - Массив - массив ссылок на банки
// 
// Возвращаемое значение:
//  Массив индексов - массив индексов банков в исходном массиве, чья деятельность прекращена
//
Функция ИндексыБанковЧьяДеятельностьПрекращена(мсвБанки) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КлассификаторБанковРФ.Ссылка
	               |ИЗ
	               |	Справочник.КлассификаторБанковРФ КАК КлассификаторБанковРФ
	               |ГДЕ
	               |	КлассификаторБанковРФ.Ссылка В(&Банки)
	               |	И КлассификаторБанковРФ.ДеятельностьПрекращена";
	Запрос.УстановитьПараметр("Банки", мсвБанки);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвРезультат.Добавить(мсвБанки.Найти(Выборка.Ссылка));
	КонецЦикла; 
	
	Возврат мсвРезультат;

КонецФункции

#КонецОбласти
