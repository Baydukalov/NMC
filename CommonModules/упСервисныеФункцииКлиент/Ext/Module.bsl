#Область ПрограммныйИнтерфейс

// Используется в командах формы, если для дальнейшей работы нужны заведомо сохраненные данные.
//
// Параметры:
//	Форма - УправляемаяФорма  - Форма.
//	ОписаниеОповещения - ОписаниеОповещения - Используется для описания вызова процедуры программного модуля.
//
Процедура ВопросОСохраненииИзмененныхДанных(Форма, ОписаниеОповещения) Экспорт
	Если Форма.Модифицированность Тогда
		ДополнительныеПараметры	= Новый Структура("Форма, ОписаниеОповещения", Форма, ОписаниеОповещения);
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОСохраненииИзмененныхДанных", упСервисныеФункцииКлиент, ДополнительныеПараметры);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры 

// Возвращает строку типового сообщения ЖР.
//
// Возвращаемое значение:
//	Строка - Результат выполнения.
//
Функция ПолучитьТекстСмотриЖР() Экспорт

	Возврат НСтр("ru = 'Подробности см. в Журнале регистрации.'");

КонецФункции // ПолучитьТекстСмЖР()

// Форма справочник объект обработка оповещения.
//
// Параметры:
//	ЭтаФорма - УправляемаяФорма  - Форма.
//	ИмяСобытия - Строка - Имя события.
//	Параметр - Произвольный - Параметр сообщения.
//	Источник - Произвольный - Источник события.
//
Процедура ФормаСсылочныйОбъект_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = "ЗаписанОбъект" Тогда
		Если Параметр = ЭтаФорма.Объект.Ссылка Тогда
			Если Не ЭтаФорма.Модифицированность Тогда
				ЭтаФорма.Прочитать();
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

// Оповестить О записи объекта.
//
// Параметры:
//  Ссылка		 - СсылкаНаОбъект	 - Ссылка.
//  Источник		 - Произвольный	 - Источник события.
//  СообщитьОЗаписи	 - Булево			 - Выполнить.
//
Процедура ОповеститьОЗаписиОбъекта(Ссылка, Источник = Неопределено, СообщитьОЗаписи = Ложь) Экспорт
	
	Оповестить("ЗаписанОбъект", Ссылка, Источник);
	Если СообщитьОЗаписи Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Записан объект ",,,Ссылка);
	КонецЕсли; 
	ОповеститьОбИзменении(Ссылка);
	
КонецПроцедуры

// Функция - Возвращает разделитель используемый для имен событий при оповещении, для задания вызова нескольких обработчиков.
// 
// Возвращаемое значение:
//  Строка - разделитель
//
Функция РазделительИменСобытий() Экспорт
	Возврат ";";
КонецФункции

Функция НайтиСтрокуВТекстахЭлементовФормы(Элемент, СтарыеСвойстваПоиска, СтрокаПоиска)
	
	Результат = Ложь;
	Для Каждого ПодчиненныйЭлемент Из Элемент.ПодчиненныеЭлементы Цикл
		ИменаСвойств = Новый Структура("Имя, Заголовок");
		ЗаполнитьЗначенияСвойств(ИменаСвойств, ПодчиненныйЭлемент); 
		Если Ложь
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") // Антибаг платформы. При изменении цвета заголовка группы иногда происходят серверные вызовы и аварийные завершения 
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы")
		Тогда
			//
		Иначе
			Для Каждого КлючИЗначение Из ИменаСвойств Цикл
				ИмяСвойства = КлючИЗначение.Ключ;
				Если Истина
					И ИмяСвойства = "Имя"
					И (Ложь
						Или ТипЗнч(ПодчиненныйЭлемент) <> Тип("ПолеФормы")
						Или ЗначениеЗаполнено(ИменаСвойств.Заголовок) )
				Тогда
					Продолжить;
				КонецЕсли; 
				Если СтрНайти(Нрег(КлючИЗначение.Значение), НРег(СтрокаПоиска)) > 0 Тогда
					Результат = Истина;
					Если ТипЗнч(ПодчиненныйЭлемент) = Тип("КнопкаФормы") Тогда
						ИмяСвойстваЦвета = "ЦветТекста";
					Иначе
						ИмяСвойстваЦвета = "ЦветТекстаЗаголовка";
					КонецЕсли; 
					УстановитьСвойстваЭлементаСВосстановлением(СтарыеСвойстваПоиска, ПодчиненныйЭлемент, ИмяСвойстваЦвета, WebЦвета.Красный);
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
		Если Ложь
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") 
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы")
		Тогда
			Результат = НайтиСтрокуВТекстахЭлементовФормы(ПодчиненныйЭлемент, СтарыеСвойстваПоиска, СтрокаПоиска) Или Результат;
		КонецЕсли; 
	КонецЦикла;
	Если Истина
		И ТипЗнч(Элемент) = Тип("ГруппаФормы")
		И Элемент.Вид = ВидГруппыФормы.Страница
	Тогда
		//Если Результат Тогда 
		//	УстановитьСвойстваЭлементаСВосстанлением(СтарыеСвойстваПоиска, Элемент, "ЦветТекстаЗаголовка", WebЦвета.Красный); // Фактически (визуально) цвет не меняется у заголовка страницы
		//КонецЕсли; 
		Если Не Результат Тогда 
			УстановитьСвойстваЭлементаСВосстановлением(СтарыеСвойстваПоиска, Элемент, "Видимость", Ложь);
		КонецЕсли; 
	КонецЕсли; 
	Если Истина
		И ТипЗнч(Элемент) = Тип("ГруппаФормы")
		И Элемент.Вид = ВидГруппыФормы.Страницы
	Тогда
		// Ошибка платформы? после скрытия видимости текущей страницы панели она остается текущей.
		Для Каждого Страница Из Элемент.ПодчиненныеЭлементы Цикл
			Если Страница.Видимость Тогда
				Элемент.ТекущаяСтраница = Страница;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

Функция УстановитьСвойстваЭлементаСВосстановлением(СтарыеСвойстваПоиска, Знач ПодчиненныйЭлемент, Знач ИмяСвойстваЦвета, Знач НовоеЗначение)
	
	СтарыеСвойстваЭлемента = Новый Структура(ИмяСвойстваЦвета);
	ЗаполнитьЗначенияСвойств(СтарыеСвойстваЭлемента, ПодчиненныйЭлемент); 
	СтарыеСвойстваПоиска.Вставить(ПодчиненныйЭлемент.Имя, СтарыеСвойстваЭлемента);
	НовыеСвойстваЭлемента = Новый Структура(ИмяСвойстваЦвета, НовоеЗначение);
	ЗаполнитьЗначенияСвойств(ПодчиненныйЭлемент, НовыеСвойстваЭлемента);

КонецФункции

// Процедура - Строка поиска элементов формы при изменении
//
// Параметры:
//  ЭтаФорма			 - Форма	 - форма
//  КорневойЭлементПоиска - Элемент	 - элемент
//
Процедура СтрокаПоискаЭлементовФормыПриИзменении(ЭтаФорма, КорневойЭлементПоиска) Экспорт 
	
	// Глючно работает горячий фильтр https://partners.v8.1c.ru/forum/t/1623619/m/1623619
	Элементы = ЭтаФорма.Элементы;
	Если ЗначениеЗаполнено(ЭтаФорма.АдресСтарыхСвойствПоиска) Тогда
		СтарыеСвойстваПоиска = ПолучитьИзВременногоХранилища(ЭтаФорма.АдресСтарыхСвойствПоиска);
		Для Каждого СтруктураСвойств Из СтарыеСвойстваПоиска Цикл
			ВосстанавливаемыйЭлемент = Элементы[СтруктураСвойств.Ключ];
			ЗаполнитьЗначенияСвойств(ВосстанавливаемыйЭлемент, СтруктураСвойств.Значение); 
		КонецЦикла;
		ЭтаФорма.АдресСтарыхСвойствПоиска = "";
	КонецЕсли; 
	СтрокаПоиска = Элементы.СтрокаПоиска.ТекстРедактирования;
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		СтарыеСвойстваПоиска = Новый Структура;
		НайтиСтрокуВТекстахЭлементовФормы(КорневойЭлементПоиска, СтарыеСвойстваПоиска, СтрокаПоиска);
		ЭтаФорма.АдресСтарыхСвойствПоиска = ПоместитьВоВременноеХранилище(СтарыеСвойстваПоиска, ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	ПолеСтрокиПоиска = Элементы.СтрокаПоиска;
	Если ЗначениеЗаполнено(ПолеСтрокиПоиска.ТекстРедактирования) Тогда
		ПолеСтрокиПоиска.ЦветФона = WebЦвета.Желтый;
	Иначе
		ПолеСтрокиПоиска.ЦветФона = Новый Цвет;
	КонецЕсли;

КонецПроцедуры


#Область ИнтерактивныеДействия

// Процедура позволяет установить период через стандартный диалог выбора периода.
//
// Параметры:
//  Объект                - Произвольный - Объект в котором устанавливается значения периода.
//  ПараметрыПериода      - Структура - структура со свойствами "ДатаНачала", "ДатаОкончания" и в значениях имена полей.
//                              объекта, для свойства "Вариант" - значение варианта стандартного периода.
//  ОповещениеПослеВыбора - ОписаниеОповещения - Описание оповещение которое выполняется после установки периода. 
//                              Может быть установлена пост-обрабокта в месте вызова после выбора периода.
// 
Процедура РедактироватьПериод(Объект, ПараметрыПериода = Неопределено, ОповещениеПослеВыбора = Неопределено) Экспорт
	
	Если ПараметрыПериода = Неопределено Тогда
		ПараметрыПериода = Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачала", "ДатаОкончания");
	КонецЕсли;
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Если ПараметрыПериода.Свойство("ДатаНачала") Тогда
		Диалог.Период.ДатаНачала = Объект[ПараметрыПериода.ДатаНачала];
	КонецЕсли; 
	Если ПараметрыПериода.Свойство("ДатаОкончания") Тогда
		Диалог.Период.ДатаОкончания = Объект[ПараметрыПериода.ДатаОкончания];
	КонецЕсли; 
	Если ПараметрыПериода.Свойство("Вариант") Тогда
		Диалог.Период.Вариант = ПараметрыПериода.Вариант;
	КонецЕсли; 
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект",           Объект);
	ДополнительныеПараметры.Вставить("ПараметрыПериода", ПараметрыПериода);
	Если ОповещениеПослеВыбора <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОповещениеПослеВыбора", ОповещениеПослеВыбора);
	КонецЕсли; 
	
	Оповещение = Новый ОписаниеОповещения(
		"РедактироватьПериодЗавершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	Диалог.Показать(Оповещение);

КонецПроцедуры

// Процедура завершения для РедактироватьПериод()
//
// Параметры:
//	Период - Дата  - Период.
//	ДополнительныеПараметры - Структура - Параметры.
//
Процедура РедактироватьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт 

	ПараметрыПериода = ДополнительныеПараметры.ПараметрыПериода;
	Объект           = ДополнительныеПараметры.Объект;
	Если Период <> Неопределено Тогда
		Если ПараметрыПериода.Свойство("ДатаНачала") Тогда
			Объект[ПараметрыПериода.ДатаНачала]= Период.ДатаНачала;
		КонецЕсли; 
		Если ПараметрыПериода.Свойство("ДатаОкончания") Тогда
			Объект[ПараметрыПериода.ДатаОкончания]= Период.ДатаОкончания;
		КонецЕсли; 
		Если ПараметрыПериода.Свойство("Вариант") Тогда
			Объект[ПараметрыПериода.Вариант]= Период.Вариант;
		КонецЕсли;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ОповещениеПослеВыбора") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеВыбора, Период);
	КонецЕсли;
	
КонецПроцедуры

// Показывает окно извещения об ошибках
//
// Параметры:
//  Сообщение					 - Строка	 - Текст.
//  БылиОшибки					 - Булево	 - Выполнить.
//  УстановитьОтборПоТекущемуСеансу - Булево	 - установить отбор.
//
Процедура ПоказатьОповещениеПользователяНаше(Сообщение = "", БылиОшибки = Ложь, УстановитьОтборПоТекущемуСеансу = Истина) Экспорт 
	
	ОткрытьФорму("ОбщаяФорма.упОповещениеПользователя", Новый Структура("Сообщение, БылиОшибки, ОтборПоТекущемуСеансу", Сообщение, БылиОшибки, УстановитьОтборПоТекущемуСеансу));

КонецПроцедуры // ПеренестиСтрокиВнутриДереваРейсовНаКлиенте()

// Выполнить помещение даных.
//
// Параметры:
//	Текст - Строка - Текст.
//
Процедура ПоместитьТекстВБуферОбменаОС(Текст) Экспорт
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоWindowsКлиент() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Работа с буфером обмена ОС реализована только на ОС Windows");
	Иначе
		// http://partners.v8.1c.ru/forum/thread.jsp?id=1075241#1075241.
		// Так падает после нескольких вызовов.
		// Документ = ирКэш.Получить().СлужебноеПолеHtmlДокумента.Документ;
		Документ = Новый COMОбъект("HTMLFILE");
		Окно = Документ.parentWindow;
		Окно.ClipboardData.SetData("Text", Текст); 
	КонецЕсли; 
	
Конецпроцедуры

// Выполнить получение данных.
//
// Возвращаемое значение:
//	Строка - Результат выполнения.
//
Функция ПолучитьТекстИзБуфераОбменаОС() Экспорт
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоWindowsКлиент() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Работа с буфером обмена ОС реализована только на ОС Windows");
	Иначе
		// http://partners.v8.1c.ru/forum/thread.jsp?id=1075241#1075241.
		// Так падает после нескольких вызовов.
		// Документ = ирКэш.Получить().СлужебноеПолеHtmlДокумента.Документ;
		Документ = Новый COMОбъект("HTMLFILE");
		Окно = Документ.parentWindow;
		Результат = Окно.ClipboardData.GetData("Text"); 
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Конецобласти

#Область СлужебныеПроцедурыФункции

// Обработчик ответа на вопрос пользователя на сохранение измененных данных.
//
// Параметры:
//	РезультатВопроса - РежимДиалогаВопрос - вариант состава кнопок окна диалога вопроса.
//	ДополнительныеПараметры - Структура - Параметры.
//
Процедура ОбработкаОтветаНаВопросОСохраненииИзмененныхДанных(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.Форма.Записать();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры

// Процедура проверки доступновти компонент Администратором.
//
// Параметры:
//	РезультатВопроса - РежимДиалогаВопрос - вариант состава кнопок окна диалога вопроса.
//	ДополнительныеПараметры - Структура - Параметры.
//
Процедура ПроверкаДоступностиКомпонентАдминистраторомЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса.Значение = "КодВозвратаДиалога.OK" Тогда
		ИспользуетсяУправлениеПеревозками = ПолучитьФункциональнуюОпциюИнтерфейса("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
		ИспользуетсяУправлениеТранспортом = ПолучитьФункциональнуюОпциюИнтерфейса("упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом");
		ИспользуетсяСпутниковыйМониторинг = ПолучитьФункциональнуюОпциюИнтерфейса("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг");
		
		сткВозврат = упЗащищенныеФункции.ДоступныеМодули();
		упСервисныеФункцииВызовСервера.ОтменитьИспользованиеГлобальныхКомпонент(ИспользуетсяУправлениеПеревозками И Не сткВозврат.УправлениеПеревозками,
		                                                                        ИспользуетсяУправлениеТранспортом И Не сткВозврат.УправлениеТранспортом,
		                                                                        ИспользуетсяСпутниковыйМониторинг И Не сткВозврат.СпутниковыйМониторинг);
		ОбновитьИнтерфейс();																		
	Иначе
		ЗавершитьРаботуСистемы(Ложь, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заверешния работы системы при недоступности компонент.
//
//	РезультатВопроса - РежимДиалогаВопрос - вариант состава кнопок окна диалога вопроса.
//	ДополнительныеПараметры - Структура - Параметры.
//
Процедура ПроверкаДоступностиКомпонентЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ЗавершитьРаботуСистемы(Ложь, Ложь);
	
КонецПроцедуры

Процедура ОткрытьЖурналРегистрации(ДополнительныеПараметры) Экспорт
	ОтборПоУровню = Новый СписокЗначений;
	ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
	НомерСеанса = упСервисныеФункцииВызовСервера.СписокНомеровСеансаБазы();
	Если НомерСеанса.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Сеанс", НомерСеанса);
	КонецЕсли; 
	
	ДатаНачала = ТекущаяДата() - 8 * 60 * 60;

	Если ЗначениеЗаполнено(ДатаНачала) Тогда
		ПараметрыФормы.Вставить("ДатаНачала", ДатаНачала);
	КонецЕсли;
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы);
	
КонецПроцедуры

Процедура ПровестиИЗакрыть(Форма, РежимЗаписиПроведение = Истина) Экспорт
	
	ОчиститьСообщения();
	Форма.ЗакрытьФорму = Истина;
	ПараметрыЗаписи = Новый Структура;
	Если РежимЗаписиПроведение Тогда
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Иначе
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
	КонецЕсли;
	ПараметрыЗаписи.Вставить("ЗакрытьФорму", Истина);
	Форма.Записать(ПараметрыЗаписи);
	
КонецПроцедуры

Процедура ВыполнитьДействияПослеЗаписи(Форма, Объект, ПараметрыЗаписи) Экспорт
	
	Если Форма.ЗакрытьФорму Тогда
		Форма.ПодключитьОбработчикОжидания("ПодключаемыйЗакрытьФорму", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ОбработатьЗаписьОбъектаВФорме(Форма, ПараметрыЗаписи, Отказ = Ложь) Экспорт
	
	Результат = Ложь;
	
	Если Отказ Тогда
		Возврат Результат;
	КонецЕсли;
	
	Форма.ПропуститьПроверкуПередЗаписью = Истина;
	
	Отказ = Истина;
	ПараметрыЗаписи.Вставить("ЗакрытьФорму", Форма.ЗакрытьФорму);
	Результат = Форма.Записать(ПараметрыЗаписи);
	
	Форма.ЗакрытьФорму = Ложь;
	Форма.ПропуститьПроверкуПередЗаписью = Ложь;
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти

#Область ПрочиеКлиентскиеПроцедурыФункции

// Формирует предложение перевозчика по заявке на ТС
//
Процедура СформироватьПредложениеПеревозчика(ЗаявкаНаТС) Экспорт
	
	Отказ = Ложь;
	ПредложениеПеревозчика = упСервисныеФункцииВызовСервера.ВозможноСозданиеПредложенияПеревозчика(ЗаявкаНаТС, Отказ);
	
	Если Не Отказ Тогда
		// при создании формы предложение перевозчика будет дозаполнено по переданной в параметрах формы заявке на ТС
		ПараметрыФормы = Новый Структура("ЗаявкаНаТС", ЗаявкаНаТС);
		ОткрытьФорму("Документ.упПредложениеПеревозчика.Форма.ФормаДокумента", ПараметрыФормы);
	ИначеЕсли ЗначениеЗаполнено(ПредложениеПеревозчика) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ПредложениеПеревозчика);
		ОткрытьФорму("Документ.упПредложениеПеревозчика.Форма.ФормаДокумента", ПараметрыФормы);
	КонецЕсли; 
	
КонецПроцедуры

// Формирует подтверждение по заявке на ТС или по предложению перевозчика
//
Процедура СформироватьПодтверждениеЗаявкиНаТС(ДокументОснование) Экспорт
	
	Отказ = Ложь;
	Подтверждение = упСервисныеФункцииВызовСервера.ВозможноСозданиеПодтвержденияЗаявкиНаТС(ДокументОснование, Отказ);
	
	Если Не Отказ Тогда
		// при создании формы подтверждение заявки на ТС будет дозаполнено по переданному в параметрах формы документу-основанию
		ПараметрыФормы = Новый Структура("ДокументОснование", ДокументОснование);
		ОткрытьФорму("Документ.упПодтверждениеЗаявкиНаТС.Форма.ФормаДокумента", ПараметрыФормы);
	ИначеЕсли ЗначениеЗаполнено(Подтверждение) Тогда
		ПараметрыФормы = Новый Структура("Ключ", Подтверждение);
		ОткрытьФорму("Документ.упПодтверждениеЗаявкиНаТС.Форма.ФормаДокумента", ПараметрыФормы);
	КонецЕсли; 
	
КонецПроцедуры
	
#КонецОбласти 