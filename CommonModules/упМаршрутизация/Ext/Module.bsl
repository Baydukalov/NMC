#Область ПрограммныйИнтерфейс

#Область РасчетРасстояний

// Процедура рассчитывает расстояния между точками.
//
// Параметры:
//  мсвТочек - Массив - Список точек.
//	спзИдентификаторов - СписокЗначений - Список идентификаторов.
//	ТолькоНеРассчитанные - Булево - Получить не расчитанные.
//  РазрешитьБлижайшие	- Булево - Выполнить разрешение.
//
Процедура РассчитатьРасстоянияПоКарте(АдресТаблицыАдресов = Неопределено, спзИдентификаторов = Неопределено, ТолькоНеРассчитанные = Ложь, РазрешитьБлижайшие = Истина) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если Не ПолучитьФункциональнуюОпцию("упИспользуютсяЭлектронныеГеографическиеКарты") Тогда  
		Возврат;
	КонецЕсли;
	Если АдресТаблицыАдресов <> Неопределено Тогда
		ТаблицаАдресов = ПолучитьИзВременногоХранилища(АдресТаблицыАдресов);
	КонецЕсли; 
	Порции = ПолучитьНерассчитанныеРасстояния(ТаблицаАдресов, ТолькоНеРассчитанные, РазрешитьБлижайшие);
	ЗапуститьФоновыеЗаданияРасчетаРасстояний(Порции, АдресТаблицыАдресов <> Неопределено, спзИдентификаторов);
	УстановитьПривилегированныйРежим(Ложь);	
КонецПроцедуры

Процедура ЗапуститьФоновыеЗаданияРасчетаРасстояний(Знач Порции, Знач ВызовИзФормы = Ложь, Знач спзИдентификаторов = Неопределено) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
		спзИдентификаторов = Новый СписокЗначений;
	КонецЕсли;
	мсвФоновыхЗаданий = Новый Массив;
	Для Каждого Порция Из Порции Цикл
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(Порция);
		мсвПараметров.Добавить(ВызовИзФормы);
		ФоновоеЗадание = ФоновыеЗадания.Выполнить("упМаршрутизация.РассчитатьПорциюРасстоянийПоКарте", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Расчет порции расстояний'"));
		Если ВызовИзФормы Тогда 
			спзИдентификаторов.Добавить(ФоновоеЗадание.УникальныйИдентификатор, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поток %1'"), (спзИдентификаторов.Количество() + 1)));
		Иначе	
			мсвФоновыхЗаданий.Добавить(ФоновоеЗадание);
		КонецЕсли;
	КонецЦикла;
	Если мсвФоновыхЗаданий.Количество() Тогда 
		Если Не ВызовИзФормы Тогда 
			ФоновыеЗадания.ОжидатьЗавершения(мсвФоновыхЗаданий, 3600);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Получить массив порций нерассчитанных расстояний
// Параметры:
//  Точки - Массив, СписокЗначений
Функция ПолучитьНерассчитанныеРасстояния(Знач ТаблицаАдресов = Неопределено, Знач ТолькоНеРассчитанные, Знач РассчитыватьТолькоБлижайшие = Истина,
	АдресВременногоХранилищаРезультата = "", НеобходимыеПарыАдресов = Неопределено) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	ВызовИзФормы = ТаблицаАдресов <> Неопределено;
	РазмерПорции       = упСервисныеФункции.ПолучитьЗначениеКонстанты("упРазмерПорцииРассчитываемыхДанных");
	КоличествоПотоков  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоПотоковРасчета");
	ПакетныйРежимЗапросовСервисуМаршрутизации  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПакетныйРежимЗапросовСервисуМаршрутизации");
	ТипМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
	МаксимальноеКоличествоПотоковПакетногоРежима = 5;
	Если Истина
		И ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД
		И ПакетныйРежимЗапросовСервисуМаршрутизации 
		И КоличествоПотоков > МаксимальноеКоличествоПотоковПакетногоРежима 
	Тогда
		КоличествоПотоков = МаксимальноеКоличествоПотоковПакетногоРежима;
	КонецЕсли; 
	ПериодАктуальности = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПериодАктуальностиРасчета");
	ОграниченияМаршрутизатора = ПолучитьТаблицуОграниченийМаршрутизатора();
	РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД = ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД");
	
	РассчитаноБлижайшие = 0;
	НеРассчитаноБлижайшие = 0;
	НевозможноРассчитатьБлижайшие = 0;
	Если ПериодАктуальности = 0 ИЛИ ТолькоНеРассчитанные Тогда 
		ДатаИзменения = '00010101';
	Иначе	
		ДатаИзменения = ТекущаяДатаСеанса() - (ПериодАктуальности * 86400);
	КонецЕсли;	
	РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны = Новый Массив;
	РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны.Добавить(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОграниченияМаршрутизатора", ОграниченияМаршрутизатора);
	Запрос.Текст = "ВЫБРАТЬ ОграниченияМаршрутизатора.Ограничение ПОМЕСТИТЬ ОграниченияМаршрутизатора ИЗ &ОграниченияМаршрутизатора КАК ОграниченияМаршрутизатора";
	Запрос.Выполнить();
	Запрос.УстановитьПараметр("ТаблицаАдресов", ТаблицаАдресов);
	Запрос.УстановитьПараметр("РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны", РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны);
	Если ВызовИзФормы Тогда
		РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны.Добавить(Ложь);
		Запрос.Текст = 
		"
		|ВЫБРАТЬ
		//|	ТаблицаАдресов.ПриоритетноеПостроениеМассиваРасстояний КАК ПриоритетноеПостроениеМассиваРасстояний,
		//|	ТаблицаАдресов.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД,
		|	ТаблицаАдресов.Адрес КАК Адрес
		|ПОМЕСТИТЬ ТаблицаАдресов
		|ИЗ
		|	&ТаблицаАдресов КАК ТаблицаАдресов
		|;
		|ВЫБРАТЬ
		|	0 КАК Зона,
		|	ТаблицаАдресов.Адрес.Широта КАК Широта,
		|	ТаблицаАдресов.Адрес.Долгота КАК Долгота,
		//|	МАКСИМУМ(ТаблицаАдресов.ПриоритетноеПостроениеМассиваРасстояний) КАК ПриоритетноеПостроениеМассиваРасстояний,
		//|	МАКСИМУМ(ТаблицаАдресов.ДопОграниченияСитиГИД) КАК ДопОграниченияСитиГИД
		|	МАКСИМУМ(ТаблицаАдресов.Адрес.ВидАдреса.ПриоритетноеПостроениеМассиваРасстояний) КАК ПриоритетноеПостроениеМассиваРасстояний,
		|	МАКСИМУМ(ЕСТЬNULL(упГеографическиеЗоны.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД, ЛОЖЬ)) КАК ДопОграниченияСитиГИД
		|ПОМЕСТИТЬ втАдреса
		|ИЗ
		|	ТаблицаАдресов КАК ТаблицаАдресов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
		|		ПО упГеографическиеЗоны.Ссылка = упИерархияГеографическихЗон.Родитель
		|	ПО ТаблицаАдресов.Адрес.ГеографическаяЗона = упИерархияГеографическихЗон.Зона
		|ГДЕ
		|	упГеографическиеЗоны.РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны В  (&РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны)
		|	И (ТаблицаАдресов.Адрес.Долгота <> 0
		|		ИЛИ ТаблицаАдресов.Адрес.Широта <> 0)
		|СГРУППИРОВАТЬ ПО
		|	0,
		|	ТаблицаАдресов.Адрес.Широта,
		|	ТаблицаАдресов.Адрес.Долгота
		|;";
	Иначе	
		Если РассчитыватьТолькоБлижайшие И ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияТолькоДляЗаданийВСтатусеКПланированию") Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	упЗаданиеНаПеревозкуГруза.АдресОтправления,
			|	упЗаданиеНаПеревозкуГруза.АдресПолучения
			|ПОМЕСТИТЬ втАдресаЗаданий
			|ИЗ
			|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданий
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
			|		ПО (упСтатусыЗаданий.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию))
			|			И упСтатусыЗаданий.Документ = упЗаданиеНаПеревозкуГруза.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втАдресаЗаданий.АдресОтправления КАК Адрес
			|ПОМЕСТИТЬ втАдресаПодготовка
			|ИЗ
			|	втАдресаЗаданий КАК втАдресаЗаданий
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	втАдресаЗаданий.АдресПолучения
			|ИЗ
			|	втАдресаЗаданий КАК втАдресаЗаданий
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	упГеографическиеЗоны.Ссылка КАК Зона,
			|	упАдреса.Широта КАК Широта,
			|	упАдреса.Долгота КАК Долгота,
			|	МАКСИМУМ(упАдреса.ВидАдреса.ПриоритетноеПостроениеМассиваРасстояний) КАК ПриоритетноеПостроениеМассиваРасстояний,
			|	МАКСИМУМ(упГеографическиеЗоны.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД) КАК ДопОграниченияСитиГИД
			|ПОМЕСТИТЬ втАдреса
			|ИЗ
			|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
			|					втАдресаПодготовка.Адрес КАК Адрес
			|				ИЗ
			|					втАдресаПодготовка КАК втАдресаПодготовка) КАК втАдресаПодготовка
			|				ПО упАдреса.Ссылка = втАдресаПодготовка.Адрес
			|			ПО упИерархияГеографическихЗон.Зона = упАдреса.ГеографическаяЗона
			|		ПО упГеографическиеЗоны.Ссылка = упИерархияГеографическихЗон.Родитель
			|ГДЕ
			|	упГеографическиеЗоны.РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны  В (&РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны)
			|	И (упАдреса.Долгота <> 0
			|			ИЛИ упАдреса.Широта <> 0)
			|	И НЕ упАдреса.ПометкаУдаления
			|	И НЕ упГеографическиеЗоны.ПометкаУдаления
			|
			|СГРУППИРОВАТЬ ПО
			|	упГеографическиеЗоны.Ссылка,
			|	упАдреса.Широта,
			|	упАдреса.Долгота";
		Иначе
			Запрос.Текст =
			"ВЫБРАТЬ
			|	упГеографическиеЗоны.Ссылка КАК Зона,
			|	упАдреса.Широта КАК Широта,
			|	упАдреса.Долгота КАК Долгота,
			|	МАКСИМУМ(упАдреса.ВидАдреса.ПриоритетноеПостроениеМассиваРасстояний) КАК ПриоритетноеПостроениеМассиваРасстояний,
			|	МАКСИМУМ(упГеографическиеЗоны.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД) КАК ДопОграниченияСитиГИД
			|ПОМЕСТИТЬ втАдреса
			|ИЗ
			|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
			|			ПО упИерархияГеографическихЗон.Зона = упАдреса.ГеографическаяЗона
			|		ПО упГеографическиеЗоны.Ссылка = упИерархияГеографическихЗон.Родитель
			|ГДЕ
			|	упГеографическиеЗоны.РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны В (&РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны)
			|	И (упАдреса.Долгота <> 0
			|			ИЛИ упАдреса.Широта <> 0)
			|	И НЕ упАдреса.ПометкаУдаления
			|	И НЕ упГеографическиеЗоны.ПометкаУдаления
			|
			|СГРУППИРОВАТЬ ПО
			|	упГеографическиеЗоны.Ссылка,
			|	упАдреса.Широта,
			|	упАдреса.Долгота
			|;";
		КонецЕсли; 
	КонецЕсли;
	
	Если РассчитыватьТолькоБлижайшие Тогда
		
		Запрос.Выполнить();
		
		КоличествоАдресов = 0;
		
		Запрос.Текст = "ВЫБРАТЬ КОЛИЧЕСТВО(*) КАК КоличествоАдресов ИЗ втАдреса";
		ВыборкаКоличество = Запрос.Выполнить().Выбрать();
		Если ВыборкаКоличество.Следующий() Тогда 
			КоличествоАдресов = ВыборкаКоличество.КоличествоАдресов;
		КонецЕсли;
		
		РазмерПорцииАдресов = КоличествоАдресов;
		Если РазмерПорцииАдресов > 1000 Тогда 
			//РазмерПорцииАдресов = 1000000 / КоличествоАдресов;
			РазмерПорцииАдресов = 10000;
		КонецЕсли;	
		
		Если КоличествоПотоков = 0 Тогда 
			КоличествоПотоков = 1;
		КонецЕсли;
		Если РазмерПорции = 0 Тогда 
			РазмерПорции = 100;
		КонецЕсли;
		КоличествоТочекПоАдресу = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоГеографическиБлизкихТочек");
		
		СчетчикРасстояний = 0;
		мсвФоновыхЗаданий = Новый Массив;
		ТаблицыПорций         = Новый Массив;
		
		ТаблицыЗаполнены = Ложь;
		
		Если РазмерПорцииАдресов > 0 Тогда
			КоличествоПорцийАдресов = Окр(КоличествоАдресов / РазмерПорцииАдресов + 0.4999);
		Иначе
			КоличествоПорцийАдресов = 1;
		КонецЕсли; 
		РазмерПорцииАдресов     = Окр(КоличествоАдресов / КоличествоПорцийАдресов + 0.4999);
		
		Запрос.Текст = "ВЫБРАТЬ * ИЗ втАдреса";
		тбзАдреса = Запрос.Выполнить().Выгрузить();
		
		мсвТаблицАдресов = Новый Массив;
		СчАдресов = 0;
		тбзПромежуточная = Неопределено;
		Для Каждого текСтрока Из тбзАдреса Цикл 
			Если тбзПромежуточная = Неопределено Тогда 
				тбзПромежуточная = тбзАдреса.СкопироватьКолонки();
				мсвТаблицАдресов.Добавить(тбзПромежуточная);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(тбзПромежуточная.Добавить(), текСтрока);
			
			СчАдресов = СчАдресов + 1;
			Если СчАдресов = РазмерПорцииАдресов Тогда 
				СчАдресов = 0;
				тбзПромежуточная = Неопределено;
			КонецЕсли;
		КонецЦикла;	
		Для Каждого текТаблица Из мсвТаблицАдресов Цикл 
			Запрос.Текст =
			"
			|ВЫБРАТЬ
			|	тбзАдреса.Зона КАК Зона,
			|	тбзАдреса.Широта КАК Широта,
			|	тбзАдреса.Долгота КАК Долгота,
			|	тбзАдреса.ПриоритетноеПостроениеМассиваРасстояний КАК ПриоритетноеПостроениеМассиваРасстояний,
			|	тбзАдреса.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
			|ПОМЕСТИТЬ втПорцияАдреса
			|ИЗ
			|	&тбзАдреса КАК тбзАдреса
			|;
			|ВЫБРАТЬ
			|	НеобходимыеПарыАдресов.АдресОтправления КАК АдресОтправления,
			|	НеобходимыеПарыАдресов.АдресПолучения КАК АдресПолучения
			|ПОМЕСТИТЬ НеобходимыеПарыАдресов
			|ИЗ
			|	&НеобходимыеПарыАдресов КАК НеобходимыеПарыАдресов
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втАдресаПоЗонамНачало.Широта КАК НачальнаяШирота,
			|	втАдресаПоЗонамНачало.Долгота КАК НачальнаяДолгота,
			|	втАдресаПоЗонамОкончание.Широта КАК КонечнаяШирота,
			|	втАдресаПоЗонамОкончание.Долгота КАК КонечнаяДолгота,
			|	ВЫБОР
			|		КОГДА втАдресаПоЗонамОкончание.ПриоритетноеПостроениеМассиваРасстояний
			|				ИЛИ втАдресаПоЗонамНачало.ПриоритетноеПостроениеМассиваРасстояний
			|			ТОГДА 0
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК ПриоритетноеПостроениеМассиваРасстояний,
			|	втАдресаПоЗонамНачало.ДопОграниченияСитиГИД
			|		И втАдресаПоЗонамОкончание.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
			|ПОМЕСТИТЬ втМассивОтрезков
			|ИЗ
			|	втПорцияАдреса КАК втАдресаПоЗонамНачало,
			|	втАдреса КАК втАдресаПоЗонамОкончание
			|ГДЕ
			|	втАдресаПоЗонамНачало.Зона = втАдресаПоЗонамОкончание.Зона
			|	И (втАдресаПоЗонамНачало.Широта <> втАдресаПоЗонамОкончание.Широта
			|			ИЛИ втАдресаПоЗонамНачало.Долгота <> втАдресаПоЗонамОкончание.Долгота)
			|
			|ОБЪЕДИНИТЬ
			|ВЫБРАТЬ
			|	НеобходимыеПарыАдресов.АдресОтправления.Широта КАК НачальнаяШирота,
			|	НеобходимыеПарыАдресов.АдресОтправления.Долгота КАК НачальнаяДолгота,
			|	НеобходимыеПарыАдресов.АдресПолучения.Широта КАК КонечнаяШирота,
			|	НеобходимыеПарыАдресов.АдресПолучения.Долгота КАК КонечнаяДолгота,
			|	1 КАК ПриоритетноеПостроениеМассиваРасстояний,
			|	ИСТИНА КАК ДопОграниченияСитиГИД
			|ИЗ
			|	НеобходимыеПарыАдресов КАК НеобходимыеПарыАдресов
			|ИНДЕКСИРОВАТЬ ПО
			|	НачальнаяШирота,
			|	НачальнаяДолгота,
			|	КонечнаяШирота,
			|	КонечнаяДолгота
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ втПорцияАдреса
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втМассивОтрезков.НачальнаяШирота КАК НачальнаяШирота,
			|	втМассивОтрезков.НачальнаяДолгота КАК НачальнаяДолгота,
			|	втМассивОтрезков.КонечнаяШирота,
			|	втМассивОтрезков.КонечнаяДолгота,
			|	втМассивОтрезков.ПриоритетноеПостроениеМассиваРасстояний,
			|	втМассивОтрезков.ДопОграниченияСитиГИД,
			|	ОграниченияМаршрутизатора.Ограничение,
			|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПытатьсяРассчитатьСнова, ИСТИНА) КАК ПытатьсяРассчитатьСнова,
			|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПопыткаРасчета, 1) КАК ПопыткаРасчета,
			|	ЕСТЬNULL(упРасстоянияМеждуТочками.ДатаИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаИзменения,
			|	ЕСТЬNULL(упРасстоянияМеждуТочками.АвтоматическийРасчет, ИСТИНА) КАК АвтоматическийРасчет,
			|	(втМассивОтрезков.НачальнаяШирота - втМассивОтрезков.КонечнаяШирота) * (втМассивОтрезков.НачальнаяШирота - втМассивОтрезков.КонечнаяШирота) + (втМассивОтрезков.НачальнаяДолгота - втМассивОтрезков.КонечнаяДолгота) * (втМассивОтрезков.НачальнаяДолгота - втМассивОтрезков.КонечнаяДолгота) КАК Дельта
			|ИЗ
			|	втМассивОтрезков КАК втМассивОтрезков
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОграниченияМаршрутизатора
			|	ПО втМассивОтрезков.ДопОграниченияСитиГИД 
			|		ИЛИ ОграниченияМаршрутизатора.Ограничение = ЗНАЧЕНИЕ(Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка)
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
			|	ПО втМассивОтрезков.НачальнаяШирота = упРасстоянияМеждуТочками.НачальнаяШирота
			|			И втМассивОтрезков.НачальнаяДолгота = упРасстоянияМеждуТочками.НачальнаяДолгота
			|			И втМассивОтрезков.КонечнаяШирота = упРасстоянияМеждуТочками.КонечнаяШирота
			|			И втМассивОтрезков.КонечнаяДолгота = упРасстоянияМеждуТочками.КонечнаяДолгота
			|			И ОграниченияМаршрутизатора.Ограничение = упРасстоянияМеждуТочками.Ограничение
			|//ГДЕ
			|//	(ЕСТЬNULL(упРасстоянияМеждуТочками.ПытатьсяРассчитатьСнова, ИСТИНА) ИЛИ упРасстоянияМеждуТочками.ПопыткаРасчета = 0)
			|//	И ЕСТЬNULL(упРасстоянияМеждуТочками.АвтоматическийРасчет, ИСТИНА)
			|
			|УПОРЯДОЧИТЬ ПО
			|	втМассивОтрезков.НачальнаяШирота,
			|	втМассивОтрезков.НачальнаяДолгота,
			|	втМассивОтрезков.ПриоритетноеПостроениеМассиваРасстояний,
			|	Дельта,
			|	ПопыткаРасчета,
			|	ДатаИзменения
			|ИТОГИ ПО
			|	НачальнаяДолгота,
			|	НачальнаяШирота
			|";
			Запрос.УстановитьПараметр("тбзАдреса", текТаблица);
			Если НеобходимыеПарыАдресов = Неопределено Тогда
				НеобходимыеПарыАдресов = Новый ТаблицаЗначений;
				НеобходимыеПарыАдресов.Колонки.Добавить("АдресОтправления", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
				НеобходимыеПарыАдресов.Колонки.Добавить("АдресПолучения", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
			КонецЕсли; 
			Запрос.УстановитьПараметр("НеобходимыеПарыАдресов", НеобходимыеПарыАдресов);
			НеобходимыеПарыАдресов = Неопределено;
			
			ВыборкаНачало = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНачало.Следующий() Цикл 
				ВыборкаАдрес = ВыборкаНачало.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаАдрес.Следующий() Цикл 
					КоличествоТочекПоТекущемуАдресу = 0;
					Выборка = ВыборкаАдрес.Выбрать();
					Пока Выборка.Следующий() Цикл 
						Если Выборка.ПриоритетноеПостроениеМассиваРасстояний = 1 Тогда // Если расстояние НЕ приоритетное
							Если Выборка.Ограничение = Перечисления.упОграниченияМаршрутизаторов.ПустаяСсылка() Тогда 
								КоличествоТочекПоТекущемуАдресу = КоличествоТочекПоТекущемуАдресу + 1;
							КонецЕсли; 
						Иначе
							Пустышка = 0; // Для отладки
						КонецЕсли;
						Если КоличествоТочекПоТекущемуАдресу > КоличествоТочекПоАдресу Тогда 
							Прервать;
						КонецЕсли;
						Если Ложь
							//Или Не Выборка.ПытатьсяРассчитатьСнова
							Или (Выборка.ПопыткаРасчета = 0 И Выборка.ДатаИзменения > ДатаИзменения) 
						Тогда 
							РассчитаноБлижайшие = РассчитаноБлижайшие + 1;
							Продолжить;
						КонецЕсли;
						НеРассчитаноБлижайшие = НеРассчитаноБлижайшие + 1;
						Если Не Выборка.ПытатьсяРассчитатьСнова Тогда
							НевозможноРассчитатьБлижайшие = НевозможноРассчитатьБлижайшие + 1;
						КонецЕсли; 
						Если ТаблицыПорций.Количество() = СчетчикРасстояний Тогда 
							тбзПланирование = НоваяТаблицаПорцииРасстояний();
							ТаблицыПорций.Добавить(тбзПланирование);
						КонецЕсли;
						
						Если Истина
							И Не ВызовИзФормы
							И ТаблицыПорций[СчетчикРасстояний].Количество() >= РазмерПорции 
						Тогда 
							ТаблицыЗаполнены = Истина;
							Прервать;
						КонецЕсли;	
						
						НоваяСтрока = ТаблицыПорций[СчетчикРасстояний].Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
						НоваяСтрока.ДопОграниченияСитиГИД = РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД И Выборка.ДопОграниченияСитиГИД;							
						
						СчетчикРасстояний = СчетчикРасстояний + 1;
						Если СчетчикРасстояний = КоличествоПотоков Тогда 
							СчетчикРасстояний = 0;
						КонецЕсли;
					КонецЦикла;
					Если ТаблицыЗаполнены Тогда 
						Прервать;
					КонецЕсли;	
				КонецЦикла;
				Если ТаблицыЗаполнены Тогда 
					Прервать;
				КонецЕсли;	
			КонецЦикла;
			Если ТаблицыЗаполнены Тогда 
				Прервать;
			КонецЕсли;
			Запрос.Текст = "УНИЧТОЖИТЬ втМассивОтрезков";
			Запрос.Выполнить();
		КонецЦикла;
	Иначе
		Запрос.Текст = Запрос.Текст + 
		"ВЫБРАТЬ
		|	втАдресаПоЗонамНачало.Широта КАК НачальнаяШирота,
		|	втАдресаПоЗонамНачало.Долгота КАК НачальнаяДолгота,
		|	втАдресаПоЗонамОкончание.Широта КАК КонечнаяШирота,
		|	втАдресаПоЗонамОкончание.Долгота КАК КонечнаяДолгота,
		|	ВЫБОР
		|		КОГДА втАдресаПоЗонамОкончание.ПриоритетноеПостроениеМассиваРасстояний
		|				ИЛИ втАдресаПоЗонамНачало.ПриоритетноеПостроениеМассиваРасстояний
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ПриоритетноеПостроениеМассиваРасстояний,
		|	втАдресаПоЗонамНачало.ДопОграниченияСитиГИД
		|		И втАдресаПоЗонамОкончание.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
		|ПОМЕСТИТЬ втМассивОтрезков
		|ИЗ
		|	втАдреса КАК втАдресаПоЗонамНачало
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдресаПоЗонамОкончание
		|		ПО (втАдресаПоЗонамНачало.Широта <> втАдресаПоЗонамОкончание.Широта
		|				ИЛИ втАдресаПоЗонамНачало.Долгота <> втАдресаПоЗонамОкончание.Долгота)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НачальнаяШирота,
		|	НачальнаяДолгота,
		|	КонечнаяШирота,
		|	КонечнаяДолгота
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	втМассивОтрезков.НачальнаяШирота,
		|	втМассивОтрезков.НачальнаяДолгота,
		|	втМассивОтрезков.КонечнаяШирота,
		|	втМассивОтрезков.КонечнаяДолгота,
		|	втМассивОтрезков.ПриоритетноеПостроениеМассиваРасстояний,
		|	втМассивОтрезков.ДопОграниченияСитиГИД,
		|	ОграниченияМаршрутизатора.Ограничение,
		|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПытатьсяРассчитатьСнова, ИСТИНА) ИЛИ ЕСТЬNULL(упРасстоянияМеждуТочками.ДатаИзменения, ДАТАВРЕМЯ(1, 1, 1)) <= &ДатаИзменения КАК ПытатьсяРассчитатьСнова, 
		|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПопыткаРасчета, 0) КАК ПопыткаРасчета,
		|	ЕСТЬNULL(упРасстоянияМеждуТочками.ДатаИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаИзменения,
		|	(втМассивОтрезков.НачальнаяШирота - втМассивОтрезков.КонечнаяШирота) * (втМассивОтрезков.НачальнаяШирота - втМассивОтрезков.КонечнаяШирота) + (втМассивОтрезков.НачальнаяДолгота - втМассивОтрезков.КонечнаяДолгота) * (втМассивОтрезков.НачальнаяДолгота - втМассивОтрезков.КонечнаяДолгота) КАК Дельта
		|ИЗ
		|	втМассивОтрезков КАК втМассивОтрезков
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОграниченияМаршрутизатора
		|	ПО втМассивОтрезков.ДопОграниченияСитиГИД 
		|		ИЛИ ОграниченияМаршрутизатора.Ограничение = ЗНАЧЕНИЕ(Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
		|	ПО втМассивОтрезков.НачальнаяШирота = упРасстоянияМеждуТочками.НачальнаяШирота
		|			И втМассивОтрезков.НачальнаяДолгота = упРасстоянияМеждуТочками.НачальнаяДолгота
		|			И втМассивОтрезков.КонечнаяШирота = упРасстоянияМеждуТочками.КонечнаяШирота
		|			И втМассивОтрезков.КонечнаяДолгота = упРасстоянияМеждуТочками.КонечнаяДолгота
		|			И ОграниченияМаршрутизатора.Ограничение = упРасстоянияМеждуТочками.Ограничение
		|ГДЕ
		|	(ЕСТЬNULL(упРасстоянияМеждуТочками.ДатаИзменения, ДАТАВРЕМЯ(1, 1, 1)) <= &ДатаИзменения
		|			ИЛИ (ЕСТЬNULL(упРасстоянияМеждуТочками.ПопыткаРасчета, 1) <> 0
		|	И ЕСТЬNULL(упРасстоянияМеждуТочками.ПытатьсяРассчитатьСнова, ИСТИНА)))
		|	И ЕСТЬNULL(упРасстоянияМеждуТочками.АвтоматическийРасчет, ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	втМассивОтрезков.ПриоритетноеПостроениеМассиваРасстояний,
		|	ПопыткаРасчета,
		|	Дельта,
		|	ДатаИзменения";
		Запрос.УстановитьПараметр("ДатаИзменения", ДатаИзменения);
		
		Если КоличествоПотоков = 0 Тогда 
			КоличествоПотоков = 1;
		КонецЕсли;
		Если РазмерПорции = 0 Тогда 
			РазмерПорции = 100;
		КонецЕсли;
		
		Если ВызовИзФормы Тогда 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ ПЕРВЫЕ 1", "ВЫБРАТЬ");
		Иначе	
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ ПЕРВЫЕ 1", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(КоличествоПотоков * РазмерПорции, "ЧДЦ=; ЧГ="));
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		СчетчикРасстояний = 0;
		ТаблицыПорций         = Новый Массив;
		
		Пока Выборка.Следующий() Цикл 
			Если ТаблицыПорций.Количество() = СчетчикРасстояний Тогда 
				тбзПланирование = НоваяТаблицаПорцииРасстояний();
				ТаблицыПорций.Добавить(тбзПланирование);
			КонецЕсли;
			НоваяСтрока = ТаблицыПорций[СчетчикРасстояний].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ДопОграниченияСитиГИД = РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД И Выборка.ДопОграниченияСитиГИД;
			СчетчикРасстояний = СчетчикРасстояний + 1;
			Если СчетчикРасстояний = КоличествоПотоков Тогда 
				СчетчикРасстояний = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(АдресВременногоХранилищаРезультата) Тогда
		Результат = Новый Структура("ТаблицыПорций, РассчитаноБлижайшие, НеРассчитаноБлижайшие, НевозможноРассчитатьБлижайшие", ТаблицыПорций, РассчитаноБлижайшие, НеРассчитаноБлижайшие, НевозможноРассчитатьБлижайшие);
		ПоместитьВоВременноеХранилище(Результат, АдресВременногоХранилищаРезультата);
	КонецЕсли; 
	Возврат ТаблицыПорций;
	
КонецФункции

Функция _КоличествоНерассчитанныхРасстояний(Знач Точки = Неопределено, Знач ТолькоНеРассчитанные, Знач РазрешитьБлижайшие = Истина, АдресВременногоХранилищаРезультата = "") Экспорт 
	
	мсвТаблиц = ПолучитьНерассчитанныеРасстояния(Точки, ТолькоНеРассчитанные, РазрешитьБлижайшие);
	Результат = 0;
	Для Каждого ТаблицаПорции Из мсвТаблиц Цикл
		Результат = Результат + ТаблицаПорции.Количество();
	КонецЦикла;
	Если ЗначениеЗаполнено(АдресВременногоХранилищаРезультата) Тогда
		ПоместитьВоВременноеХранилище(Результат, АдресВременногоХранилищаРезультата);
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

// Процедура рассчитывает порцию расстояний между точками.
//
// Параметры:
//  тбзПланирование - ТаблицаЗначений - Таблица данных.
//	СообщатьСостояние - Булево - Сообщить.
//
Процедура РассчитатьПорциюРасстоянийПоКарте(ТаблицаПланирования, СообщатьСостояние = Ложь) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если Ложь Тогда // Для контекстной подсказки
		ТаблицаПланирования = Новый ТаблицаЗначений;
	КонецЕсли;
	Количество = ТаблицаПланирования.Количество();
	Сч         = 0;
	Процент    = 0;
	
	ПараметрыКарты = ПодготовитьКартуПоТипу(,Истина);
	Если НЕ ПараметрыКарты = Неопределено Тогда 
		ПакетныйРежимОтправкиЗапросаСервису = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПакетныйРежимЗапросовСервисуМаршрутизации");
		Если Истина
			И ПакетныйРежимОтправкиЗапросаСервису
			И (Ложь 
				Или ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД)
		Тогда
			РазмерПорцииДляВызоваСервиса = 30;
			РазмерПакетаПользовательский = упСервисныеФункции.ПолучитьЗначениеКонстанты("упРазмерПакетаВЗапросеКМаршрутизатору");
			Если ЗначениеЗаполнено(РазмерПакетаПользовательский) Тогда
				РазмерПорцииДляВызоваСервиса = РазмерПакетаПользовательский;
			КонецЕсли; 
			Для Каждого ЭлементСпискаОграничения Из ПараметрыКарты.СписокОграниченийСитиГИД Цикл
				ОтборПарТочек = Новый Структура("Ограничение", ЭлементСпискаОграничения.Значение);
				ТаблицаПоОграничению = ТаблицаПланирования.НайтиСтроки(ОтборПарТочек);
				ПараметрыКарты.Вставить("Ограничение", ЭлементСпискаОграничения.Значение);
				Если ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
					Если ЗначениеЗаполнено(ЭлементСпискаОграничения.Значение) Тогда
						ОтборПарТочек.Вставить("ДопОграниченияСитиГИД", Истина);
						ПараметрыКарты.Вставить("ДопОграниченияСитиГИД", Истина);
						ТекущийЭлемент = ПараметрыКарты.СписокОграниченийСитиГИД.НайтиПоЗначению(ЭлементСпискаОграничения.Значение);
						ПараметрыКарты.Параметры.parameters.Вставить("limitsName", ТекущийЭлемент.Представление);
						ПараметрыКарты.ПараметрыМаршрутизацииСитиГИД.Вставить("limitsName", ТекущийЭлемент.Представление);
					Иначе
						ПараметрыКарты.Вставить("ДопОграниченияСитиГИД", Ложь);
					КонецЕсли; 
				КонецЕсли;
				Пакет = Новый Массив;
				Для Каждого СтрокаПорции Из ТаблицаПоОграничению Цикл
					Если Не СтрокаПорции.ПытатьсяРассчитатьСнова Тогда 
						Продолжить;
					КонецЕсли;
					НачальныйАдрес = Новый Структура("Широта,Долгота", СтрокаПорции.НачальнаяШирота,СтрокаПорции.НачальнаяДолгота);
					КонечныйАдрес  = Новый Структура("Широта,Долгота", СтрокаПорции.КонечнаяШирота,СтрокаПорции.КонечнаяДолгота);
					Если СтрокаПорции.НачальнаяШирота = СтрокаПорции.КонечнаяШирота И СтрокаПорции.НачальнаяДолгота = СтрокаПорции.КонечнаяДолгота Тогда 
						сткВозврат = Новый Структура("Ограничение, Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь",
						СтрокаПорции.Ограничение, 0, 0, 0, 0, 0, 0, 0, 0, 0, "");
						ЗаписатьРасстоянияМеждуТочками(СтрокаПорции.НачальнаяШирота, СтрокаПорции.НачальнаяДолгота, СтрокаПорции.КонечнаяШирота, СтрокаПорции.КонечнаяДолгота, сткВозврат);
					Иначе
						ОписаниеРасстояния = Новый Структура("НачальныйАдрес, КонечныйАдрес", НачальныйАдрес, КонечныйАдрес);
						Пакет.Добавить(ОписаниеРасстояния);
					КонецЕсли;
					упСервисныеФункции.ОбновитьИндикатор(Количество, Сч, Процент);
					Если Ложь
						Или Пакет.Количество() = РазмерПорцииДляВызоваСервиса
						Или ТаблицаПоОграничению.ВГраница() = ТаблицаПоОграничению.Найти(СтрокаПорции)
					Тогда
						РассчитатьРасстояниеМеждуТочкамиПоТипуКартыПакетно(Пакет, ПараметрыКарты);
						Пакет = Новый Массив;
					КонецЕсли; 
				КонецЦикла;	
				Если Пакет.Количество() > 0 Тогда
					ВызватьИсключение "Остаток " + Пакет.Количество();
				КонецЕсли; 
			КонецЦикла;
		Иначе
			Для Каждого СтрокаПорции Из ТаблицаПланирования Цикл
				Если Не СтрокаПорции.ПытатьсяРассчитатьСнова Тогда 
					Продолжить;
				КонецЕсли;
				НачальныйАдрес = Новый Структура("Широта,Долгота",СтрокаПорции.НачальнаяШирота,СтрокаПорции.НачальнаяДолгота);
				КонечныйАдрес  = Новый Структура("Широта,Долгота",СтрокаПорции.КонечнаяШирота,СтрокаПорции.КонечнаяДолгота);
				
				Если СтрокаПорции.НачальнаяШирота = СтрокаПорции.КонечнаяШирота И СтрокаПорции.НачальнаяДолгота = СтрокаПорции.КонечнаяДолгота Тогда 
					сткВозврат = Новый Структура("Ограничение, Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь",
					СтрокаПорции.Ограничение, 0, 0, 0, 0, 0, 0, 0, 0, 0, "");
					ЗаписатьРасстоянияМеждуТочками(СтрокаПорции.НачальнаяШирота, СтрокаПорции.НачальнаяДолгота, СтрокаПорции.КонечнаяШирота, СтрокаПорции.КонечнаяДолгота, сткВозврат);
				Иначе
					ПараметрыКарты.Вставить("Ограничение", СтрокаПорции.Ограничение);
					Если ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда	
						ПараметрыКарты.Вставить("ДопОграниченияСитиГИД", СтрокаПорции.ДопОграниченияСитиГИД);
						ТекущийЭлемент = ПараметрыКарты.СписокОграниченийСитиГИД.НайтиПоЗначению(СтрокаПорции.Ограничение);
						ПараметрыКарты.Параметры.parameters.Вставить("limitsName", ТекущийЭлемент.Представление);
						ПараметрыКарты.ПараметрыМаршрутизацииСитиГИД.Вставить("limitsName", ТекущийЭлемент.Представление);				
					КонецЕсли;	
					
					// начало замера				
					ВремяНачала = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени();
					// начало замера				
					сткВозврат = ПолучитьРасстояниеМеждуТочкамиПоТипуКарты(ПараметрыКарты, НачальныйАдрес,КонечныйАдрес);
					Если сткВозврат = Неопределено Тогда
						ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(СтрокаПорции.НачальнаяШирота, СтрокаПорции.НачальнаяДолгота, СтрокаПорции.КонечнаяШирота, СтрокаПорции.КонечнаяДолгота, СтрокаПорции.Ограничение);
					КонецЕсли;	
					// окончание замера				
					КлючеваяОперация = Неопределено;
					Если ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
						КлючеваяОперация = Справочники.КлючевыеОперации.РасчетМаршрутаПолныйСитиГИД;
					ИначеЕсли ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
						КлючеваяОперация = Справочники.КлючевыеОперации.РасчетМаршрутаПолныйAxelotMaps;	
					КонецЕсли; 
					
					Если Не КлючеваяОперация = Неопределено Тогда
						ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(КлючеваяОперация, ВремяНачала);
					КонецЕсли; 
					// окончание замера
				КонецЕсли;
				упСервисныеФункции.ОбновитьИндикатор(Количество, Сч, Процент);
			КонецЦикла;	
		КонецЕсли; 
	КонецЕсли;
	
	ПостОбработкаКартыПоТипу(ПараметрыКарты);
	УстановитьПривилегированныйРежим(Ложь);	
КонецПроцедуры

Функция НоваяТаблицаПорцииРасстояний()
	
	тбзПланирование = Новый ТаблицаЗначений;
	тбзПланирование.Колонки.Добавить("НачальнаяШирота");
	тбзПланирование.Колонки.Добавить("НачальнаяДолгота");
	тбзПланирование.Колонки.Добавить("КонечнаяШирота");
	тбзПланирование.Колонки.Добавить("КонечнаяДолгота");
	тбзПланирование.Колонки.Добавить("Ограничение");
	тбзПланирование.Колонки.Добавить("ДопОграниченияСитиГИД");
	тбзПланирование.Колонки.Добавить("ПытатьсяРассчитатьСнова");
	Возврат тбзПланирование;
	
КонецФункции //РассчитатьРасстоянияПоКарте()

// Процедура рассчитывает расстояния между точками
//
// Параметры:
//  НачальнаяШирота - Число - Значение широта.
//	НачальнаяДолгота - Число - Значение долгота.
//  КонечнаяШирота - Число - Значение широта.
//	КонечнаяДолгота - Число - Значение долгота.
//  Данные - Структура -  параметры заполнения.
//
Процедура ЗаписатьРасстоянияМеждуТочками(НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота, Данные, Ограничение = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если Ограничение = Неопределено Тогда
		Ограничение = ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка");
	КонецЕсли; 
	Запись = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
	Запись.НачальнаяШирота  = НачальнаяШирота;
	Запись.НачальнаяДолгота = НачальнаяДолгота;
	Запись.КонечнаяШирота   = КонечнаяШирота;
	Запись.КонечнаяДолгота  = КонечнаяДолгота;
	Запись.Ограничение      = Ограничение;
	Запись.Прочитать();
	Запись.НачальнаяШирота  = НачальнаяШирота;
	Запись.НачальнаяДолгота = НачальнаяДолгота;
	Запись.КонечнаяШирота   = КонечнаяШирота;
	Запись.КонечнаяДолгота  = КонечнаяДолгота;
	Запись.Ограничение      = Ограничение;
	Запись.Ошибка = "";
	Запись.ПутьПростой = "";
	ЗаполнитьЗначенияСвойств(Запись, Данные);
	Запись.АвтоматическийРасчет = Истина;
	Запись.ПопыткаРасчета = 0;
	Запись.ВремяБазовое = 0;
	Запись.ДатаИзменения = ТекущаяДатаСеанса();
	Запись.Записать();
	
КонецПроцедуры // ЗаписатьРасстоянияМеждуТочками()

// Процедура рассчитывает расстояния между точками.
//
// Параметры:
//  НачальнаяШирота - Число - Значение широта.
//	НачальнаяДолгота - Число - Значение долгота.
//  КонечнаяШирота - Число - Значение широта.
//	КонечнаяДолгота - Число - Значение долгота.
//	Ограничение - ПеречислениеСсылка.упОграниченияМаршрутизаторов - Вид ограничения.
//
Процедура ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота, Ограничение = Неопределено, ПытатьсяРассчитатьСнова = Истина,
	Ошибка = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущееОграничение = Ограничение;
	Если Ограничение = Неопределено Тогда
		ТекущееОграничение = Перечисления.упОграниченияМаршрутизаторов.ПустаяСсылка();
	КонецЕсли;
	
	Запись = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
	Запись.НачальнаяШирота  = НачальнаяШирота;
	Запись.НачальнаяДолгота = НачальнаяДолгота;
	Запись.КонечнаяШирота   = КонечнаяШирота;
	Запись.КонечнаяДолгота  = КонечнаяДолгота;
	Запись.Ограничение      = ТекущееОграничение;
	Запись.Прочитать();
	
	Запись.НачальнаяШирота      = НачальнаяШирота;
	Запись.НачальнаяДолгота     = НачальнаяДолгота;
	Запись.КонечнаяШирота       = КонечнаяШирота;
	Запись.КонечнаяДолгота      = КонечнаяДолгота;
	Запись.Ограничение          = ТекущееОграничение;
	Запись.АвтоматическийРасчет = Истина;
	Запись.ПопыткаРасчета       = Запись.ПопыткаРасчета + 1;
	Запись.ПытатьсяРассчитатьСнова = ПытатьсяРассчитатьСнова;
	Запись.Ошибка = Ошибка;
	Запись.ДатаИзменения        = ТекущаяДата();
	Запись.Записать();
	
КонецПроцедуры // ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками()

// Добавим счетчик попытки получить данные геосервиса.
//
// Параметры:
//	ТипМаршрутизатора - ПеречислениеСсылка.упТипыМаршрутизаторов - тип маршрутизатора.
//  УдачноеПодключение  - Булево - Если данные получены от геокодера.
//  ПараметрыОтвета  - Структура - Параметры ответа от сервиса геокодирования.
//
Процедура ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,УдачноеПодключение,ПараметрыОтвета = Неопределено, КоличествоРасстояний = 1) Экспорт
	
	Если ТипЗнч(ПараметрыОтвета)=ТИП("Структура") И ПараметрыОтвета.Свойство("СообщениеОбОшибке")Тогда
		СтрокаПоиска = НРег("Ошибка работы с Интернет");
		Если СтрНайти(НРег(ПараметрыОтвета.СообщениеОбОшибке),СтрокаПоиска) > 0 Тогда
			// Ошибка подключения к интернету.
			// Такой вариант не обрабатываем.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущаяДатаОбращения = НачалоДня(ТекущаяДатаСеанса());
	
	НачатьТранзакцию();
	
	НомерСеанса = НомерСеансаИнформационнойБазы();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатистикаОбращенийКСервисамМаршрутизации");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Период"           , ТекущаяДатаОбращения);
	ЭлементБлокировки.УстановитьЗначение("ТипМаршрутизатора", ТипМаршрутизатора);
	ЭлементБлокировки.УстановитьЗначение("НомерСеанса"      , НомерСеанса);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли; 
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	Запись = РегистрыСведений.упСтатистикаОбращенийКСервисамМаршрутизации.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДатаОбращения;
	Запись.ТипМаршрутизатора = ТипМаршрутизатора;
	Запись.НомерСеанса = НомерСеанса;
	Запись.Прочитать();
	
	Если НЕ Запись.Выбран() Тогда
		Запись.Период = ТекущаяДатаОбращения;
		Запись.ТипМаршрутизатора = ТипМаршрутизатора;
		Запись.НомерСеанса = НомерСеанса;
	КонецЕсли;
	
	Если УдачноеПодключение Тогда
		Запись.КоличествоУспешно = Запись.КоличествоУспешно + КоличествоРасстояний;
	Иначе
		Запись.КоличествоНеудачно = Запись.КоличествоНеудачно + КоличествоРасстояний;
	КонецЕсли;
	
	Запись.Записать();
	
	Если ТранзакцияАктивна() Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли; 
	
КонецПроцедуры // ДобавитьРезультатОбращенияКСервисуГеоКодирования()

// Подготовка картографического сервиса Graphhopper.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//   Структура - Ключи:
//		ИмяФайла - Строка - имя файла на диск.
//   	Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   	Ключ - Строка - Параметры подключения для карты.
//
Функция ПодготовитьКартуGraphhopper()
	
	ИмяФайла = СтрШаблон("%1Graphhopper%2.xml",КаталогВременныхФайлов(),Строка(Новый УникальныйИдентификатор));
	URLСервера = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераМаршрутизацииGraphhopper");
	Если НЕ(Прав(URLСервера,1) = "/") Тогда
		URLСервера = URLСервера + "/";
	КонецЕсли;
	
	//ИспользоватьТестовоеПодключениеМаршрутизация = упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользоватьТестовоеПодключениеМаршрутизация");
	//Структура = упМаршрутизация.ПолучитьТекстАдресаПодключенияКСерверу(URLСервера ,ИспользоватьТестовоеПодключениеМаршрутизация, Истина);
	//
	//URLСервера = Структура.АдресСервера;
	
	ПараметрыПолучения = Неопределено;
	
	//Пользователь = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПользовательСервисаМаршрутизацииИКартографииAXELOT");
	//
	//Если НЕ ПустаяСтрока(Пользователь) Тогда
	//	ПараметрыПолучения = Новый Структура();
	//	ПараметрыПолучения.Вставить("Пользователь",Пользователь);
	//КонецЕсли;
	//
	//Если НЕ ПараметрыПолучения = Неопределено Тогда
	//	Пароль = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПарольСервисаМаршрутизацииИКартографииAXELOT");
	//	ПараметрыПолучения.Вставить("Пароль",Пароль);
	//КонецЕсли;
	
	Возврат Новый Структура("ИмяФайла, Соединение, Ключ,ПараметрыПолучения", ИмяФайла, URLСервера, Неопределено,ПараметрыПолучения);
	
КонецФункции //ПодготовитьКартуGraphhopper

// Подготовка картографического сервиса AxelotMaps.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//   Структура - Ключи:
//		ИмяФайла - Строка - имя файла на диск.
//   	Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   	Ключ - Строка - Параметры подключения для карты.
//
Функция ПодготовитьКартуAxelotMaps()
	
	ИмяФайла = СтрШаблон("%1AxelotMaps%2.xml",КаталогВременныхФайлов(),Строка(Новый УникальныйИдентификатор));
	URLСервера = упМаршрутизация.ПолучитьПараметрыПодключенияAxelotMaps(Истина).АдресСервера;
	Если НЕ(Прав(URLСервера,1) = "/") Тогда
		URLСервера = URLСервера + "/";
	КонецЕсли;
	
	//ИспользоватьТестовоеПодключениеМаршрутизация = упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользоватьТестовоеПодключениеМаршрутизация");
	//Структура = упМаршрутизация.ПолучитьТекстАдресаПодключенияКСерверу(URLСервера ,ИспользоватьТестовоеПодключениеМаршрутизация, Истина);
	//
	//URLСервера = Структура.АдресСервера;
	
	ПараметрыПолучения = Неопределено;
	
	//Пользователь = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПользовательСервисаМаршрутизацииИКартографииAXELOT");
	//
	//Если НЕ ПустаяСтрока(Пользователь) Тогда
	//	ПараметрыПолучения = Новый Структура();
	//	ПараметрыПолучения.Вставить("Пользователь",Пользователь);
	//КонецЕсли;
	//
	//Если НЕ ПараметрыПолучения = Неопределено Тогда
	//	Пароль = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПарольСервисаМаршрутизацииИКартографииAXELOT");
	//	ПараметрыПолучения.Вставить("Пароль",Пароль);
	//КонецЕсли;
	
	Возврат Новый Структура("ИмяФайла, Соединение, Ключ,ПараметрыПолучения", ИмяФайла, URLСервера, Неопределено,ПараметрыПолучения);
	
КонецФункции //ПодготовитьКартуGraphhopper


// Подготовка картографического сервиса Google.
//
// Параметры:
//	Ключ - Строка - Параметры подключения для карты.
//	Обработка - Булево - Флаг, потребуеться экземпляр InternetExplorer.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			ИмяФайла - Строка - имя файла на диск.
//   		Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   		Ключ - Строка - Параметры подключения для карты.
//   		IExplorer - COMОбъект - экземпляр объекта IExplorer.
//   		HTMLДокумент - Строка - текст HTML документа получен из общего макета.
//   		HTML_ИмяФайла - Строка - Имя файла HTML документа.
//
Функция ПодготовитьКартуGoogle(Ключ = Неопределено,Обработка = Неопределено)
	
	Если Ключ = Неопределено Тогда
		Ключ = упСервисныеФункции.ПолучитьЗначениеКонстанты("упДополнительныеПараметрыЗапросаМаршрутизатора");
	КонецЕсли;
	
	КаталогВременныхФайлов = КаталогВременныхФайлов();
	// Имя файла для хранения результата.
	ИмяФайла = СтрШаблон("%1Google-route%2.xml",КаталогВременныхФайлов,Строка(Новый УникальныйИдентификатор));
	URLСервера = "maps.googleapis.com";
	Прокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("http://" + URLСервера);
	Соединение = Новый HTTPСоединение(URLСервера,,,,Прокси);
	
	Структура = Неопределено;
	
	Если Обработка = Неопределено Тогда
		Структура = Новый Структура("ИмяФайла, Соединение, Ключ", ИмяФайла, Соединение, Ключ);
	ИначеЕсли Обработка = Истина Тогда
		
		HTMLДокумент = ПолучитьОбщийМакет("HTML_Google_Route").ПолучитьТекст();	
		// Имя файла для документа html.
		HTML_ИмяФайла = СтрШаблон("%1Google-route%2.html",КаталогВременныхФайлов(),Строка(Новый УникальныйИдентификатор));
		
		ЗТ = Новый ЗаписьТекста(HTML_ИмяФайла, КодировкаТекста.UTF8);
		ЗТ.ЗаписатьСтроку(HTMLДокумент);    
		ЗТ.Закрыть();
		
		Попытка
			IExplorer = Новый COMОбъект("InternetExplorer.Application");
			IExplorer.Visible = Ложь;
			IExplorer.Navigate(HTML_ИмяФайла,8);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,, "Ошибка создания приложения InternetExplorer.Application...");
			IExplorer = Неопределено;
		КонецПопытки;
		Структура = Новый Структура("ИмяФайла,Соединение,Ключ,IExplorer,HTML_ИмяФайла,Обработка",ИмяФайла,Соединение,Ключ,IExplorer,HTML_ИмяФайла,Истина);
	КонецЕсли;
	
	Возврат Структура
	
КонецФункции //ПодготовитьКартуGoogle()

// Рассчитывает расстояние между точками при помощи ЭГК Yandex.
//
// Параметры:
//	Ключ - Строка - Параметры подключения для карты.
//	Обработка - Булево - Флаг, потребуеться экземпляр InternetExplorer.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			ИмяФайла - Строка - имя файла на диск.
//   		Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   		Ключ - Строка - Параметры подключения для карты.
//
Функция ПодготовитьКартуYandex(Ключ = Неопределено,Обработка = Неопределено)
	
	Если Ключ = Неопределено Тогда
		
		Ключ = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючСервисаМаршрутизации");
		
	КонецЕсли;
	
	ИмяФайла = СтрШаблон("%1Yandex-route_%2.JSON",КаталогВременныхФайлов(),Строка(Новый УникальныйИдентификатор));
	URLСервера = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераМаршрутизацииЯндекс");
	Если НЕ(Прав(URLСервера,1) = "/") Тогда
		URLСервера = URLСервера + "/";
	КонецЕсли;
	
	URLСтрока = URLСервера + "router/route?points=";
	
	ПараметрыПолучения = Неопределено;
	
	Возврат Новый Структура("ИмяФайла, Соединение, Ключ,ПараметрыПолучения", ИмяФайла, URLСтрока, Ключ,ПараметрыПолучения);
	
КонецФункции //ПодготовитьКартуYandex()

// Рассчитывает расстояние между точками при помощи Яндекс.Маршрутизация: Матрица расстояний.
//
// Параметры:
//	Ключ - Строка - Параметры подключения для карты.
//	Обработка - Булево - Флаг, потребуеться экземпляр InternetExplorer.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			ИмяФайла - Строка - имя файла на диск.
//   		Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   		Ключ - Строка - Параметры подключения для карты.
//
Функция ПодготовитьЯндексМаршрутизация(Ключ = Неопределено,Обработка = Неопределено, ДатаНачалаПланирования = Неопределено)
	Если Ключ = Неопределено Тогда
		Ключ = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючСервисаМаршрутизации");
	КонецЕсли;
	ИмяФайла = СтрШаблон("%1Yandex-route_%2.JSON",КаталогВременныхФайлов(),Строка(Новый УникальныйИдентификатор));
	
	ПараметрыПолучения = Неопределено;
	ДополнительныеПараметрыЗапросаМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упДополнительныеПараметрыЗапросаМаршрутизатора");
	Если ЗначениеЗаполнено(ДополнительныеПараметрыЗапросаМаршрутизатора) Тогда
		ПараметрыПолучения = Новый Структура("ДополнительныеПараметрыЗапросаМаршрутизатора", ДополнительныеПараметрыЗапросаМаршрутизатора);
	КонецЕсли;
	departure_time = "";
	ВремяОтправленияПоУмолчаниюЯндексМаршрутизация = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВремяОтправленияПоУмолчаниюЯндексМаршрутизация");
	Если ДатаНачалаПланирования = Неопределено Тогда
		ВремяОтправления = ТекущаяДатаСеанса();
	Иначе
		ВремяОтправления = ДатаНачалаПланирования;
	КонецЕсли;
	Если ЗначениеЗаполнено(ВремяОтправленияПоУмолчаниюЯндексМаршрутизация)
		ИЛИ НЕ ДатаНачалаПланирования = Неопределено Тогда
		
		departure_time = ПодготовитьВремяОтправленияВUNIXФормат(ВремяОтправленияПоУмолчаниюЯндексМаршрутизация, ВремяОтправления);
		Если ПараметрыПолучения = Неопределено Тогда
			ПараметрыПолучения = Новый Структура("ВремяОтправленияПоУмолчаниюЯндексМаршрутизация", ВремяОтправленияПоУмолчаниюЯндексМаршрутизация);
		Иначе
			ПараметрыПолучения.Вставить("ВремяОтправленияПоУмолчаниюЯндексМаршрутизация", ВремяОтправленияПоУмолчаниюЯндексМаршрутизация);
		КонецЕсли;
	КонецЕсли;
	URLСтрока = СтрШаблон("https://api.routing.yandex.net/v1.0/route?apikey=%1%2%3&waypoints=", Ключ, departure_time, ДополнительныеПараметрыЗапросаМаршрутизатора);
	Возврат Новый Структура("ИмяФайла, Соединение, Ключ,ПараметрыПолучения", ИмяФайла, URLСтрока, Ключ,ПараметрыПолучения);
	
КонецФункции //ПодготовитьЯндексМаршрутизация()

// Выполним перевод верени в нужный формат
//
// Параметры:
//  ВремяОтправления  - Число - Количество часов.
//  ДатаОтправления  - Дата - Дата для формиррования.
//
// Возвращаемое значение:
//   Строка   - Параметр для подстановки.
//
Функция ПодготовитьВремяОтправленияВUNIXФормат(ВремяОтправления, ДатаОтправления)
	Результат = "";
	Если ЗначениеЗаполнено(ВремяОтправления) Тогда
		ОжидаемоеВремяОтправления = ДатаОтправления + (ВремяОтправления * 60 * 60);
		ВремяВUNIXФормат = упСервисныеФункции.ПеревестиДатуВремяВUNIXФормат(ОжидаемоеВремяОтправления);
		Результат = СтрШаблон("&departure_time=%1", Формат(ВремяВUNIXФормат, "ЧН=0; ЧГ=0"));
	КонецЕсли;
	Возврат Результат;
КонецФункции // ПодготовитьВремяОтправленияВUNIXФормат()


// Подготовка параметров подключение к сервису маршрутизации СитиГИД.
//
// Параметры:
//	Ключ - Строка - Параметры подключения для карты.
//	Обработка - Булево - Флаг, потребуеться экземпляр InternetExplorer.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			ИмяФайла - Строка - имя файла на диск.
//   		Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   		Ключ - Строка - Параметры подключения для карты.
//
Функция ПодготовитьКартуСитиГИД(Ключ = Неопределено,Обработка = Неопределено)
	
	Если Ключ = Неопределено Тогда
		Ключ = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючДоступаСервисаМаршрутизацииСитиГИД");
	КонецЕсли;
	
	Сервер = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераМаршрутизацииСитиГИД");
	
	Ресурс = упСервисныеФункции.ПолучитьЗначениеКонстанты("упРесурсСитиГИД");
	Прокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("http://" + Сервер);
	
	HTTP =  Новый HTTPСоединение(Сервер,,,,Прокси);
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("url", Ресурс);	
	ЗаголовокHTTP.Вставить("type", "POST");
	ЗаголовокHTTP.Вставить("dataType", "text");
	ЗаголовокHTTP.Вставить("cache", false);
	ЗаголовокHTTP.Вставить("timeout", 90000);
	
	ФайлЗапроса = ПолучитьИмяВременногоФайла("JSON");
	ФайлРезультата = ПолучитьИмяВременногоФайла("JSON");
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("accessKey",Ключ);
	сткПараметры.Вставить("getPoints",Истина);
	
	сткПараметр = ПолучитьПараметрыМаршрутизацииСитиГИД();
	
	Если сткПараметр = Неопределено Тогда
		сткПараметр = Новый Структура;
		сткПараметр.Вставить("type","OptimizeLength");
		сткПараметр.Вставить("allowYards",Истина);
		сткПараметр.Вставить("allowSideWays",Истина);
		сткПараметр.Вставить("allowGroundRoads",Истина);
		сткПараметр.Вставить("allowPaidRoads",Истина);
		сткПараметр.Вставить("useJams",Ложь);
		сткПараметр.Вставить("useLimitsUpd",Ложь);
		сткПараметр.Вставить("limitsName","");
		сткПараметр.Вставить("useForecast",Ложь);	
		сткПараметр.Вставить("forecastDateTimeUtc",ДАТА("00010101000000"));
		СписокЗначений = Новый СписокЗначений;
		Для каждого МетаЗначение Из Метаданные.Перечисления.упОграниченияМаршрутизаторов.ЗначенияПеречисления Цикл
			ТекущийПропуск = Перечисления.упОграниченияМаршрутизаторов[МетаЗначение.Имя];
			СтруктураОграничения = Новый Структура();
			СтруктураОграничения.Вставить("Пометка",Ложь);
			СтруктураОграничения.Вставить("Ограничение", ТекущийПропуск);
			СписокЗначений.Добавить(СтруктураОграничения);
		КонецЦикла;
		
		сткПараметр.Вставить("ДополнительныеОграниченияСитиГИД", СписокЗначений);
		
	КонецЕсли;
	
	сткКонстанта = Новый Структура;	
	Для каждого КлючЗначение Из сткПараметр Цикл
		сткКонстанта.Вставить(КлючЗначение.Ключ,КлючЗначение.Значение);
		Если КлючЗначение.Ключ = "ДополнительныеОграниченияСитиГИД" Тогда
			сткПараметр.Удалить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	сткПараметры.Вставить("parameters",сткПараметр);
	
	Структура = Новый Структура("ИмяФайла,Соединение,Ключ,Обработка","",Неопределено,Ключ,Истина);
	Структура.Вставить("HTTP",HTTP);
	Структура.Вставить("ФайлЗапроса",ФайлЗапроса);
	Структура.Вставить("ФайлРезультата",ФайлРезультата);
	Структура.Вставить("ЗаголовокHTTP",ЗаголовокHTTP);
	Структура.Вставить("Параметры",сткПараметры);	
	Структура.Вставить("ПараметрыМаршрутизацииСитиГИД",сткКонстанта);
	
	ДополнительныеОграниченияСитиГИД = сткКонстанта.ДополнительныеОграниченияСитиГИД;
	
	ДоступныеОграничения = Новый СписокЗначений;
	ДоступныеОграничения.Добавить(Перечисления.упОграниченияМаршрутизаторов.ПустаяСсылка(), "");
	Если Константы.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД.Получить() Тогда
		Для каждого ЭлементСпискаЗначений Из ДополнительныеОграниченияСитиГИД Цикл
			Если ЭлементСпискаЗначений.Значение.Пометка Тогда
				Если ЭлементСпискаЗначений.Значение.Ограничение = Перечисления.упОграниченияМаршрутизаторов.КромеМКАД Тогда
					ИмяОграничения = "mkad";
				ИначеЕсли ЭлементСпискаЗначений.Значение.Ограничение = Перечисления.упОграниченияМаршрутизаторов.КромеСадовогоКольца Тогда
					ИмяОграничения = "sadovoe";
				ИначеЕсли ЭлементСпискаЗначений.Значение.Ограничение = Перечисления.упОграниченияМаршрутизаторов.КромеТТК Тогда
					ИмяОграничения = "ttk";
				Иначе
					ИмяОграничения = "";
				КонецЕсли;
				ДоступныеОграничения.Добавить(ЭлементСпискаЗначений.Значение.Ограничение, ИмяОграничения);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	Структура.Вставить("СписокОграниченийСитиГИД", ДоступныеОграничения);
	Структура.Вставить("Ограничение", ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка"));
	Возврат Структура;
	
КонецФункции //ПодготовитьКартуСитиГИД()

Функция ПорядокОграниченийМаршрутизатора() Экспорт 
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.упОграниченияМаршрутизаторов.ПустаяСсылка());
	Для каждого МетаЗначение Из Метаданные.Перечисления.упОграниченияМаршрутизаторов.ЗначенияПеречисления Цикл
		Результат.Добавить(Перечисления.упОграниченияМаршрутизаторов[МетаЗначение.Имя]);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

// Выполним подготовку карты согласно типа карт.
//
// Параметры:
//	ТипМаршрутизатора - ПеречислениеСсылка.упТипыМаршрутизаторов - тип маршрутизатора.
//	Обработка - Булево - Флаг, потребуеться экземпляр InternetExplorer.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			ИмяФайла - Строка - имя файла на диск.
//   		Соединение - HTTPСоединение - Предназначен для работы с файлами на http-серверах.
//   		Ключ - Строка - Параметры подключения для карты.
//
Функция ПодготовитьКартуПоТипу(ТипМаршрутизатора = Неопределено, Обработка = Неопределено, ДатаНачалаПланирования = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если ТипМаршрутизатора = Неопределено Тогда
		ТипМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
	КонецЕсли;
	
	Если ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
		
		Результат = ПодготовитьКартуGoogle(,Обработка);
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.Graphhopper Тогда
		
		Результат = ПодготовитьКартуGraphhopper();
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
		
		Результат = ПодготовитьКартуAxelotMaps();
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексКарты Тогда
		
		Результат = ПодготовитьКартуYandex();	
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексМаршрутизация Тогда
		
		Результат = ПодготовитьЯндексМаршрутизация(, Обработка, ДатаНачалаПланирования);	
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
		
		Результат = ПодготовитьКартуСитиГИД();
		
	КонецЕсли;
	
	Если ТипЗнч(Результат) = ТИП("Структура") Тогда
		Результат.Вставить("ТипМаршрутизатора", ТипМаршрутизатора);
		Результат.Вставить("упИспользуетсяПостроительМаршрутов", упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользуетсяПостроительМаршрутов"));
		Результат.Вставить("упКоличествоОбрабатываемыхТочекМаршрутизатора", упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоОбрабатываемыхТочекМаршрутизатора"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции //ПодготовитьКартуПоТипу()

// Выполним действия после обработки карты  согласно типу, 
//  удалил временные файлы, закроем открытый COMОбъект(ы).
//
// Параметры:
//	ПараметрыКарты  - структура -  ключи парамтры загрузки карты, Координата широты начало маршрута.
//
// Возвращаемое значение:
//   Нет.
//
Функция ПостОбработкаКартыПоТипу(ПараметрыКарты) Экспорт
	
	Если ПараметрыКарты.Свойство("Обработка")И ПараметрыКарты.Обработка Тогда
		Если ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексКарты Тогда		
			// Нет.
		ИначеЕсли ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
			Если НЕ ПараметрыКарты.IExplorer = Неопределено Тогда
				ПараметрыКарты.IExplorer.Quit();
			КонецЕсли;			
			УдалитьУказанныйФайл(ПараметрыКарты.HTML_ИмяФайла);
		КонецЕсли;
	КонецЕсли;
	
	УдалитьУказанныйФайл(ПараметрыКарты.ИмяФайла);
	
КонецФункции //ПостОбработкаКартыПоТипу()

// Рассчитывает расстояние между точками при помощи ЭГК Google.
//
// Параметры:
//	сткКарта  - Структура, ключи парамтры загрузки карты - Координата широты начало маршрута.
//	НачальнаяШирота  - Число - Координата широты начало маршрута.
//	НачальнаяДолгота - Число - Координата долготы начало маршрута.
//	КонечнаяШирота   - Число - Координата широты окончания маршрута.
//	КонечнаяДолгота  - Число - Координата долготы окончания маршрута.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиGoogle(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота)
	
	УспешноеПостроение = Ложь;
	СообщениеОбОшибке = "";
	
	ДлинаМаршрута = 0;
	ВремяМаршрута = 0;
	ИмяФайла = сткКарта.ИмяФайла;
	флгОбработка = Ложь;
	Если сткКарта.Свойство("Обработка") И сткКарта.Обработка Тогда
		флгОбработка = Истина;
	КонецЕсли;
	
	Сч = 1;
	
	тзВремяМеждуТочками = ПолучитьКаркасТаблицыВремениМеждуТочками();
	
	ТекущаяДатаОбработки = ТекущаяДатаСеанса();
	
	СтрКоординаты1 = ПредставлениеКоординаты(НачальнаяШирота) + "," + ПредставлениеКоординаты(НачальнаяДолгота);
	СтрКоординаты2 = ПредставлениеКоординаты(КонечнаяШирота) + "," + ПредставлениеКоординаты(КонечнаяДолгота);
	
	// На использование службы API маршрутов Google накладывается ограничение, составляющее 2500 запросов маршрутов в день.
	ClientSignature = сткКарта.Ключ; // &client=gme-YOUR_CLIENT_ID&signature=YOUR_URL_SIGNATURE.
	ТекстПериода = "&departure_time=" + Формат((ТекущаяДатаСеанса() + 43200) - Дата("19700101000000"),"ЧГ=0") + "&traffic_model=pessimistic"; // прогноз пробок работает только на будущее время.
	URLСтраницы = "maps/api/directions/xml?origin=" + СтрКоординаты1 + "&destination=" + СтрКоординаты2 + "&sensor=false&mode=driving&units=metric&language=ru-RU" + ТекстПериода + ClientSignature;
	
	Попытка
		
		НастройкаСохранения = Новый Соответствие;
		НастройкаСохранения.Вставить("МестоХранения", "Сервер");
		НастройкаСохранения.Вставить("Путь",          Неопределено);
		
		НастройкиПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.СтруктураПараметровПолученияФайла();
		ФайлКоординат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла("https://maps.googleapis.com/" + URLСтраницы + "",НастройкиПолучения,НастройкаСохранения);
		ИмяФайла = ФайлКоординат.Путь;
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,ФайлКоординат.Статус,ФайлКоординат);
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
		
		Текст = "";
		step = Ложь;
		polyline = Ложь;
		points = Новый Массив;
		
		Сч = 1;
		ТекущийИндекс = Неопределено;
		флгПерваяСтрока = Ложь;
		
		Пока ЧтениеXML.Прочитать() Цикл
			
			Если step И ЧтениеXML.Имя = "distance" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда				
				Пока ЧтениеXML.Прочитать() Цикл				
					Если ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Если ЧтениеXML.Прочитать() Тогда
							ДлинаМаршрута = ДлинаМаршрута + ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение) / 1000; // в км.
							УспешноеПостроение = Истина;
						КонецЕсли;
					ИначеЕсли ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
						Прервать;
					КонецЕсли;				
				КонецЦикла;
			ИначеЕсли step И ЧтениеXML.Имя = "duration" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда				
				Пока ЧтениеXML.Прочитать() Цикл				
					Если ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Если ЧтениеXML.Прочитать() Тогда
							ВремяМаршрута = ВремяМаршрута + ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение); // в сек.
							УспешноеПостроение = Истина;
							Если ЗначениеЗаполнено(ТекущийИндекс)Тогда
								ТекущаяСтрока = тзВремяМеждуТочками[ТекущийИндекс];
								ТекущаяСтрока.Время = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
								ТекущаяДатаОбработки = ТекущаяДатаОбработки + ТекущаяСтрока.Время;
								ТекущаяСтрока.Период = ТекущаяДатаОбработки;
							КонецЕсли;
						КонецЕсли;
					ИначеЕсли ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
						Прервать;
					КонецЕсли;				
				КонецЦикла;
			ИначеЕсли step И ЧтениеXML.Имя = "start_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				
				Если НЕ флгПерваяСтрока Тогда
					
					НоваяСтрока = тзВремяМеждуТочками.Добавить();
					НоваяСтрока.Время = 0;
					НоваяСтрока.Период = ТекущаяДатаОбработки;
					НоваяСтрока.Номер = Сч;
					Сч = Сч + 1;
					ТекущийИндекс = Неопределено;
					Пока ЧтениеXML.Прочитать() Цикл				
						Если ЧтениеXML.Имя = "lat" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
							Если ЧтениеXML.Прочитать() Тогда
								НоваяСтрока.Широта = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
							КонецЕсли;
						ИначеЕсли ЧтениеXML.Имя = "lng" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
							Если ЧтениеXML.Прочитать() Тогда
								НоваяСтрока.Долгота = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
							КонецЕсли;	
						ИначеЕсли ЧтениеXML.Имя = "start_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
							Прервать;
						КонецЕсли;				
					КонецЦикла;
					
					флгПерваяСтрока = Истина;
				КонецЕсли;
			ИначеЕсли step И ЧтениеXML.Имя = "end_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				НоваяСтрока = тзВремяМеждуТочками.Добавить(); 
				НоваяСтрока.Время = 0;
				НоваяСтрока.Номер = Сч;
				НоваяСтрока.Период = ТекущаяДатаОбработки;
				Сч = Сч + 1;
				ТекущийИндекс = тзВремяМеждуТочками.Индекс(НоваяСтрока);
				Пока ЧтениеXML.Прочитать() Цикл				
					Если ЧтениеXML.Имя = "lat" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Если ЧтениеXML.Прочитать() Тогда
							НоваяСтрока.Широта = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
						КонецЕсли;
					ИначеЕсли ЧтениеXML.Имя = "lng" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Если ЧтениеXML.Прочитать() Тогда
							НоваяСтрока.Долгота = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
						КонецЕсли;	
					ИначеЕсли ЧтениеXML.Имя = "end_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
						Прервать;
					КонецЕсли;				
				КонецЦикла;	
			ИначеЕсли ЧтениеXML.Имя = "step" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				step = Истина;
			ИначеЕсли ЧтениеXML.Имя = "step" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда					
				step = Ложь;
				Продолжить;
			ИначеЕсли ЧтениеXML.Имя = "polyline" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Пока ЧтениеXML.Прочитать() Цикл				
					Если ЧтениеXML.Имя = "points" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Если ЧтениеXML.Прочитать() Тогда
							points.Добавить(ЧтениеXML.Значение);
						КонецЕсли;
					ИначеЕсли ЧтениеXML.Имя = "points" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
						Прервать;
					КонецЕсли;				
				КонецЦикла;
			ИначеЕсли ЧтениеXML.Имя = "DirectionsResponse" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				Прервать;				
			КонецЕсли;					
			
		КонецЦикла;
		
		ЧтениеXML.Закрыть();
		
		УдалитьФайлы(ИмяФайла);
		
		Если НЕ УспешноеПостроение И ДлинаМаршрута = 0 И ВремяМаршрута = 0 Тогда 
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Google: Не удалось рассчитать расстояние между ""%1"" и ""%2""'"), СтрКоординаты1, СтрКоординаты2);
		Иначе			
			Если points.Количество() И флгОбработка Тогда
				
				МассивСоединить = Новый Массив;
				Для каждого мсвСтрока Из points Цикл						
					ДлинаСтроки = СтрДлина(мсвСтрока);
					МассивСоединить1 = Новый Массив;
					Для i = 1 По ДлинаСтроки Цикл						
						МассивСоединить1.Добавить(КодСимвола(Сред(мсвСтрока,i,1)));
					КонецЦикла;
					Если МассивСоединить1.Количество() Тогда
						МассивСоединить.Добавить(СтрСоединить(МассивСоединить1,","));
					КонецЕсли;
				КонецЦикла;
				ТекстКоординат = СтрСоединить(МассивСоединить,";");
				ТекстДанных = ПолучитьДанныеGoogle(сткКарта, ТекстКоординат);
				
				ТекстМаршрута = ?(ТекстДанных = Неопределено,"", ТекстДанных);
				
				Если НЕ ПустаяСтрока(ТекстМаршрута)Тогда
					Текст = ТекстМаршрута;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		СообщениеОбОшибке = URLСтраницы + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Результат = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");	
	
	Если ПустаяСтрока(СообщениеОбОшибке) Тогда
		
		//Если тзВремяМеждуТочками.Количество() < 1000 И ПолучитьФункциональнуюОпцию("упУчитыватьКоэффициентыРасчетаВремениВПути") Тогда
		//	Структура = Справочники.упГеографическиеЗоны.ВернутьВремяВПутиПоДнямНедели(тзВремяМеждуТочками);
		//Иначе
			Структура = Новый Структура;
			ВремяМеждуАдресами = тзВремяМеждуТочками.Итог("Время");
			Структура.Вставить("Время"           , ВремяМеждуАдресами);
			Структура.Вставить("ВремяПонедельник", ВремяМеждуАдресами);
			Структура.Вставить("ВремяВторник"    , ВремяМеждуАдресами);
			Структура.Вставить("ВремяСреда"      , ВремяМеждуАдресами);
			Структура.Вставить("ВремяЧетверг"    , ВремяМеждуАдресами);
			Структура.Вставить("ВремяПятница"    , ВремяМеждуАдресами);
			Структура.Вставить("ВремяСуббота"    , ВремяМеждуАдресами);
			Структура.Вставить("ВремяВоскресенье", ВремяМеждуАдресами);
		//КонецЕсли;
		Структура.Вставить("Путь",       Текст);
		Структура.Вставить("Расстояние", ДлинаМаршрута);
		Структура.Время = ВремяМаршрута;
		Результат = Структура;
		
	Иначе
		Результат.Вставить("Ошибка", СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации("Расчет расстояний Google", УровеньЖурналаРегистрации.Ошибка,,, СообщениеОбОшибке);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьРасстояниеМеждуТочкамиGoogle()

// Рассчитывает расстояние между точками при помощи ЭГК Yandex.
//
// Параметры:
//	сткКарта  - структура ключи парамтры загрузки карты - Координата широты начало маршрута.
//	НачальнаяШирота  - Число - Координата широты начало маршрута.
//	НачальнаяДолгота - Число - Координата долготы начало маршрута.
//	КонечнаяШирота   - Число - Координата широты окончания маршрута.
//	КонечнаяДолгота  - Число - Координата долготы окончания маршрута.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиYandex(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота)
	
	сткКарта.Вставить("НачальнаяШирота",  НачальнаяШирота);
	сткКарта.Вставить("НачальнаяДолгота", НачальнаяДолгота);
	сткКарта.Вставить("КонечнаяШирота",   КонечнаяШирота);
	сткКарта.Вставить("КонечнаяДолгота",  КонечнаяДолгота);
	
	Массив = Новый Массив();
	Массив.Добавить(Новый Структура("Широта,Долгота",ПредставлениеКоординаты(НачальнаяШирота),ПредставлениеКоординаты(НачальнаяДолгота)));
	Массив.Добавить(Новый Структура("Широта,Долгота",ПредставлениеКоординаты(КонечнаяШирота),ПредставлениеКоординаты(КонечнаяДолгота)));
	
	Структура = ПолучитьДанныеYandex(сткКарта, Массив);
	
	Возврат Структура;
	
КонецФункции // ПолучитьРасстояниеМеждуТочкамиYandex()

// Рассчитывает расстояние между точками при помощи  Яндекс.Маршрутизация: Матрица расстояний.
//
// Параметры:
//	сткКарта  - структура ключи парамтры загрузки карты - Координата широты начало маршрута.
//	НачальнаяШирота  - Число - Координата широты начало маршрута.
//	НачальнаяДолгота - Число - Координата долготы начало маршрута.
//	КонечнаяШирота   - Число - Координата широты окончания маршрута.
//	КонечнаяДолгота  - Число - Координата долготы окончания маршрута.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизацияМатрицаРасстояний(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота)
	
	сткКарта.Вставить("НачальнаяШирота",  НачальнаяШирота);
	сткКарта.Вставить("НачальнаяДолгота", НачальнаяДолгота);
	сткКарта.Вставить("КонечнаяШирота",   КонечнаяШирота);
	сткКарта.Вставить("КонечнаяДолгота",  КонечнаяДолгота);
	
	Массив = Новый Массив();
	Массив.Добавить(Новый Структура("Широта,Долгота",ПредставлениеКоординаты(НачальнаяШирота),ПредставлениеКоординаты(НачальнаяДолгота)));
	Массив.Добавить(Новый Структура("Широта,Долгота",ПредставлениеКоординаты(КонечнаяШирота),ПредставлениеКоординаты(КонечнаяДолгота)));
	
	Структура = ПолучитьДанныеЯндексМаршрутизация(сткКарта, Массив);
	
	Возврат Структура;
	
КонецФункции // ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизацияМатрицаРасстояний()

// Рассчитывает расстояние между точками при помощи СитиГИД.
//
// Параметры:
//	сткКарта  - структура ключи парамтры загрузки карты - Координата широты начало маршрута.
//	НачальнаяШирота  - Число - Координата широты начало маршрута.
//	НачальнаяДолгота - Число - Координата долготы начало маршрута.
//	КонечнаяШирота   - Число - Координата широты окончания маршрута.
//	КонечнаяДолгота  - Число - Координата долготы окончания маршрута.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиСитиГИД(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота, ДатаРасчета = Неопределено)
	
	сткКарта.Вставить("НачальнаяШирота",  НачальнаяШирота);
	сткКарта.Вставить("НачальнаяДолгота", НачальнаяДолгота);
	сткКарта.Вставить("КонечнаяШирота",   КонечнаяШирота);
	сткКарта.Вставить("КонечнаяДолгота",  КонечнаяДолгота);
	
	Массив = Новый Массив();
	Массив.Добавить(Новый Структура("Широта,Долгота",ПредставлениеКоординаты(НачальнаяШирота),ПредставлениеКоординаты(НачальнаяДолгота)));
	Массив.Добавить(Новый Структура("Широта,Долгота",ПредставлениеКоординаты(КонечнаяШирота),ПредставлениеКоординаты(КонечнаяДолгота)));
	
	Структура = ПолучитьДанныеСитиГИД(сткКарта, Массив,, ДатаРасчета);
	
	Возврат Структура;
	
КонецФункции // ПолучитьРасстояниеМеждуТочкамиСитиГИД()

// Получаем маршрут из указанных координат начала и окончания.
//
// Параметры:
//	сткКарта  - структура -  ключи парамтры загрузки карты, Координата широты начало маршрута.
//	КоординатыНачала    - структура -  с ключами Долгота и Широта, содержит географические координаны точки начала.
//	КоординатыОкончания - структура -  с ключами Долгота и Широта, содержит географические координаны точки окончания.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиGraphhopper(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота) Экспорт
	
	упАдресСервераМаршрутизации = сткКарта.Соединение;	
	сткРезультат = Неопределено;
	
	Если НЕ ПустаяСтрока(упАдресСервераМаршрутизации) Тогда		
		
		СтрКоординаты1 = ПредставлениеКоординаты(НачальнаяШирота) + "," + ПредставлениеКоординаты(НачальнаяДолгота);
		СтрКоординаты2 = ПредставлениеКоординаты(КонечнаяШирота) + "," + ПредставлениеКоординаты(КонечнаяДолгота);
		
		сткКарта.Вставить("СтрКоординаты1", СтрКоординаты1);
		сткКарта.Вставить("СтрКоординаты2", СтрКоординаты2);
		
		URLСтрока = упАдресСервераМаршрутизации + "route?point=" + СтрКоординаты1 + "&point=" + СтрКоординаты2 + "&type=gpx&locale=ru-RU";
		
		сткКарта.Вставить("НачальнаяШирота",  НачальнаяШирота);
		сткКарта.Вставить("НачальнаяДолгота", НачальнаяДолгота);
		сткКарта.Вставить("КонечнаяШирота",   КонечнаяШирота);
		сткКарта.Вставить("КонечнаяДолгота",  КонечнаяДолгота);
		
		сткРезультат = ПолучитьДанныеСервисаGraphhopper(сткКарта, URLСтрока);
		
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции // ПолучитьРасстояниеМеждуТочкамиGraphhopper()

// Преобразует значение координаты в строку.
//
// Параметры:
//	Координата - Число - Координата.
//
// Возвращаемое значение:
//	Строка - представление координаты в формате сервиса.
//
Функция ПредставлениеКоординаты(Координата)
	
	Возврат Формат(Координата, "ЧЦ=9; ЧДЦ=6; ЧРД=.; ЧН=0; ЧГ=");
	
КонецФункции

// Безопастно преобразует строковое представление кооридинаты в число.
//
// Параметры:
//	ПредставлениеКоординаты - Строка - Текст координаты.
//
// Возвращаемое значение:
//	Число - значение координаты.
//
Функция ПредставлениеКоординатыВЧисло(ПредставлениеКоординаты)
	
	текОписаниеТипов = Новый ОписаниеТипов("Число");
	Возврат текОписаниеТипов.ПривестиЗначение(ПредставлениеКоординаты);
	
КонецФункции

// Рассчитывает расстояние между точками при помощи Google.
//
// Параметры:
//	АдресНачало - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	АдресОкончание - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь	   - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	   - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиПоКартеGoogle(АдресНачало, АдресОкончание, ПараметрыКарты = Неопределено) Экспорт
	
	текОшибка = Ложь;
	
	сткВозврат = ВыполнитьПроверкуТочек(АдресНачало, АдресОкончание, текОшибка);
	
	Если текОшибка = Ложь Тогда
		сткВозврат = ПолучитьРасстояниеМеждуТочкамиGoogle(ПараметрыКарты, АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота);
		Если ТипЗнч(сткВозврат) = ТИП("Структура")Тогда
			Если сткВозврат.Свойство("Ошибка") Тогда
				СообщениеОбОшибке = сткВозврат.Ошибка;
				ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота,,, сткВозврат.Ошибка);
			Иначе
				ЗаписатьРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, сткВозврат);
			КонецЕсли;
			сткВозврат.Вставить("Записать",ЛОЖЬ);
		КонецЕсли;
	ИначеЕсли текОшибка = Истина Тогда
		сткВозврат = Неопределено;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиПоКартеGoogle()

// Рассчитывает расстояние между точками при помощи Graphhopper.
//
// Параметры:
//	АдресНачало - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	АдресОкончание - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиПоКартеGraphhopper(АдресНачало, АдресОкончание, ПараметрыКарты = Неопределено) Экспорт
	
	текОшибка = Ложь;
	
	сткВозврат = ВыполнитьПроверкуТочек(АдресНачало, АдресОкончание, текОшибка);
	
	Если текОшибка = Ложь Тогда
		сткВозврат = ПолучитьРасстояниеМеждуТочкамиGraphhopper(ПараметрыКарты, АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота);
		Если сткВозврат <> Неопределено Тогда			
			Если сткВозврат.Свойство("Ошибка") Тогда
				ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота,,, сткВозврат.Ошибка);
			Иначе				
				ЗаписатьРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, сткВозврат);
			КонецЕсли;
			сткВозврат.Вставить("Записать", ЛОЖЬ);
		КонецЕсли;
	ИначеЕсли текОшибка = Истина Тогда
		сткВозврат = Неопределено;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиПоКартеGraphhopper()

// Рассчитывает расстояние между точками при помощи Yandex.
//
// Параметры:
//	АдресНачало - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	АдресОкончание - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь	   - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	   - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиПоКартеYandex(АдресНачало, АдресОкончание, ПараметрыКарты = Неопределено) Экспорт
	текОшибка = Ложь;
	
	Если Не ЗначениеЗаполнено(ПараметрыКарты.Ключ) Тогда		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний ЯндексAPI'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.РегламентныеЗадания.упРасчетРасстояний,, НСтр("ru = 'Не указаны дополнительные параметры для сервиса, API маршрутов Yandex'"));
		текОшибка = Истина;
	КонецЕсли;
	
	сткВозврат = ВыполнитьПроверкуТочек(АдресНачало, АдресОкончание, текОшибка);
	
	Если текОшибка = Ложь Тогда
		сткВозврат = ПолучитьРасстояниеМеждуТочкамиYandex(ПараметрыКарты, АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота);
		Если сткВозврат = Неопределено Тогда
			сткВозврат = Новый Структура("Расстояние, Время, Путь", Неопределено, Неопределено, Неопределено);
		Иначе
			Если ТипЗнч(сткВозврат)=ТИП("Структура") И сткВозврат.Свойство("Ошибка") Тогда
				ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота,,, сткВозврат.Ошибка);
			Иначе
				ЗаписатьРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, сткВозврат);
			КонецЕсли;
			сткВозврат.Вставить("Записать",ЛОЖЬ);
		КонецЕсли;
	ИначеЕсли текОшибка = Истина Тогда
		сткВозврат = Неопределено;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиПоКартеYandex()

// Рассчитывает расстояние между точками при помощи Яндекс.Маршрутизация: Матрица расстояний.
//
// Параметры:
//	АдресНачало - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	АдресОкончание - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь	   - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	   - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизация(АдресНачало, АдресОкончание, ПараметрыКарты = Неопределено) Экспорт
	текОшибка = Ложь;
	
	Если Не ЗначениеЗаполнено(ПараметрыКарты.Ключ) Тогда		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний Яндекс.Маршрутизация'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.РегламентныеЗадания.упРасчетРасстояний,, НСтр("ru = 'Не указаны дополнительные параметры для сервиса, Яндекс.Маршрутизация'"));
		текОшибка = Истина;
	КонецЕсли;
	
	сткВозврат = ВыполнитьПроверкуТочек(АдресНачало, АдресОкончание, текОшибка);
	
	Если текОшибка = Ложь Тогда
		сткВозврат = ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизацияМатрицаРасстояний(ПараметрыКарты, АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота);
		Если сткВозврат = Неопределено Тогда
			сткВозврат = Новый Структура("Расстояние, Время, Путь", Неопределено, Неопределено, Неопределено);
		Иначе
			Если ТипЗнч(сткВозврат)=ТИП("Структура") И сткВозврат.Свойство("Ошибка") Тогда
				ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота,,, сткВозврат.Ошибка);
			Иначе
				ЗаписатьРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, сткВозврат);
			КонецЕсли;
			сткВозврат.Вставить("Записать",ЛОЖЬ);
		КонецЕсли;
	ИначеЕсли текОшибка = Истина Тогда
		сткВозврат = Неопределено;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизация()

// Рассчитывает расстояние между точками при помощи СитиГИД.
//
// Параметры:
//	АдресНачало - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	АдресОкончание - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь	   - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	   - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИД(АдресНачало, АдресОкончание, ПараметрыКарты = Неопределено) Экспорт
	текОшибка = Ложь;
	
	Если Не ЗначениеЗаполнено(ПараметрыКарты.Ключ) Тогда		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний СитиГИД'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.РегламентныеЗадания.упРасчетРасстояний,, НСтр("ru = 'Не указаны дополнительные параметры для сервиса, API маршрутов СитиГИД'"));
		текОшибка = Истина;
	КонецЕсли;
	
	сткВозврат = ВыполнитьПроверкуТочек(АдресНачало, АдресОкончание, текОшибка);
	
	Если текОшибка = Ложь Тогда
		сткВозврат = ПолучитьРасстояниеМеждуТочкамиСитиГИД(ПараметрыКарты, АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота);
		Если сткВозврат = Неопределено Тогда
			сткВозврат = Новый Структура("Расстояние, Время, Путь", Неопределено, Неопределено, Неопределено);
		Иначе
			ТекущееОграничение = ПараметрыКарты.Ограничение;
			Если ТипЗнч(сткВозврат) = ТИП("Структура") И сткВозврат.Свойство("Ошибка") Тогда
				ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, ТекущееОграничение,
				сткВозврат.ПытатьсяРассчитатьСнова, сткВозврат.Ошибка);
			Иначе
				ЗаписатьРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, сткВозврат, ТекущееОграничение);
			КонецЕсли;
			сткВозврат.Вставить("Записать", Ложь);
		КонецЕсли;
	ИначеЕсли текОшибка = Истина Тогда
		сткВозврат = Неопределено;
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИД()

// Рассчитывает расстояние между точками при помощи СитиГИД пакетно.
//
// Параметры:
//	Пакет - Массив - элементами являются структуры описания расстояний;
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Массив - элементами являются Структура - Ключи:
//			Путь	   - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	   - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИДПакетно(Пакет, ПараметрыМаршрутизатора = Неопределено) Экспорт
	
	Маршруты = Новый Массив;
	Для Каждого ПараТочек Из Пакет Цикл
		НачальныйАдрес = ПараТочек.НачальныйАдрес;
		КонечныйАдрес = ПараТочек.КонечныйАдрес;
		ВходнойМаршрут = Новый Массив();
		ВходнойМаршрут.Добавить(Новый Структура("Широта,Долгота", ПредставлениеКоординаты(НачальныйАдрес.Широта), ПредставлениеКоординаты(НачальныйАдрес.Долгота)));
		ВходнойМаршрут.Добавить(Новый Структура("Широта,Долгота", ПредставлениеКоординаты(КонечныйАдрес.Широта), ПредставлениеКоординаты(КонечныйАдрес.Долгота)));
		Маршруты.Добавить(ВходнойМаршрут);
	КонецЦикла; 
	РезультатРассчета = ПолучитьДанныеСитиГИД(ПараметрыМаршрутизатора, Маршруты, Истина);
	Для ИндексЭлемента = 0 По Пакет.ВГраница() Цикл
		ПараТочек = Пакет[ИндексЭлемента];
		НачальныйАдрес = ПараТочек.НачальныйАдрес;
		КонечныйАдрес = ПараТочек.КонечныйАдрес;
		сткВозврат = РезультатРассчета.Маршруты[ИндексЭлемента];
		Если сткВозврат = Неопределено Тогда
			сткВозврат = Новый Структура("Расстояние, Время, Путь", Неопределено, Неопределено, Неопределено);
		Иначе
			ТекущееОграничение = ПараметрыМаршрутизатора.Ограничение;
			Если Истина
				И ТипЗнч(сткВозврат) = ТИП("Структура") 
				И (Ложь
					Или сткВозврат.Свойство("Ошибка")
					Или РезультатРассчета.Свойство("Ошибка"))
			Тогда
				Если сткВозврат.Свойство("Ошибка") Тогда
					Ошибка = сткВозврат.Ошибка;
				Иначе
					Ошибка = РезультатРассчета.Ошибка;
				КонецЕсли; 
				ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(НачальныйАдрес.Широта, НачальныйАдрес.Долгота, КонечныйАдрес.Широта, КонечныйАдрес.Долгота, ТекущееОграничение,
				сткВозврат.ПытатьсяРассчитатьСнова,  Ошибка);
			Иначе
				ЗаписатьРасстоянияМеждуТочками(НачальныйАдрес.Широта, НачальныйАдрес.Долгота, КонечныйАдрес.Широта, КонечныйАдрес.Долгота, сткВозврат, ТекущееОграничение);
			КонецЕсли;
			сткВозврат.Вставить("Записать", Ложь);
		КонецЕсли;
		Если сткВозврат = Неопределено Тогда
			ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(НачальныйАдрес.Широта, НачальныйАдрес.Долгота, КонечныйАдрес.Широта, КонечныйАдрес.Долгота, ТекущееОграничение);
		КонецЕсли;	
	КонецЦикла;
	Возврат сткВозврат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИД()

// Выполним провекру для координат точек.
//
// Параметры:
//	АдресНачало - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	АдресОкончание - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//  Ошибка - Булево - Флаг об ошибках.
//
// Возвращаемое значение:
//   Структура - Ключи:
//			Путь	   - Строка - lat;lon;lat;lon;lat;lon….
//   		Расстояние - Число - расстояние маршрута в км.
//   		Время 	   - Число - длительность маршрут в сек.
//
Функция ВыполнитьПроверкуТочек(АдресНачало, АдресОкончание, Ошибка)
	
	Результат = Неопределено;
	
	Если НЕ Ошибка Тогда
		Если АдресНачало.Долгота = 0 И АдресНачало.Широта = 0 Тогда 
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не указаны координаты адреса ""%1""'"), АдресНачало));
			Ошибка = Истина;
		КонецЕсли;	
		
		Если АдресОкончание.Долгота = 0 И АдресОкончание.Широта = 0 Тогда 
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не указаны координаты адреса ""%1""'"), АдресОкончание));
			Ошибка = Истина;
		КонецЕсли;	
		
		Если АдресНачало.Долгота = АдресОкончание.Долгота И АдресНачало.Широта = АдресОкончание.Широта Тогда 
			Ошибка = Неопределено;
			Результат = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");	
			ЗаписатьРасстоянияМеждуТочками(АдресНачало.Широта, АдресНачало.Долгота, АдресОкончание.Широта, АдресОкончание.Долгота, Результат);
			
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции // ВыполнитьПроверкуТочек()

// Рассчитывает расстояние между точками при помощи Graphhopper.
//
// Параметры:
//	ТочкиРасчета - Массив - структур с ключами Долгота и Широта - содержит географические координаны точки начала,
//		также можно передать массив элементов СправочникСсылка.упАдреса.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Массив - Массив структур с ключами Расстояние, Время, Путь.
//
Функция ПолучитьРасстояниеМеждуТочкамиМассивGraphhopper(ТочкиРасчета, ПараметрыКарты = Неопределено) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Если ТочкиРасчета.Количество() Тогда
		мсвТочки = ПолучитьПоследовательностьТочек(ТочкиРасчета);
		Для каждого мсвЭлемент Из мсвТочки Цикл
			НачальныйАдрес = мсвЭлемент.Точка1;
			КонечныйАдрес  = мсвЭлемент.Точка2;
			Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеGraphhopper(НачальныйАдрес,КонечныйАдрес,ПараметрыКарты);
			Если Структура = Неопределено Тогда
				Структура = Новый Структура("Расстояние, Время, Путь", Неопределено, Неопределено, Неопределено);
			ИначеЕсли ТипЗнч(Структура) = ТИП("Структура") И Структура.Свойство("Ошибка") Тогда
				Структура.Вставить("Расстояние", Неопределено);
				Структура.Вставить("Время",      Неопределено);
				Структура.Вставить("Путь",       Неопределено);
			КонецЕсли;
			Структура.Вставить("НачальнаяШирота",  НачальныйАдрес.Широта);
			Структура.Вставить("НачальнаяДолгота", НачальныйАдрес.Долгота);
			Структура.Вставить("КонечнаяШирота",   КонечныйАдрес.Широта);
			Структура.Вставить("КонечнаяДолгота",  КонечныйАдрес.Долгота);
			мсвРезультат.Добавить(Структура);			
		КонецЦикла;
	КонецЕсли;
	
	Если мсвРезультат.Количество() = 0 Тогда
		мсвРезультат = Неопределено;
	Конецесли;
	
	Возврат мсвРезультат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиМассивGraphhopper()

// Рассчитывает расстояние между точками при помощи Google.
//
// Параметры:
//	ТочкиРасчета - Массив - структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//						ТочкиМаршрута  - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Массив   - Массив структур с ключами Расстояние, Время, Путь.
//
Функция ПолучитьРасстояниеМеждуТочкамиМассивGoogleMaps(ТочкиРасчета, ПараметрыКарты = Неопределено) Экспорт
	
	// На использование службы API маршрутов Google накладывается ограничение, составляющее 2500 запросов маршрутов в день.
	ClientSignature = ПараметрыКарты.Ключ; // &client=gme-YOUR_CLIENT_ID&signature=YOUR_URL_SIGNATURE.
	упКоличествоОбрабатываемыхТочекМаршрутизатора = ПараметрыКарты.упКоличествоОбрабатываемыхТочекМаршрутизатора;
	Если ПустаяСтрока(ClientSignature) Тогда
		// Отдельные запросы автомобильных, велосипедных или пешеходных.
		// Маршрутов могут содержать до 8 промежуточных путевых точек.
		Если упКоличествоОбрабатываемыхТочекМаршрутизатора > 8 Тогда
			упКоличествоОбрабатываемыхТочекМаршрутизатора = 8;
		КонецЕсли;
	Иначе
		// Пользователи API Google Карт для организаций в течение дня могут запрашивать.
		// до 100 000 маршрутов, для каждого из которых допускается до 23 путевых точек.
		Если упКоличествоОбрабатываемыхТочекМаршрутизатора > 23 Тогда
			упКоличествоОбрабатываемыхТочекМаршрутизатора = 23;
		КонецЕсли;
	КонецЕсли;	
	// Разобъем массив на блоки и будем обрабатывать каждый отдельно.
	мсвБлоки = ПолучитьТочкиМаршрутаПорциями(ТочкиРасчета, упКоличествоОбрабатываемыхТочекМаршрутизатора);
	
	мсвРезультат = Новый Массив;
	мсвСообщенияОбОшибках = Новый Массив;
	
	Сч = 1;
	
	тзВремяМеждуТочками = ПолучитьКаркасТаблицыВремениМеждуТочками();
	
	ТекущаяДатаОбработки = ТекущаяДатаСеанса();
	
	ТекстПериода = "&departure_time=" + Формат((ТекущаяДатаСеанса() + 43200) - Дата("19700101000000"),"ЧГ=0") + "&traffic_model=pessimistic"; // прогноз пробок работает только на будущее время
	
	Для каждого мсвБлок Из мсвБлоки Цикл		
		ИндексНаибольший = мсвБлок.ВГраница(); // определяем размер массив.
		СтрКоординаты1 = ПредставлениеКоординаты(мсвБлок[0].Широта) + "," + ПредставлениеКоординаты(мсвБлок[0].Долгота);
		СтрКоординаты2 = ПредставлениеКоординаты(мсвБлок[ИндексНаибольший].Широта) + "," + ПредставлениеКоординаты(мсвБлок[ИндексНаибольший].Долгота);
		МассивСоединить = Новый Массив;
		МассивСоединить.Добавить(СтрШаблон("origin=%1&destination=%2%3",СтрКоординаты1,СтрКоординаты2,?(мсвБлок.Количество() <=2,"","&waypoints=")));
		Для i = 1 По ИндексНаибольший - 1 Цикл
			МассивСоединить.Добавить(СтрШаблон("%1,%2",ПредставлениеКоординаты(мсвБлок[i].Широта),ПредставлениеКоординаты(мсвБлок[i].Долгота)));
		КонецЦикла;
		стрТочки = СтрСоединить(МассивСоединить,"|");
		URLСтраницы = СтрШаблон("maps/api/directions/xml?%1&sensor=false&mode=driving&units=metric&language=ru-RU%2%3",стрТочки,ТекстПериода,ClientSignature);
		
		УспешноеПостроение = Ложь;
		
		ДлинаМаршрута = 0;
		ВремяМаршрута = 0;
		ИмяФайла = ПараметрыКарты.ИмяФайла;
		НомерПарыТочек = 0;
		
		Попытка
			
			НастройкаСохранения = Новый Соответствие;
			НастройкаСохранения.Вставить("МестоХранения", "Сервер");
			НастройкаСохранения.Вставить("Путь",          Неопределено);
			
			НастройкиПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.СтруктураПараметровПолученияФайла();
			ФайлКоординат = ПолучениеФайловИзИнтернетаКлиентСервер.ПодготовитьПолучениеФайла("https://maps.googleapis.com/" + URLСтраницы + "",НастройкиПолучения,НастройкаСохранения);
			
			ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ПараметрыКарты.ТипМаршрутизатора,ФайлКоординат.Статус,ФайлКоординат);
			
			Если НЕ ФайлКоординат.Статус Тогда				
				ВызватьИсключение "Ошибка, обращения к сервису Google, данные не получены";
			Иначе
				
				ИмяФайла = ФайлКоординат.Путь;
				
				ЧтениеXML = Новый ЧтениеXML;
				ЧтениеXML.ОткрытьФайл(ИмяФайла);
				
				Текст = "";
				step = Ложь;
				polyline = Ложь;
				points = Новый Массив;
				Сч = 1;
				ТекущийИндекс = Неопределено;
				флгПерваяСтрока = Ложь;
				Пока ЧтениеXML.Прочитать() Цикл
					
					Если step И ЧтениеXML.Имя = "distance" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда				
						Пока ЧтениеXML.Прочитать() Цикл				
							Если ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
								Если ЧтениеXML.Прочитать() Тогда
									ДлинаМаршрута = ДлинаМаршрута + ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение) / 1000; // в км.
									УспешноеПостроение = Истина;
								КонецЕсли;
							ИначеЕсли ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
								Прервать;
							КонецЕсли;
						КонецЦикла;
					ИначеЕсли step И ЧтениеXML.Имя = "duration" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда				
						Пока ЧтениеXML.Прочитать() Цикл				
							Если ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
								Если ЧтениеXML.Прочитать() Тогда
									ВремяМаршрута = ВремяМаршрута + ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение); // в сек.
									УспешноеПостроение = Истина;
									Если ЗначениеЗаполнено(ТекущийИндекс)Тогда
										ТекущаяСтрока = тзВремяМеждуТочками[ТекущийИндекс];
										ТекущаяСтрока.Время  = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
										ТекущаяДатаОбработки = ТекущаяДатаОбработки + ТекущаяСтрока.Время;
										ТекущаяСтрока.Период = ТекущаяДатаОбработки;
									КонецЕсли;
								КонецЕсли;
							ИначеЕсли ЧтениеXML.Имя = "value" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
								Прервать;
							КонецЕсли;				
						КонецЦикла;
					ИначеЕсли step И ЧтениеXML.Имя = "start_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						
						Если НЕ флгПерваяСтрока Тогда
							
							НоваяСтрока = тзВремяМеждуТочками.Добавить();
							НоваяСтрока.Время  = 0;
							НоваяСтрока.Период = ТекущаяДатаОбработки;
							НоваяСтрока.Номер  = Сч;
							Сч = Сч + 1;
							ТекущийИндекс = Неопределено;
							Пока ЧтениеXML.Прочитать() Цикл				
								Если ЧтениеXML.Имя = "lat" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
									Если ЧтениеXML.Прочитать() Тогда
										НоваяСтрока.Широта = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
									КонецЕсли;
								ИначеЕсли ЧтениеXML.Имя = "lng" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
									Если ЧтениеXML.Прочитать() Тогда
										НоваяСтрока.Долгота = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
									КонецЕсли;	
								ИначеЕсли ЧтениеXML.Имя = "start_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
									Прервать;
								КонецЕсли;				
							КонецЦикла;
							
							флгПерваяСтрока = Истина;
						КонецЕсли;
					ИначеЕсли step И ЧтениеXML.Имя = "end_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						НоваяСтрока = тзВремяМеждуТочками.Добавить(); 
						НоваяСтрока.Время  = 0;
						НоваяСтрока.Период = ТекущаяДатаОбработки;
						НоваяСтрока.Номер  = Сч;
						Сч = Сч + 1;
						ТекущийИндекс = тзВремяМеждуТочками.Индекс(НоваяСтрока);
						Пока ЧтениеXML.Прочитать() Цикл				
							Если ЧтениеXML.Имя = "lat" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
								Если ЧтениеXML.Прочитать() Тогда
									НоваяСтрока.Широта = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
								КонецЕсли;
							ИначеЕсли ЧтениеXML.Имя = "lng" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
								Если ЧтениеXML.Прочитать() Тогда
									НоваяСтрока.Долгота = ПредставлениеКоординатыВЧисло(ЧтениеXML.Значение);
								КонецЕсли;	
							ИначеЕсли ЧтениеXML.Имя = "end_location" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
								Прервать;
							КонецЕсли;				
						КонецЦикла;	
					ИначеЕсли ЧтениеXML.Имя = "step" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						step = Истина;
					ИначеЕсли ЧтениеXML.Имя = "step" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
						step = Ложь;
						Продолжить;
					ИначеЕсли ЧтениеXML.Имя = "polyline" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						Пока ЧтениеXML.Прочитать() Цикл				
							Если ЧтениеXML.Имя = "points" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
								Если ЧтениеXML.Прочитать() Тогда
									points.Добавить(ЧтениеXML.Значение);
								КонецЕсли;
							ИначеЕсли ЧтениеXML.Имя = "points" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда	
								Прервать;
							КонецЕсли;				
						КонецЦикла;
					ИначеЕсли ЧтениеXML.Имя = "leg" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
						НомерПарыТочек = НомерПарыТочек + 1;
					ИначеЕсли ЧтениеXML.Имя = "leg" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
						
						//Если тзВремяМеждуТочками.Количество() < 1000 И ПолучитьФункциональнуюОпцию("упУчитыватьКоэффициентыРасчетаВремениВПути") Тогда
						//	Структура = Справочники.упГеографическиеЗоны.ВернутьВремяВПутиПоДнямНедели(тзВремяМеждуТочками);
						//Иначе
							Структура = Новый Структура;
							ВремяМеждуАдресами = тзВремяМеждуТочками.Итог("Время");
							Структура.Вставить("Время"           , ВремяМеждуАдресами);
							Структура.Вставить("ВремяПонедельник", ВремяМеждуАдресами);
							Структура.Вставить("ВремяВторник"    , ВремяМеждуАдресами);
							Структура.Вставить("ВремяСреда"      , ВремяМеждуАдресами);
							Структура.Вставить("ВремяЧетверг"    , ВремяМеждуАдресами);
							Структура.Вставить("ВремяПятница"    , ВремяМеждуАдресами);
							Структура.Вставить("ВремяСуббота"    , ВремяМеждуАдресами);
							Структура.Вставить("ВремяВоскресенье", ВремяМеждуАдресами);
						//КонецЕсли;
						Структура.Вставить("Путь",       Текст);
						Структура.Вставить("Расстояние", ДлинаМаршрута);
						Структура.Время = ВремяМаршрута;
						Структура.Вставить("НачальнаяШирота",  мсвБлок[НомерПарыТочек - 1].Широта);
						Структура.Вставить("НачальнаяДолгота", мсвБлок[НомерПарыТочек - 1].Долгота);
						Структура.Вставить("КонечнаяШирота",   мсвБлок[НомерПарыТочек].Широта);
						Структура.Вставить("КонечнаяДолгота",  мсвБлок[НомерПарыТочек].Долгота);
						Структура.Вставить("Расшифровка", points);
						Структура.Вставить("Записать", ИСТИНА);
						мсвРезультат.Добавить(Структура);
						ДлинаМаршрута = 0;
						ВремяМаршрута = 0;
						Текст = "";
						points = Новый Массив;
						тзВремяМеждуТочками.Очистить();
						Сч = 1;
						ТекущийИндекс = Неопределено;
						флгПерваяСтрока = Ложь;
					ИначеЕсли ЧтениеXML.Имя = "DirectionsResponse" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
						Прервать;				
					КонецЕсли;					
					
				КонецЦикла;
				
				ЧтениеXML.Закрыть();
				
			КонецЕсли;
			
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			мсвСообщенияОбОшибках.Добавить(URLСтраницы + Символы.ПС + ОписаниеОшибки);
			ЗаписатьПопыткуРасчетаРасстоянияМеждуТочками(мсвБлок[0].Широта, мсвБлок[0].Долгота, мсвБлок[ИндексНаибольший].Широта, мсвБлок[ИндексНаибольший].Долгота,,, ОписаниеОшибки);
		КонецПопытки;		
		
	КонецЦикла;
	
	УдалитьФайлы(ИмяФайла);
	
	Если мсвСообщенияОбОшибках.Количество() Тогда
		текИндекс = 1;
		СообщениеОбОшибке = "";
		Для каждого текОшибка Из мсвСообщенияОбОшибках Цикл
			СообщениеОбОшибке = СообщениеОбОшибке + ?(ПустаяСтрока(СообщениеОбОшибке),"",Символы.ПС) + Строка(текИндекс) + ". " + текОшибка;
			текИндекс = текИндекс + 1;
		КонецЦикла;
		ЗаписьЖурналаРегистрации("Расчет расстояний GoogleMaps", УровеньЖурналаРегистрации.Ошибка,,, СообщениеОбОшибке);
	Конецесли;
	
	Если мсвРезультат.Количество() = 0 Тогда
		мсвРезультат = Неопределено;
	Конецесли;
	
	Возврат мсвРезультат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиМассивGoogleMaps()

// Рассчитывает расстояние между точками при помощи Yandex.
//
// Параметры:
//	ТочкиРасчета - Массив - структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//					ТочкиМаршрута  - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Массив - Массив структур с ключами Расстояние, Время, Путь.
//
Функция ПолучитьРасстояниеМеждуТочкамиМассивYandex(ТочкиРасчета, ПараметрыКарты = Неопределено) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Если ТочкиРасчета.Количество() Тогда
		мсвТочки = ПолучитьПоследовательностьТочек(ТочкиРасчета);
		
		Если мсвТочки.Количество() Тогда
			колОбращений = мсвТочки.Количество();
			Сч = 1;
			Для каждого мсвЭлемент Из мсвТочки Цикл
				НачальныйАдрес = мсвЭлемент.Точка1;
				КонечныйАдрес  = мсвЭлемент.Точка2;
				Если колОбращений = Сч Тогда
					ПараметрыКарты.Вставить("УничтожитьCOMОбъект");
				КонецЕсли; 
				
				Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеYandex(НачальныйАдрес,КонечныйАдрес,ПараметрыКарты);
				Если Структура = Неопределено Тогда
					Структура = Новый Структура("Расстояние,Время,Путь",Неопределено,Неопределено,Неопределено);
				КонецЕсли;
				Структура.Вставить("НачальнаяШирота",  НачальныйАдрес.Широта);
				Структура.Вставить("НачальнаяДолгота", НачальныйАдрес.Долгота);
				Структура.Вставить("КонечнаяШирота",   КонечныйАдрес.Широта);
				Структура.Вставить("КонечнаяДолгота",  КонечныйАдрес.Долгота);
				мсвРезультат.Добавить(Структура);			
				
				Сч = Сч + 1;
			КонецЦикла;
		КонецЕсли; 
		
	КонецЕсли;
	
	Если мсвРезультат.Количество() = 0 Тогда
		мсвРезультат = Неопределено;
	Конецесли;
	
	Возврат мсвРезультат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиМассивYandex()

// Рассчитывает расстояние между точками при помощи  Яндекс.Маршрутизация: Матрица расстояний.
//
// Параметры:
//	ТочкиРасчета - Массив - структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//					ТочкиМаршрута  - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Массив - Массив структур с ключами Расстояние, Время, Путь.
//
Функция ПолучитьРасстояниеМеждуТочкамиМассивЯндексМаршрутизация(ТочкиРасчета, ПараметрыКарты = Неопределено) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Если ТочкиРасчета.Количество() Тогда
		мсвТочки = ПолучитьПоследовательностьТочек(ТочкиРасчета);
		
		Если мсвТочки.Количество() Тогда
			колОбращений = мсвТочки.Количество();
			Сч = 1;
			Для каждого мсвЭлемент Из мсвТочки Цикл
				НачальныйАдрес = мсвЭлемент.Точка1;
				КонечныйАдрес  = мсвЭлемент.Точка2;
				//Если колОбращений = Сч Тогда
				//	ПараметрыКарты.Вставить("УничтожитьCOMОбъект");
				//КонецЕсли; 
				
				Структура = ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизация(НачальныйАдрес,КонечныйАдрес,ПараметрыКарты);
				Если Структура = Неопределено Тогда
					Структура = Новый Структура("Расстояние,Время,Путь",Неопределено,Неопределено,Неопределено);
				КонецЕсли;
				Структура.Вставить("НачальнаяШирота",  НачальныйАдрес.Широта);
				Структура.Вставить("НачальнаяДолгота", НачальныйАдрес.Долгота);
				Структура.Вставить("КонечнаяШирота",   КонечныйАдрес.Широта);
				Структура.Вставить("КонечнаяДолгота",  КонечныйАдрес.Долгота);
				мсвРезультат.Добавить(Структура);			
				
				Сч = Сч + 1;
			КонецЦикла;
		КонецЕсли; 
		
	КонецЕсли;
	
	Если мсвРезультат.Количество() = 0 Тогда
		мсвРезультат = Неопределено;
	Конецесли;
	
	Возврат мсвРезультат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиМассивЯндексМаршрутизация()

// Рассчитывает расстояние между точками при помощи СитиГИД.
//
// Параметры:
//	ТочкиРасчета - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//					ТочкиМаршрута  - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//  ПараметрыКарты - Структура - ключи параметры карты.
//
// Возвращаемое значение:
//   Массив   - Массив структур с ключами Расстояние, Время, Путь.
//
Функция ПолучитьРасстояниеМеждуТочкамиМассивСитиГИД(ТочкиРасчета, ПараметрыКарты = Неопределено) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Если ТочкиРасчета.Количество() Тогда
		мсвТочки = ПолучитьПоследовательностьТочек(ТочкиРасчета);
		Для каждого мсвЭлемент Из мсвТочки Цикл
			НачальныйАдрес = мсвЭлемент.Точка1;
			КонечныйАдрес  = мсвЭлемент.Точка2;
			ДопОграниченияСитиГИД = Ложь;
			Если мсвЭлемент.Точка1.Свойство("ДопОграниченияСитиГИД") И мсвЭлемент.Точка2.Свойство("ДопОграниченияСитиГИД") Тогда
				ДопОграниченияСитиГИД = мсвЭлемент.Точка1.ДопОграниченияСитиГИД И мсвЭлемент.Точка2.ДопОграниченияСитиГИД;
			КонецЕсли;
			
			СписокОграниченийСитиГИД = Неопределено;
			
			Если ПараметрыКарты.Свойство("СписокОграниченийСитиГИД") Тогда
				СписокОграниченийСитиГИД = ПараметрыКарты.СписокОграниченийСитиГИД;
			КонецЕсли;
			
			ПараметрыКарты.Вставить("ДопОграниченияСитиГИД",ДопОграниченияСитиГИД);
			ПараметрыКарты.Вставить("Ограничение",ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка"));
			
			Если ДопОграниченияСитиГИД И СписокОграниченийСитиГИД <> Неопределено Тогда
				Для Каждого ТекущийЭлемент Из СписокОграниченийСитиГИД Цикл
					ПараметрыКарты.Параметры.parameters.Вставить("limitsName", ТекущийЭлемент.Представление);
					ПараметрыКарты.ПараметрыМаршрутизацииСитиГИД.Вставить("limitsName",ТекущийЭлемент.Представление);
					ПараметрыКарты.Вставить("Ограничение", ТекущийЭлемент.Значение);
					Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИД(НачальныйАдрес,КонечныйАдрес,ПараметрыКарты);
					Если ТекущийЭлемент.Значение <> ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка") Тогда
						// В Итоговый массив не будем добавлять данные с ограничениями они уже были записаны при обращении к сервису.
						Продолжить;
					КонецЕсли;
					Если Структура = Неопределено Тогда
						Структура = Новый Структура("Расстояние,Время,Путь",Неопределено,Неопределено,Неопределено);
					КонецЕсли;					
					Структура.Вставить("НачальнаяШирота",НачальныйАдрес.Широта);
					Структура.Вставить("НачальнаяДолгота",НачальныйАдрес.Долгота);
					Структура.Вставить("КонечнаяШирота",КонечныйАдрес.Широта);
					Структура.Вставить("КонечнаяДолгота",КонечныйАдрес.Долгота);
					Структура.Вставить("Ограничение",ТекущийЭлемент.Значение);
					мсвРезультат.Добавить(Структура);
				КонецЦикла;
			Иначе				
				Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИД(НачальныйАдрес,КонечныйАдрес,ПараметрыКарты);
				Если Структура = Неопределено Тогда
					Структура = Новый Структура("Расстояние,Время,Путь",Неопределено,Неопределено,Неопределено);
				КонецЕсли;
				Структура.Вставить("НачальнаяШирота",  НачальныйАдрес.Широта);
				Структура.Вставить("НачальнаяДолгота", НачальныйАдрес.Долгота);
				Структура.Вставить("КонечнаяШирота",   КонечныйАдрес.Широта);
				Структура.Вставить("КонечнаяДолгота",  КонечныйАдрес.Долгота);
				Структура.Вставить("Ограничение",ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка"));
				мсвРезультат.Добавить(Структура);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если мсвРезультат.Количество() = 0 Тогда
		мсвРезультат = Неопределено;
	Конецесли;
	
	Возврат мсвРезультат;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиМассивСитиГИД()

// Рассчитывает расстояние между точками при помощи.
//
// Параметры:
//	ТочкиРасчета - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//					ТочкиМаршрута  - Массив структур с ключами Долгота и Широта - содержит географические координаны точки начала.
//
// Возвращаемое значение:
//   Массив   - Массив структур с ключами Расстояние, Время, Путь.
//
Функция ПолучитьМассивРасстоянийМеждуТочками(ТочкиРасчета, ПараметрыКарты = Неопределено) Экспорт
	
	мсвРезультат = Неопределено;
	
	Если ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
		
		мсвРезультат = ПолучитьРасстояниеМеждуТочкамиМассивGoogleMaps(ТочкиРасчета, ПараметрыКарты);
		
	ИначеЕсли ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.Graphhopper Или ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
		
		мсвРезультат = ПолучитьРасстояниеМеждуТочкамиМассивGraphhopper(ТочкиРасчета, ПараметрыКарты);
		
	ИначеЕсли ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексКарты Тогда
		
		мсвРезультат = ПолучитьРасстояниеМеждуТочкамиМассивYandex(ТочкиРасчета, ПараметрыКарты);
		
	ИначеЕсли ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексМаршрутизация Тогда
		
		мсвРезультат = ПолучитьРасстояниеМеждуТочкамиМассивЯндексМаршрутизация(ТочкиРасчета, ПараметрыКарты);
		
	ИначеЕсли ПараметрыКарты.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
		
		мсвРезультат = ПолучитьРасстояниеМеждуТочкамиМассивСитиГИД(ТочкиРасчета, ПараметрыКарты);
		
	КонецЕсли;	
	
	Возврат мсвРезультат;
	
КонецФункции //ПолучитьМассивРасстоянийМеждуТочками()

// Рассчитывает расстояние между точками при помощи.
//
// Параметры:
//	НачальныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	КонечныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//
// Возвращаемое значение:
//   Структура - Ключи: 
//					Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   			 	Расстояние - Число - расстояние маршрута в км.
//   			 	Время 	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуТочкамиПоТипуКарты(сткКарта, НачальныйАдрес, КонечныйАдрес) Экспорт
	
	Структура = Неопределено;
	ПостОбработка = Ложь; // флаг, Выполнить постобработку.
	Если сткКарта = Неопределено Тогда
		ПостОбработка = Истина;
		сткКарта = ПодготовитьКартуПоТипу(,Истина);
	КонецЕсли;
	Если сткКарта.упИспользуетсяПостроительМаршрутов Тогда
		Если сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеGoogle(НачальныйАдрес, КонечныйАдрес, сткКарта);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.Graphhopper Или сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеGraphhopper(НачальныйАдрес, КонечныйАдрес, сткКарта);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексКарты Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеYandex(НачальныйАдрес, КонечныйАдрес, сткКарта);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексМаршрутизация Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизация(НачальныйАдрес, КонечныйАдрес, сткКарта);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда	
			Структура = ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИД(НачальныйАдрес, КонечныйАдрес, сткКарта);
		КонецЕсли;
	КонецЕсли;
	Если ПостОбработка Тогда
		ПостОбработкаКартыПоТипу(сткКарта);
	КонецЕсли;
	Возврат Структура;
	
КонецФункции //ПолучитьРасстояниеМеждуТочкамиПоТипуКарты()

// Рассчитывает расстояние между точками при помощи.
//
// Параметры:
//	НачальныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	КонечныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//
// Возвращаемое значение:
//   Структура - Ключи: 
//					Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   			 	Расстояние - Число - расстояние маршрута в км.
//   			 	Время 	  - Число - длительность маршрут в сек.
//
Функция ПолучитьРасстояниеМеждуКоординатамиПоТипуКарты(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота, ДатаРасчета = Неопределено, выхОшибка = Неопределено) Экспорт
	
	выхОшибка = Ложь;
	Структура = Неопределено;
	ПостОбработка = Ложь; // флаг, Выполнить постобработку.
	Если сткКарта = Неопределено Тогда
		ПостОбработка = Истина;
		сткКарта = ПодготовитьКартуПоТипу(,Истина, ДатаРасчета);
	КонецЕсли;
	Если сткКарта.упИспользуетсяПостроительМаршрутов Тогда
		Если сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиGoogle(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.Graphhopper Или сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиGraphhopper(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексКарты Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиYandex(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексМаршрутизация Тогда
			Структура = ПолучитьРасстояниеМеждуТочкамиЯндексМаршрутизацияМатрицаРасстояний(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
		ИначеЕсли сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда	
			Структура = ПолучитьРасстояниеМеждуТочкамиСитиГИД(сткКарта, НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота, ДатаРасчета);
		КонецЕсли;
		Если Ложь
			Или Структура = Неопределено 
			Или ТипЗнч(Структура) = Тип("Структура") И Структура.Свойство("Ошибка")
		Тогда
			выхОшибка = Истина;
		КонецЕсли;
	КонецЕсли;
	Если ПостОбработка Тогда
		ПостОбработкаКартыПоТипу(сткКарта);
	КонецЕсли;
	Возврат Структура;
	
КонецФункции 

// Рассчитывает расстояние между точками при помощи.
//
// Параметры:
//	НачальныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	КонечныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//
// Возвращаемое значение:
//   Структура - Ключи: 
//					Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   			 	Расстояние - Число - расстояние маршрута в км.
//   			 	Время 	  - Число - длительность маршрут в сек.
//
Процедура РассчитатьРасстояниеМеждуТочкамиПоТипуКартыПакетно(Пакет, сткКарта) Экспорт
	
	ПостОбработка = Ложь; // флаг, Выполнить постобработку.
	Если сткКарта = Неопределено Тогда
		ПостОбработка = Истина;
		сткКарта = ПодготовитьКартуПоТипу(,Истина);
	КонецЕсли;
	Если сткКарта.упИспользуетсяПостроительМаршрутов Тогда
		Если сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
			ПолучитьРасстояниеМеждуТочкамиПоКартеСитиГИДПакетно(Пакет, сткКарта);
		Иначе
			ВызватьИсключение "Для маршрутизатора " + сткКарта.ТипМаршрутизатора + " пакетный режим вызова не поддерживается";
		КонецЕсли;
	КонецЕсли;
	Если ПостОбработка Тогда
		ПостОбработкаКартыПоТипу(сткКарта);
	КонецЕсли;
	
КонецПроцедуры //ПолучитьРасстояниеМеждуТочкамиПоТипуКарты()

// Функция определяет расстояние между точками маршрута цепочки.
//
// Параметры:
//	НачальныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	КонечныйАдрес - СправочникСсылка.упАдреса - Ссылка на элемент справочника.
//	ВозвращатьЛоманую - Булево - Выполнить возвращение.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПолучитьРасстояниеИВремя(НачальныйАдрес, КонечныйАдрес, ВозвращатьЛоманую = Ложь) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ПолучитьРасстояниеИВремя(НачальныйАдрес, КонечныйАдрес, ВозвращатьЛоманую);
	
КонецФункции //ПолучитьРасстояниеИВремя()

// Функция определяет расстояние между Географическими Зонами.
//
// Параметры:
//	НачальнаяЗона - СправочникСсылка.упГеографическиеЗоны - Ссылка на элемент справочника.
//	КонечнаяЗона - СправочникСсылка.упГеографическиеЗоны - Ссылка на элемент справочника.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПолучитьРасстояниеИВремяМеждуГеографическимиЗонами(НачальнаяЗона, КонечнаяЗона) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ПолучитьРасстояниеИВремяМеждуГеографическимиЗонами(НачальнаяЗона, КонечнаяЗона);
	
КонецФункции

// Функция определяет расстояние между точками маршрута цепочки.
//
// Параметры:
//  АдресаМаршрута  - Массив элементов СправочникСсылка.упАдреса.
//
// Возвращаемое значение:
//   Массив   - Массив структур с ключами начальный адрес, конечный адрес.
//				начальную широту и долготу, конечные широту и долготу,
//					длину маршрута (км.), время маршрута (сек.), текст маршрута.
//
Функция ПолучитьРасстояниеИВремяМассиваТочек(АдресаМаршрута) Экспорт
	
	ТипМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
	ОграниченияСитиГИД = Ложь;
	Если ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда	
		ИспользуетсяПостроительМаршрутов = упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользуетсяПостроительМаршрутов");
		ОграниченияСитиГИД = ИспользуетсяПостроительМаршрутов И ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД");
	КонецЕсли;
	
	мсвРезультат = Новый Массив; 
	
	КЧ = Новый КвалификаторыЧисла(12,0);
	Массив = Новый Массив;
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,КЧ);
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("НачальныйАдрес",Новый ОписаниеТипов("СправочникСсылка.упАдреса"),"НачальныйАдрес");
	ТаблицаЗначений.Колонки.Добавить("КонечныйАдрес",Новый ОписаниеТипов("СправочникСсылка.упАдреса"),"КонечныйАдрес");
	ТаблицаЗначений.Колонки.Добавить("Номер",ОписаниеТиповЧ,"Номер");
	
	n = 0; // порядковый номер последовательности.
	// получаем нужную последовательность точек.
	мсвТочки = ПолучитьПоследовательностьТочек(АдресаМаршрута);
	Для каждого мсвЭлемент Из мсвТочки Цикл
		n = n + 1;
		тбзСтрока = ТаблицаЗначений.Добавить();
		тбзСтрока.НачальныйАдрес = мсвЭлемент.Точка1;
		тбзСтрока.КонечныйАдрес  = мсвЭлемент.Точка2;
		тбзСтрока.Номер			 = n;		
	КонецЦикла;
	
	// производим запрос к базе на данные точек к базе.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
	Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначений);
	Запрос.УстановитьПараметр("ДопОграниченияСитиГИД", ОграниченияСитиГИД);
	Запрос.Выполнить();	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(тбзАдресаМаршрута.НачальныйАдрес КАК Справочник.упАдреса) КАК НачальныйАдрес,
	|	ВЫРАЗИТЬ(тбзАдресаМаршрута.КонечныйАдрес КАК Справочник.упАдреса) КАК КонечныйАдрес,
	|	ВЫРАЗИТЬ(тбзАдресаМаршрута.Номер КАК ЧИСЛО(12, 0)) КАК Номер
	|ПОМЕСТИТЬ втАдрес
	|ИЗ
	|	ТЗ КАК тбзАдресаМаршрута
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдрес.НачальныйАдрес КАК Адрес
	|ПОМЕСТИТЬ вт_Адреса
	|ИЗ
	|	втАдрес КАК втАдрес
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втАдрес.КонечныйАдрес
	|ИЗ
	|	втАдрес КАК втАдрес
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упАдреса.Ссылка КАК Адрес,
	|	МАКСИМУМ(упГеографическиеЗоны.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД) КАК ДопОграниченияСитиГИД
	|ПОМЕСТИТЬ вт_ДоОграничения
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	|			ПО (упИерархияГеографическихЗон.Родитель = упГеографическиеЗоны.Ссылка)
	|		ПО (упАдреса.ГеографическаяЗона = упИерархияГеографическихЗон.Зона)
	|ГДЕ
	|	упАдреса.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Адреса.Адрес
	|			ИЗ
	|				вт_Адреса КАК вт_Адреса)
	|
	|СГРУППИРОВАТЬ ПО
	|	упАдреса.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдрес.Номер КАК Номер,
	|	втАдрес.НачальныйАдрес КАК НачальныйАдрес,
	|	втАдрес.КонечныйАдрес КАК КонечныйАдрес,
	|	ВЫБОР
	|		КОГДА &ДопОграниченияСитиГИД = ЛОЖЬ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДО_НА.ДопОграниченияСитиГИД = ИСТИНА
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ДО_НА,
	|	ВЫБОР
	|		КОГДА &ДопОграниченияСитиГИД = ЛОЖЬ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДО_КА.ДопОграниченияСитиГИД = ИСТИНА
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ДО_КА,
	|	ВЫБОР
	|		КОГДА &ДопОграниченияСитиГИД = ЛОЖЬ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДО_КА.ДопОграниченияСитиГИД = ИСТИНА
	|						И ДО_НА.ДопОграниченияСитиГИД = ИСТИНА
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ДопОграниченияСитиГИД,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Расстояние ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упРасстоянияМеждуТочками.Расстояние = 0
	|						И упРасстоянияМеждуТочками.Время = 0
	|						И упРасстоянияМеждуТочками.АвтоматическийРасчет = ИСТИНА
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ упРасстоянияМеждуТочками.Расстояние
	|			КОНЕЦ
	|	КОНЕЦ КАК ДлинаМаршрута,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Время ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упРасстоянияМеждуТочками.Расстояние = 0
	|						И упРасстоянияМеждуТочками.Время = 0
	|						И упРасстоянияМеждуТочками.АвтоматическийРасчет = ИСТИНА
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ упРасстоянияМеждуТочками.Время
	|			КОНЕЦ
	|	КОНЕЦ КАК ВремяМаршрута,
	|	ЕСТЬNULL(упРасстоянияМеждуТочками.Путь, """") КАК ТекстМаршрута,
	|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПутьПростой, """") КАК ТекстМаршрутаПростого,
	|	втАдрес.НачальныйАдрес.Широта КАК НачальнаяШирота,
	|	втАдрес.НачальныйАдрес.Долгота КАК НачальнаяДолгота,
	|	втАдрес.КонечныйАдрес.Широта КАК КонечнаяШирота,
	|	втАдрес.КонечныйАдрес.Долгота КАК КонечнаяДолгота,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Расстояние ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Записать,
	|	упРасстоянияМеждуТочками.ВремяПонедельник,
	|	упРасстоянияМеждуТочками.ВремяВторник,
	|	упРасстоянияМеждуТочками.ВремяСреда,
	|	упРасстоянияМеждуТочками.ВремяЧетверг,
	|	упРасстоянияМеждуТочками.ВремяПятница,
	|	упРасстоянияМеждуТочками.ВремяСуббота,
	|	упРасстоянияМеждуТочками.ВремяВоскресенье
	|ИЗ
	|	втАдрес КАК втАдрес
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	|		ПО втАдрес.НачальныйАдрес.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
	|			И втАдрес.НачальныйАдрес.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
	|			И втАдрес.КонечныйАдрес.Широта = упРасстоянияМеждуТочками.КонечнаяШирота
	|			И втАдрес.КонечныйАдрес.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота
	|			И (упРасстоянияМеждуТочками.Ограничение = ЗНАЧЕНИЕ(Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДоОграничения КАК ДО_НА
	|		ПО втАдрес.НачальныйАдрес = ДО_НА.Адрес
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ДоОграничения КАК ДО_КА
	|		ПО втАдрес.КонечныйАдрес = ДО_КА.Адрес
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТЗ";
	
	тбзАдреса = Запрос.Выполнить().Выгрузить();
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	// разбирем результат запроса.
	мсвПоследовательности  = Новый Массив; // последовательности точек.
	мсвТочки			   = Новый Массив; // точки последовательности.
	флагДобавлен		   = Ложь; // флаг означающий что добавлена последовательность.
	предПоследовательность = Неопределено; // предыдущая строку таблицы значений.
	
	Для каждого тбзСтрока Из тбзАдреса Цикл		
		Если тбзСтрока.ДлинаМаршрута = Неопределено И тбзСтрока.ВремяМаршрута = Неопределено Тогда
			// параметры маршрута неопределены.
			Если предПоследовательность = Неопределено Тогда
				// для первой последовательночти запоминаем данные.
				предПоследовательность = тбзСтрока;
				стрТочки = Новый Структура("Долгота,Широта,Адрес",тбзСтрока.НачальнаяДолгота,тбзСтрока.НачальнаяШирота,тбзСтрока.НачальныйАдрес);
				стрТочки.Вставить("ДопОграниченияСитиГИД",тбзСтрока.ДО_НА);
				мсвТочки.Добавить(стрТочки);
				стрТочки = Новый Структура("Долгота,Широта,Адрес",тбзСтрока.КонечнаяДолгота,тбзСтрока.КонечнаяШирота,тбзСтрока.КонечныйАдрес);
				стрТочки.Вставить("ДопОграниченияСитиГИД",тбзСтрока.ДО_КА);
				мсвТочки.Добавить(стрТочки);				
			Иначе
				// Найдем разность если 1 тогда это текущаю последовательность больше одного.
				// Новоя последовательность.
				Разность = тбзСтрока.Номер - предПоследовательность.Номер;
				Если Разность = 1 Тогда
					// Последовательность сохроняеться.
					флагДобавлен = Ложь;
					стрТочки = Новый Структура("Долгота,Широта,Адрес",тбзСтрока.КонечнаяДолгота,тбзСтрока.КонечнаяШирота,тбзСтрока.КонечныйАдрес);
					стрТочки.Вставить("ДопОграниченияСитиГИД",тбзСтрока.ДО_КА);
					мсвТочки.Добавить(стрТочки);
					
				ИначеЕсли Разность > 1 Тогда
					// Объявляем новую последовательность.
					мсвПоследовательности.Добавить(мсвТочки);
					флагДобавлен = Истина;
					мсвТочки = Новый Массив;
					стрТочки = Новый Структура("Долгота,Широта,Адрес",тбзСтрока.НачальнаяДолгота,тбзСтрока.НачальнаяШирота,тбзСтрока.НачальныйАдрес);
					стрТочки.Вставить("ДопОграниченияСитиГИД",тбзСтрока.ДО_НА);
					мсвТочки.Добавить(стрТочки);
					стрТочки = Новый Структура("Долгота,Широта,Адрес",тбзСтрока.КонечнаяДолгота,тбзСтрока.КонечнаяШирота,тбзСтрока.КонечныйАдрес);
					стрТочки.Вставить("ДопОграниченияСитиГИД",тбзСтрока.ДО_КА);
					мсвТочки.Добавить(стрТочки);					
				КонецЕсли;
				// Запоминаем предыдущую точку.
				предПоследовательность = тбзСтрока;				
			КонецЕсли;
		Иначе
			// Обнулим все данные.
			предПоследовательность = Неопределено;
			Если НЕ флагДобавлен И мсвТочки.Количество() Тогда
				// мсвТочки - содержит точки добавим новую последовательность.
				мсвПоследовательности.Добавить(мсвТочки);
				мсвТочки = Новый Массив;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ флагДобавлен И мсвТочки.Количество() Тогда
		мсвПоследовательности.Добавить(мсвТочки);
	КонецЕсли;
	// Подготавливаем карту.
	сткКарта = ПодготовитьКартуПоТипу(ТипМаршрутизатора,Истина);
	Если сткКарта <> Неопределено И сткКарта.упИспользуетсяПостроительМаршрутов Тогда 
		// обход последовательностей.
		Для каждого мсвЭлемент Из мсвПоследовательности Цикл
			// получим данные за массив точек.
			мсвРезультат = ПолучитьМассивРасстоянийМеждуТочками(мсвЭлемент, сткКарта);
			Если ТипЗнч(мсвРезультат)=ТИП("Массив") Тогда
				// обойдем результат.
				Для каждого мсвДанные Из мсвРезультат Цикл
					Если мсвДанные.Расстояние = Неопределено Тогда
						Продолжить;
					Иначе
						Отбор = Новый Структура;
						Отбор.Вставить("НачальнаяШирота",  мсвДанные.НачальнаяШирота);
						Отбор.Вставить("НачальнаяДолгота", мсвДанные.НачальнаяДолгота);
						Отбор.Вставить("КонечнаяШирота",   мсвДанные.КонечнаяШирота);
						Отбор.Вставить("КонечнаяДолгота",  мсвДанные.КонечнаяДолгота);
						НайденныеСтроки = тбзАдреса.НайтиСтроки(Отбор);
						Если НайденныеСтроки.Количество() Тогда
							// для найденных строк добавим результат.
							ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], мсвДанные);
							НайденныеСтроки[0].ВремяМаршрута = мсвДанные.Время;
							НайденныеСтроки[0].ДлинаМаршрута = мсвДанные.Расстояние;
							НайденныеСтроки[0].ТекстМаршрута = мсвДанные.Путь;
							Если мсвДанные.Свойство("Записать") Тогда
								НайденныеСтроки[0].Записать = мсвДанные.Записать;
							КонецЕсли;
							Если мсвДанные.Свойство("Расшифровка") И сткКарта.Свойство("IExplorer") Тогда
								Если сткКарта.IExplorer = Неопределено Тогда
									Продолжить;
								КонецЕсли;
								МассивСоединить = Новый Массив;
								Для каждого мсвСтрока Из мсвДанные.Расшифровка Цикл						
									ДлинаСтроки = СтрДлина(мсвСтрока);
									МассивСоединить1 = Новый Массив;
									Для i = 1 По ДлинаСтроки Цикл
										МассивСоединить1.Добавить(КодСимвола(Сред(мсвСтрока,i,1)));
									КонецЦикла;									
									Если МассивСоединить1.Количество() Тогда										
										МассивСоединить.Добавить(СтрСоединить(МассивСоединить1,","));
									КонецЕсли;
								КонецЦикла;
								ТекстКоординат = СтрСоединить(МассивСоединить,";");
								ТекстДанных   = ПолучитьДанныеGoogle(сткКарта, ТекстКоординат);
								ТекстМаршрута = ?(ТекстДанных = Неопределено,"", ТекстДанных);
								Если НЕ ПустаяСтрока(ТекстМаршрута)Тогда
									НайденныеСтроки[0].ТекстМаршрута = ТекстМаршрута;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ПостОбработкаКартыПоТипу(сткКарта);
	
	Массив = Новый Массив; // последовательности точек, результат.
	Для каждого тбзСтрока Из тбзАдреса Цикл
		// заполним массив результатом.
		Структура = Новый Структура("НачальныйАдрес,КонечныйАдрес,НачальнаяШирота,НачальнаяДолгота,КонечнаяШирота,КонечнаяДолгота,ДлинаМаршрута,ВремяМаршрута,ТекстМаршрута,ТекстМаршрутаПростого");
		ЗаполнитьЗначенияСвойств(Структура,тбзСтрока);
		Массив.Добавить(Структура);
		
		Если тбзСтрока.ДлинаМаршрута = Неопределено ИЛИ тбзСтрока.ВремяМаршрута = Неопределено  Тогда
			тбзСтрока.Записать = ЛОЖЬ;
		КонецЕсли;
		
		Если тбзСтрока.Записать Тогда
			сткДанные = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");	
			сткДанные.Расстояние = тбзСтрока.ДлинаМаршрута;
			сткДанные.Время = тбзСтрока.ВремяМаршрута;
			сткДанные.Путь = тбзСтрока.ТекстМаршрута;
			ЗаполнитьЗначенияСвойств(сткДанные, тбзСтрока);
			
			ЗаписатьРасстоянияМеждуТочками(тбзСтрока.НачальнаяШирота, тбзСтрока.НачальнаяДолгота, тбзСтрока.КонечнаяШирота, тбзСтрока.КонечнаяДолгота, сткДанные);
		КонецЕсли;		
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции // ПолучитьРасстояниеИВремяМассиваТочек()

// Возращает все возможные значения измерения "Ограничение" регистра "Расстояния между точками"
// в виде таблицы с колонкой "Ограничение"
Функция ПолучитьТаблицуОграниченийМаршрутизатора() Экспорт 
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Ограничение", Новый ОписаниеТипов("ПеречислениеСсылка.упОграниченияМаршрутизаторов"));
	Результат.Добавить().Ограничение = Перечисления.упОграниченияМаршрутизаторов.ПустаяСсылка();
	ТипМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
	Если Истина
		И ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД 
		И ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД")
		Тогда
		ПараметрыМаршрутизацииСитиГИД = ПолучитьПараметрыМаршрутизацииСитиГИД();
		Для Каждого ЭлементСписка Из ПараметрыМаршрутизацииСитиГИД.ДополнительныеОграниченияСитиГИД Цикл
			Если ЭлементСписка.Значение.Пометка = Истина Тогда
				Результат.Добавить().Ограничение = ЭлементСписка.Значение.Ограничение;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#Область ПрочиеПроцедурыИФункции

// Получим текст ключа API для подключения к сервису.
//
// Параметры:
//  КлючAPI  - Строка - Ключ API.
//  ТипПодключения  - ПеречислениеСсылка - тип сервиса подключения.
//
// Возвращаемое значение:
//   Строка   - Текст Ключа подключения к сервису.
//
Функция ПолучитьТекстКлючаAPI(КлючAPI, ТипПодключения) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ПолучитьТекстКлючаAPI(КлючAPI, ТипПодключения);
	
КонецФункции // ПолучитьТекстКлючаAPI()

// Выполним подключение к сервису маршрутизации, и отправить новый ключ для формирования токена.
//
//ПараметрыОтправки.Вставить("упТипМаршрутизатора",НаборКонстант.упТипМаршрутизатора);
//	ПараметрыОтправки.Вставить("упКлючСервисаМаршрутизации",НаборКонстант.упКлючСервисаМаршрутизации);
//	ПараметрыОтправки.Вставить("упАдресСервераМаршрутизацииЯндекс",НаборКонстант.упАдресСервераМаршрутизацииЯндекс);
// Параметры:
//  ПараметрыОтправки  - Структура - Параментры подключения к сервисам маршрутизации:
//                                  * Ключ - упТипМаршрутизатора, тип маршрутизатора.
//                                  * Ключ - упКлючСервисаМаршрутизации, строка подключение ключ API  к сервису.
//                                  * Ключ - упАдресСервераМаршрутизацииЯндекс, строка, адрес сервиса.
//
// Возвращаемое значение:
//   Структура - Ключи:
//					* Ключ "Статус" - Булево, Истина если соединение установлено.
//               	* Ключ "Ошибка" - Строка, текст ошибки.
//
Функция ВыполнитьОтправкуКлючаСервисаМаршрутизации(ПараметрыОтправки) Экспорт
	
	Ключ   = ПараметрыОтправки.упКлючСервисаМаршрутизации;
	Сервер = ПараметрыОтправки.упАдресСервераМаршрутизацииЯндекс;	
	Прокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("http://" + Сервер);
	
	Ресурс = "/router/apiKey?key=" + Ключ;
	
	HTTP =  Новый HTTPСоединение(Сервер,,,,Прокси);
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("url", Ресурс);	
	ЗаголовокHTTP.Вставить("type", "POST");
	ЗаголовокHTTP.Вставить("dataType", "text");
	ЗаголовокHTTP.Вставить("cache", false);
	ЗаголовокHTTP.Вставить("timeout", 90000);
	
	ЗапросHttp	= Новый HTTPЗапрос(Ресурс, ЗаголовокHTTP);
	
	текОшибки = Истина;
	СообщениеОбОшибке = "";
	Попытка
		ОтветHttp = HTTP.ОтправитьДляОбработки(ЗапросHttp);
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		текОшибки = Ложь;
	КонецПопытки;
	Если текОшибки Тогда
		ТекстОтветJSON = ОтветHttp.ПолучитьТелоКакСтроку();
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(ТекстОтветJSON);
		ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда			
			Если ОтветJSON.Свойство("errorMessage") Тогда
				Если ЗначениеЗаполнено(ОтветJSON.errorMessage) Тогда
					СообщениеОбОшибке = "Яндекс - errorMessage: " + ОтветJSON.errorMessage;
					текОшибки = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если ОтветJSON.Свойство("token") Тогда
				Если ПустаяСтрока(ОтветJSON.token) Тогда
					СообщениеОбОшибке = "Яндекс - errorMessage: в ответе сервиса нет токена!!! Токен не назначен";
					текОшибки = Ложь;
				КонецЕсли;
			Иначе
				СообщениеОбОшибке = "Яндекс - errorMessage: в ответе сервиса нет токена!!! Токен не назначен";
				текОшибки = Ложь;
			КонецЕсли;
		Иначе
			СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера маршрутизации не получен. " + Сервер;
			текОшибки = Ложь;
		КонецЕсли;			
		
	КонецЕсли;
	
	Результат = Новый Структура("Ошибка,Статус","Не удалось выполнить подключение",Ложь);	
	
	ПараметрыОтвета = Неопределено;
	
	Если текОшибки Тогда
		ПараметрыОтвета = Новый Структура("СообщениеОбОшибке", СообщениеОбОшибке)
	КонецЕсли;
	
	ТипМаршрутизатора = ПараметрыОтправки.упТипМаршрутизатора;
	
	ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,НЕ текОшибки,ПараметрыОтвета);
	
	Результат.Ошибка = СообщениеОбОшибке;
	Результат.Статус = текОшибки;
	
	Возврат Результат;
	
КонецФункции // ВыполнитьОтправкуКлючаСервисаМаршрутизации()

// Выполним подключение к сервису маршрутизации.
//
// Параметры:
//  ПараметрыПроверки  - Структура - Параментры подключения к сервисам маршрутизации:
//                                  * Ключ - упТипМаршрутизатора, тип маршрутизатора.
//                                  * Ключ - упКлючДоступаСервисаМаршрутизацииСитиГИД, строка подключение ключ API  к сервису.
//                                  * Ключ - упАдресСервераМаршрутизацииСитиГИД, строка, адрес сервиса.
//                                  * Ключ - упАдресСервераМаршрутизацииGraphhopper, строка, адрес сервиса.
//                                  * Ключ - упДополнительныеПараметрыЗапросаМаршрутизатора, строка, дополнительные параметры подключения.
//
// Возвращаемое значение:
//   Структура - Ключи:
//					* Ключ "Статус" - Булево, Истина если соединение установлено.
//               	* Ключ "Ошибка" - Строка, текст ошибки.
//
Функция ВыполнитьПровекруСервисаМаршрутизации(ПараметрыПроверки) Экспорт
	
	Результат = Новый Структура("Ошибка,Статус","Не удалось выполнить подключение",Ложь);	
	
	НачальнаяШирота = 55.712414;
	НачальнаяДолгота = 37.597275;
	КонечнаяШирота = 55.73059;
	КонечнаяДолгота = 37.635727;
	
	ТипМаршрутизатора = ПараметрыПроверки.упТипМаршрутизатора;
	
	//сткКарта = ПодготовитьКартуПоТипу(ТипМаршрутизатора,Истина);
	сткКарта = Новый Структура();
	
	сткКарта.Вставить("НачальнаяШирота",  НачальнаяШирота);
	сткКарта.Вставить("НачальнаяДолгота", НачальнаяДолгота);
	сткКарта.Вставить("КонечнаяШирота",   КонечнаяШирота);
	сткКарта.Вставить("КонечнаяДолгота",  КонечнаяДолгота);
	
	Если ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.GoogleMaps Тогда
		
		ТекущийКлюч = ПолучитьТекстКлючаAPI(ПараметрыПроверки.упКлючСервисаМаршрутизации, ТипМаршрутизатора);
		
		СтрКоординаты1 = ПредставлениеКоординаты(НачальнаяШирота) + "," + ПредставлениеКоординаты(НачальнаяДолгота);
		СтрКоординаты2 = ПредставлениеКоординаты(КонечнаяШирота) + "," + ПредставлениеКоординаты(КонечнаяДолгота);
		
		// На использование службы API маршрутов Google накладывается ограничение, составляющее 2500 запросов маршрутов в день.
		ClientSignature = ПараметрыПроверки.упДополнительныеПараметрыЗапросаМаршрутизатора; // &client=gme-YOUR_CLIENT_ID&signature=YOUR_URL_SIGNATURE.
		ТекстПериода = "&departure_time=" + Формат((ТекущаяДатаСеанса() + 43200) - Дата("19700101000000"),"ЧГ=0") + "&traffic_model=pessimistic"; // прогноз пробок работает только на будущее время.
		URLСтраницы = "maps/api/directions/xml?origin=" + СтрКоординаты1 + "&destination=" + СтрКоординаты2 + "&sensor=false&mode=driving&units=metric&language=ru-RU" + ТекущийКлюч + ТекстПериода + ClientSignature;
		
		URLСтрока = "https://maps.googleapis.com/" + URLСтраницы;
		
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока);
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);
		
		Результат.Ошибка = ?(РезультатПолученияФайлов.Свойство("СообщениеОбОшибке"),РезультатПолученияФайлов.СообщениеОбОшибке,"");
		Результат.Статус = РезультатПолученияФайлов.Статус;
		
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.Graphhopper Или ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
		
		СтрКоординаты1 = ПредставлениеКоординаты(НачальнаяШирота) + "," + ПредставлениеКоординаты(НачальнаяДолгота);
		СтрКоординаты2 = ПредставлениеКоординаты(КонечнаяШирота) + "," + ПредставлениеКоординаты(КонечнаяДолгота);
		
		Если ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
			URLСервера = упМаршрутизация.ПолучитьПараметрыПодключенияAxelotMaps(Истина).АдресСервера;
		Иначе
			URLСервера = ПараметрыПроверки.упАдресСервераМаршрутизацииGraphhopper;
		КонецЕсли; 
		
		Если НЕ(Прав(URLСервера,1) = "/") Тогда
			URLСервера = URLСервера + "/";
		КонецЕсли;
		
		URLСтрока = URLСервера + "route?point=" + СтрКоординаты1 + "&point=" + СтрКоординаты2 + "&type=gpx&locale=ru-RU";
		
		ПараметрыПолучения = Неопределено;
		
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);
		
		Результат.Ошибка = ?(РезультатПолученияФайлов.Свойство("СообщениеОбОшибке"),РезультатПолученияФайлов.СообщениеОбОшибке,"");
		Результат.Статус = РезультатПолученияФайлов.Статус;
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексКарты Тогда
		
		СтрКоординаты1 = ПредставлениеКоординаты(НачальнаяДолгота) + "," + ПредставлениеКоординаты(НачальнаяШирота);
		СтрКоординаты2 = ПредставлениеКоординаты(КонечнаяДолгота) + "," + ПредставлениеКоординаты(КонечнаяШирота);
		
		URLСервера = ПараметрыПроверки.упАдресСервераМаршрутизацииЯндекс;
		
		Если НЕ(Прав(URLСервера,1) = "/") Тогда
			URLСервера = URLСервера + "/";
		КонецЕсли;
		
		URLСтрока = URLСервера + "router/route?points=" + СтрКоординаты1 + "~" + СтрКоординаты2;
		
		ПараметрыПолучения = Неопределено;
		
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);
		
		Если РезультатПолученияФайлов.Свойство("СообщениеОбОшибке") Тогда 
			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.УстановитьСтроку(РезультатПолученияФайлов.СообщениеОбОшибке);
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
			
			Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда			
				Если ОтветJSON.Свойство("errorMessage") Тогда
					Если ЗначениеЗаполнено(ОтветJSON.errorMessage) Тогда
						Результат.Ошибка = "Яндекс - errorMessage: " + ОтветJSON.errorMessage;
					КонецЕсли;
				КонецЕсли;
				Если ОтветJSON.Свойство("token") Тогда
					Если ПустаяСтрока(ОтветJSON.token) Тогда
						Результат.Ошибка = "Яндекс - errorMessage: в ответе сервиса нет токена!!! Токен не назначен";
					КонецЕсли;
				Иначе
					Результат.Ошибка = "Яндекс - errorMessage: в ответе сервиса нет токена!!! Токен не назначен";
				КонецЕсли;
			Иначе
				Результат.Ошибка = "Яндекс - errorMessage: Ответ от сервера маршрутизации не получен. " + URLСервера;
			КонецЕсли;			
		Иначе	
			Результат.Ошибка = "";
		КонецЕсли;	
		Результат.Статус = РезультатПолученияФайлов.Статус;	
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.ЯндексМаршрутизация Тогда
		
		СтрКоординаты1 = ПредставлениеКоординаты(НачальнаяШирота) + "," + ПредставлениеКоординаты(НачальнаяДолгота);
		СтрКоординаты2 = ПредставлениеКоординаты(КонечнаяШирота) + "," + ПредставлениеКоординаты(КонечнаяДолгота);
		ВремяОтправленияПоУмолчаниюЯндексМаршрутизация = ПараметрыПроверки.ВремяОтправленияПоУмолчаниюЯндексМаршрутизация;
		departure_time = ПодготовитьВремяОтправленияВUNIXФормат(ВремяОтправленияПоУмолчаниюЯндексМаршрутизация, ТекущаяДатаСеанса());;
		ДополнительныеПараметры = ПараметрыПроверки.упДополнительныеПараметрыЗапросаМаршрутизатора;
		Ключ = ПараметрыПроверки.упКлючСервисаМаршрутизации;
		URLСтрока = СтрШаблон("https://api.routing.yandex.net/v1.0/route?apikey=%1%2&waypoints=%3|%4%5", Ключ, departure_time, СтрКоординаты1, СтрКоординаты2, ДополнительныеПараметры);
		
		ПараметрыПолучения = Неопределено;
		
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);
		
		Если РезультатПолученияФайлов.Свойство("СообщениеОбОшибке") Тогда 
			Если РезультатПолученияФайлов.КодСостояния = 500 
				ИЛИ РезультатПолученияФайлов.КодСостояния = 504 Тогда
				Результат.Ошибка = "Системная ошибка сервера. Повторите запрос с небольшой задержкой.";
			Иначе
				Результат.Ошибка = РезультатПолученияФайлов.СообщениеОбОшибке;
			КонецЕсли;
		Иначе	
			Результат.Ошибка = "";
		КонецЕсли;	
		Результат.Статус = РезультатПолученияФайлов.Статус;	
		
	ИначеЕсли ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
		сткКарта.Очистить();
		сткКарта = ПодготовитьКартуПоТипу(ТипМаршрутизатора,Истина);
		сткКарта.Вставить("НачальнаяШирота",  НачальнаяШирота);
		сткКарта.Вставить("НачальнаяДолгота", НачальнаяДолгота);
		сткКарта.Вставить("КонечнаяШирота",   КонечнаяШирота);
		сткКарта.Вставить("КонечнаяДолгота",  КонечнаяДолгота);
		
		Ключ = ПолучитьТекстКлючаAPI(ПараметрыПроверки.упКлючДоступаСервисаМаршрутизацииСитиГИД, ТипМаршрутизатора);
		Сервер = ПараметрыПроверки.упАдресСервераМаршрутизацииСитиГИД;
		Прокси = ПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси("http://" + Сервер);
		
		сткКарта.Вставить("accessKey",Ключ);
		
		// Заполняем Маршрут точки маршрута для анализа.
		мсвПуть = Новый Массив;
		
		сткТочка = Новый Структура;
		сткТочка.Вставить("latitude",НачальнаяШирота);
		сткТочка.Вставить("longitude",НачальнаяДолгота);
		мсвПуть.Добавить(сткТочка);
		
		сткТочка = Новый Структура;
		сткТочка.Вставить("latitude",КонечнаяШирота);
		сткТочка.Вставить("longitude",КонечнаяДолгота);
		мсвПуть.Добавить(сткТочка);
		
		singleRouteRequest = Новый Структура("Points",мсвПуть);
		мсвМаршрут = Новый Массив;
		мсвМаршрут.Добавить(singleRouteRequest);			
		
		ПараметрыПодключения = сткКарта.Параметры;
		
		ПараметрыПодключения.Вставить("routes",мсвМаршрут);
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ФайлЗапроса = сткКарта.ФайлЗапроса;
		ЗаписьJSON.ОткрытьФайл(ФайлЗапроса,КодировкаТекста.UTF8,,Новый ПараметрыЗаписиJSON(,Символы.Таб));
		// формируем файл формата JSON для запроса к сервису.
		НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON();
		НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
		НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
		ЗаписатьJSON(ЗаписьJSON,ПараметрыПодключения,НастройкиСериализацииJSON);
		
		ЗаписьJSON.Закрыть();
		
		ТекстФайл = Новый ТекстовыйДокумент;
		ТекстФайл.Прочитать(ФайлЗапроса,КодировкаТекста.UTF8);
		Тело = ТекстФайл.ПолучитьТекст();
		
		Ресурс = ПараметрыПроверки.упРесурсСитиГИД;
		HTTP =  Новый HTTPСоединение(Сервер,,,,Прокси);
		ФайлРезультата = сткКарта.ФайлРезультата;
		ЗаголовокHTTP = сткКарта.ЗаголовокHTTP;
		
		ЗапросHttp	= Новый HTTPЗапрос(Ресурс, ЗаголовокHTTP);
		ЗапросHttp.УстановитьТелоИзСтроки(Тело,КодировкаТекста.UTF8,ИспользованиеByteOrderMark.НеИспользовать);
		
		текОшибки = Истина;
		СообщениеОбОшибке = "";
		Попытка
			ОтветHttp = HTTP.ОтправитьДляОбработки(ЗапросHttp);
		Исключение
			СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			текОшибки = Ложь;
		КонецПопытки;
		Если текОшибки Тогда
			ТекстОтветJSON = ОтветHttp.ПолучитьТелоКакСтроку();
			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.УстановитьСтроку(ТекстОтветJSON);
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
			
			УдалитьФайлы(ФайлЗапроса);
			УдалитьФайлы(ФайлРезультата);
			
			Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда
				Если ОтветJSON.Свойство("errorMessage") Тогда
					Если ЗначениеЗаполнено(ОтветJSON.errorMessage) Тогда
						СообщениеОбОшибке = "СитиГИД - errorMessage: " + ОтветJSON.errorMessage;
						текОшибки = Ложь;
					КонецЕсли;
				Иначе
					СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера маршрутизации не получен. " + Строка(HTTP.Сервер);
					текОшибки = Ложь;
				КонецЕсли;
			Иначе
				СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера маршрутизации не получен. " + Строка(HTTP.Сервер);
				текОшибки = Ложь;
			КонецЕсли;			
			
		КонецЕсли;
		
		ПараметрыОтвета = Неопределено;
		
		Если текОшибки Тогда
			ПараметрыОтвета = Новый Структура("СообщениеОбОшибке", СообщениеОбОшибке)
		КонецЕсли;
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(ТипМаршрутизатора,НЕ текОшибки,ПараметрыОтвета);
		
		Результат.Ошибка = СообщениеОбОшибке;
		Результат.Статус = текОшибки;
		
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции // ВыполнитьПровекруСервисаМаршрутизации()

// Получить структуру параметров маршрутизации СитиГИД.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Структура   - Параметры маршрутизатора СитиГИД.
//
Функция ПолучитьПараметрыМаршрутизацииСитиГИД() Экспорт
	
	Результат = Неопределено;
	
	упИспользуетсяПостроительМаршрутов = упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользуетсяПостроительМаршрутов");
	Если упИспользуетсяПостроительМаршрутов Тогда
		
		ТипМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
		
		Если ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД Тогда
			
			ХранилищеЗначений = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПараметрыМаршрутизацииСитиГИД");
			упПараметрыМаршрутизацииСитиГИД = ХранилищеЗначений.Получить();
			
			Структура = Новый Структура;
			Если ТипЗнч(упПараметрыМаршрутизацииСитиГИД) = ТИП("Структура")Тогда		
				
				Структура.Вставить("type",             упПараметрыМаршрутизацииСитиГИД.type);
				Структура.Вставить("allowYards",       упПараметрыМаршрутизацииСитиГИД.allowYards);
				Структура.Вставить("allowSideWays",    упПараметрыМаршрутизацииСитиГИД.allowSideWays);
				Структура.Вставить("allowGroundRoads", упПараметрыМаршрутизацииСитиГИД.allowGroundRoads);
				Структура.Вставить("allowPaidRoads",   упПараметрыМаршрутизацииСитиГИД.allowPaidRoads);
				Структура.Вставить("useJams",          упПараметрыМаршрутизацииСитиГИД.useJams);
				Структура.Вставить("useLimitsUpd",     упПараметрыМаршрутизацииСитиГИД.useLimitsUpd);
				Структура.Вставить("limitsName","");
				Структура.Вставить("useForecast",      упПараметрыМаршрутизацииСитиГИД.useForecast);
				Структура.Вставить("forecastDateTimeUtc",ДАТА("00010101000000"));
				
				Если упПараметрыМаршрутизацииСитиГИД.Свойство("ДополнительныеОграниченияСитиГИД") Тогда
					СписокЗначений = Новый СписокЗначений;
					Для каждого СтрокаОграничения Из упПараметрыМаршрутизацииСитиГИД.ДополнительныеОграниченияСитиГИД Цикл
						СтруктураОграничения = Новый Структура();
						СтруктураОграничения.Вставить("Пометка",СтрокаОграничения.Пометка);
						СтруктураОграничения.Вставить("Ограничение",СтрокаОграничения.Ограничение);
						СписокЗначений.Добавить(СтруктураОграничения);
					КонецЦикла;
				Иначе
					СписокЗначений = ЗаполнитьДополнительныеОграниченияСитиГИДПоУмолчанию();
				КонецЕсли; 
				Структура.Вставить("ДополнительныеОграниченияСитиГИД", СписокЗначений);
				
			Иначе
				
				Структура.Вставить("type","OptimizeLength");
				Структура.Вставить("allowYards",Истина);
				Структура.Вставить("allowSideWays",Истина);
				Структура.Вставить("allowGroundRoads",Истина);
				Структура.Вставить("allowPaidRoads",Истина);
				Структура.Вставить("useJams",Ложь);
				Структура.Вставить("useLimitsUpd",Ложь);
				Структура.Вставить("limitsName","");
				Структура.Вставить("useForecast",Ложь);	
				Структура.Вставить("forecastDateTimeUtc",ДАТА("00010101000000"));
				
				СписокЗначений = ЗаполнитьДополнительныеОграниченияСитиГИДПоУмолчанию();
				Структура.Вставить("ДополнительныеОграниченияСитиГИД", СписокЗначений);
				
			КонецЕсли;
			
			Результат = Структура;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьПараметрыМаршрутизацииСитиГИД()

Функция ЗаполнитьДополнительныеОграниченияСитиГИДПоУмолчанию()
	
	СписокЗначений = Новый СписокЗначений;
	Для каждого МетаЗначение Из Метаданные.Перечисления.упОграниченияМаршрутизаторов.ЗначенияПеречисления Цикл
		СтрокаОграничения = Перечисления.упОграниченияМаршрутизаторов[МетаЗначение.Имя];
		СтруктураОграничения = Новый Структура();
		СтруктураОграничения.Вставить("Пометка",Ложь);
		СтруктураОграничения.Вставить("Ограничение",СтрокаОграничения);
		СписокЗначений.Добавить(СтруктураОграничения);
	КонецЦикла;
	Возврат СписокЗначений; 
	
КонецФункции

// Выполнить обращение к сервису маршрутизации.
//
// Параметры:
//  ТаблицаАдресов  - ТаблицаЗначений - Таблица Данных.
//  КоэффициентПереводаВЧасы  - Число - Коэффициент.
//  ВызыватьМаршрутизацию  - Булево - Выполнить.
//  ДополнительныеПараметры  - Структура - Ключи: 
//					* ПараметрыМаршрутизацииСитиГИД - структура, параметры для маршрутизации ситигид.
//                  * ДатаНачалаПланирования - Дата, период начиная с которго выполнить маршрутизацию сервисом ситигид.
//
// Возвращаемое значение:
//   Соответсвие - Значения, Результат выполнения.
//
Функция ПолучитьРасстояниеИВремяМеждуТочками(ТаблицаАдресов, КоэффициентПереводаВЧасы, ВызыватьМаршрутизацию = Ложь, ДополнительныеПараметры = Неопределено, ОграничениеМаршрутизатора = Неопределено) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ПолучитьРасстояниеИВремяМеждуТочками(ТаблицаАдресов, КоэффициентПереводаВЧасы, ВызыватьМаршрутизацию, ДополнительныеПараметры, ОграничениеМаршрутизатора);
	
КонецФункции // ПолучитьРасстояниеИВремяМеждуТочками()

// Ищем значение для элмента коллекции элементов, DOM HTML документа.
//
// Параметры:
//	Document  - COMОбъект - объект документа,
//			(объект, функционально эквивалентный объекту документа, используемому в скриптах на Web-страницах).
//	ИмяСлоя  - Строка - Идентирфикатор слоя в документе HTML.
//
// Возвращаемое значение:
//   Строка - текстовое представление значения.
//
Функция ВернутьЗначениеЭлемента(Document, ИмяСлоя)
	
	Результат = Неопределено;
	div = Document.getElementById(ИмяСлоя);
	Elements = div.getElementsByTagName("*");
	Для каждого tag Из Elements Цикл
		Результат = tag.value;
	КонецЦикла;	
	Возврат Результат;
	
КонецФункции // ВернутьЗначениеЭлемента()

// Установим значение для элмента коллекции элементов, DOM HTML документа.
//
// Параметры:
//	Document  - COMОбъект - объект документа (объект, функционально эквивалентный объекту документа, используемому в скриптах на Web-страницах).
//	ИмяСлоя  - Строка - Идентирфикатор слоя в документе HTML.
//	ТекстЗначения  - Строка - значение которое надо установить.
//
// Возвращаемое значение:
//   Нет.
//
Функция УстановитьЗначениеЭлемента(Document, ИмяСлоя, ТекстЗначения)
	
	div = Document.getElementById(ИмяСлоя);
	Elements = div.getElementsByTagName("*");
	Для каждого tag Из Elements Цикл
		tag.value = ТекстЗначения;
	КонецЦикла;
	
КонецФункции // УстановитьЗначениеЭлемента()

// Выполним удаление файла.
//
// Параметры:
//  ИмяФайла  - Строка - Полное имя удаляемого файла.
//
// Возвращаемое значение:
//   Булево - флаг что файл удален.
//
Функция УдалитьУказанныйФайл(ИмяФайла)
	
	Результат = Ложь;
	
	Если НЕ ПустаяСтрока(ИмяФайла) Тогда		
		ТекущийФайл = Новый Файл(ИмяФайла);		
		Если ТекущийФайл.Существует() Тогда		
			ТекущийФайл = Неопределено;
			Попытка
				УдалитьФайлы(ИмяФайла); 
				Результат = Истина;
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки());
			КонецПопытки;			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // УдалитьУказанныйФайл()

// Выполним установку точек в последовательности.
//
// Параметры:
//  ТочкиРасчета  - Массив - Содержит точки последовательности.
//
// Возвращаемое значение:
//   Массив   - Содержит структуры с ключами, Точка1-начальная точка, Точка2-конечная точка.
//
Функция ПолучитьПоследовательностьТочек(ТочкиРасчета)
	
	Массив = Новый Массив;
	
	ИндексНаибольший = ТочкиРасчета.ВГраница(); // определяем размер массив.
	
	Для i = 0 По ИндексНаибольший Цикл
		j = i + 1;
		Если j > ИндексНаибольший Тогда
			Прервать;
		Иначе
			Структура = Новый Структура("Точка1, Точка2", ТочкиРасчета[i], ТочкиРасчета[j]);
			Массив.Добавить(Структура);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции // ПолучитьПоследовательностьТочек()

// Выполним разбиение массива на порции.
//
// Параметры:
//  ТочкиРасчета  - Массив - Точки маршрута.
//  ГраницаТочек  - Число - Граница точек.
//
// Возвращаемое значение:
//   Массив   - Массив массивов порций.
//
Функция ПолучитьТочкиМаршрутаПорциями(ТочкиРасчета, ГраницаТочек)
	
	мсвБлоки = Новый Массив; // массив содержит порции точек.
	
	Если ГраницаТочек <= 1 ИЛИ ГраницаТочек > ТочкиРасчета.Количество() Тогда
		мсвБлоки.Добавить(ТочкиРасчета);
	Иначе
		i = 0; // номер текущего элемента.
		мсвТочки = Новый Массив; // массив одной порции точек.
		текЭлемент = Неопределено;
		
		Для каждого мсвЭлемент Из ТочкиРасчета Цикл			
			Если i = ГраницаТочек Тогда			
				i = 1;
				мсвБлоки.Добавить(мсвТочки);
				мсвТочки = Новый Массив;
				мсвТочки.Добавить(текЭлемент);
				мсвТочки.Добавить(мсвЭлемент);
				текЭлемент = мсвЭлемент;
			Иначе
				текЭлемент = мсвЭлемент;
				мсвТочки.Добавить(текЭлемент);			
			КонецЕсли;
			i = i + 1;			
		КонецЦикла;
		
		Если i > 1 Тогда
			мсвБлоки.Добавить(мсвТочки);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат мсвБлоки;
	
КонецФункции // ПолучитьТочкиМаршрутаПорциями()

// Получим расчет данных Google.
//
// Параметры:
//	сткКарта  - структура - ключи парамтры загрузки карты - Координата широты начало маршрута.
//  ТекстДанных  - Строка - текст данных координат.
//
// Возвращаемое значение:
//   Строка   - Содержит, перечисленные координаты.
//
Функция ПолучитьДанныеGoogle(сткКарта, ТекстДанных)
	
	текОшибки = Ложь; // флаг, ошибок при выполнении операции.
	
	appIE = сткКарта.IExplorer;
	Если appIE = Неопределено Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,, "Ошибка в ПолучитьДанныеGoogle() нет экземпляра Internet Explorer, для работы");
		текОшибки = Истина;
	Иначе
		Попытка
			ДатаНачала = ТекущаяДата();
			Пока appIE.Busy Цикл
				КоличествоСекунд = ТекущаяДата() - ДатаНачала;
				Если КоличествоСекунд > 5 Тогда					
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,, "Ошибка создания документа html: Ответ сервиса маршрутизации Google больше 120 сек.");
					appIE.Stop();
					текОшибки = Истина;
				КонецЕсли;
			КонецЦикла;
			Если НЕ текОшибки Тогда
				htmlDoc = appIE.Document;				
			КонецЕсли;
		Исключение			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка создания документа html: ""%1"", возможно нет прав для использования экземпляра Internet Explorer'"), ОписаниеОшибки()));
			текОшибки = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ текОшибки Тогда
		Попытка
			УстановитьЗначениеЭлемента(htmlDoc, "GetStatus", "great");
			УстановитьЗначениеЭлемента(htmlDoc, "GetData", ТекстДанных);
			htmlDoc.getElementById("ButtonExe").click();
			ФлагИс = Истина;
			ДатаНачала = ТекущаяДата();
			ТекстОшибки = "";
			Пока ФлагИс Цикл
				div      = htmlDoc.getElementById("GetStatus");
				Elements = div.getElementsByTagName("*");
				Для каждого tag Из Elements Цикл
					ТекстОшибки = tag.value;
					Если tag.value = "ok" Тогда
						УспешноеПостроение = Истина;
						ФлагИс = Ложь;
						Прервать;
					КонецЕсли;		
				КонецЦикла;
				КоличествоСекунд = ТекущаяДата() - ДатаНачала;
				Если ФлагИс И КоличествоСекунд > 5 Тогда					
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ответ сервиса маршрутизации Google больше 120 сек. ""%1""'"),ТекстОшибки));
					appIE.Stop();
					текОшибки = Истина;
				КонецЕсли;			
			КонецЦикла;
			Текст = ВернутьЗначениеЭлемента(htmlDoc, "GetAddres");
			Если Не УспешноеПостроение И Текст = Неопределено Тогда 
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний'"), УровеньЖурналаРегистрации.Ошибка,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Google: Не удалось дешифровать ""%1""'"),ТекстДанных));
			КонецЕсли;
		Исключение
			ЗаписьЖурналаРегистрации("Расчет расстояний", УровеньЖурналаРегистрации.Ошибка,,, "HTML_Google_Route" + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			текОшибки = Истина;
		КонецПопытки;		
	КонецЕсли;
	
	Результат = Неопределено;
	Если НЕ текОшибки Тогда
		
		мсвКоординат = СтрРазделить(Текст,";");
		Массив = Новый Массив; // массив координат.
		Для Сч = 0 По ((мсвКоординат.Количество() - 1) / 2) - 1 Цикл 
			Структура = Новый Структура; // структура координат точки.
			Структура.Вставить("Широта",ПредставлениеКоординатыВЧисло(мсвКоординат[Сч * 2]));
			Структура.Вставить("Долгота",ПредставлениеКоординатыВЧисло(мсвКоординат[(Сч * 2) + 1]));
			Массив.Добавить(Структура);
		КонецЦикла;
		
		СтруктураУпрощенния = ПолучитьУпрощенныеКоординаты(Массив);
		
		Если СтруктураУпрощенния.Упрощение Тогда			
			Текст = Неопределено;			
			Результат = ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(СтруктураУпрощенния.Координаты);
		Иначе
			Результат = Текст;
		КонецЕсли;
		
		мсвКоординат		= Неопределено;
		Массив				= Неопределено;
		СтруктураУпрощенния = Неопределено;
		
	КонецЕсли;
	
	ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,НЕ текОшибки);
	
	Возврат Результат;
	
КонецФункции // ПолучитьДанныеGoogle()

// Получим расчет данных Yandex.
//
// Параметры:
//	сткКарта  - структура - ключи парамтры загрузки карты - Координата широты начало маршрута.
//  ТочкиРассчета  - Массив - точек рассчета.
//
// Возвращаемое значение:
//   Структура   -  Ключи:
//					Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   			    Расстояние - Число - расстояние маршрута в км.
//   			    Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьДанныеYandex(сткКарта, ТочкиРасчета)
	
	текОшибки = Ложь; // флаг, ошибок при выполнении операции.
	СообщениеОбОшибке = "";
	
	МассивСоединить = Новый Массив;
	Для каждого мсвЭлемент Из ТочкиРасчета Цикл
		МассивСоединить.Добавить(СтрШаблон("%1,%2",мсвЭлемент.Долгота,мсвЭлемент.Широта));
	КонецЦикла;
	стрТочки = СтрСоединить(МассивСоединить,"~");
	Структура = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");
	
	URLСтрока = сткКарта.Соединение + стрТочки;
	
	Сч = 1;
	
	тзВремяМеждуТочками = ПолучитьКаркасТаблицыВремениМеждуТочками();
	
	текОтметкаВремени = Неопределено;
	
	ПараметрыПолучения = ?(сткКарта.Свойство("ПараметрыПолучения"),сткКарта.ПараметрыПолучения,Неопределено);
	
	Попытка
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);	
		
		Если РезультатПолученияФайлов.Статус Тогда
			
			ФайлРезультата = РезультатПолученияФайлов.Путь;
			
			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.ОткрытьФайл(ФайлРезультата);
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
			
			УдалитьФайлы(ФайлРезультата);		
			
			Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда
				Если ОтветJSON.Свойство("errorMessage") Тогда
					Если ЗначениеЗаполнено(ОтветJSON.errorMessage) Тогда
						СообщениеОбОшибке = "Маршрутизатор Яндекс - errorMessage: " + ОтветJSON.errorMessage;
						текОшибки = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ текОшибки Тогда
					
					Текст = "";
					ДлинаМаршрута = 0;
					ВремяМаршрута = 0;
					ВремяМаршрутаПробки = 0;
					Если ТипЗнч(ОтветJSON.features) = ТИП("Массив") Тогда
						ТекущиеОтрезкиПути = ОтветJSON.features;
						Для каждого ТекущийОтрезок Из ТекущиеОтрезкиПути Цикл
							ТекущиеНаправления = ТекущийОтрезок.features;
							Для каждого ТекущееНаправление Из ТекущиеНаправления Цикл
								
								ДлинаПути        = ТекущееНаправление.properties.pathMetaData.distance.value; // в метрах.
								ВремяПути        = ТекущееНаправление.properties.pathMetaData.duration.value; // в секундах.
								ВремяПути_Пробки = ТекущееНаправление.properties.pathMetaData.durationInTraffic.value; // в секундах.
								МассивТочек      = ТекущееНаправление.properties.coordinates; // массив точек.
								
								ДлинаМаршрута       = ДлинаПути / 1000;
								ВремяМаршрута       = ВремяПути;
								ВремяМаршрутаПробки = ВремяПути_Пробки;
								
								Если НЕ МассивТочек = Неопределено Тогда
									МассивКоординат = Новый Массив;
									Если МассивТочек.Количество() < 1000 Тогда
										Для каждого текКординаты Из МассивТочек Цикл 
											МассивКоординат.Добавить(ПредставлениеКоординаты(текКординаты[1]));
											МассивКоординат.Добавить(ПредставлениеКоординаты(текКординаты[0]));
										КонецЦикла;
										Текст = СтрСоединить(МассивКоординат, ";");
									Иначе
										Для каждого текКординаты Из МассивТочек Цикл         									
											сткКоориднаты = Новый Структура("Широта,Долгота",текКординаты[1],текКординаты[0]);
											МассивКоординат.Добавить(сткКоориднаты);
										КонецЦикла;
										СтруктураУпрощения = ПолучитьУпрощенныеКоординаты(МассивКоординат);
										Текст = ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(СтруктураУпрощения.Координаты);
									КонецЕсли; 
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
						
						Путь = "";
						
						Если Истина
							И ДлинаМаршрута = 0
							И ПолучитьФункциональнуюОпцию("упПрямойРасчетГеографическиБлизкихТочекПриНеудаче") 
						Тогда
							КритерийБлизостиТочекВМетрах = ПолучитьФункциональнуюОпцию("упКритерийБлизостиТочекВМетрах");
							НачальнаяШирота  = Неопределено;
							НачальнаяДолгота = Неопределено;
							КонечнаяШирота   = Неопределено;
							КонечнаяДолгота  = Неопределено;
									
							ДлинаПути = 0;
							Для каждого КоординатыТочки Из ТочкиРасчета Цикл
								КонечнаяШирота   = КоординатыТочки.Широта;
								КонечнаяДолгота  = КоординатыТочки.Долгота;
								
								Путь = Путь + ПредставлениеКоординаты(КонечнаяШирота) + ";"	+ ПредставлениеКоординаты(КонечнаяДолгота) + ";";
								
								Если Истина
									И НачальнаяШирота <> Неопределено
									И НачальнаяДолгота <> Неопределено 
									И КонечнаяШирота <> Неопределено 
									И КонечнаяДолгота <> Неопределено
								Тогда
								     ДлинаПутиМетры = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
									Если Истина
										И ДлинаПутиМетры <> 0
										И ДлинаПутиМетры <= КритерийБлизостиТочекВМетрах
									Тогда
										ДлинаПути = ДлинаПути + ДлинаПутиМетры;
									КонецЕсли;
								КонецЕсли;
								НачальнаяШирота   = КонечнаяШирота;
								НачальнаяДолгота  = КонечнаяДолгота;
							КонецЦикла;
														
							ДлинаМаршрута = ДлинаПути / 1000;
							ВремяМаршрута = 6 * ДлинаПути / 100; // 3600 * ДлинаПути / 1000 / 60
							ВремяМаршрутаПробки = ВремяМаршрута;

						КонецЕсли;
												
						Структура.Расстояние = ДлинаМаршрута;
						Структура.Время      = ВремяМаршрута;
						Структура.Путь       = ?(ЗначениеЗаполнено(Текст), Текст, Путь);
						Структура.ВремяПонедельник = ВремяМаршрутаПробки;
						Структура.ВремяВторник     = ВремяМаршрутаПробки;
						Структура.ВремяСреда       = ВремяМаршрутаПробки;
						Структура.ВремяЧетверг     = ВремяМаршрутаПробки;
						Структура.ВремяПятница     = ВремяМаршрутаПробки;
						Структура.ВремяСуббота     = ВремяМаршрутаПробки;
						Структура.ВремяВоскресенье = ВремяМаршрутаПробки;
						
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				СообщениеОбОшибке = "Маршрутизатор Яндекс - errorMessage: Ответ от сервера маршрутизации не получен. " + сткКарта.Соединение;
				текОшибки = Истина;
			КонецЕсли;
		Иначе		
			Если РезультатПолученияФайлов.Свойство("СообщениеОбОшибке") И НЕ ПустаяСтрока(РезультатПолученияФайлов.СообщениеОбОшибке)Тогда
				СообщениеОбОшибке = РезультатПолученияФайлов.СообщениеОбОшибке;
			КонецЕсли;		
		КонецЕсли;
		
	Исключение
		СообщениеОбОшибке = URLСтрока + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Результат = Неопределено;
	Если текОшибки Тогда
		Результат = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");	
		Результат.Вставить("Ошибка",СообщениеОбОшибке);
		Результат.Вставить("СообщениеОбОшибке",СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний Яндекс'"), УровеньЖурналаРегистрации.Ошибка,,, СообщениеОбОшибке);
	Иначе		
		Результат = Структура;
	КонецЕсли;
	
	ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,НЕ текОшибки,Результат);
	
	Возврат Результат;
	
КонецФункции // ПолучитьДанныеYandex()

// Получим расчет данных Яндекс.Маршрутизация: Матрица расстояний.
//
// Параметры:
//	сткКарта  - структура - ключи парамтры загрузки карты - Координата широты начало маршрута.
//  ТочкиРассчета  - Массив - точек рассчета.
//
// Возвращаемое значение:
//   Структура   -  Ключи:
//					Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//   			    Расстояние - Число - расстояние маршрута в км.
//   			    Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьДанныеЯндексМаршрутизация(сткКарта, ТочкиРасчета)
	
	текОшибки = Ложь; // флаг, ошибок при выполнении операции.
	СообщениеОбОшибке = "";
	
	МассивСоединить = Новый Массив;
	Для каждого мсвЭлемент Из ТочкиРасчета Цикл
		МассивСоединить.Добавить(СтрШаблон("%1,%2", мсвЭлемент.Широта, мсвЭлемент.Долгота));
	КонецЦикла;
	стрТочки = СтрСоединить(МассивСоединить,"|");
	Структура = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");
	
	URLСтрока = сткКарта.Соединение + стрТочки;
	
	Сч = 1;
	
	тзВремяМеждуТочками = ПолучитьКаркасТаблицыВремениМеждуТочками();
	
	текОтметкаВремени = Неопределено;
	
	ПараметрыПолучения = ?(сткКарта.Свойство("ПараметрыПолучения"),сткКарта.ПараметрыПолучения,Неопределено);
	
	Попытка
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);	
		
		Если РезультатПолученияФайлов.Статус Тогда
			
			ФайлРезультата = РезультатПолученияФайлов.Путь;
			
			ЧтениеJSON = Новый ЧтениеJSON();
			ЧтениеJSON.ОткрытьФайл(ФайлРезультата);
			ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
			
			УдалитьФайлы(ФайлРезультата);		
			
			Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда
				Если ОтветJSON.Свойство("errorMessage") Тогда
					Если ЗначениеЗаполнено(ОтветJSON.errorMessage) Тогда
						СообщениеОбОшибке = "Маршрутизатор Яндекс - errorMessage: " + ОтветJSON.errorMessage;
						текОшибки = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ текОшибки Тогда
					
					МассивТекстов = Новый Массив;
					Текст = "";
					ДлинаМаршрута = 0;
					ВремяМаршрута = 0;
					ВремяМаршрутаПробки = 0;
					Если ТипЗнч(ОтветJSON.route.legs) = ТИП("Массив") Тогда
						ТекущиеОтрезкиПути = ОтветJSON.route.legs;
						Для каждого ТекущийОтрезок Из ТекущиеОтрезкиПути Цикл
							Если НЕ ТекущийОтрезок.status = "OK" Тогда
								//Статус построения маршрута. Возможные значения: OK, FAIL.
								Продолжить;
							КонецЕсли;
							ТекущиеНаправления = ТекущийОтрезок.steps;
							Для каждого ТекущееНаправление Из ТекущиеНаправления Цикл
								
								ДлинаПути        = ТекущееНаправление.length; // в метрах.
								ВремяПути        = ТекущееНаправление.duration; // в секундах.
								// waiting_duration - Время ожидания без движения на участке маршрута.
								// Например, учитывается задержка, вызванная ожиданием сведения моста.
								ВремяПути_Пробки = ВремяПути + ТекущееНаправление.waiting_duration; // в секундах.
								МассивТочек      = ТекущееНаправление.polyline.points; // массив точек.
								
								ДлинаМаршрута       = ДлинаМаршрута + (ДлинаПути / 1000);
								ВремяМаршрута       = ВремяМаршрута + ВремяПути;
								ВремяМаршрутаПробки = ВремяМаршрутаПробки + ВремяПути_Пробки;
								
								Если НЕ МассивТочек = Неопределено Тогда
									МассивКоординат = Новый Массив;
									Если МассивТочек.Количество() < 1000 Тогда
										Для каждого текКординаты Из МассивТочек Цикл 
											МассивКоординат.Добавить(ПредставлениеКоординаты(текКординаты[0]));
											МассивКоординат.Добавить(ПредставлениеКоординаты(текКординаты[1]));
										КонецЦикла;
										ТекстКоординат = СтрСоединить(МассивКоординат, ";");
										МассивТекстов.Добавить(ТекстКоординат);
									Иначе
										Для каждого текКординаты Из МассивТочек Цикл         									
											сткКоориднаты = Новый Структура("Широта,Долгота",текКординаты[0],текКординаты[1]);
											МассивКоординат.Добавить(сткКоориднаты);
										КонецЦикла;
										СтруктураУпрощения = ПолучитьУпрощенныеКоординаты(МассивКоординат);
										ТекстКоординат = ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(СтруктураУпрощения.Координаты);
										МассивТекстов.Добавить(ТекстКоординат);
									КонецЕсли; 
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
						Если МассивТекстов.Количество() Тогда
							Текст = СтрСоединить(МассивТекстов, ";");
							МассивТекстов.Очистить();
							МассивТекстов = Неопределено;
						КонецЕсли;
						ОтветJSON.Очистить();
						Путь = "";
						
						Если Истина
							И ДлинаМаршрута = 0
							И ПолучитьФункциональнуюОпцию("упПрямойРасчетГеографическиБлизкихТочекПриНеудаче") 
						Тогда
							КритерийБлизостиТочекВМетрах = ПолучитьФункциональнуюОпцию("упКритерийБлизостиТочекВМетрах");
							НачальнаяШирота  = Неопределено;
							НачальнаяДолгота = Неопределено;
							КонечнаяШирота   = Неопределено;
							КонечнаяДолгота  = Неопределено;
									
							ДлинаПути = 0;
							Для каждого КоординатыТочки Из ТочкиРасчета Цикл
								КонечнаяШирота   = КоординатыТочки.Широта;
								КонечнаяДолгота  = КоординатыТочки.Долгота;
								
								Путь = Путь + ПредставлениеКоординаты(КонечнаяШирота) + ";"	+ ПредставлениеКоординаты(КонечнаяДолгота) + ";";
								
								Если Истина
									И НачальнаяШирота <> Неопределено
									И НачальнаяДолгота <> Неопределено 
									И КонечнаяШирота <> Неопределено 
									И КонечнаяДолгота <> Неопределено
								Тогда
								     ДлинаПутиМетры = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
									Если Истина
										И ДлинаПутиМетры <> 0
										И ДлинаПутиМетры <= КритерийБлизостиТочекВМетрах
									Тогда
										ДлинаПути = ДлинаПути + ДлинаПутиМетры;
									КонецЕсли;
								КонецЕсли;
								НачальнаяШирота   = КонечнаяШирота;
								НачальнаяДолгота  = КонечнаяДолгота;
							КонецЦикла;
														
							ДлинаМаршрута = ДлинаПути / 1000;
							ВремяМаршрута = 6 * ДлинаПути / 100; // 3600 * ДлинаПути / 1000 / 60
							ВремяМаршрутаПробки = ВремяМаршрута;

						КонецЕсли;
												
						Структура.Расстояние = ДлинаМаршрута;
						Структура.Время      = ВремяМаршрута;
						Структура.Путь       = ?(ЗначениеЗаполнено(Текст), Текст, Путь);
						Структура.ВремяПонедельник = ВремяМаршрутаПробки;
						Структура.ВремяВторник     = ВремяМаршрутаПробки;
						Структура.ВремяСреда       = ВремяМаршрутаПробки;
						Структура.ВремяЧетверг     = ВремяМаршрутаПробки;
						Структура.ВремяПятница     = ВремяМаршрутаПробки;
						Структура.ВремяСуббота     = ВремяМаршрутаПробки;
						Структура.ВремяВоскресенье = ВремяМаршрутаПробки;
						
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				СообщениеОбОшибке = "Яндекс.Маршрутизация: Матрица расстояний - error: Ответ от сервера маршрутизации не получен. " + сткКарта.Соединение;
				текОшибки = Истина;
			КонецЕсли;
		Иначе		
			Если РезультатПолученияФайлов.Свойство("СообщениеОбОшибке") И НЕ ПустаяСтрока(РезультатПолученияФайлов.СообщениеОбОшибке)Тогда
				СообщениеОбОшибке = РезультатПолученияФайлов.СообщениеОбОшибке;
				текОшибки = Истина;
			КонецЕсли;		
		КонецЕсли;
		
	Исключение
		СообщениеОбОшибке = URLСтрока + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		текОшибки = Истина;
	КонецПопытки;
	
	Результат = Неопределено;
	Если текОшибки Тогда
		Результат = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");	
		Результат.Вставить("Ошибка",СообщениеОбОшибке);
		Результат.Вставить("СообщениеОбОшибке",СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний Яндекс'"), УровеньЖурналаРегистрации.Ошибка,,, СообщениеОбОшибке);
	Иначе		
		Результат = Структура;
	КонецЕсли;
	
	ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,НЕ текОшибки,Результат);
	
	Возврат Результат;
	
КонецФункции // ПолучитьДанныеЯндексМаршрутизация()

// Выполним форматирование даты.
//
// Параметры:
//  Период  - Дата - ДатаВремя.
//
// Возвращаемое значение:
//   Строка   - Текстовое представление дня недели.
//
Функция ПолучитьДеньНедели(Период)
	
	Возврат Формат(Период,"ДФ=dddd");
	
КонецФункции // ПолучитьДеньНедели()

// Получим расчет данных СитиГИД.
//
// Параметры:
//	сткКарта      - структура - ключи парамтры загрузки карты - Координата широты начало маршрута.
//	ТочкиРассчета - Массив    - точек рассчета.
//
// Возвращаемое значение:
//   Структура   - Ключи: 
//	 * Путь       - Строка - lat;lon;lat;lon;lat;lon….
//    * Расстояние - Число - расстояние маршрута в км.
//    * Время      - Число - длительность маршрут в сек.
//
Функция ПолучитьДанныеСитиГИД(сткКарта, ТочкиРассчета, ПакетныйРежим = Ложь, ДатаРасчета = Неопределено)
	
	Если ДатаРасчета = Неопределено Тогда
		// Подготовим даты за которые требуется выполнять запросы,
		//	в данном запросе это на неделю назад от текущей даты на 12:00 дня.
		ДатаРасчета = Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMdd");
	КонецЕсли; 
	Период = Дата(ДатаРасчета+"120000");
	
	Периоды = Новый Массив();	
	
	ТекущееОграничение = Перечисления.упОграниченияМаршрутизаторов.ПустаяСсылка();
	Если сткКарта.Свойство("Ограничение") Тогда
		ТекущееОграничение = сткКарта.Ограничение;
	КонецЕсли;
	
	Если сткКарта.Свойство("ДатаНачалаПланирования") Тогда
		Если сткКарта.ДатаНачалаПланирования = Неопределено Тогда
			сткКарта.ДатаНачалаПланирования = Период;
		Иначе
			Период = сткКарта.ДатаНачалаПланирования;
		КонецЕсли;		
	КонецЕсли;
	Периоды.Добавить(Новый Структура("Дата,ДеньНедели", Период, ПолучитьДеньНедели(Период)));
	
	УстанавливатьПараметры = сткКарта.Свойство("ПараметрыМаршрутизацииСитиГИД");
	
	// Заполняем Маршрут точки маршрута для анализа.
	Если Не ПакетныйРежим Тогда
		ПарыТочек = Новый Массив;
		ПарыТочек.Добавить(ТочкиРассчета);
	Иначе
		ПарыТочек = ТочкиРассчета;
	КонецЕсли; 
	
	мсвМаршрут = Новый Массив;
	Маршруты   = Новый Массив;
	
	Для Каждого ПараТочек Из ПарыТочек Цикл
		
		мсвПуть = Новый Массив;
		Для каждого мсвЭлемент Из ПараТочек Цикл
			сткТочка = Новый Структура;
			сткТочка.Вставить("latitude",мсвЭлемент.Широта);
			сткТочка.Вставить("longitude",мсвЭлемент.Долгота);
			мсвПуть.Добавить(сткТочка);		
		КонецЦикла;
		singleRouteRequest = Новый Структура("Points", мсвПуть);
		мсвМаршрут.Добавить(singleRouteRequest);
		РассчитанныйМаршрут = СтруктураРассчитанногоСервисомМаршрута();
		Маршруты.Добавить(РассчитанныйМаршрут);
		
	КонецЦикла;
	
	НомерПериода = 0;
	ПараметрыПодключения = сткКарта.Параметры;
	Для каждого текПериод Из Периоды Цикл
		НомерПериода = НомерПериода + 1;
		Если ЗначениеЗаполнено(текПериод) И НЕ УстанавливатьПараметры Тогда	
			// когда дата для анализа установлена.
			ПараметрыПодключения.parameters.useJams      = Истина;
			ПараметрыПодключения.parameters.useLimitsUpd = Истина;
			ПараметрыПодключения.parameters.useForecast  = Истина;
			ПараметрыПодключения.parameters.forecastDateTimeUtc = текПериод.Дата;
			// Устанавливаем параметры, для учета пробок и использовании статистики при расчетах.
			ПараметрыПодключения.getPoints = Ложь;
			// набор точек при этом не получаем нужно только время в пути.
		ИначеЕсли ЗначениеЗаполнено(текПериод) И УстанавливатьПараметры Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыПодключения.parameters,сткКарта.ПараметрыМаршрутизацииСитиГИД);
			ПараметрыПодключения.parameters.forecastDateTimeUtc = текПериод.Дата;
			ПараметрыПодключения.getPoints = ?(НомерПериода=1,Истина,Ложь);
		КонецЕсли;
		ПараметрыПодключения.Вставить("routes", мсвМаршрут);
		ЗаписьJSON  = Новый ЗаписьJSON;
		ФайлЗапроса = сткКарта.ФайлЗапроса;
		ЗаписьJSON.ОткрытьФайл(ФайлЗапроса,,,Новый ПараметрыЗаписиJSON(,Символы.Таб));
		// формируем файл формата JSON для запроса к сервису.
		НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON();
		НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
		НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
		ЗаписатьJSON(ЗаписьJSON,ПараметрыПодключения,НастройкиСериализацииJSON);
		
		ЗаписьJSON.Закрыть();
		
		Если ПолучитьФункциональнуюОпцию("упЛогироватьОбращенияКСервисуМаршрутизацииСитиГИД") Тогда
			ЛогУИД = Строка(Новый УникальныйИдентификатор());
			Файл = Новый ЧтениеТекста(ФайлЗапроса);
			ТекстФайла = Файл.Прочитать();
			Файл.Закрыть();
			ЗаписьЖурналаРегистрации("ЗапросСитиГИД",,,, СтрШаблон("%1:%2%3", ЛогУИД, Символы.ПС, СтрЗаменить(ТекстФайла, ПараметрыПодключения.accessKey, "***********"))); // засекретим пароль 
		КонецЕсли; 
		
		Ресурс = упСервисныеФункции.ПолучитьЗначениеКонстанты("упРесурсСитиГИД");
		
		HTTP =  сткКарта.HTTP;
		
		ФайлРезультата = сткКарта.ФайлРезультата;
		ЗаголовокHTTP  = сткКарта.ЗаголовокHTTP;
		
		ЕстьОшибкиВызова = Ложь;
		
		Попытка
			ВремяНачала = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени();
			HTTP.ОтправитьДляОбработки(ФайлЗапроса, Ресурс, ФайлРезультата, ЗаголовокHTTP);
			ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(Справочники.КлючевыеОперации.РасчетМаршрутаСитиГИД, ВремяНачала);
		Исключение
			СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЕстьОшибкиВызова  = Истина;
			Прервать;
		КонецПопытки;		
		
		Если ПолучитьФункциональнуюОпцию("упЛогироватьОбращенияКСервисуМаршрутизацииСитиГИД") Тогда
			Файл = Новый ЧтениеТекста(ФайлРезультата);
			ТекстФайла = Файл.Прочитать();
			Файл.Закрыть();
			ЗаписьЖурналаРегистрации("ОтветСитиГИД",,,, СтрШаблон("%1:%2%3", ЛогУИД, Символы.ПС, ТекстФайла));  
		КонецЕсли; 
		
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.ОткрытьФайл(ФайлРезультата);
		ОтветJSON = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		УдалитьФайлы(ФайлЗапроса);
		УдалитьФайлы(ФайлРезультата);
		Если ТипЗнч(ОтветJSON) = ТИП("Структура") Тогда
			Если ОтветJSON.Свойство("errorMessage") Тогда
				Если ЗначениеЗаполнено(ОтветJSON.errorMessage) Тогда
					СообщениеОбОшибке = "СитиГИД - errorMessage: " + ОтветJSON.errorMessage;
					ЕстьОшибкиВызова = Истина;
				Иначе
					Если ТипЗнч(ОтветJSON.features) = ТИП("Массив") Тогда
						Если ПараметрыПодключения.getPoints Тогда
							Текст = "";
							ДлинаМаршрута = 0;
							ВремяМаршрута = 0;
							ИндексМаршрута = 0;
							Для каждого мсвЭлемент Из ОтветJSON.features Цикл
								РассчитанныйМаршрут = Маршруты[ИндексМаршрута];
								ИндексМаршрута = ИндексМаршрута + 1;
								Текст = "";
								ДлинаМаршрута = 0;
								ВремяМаршрута = 0;
								ДлинаПути = мсвЭлемент.properties.totalDistance; // в метрах.
								ВремяПути  = мсвЭлемент.properties.totalTime; // в секундах.
								мсвКоординаты = мсвЭлемент.geometry.coordinates; // массив точек.
								Если Истина
									И СтрСравнить(мсвЭлемент.properties.result, "Ok") <> 0 
									//И СтрСравнить(мсвЭлемент.properties.result, "Restricted by limits") <> 0 
									Тогда
									// Ok - расчет произведен успешно.
									// Unknown error — внутренняя ошибка.
									// No route — точки маршрута находятся на изолированных друг от друга участках.
									// Same ends — старт и финиш маршрута совпадают.
									// Too far from roads — не удалось привязать точки к дорожному графу.
									// Restricted by limits — точки маршрута попадают в закрытую файлами ограничений зону. Время и дистанция посчитаны для движения по локсодроме со скоростью 60 км/ч.
									РассчитанныйМаршрут.Вставить("Ошибка", мсвЭлемент.properties.result);
									РассчитанныйМаршрут.Вставить("ПытатьсяРассчитатьСнова", Ложь);
								КонецЕсли; 
								
								Путь = "";
								
								Если Истина
									И ПолучитьФункциональнуюОпцию("упПрямойРасчетГеографическиБлизкихТочекПриНеудаче")
									И (Ложь 
									Или (Истина
									И ДлинаПути = 0
									И СтрСравнить(мсвЭлемент.properties.result, "Ok") = 0)
									Или СтрСравнить(мсвЭлемент.properties.result, "Too far from roads") = 0)
									Тогда
									КритерийБлизостиТочекВМетрах = ПолучитьФункциональнуюОпцию("упКритерийБлизостиТочекВМетрах");
									мсвРасчетныеМаршруты = ПараметрыПодключения.routes;
									НачальнаяШирота  = Неопределено;
									НачальнаяДолгота = Неопределено;
									КонечнаяШирота   = Неопределено;
									КонечнаяДолгота  = Неопределено;
									
									ДлинаПути = 0;
									Для каждого РасчетныйМаршрут Из мсвРасчетныеМаршруты Цикл
										мсвТочки = РасчетныйМаршрут.Points;
										Для каждого КоординатыТочки Из мсвТочки Цикл
											КонечнаяШирота   = ПредставлениеКоординатыВЧисло(КоординатыТочки.latitude);
											КонечнаяДолгота  = ПредставлениеКоординатыВЧисло(КоординатыТочки.longitude);
											
											Путь = Путь + КоординатыТочки.latitude + ";" + КоординатыТочки.longitude + ";";
											
											Если Истина
												И НачальнаяШирота <> Неопределено
												И НачальнаяДолгота <> Неопределено 
												И КонечнаяШирота <> Неопределено 
												И КонечнаяДолгота <> Неопределено
												Тогда
												ДлинаПутиМетры = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(НачальнаяШирота, НачальнаяДолгота, КонечнаяШирота, КонечнаяДолгота);
												Если Истина
													И ДлинаПутиМетры <> 0
													И ДлинаПутиМетры <= КритерийБлизостиТочекВМетрах
													Тогда
													ДлинаПути = ДлинаПути + ДлинаПутиМетры;
												КонецЕсли;
											КонецЕсли;
											НачальнаяШирота   = КонечнаяШирота;
											НачальнаяДолгота  = КонечнаяДолгота;
										КонецЦикла;
									КонецЦикла;
									
									Если ДлинаПути <> 0 Тогда
										РассчитанныйМаршрут.Удалить("Ошибка");
									КонецЕсли;
									
									ВремяПути = 6 * ДлинаПути / 100;// 3600 * ДлинаПути / 1000 / 60
									
								КонецЕсли;
								
								ДлинаМаршрута = ДлинаПути / 1000;
								ВремяМаршрута = ВремяПути;
								
								Если НЕ мсвКоординаты = Неопределено Тогда
									МассивКоординат = Новый Массив;
									Если мсвКоординаты.Количество() < 1000 Тогда
										Для каждого текКординаты Из мсвКоординаты Цикл 
											МассивКоординат.Добавить(ПредставлениеКоординаты(текКординаты[1]));
											МассивКоординат.Добавить(ПредставлениеКоординаты(текКординаты[0]));
										КонецЦикла;
										Текст = СтрСоединить(МассивКоординат, ";");
									Иначе
										Для каждого текКординаты Из мсвКоординаты Цикл
											сткКоориднаты = Новый Структура("Широта,Долгота",текКординаты[1],текКординаты[0]);
											МассивКоординат.Добавить(сткКоориднаты);
										КонецЦикла;
										СтруктураУпрощения = ПолучитьУпрощенныеКоординаты(МассивКоординат);
										Текст = ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(СтруктураУпрощения.Координаты);
									КонецЕсли; 
								КонецЕсли;
								РассчитанныйМаршрут.Расстояние = ДлинаМаршрута;
								РассчитанныйМаршрут.Время = ВремяМаршрута;
								РассчитанныйМаршрут.Путь  = ?(ЗначениеЗаполнено(Текст), Текст, Путь);
								Если ЗначениеЗаполнено(текПериод) Тогда
									РассчитанныйМаршрут.Вставить("Время" + текПериод.ДеньНедели, ВремяМаршрута);
								КонецЕсли;
								РассчитанныйМаршрут.ВремяПонедельник = РассчитанныйМаршрут.Время;
								РассчитанныйМаршрут.ВремяВторник     = РассчитанныйМаршрут.Время;
								РассчитанныйМаршрут.ВремяСреда       = РассчитанныйМаршрут.Время;
								РассчитанныйМаршрут.ВремяЧетверг     = РассчитанныйМаршрут.Время;
								РассчитанныйМаршрут.ВремяПятница     = РассчитанныйМаршрут.Время;
								РассчитанныйМаршрут.ВремяСуббота     = РассчитанныйМаршрут.Время;
								РассчитанныйМаршрут.ВремяВоскресенье = РассчитанныйМаршрут.Время;
							КонецЦикла;
						Иначе
							ИндексМаршрута = 0;
							Для каждого мсвЭлемент Из ОтветJSON.features Цикл
								РассчитанныйМаршрут = Маршруты[ИндексМаршрута];
								РассчитанныйМаршрут.Вставить("Время" + текПериод.ДеньНедели, ВремяМаршрута);
								ИндексМаршрута = ИндексМаршрута + 1;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера маршрутизации не получен. " + Строка(HTTP.Сервер);
				ЕстьОшибкиВызова = Истина;
			КонецЕсли;
		Иначе
			СообщениеОбОшибке = "СитиГИД - errorMessage: Ответ от сервера маршрутизации не получен. " + Строка(HTTP.Сервер);
			ЕстьОшибкиВызова = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Результат = Неопределено;
	Если ПакетныйРежим Тогда
		Результат = Новый Структура("Маршруты", Маршруты);
	КонецЕсли; 
	Если ЕстьОшибкиВызова Тогда
		Если Не ПакетныйРежим Тогда
			Результат = СтруктураРассчитанногоСервисомМаршрута();
		КонецЕсли; 
		Результат.Вставить("Ошибка", СообщениеОбОшибке);
		Результат.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
		Результат.Вставить("Ограничение", ТекущееОграничение);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Расчет расстояний СитиГИД'"), УровеньЖурналаРегистрации.Ошибка,,, СообщениеОбОшибке);
	Иначе
		Если Не ПакетныйРежим Тогда
			Результат = Маршруты[0];
		КонецЕсли; 
	КонецЕсли;
	КоличествоРасстояний = ПарыТочек.Количество() * Периоды.Количество();
	ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора, НЕ ЕстьОшибкиВызова, Результат, КоличествоРасстояний);
	Возврат Результат;
	
КонецФункции

Функция СтруктураРассчитанногоСервисомМаршрута()
	
	РассчитанныйМаршрут = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, Путь", 0, 0, 0, 0, 0, 0, 0, 0, 0, "");
	//РассчитанныйМаршрут.Вставить("Ограничение", ТекущееОграничение);
	РассчитанныйМаршрут.Вставить("ПытатьсяРассчитатьСнова", Истина);
	Возврат РассчитанныйМаршрут;
	
КонецФункции // ПолучитьДанныеСитиГИД()

// Создадим таблицу для обработки данных полученных от сервиса маршрутизации.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица в которую поместим данные от сервиса маршрутизации.
//
Функция ПолучитьКаркасТаблицыВремениМеждуТочками()
	
	тзВремяМеждуТочками = Новый ТаблицаЗначений;
	тзВремяМеждуТочками.Колонки.Добавить("Номер",   Новый ОписаниеТипов("Число"));
	тзВремяМеждуТочками.Колонки.Добавить("Широта",  Новый ОписаниеТипов("Число"));
	тзВремяМеждуТочками.Колонки.Добавить("Долгота", Новый ОписаниеТипов("Число"));
	тзВремяМеждуТочками.Колонки.Добавить("Время",   Новый ОписаниеТипов("Число"));
	тзВремяМеждуТочками.Колонки.Добавить("Период",  Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	
	Возврат тзВремяМеждуТочками;
	
КонецФункции // ПолучитьКаркасТаблицыВремениМеждуТочками()

// Выполним обработку полученных данных.
//
// Параметры:
//  ТаблицаДанных  - ТаблицаЗначений - Данные которые нужно обработать.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица содержит вариант упрощения.
//
Функция ВыполнитьОбработкуДанныхТаблицыВремениМеждуТочками(ТаблицаДанных)	
	
	ТекущаяТаблица = ТаблицаДанных.Скопировать();
	
	МассивКоординат = Новый Массив;
	
	Для каждого тбзТекущаяСтрока Из ТекущаяТаблица Цикл			
		Структура = Новый Структура("Номер,Широта,Долгота,Период",0,0,0,ДАТА("00010101000000"));
		ЗаполнитьЗначенияСвойств(Структура,тбзТекущаяСтрока);
		МассивКоординат.Добавить(Структура);
	КонецЦикла;
	
	СтруктураУпрощенния = ПолучитьУпрощенныеКоординаты(МассивКоординат);
	
	Если СтруктураУпрощенния.Упрощение Тогда
		ТекущаяТаблица.Очистить();
		Счетчик = 0;
		текОтметкаВремени = Неопределено;
		Для каждого ТекущийЭлемент Из СтруктураУпрощенния.Координаты Цикл			
			Счетчик = Счетчик+1;
			НоваяСтрока = ТекущаяТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекущийЭлемент);
			НоваяСтрока.Номер = Счетчик;
			НоваяСтрока.Время = 0;
			Если НЕ текОтметкаВремени = Неопределено И ЗначениеЗаполнено(текОтметкаВремени) Тогда
				НоваяСтрока.Время = ТекущийЭлемент.Период - текОтметкаВремени;
			КонецЕсли;	
			текОтметкаВремени = ТекущийЭлемент.Период;
		КонецЦикла;			
	КонецЕсли;
	
	Возврат ТекущаяТаблица;
	
КонецФункции // ВыполнитьОбработкуДанныхТаблицыВремениМеждуТочками()

// Обновить простой путь в строке расстояния
Процедура ОбновитьПростойПутьВСтрокеРасстояния(Знач МенеджерЗаписи) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    МенеджерЗаписи = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
	КонецЕсли;
	СколькоТочекНаКилометрМакс = 5; // Сильно влияет на длительность расчета правил
	МаксКоличествоТочек = Макс(200, Цел(СколькоТочекНаКилометрМакс * МенеджерЗаписи.Расстояние));
	МассивНовыхТочек = упРаботаСКартами.ВыполнитьУпрощениеПолигональнойЦепи(МенеджерЗаписи.Путь, , МаксКоличествоТочек);
	МенеджерЗаписи.ПутьПростой = ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(МассивНовыхТочек);

КонецПроцедуры

// Получим путь из таблицы данных.
//
// Параметры:
//  ТаблицаДанных  - ТаблицаЗначений - Данный из которых получаем результирующий путь.
//
// Возвращаемое значение:
//   Строка   - Координаты маршрута "Широта;Долгота".
//
Функция ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(ТаблицаДанных)
	
	Массив = Новый Массив;
	Для каждого тбзТекущаяСтрока Из ТаблицаДанных Цикл
		Массив.Добавить(ПредставлениеКоординаты(тбзТекущаяСтрока.Широта));
		Массив.Добавить(ПредставлениеКоординаты(тбзТекущаяСтрока.Долгота));
	КонецЦикла;
	
	СтрокаКоординат = СтрСоединить(Массив,";");
	
	Массив = Неопределено;
	ТаблицаДанных = Неопределено;
	
	Возврат СтрокаКоординат;
	
КонецФункции // ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками()

// Получим расчет данных Graphhopper.
//
// Параметры:
//	сткКарта  - структура - ключи парамтры загрузки карты - Координата широты начало маршрута.
//  URLСтрока - Строка - HTTP запрос к сервису.
//
// Возвращаемое значение:
//   Структура   - Ключи:
//					Путь		  - Строка - lat;lon;lat;lon;lat;lon….
//					Расстояние - Число - расстояние маршрута в км.
//					Время	  - Число - длительность маршрут в сек.
//
Функция ПолучитьДанныеСервисаGraphhopper(сткКарта, URLСтрока)
	
	СообщениеОбОшибке = "";
	СтрокаКоординат   = ""; // координаты пути.
	ДлинаМаршрута	   = 0;   // расстояние, длина маршрута.
	
	сткРезультат = Новый Структура();
	
	Если Истина
		И ПолучитьФункциональнуюОпцию("упПрямойРасчетГеографическиБлизкихТочекПриНеудаче")
		И ПолучитьФункциональнуюОпцию("упПорядокПрямогоРасчетаГеографическиБлизкихТочек") = Перечисления.упПорядокПрямогоРасчетаГеографическиБлизкихТочек.Всегда
	Тогда
		КритерийБлизостиТочекВМетрах = ПолучитьФункциональнуюОпцию("упКритерийБлизостиТочекВМетрах");
		ДлинаМаршрутаМетры = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(сткКарта.НачальнаяШирота, сткКарта.НачальнаяДолгота, сткКарта.КонечнаяШирота, сткКарта.КонечнаяДолгота);
				
		Если Истина
			И ДлинаМаршрутаМетры <> 0
			И ДлинаМаршрутаМетры <= КритерийБлизостиТочекВМетрах
		Тогда
		
			ДлинаМаршрута = ДлинаМаршрутаМетры / 1000;
			
			сткРезультат.Вставить("Расстояние", ДлинаМаршрута);
			
			Время = 3600 * ДлинаМаршрута / 60; // приближенное время из расчета, что скорость движения составляет 60 км/ч
			
			сткРезультат.Вставить("Время",            Время);
			сткРезультат.Вставить("ВремяПонедельник", Время);
			сткРезультат.Вставить("ВремяВторник",     Время);
			сткРезультат.Вставить("ВремяСреда",       Время);
			сткРезультат.Вставить("ВремяЧетверг",     Время);
			сткРезультат.Вставить("ВремяПятница",     Время);
			сткРезультат.Вставить("ВремяСуббота",     Время);
			сткРезультат.Вставить("ВремяВоскресенье", Время);
			Путь = ПредставлениеКоординаты(сткКарта.НачальнаяШирота) + ";" 
					+ ПредставлениеКоординаты(сткКарта.НачальнаяДолгота) + ";" 
					+ ПредставлениеКоординаты(сткКарта.КонечнаяШирота) + ";" 
					+ ПредставлениеКоординаты(сткКарта.КонечнаяДолгота) + ";";
			
			сткРезультат.Вставить("Путь", Путь);
			
			Возврат сткРезультат;
			
		КонецЕсли;
     КонецЕсли;
	
	ДлинаМаршрута = 0;
	Сч = 1;
	
	тзВремяМеждуТочками = ПолучитьКаркасТаблицыВремениМеждуТочками();
	
	текОтметкаВремени = Неопределено;
	
	ПараметрыПолучения = ?(сткКарта.Свойство("ПараметрыПолучения"),сткКарта.ПараметрыПолучения,Неопределено);

	Попытка
		
		ВремяНачала = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени();
		РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока,ПараметрыПолучения);
		Если сткКарта.ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps Тогда
			ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(Справочники.КлючевыеОперации.РасчетМаршрутаAxelotMaps, ВремяНачала);
		КонецЕсли; 
		
		ЗаписатьРезультатОбращенияКСервисуМаршрутизации(сткКарта.ТипМаршрутизатора,РезультатПолученияФайлов.Статус,РезультатПолученияФайлов);
		
		Если РезультатПолученияФайлов.Статус Тогда
			
			ЧтениеXML = Новый ЧтениеXML;
			ЧтениеXML.ОткрытьФайл(РезультатПолученияФайлов.Путь);		
			
			extensions = Ложь; // инструкции маршрута.
			trkpt      = Ложь;
			
			Пока ЧтениеXML.Прочитать() Цикл
				
				Если ЧтениеXML.Имя = "trkpt" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
					
					trkpt = Истина;
					НоваяСтрока = тзВремяМеждуТочками.Добавить();
					НоваяСтрока.Номер = Сч;
					Сч = Сч + 1;
					СтруктураКоординат = Новый Структура;
					Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
						Если ЧтениеXML.Имя = "lat" Тогда
							НоваяСтрока.Широта = ЧтениеXML.Значение;
						КонецЕсли;
						Если ЧтениеXML.Имя = "lon" Тогда
							НоваяСтрока.Долгота = ЧтениеXML.Значение;
						КонецЕсли;
					КонецЦикла;
					
				ИначеЕсли trkpt И ЧтениеXML.Имя = "time" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
					
					Если ЧтениеXML.Прочитать() Тогда
						новОтметкаВремени = XMLЗначение(Тип("Дата"), ЧтениеXML.Значение);
						Если текОтметкаВремени = Неопределено Тогда
							НоваяСтрока.Время = 0;
							НоваяСтрока.Период = ДАТА("00010101000000");
						Иначе
							НоваяСтрока.Время = новОтметкаВремени - текОтметкаВремени;
							НоваяСтрока.Период = новОтметкаВремени;
						КонецЕсли;	
						текОтметкаВремени = новОтметкаВремени;
					КонецЕсли;	
					
				ИначеЕсли ЧтениеXML.Имя = "extensions" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
					
					extensions = Истина;
					trkpt = Ложь;
					
				ИначеЕсли extensions И (ЧтениеXML.Имя = "gh:distance" Или ЧтениеXML.Имя = "distance") И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда	
					
					
					Если ЧтениеXML.Прочитать() Тогда
						ДлинаМаршрута = ДлинаМаршрута + XMLЗначение(Тип("Число"), ЧтениеXML.Значение); // в м.
					КонецЕсли;
					
				ИначеЕсли ЧтениеXML.Имя = "extensions" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
					extensions = Ложь;
					
				КонецЕсли;					
				
			КонецЦикла;
			ЧтениеXML.Закрыть();
		Иначе		
			Если РезультатПолученияФайлов.Свойство("СообщениеОбОшибке") И НЕ ПустаяСтрока(РезультатПолученияФайлов.СообщениеОбОшибке)Тогда
				СообщениеОбОшибке = РезультатПолученияФайлов.СообщениеОбОшибке;
			КонецЕсли;		
		КонецЕсли;
		
	Исключение
		СообщениеОбОшибке = URLСтрока + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
		
	ТекстОшибки  = "";
	РасстояниеРасчитано = ДлинаМаршрута <> 0;
	ЕстьОшибки   = НЕ ПустаяСтрока(СообщениеОбОшибке);
	
	Если НЕ ЕстьОшибки Тогда
		
		Если РасстояниеРасчитано Тогда
			упУчитыватьКоэффициентыРасчетаВремениВПути = ПолучитьФункциональнуюОпцию("упУчитыватьКоэффициентыРасчетаВремениВПути");
			Если упУчитыватьКоэффициентыРасчетаВремениВПути Тогда
				тзВремяМеждуТочками = ВыполнитьОбработкуДанныхТаблицыВремениМеждуТочками(тзВремяМеждуТочками);
			КонецЕсли;
			//Если упУчитыватьКоэффициентыРасчетаВремениВПути И тзВремяМеждуТочками.Количество() < 1000 Тогда
			//	сткРезультат = Справочники.упГеографическиеЗоны.ВернутьВремяВПутиПоДнямНедели(тзВремяМеждуТочками);
			//Иначе
				ВремяМеждуАдресами = тзВремяМеждуТочками.Итог("Время");
				сткРезультат.Вставить("Время"           , ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяПонедельник", ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяВторник"    , ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяСреда"      , ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяЧетверг"    , ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяПятница"    , ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяСуббота"    , ВремяМеждуАдресами);
				сткРезультат.Вставить("ВремяВоскресенье", ВремяМеждуАдресами);
			//КонецЕсли;
			
			СтрокаКоординат = ПолучитьСтрокуКоординатИзТаблицыВремениМеждуТочками(тзВремяМеждуТочками);
			
			сткРезультат.Вставить("Путь",       СтрокаКоординат);
			сткРезультат.Вставить("Расстояние", ?(ДлинаМаршрута = 0, 0, ДлинаМаршрута / 1000));

		Иначе	
			
			Если ПолучитьФункциональнуюОпцию("упПрямойРасчетГеографическиБлизкихТочекПриНеудаче") Тогда
				
				КритерийБлизостиТочекВМетрах = ПолучитьФункциональнуюОпцию("упКритерийБлизостиТочекВМетрах");
				ДлинаМаршрутаМетры = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(сткКарта.НачальнаяШирота, сткКарта.НачальнаяДолгота, сткКарта.КонечнаяШирота, сткКарта.КонечнаяДолгота);
				
				Если Истина
					И ДлинаМаршрутаМетры <> 0
					И ДлинаМаршрутаМетры <= КритерийБлизостиТочекВМетрах
				Тогда
					ДлинаМаршрута = ДлинаМаршрутаМетры / 1000;
					сткРезультат.Вставить("Расстояние", ДлинаМаршрута);
					Время = 3600 * ДлинаМаршрута / 60; // приближенное время из расчета, что скорость движения составляет 60 км/ч
					сткРезультат.Вставить("Время",            Время);
					сткРезультат.Вставить("ВремяПонедельник", Время);
					сткРезультат.Вставить("ВремяВторник",     Время);
					сткРезультат.Вставить("ВремяСреда",       Время);
					сткРезультат.Вставить("ВремяЧетверг",     Время);
					сткРезультат.Вставить("ВремяПятница",     Время);
					сткРезультат.Вставить("ВремяСуббота",     Время);
					сткРезультат.Вставить("ВремяВоскресенье", Время);
					Путь = ПредставлениеКоординаты(сткКарта.НачальнаяШирота) + ";" 
							+ ПредставлениеКоординаты(сткКарта.НачальнаяДолгота) + ";" 
							+ ПредставлениеКоординаты(сткКарта.КонечнаяШирота) + ";" 
							+ ПредставлениеКоординаты(сткКарта.КонечнаяДолгота) + ";";
					сткРезультат.Вставить("Путь", Путь);
				Иначе	
					ЕстьОшибки  = Истина;
				КонецЕсли;
			Иначе	
				ЕстьОшибки  = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
			
		Если РасстояниеРасчитано Тогда
			ТекстОшибки = СообщениеОбОшибке;
		Иначе	
			ТекстТекущейОшибки = "";
			Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
				ТекстТекущейОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '. Текст текущей ошибки: %1'"), СообщениеОбОшибке);
			КонецЕсли;
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Graphhopper: Не удалось рассчитать расстояние между ""%1"" и ""%2""%3'"),
																	сткКарта.СтрКоординаты1,
																	сткКарта.СтрКоординаты2,
																	ТекстТекущейОшибки);
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации("Расчет расстояний Graphhopper", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		сткРезультат.Вставить("Ошибка", ТекстОшибки);
		
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции // ПолучитьДанныеСервисаGraphhopper()

// Выполнить подготовку данных полученных от сервиса геокодирования.
// если точек много уменьшить их количество.
//
// Параметры:
//  СтруктураДанных  - Структура - Отчет от сервиса геокодирования.
//
// Возвращаемое значение:
//   Массив   - Структур ключи долгота и широта, координаты полигона.
//
Функция ПолучитьУпрощенныеКоординаты(МассивКоординат)	
	
	КоличествоТочекПосле = 0;
	КоличествоТочекДо    = МассивКоординат.Количество();	
	Результат            = Неопределено;
	
	Если КоличествоТочекДо <= 1000 Тогда
		// в том случае когда точек меньше 1000 не имеет смысла проводить упрощение.
	Иначе
		Отклонение = 0.0005;	
		Результат = упРаботаСКартами.ВыполнитьУпрощениеПолигональнойЦепи(МассивКоординат, Отклонение);
	КонецЕсли;	
	
	Если Результат = Неопределено Тогда
		Результат = МассивКоординат;
	Иначе
		КоличествоТочекПосле = Результат.Количество();
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Координаты",Результат);
	Структура.Вставить("КоличествоТочекДо",КоличествоТочекДо);
	Структура.Вставить("КоличествоТочекПосле",КоличествоТочекПосле);
	Структура.Вставить("Упрощение",?(КоличествоТочекПосле=0, Ложь, Истина));
	
	Возврат Структура;
	
КонецФункции // ПолучитьУпрощенныеКоординаты()

// Получим адрес к серверу Axelot.
//
// Параметры:
//  ЭтоМаршрутизатор - Булево - Флаг выполняется получения параметров маршрутизатора.
//
// Возвращаемое значение:
//   Структура - Ключи параметры подключения.
//
Функция ПолучитьПараметрыПодключенияAxelotMaps(ЭтоМаршрутизатор = Ложь) Экспорт
	
	Результат = Новый Структура;
	Если ЭтоМаршрутизатор Тогда
		Результат.Вставить("АдресСервера","http://maps.axelot.ru:8989/");
	Иначе
		//Результат.Вставить("КаталогБиблиотек","https://maps.axelot.ru/leaflet/");
		Результат.Вставить("КаталогБиблиотек","https://maps.axelot.ru/leaflet");
		Результат.Вставить("АдресСервера"    ,"https://maps.axelot.ru/osm");
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // ПолучитьПараметрыПодключенияAxelotMaps()

// Получим общий макет для указанного типа карт в системе.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Строка - текст, макета.
//
Функция ПолучитьТекстОбщегоМакетаКартографическогоСервиса(ТолькоПроверка = Ложь) Экспорт
	
	ТипКарты = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипКарт");
	КлючAPI = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючЭлектронныхГеографическихКарт");
	АдресСервера = упСервисныеФункции.ПолучитьЗначениеКонстанты("упАдресСервераЭлектронныхКарт");
	Результат = ПолучитьТекстОбщегоМакетаКартографическогоСервисаСПараметрами(АдресСервера, КлючAPI, ТипКарты, ТолькоПроверка);
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекстОбщегоМакетаКартографическогоСервисаСПараметрами(Знач АдресСервера, Знач КлючAPI, Знач ТипКарты, Знач ТолькоПроверка = Ложь) Экспорт 
	
	ЗаменаAPI = ПолучитьОбщийМакет("JS_Axelot_API").ПолучитьТекст();
	Если упСервисныеФункции.ИспользуетсяWebKit() Тогда
		Результат = ПолучитьОбщийМакет("HTML_LeafletAPI_WebKit").ПолучитьТекст();
		ТекстРежимСовместимостиКарты = "";
	Иначе
		Результат = ПолучитьОбщийМакет("HTML_LeafletAPI").ПолучитьТекст();
		ТекстРежимСовместимостиКарты = "<meta http-equiv=""X-UA-Compatible"" content=""IE=9""/>";
	КонецЕсли;  
	Результат = СтрЗаменить(Результат, "%API%", ЗаменаAPI);
	
	Если ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.AxelotMaps") Или ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.OSM") Тогда
		Если ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.AxelotMaps") Тогда
			КлючAPI = упМаршрутизация.ПолучитьТекстКлючаAPI(КлючAPI, ТипКарты);
			ТекстПодключения_Карты = "%КлючAPI%, {maxZoom: 18,attribution: 'Map data &copy; <a href=""http://openstreetmap.org"">OpenStreetMap</a> contributors, Imagery © <a href=""http://axelot.ru"">Axelot</a>'}).addTo(map)";
			
			Структура = упМаршрутизация.ПолучитьПараметрыПодключенияAxelotMaps(); 
			//ПутьКLeaflet = Структура.КаталогБиблиотек;
			АдресСервера = Структура.АдресСервера;	
			
		ИначеЕсли ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.OSM") Тогда
			КлючAPI = упМаршрутизация.ПолучитьТекстКлючаAPI(КлючAPI, ТипКарты);
			ТекстПодключения_Карты = "%КлючAPI%, {maxZoom: 18, attribution: 'Map data &copy; <a href=""http://openstreetmap.org"">OpenStreetMap</a> contributors, Imagery © <a href=""http://cloudmade.com"">CloudMade</a>'}).addTo(map)";
			
		КонецЕсли;
		
		ТекстПодключения = "L.tileLayer('" + АдресСервера + КлючAPI + "/{z}/{x}/{y}.png'";
		Если ПустаяСтрока(АдресСервера) Тогда
			ТекстПодключения = "L.tileLayer('http://{s}.tile.openstreetmap.org" + КлючAPI + "/{z}/{x}/{y}.png'";
		КонецЕсли;	
		
		ТекстПодключения_Карты = СтрЗаменить(ТекстПодключения_Карты, "%КлючAPI%", ТекстПодключения);
		ТекстПодключения_Карты = ТекстПодключения_Карты + "; var osm2 = new " + СтрЗаменить(ТекстПодключения_Карты, ".addTo(map)", "") + "; var miniMap = new L.Control.MiniMap(osm2, {toggleDisplay: true,minimized: true}).addTo(map);";
		
		Результат = СтрЗаменить(Результат, "%КлючПодключенияКарты%", ТекстПодключения_Карты);
		Результат = СтрЗаменить(Результат, "%СкриптДополнительныхКарт%", "");
		Результат = СтрЗаменить(Результат, "%ПакетДополнительныхКарт%", "");
		Результат = СтрЗаменить(Результат, "%РежимСовместимостиКарты%", ТекстРежимСовместимостиКарты);
		
		ПлагинМиниКарты = ПолучитьОбщийМакет("JS_Leaflet_MiniMap").ПолучитьТекст();	
		Результат = СтрЗаменить(Результат, "src="".../dist/minimap-src.js"">", ">" + ПлагинМиниКарты);
		
	ИначеЕсли ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.СитиГИД") Тогда
		
		КлючAPI = упМаршрутизация.ПолучитьТекстКлючаAPI(КлючAPI, ТипКарты);
		
		// minZoom: 5 - добавлено для обхода признанных проблем поставщика сервиса с отрисовкой тайлов 
		ТекстПодключения_Карты = "L.tileLayer('" + АдресСервера + "/{z}/{x}/{y}', {minZoom: 5, maxZoom: 18,attribution: 'Map data &copy; <a href=""http://probki.net"">CityGuide</a>'}).addTo(map)";
		
		Результат = СтрЗаменить(Результат, "%КлючПодключенияКарты%", ТекстПодключения_Карты);
		Результат = СтрЗаменить(Результат, "%СкриптДополнительныхКарт%", "");
		Результат = СтрЗаменить(Результат, "%ПакетДополнительныхКарт%", "");
		Результат = СтрЗаменить(Результат, "%РежимСовместимостиКарты%", ТекстРежимСовместимостиКарты);
		
	ИначеЕсли ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.GoogleMaps") Тогда
		
		ТекстКлюча = упМаршрутизация.ПолучитьТекстКлючаAPI(КлючAPI, ТипКарты);	
		ТекстПодключения = "var ggl = new L.Google('ROADMAP');map.addLayer(ggl);";
		
		Результат = СтрЗаменить(Результат, "%КлючПодключенияКарты%", ТекстПодключения);
		
		СкриптGoogle = ПолучитьОбщийМакет("JS_GoogleLeafletAPI").ПолучитьТекст();
		
		Результат = СтрЗаменить(Результат, "%СкриптДополнительныхКарт%", СкриптGoogle);
		
		СкриптGoogleПакета = "<script type=""text/javascript"" src=""https://maps.googleapis.com/maps/api/js?v=3" + ТекстКлюча + "&region=RU""></script>";
		
		Результат = СтрЗаменить(Результат, "%ПакетДополнительныхКарт%", СкриптGoogleПакета);
		
		ТекстРежимаСовместимости = ТекстРежимСовместимостиКарты + "<script type=""text/javascript"">
		|if (!window.JSON) {
		|  window.JSON = {
		|    parse: function(sJSON) { return eval('(' + sJSON + ')'); },
		|    stringify: (function () {
		|      var toString = Object.prototype.toString;
		|      var isArray = Array.isArray || function (a) { return toString.call(a) === '[object Array]'; };
		|      var escMap = {'""': '\\""', '\\': '\\\\', '\b': '\\b', '\f': '\\f', '\n': '\\n', '\r': '\\r', '\t': '\\t'};
		|      var escFunc = function (m) { return escMap[m] || '\\u' + (m.charCodeAt(0) + 0x10000).toString(16).substr(1); };
		|      var escRE = /[\\""\u0000-\u001F\u2028\u2029]/g;
		|      return function stringify(value) {
		|        if (value == null) {
		|          return 'null';
		|        } else if (typeof value === 'number') {
		|          return isFinite(value) ? value.toString() : 'null';
		|        } else if (typeof value === 'boolean') {
		|          return value.toString();
		|        } else if (typeof value === 'object') {
		|          if (typeof value.toJSON === 'function') {
		|            return stringify(value.toJSON());
		|          } else if (isArray(value)) {
		|            var res = '[';
		|            for (var i = 0; i < value.length; i++)
		|              res += (i ? ', ' : '') + stringify(value[i]);
		|            return res + ']';
		|          } else if (toString.call(value) === '[object Object]') {
		|            var tmp = [];
		|            for (var k in value) {
		|              if (value.hasOwnProperty(k))
		|                tmp.push(stringify(k) + ': ' + stringify(value[k]));
		|            }
		|            return '{' + tmp.join(', ') + '}';
		|          }
		|        }
		|        return '""' + value.toString().replace(escRE, escFunc) + '""';
		|      };
		|    })()
		|  };
		|}
		|</script>
		|<meta http-equiv=""X-UA-Compatible"" content=""IE=edge""/>
		|<meta name=""viewport"" content=""initial-scale=1.0, user-scalable=no"" />
		|<style type=""text/css"">
		|    html { height: 100% }
		|    body { height: 100%; margin: 0; padding: 0 }
		|    #map_canvas { height: 100% }
		|</style>";
		
		Результат = СтрЗаменить(Результат, "%РежимСовместимостиКарты%", ТекстРежимаСовместимости);
		
	ИначеЕсли ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.ЯндексКарты") Тогда
		
		ТекстКлюча = упМаршрутизация.ПолучитьТекстКлючаAPI(КлючAPI, ТипКарты);
		//ТекстПодключения = "var YandexLeaflet = new L.Yandex('null', {traffic:false, opacity:1, overlay: false});ymaps.ready(function(){map.addLayer(YandexLeaflet);map_Load = true;});";
		ТекстПодключения = "var YandexLeaflet = new L.Yandex('null', {traffic:false, opacity:1, overlay: false});try{ymaps.ready(function(){map.addLayer(YandexLeaflet);map_Load = true;});} catch(e) {alert(e.name + ': ' + e.message);}";
		
		Результат = СтрЗаменить(Результат, "%КлючПодключенияКарты%", ТекстПодключения);
		
		СкриптЯндекс = ПолучитьОбщийМакет("JS_YandexLeafletAPI").ПолучитьТекст();
		Результат = СтрЗаменить(Результат, "%СкриптДополнительныхКарт%", СкриптЯндекс);
		
		ТекущийСервиса = "https://enterprise.api-maps.yandex.ru/2.1/?lang=ru_RU";
		ПроверяемыйКлюч = СтрЗаменить(НРег(ТекстКлюча),НРег("&apikey="),"");
		Если ПустаяСтрока(ПроверяемыйКлюч) Тогда
			ТекущийСервиса = "https://api-maps.yandex.ru/2.1/?lang=ru_RU";
			ТекстКлюча = "";
		КонецЕсли;
		Если ТолькоПроверка Тогда
			СтрокаРежимаОтладки = "&mode=debug";
		Иначе
			СтрокаРежимаОтладки = "";
		КонецЕсли; 
		
		СкриптЯндексПакета = "<script src=""" + ТекущийСервиса + ТекстКлюча + "&load=package.map" + СтрокаРежимаОтладки + """ type=""text/javascript""></script>";
		Результат = СтрЗаменить(Результат, "%ПакетДополнительныхКарт%", СкриптЯндексПакета);
		
		// Теперь это глобально делается в общем макете HTML_LeafletAPI
		//Результат = СтрЗаменить(Результат, "<div id=" + Символ(34) + "map" + Символ(34) + " style=" + Символ(34) + "width: 100%; height: 100%" + Символ(34) + "></div>", "<div id=" 
		//	+ Символ(34) + "map" + Символ(34) + " style=" + Символ(34) + "width: 99vw;height: 96vh;" + Символ(34) + "></div>");
		
		Результат = СтрЗаменить(Результат, "%РежимСовместимостиКарты%", ТекстРежимСовместимостиКарты);
		Результат = СтрЗаменить(Результат, "map.on('load', function(e){map_Load = true});", ""); // При проверке отсутствовала
		
	КонецЕсли;
	
	ПлагинКластеризации = ПолучитьОбщийМакет("JS_Leaflet_Markercluster").ПолучитьТекст();	
	Результат = СтрЗаменить(Результат, "src="".../dist/markercluster-src.js"">", ">" + ПлагинКластеризации);
	ПлагинДекорированиеПолилиний = ПолучитьОбщийМакет("JS_Leaflet_PolylineDecorator").ПолучитьТекст();	
	Результат = СтрЗаменить(Результат, "src="".../dist/polylinedecorator.js"">", ">" + ПлагинДекорированиеПолилиний);
	ПлагинРисования = ПолучитьОбщийМакет("JS_Leaflet_Draw").ПолучитьТекст();
	Результат = СтрЗаменить(Результат, "src="".../dist/leaflet.draw.js"">", ">" + ПлагинРисования);
	ПлагинАнимации = ПолучитьОбщийМакет("JS_Leaflet_MovingMarker").ПолучитьТекст();
	Результат = СтрЗаменить(Результат, "src="".../dist/MovingMarker.js"">", ">" + ПлагинАнимации);
	ПлагинТеплокарт = ПолучитьОбщийМакет("JS_Leaflet_Heat").ПолучитьТекст();
	Результат = СтрЗаменить(Результат, "src="".../dist/leaflet-heat.js"">", ">" + ПлагинТеплокарт);
	
	//Если ПустаяСтрока(ПутьКLeaflet) Тогда
	//	Результат = НСтр("ru = 'Не указан путь к библиотекам Leaflet API'");
	//Иначе
	//	Результат = СтрЗаменить(Результат, "...MainDirectoryCSS", ПутьКLeaflet);
	//	Результат = СтрЗаменить(Результат, "...MainDirectoryJS", ПутьКLeaflet);
	//КонецЕсли;
	Leaflet_CSS = ПолучитьОбщийМакет("CSS_Leaflet_0_7_7").ПолучитьТекст();
	Leaflet_JS  = ПолучитьОбщийМакет("JS_Leaflet_0_7_7").ПолучитьТекст();
	Результат = СтрЗаменить(Результат, "<link rel=""stylesheet"" href=""...MainDirectoryCSS/leaflet.css"" />", "<style>" + Leaflet_CSS + "</style>");
	Результат = СтрЗаменить(Результат, "<script src=""...MainDirectoryJS/leaflet.js"">", "<script>" + Leaflet_JS + "</script>");
	
	Возврат Результат;
	
КонецФункции // ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса()

// Возвращает типовой макет.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   ГеографическаяСхема - Макет Схемы.
//
Функция ПолучитьГеографическуюСхемуПоУмолчанию() Экспорт
	
	Схема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упГеографическаяСхема");
	Если ЗначениеЗаполнено(Схема) Тогда
		Если Схема = Перечисления.упГеографическиеСхемы.КартаМира Тогда
			МакетСхемы = ПолучитьОбщийМакет("упКартаМира");
		ИначеЕсли Схема = Перечисления.упГеографическиеСхемы.РоссияРегионыИВсеГорода Тогда
			МакетСхемы = ПолучитьОбщийМакет("упРоссияРегионыИВсеГорода");
		ИначеЕсли Схема = Перечисления.упГеографическиеСхемы.РоссияРегионыИСтолицы Тогда 
			МакетСхемы = ПолучитьОбщийМакет("упРоссияРегионыИСтолицы");
		КонецЕсли;
		
	Иначе
		МакетСхемы = ПолучитьОбщийМакет("упКартаМира");
		
	КонецЕсли;
	
	Возврат МакетСхемы;
	
КонецФункции // ПолучитьГеографическуюСхемуПоУмолчанию()

// Получим географические координат начальной позиции.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Структура - ключи, Широта,Долгота,Зум.
//
Функция ПолучитьКоординатыНачальнойПозиции() Экспорт
	
	Результат = Новый Структура("Широта,Долгота,Зум",0,0,13);
	
	НачальнаяШирота  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упШиротаДляРасчетаКоличестваМетровВГрадусе");
	НачальнаяДолгота = упСервисныеФункции.ПолучитьЗначениеКонстанты("упДолготаНачальная");	
	Результат.Широта  = ?(НачальнаяШирота = 0, 55.84128, НачальнаяШирота);
	Результат.Долгота = ?(НачальнаяДолгота = 0, 37.6495, НачальнаяДолгота);
	
	Возврат Результат;
	
КонецФункции // ПолучитьКоординатыНачальнойПозиции()

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
//
// Параметры:
//  мсвГеографическиеЗоны - Массив - Список данных.
//
// Возвращаемое значение:
//   СправочникСсылка.упГеографическиеЗоны - Результат выполнения.
//
Функция ОбщаяГеографическаяЗона(мсвГеографическиеЗоны) Экспорт
	
	текРезультат = Справочники.упГеографическиеЗоны.ПустаяСсылка();
	текКоличествоГеоЗон = мсвГеографическиеЗоны.Количество();
	Если текКоличествоГеоЗон > 0 Тогда
		
		текРезультат = мсвГеографическиеЗоны[0];
		
		Если НЕ текРезультат = Справочники.упГеографическиеЗоны.ПустаяСсылка() Тогда
			
			Для й = 1 По текКоличествоГеоЗон - 1 Цикл
				
				текГеоЗона = мсвГеографическиеЗоны[й];
				
				Пока НЕ (текГеоЗона = текРезультат ИЛИ текГеоЗона.ПринадлежитЭлементу(текРезультат))  Цикл
					текРезультат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текРезультат, "Родитель");	
					
					Если текРезультат = Справочники.упГеографическиеЗоны.ПустаяСсылка() Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если текРезультат = Справочники.упГеографическиеЗоны.ПустаяСсылка() Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат текРезультат;				
	
КонецФункции


#КонецОбласти 

#Область РасчетПройденногоРасстояниеВЗоне_ЗаПределамиЗоны

// расчет планового расстояния в зоне и за пределами зоны.
//
// Параметры:
//  МассивЗон  - Массив - Массив ссылок СправочникСсылка.упГеографическиеЗоны.
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ рейса.
//  ВызыватьМаршрутизацию  - Булево - признак, влияющий на осуществление запросов к маршрутизатору 
//                                    при обнаружении нерассчитанных расстояний.
//
// Возвращаемое значение:
//   Соответствие - Сопоставление географической зоны и результата вычисления.
//
Функция РассчитатьПлановыйПробегВРейсеПоЗонам(МассивЗон, Рейс, ВызыватьМаршрутизацию = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПервых.Рейс КАК Рейс,
	|	упМаршрутПоРейсуСрезПервых.Идентификатор,
	|	упМаршрутПоРейсуСрезПервых.Адрес,
	|	упМаршрутПоРейсуСрезПервых.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПервых.Предшественники КАК Предшественники
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПервых(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПервых
	|
	|УПОРЯДОЧИТЬ ПО
	|	Рейс,
	|	СтрокаНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.Рейс,
	|	упПлановыеПараметрыРейса.Расстояние
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс = &Рейс";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ТаблицаКоординатыТочекМаршрута = ПакетЗапросов[0].Выгрузить();
	Выборка = ПакетЗапросов[1].Выбрать();
	РасстояниеПлан = 0;
	Пока Выборка.Следующий() Цикл
		РасстояниеПлан = Выборка.Расстояние;
	КонецЦикла;
	
	КопияТаблицыЗначений=Неопределено;
	
	РазместитьМаршрутПоПредшественникам(ТаблицаКоординатыТочекМаршрута,КопияТаблицыЗначений);
		
	Соответствие = Новый Соответствие;
	Если КопияТаблицыЗначений=Неопределено Тогда
		Для каждого ТекущаяЗона Из МассивЗон Цикл
			Структура = Новый Структура();
			Структура.Вставить("РасстояниеВЗоне",0);
			Структура.Вставить("РасстояниеВнеЗоны",0);
			Структура.Вставить("КоличествоПересеченийЗоны",0);
			Структура.Вставить("Ошибка",СтрШаблон(Нстр("ru = 'Для рейса %1 не получен маршрут'"),Строка(Рейс)));
			Соответствие.Вставить(ТекущаяЗона,Структура);
		КонецЦикла;
	Иначе
		АдресаМаршрута = КопияТаблицыЗначений.ВыгрузитьКолонку("Адрес");
		Если ВызыватьМаршрутизацию Тогда
			БольшиеОтрезкиМаршрута = ПолучитьРасстояниеИВремяМассиваТочек(АдресаМаршрута);
		Иначе
			БольшиеОтрезкиМаршрута = ПолучитьРасстояниеИВремяМассиваТочекРейса(АдресаМаршрута);
		КонецЕсли;
		ЗаполнимДанныеМаршрутаРейса(БольшиеОтрезкиМаршрута, Истина); 
		ТекстОшибки = Неопределено;
		Если НЕ БольшиеОтрезкиМаршрута.Количество() Тогда
			ТекстОшибки = СтрШаблон(Нстр("ru = 'Для рейса %1 маршрут не расчитан. Данных планового маршрута нет.'"), Строка(Рейс))
		КонецЕсли;
		Для каждого ТекущаяЗона Из МассивЗон Цикл
			РасстояниеВЗоне = 0;
			РасстояниеВнеЗоны = 0;
			КоличествоПересеченийЗоны = -1; // начинаем подсчет с -1  
			ПересечениеЗоны = 0;
			Если ТекстОшибки = Неопределено Тогда
				Для каждого БольшойОтрезокМаршрута Из БольшиеОтрезкиМаршрута Цикл
					ТаблицаМаршрут = БольшойОтрезокМаршрута.Маршрут;
					ВычислитьРастоянияЗоны(ТекущаяЗона, ТаблицаМаршрут, РасстояниеВЗоне, РасстояниеВнеЗоны, КоличествоПересеченийЗоны, ПересечениеЗоны);
				КонецЦикла;
			КонецЕсли;
			Структура = Новый Структура();
			Если РасстояниеВЗоне = 0 Тогда
				Структура.Вставить("РасстояниеВЗоне",0);
				Структура.Вставить("РасстояниеВнеЗоны",РасстояниеПлан);
			Иначе
				Структура.Вставить("РасстояниеВЗоне",Окр(РасстояниеВЗоне/1000,3)); // переводим в км.
				Структура.Вставить("РасстояниеВнеЗоны",Окр(РасстояниеВнеЗоны/1000,3)); // переводим в км.
			КонецЕсли;
			
			Структура.Вставить("КоличествоПересеченийЗоны", КоличествоПересеченийЗоны);
			Структура.Вставить("Ошибка", ТекстОшибки);
			Соответствие.Вставить(ТекущаяЗона, Структура);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Соответствие;
	
КонецФункции // РассчитатьПлановыйПробегВРейсеПоЗонам()

// Выполним обход маршрута по плечам.
//
// Параметры:
//  ТаблицаКоординатыТочекМаршрута  - ТаблицаЗначений - Данные заполнения.
//  КопияТаблицыЗначений  - ТаблицаЗначений - Таблица результат в которую передаем заполненые данные.
//
Процедура РазместитьМаршрутПоПредшественникам(ТаблицаКоординатыТочекМаршрута,КопияТаблицыЗначений)
	
	// -- расставим строки согласно предшественникам
	Массив = Новый Массив;		
	Для каждого ТекущаяСтрокаМаршрута Из ТаблицаКоординатыТочекМаршрута Цикл
		Если ПустаяСтрока(ТекущаяСтрокаМаршрута.Предшественники) Тогда
			Массив.Добавить(ТекущаяСтрокаМаршрута.Идентификатор);
		Иначе
			МассивПредшественники = СтрРазделить(ТекущаяСтрокаМаршрута.Предшественники,",");
			Количество = МассивПредшественники.Количество();
			Пока Количество > 0 Цикл
				Количество = Количество - 1;
				Идентификатор = МассивПредшественники[Количество];
				Если Массив.Найти(Идентификатор)=Неопределено Тогда
					Массив.Добавить(Идентификатор);
				КонецЕсли;
			КонецЦикла;
			Массив.Добавить(ТекущаяСтрокаМаршрута.Идентификатор);
		КонецЕсли;		
	КонецЦикла;
	
	Если Массив.Количество() Тогда
		КопияТаблицыЗначений = ТаблицаКоординатыТочекМаршрута.Скопировать();
		КопияТаблицыЗначений.Очистить();
		Для каждого ТекущаяСтрокаТаблицы Из Массив Цикл
			ТекущийОтбор = Новый Структура("Идентификатор",ТекущаяСтрокаТаблицы);
			НайденнаяСтрока = ТаблицаКоординатыТочекМаршрута.НайтиСтроки(ТекущийОтбор);
			Если НайденнаяСтрока.Количество() Тогда
				НоваяСтрока = КопияТаблицыЗначений.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,НайденнаяСтрока[0]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	// -- Предшественники
	
КонецПроцедуры // РазместитьМаршрутПоПредшественникам()

// Функция определяет расстояние между точками маршрута цепочки.
//
// Параметры:
//  АдресаМаршрута  - Массив элементов СправочникСсылка.упАдреса.
//
// Возвращаемое значение:
//   Массив   - Массив структур с ключами начальный адрес, конечный адрес.
//				начальную широту и долготу, конечные широту и долготу,
//					длину маршрута (км.), время маршрута (сек.), текст маршрута.
//
Функция ПолучитьРасстояниеИВремяМассиваТочекРейса(АдресаМаршрута) Экспорт
	
	
	мсвРезультат = Новый Массив; 
	
	КЧ = Новый КвалификаторыЧисла(12,0);
	Массив = Новый Массив;
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,КЧ);
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("НачальныйАдрес",Новый ОписаниеТипов("СправочникСсылка.упАдреса"),"НачальныйАдрес");
	ТаблицаЗначений.Колонки.Добавить("КонечныйАдрес",Новый ОписаниеТипов("СправочникСсылка.упАдреса"),"КонечныйАдрес");
	ТаблицаЗначений.Колонки.Добавить("Номер",ОписаниеТиповЧ,"Номер");
	
	n = 0; // порядковый номер последовательности.
	// получаем нужную последовательность точек.
	мсвТочки = ПолучитьПоследовательностьТочек(АдресаМаршрута);
	Для каждого мсвЭлемент Из мсвТочки Цикл
		n = n + 1;
		тбзСтрока = ТаблицаЗначений.Добавить();
		тбзСтрока.НачальныйАдрес = мсвЭлемент.Точка1;
		тбзСтрока.КонечныйАдрес  = мсвЭлемент.Точка2;
		тбзСтрока.Номер			 = n;		
	КонецЦикла;
	
	// производим запрос к базе на данные точек к базе.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
	Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначений);
	Запрос.Выполнить();	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(тбзАдресаМаршрута.НачальныйАдрес КАК Справочник.упАдреса) КАК НачальныйАдрес,
	|	ВЫРАЗИТЬ(тбзАдресаМаршрута.КонечныйАдрес КАК Справочник.упАдреса) КАК КонечныйАдрес,
	|	ВЫРАЗИТЬ(тбзАдресаМаршрута.Номер КАК ЧИСЛО(12, 0)) КАК Номер
	|ПОМЕСТИТЬ втАдрес
	|ИЗ
	|	ТЗ КАК тбзАдресаМаршрута
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдрес.НачальныйАдрес КАК Адрес
	|ПОМЕСТИТЬ вт_Адреса
	|ИЗ
	|	втАдрес КАК втАдрес
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втАдрес.КонечныйАдрес
	|ИЗ
	|	втАдрес КАК втАдрес
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдрес.Номер КАК Номер,
	|	втАдрес.НачальныйАдрес КАК НачальныйАдрес,
	|	втАдрес.КонечныйАдрес КАК КонечныйАдрес,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Расстояние ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упРасстоянияМеждуТочками.Расстояние = 0
	|						И упРасстоянияМеждуТочками.Время = 0
	|						И упРасстоянияМеждуТочками.АвтоматическийРасчет = ИСТИНА
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ упРасстоянияМеждуТочками.Расстояние
	|			КОНЕЦ
	|	КОНЕЦ КАК ДлинаМаршрута,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Время ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упРасстоянияМеждуТочками.Расстояние = 0
	|						И упРасстоянияМеждуТочками.Время = 0
	|						И упРасстоянияМеждуТочками.АвтоматическийРасчет = ИСТИНА
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ упРасстоянияМеждуТочками.Время
	|			КОНЕЦ
	|	КОНЕЦ КАК ВремяМаршрута,
	|	ЕСТЬNULL(упРасстоянияМеждуТочками.Путь, """") КАК ТекстМаршрута,
	|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПутьПростой, """") КАК ТекстМаршрутаПростого,
	|	втАдрес.НачальныйАдрес.Широта КАК НачальнаяШирота,
	|	втАдрес.НачальныйАдрес.Долгота КАК НачальнаяДолгота,
	|	втАдрес.КонечныйАдрес.Широта КАК КонечнаяШирота,
	|	втАдрес.КонечныйАдрес.Долгота КАК КонечнаяДолгота,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Расстояние ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Записать,
	|	упРасстоянияМеждуТочками.ВремяПонедельник,
	|	упРасстоянияМеждуТочками.ВремяВторник,
	|	упРасстоянияМеждуТочками.ВремяСреда,
	|	упРасстоянияМеждуТочками.ВремяЧетверг,
	|	упРасстоянияМеждуТочками.ВремяПятница,
	|	упРасстоянияМеждуТочками.ВремяСуббота,
	|	упРасстоянияМеждуТочками.ВремяВоскресенье
	|ИЗ
	|	втАдрес КАК втАдрес
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	|		ПО втАдрес.НачальныйАдрес.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
	|			И втАдрес.НачальныйАдрес.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
	|			И втАдрес.КонечныйАдрес.Широта = упРасстоянияМеждуТочками.КонечнаяШирота
	|			И втАдрес.КонечныйАдрес.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота
	|			И (упРасстоянияМеждуТочками.Ограничение = ЗНАЧЕНИЕ(Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТЗ";
	
	тбзАдреса = Запрос.Выполнить().Выгрузить();
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	
	Массив = Новый Массив; // последовательности точек, результат.
	Для каждого тбзСтрока Из тбзАдреса Цикл
		// заполним массив результатом.
		Структура = Новый Структура("НачальныйАдрес,КонечныйАдрес,НачальнаяШирота,НачальнаяДолгота,КонечнаяШирота,КонечнаяДолгота,ДлинаМаршрута,ВремяМаршрута,ТекстМаршрута,ТекстМаршрутаПростого");
		ЗаполнитьЗначенияСвойств(Структура,тбзСтрока);
		Массив.Добавить(Структура);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции // ПолучитьРасстояниеИВремяМассиваТочекРейса()

// Поиск точек которые входят в зону, делим длинные отрезки по вместимости.
//
// Параметры:
//  ТаблицаКоординат - ТаблицаЗначений - Координаты.
//  ГеоЗона - СправочникСсылка.упГеографическиеЗоны  - ссылка на географическую зону.
//
// Возвращаемое значение:
//   ТаблицаЗначений  - Перечислены точки которые входят в зону.
//
Функция ВернутьГеографическуюЗону(ТаблицаКоординат, ГеоЗона) Экспорт

	Если Ложь Тогда // Для контекстной подсказки
	    ГеоЗона = Справочники.упГеографическиеЗоны.ПустаяСсылка();
	КонецЕсли;
	Если ГеоЗона.ПолигонПростой.Количество() = 0 Тогда
		ГеоЗонаОбъект = ГеоЗона.ПолучитьОбъект();
		СтрокиТЧ = упРаботаСКартами.ВыполнитьУпрощениеПолигональнойЦепи(ГеоЗонаОбъект.Полигон,, 200);
		Если ТипЗнч(СтрокиТЧ) = Тип("Массив") Тогда
			ПолигонПростой = ГеоЗонаОбъект.Полигон.Выгрузить(СтрокиТЧ);
		Иначе
			ПолигонПростой = ГеоЗонаОбъект.Полигон.Выгрузить();
		КонецЕсли; 
		ГеоЗонаОбъект.ПолигонПростой.Загрузить(ПолигонПростой);
		ГеоЗонаОбъект.ДополнительныеСвойства.Вставить("ПолигонПростойАктуален");
		ГеоЗонаОбъект.Записать();
	КонецЕсли; 
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
	Запрос.УстановитьПараметр("ТЗ", ТаблицаКоординат);
	Запрос.Выполнить();	
	Запрос.УстановитьПараметр("ГеоЗона", ГеоЗона);
	Запрос.Текст = " 
|//{Запрос: 1 ////////////////////////////////////////
|ВЫБРАТЬ
|	ВЫРАЗИТЬ(ТаблицаТочки.Широта КАК ЧИСЛО(9, 6)) КАК У,
|	ВЫРАЗИТЬ(ТаблицаТочки.Долгота КАК ЧИСЛО(9, 6)) КАК Х,
|	ВЫРАЗИТЬ(ТаблицаТочки.Номер КАК ЧИСЛО(12, 0)) КАК Точка
|ПОМЕСТИТЬ Точки
|ИЗ
|	ТЗ КАК ТаблицаТочки
|ИНДЕКСИРОВАТЬ ПО
|	Точка
|;
|//{Запрос: 2 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	КОЛИЧЕСТВО(Полигон.НомерСтроки) КАК Количество,
|	МИНИМУМ(Полигон.Широта) КАК Широта_Мин,
|	МАКСИМУМ(Полигон.Широта) КАК Широта_Макс,
|	МИНИМУМ(ВЫБОР
|		КОГДА Полигон.Ссылка.ПроходитЧерез180Меридиан
|			ТОГДА ВЫБОР
|				КОГДА Полигон.Долгота < 0
|					ТОГДА 360 + Полигон.Долгота
|				ИНАЧЕ Полигон.Долгота
|			КОНЕЦ
|		ИНАЧЕ Полигон.Долгота
|	КОНЕЦ) КАК Долгота_Мин,
|	МАКСИМУМ(ВЫБОР
|		КОГДА Полигон.Ссылка.ПроходитЧерез180Меридиан
|			ТОГДА ВЫБОР
|				КОГДА Полигон.Долгота < 0
|					ТОГДА 360 + Полигон.Долгота
|				ИНАЧЕ Полигон.Долгота
|			КОНЕЦ
|		ИНАЧЕ Полигон.Долгота
|	КОНЕЦ) КАК Долгота_Макс,
|	Полигон.Ссылка.ПроходитЧерез180Меридиан КАК Меридиан_180
|ПОМЕСТИТЬ втПолигонПараметры
|ИЗ
|	Справочник.упГеографическиеЗоны.ПолигонПростой КАК Полигон
|ГДЕ Полигон.Ссылка = &ГеоЗона
|		
|СГРУППИРОВАТЬ ПО
|	Полигон.Ссылка,
|	Полигон.Ссылка.ПроходитЧерез180Меридиан
|
|;
|//{Запрос: 3 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	Точки.Точка КАК Точка,
|	ВЫБОР
|		КОГДА втПолигонПараметры.Меридиан_180
|			ТОГДА ВЫБОР
|				КОГДА Точки.Х < 0
|					ТОГДА 360 + Точки.Х
|				ИНАЧЕ Точки.Х
|			КОНЕЦ
|		ИНАЧЕ Точки.Х
|	КОНЕЦ КАК Х,
|	Точки.У КАК У,
|	втПолигонПараметры.Меридиан_180 КАК Меридиан_180,
|	втПолигонПараметры.Количество КАК Количество
|ПОМЕСТИТЬ втСоответствиеПолигоновТочке
|ИЗ
|	втПолигонПараметры КАК втПолигонПараметры
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Точки КАК Точки
|	ПО ИСТИНА
|		И ВЫБОР
|				КОГДА втПолигонПараметры.Меридиан_180
|					ТОГДА ВЫБОР
|						КОГДА Точки.Х < 0
|							ТОГДА 360 + Точки.Х
|						ИНАЧЕ Точки.Х
|					КОНЕЦ
|				ИНАЧЕ Точки.Х
|			КОНЕЦ МЕЖДУ втПолигонПараметры.Долгота_Мин И втПолигонПараметры.Долгота_Макс
|		И Точки.У МЕЖДУ втПолигонПараметры.Широта_Мин И втПолигонПараметры.Широта_Макс
|
|
|;
|//{Запрос: 4 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втСоответствиеПолигоновТочке.Точка КАК Точка,
|	втСоответствиеПолигоновТочке.Х КАК Точка_Х,
|	втСоответствиеПолигоновТочке.У КАК Точка_У,
|	ПолигонГеоЗоны.НомерСтроки КАК НомерСтроки,
|	ПолигонГеоЗоны.Широта КАК У,
|	ВЫБОР
|		КОГДА втСоответствиеПолигоновТочке.Меридиан_180
|			ТОГДА ВЫБОР
|				КОГДА ПолигонГеоЗоны.Долгота < 0
|					ТОГДА 360 + ПолигонГеоЗоны.Долгота
|				ИНАЧЕ ПолигонГеоЗоны.Долгота
|			КОНЕЦ
|		ИНАЧЕ ПолигонГеоЗоны.Долгота
|	КОНЕЦ КАК Х,
|	втСоответствиеПолигоновТочке.Количество КАК Размер,
|	втСоответствиеПолигоновТочке.Меридиан_180 КАК Меридиан_180
|ПОМЕСТИТЬ втПолигоны
|ИЗ
|	втСоответствиеПолигоновТочке КАК втСоответствиеПолигоновТочке
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны.ПолигонПростой КАК ПолигонГеоЗоны
|	ПО ПолигонГеоЗоны.Ссылка = &ГеоЗона
|
|
|;
|//{Запрос: 5 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втПолигоны.НомерСтроки КАК НомерТочкиПолигона,
|	втПолигоны.Точка КАК Точка,
|	втПолигоны.Х - втПолигоны.Точка_Х КАК Разница_Х,
|	втПолигоны.У - втПолигоны.Точка_У КАК Разница_У,
|	ВЫБОР
|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х < 0
|			И втПолигоны.У - втПолигоны.Точка_У < 0
|			ТОГДА 2
|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х < 0
|			И втПолигоны.У - втПолигоны.Точка_У >= 0
|			ТОГДА 1
|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х >= 0
|			И втПолигоны.У - втПолигоны.Точка_У < 0
|			ТОГДА 3
|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х >= 0
|			И втПолигоны.У - втПолигоны.Точка_У >= 0
|			ТОГДА 0
|	КОНЕЦ КАК К
|ИЗ
|	втПолигоны КАК втПолигоны
|УПОРЯДОЧИТЬ ПО
|	НомерТочкиПолигона
|ИТОГИ
|ПО
|	Точка
|";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТекущаяТочкаПолигона = Новый Структура;
	ПредыдущаяТочкаПолигона = Новый Структура;
	Для Каждого КолонкаРезультата Из РезультатЗапроса.Колонки Цикл
		Если Ложь
			Или КолонкаРезультата.Имя = "НомерТочкиПолигона" 
			Или КолонкаРезультата.Имя = "Точка" 
		Тогда
			Продолжить;
		КонецЕсли; 
		ТекущаяТочкаПолигона.Вставить(КолонкаРезультата.Имя);
		ПредыдущаяТочкаПолигона.Вставить(КолонкаРезультата.Имя);
	КонецЦикла;
	ТаблицаЗначений.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
	ТаблицаЗначений.Колонки.Добавить("Точка", Новый ОписаниеТипов("Число"));
	ВыборкаТочка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Точка");
	Пока ВыборкаТочка.Следующий() Цикл
		РезультатТочки = 0;
		ВыборкаТочкиПолигона = ВыборкаТочка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ПервыйПроход = Истина;
		Пока ВыборкаТочкиПолигона.Следующий() Цикл
			Если Не ПервыйПроход Тогда
				ЗаполнитьЗначенияСвойств(ПредыдущаяТочкаПолигона, ТекущаяТочкаПолигона); 
			КонецЕсли; 
			ЗаполнитьЗначенияСвойств(ТекущаяТочкаПолигона, ВыборкаТочкиПолигона); 
			Если Не ПервыйПроход Тогда
				РезультатТочки = РезультатТочки + ВычислитьОтношениеТочкиКГраниПолигона(ПредыдущаяТочкаПолигона, ТекущаяТочкаПолигона);
			Иначе
				ПервыйПроход = Ложь;
			КонецЕсли; 
		КонецЦикла;
		// Замыкаем последнюю точку с первой
		ВыборкаТочкиПолигона.Сбросить();
		Если ВыборкаТочкиПолигона.Следующий() Тогда 
			ЗаполнитьЗначенияСвойств(ПредыдущаяТочкаПолигона, ТекущаяТочкаПолигона); 
			ЗаполнитьЗначенияСвойств(ТекущаяТочкаПолигона, ВыборкаТочкиПолигона); 
			РезультатТочки = РезультатТочки + ВычислитьОтношениеТочкиКГраниПолигона(ПредыдущаяТочкаПолигона, ТекущаяТочкаПолигона);
		КонецЕсли;
		СтрокаТочки = ТаблицаЗначений.Добавить();
		СтрокаТочки.Точка = ВыборкаТочка.Точка;
		СтрокаТочки.Результат = РезультатТочки;
	КонецЦикла;
	
	Запрос.Текст = "ВЫБРАТЬ *, ВЫРАЗИТЬ(&ГеоЗона КАК Справочник.упГеографическиеЗоны) КАК Зона ПОМЕСТИТЬ втЗоны ИЗ &ТЗ КАК ТЗ";
	Запрос.УстановитьПараметр("ТЗ", ТаблицаЗначений);
	Запрос.Выполнить();
	Запрос.Текст = "
//|//{Запрос: 0 ////////////////////////////////////////
//|//////////////////////////////////////////////////////////////////////////////
//|ВЫБРАТЬ
//|	Развернуто.Точка КАК Точка,
//|	ВЫРАЗИТЬ(&ГеоЗона КАК Справочник.упГеографическиеЗоны) КАК Зона,
//|	СУММА(Развернуто.Результат) КАК Результат
//|ПОМЕСТИТЬ втЗоны
//|ИЗ
//|	Развернуто КАК Развернуто
//|СГРУППИРОВАТЬ ПО
//|	Развернуто.Точка
//|ИНДЕКСИРОВАТЬ ПО
//|	Точка
//|;
|//{Запрос: 1 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втЗоны.Точка КАК Точка,
|	втЗоны.Зона КАК Зона,
|	втЗоны.Зона.Страна КАК Страна,
|	втЗоны.Зона.Регион КАК Регион,
|	упИерархияГеографическихЗон.Уровень КАК Уровень,
|	втЗоны.Результат КАК Результат
|ИЗ
|	втЗоны КАК втЗоны
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
|	ПО ИСТИНА
|		И втЗоны.Зона = упИерархияГеографическихЗон.Зона
|		И втЗоны.Зона = упИерархияГеографическихЗон.Родитель
|		
|ГДЕ втЗоны.Результат <> 0
|		
|УПОРЯДОЧИТЬ ПО
|	Точка,
|	Уровень УБЫВ
|";
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаЗначений.Скопировать(,"Точка");
	
КонецФункции // ВернутьГеографическуюЗону()

Функция ВычислитьОтношениеТочкиКГраниПолигона(ПредыдущаяТочкаПолигона, ТекущаяТочкаПолигона)
	
	//	ВЫБОР
	//		КОГДА Таб.К - ТабПред.К = -3
	//			ТОГДА 1
	//		КОГДА Таб.К - ТабПред.К = 3
	//			ТОГДА -1
	//		КОГДА Таб.К - ТабПред.К = -2
	//				И ТабПред.Разница_Х * Таб.Разница_У >= ТабПред.Разница_У * Таб.Разница_Х
	//			ТОГДА 1
	//		КОГДА Таб.К - ТабПред.К = 2
	//				И НЕ ТабПред.Разница_Х * Таб.Разница_У >= ТабПред.Разница_У * Таб.Разница_Х
	//			ТОГДА -1
	//		ИНАЧЕ 0
	//	КОНЕЦ КАК Результат
	Если ТекущаяТочкаПолигона.К - ПредыдущаяТочкаПолигона.К = -3 Тогда
		Результат = 1;
	ИначеЕсли ТекущаяТочкаПолигона.К - ПредыдущаяТочкаПолигона.К = 3 Тогда
		Результат = -1;
	ИначеЕсли Истина
		И ТекущаяТочкаПолигона.К - ПредыдущаяТочкаПолигона.К = -2 
		И ПредыдущаяТочкаПолигона.Разница_Х * ТекущаяТочкаПолигона.Разница_У >= ПредыдущаяТочкаПолигона.Разница_У * ТекущаяТочкаПолигона.Разница_Х
	Тогда
		Результат = 1;
	ИначеЕсли Истина
		И ТекущаяТочкаПолигона.К - ПредыдущаяТочкаПолигона.К = 2 
		И ПредыдущаяТочкаПолигона.Разница_Х * ТекущаяТочкаПолигона.Разница_У < ПредыдущаяТочкаПолигона.Разница_У * ТекущаяТочкаПолигона.Разница_Х
	Тогда
		Результат = -1;
	Иначе
		Результат = 0;
	КонецЕсли; 
	Возврат Результат;

КонецФункции

// Расчет фактического расстояния в зоне и за пределами зоны.
//
// Параметры:
//  МассивЗон  - Массив - Массив ссылок СправочникСсылка.упГеографическиеЗоны.
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ рейса.
//  ВызыватьМаршрутизацию  - Булево - признак, влияющий на осуществление запросов к маршрутизатору 
//                                    при обнаружении нерассчитанных расстояний.
//
// Возвращаемое значение:
//   Соответствие - Сопоставление географической зоны и результата вычисления.
//
Функция РассчитатьФактическийПробегВРейсеПоЗонам (МассивЗон, Рейс, ВызыватьМаршрутизацию = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	0 КАК Номер,
	|	упДанныеТрекеров.Рейс,
	|	упДанныеТрекеров.Период КАК Период,
	|	упДанныеТрекеров.Широта,
	|	упДанныеТрекеров.Долгота
	|ИЗ
	|	РегистрСведений.упДанныеТрекеров КАК упДанныеТрекеров
	|ГДЕ
	|	упДанныеТрекеров.Рейс В(&Рейс)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутПоРейсу.Рейс КАК Рейс,
	|	МаршрутПоРейсу.Идентификатор,
	|	МаршрутПоРейсу.Адрес,
	|	МаршрутПоРейсу.СтрокаНомер КАК СтрокаНомер,
	|	МаршрутПоРейсу.Предшественники КАК Предшественники
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК МаршрутПоРейсу
	|
	|УПОРЯДОЧИТЬ ПО
	|	Рейс,
	|	СтрокаНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.Рейс,
	|	упПлановыеПараметрыРейса.Расстояние
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упФактическиеПараметрыРейса.Рейс,
	|	упФактическиеПараметрыРейса.Расстояние
	|ИЗ
	|	РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	|ГДЕ
	|	упФактическиеПараметрыРейса.Рейс = &Рейс";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ТаблицаПройденныхТочек = ПакетЗапросов[0].Выгрузить();
	Сч = 0;
	Для каждого ТекущаяСтрокаДанныхТрекера Из ТаблицаПройденныхТочек Цикл
		Сч = Сч+1;
		ТекущаяСтрокаДанныхТрекера.Номер = Сч;
	КонецЦикла;
	
	ОбработатьДанныеТрекера = ?(ТаблицаПройденныхТочек.Количество(),Истина,Ложь);
	
	ВыборкаП = ПакетЗапросов[2].Выбрать();
	ВыборкаФ = ПакетЗапросов[3].Выбрать();
	РасстояниеПлан = 0;
	Пока ВыборкаП.Следующий() Цикл
		РасстояниеПлан = ВыборкаП.Расстояние;
	КонецЦикла;
	РасстояниеФакт = 0;
	Пока ВыборкаФ.Следующий() Цикл
		РасстояниеФакт = ВыборкаФ.Расстояние;
	КонецЦикла;
	
	КопияТаблицыЗначений=Неопределено;
	
	Если НЕ ОбработатьДанныеТрекера Тогда
		ТаблицаКоординатыТочекМаршрута = ПакетЗапросов[1].Выгрузить();
		РазместитьМаршрутПоПредшественникам(ТаблицаКоординатыТочекМаршрута, КопияТаблицыЗначений);
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	МассивРезультаты = Новый Массив;
	Если НЕ ОбработатьДанныеТрекера И КопияТаблицыЗначений = Неопределено Тогда
		Для каждого ТекущаяЗона Из МассивЗон Цикл
			Структура = Новый Структура();
			Структура.Вставить("РасстояниеВЗоне",0);
			Структура.Вставить("РасстояниеВнеЗоны",0);
			Структура.Вставить("КоличествоПересеченийЗоны",0);
			Структура.Вставить("Ошибка",СтрШаблон(Нстр("ru = 'Для рейса %1 не получен маршрут'"),Строка(Рейс)));
			Соответствие.Вставить(ТекущаяЗона,Структура);
		КонецЦикла;
	ИначеЕсли НЕ ОбработатьДанныеТрекера Тогда
		АдресаМаршрута = КопияТаблицыЗначений.ВыгрузитьКолонку("Адрес");
		Если ВызыватьМаршрутизацию Тогда
			МассивРезультаты = ПолучитьРасстояниеИВремяМассиваТочек(АдресаМаршрута);
		Иначе
			МассивРезультаты = ПолучитьРасстояниеИВремяМассиваТочекРейса(АдресаМаршрута);
		КонецЕсли;
		ЗаполнимДанныеМаршрутаРейса(МассивРезультаты, Истина);
	КонецЕсли;
	
	ТекстОишбки = Неопределено;
	Если НЕ ОбработатьДанныеТрекера И НЕ МассивРезультаты.Количество() Тогда
		ТекстОишбки = СтрШаблон(Нстр("ru = 'Для рейса %1 маршрут не расчитан. Данных планового маршрута нет.'"),Строка(Рейс))
	КонецЕсли;
	
	Для каждого ТекущаяЗона Из МассивЗон Цикл
		РасстояниеВЗоне = 0;
		РасстояниеВнеЗоны = 0;
		КоличествоПересеченийЗоны = -1; // начинаем подсчет с -1  
		ПересечениеЗоны = 0;
		
		Если ТекстОишбки = Неопределено Тогда
			
			Если ОбработатьДанныеТрекера Тогда
				ВычислитьРастоянияЗоны(ТекущаяЗона, ТаблицаПройденныхТочек,РасстояниеВЗоне,РасстояниеВнеЗоны,КоличествоПересеченийЗоны,ПересечениеЗоны);
			Иначе
				Для каждого ОтрезокМаршрута Из МассивРезультаты Цикл
					
					ТаблицаМаршрут = ОтрезокМаршрута.Маршрут;
					ВычислитьРастоянияЗоны(ТекущаяЗона, ТаблицаМаршрут,РасстояниеВЗоне,РасстояниеВнеЗоны,КоличествоПересеченийЗоны,ПересечениеЗоны);
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Структура = Новый Структура();
		Если РасстояниеВЗоне = 0 Тогда
			Структура.Вставить("РасстояниеВЗоне",0);
			Структура.Вставить("РасстояниеВнеЗоны",?(РасстояниеФакт=0,РасстояниеПлан,РасстояниеФакт));
		Иначе
			Структура.Вставить("РасстояниеВЗоне",Окр(РасстояниеВЗоне/1000,3)); // переводим в км.
			Структура.Вставить("РасстояниеВнеЗоны",Окр(РасстояниеВнеЗоны/1000,3)); // переводим в км.
		КонецЕсли;
		
		Структура.Вставить("КоличествоПересеченийЗоны",КоличествоПересеченийЗоны);
		Структура.Вставить("Ошибка",ТекстОишбки);
		Соответствие.Вставить(ТекущаяЗона,Структура);
		
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции // РассчитатьФактическийПробегВРейсеПоЗонам()

// Заполним маршрут рейса данными координат.
//
// Параметры:
//  МассивПлечМаршрута  - Массив - Список отрезков маршрута.
//
Процедура ЗаполнимДанныеМаршрутаРейса(МассивПлечМаршрута, ИспользоватьПростойМаршрут = Ложь)
	
	Для каждого ОтрезокМаршрута Из МассивПлечМаршрута Цикл
		
		Если ПустаяСтрока(ОтрезокМаршрута.ТекстМаршрута) Тогда
			Продолжить;
		КонецЕсли;
		Если ИспользоватьПростойМаршрут Тогда
			ТекстМаршрута = ОтрезокМаршрута.ТекстМаршрутаПростого;
			Если Не ЗначениеЗаполнено(ТекстМаршрута) Тогда
				Запись = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, ОтрезокМаршрута, "НачальнаяШирота,НачальнаяДолгота,КонечнаяШирота,КонечнаяДолгота"); 
				Запись.Прочитать();
				упМаршрутизация.ОбновитьПростойПутьВСтрокеРасстояния(Запись);
				Запись.Записать();
			КонецЕсли; 
		Иначе
			ТекстМаршрута = ОтрезокМаршрута.ТекстМаршрута;
		КонецЕсли; 
		МассивКоординат = СтрРазделить(ТекстМаршрута, ";", Ложь);
		Количество = МассивКоординат.Количество();
		МассивТочек = Новый Массив;
		Номер = 0;
		
		КЧ = Новый КвалификаторыЧисла(12,0);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Число"));
		ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,КЧ);
		
		КЧ = Новый КвалификаторыЧисла(9,6);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Число"));
		ОписаниеТиповЧ_1 = Новый ОписаниеТипов(Массив, , ,КЧ);
		
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ТаблицаЗначений.Колонки.Добавить("Номер",ОписаниеТиповЧ,"Номер");
		ТаблицаЗначений.Колонки.Добавить("широта",ОписаниеТиповЧ_1,"широта");
		ТаблицаЗначений.Колонки.Добавить("долгота",ОписаниеТиповЧ_1,"долгота");
		Для Сч = 0 По (Количество / 2) - 1 Цикл 
			Номер = Номер+1;
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.Номер = Номер;
			НоваяСтрока.Широта = ПредставлениеКоординатыВЧисло(МассивКоординат[Сч * 2]);
			НоваяСтрока.Долгота = ПредставлениеКоординатыВЧисло(МассивКоординат[(Сч * 2) + 1]);
		КонецЦикла;
		
		ОтрезокМаршрута.Вставить("Маршрут",ТаблицаЗначений);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнимДанныеМаршрутаРейса()

// Выполним вычисления растояния в зоне и вне зоны.
//
// Параметры:
//  Зона  - СправочникСсылка.упГеографическиеЗоны - Ссылка на гео зону.
//  ТаблицаМаршрут  - ТаблицаЗначений - Список точек проверки.
//  РасстояниеВЗоне  - Число - Расстояние пройденное внутри зоны.
//  РасстояниеВнеЗоны  - Число - Расстояние пройденное вне зоны.
//  КоличествоПересеченийЗоны  - Число - Количество пересечений зоны.
//  ПересечениеЗоны  - Число - Флаг что мы находимся в зон, значения 1 - в зон, 2- вне зоны.
//
Процедура ВычислитьРастоянияЗоны(Зона,ТаблицаМаршрут,РасстояниеВЗоне,РасстояниеВнеЗоны,КоличествоПересеченийЗоны,ПересечениеЗоны)
	
	ТаблицаТочек = ВернутьГеографическуюЗону(ТаблицаМаршрут,Зона); // точки которые в зоне.
	
	ПредшественникВ = Неопределено;
	ПредшественникВне = Неопределено;
	Для каждого ТекущаяТочка Из ТаблицаМаршрут Цикл
		
		ВЗоне = Ложь;
		ОтборТочек = Новый Структура();
		ОтборТочек.Вставить("Точка",ТекущаяТочка.Номер);
		НайтиСтроки = ТаблицаТочек.НайтиСтроки(ОтборТочек);
		Если НайтиСтроки.Количество() Тогда
			ВЗоне = Истина;
		КонецЕсли;
		
		Если ВЗоне Тогда
			ПредшественникВне = Неопределено;
			Если НЕ ПредшественникВ = Неопределено Тогда
				Растояние = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(ПредшественникВ.широта,ПредшественникВ.долгота,ТекущаяТочка.широта,ТекущаяТочка.долгота);
				РасстояниеВЗоне = РасстояниеВЗоне + Растояние;
			КонецЕсли;
			Если НЕ ПересечениеЗоны = 1 Тогда
				КоличествоПересеченийЗоны = КоличествоПересеченийЗоны+1;
				ПересечениеЗоны = 1;
			КонецЕсли;
			ПредшественникВ = ТекущаяТочка;
		Иначе
			ПредшественникВ = Неопределено;
			Если НЕ ПредшественникВне = Неопределено Тогда
				Растояние = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(ПредшественникВне.широта,ПредшественникВне.долгота,ТекущаяТочка.широта,ТекущаяТочка.долгота);
				РасстояниеВнеЗоны = РасстояниеВнеЗоны + Растояние;
			КонецЕсли;
			Если НЕ ПересечениеЗоны = 2 Тогда
				КоличествоПересеченийЗоны = КоличествоПересеченийЗоны+1;
				ПересечениеЗоны = 2;
			КонецЕсли;
			ПредшественникВне = ТекущаяТочка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ВычислитьРастоянияЗоны()

#КонецОбласти

Функция ПолучитьРасстояниеИВремяМеждуТочкамиПоДаннымИБПоТаблице(ТаблицаАдресов, КоэффициентПереводаВЧасы, ВызыватьМаршрутизацию = Ложь)
	
	РезультатТаблица = Новый ТаблицаЗначений;
	РезультатТаблица.Колонки.Добавить("АдресНачальнойТочки", 	Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	РезультатТаблица.Колонки.Добавить("АдресКонечнойТочки", 	Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	РезультатТаблица.Колонки.Добавить("Данные");
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	              |	ТаблицаАдресов.АдресНачальнойТочки,
	              |	ТаблицаАдресов.АдресКонечнойТочки
	              |ПОМЕСТИТЬ втАдреса
	              |ИЗ
	              |	&ТаблицаАдресов КАК ТаблицаАдресов
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	тбНачальнаяТочкаАдрес.Широта КАК НачальнаяШирота,
	              |	тбНачальнаяТочкаАдрес.Долгота КАК НачальнаяДолгота,
	              |	тбКонечнаяТочкаАдрес.Широта КАК КонечнаяШирота,
	              |	тбКонечнаяТочкаАдрес.Долгота КАК КонечнаяДолгота,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.Расстояние, 0) КАК Расстояние,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.Время, 0) / &КоэффициентПереводаВЧасы КАК Время,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяПонедельник, 0) / &КоэффициентПереводаВЧасы КАК ВремяПонедельник,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяВторник, 0) / &КоэффициентПереводаВЧасы КАК ВремяВторник,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяСреда, 0) / &КоэффициентПереводаВЧасы КАК ВремяСреда,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяЧетверг, 0) / &КоэффициентПереводаВЧасы КАК ВремяЧетверг,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяПятница, 0) / &КоэффициентПереводаВЧасы КАК ВремяПятница,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяСуббота, 0) / &КоэффициентПереводаВЧасы КАК ВремяСуббота,
	              |	ЕСТЬNULL(упРасстоянияМеждуТочками.ВремяВоскресенье, 0) / &КоэффициентПереводаВЧасы КАК ВремяВоскресенье,
	              |	втАдреса.АдресНачальнойТочки КАК НачальнаяТочка,
	              |	втАдреса.АдресКонечнойТочки КАК КонечнаяТочка
	              |ИЗ
	              |	втАдреса КАК втАдреса
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК тбНачальнаяТочкаАдрес
	              |		ПО втАдреса.АдресНачальнойТочки = тбНачальнаяТочкаАдрес.Ссылка
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК тбКонечнаяТочкаАдрес
	              |		ПО втАдреса.АдресКонечнойТочки = тбКонечнаяТочкаАдрес.Ссылка
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	              |		ПО (тбНачальнаяТочкаАдрес.Широта = упРасстоянияМеждуТочками.НачальнаяШирота)
	              |			И (тбНачальнаяТочкаАдрес.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота)
	              |			И (тбКонечнаяТочкаАдрес.Широта = упРасстоянияМеждуТочками.КонечнаяШирота)
	              |			И (тбКонечнаяТочкаАдрес.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота)
	              |			И (упРасстоянияМеждуТочками.ПопыткаРасчета = 0)";

	Запрос.УстановитьПараметр("ТаблицаАдресов", 			ТаблицаАдресов);
	Запрос.УстановитьПараметр("КоэффициентПереводаВЧасы", 	КоэффициентПереводаВЧасы);
	
	сткКарта = Неопределено;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
				
		сткДанные = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, ПроездНеРассчитан", 0, 0, 0, 0, 0, 0, 0, 0, 0, Ложь);
		
		НоваяСтрока = РезультатТаблица.Добавить();
		НоваяСтрока.Данные 				= сткДанные;
		НоваяСтрока.АдресНачальнойТочки = Выборка.НачальнаяТочка;
		НоваяСтрока.АдресКонечнойТочки 	= Выборка.КонечнаяТочка;
		
		ЗаполнитьЗначенияСвойств(сткДанные, Выборка);
		Если сткДанные.Расстояние = 0 Тогда
			Если ВызыватьМаршрутизацию И ЗначениеЗаполнено(Выборка.НачальнаяТочка) И ЗначениеЗаполнено(Выборка.КонечнаяТочка) Тогда
				
				Если сткКарта = Неопределено Тогда
					сткКарта = упМаршрутизация.ПодготовитьКартуПоТипу(,Истина);
				КонецЕсли;
				
				сткВозврат = упМаршрутизация.ПолучитьРасстояниеМеждуТочкамиПоТипуКарты(сткКарта, Выборка.НачальнаяТочка, Выборка.КонечнаяТочка);			
				
				Если сткВозврат <> Неопределено И сткВозврат.Свойство("Расстояние") И сткВозврат.Расстояние > 0 Тогда 
					сткДанные.Расстояние = сткВозврат.Расстояние;
					сткДанные.Время      = сткВозврат.Время/КоэффициентПереводаВЧасы;
					сткДанные.ВремяПонедельник = сткВозврат.ВремяПонедельник/КоэффициентПереводаВЧасы;
					сткДанные.ВремяВторник     = сткВозврат.ВремяВторник/КоэффициентПереводаВЧасы;
					сткДанные.ВремяСреда       = сткВозврат.ВремяСреда/КоэффициентПереводаВЧасы;
					сткДанные.ВремяЧетверг     = сткВозврат.ВремяЧетверг/КоэффициентПереводаВЧасы;
					сткДанные.ВремяПятница     = сткВозврат.ВремяПятница/КоэффициентПереводаВЧасы;
					сткДанные.ВремяСуббота     = сткВозврат.ВремяСуббота/КоэффициентПереводаВЧасы;
					сткДанные.ВремяВоскресенье = сткВозврат.ВремяВоскресенье/КоэффициентПереводаВЧасы;
				КонецЕсли;
			КонецЕсли;
			
			Если сткДанные.Расстояние = 0 Тогда
				Если Истина
					И ЗначениеЗаполнено(Выборка.НачальнаяШирота)
					И ЗначениеЗаполнено(Выборка.НачальнаяДолгота)
					И ЗначениеЗаполнено(Выборка.КонечнаяШирота)
					И ЗначениеЗаполнено(Выборка.КонечнаяДолгота)
				Тогда
					сткДанные.Расстояние = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(Выборка.НачальнаяШирота, Выборка.НачальнаяДолгота, Выборка.КонечнаяШирота, Выборка.КонечнаяДолгота)/1000;
					сткДанные.Время = сткДанные.Расстояние/60; // приближенное время из расчета, что скорость движения составляет 60 км/ч
					сткДанные.ВремяПонедельник = сткДанные.Время;
					сткДанные.ВремяВторник     = сткДанные.Время;
					сткДанные.ВремяСреда       = сткДанные.Время;
					сткДанные.ВремяЧетверг     = сткДанные.Время;
					сткДанные.ВремяПятница     = сткДанные.Время;
					сткДанные.ВремяСуббота     = сткДанные.Время;
					сткДанные.ВремяВоскресенье = сткДанные.Время;
				КонецЕсли; 
				сткДанные.ПроездНеРассчитан = Истина;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла; 
	
	Если НЕ сткКарта = Неопределено Тогда
		упМаршрутизация.ПостОбработкаКартыПоТипу(сткКарта);
	КонецЕсли;
	
	Возврат РезультатТаблица;
	
КонецФункции // ПолучитьРасстояниеИВремяМеждуТочкамиПоДаннымИБПоТаблице()

// Выполнить обращение к сервису маршрутизации
//
// Параметры:
//  ТаблицаАдресов  - ТаблицаЗначений - ...
//  КоэффициентПереводаВЧасы  - Число - ...
//  ВызыватьМаршрутизацию  - Булево - ...
//  ДополнительныеПараметры  - Структура - дополнительные параметры маршрутизации:
//   * ПараметрыМаршрутизацииСитиГИД - структура, параметры для маршрутизации ситигид
//   * ДатаНачалаПланирования - Дата, период начиная с которго выполнить маршрутизацию сервисом ситигид
//
// Возвращаемое значение:
//   Соответсвие   - Значения
//        
Функция ПолучитьРасстояниеИВремяПоТаблицеАдресов(ТаблицаАдресов, КоэффициентПереводаВЧасы, ВызыватьМаршрутизацию = Ложь, ДополнительныеПараметры = Неопределено) Экспорт
	
	//Если УправлениеПеревозками <> "1" И УправлениеТранспортом <> "1" И СпутниковыйМониторинг <> "1" Тогда 
	//	ВызватьИсключение НСтр("ru = 'Ни один из модулей системы не доступен'");
	//КонецЕсли;
	
	РезультатТаблица = Новый ТаблицаЗначений;
	РезультатТаблица.Колонки.Добавить("АдресНачальнойТочки", 	Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	РезультатТаблица.Колонки.Добавить("АдресКонечнойТочки", 	Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	РезультатТаблица.Колонки.Добавить("Данные");
	
	Если ВызыватьМаршрутизацию Тогда
		ТипМаршрутизатора = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
	КонецЕсли; 
	
	Если ВызыватьМаршрутизацию 
			И ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.СитиГИД 
			И (Не ДополнительныеПараметры = Неопределено И ДополнительныеПараметры.Свойство("ПараметрыМаршрутизацииСитиГИД")) Тогда
		
		сткКарта   = упМаршрутизация.ПодготовитьКартуПоТипу(ТипМаршрутизатора, Истина);
		
		ЗаполнитьЗначенияСвойств(сткКарта.Параметры.parameters,ДополнительныеПараметры.ПараметрыМаршрутизацииСитиГИД);
		сткКарта.Вставить("ПараметрыМаршрутизацииСитиГИД",ДополнительныеПараметры.ПараметрыМаршрутизацииСитиГИД);
		Если ДополнительныеПараметры.Свойство("ДатаНачалаПланирования") И НЕ ДополнительныеПараметры.ДатаНачалаПланирования = Неопределено Тогда
			сткКарта.Вставить("ДатаНачалаПланирования",ДополнительныеПараметры.ДатаНачалаПланирования);
		КонецЕсли;
		
		Для каждого СтрокаАдреса Из ТаблицаАдресов Цикл
		
			НачальнаяТочка 	= СтрокаАдреса.АдресНачальнойТочки;
			КонечнаяТочка 	= СтрокаАдреса.АдресКонечнойТочки;
				
			сткДанные = Новый Структура("Расстояние, Время, ВремяПонедельник, ВремяВторник, ВремяСреда, ВремяЧетверг, ВремяПятница, ВремяСуббота, ВремяВоскресенье, ПроездНеРассчитан", 0, 0, 0, 0, 0, 0, 0, 0, 0, Ложь);
			
			НоваяСтрока		= РезультатТаблица.Добавить();
			НоваяСтрока.АдресНачальнойТочки = НачальнаяТочка;
			НоваяСтрока.АдресКонечнойТочки 	= КонечнаяТочка;
			НоваяСтрока.Данные				= сткДанные;
			
			Если ЗначениеЗаполнено(НачальнаяТочка) И ЗначениеЗаполнено(КонечнаяТочка) Тогда
				
				сткВозврат = упМаршрутизация.ПолучитьРасстояниеМеждуТочкамиПоТипуКарты(сткКарта, НачальнаяТочка, КонечнаяТочка);				
				
				Если НЕ сткВозврат = Неопределено И сткВозврат.Свойство("Расстояние") И сткВозврат.Расстояние > 0 Тогда
					
					Если сткКарта.Свойство("ДатаНачалаПланирования") И сткВозврат.Время > 0  И НЕ сткКарта.ДатаНачалаПланирования = Неопределено Тогда
						сткКарта.ДатаНачалаПланирования = сткКарта.ДатаНачалаПланирования + сткВозврат.Время;
					КонецЕсли;
					
					сткДанные.Расстояние = сткВозврат.Расстояние;
					сткДанные.Время      = сткВозврат.Время/КоэффициентПереводаВЧасы;
					сткДанные.ВремяПонедельник = сткВозврат.ВремяПонедельник/КоэффициентПереводаВЧасы;
					сткДанные.ВремяВторник     = сткВозврат.ВремяВторник/КоэффициентПереводаВЧасы;
					сткДанные.ВремяСреда       = сткВозврат.ВремяСреда/КоэффициентПереводаВЧасы;
					сткДанные.ВремяЧетверг     = сткВозврат.ВремяЧетверг/КоэффициентПереводаВЧасы;
					сткДанные.ВремяПятница     = сткВозврат.ВремяПятница/КоэффициентПереводаВЧасы;
					сткДанные.ВремяСуббота     = сткВозврат.ВремяСуббота/КоэффициентПереводаВЧасы;
					сткДанные.ВремяВоскресенье = сткВозврат.ВремяВоскресенье/КоэффициентПереводаВЧасы;
				Иначе
					мсвАдресов = Новый Массив;
					мсвАдресов.Добавить(НачальнаяТочка);
					мсвАдресов.Добавить(КонечнаяТочка);
					КоординатыСоответствие	= ОбщегоНазначения.ЗначенияРеквизитовОбъектов(мсвАдресов, "Широта, Долгота");
					
					Запись = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
					Запись.НачальнаяШирота  = КоординатыСоответствие.Получить(НачальнаяТочка).Широта;
					Запись.НачальнаяДолгота = КоординатыСоответствие.Получить(НачальнаяТочка).Долгота;
					Запись.КонечнаяШирота   = КоординатыСоответствие.Получить(КонечнаяТочка).Широта;
					Запись.КонечнаяДолгота  = КоординатыСоответствие.Получить(КонечнаяТочка).Долгота;
					Запись.Прочитать();
					
					Если Запись.Выбран() Тогда
						сткДанные.Расстояние = Запись.Расстояние;
						сткДанные.Время      = Запись.Время/КоэффициентПереводаВЧасы;
						сткДанные.ВремяПонедельник = Запись.ВремяПонедельник/КоэффициентПереводаВЧасы;
						сткДанные.ВремяВторник     = Запись.ВремяВторник/КоэффициентПереводаВЧасы;
						сткДанные.ВремяСреда       = Запись.ВремяСреда/КоэффициентПереводаВЧасы;
						сткДанные.ВремяЧетверг     = Запись.ВремяЧетверг/КоэффициентПереводаВЧасы;
						сткДанные.ВремяПятница     = Запись.ВремяПятница/КоэффициентПереводаВЧасы;
						сткДанные.ВремяСуббота     = Запись.ВремяСуббота/КоэффициентПереводаВЧасы;
						сткДанные.ВремяВоскресенье = Запись.ВремяВоскресенье/КоэффициентПереводаВЧасы;
					КонецЕсли;					
				КонецЕсли;
			КонецЕсли;
	
		КонецЦикла;
		
		упМаршрутизация.ПостОбработкаКартыПоТипу(сткКарта);
		
	Иначе	
		РезультатТаблица = ПолучитьРасстояниеИВремяМеждуТочкамиПоДаннымИБПоТаблице(ТаблицаАдресов, КоэффициентПереводаВЧасы, ВызыватьМаршрутизацию);
	КонецЕсли;
	
	Возврат РезультатТаблица;
	
КонецФункции // ПолучитьРасстояниеИВремяПоТаблицеАдресов() 

#КонецОбласти