#Область ПрограммныйИнтерфейс

// Создает записи регистра "упДоступныеВодители".
//
// Параметры:
//  Ссылка - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Процедура СформироватьЗаписиДоступныеВодители(Ссылка) Экспорт

	// Если формируются записи по справочнику "ПравилаДоступностиРесурсов".
	Если ТипЗнч(Ссылка) = тип("СправочникСсылка.упПравилаДоступностиРесурсов") Тогда
		
		ВСписке = НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "СписокЗапрещенныхВодителей");
		
		// Ищутся все доступные для правила водители.
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упПравилаДоступностиРесурсовДоступныеВодители.Водитель КАК Водитель
		               |ПОМЕСТИТЬ втДоступныеВодители
		               |ИЗ
		               |	Справочник.упПравилаДоступностиРесурсов.ДоступныеВодители КАК упПравилаДоступностиРесурсовДоступныеВодители
		               |ГДЕ
		               |	упПравилаДоступностиРесурсовДоступныеВодители.Ссылка = &Правило
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Водитель
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упСотрудники.Ссылка КАК Сотрудник
		               |ИЗ
		               |	Справочник.упСотрудники КАК упСотрудники
		               |ГДЕ
		               |	упСотрудники.ТипСотрудника В(&ДоступныеТипыСотрудников)
		               |	И (&Правило = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиРесурсов.ДоступныВсе)
		               |			ИЛИ ВЫБОР
		               |				КОГДА &ВСписке
		               |					ТОГДА упСотрудники.Ссылка В
		               |							(ВЫБРАТЬ
		               |								втДоступныеВодители.Водитель
		               |							ИЗ
		               |								втДоступныеВодители)
		               |				ИНАЧЕ НЕ упСотрудники.Ссылка В
		               |							(ВЫБРАТЬ
		               |								втДоступныеВодители.Водитель
		               |							ИЗ
		               |								втДоступныеВодители)
		               |			КОНЕЦ)";
		Запрос.УстановитьПараметр("Правило", 					Ссылка);
		Запрос.УстановитьПараметр("ДоступныеТипыСотрудников", 	ПолучитьСписокДоступныхТиповСотрудников());
		Запрос.УстановитьПараметр("ВСписке", 					ВСписке);
		текВыборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписейДоступныеВодители = РегистрыСведений.упДоступныеВодители.СоздатьНаборЗаписей();
		НаборЗаписейДоступныеВодители.Отбор.Правило.Установить(Ссылка);
		Пока текВыборка.Следующий() Цикл
			текЗапись = НаборЗаписейДоступныеВодители.Добавить();
			текЗапись.Правило 	= Ссылка;
			текЗапись.Водитель 	= текВыборка.Сотрудник;
		КонецЦикла;
		НаборЗаписейДоступныеВодители.Записать();
		
	// Если формируются записи по справочнику "Сотрудники".
	Иначе	
		СписокДоступныеТипыСотрудников = ПолучитьСписокДоступныхТиповСотрудников();
	
		Если  НЕ СписокДоступныеТипыСотрудников.НайтиПоЗначению(Ссылка.ТипСотрудника) = Неопределено Тогда
			// Ищутся все правила для которых данный сотрудник доступен.
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	упПравилаДоступностиРесурсовДоступныеВодители.Ссылка КАК Правило
			|ИЗ
			|	Справочник.упПравилаДоступностиРесурсов.ДоступныеВодители КАК упПравилаДоступностиРесурсовДоступныеВодители
			|ГДЕ
			|	ВЫБОР
			|			КОГДА упПравилаДоступностиРесурсовДоступныеВодители.Ссылка.СписокЗапрещенныхВодителей
			|				ТОГДА НЕ упПравилаДоступностиРесурсовДоступныеВодители.Водитель = &Водитель
			|			ИНАЧЕ упПравилаДоступностиРесурсовДоступныеВодители.Водитель = &Водитель
			|		КОНЕЦ";
			Запрос.УстановитьПараметр("Водитель", Ссылка);
			текВыборка	= Запрос.Выполнить().Выбрать();
			
			// Добавляются записи в регистр.
			НаборЗаписейДоступныеВодители = РегистрыСведений.упДоступныеВодители.СоздатьНаборЗаписей();
			НаборЗаписейДоступныеВодители.Отбор.Водитель.Установить(Ссылка);
			текЗапись = НаборЗаписейДоступныеВодители.Добавить();
			текЗапись.Правило 	= Справочники.упПравилаДоступностиРесурсов.ДоступныВсе;
			текЗапись.Водитель 	= Ссылка;
			
			Пока текВыборка.Следующий() Цикл
				текЗапись = НаборЗаписейДоступныеВодители.Добавить();
				текЗапись.Правило 	= текВыборка.Правило;
				текЗапись.Водитель 	= Ссылка;
			КонецЦикла;
			НаборЗаписейДоступныеВодители.Записать();
		Иначе
			НаборЗаписейДоступныеВодители = РегистрыСведений.упДоступныеВодители.СоздатьНаборЗаписей();
			НаборЗаписейДоступныеВодители.Отбор.Водитель.Установить(Ссылка);
			НаборЗаписейДоступныеВодители.Записать();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // СформироватьЗаписиДоступныеВодители()

// Создает записи регистра "упДоступныеТС".
//
// Параметры:
//  Ссылка - СправочникСсылка - Ссылка на элемент.
//
Процедура СформироватьЗаписиДоступныеТС(Ссылка) Экспорт
	
		
	// Если формируются записи по справочнику "ПравилаДоступностиРесурсов".
	Если ТипЗнч(Ссылка) = тип("СправочникСсылка.упПравилаДоступностиРесурсов") Тогда
		сткРеквизиты 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "СписокЗапрещенныхТС,СписокЗапрещенныхМоделейТС");
		ЭтоРазрешенныйСписокТС = НЕ сткРеквизиты.СписокЗапрещенныхТС;
		ЭтоРазрешенныйСписокМоделей = НЕ сткРеквизиты.СписокЗапрещенныхМоделейТС;
		
		// Ищутся все доступные для правила ТС.
		Запрос = Новый Запрос;
		Запрос.Текст =
"
|ВЫБРАТЬ
|	ПравилаДоступныеТС.ТранспортноеСредство
|ПОМЕСТИТЬ втДоступныеТС
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов.ДоступныеТС КАК ПравилаДоступныеТС
|ГДЕ
|	ПравилаДоступныеТС.Ссылка = &Правило
|
|ИНДЕКСИРОВАТЬ ПО
|	ПравилаДоступныеТС.ТранспортноеСредство
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	КОЛИЧЕСТВО(*) КАК Количество
|ПОМЕСТИТЬ втКоличествоДоступныеТС
|ИЗ
|	втДоступныеТС КАК втДоступныеТС
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ПравилаДоступныМодели.МодельТС
|ПОМЕСТИТЬ втДоступныеМодели
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов.ДоступныеМоделиТС КАК ПравилаДоступныМодели
|ГДЕ
|	ПравилаДоступныМодели.Ссылка = &Правило
|
|ИНДЕКСИРОВАТЬ ПО
|	ПравилаДоступныМодели.МодельТС
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	КОЛИЧЕСТВО(*) КАК Количество
|ПОМЕСТИТЬ втКоличествоМоделей
|ИЗ
|	втДоступныеМодели КАК втДоступныеМодели
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	тбТС.Ссылка КАК ТранспортноеСредство
|ИЗ
|	Справочник.упТранспортныеСредства КАК тбТС,
|	втКоличествоДоступныеТС КАК втКоличествоДоступныеТС,
|	втКоличествоМоделей КАК втКоличествоМоделей
|ГДЕ
|	(&Правило = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиРесурсов.ДоступныВсе)
|			ИЛИ втКоличествоДоступныеТС.Количество = 0
|				И втКоличествоМоделей.Количество = 0
|				И НЕ &ЭтоРазрешенныйСписокТС
|				И НЕ &ЭтоРазрешенныйСписокМоделей
|			ИЛИ &ЭтоРазрешенныйСписокТС
|				И тбТС.Ссылка В
|					(ВЫБРАТЬ
|						втДоступныеТС.ТранспортноеСредство
|					ИЗ
|						втДоступныеТС)
|			ИЛИ втКоличествоМоделей.Количество = 0
|				И НЕ &ЭтоРазрешенныйСписокТС
|				И НЕ тбТС.Ссылка В
|						(ВЫБРАТЬ
|							втДоступныеТС.ТранспортноеСредство
|						ИЗ
|							втДоступныеТС)
|			ИЛИ (НЕ &ЭтоРазрешенныйСписокТС
|					И НЕ тбТС.Ссылка В
|							(ВЫБРАТЬ
|								втДоступныеТС.ТранспортноеСредство
|							ИЗ
|								втДоступныеТС)
|				ИЛИ втКоличествоДоступныеТС.Количество = 0)
|				И (НЕ &ЭтоРазрешенныйСписокМоделей
|						И НЕ тбТС.Модель В
|								(ВЫБРАТЬ
|									втДоступныеМодели.МодельТС
|								ИЗ
|									втДоступныеМодели)
|					ИЛИ &ЭтоРазрешенныйСписокМоделей
|						И тбТС.Модель В
|							(ВЫБРАТЬ
|								втДоступныеМодели.МодельТС
|							ИЗ
|								втДоступныеМодели)))
|";
		Запрос.УстановитьПараметр("Правило", 					Ссылка);
		Запрос.УстановитьПараметр("ЭтоРазрешенныйСписокТС", ЭтоРазрешенныйСписокТС);
		Запрос.УстановитьПараметр("ЭтоРазрешенныйСписокМоделей", ЭтоРазрешенныйСписокМоделей);
		текВыборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписейДоступныеТС = РегистрыСведений.упДоступныеТС.СоздатьНаборЗаписей();
		НаборЗаписейДоступныеТС.Отбор.Правило.Установить(Ссылка);
		Пока текВыборка.Следующий() Цикл
			текЗапись = НаборЗаписейДоступныеТС.Добавить();
			текЗапись.Правило 	= Ссылка;
			текЗапись.ТранспортноеСредство 	= текВыборка.ТранспортноеСредство;
		КонецЦикла;
		НаборЗаписейДоступныеТС.Записать();
		
	// Если формируются записи по справочнику "Транспортные средства".
	Иначе	
		
		МодельТС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Модель");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
"
|ВЫБРАТЬ РАЗЛИЧНЫЕ
|	упПравилаДоступностиРесурсовДоступныеТС.Ссылка КАК Правило,
|	КОЛИЧЕСТВО(*) КАК КоличествоСтрок,
|	ВЫБОР
|		КОГДА упПравилаДоступностиРесурсовДоступныеТС.ТранспортноеСредство = &ТранспортноеСредство
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ КАК ЕстьВСписке
|ПОМЕСТИТЬ втДоступныеТС
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов.ДоступныеТС КАК упПравилаДоступностиРесурсовДоступныеТС
|
|СГРУППИРОВАТЬ ПО
|	упПравилаДоступностиРесурсовДоступныеТС.Ссылка,
|	ВЫБОР
|		КОГДА упПравилаДоступностиРесурсовДоступныеТС.ТранспортноеСредство = &ТранспортноеСредство
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упПравилаДоступностиРесурсовДоступныеМоделиТС.Ссылка КАК Правило,
|	КОЛИЧЕСТВО(*) КАК КоличествоСтрок,
|	ВЫБОР
|		КОГДА упПравилаДоступностиРесурсовДоступныеМоделиТС.МодельТС = &МодельТС
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ КАК ЕстьВСписке
|ПОМЕСТИТЬ втДоступныеМодели
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов.ДоступныеМоделиТС КАК упПравилаДоступностиРесурсовДоступныеМоделиТС
|
|СГРУППИРОВАТЬ ПО
|	упПравилаДоступностиРесурсовДоступныеМоделиТС.Ссылка,
|	ВЫБОР
|		КОГДА упПравилаДоступностиРесурсовДоступныеМоделиТС.МодельТС = &МодельТС
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упПравилаДоступностиРесурсов.Ссылка КАК Правило,
|	ЕСТЬNULL(втДоступныеТС.КоличествоСтрок, 0) КАК КоличествоСтрокТС,
|	ВЫБОР
|		КОГДА ЕСТЬNULL(втДоступныеТС.ЕстьВСписке, 0) > 0
|			ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ КАК ЕстьВСпискеТС,
|	ЕСТЬNULL(втДоступныеМодели.КоличествоСтрок, 0) КАК КоличествоСтрокМоделиТС,
|	ВЫБОР
|		КОГДА ЕСТЬNULL(втДоступныеМодели.ЕстьВСписке, 0) > 0
|			ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ КАК ЕстьВСпискеМоделиТС,
|	НЕ упПравилаДоступностиРесурсов.СписокЗапрещенныхТС КАК ЭтоРазрешенныйСписокТС,
|	НЕ упПравилаДоступностиРесурсов.СписокЗапрещенныхМоделейТС КАК ЭтоРазрешенныйСписокМоделей
|ПОМЕСТИТЬ втПравила
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов КАК упПравилаДоступностиРесурсов
|		ЛЕВОЕ СОЕДИНЕНИЕ втДоступныеМодели КАК втДоступныеМодели
|		ПО (втДоступныеМодели.Правило = упПравилаДоступностиРесурсов.Ссылка)
|		ЛЕВОЕ СОЕДИНЕНИЕ втДоступныеТС КАК втДоступныеТС
|		ПО (втДоступныеТС.Правило = упПравилаДоступностиРесурсов.Ссылка)
|ГДЕ
|	НЕ упПравилаДоступностиРесурсов.Ссылка = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиРесурсов.ДоступныВсе)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втПравила.Правило,
|	втПравила.КоличествоСтрокТС,
|	втПравила.ЕстьВСпискеТС,
|	втПравила.КоличествоСтрокМоделиТС,
|	втПравила.ЕстьВСпискеМоделиТС
|ИЗ
|	втПравила КАК втПравила
|ГДЕ
|	(втПравила.КоличествоСтрокТС = 0
|				И втПравила.КоличествоСтрокМоделиТС = 0
|				И НЕ втПравила.ЭтоРазрешенныйСписокТС
|				И НЕ втПравила.ЭтоРазрешенныйСписокМоделей
|			ИЛИ втПравила.ЭтоРазрешенныйСписокТС
|				И втПравила.ЕстьВСпискеТС
|			ИЛИ втПравила.КоличествоСтрокМоделиТС = 0
|				И НЕ втПравила.ЭтоРазрешенныйСписокТС
|				И НЕ втПравила.ЕстьВСпискеТС
|			ИЛИ (НЕ втПравила.ЭтоРазрешенныйСписокТС
|					И НЕ втПравила.ЕстьВСпискеТС
|				ИЛИ втПравила.КоличествоСтрокТС = 0)
|				И (НЕ втПравила.ЭтоРазрешенныйСписокМоделей
|						И НЕ втПравила.ЕстьВСпискеМоделиТС
|					ИЛИ втПравила.ЭтоРазрешенныйСписокМоделей
|						И втПравила.ЕстьВСпискеМоделиТС))
|";
		Запрос.УстановитьПараметр("ТранспортноеСредство", Ссылка);
		Запрос.УстановитьПараметр("МодельТС", МодельТС);
				
		текВыборка	= Запрос.Выполнить().Выбрать();
		
		НаборЗаписейДоступныеТС = РегистрыСведений.упДоступныеТС.СоздатьНаборЗаписей();
		НаборЗаписейДоступныеТС.Отбор.ТранспортноеСредство.Установить(Ссылка);
		текЗапись = НаборЗаписейДоступныеТС.Добавить();
		текЗапись.Правило 	= Справочники.упПравилаДоступностиРесурсов.ДоступныВсе;
		текЗапись.ТранспортноеСредство 	= Ссылка;
		
		Пока текВыборка.Следующий() Цикл
			текЗапись = НаборЗаписейДоступныеТС.Добавить();
			текЗапись.Правило 	= текВыборка.Правило;
			текЗапись.ТранспортноеСредство 	= Ссылка;
		КонецЦикла;
		НаборЗаписейДоступныеТС.Записать();
	КонецЕсли;

КонецПроцедуры

// Создает записи регистра "упДоступныеМоделиТранспорта".
//
// Параметры:
//  Ссылка - СправочникСсылка - Ссылка на элемент.
//
Процедура СформироватьЗаписиДоступныеМоделиТранспорта(Ссылка) Экспорт
	
		
	// Если формируются записи по справочнику "ПравилаДоступностиРесурсов".
	Если ТипЗнч(Ссылка) = тип("СправочникСсылка.упПравилаДоступностиРесурсов") Тогда
		сткРеквизиты 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "СписокЗапрещенныхМоделейТС");
		ЭтоРазрешенныйСписок = НЕ сткРеквизиты.СписокЗапрещенныхМоделейТС;
		
		// Ищутся все доступные для правила модели.
		Запрос = Новый Запрос;
		Запрос.Текст = 
"
|//{Запрос: 0 ////////////////////////////////////////
|ВЫБРАТЬ
|	ПравилаДоступныМодели.МодельТС КАК МодельТранспорта
|ПОМЕСТИТЬ втДоступныеМодели
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов.ДоступныеМоделиТС КАК ПравилаДоступныМодели
|ГДЕ
|	ПравилаДоступныМодели.Ссылка = &Правило
|ИНДЕКСИРОВАТЬ ПО
|	ПравилаДоступныМодели.МодельТС
|;
|//{Запрос: 1 ////////////////////////////////////////
|ВЫБРАТЬ
|	КОЛИЧЕСТВО(*) КАК Количество
|ПОМЕСТИТЬ втКоличествоМоделей
|ИЗ
|	втДоступныеМодели КАК втДоступныеМодели
|;
|//{Запрос: 2 ////////////////////////////////////////
|ВЫБРАТЬ
|	МоделиТранспорта.Ссылка КАК МодельТранспорта
|ИЗ
|	Справочник.упМоделиТС КАК МоделиТранспорта,
|	втКоличествоМоделей КАК втКоличествоМоделей
|ГДЕ
|	&Правило = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиРесурсов.ДоступныВсе)
|		ИЛИ (втКоличествоМоделей.Количество = 0
|			И НЕ (&ЭтоРазрешенныйСписок))
|		ИЛИ (НЕ (&ЭтоРазрешенныйСписок)
|			И НЕ (МоделиТранспорта.Ссылка В (
|					ВЫБРАТЬ
|						втДоступныеМодели.МодельТранспорта КАК МодельТранспорта
|					ИЗ
|						втДоступныеМодели КАК втДоступныеМодели)))
|		ИЛИ (&ЭтоРазрешенныйСписок
|			И МоделиТранспорта.Ссылка В (
|					ВЫБРАТЬ
|						втДоступныеМодели.МодельТранспорта КАК МодельТранспорта
|					ИЗ
|						втДоступныеМодели КАК втДоступныеМодели))
|";
		Запрос.УстановитьПараметр("Правило", 					Ссылка);
		Запрос.УстановитьПараметр("ЭтоРазрешенныйСписок", 			ЭтоРазрешенныйСписок);
		текВыборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписейДоступныеТС = РегистрыСведений.упДоступныеМоделиТранспорта.СоздатьНаборЗаписей();
		НаборЗаписейДоступныеТС.Отбор.Правило.Установить(Ссылка);
		Пока текВыборка.Следующий() Цикл
			текЗапись = НаборЗаписейДоступныеТС.Добавить();
			текЗапись.Правило 	= Ссылка;
			текЗапись.МодельТранспорта = текВыборка.МодельТранспорта;
		КонецЦикла;
		НаборЗаписейДоступныеТС.Записать();
		
	// Если формируются записи по справочнику "Модели транспорта".
	Иначе	
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
"
|//{Запрос: 0 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упПравилаДоступностиРесурсовДоступныеМоделиТС.Ссылка КАК Правило,
|	КОЛИЧЕСТВО(*) КАК КоличествоСтрок,
|	ВЫБОР
|		КОГДА упПравилаДоступностиРесурсовДоступныеМоделиТС.МодельТС = &МодельТранспорта
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ КАК ЕстьВСписке
|ПОМЕСТИТЬ втДоступныеМодели
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов.ДоступныеМоделиТС КАК упПравилаДоступностиРесурсовДоступныеМоделиТС
|СГРУППИРОВАТЬ ПО
|	упПравилаДоступностиРесурсовДоступныеМоделиТС.Ссылка,
|	ВЫБОР
|		КОГДА упПравилаДоступностиРесурсовДоступныеМоделиТС.МодельТС = &МодельТранспорта
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ
|;
|//{Запрос: 1 ////////////////////////////////////////
|//////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упПравилаДоступностиРесурсов.Ссылка КАК Правило,
|	ЕСТЬNULL(втДоступныеМодели.КоличествоСтрок, 0) КАК КоличествоСтрокМоделиТС,
|	ВЫБОР
|		КОГДА ЕСТЬNULL(втДоступныеМодели.ЕстьВСписке, 0) > 0
|			ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ КАК ЕстьВСпискеМоделиТС,
|	НЕ упПравилаДоступностиРесурсов.СписокЗапрещенныхМоделейТС КАК ЭтоРазрешенныйСписок
|ПОМЕСТИТЬ втПравила
|ИЗ
|	Справочник.упПравилаДоступностиРесурсов КАК упПравилаДоступностиРесурсов
|	ЛЕВОЕ СОЕДИНЕНИЕ втДоступныеМодели КАК втДоступныеМодели
|	ПО втДоступныеМодели.Правило = упПравилаДоступностиРесурсов.Ссылка
|ГДЕ
|	НЕ (упПравилаДоступностиРесурсов.Ссылка = ЗНАЧЕНИЕ(Справочник.упПравилаДоступностиРесурсов.ДоступныВсе))
|;
|//{Запрос: 2 ////////////////////////////////////////
|////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втПравила.Правило КАК Правило,
|	втПравила.КоличествоСтрокМоделиТС КАК КоличествоСтрокМоделиТС,
|	втПравила.ЕстьВСпискеМоделиТС КАК ЕстьВСпискеМоделиТС
|ИЗ
|	втПравила КАК втПравила
|ГДЕ
|	(втПравила.КоличествоСтрокМоделиТС = 0
|			И НЕ (втПравила.ЭтоРазрешенныйСписок))
|		ИЛИ (НЕ (втПравила.ЭтоРазрешенныйСписок)
|			И НЕ (втПравила.ЕстьВСпискеМоделиТС))
|		ИЛИ (втПравила.ЭтоРазрешенныйСписок
|			И втПравила.ЕстьВСпискеМоделиТС)
|";
		Запрос.УстановитьПараметр("МодельТранспорта", Ссылка);
				
		текВыборка	= Запрос.Выполнить().Выбрать();
		
		НаборЗаписейДоступныеТС = РегистрыСведений.упДоступныеМоделиТранспорта.СоздатьНаборЗаписей();
		НаборЗаписейДоступныеТС.Отбор.МодельТранспорта.Установить(Ссылка);
		текЗапись = НаборЗаписейДоступныеТС.Добавить();
		текЗапись.Правило = Справочники.упПравилаДоступностиРесурсов.ДоступныВсе;
		текЗапись.МодельТранспорта = Ссылка;
		
		Пока текВыборка.Следующий() Цикл
			текЗапись = НаборЗаписейДоступныеТС.Добавить();
			текЗапись.Правило = текВыборка.Правило;
			текЗапись.МодельТранспорта = Ссылка;
		КонецЦикла;
		НаборЗаписейДоступныеТС.Записать();
	КонецЕсли;

КонецПроцедуры

// Вычисляет доступность транспорта по таблице периодов занятости.
//
// Параметры:
//  Транспорт				 - СправочникСсылка.упТранспортныеСредства	 - ПериодыЗанятости - ТаблицаЗначений - выборка из РегистрСведений.упЗанятостьТранспорта
//  НачалоРейса			 - Дата								 - начало.
//  ОкончаниеРейса			 - Дата								 - окончание.
//  ВсеПериодыЗанятости		 - ТаблицаЗначений						 - периоды.
//  УчитыватьМестоположенияТС	 - Булево								 - признак учета местоположения.
//  ЗонаНачала				 - СправочникСсылка.упГеографическиеЗоны	 - зона начала.
//  ЗонаОкончания			 - СправочникСсылка.упГеографическиеЗоны	 - зона окончания.
//  ИерархияУчетныхЗон		 - ТаблицаЗначений						 - выборка из РегистрСведений.упИерархияГеографическихЗон
// 
// Возвращаемое значение:
//  Булево - доступен ли транспорт
//
Функция ТранспортДоступенПоЗанятости(Транспорт, НачалоРейса, ОкончаниеРейса, ВсеПериодыЗанятости, УчитыватьМестоположенияТС, ЗонаНачала, ЗонаОкончания, ИерархияУчетныхЗон) Экспорт
	
	ТранспортДоступен = Истина;
	ПериодыЗанятостиТранспорта = ВсеПериодыЗанятости.НайтиСтроки(Новый Структура("ТранспортноеСредство", Транспорт));
	ПоследнийПериодДо = Неопределено;
	ПервыйПериодПосле = Неопределено;
	Для Каждого ПериодЗанятости Из ПериодыЗанятостиТранспорта Цикл
		Если Ложь // Период рейса пересекается с периодом занятости
			Или (Истина
				И НачалоРейса > ПериодЗанятости.ДатаНачала
				И НачалоРейса < ПериодЗанятости.ДатаОкончания)
			Или (Истина
				И ОкончаниеРейса > ПериодЗанятости.ДатаНачала
				И ОкончаниеРейса < ПериодЗанятости.ДатаОкончания)
			Или (Истина
				И НачалоРейса < ПериодЗанятости.ДатаНачала
				И ОкончаниеРейса > ПериодЗанятости.ДатаНачала)
			Или (Истина
				И НачалоРейса < ПериодЗанятости.ДатаОкончания
				И ОкончаниеРейса > ПериодЗанятости.ДатаОкончания)
		Тогда
			ТранспортДоступен = Ложь;
			Прервать;
		КонецЕсли; 
		Если УчитыватьМестоположенияТС Тогда 
			Если Истина
				И ПериодЗанятости.ДатаОкончания < НачалоРейса
				И (Ложь
					Или ПоследнийПериодДо = Неопределено 
					Или ПоследнийПериодДо.ДатаОкончания < ПериодЗанятости.ДатаОкончания
				   )
			Тогда
				ПоследнийПериодДо = ПериодЗанятости;
			КонецЕсли; 
			Если Истина
				И ПериодЗанятости.ДатаОкончания > ОкончаниеРейса
				И (Ложь
					Или ПервыйПериодПосле = Неопределено 
					Или ПервыйПериодПосле.ДатаОкончания > ПериодЗанятости.ДатаОкончания
				   )
			Тогда
				ПервыйПериодПосле = ПериодЗанятости;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	Если ТранспортДоступен И ПоследнийПериодДо <> Неопределено Тогда
		ТранспортДоступен = Ложь;
		УчетныеЗоны = ИерархияУчетныхЗон.НайтиСтроки(Новый Структура("Родитель", ПоследнийПериодДо.ЗонаОкончания));
		Для Каждого СтрокаУчетнойЗоны Из УчетныеЗоны Цикл
			ТранспортДоступен = СтрокаУчетнойЗоны.Зона = ЗонаНачала;
			Если ТранспортДоступен Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	Если ТранспортДоступен И ПервыйПериодПосле <> Неопределено Тогда
		ТранспортДоступен = Ложь;
		УчетныеЗоны = ИерархияУчетныхЗон.НайтиСтроки(Новый Структура("Родитель", ПервыйПериодПосле.ЗонаНачала));
		Для Каждого СтрокаУчетнойЗоны Из УчетныеЗоны Цикл
			ТранспортДоступен = СтрокаУчетнойЗоны.Зона = ЗонаОкончания;
			Если ТранспортДоступен Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	Возврат ТранспортДоступен;
КонецФункции

// Выполнить получение списка доступных типов сотрудников.
// 
// Возвращаемое значение:
//  СписокЗначений - Результат выполнения.
//
Функция ПолучитьСписокДоступныхТиповСотрудников() Экспорт 

	Результат = Новый СписокЗначений;
	Результат.Добавить(Перечисления.упТипыСотрудников.Водитель);
	Результат.Добавить(Перечисления.упТипыСотрудников.ВодительЭкспедитор);
	
	Возврат Результат;

КонецФункции // ПолучитьСписокДоступныхТиповСотрудников()
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти 
