
#Область ПрограммныйИнтерфейс

#Если ТолстыйКлиентУправляемоеПриложение Тогда
#Иначе
// Функция - httpPushArrayOfMessagePUT
//
// Параметры:
//  Запрос	- запрос для http-сервиса.
// 
// Возвращаемое значение:
// Строка  -  Ответ, форммируемый http-сервисом.
//
Функция httpPushArrayOfMessagePUT(Запрос) Экспорт
	
	httpОтвет = Новый HTTPСервисОтвет(200);
	Попытка
		форматПакета			= сшпКэшируемыеФункции.ПолучитьТипСообщения(Запрос.Заголовки["Content-Type"]);
		телоПакета 				= ПолучитьТелоСообщения(Запрос, форматПакета);
		xdtoТип 				= ФабрикаXDTO.Тип("http://esb.axelot.ru", "ArrayOfMessage1C");
		//xdtoТип 				= ФабрикаXDTO.Тип("http://esb.axelot.ru", "Messages");
		xdtoПакет 				= сшпОбщегоНазначения.ПолучитьОбъектXDTO(форматПакета, телоПакета, xdtoТип);
		МассивСообщений = xdtoПакет.ПолучитьСписок("Message1C");
		//МассивСообщений = xdtoПакет.ПолучитьСписок("Message");
		Если НЕ МассивСообщений = Неопределено тогда
			Для  каждого xdtoСообщение из МассивСообщений цикл
				httpОтвет.КодСостояния	= сшпВзаимодействиеСАдаптером.ПолучениеСообщения(форматПакета, xdtoСообщение);
			КонецЦикла;
		КонецЕсли;
	Исключение
		ошибкаИнформация = ИнформацияОбОшибке();
		ошибкаПредставление 	= ПодробноеПредставлениеОшибки(ошибкаИнформация);
		httpОтвет.Причина 		= КраткоеПредставлениеОшибки(ошибкаИнформация);
		httpОтвет.КодСостояния	= 500; //неверный запрос
		httpОтвет.УстановитьТелоИзСтроки(ошибкаПредставление);
		ЗаписьЖурналаРегистрации("Datareon. Взаимодействие с адаптером", УровеньЖурналаРегистрации.Ошибка,,, ошибкаПредставление);
	КонецПопытки;
	
	Возврат httpОтвет;
	
КонецФункции

// Функция - httpPushMessageBatchPOST
//
// Параметры:
//  Запрос	- запрос для http-сервиса.
// 
// Возвращаемое значение:
// Строка  -  Ответ, форммируемый http-сервисом.
//
Функция httpPushMessageBatchPOST(Запрос) Экспорт
	
	httpОтвет = Новый HTTPСервисОтвет(200);
	Попытка
		форматПакета			= сшпКэшируемыеФункции.ПолучитьТипСообщения(Запрос.Заголовки["Content-Type"]);
		телоПакета 				= ПолучитьТелоСообщения(Запрос, форматПакета);
		xdtoТип 				= ФабрикаXDTO.Тип("http://esb.axelot.ru", "Messages");
		xdtoПакет 				= сшпОбщегоНазначения.ПолучитьОбъектXDTO(форматПакета, телоПакета, xdtoТип);
		МассивСообщений = xdtoПакет.ПолучитьСписок("Message");
		Если НЕ МассивСообщений = Неопределено тогда
			Для  каждого xdtoСообщение из МассивСообщений цикл
				httpОтвет.КодСостояния	= сшпВзаимодействиеСАдаптером.ПолучениеСообщения(форматПакета, xdtoСообщение);
			КонецЦикла;
		КонецЕсли;
	Исключение
		ошибкаИнформация = ИнформацияОбОшибке();
		ошибкаПредставление 	= ПодробноеПредставлениеОшибки(ошибкаИнформация);
		httpОтвет.Причина 		= КраткоеПредставлениеОшибки(ошибкаИнформация);
		httpОтвет.КодСостояния	= 500; //неверный запрос
		httpОтвет.УстановитьТелоИзСтроки(ошибкаПредставление);
		ЗаписьЖурналаРегистрации("Datareon. Взаимодействие с адаптером", УровеньЖурналаРегистрации.Ошибка,,, ошибкаПредставление);
	КонецПопытки;
	
	Возврат httpОтвет;
	
КонецФункции

// Функция - httpPushMessageGET
//
// Параметры:
//  Запрос	- запрос для http-сервиса.
// 
// Возвращаемое значение:
// Строка  - Ответ, форммируемый http-сервисом. 
//
Функция httpPushMessageGET(Запрос) Экспорт
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("Сервис работает.");
	Возврат Ответ;
	
КонецФункции

// Функция - httpPushMessageBatchGET
//
// Параметры:
//  Запрос	- запрос для http-сервиса.
// 
// Возвращаемое значение:
// Строка  - Ответ, форммируемый http-сервисом. 
//
Функция httpPushMessageBatchGET(Запрос) Экспорт
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("Сервис работает.");
	Возврат Ответ;
	
КонецФункции

// Функция - httpPushMessagePOST
//
// Параметры:
//  Запрос	- запрос для http-сервиса.
// 
// Возвращаемое значение:
// Строка  - Ответ, форммируемый http-сервисом. 
//
Функция httpPushMessagePOST(Запрос) Экспорт
	httpОтвет = Новый HTTPСервисОтвет(200);
	Попытка
		форматПакета			= сшпКэшируемыеФункции.ПолучитьТипСообщения(Запрос.Заголовки["Content-Type"]);
		телоПакета 				= ПолучитьТелоСообщения(Запрос, форматПакета);
		xdtoТип 				= ФабрикаXDTO.Тип("http://esb.axelot.ru", "Message");
		xdtoПакет 				= сшпОбщегоНазначения.ПолучитьОбъектXDTO(форматПакета, телоПакета, xdtoТип);
		httpОтвет.КодСостояния	= сшпВзаимодействиеСАдаптером.ПолучениеСообщения(форматПакета, xdtoПакет);
	Исключение
		ошибкаИнформация = ИнформацияОбОшибке();
		ошибкаПредставление 	= ПодробноеПредставлениеОшибки(ошибкаИнформация);
		httpОтвет.Причина 		= КраткоеПредставлениеОшибки(ошибкаИнформация);
		httpОтвет.КодСостояния	= 500; //неверный запрос
		httpОтвет.УстановитьТелоИзСтроки(ошибкаПредставление);
		ЗаписьЖурналаРегистрации("Datareon. Взаимодействие с адаптером", УровеньЖурналаРегистрации.Ошибка,,, ошибкаПредставление);
	КонецПопытки;
	
	Возврат httpОтвет;
КонецФункции

#КонецЕсли

// Функция - ОтправитьСообщение
//
// Параметры:
//  httpСоединение - текущее http-cоединение. 
//  Сообщение - сообщение, которое необходимо отправить.
// 
// Возвращаемое значение:
// текРезультат  - Ответ, форммируемый http-сервисом - результат отправки сообщения.
//
Функция ОтправитьСообщение(httpСоединение, Сообщение) Экспорт
	текРезультат = Ложь;
	Попытка
		// +++.17/08/22-10:45:00.<УП>
		//ИмяТочкиПодключенияESB = ?(ТипЗнч(Сообщение) = Тип("Массив"), "/sendMessageBatch", "/sendMessage");
		Если сшпФункциональныеОпции.ТипОбмена() = Перечисления.упТипОбмена.Web Тогда 
			ИмяТочкиПодключенияESB = ?(ТипЗнч(Сообщение) = Тип("Массив"), "/PushMessageBatch", "/PushMessage");
		Иначе
			ИмяТочкиПодключенияESB = ?(ТипЗнч(Сообщение) = Тип("Массив"), "/sendMessageBatch", "/sendMessage");
		КонецЕсли;
		// ---.17/08/22-10:45:00.<УП>
		httpЗапрос = сшпКэшируемыеФункции.ПолучитьHTTPЗапрос(сшпФункциональныеОпции.ИмяСервисаESB(), ИмяТочкиПодключенияESB, сшпФункциональныеОпции.ФорматСообщения());
		xmlПакет = сшпОбщегоНазначения.СформироватьСообщениеESB_HTTP(ФабрикаXDTO, Сообщение);
		httpЗапрос.УстановитьТелоИзСтроки(xmlПакет, "CESU-8");
		httpОтвет = httpСоединение.ОтправитьДляОбработки(httpЗапрос);
		
		текРезультат = httpОтвет.КодСостояния = 200;
		текЗапись = Неопределено;
		Если НЕ текРезультат тогда
			ЗаписьЖурналаРегистрации("Datareon. Взаимодействие с адаптером", УровеньЖурналаРегистрации.Ошибка,,, "Код ошибки: " + httpОтвет.КодСостояния + ". " + httpОтвет.ПолучитьТелоКакСтроку());
		КонецЕсли	
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации("Datareon. Взаимодействие с адаптером", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	Возврат текРезультат;
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТелоСообщения(Запрос, Формат)
	Возврат ?(Формат = Перечисления.сшпФорматыСообщений.FastInfoset, Запрос.ПолучитьТелоКакДвоичныеДанные(), Запрос.ПолучитьТелоКакСтроку());
КонецФункции	

#КонецОбласти