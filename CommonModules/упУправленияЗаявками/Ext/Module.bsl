#Область ПрограммныйИнтерфейс

// Функция проверяет корректность остатков в регистре Заявки к выполнению,
// 	Возвращает информацию: есть ли еще грузы к планированию.
//
// Параметры:
//	Заявка           - ДокументСсылка.упЗаявкаНаПеревозкуГруза - ссылка на заявку.
//                    - Массив                                  - заявки (ДокументСсылка.упЗаявкаНаПеревозкуГруза)
//                                                                на перевозку груза.
//	Отказ            - Булево                                  - Истина, в случае ошибки.
//	ДокументКонтроля - ДокументСсылка                          - ссылка на документ в котором происходит контроль 
//                                                                выполнения заявок.
//
// Возвращаемое значение:
//	Булево - Результат выполнения.
//
Функция ПроконтролироватьЗаявкиКВыполнению(Заявка, Отказ, ДокументКонтроля) Экспорт
	
	Если Отказ Тогда 
		Возврат Ложь; 
	КонецЕсли;
	
	ЕстьНеСпланированныеГрузы = Ложь;
	
	ТипДокумента = ТипЗнч(ДокументКонтроля);
	
	Если ТипЗнч(Заявка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		мсвЗаявки = Новый Массив;
		мсвЗаявки.Добавить(Заявка);
	Иначе	
	     мсвЗаявки = Заявка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкиКВыполнениюОстатки.Заявка КАК Заявка,
	|	упЗаявкиКВыполнениюОстатки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упЗаявкиКВыполнениюОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	упЗаявкиКВыполнениюОстатки.Заявка.ВидТранспортнойУслуги КАК ЗаявкаВидТранспортнойУслуги,
	|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(, Заявка В (&Заявки)) КАК упЗаявкиКВыполнениюОстатки";
	Запрос.УстановитьПараметр("Заявки", мсвЗаявки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если Выборка.КоличествоОстаток < 0 Тогда 
			ТекстСообщения = "";
			ЗаявкаТекущая = Выборка.Заявка;
			Если Выборка.ЗаявкаВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов Тогда
				Если ЗначениеЗаполнено(Выборка.ГрузовоеМесто) Тогда 
					Если ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда 
						ТекстСообщения = СтрШаблон(НСтр("ru = 'По грузовому месту %1 уже оформлено задание на перевозку, его нельзя исключить из %2.'"), Выборка.ГрузовоеМесто, ЗаявкаТекущая);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументКонтроля,,, Отказ);
					Иначе
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По грузовому месту %1 нет плана для включения в задание (уже оформлено задание на перевозку или нет плана по грузовому месту в %2.'"), Выборка.ГрузовоеМесто, ЗаявкаТекущая);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументКонтроля,,, Отказ);
					КонецЕсли;
				Иначе
					Если ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда 
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По %1 уже оформлено задание на перевозку, ее нельзя отменить.'"), ЗаявкаТекущая);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументКонтроля,,, Отказ);
					Иначе
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По заявке(%1) уже оформлено задание на перевозку.'"), ЗаявкаТекущая);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументКонтроля,,, Отказ);
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если ТипДокумента = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда 
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По %1 уже выдан путевой лист, ее нельзя отменить.'"), ЗаявкаТекущая);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументКонтроля,,, Отказ);
				Иначе
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'По заявке(%1) уже оформлены путевые листы.'"), ЗаявкаТекущая);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументКонтроля,,, Отказ);
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ДокументКонтроля, ДокументКонтроля, ТекстСообщения); 
			КонецЕсли;
		ИначеЕсли Выборка.КоличествоОстаток > 0 Тогда 
			ЕстьНеСпланированныеГрузы = Истина;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ЕстьНеСпланированныеГрузы;
	
КонецФункции	

// Контролируется непревышение количества уровней вложенности грузов в тару. Максимальное количество уровней вложенности = 3
//
// Параметры:
//	Ссылка - ДокументСсылка.упЗаявкаНаПеревозкуГруза/ДокументСсылка.упЗаданиеНаПеревозкуГруза
//	Отказ - Булево
//
Процедура ПроконтролироватьОграничениеПоУровнямВложенностиГрузов(Ссылка, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если Не ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	Документ." + ?(ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза"), "упЗаявкаНаПеревозкуГруза", "упЗаданиеНаПеревозкуГруза") + ".ГрузовыеМеста КАК упГрузовыеМеста
	|ГДЕ
	|	упГрузовыеМеста.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньОдинТри
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.Тара = втГрузовыеМестаТара.ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньДваТри
	|ИЗ
	|	втУровеньОдинТри КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньОдинТри КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ПОМЕСТИТЬ втУровеньТри
	|ИЗ
	|	втУровеньДваТри КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньДваТри КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	втГрузовыеМестаТара.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаТара.Тара КАК Тара
	|ИЗ
	|	втУровеньТри КАК втГрузовыеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУровеньТри КАК втГрузовыеМестаТара
	|		ПО втГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаТара.Тара";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Превышено максимальное количество уровней вложенности грузов (поддерживается не более трех)!'"), Ссылка,,, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

// Функция - Провести проверки при переводе заявки в статус к выполнению
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Применить изменения.
//
Процедура ПровестиПроверкиПриПереводеЗаявкиВСтатусКВыполнению(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	// Если статус устанавливается по кнопке из документа или по событию
	Если Истина
		И ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению 
		И ДополнительныеСвойства.ТекущийСтатус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению Тогда
			
		// Проверка не рассчитанных плановых доходов
		Если ДополнительныеСвойства.ВестиУчетРасчетовСЗаказчиками Тогда 
			
			ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов = ДополнительныеСвойства.ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов;		 
			Если ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов Тогда
				Для каждого Строка Из Реквизиты.ПлановыеДоходы Цикл
					Если Строка.Сумма = 0 Тогда
						ТекстСообщения = Нстр("ru = 'Не заполнена колонка ""Всего с НДС"" в строке %1 списка ""Плановые доходы""'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.НомерСтроки);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ПлановыеДоходы", Строка.НомерСтроки, "Сумма"),, Отказ);
						ЗаписьЖурналаРегистрации("Проверка при переводе заявки в статус ""К выполнению""", УровеньЖурналаРегистрации.Предупреждение, ,Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
						Прервать;
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка попадания во временные окна
		
		// Если в виде перевозки указано контролировать попадания во временные окна и это мультимодальная перевозка
		Если Истина
			И Реквизиты.ИспользуютсяМультимодальныеПеревозки
			И ДополнительныеСвойства.КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению 
			И ЗначениеЗаполнено(Реквизиты.СхемаМультимодальнойПеревозки)
		Тогда
						
			// Если известна дата границы отправления "Отправление с"
			Если ЗначениеЗаполнено(Реквизиты.ОтправлениеГрузаС) Тогда
				
			    // Если известна дата "Получения по"
				Если ЗначениеЗаполнено(Реквизиты.ПолучениеГрузаПо) Тогда
					
					// Рассчитывается дата прибытия и смотрится не позже ли она "Получения по"
					ДатаПрибытияВПоследнююТочку = РассчитатьДатуПрибытияВПоследнююТочку(Реквизиты.АдресОтправления, Реквизиты.ОтправлениеГрузаС, Реквизиты.АдресПолучения, Реквизиты.СхемаМультимодальнойПеревозки);					
					Если ДатаПрибытияВПоследнююТочку > Реквизиты.ПолучениеГрузаПо Тогда
						ТекстСообщения = Нстр("ru = 'Рассчитанная дата получения груза, учитывающая время прохождения всех точек %1, не попадает во временной интервал получения груза'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДатаПрибытияВПоследнююТочку);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
						ЗаписьЖурналаРегистрации("Проверка при переводе заявки в статус ""К выполнению""", УровеньЖурналаРегистрации.Предупреждение, ,Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					КонецЕсли;
					
				// Если есть график работы в точке получения
				ИначеЕсли ЗначениеЗаполнено(Реквизиты.ГрафикРаботыАдресаПолучения) Тогда
					
					// Рассчитывается дата прибытия и проверяется по графику
					ДатаПрибытияВПоследнююТочку = РассчитатьДатуПрибытияВПоследнююТочку(Реквизиты.АдресОтправления, Реквизиты.ОтправлениеГрузаС, Реквизиты.АдресПолучения, Реквизиты.СхемаМультимодальнойПеревозки);					
					
					Запрос = Новый Запрос;
					МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
					Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    					ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(МенеджерВременныхТаблиц, Реквизиты.ГрафикРаботыАдресаПолучения, НачалоДня(ДатаПрибытияВПоследнююТочку), КонецДня(ДатаПрибытияВПоследнююТочку));
					Запрос.Текст = "ВЫБРАТЬ
					               |	ВТРасписанияРаботы.ВремяОкончания,
					               |	ВТРасписанияРаботы.ДатаГрафика,
					               |	ВТРасписанияРаботы.ВремяНачала,
					               |	ВТРасписанияРаботы.ВремяОкончания
					               |ИЗ
					               |	ВТРасписанияРаботы КАК ВТРасписанияРаботы";
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						КоличествоСекунд = Выборка.ВремяОкончания - '0001.01.01';
						Если ДатаПрибытияВПоследнююТочку > Выборка.ДатаГрафика + КоличествоСекунд Тогда
							ТекстСообщения = Нстр("ru = 'Рассчитанная дата получения груза, учитывающая время прохождения всех точек %1, не попадает во временной интервал графика на дату %2 (время работы с %3 по %4)'");
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДатаПрибытияВПоследнююТочку, Формат(Выборка.ДатаГрафика, "ДФ=dd.MM.yyyy"), Формат(Выборка.ВремяНачала,"ДЛФ=T"), Формат(Выборка.ВремяОкончания,"ДЛФ=T"));
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
							ЗаписьЖурналаРегистрации("Проверка при переводе заявки в статус ""К выполнению""", УровеньЖурналаРегистрации.Предупреждение, ,Ссылка, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
						КонецЕсли;	
					КонецЕсли;
				
				Иначе
				    // Не контролируется
				КонецЕсли;
			Иначе
			    // Не контролировать
		    КонецЕсли;
		    
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

Функция РассчитатьДатуПрибытияВПоследнююТочку(АдресОтправления, ОтправлениеС, АдресПолучения, СхемаМультимодальнойПеревозки)
	
	ПлановоеВремяПрибытия = ОтправлениеС;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	&АдресОтправления,
	               |	&АдресПолучения,
	               |	упСхемыМультимодальныхПеревозок.ВремяВПутиДоКонечнойТочки,
	               |	ЕстьNull(упАдресОтправления.ВидАдреса.ВремяПрохожденияТочки,0) КАК ВремяПрохожденияТочкиОтправления,
	               |	ЕстьNull(упАдресПрибытия.ВидАдреса.ВремяПрохожденияТочки,0) КАК ВремяПрохожденияТочкиПрибытия
	               |ПОМЕСТИТЬ втТочкиОтправленияПрибытия
	               |ИЗ
	               |	Справочник.упСхемыМультимодальныхПеревозок КАК упСхемыМультимодальныхПеревозок
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресОтправления
	               |		ПО (упАдресОтправления.Ссылка = &АдресОтправления)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресПрибытия
	               |		ПО (упАдресПрибытия.Ссылка = &АдресПолучения)
	               |ГДЕ
	               |	упСхемыМультимодальныхПеревозок.Ссылка = &СхемаМультимодальнойПеревозки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	0 КАК ВремяВПути,
	               |	втТочкиОтправленияПрибытия.ВремяПрохожденияТочкиОтправления КАК ВремяПрохожденияТочки,
	               |	втТочкиОтправленияПрибытия.АдресОтправления КАК Адрес
	               |ИЗ
	               |	втТочкиОтправленияПрибытия КАК втТочкиОтправленияПрибытия
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	упСхемыМультимодальныхПеревозокПромежуточныеТочкиМаршрута.ВремяВПути,
	               |	ЕСТЬNULL(упАдреса.ВидАдреса.ВремяПрохожденияТочки, 0),
	               |	ЕСТЬNULL(упАдреса.Ссылка, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	               |ИЗ
	               |	Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК упСхемыМультимодальныхПеревозокПромежуточныеТочкиМаршрута
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
	               |		ПО упСхемыМультимодальныхПеревозокПромежуточныеТочкиМаршрута.Адрес = упАдреса.Ссылка
	               |ГДЕ
	               |	упСхемыМультимодальныхПеревозокПромежуточныеТочкиМаршрута.Ссылка = &СхемаМультимодальнойПеревозки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	втТочкиОтправленияПрибытия.ВремяВПутиДоКонечнойТочки,
	               |	втТочкиОтправленияПрибытия.ВремяПрохожденияТочкиПрибытия,
	               |	втТочкиОтправленияПрибытия.АдресПолучения
	               |ИЗ
	               |	втТочкиОтправленияПрибытия КАК втТочкиОтправленияПрибытия";
	Запрос.УстановитьПараметр("СхемаМультимодальнойПеревозки", 	СхемаМультимодальнойПеревозки);
	Запрос.УстановитьПараметр("АдресОтправления", 				АдресОтправления);
	Запрос.УстановитьПараметр("АдресПолучения", 				АдресПолучения);
	тбзАдреса = Запрос.Выполнить().Выгрузить();
	
	ПредыдущийАдрес 		= Справочники.упАдреса.ПустаяСсылка();
	ТекущийАдрес			= Справочники.упАдреса.ПустаяСсылка();
	мсвМассивыТочекДляРасчета 		= Новый Массив;
	мсвПоследовательностьАдресов	= Новый Массив;
	
	Для каждого Строка Из тбзАдреса Цикл
		
		ТекущийАдрес = Строка.Адрес;
		
		ПлановоеВремяПрибытия = ПлановоеВремяПрибытия + Строка.ВремяВПути * 3600 + Строка.ВремяПрохожденияТочки;
				
		// Если это не первая точка
		Если Строка.ВремяВПути = 0 Тогда
			Если ЗначениеЗаполнено(ПредыдущийАдрес) 
				И Строка.ВремяВПути = 0 Тогда
				Если мсвПоследовательностьАдресов.Количество() = 0 Тогда
					мсвМассивыТочекДляРасчета.Добавить(мсвПоследовательностьАдресов);	
					мсвПоследовательностьАдресов.Добавить(ПредыдущийАдрес);	
				КонецЕсли;
				мсвПоследовательностьАдресов.Добавить(ТекущийАдрес);	
			КонецЕсли;
		Иначе	
			Если мсвПоследовательностьАдресов.Количество() > 0 Тогда
				мсвПоследовательностьАдресов	= Новый Массив;
			КонецЕсли;
		КонецЕсли;
				
		ПредыдущийАдрес = ТекущийАдрес;
		
	КонецЦикла;

	Если мсвМассивыТочекДляРасчета.Количество() > 0 Тогда
		Для каждого МассивТочек Из мсвМассивыТочекДляРасчета Цикл
			мсвРезультаты = упМаршрутизация.ПолучитьРасстояниеИВремяМассиваТочек(МассивТочек);	
			Для каждого СтрокаРасстояниеМеждуАдресами Из мсвРезультаты Цикл
				
				// Считается по прямой
				Если СтрокаРасстояниеМеждуАдресами.ВремяМаршрута = Неопределено ИЛИ СтрокаРасстояниеМеждуАдресами.ВремяМаршрута = 0 Тогда
					Расстояние 	= упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(СтрокаРасстояниеМеждуАдресами.НачальнаяШирота, СтрокаРасстояниеМеждуАдресами.НачальнаяДолгота, СтрокаРасстояниеМеждуАдресами.КонечнаяШирота, СтрокаРасстояниеМеждуАдресами.КонечнаяДолгота)/1000;
					Время 		= Расстояние / 60; // приближенное время из расчета, что скорость движения составляет 60 км/ч
					ПлановоеВремяПрибытия = ПлановоеВремяПрибытия + Время * 3600;
					
				// Время занесено в регистр "упРасстоянияМеждуТочками" или рассчитывается при помощи маршрутизатора	
				Иначе	
					 ПлановоеВремяПрибытия = ПлановоеВремяПрибытия + СтрокаРасстояниеМеждуАдресами.ВремяМаршрута;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
			
	Возврат ПлановоеВремяПрибытия;
		
КонецФункции

// Процедура - Проверяет наличие задания по заявке в статусе отличном от "Новый", "Отменено" без статуса.
//
// Параметры:
//  Заявка	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза - заявка.
//  Отказ	 - Булево - признак неудачной проверки.
//
Процедура ЕстьЗаданияПоЗаявке(Заявка, Отказ) Экспорт
	Результат = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаданиеНаПеревозкуГруза.Ссылка
	               |ИЗ
	               |	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	               |		ПО упЗаданиеНаПеревозкуГруза.Ссылка = упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ
	               |ГДЕ
	               |	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
	               |	И НЕ(упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое), ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено))
	               |				ИЛИ упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус ЕСТЬ NULL)";
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", Заявка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСообщения = Нстр("ru = 'Сначала необходимо отменить %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.Ссылка,,,Отказ);				
	КонецЕсли;
	
КонецПроцедуры

// Функция - Есть изменения плановых этапов перевозки
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза	- проверяемая заявка.
// 
// Возвращаемое значение:
//  Булево - Истина, если по заявке есть изменения.
//
Функция ЕстьИзмененияПлановыхЭтаповПеревозки(ЗаявкаНаПеревозкуГруза) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	ПлановыеЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ИЗ
	|	РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза) КАК ПлановыеЭтапыПеревозки
	|ГДЕ ПлановыеЭтапыПеревозки.Изменен = ИСТИНА
	|";

	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Возвращает возможность формирования по грузовым местам. 
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	ПутьКВидуПеревозки - Строка - Наименование.
//
// Возвращаемое значение:
//	Булево - Результат выполнения.
//
Функция ФормироватьПоГрузовымМестам(Ссылка, ПутьКВидуПеревозки) Экспорт

	ПараметрВводаИнформации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ПутьКВидуПеревозки);
	
	Возврат ФормироватьПоГрузовымМестамПоПараметруВводаИнформации(ПараметрВводаИнформации);
	
КонецФункции // ФормироватьПоГрузовымМестам()

// Функция - Формировать по грузовым местам по параметру ввода информации
//
// Параметры:
//  ПараметрВводаИнформацииОГрузовыхМестах	 - ПеречислениеСсылка.упПараметрыВводаИнформацииОГрузовыхМестах 	 - параметр 
//                                              ввода грузовых мест.
// 
// Возвращаемое значение:
//  Булево - Истина еслипо грузовым местам.
//
Функция ФормироватьПоГрузовымМестамПоПараметруВводаИнформации(ПараметрВводаИнформацииОГрузовыхМестах) Экспорт
	
	Возврат Ложь
		   Или ПараметрВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку 
		   Или ПараметрВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса; 
		
КонецФункции

// Процедура формирует записи по регистру "упАдресаПартнеров".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Реквизиты - Структура - Реквизиты.
//
Процедура ЗаписатьАдресаПартнеров(ДополнительныеСвойства, Реквизиты) Экспорт
	
	Если ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению Тогда
		
		Отправитель = Реквизиты.Отправитель;
		Если ЗначениеЗаполнено(Отправитель) Тогда
			мсвАдресов = Реквизиты.ГрузовыеМеста.ВыгрузитьКолонку("АдресОтправления");
			мсвАдресов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвАдресов);
			ГрафикРаботы = Реквизиты.ГрафикРаботыАдресаОтправления;
			Для каждого текАдрес Из мсвАдресов Цикл
				Если ЗначениеЗаполнено(текАдрес) Тогда
					Попытка
						НачатьТранзакцию();
						
						Блокировка = Новый БлокировкаДанных;
						
						ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упАдресаПартнеров");
						ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
						ЭлементБлокировки.УстановитьЗначение("Партнер", Отправитель);
						ЭлементБлокировки.УстановитьЗначение("Адрес", текАдрес);
						
						Блокировка.Заблокировать();
						
						НаборЗаписей = РегистрыСведений.упАдресаПартнеров.СоздатьНаборЗаписей();
						НаборЗаписей.Отбор.Партнер.Установить(Отправитель);
						НаборЗаписей.Отбор.Адрес.Установить(текАдрес);
						НаборЗаписей.Прочитать();
						Если НаборЗаписей.Количество() И ЗначениеЗаполнено(НаборЗаписей[0].ГрафикРаботы) Тогда
							ГрафикРаботы = НаборЗаписей[0].ГрафикРаботы;
						КонецЕсли; 
						НаборЗаписей.Очистить();
						
						НоваяЗапись = НаборЗаписей.Добавить();
						НоваяЗапись.Партнер = Отправитель;
						НоваяЗапись.Адрес = текАдрес;
						НоваяЗапись.ДатаИзменения = ТекущаяДатаСеанса();
						НоваяЗапись.ГрафикРаботы = ГрафикРаботы;
						НаборЗаписей.Записать();
						
						ЗафиксироватьТранзакцию();
						
					Исключение
						Если ТранзакцияАктивна() Тогда 
							ОтменитьТранзакцию();
						КонецЕсли;
					КонецПопытки;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		
		Получатель = Реквизиты.Получатель;
		Если ЗначениеЗаполнено(Получатель) Тогда
			мсвАдресов = Реквизиты.ГрузовыеМеста.ВыгрузитьКолонку("АдресПолучения");
			мсвАдресов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвАдресов);
			ГрафикРаботы = Реквизиты.ГрафикРаботыАдресаПолучения;
			Для каждого текАдрес Из мсвАдресов Цикл
				Если ЗначениеЗаполнено(текАдрес) Тогда
					Попытка
						НачатьТранзакцию();
						
						Блокировка = Новый БлокировкаДанных;
						
						ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.упАдресаПартнеров");
						ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
						ЭлементБлокировки.УстановитьЗначение("Партнер", Получатель);
						ЭлементБлокировки.УстановитьЗначение("Адрес", текАдрес);
						
						Блокировка.Заблокировать();
						
						НаборЗаписей = РегистрыСведений.упАдресаПартнеров.СоздатьНаборЗаписей();
						НаборЗаписей.Отбор.Партнер.Установить(Получатель);
						НаборЗаписей.Отбор.Адрес.Установить(текАдрес);
						НаборЗаписей.Прочитать();
						Если НаборЗаписей.Количество() И ЗначениеЗаполнено(НаборЗаписей[0].ГрафикРаботы) Тогда
							ГрафикРаботы = НаборЗаписей[0].ГрафикРаботы;
						КонецЕсли; 
						НаборЗаписей.Очистить();
						
						НоваяЗапись = НаборЗаписей.Добавить();
						НоваяЗапись.Партнер = Получатель;
						НоваяЗапись.Адрес = текАдрес;
						НоваяЗапись.ДатаИзменения = ТекущаяДатаСеанса();
						НоваяЗапись.ГрафикРаботы = ГрафикРаботы;
						НаборЗаписей.Записать();
						
						ЗафиксироватьТранзакцию();
						
					Исключение
						Если ТранзакцияАктивна() Тогда 
							ОтменитьТранзакцию();
						КонецЕсли;
					КонецПопытки;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция - Подобрать аналитический разрез по партнерам.
//
// Параметры:
//  Организация - СправочникСсылка.Организации			 - организация.
//  Партнер	 - СправочникСсылка.упПартнеры			 - партнер.
//  Контрагент	 - СправочникСсылка.упКонтрагент			 - контрагент.
//  Соглашение	 - СправочникСсылка.упСоглашенияСКлиентами	 - соглашение.
//  Отказ		 - Булево								 - Истина, если ошибка.
// 
// Возвращаемое значение:
//  СправочникСсылка.упАналитическиеРазрезыПоПартнерам - аналитический разрез по партнерам.
//
Функция ПодобратьАналитическийРазрезПоПартнерам(Организация, Партнер, Контрагент, Соглашение, Отказ) Экспорт
	
	Результат = Неопределено;
			
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упАналитическиеРазрезыПоПартнерам.Ссылка
	|ИЗ
	|	Справочник.упАналитическиеРазрезыПоПартнерам КАК упАналитическиеРазрезыПоПартнерам
	|ГДЕ
	|	упАналитическиеРазрезыПоПартнерам.Партнер = &Партнер
	|	И упАналитическиеРазрезыПоПартнерам.Контрагент = &Контрагент
	|	И упАналитическиеРазрезыПоПартнерам.Соглашение = &Соглашение
	|	И упАналитическиеРазрезыПоПартнерам.Организация = &Организация";
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;	
	Иначе	
		АналитическийРазрез = Справочники.упАналитическиеРазрезыПоПартнерам.СоздатьЭлемент();
		АналитическийРазрез.Партнер		= Партнер;
		АналитическийРазрез.Контрагент	= Контрагент;
		АналитическийРазрез.Соглашение	= Соглашение;
		АналитическийРазрез.Организация	= Организация;
		АналитическийРазрез.Наименование	= Справочники.упАналитическиеРазрезыПоПартнерам.СформироватьНаименование(Организация, Партнер, Контрагент, Соглашение);
		
		Попытка
			АналитическийРазрез.Записать();	
			Результат	= АналитическийРазрез.Ссылка;
		Исключение
			ТекстСообщения = Нстр("ru = 'Не удалось сформировать аналитический разрез по организации:%1; партнеру:%2; контрагенту:%3; соглашению:%4'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Организация, Партнер, Контрагент, Соглашение);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);

			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
								
	КонецЕсли; 

	Возврат Результат;
	
КонецФункции

// Функция - Подобрать аналитический разрез по партнерам соглашения берутся из заявок.
//
// Параметры:
//  Организация			 - СправочникСсылка.Организации	 - организация.
//  Партнер				 - СправочникСсылка.упПартнеры	 - партнер.
//  Контрагент				 - СправочникСсылка.упКонтрагент	 - контрагент.
//  мсвЗаявкиНаПеревозкуГрузов - Массив						 - массив заявок.
//  Отказ					 - Булево						 - Истина, в случае ошибки.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица значений с колонками:
//  * Соглашение - СправочникСсылка.упСоглашенияСКлиентами - соглашение.
//  * ЗаявкаНаПервозкуГрузов			- ДокументСсылка.упЗаявкаНаПеревозкуГрузов - заявка на перевозку грузов.
//  * АналитическийРазрезПоПартнерам	- СправочникСсылка.упАналитическиеРазрезыПоПартнерам - аналитический разрез по партнерам.
//
Функция ПодобратьАналитическийРазрезПоПартнерамДляКаждойЗаявки(Организация, Партнер, Контрагент, мсвЗаявкиНаПеревозкуГрузов, Отказ) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаявкаНаПеревозкуГруза.Соглашение,
	               |	упЗаявкаНаПеревозкуГруза.Ссылка
	               |ПОМЕСТИТЬ втЗаявкиСоглашения
	               |ИЗ
	               |	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	               |ГДЕ
	               |	упЗаявкаНаПеревозкуГруза.Ссылка В(&мсвЗаявки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	втЗаявкиСоглашения.Соглашение
	               |ПОМЕСТИТЬ втСоглашения
	               |ИЗ
	               |	втЗаявкиСоглашения КАК втЗаявкиСоглашения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(упАналитическиеРазрезыПоПартнерам.Ссылка, ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезыПоПартнерам.ПустаяСсылка)) КАК АналитическийРазрезПоПартнерам,
	               |	втСоглашения.Соглашение
	               |ИЗ
	               |	втСоглашения КАК втСоглашения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезыПоПартнерам КАК упАналитическиеРазрезыПоПартнерам
	               |		ПО втСоглашения.Соглашение = упАналитическиеРазрезыПоПартнерам.Соглашение
	               |			И (упАналитическиеРазрезыПоПартнерам.Партнер = &Партнер)
	               |			И (упАналитическиеРазрезыПоПартнерам.Контрагент = &Контрагент)
	               |			И (упАналитическиеРазрезыПоПартнерам.Организация = &Организация)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗаявкиСоглашения.Соглашение,
	               |	втЗаявкиСоглашения.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	               |	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезыПоПартнерам.ПустаяСсылка) КАК АналитическийРазрезПоПартнерам
	               |ИЗ
	               |	втЗаявкиСоглашения КАК втЗаявкиСоглашения";
	Запрос.УстановитьПараметр("Партнер", 		Партнер);
	Запрос.УстановитьПараметр("Контрагент", 	Контрагент);
	Запрос.УстановитьПараметр("мсвЗаявки", 		мсвЗаявкиНаПеревозкуГрузов);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	
	мсвРезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаРазрезыПоСоглашениям = мсвРезультатыЗапроса[2].Выбрать();
	Результат =	мсвРезультатыЗапроса[3].Выгрузить();
	
	Пока ВыборкаРазрезыПоСоглашениям.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаРазрезыПоСоглашениям.АналитическийРазрезПоПартнерам) Тогда
			 АналитическийРазрезПоПартнерамСсылка = ВыборкаРазрезыПоСоглашениям.АналитическийРазрезПоПартнерам;
		Иначе	
			АналитическийРазрез = Справочники.упАналитическиеРазрезыПоПартнерам.СоздатьЭлемент();
			АналитическийРазрез.Партнер		= Партнер;
			АналитическийРазрез.Контрагент	= Контрагент;
			АналитическийРазрез.Соглашение	= ВыборкаРазрезыПоСоглашениям.Соглашение;
			АналитическийРазрез.Организация	= Организация;
			АналитическийРазрез.Наименование	= Справочники.упАналитическиеРазрезыПоПартнерам.СформироватьНаименование(Организация, Партнер, Контрагент, ВыборкаРазрезыПоСоглашениям.Соглашение);
			
			Попытка
				АналитическийРазрез.Записать();	
			Исключение
				ТекстСообщения = Нстр("ru = 'Не удалось сформировать аналитический разрез по организации:%1; партнеру:%2; контрагенту:%3; соглашению:%4'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Организация, Партнер, Контрагент, ВыборкаРазрезыПоСоглашениям.Соглашение);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецПопытки;	
			
			Если НЕ Отказ Тогда
				АналитическийРазрезПоПартнерамСсылка = АналитическийРазрез.Ссылка;
			Иначе
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			мсвСтрокиПоСоглашению = Результат.НайтиСтроки(Новый Структура("Соглашение", ВыборкаРазрезыПоСоглашениям.Соглашение));
			Для каждого СтрокаЗаявка Из мсвСтрокиПоСоглашению Цикл
				СтрокаЗаявка.АналитическийРазрезПоПартнерам = АналитическийРазрезПоПартнерамСсылка;	
			КонецЦикла;
		КонецЕсли;
						
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

// Процедура - Заполняет по умолчанию реквизиты объекта "порядок исполнения заявки" и "вид перевозки".
//
// Параметры:
//  Объект					 - ДокументОбъект - объект в котором необходимо произвести заполнение.
//  ЗаполнитьВидТранспортнойУслуги	 - Булево - Истина, если необходимо заполнение вида транспортной услуги объекта.
//
Процедура ЗаполнитьПорядокИсполненияЗаявки(Объект, ЗаполнитьВидТранспортнойУслуги = Истина) Экспорт
	
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		сткРеквизитыВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, "ВидТранспортнойУслуги, ПорядокИсполненияЗаявки, КоличествоРабочихСмен");
		ИсключаяСвойства = Новый Массив;
		Если Не ЗаполнитьВидТранспортнойУслуги Тогда
			ИсключаяСвойства.Добавить("ВидТранспортнойУслуги");
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "КоличествоРабочихСмен") Тогда
			ИсключаяСвойства.Добавить("КоличествоРабочихСмен");
			ЗаполнитьЗначенияСвойств(Объект, сткРеквизитыВидаПеревозки,,СтрСоединить(ИсключаяСвойства, ","));
			Если Истина
				И сткРеквизитыВидаПеревозки.ПорядокИсполненияЗаявки = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом
				И сткРеквизитыВидаПеревозки.КоличествоРабочихСмен > 0 
			Тогда
				Объект.КоличествоРабочихСмен = сткРеквизитыВидаПеревозки.КоличествоРабочихСмен;	
			КонецЕсли;
			
		Иначе
			ЗаполнитьЗначенияСвойств(Объект, сткРеквизитыВидаПеревозки,,СтрСоединить(ИсключаяСвойства, ","));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Заполнить грузовые места
//
// Параметры:
//  ТекущийОбъект	 - ДокументОбъект.упЗаявкаНаПеревозкуГруза 	 - заявка на перевозку груза.
//  Отказ			 - Булево	 - Истина, если ошибка.
//
Процедура ЗаполнитьГрузовыеМеста(ТекущийОбъект, Отказ) Экспорт
	
	ДопСвойства = ТекущийОбъект.ДополнительныеСвойства;
	ОбменДаннымиЗагрузка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ОбменДаннымиЗагрузка", Ложь);
	ГрузовыеМеста = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ГрузовыеМеста", Неопределено);
	ТоварныйСостав = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ТоварныйСостав", Неопределено);
	
	ТекущийОбъект.ГрузовыеМеста.Очистить();
	
	Событие = НСтр("ru = 'Запись информации о грузе: '");
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Истина
		И ГрузовыеМеста <> Неопределено
		И ТоварныйСостав <> Неопределено 
	Тогда
	
		Если ТекущийОбъект.ЭтоНовый() Тогда 
			СсылкаНаОснование = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСсылку(Новый УникальныйИдентификатор);
			ТекущийОбъект.УстановитьСсылкуНового(СсылкаНаОснование);
			ДокументОснование = СсылкаНаОснование;
		Иначе
			ДокументОснование = ТекущийОбъект.Ссылка;
		КонецЕсли;	
	
		НомерГруза = 1;
		
		Для Каждого ГрузСтрока Из ГрузовыеМеста Цикл 
			
			Если ЗначениеЗаполнено(ГрузСтрока.ГрузовоеМесто) Тогда 
				ГрузОбъект = ГрузСтрока.ГрузовоеМесто.ПолучитьОбъект();
			Иначе
				ГрузОбъект = Справочники.упГрузовыеМеста.СоздатьЭлемент();
				Если Не ЗначениеЗаполнено(ГрузСтрока.Код) Тогда 
					Если Не ЗначениеЗаполнено(ТекущийОбъект.Номер) Тогда 
						ТекущийОбъект.УстановитьНовыйНомер();
					КонецЕсли;
					
					ГрузСтрока.Код = Формат(ТекущийОбъект.Дата, "ДФ=гг") + СокрЛП(ТекущийОбъект.Номер) + ?(ТекущийОбъект.ОбщийГруз,"","-" + НомерГруза);
				КонецЕсли;
				
				ГрузОбъект.Код = ГрузСтрока.Код;
				ГрузОбъект.ДокументОснование = ДокументОснование;
				
			КонецЕсли;	
			
			ЗаполнитьЗначенияСвойств(ГрузОбъект, ГрузСтрока,, "Код");
			ГрузОбъект.ОбщийГрузПоЗаявке = ТекущийОбъект.ОбщийГруз;
			ГрузОбъект.Описание = ТекущийОбъект.Комментарий;
			
			ГрузОбъект.ТоварныйСоставГруза.Очистить();
			
			мсвСтрокиТоварныйСостав = ТоварныйСостав.НайтиСтроки(Новый Структура("Идентификатор", ГрузСтрока.Идентификатор));
			Для Каждого текСтрокаНоменклатуры Из мсвСтрокиТоварныйСостав Цикл 
				НоваяСтрокаСостава = ГрузОбъект.ТоварныйСоставГруза.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСостава, текСтрокаНоменклатуры);
			КонецЦикла;	
			
			НомерГруза = НомерГруза + 1;
						
			Если Ложь 
				Или ОбменДаннымиЗагрузка
				Или ГрузОбъект.ПроверитьЗаполнение()
			Тогда 
				Попытка
					ГрузОбъект.ОбменДанными.Загрузка = ОбменДаннымиЗагрузка;
					ГрузОбъект.Записать();
					ГрузовоеМесто = ГрузОбъект.Ссылка;
					
					НовыйГруз = ТекущийОбъект.ГрузовыеМеста.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйГруз, ГрузСтрока);
					НовыйГруз.ГрузовоеМесто = ГрузОбъект.Ссылка;										
				Исключение
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Событие + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), , ?(ТекущийОбъект.ОбщийГруз,"Код","СписокГрузов"), , Отказ);
				КонецПопытки;
			Иначе
				Отказ = Истина;
			КонецЕсли;		
			
			Если НЕ Отказ Тогда
				ГрузСтрока.ГрузовоеМесто = ГрузОбъект.Ссылка;
				
				мсвСтрокиТоварныйСостав = ТоварныйСостав.НайтиСтроки(Новый Структура("Идентификатор", ГрузСтрока.Идентификатор));
				Для каждого ТоварныйСоставСтрока Из мсвСтрокиТоварныйСостав Цикл
					ТоварныйСоставСтрока.ГрузовоеМесто = ГрузСтрока.ГрузовоеМесто;
					ТоварныйСоставСтрока.Код = ГрузСтрока.Код;
				КонецЦикла;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры


// Процедура - Заполнить вид транспортной услуги объект по виду перевозки.
//
// Параметры:
//  Объект - ДокументОбъект - документ для заполнения реквизита.
//
Процедура ЗаполнитьВидТранспортнойУслуги(Объект) Экспорт
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		сткРеквизитыВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, "ВидТранспортнойУслуги");
		ЗаполнитьЗначенияСвойств(Объект, сткРеквизитыВидаПеревозки);
	КонецЕсли;
КонецПроцедуры

// Функция - Маршрут следования по заявкам на перевозку груза
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза	 - Массив	- заявки на перевозку груза ( ДокументСсылка.упЗаявкаНаПеревозкуГруза).
// 
// Возвращаемое значение:
//  Массив - массив неповторяющихся адресов(СправочникСсылка.упТиповыеМаршруты) 
//
Функция МаршрутСледованияПоЗаявкамНаПеревозкуГруза(мсвЗаявкиНаПеревозкуГруза) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	упЗаявкаНаПеревозкуГруза.АдресПолучения КАК АдресПолучения
	|ПОМЕСТИТЬ втАдресаЗаявкиБезМаршрута
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ ИСТИНА
	|	И упЗаявкаНаПеревозкуГруза.Ссылка В (&Заявки)
	|	И упЗаявкаНаПеревозкуГруза.МаршрутСледования = ЗНАЧЕНИЕ(Справочник.упТиповыеМаршруты.ПустаяСсылка)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдресаЗаявкиБезМаршрута.АдресОтправления КАК Адрес,
	|	ЗНАЧЕНИЕ(Справочник.упТиповыеМаршруты.ПустаяСсылка) КАК Ссылка,
	|	1 КАК НомерСтроки
	|ИЗ
	|	втАдресаЗаявкиБезМаршрута КАК втАдресаЗаявкиБезМаршрута
	|ГДЕ втАдресаЗаявкиБезМаршрута.АдресОтправления <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втАдресаЗаявкиБезМаршрута.АдресПолучения КАК Адрес,
	|	ЗНАЧЕНИЕ(Справочник.упТиповыеМаршруты.ПустаяСсылка) КАК Ссылка,
	|	2 КАК НомерСтроки
	|ИЗ
	|	втАдресаЗаявкиБезМаршрута КАК втАдресаЗаявкиБезМаршрута
	|ГДЕ втАдресаЗаявкиБезМаршрута.АдресПолучения <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упТиповыеМаршрутыМаршрут.Адрес КАК Адрес,
	|	упТиповыеМаршрутыМаршрут.Ссылка КАК Ссылка,
	|	упТиповыеМаршрутыМаршрут.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТиповыеМаршруты.Маршрут КАК упТиповыеМаршрутыМаршрут
	|	ПО упЗаявкаНаПеревозкуГруза.МаршрутСледования = упТиповыеМаршрутыМаршрут.Ссылка
	|ГДЕ ИСТИНА
	|	И упЗаявкаНаПеревозкуГруза.Ссылка В (&Заявки)
	|	И упТиповыеМаршрутыМаршрут.Адрес <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|";

	Запрос.УстановитьПараметр("Заявки", мсвЗаявкиНаПеревозкуГруза);
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Адрес");
		
	Возврат Результат;		
	
КонецФункции

Процедура ПроконтролироватьИспользованиеТары(ГрузовыеМеста, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втГрузовыеМеста
	|	ИЗ &ГрузовыеМеста КАК втГрузовыеМеста 
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.*
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втТара
	|	ПО втГрузовыеМеста.Тара = втТара.ГрузовоеМесто
	|ГДЕ втТара.ГрузовоеМесто ЕСТЬ NULL
	| 	И втГрузовыеМеста.Тара <>ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) 
	|";

	Запрос.УстановитьПараметр("ГрузовыеМеста", ГрузовыеМеста.Выгрузить(, "НомерСтроки, ГрузовоеМесто, Тара"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = Нстр("ru = 'Тара:%1 указанная для грузового места:%2 должна содержаться в табличной части еще и как отдельное грузовое место.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Тара, Выборка.ГрузовоеМесто);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ГрузовыеМеста[" + Формат(Выборка.НомерСтроки - 1,"ЧН=0; ЧГ=0") + "].Код",,Отказ);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Контроль тары'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры


#Область ФормированиеДвижений

// Процедура формирует записи по регистру "упЗаявкиКВыполнению".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияЗаявкиКВыполнению = Движения.упЗаявкиКВыполнению;
		
	Статус = ДополнительныеСвойства.Статус;
	Если Ложь
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
		//Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		//Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		//Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется
		//Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути
		//Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
		//Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично
	Тогда 
		ДвиженияЗаявкиКВыполнению.Записывать = Истина;
		Если Реквизиты.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов Тогда
			
			ИспользуютсяМультимодальныеПеревозки = Реквизиты.ИспользуютсяМультимодальныеПеревозки;
			Если Реквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку Тогда 
				
				Если ИспользуютсяМультимодальныеПеревозки Тогда
					Если ДополнительныеСвойства.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
						Если ДополнительныеСвойства.Свойство("ЭтапыМультимодальнойПеревозки") Тогда
							ЭтапыМультимодальнойПеревозки = ДополнительныеСвойства.ЭтапыМультимодальнойПеревозки;
							Для каждого текЭтап Из ЭтапыМультимодальнойПеревозки Цикл
								ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
								ДвижениеЗаявки.Период = Реквизиты.Дата;
								ДвижениеЗаявки.Регистратор = Ссылка;
								ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
								ДвижениеЗаявки.Заявка = Ссылка;
								ДвижениеЗаявки.ГрузовоеМесто = текЭтап.ГрузовоеМесто;
								ДвижениеЗаявки.Тара = текЭтап.Тара;
								ДвижениеЗаявки.НомерВЦепиПоставок = текЭтап.НомерВЦепиПоставок;
								ДвижениеЗаявки.НомерВЦепиПоставокКонец = текЭтап.НомерВЦепиПоставокКонец;
								ДвижениеЗаявки.Количество = текЭтап.КоличествоГрузов;
								ДвижениеЗаявки.СхемаМультимодальнойПеревозки = текЭтап.СхемаМультимодальнойПеревозки;
							КонецЦикла; 
						Иначе
							ДвиженияЗаявкиКВыполнению.Записывать = Ложь;
						КонецЕсли;
					Иначе 
						
						дрвДвиженияМультимод = ПодготовитьТаблицуДвиженийСНомерамиЦепейПоставок(Ссылка);
						СчетчикВершин = 1;
						Сч = 1;
						Для каждого текСхема Из дрвДвиженияМультимод.Строки Цикл
							Для каждого текГруз Из текСхема.Строки Цикл
								Если текГруз.НомерВЦепиПоставок = 0 Тогда
									ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
									ДвижениеЗаявки.Период = Реквизиты.Дата;
									ДвижениеЗаявки.Регистратор = Ссылка;
									ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
									ДвижениеЗаявки.Заявка = Ссылка;
									ДвижениеЗаявки.ГрузовоеМесто = текГруз.ГрузовоеМесто;
									ДвижениеЗаявки.Тара = текГруз.Тара;
									ДвижениеЗаявки.Количество = текГруз.КоличествоГрузов;
								Иначе
									максСчет = ?(СчетчикВершин = 1, текГруз.НомерВЦепиПоставок,  текГруз.НомерВЦепиПоставок + СчетчикВершин);
									Для Сч = СчетчикВершин По максСчет Цикл
										ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
										ДвижениеЗаявки.Период = Реквизиты.Дата;
										ДвижениеЗаявки.Регистратор = Ссылка;
										ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
										ДвижениеЗаявки.Заявка = Ссылка;
										ДвижениеЗаявки.ГрузовоеМесто = текГруз.ГрузовоеМесто;
										ДвижениеЗаявки.Тара = текГруз.Тара;
										ДвижениеЗаявки.НомерВЦепиПоставок = Сч;
										ДвижениеЗаявки.НомерВЦепиПоставокКонец = Сч + 1;
										ДвижениеЗаявки.Количество = текГруз.КоличествоГрузов;
										ДвижениеЗаявки.СхемаМультимодальнойПеревозки = текГруз.Схема;
									КонецЦикла;
								КонецЕсли; 
							КонецЦикла;
							СчетчикВершин = Сч + 1;
						КонецЦикла;
						
					КонецЕсли;
				Иначе
					Для Каждого текГруз Из Реквизиты.ГрузовыеМеста Цикл 
						ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
						ДвижениеЗаявки.Период = Реквизиты.Дата;
						ДвижениеЗаявки.Регистратор = Ссылка;
						ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
						ДвижениеЗаявки.Заявка = Ссылка;
						ДвижениеЗаявки.ГрузовоеМесто = текГруз.ГрузовоеМесто;
						ДвижениеЗаявки.Тара = текГруз.Тара;
						ДвижениеЗаявки.Количество = текГруз.КоличествоГрузов;
					КонецЦикла;
				КонецЕсли;
				
			Иначе
				Если ИспользуютсяМультимодальныеПеревозки Тогда
					Если ДополнительныеСвойства.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
						Если ДополнительныеСвойства.Свойство("ЭтапыМультимодальнойПеревозки") Тогда
							ЭтапыМультимодальнойПеревозки = ДополнительныеСвойства.ЭтапыМультимодальнойПеревозки.Скопировать();
							ЭтапыМультимодальнойПеревозки.Свернуть("НомерВЦепиПоставок, НомерВЦепиПоставокКонец, СхемаМультимодальнойПеревозки");
							Для каждого текЭтап Из ЭтапыМультимодальнойПеревозки Цикл
								ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
								ДвижениеЗаявки.Период = Реквизиты.Дата;
								ДвижениеЗаявки.Регистратор = Ссылка;
								ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
								ДвижениеЗаявки.Заявка = Ссылка;
								ДвижениеЗаявки.НомерВЦепиПоставок = текЭтап.НомерВЦепиПоставок;
								ДвижениеЗаявки.НомерВЦепиПоставокКонец = текЭтап.НомерВЦепиПоставокКонец;
								ДвижениеЗаявки.Количество = 1;
								ДвижениеЗаявки.СхемаМультимодальнойПеревозки = текЭтап.СхемаМультимодальнойПеревозки;
							КонецЦикла;
						Иначе
							ДвиженияЗаявкиКВыполнению.Записывать = Ложь;	
						КонецЕсли;
						
					ИначеЕсли ЗначениеЗаполнено(Реквизиты.СхемаМультимодальнойПеревозки) Тогда
						Для Сч = 1 По Реквизиты.ДлинаЦепиПоставок Цикл
							ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
							ДвижениеЗаявки.Период = Реквизиты.Дата;
							ДвижениеЗаявки.Регистратор = Ссылка;
							ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
							ДвижениеЗаявки.Заявка = Ссылка;
							ДвижениеЗаявки.НомерВЦепиПоставок = Сч;
							ДвижениеЗаявки.НомерВЦепиПоставокКонец = Сч + 1;
							ДвижениеЗаявки.Количество = 1;
							ДвижениеЗаявки.СхемаМультимодальнойПеревозки = Реквизиты.СхемаМультимодальнойПеревозки;
						КонецЦикла;
					Иначе
						ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
						ДвижениеЗаявки.Период = Реквизиты.Дата;
						ДвижениеЗаявки.Регистратор = Ссылка;
						ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
						ДвижениеЗаявки.Заявка = Ссылка;
						ДвижениеЗаявки.Количество = 1;
					КонецЕсли;
				Иначе	
					ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
					ДвижениеЗаявки.Период = Реквизиты.Дата;
					ДвижениеЗаявки.Регистратор = Ссылка;
					ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
					ДвижениеЗаявки.Заявка = Ссылка;
					ДвижениеЗаявки.Количество = 1;
				КонецЕсли;
				
			КонецЕсли;	
		Иначе
			
			ДвижениеЗаявки = ДвиженияЗаявкиКВыполнению.Добавить();
			ДвижениеЗаявки.Период = Реквизиты.Дата;
			ДвижениеЗаявки.Регистратор = Ссылка;
			ДвижениеЗаявки.ВидДвижения = ВидДвиженияНакопления.Приход;
			ДвижениеЗаявки.Заявка = Ссылка;
			ДвижениеЗаявки.Количество = Реквизиты.КоличествоРабочихСмен;

		КонецЕсли;
	
		ДополнительныеСвойства.Вставить("РассчитатьСтатус", Истина);
		
	ИначеЕсли Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена Тогда 
		ДвиженияЗаявкиКВыполнению.Записывать = Истина;
	КонецЕсли;	
	
КонецПроцедуры

// Процедура формирует записи по регистру "упСтатусыЗаявокНаПеревозкуГруза".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСтатусЗаявки(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияСтатуса = Движения.упСтатусыЗаявокНаПеревозкуГруза;
	
	мсвЗаявокСИзмененнымСтатусом = Неопределено;
	ДополнительныеСвойства.Свойство("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус",мсвЗаявокСИзмененнымСтатусом);
			
	Статус = ДополнительныеСвойства.Статус;
	ЗаписыватьСтатус = ДополнительныеСвойства.ЗаписыватьСтатус;
	РассчитатьСтатус = ДополнительныеСвойства.РассчитатьСтатус;
		
	Если Истина
		И Не ЗаписыватьСтатус 
		И РассчитатьСтатус 
	Тогда 
		
		мсвЗаявкиНаПеревозку = Новый Массив;
		мсвЗаявкиНаПеревозку.Добавить(Ссылка);
		тбзЗаявкиСтатусы = упРаботаСоСтатусами.РассчитатьСтатусЗаявкиНаПеревозку(мсвЗаявкиНаПеревозку);
		
		Если тбзЗаявкиСтатусы.Количество()>0 Тогда
		     РасчетныйСтатус = тбзЗаявкиСтатусы[0].Статус;
		Иначе	
		     РасчетныйСтатус = Неопределено;
		КонецЕсли;
		
		Если Статус <> РасчетныйСтатус Тогда 
			Статус = РасчетныйСтатус;
			ЗаписыватьСтатус = Истина;
			Если мсвЗаявокСИзмененнымСтатусом <> Неопределено Тогда
				мсвЗаявокСИзмененнымСтатусом.Добавить(Ссылка);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
	Период = ?(Реквизиты.Свойство("ДатаСтатуса"), Реквизиты.ДатаСтатуса, Реквизиты.Дата);
	
	Если ЗаписыватьСтатус Тогда 
		
		ДвиженияСтатуса = Движения.упСтатусыЗаявокНаПеревозкуГруза;
		ДвиженияСтатуса.Записывать = Истина;
		ДвиженияСтатуса.Прочитать();
				 
		Для каждого текЗаписьСтатус Из ДвиженияСтатуса Цикл
			Если текЗаписьСтатус.Период = Период Тогда
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период = Период;
		ДвижениеСтатуса.Документ = Ссылка;
		ДвижениеСтатуса.Статус = Статус;
		
		Если мсвЗаявокСИзмененнымСтатусом <> Неопределено Тогда
			ДополнительныеСвойства.ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус = мсвЗаявокСИзмененнымСтатусом;
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упСтатусыЗаявокНаПеревозкуГруза".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	мсвЗаявокСИзмененнымСтатусом = Неопределено;
	ДополнительныеСвойства.Свойство("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус",мсвЗаявокСИзмененнымСтатусом);
		
	ДвиженияСтатуса = Движения.упСтатусыЗаявокНаПеревозкуГруза;
	Если ДополнительныеСвойства.Свойство("ОчиститьЗаписиСтатусЗаявки") Тогда
		ДвиженияСтатуса.Очистить();	
	Иначе	
		ДвиженияСтатуса.Прочитать();	
	КонецЕсли;
	
	мсвЗаявки = Неопределено;
	
	Если НЕ ДополнительныеСвойства.Свойство("мсвЗаявки", мсвЗаявки) Тогда
		Если ДополнительныеСвойства.Свойство("мсвЗадания") Тогда
			мсвЗадания = ДополнительныеСвойства.мсвЗадания;
		Иначе	
			тбзЗадания = Реквизиты.Задания.Выгрузить(,"Задание");
			тбзЗадания.Свернуть("Задание");
			мсвЗадания = тбзЗадания.ВыгрузитьКолонку("Задание");
		КонецЕсли;
		
		ствЗаявкиНаПеревозкуГруза = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвЗадания, "ЗаявкаНаПеревозкуГруза");
		
		мсвЗаявки = Новый Массив;
		Для каждого текЭлемент Из ствЗаявкиНаПеревозкуГруза Цикл
			мсвЗаявки.Добавить(текЭлемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	// Если задание с заявкой было удалено из рейса
	мсвЗаписиСтатусаДляУдаления = Новый Массив;
	Для каждого текЗапись Из ДвиженияСтатуса Цикл
		Если мсвЗаявки.Найти(текЗапись.Документ) = Неопределено Тогда
			мсвЗаписиСтатусаДляУдаления.Добавить(текЗапись);
			Если мсвЗаявокСИзмененнымСтатусом.Найти(текЗапись.Документ) = Неопределено Тогда
				мсвЗаявокСИзмененнымСтатусом.Добавить(текЗапись.Документ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если мсвЗаписиСтатусаДляУдаления.Количество() Тогда
		Для каждого текЗапись Из мсвЗаписиСтатусаДляУдаления Цикл
			ДвиженияСтатуса.Удалить(текЗапись);	
		КонецЦикла;
		ДвиженияСтатуса.Записывать = Истина;
	КонецЕсли; 
		
	тбзСтатусыЗаявок = упРаботаСоСтатусами.РассчитатьСтатусЗаявкиНаПеревозку(мсвЗаявки);
	
	Период = ?(Реквизиты.Свойство("ДатаСтатуса"), Реквизиты.ДатаСтатуса, Реквизиты.Дата);
	Для каждого текСтрока Из тбзСтатусыЗаявок Цикл
		
		Если текСтрока.Статус = текСтрока.ТекущийСтатус Тогда
			Продолжить;	
		КонецЕсли;
		
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период      = Период;
		ДвижениеСтатуса.Документ    = текСтрока.Заявка;
		ДвижениеСтатуса.Статус      = текСтрока.Статус;
			
		Если НЕ мсвЗаявокСИзмененнымСтатусом = Неопределено Тогда
			мсвЗаявокСИзмененнымСтатусом.Добавить(текСтрока.Заявка);
		КонецЕсли;	
	КонецЦикла;
	
	Если ДвиженияСтатуса.Количество() > 0 Тогда
		Если НЕ мсвЗаявокСИзмененнымСтатусом = Неопределено Тогда
			ДополнительныеСвойства.ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус = мсвЗаявокСИзмененнымСтатусом;
		КонецЕсли;	
		ДвиженияСтатуса.Записывать = Истина;
	КонецЕсли;
		
КонецПроцедуры

// Процедура формирует записи по регистру "упДоходы".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияДоходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияДоходы	= Движения.упДоходы;
	ДвиженияДоходы.Записывать = Истина;
		
	Статус = ДополнительныеСвойства.Статус;
	Если Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично Тогда 
		
		тбзДоходы = ДополнительныеСвойства.ПлановыеДоходы;
		ВедетсяВалютныйУчет = ДополнительныеСвойства.ВедетсяВалютныйУчет;
		
		Для каждого СтрокаДохода Из тбзДоходы Цикл
			ДвижениеДоходы = ДвиженияДоходы.Добавить();
			ДвижениеДоходы.Период = Реквизиты.Дата;
			ДвижениеДоходы.Регистратор = Ссылка;
			ДвижениеДоходы.ЗаявкаНаПеревозкуГруза = Ссылка;
			ДвижениеДоходы.СтатьяДоходов = СтрокаДохода.СтатьяДоходов;
			
			Если ВедетсяВалютныйУчет Тогда
				ДвижениеДоходы.Валюта = СтрокаДохода.Валюта;
				ДвижениеДоходы.СуммаПланСНДС = СтрокаДохода.Сумма;
				ДвижениеДоходы.СуммаПланБезНДС = СтрокаДохода.Сумма - СтрокаДохода.СуммаНДС;
				ДвижениеДоходы.СуммаПланВалютаСНДС = СтрокаДохода.СуммаВалюта;
				ДвижениеДоходы.СуммаПланВалютаБезНДС = СтрокаДохода.СуммаВалюта - СтрокаДохода.СуммаВалютаНДС;
				ДвижениеДоходы.СуммаФактБезНДС = 0;
				ДвижениеДоходы.СуммаФактСНДС = 0;
				ДвижениеДоходы.СуммаФактВалютаБезНДС = 0;
				ДвижениеДоходы.СуммаФактВалютаСНДС = 0;

			Иначе
				ДвижениеДоходы.Валюта = Справочники.Валюты.ПустаяСсылка();
				ДвижениеДоходы.СуммаПланСНДС = СтрокаДохода.Сумма;
				ДвижениеДоходы.СуммаПланБезНДС = СтрокаДохода.Сумма - СтрокаДохода.СуммаНДС;	
				ДвижениеДоходы.СуммаПланВалютаСНДС = 0;
				ДвижениеДоходы.СуммаПланВалютаБезНДС = 0;
				ДвижениеДоходы.СуммаФактБезНДС = 0;
				ДвижениеДоходы.СуммаФактСНДС = 0;
				ДвижениеДоходы.СуммаФактВалютаБезНДС = 0;
				ДвижениеДоходы.СуммаФактВалютаСНДС = 0;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;	
		
КонецПроцедуры

// Процедура формирует записи по регистру "упДоходы".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияФактическиеДоходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияДоходы	= Движения.упДоходы;
	ДвиженияДоходы.Записывать = Истина;

	тбзДоходы = ДополнительныеСвойства.Доходы;
	ВедетсяВалютныйУчет = ДополнительныеСвойства.ВедетсяВалютныйУчет;
	
	Для каждого СтрокаДохода Из тбзДоходы Цикл
		ДвижениеДоходы = ДвиженияДоходы.Добавить();
		ДвижениеДоходы.Период      = Реквизиты.Дата;
		ДвижениеДоходы.Регистратор = Ссылка;
		ДвижениеДоходы.ЗаявкаНаПеревозкуГруза = ?(Реквизиты.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам, 
														СтрокаДохода.ЗаявкаНаПеревозкуГруза, 
														Реквизиты.ЗаявкаНаПеревозкуГруза);
		ДвижениеДоходы.СтатьяДоходов = СтрокаДохода.СтатьяДоходов;
		Если ВедетсяВалютныйУчет Тогда
			ДвижениеДоходы.Валюта = СтрокаДохода.Валюта;
			ДвижениеДоходы.СуммаПланВалютаСНДС   = 0;
			ДвижениеДоходы.СуммаПланВалютаБезНДС = 0;
			ДвижениеДоходы.СуммаПланСНДС         = 0;
			ДвижениеДоходы.СуммаПланБезНДС       = 0;
			ДвижениеДоходы.СуммаФактБезНДС       = СтрокаДохода.Сумма - СтрокаДохода.СуммаНДС;
			ДвижениеДоходы.СуммаФактСНДС         = СтрокаДохода.Сумма;
			ДвижениеДоходы.СуммаФактВалютаБезНДС = СтрокаДохода.СуммаВалюта - СтрокаДохода.СуммаВалютаНДС;
			ДвижениеДоходы.СуммаФактВалютаСНДС   = СтрокаДохода.СуммаВалюта;
		Иначе
			ДвижениеДоходы.Валюта = Справочники.Валюты.ПустаяСсылка();
			ДвижениеДоходы.СуммаПланВалютаСНДС   = 0;
			ДвижениеДоходы.СуммаПланВалютаБезНДС = 0;
			ДвижениеДоходы.СуммаПланСНДС         = 0;
			ДвижениеДоходы.СуммаПланБезНДС       = 0;	
			ДвижениеДоходы.СуммаФактБезНДС       = СтрокаДохода.Сумма - СтрокаДохода.СуммаНДС;
			ДвижениеДоходы.СуммаФактСНДС         = СтрокаДохода.Сумма;
			ДвижениеДоходы.СуммаФактВалютаБезНДС = 0;
			ДвижениеДоходы.СуммаФактВалютаСНДС   = 0;
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры

// Процедура формирует записи по регистру "упИнкассацияДенежныхСредств".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ ИЛИ НЕ ДополнительныеСвойства.ИспользуютсяУслугиИнкассации Тогда 
		Возврат; 
	КонецЕсли;
		
	ДвиженияИнкассация	= Движения.упИнкассацияДенежныхСредств;
	ДвиженияИнкассация.Записывать = Истина;
		
	Статус	= ДополнительныеСвойства.Статус;
	Если (Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
		ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично) Тогда 
		
		Если ДополнительныеСвойства.СпособУчетаИнкассируемыхЦенностей = Перечисления.упСпособыУчетаИнкассируемыхЦенностей.ВЦеломПоЗаявкеНаПеревозкуГруза Тогда
			Если Реквизиты.СуммаИнкассацииПлан > 0 Тогда
				ДвижениеИнкассация = ДвиженияИнкассация.Добавить();
				ДвижениеИнкассация.Период = Реквизиты.Дата;
				ДвижениеИнкассация.Регистратор = Ссылка;
				ДвижениеИнкассация.ЗаявкаНаПеревозкуГруза = Ссылка;
				ДвижениеИнкассация.Валюта = Реквизиты.Валюта;
				ДвижениеИнкассация.СуммаПлан = Реквизиты.СуммаИнкассацииПлан;
				ДвижениеИнкассация.СуммаФакт = 0;
			КонецЕсли;
		Иначе	
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК ЗаявкаНаПеревозкуГруза,
			               |	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто,
			               |	упГрузовыеМеста.Валюта,
			               |	упГрузовыеМеста.Сумма КАК СуммаПлан
			               |ИЗ
			               |	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК упЗаявкаНаПеревозкуГрузаГрузовыеМеста
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
			               |		ПО упЗаявкаНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка
			               |ГДЕ
			               |	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.Ссылка = &Ссылка
			               |	И упГрузовыеМеста.Сумма > 0
			               |	И упГрузовыеМеста.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность)";
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Выборка	= Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ДвижениеИнкассация = ДвиженияИнкассация.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеИнкассация, Выборка);
				ДвижениеИнкассация.Период = Реквизиты.Дата;
				ДвижениеИнкассация.Регистратор = Ссылка;
				ДвижениеИнкассация.СуммаФакт = 0;		
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует записи по регистру "Расчеты с заказчиками".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ ИЛИ НЕ ДополнительныеСвойства.ВестиУчетРасчетовСЗаказчиками Тогда 
		Возврат; 
	КонецЕсли;
		
	ДвиженияРасчетыСЗаказчиками = Движения.упРасчетыСЗаказчиками;
	ДвиженияРасчетыСЗаказчиками.Записывать = Истина;
		
	Статус	= ДополнительныеСвойства.Статус;
	Если (Ложь
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
		 Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично)
	Тогда 
			
		сткПроцентыПоЭтапам = ЭтапыОплатыВСхемеПоСоглашеню(ДополнительныеСвойства, Реквизиты.Соглашение);
		Если ДополнительныеСвойства.КонтролироватьПредоплату Тогда
			Для каждого СтрокаПлановыеДоходы Из ДополнительныеСвойства.ПлановыеДоходы Цикл
				ДвижениеРасчетыСЗаказчиками = ДвиженияРасчетыСЗаказчиками.Добавить();
				ДвижениеРасчетыСЗаказчиками.Период = Реквизиты.Дата;
				ДвижениеРасчетыСЗаказчиками.ВидДвижения	= ВидДвиженияНакопления.Приход;
				ДвижениеРасчетыСЗаказчиками.Регистратор = Ссылка;
				ДвижениеРасчетыСЗаказчиками.АналитическийРазрезПоПартнерам 	= ДополнительныеСвойства.АналитическийРазрезПоПартнерам;
				ДвижениеРасчетыСЗаказчиками.ОбъектРасчетов = Ссылка;
				ДвижениеРасчетыСЗаказчиками.СтатьяДоходов  = СтрокаПлановыеДоходы.СтатьяДоходов;
				ДвижениеРасчетыСЗаказчиками.Валюта = СтрокаПлановыеДоходы.Валюта;
				ДвижениеРасчетыСЗаказчиками.Сумма = 0;
				Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
					ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаРаботыСЗаявкой = СтрокаПлановыеДоходы.СуммаВалюта * сткПроцентыПоЭтапам.ПредоплатаДоПередачиЗаявкиКВыполнению / 100;
					ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаВыполненияРейса = СтрокаПлановыеДоходы.СуммаВалюта * сткПроцентыПоЭтапам.ПредоплатаДоНачалаРейса / 100;
				Иначе
					ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаРаботыСЗаявкой = СтрокаПлановыеДоходы.Сумма * сткПроцентыПоЭтапам.ПредоплатаДоПередачиЗаявкиКВыполнению / 100;
					ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаВыполненияРейса = СтрокаПлановыеДоходы.Сумма * сткПроцентыПоЭтапам.ПредоплатаДоНачалаРейса / 100;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Процедура формирует записи по регистру "Расчеты с заказчиками" для документа фактические доходы.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияФактическиеДоходыРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ ИЛИ НЕ ДополнительныеСвойства.ВестиУчетРасчетовСЗаказчиками Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРасчетыСЗаказчиками	= Движения.упРасчетыСЗаказчиками;
	ДвиженияРасчетыСЗаказчиками.Записывать = Истина;
		
	Для каждого СтрокаДоходы Из ДополнительныеСвойства.Доходы Цикл
		ДвижениеРасчетыСЗаказчиками = ДвиженияРасчетыСЗаказчиками.Добавить();
		ДвижениеРасчетыСЗаказчиками.ВидДвижения				= ВидДвиженияНакопления.Приход;
		ДвижениеРасчетыСЗаказчиками.Период               	= Реквизиты.Дата;
		ДвижениеРасчетыСЗаказчиками.Регистратор          	= Ссылка;
		ДвижениеРасчетыСЗаказчиками.АналитическийРазрезПоПартнерам 	= ДополнительныеСвойства.АналитическийРазрезПоПартнерам;
		ДвижениеРасчетыСЗаказчиками.ОбъектРасчетов 			= ?(Реквизиты.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам, 
																СтрокаДоходы.ЗаявкаНаПеревозкуГруза, 
																Реквизиты.ЗаявкаНаПеревозкуГруза);
		ДвижениеРасчетыСЗаказчиками.СтатьяДоходов 			= СтрокаДоходы.СтатьяДоходов;
		ДвижениеРасчетыСЗаказчиками.Валюта 					= СтрокаДоходы.Валюта;
		Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
			ДвижениеРасчетыСЗаказчиками.Сумма					= СтрокаДоходы.СуммаВалюта;
		Иначе
			ДвижениеРасчетыСЗаказчиками.Сумма					= СтрокаДоходы.Сумма;
		КонецЕсли;
		ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаРаботыСЗаявкой	= 0;
		ДвижениеРасчетыСЗаказчиками.КОплатеДоНачалаВыполненияРейса	= 0;
	КонецЦикла;
		
КонецПроцедуры

// Процедура формирует запись в регистр "Аналитические разрезы по партнерам"
//
// Параметры:
//  Партнер					 - СправочникСсылка.упПартнеры					 - партнер
//  ПартнерДоИзменения			 - СправочникСсылка.упПартнеры					 - партнер до изменения.
//  Организация				 - СправочникСсылка.Организации					 - организация
//  ОрганизацияДоИзменения		 - СправочникСсылка.Организации					 - организация  до изменения.
//  Соглашение					 - СправочникСсылка.упСоглашенияСКлиентами			 - соглашение
//  СоглашениеДоИзменения		 - СправочникСсылка.упСоглашенияСКлиентами			 - соглашение до изменения.
//  Контрагент					 - СправочникСсылка.упКонтрагенты					 - контрагент
//  КонтрагентДоИзменения		 - СправочникСсылка.упКонтрагенты					 - контрагент до изменения.
//  АналитическийРазрезПоПартнерам	 - СправочникСсылка.упАналитическиеРазрезыПоПартнерам	 - аналитический разрез
//  Отказ						 - Булево										 - Истина, если ошибка.
//
Процедура СформироватьАналитическиеРазрезыПоПартнерам(Партнер, ПартнерДоИзменения = Неопределено, Организация, ОрганизацияДоИзменения = Неопределено, Соглашение, СоглашениеДоИзменения = Неопределено, Контрагент, КонтрагентДоИзменения = Неопределено, АналитическийРазрезПоПартнерам, Отказ) Экспорт
	
	Если НЕ ПартнерДоИзменения = Неопределено Тогда
		Если НЕ ПартнерДоИзменения = Партнер
			ИЛИ НЕ ОрганизацияДоИзменения = Организация 
			ИЛИ НЕ СоглашениеДоИзменения = Соглашение 
			ИЛИ НЕ КонтрагентДоИзменения = Контрагент Тогда
			
			ЗаписьТекущая = РегистрыСведений.упАналитическиеРазрезыПоПартнерам.СоздатьМенеджерЗаписи();	
			ЗаписьТекущая.Организация 	= ОрганизацияДоИзменения;
			ЗаписьТекущая.Партнер 		= ПартнерДоИзменения;
			ЗаписьТекущая.Соглашение	= СоглашениеДоИзменения;
			ЗаписьТекущая.Контрагент	= КонтрагентДоИзменения;
			ЗаписьТекущая.Прочитать();
		Иначе
			Возврат;
		КонецЕсли;
	Иначе	
		ЗаписьТекущая = РегистрыСведений.упАналитическиеРазрезыПоПартнерам.СоздатьМенеджерЗаписи();	
	КонецЕсли;
	
	ЗаписьТекущая.Организация 	= Организация;
	ЗаписьТекущая.Партнер 		= Партнер;
	ЗаписьТекущая.Соглашение	= Соглашение;
	ЗаписьТекущая.Контрагент	= Контрагент;
	ЗаписьТекущая.АналитическийРазрезПоПартнерам = АналитическийРазрезПоПартнерам;
	
	Попытка
		ЗаписьТекущая.Записать();	
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("ОшибкаПриЗаписиАналитическогоРазреза", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецПопытки;
	
КонецПроцедуры

// Процедура формирует записи по регистру "упПлановыеЭтапыПеревозки".
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьДвиженияПлановыеЭтапыМультимодальнойПеревозки(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
		
	ДвиженияПлановыеЭтапы = Движения.упПлановыеЭтапыПеревозки;
	ДвиженияПлановыеЭтапы.Записывать = Ложь;
	
	Статус = ДополнительныеСвойства.Статус;
	Если ДополнительныеСвойства.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
		Если ДополнительныеСвойства.Свойство("ЭтапыМультимодальнойПеревозки") Тогда
			ДвиженияПлановыеЭтапы.Записывать = Истина;
			ЭтапыМультимодальнойПеревозки = ДополнительныеСвойства.ЭтапыМультимодальнойПеревозки;
			Для каждого Строка Из ЭтапыМультимодальнойПеревозки Цикл
				ДвижениеПлановыеЭтапы = ДвиженияПлановыеЭтапы.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеПлановыеЭтапы, Строка);
				ДвижениеПлановыеЭтапы.Период = Реквизиты.Дата;
			КонецЦикла;		
		КонецЕсли; 
	Иначе
		ДвиженияПлановыеЭтапы.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает таблицу схем мультимодальных перевозок. 
//
// Параметры:
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Схема - СправочникСсылка.упСхемыМультимодальныхПеревозок - Ссылка на элемент.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Результат выполнения.
//
Функция ПодготовитьТаблицуДвиженийСНомерамиЦепейПоставок(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|//Параметры грузовых мест
	|ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.Тара КАК Тара,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.СхемаМультимодальнойПеревозки КАК Схема,
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК упЗаявкаНаПеревозкуГрузаГрузовыеМеста
	|ГДЕ
	|	упЗаявкаНаПеревозкуГрузаГрузовыеМеста.Ссылка = &Ссылка
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//Схемы мультимодальных перевозок с адресами
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	ВЫБОР
	|		КОГДА НЕ втГрузовыеМеста.ВозвратПоЦепиПоставок
	|			ТОГДА тбСхемы.АдресОтправления
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|	КОНЕЦ КАК АдресОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втГрузовыеМеста.ВозвратПоЦепиПоставок
	|			ТОГДА тбСхемы.АдресПолучения
	|		ИНАЧЕ тбСхемы.АдресОтправления
	|	КОНЕЦ КАК АдресПолучения
	|ПОМЕСТИТЬ втСхемыМультимодальныхПеревозок
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок КАК тбСхемы
	|	ПО тбСхемы.Ссылка = втГрузовыеМеста.Схема
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//Промежуточные точки по схемам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПромежуточныеТочки.НомерСтроки КАК НомерСтроки,
	|	ПромежуточныеТочки.Адрес КАК Адрес,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемы.Схема КАК Схема
	|ПОМЕСТИТЬ втПромежуточныеТочкиПоСхемам
	|ИЗ
	|	втСхемыМультимодальныхПеревозок КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочки
	|	ПО втСхемы.Схема = ПромежуточныеТочки.Ссылка
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|//Для промежуточных точек определяются номера последних точек
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиПоСхемам.Схема КАК Схема,
	|	втПромежуточныеТочкиПоСхемам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	МАКСИМУМ(втПромежуточныеТочкиПоСхемам.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ втНомераПоследнихПромежуточныхТочек
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочкиПоСхемам
	|СГРУППИРОВАТЬ ПО
	|	втПромежуточныеТочкиПоСхемам.Схема,
	|	втПромежуточныеТочкиПоСхемам.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|//Для последних промежуточных точек схем возврата, заполняются адреса
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втНомераПоследнихТочек.НомерСтроки КАК НомерПоследнейТочки
	|ПОМЕСТИТЬ втАдресаОтправленияПоСхемамВозврата
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И втПромежуточныеТочки.НомерСтроки = втНомераПоследнихТочек.НомерСтроки
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|//Схемы с адресами, с учетом схем возврата
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	ВЫБОР
	|		КОГДА НЕ втСхемы.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемы.АдресОтправления
	|		ИНАЧЕ втСхемы.АдресОтправления
	|	КОНЕЦ КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	ЕстьNull(втАдресаОтправленияПоСхемамВозврата.НомерПоследнейТочки,0) КАК НомерПоследнейТочки
	|ПОМЕСТИТЬ втСхемыСАдресами
	|ИЗ
	|	втСхемыМультимодальныхПеревозок КАК втСхемы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаОтправленияПоСхемамВозврата КАК втАдресаОтправленияПоСхемамВозврата
	|	ПО втСхемы.Схема = втАдресаОтправленияПоСхемамВозврата.Схема
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|//Промежуточные точки для схем возврата (без последней промежуточной точки)
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Адрес КАК Адрес,
	|	втПромежуточныеТочки.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втПромежуточныеТочки.НомерСтроки КАК НомерСтроки,
	|	втПромежуточныеТочки.Схема КАК Схема
	|ПОМЕСТИТЬ втПромежуточныеТочкиДляСхемВозврата
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихПромежуточныхТочек
	|	ПО втПромежуточныеТочки.Схема = втНомераПоследнихПромежуточныхТочек.Схема
	|		И втНомераПоследнихПромежуточныхТочек.ВозвратПоЦепиПоставок = втНомераПоследнихПромежуточныхТочек.ВозвратПоЦепиПоставок
	|		И втНомераПоследнихПромежуточныхТочек.ВозвратПоЦепиПоставок
	|ГДЕ
	|	втПромежуточныеТочки.НомерСтроки <> втНомераПоследнихПромежуточныхТочек.НомерСтроки
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|//Схемы без возврата по цепи поставок
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА упСхемы.Схема ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|			КОГДА втПромежуточныеТочки.Адрес ЕСТЬ NULL
	|				ТОГДА упСхемы.НомерПоследнейТочки + 1
	|			КОГДА втПромежуточныеТочки.НомерСтроки = 1
	|				ТОГДА 0
	|			ИНАЧЕ втПромежуточныеТочки.НомерСтроки + 1
	|		КОНЕЦ
	|	КОНЕЦ КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.Схема КАК Схема
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСхемыСАдресами КАК упСхемы
	|	ПО упСхемы.Схема = втГрузовыеМеста.Схема
	|		И упСхемы.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И упСхемы.АдресОтправления В (втГрузовыеМеста.АдресОтправления,
	|				ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|		И упСхемы.АдресПолучения В (втГрузовыеМеста.АдресПолучения,
	|				ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ПО втГрузовыеМеста.АдресПолучения = втПромежуточныеТочки.Адрес
	|		И втГрузовыеМеста.Схема = втПромежуточныеТочки.Схема
	|ГДЕ
	|	НЕ (втГрузовыеМеста.ВозвратПоЦепиПоставок)
	|ОБЪЕДИНИТЬ ВСЕ
	|//Схемы с возвратом по цепи поставок
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА упСхемы.Схема ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|			КОГДА втПромежуточныеТочки.Адрес ЕСТЬ NULL
	|				ТОГДА упСхемы.НомерПоследнейТочки
	|			КОГДА втПромежуточныеТочки.НомерСтроки + 1 = упСхемы.НомерПоследнейТочки
	|				ТОГДА 0
	|			ИНАЧЕ втПромежуточныеТочки.НомерСтроки
	|		КОНЕЦ
	|	КОНЕЦ КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.Схема КАК Схема
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСхемыСАдресами КАК упСхемы
	|	ПО упСхемы.Схема = втГрузовыеМеста.Схема
	|		И упСхемы.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И упСхемы.АдресОтправления В (втГрузовыеМеста.АдресОтправления,
	|				ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|		И упСхемы.АдресПолучения В (втГрузовыеМеста.АдресПолучения,
	|				ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПромежуточныеТочкиДляСхемВозврата КАК втПромежуточныеТочки
	|	ПО втГрузовыеМеста.АдресОтправления = втПромежуточныеТочки.Адрес
	|		И втГрузовыеМеста.Схема = втПромежуточныеТочки.Схема
	|ГДЕ
	|	втГрузовыеМеста.ВозвратПоЦепиПоставок
	|УПОРЯДОЧИТЬ ПО
	|	втГрузовыеМеста.Схема
	|ИТОГИ ПО
	|	Схема
	|";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);

КонецФункции

// Процедура - Заполнить этапы оплаты ТЧСхемы по умолчанию
//
// Параметры:
//  ТабличнаяЧасть	 - ТабличнаяЧасть	 - Табличная часть
//
Процедура ЗаполнитьЭтапыОплатыТЧСхемыПоУмолчанию(ТабличнаяЧасть) Экспорт
	НоваяСтрока = ТабличнаяЧасть.Добавить();	
	НоваяСтрока.ВариантОплаты = ПредопределенноеЗначение("Перечисление.упВариантыОплаты.ПредоплатаДоПередачиЗаявкиКВыполнению");
	НоваяСтрока.ПроцентОплаты = 100;
	
	НоваяСтрока = ТабличнаяЧасть.Добавить();	
	НоваяСтрока.ВариантОплаты = ПредопределенноеЗначение("Перечисление.упВариантыОплаты.ПредоплатаДоНачалаРейса");
	НоваяСтрока.ПроцентОплаты = 0;

	НоваяСтрока = ТабличнаяЧасть.Добавить();	
	НоваяСтрока.ВариантОплаты = ПредопределенноеЗначение("Перечисление.упВариантыОплаты.ОплатаПослеОкончанияРейса");
	НоваяСтрока.ПроцентОплаты = 0;
КонецПроцедуры

Функция ЭтапыОплатыВСхемеПоСоглашеню(ДополнительныеСвойства, СоглашениеСКлиентом)
	
	сткПроцентыПоЭтапам = Новый Структура();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСхемыОплатыУслугЭтапыОплаты.ВариантОплаты,
	               |	упСхемыОплатыУслугЭтапыОплаты.ПроцентОплаты
	               |ИЗ
	               |	Справочник.упСоглашенияСКлиентами КАК упСоглашенияСКлиентами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыОплатыУслуг.ЭтапыОплаты КАК упСхемыОплатыУслугЭтапыОплаты
	               |		ПО упСоглашенияСКлиентами.СхемаОплаты = упСхемыОплатыУслугЭтапыОплаты.Ссылка
	               |ГДЕ
	               |	упСоглашенияСКлиентами.Ссылка = &Соглашение";
	Запрос.УстановитьПараметр("Соглашение", СоглашениеСКлиентом);
	тбзЭтапы = Запрос.Выполнить().Выгрузить();
	Если тбзЭтапы.Количество() = 0 Тогда
		ДополнительныеСвойства.Вставить("КонтролироватьПредоплату", Ложь);
	Иначе
		ДополнительныеСвойства.Вставить("КонтролироватьПредоплату", Истина);
	КонецЕсли;
	Для каждого СтрокаЭтап Из тбзЭтапы Цикл
		сткПроцентыПоЭтапам.Вставить(ОбщегоНазначения.ИмяЗначенияПеречисления(СтрокаЭтап.ВариантОплаты), СтрокаЭтап.ПроцентОплаты);			
	КонецЦикла;
	
	Возврат сткПроцентыПоЭтапам;
	
КонецФункции

// Процедура - Настроить форму путевого листа.
//
// Параметры:
//  Форма				 - Форма	 - настраиваемая форма путевого листа.
//  ПорядокИсполнения	 - ПеречислениеСсылка.упПорядокИсполненияЗаявок	 - порядок исполнения заявок.
//  СчетчикМоточасов	 - Булево	 - Истина, если используется счетчик.
//
Процедура НастроитьФормуПутевогоЛиста(Форма, ПорядокИсполнения, СчетчикМоточасов) Экспорт
	
	сткНастроек = Новый Структура;
	сткНастроек.Вставить("ИспользоватьПорядокИсполненияЗаявокПутевымЛистом", ПорядокИсполнения = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом);
	сткНастроек.Вставить("ИспользоватьСчетчикМоточасовТранспортныхСредств",  СчетчикМоточасов);
	упСервисныеФункции.НастроитьФормуПоПараметрам(Форма, сткНастроек);
		
КонецПроцедуры

#Область ФормированиеЗаданияНаПеревозкуГруза

// Процедура - Добавляет и заполняет строку в таблицу графики работ.
//
// Параметры:
//  ТаблицаГрафикиРабот		 - ТаблицаЗначений	 - таблица с колонками:
//		* ГрафикРаботы		 - СправочникСсылка.Календари - график работы.
//		* ДатаС			 - ДатаВремя	 - начало временного окна.
//		* ДатаПо			 - ДатаВремя	 - окончание временного окна.
//  ГрафикРаботы			 - СправочникСсылка.Календари	 - добавляемый график работы.
//  ДатаС					 - ДатаВремя	 - добавляемое начало интервала.
//  ДатаПо				 - ДатаВремя	 - добавляемое окончание интервала.
//  ДатаНачалаРасписаний		 - ДатаВремя	 - нижняя граница дат заполнения расписаний.
//  ДатаОкончанияРасписаний	 - ДатаВремя	 - верхняя граница дат заполнения расписаний.
//  мсвГрафикиРабот			 - Массив	 - массив графиков работ.
//
Процедура ДобавитьСтрокуВТаблицуГрафикиРабот(ТаблицаГрафикиРабот, ГрафикРаботы, ДатаС, ДатаПо, ДатаНачалаРасписаний, ДатаОкончанияРасписаний, мсвГрафикиРабот) Экспорт
	Если Истина
		И ЗначениеЗаполнено(ГрафикРаботы) 
		И (Ложь
			Или ЗначениеЗаполнено(ДатаС)
			Или ЗначениеЗаполнено(ДатаПо)) 
	Тогда
		НоваяСтрокаГрафик = ТаблицаГрафикиРабот.Добавить();
		НоваяСтрокаГрафик.График = ГрафикРаботы;
		НоваяСтрокаГрафик.ДатаС	= ДатаС;
		НоваяСтрокаГрафик.ДатаПо	= ДатаПо;
		
		Если ЗначениеЗаполнено(ДатаС) Тогда
			ДатаНачалаРасписаний = Мин(ДатаНачалаРасписаний, ДатаС);
		КонецЕсли;
		
		ДатаОкончанияРасписаний = Макс(ДатаОкончанияРасписаний, ДатаПо);
		
		мсвГрафикиРабот.Добавить(ГрафикРаботы);	
	КонецЕсли;
КонецПроцедуры	

// Функция - Ближайшие окна расписаний для заданных интервалов
//
// Параметры:
//  ТаблицаИнтервалов		 - ТаблицаЗначений	 -  таблица с колонками:
//		* ГрафикРаботы		 - СправочникСсылка.Календари - график работы.
//		* ДатаС			 - ДатаВремя	 - начало временного окна.
//		* ДатаПо			 - ДатаВремя	 - окончание временного окна.
//  ДатаНачалаРасписаний		 - ДатаВремя	 - нижняя граница дат заполнения расписаний.
//  ДатаОкончанияРасписаний	 - ДатаВремя	 - верхняя граница дат заполнения расписаний.
//  мсвГрафикиРабот			 - Массив	 - массив графиков работ (СправочникСсылка.Календари).
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками:
//		* ГрафикРаботы		 - СправочникСсылка.Календари - график работы.
//		* ДатаС			 - ДатаВремя	 - дата с.
//		* ДатаПо			 - ДатаВремя	 - дата по.
//		* ДатаВремяНачала	 - ДатаВремя	 - дата начала.
//		* ДатаВремяОкончания - ДатаВремя	 - дата окончания.
//
Функция БлижайшиеОкнаРасписанийДляЗаданныхИнтервалов(ТаблицаИнтервалов, ДатаНачалаРасписаний, ДатаОкончанияРасписаний, мсвГрафикиРабот) Экспорт
	
	тбзРезультат = Неопределено;
	НачалоРасписаний 	= ДатаНачалаРасписаний;
	ОкончаниеРасписаний	= ДатаОкончанияРасписаний;
	
	Если Ложь
		Или ЗначениеЗаполнено(НачалоРасписаний)
		Или ЗначениеЗаполнено(ОкончаниеРасписаний) 
 	Тогда
	    Если НЕ ЗначениеЗаполнено(НачалоРасписаний) Тогда
			НачалоРасписаний = ОкончаниеРасписаний;
		ИначеЕсли НЕ ЗначениеЗаполнено(ОкончаниеРасписаний) Тогда
			ОкончаниеРасписаний = НачалоРасписаний;
		КонецЕсли;	
		
		// Расписание берется с отклонением в 3 месяца, что бы наверняка получить в расписании значения для даты начала и окончания.
		НачалоРасписаний	= ДобавитьМесяц(НачалоРасписаний, -3);
		Если ОкончаниеРасписаний < Дата("39990101") Тогда
			ОкончаниеРасписаний	= ДобавитьМесяц(ОкончаниеРасписаний, 3);
		КонецЕсли;

		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		// Создаем временную таблицу расписаний.
		ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(МенеджерВременныхТаблиц, мсвГрафикиРабот, НачалоРасписаний, ОкончаниеРасписаний);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	РасписанияРаботы.ГрафикРаботы КАК ГрафикРаботы,
		|	РасписанияРаботы.ДатаГрафика КАК ДатаГрафика,
		|	РасписанияРаботы.ВремяНачала КАК ВремяНачала,
		|	РасписанияРаботы.ВремяОкончания КАК ВремяОкончания,
		|	ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), РасписанияРаботы.ВремяНачала, СЕКУНДА)) КАК ДатаВремяНачала,
		|	ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), РасписанияРаботы.ВремяОкончания, СЕКУНДА)) КАК ДатаВремяОкончания
		|ПОМЕСТИТЬ ВТРасписаниеРаботыСВременем
		|ИЗ
		|	ВТРасписанияРаботы КАК РасписанияРаботы
		|;
		|//{Запрос: 7 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикиРаботТ.График КАК График,
		|	ГрафикиРаботТ.ДатаС КАК ДатаС,
		|	ГрафикиРаботТ.ДатаПо КАК ДатаПо
		|ПОМЕСТИТЬ втВременныеОкна
		|ИЗ
		|	&ГрафикиРабот КАК ГрафикиРаботТ
		|;
		|//{Запрос: 8 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втВременныеОкнаТ.График КАК График,
		|	втВременныеОкнаТ.ДатаПо КАК ДатаПо,
		|	втВременныеОкнаТ.ДатаС КАК ДатаС,
		|	МИНИМУМ(ВТРасписанияРаботы.ДатаВремяНачала) КАК ДатаВремяНачала,
		|	МИНИМУМ(ВТРасписанияРаботы.ДатаВремяОкончания) КАК ДатаВремяОкончания
		|ИЗ
		|	втВременныеОкна КАК втВременныеОкнаТ
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТРасписаниеРаботыСВременем КАК ВТРасписанияРаботы
		|	ПО ИСТИНА
		|		И втВременныеОкнаТ.График = ВТРасписанияРаботы.ГрафикРаботы
		|			И (ЛОЖЬ
		|				ИЛИ втВременныеОкнаТ.ДатаС <= ВТРасписанияРаботы.ДатаВремяНачала
		|				ИЛИ втВременныеОкнаТ.ДатаС <= ВТРасписанияРаботы.ДатаВремяОкончания)
		|СГРУППИРОВАТЬ ПО
		|	втВременныеОкнаТ.График,
		|	втВременныеОкнаТ.ДатаС,
		|	втВременныеОкнаТ.ДатаПо
		|";
		
		ЗапросРасписание = Новый Запрос(ТекстЗапроса);
		ЗапросРасписание.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ЗапросРасписание.УстановитьПараметр("ГрафикиРабот", ТаблицаИнтервалов);
		
		тбзРезультат = ЗапросРасписание.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Возврат тбзРезультат;		
	
КонецФункции

// Процедура - Заполнить ограничения временных окон по расписаниям
//
// Параметры:
//  ЗаданиеСтрока				 - СтрокаДереваЗначений	 - строка дерева.
//  ЭтоОкноОтправления			 - Булево				 - это окно отправления.
//  ТаблицаИнтерваловПоСРасписаниям - ТаблицаЗначений		 - таблица интервалов.
//  ГрафикРаботы				 - СправочникСсылка.Календари	 - график работы.
//  ДатаС						 - ДатаВремя				 - дата с.
//  ДатаПо					 - ДатаВремя				 - дата по.
//
Процедура ЗаполнитьОграниченияВременныхОконПоРасписаниям(ЗаданиеСтрока, ЭтоОкноОтправления = Истина, ТаблицаИнтерваловПоСРасписаниям, ГрафикРаботы, ДатаС, ДатаПо) Экспорт
	Если Истина
		И ЗначениеЗаполнено(ГрафикРаботы) 
		И (Ложь
			Или ЗначениеЗаполнено(ДатаС)
			Или ЗначениеЗаполнено(ДатаПо)) 
	Тогда
	
		Если ЭтоОкноОтправления Тогда
			ИмяВременногоОкна = "ОтправлениеГруза";
		Иначе	
			ИмяВременногоОкна = "ПолучениеГруза";
		КонецЕсли;	
		
		ИмяПоляС	= ИмяВременногоОкна + "С";
		ИмяПоляПо	= ИмяВременногоОкна + "По";;
	
		сткОтбор = Новый Структура("График, ДатаС, ДатаПо", ГрафикРаботы, ДатаС, ДатаПо);
		мсвСтрокиРасписания = ТаблицаИнтерваловПоСРасписаниям.НайтиСтроки(сткОтбор);
		Если мсвСтрокиРасписания.Количество() > 0 Тогда
			ДатаВремяНачала 	= мсвСтрокиРасписания[0].ДатаВремяНачала;
			ДатаВремяОкончания	= мсвСтрокиРасписания[0].ДатаВремяОкончания;
			Если ЗначениеЗаполнено(ДатаС)	И ЗначениеЗаполнено(ДатаВремяНачала) Тогда
				ЗаданиеСтрока[ИмяПоляС] = ДатаВремяНачала;
			КонецЕсли;
			Если ЗначениеЗаполнено(ДатаПо) И ЗначениеЗаполнено(ДатаВремяОкончания) Тогда
				ЗаданиеСтрока[ИмяПоляПо] = ДатаВремяОкончания;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - Установить ограничение на временное окно
//
// Параметры:
//  ЗаданиеСтрока					 - СтрокаДереваЗначений	 - строка дерева.
//  ЭтоОкноОтправления				 - Булево	 - это окно отправления.
//  ВариантОпределенияВременногоОкна	 - Перечисление	 - вариант определения временного окна.
//
Процедура УстановитьОграничениеНаВременноеОкно(ЗаданиеСтрока, ЭтоОкноОтправления = Истина, ВариантОпределенияВременногоОкна) Экспорт
	
	Если ЗаданиеСтрока.НомерВЦепиПоставок > 0 Тогда
		Если ЭтоОкноОтправления Тогда
			ИмяВременногоОкна = "ОтправлениеГруза";
		Иначе	
			ИмяВременногоОкна = "ПолучениеГруза";
		КонецЕсли;	
		
		ИмяПоляС	= ИмяВременногоОкна + "С";
		ИмяПоляПо	= ИмяВременногоОкна + "По";;
		
		Если ВариантОпределенияВременногоОкна = Перечисления.упВариантыОпределенияВременныхОкон.ПостояннаяДоступность Тогда
			ЗаданиеСтрока[ИмяПоляС] 	= '00010101';	
			ЗаданиеСтрока[ИмяПоляПо]	= '00010101';
		ИначеЕсли ВариантОпределенияВременногоОкна = Перечисления.упВариантыОпределенияВременныхОкон.ТолькоНачалоОкна Тогда
			ЗаданиеСтрока[ИмяПоляПо]	= '00010101';	
		ИначеЕсли ВариантОпределенияВременногоОкна = Перечисления.упВариантыОпределенияВременныхОкон.ТолькоОкончаниеОкна Тогда	
			ЗаданиеСтрока[ИмяПоляС] 	= '00010101';	
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

// Процедура устанавливает признак необходимости расчета статуса заявки.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура УстановитьПризнакНеобходимостиРасчетаСтатуса(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РассчитатьСтатус", Ложь);
			
	Статус = ДополнительныеСвойства.Статус;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично
	Тогда 
			
		ДополнительныеСвойства.Вставить("РассчитатьСтатус", Истина);
		
	КонецЕсли;	
	
КонецПроцедуры

// Процедура формирует плановые этапы мультимодальной перевозки.
//
// Параметры:
//	ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//	Движения - КоллекцияДвижений - Коллекция движений документа.
//	Реквизиты - Структура - Реквизиты.
//	Отказ - Булево - Применить изменения.
//
Процедура СформироватьПлановыеЭтапыМультимодальнойПеревозки(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ТекущийСтатус = ДополнительныеСвойства.Статус;
	Статус        = ДополнительныеСвойства.Статус;
	
	Если ДополнительныеСвойства.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
		Если Не ДополнительныеСвойства.Свойство("ЭтапыМультимодальнойПеревозки") Тогда // этапы не сформированы в рабочем месте
			СформироватьЭтапы = Ложь;
			ПрочитатьЭтапы = Ложь;
			Если ДополнительныеСвойства.Свойство("ПересчитатьЭтапыПеревозки") Тогда 
				СформироватьЭтапы = Истина;
			ИначеЕсли Истина
					И ДополнительныеСвойства.ИзменяетсяСтатус
				     И Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению Тогда 	
				
				Запрос = Новый Запрос;
				Запрос.Текст =
				"ВЫБРАТЬ
				|	*
				|ИЗ
				|	РегистрСведений.упПлановыеЭтапыПеревозки КАК УпПлановыеЭтапыМультимодальнойПеревозки
				|ГДЕ 
				| ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
				|";
				Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", Ссылка);
				РезультатЗапроса = Запрос.Выполнить(); 
				
				Если Не РезультатЗапроса.Пустой() Тогда
					ДополнительныеСвойства.Вставить("ЭтапыМультимодальнойПеревозки", РезультатЗапроса.Выгрузить()); 					
				Иначе
					СформироватьЭтапы = Истина;	
				КонецЕсли;
			Иначе
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Ссылка", Ссылка);
				Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	упПлановыеЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
				|ИЗ
				|	РегистрСведений.упПлановыеЭтапыПеревозки КАК упПлановыеЭтапыПеревозки
				|ГДЕ
				|	упПлановыеЭтапыПеревозки.ЗаявкаНаПеревозкуГруза = &Ссылка
				|	И упПлановыеЭтапыПеревозки.Изменен";
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					Возврат;
				КонецЕсли; 
				
				Если Ложь
					Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
					Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок
					Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована
					Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована
					Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
				Тогда	
					СформироватьЭтапы = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если СформироватьЭтапы Тогда
				ТекстОшибки = "";
				ДополнительныеСвойства.Вставить("ЭтапыМультимодальнойПеревозки", Документы.упЗаявкаНаПеревозкуГруза.СформироватьЭтапыПеревозки(Ссылка, ТекстОшибки, Отказ)); 					
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция СобытиеОшибкаЗаявкаНаПеревозкуГруза(ПодСобытие = "") Экспорт
	
	Результат = СтрШаблон(НСтр("ru = 'Заявка на перевозку груза%1'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ?(ЗначениеЗаполнено(ПодСобытие), "." + ПодСобытие, ""));
	
	Возврат Результат;
КонецФункции

#КонецОбласти 

#КонецОбласти