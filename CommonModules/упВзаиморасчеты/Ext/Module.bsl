
#Область СлужебныеПроцедурыИФункции

// Возвращае структуру параметров для соединения с сервисом УТ11.
//
Функция ПараметрыHTTPСоединенияССервисомУТ11() Экспорт
	
	ПутьКСервису	= упСервисныеФункции.ПолучитьЗначениеКонстанты("упВзаиморасчетыПутьКСервису");
	ИспользоватьАутентификациюОС	= упСервисныеФункции.ПолучитьЗначениеКонстанты("упВзаиморасчетыИспользоватьАутентификациюОС");
	Пользователь	= упСервисныеФункции.ПолучитьЗначениеКонстанты("упВзаиморасчетыПользователь");
	Пароль			= упСервисныеФункции.ПолучитьЗначениеКонстанты("упВзаиморасчетыПароль");
	
	сткПараметрыURI	= ОбщегоНазначенияКлиентСервер.СтруктураURI(ПутьКСервису);
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Хост", 							сткПараметрыURI.Хост); // Например, localhost
	Если ИспользоватьАутентификациюОС Тогда
		сткПараметры.Вставить("Пользователь",				Неопределено);
		сткПараметры.Вставить("Пароль", 					Неопределено);
	Иначе	
		сткПараметры.Вставить("Пользователь",				Пользователь);
		сткПараметры.Вставить("Пароль", 					Пароль);	
	КонецЕсли;
	
	сткПараметры.Вставить("Порт", 							сткПараметрыURI.Порт);
	сткПараметры.Вставить("ИспользоватьАутентификациюОС ", 	ИспользоватьАутентификациюОС);
	
	текПутьКСервису	= сткПараметрыURI.ПутьНаСервере + "hs/SettlementWithPartners/";
	
	сткПараметры.Вставить("АдресРесурсаВзаиморасчетыСПоставщиками", текПутьКСервису+ "GetStatusSettlementsWithSuppliers");
	сткПараметры.Вставить("АдресРесурсаВзаиморасчетыСКлиентами", 	текПутьКСервису+ "GetStatusSettlementsWithCustomers");
		
	сткРезультат = Новый ФиксированнаяСтруктура(сткПараметры);
	
	Возврат сткРезультат;
		
КонецФункции

Функция СостояниеВзаиморасчетов(Партнер, Организация, АдресХранилища, АдресРесурса)
	
	текИдентификаторПартнера 	= ПолучитьИдентификаторОбменаПоОбъекту(Партнер);
	текИдентификаторОрганизации = ПолучитьИдентификаторОбменаПоОбъекту(Организация);
	
	текРезультат = Ложь;
	Попытка
		сткПараметрыHTTPСоединения = ПараметрыHTTPСоединенияССервисомУТ11();
			
		HTTPСоединение = Новый HTTPСоединение(сткПараметрыHTTPСоединения.Хост,
												сткПараметрыHTTPСоединения.Порт,
												сткПараметрыHTTPСоединения.Пользователь,
												сткПараметрыHTTPСоединения.Пароль,,,,
												сткПараметрыHTTPСоединения.ИспользоватьАутентификациюОС);
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = сткПараметрыHTTPСоединения[АдресРесурса]	+ "?PartnerUID=" + текИдентификаторПартнера + "&OrganizationUID=" + текИдентификаторОрганизации;
		HTTPЗапрос.Заголовки.Вставить("Content-type", "text/xml");
		HTTPОтвет 	= HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		ЕстьОшибка 	= НЕ HTTPОтвет.КодСостояния = 200;
		Если НЕ ЕстьОшибка тогда
			ПоместитьВоВременноеХранилище(HTTPОтвет.ПолучитьТелоКакСтроку(), АдресХранилища);		
		Иначе
			ПоместитьВоВременноеХранилище(Ложь, АдресХранилища);		
			ТекстСообщения = Нстр("ru = 'Не удалось загрузить состояние взаиморасчетов. Код состояния подключения:%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, HTTPОтвет.КодСостояния);
			ЗаписьЖурналаРегистрации("HTPPСервисВзаиморасчетыСКлиентом", УровеньЖурналаРегистрации.Ошибка,,,ТекстСообщения);
		КонецЕсли	
	Исключение
		ПоместитьВоВременноеХранилище(Ложь, АдресХранилища);		
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("HTPPСервисВзаиморасчетыСКлиентом", УровеньЖурналаРегистрации.Ошибка,,,ТекстСообщения);
	КонецПопытки;

КонецФункции

// Записывает информация по взаиморасчетам с клиентом по адресу во временное хранилище.
//
Функция СостояниеВзаиморасчетовСКлиентом(Партнер, Организация, АдресХранилища) Экспорт
	
	СостояниеВзаиморасчетов(Партнер, Организация, АдресХранилища, "АдресРесурсаВзаиморасчетыСКлиентами");

КонецФункции

// Записывает информация по взаиморасчетам с поставщикосм по адресу во временное хранилище.
//
Функция СостояниеВзаиморасчетовСПоставщиками(Партнер, Организация, АдресХранилища) Экспорт
	
	СостояниеВзаиморасчетов(Партнер, Организация, АдресХранилища, "АдресРесурсаВзаиморасчетыСПоставщиками");

КонецФункции

// Возвращает таблицу с данными по взаиморасчетамми с клиентом.
//
Функция СформироватьТаблицуВзаиморасчетовСКлиентами(СтрокаXML) Экспорт
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипЧисло	= Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,2));
	тбзДанныеПоРасчетам = Новый ТаблицаЗначений;
	тбзДанныеПоРасчетам.Колонки.Добавить("Валюта", 					ТипСтрока);
	тбзДанныеПоРасчетам.Колонки.Добавить("ДолгКлиента", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КОплате", 				ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КОтгрузке", 				ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("НашДолг", 				ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("АвансДоОбеспечения", 		ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("ПредоплатаДоОтгрузки",	ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КредитПослеОтгрузки",		ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КОплатеПросрочено",		ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КОтгрузкеПросрочено",		ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КВозвратуДС", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КВозвратуДСПросрочено", 	ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("Отгружается", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("Оплачивается", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("СальдоДолга", 			ТипЧисло);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	
	// Чтение строк
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента 
			И ЧтениеXML.Имя = "row" Тогда
			
			НоваяСтрока = тбзДанныеПоРасчетам.Добавить();
			НоваяСтрока.Валюта 		= ЧтениеXML.ПолучитьАтрибут("currency");
			НоваяСтрока.ДолгКлиента = ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("debt"));
			НоваяСтрока.КОплате 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_pay"));
			НоваяСтрока.КОтгрузке 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("for_shipment"));
			НоваяСтрока.НашДолг 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("our_debt"));
			НоваяСтрока.АвансДоОбеспечения 		= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("prepayment_provision"));
			НоваяСтрока.ПредоплатаДоОтгрузки 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("prepayment_for_shipment"));
			НоваяСтрока.КредитПослеОтгрузки 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("credit_after_shipment"));
			НоваяСтрока.КОплатеПросрочено 		= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_pay_overdue"));
			НоваяСтрока.КОтгрузкеПросрочено 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("for_shipment_overdue"));
			НоваяСтрока.КВозвратуДС 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_return"));
			НоваяСтрока.КВозвратуДСПросрочено 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_return_overdue"));
			НоваяСтрока.СальдоДолга 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("balance_of_the_debt"));
			НоваяСтрока.Отгружается 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("ships"));
			НоваяСтрока.Оплачивается 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("payable"));
		КонецЕсли;
		
	КонецЦикла; 
		
	
	
	Возврат тбзДанныеПоРасчетам;
		
КонецФункции

// Возвращает таблицу с данными по взаиморасчетамми с поставщиками.
//
Функция СформироватьТаблицуВзаиморасчетовСПоставщиками(СтрокаXML) Экспорт
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипЧисло	= Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,2));
	тбзДанныеПоРасчетам = Новый ТаблицаЗначений;
	тбзДанныеПоРасчетам.Колонки.Добавить("Валюта", 					ТипСтрока);
	тбзДанныеПоРасчетам.Колонки.Добавить("ДолгПоставщика", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("НашДолг", 				ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("СальдоДолга", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("Оплачивается", 			ТипЧисло);
	
	тбзДанныеПоРасчетам.Колонки.Добавить("КОплате", 				ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КОплатеПросрочено",		ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("АвансДоПодтверждения", 		ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("ПредоплатаДоПоступления",	ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КредитПослеПоступления",		ТипЧисло);
	
	тбзДанныеПоРасчетам.Колонки.Добавить("КПоступлению", 				ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КПоступлениюПросрочено",		ТипЧисло);
	
	тбзДанныеПоРасчетам.Колонки.Добавить("КВозвратуДС", 			ТипЧисло);
	тбзДанныеПоРасчетам.Колонки.Добавить("КВозвратуДСПросрочено", 	ТипЧисло);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	
	// Чтение строк
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента 
			И ЧтениеXML.Имя = "row" Тогда
			
			НоваяСтрока = тбзДанныеПоРасчетам.Добавить();
			НоваяСтрока.Валюта 					= ЧтениеXML.ПолучитьАтрибут("currency");
			НоваяСтрока.ДолгПоставщика 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("debt"));
			НоваяСтрока.НашДолг 				= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("our_debt"));
			НоваяСтрока.СальдоДолга 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("balance_of_the_debt"));
			НоваяСтрока.Оплачивается 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("payable"));
			
			НоваяСтрока.КОплате 				= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_pay"));
			НоваяСтрока.КОплатеПросрочено 		= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_pay_overdue"));
			НоваяСтрока.АвансДоПодтверждения	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("prepayment_confirmation"));
			НоваяСтрока.ПредоплатаДоПоступления = ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("prepayment_load"));
			НоваяСтрока.КредитПослеПоступления 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("credit_after_load"));
						
			НоваяСтрока.КПоступлению 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_load"));
			НоваяСтрока.КПоступлениюПросрочено 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_load_overdue"));
			
			НоваяСтрока.КВозвратуДС 			= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_return"));
			НоваяСтрока.КВозвратуДСПросрочено 	= ТипЧисло.ПривестиЗначение(ЧтениеXML.ПолучитьАтрибут("to_return_overdue"));
			
		КонецЕсли;
		
	КонецЦикла; 
	
	
	Возврат тбзДанныеПоРасчетам;
		
КонецФункции

Функция ПолучитьИдентификаторОбменаПоОбъекту(ОбъектОбмена)
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упИдентификаторыОбмена.Идентификатор
	|ИЗ
	|	РегистрСведений.упИдентификаторыОбмена КАК упИдентификаторыОбмена
	|ГДЕ
	|	упИдентификаторыОбмена.Объект = &Объект";
	Запрос.УстановитьПараметр("Объект", ОбъектОбмена);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Идентификатор;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти


