
#Область ПрограммныйИнтерфейс

// Процедура - Установить параметры адаптера
//
// Параметры:
//  ОбъектСообщения	 - ОбъектXDTO - объект сообщения.
//
Процедура УстановитьПараметрыАдаптера(Параметры) Экспорт
	ЗначенияКонстантИзменены = Ложь;
	Для каждого текПараметр из Параметры цикл
		Если текПараметр.Ключ = "AdapterType" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпТипИспользуемогоКоннектораESB", Перечисления.сшпТипыКоннекторовESB[текПараметр.Значение]);
		ИначеЕсли текПараметр.Ключ = "Uri" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпАдресАдаптераESB", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "ConfigurationName" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпИмяСервисаESB", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "EndpointName" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпИмяТочкиПодключенияESB", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "MessageFormat" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпФорматСообщений", Перечисления.сшпФорматыСообщений[текПараметр.Значение]);
		ИначеЕсли текПараметр.Ключ = "ServerMode" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпРежимПередачиСообщений", Перечисления.сшпРежимыПередачиСообщений[текПараметр.Значение]);
		ИначеЕсли текПараметр.Ключ = "MaxBatchSize" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпРазмерПакета", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "AutoStartProcessing" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпАвтоматическийСтартОбработчиков", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "WaitingTime" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпДлительностьОжидания", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "MaxFlowIn" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпМаксимальноеКоличествоПотоковОбработкиВходящих", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "MaxFlowOut" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпМаксимальноеКоличествоПотоковОбработкиИсходящих", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "ShutDown" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпОтключитьПотокиОбработкиДанных", текПараметр.Значение);
		ИначеЕсли текПараметр.Ключ = "SerializationFormat" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпФорматДляСериализации", Перечисления.сшпФорматыСообщений[текПараметр.Значение]);
		ИначеЕсли текПараметр.Ключ = "LiveTime" тогда
			ТекущаяТаблица = сшпРаботаСКонстантами.ПолучитьДлительностьХранения();
			ИмяКонстанты = "сшпДлительностьХраненияСообщенийПоОчередямИСостояниям";
			ТаблицаДлительностиОжидания = сшпРаботаСКонстантами.СформироватьЗначениеКонстантыДлительностьХранения();
			
			КоличествоСтрокВТаблице = СтрЧислоСтрок(текПараметр.Значение)-1;
			КоличествоСтрокВТекущейТаблице = ТекущаяТаблица.Количество();
			
			Если КоличествоСтрокВТаблице = 0 Тогда 
				Если Не КоличествоСтрокВТекущейТаблице = 0 Тогда 
					Константы[ИмяКонстанты].Установить(Новый ХранилищеЗначения(ТаблицаДлительностиОжидания));
					ЗначенияКонстантИзменены = Истина;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			ПроверятьНаличиеИзменений = КоличествоСтрокВТаблице = КоличествоСтрокВТекущейТаблице;
			СтрокаТекущейТаблицы = Неопределено;
			СтруктураКолонок = Новый Структура;
			ИзмененияНайдены = Не ПроверятьНаличиеИзменений;
			Для Счетчик = 1 По КоличествоСтрокВТаблице+1 Цикл
				ТекСтрока = СтрПолучитьСтроку(текПараметр.Значение, Счетчик);
				МассивПараметров = СтрРазделить(ТекСтрока, ";");
				
				Если Счетчик = 1 Тогда 
					Отказ = Ложь;
					Для Каждого ИмяКолонки Из ТаблицаДлительностиОжидания.Колонки Цикл 
						ИндексКолонки = МассивПараметров.Найти(ИмяКолонки.Имя);
						Если ИндексКолонки = Неопределено Тогда 
							ЗаписьЖурналаРегистрации("Datareon. Обработка системного сообщения", УровеньЖурналаРегистрации.Ошибка, Метаданные.ОбщиеМодули.сшпРаботаСДанными,, "Сообщение CSA содержит не корректную структуру параметра LiveTime: в параметре не определено поле " + ИмяКолонки.Имя);
							Счетчик = СтрЧислоСтрок(текПараметр.Значение);
						Иначе
							СтруктураКолонок.Вставить(ИмяКолонки.Имя, ИндексКолонки);
						КонецЕсли;
					КонецЦикла;
				Иначе
					НоваяСтрокаТЗ = ТаблицаДлительностиОжидания.Добавить();
					Если ПроверятьНаличиеИзменений Тогда 
						СтрокаТекущейТаблицы = ТекущаяТаблица[Счетчик-2];
					КонецЕсли;
					Для Каждого ЭлементСтруктуры Из СтруктураКолонок Цикл 
						Если ЭлементСтруктуры.Ключ = "ТипОчереди" Тогда
							НоваяСтрокаТЗ[ЭлементСтруктуры.Ключ] = Перечисления.сшпТипыОчередей[МассивПараметров[ЭлементСтруктуры.Значение]];
						ИначеЕсли ЭлементСтруктуры.Ключ = "СтатусСообщения" Тогда 
							НоваяСтрокаТЗ[ЭлементСтруктуры.Ключ] = Перечисления.сшпСтатусыСообщений[МассивПараметров[ЭлементСтруктуры.Значение]];
						Иначе
							НоваяСтрокаТЗ[ЭлементСтруктуры.Ключ] = МассивПараметров[ЭлементСтруктуры.Значение];
						КонецЕсли;
						
						Если ПроверятьНаличиеИзменений И СтрокаТекущейТаблицы[ЭлементСтруктуры.Ключ] <> НоваяСтрокаТЗ[ЭлементСтруктуры.Ключ] Тогда 
							ИзмененияНайдены = Истина;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			ЗначениеКонстанты = Новый ХранилищеЗначения(ТаблицаДлительностиОжидания);
			Если ИзмененияНайдены тогда
				Константы[ИмяКонстанты].Установить(ЗначениеКонстанты);
				РезультатУстановки = Истина;
			Иначе
				РезультатУстановки = Ложь;
			КонецЕсли; 
		ИначеЕсли текПараметр.Ключ = "LiveTimeDefault" тогда
			РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпДлительностьХраненияСообщений", текПараметр.Значение);
		Иначе
			РезультатУстановки = Ложь;
		КонецЕсли;	
		ЗначенияКонстантИзменены = ЗначенияКонстантИзменены Или РезультатУстановки;
	КонецЦикла;
	
	РезультатУстановки = сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпИспользоватьСШП", Истина);
	ЗначенияКонстантИзменены = ЗначенияКонстантИзменены Или РезультатУстановки;
	
	Если ЗначенияКонстантИзменены Тогда 
		сшпОтключитьПотокиОбработкиДанных = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпОтключитьПотокиОбработкиДанных");

		// Выполним перезапуск потока управления для получения новых значений констант.
		Если Не сшпОтключитьПотокиОбработкиДанных Тогда 
			сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпОтключитьПотокиОбработкиДанных", Истина);
		КонецЕсли;
		Пока Истина = Истина Цикл
			мсвКлючи = Новый Массив;
			мсвКлючи.Добавить("ОбработкаОчередиИсходящихСообщений");
			мсвКлючи.Добавить("ОбработкаОчередиВходящихСообщений");
			мсвКлючи.Добавить("ОбработкаОчередиСистемныхСообщений");
			мсвКлючи.Добавить("ОбработкаОчередиОтправляемых");
			количествоПотоков = сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Ключ", мсвКлючи);
			Если количествоПотоков = 0 тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
		Если Не сшпОтключитьПотокиОбработкиДанных Тогда 
			ИмяКонстанты = "сшпОтключитьПотокиОбработкиДанных";
			Константы[ИмяКонстанты].Установить(Ложь);
			//сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпОтключитьПотокиОбработкиДанных", Ложь);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Процедура - Установить состояние сообщения
//
// Параметры:
//  Идентификатор	 - УникальныйИдентификатор - индентификатор сообщения.
//  НовоеСостояние	 - Перечисление.сшпСтатусыСообщений - новое состояние сообщения. 
//
Процедура УстановитьСостояниеСообщения(Идентификатор, НовоеСостояние, задержка = 0, ОписаниеОшибки = "") Экспорт
	идентификаторСообщения = ?(ТипЗнч(Идентификатор) = Тип("Строка"), Новый УникальныйИдентификатор(идентификатор), идентификатор);
	набор = РегистрыСведений.сшпСостояниеСообщений.СоздатьНаборЗаписей();
	набор.Отбор.ИдентификаторСообщения.Установить(идентификаторСообщения, Истина);
	набор.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
	
	текЗапись = набор.Добавить();
	текЗапись.ДатаИзменения = ТекущаяДатаСеанса();
	текЗапись.ИдентификаторСообщения = идентификаторСообщения;
	текЗапись.СтатусСообщения = НовоеСостояние;
	текЗапись.Задержка = задержка;
	текЗапись.ЗадержкаЧисло = сшпОбщегоНазначения.ПеревестиДатуВЧисло(текЗапись.ДатаИзменения, текЗапись.Задержка);
	текЗапись.ОписаниеОшибки = ОписаниеОшибки;
	
	набор.Записать(Истина);
КонецПроцедуры

// Процедура - Удалить сообщение
//
// Параметры:
//  ТипИнтеграции	 - Перечисление.сшпТипыИнтеграции - тип интеграции, указывает очередь из которой необходимо удалить сообщение. 
//  Идентификатор	 - УникальныйИдентификатор - индентификатор сообщения.
//
Процедура УдалитьСообщение(типочереди, Идентификатор) Экспорт
	Если типочереди = Перечисления.сшпТипыОчередей.Системная тогда
		текЗапись = РегистрыСведений.сшпОчередьСистемныхСообщений.СоздатьМенеджерЗаписи();
	ИначеЕсли типочереди = Перечисления.сшпТипыОчередей.Входящая тогда
			текЗапись = РегистрыСведений.сшпОчередьВходящихСообщений.СоздатьМенеджерЗаписи();
	ИначеЕсли типочереди = Перечисления.сшпТипыОчередей.Исходящая тогда
			текЗапись = РегистрыСведений.сшпОчередьИсходящихСообщений.СоздатьМенеджерЗаписи();
	ИначеЕсли типочереди = Перечисления.сшпТипыОчередей.Отправки тогда
			текЗапись = РегистрыСведений.сшпОчередьОтправляемыхСообщений.СоздатьМенеджерЗаписи();
	ИначеЕсли типочереди = Перечисления.сшпТипыОчередей.ОтправкиСистемныхСообщений тогда
			текЗапись = РегистрыСведений.сшпОчередьОтправляемыхСистемныхСообщений.СоздатьМенеджерЗаписи();
	КонецЕсли;
	текЗапись.ИдентификаторСообщения = Идентификатор;
	текЗапись.Удалить();
	
	текЗапись = РегистрыСведений.сшпСостояниеСообщений.СоздатьМенеджерЗаписи();
	текЗапись.ИдентификаторСообщения = Идентификатор;
	текЗапись.Удалить();
КонецПроцедуры	

// Процедура - Поместить в очередь исходящих
//
// Параметры:
//  типобъекта	 	- Строка - Имя объекта по которому произошло событие. 
//  ОбъектСобщения	- Строка, ДвоичныеДанные - сериализованное представление объекта на момент события. 
//  Формат			- Перечисление.сшпФорматыСообщений - формат в котором выполняется сериализация сообщения. 
//  Метод			- Перечисление.сшпМетодХранения - метод сохранения объекта события. 
//
Процедура ПоместитьВОчередьИсходящих(типобъекта, ОбъектСобщения, формат = Неопределено, метод = Неопределено, ПомещатьВХранилищеЗначения = Истина, ЭтоУдаление = Ложь, СсылкаНаОбъект = Неопределено) Экспорт
	идентификаторСообщения = Новый УникальныйИдентификатор();
	УстановитьСостояниеСообщения(идентификаторСообщения, Перечисления.сшпСтатусыСообщений.Новое);
	
	набор = РегистрыСведений.сшпОчередьИсходящихСообщений.СоздатьНаборЗаписей();
	набор.Отбор.ИдентификаторСообщения.Установить(идентификаторСообщения, Истина);
	набор.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
	
	текЗапись = набор.Добавить();
	текЗапись.ИдентификаторСообщения = идентификаторСообщения;
	Если ПомещатьВХранилищеЗначения Тогда 
		текЗапись.Хранилище = Новый ХранилищеЗначения(ОбъектСобщения);
	Иначе
		текЗапись.Хранилище = ОбъектСобщения;
	КонецЕсли;
	текЗапись.МетодХранения = ?(метод = Неопределено, Перечисления.сшпМетодХранения.Сериализация, метод);
	текЗапись.ДатаРегистрации = ТекущаяДатаСеанса();
	текЗапись.ОбъектСобытия = типобъекта;
	текЗапись.ФорматСообщения = ?(формат = Неопределено, сшпФункциональныеОпции.ФорматСериализации(), формат);
	текЗапись.ЭтоУдаление = ЭтоУдаление;
	текЗапись.СсылкаНаОбъект = СсылкаНаОбъект;
	
	Если ЭтоУдаление Тогда 
		набор.ДополнительныеСвойства.Вставить("ЭтоУдаление");
	КонецЕсли;
	
 	набор.Записать(Истина);
	
КонецПроцедуры

// Процедура - Поместить в очередь входящих
//
// Параметры:
//  Формат		- Перечисление.сшпФорматыСообщений - формат поступившего сообщения. 
//  xdtoПакет	- ОбъектXDTO - объект типа message1C. 
//
Процедура ПоместитьВОчередьВходящих(формат, xdtoПакет) Экспорт
	идентификаторСообщения = ?(ТипЗнч(xdtoПакет.Id) = Тип("Строка"), Новый УникальныйИдентификатор(xdtoПакет.Id), xdtoПакет.Id);
	УстановитьСостояниеСообщения(идентификаторСообщения, Перечисления.сшпСтатусыСообщений.Новое);
	
	набор = РегистрыСведений.сшпОчередьВходящихСообщений.СоздатьНаборЗаписей();
	набор.Отбор.ИдентификаторСообщения.Установить(идентификаторСообщения, Истина);
	набор.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
	
	текЗапись = набор.Добавить();
	текЗапись.ИдентификаторСообщения = идентификаторСообщения;
	текЗапись.ДатаРегистрации = ТекущаяДатаСеанса();
	текЗапись.ФорматСообщения = Формат;
	текЗапись.КлассСообщения = xdtoПакет.ClassId;
	текЗапись.ДатаСоздания = МестноеВремя(xdtoПакет.CreationTime, ЧасовойПояс());
	xmlПакет = сшпОбщегоНазначения.ЗаписатьОбъектВПоток(Формат, xdtoПакет);
	текЗапись.Хранилище = Новый ХранилищеЗначения(xmlПакет);
	
	набор.Записать(Истина);
	
КонецПроцедуры

// Процедура - Поместить в системную очередь
//
// Параметры:
//  Идентификатор	 - Строка - индентификатор сообщения. 
//  Формат			 - Перечисление.сшпФорматыСообщений - формат поступившего сообщения. 
//  Класс			 - Строка - Идентификатор класса поступившего сообщения. 
//  ОбъектСообщения	 - Строка, ДвоичныеДанные - Тело сообщения в указанном формате.
//
Процедура ПоместитьВСистемнуюОчередь(Идентификатор, Формат = Неопределено, Класс, ОбъектСообщения = Неопределено) Экспорт
	идентификаторСообщения = ?(ЗначениеЗаполнено(Идентификатор), Новый УникальныйИдентификатор(Идентификатор),  Новый УникальныйИдентификатор);
	УстановитьСостояниеСообщения(идентификаторСообщения, Перечисления.сшпСтатусыСообщений.Новое);
	
	набор = РегистрыСведений.сшпОчередьСистемныхСообщений.СоздатьНаборЗаписей();
	набор.Отбор.ИдентификаторСообщения.Установить(идентификаторСообщения, Истина);
	набор.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
	
	текЗапись = набор.Добавить();
	текЗапись.ИдентификаторСообщения = идентификаторСообщения;
	текЗапись.Хранилище = Новый ХранилищеЗначения(?(ОбъектСообщения = Неопределено, "Значение не установлено", ОбъектСообщения));
	текЗапись.ДатаРегистрации = ТекущаяДатаСеанса();
	текЗапись.ФорматСообщения = ?(Формат = Неопределено, сшпФункциональныеОпции.ФорматСообщения(), Формат);
	текЗапись.КлассСообщения = Класс;
	
	набор.Записать(Истина);
	
	Если НЕ Класс = "SSM" тогда
		Попытка
			ФоновыеЗадания.Выполнить("сшпОбслуживаниеОчередей.ОбработкаОчередиСистемныхСообщений",, "ОбработкаОчередиСистемныхСообщений", "ОбработкаОчередиСистемныхСообщений");
		Исключение
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры	

// Процедура - Обработать запись очереди
//
// Параметры:
//  ключ		 - КлючЗаписиРегистраСведений - Ключ записи для которой выполняется изменение статуса 
//  новыйстатус	 - Строка - Название нового статуса сообщения.
//
Процедура ОбработатьЗаписьОчереди(ключ, новыйстатус) Экспорт
	УстановитьСостояниеСообщения(ключ.ИдентификаторСообщения, Перечисления.сшпСтатусыСообщений[новыйстатус]);
КонецПроцедуры	

// Процедура - Установить статус обработчика
//
// Параметры:
//  идентификатор	- УникальныйИдентификатор - индентификатор обработчика.
//  статус	 		- Перечисление.сшпСтатусыОбработчиков - новый статус обработчика. 
//
Процедура УстановитьСтатусОбработчика(идентификатор, статус) Экспорт
	идентификаторОбработчика = ?(ТипЗнч(Идентификатор) = Тип("Строка"), Новый УникальныйИдентификатор(идентификатор), идентификатор);
	
	набор = РегистрыСведений.сшпСтатусыОбработчиков.СоздатьНаборЗаписей();
	набор.Отбор.ИдентификаторОбработчика.Установить(идентификаторОбработчика, Истина);
	набор.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
	
	текЗапись = набор.Добавить();
	текЗапись.ДатаИзменения = ТекущаяДатаСеанса();
	текЗапись.ИдентификаторОбработчика = идентификаторОбработчика;
	текЗапись.Статус = статус;
	
	набор.Записать(Истина);
	
	сшпРаботаСКонстантами.УстановитьЗначениеКонстанты("сшпВерсияОбработчиков", Строка(Новый УникальныйИдентификатор));
КонецПроцедуры

#КонецОбласти
