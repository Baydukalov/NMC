#Область ПрограммныйИнтерфейс

#Область Пересчет

// Процедура пересчсета "Цены", "СуммаНДС", "СуммаБезНДС" переданной строки.
//
// Параметры:
//  Строка - Строка - Строка табличной части, строка товарного состава.
//
Процедура ТоварныйСоставГрузаСуммаПриИзменении(Строка) Экспорт
	
	Если Строка.Количество Тогда 
		Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") Тогда 
			Строка.СуммаНДС    = Строка.Сумма * 9 / 59;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			Строка.Цена        = Строка.СуммаБезНДС / Строка.Количество;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
			Строка.СуммаНДС    = Строка.Сумма * 9 / 59;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			Строка.Цена        = Строка.Сумма / Строка.Количество;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20") Тогда 
			Строка.СуммаНДС    = Строка.Сумма / 20;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			Строка.Цена        = Строка.СуммаБезНДС / Строка.Количество;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
			Строка.СуммаНДС    = Строка.Сумма / 20;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			Строка.Цена        = Строка.Сумма / Строка.Количество;	
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10") Тогда 
			Строка.СуммаНДС    = Строка.Сумма / 10;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			Строка.Цена        = Строка.СуммаБезНДС / Строка.Количество;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
			Строка.СуммаНДС    = Строка.Сумма / 10;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			Строка.Цена        = Строка.Сумма / Строка.Количество;
		Иначе
			Строка.СуммаНДС    = 0;
			Строка.СуммаБезНДС = Строка.Сумма;
			Строка.Цена        = Строка.Сумма / Строка.Количество;
		КонецЕсли;	
	Иначе
		Строка.Цена        = 0;
		Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18")
			ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
			Строка.СуммаНДС    = Строка.Сумма * 9 / 59;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20")
			ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
			Строка.СуммаНДС    = Строка.Сумма / 20;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10")
			ИЛИ Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
			Строка.СуммаНДС    = Строка.Сумма / 10;
			Строка.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
		Иначе
			Строка.СуммаНДС    = 0;
			Строка.СуммаБезНДС = Строка.Сумма;
		КонецЕсли;	
	КонецЕсли;	
		
КонецПроцедуры // ТоварныйСоставГрузаСуммаПриИзменении()

// Процедура пересчсета "Сумма", "СуммаНДС", "СуммаБезНДС" переданной строки.
//
// Параметры:
//  Строка - Строка - Строка табличной части, строка товарного состава.
//
Процедура ТоварныйСоставГрузаРассчитатьСуммы(Строка) Экспорт
	
	Сумма = Строка.Цена * Строка.Количество;
	Если Сумма Тогда 
		Если Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") Тогда 
			Строка.СуммаНДС    = Сумма * 0.18;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма + Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
			Строка.СуммаНДС    = Сумма * 9 / 59;
			Строка.СуммаБезНДС = Сумма / 1.18;
			Строка.Сумма       = Сумма;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20") Тогда 
			Строка.СуммаНДС    = Сумма / 20;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма + Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
			Строка.СуммаНДС    = Сумма / 12;
			Строка.СуммаБезНДС = Сумма / 1.2;
			Строка.Сумма       = Сумма;	
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10") Тогда 
			Строка.СуммаНДС    = Сумма / 10;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма + Строка.СуммаНДС;
		ИначеЕсли Строка.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
			Строка.СуммаНДС    = Сумма / 11;
			Строка.СуммаБезНДС = Сумма / 1.1;
			Строка.Сумма       = Сумма;
		Иначе
			Строка.СуммаНДС    = 0;
			Строка.СуммаБезНДС = Сумма;
			Строка.Сумма       = Сумма;
		КонецЕсли;	
	Иначе
		Строка.СуммаНДС    = 0;
		Строка.СуммаБезНДС = 0;
		Строка.Сумма       = 0;
	КонецЕсли;	
		
КонецПроцедуры // ТоварныйСоставГрузаРассчитатьСуммы()

// Процедура измененмя задания на перевозку в рейсе.
//
// Параметры:
//  ТекущаяСтрока - Строка - Строка табличной части, строка товарного состава.
//  НоваяСтрока - Булево - флаг новой строки.
//  Удалить - Булево - флаг выполнить удаление.
//  Отказ - Булево - Флаг фиксирования данных.
//
Процедура РейсЗаданиеНаПеревозкуИзменить(ТекущаяСтрока, НоваяСтрока, Удалить, Отказ) Экспорт
	
	Если НоваяСтрока Тогда
		
		// Проверить есть ли уже строка с текущими параметрами.
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура пересчета объема при изменение размера груза.
//
// Параметры:
//  ТекущаяСтрока - Строка - Строка табличной части, строка товарного состава.
//  КоэффициентПересчетаОбъема - Число - Коэффициент.
//
Процедура ГрузРазмерПриИзменении(ТекущаяСтрока, КоэффициентПересчетаОбъема) Экспорт

	ТекущаяСтрока.Объем = ТекущаяСтрока.Ширина * ТекущаяСтрока.Высота * ТекущаяСтрока.Длина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры // ГрузРазмерПриИзменении()

// Пересчет ВГХ грузка.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма объекта.
//  ИдентификаторЭлементаКоллекцииГруза - Строка - Идентификатор.
//  ПараметрВГХ - Структура - Параметры.
//  ЗаполнитьКлассОпасности - Булево - Выполнить заполнение.
//  ЗаполнитьТемпературныйРежим - Булево - Выполнить заполнение.
//
Процедура ПересчитатьХарактеристикиГруза(Форма, ИдентификаторЭлементаКоллекцииГруза, ПараметрВГХ = Неопределено, ЗаполнитьКлассОпасности = Ложь, ЗаполнитьТемпературныйРежим = Ложь) Экспорт

	текОбъект = Форма.Объект;
	
	мсвТоварныйСостав = новый Массив;
	Если текОбъект.ОбщийГруз Тогда
		текЭлемент	= Форма;
		Для каждого текСтрока Из Форма.ТоварныйСостав Цикл
			сткСтрока = Новый Структура("Номенклатура, УпаковкаНоменклатуры, Количество");
			ЗаполнитьЗначенияСвойств(сткСтрока, текСтрока);
			мсвТоварныйСостав.Добавить(сткСтрока);
		КонецЦикла;
	Иначе	
		текЭлемент  = Форма.ГрузовыеМеста.НайтиПоИдентификатору(ИдентификаторЭлементаКоллекцииГруза);
		Для каждого текСтрока Из Форма.ТоварныйСостав Цикл
			Если текСтрока.Идентификатор = текЭлемент.Идентификатор Тогда
				сткСтрока = Новый Структура("Номенклатура, УпаковкаНоменклатуры, Количество");
				ЗаполнитьЗначенияСвойств(сткСтрока, текСтрока);
				мсвТоварныйСостав.Добавить(сткСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	сткХарактеристикиГруза = упЗаданиеНаПеревозкуВызовСервера.ПолучитьХарактеристикиПоТоварам(мсвТоварныйСостав, ЗаполнитьКлассОпасности, ЗаполнитьТемпературныйРежим);
	
	// Масса.
	Если ЗначениеЗаполнено(сткХарактеристикиГруза.Масса) И (сткХарактеристикиГруза.Масса > текЭлемент.Масса ИЛИ ПараметрВГХ = "Масса") Тогда 
		текЭлемент.Масса = сткХарактеристикиГруза.Масса;
	ИначеЕсли ПараметрВГХ = "Масса" Тогда 
		текЭлемент.Масса = 0;
	КонецЕсли;
	
	// Объем.
	Если ЗначениеЗаполнено(сткХарактеристикиГруза.Объем) И (сткХарактеристикиГруза.Объем > текЭлемент.Объем ИЛИ ПараметрВГХ = "Объем") Тогда 
		текЭлемент.Объем = сткХарактеристикиГруза.Объем;
	ИначеЕсли ПараметрВГХ = "Объем" Тогда 
		текЭлемент.Объем = 0;
	КонецЕсли;
	
	// Класс опасности.
	Если ЗаполнитьКлассОпасности И НЕ сткХарактеристикиГруза.КлассОпасности = Неопределено Тогда
		текЭлемент.КлассОпасности	= сткХарактеристикиГруза.КлассОпасности;
	КонецЕсли;
	
	// Температурный режим.
	Если ЗаполнитьТемпературныйРежим И НЕ сткХарактеристикиГруза.ТемпературныйРежим = Неопределено Тогда
		текЭлемент.ТемпературныйРежим	= сткХарактеристикиГруза.ТемпературныйРежим;
	КонецЕсли;

КонецПроцедуры // ПересчитатьХарактеристикиГруза()

// Пересчет итоговой суммы товарного состава.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма объекта.
//  ИдентификаторЭлементаКоллекцииГруза - Строка - Идентификатор.
//
Процедура ПересчитатьИтоговыеСуммы(Форма, ИдентификаторЭлементаКоллекцииГруза) Экспорт
	
	текОбъект = Форма.Объект;
	
	мсвТоварныйСостав = новый Массив;
	сткСтрока = Новый Структура("Номенклатура, УпаковкаНоменклатуры, Количество");
	Если текОбъект.ОбщийГруз Тогда
		текЭлемент	= Форма;
		текЭлемент.Сумма 	= 0;
		текЭлемент.СуммаНДС = 0;
		Для каждого текСтрока Из Форма.ТоварныйСостав Цикл
			текЭлемент.Сумма 	=  текЭлемент.Сумма + текСтрока.Сумма;
			текЭлемент.СуммаНДС =  текЭлемент.СуммаНДС + текСтрока.СуммаНДС;
		КонецЦикла;
	Иначе	
		текЭлемент  = Форма.ГрузовыеМеста.НайтиПоИдентификатору(ИдентификаторЭлементаКоллекцииГруза);
		текЭлемент.Сумма 	= 0;
		текЭлемент.СуммаНДС = 0;
		Для каждого текСтрока Из Форма.ТоварныйСостав Цикл
			Если текСтрока.Идентификатор = текЭлемент.Идентификатор Тогда
				текЭлемент.Сумма 	=  текЭлемент.Сумма + текСтрока.Сумма;
				текЭлемент.СуммаНДС =  текЭлемент.СуммаНДС + текСтрока.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

// Проверка корректноти заполнения свойств грузового места.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма объекта.
//  Отказ - Булево - Фиксировать изменения.
//  ПроверятьАдреса - Булево - Выполнить проверку.
//  РазличныеАдресаПолучения - Булево - Это различные адреса получения.
//  РазличныеАдресаОтправления - Булево - Это различные адреса отправления.
//
Процедура ПроверитьГрузовыеМеста(Форма, Отказ, ПроверятьАдреса = Ложь, РазличныеАдресаПолучения = Ложь, РазличныеАдресаОтправления = Ложь) Экспорт

	текОбъект 			= Форма.Объект;
	текГрузовыеМеста 	= Форма.ГрузовыеМеста;
	текТоварныйСостав	= Форма.ТоварныйСостав;
	
	Если текОбъект.ОбщийГруз Тогда 
		
		Если ПроверятьАдреса Тогда 
			Если Не ЗначениеЗаполнено(текОбъект.АдресОтправления) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан адрес отправления'"), текОбъект.Ссылка, "АдресОтправления",, Отказ);
			КонецЕсли;	
			
			Если Не ЗначениеЗаполнено(текОбъект.АдресПолучения) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан адрес получения'"), текОбъект.Ссылка, "АдресПолучения",, Отказ);
			КонецЕсли;	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Форма.ТипГруза) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан тип груза'"), текОбъект.Ссылка, "ТипГруза",, Отказ);
		КонецЕсли;	
		
		Для Каждого текСтрокаСостава Из текТоварныйСостав Цикл 
			Если Не ЗначениеЗаполнено(текСтрокаСостава.Номенклатура) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана номенклатура'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав", текТоварныйСостав.Индекс(текСтрокаСостава) + 1, "Номенклатура"),, Отказ);
			КонецЕсли;	
			Если Не ЗначениеЗаполнено(текСтрокаСостава.Количество) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указано количество'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав", текТоварныйСостав.Индекс(текСтрокаСостава) + 1, "Количество"),, Отказ);
			КонецЕсли;	
		КонецЦикла;	
	Иначе
		
		Если ПроверятьАдреса Тогда 
			Если Не РазличныеАдресаОтправления Тогда 
				Если Не ЗначениеЗаполнено(текОбъект.АдресОтправления) Тогда 
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан адрес отправления'"), текОбъект.Ссылка, "АдресОтправления",, Отказ);
				КонецЕсли;	
			КонецЕсли;	
			
			Если Не РазличныеАдресаПолучения Тогда 
				Если Не ЗначениеЗаполнено(текОбъект.АдресПолучения) Тогда 
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан адрес получения'"), текОбъект.Ссылка, "АдресПолучения",, Отказ);
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	

		
		Если текГрузовыеМеста.Количество() = 0 Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан список грузов'"), текОбъект.Ссылка,"ГрузовыеМеста",, Отказ);
		КонецЕсли;	
		
		Для Каждого текГруз Из текГрузовыеМеста Цикл 
			
			Если ПроверятьАдреса Тогда 
				Если РазличныеАдресаОтправления И Не ЗначениеЗаполнено(текОбъект.АдресОтправления) Тогда 
					Если Не ЗначениеЗаполнено(текГруз.АдресОтправления) Тогда 
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан адрес отправления грузового места'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СписокГрузов", текГрузовыеМеста.Индекс(текГруз) + 1, "АдресОтправления"),, Отказ);
					КонецЕсли;	
				КонецЕсли;	
				
				Если РазличныеАдресаПолучения И Не ЗначениеЗаполнено(текОбъект.АдресПолучения) Тогда 
					Если Не ЗначениеЗаполнено(текГруз.АдресПолучения) Тогда 
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан адрес получения грузового места'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СписокГрузов", текГрузовыеМеста.Индекс(текГруз) + 1, "АдресПолучения"),, Отказ);
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;	
			
			Если Не ЗначениеЗаполнено(текГруз.ТипГруза) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан тип груза'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ГрузовыеМеста", текГрузовыеМеста.Индекс(текГруз) + 1, "ТипГруза"),, Отказ);
			КонецЕсли;
			
			текИдентификатор = текГруз.Идентификатор;
			
			мсвСтрокиСостава	= текТоварныйСостав.НайтиСтроки(Новый Структура("Идентификатор", текИдентификатор));
			
			Для каждого текСтрокаСостава Из  мсвСтрокиСостава Цикл
				Если Не ЗначениеЗаполнено(текСтрокаСостава.Номенклатура) Тогда 
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана номенклатура'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав", текТоварныйСостав.Индекс(текСтрокаСостава) + 1, "Номенклатура"),, Отказ);
				КонецЕсли;	
				Если Не ЗначениеЗаполнено(текСтрокаСостава.Количество) Тогда 
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указано количество'"), текОбъект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав", текТоварныйСостав.Индекс(текСтрокаСостава) + 1, "Количество"),, Отказ);
				КонецЕсли;	
			КонецЦикла;
			
			Если Отказ Тогда
				Форма.Элементы.ГрузовыеМеста.ТекущаяСтрока = текГруз.ПолучитьИдентификатор();
				Форма.Элементы.ТоварныйСостав.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", текИдентификатор);
				Прервать;				
			КонецЕсли;
			
		КонецЦикла;	
	КонецЕсли;	
КонецПроцедуры

// Процедура поиска дублей таблицы с ГСМ.
//
// Параметры:
//  ТекущаяСтрока	 - Строка	 - Строка табличной части, строка данных.
//  Форма			 - УправляемаяФорма	 - Форма объекта.
//  ИмяТЧ			 - Строка			 - имя табличной части.
//  Отказ			 - Булево			 - Фиксировать изменения.
//
Процедура ЕстьДублиСтрокВТабличнойЧасти(ТекущаяСтрока, Форма, ИмяТЧ, Отказ) Экспорт
	
	Если Форма.ИмяФормы = "Документ.упПутевойЛист.Форма.ФормаДокумента" Тогда
		Если ИмяТЧ = "ГСМ" Тогда
			сткОтбора = Новый Структура("ВидГСМ, ДопОборудование",ТекущаяСтрока.ВидГСМ, ТекущаяСтрока.ДопОборудование);
			ТекстСообщения = Нстр("ru = 'По виду гсм ""%1"" и ""%2"" уже есть строка'");
			ТранспортноеСредство = ?(ЗначениеЗаполнено(ТекущаяСтрока.ДопОборудование), ТекущаяСтрока.ДопОборудование, Форма.Объект.ТранспортноеСредство);
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущаяСтрока.ВидГСМ, ТранспортноеСредство);
		ИначеЕсли ИмяТЧ = "ДополнительноеОборудование" Тогда	
			сткОтбора = Новый Структура("ДополнительноеОборудование", ТекущаяСтрока.ДополнительноеОборудование);
			ТекстСообщения = Нстр("ru = 'В табличной части уже есть строка с доп. оборудованием ""%1""'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущаяСтрока.ДополнительноеОборудование);
		ИначеЕсли ИмяТЧ = "ГСМДополнительноеОборудование" Тогда	
			сткОтбора = Новый Структура("ДополнительноеОборудование, ВидГСМ", ТекущаяСтрока.ДополнительноеОборудование, ТекущаяСтрока.ВидГСМ);
			ТекстСообщения = Нстр("ru = 'В табличной части уже есть строка с доп. оборудованием ""%1"" и видом гсм ""%2""'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущаяСтрока.ДополнительноеОборудование, ТекущаяСтрока.ВидГСМ);	
		КонецЕсли;
	КонецЕсли;
	
	ДанныеФормыКоллекция = Форма.Объект[ИмяТЧ];	
	мсвСтроки = ДанныеФормыКоллекция.НайтиСтроки(сткОтбора);
	Если мсвСтроки.Количество() > 1 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти 