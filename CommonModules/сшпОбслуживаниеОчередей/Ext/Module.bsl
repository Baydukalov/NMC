
#Область ПроцедурыИФункцииМодуля

// Процедура - Управление пулом обработчиков
//
Процедура УправлениеПуломОбработчиков() Экспорт
	сшпОбновлениеВерсииКонфигурации.ВыполнитьОбновлениеДанныхКонфигурации();
	Если ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") И НЕ ПолучитьФункциональнуюОпцию("сшпОтключитьПотоки") тогда
		Если НЕ сшпОбщегоНазначения.ПроверитьЭкземплярИнформационнойБазы() тогда
			ЗаписьЖурналаРегистрации("Datareon. Управление пулом обработчиков", УровеньЖурналаРегистрации.Ошибка,,, "Рассинхронизация идентификаторов информационной базы");
			Возврат;
		КонецЕсли;
		
		// Выполним предварительную очистку от накопившегося мусора
		ОчисткаОчередейСообщений();
		
		сшпОбщегоНазначения.ЗапуститьОбработчикОчереди("ОбработкаОчередиСистемныхСообщений");
		сшпОбщегоНазначения.ЗапуститьОбработчикОчереди("ОбработкаОчередиИсходящихСообщений");
		сшпОбщегоНазначения.ЗапуститьОбработчикОчереди("ОбработкаОчередиВходящихСообщений");
		сшпОбщегоНазначения.ЗапуститьОбработчикОчереди("ОбработкаОчередиОтправляемых");
		сшпОбщегоНазначения.ЗапуститьОбработчикОчереди("ОбработкаОчередиОтправляемыхСистемныхСообщений");
	КонецЕсли;
КонецПроцедуры

// Процедура - Обработка очереди системных сообщений
//
Процедура ОбработкаОчередиСистемныхСообщений() Экспорт
	Если НЕ ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") тогда
		Возврат;
	КонецЕсли;
	
	текЗапрос = Новый Запрос("ВЫБРАТЬ
	|	тбОчередь.ИдентификаторСообщения,
	|	тбОчередь.Хранилище,
	|	тбОчередь.КлассСообщения,
	|	тбСостояние.ДатаИзменения,
	|	тбОчередь.ФорматСообщения
	|ПОМЕСТИТЬ втПакеты
	|ИЗ
	|	РегистрСведений.сшпОчередьСистемныхСообщений КАК тбОчередь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
	|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
	|			И (тбСостояние.СтатусСообщения В (&СписокСтатусов))
	|			И тбСостояние.ЗадержкаЧисло <= &ЗадержкаЧисло
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбОчередь.ИдентификаторСообщения,
	|	тбОчередь.Хранилище,
	|	тбОчередь.КлассСообщения,
	|	тбОчередь.ФорматСообщения
	|ИЗ
	|	втПакеты КАК тбОчередь
	|ГДЕ
	|	тбОчередь.КлассСообщения В(&СписокОпераций)
	|
	|УПОРЯДОЧИТЬ ПО
	|	тбОчередь.ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбОчередь.ИдентификаторСообщения,
	|	тбОчередь.Хранилище,
	|	тбОчередь.КлассСообщения,
	|	тбОчередь.ФорматСообщения
	|ИЗ
	|	втПакеты КАК тбОчередь
	|ГДЕ
	|	НЕ тбОчередь.КлассСообщения В (&СписокОпераций)
	|
	|УПОРЯДОЧИТЬ ПО
	|	тбОчередь.ДатаИзменения");
	массивФильтр = Новый Массив;
	массивФильтр.Добавить("TUM");
	массивФильтр.Добавить("TRM");
	массивФильтр.Добавить("TCS");
	текЗапрос.УстановитьПараметр("СписокОпераций", массивФильтр);
	текЗапрос.УстановитьПараметр("СписокСтатусов", сшпКэшируемыеФункции.ПоучитьСписокРабочихСтатусов());
	текЗапрос.УстановитьПараметр("ЗадержкаЧисло", сшпОбщегоНазначения.ПеревестиДатуВЧисло(ТекущаяДатаСеанса()));
	мсвРезультаты = текЗапрос.ВыполнитьПакет();
	Если мсвРезультаты[1].Пустой() И мсвРезультаты[2].Пустой() тогда // очередь системных сообщений пустая.
		Возврат;
	Иначе
		#Область Обработка_сообщений_на_обновление_списка_обработчиков
		Если НЕ мсвРезультаты[1].Пустой() тогда // Есть сообщения на обновление списка обработчиков. Остановим рабочие потоки.
			Попытка
				мсвПараметры = Новый Массив;
				мсвПараметры.Добавить(мсвРезультаты[1].Выгрузить());
				заданиеОбновления = ФоновыеЗадания.Выполнить("сшпОбслуживаниеОчередей.ВыполнитьИзменениеОбработчиков",мсвПараметры, "ВыполнитьИзменениеОбработчиков", "ВыполнитьИзменениеОбработчиков");
				заданиеОбновления.ОжидатьЗавершения(20);
			Исключение
				ОбработкаОчередиСистемныхСообщений();
				//Продолжить; // Либо такое задание уже есть, либо текущее выполняется долго. Попробуем повторить попытку. 
			КонецПопытки;
		КонецЕсли;
		#КонецОбласти
		
		#Область Обработка_системных_сообщения_без_обновления_списка_обработчиков
		Если НЕ мсвРезультаты[2].Пустой() тогда // Есть системные сообщения без обновление списка обработчиков.
			текИдентификатор = "Идентификатор не определен";
			текВыборка =  мсвРезультаты[2].Выбрать();
			Пока текВыборка.Следующий() цикл
				Попытка			
					текИдентификатор = текВыборка.ИдентификаторСообщения;
					телоПакета = текВыборка.Хранилище.Получить();
					статусЗавершение = Перечисления.сшпСтатусыСообщений.Обработано;
					сшпРаботаСДанными.УстановитьСостояниеСообщения(текИдентификатор, Перечисления.сшпСтатусыСообщений.ВОбработке);
					Если текВыборка.КлассСообщения = "TLR" тогда
						отправкаРезультат = сшпСистемныеСообщения.ПолучитьСписокВерсийОбработчиковСобытий();
					ИначеЕсли текВыборка.КлассСообщения = "TSR" тогда
						отправкаРезультат = сшпСистемныеСообщения.ПолучитьСписокОбработчиковСобытий(текВыборка.ФорматСообщения, телоПакета);
					ИначеЕсли текВыборка.КлассСообщения = "RML" тогда
						отправкаРезультат = сшпСистемныеСообщения.ВозвратПакетовВОбработку(текВыборка.ФорматСообщения, телоПакета);
					ИначеЕсли текВыборка.КлассСообщения = "BMR" тогда
						заданиеПолученияСтуктуры = ФоновыеЗадания.Выполнить("сшпСистемныеСообщения.ПолучитьСтруктуруКонфигурации",, текИдентификатор, "ПолучитьСтруктуруКонфигурации");
						//отправкаРезультат = сшпСистемныеСообщения.ПолучитьСтруктуруКонфигурации();
					ИначеЕсли текВыборка.КлассСообщения = "V1C" тогда
						отправкаРезультат = сшпСистемныеСообщения.ПолучитьВерсиюПодсистемы1С();
					ИначеЕсли текВыборка.КлассСообщения = "CSB" тогда
						отправкаРезультат = сшпСистемныеСообщения.ПолучитьПараметрыПодключения();
					ИначеЕсли текВыборка.КлассСообщения = "FND" тогда    
						мсвПараметры = Новый Массив;
						мсвПараметры.Добавить(текВыборка.ФорматСообщения);
						мсвПараметры.Добавить(телоПакета);
						мсвПараметры.Добавить(текИдентификатор);
						заданиеПоиска = ФоновыеЗадания.Выполнить("сшпСистемныеСообщения.НайтиСообщения",мсвПараметры, текИдентификатор, "НайтиСообщения");
					ИначеЕсли текВыборка.КлассСообщения = "GPS" тогда
						отправкаРезультат = сшпСистемныеСообщения.ПолучитьСостониеОбработки(текВыборка.ФорматСообщения, телоПакета);
					ИначеЕсли текВыборка.КлассСообщения = "DEB" тогда
						мсвПараметры = Новый Массив;
						мсвПараметры.Добавить(текВыборка.ФорматСообщения);
						мсвПараметры.Добавить(телоПакета);
						мсвПараметры.Добавить(текИдентификатор);
						заданиеОбновления = ФоновыеЗадания.Выполнить("сшпОтладкаОбработчиков.ВыполнитьОтладкуОбработчика",мсвПараметры, текИдентификатор, "ВыполнитьОтладкуОбработчика");
					Иначе
						статусЗавершение = Перечисления.сшпСтатусыСообщений.ОтсутствуетОбработчик;
					КонецЕсли;
				Исключение
					ИнформацияОбОшибке = ИнформацияОбОшибке();
					ЗаписьЖурналаРегистрации("Datareon. Обработка очереди системных сообщений", УровеньЖурналаРегистрации.Ошибка,,текИдентификатор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
					статусЗавершение = Перечисления.сшпСтатусыСообщений.ОшибкаОбработки;	
				КонецПопытки;
				сшпРаботаСДанными.УстановитьСостояниеСообщения(текИдентификатор, статусЗавершение);
			КонецЦикла;
		КонецЕсли;
		#КонецОбласти
	КонецЕсли;
КонецПроцедуры
	
// Процедура - Обработка очереди исходящих сообщений
//
Процедура ОбработкаОчередиИсходящихСообщений() Экспорт
	Если НЕ сшпОбщегоНазначения.ПроверитьЭкземплярИнформационнойБазы() тогда
		ЗаписьЖурналаРегистрации("Datareon. Управление пулом обработчиков", УровеньЖурналаРегистрации.Ошибка,,, "Рассинхронизация идентификаторов информационной базы");
		Возврат;
	КонецЕсли;
	
	времяОжидания = ?(сшпФункциональныеОпции.АвтоматическийСтартОбработчиков(), сшпКэшируемыеФункции.ДлительностьОжиданияПриАвтоматическомСтарте(), сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпДлительностьОжидания")) * 1000;
	остатокВремениОжидания = времяОжидания;
	максимумПотоков = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпМаксимальноеКоличествоПотоковОбработкиИсходящих");
	Пока остатокВремениОжидания > 0 цикл
		СтартОжидания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Если НЕ ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") ИЛИ ПолучитьФункциональнуюОпцию("сшпОтключитьПотоки") ИЛИ ПолучитьФункциональнуюОпцию("сшпОтключитьПотокОбработкиОчередиИсходящих") тогда
			Прервать;
		ИначеЕсли ИдетОбработкаСистемныхСобытий() тогда // Если включена обработка системных событий приостанавливаем рабочие потоки обработки.
			сшпОбщегоНазначения.Ожидание(10);
			Продолжить;
		КонецЕсли;
		текКоличествоПотоков = максимумПотоков - сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Наименование", "СформироватьИсходящееСообщение");
		Если текКоличествоПотоков > 0 тогда
			текЗапрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ " + Строка(текКоличествоПотоков) + "
			|	тбОчередь.ИдентификаторСообщения,
			|	тбОчередь.ДатаРегистрации,
			|	тбОчередь.ФорматСообщения,
			|	тбОчередь.МетодХранения,
			|	тбОчередь.ЭтоУдаление,
			|	тбОчередь.ОбъектСобытия
			|ИЗ
			|	РегистрСведений.сшпОчередьИсходящихСообщений КАК тбОчередь
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
			|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
			|			И (тбСостояние.СтатусСообщения  В (&СписокСтатусов))
			|			И тбСостояние.ЗадержкаЧисло <= &ЗадержкаЧисло
			|
			|УПОРЯДОЧИТЬ ПО
			|	тбСостояние.ДатаИзменения");
			текЗапрос.УстановитьПараметр("СписокСтатусов", сшпКэшируемыеФункции.ПоучитьСписокРабочихСтатусов());
			текЗапрос.УстановитьПараметр("ЗадержкаЧисло", сшпОбщегоНазначения.ПеревестиДатуВЧисло(ТекущаяДатаСеанса()));
			текРезультат = текЗапрос.Выполнить();
			Если текРезультат.Пустой() тогда
				сшпОбщегоНазначения.Ожидание(10);
				остатокВремениОжидания = остатокВремениОжидания - (ТекущаяУниверсальнаяДатаВМиллисекундах() - СтартОжидания);
			Иначе
				текИдентификатор = "Идентификатор не определен";
				Попытка
					
					текВыборка = текРезультат.Выбрать();
					Пока текВыборка.Следующий() цикл
						текИдентификатор = текВыборка.ИдентификаторСообщения;
						сшпРаботаСДанными.УстановитьСостояниеСообщения(текИдентификатор, Перечисления.сшпСтатусыСообщений.ВОбработке);
						
						мсвПараметры = Новый Массив;
						мсвПараметры.Добавить(текИдентификатор);
						мсвПараметры.Добавить(текВыборка.ФорматСообщения);
						мсвПараметры.Добавить(текВыборка.ОбъектСобытия);
						мсвПараметры.Добавить(текВыборка.МетодХранения);
						мсвПараметры.Добавить(текВыборка.ДатаРегистрации);
						мсвПараметры.Добавить(текВыборка.ЭтоУдаление);
						ФоновыеЗадания.Выполнить("сшпОбслуживаниеОчередей.СформироватьИсходящееСообщение",мсвПараметры, текИдентификатор, "СформироватьИсходящееСообщение");
					КонецЦикла;
					
					остатокВремениОжидания = времяОжидания;
				Исключение
					ИнформацияОбОшибке = ИнформацияОбОшибке();
					ЗаписьЖурналаРегистрации("Datareon. Обработка очереди исходящих сообщений", УровеньЖурналаРегистрации.Ошибка,,текИдентификатор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				КонецПопытки;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

// Процедура - Обработка очереди входящих сообщений
//
Процедура ОбработкаОчередиВходящихСообщений() Экспорт
	Если НЕ сшпОбщегоНазначения.ПроверитьЭкземплярИнформационнойБазы() тогда
		ЗаписьЖурналаРегистрации("Datareon. Управление пулом обработчиков", УровеньЖурналаРегистрации.Ошибка,,, "Рассинхронизация идентификаторов информационной базы");
		Возврат;
	КонецЕсли;
	
	времяОжидания = ?(сшпФункциональныеОпции.АвтоматическийСтартОбработчиков(), сшпКэшируемыеФункции.ДлительностьОжиданияПриАвтоматическомСтарте(), сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпДлительностьОжидания")) * 1000;
	остатокВремениОжидания = времяОжидания;
	максимумПотоков = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпМаксимальноеКоличествоПотоковОбработкиВходящих");
	Пока остатокВремениОжидания > 0 цикл
		СтартОжидания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Если НЕ ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") ИЛИ ПолучитьФункциональнуюОпцию("сшпОтключитьПотоки") тогда
			Прервать;
		ИначеЕсли ИдетОбработкаСистемныхСобытий() тогда // Если включена обработка системных событий приостанавливаем рабочие потоки.
			сшпОбщегоНазначения.Ожидание(10);
			Продолжить;
		КонецЕсли;
		текКоличествоПотоков = максимумПотоков - сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Наименование", "ОбработатьВходящееСообщение");
		Если текКоличествоПотоков > 0 тогда
			текЗапрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ " + Строка(текКоличествоПотоков) + "
			|	тбОчередь.ДатаРегистрации,
			|	тбОчередь.ИдентификаторСообщения,
			|	тбОчередь.ФорматСообщения,
			|	тбОчередь.КлассСообщения
			|ИЗ
			|	РегистрСведений.сшпОчередьВходящихСообщений КАК тбОчередь
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
			|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
			|			И (тбСостояние.СтатусСообщения В (&СписокСтатусов))
			|			И тбСостояние.ЗадержкаЧисло <= &ЗадержкаЧисло
			|
			|УПОРЯДОЧИТЬ ПО
			|	тбОчередь.ДатаСоздания,
			|	тбСостояние.ДатаИзменения");
			текЗапрос.УстановитьПараметр("СписокСтатусов", сшпКэшируемыеФункции.ПоучитьСписокРабочихСтатусов());
			текЗапрос.УстановитьПараметр("ЗадержкаЧисло", сшпОбщегоНазначения.ПеревестиДатуВЧисло(ТекущаяДатаСеанса()));
			текРезультат = текЗапрос.Выполнить();
			Если текРезультат.Пустой() тогда
				сшпОбщегоНазначения.Ожидание(10);
				остатокВремениОжидания = остатокВремениОжидания - (ТекущаяУниверсальнаяДатаВМиллисекундах() - СтартОжидания);
			Иначе
				текИдентификатор = "Идентификатор не определен";
				Попытка
					
					текВыборка = текРезультат.Выбрать();
					Пока текВыборка.Следующий() цикл
						текИдентификатор = текВыборка.ИдентификаторСообщения;
						
						сшпРаботаСДанными.УстановитьСостояниеСообщения(текИдентификатор, Перечисления.сшпСтатусыСообщений.ВОбработке);
						
						мсвПараметры = Новый Массив;
						мсвПараметры.Добавить(текИдентификатор);
						мсвПараметры.Добавить(текВыборка.ФорматСообщения);
						мсвПараметры.Добавить(текВыборка.КлассСообщения);
						мсвПараметры.Добавить(текВыборка.ДатаРегистрации);
						ФоновыеЗадания.Выполнить("сшпОбслуживаниеОчередей.ОбработатьВходящееСообщение",мсвПараметры, текИдентификатор, "ОбработатьВходящееСообщение");
					КонецЦикла;
					
					остатокВремениОжидания = времяОжидания;
				Исключение
					ИнформацияОбОшибке = ИнформацияОбОшибке();
					ЗаписьЖурналаРегистрации("Datareon. Обработка очереди входящих сообщений", УровеньЖурналаРегистрации.Ошибка,,текИдентификатор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				КонецПопытки;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

// Процедура - Обработка очереди отправляемых
//
Процедура ОбработкаОчередиОтправляемых() Экспорт
	коннектор = сшпВзаимодействиеСАдаптером.ПолучитьКоннектор();
	ИспользоватьПакетныйРежим = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпРежимПередачиСообщений") = Перечисления.сшпРежимыПередачиСообщений.Batch;
	РазмерПакета = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпРазмерПакета");
	КоличествоСообщений = ?(ИспользоватьПакетныйРежим, Макс(РазмерПакета, 1), 10);
	максимумПотоков = 1;
	МассивСообщенийДляОтправки = Новый Массив;
	Если коннектор = Неопределено Тогда 
		Возврат;	
	КонецЕсли;

	Пока Истина цикл
		СтартОжидания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Если НЕ ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") ИЛИ ПолучитьФункциональнуюОпцию("сшпОтключитьПотоки") тогда
			Прервать;
		КонецЕсли;
		
		текКоличествоПотоков = максимумПотоков - сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Наименование", "ОтправитьСообщение");
		Если текКоличествоПотоков = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		текЗапрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ " + Формат(КоличествоСообщений*текКоличествоПотоков, "ЧГ=0") + "
		|	тбОчередь.ИдентификаторСообщения,
		|	тбОчередь.Хранилище
		|ИЗ
		|	РегистрСведений.сшпОчередьОтправляемыхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОжиданиеОтправки))
		|			И тбСостояние.ЗадержкаЧисло <= &ЗадержкаЧисло
		|
		|УПОРЯДОЧИТЬ ПО
		|	тбСостояние.ДатаИзменения");
		текЗапрос.УстановитьПараметр("ЗадержкаЧисло", сшпОбщегоНазначения.ПеревестиДатуВЧисло(ТекущаяДатаСеанса()));
		текРезультат = текЗапрос.Выполнить();
		Если текРезультат.Пустой() тогда
			Прервать;
		Иначе
			текИдентификатор = "Идентификатор не определен";
			МассивСообщенийДляОтправки.Очистить();
			Индекс = 0;
			
			Попытка
				текВыборка = текРезультат.Выбрать();
				Пока текВыборка.Следующий() цикл
					текИдентификатор = текВыборка.ИдентификаторСообщения;
					сообщение = текВыборка.Хранилище.Получить();
					сообщение.Id = текИдентификатор;
					
					Если ИспользоватьПакетныйРежим Тогда 
						Индекс = Индекс + 1;
						МассивСообщенийДляОтправки.Добавить(сообщение);
						Если Индекс >= КоличествоСообщений Тогда 
							сшпОбслуживаниеОчередей.ОтправитьСообщение(коннектор, МассивСообщенийДляОтправки);
							
							Индекс = 0;
							МассивСообщенийДляОтправки.Очистить();
						КонецЕсли;
					Иначе
						сшпОбслуживаниеОчередей.ОтправитьСообщение(коннектор, сообщение);
					КонецЕсли;
					Если коннектор = Неопределено Тогда 
						Возврат;
					КонецЕсли;
				КонецЦикла;
				
				Если МассивСообщенийДляОтправки.Количество() Тогда 
					сшпОбслуживаниеОчередей.ОтправитьСообщение(коннектор, МассивСообщенийДляОтправки);
					Если коннектор = Неопределено Тогда 
						Возврат;
					КонецЕсли;
				КонецЕсли;
			Исключение
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации("Datareon. Обработка очереди отправляемых сообщений", УровеньЖурналаРегистрации.Ошибка,,текИдентификатор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			КонецПопытки;
		КонецЕсли;	
	КонецЦикла;

	коннектор = Неопределено;
КонецПроцедуры	

// Процедура - Обработка очереди отправляемых
//
Процедура ОбработкаОчередиОтправляемыхСистемныхСообщений() Экспорт
	коннектор = сшпВзаимодействиеСАдаптером.ПолучитьКоннектор();
	ИспользоватьПакетныйРежим = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпРежимПередачиСообщений") = Перечисления.сшпРежимыПередачиСообщений.Batch;
	РазмерПакета = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпРазмерПакета");
	КоличествоСообщений = ?(ИспользоватьПакетныйРежим, Макс(РазмерПакета, 1), 10);
	//максимумПотоков = сшпРаботаСКонстантами.ПолучитьЗначениеКонстанты("сшпМаксимальноеКоличествоПотоковОтправкиСообщений");
	максимумПотоков = 1;
	МассивСообщенийДляОтправки = Новый Массив;
	Если коннектор = Неопределено Тогда 
		Возврат;	
	КонецЕсли;

	
	Пока Истина цикл
		СтартОжидания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Если НЕ ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") тогда
			Прервать;
		КонецЕсли;
		
		текЗапрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ " + Формат(КоличествоСообщений, "ЧГ=0") + "
		|	тбОчередь.ИдентификаторСообщения,
		|	тбОчередь.Хранилище
		|ИЗ
		|	РегистрСведений.сшпОчередьОтправляемыхСистемныхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОжиданиеОтправки))
		|			И тбСостояние.ЗадержкаЧисло <= &ЗадержкаЧисло
		|
		|УПОРЯДОЧИТЬ ПО
		|	тбСостояние.ДатаИзменения");
		текЗапрос.УстановитьПараметр("ЗадержкаЧисло", сшпОбщегоНазначения.ПеревестиДатуВЧисло(ТекущаяДатаСеанса()));
		текРезультат = текЗапрос.Выполнить();
		Если текРезультат.Пустой() тогда
			Прервать;
		Иначе
			текИдентификатор = "Идентификатор не определен";
			МассивСообщенийДляОтправки.Очистить();
			Индекс = 0;
			
			Попытка
				текВыборка = текРезультат.Выбрать();
				Пока текВыборка.Следующий() цикл
					текИдентификатор = текВыборка.ИдентификаторСообщения;
					сообщение = текВыборка.Хранилище.Получить();
					сообщение.Id = текИдентификатор;
					
					Если ИспользоватьПакетныйРежим Тогда 
						Индекс = Индекс + 1;
						МассивСообщенийДляОтправки.Добавить(сообщение);
						Если Индекс >= КоличествоСообщений Тогда 
							сшпОбслуживаниеОчередей.ОтправитьСообщение(коннектор, МассивСообщенийДляОтправки);
							Индекс = 0;
							МассивСообщенийДляОтправки.Очистить();
						КонецЕсли;
					Иначе
						сшпОбслуживаниеОчередей.ОтправитьСообщение(коннектор, сообщение);
					КонецЕсли;
					Если коннектор = Неопределено Тогда 
						Возврат;
					КонецЕсли;
				КонецЦикла;
				
				Если МассивСообщенийДляОтправки.Количество() Тогда 
					сшпОбслуживаниеОчередей.ОтправитьСообщение(коннектор, МассивСообщенийДляОтправки);
					Если коннектор = Неопределено Тогда 
						Возврат;
					КонецЕсли;
				КонецЕсли;
			Исключение
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации("Datareon. Обработка очереди отправляемых сообщений", УровеньЖурналаРегистрации.Ошибка,,текИдентификатор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			КонецПопытки;
		КонецЕсли;	
	КонецЦикла;

	коннектор = Неопределено;
КонецПроцедуры	

// Процедура - Отправить сообщение
//
// Параметры:
//  Коннектор	 - HTTPСоединение,WSПрокси - Соединение с адаптером.
//  Сообщение	 - Массив, Структура - структура, содержащая исходящее сообщение или массив структур.
//
Процедура ОтправитьСообщение(Коннектор, Сообщение) Экспорт 
	сшпВзаимодействиеСАдаптером.ОтправитьСообщениеНаАдаптер(Коннектор, Сообщение);
	Если Коннектор = Неопределено Тогда 
		ЗаписьЖурналаРегистрации("Datareon. Взаимодействие с адаптером", УровеньЖурналаРегистрации.Предупреждение,,, "Отсутствует связь с адаптером");
	КонецЕсли;
КонецПроцедуры

// Процедура - Очистка очередей сообщений
//
Процедура ОчисткаОчередейСообщений() Экспорт
	мсвКлючи = Новый Массив;
	мсвКлючи.Добавить("ОтправитьСообщениеНаАдаптер");
	мсвКлючи.Добавить("ОбработатьСообщениеОтАдаптера");
	текКоличествоПотоков = сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Наименование", мсвКлючи);
	Если текКоличествоПотоков = 0 тогда
		//возврат в обработку "зависших" сообщений
		текЗапрос = Новый Запрос("ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения КАК Идентификатор,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Исходящая) КАК ТипОчереди
		|ИЗ
		|	РегистрСведений.сшпОчередьИсходящихСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ВОбработке))
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, МИНУТА) > 60)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения КАК Идентификатор,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Входящая) КАК ТипОчереди
		|ИЗ
		|	РегистрСведений.сшпОчередьВходящихСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ВОбработке))
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, МИНУТА) > 60)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения КАК Идентификатор,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Системная) КАК ТипОчереди
		|ИЗ
		|	РегистрСведений.сшпОчередьСистемныхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ВОбработке))
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, МИНУТА) > 60)
		|");
		текЗапрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Пока текВыборка.Следующий() цикл
			состояние = ?(текВыборка.ТипОчереди = Перечисления.сшпТипыОчередей.Отправки, Перечисления.сшпСтатусыСообщений.ОжиданиеОтправки, Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки);
			сшпРаботаСДанными.УстановитьСостояниеСообщения(текВыборка.Идентификатор, состояние);
		КонецЦикла;
		
		//очистка очередей от старых сообщений
		текЗапрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаДлительностиОжидания.ТипОчереди КАК ТипОчереди,
		|	ТаблицаДлительностиОжидания.СтатусСообщения КАК СтатусСообщения,
		|	ТаблицаДлительностиОжидания.ДлительностьХранения КАК Длительность
		|ПОМЕСТИТЬ ТаблицаДлительностиОжидания
		|ИЗ
		|	&ТаблицаДлительностиОжидания КАК ТаблицаДлительностиОжидания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения КАК Идентификатор,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Исходящая) КАК ТипОчереди
		|ИЗ
		|	РегистрСведений.сшпОчередьИсходящихСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДлительностиОжидания КАК ТаблицаДлительностиОжидания
		|			ПО тбСостояние.СтатусСообщения = ТаблицаДлительностиОжидания.СтатусСообщения
		|				И (ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Исходящая) = ТаблицаДлительностиОжидания.ТипОчереди)
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, ДЕНЬ) > ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию))
		|			И (ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию) > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Входящая)
		|ИЗ
		|	РегистрСведений.сшпОчередьВходящихСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДлительностиОжидания КАК ТаблицаДлительностиОжидания
		|			ПО тбСостояние.СтатусСообщения = ТаблицаДлительностиОжидания.СтатусСообщения
		|				И (ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Входящая) = ТаблицаДлительностиОжидания.ТипОчереди)
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, ДЕНЬ) > ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию))
		|			И (ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию) > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Отправки)
		|ИЗ
		|	РегистрСведений.сшпОчередьОтправляемыхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДлительностиОжидания КАК ТаблицаДлительностиОжидания
		|			ПО тбСостояние.СтатусСообщения = ТаблицаДлительностиОжидания.СтатусСообщения
		|				И (ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Отправки) = ТаблицаДлительностиОжидания.ТипОчереди)
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, ДЕНЬ) > ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию))
		|			И (ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию) > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.ОтправкиСистемныхСообщений)
		|ИЗ
		|	РегистрСведений.сшпОчередьОтправляемыхСистемныхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДлительностиОжидания КАК ТаблицаДлительностиОжидания
		|			ПО тбСостояние.СтатусСообщения = ТаблицаДлительностиОжидания.СтатусСообщения
		|				И (ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Отправки) = ТаблицаДлительностиОжидания.ТипОчереди)
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, ДЕНЬ) > ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию))
		|			И (ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию) > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения,
		|	ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Системная)
		|ИЗ
		|	РегистрСведений.сшпОчередьСистемныхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДлительностиОжидания КАК ТаблицаДлительностиОжидания
		|			ПО тбСостояние.СтатусСообщения = ТаблицаДлительностиОжидания.СтатусСообщения
		|				И (ЗНАЧЕНИЕ(Перечисление.сшпТипыОчередей.Системная) = ТаблицаДлительностиОжидания.ТипОчереди)
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, ДЕНЬ) > ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию))
		|			И (ЕСТЬNULL(ТаблицаДлительностиОжидания.Длительность, &СрокХраненияПоУмолчанию) > 0)");
		текЗапрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		текЗапрос.УстановитьПараметр("СрокХраненияПоУмолчанию", сшпРаботаСКонстантами.ПолучитьДлительностьХраненияПоУмолчанию());
		текЗапрос.УстановитьПараметр("ТаблицаДлительностиОжидания", сшпРаботаСКонстантами.ПолучитьДлительностьХранения());
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Пока текВыборка.Следующий() цикл
			сшпРаботаСДанными.УдалитьСообщение(текВыборка.ТипОчереди, текВыборка.Идентификатор);
		КонецЦикла;
		
		//Отмена отправки неактуальных системных сообщений
		текЗапрос = Новый Запрос(
		"ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.сшпОчередьОтправляемыхСистемныхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.сшпСтатусыСообщений.ОжиданиеОтправки))
		|			И (РАЗНОСТЬДАТ(тбОчередь.ДатаРегистрации, &ТекущаяДата, МИНУТА) > 10)");
		текЗапрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Пока текВыборка.Следующий() цикл
			сшпРаботаСДанными.УстановитьСостояниеСообщения(текВыборка.ИдентификаторСообщения, Перечисления.сшпСтатусыСообщений.ОтправкаОтменена);
		КонецЦикла;
		
		//Отмена обработки неактуальных системных сообщений
		фильтрСостояний = Новый СписокЗначений;
		фильтрСостояний.Добавить(Перечисления.сшпСтатусыСообщений.Новое);
		фильтрСостояний.Добавить(Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки);
		
		текЗапрос = Новый Запрос(
		"ВЫБРАТЬ
		|	тбОчередь.ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.сшпОчередьСистемныхСообщений КАК тбОчередь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.сшпСостояниеСообщений КАК тбСостояние
		|		ПО тбОчередь.ИдентификаторСообщения = тбСостояние.ИдентификаторСообщения
		|			И (тбСостояние.СтатусСообщения В (&ФильтрСостояний))
		|			И (РАЗНОСТЬДАТ(тбСостояние.ДатаИзменения, &ТекущаяДата, МИНУТА) > 10)");
		текЗапрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		текЗапрос.УстановитьПараметр("ФильтрСостояний", фильтрСостояний);
		текВыборка = текЗапрос.Выполнить().Выбрать();
		Пока текВыборка.Следующий() цикл
			сшпРаботаСДанными.УстановитьСостояниеСообщения(текВыборка.ИдентификаторСообщения, Перечисления.сшпСтатусыСообщений.ОбработкаОтменена);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

// Процедура - Отправить исходящее сообщение 
//
// Параметры:
//  Идентификатор	 - УникальныйИдентификатор	 - Идентификатор сообщения.
//  ФорматСообщения	 - Перечисление.сшпФорматыСообщений	 - Формат отправляемого сообщения.
//  ОбъектСобытия	 - Строка - Имя объекта по которому зарегистрировано событие. По данному имени выполняется получение обработчика для события.
//  ТелоСообщения	 - Строка,ДвоичныеДанные - Сериализованное состояние объекта на момент события. 
//
Процедура СформироватьИсходящееСообщение(идентификатор, форматсообщения, типобъекта, методхранения, ДатаРегистрации, ЭтоУдаление) Экспорт
	Если ИдетОбработкаСистемныхСобытий() тогда
		Возврат; // Если включена обработка системных событий останавливаем рабочие потоки.
	КонецЕсли;
	
	сткОбработчик = сшпКэшируемыеФункции.ПолучитьОбработчик(типобъекта, Перечисления.сшпТипыИнтеграции.Исходящая, сшпФункциональныеОпции.ВерсияОбработчиков());
	Если НЕ ЗначениеЗаполнено(сткОбработчик.ПроцедураОбработки) тогда
		сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, Перечисления.сшпСтатусыСообщений.ОтсутствуетОбработчик);
	Иначе	
		Если сткОбработчик.Статус = Перечисления.сшпСтатусыОбработчиков.Отключен тогда
			сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, Перечисления.сшпСтатусыСообщений.ОбработкаОтменена);
		Иначе
			задержка = 0;
			ОписаниеОшибки = "";
			ОтменитьОтправку = Ложь;
			текЗаголовокЖурнала = "Datareon. Получение объекта события";
			объектсобытия = ПолучитьОбъектСобытия(Перечисления.сшпТипыИнтеграции.Исходящая, идентификатор);
			
			текЗаголовокЖурнала = "Datareon. Формирование сообщения";
			РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета();
			РезультатОбработки.Id = Идентификатор;
			Попытка
				СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано; // Переменная для установки нового состояние сообщения
				ОбъектОбработки = Неопределено;
				Если методхранения = Перечисления.сшпМетодХранения.ПоСсылке тогда
					Если ТипЗнч(объектсобытия) = Тип("Отбор") тогда
						имяРегистра = сшпКэшируемыеФункции.ПолучитьИмяОбъекта(типобъекта);
						ОбъектОбработки = РегистрыСведений[имяРегистра].СоздатьНаборЗаписей();
						Для Каждого элементОтбор из объектсобытия цикл
							ЗаполнитьЗначенияСвойств(ОбъектОбработки.Отбор[элементОтбор.Имя], элементОтбор);
						КонецЦикла;
						ОбъектОбработки.Прочитать();
					Иначе
						ОбъектОбработки = объектсобытия;
					КонецЕсли;
				Иначе	
					ОбъектОбработки = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, объектсобытия);
				КонецЕсли;
				Выполнить(сткОбработчик.ПроцедураОбработки);
				
				Если СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано тогда
					текЗаголовокЖурнала = "Datareon. Помещение сообщения в очередь отправки";
					текВозврат = сшпВзаимодействиеСАдаптером.ОтправитьСообщение(РезультатОбработки);
					Если НЕ текВозврат тогда
						СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки;
						задержка = 30;
					КонецЕсли;
				КонецЕсли;
			Исключение
				ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации(текЗаголовокЖурнала, УровеньЖурналаРегистрации.Ошибка,,Идентификатор, ОписаниеОшибки);
				Если текЗаголовокЖурнала = "Datareon. Формирование сообщения" тогда
					СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОшибкаОбработки;
					сткСвойства = Новый Структура;
					сткСвойства.Вставить("TemplateId", Строка(сткОбработчик.ИдентификаторШаблона));
					сткСвойства.Вставить("TemplateVersion", сткОбработчик.Версия);
					сткСвойства.Вставить("MessageId", Строка(Идентификатор));
					сткСвойства.Вставить("TemplateName", сткОбработчик.Наименование);  
					сшпСистемныеСообщения.ОтправитьСообщениеОбОшибке("Handler", ОписаниеОшибки, сткСвойства);
				Иначе
					СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОжиданиеОбработки;
					задержка = 30;
				КонецЕсли;	
			КонецПопытки;
			сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, СостояниеСообщения, задержка, ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - Обработать входящее сообщение 
//
// Параметры:
//  Идентификатор	 - УникальныйИдентификатор - Идентификатор сообщения.
//  ФорматСообщения	 - Перечисление.сшпФорматыСообщений	 - Формат поступившего сообщения.
//  КлассСообщения	 - Строка - Идентификатор класса поступившего сообщения. По данному классу выполняется получение обработчика для сообщения.
//  ТелоСообщения	 - Строка,ДвоичныеДанные - Тело сообщения в указанном формате.
//
Процедура ОбработатьВходящееСообщение(Идентификатор, ФорматСообщения, КлассСообщения, ДатаРегистрации) Экспорт
	Если ИдетОбработкаСистемныхСобытий() тогда
		Возврат; // Если включена обработка системных событий останавливаем рабочие потоки.
	КонецЕсли;
	
	сткОбработчик = сшпКэшируемыеФункции.ПолучитьОбработчик(КлассСообщения, Перечисления.сшпТипыИнтеграции.Входящая, сшпФункциональныеОпции.ВерсияОбработчиков());
	Если НЕ ЗначениеЗаполнено(сткОбработчик.ПроцедураОбработки) тогда
		сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, Перечисления.сшпСтатусыСообщений.ОтсутствуетОбработчик);
	Иначе	
		Если сткОбработчик.Статус = Перечисления.сшпСтатусыОбработчиков.Отключен тогда
			сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, Перечисления.сшпСтатусыСообщений.ОбработкаОтменена);
		Иначе	
			Попытка
				задержка = 0;
				текЗаголовокЖурнала = "Datareon. Получение объекта события";
				объектсобытия = ПолучитьОбъектСобытия(Перечисления.сшпТипыИнтеграции.Входящая, идентификатор);
				идШаблона = сткОбработчик.ИдентификаторШаблона;
				версияШаблона = сткОбработчик.Версия;
				
				СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано; // Переменная для установки нового состояние сообщения
				ОбъектСообщение = сшпОбщегоНазначения.ПолучитьОбъектXDTO(ФорматСообщения, объектсобытия);
				Выполнить(сткОбработчик.ПроцедураОбработки);
				
				сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, СостояниеСообщения, задержка);
			Исключение
				информацияОшибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации("Datareon. Обработка сообщения", УровеньЖурналаРегистрации.Ошибка,,Идентификатор, информацияОшибка);
				сткСвойства = Новый Структура;
				сткСвойства.Вставить("TemplateId", Строка(идШаблона));
				сткСвойства.Вставить("TemplateVersion", версияШаблона);
				сткСвойства.Вставить("MessageId", Строка(Идентификатор));
				сткСвойства.Вставить("TemplateName", сткОбработчик.Наименование);  
				сшпСистемныеСообщения.ОтправитьСообщениеОбОшибке("Handler", информацияОшибка, сткСвойства);
				сшпРаботаСДанными.УстановитьСостояниеСообщения(Идентификатор, Перечисления.сшпСтатусыСообщений.ОшибкаОбработки);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Функция - Идет обработка системных событий
// 
// Возвращаемое значение:
// Булево  - Признак выполнения системных сообщений по изменению обработчиков.
//
Функция ИдетОбработкаСистемныхСобытий() Экспорт
	мсвКлючи = Новый Массив;
	мсвКлючи.Добавить("ВыполнитьИзменениеОбработчиков");
	количествоПотоков = сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Ключ", мсвКлючи);
	Возврат НЕ количествоПотоков = 0; 
КонецФункции	

// Процедура - Выполнить изменение обработчиков
//
// Параметры:
//  Сообщения	 - ТаблицаЗначений - список событий по изменению обработчиков. 
//
Процедура ВыполнитьИзменениеОбработчиков(Сообщения) Экспорт
	#Область Ожидание // Ожидаем завершения текущих процессов обработки рабочих очередей.
	счетчикПопытки = 100;
	Пока Истина = Истина Цикл
		мсвКлючи = Новый Массив;
		мсвКлючи.Добавить("ОтправитьСообщениеНаАдаптер");
		мсвКлючи.Добавить("ОбработатьСообщениеОтАдаптера");
		количествоПотоков = сшпОбщегоНазначения.ПолучитьКоличествоПотоков("Наименование", мсвКлючи);
		Если количествоПотоков = 0 тогда
			Прервать;
		Иначе
			счетчикПопытки = счетчикПопытки - 1;
			Если счетчикПопытки = 0 тогда
				ЗаписьЖурналаРегистрации("Datareon. Обработка очереди системных сообщений", УровеньЖурналаРегистрации.Ошибка,,, "Не удалось дождаться остановки потоков обработки рабочих очередей.");
				Возврат;
			Иначе	
				сшпОбщегоНазначения.Ожидание(5);
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	#КонецОбласти
	
	текИдентификатор = "Идентификатор не определен";
	Для каждого строкаСообщение из Сообщения цикл
		Попытка
			результатОперации = Истина;
			текИдентификатор = строкаСообщение.ИдентификаторСообщения;
			телоПакета = строкаСообщение.Хранилище.Получить();
			статусЗавершение = Перечисления.сшпСтатусыСообщений.Обработано;
			сшпРаботаСДанными.УстановитьСостояниеСообщения(текИдентификатор, Перечисления.сшпСтатусыСообщений.ВОбработке);
			Если строкаСообщение.КлассСообщения = "TUM" тогда
				результатОперации = сшпСистемныеСообщения.ОбновитьОбработчикСобытия(строкаСообщение.ФорматСообщения, телоПакета);
			ИначеЕсли строкаСообщение.КлассСообщения = "TCS" тогда
				результатОперации = сшпСистемныеСообщения.УправлениеСостояниемОбработчика(строкаСообщение.ФорматСообщения, телоПакета);
			ИначеЕсли строкаСообщение.КлассСообщения = "TRM" тогда
				результатОперации = сшпСистемныеСообщения.УдалитьОбработчикСобытия(строкаСообщение.ФорматСообщения, телоПакета);
			КонецЕсли;
			Если НЕ результатОперации тогда
				статусЗавершение = Перечисления.сшпСтатусыСообщений.ОшибкаОбработки;
			КонецЕсли;	
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			статусЗавершение = Перечисления.сшпСтатусыСообщений.ОшибкаОбработки;
			ЗаписьЖурналаРегистрации("Datareon. Обработка очереди системных сообщений", УровеньЖурналаРегистрации.Ошибка,,текИдентификатор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		КонецПопытки;
		сшпРаботаСДанными.УстановитьСостояниеСообщения(текИдентификатор, статусЗавершение);
	КонецЦикла;
КонецПроцедуры	

// Функция - Получить объект события
//
// Параметры:
//  типочереди		 - Перечисление.сшпТипыИнтеграции - тип очереди из которой необходимо получить данные 
//  идентификатор	 - УникальныйИдентификатор - идентификатор события по которому требуется получить данные. 
// 
// Возвращаемое значение:
//  Строка - данные события.
//
Функция ПолучитьОбъектСобытия(типочереди, идентификатор) Экспорт 
	запросОбъектСобытия = Новый Запрос;
	запросОбъектСобытия.Текст = "ВЫБРАТЬ
	|	тбОчередь.Хранилище
	|ИЗ
	|	РегистрСведений." +?(типочереди = Перечисления.сшпТипыИнтеграции.Исходящая, "сшпОчередьИсходящихСообщений", "сшпОчередьВходящихСообщений") + " КАК тбОчередь
	|ГДЕ
	|	тбОчередь.ИдентификаторСообщения = &Идентификатор";
	запросОбъектСобытия.УстановитьПараметр("Идентификатор", идентификатор);
	запросВыборка = запросОбъектСобытия.Выполнить().Выбрать();
	запросВыборка.Следующий();
	Возврат запросВыборка.Хранилище.Получить();
КонецФункции

// Процедура - Выполнить внешнюю команду
//
// Параметры:
//  идентификатор	 - УникальныйИдентификатор	 - идентификатор текущей команды
//  форматсообщения	 - Перечисление.сшпФорматыСообщений	 - формат команды
//  xmlпакет		 - Строка - тело полученного сообщения команды.
//
Процедура ВыполнитьВнешнююКоманду(идентификатор, форматсообщения, xmlпакет) Экспорт
	Если ПолучитьФункциональнуюОпцию("сшпИспользоватьСШП") И НЕ ПолучитьФункциональнуюОпцию("сшпОтключитьПотоки") тогда
		xdtoТип 				= ФабрикаXDTO.Тип("http://esb.axelot.ru", "Message");
		xdtoПакет 				= сшпОбщегоНазначения.ПолучитьОбъектXDTO(форматсообщения, xmlпакет, xdtoТип);
		отправитьОтвет = Ложь;
		текЗаголовокЖурнала = "Datareon. Выполнение внешней команды";
		РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета();
		Попытка
			СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано; // Переменная для установки нового состояние сообщения
			Выполнить(xdtoПакет.Body);
			
			Если отправитьОтвет тогда
				текЗаголовокЖурнала = "Datareon. Отправка сообщения";
				текВозврат = сшпВзаимодействиеСАдаптером.ОтправитьСообщение(РезультатОбработки);
			КонецЕсли;
		Исключение
			информацияОшибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(текЗаголовокЖурнала, УровеньЖурналаРегистрации.Ошибка,,Идентификатор, информацияОшибка);
			Если текЗаголовокЖурнала = "Datareon. Выполнение внешней команды" тогда
				текНовоеСостояние = Перечисления.сшпСтатусыСообщений.ОшибкаОбработки;
				сткСвойства = сшпОбщегоНазначения.ПолучитьПараметрыСообщенияСтруктурой(xdtoПакет);
				сшпСистемныеСообщения.ОтправитьСообщениеОбОшибке("Command", информацияОшибка, сткСвойства);
			КонецЕсли;	
		КонецПопытки;
	КонецЕсли;	
КонецПроцедуры

// Процедура - Выполнить внешнюю команду
//
// Параметры:
//  идентификатор	 - УникальныйИдентификатор	 - идентификатор текущей команды
//  форматсообщения	 - Перечисление.сшпФорматыСообщений	 - формат команды
//  xmlпакет		 - Строка - тело полученного сообщения команды.
//
Процедура УстановитьПараметрыПользователя(идентификатор, СтруктураПараметров) Экспорт
	ПараметрыПользователяПолучены = СтруктураПараметров.Свойство("User") И СтруктураПараметров.Свойство("Password");
	текЗаголовокЖурнала = "Datareon. Установка параметров пользователя";
	
	Если Не ПараметрыПользователяПолучены Тогда
		информацияОшибка = "Полученное сообщение SUS не содержит обязательных свойств User и Password";
		ЗаписьЖурналаРегистрации(текЗаголовокЖурнала, УровеньЖурналаРегистрации.Ошибка,,Идентификатор, информацияОшибка);
		сшпСистемныеСообщения.ОтправитьСообщениеОбОшибке("SUS", информацияОшибка);
		Возврат;
	КонецЕсли;

	ИмяПользователя = СтруктураПараметров["User"];
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя);
	
	Попытка
		Если ПользовательИБ = Неопределено Тогда
			НовыйПользователь = ПользователиИнформационнойБазы.СоздатьПользователя();
			НовыйПользователь.Имя = ИмяПользователя;
			НовыйПользователь.ПолноеИмя = ИмяПользователя;
			НовыйПользователь.Пароль = СтруктураПараметров["Password"];
			НовыйПользователь.АутентификацияСтандартная = Истина;
			НовыйПользователь.Роли.Добавить(Метаданные.Роли.сшпОбменСESB);
			НовыйПользователь.Записать();
		ИначеЕсли Не ПользовательИБ.Роли.Содержит(Метаданные.Роли.сшпОбменСESB) Тогда 
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.сшпОбменСESB);
			ПользовательИБ.Записать();
		КонецЕсли;
	Исключение
		информацияОшибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(текЗаголовокЖурнала, УровеньЖурналаРегистрации.Ошибка,,Идентификатор, информацияОшибка);
		сшпСистемныеСообщения.ОтправитьСообщениеОбОшибке("SUS", информацияОшибка);
	КонецПопытки;
КонецПроцедуры

#КонецОбласти