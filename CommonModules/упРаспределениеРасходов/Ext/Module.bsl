#Область ПрограммныйИнтерфейс

// Процедура - расчета коэффициента распределения.
//
// Параметры:
//  Формула - Строка - Текстовое значение формулы рассчета.
//	ТаблицаДокументов - ТаблицаЗначений - Таблица данных.
//	сткКолонкиДляВыводаОшибки - Структура - Перечислены колонки.
//
Процедура РассчитатьКоэффициентыРаспределения(Формула, ТаблицаДокументов, сткКолонкиДляВыводаОшибки = Неопределено) Экспорт
	
	//упСЛК.СоздатьОбъект().РассчитатьКоэффициентыРаспределения(Формула, ТаблицаДокументов, сткКолонкиДляВыводаОшибки);
	
	//ПроверитьОграничениеПоДоступностиКомпонент(1);
	
	текКоличествоПроблемПриРасчете = 0;
	
	Для каждого текСтрока Из ТаблицаДокументов  Цикл
		Время                       = текСтрока.Время;
		Расстояние                  = текСтрока.Расстояние;
		Масса                       = текСтрока.Масса;
		Объем                       = текСтрока.Объем;
		КоличествоМест              = текСтрока.КоличествоМест;
		КоличествоЗагрузочныхМетров = текСтрока.КоличествоЗагрузочныхМетров;
		ПлановыеРасходыПоРейсу      = текСтрока.ПлановыеРасходыПоРейсу;
		ОценочнаяСтоимостьГруза	    = текСтрока.ОценочнаяСтоимостьГруза;
		ФактическиеРасходыПоРейсу   = текСтрока.ФактическиеРасходыПоРейсу;
				
		Попытка
			текСтрока.Коэффициент = Вычислить(Формула);
		Исключение
			
			текСтрока.Коэффициент = 0;
			текКоличествоПроблемПриРасчете = текКоличествоПроблемПриРасчете + 1;
			
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ТекстСообщения = Нстр("ru = 'При расчете коэффициентов распределения произошла ошибка:%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстОшибки);
			
			Если НЕ сткКолонкиДляВыводаОшибки = Неопределено И сткКолонкиДляВыводаОшибки.Количество() > 0 Тогда
				ТекстСообщения = ТекстСообщения + "(";
				Для каждого текКолонка Из сткКолонкиДляВыводаОшибки Цикл
					ТекстСообщения = ТекстСообщения + текКолонка.Ключ + ":" + текСтрока[текКолонка.Ключ] + ";";	
				КонецЦикла;
				ТекстСообщения = Лев(ТекстСообщения, СтрДлина(ТекстСообщения)-1);
				ТекстСообщения = ТекстСообщения + ")";
			КонецЕсли;
			
			//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
						
			ЗаписьЖурналаРегистрации("ОшибкаРасчетаФормулы", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;	
	
	Если текКоличествоПроблемПриРасчете > 0 И текКоличествоПроблемПриРасчете = ТаблицаДокументов.Количество() Тогда
		
		ТаблицаДокументов.ЗаполнитьЗначения(1/текКоличествоПроблемПриРасчете, "Коэффициент");
		
	КонецЕсли;

	
КонецПроцедуры	

// Процедура - расчета коэффициента распределения.
//
// Параметры:
//  СпособРаспределения		 - ПеречислениеСсылка.упСпособыРаспределенияРасходов - способ распределения расходов.
//  Показатель				 - СправочникСсылка.упПоказателиРасчета - показатель расчета
//  Формула				 - Строка - Текстовое значение формулы рассчета.
//  ТаблицаДокументов		 - ТаблицаЗначений - Таблица данных.
//  сткКолонкиДляВыводаОшибки	 - Структура - Перечислены колонки.
//
Процедура РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(СпособРаспределения, Показатель, Формула, ТаблицаДокументов, сткКолонкиДляВыводаОшибки = Неопределено) Экспорт
	
	//упСЛК.СоздатьОбъект().РассчитатьКоэффициентыРаспределения(Формула, ТаблицаДокументов, сткКолонкиДляВыводаОшибки);
	
	//ПроверитьОграничениеПоДоступностиКомпонент(1);
	
	Если СпособРаспределения = Перечисления.упСпособыРаспределенияРасходов.ПропорциональноПараметру Тогда
		 		
		сткФормулы = ПолучитьФормулуРаспределенияБезСуммы(Показатель);
		Формула = сткФормулы.Формула;
		
		СуммаПоВсемДокументам = СуммаПоВсемДокументам(сткФормулы.ФормулаСумма, ТаблицаДокументов, сткКолонкиДляВыводаОшибки);
		
		текКоличествоПроблемПриРасчете = 0;
		
		Для каждого текСтрока Из ТаблицаДокументов  Цикл
			Время                       = текСтрока.Время;
			Расстояние                  = текСтрока.Расстояние;
			Масса                       = текСтрока.Масса;
			Объем                       = текСтрока.Объем;
			КоличествоМест              = текСтрока.КоличествоМест;
			КоличествоЗагрузочныхМетров = текСтрока.КоличествоЗагрузочныхМетров;
			ПлановыеРасходыПоРейсу      = текСтрока.ПлановыеРасходыПоРейсу;
			ОценочнаяСтоимостьГруза	   = текСтрока.ОценочнаяСтоимостьГруза;
			ФактическиеРасходыПоРейсу   = текСтрока.ФактическиеРасходыПоРейсу;
			
			Попытка
				текСтрока.Коэффициент = Вычислить(Формула);
			Исключение
				
				текСтрока.Коэффициент = 0;
				текКоличествоПроблемПриРасчете = текКоличествоПроблемПриРасчете + 1;
				
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				
				ТекстСообщения = Нстр("ru = 'При расчете коэффициентов распределения произошла ошибка:%1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстОшибки);
				
				Если НЕ сткКолонкиДляВыводаОшибки = Неопределено И сткКолонкиДляВыводаОшибки.Количество() > 0 Тогда
					ТекстСообщения = ТекстСообщения + "(";
					Для каждого текКолонка Из сткКолонкиДляВыводаОшибки Цикл
						ТекстСообщения = ТекстСообщения + текКолонка.Ключ + ":" + текСтрока[текКолонка.Ключ] + ";";	
					КонецЦикла;
					ТекстСообщения = Лев(ТекстСообщения, СтрДлина(ТекстСообщения)-1);
					ТекстСообщения = ТекстСообщения + ")";
				КонецЕсли;
				
				//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
				ЗаписьЖурналаРегистрации("ОшибкаРасчетаФормулы", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			КонецПопытки;
			
		КонецЦикла;	
		
		Если текКоличествоПроблемПриРасчете > 0 И текКоличествоПроблемПриРасчете = ТаблицаДокументов.Количество() Тогда
			
			ТаблицаДокументов.ЗаполнитьЗначения(1/текКоличествоПроблемПриРасчете, "Коэффициент");
			
		КонецЕсли;
		
	Иначе
		РассчитатьКоэффициентыРаспределения(Формула, ТаблицаДокументов, сткКолонкиДляВыводаОшибки);
	КонецЕсли;

КонецПроцедуры

// Функция возвращает сумму по всем документам распределения
//
// Параметры:
//  Формула				 - Строка	 - формула.
//  ТаблицаДокументов		 - ТаблицаЗначений - таблица с показателями.
//  сткКолонкиДляВыводаОшибки	 - Структура - структура с именами полей.
// 
// Возвращаемое значение:
//  Число - сумма.
//
Функция СуммаПоВсемДокументам(Формула, ТаблицаДокументов, сткКолонкиДляВыводаОшибки = Неопределено) Экспорт
	
	//ПроверитьОграничениеПоДоступностиКомпонент(0);
	
	Результат = 0;
	
	Для Каждого текСтрока Из ТаблицаДокументов Цикл 
		Время                       = текСтрока.Время;
		Расстояние                  = текСтрока.Расстояние;
		Масса                       = текСтрока.Масса;
		Объем                       = текСтрока.Объем;
		КоличествоМест              = текСтрока.КоличествоМест;
		КоличествоЗагрузочныхМетров = текСтрока.КоличествоЗагрузочныхМетров;
		ПлановыеРасходыПоРейсу      = текСтрока.ПлановыеРасходыПоРейсу;
		ОценочнаяСтоимостьГруза		= текСтрока.ОценочнаяСтоимостьГруза;
		ФактическиеРасходыПоРейсу   = текСтрока.ФактическиеРасходыПоРейсу;
		
		Попытка
			Результат = Результат + Вычислить(Формула);	
		Исключение
			Результат = Неопределено;

			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ТекстСообщения = Нстр("ru = 'При расчете суммы по всем документам произошла ошибка:%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстОшибки);
			
			Если НЕ сткКолонкиДляВыводаОшибки = Неопределено И сткКолонкиДляВыводаОшибки.Количество() > 0 Тогда
				ТекстСообщения = ТекстСообщения + "(";
				Для каждого текКолонка Из сткКолонкиДляВыводаОшибки Цикл
					ТекстСообщения = ТекстСообщения + текКолонка.Ключ + ":" + текСтрока[текКолонка.Ключ] + ";";	
				КонецЦикла;
				ТекстСообщения = Лев(ТекстСообщения, СтрДлина(ТекстСообщения)-1);
				ТекстСообщения = ТекстСообщения + ")";
			КонецЕсли;
			
			//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
			ЗаписьЖурналаРегистрации("ОшибкаРасчетаСуммы", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ЗаполнениеРасходовВДокументах

// Формирование списков статей расходов.
// 
// Параметры:
//  ДокументОснование - ДокументСсылка - Ссылка на элемент.
//	ТипТС - СправочникСсылка.упТипыТС - Ссылка на элемент.
//	Соглашение - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьСписокСтатейРасходов(ДокументОснование, ТипТС, Соглашение) Экспорт

	текРезультат = Неопределено;
	
	текЗапрос = Новый Запрос;
		
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упРейс") Тогда
		текЗапрос.Текст = ТекстЗапросаСтатейРасходовПоРейсу();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда		
		текЗапрос.Текст = ТекстЗапросаСтатейРасходовПоЗаявкеНаТС();	
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда		
		текЗапрос.Текст = ТекстЗапросаСтатейРасходовПоПредложениюПеревозчика();	
	Иначе
		Возврат текРезультат;	
	КонецЕсли;
	
	текЗапрос.УстановитьПараметр("ТипТС", 				ТипТС);
	текЗапрос.УстановитьПараметр("Соглашение", 			Соглашение);
	текЗапрос.УстановитьПараметр("ДокументОснование", 	ДокументОснование);
	
	текРезультат 	= текЗапрос.Выполнить().Выгрузить();
	
	Возврат текРезультат;				  

КонецФункции // ПолучитьСписокСтатейРасходов()

// Список плановых расходов по заявке на ТС.
//
// Параметры:
//  ЗаявкаНаТС	 - ДокументСсылка.упЗаявкаНаТС - заявка на ТС.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьСписокСтатейРасходовПоЗаявкеНаТС(ЗаявкаНаТС) Экспорт

	Результат    = Неопределено;
	
	Запрос       = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	упЗаявкаНаТСПлановыеРасходы.СтатьяРасходов,
	|	упЗаявкаНаТСПлановыеРасходы.ПравилоРаспределения,
	|	упЗаявкаНаТСПлановыеРасходы.Валюта,
	|	упЗаявкаНаТСПлановыеРасходы.Задание,
	|	упЗаявкаНаТСПлановыеРасходы.Цена,
	|	упЗаявкаНаТСПлановыеРасходы.Количество,
	|	упЗаявкаНаТСПлановыеРасходы.Сумма,
	|	упЗаявкаНаТСПлановыеРасходы.СтавкаНДС,
	|	упЗаявкаНаТСПлановыеРасходы.СуммаНДС
	|ИЗ
	|	Документ.упЗаявкаНаТС.ПлановыеРасходы КАК упЗаявкаНаТСПлановыеРасходы
	|ГДЕ
	|	упЗаявкаНаТСПлановыеРасходы.Ссылка = &ЗаявкаНаТС";
	
	Запрос.УстановитьПараметр("ЗаявкаНаТС", ЗаявкаНаТС);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;				  

КонецФункции // ПолучитьСписокСтатейРасходов()

#КонецОбласти

// Процедура - Формирование движений в регистры "упДоходы", "упДоходыРасходыПредприятия".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Набор движений.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//
Процедура СформироватьДоходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияДоходы = Движения.упДоходы;
	ДвиженияДоходы.Записывать = Истина;

	ДвиженияДоходыРасходыПредприятия = Движения.Найти("упДоходыРасходыПредприятия");
	ФормируютсяДвиженияФинРезультат  = ДвиженияДоходыРасходыПредприятия <> Неопределено;
	тбзДоходыРасходыПредприятия      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ДоходыРасходыПредприятия", Неопределено);
	
	Если ДвиженияДоходыРасходыПредприятия <> Неопределено Тогда
		ДвиженияДоходыРасходыПредприятия.Записывать = ФормируютсяДвиженияФинРезультат;	
	КонецЕсли;
	
	тбзДоходы = ДополнительныеСвойства.Доходы;
	
	Для каждого СтрокаДоход Из тбзДоходы Цикл
		
		ДвижениеДоход = ДвиженияДоходы.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеДоход, СтрокаДоход);
		сткСуммы	= СтрокаДоход.сткСуммы;
		ЗаполнитьЗначенияСвойств(ДвижениеДоход, сткСуммы);
		ДвижениеДоход.Период      = Реквизиты.Дата;
		ДвижениеДоход.Регистратор = Ссылка;
		
	КонецЦикла;

	
	Если Истина
		И ФормируютсяДвиженияФинРезультат
		И тбзДоходыРасходыПредприятия <> Неопределено 
	Тогда
			
		Для каждого СтрокаДоход Из тбзДоходыРасходыПредприятия Цикл
			ДвижениеДоход = ДвиженияДоходыРасходыПредприятия.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеДоход, СтрокаДоход);
			ДвижениеДоход.Период      = Реквизиты.Дата;
			ДвижениеДоход.Регистратор = Ссылка;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Формирование движений в регистры "упРасходы", "упРасходыНаТС", "упДоходыРасходыПредприятия".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Набор движений.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//
Процедура СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРасходы = Движения.упРасходы;
	ДвиженияРасходы.Записывать = Истина;

	тбзРаспределениеРасходов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РаспределениеРасходов", Неопределено);;
	тбзРаспределениеРасходовДополнение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РаспределениеРасходовДополнение", Неопределено);
	
	ДополнительныеСвойства.Вставить("РаспределятьРасходыНаРейсы", тбзРаспределениеРасходов = Неопределено);
	
	тбзРаспределениеРасходовНаТС = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РаспределениеРасходовНаТС", Неопределено);
		
	ДвиженияРасходыНаТС = Движения.Найти("упРасходыНаТС");
	ФормируютсяДвиженияРасходыНаТС = ДвиженияРасходыНаТС <> Неопределено;
	
	Если ДвиженияРасходыНаТС <> Неопределено Тогда
		ДвиженияРасходыНаТС.Записывать = Истина;	
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РаспределятьРасходыНаТС", Истина
												    И ФормируютсяДвиженияРасходыНаТС
												    И тбзРаспределениеРасходовНаТС = Неопределено);
	
	
	ДвиженияРасходыНаПредприятие = Движения.Найти("упДоходыРасходыПредприятия");
	ФормируютсяДвиженияФинРезультат = ДвиженияРасходыНаПредприятие <> Неопределено;
	
	Если ДвиженияРасходыНаПредприятие <> Неопределено Тогда
		ДвиженияРасходыНаПредприятие.Записывать = ФормируютсяДвиженияФинРезультат;	
	КонецЕсли;
	
	тбзРаспределениеРасходовНаПредприятие = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РаспределениеРасходовНаПредприятие", Неопределено);;
	ДополнительныеСвойства.Вставить("РаспределятьРасходыНаФинРезультат", Истина
														    И ФормируютсяДвиженияФинРезультат
														    И тбзРаспределениеРасходовНаПредприятие = Неопределено);
															

	
	
	ДополнительныеСвойства.Вставить("СобытиеОшибкаРаспределенияРасходов", СобытиеОшибкаРаспределенияРасходов());
	
	Если НЕ ДополнительныеСвойства.Свойство("ВедетсяВалютныйУчет") Тогда
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет"));
	КонецЕсли;
	
	
		
	ИнициализироватьТаблицыРаспределений(тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, тбзРаспределениеРасходовНаПредприятие);
	
	//*****************************************
	
	СформироватьРаспределениеРасходов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, тбзРаспределениеРасходовНаПредприятие, Отказ);
		
	//*****************************************
	
	СвернутьТаблицыРаспределений(ДополнительныеСвойства, тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, тбзРаспределениеРасходовНаПредприятие, Отказ);

	Для каждого СтрокаРасход Из тбзРаспределениеРасходов Цикл
		
		ДвижениеРасход = ДвиженияРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеРасход, СтрокаРасход);
		сткСуммы	= СтрокаРасход.СткСуммы;
		ЗаполнитьЗначенияСвойств(ДвижениеРасход, сткСуммы);
		ДвижениеРасход.Период		= Реквизиты.Дата;
		ДвижениеРасход.Регистратор	= Ссылка;
		
	КонецЦикла;
	
	Если тбзРаспределениеРасходовДополнение <> Неопределено Тогда
		Для каждого СтрокаРасход Из тбзРаспределениеРасходовДополнение Цикл
			
			ДвижениеРасход = ДвиженияРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеРасход, СтрокаРасход);
			сткСуммы	= СтрокаРасход.СткСуммы;
			ЗаполнитьЗначенияСвойств(ДвижениеРасход, сткСуммы);
			ДвижениеРасход.Период		= Реквизиты.Дата;
			ДвижениеРасход.Регистратор	= Ссылка;
			
		КонецЦикла;
	КонецЕсли;
	
	Если Истина
		И ФормируютсяДвиженияРасходыНаТС
		И тбзРаспределениеРасходовНаТС <> Неопределено
	Тогда
			
		Для каждого СтрокаРасход Из тбзРаспределениеРасходовНаТС Цикл
			
			ДвижениеРасход = ДвиженияРасходыНаТС.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеРасход, СтрокаРасход);
			сткСуммы	= СтрокаРасход.СткСуммы;
			ЗаполнитьЗначенияСвойств(ДвижениеРасход, сткСуммы);
			ДвижениеРасход.Период      = Реквизиты.Дата;
			ДвижениеРасход.Регистратор = Ссылка;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Истина
		И ФормируютсяДвиженияФинРезультат
		И тбзРаспределениеРасходовНаПредприятие <> Неопределено 
	Тогда
			
		Для каждого СтрокаРасход Из тбзРаспределениеРасходовНаПредприятие Цикл
			
			сткСуммы	= СтрокаРасход.СткСуммы;
			
			Если сткСуммы.СуммаФактСНДС = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ДвижениеРасход = ДвиженияРасходыНаПредприятие.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеРасход, СтрокаРасход);
			ДвижениеРасход.Период      = Реквизиты.Дата;
			ДвижениеРасход.Регистратор = Ссылка;
			ДвижениеРасход.СтатьяДоходовРасходов = СтрокаРасход.СтатьяРасходов; 
			ДвижениеРасход.Расходы     = сткСуммы.СуммаФактСНДС; 
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#Область РаспределениеПрочихРасходов

// Процедура - Заполнить прочие расходы
//
// Параметры:
//  Реквизиты	 - Структура	 - содержит значения реквизитов объекта.
//  Отказ		 - Булево		 - Истина, если произошла ошибка.
//
Процедура ЗаполнитьПрочиеРасходы(Реквизиты, Отказ) Экспорт
	ПрочиеРасходы = Реквизиты.ПрочиеРасходы;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПрочиеРасходыОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПрочиеРасходыОстатки.АналитическийРазрез КАК АналитическийРазрез,
	|	упПрочиеРасходыОстатки.СуммаСНДСОстаток - упПрочиеРасходыОстатки.СуммаБезНДСОстаток КАК СуммаНДС,
	|	упПрочиеРасходыОстатки.СуммаСНДСОстаток КАК Сумма,
	|	упПрочиеРасходыОстатки.СуммаВалютаСНДСОстаток - упПрочиеРасходыОстатки.СуммаВалютаБезНДСОстаток КАК СуммаВалютаНДС,
	|	упПрочиеРасходыОстатки.СуммаВалютаСНДСОстаток КАК СуммаВалюта,
	|	упПрочиеРасходыОстатки.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ОпределятьНаправлениеДеятельностиПриРаспределении
	|			ТОГДА ЕСТЬNULL(НаправленияДеятельностиТранспортныхСредств.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиОтбор,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ОпределятьПодразделениеПриРаспределении
	|			ТОГДА ЕСТЬNULL(ПодразделенияТранспортныхСредств.Подразделение, ЗНАЧЕНИЕ(Справочник.упПодразделенияПартнеров.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПодразделенияПартнеров.ПустаяСсылка)
	|	КОНЕЦ КАК ПодразделениеОтбор,
	|	упПрочиеРасходыОстатки.Валюта КАК Валюта
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	РегистрНакопления.упПрочиеРасходы.Остатки(
	|		&Период,
	|		ИСТИНА
	|		И Организация = &Организация
	|		И &Подразделение В (Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка))
	|		И СтатьяРасходов = &СтатьяРасходов
	|		И &АналитическийРазрез В (АналитическийРазрез,
	|		ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка))
	|		И Валюта = &Валюта) КАК упПрочиеРасходыОстатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО упПрочиеРасходыОстатки.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНаправленияДеятельностиТранспортныхСредств.СрезПоследних(
	|			&Период,
	|			) КАК НаправленияДеятельностиТранспортныхСредств
	|		ПО НаправленияДеятельностиТранспортныхСредств.ТранспортноеСредство = упАналитическиеРазрезы.ТранспортноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПодразделенияТранспортныхСредств.СрезПоследних(
	|			&Период,
	|			) КАК ПодразделенияТранспортныхСредств
	|		ПО ПодразделенияТранспортныхСредств.ТранспортноеСредство = упАналитическиеРазрезы.ТранспортноеСредство
	|	ПО упПрочиеРасходыОстатки.АналитическийРазрез = упАналитическиеРазрезы.Ссылка
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеРасходыТ.АналитическийРазрез КАК АналитическийРазрез,
	|	втПрочиеРасходыТ.Валюта КАК Валюта,
	|	втПрочиеРасходыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПрочиеРасходыТ.НаправлениеДеятельностиОтбор КАК НаправлениеДеятельностиОтбор,
	|	втПрочиеРасходыТ.Подразделение КАК Подразделение,
	|	втПрочиеРасходыТ.ПодразделениеОтбор КАК ПодразделениеОтбор,
	|	СУММА(втПрочиеРасходыТ.Сумма) КАК Сумма,
	|	СУММА(втПрочиеРасходыТ.СуммаВалюта) КАК СуммаВалюта,
	|	СУММА(втПрочиеРасходыТ.СуммаВалютаНДС) КАК СуммаВалютаНДС,
	|	СУММА(втПрочиеРасходыТ.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	втПрочиеРасходы КАК втПрочиеРасходыТ
	|СГРУППИРОВАТЬ ПО
	|	втПрочиеРасходыТ.АналитическийРазрез,
	|	втПрочиеРасходыТ.Валюта,
	|	втПрочиеРасходыТ.НаправлениеДеятельности,
	|	втПрочиеРасходыТ.НаправлениеДеятельностиОтбор,
	|	втПрочиеРасходыТ.Подразделение,
	|	втПрочиеРасходыТ.ПодразделениеОтбор
	|";


	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("СтатьяРасходов", Реквизиты.СтатьяРасходов);
	Запрос.УстановитьПараметр("АналитическийРазрез", Реквизиты.АналитическийРазрез);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	Период = Реквизиты.ПериодРаспределенияЗатратОкончание;
	Если ЗначениеЗаполнено(Период) Тогда
		Период = Новый Граница(НачалоДня(КонецДня(Период)+1), ВидГраницы.Исключая);
	КонецЕсли;
	Запрос.УстановитьПараметр("Период", Период);
	
	ПрочиеРасходы.Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Процедура - Распределяет остатки по прочим расходам за период по таблицам "РасходыПоРейсам", "РасходыПоТС", "РезультатыФинансовыйРезультат".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура - дополнительные свойства объекта.
//  Ссылка				 - ДокументСсылка.упРаспределениеПрочихРасходов - ссылка на документ.
//  Реквизиты				 - Структура - содержит значения реквизитов объекта.
//  Отказ					 - Булево    - Истина, если произошла ошибка.
//  сткПараметры               - Структура - сообщения для расшифровки, 
//                               используется при формировании документов из обработки "Закрытие месяца".
//
Процедура РаспределитьПрочиеРасходы(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ, сткПараметры = Неопределено) Экспорт
	
	ПрочиеРасходы                 = Реквизиты.ПрочиеРасходы;
	РасходыПоРейсам               = Реквизиты.РасходыПоРейсам;
	РасходыПоТС                   = Реквизиты.РасходыПоТС;
	РезультатыФинансовыйРезультат = Реквизиты.РезультатыФинансовыйРезультат;
	
	ЗаполнитьРасходыПоРейсам            = ДополнительныеСвойства.ЗаполнитьРасходыПоРейсам;
	ЗаполнитьРасходыПоТС                = ДополнительныеСвойства.ЗаполнитьРасходыПоТС;
	ЗаполнитьРасходыФинансовыйРезультат = ДополнительныеСвойства.ЗаполнитьРасходыФинансовыйРезультат;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилоРаспределения.Ссылка КАК ПравилоРаспределенияКосвенныхРасходов,
	|	ВариантыРаспределения.Ссылка КАК ВариантРаспределения,
	|	ВариантыРаспределения.АлгоритмПроверки КАК АлгоритмПроверки,
	|	ВариантыРаспределения.ПравилоРаспределенияРасходов КАК ПравилоРаспределенияРасходов,
	|	ВариантыРаспределения.ПроизвольныйАлгоритмПроверки КАК ПроизвольныйАлгоритмПроверки,
	|	ВариантыРаспределения.СхемаКомпоновкиДанных КАК СхемаКомпоновкиДанных,
	|	ПравилоРаспределения.НомерСтроки КАК НомерСтроки,
	|	упПравилаРаспределенияРасходов.Формула КАК Формула,
	|	упПравилаРаспределенияРасходов.СпособРаспределения КАК СпособРаспределения,
	|	упПравилаРаспределенияРасходов.Показатель КАК Показатель,
	|	ПравилоРаспределения.БезАналитики КАК БезАналитики,
	|	ПравилоРаспределения.АналитикаАгрегат КАК АналитикаАгрегат,
	|	ПравилоРаспределения.АналитикаВидПеревозки КАК АналитикаВидПеревозки,
	|	ПравилоРаспределения.АналитикаСотрудник КАК АналитикаСотрудник,
	|	ПравилоРаспределения.АналитикаТранспортноеСредство КАК АналитикаТранспортноеСредство
	|ИЗ
	|	Справочник.упПравилаРаспределенияКосвенныхРасходов.ВариантыРаспределения КАК ПравилоРаспределения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВариантыРаспределенияРасходов КАК ВариантыРаспределения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПравилаРаспределенияРасходов КАК упПравилаРаспределенияРасходов
	|			ПО ВариантыРаспределения.ПравилоРаспределенияРасходов = упПравилаРаспределенияРасходов.Ссылка
	|		ПО ПравилоРаспределения.ВариантРаспределенияКосвенныхРасходов = ВариантыРаспределения.Ссылка
	|ГДЕ
	|	ПравилоРаспределения.Ссылка В(&ПравилоРаспределенияКосвенныхРасходов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ ПО
	|	ПравилоРаспределенияКосвенныхРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПрочиеРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	упПрочиеРасходы.Валюта КАК Валюта,
	|	упПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПрочиеРасходы.НаправлениеДеятельностиОтбор КАК НаправлениеДеятельностиОтбор,
	|	упПрочиеРасходы.Подразделение КАК Подразделение,
	|	упПрочиеРасходы.ПодразделениеОтбор КАК ПодразделениеОтбор,
	|	упПрочиеРасходы.Сумма КАК Сумма,
	|	упПрочиеРасходы.СуммаВалюта КАК СуммаВалюта,
	|	упПрочиеРасходы.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	упПрочиеРасходы.Сумма КАК Расходы,
	|	упПрочиеРасходы.СуммаНДС КАК СуммаНДС,
	|	&Организация КАК Организация,
	|	&СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	&ПрочиеРасходы КАК упПрочиеРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПрочиеРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	упПрочиеРасходы.Валюта КАК Валюта,
	|	упПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПрочиеРасходы.НаправлениеДеятельностиОтбор КАК НаправлениеДеятельностиОтбор,
	|	упПрочиеРасходы.Подразделение КАК Подразделение,
	|	упПрочиеРасходы.ПодразделениеОтбор КАК ПодразделениеОтбор,
	|	упПрочиеРасходы.Сумма КАК Сумма,
	|	упПрочиеРасходы.СуммаВалюта КАК СуммаВалюта,
	|	упПрочиеРасходы.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	упПрочиеРасходы.Расходы КАК Расходы,
	|	упПрочиеРасходы.СуммаНДС КАК СуммаНДС,
	|	упПрочиеРасходы.Организация КАК Организация,
	|	упПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов1,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Сотрудник, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК СотрудникАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ВидПеревозки, ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)) КАК ВидПеревозкиАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Агрегат, ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)) КАК АгрегатАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредствоАР,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	упСтатьиДоходовРасходов.Ссылка КАК СтатьяРасходов
	|ИЗ
	|	втПрочиеРасходы КАК упПрочиеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|		ПО упПрочиеРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|		ПО упПрочиеРасходы.АналитическийРазрез = упАналитическиеРазрезы.Ссылка";
     
	
	мсвПравилаРаспределенияКосвенныхРасходов = Новый Массив;
	Если ЗаполнитьРасходыПоРейсам Тогда
		мсвПравилаРаспределенияКосвенныхРасходов.Добавить(Реквизиты.ПравилоРаспределенияПоРейсам);	
	КонецЕсли;
	
	Если ЗаполнитьРасходыПоТС Тогда
		мсвПравилаРаспределенияКосвенныхРасходов.Добавить(Реквизиты.ПравилоРаспределенияПоТС);
	КонецЕсли;
	
	Если ЗаполнитьРасходыФинансовыйРезультат Тогда
		мсвПравилаРаспределенияКосвенныхРасходов.Добавить(Реквизиты.ПравилоРаспределенияНаФинансовыйРезультат);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПравилоРаспределенияКосвенныхРасходов", мсвПравилаРаспределенияКосвенныхРасходов);
	Запрос.УстановитьПараметр("ПрочиеРасходы",  ПрочиеРасходы);
	Запрос.УстановитьПараметр("Организация",    Реквизиты.Организация);
	Запрос.УстановитьПараметр("СтатьяРасходов", Реквизиты.СтатьяРасходов);
		
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексОстаткиПрочиеРасходы = 2;
	ИндексПравилаРаспределния  = 0;
	
	тбзОстаткиПрочиеРасходы = мсвРезультатЗапроса[ИндексОстаткиПрочиеРасходы].Выгрузить();
			
	Если ЗаполнитьРасходыПоТС Тогда
		РасходыПоТС.Очистить();
	КонецЕсли;

	Если ЗаполнитьРасходыФинансовыйРезультат Тогда
		РезультатыФинансовыйРезультат.Очистить();
	КонецЕсли;
	
	сткРеквизитыАналитическогоРазреза = Новый Структура();
	Если ЗначениеЗаполнено(Реквизиты.АналитическийРазрез) Тогда
		сткРеквизитыАналитическогоРазреза = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.АналитическийРазрез, "Сотрудник, Агрегат, ТранспортноеСредство, ВидПеревозки");
	КонецЕсли;
	
	// 1. Для каждого правила для всех его вариантов распределения 
	//    формируются базовые таблицы по СКД с наложенными отборами 
	//    или по произвольному запросу(свособ зависит от настроек 
	//    варианта распределения косвенных расходов).
	//
	//    Результат сохраняется в соответствие по следующим правилам:
	//    соотПравилаРаспределения - Соответствие 
	//      * Ключ     - СправочникСсылка.упПравилаРаспределенияКосвенныхРасходов - правило распределения косвенных расходов.
	//      * Значение - Массив                                                   - содержит структуры со свойствами:
	//         ** ИспользуетсяОтборПоАналитическомуРазрезу - Булево - использовать или нет в варианте распределения отбор по аналитике.
	//         ** БазаРаспределения   - ТаблицаЗначений                                 - базовая таблица для распределения.
	//         ** ПравилоРапределения - СправочникСсылка.упПравилаРаспределенияРасходов - правило распределения расходов по базовой таблице.
	//
	соотПравилаРаспределения = Новый Соответствие;
	ВыборкаПравилоРаспределенияКосвенныхРасходов = мсвРезультатЗапроса[ИндексПравилаРаспределния].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПравилоРаспределенияКосвенныхРасходов.Следующий() Цикл
		
		ПравилоРаспределенияКосвенныхРасходов = ВыборкаПравилоРаспределенияКосвенныхРасходов.ПравилоРаспределенияКосвенныхРасходов;
		
		ВыборкаВариантРаспределенияРасходов   = ВыборкаПравилоРаспределенияКосвенныхРасходов.Выбрать();
		
		мсвБазовыеТаблицы = Новый Массив;
		
		Пока ВыборкаВариантРаспределенияРасходов.Следующий() Цикл
			
			сткВариантРаспределения = ИнициализироватьПараметрыВариантаРаспределения();
			ЗаполнитьЗначенияСвойств(сткВариантРаспределения, ВыборкаВариантРаспределенияРасходов);
			ЗаполнитьБазуРаспределенияРасходов(мсвБазовыеТаблицы, сткВариантРаспределения, Реквизиты, сткРеквизитыАналитическогоРазреза);
								
		КонецЦикла;
		
		соотПравилаРаспределения.Вставить(ПравилоРаспределенияКосвенныхРасходов, мсвБазовыеТаблицы);
		
	КонецЦикла;
	
	Если ЗаполнитьРасходыПоРейсам Тогда
								
		Если ЗначениеЗаполнено(Реквизиты.ПравилоРаспределенияПоРейсам) Тогда
			
			РасходыПоРейсам.Очистить();
			тбзРасходыПоРейсам = РасходыПоРейсам.Выгрузить();
			
			мсвБазовыеТаблицы = соотПравилаРаспределения.Получить(Реквизиты.ПравилоРаспределенияПоРейсам);
	          ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвБазовыеТаблицы, тбзОстаткиПрочиеРасходы, тбзРасходыПоРейсам, "БазаРаспределенияПоРейсам", Истина, Отказ);
			
			тбзРасходыПоРейсам.Свернуть("АналитическийРазрез, ПравилоРаспределения, Рейс, СтатьяРасходов, ВариантРаспределения", 
									"Сумма, СуммаВалюта, СуммаВалютаНДС, СуммаНДС");
			РасходыПоРейсам.Загрузить(тбзРасходыПоРейсам);
		Иначе	
			
			Если сткПараметры = Неопределено Тогда
				ТекстСообщения = Нстр("ru = 'Поле ""Правило распределения"" не заполнено'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ПравилоРаспределенияПоРейсам","Объект",Отказ);
			Иначе
				ТекстСообщения = Нстр("ru = 'Правило распределения по рейсам для статьи расходов:""%1"" не заполнено'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Реквизиты.СтатьяРасходов);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткПараметры,,"Ошибка",,ТекстСообщения,1);
			КонецЕсли;
			
		КонецЕсли;
					
	КонецЕсли;
	
	Если ЗаполнитьРасходыПоТС Тогда
		
		Если ЗначениеЗаполнено(Реквизиты.ПравилоРаспределенияПоТС) Тогда
			
			тбзРасходыПоТС = РасходыПоТС.ВыгрузитьКолонки();
			
			мсвБазовыеТаблицы = соотПравилаРаспределения.Получить(Реквизиты.ПравилоРаспределенияПоТС);
			
			ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвБазовыеТаблицы, тбзОстаткиПрочиеРасходы, тбзРасходыПоТС, "БазаРаспределенияПоТС", Истина, Отказ);
			
			тбзРасходыПоТС.Свернуть("АналитическийРазрез, ТранспортноеСредство, СтатьяРасходов, ВариантРаспределения", 
									"Сумма, СуммаВалюта, СуммаВалютаНДС, СуммаНДС");
			
			РасходыПоТС.Загрузить(тбзРасходыПоТС);
			
		Иначе	
				
			Если сткПараметры = Неопределено Тогда
				ТекстСообщения = Нстр("ru = 'Поле ""Правило распределения"" не заполнено'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ПравилоРаспределенияПоТС","Объект",Отказ);
			Иначе
				ТекстСообщения = Нстр("ru = 'Правило распределения по транспортным средствам для статьи расходов:""%1"" не заполнено'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Реквизиты.СтатьяРасходов);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткПараметры,,"Ошибка",,ТекстСообщения,1);
			КонецЕсли;

						
		КонецЕсли;
					
	КонецЕсли;
	
	Если ЗаполнитьРасходыФинансовыйРезультат Тогда
		                                				
		Если ЗначениеЗаполнено(Реквизиты.ПравилоРаспределенияНаФинансовыйРезультат) Тогда
			
			тбзРезультатыФинансовыйРезультат = РезультатыФинансовыйРезультат.ВыгрузитьКолонки();
			
			мсвБазовыеТаблицы = соотПравилаРаспределения.Получить(Реквизиты.ПравилоРаспределенияНаФинансовыйРезультат);
	          ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвБазовыеТаблицы, тбзОстаткиПрочиеРасходы, тбзРезультатыФинансовыйРезультат, "БазаРаспределенияПоФинансовомуРезультату", Истина, Отказ);
			
			тбзРезультатыФинансовыйРезультат.Свернуть("АналитическийРазрез, НаправлениеДеятельности, Организация, Подразделение, СтатьяРасходов, ВариантРаспределения","Расходы");
			РезультатыФинансовыйРезультат.Загрузить(тбзРезультатыФинансовыйРезультат);
			
		Иначе	
	
			Если сткПараметры = Неопределено Тогда
				ТекстСообщения = Нстр("ru = 'Поле ""Правило распределения"" не заполнено'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ПравилоРаспределенияНаФинансовыйРезультат","Объект",Отказ);
			Иначе
                    ТекстСообщения = Нстр("ru = 'Правило распределения на финансовый результат для статьи расходов:""%1"" не заполнено'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Реквизиты.СтатьяРасходов);
				упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткПараметры,,"Ошибка",,ТекстСообщения,1);
			КонецЕсли;
					
		КонецЕсли;
					
	КонецЕсли;
			
КонецПроцедуры

Процедура ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвПоказатели, тбзРасходы, тбзРаспределениеРасходов, ИмяТаблицыБазыРаспределения, ЕстьОпцияОтключитьПроверкуАналитическогоРазреза = Ложь, Отказ)
	
	Если тбзРасходы.Колонки.Найти("сткСуммы") = Неопределено Тогда
		тбзРасходы.Колонки.Добавить("сткСуммы");	
	КонецЕсли;
		
	// Для каждой строки остатков прочих расходов.
	Для каждого СтрокаРасход Из тбзРасходы Цикл
		
		сткСумма = ШаблонСуммДляРаспределенияПрочихРасходовПоРейсам();
		ЗаполнитьЗначенияСвойств(сткСумма, СтрокаРасход);
		СтрокаРасход.сткСуммы = сткСумма;
		
		сткСтрокаРасход = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаРасход);
		
		АналитическийРазрезСтрокаРасход    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "АналитическийРазрез");
		ТранспортноеСредствоАРСтрокаРасход = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "ТранспортноеСредствоАР");
		СотрудникАРСтрокаРасход            = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "СотрудникАР");
		ВидПеревозкиАРСтрокаРасход         = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "ВидПеревозкиАР");
		АгрегатАРСтрокаРасход              = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "АгрегатАР");
		ОрганизацияСтрокаРасход 	          = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "Организация");
		ПодразделениеСтрокаРасход          = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "Подразделение");
		НаправлениеДеятельностиСтрокаРасход = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "НаправлениеДеятельности");
		СтатьяРасходовСтрокаРасход          = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "СтатьяРасходов");
		НаправлениеДеятельностиОтборСтрокаРасход = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "НаправлениеДеятельностиОтбор");
		ПодразделениеОтборСтрокаРасход      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткСтрокаРасход, "ПодразделениеОтбор");
		
		ВыполненоРаспределение = Ложь;
		
		// Для каждого способа отбора
		Для каждого ЭлементБазоваяТаблица Из мсвПоказатели Цикл
			
			Если Истина
				И ЕстьОпцияОтключитьПроверкуАналитическогоРазреза 
				И ПолучитьФункциональнуюОпцию("упИспользоватьДопАналитическийРазрезПриРаспределенииРасходов") Тогда
				
				БезАналитики                  = ЭлементБазоваяТаблица.БезАналитики;
				АналитикаТранспортноеСредство = ЭлементБазоваяТаблица.АналитикаТранспортноеСредство;
				АналитикаВидПеревозки         = ЭлементБазоваяТаблица.АналитикаВидПеревозки;
				АналитикаАгрегат              = ЭлементБазоваяТаблица.АналитикаАгрегат;
				АналитикаСотрудник            = ЭлементБазоваяТаблица.АналитикаСотрудник;
				
				ЭтоПодходящийАналитическийРазрез = Ложь
							Или БезАналитики И НЕ ЗначениеЗаполнено(АналитическийРазрезСтрокаРасход)
							Или АналитикаТранспортноеСредство И ЗначениеЗаполнено(ТранспортноеСредствоАРСтрокаРасход)
							Или АналитикаСотрудник И ЗначениеЗаполнено(СотрудникАРСтрокаРасход)
							Или АналитикаВидПеревозки И ЗначениеЗаполнено(ВидПеревозкиАРСтрокаРасход)
							Или АналитикаАгрегат И ЗначениеЗаполнено(АгрегатАРСтрокаРасход);
							
				Если НЕ ЭтоПодходящийАналитическийРазрез Тогда
					Продолжить;
				КонецЕсли;			
			КонецЕсли;
			
			ВариантРаспределения = ЭлементБазоваяТаблица.ВариантРаспределения;
			БазаРаспределения    = ЭлементБазоваяТаблица[ИмяТаблицыБазыРаспределения];
			ИспользуетсяОтборПоАналитическомуРазрезу = ЭлементБазоваяТаблица.ИспользуетсяОтборПоАналитическомуРазрезу;
			ИспользуетсяОтборПоНаправлениюДеятельности = Истина 
												И ЭлементБазоваяТаблица.ИспользуетсяОтборПоНаправлениюДеятельности
												И (Ложь
													Или НаправлениеДеятельностиСтрокаРасход <> Неопределено
													Или НаправлениеДеятельностиОтборСтрокаРасход <> Неопределено);
			ИспользуетсяОтборПоПодразделению = Истина
										И ЭлементБазоваяТаблица.ИспользуетсяОтборПоПодразделению
										И (Ложь
											Или ПодразделениеСтрокаРасход <> Неопределено
											Или ПодразделениеОтборСтрокаРасход <> Неопределено);
		
			// Формируется таблица рейсов на основании показателя из
			// варианта распределения и соответствующего показателя таблицы. 		
							
			тбзБазаРаспределения = Неопределено;
			
			сткОтбора = Новый Структура();
			
			Если ИспользуетсяОтборПоАналитическомуРазрезу Тогда
				
				// Таблица формируется путем наложения отбора по аналитическому разрезу.
				// (Если аналитический разрез заполнен).
				Если ЗначениеЗаполнено(АналитическийРазрезСтрокаРасход) Тогда
					Если ЗначениеЗаполнено(ТранспортноеСредствоАРСтрокаРасход) Тогда
						сткОтбора.Вставить("ТранспортноеСредство", ТранспортноеСредствоАРСтрокаРасход);// Транспортное средство или прицеп.
						
					ИначеЕсли ЗначениеЗаполнено(ВидПеревозкиАРСтрокаРасход) Тогда
						сткОтбора.Вставить("ВидПеревозки", ВидПеревозкиАРСтрокаРасход);
						сткОтбора.Вставить("ЭтоПрицеп", Ложь); // Не учитывать строки по прицепам, они дублируют строку с основным ТС.
					ИначеЕсли ЗначениеЗаполнено(СотрудникАРСтрокаРасход) Тогда
						сткОтбора.Вставить("Сотрудник", СотрудникАРСтрокаРасход);
						сткОтбора.Вставить("ЭтоПрицеп", Ложь); // Не учитывать строки по прицепам, они дублируют строку с основным ТС.
						//ИначеЕсли ЗначениеЗаполнено(Строка.Агрегат) Тогда
						//   сткОтбора.Вставить("Агрегат", СтрокаРасход.Агрегат);
						
					КонецЕсли;
				Иначе
					ТекстСообщения = Нстр("ru = 'Вариант распределения:%1 не был применен для распределения прочих расходов,
										    | потому что в варианте распределения установлен отбор по аналитическому разрезу, а в прочих расходах разрез не заполнен
										    |(Организация:""%2""; 
										    | Подразделение:""%3""; 
										    | НаправлениеДеятельности:""%4""; 
										    | СтатьяРасходов:""%5"").'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
																			   ВариантРаспределения, 
																			   ОрганизацияСтрокаРасход, 
																			   ПодразделениеСтрокаРасход,
																			   НаправлениеДеятельностиОтборСтрокаРасход,
																			   СтатьяРасходовСтрокаРасход);
					ЗаписьЖурналаРегистрации(СобытиеОшибкаРаспределенияРасходов(),УровеньЖурналаРегистрации.Информация,,,ТекстСообщения);
					Продолжить;
				КонецЕсли;
			Иначе
				сткОтбора.Вставить("ЭтоПрицеп", Ложь); // Не учитывать строки по прицепам, они дублируют строку с основным ТС
			КонецЕсли;
			
			Если ИспользуетсяОтборПоНаправлениюДеятельности Тогда
								
				Если ЗначениеЗаполнено(НаправлениеДеятельностиОтборСтрокаРасход) Тогда
				     сткОтбора.Вставить("НаправлениеДеятельности", НаправлениеДеятельностиОтборСтрокаРасход);
				ИначеЕсли ЗначениеЗаполнено(НаправлениеДеятельностиСтрокаРасход) Тогда
					сткОтбора.Вставить("НаправлениеДеятельности", НаправлениеДеятельностиСтрокаРасход);
				Иначе
					ТекстСообщения = Нстр("ru = 'Вариант распределения:%1 не был применен для распределения прочих расходов,
										    | потому что в варианте распределения установлен отбор по направлению деятельности, а в прочих расходах направление не заполнено
										    |(Организация:""%2""; 
										    | Подразделение:""%3""; 
										    | НаправлениеДеятельности:""%4""; 
										    | СтатьяРасходов:""%5"").'");

					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
																			   ВариантРаспределения, 
																			   ОрганизацияСтрокаРасход, 
																			   ПодразделениеСтрокаРасход,
																			   НаправлениеДеятельностиОтборСтрокаРасход,
																			   СтатьяРасходовСтрокаРасход);

					ЗаписьЖурналаРегистрации(СобытиеОшибкаРаспределенияРасходов(),УровеньЖурналаРегистрации.Информация,,,ТекстСообщения);
					Продолжить;

				КонецЕсли;
			КонецЕсли;
			
			Если ИспользуетсяОтборПоПодразделению Тогда
				
				Если ЗначениеЗаполнено(СтрокаРасход.ПодразделениеОтбор) Тогда
				     сткОтбора.Вставить("Подразделение", ПодразделениеОтборСтрокаРасход);
				ИначеЕсли ЗначениеЗаполнено(СтрокаРасход.Подразделение) Тогда
					сткОтбора.Вставить("Подразделение", ПодразделениеСтрокаРасход);
				Иначе
					ТекстСообщения = Нстр("ru = 'Вариант распределения:%1 не был применен для распределения прочих расходов,
										    | потому что в варианте распределения установлен отбор по подразделению, а в прочих расходах подразделение не заполнено
										    |(Организация:""%2""; 
										    | Подразделение:""%3""; 
										    | НаправлениеДеятельности:""%4""; 
										    | СтатьяРасходов:""%5"").'");

					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
																			   ВариантРаспределения, 
																			   ОрганизацияСтрокаРасход, 
																			   ПодразделениеСтрокаРасход,
																			   НаправлениеДеятельностиОтборСтрокаРасход,
																			   СтатьяРасходовСтрокаРасход);
					ЗаписьЖурналаРегистрации(СобытиеОшибкаРаспределенияРасходов(),УровеньЖурналаРегистрации.Информация,,,ТекстСообщения);
					Продолжить;

				КонецЕсли;
			КонецЕсли;

			Если сткОтбора.Количество() > 0 Тогда
				мсвСтрокиБазы = БазаРаспределения.НайтиСтроки(сткОтбора);
				Если Истина
					И мсвСтрокиБазы <> Неопределено 
					И мсвСтрокиБазы.Количество() > 0
				Тогда
					тбзБазаРаспределения = БазаРаспределения.СкопироватьКолонки();
					Для каждого СтрокаБазы Из мсвСтрокиБазы Цикл
						НоваяСтрокаБазы = тбзБазаРаспределения.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаБазы, СтрокаБазы);
					КонецЦикла;
				Иначе
					Продолжить;
				КонецЕсли;
			Иначе
				тбзБазаРаспределения = БазаРаспределения.Скопировать();
			КонецЕсли;
						
			Если Истина
				И тбзБазаРаспределения <> Неопределено
				И тбзБазаРаспределения.Количество() > 0 
			Тогда
			
			     ВыполненоРаспределение = Истина;
			
				Если ЗначениеЗаполнено(ТранспортноеСредствоАРСтрокаРасход) Тогда
								
					Если ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоФинансовомуРезультату" Тогда
						тбзБазаРаспределения.Свернуть("ТранспортноеСредство, НаправлениеДеятельности, Подразделение", КолонкиПоказателейРаспределения());
					ИначеЕсли	ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоТС" Тогда
						тбзБазаРаспределения.Свернуть("ТранспортноеСредство", КолонкиПоказателейРаспределения());
					КонецЕсли;
					
				ИначеЕсли ЗначениеЗаполнено(ВидПеревозкиАРСтрокаРасход) Тогда
					
					Если ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоФинансовомуРезультату" Тогда
						тбзБазаРаспределения.Свернуть("ВидПеревозки, НаправлениеДеятельности, Подразделение", КолонкиПоказателейРаспределения());
					ИначеЕсли	ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоТС" Тогда
						тбзБазаРаспределения.Свернуть("ВидПеревозки, ТранспортноеСредство", КолонкиПоказателейРаспределения());
					КонецЕсли;

				ИначеЕсли ЗначениеЗаполнено(СотрудникАРСтрокаРасход) Тогда
										
					Если ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоФинансовомуРезультату" Тогда
						тбзБазаРаспределения.Свернуть("Сотрудник, НаправлениеДеятельности, Подразделение", КолонкиПоказателейРаспределения());
					ИначеЕсли	ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоТС" Тогда
						тбзБазаРаспределения.Свернуть("Сотрудник, ТранспортноеСредство", КолонкиПоказателейРаспределения());
					КонецЕсли;
					
				Иначе
					
					Если ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоФинансовомуРезультату" Тогда
						тбзБазаРаспределения.Свернуть("НаправлениеДеятельности, Организация, Подразделение", КолонкиПоказателейРаспределения());
					ИначеЕсли	ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоТС" Тогда
						тбзБазаРаспределения.Свернуть("ТранспортноеСредство", КолонкиПоказателейРаспределения());
					КонецЕсли;

				//ИначеЕсли ЗначениеЗаполнено(Строка.Агрегат) Тогда
				КонецЕсли;
								
			     тбзБазаРаспределения.Колонки.Добавить("Коэффициент");
				тбзБазаРаспределения.Колонки.Добавить("сткСуммы");
			
				// По формуле рассчитываются коэффициенты.
				//РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(ЭлементБазоваяТаблица.СпособРаспределения, ЭлементБазоваяТаблица.Показатель, ЭлементБазоваяТаблица.Формула, тбзБазаРаспределения, Новый Структура("Рейс"));
				РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(ЭлементБазоваяТаблица.СпособРаспределения, ЭлементБазоваяТаблица.Показатель, ЭлементБазоваяТаблица.Формула, тбзБазаРаспределения);
				
				мсвСтрокиСНулевымКоэффициентом = тбзБазаРаспределения.НайтиСтроки(Новый Структура("Коэффициент", 0));
				Для каждого СтрокаСНулевымКоэф Из мсвСтрокиСНулевымКоэффициентом Цикл
					тбзБазаРаспределения.Удалить(СтрокаСНулевымКоэф);
				КонецЦикла;
				
				сткСуммыНакопительная = ШаблонСуммДляРаспределенияПрочихРасходовПоРейсам();// Накопительная сумма по мере обхода.
				КоличествоСтрок       = тбзБазаРаспределения.Количество();
				й_Шаг = 1; 
				
				сткСуммыПоСтатьеРасходов	= СтрокаРасход.сткСуммы; 
				
				// Сумма расходов распределяется по базе.
				Для каждого СтрокаБазы Из тбзБазаРаспределения Цикл

					// Если распределяется сумма по последнему ТС.
					Если й_Шаг = КоличествоСтрок Тогда
						СтрокаБазы.сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(сткСуммыПоСтатьеРасходов, сткСуммыНакопительная, Отказ); // Берется остаток.
					Иначе
						Если СтрокаБазы.Коэффициент = 0 Тогда
							Продолжить;
						КонецЕсли;
						СтрокаБазы.сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(сткСуммыПоСтатьеРасходов, СтрокаБазы.Коэффициент); // Сумма распределения умножается на коэффициент.
					КонецЕсли;
					сткСуммыНакопительная = упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыНакопительная, СтрокаБазы.сткСуммы, Отказ);
					
					СтрокаРаспределение = тбзРаспределениеРасходов.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаРасход);
					ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаБазы);
					
					Если ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоФинансовомуРезультату" Тогда
						СтрокаРаспределение.Расходы = СтрокаБазы.сткСуммы.Сумма;						
					Иначе
						ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаБазы.сткСуммы);
					КонецЕсли;
					
					СтрокаРаспределение.ВариантРаспределения = ВариантРаспределения;
					
					й_Шаг = й_Шаг + 1;
										
				КонецЦикла;	
				
				// В случае если найдена база распределения, дальнейшие варианты перебирать не надо.
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Если не удалось подобрать базу рейсов для строки расходов.
		Если НЕ ВыполненоРаспределение Тогда
			
			СтрокаРаспределение = тбзРаспределениеРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаРасход);
				
			Если ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоФинансовомуРезультату" Тогда
				СтрокаРаспределение.Расходы = СтрокаРасход.сткСуммы.Сумма;						
				Если ЗначениеЗаполнено(НаправлениеДеятельностиОтборСтрокаРасход) Тогда
					СтрокаРаспределение.НаправлениеДеятельности = НаправлениеДеятельностиОтборСтрокаРасход;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПодразделениеОтборСтрокаРасход) Тогда
					СтрокаРаспределение.Подразделение = ПодразделениеОтборСтрокаРасход;
				КонецЕсли;
			ИначеЕсли ИмяТаблицыБазыРаспределения = "БазаРаспределенияПоТС" Тогда	
				ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаРасход);
				Если Истина
					 И НЕ ЗначениеЗаполнено(СтрокаРаспределение.ТранспортноеСредство)
					 И ЗначениеЗаполнено(ТранспортноеСредствоАРСтрокаРасход)
				Тогда
				     СтрокаРаспределение.ТранспортноеСредство = ТранспортноеСредствоАРСтрокаРасход;
				КонецЕсли;		
	
			Иначе
				ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаРасход);	
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИнициализироватьПараметрыВариантаРаспределения()
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("АлгоритмПроверки",             Неопределено);
	сткРезультат.Вставить("СхемаКомпоновкиДанных",        Неопределено);
	сткРезультат.Вставить("ПроизвольныйАлгоритмПроверки", Неопределено);
	сткРезультат.Вставить("ВариантРаспределения",         Неопределено);
	сткРезультат.Вставить("БезАналитики",                 Неопределено);
	сткРезультат.Вставить("АналитикаТранспортноеСредство", Неопределено);
	сткРезультат.Вставить("АналитикаСотрудник",           Неопределено);
	сткРезультат.Вставить("АналитикаАгрегат",             Неопределено);
	сткРезультат.Вставить("АналитикаВидПеревозки",        Неопределено);
	сткРезультат.Вставить("ПравилоРаспределенияРасходов", Неопределено);
	сткРезультат.Вставить("Формула",                      Неопределено);
	сткРезультат.Вставить("СпособРаспределения",          Неопределено);
	сткРезультат.Вставить("Показатель",                   Неопределено);
	
	Возврат сткРезультат;
		
КонецФункции

Процедура ЗаполнитьБазуРаспределенияРасходов(мсвБазовыеТаблицы, сткПараметрыВарианта, Реквизиты, сткРеквизитыАналитическогоРазреза = Неопределено) 
									
	АлгоритмПроверки = сткПараметрыВарианта.АлгоритмПроверки;
	Результат = Неопределено;
									
	Если АлгоритмПроверки = Перечисления.упАлгоритмыПроверкиСобытия.НастройкаОтбора Тогда 
		
		СхемаКомпоновкиДанных = сткПараметрыВарианта.СхемаКомпоновкиДанных.Получить();
		Параметры             = СхемаКомпоновкиДанных.НастройкиПоУмолчанию.ПараметрыДанных;
		
		ПараметрНачало                 = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодРаспределенияНачало"));
		ПараметрНачало.Значение        = НачалоДня(Реквизиты.ПериодРаспределенияЗатратНачало);
		
		ПараметрОкончание              = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодРаспределенияОкончание"));
		ПараметрОкончание.Значение     = КонецДня(Реквизиты.ПериодРаспределенияЗатратОкончание);
		
		ПараметрОрганизация            = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
		ПараметрОрганизация.Значение   = Реквизиты.Организация;
		
		ПараметрНаправлениеДеятельности            = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользуетсяОтборПоНаправлениюДеятельности"));
		ИспользуетсяОтборПоНаправлениюДеятельности = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПоНаправлениямДеятельности")
		                                             И ПараметрНаправлениеДеятельности.Использование;		
		ПараметрПодразделение            = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользуетсяОтборПоПодразделению"));
		ИспользуетсяОтборПоПодразделению = ПараметрПодразделение.Использование;
		
		ПараметрАналитическийРазрез    = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользуетсяОтборПоАналитическомуРазрезу"));
		ИспользуетсяОтборПоАналитическомуРазрезу = ПараметрАналитическийРазрез.Использование;
		
		Если Истина
			И сткРеквизитыАналитическогоРазреза <> Неопределено
			И ИспользуетсяОтборПоАналитическомуРазрезу
			И ЗначениеЗаполнено(Реквизиты.АналитическийРазрез) 
		Тогда
			Если Истина
				//И сткРеквизитыСтатьиРасходов.Сотрудник
				И ЗначениеЗаполнено(сткРеквизитыАналитическогоРазреза.Сотрудник)	Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Сотрудник", сткРеквизитыАналитическогоРазреза.Сотрудник, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
				
			ИначеЕсли Истина
				//И сткРеквизитыСтатьиРасходов.ВидПеревозки
				И ЗначениеЗаполнено(сткРеквизитыАналитическогоРазреза.ВидПеревозки) Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "ВидПеревозки", сткРеквизитыАналитическогоРазреза.ВидПеревозки, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
				
			ИначеЕсли Истина
				//И сткРеквизитыСтатьиРасходов.ТранспортноеСредство
				И ЗначениеЗаполнено(сткРеквизитыАналитическогоРазреза.ТранспортноеСредство) Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "ТранспортноеСредство", сткРеквизитыАналитическогоРазреза.ТранспортноеСредство, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
				
				
				// Заккоментировано, потому что пока по рейсу не определяется агрегат	
				//ИначеЕсли Истина
				//	И сткРеквизитыСтатьиРасходов.Агрегат
				//	И ЗначениеЗаполнено(сткРеквизитыАналитическогоРазреза.Агрегат)	Тогда
				//	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Агрегат", сткРеквизитыАналитическогоРазреза.Агрегат, ВидСравненияКомпоновкиДанных.Равно,, Истина);		
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Истина
			И ИспользуетсяОтборПоНаправлениюДеятельности
			И ЗначениеЗаполнено(Реквизиты.НаправлениеДеятельности) 
			Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "НаправлениеДеятельности", Реквизиты.НаправлениеДеятельности, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
		КонецЕсли;
		
		Если Истина
			И ИспользуетсяОтборПоПодразделению
			И ЗначениеЗаполнено(Реквизиты.Подразделение) 
			Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Подразделение", Реквизиты.Подразделение, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
		КонецЕсли;
		
		// Таблица значений, в которую будет получен результат.
		Результат = упСервисныеФункции.ВыполнитьСКДвТЗ(СхемаКомпоновкиДанных);
		
		// Если в настройках отключено использование направления деятельности, то колонки "Направления деятельности" не будет
		Если Результат.Колонки.Найти("НаправлениеДеятельности") = Неопределено Тогда
			НоваяКолонка = Результат.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.упНаправленияДеятельности"));
			Результат.ЗаполнитьЗначения("НаправлениеДеятельности", Справочники.упНаправленияДеятельности.ПустаяСсылка());
		КонецЕсли;
		
	Иначе
		
		упСобытия.ПроверкаПроизвольногоАлгоритма(сткПараметрыВарианта.ПроизвольныйАлгоритмПроверки, Реквизиты, Результат);
		
	КонецЕсли;
	
	Если Истина
		И ТипЗнч(Результат) = Тип("ТаблицаЗначений")
		И Результат.Количество() > 0 
	Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаБазаДляРаспределенияРасходов();
		Запрос.УстановитьПараметр("Результат",   Результат);
		Запрос.УстановитьПараметр("Валюта",      Реквизиты.Валюта);
		Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
		
		мсвРезультаты = Запрос.ВыполнитьПакет();
		ИндексРейсы	           = 13;
		ИндексТС                  = 14;					
		ИндексФинансовыйРезультат = 15;
		
		тбзРейсы               = мсвРезультаты[ИндексРейсы].Выгрузить();
		тбзТС                  = мсвРезультаты[ИндексТС].Выгрузить();
		тбзФинансовыйРезультат = мсвРезультаты[ИндексФинансовыйРезультат].Выгрузить();
		
		сткБазоваяТаблица = Новый Структура();
		сткБазоваяТаблица.Вставить("ИспользуетсяОтборПоАналитическомуРазрезу",   ИспользуетсяОтборПоАналитическомуРазрезу);
		сткБазоваяТаблица.Вставить("ИспользуетсяОтборПоНаправлениюДеятельности", ИспользуетсяОтборПоНаправлениюДеятельности);
		сткБазоваяТаблица.Вставить("ИспользуетсяОтборПоПодразделению",           ИспользуетсяОтборПоПодразделению);
		сткБазоваяТаблица.Вставить("БазаРаспределенияПоРейсам",                тбзРейсы);
		сткБазоваяТаблица.Вставить("БазаРаспределенияПоТС",                    тбзТС);
		сткБазоваяТаблица.Вставить("БазаРаспределенияПоФинансовомуРезультату", тбзФинансовыйРезультат);
		
		сткБазоваяТаблица.Вставить("ВариантРаспределения",          сткПараметрыВарианта.ВариантРаспределения);
		
		сткБазоваяТаблица.Вставить("БезАналитики",                  сткПараметрыВарианта.БезАналитики);
		сткБазоваяТаблица.Вставить("АналитикаТранспортноеСредство", сткПараметрыВарианта.АналитикаТранспортноеСредство);
		сткБазоваяТаблица.Вставить("АналитикаСотрудник",            сткПараметрыВарианта.АналитикаСотрудник);
		сткБазоваяТаблица.Вставить("АналитикаАгрегат",              сткПараметрыВарианта.АналитикаАгрегат);
		сткБазоваяТаблица.Вставить("АналитикаВидПеревозки",         сткПараметрыВарианта.АналитикаВидПеревозки);
		
		сткБазоваяТаблица.Вставить("ПравилоРапределения",           сткПараметрыВарианта.ПравилоРаспределенияРасходов);
		сткБазоваяТаблица.Вставить("Формула",                       сткПараметрыВарианта.Формула);
		сткБазоваяТаблица.Вставить("СпособРаспределения",           сткПараметрыВарианта.СпособРаспределения);
		сткБазоваяТаблица.Вставить("Показатель",                    сткПараметрыВарианта.Показатель);
		
		мсвБазовыеТаблицы.Добавить(сткБазоваяТаблица);
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область РаспределениеРасходовБудщихПериодов

// Процедура - Заполнить расходы будущих периодов, прочими расходами.
//
// Параметры:
//  Реквизиты - Структура - Содержит значения реквизитов объекта.
//  мсвПрочиеРасходы - Массив - Содержит структуры-строки параметров отбора из остатков прочих расходов.
//     * Организация    - СправочникСсылка.Организации             - Организация.
//     * Подразделение  - СправочникСсылка.упСтруктураПредприятия  - Подразделение.
//     * СтатьяРасходов - СправочникСсылка.упСтатьиДоходовРасходов - Статья расходов.
//     * Валюта         - СправочникСсылка.Валюты                  - Валюта.
//
Процедура ЗаполнитьПрочиеРасходыРасходовБудущихПериодов(Реквизиты, мсвПрочиеРасходы) Экспорт
	
	ПрочиеРасходы = Реквизиты.ПрочиеРасходы;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеРасходы.Организация КАК Организация,
	|	втПрочиеРасходы.Подразделение КАК Подразделение,
	|	втПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	втПрочиеРасходы.Валюта КАК Валюта
	|ПОМЕСТИТЬ втПрочиеРасходыОтбор
	|ИЗ
	|	&ПрочиеРасходы КАК втПрочиеРасходы
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПрочиеРасходыОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПрочиеРасходыОстатки.АналитическийРазрез КАК АналитическийРазрез,
	|	упПрочиеРасходыОстатки.СуммаСНДСОстаток - упПрочиеРасходыОстатки.СуммаБезНДСОстаток КАК СуммаНДС,
	|	упПрочиеРасходыОстатки.СуммаСНДСОстаток КАК Сумма,
	|	упПрочиеРасходыОстатки.СуммаВалютаСНДСОстаток - упПрочиеРасходыОстатки.СуммаВалютаБезНДСОстаток КАК СуммаВалютаНДС,
	|	упПрочиеРасходыОстатки.СуммаВалютаСНДСОстаток КАК СуммаВалюта,
	|	упПрочиеРасходыОстатки.СтатьяРасходов КАК СтатьяРасходов,
	|	упПрочиеРасходыОстатки.Подразделение КАК Подразделение,
	|	упПрочиеРасходыОстатки.Организация КАК Организация,
	|	упПрочиеРасходыОстатки.Валюта КАК Валюта
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	РегистрНакопления.упПрочиеРасходы.Остатки(
	|		&Период,
	|		(Организация, Подразделение,
	|			СтатьяРасходов,
	|			Валюта) В (
	|				ВЫБРАТЬ
	|				втПрочиеРасходыОтбор.Организация КАК Организация,
	|				втПрочиеРасходыОтбор.Подразделение КАК Подразделение,
	|				втПрочиеРасходыОтбор.СтатьяРасходов КАК СтатьяРасходов,
	|				втПрочиеРасходыОтбор.Валюта КАК Валюта
	|			ИЗ
	|				втПрочиеРасходыОтбор КАК втПрочиеРасходыОтбор)) КАК упПрочиеРасходыОстатки
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(упПрочиеРасходыТ.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК НачалоПериода,
	|	упПрочиеРасходыТ.АналитическийРазрез КАК АналитическийРазрез,
	|	упПрочиеРасходыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПрочиеРасходыТ.Валюта КАК Валюта,
	|	упПрочиеРасходыТ.Подразделение КАК Подразделение,
	|	упПрочиеРасходыТ.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втПрочиеРасходыПериод
	|ИЗ
	|	втПрочиеРасходы КАК втПрочиеРасходыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упПрочиеРасходы КАК упПрочиеРасходыТ
	|	ПО ИСТИНА
	|		И упПрочиеРасходыТ.АналитическийРазрез = втПрочиеРасходыТ.АналитическийРазрез
	|		И упПрочиеРасходыТ.Валюта = втПрочиеРасходыТ.Валюта
	|		И упПрочиеРасходыТ.НаправлениеДеятельности = втПрочиеРасходыТ.НаправлениеДеятельности
	|		И упПрочиеРасходыТ.Подразделение = втПрочиеРасходыТ.Подразделение
	|		И упПрочиеРасходыТ.СтатьяРасходов = втПрочиеРасходыТ.СтатьяРасходов
	|		И упПрочиеРасходыТ.Организация = втПрочиеРасходыТ.Организация
	|		И упПрочиеРасходыТ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И упПрочиеРасходыТ.Период <= &ПериодДвижений
	|СГРУППИРОВАТЬ ПО
	|	упПрочиеРасходыТ.АналитическийРазрез,
	|	упПрочиеРасходыТ.НаправлениеДеятельности,
	|	упПрочиеРасходыТ.Валюта,
	|	упПрочиеРасходыТ.Подразделение,
	|	упПрочиеРасходыТ.СтатьяРасходов
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеРасходыТ.АналитическийРазрез КАК АналитическийРазрез,
	|	втПрочиеРасходыТ.Валюта КАК Валюта,
	|	втПрочиеРасходыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПрочиеРасходыТ.Подразделение КАК Подразделение,
	|	втПрочиеРасходыТ.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(втПрочиеРасходыТ.Сумма) КАК Сумма,
	|	СУММА(втПрочиеРасходыТ.СуммаВалюта) КАК СуммаВалюта,
	|	СУММА(втПрочиеРасходыТ.СуммаВалютаНДС) КАК СуммаВалютаНДС,
	|	СУММА(втПрочиеРасходыТ.СуммаНДС) КАК СуммаНДС,
	|	МАКСИМУМ(втПрочиеРасходыТ.СтатьяРасходов.КоличествоМесяцевПоУмолчанию) КАК КоличествоМесяцев,
	|	МАКСИМУМ(втПрочиеРасходыПериодТ.НачалоПериода) КАК НачалоПериода
	|ИЗ
	|	втПрочиеРасходы КАК втПрочиеРасходыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПрочиеРасходыПериод КАК втПрочиеРасходыПериодТ
	|	ПО ИСТИНА
	|		И втПрочиеРасходыПериодТ.АналитическийРазрез = втПрочиеРасходыТ.АналитическийРазрез
	|		И втПрочиеРасходыПериодТ.Валюта = втПрочиеРасходыТ.Валюта
	|		И втПрочиеРасходыПериодТ.НаправлениеДеятельности = втПрочиеРасходыТ.НаправлениеДеятельности
	|		И втПрочиеРасходыПериодТ.Подразделение = втПрочиеРасходыТ.Подразделение
	|		И втПрочиеРасходыПериодТ.СтатьяРасходов = втПрочиеРасходыТ.СтатьяРасходов
	|СГРУППИРОВАТЬ ПО
	|	втПрочиеРасходыТ.АналитическийРазрез,
	|	втПрочиеРасходыТ.Валюта,
	|	втПрочиеРасходыТ.НаправлениеДеятельности,
	|	втПрочиеРасходыТ.Подразделение,
	|	втПрочиеРасходыТ.СтатьяРасходов
	|";


	тбзПрочиеРасходы = Новый ТаблицаЗначений;
	тбзПрочиеРасходы.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	тбзПрочиеРасходы.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.упСтруктураПредприятия")); 
	тбзПрочиеРасходы.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.упНаправленияДеятельности")); 
	тбзПрочиеРасходы.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
	тбзПрочиеРасходы.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	Для каждого сткСтрокаПрочиеРасходы Из мсвПрочиеРасходы Цикл
		НоваяСтрока = тбзПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, сткСтрокаПрочиеРасходы);
	КонецЦикла;
		
	Запрос.УстановитьПараметр("ПрочиеРасходы", тбзПрочиеРасходы);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(Реквизиты.НачалоПериода),КонецДня(Реквизиты.НачалоПериода), Реквизиты.НачалоПериода));	
	Запрос.УстановитьПараметр("ПериодДвижений", ?(ЗначениеЗаполнено(Реквизиты.НачалоПериода),КонецДня(Реквизиты.НачалоПериода), '39991231235959'));	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.КоличествоМесяцев) Тогда
			НоваяСтрока.КоличествоМесяцев = Реквизиты.КоличествоМесяцев;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.НачалоПериода) Тогда
			НоваяСтрока.НачалоПериода = Реквизиты.НачалоПериода;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

// Процедура - Заполнить расходы будущих периодов, прочими расходами.
//
// Параметры:
//  Реквизиты - Структура - Содержит значения реквизитов объекта.
//  мсвПрочиеРасходы - Массив - Содержит структуры-строки параметров отбора из остатков прочих расходов.
//     * Организация    - СправочникСсылка.Организации             - Организация.
//     * Подразделение  - СправочникСсылка.упСтруктураПредприятия  - Подразделение.
//     * СтатьяРасходов - СправочникСсылка.упСтатьиДоходовРасходов - Статья расходов.
//     * Валюта         - СправочникСсылка.Валюты                  - Валюта.
//
Процедура ЗаполнитьПрочиеРасходыРасходовБудущихПериодовСАналитическимиРазрезами(Реквизиты, мсвПрочиеРасходы) Экспорт
	
	ПрочиеРасходы = Реквизиты.ПрочиеРасходы;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеРасходы.Организация КАК Организация,
	|	втПрочиеРасходы.Подразделение КАК Подразделение,
	|	втПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	втПрочиеРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	втПрочиеРасходы.Валюта КАК Валюта,
	|	втПрочиеРасходы.СуммаВалюта КАК СуммаВалюта,
	|	втПрочиеРасходы.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	втПрочиеРасходы.Сумма КАК Сумма,
	|	втПрочиеРасходы.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ втПрочиеРасходыОтбор
	|ИЗ
	|	&ПрочиеРасходы КАК втПрочиеРасходы
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(упПрочиеРасходыТ.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК НачалоПериода,
	|	упПрочиеРасходыТ.АналитическийРазрез КАК АналитическийРазрез,
	|	упПрочиеРасходыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПрочиеРасходыТ.Валюта КАК Валюта,
	|	упПрочиеРасходыТ.Подразделение КАК Подразделение,
	|	упПрочиеРасходыТ.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втПрочиеРасходыПериод
	|ИЗ
	|	втПрочиеРасходыОтбор КАК втПрочиеРасходыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упПрочиеРасходы КАК упПрочиеРасходыТ
	|	ПО ИСТИНА
	|		И упПрочиеРасходыТ.АналитическийРазрез = втПрочиеРасходыТ.АналитическийРазрез
	|		И упПрочиеРасходыТ.Валюта = втПрочиеРасходыТ.Валюта
	|		И упПрочиеРасходыТ.НаправлениеДеятельности = втПрочиеРасходыТ.НаправлениеДеятельности
	|		И упПрочиеРасходыТ.Подразделение = втПрочиеРасходыТ.Подразделение
	|		И упПрочиеРасходыТ.СтатьяРасходов = втПрочиеРасходыТ.СтатьяРасходов
	|		И упПрочиеРасходыТ.Организация = втПрочиеРасходыТ.Организация
	|		И упПрочиеРасходыТ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И упПрочиеРасходыТ.Период <= &Период
	|СГРУППИРОВАТЬ ПО
	|	упПрочиеРасходыТ.АналитическийРазрез,
	|	упПрочиеРасходыТ.НаправлениеДеятельности,
	|	упПрочиеРасходыТ.Валюта,
	|	упПрочиеРасходыТ.Подразделение,
	|	упПрочиеРасходыТ.СтатьяРасходов
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеРасходыТ.АналитическийРазрез КАК АналитическийРазрез,
	|	втПрочиеРасходыТ.Валюта КАК Валюта,
	|	втПрочиеРасходыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПрочиеРасходыТ.Подразделение КАК Подразделение,
	|	втПрочиеРасходыТ.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(втПрочиеРасходыТ.Сумма) КАК Сумма,
	|	СУММА(втПрочиеРасходыТ.СуммаВалюта) КАК СуммаВалюта,
	|	СУММА(втПрочиеРасходыТ.СуммаВалютаНДС) КАК СуммаВалютаНДС,
	|	СУММА(втПрочиеРасходыТ.СуммаНДС) КАК СуммаНДС,
	|	МАКСИМУМ(упСтатьиДоходовРасходов.КоличествоМесяцевПоУмолчанию) КАК КоличествоМесяцев,
	|	МАКСИМУМ(втПрочиеРасходыПериодТ.НачалоПериода) КАК НачалоПериода
	|ИЗ
	|	втПрочиеРасходыОтбор КАК втПрочиеРасходыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО втПрочиеРасходыТ.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка 
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПрочиеРасходыПериод КАК втПрочиеРасходыПериодТ
	|	ПО ИСТИНА
	|		И втПрочиеРасходыПериодТ.АналитическийРазрез = втПрочиеРасходыТ.АналитическийРазрез
	|		И втПрочиеРасходыПериодТ.Валюта = втПрочиеРасходыТ.Валюта
	|		И втПрочиеРасходыПериодТ.НаправлениеДеятельности = втПрочиеРасходыТ.НаправлениеДеятельности
	|		И втПрочиеРасходыПериодТ.Подразделение = втПрочиеРасходыТ.Подразделение
	|		И втПрочиеРасходыПериодТ.СтатьяРасходов = втПрочиеРасходыТ.СтатьяРасходов
	|СГРУППИРОВАТЬ ПО
	|	втПрочиеРасходыТ.АналитическийРазрез,
	|	втПрочиеРасходыТ.Валюта,
	|	втПрочиеРасходыТ.НаправлениеДеятельности,
	|	втПрочиеРасходыТ.Подразделение,
	|	втПрочиеРасходыТ.СтатьяРасходов
	|";



	тбзПрочиеРасходы = Новый ТаблицаЗначений;
	тбзПрочиеРасходы.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	тбзПрочиеРасходы.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.упСтруктураПредприятия")); 
	тбзПрочиеРасходы.Колонки.Добавить("АналитическийРазрез", Новый ОписаниеТипов("СправочникСсылка.упАналитическиеРазрезы")); 
	тбзПрочиеРасходы.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.упНаправленияДеятельности")); 
	тбзПрочиеРасходы.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
	тбзПрочиеРасходы.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	тбзПрочиеРасходы.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число",
									   Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	тбзПрочиеРасходы.Колонки.Добавить("СуммаВалюта", Новый ОписаниеТипов("Число",
									   Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	тбзПрочиеРасходы.Колонки.Добавить("СуммаВалютаНДС", Новый ОписаниеТипов("Число",
									   Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	тбзПрочиеРасходы.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число",
									   Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));								   
	Для каждого сткСтрокаПрочиеРасходы Из мсвПрочиеРасходы Цикл
		НоваяСтрока = тбзПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, сткСтрокаПрочиеРасходы);
	КонецЦикла;
		
	Запрос.УстановитьПараметр("ПрочиеРасходы", тбзПрочиеРасходы);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(Реквизиты.НачалоПериода),КонецДня(Реквизиты.НачалоПериода),'39991231235959'));	
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.КоличествоМесяцев) Тогда
			НоваяСтрока.КоличествоМесяцев = Реквизиты.КоличествоМесяцев;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(НоваяСтрока.НачалоПериода) Тогда
			НоваяСтрока.НачалоПериода = Реквизиты.НачалоПериода;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаспределениеПрямыхРасходов

// Процедура - Заполнить прямые расходы
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//  сткПараметры			 - Структура	 - параметры.
//
Процедура ЗаполнитьПрямыеРасходы(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ, сткПараметры = Неопределено) Экспорт
	
	ЗаполнитьРасходыПоРейсам            = ДополнительныеСвойства.ЗаполнитьРасходыПоРейсам;
	ЗаполнитьРасходыПоТС                = ДополнительныеСвойства.ЗаполнитьРасходыПоТС;
	ЗаполнитьРасходыФинансовыйРезультат = ДополнительныеСвойства.ЗаполнитьРасходыФинансовыйРезультат;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределяемыеРасходы.Ссылка КАК ПравилоРаспределенияПрямыхРасходов, 
	|	РаспределяемыеРасходы.Ссылка.РасходыНаРейсы КАК РасходыНаРейсы,
	|	РаспределяемыеРасходы.Ссылка.РасходыНаТранспорт КАК РасходыНаТранспорт,
	|	РаспределяемыеРасходы.Ссылка.ФинансовыйРезультат КАК ФинансовыйРезультат,
	|	РаспределяемыеРасходы.ПредметРаспределения КАК ПредметРаспределения,
	|	ВариантыРаспределения.АлгоритмПроверки КАК АлгоритмПроверки,
	|	ВариантыРаспределения.ПравилоРаспределенияРасходов КАК ПравилоРаспределенияРасходов,
	|	ВариантыРаспределения.ПроизвольныйАлгоритмПроверки КАК ПроизвольныйАлгоритмПроверки,
	|	ВариантыРаспределения.СхемаКомпоновкиДанных КАК СхемаКомпоновкиДанных,
	|	РаспределяемыеРасходы.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Справочник.упПравилаРаспределенияПрямыхРасходов.РаспределяемыеРасходы КАК РаспределяемыеРасходы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВариантыРаспределенияРасходов КАК ВариантыРаспределения
	|	ПО РаспределяемыеРасходы.ПредметРаспределения = ВариантыРаспределения.Ссылка
	|ГДЕ РаспределяемыеРасходы.Ссылка= &ПравилоРаспределенияПрямыхРасходов
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";

	Запрос.УстановитьПараметр("ПравилоРаспределенияПрямыхРасходов", Реквизиты.ПравилоРаспределенияПрямыхРасходов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Результат = Неопределено;
		Если Выборка.АлгоритмПроверки = Перечисления.упАлгоритмыПроверкиСобытия.НастройкаОтбора Тогда 
			
			СхемаКомпоновкиДанных = Выборка.СхемаКомпоновкиДанных.Получить();
			Параметры             = СхемаКомпоновкиДанных.НастройкиПоУмолчанию.ПараметрыДанных;
			
			ПараметрНачало                 = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодРаспределенияНачало"));
			ПараметрНачало.Значение        = НачалоДня(Реквизиты.ПериодРаспределенияЗатратНачало);
			
			ПараметрОкончание              = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодРаспределенияОкончание"));
			ПараметрОкончание.Значение     = КонецДня(Реквизиты.ПериодРаспределенияЗатратОкончание);
			
			ПараметрОрганизация            = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
			ПараметрОрганизация.Значение   = Реквизиты.Организация;
			
			ПараметрНаправлениеДеятельности            = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользуетсяОтборПоНаправлениюДеятельности"));
			ИспользуетсяОтборПоНаправлениюДеятельности = ПараметрНаправлениеДеятельности.Использование;
			
			ПараметрПодразделение            = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользуетсяОтборПоПодразделению"));
			ИспользуетсяОтборПоПодразделению = ПараметрПодразделение.Использование;
			
			ПараметрАналитическийРазрез    = Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИспользуетсяОтборПоАналитическомуРазрезу"));
			ИспользуетсяОтборПоАналитическомуРазрезу = ПараметрАналитическийРазрез.Использование;
			
			Если Истина
				И ИспользуетсяОтборПоНаправлениюДеятельности
				И ЗначениеЗаполнено(Реквизиты.НаправлениеДеятельности) 
			Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "НаправлениеДеятельности", Реквизиты.НаправлениеДеятельности, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
			КонецЕсли;
			
			Если Истина
				И ИспользуетсяОтборПоПодразделению
				И ЗначениеЗаполнено(Реквизиты.Подразделение) 
			Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Подразделение", Реквизиты.Подразделение, ВидСравненияКомпоновкиДанных.Равно,, Истина);	
			КонецЕсли;
			
			// Таблица значений, в которую будет получен результат.
			Результат = упСервисныеФункции.ВыполнитьСКДвТЗ(СхемаКомпоновкиДанных);
			
		Иначе
			
			упСобытия.ПроверкаПроизвольногоАлгоритма(Выборка.ПроизвольныйАлгоритмПроверки, Реквизиты, Результат);
			
		КонецЕсли;
			
		Если Истина
			И ТипЗнч(Результат) = Тип("ТаблицаЗначений")
			И Результат.Количество() > 0 
		Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапросаОборотыПоРасходам(ЗаполнитьРасходыПоРейсам, ЗаполнитьРасходыПоТС, ЗаполнитьРасходыФинансовыйРезультат);
			Запрос.УстановитьПараметр("Результат",   Результат);
			Запрос.УстановитьПараметр("Валюта",      Реквизиты.Валюта);
			Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
			Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка());
			Запрос.УстановитьПараметр("ЗаполнитьРасходыПоРейсам",            Выборка.РасходыНаРейсы);
			Запрос.УстановитьПараметр("ЗаполнитьРасходыПоТС",                Выборка.РасходыНаТранспорт);
			Запрос.УстановитьПараметр("ЗаполнитьРасходыФинансовыйРезультат", Выборка.ФинансовыйРезультат);
			Запрос.УстановитьПараметр("НачалоПериода",                       НачалоДня(Реквизиты.ПериодРаспределенияЗатратНачало));
			Запрос.УстановитьПараметр("ОкончаниеПериода",                    КонецДня(Реквизиты.ПериодРаспределенияЗатратОкончание));
						
			мсвРезультаты = Запрос.ВыполнитьПакет();
			
			ИндексРейсы	           = 1;
			ИндексТС                  = 2;					
			ИндексФинансовыйРезультат = 3;
			
			Если ЗаполнитьРасходыПоРейсам Тогда
				Реквизиты.ДанныеРегистраРасходы.Очистить();
				Выборка = мсвРезультаты[ИндексРейсы].Выбрать();
				Пока Выборка.Следующий() Цикл
					НоваяСтрока = 	Реквизиты.ДанныеРегистраРасходы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				КонецЦикла;
			КонецЕсли;
			
			Если ЗаполнитьРасходыПоТС Тогда
				Реквизиты.ДанныеРегистраРасходыНаТС.Очистить();
				Выборка = мсвРезультаты[ИндексТС].Выбрать();
				Пока Выборка.Следующий() Цикл
					НоваяСтрока = 	Реквизиты.ДанныеРегистраРасходыНаТС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				КонецЦикла;
			КонецЕсли;
			
			Если ЗаполнитьРасходыФинансовыйРезультат Тогда
				Реквизиты.ДанныеРегистраДоходыРасходыПредприятия.Очистить();
				Выборка = мсвРезультаты[ИндексФинансовыйРезультат].Выбрать();
				Пока Выборка.Следующий() Цикл
					НоваяСтрока = 	Реквизиты.ДанныеРегистраДоходыРасходыПредприятия.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				КонецЦикла;
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

// Процедура - Распределяет остатки по прочим расходам за период по таблицам "РасходыПоРейсам", "РасходыПоТС", "РезультатыФинансовыйРезультат".
//
// Параметры:
//  ДополнительныеСвойства	 - Структура - дополнительные свойства объекта.
//  Ссылка				 - ДокументСсылка.упРаспределениеПрочихРасходов - ссылка на документ.
//  Реквизиты				 - Структура - содержит значения реквизитов объекта.
//  Отказ					 - Булево    - Истина, если произошла ошибка.
//  сткПараметры               - Структура - сообщения для расшифровки, 
//                               используется при формировании документов из обработки "Закрытие месяца".
//
Процедура РаспределитьПрямыеРасходы(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ, сткПараметры = Неопределено) Экспорт
	
	РасходыПоРейсам               = Реквизиты.РасходыПоРейсам;
	ДанныеРегистраРасходы         = Реквизиты.ДанныеРегистраРасходы;
	РасходыПоТС                   = Реквизиты.РасходыПоТС;
	ДанныеРегистраРасходыНаТС     = Реквизиты.ДанныеРегистраРасходыНаТС;
	РасходыФинансовыйРезультат             = Реквизиты.РасходыФинансовыйРезультат;
	ДанныеРегистраДоходыРасходыПредприятия = Реквизиты.ДанныеРегистраДоходыРасходыПредприятия;
	
	ЗаполнитьРасходыПоРейсам            = ДополнительныеСвойства.ЗаполнитьРасходыПоРейсам;
	ЗаполнитьРасходыПоТС                = ДополнительныеСвойства.ЗаполнитьРасходыПоТС;
	ЗаполнитьРасходыФинансовыйРезультат = ДополнительныеСвойства.ЗаполнитьРасходыФинансовыйРезультат;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаРаспределенияПрямыхРасходов.Ссылка КАК ПравилоРаспределенияПрямыхРасходов,
	|	ПравилаРаспределенияПрямыхРасходов.ВариантРаспределенияПрямыхРасходов КАК ВариантРаспределения,
	|	ВариантыРаспределения.АлгоритмПроверки КАК АлгоритмПроверки,
	|	ВариантыРаспределения.ПравилоРаспределенияРасходов КАК ПравилоРаспределенияРасходов,
	|	ВариантыРаспределения.ПроизвольныйАлгоритмПроверки КАК ПроизвольныйАлгоритмПроверки,
	|	ВариантыРаспределения.СхемаКомпоновкиДанных КАК СхемаКомпоновкиДанных,
	|	ПравилаРаспределенияПрямыхРасходов.НомерСтроки КАК НомерСтроки,
	|	упПравилаРаспределенияРасходов.Формула КАК Формула,
	|	упПравилаРаспределенияРасходов.СпособРаспределения КАК СпособРаспределения,
	|	упПравилаРаспределенияРасходов.Показатель КАК Показатель
	|ИЗ
	|	Справочник.упПравилаРаспределенияПрямыхРасходов.ВариантыРаспределения КАК ПравилаРаспределенияПрямыхРасходов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВариантыРаспределенияРасходов КАК ВариантыРаспределения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПравилаРаспределенияРасходов КАК упПравилаРаспределенияРасходов
	|		ПО ВариантыРаспределения.ПравилоРаспределенияРасходов = упПравилаРаспределенияРасходов.Ссылка
	|	ПО ПравилаРаспределенияПрямыхРасходов.ВариантРаспределенияПрямыхРасходов = ВариантыРаспределения.Ссылка
	|ГДЕ ПравилаРаспределенияПрямыхРасходов.Ссылка = &ПравилоРаспределенияПрямыхРасходов
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНаРейсы.АналитическийРазрез КАК АналитическийРазрез,
	|	РасходыНаРейсы.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	РасходыНаРейсы.Рейс КАК Рейс,
	|	РасходыНаРейсы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасходыНаРейсы.СтатьяРасходов КАК СтатьяРасходов,
	|	РасходыНаРейсы.Сумма КАК Сумма,
	|	РасходыНаРейсы.СуммаВалюта КАК СуммаВалюта,
	|	РасходыНаРейсы.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	РасходыНаРейсы.СуммаНДС КАК СуммаНДС,
	|	&Организация КАК Организация,
	|	&Валюта КАК Валюта
	|ПОМЕСТИТЬ втРасходы
	|ИЗ
	|	&ДанныеРегистраРасходы КАК РасходыНаРейсы
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНаТС.АналитическийРазрез КАК АналитическийРазрез,
	|	РасходыНаТС.ТранспортноеСредство КАК ТранспортноеСредство,
	|	РасходыНаТС.СтатьяРасходов КАК СтатьяРасходов,
	|	РасходыНаТС.Сумма КАК Сумма,
	|	РасходыНаТС.СуммаВалюта КАК СуммаВалюта,
	|	РасходыНаТС.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	РасходыНаТС.СуммаНДС КАК СуммаНДС,
	|	&Организация КАК Организация,
	|	&Валюта КАК Валюта
	|ПОМЕСТИТЬ втРасходыНаТС
	|ИЗ
	|	&ДанныеРегистраРасходыНаТС КАК РасходыНаТС
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	РасходыПредприятия.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасходыПредприятия.Подразделение КАК Подразделение,
	|	РасходыПредприятия.СтатьяДоходовРасходов КАК СтатьяРасходов,
	|	РасходыПредприятия.Расходы КАК Расходы,
	|	&Организация КАК Организация,
	|	&Валюта КАК Валюта
	|ПОМЕСТИТЬ втРасходыПредприятия
	|ИЗ
	|	&ДанныеРегистраДоходыРасходыПредприятия КАК РасходыПредприятия
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРасходы.Валюта КАК Валюта,
	|	втРасходы.Сумма КАК Сумма,
	|	втРасходы.СуммаВалюта КАК СуммаВалюта,
	|	втРасходы.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	втРасходы.СуммаНДС КАК СуммаНДС,
	|	втРасходы.Организация КАК Организация,
	|	втРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Сотрудник, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК СотрудникАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ВидПеревозки, ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)) КАК ВидПеревозкиАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Агрегат, ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)) КАК АгрегатАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредствоАР,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения
	|ИЗ
	|	втРасходы КАК втРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО втРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|	ПО втРасходы.АналитическийРазрез = упАналитическиеРазрезы.Ссылка
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРасходы.Валюта КАК Валюта,
	|	втРасходы.Сумма КАК Сумма,
	|	втРасходы.СуммаВалюта КАК СуммаВалюта,
	|	втРасходы.СуммаВалютаНДС КАК СуммаВалютаНДС,
	|	втРасходы.СуммаНДС КАК СуммаНДС,
	|	втРасходы.Организация КАК Организация,
	|	втРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Сотрудник, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК СотрудникАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ВидПеревозки, ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)) КАК ВидПеревозкиАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Агрегат, ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)) КАК АгрегатАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредствоАР,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения
	|ИЗ
	|	втРасходыНаТС КАК втРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО втРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|	ПО втРасходы.АналитическийРазрез = упАналитическиеРазрезы.Ссылка
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходы.Подразделение КАК Подразделение,
	|	втРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРасходы.Валюта КАК Валюта,
	|	втРасходы.Расходы КАК Расходы,
	|	втРасходы.Организация КАК Организация,
	|	втРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Сотрудник, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК СотрудникАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ВидПеревозки, ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)) КАК ВидПеревозкиАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.Агрегат, ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)) КАК АгрегатАР,
	|	ЕСТЬNULL(упАналитическиеРазрезы.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредствоАР,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения
	|ИЗ
	|	втРасходыПредприятия КАК втРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО втРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|	ПО ЛОЖЬ
	|";

	Запрос.УстановитьПараметр("ПравилоРаспределенияПрямыхРасходов",     Реквизиты.ПравилоРаспределенияПрямыхРасходов);
	Запрос.УстановитьПараметр("ДанныеРегистраРасходы",                  ДанныеРегистраРасходы);
	Запрос.УстановитьПараметр("ДанныеРегистраРасходыНаТС",              ДанныеРегистраРасходыНаТС);
	Запрос.УстановитьПараметр("ДанныеРегистраДоходыРасходыПредприятия", ДанныеРегистраДоходыРасходыПредприятия);
	Запрос.УстановитьПараметр("Организация",                            Реквизиты.Организация);
	Запрос.УстановитьПараметр("Валюта",                                 Реквизиты.Валюта);
		
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексПравилаРаспределния  = 0;
	ИндексРасходы     = 4;
	ИндексРасходыНаТС = 5;
	ИндексРасходыНаФинансовыйРезультат = 6;
	
	Если ЗаполнитьРасходыПоРейсам Тогда
		РасходыПоРейсам.Очистить();
	КонецЕсли;
	
	Если ЗаполнитьРасходыПоТС Тогда
		РасходыПоТС.Очистить();
	КонецЕсли;

	Если ЗаполнитьРасходыФинансовыйРезультат Тогда
		РасходыФинансовыйРезультат.Очистить();
	КонецЕсли;
	
	Выборка = мсвРезультатЗапроса[ИндексПравилаРаспределния].Выбрать();
	
	тбзДанныеРегистраРасходы               = мсвРезультатЗапроса[ИндексРасходы].Выгрузить();
	тбзДанныеРегистраРасходыНаТС           = мсвРезультатЗапроса[ИндексРасходыНаТС].Выгрузить();
	тбзДанныеРегистраНаФинансовыйРезультат = мсвРезультатЗапроса[ИндексРасходыНаФинансовыйРезультат].Выгрузить();
	
	мсвБазовыеТаблицы = Новый Массив;
	Пока Выборка.Следующий() Цикл
				
		сткВариантРаспределения = ИнициализироватьПараметрыВариантаРаспределения();
		ЗаполнитьЗначенияСвойств(сткВариантРаспределения, Выборка);
		ЗаполнитьБазуРаспределенияРасходов(мсвБазовыеТаблицы, сткВариантРаспределения, Реквизиты);

	КонецЦикла;
		
	Если ЗначениеЗаполнено(Реквизиты.ПравилоРаспределенияПрямыхРасходов) Тогда
		
		Если ЗаполнитьРасходыПоРейсам Тогда
			тбзРасходы = РасходыПоРейсам.Выгрузить();
	          ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвБазовыеТаблицы, тбзДанныеРегистраРасходы, тбзРасходы, "БазаРаспределенияПоРейсам",, Отказ);
			тбзРасходы.Свернуть("АналитическийРазрез, НаправлениеДеятельности, ПравилоРаспределения, Рейс, СтатьяРасходов, ВариантРаспределения", 
									"Сумма, СуммаВалюта, СуммаВалютаНДС, СуммаНДС");
			РасходыПоРейсам.Загрузить(тбзРасходы);
		КонецЕсли;	
		
		Если ЗаполнитьРасходыПоТС Тогда
			тбзРасходы = РасходыПоТС.Выгрузить();
	          ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвБазовыеТаблицы, тбзДанныеРегистраРасходыНаТС, тбзРасходы, "БазаРаспределенияПоРейсам",, Отказ);
			тбзРасходы.Свернуть("АналитическийРазрез, ТранспортноеСредство, СтатьяРасходов, ВариантРаспределения", 
									"Сумма, СуммаВалюта, СуммаВалютаНДС, СуммаНДС");
			РасходыПоТС.Загрузить(тбзРасходы);
		КонецЕсли;	

		Если ЗаполнитьРасходыФинансовыйРезультат Тогда
			тбзРасходы = РасходыФинансовыйРезультат.Выгрузить();
	          ЗаполнитьТаблицуРаспределенийПрочихРасходовПоРейсам(мсвБазовыеТаблицы, тбзДанныеРегистраНаФинансовыйРезультат, тбзРасходы, "БазаРаспределенияПоРейсам",, Отказ);
			тбзРасходы.Свернуть("АналитическийРазрез, НаправлениеДеятельности, Организация, Подразделение, СтатьяРасходов, ВариантРаспределения","Расходы");
			РасходыФинансовыйРезультат.Загрузить(тбзРасходы);
		КонецЕсли;	

		
	Иначе
		ТекстСообщения = Нстр("ru = 'Правило распределения прямых расходов не заполнено'");
		упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткПараметры,,"Ошибка",,ТекстСообщения,1);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедуры_СформироватьРасходы

Процедура СформироватьРаспределениеРасходов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, тбзРаспределениеРасходовНаПредприятие, Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	сткРеквизитыПредприятия = Новый Структура();
	сткРеквизитыПредприятия.Вставить("Организация",   ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "Организация",   Справочники.Организации.ПустаяСсылка()));
	сткРеквизитыПредприятия.Вставить("Подразделение", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "Подразделение", Справочники.упСтруктураПредприятия.ПустаяСсылка()));

	тбзПрямыеРасходы     = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПрямыеРасходы");
	тбзПрямыеРасходыНаТС = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПрямыеРасходыНаТС");
	мсвРейсы	           = Новый Массив;
	
	Если Истина
		И ДополнительныеСвойства.РаспределятьРасходыНаРейсы
		И ФормироватьПрямыеРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзПрямыеРасходы, тбзПрямыеРасходыНаТС, мсвРейсы, Отказ) 
	Тогда
	
		Если Ложь
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрочиеРасходы") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРегламентнаяОперация") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРаспределениеПрочихРасходов") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРаспределениеПрямыхРасходов") 
		Тогда
		
			Если Реквизиты.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
				
				мсвРейсы.Добавить(Реквизиты.Рейс);
				
				РаспределитьРасходы(тбзПрямыеРасходы, тбзРаспределениеРасходов, тбзПрямыеРасходыНаТС, тбзРаспределениеРасходовНаТС, мсвРейсы, ДополнительныеСвойства, Отказ);
				
			ИначеЕсли Реквизиты.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам	Тогда
				
				
				Если ТипЗнч(Реквизиты.Рейсы) = Тип("ТаблицаЗначений") Тогда
					тбзРейсы = Реквизиты.Рейсы;
				Иначе
					тбзРейсы = Реквизиты.Рейсы.Выгрузить();
				КонецЕсли;
				
				тбзРейсы.Свернуть("Рейс");
				мсвРейсы = тбзРейсы.ВыгрузитьКолонку("Рейс");
				
				РаспределитьРасходы(тбзПрямыеРасходы, тбзРаспределениеРасходов, тбзПрямыеРасходыНаТС, тбзРаспределениеРасходовНаТС, мсвРейсы, ДополнительныеСвойства, Отказ);

						
			ИначеЕсли Реквизиты.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда
				
				мсвРейсыДляРаспределения = ОбщегоНазначенияКлиентСервер.СвернутьМассив(тбзПрямыеРасходы.ВыгрузитьКолонку("Рейс"));
				
				Для каждого РейсОтбор Из мсвРейсыДляРаспределения Цикл
					
					мсвРейсы	= Новый Массив;
					мсвРейсы.Добавить(РейсОтбор);
			
					тбзПрямыеРасходы     = ДополнительныеСвойства.ПрямыеРасходы.Скопировать(Новый Структура("Рейс", РейсОтбор));
					Если ДополнительныеСвойства.РаспределятьРасходыНаТС Тогда
						тбзПрямыеРасходыНаТС = ДополнительныеСвойства.ПрямыеРасходыНаТС.Скопировать(Новый Структура("Рейс", РейсОтбор));	
					КонецЕсли;
					
					РаспределитьРасходы(тбзПрямыеРасходы, тбзРаспределениеРасходов, тбзПрямыеРасходыНаТС, тбзРаспределениеРасходовНаТС, мсвРейсы, ДополнительныеСвойства, Отказ);
			
				КонецЦикла;
				
			КонецЕсли;
			
			Если Ложь
				Или Истина
					И ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрочиеРасходы")
					И Реквизиты.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоРейсам 
				Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы")	
			Тогда	
			
				// Подразделение для расходов на финансовый результат берется из рейсов распределения.
				сткРеквизитыПредприятия.Удалить("Подразделение");
			
			КонецЕсли;
			
			// Если не удалось распределить расходы по рейсам
			Если Истина
				И ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрочиеРасходы")
				И ДополнительныеСвойства.РаспределятьРасходыНаТС
				И Реквизиты.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоТранспортномуСредству
				И ЗначениеЗаполнено(Реквизиты.ТранспортноеСредство)
				И тбзПрямыеРасходыНаТС <> Неопределено
				И тбзРаспределениеРасходовНаТС.Количество() = 0
			Тогда
				Для каждого СтрокаРасход Из тбзПрямыеРасходыНаТС Цикл
					НоваяСтрока = тбзРаспределениеРасходовНаТС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасход);
					НоваяСтрока.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли Ложь
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРемонтИТОТС") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") 
		Тогда
			
			сткРеквизитыПредприятия.Вставить("НаправлениеДеятельности", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Реквизиты, "НаправлениеДеятельности", Справочники.упНаправленияДеятельности.ПустаяСсылка()));
			
			РаспределитьРасходы(тбзПрямыеРасходы, тбзРаспределениеРасходов, тбзПрямыеРасходыНаТС, тбзРаспределениеРасходовНаТС, мсвРейсы, ДополнительныеСвойства, Отказ);
			
			// Если не удалось распределить расходы по рейсам
			Если Истина
				И ДополнительныеСвойства.РаспределятьРасходыНаТС
				И тбзПрямыеРасходыНаТС <> Неопределено
				И тбзРаспределениеРасходовНаТС.Количество() = 0 
			Тогда
				Для каждого СтрокаРасход Из тбзПрямыеРасходыНаТС Цикл
					НоваяСтрока = тбзРаспределениеРасходовНаТС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасход);
					НоваяСтрока.ТранспортноеСредство = Реквизиты.ТранспортноеСредство;
				КонецЦикла;
			КонецЕсли;
			
		Иначе	
			
			РаспределитьРасходы(тбзПрямыеРасходы, тбзРаспределениеРасходов, тбзПрямыеРасходыНаТС, тбзРаспределениеРасходовНаТС, мсвРейсы, ДополнительныеСвойства, Отказ);
			
		КонецЕсли;
		
		Если ДополнительныеСвойства.РаспределятьРасходыНаФинРезультат Тогда
				
			СформироватьТаблицуРасходыНаПредприятие(тбзРаспределениеРасходовНаПредприятие, 
											тбзРаспределениеРасходов, 
											тбзРаспределениеРасходовНаТС,
											сткРеквизитыПредприятия,
											Отказ);
											
		КонецЕсли;
	КонецЕсли;									
КонецПроцедуры

Процедура СформироватьТаблицуРасходыНаПредприятие(тбзРаспределениеРасходовНаПредприятие,  тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, сткРеквизитыПредприятия, Отказ);
	
	тбзРаспределение = Неопределено;
	
	Если тбзРаспределениеРасходов.Количество() > 0 Тогда
		тбзРаспределение = тбзРаспределениеРасходов;
	ИначеЕсли тбзРаспределениеРасходовНаТС.Количество() > 0 Тогда
		тбзРаспределение = тбзРаспределениеРасходовНаТС;
	КонецЕсли;
	
	Если тбзРаспределение <> Неопределено Тогда
		
		Для каждого Строка Из тбзРаспределение Цикл
			НоваяСтрока = тбзРаспределениеРасходовНаПредприятие.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, сткРеквизитыПредприятия);
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

// Процедура - Проконтролировать прочие расходы
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//
Процедура ПроконтролироватьПрочиеРасходы(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПрочиеРасходыОстатки.АналитическийРазрез КАК АналитическийРазрез,
	               |	упПрочиеРасходыОстатки.Валюта КАК Валюта,
	               |	упПрочиеРасходыОстатки.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
	               |	упПрочиеРасходыОстатки.СуммаСНДСОстаток КАК СуммаСНДСОстаток,
	               |	упПрочиеРасходыОстатки.СуммаВалютаБезНДСОстаток КАК СуммаВалютаБезНДСОстаток,
	               |	упПрочиеРасходыОстатки.СуммаВалютаСНДСОстаток КАК СуммаВалютаСНДСОстаток
	               |ИЗ
	               |	РегистрНакопления.упПрочиеРасходы.Остатки(
	               |			,
	               |			Организация = &Организация
	               |				И СтатьяРасходов = &СтатьяРасходов) КАК упПрочиеРасходыОстатки
	               |ГДЕ
	               |	(упПрочиеРасходыОстатки.СуммаБезНДСОстаток < 0
	               |			ИЛИ упПрочиеРасходыОстатки.СуммаСНДСОстаток < 0
	               |			ИЛИ упПрочиеРасходыОстатки.СуммаВалютаБезНДСОстаток < 0
	               |			ИЛИ упПрочиеРасходыОстатки.СуммаВалютаСНДСОстаток < 0)";
	Запрос.УстановитьПараметр("Организация",    Реквизиты.Организация);
	Запрос.УстановитьПараметр("СтатьяРасходов", Реквизиты.СтатьяРасходов);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Прочих расходов списано больше чем есть на остатках 
		|Суммма без НДС = %1; Сумма с НДС = %2; Суммма в валюте без НДС: %3; Сумма в валюте с НДС: %4.
		|(Организация:""%5""; Статья расходов: ""%6""; Аналитический разрез: ""%7""; Валюта: ""%8"";)'"), 
			?(Выборка.СуммаСНДСОстаток < 0, -Выборка.СуммаСНДСОстаток, 0),
			?(Выборка.СуммаБезНДСОстаток < 0, -Выборка.СуммаБезНДСОстаток, 0),
			?(Выборка.СуммаВалютаСНДСОстаток < 0, -Выборка.СуммаВалютаСНДСОстаток, 0),
			?(Выборка.СуммаВалютаБезНДСОстаток < 0, -Выборка.СуммаВалютаБезНДСОстаток, 0),
			Реквизиты.Организация,
			Реквизиты.СтатьяРасходов,
			Выборка.АналитическийРазрез,
			Выборка.Валюта);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение прочих расходов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		 					УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстСообщения);
	КонецЦикла; 
	
КонецПроцедуры

#Область ПроверкаВозможностиФормированияРасходов

Функция ФормироватьПрямыеРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, тбзРасходыНаТС, мсвРейсы, Отказ)

	Результат = Истина;
	
	Если Ложь
		Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРейс") 
		Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС") 	Тогда
	
	 	Результат = ФормироватьПрямыеРасходыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ);
				
	ИначеЕсли Ложь
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упФактическиеРасходы") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПрочиеРасходы") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРегламентнаяОперация") Тогда
	
		Результат = ФормироватьПрямыеРасходыПоФактическимРасходам(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ);
	
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упВходящаяПретензия") Тогда
		
		Результат = ФормироватьПрямыеРасходыПоВходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ);	
				
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упСлужебноеРасследование") Тогда	
		
		Результат = ФормироватьПрямыеРасходыПоСлужебномуРасследованию(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ);	
						
	ИначеЕсли Ложь
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") 
			Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда
	
		Результат = ФормироватьПрямыеРасходыПоРемонтуИТОТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ);
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упПутевойЛист") Тогда	
		
		Результат = ФормироватьРасходыПоПутевомуЛисту(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ);
		
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции

Функция ФормироватьПрямыеРасходыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ)
	
	Статус    = ДополнительныеСвойства.Статус;
	Результат = Статус <> Перечисления.упСтатусыРейса.Отменен;
	мсвРейсы.Добавить(Реквизиты.Рейс);
	
	Возврат Результат;
	
КонецФункции

Функция ФормироватьПрямыеРасходыПоФактическимРасходам(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ)
	
	Результат = Истина;

	Возврат Результат;
	
КонецФункции

Функция ФормироватьПрямыеРасходыПоВходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ)	
	
	Результат = Ложь;
	
	ВедетсяВалютныйУчет	= ДополнительныеСвойства.ВедетсяВалютныйУчет; 
	Статус			= ДополнительныеСвойства.Статус;
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыВходящейПретензии.Удовлетворена
		Или Статус = Перечисления.упСтатусыВходящейПретензии.УдовлетворенаЧастично
	Тогда
	
		Если Истина
			И ЗначениеЗаполнено(Реквизиты.Рейс)
			И тбзРасходы.Количество() > 0 
		Тогда
			Результат = Истина;
			мсвРейсы.Добавить(Реквизиты.Рейс);	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ФормироватьПрямыеРасходыПоСлужебномуРасследованию(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ)
	
	Результат = Ложь;
		
	Если Реквизиты.СпособРаспределенияРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыЗаПериод Тогда
		
		Если Истина
			И ЗначениеЗаполнено(Реквизиты.ПериодРаспределенияЗатратНачало)
			И ЗначениеЗаполнено(Реквизиты.ПериодРаспределенияЗатратОкончание) 
		Тогда
			мсвРейсы = упРейс.СписокРейсовПоТранспортномуСредствуЗаПериод(Реквизиты.ТранспортноеСредство, 
															  Реквизиты.ПериодРаспределенияЗатратНачало, 
															  Реквизиты.ПериодРаспределенияЗатратОкончание);
			Результат = Истина;													
		КонецЕсли;

	ИначеЕсли Реквизиты.СпособРаспределенияРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		Если ЗначениеЗаполнено(Реквизиты.Рейс) Тогда
			мсвРейсы.Добавить(Реквизиты.Рейс);	
		КонецЕсли;
		
	КонецЕсли; 
	
	Если мсвРейсы.Количество() > 0 Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ФормироватьПрямыеРасходыПоРемонтуИТОТС(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ)
	
	Статус    = ДополнительныеСвойства.Статус;
	Результат = Ложь;
		
	Если Истина
		И Статус <> Перечисления.упСтатусыРемонтаИТОТС.Отменен
		И Статус <> Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена
	Тогда
	
	     Результат = Истина;
	
		Если Истина
			И ЗначениеЗаполнено(Реквизиты.ПериодРаспределенияЗатратНачало)
			И ЗначениеЗаполнено(Реквизиты.ПериодРаспределенияЗатратОкончание) 
		Тогда
			мсвРейсы = упРейс.СписокРейсовПоТранспортномуСредствуЗаПериод(Реквизиты.ТранспортноеСредство, 
															  Реквизиты.ПериодРаспределенияЗатратНачало, 
															  Реквизиты.ПериодРаспределенияЗатратОкончание);
		КонецЕсли;
				
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ФормироватьРасходыПоПутевомуЛисту(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, тбзРасходы, мсвРейсы, Отказ)
	
	Результат = Ложь;
	Статус    = ДополнительныеСвойства.Статус;
		
	Если НЕ Статус = Перечисления.упСтатусыПутевогоЛиста.Отменен Тогда

		Результат = Истина;
		мсвРейсы  = Реквизиты.Рейсы.ВыгрузитьКолонку("Рейс");
		
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Процедура ИнициализироватьТаблицыРаспределений(тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, тбзРаспределениеРасходовНаПредприятие)
	
	Если тбзРаспределениеРасходов = Неопределено Тогда
		тбзРаспределениеРасходов = Новый ТаблицаЗначений;
		тбзРаспределениеРасходов.Колонки.Добавить("Рейс");
		тбзРаспределениеРасходов.Колонки.Добавить("ЗаданиеНаПеревозку");
		тбзРаспределениеРасходов.Колонки.Добавить("СтатьяРасходов");
		тбзРаспределениеРасходов.Колонки.Добавить("Валюта");
		тбзРаспределениеРасходов.Колонки.Добавить("АналитическийРазрез");
		тбзРаспределениеРасходов.Колонки.Добавить("Организация");
		тбзРаспределениеРасходов.Колонки.Добавить("Подразделение");
		тбзРаспределениеРасходов.Колонки.Добавить("НаправлениеДеятельности");
		тбзРаспределениеРасходов.Колонки.Добавить("сткСуммы");
	КонецЕсли;
	
	Если тбзРаспределениеРасходовНаТС = Неопределено Тогда
		тбзРаспределениеРасходовНаТС = Новый ТаблицаЗначений;
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("ТранспортноеСредство");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("СтатьяРасходов");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("Валюта");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("АналитическийРазрез");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("Организация");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("Подразделение");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("НаправлениеДеятельности");
		тбзРаспределениеРасходовНаТС.Колонки.Добавить("сткСуммы");
	КонецЕсли;
	
	Если тбзРаспределениеРасходовНаПредприятие = Неопределено Тогда
		тбзРаспределениеРасходовНаПредприятие = Новый ТаблицаЗначений;
		тбзРаспределениеРасходовНаПредприятие.Колонки.Добавить("Организация");
		тбзРаспределениеРасходовНаПредприятие.Колонки.Добавить("Подразделение");
		тбзРаспределениеРасходовНаПредприятие.Колонки.Добавить("НаправлениеДеятельности");
		тбзРаспределениеРасходовНаПредприятие.Колонки.Добавить("СтатьяРасходов");
		тбзРаспределениеРасходовНаПредприятие.Колонки.Добавить("сткСуммы");
	КонецЕсли;
	
КонецПроцедуры

Процедура РаспределитьРасходы(тбзРасходы, тбзРаспределениеРасходов, тбзРасходыНаТС, тбзРаспределениеРасходовНаТС, мсвРейсы, ДополнительныеСвойства, Отказ);
	
	Если тбзРасходы = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	ИспользуетсяУчетРасходовНаТС = Истина
							 И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетРасходовНаТС")
							 И ДополнительныеСвойства.РаспределятьРасходыНаТС;
	
	Если ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
		ЗаполнитьТаблицуРаспределенийПриИспользованииУправленияПеревозками(мсвРейсы, тбзРасходы, тбзРаспределениеРасходов, тбзРасходыНаТС, тбзРаспределениеРасходовНаТС, ИспользуетсяУчетРасходовНаТС, ДополнительныеСвойства, Отказ);	
	Иначе	
		ЗаполнитьТаблицуРаспределенийПриИспользованииУправленияТранспортом(мсвРейсы, тбзРасходы, тбзРаспределениеРасходов, Ложь, Отказ);
		Если Истина
			И ИспользуетсяУчетРасходовНаТС 
			И тбзРасходыНаТС <> Неопределено 
		Тогда
			ЗаполнитьТаблицуРаспределенийПриИспользованииУправленияТранспортом(мсвРейсы, тбзРасходыНаТС, тбзРаспределениеРасходовНаТС, Истина, Отказ);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СвернутьТаблицыРаспределений(ДополнительныеСвойства, тбзРаспределениеРасходов, тбзРаспределениеРасходовНаТС, тбзРаспределениеРасходовНаПредприятие, Отказ)
	сткСуммыШаблон = ШаблонСуммДляРаспределения();
	тбзРаспределениеРасходов 	= СвернутьТаблицуССуммамиВСтруктуре(тбзРаспределениеРасходов, "Рейс,ЗаданиеНаПеревозку,СтатьяРасходов,Валюта,АналитическийРазрез", сткСуммыШаблон, Отказ);
	Если ДополнительныеСвойства.РаспределятьРасходыНаТС Тогда
		тбзРаспределениеРасходовНаТС 	= СвернутьТаблицуССуммамиВСтруктуре(тбзРаспределениеРасходовНаТС, "ТранспортноеСредство,СтатьяРасходов,Валюта,АналитическийРазрез", сткСуммыШаблон, Отказ);	
	КонецЕсли;
	Если ДополнительныеСвойства.РаспределятьРасходыНаФинРезультат Тогда
		тбзРаспределениеРасходовНаПредприятие = СвернутьТаблицуССуммамиВСтруктуре(тбзРаспределениеРасходовНаПредприятие, "Организация,Подразделение,НаправлениеДеятельности,СтатьяРасходов", сткСуммыШаблон, Отказ);
	КонецЕсли;	
КонецПроцедуры

Процедура ЗаполнитьТаблицуРаспределенийПриИспользованииУправленияПеревозками(мсвРейсы, тбзРасходы, тбзРаспределениеРасходов, тбзРасходыНаТС, тбзРаспределениеРасходовНаТС, ИспользуетсяУчетРасходовНаТС, ДополнительныеСвойства, Отказ)
	
	ИспользоватьДопАналитическийРазрезПриРаспределенииРасходов 	= ПолучитьФункциональнуюОпцию("упИспользоватьДопАналитическийРазрезПриРаспределенииРасходов");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаРаспределенияРасходов(ИспользоватьДопАналитическийРазрезПриРаспределенииРасходов);
	УчитыватьФактическиеПараметры = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "УчитыватьФактическиеПараметры", Ложь);
	Запрос.УстановитьПараметр("УчитыватьФактическиеПараметры", УчитыватьФактическиеПараметры);

	мсвРезультат = Запрос.ВыполнитьПакет();
	
	ИндексАдресаПогрузкиРазгрузки	= 10;
	ИндексЗадания                 = 14;
	ИндексРейсы                   = 17;
	ИндексТС                      = 18;
		
	Если ИспользоватьДопАналитическийРазрезПриРаспределенииРасходов Тогда 
		ИндексАналитическиеРазрезы = 22;	
	Иначе
		ИндексАналитическиеРазрезы = 21;
	КонецЕсли;
	
	тбзАдресаПогрузкиРазгрузки = мсвРезультат[ИндексАдресаПогрузкиРазгрузки].Выгрузить();
	
	тбзПоказателиРейсы = мсвРезультат[ИндексРейсы].Выгрузить();
	тбзПоказателиРейсы.Колонки.Добавить("Коэффициент");
	тбзПоказателиРейсы.Колонки.Добавить("сткСуммы");
	
	тбзПоказателиЗадания = мсвРезультат[ИндексЗадания].Выгрузить();
	тбзПоказателиЗадания.Колонки.Добавить("Коэффициент");
	тбзПоказателиЗадания.Колонки.Добавить("сткСуммы");

	тбзПоказателиАналитическиеРазрезы = мсвРезультат[ИндексАналитическиеРазрезы].Выгрузить();
	тбзПоказателиАналитическиеРазрезы.Колонки.Добавить("Коэффициент");
	тбзПоказателиАналитическиеРазрезы.Колонки.Добавить("сткСуммы");
	
	ЗаполнитьРасстояниеВремяПоАдресамПогрузкиРазгрузки(тбзПоказателиРейсы, тбзПоказателиЗадания, тбзПоказателиАналитическиеРазрезы, тбзАдресаПогрузкиРазгрузки, ДополнительныеСвойства, Отказ);
	
	Если ИспользуетсяУчетРасходовНаТС Тогда
		тбзПоказателиАналитическиеРазрезыТС = тбзПоказателиАналитическиеРазрезы.Скопировать();
		тбзПоказателиАналитическиеРазрезыТС.Свернуть("ТранспортноеСредство, АналитическийРазрез", "Масса, Объем, Расстояние, Время, ПлановыеРасходыПоРейсу, КоличествоЗагрузочныхМетров,КоличествоМест, ОценочнаяСтоимостьГруза, ФактическиеРасходыПоРейсу");
		тбзПоказателиАналитическиеРазрезыТС.Колонки.Добавить("Коэффициент");
		тбзПоказателиАналитическиеРазрезыТС.Колонки.Добавить("сткСуммы");
	КонецЕсли;	
				
	ЗаполнитьТаблицуРаспределенийПоРейсам(тбзПоказателиРейсы, тбзПоказателиЗадания, тбзПоказателиАналитическиеРазрезы, тбзРасходы, тбзРаспределениеРасходов, Отказ);
	
	Если Истина
		И ИспользуетсяУчетРасходовНаТС
		И тбзРасходыНаТС <> Неопределено
	Тогда
		тбзПоказателиТС = мсвРезультат[ИндексТС].Выгрузить();
		тбзПоказателиТС.Колонки.Добавить("Коэффициент");
		тбзПоказателиТС.Колонки.Добавить("сткСуммы");

		ЗаполнитьТаблицуРаспределенийПоТранспортнымСредствам(тбзПоказателиТС, тбзПоказателиАналитическиеРазрезыТС, тбзРасходыНаТС, тбзРаспределениеРасходовНаТС, Отказ);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРасстояниеВремяПоАдресамПогрузкиРазгрузки(тбзПоказателиРейсы, тбзПоказателиЗадания, тбзПоказателиАналитическиеРазрезы, тбзАдресаПогрузкиРазгрузки, ДополнительныеСвойства, Отказ)
	
	ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов = Ложь;
		
	Если тбзПоказателиРейсы.Количество() > 0 Тогда
		ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов = тбзПоказателиРейсы[0].ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов;
	КонецЕсли;
	
	тбзРасстоянияМеждуАдресами	= упМаршрутизация.ПолучитьРасстояниеИВремяПоТаблицеАдресов(тбзАдресаПогрузкиРазгрузки, 3600, ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов);

	Для каждого СтрокаЗадание Из тбзПоказателиЗадания Цикл
				
		мсвРасстояние = тбзРасстоянияМеждуАдресами.НайтиСтроки(Новый Структура("АдресНачальнойТочки, АдресКонечнойТочки", СтрокаЗадание.АдресПогрузки, СтрокаЗадание.АдресРазгрузки));
		
		Если мсвРасстояние.Количество() > 0 Тогда
			КоличествоЗаданийВТочкеРазгрузки = СтрокаЗадание.КоличествоЗаданийВТочкеРазгрузки;
			СтрокаЗадание.Расстояние	= ?(НЕ КоличествоЗаданийВТочкеРазгрузки = 0, мсвРасстояние[0].Данные.Расстояние / КоличествоЗаданийВТочкеРазгрузки, 0);
			СтрокаЗадание.Время		= ?(НЕ КоличествоЗаданийВТочкеРазгрузки = 0, мсвРасстояние[0].Данные.Время / КоличествоЗаданийВТочкеРазгрузки, 0);
		Иначе	
			ОписаниеОшибки	= Нстр("ru = 'Не удалось рассчитать расстояния для адресов %1 и %2'");
			ОписаниеОшибки	= СтрШаблон(ОписаниеОшибки, СтрокаЗадание.АдресПогрузки, СтрокаЗадание.АдресРазгрузки); 
			ЗаписьЖурналаРегистрации(ДополнительныеСвойства.СобытиеОшибкаРаспределенияРасходов,УровеньЖурналаРегистрации.Предупреждение,,, ОписаниеОшибки);
		КонецЕсли;	
		
	КонецЦикла;
	
	////////////////////////////////////////////////////////////////////////////////
	// Так как расстояние и время отслеживается только по заданиям/грузовым местам,
	// то в качестве показателя расстояния/времени аналитического разреза берется расстояние/время которое проехал груз имеющий в составе товар,
	// с соответствующим аналитическим показателем.
	////////////////////////////////////////////////////////////////////////////////
	Для каждого СтрокаАналитическийПоказатель Из тбзПоказателиАналитическиеРазрезы Цикл
		
		мсвРасстояние = тбзРасстоянияМеждуАдресами.НайтиСтроки(Новый Структура("АдресНачальнойТочки, АдресКонечнойТочки", СтрокаАналитическийПоказатель.АдресПогрузки, СтрокаАналитическийПоказатель.АдресРазгрузки));
		
		Если мсвРасстояние.Количество() > 0 Тогда
			КоличествоАналитическихПоказателейВТочкеРазгрузки = СтрокаАналитическийПоказатель.КоличествоАналитическихРазрезовВТочкеРазгрузки;
			СтрокаАналитическийПоказатель.Расстояние	= ?(НЕ КоличествоАналитическихПоказателейВТочкеРазгрузки = 0, мсвРасстояние[0].Данные.Расстояние / КоличествоАналитическихПоказателейВТочкеРазгрузки, 0);
			СтрокаАналитическийПоказатель.Время		= ?(НЕ КоличествоАналитическихПоказателейВТочкеРазгрузки = 0, мсвРасстояние[0].Данные.Время / КоличествоАналитическихПоказателейВТочкеРазгрузки, 0);
		Иначе	
			ОписаниеОшибки	= Нстр("ru = 'Не удалось рассчитать расстояния для адресов %1 и %2'");
			ОписаниеОшибки	= СтрШаблон(ОписаниеОшибки, СтрокаАналитическийПоказатель.АдресПогрузки, СтрокаАналитическийПоказатель.АдресРазгрузки); 
			ЗаписьЖурналаРегистрации(ДополнительныеСвойства.СобытиеОшибкаРаспределенияРасходов,УровеньЖурналаРегистрации.Предупреждение,,, ОписаниеОшибки);
		КонецЕсли;	
		
	КонецЦикла;
	
	Если тбзПоказателиАналитическиеРазрезы.Количество() = 0 Тогда
		ЗаписьЖурналаРегистрации(ДополнительныеСвойства.СобытиеОшибкаРаспределенияРасходов,УровеньЖурналаРегистрации.Предупреждение,,, Нстр("ru = 'Не найдена база для распределения расходов'"));
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьТаблицуРаспределенийПоРейсам(тбзПоказателиРейсы, тбзПоказателиЗадания, тбзПоказателиАналитическиеРазрезы, тбзРасходы, тбзРаспределениеРасходов, Отказ)
	
	ЕстьКолонкаАналитическийРазрез = Ложь;
	Если НЕ тбзРасходы.Колонки.Найти("АналитическийРазрез") = Неопределено Тогда
		ЕстьКолонкаАналитическийРазрез = Истина;	
	КонецЕсли;
	
	Для каждого СтрокаРасход Из тбзРасходы Цикл
		
		текЗаданиеНаПеревозкуГрузаПоСтроке = СтрокаРасход.Задание;
		текАналитическийРазрезПоСтроке     = Справочники.упАналитическиеРазрезы.ПустаяСсылка();
		Если ЕстьКолонкаАналитическийРазрез Тогда
			текАналитическийРазрезПоСтроке  = СтрокаРасход.АналитическийРазрез;
		КонецЕсли;
					
		// Если в строке расходов заполнено задание.
		Если ЗначениеЗаполнено(текЗаданиеНаПеревозкуГрузаПоСтроке) Тогда
			
			// В распределении участвуют рейсы только с заданным заданием.
			тбзРейсыПоЗаданию = тбзПоказателиЗадания.Скопировать(Новый Структура("ЗаданиеНаПеревозку",текЗаданиеНаПеревозкуГрузаПоСтроке));
			мсвРейсы = тбзРейсыПоЗаданию.ВыгрузитьКолонку("Рейс");
			
			тбзРейсы = тбзПоказателиРейсы.СкопироватьКолонки();
			Для каждого СтрокаРейс Из тбзПоказателиРейсы Цикл
				Если НЕ мсвРейсы.Найти(СтрокаРейс.Рейс) = Неопределено Тогда
					НоваяСтрока = тбзРейсы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРейс);
				КонецЕсли;
			КонецЦикла;
		Иначе	
			тбзРейсы = тбзПоказателиРейсы.Скопировать();
		КонецЕсли;
		
		// Формула распределения.
		ПравилоРаспределения = СтрокаРасход.ПравилоРаспределения;
		сткРеквизитыПравила  = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоРаспределения, "Формула, СпособРаспределения, Показатель");
	
		// По формуле рассчитываются коэффициенты для  рейсов.
		РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(сткРеквизитыПравила.СпособРаспределения, сткРеквизитыПравила.Показатель, сткРеквизитыПравила.Формула, тбзРейсы, Новый Структура("Рейс"));
		
		
		сткСуммыПоРейсам	 = ШаблонСуммДляРаспределения();	// Накопительная сумма по мере обхода рейсов.
		РейсыКоличествоСтрок = тбзРейсы.Количество();
		й_Шаг = 1; 
		
		сткСуммыПоСтатьеРасходов	= СтрокаРасход.сткСуммы; 
		СтатьяРасходов           = СтрокаРасход.СтатьяРасходов;
		Валюта                   = СтрокаРасход.Валюта;
		
		// Сумма расходов распределяется по рейсам.
		Для каждого СтрокаРейс Из тбзРейсы Цикл
			
			текРейс 				= СтрокаРейс.Рейс;
			
			// Если распределяется сумма по последнему рейсу.
			Если й_Шаг = РейсыКоличествоСтрок Тогда
				СтрокаРейс.сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(сткСуммыПоСтатьеРасходов, сткСуммыПоРейсам, Отказ); 			// Берется остаток.
			Иначе
				СтрокаРейс.сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(сткСуммыПоСтатьеРасходов, СтрокаРейс.Коэффициент); 	// Сумма распределения умножается на коэффициент.
			КонецЕсли;
			сткСуммыПоРейсам		= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоРейсам, СтрокаРейс.сткСуммы, Отказ);
			
			// Формируется таблица заданий по рейсу.
			тбзЗадания	= тбзПоказателиЗадания.СкопироватьКолонки();
			сткОтбораЗадание = Новый Структура("Рейс",текРейс);
			
			// Если в строке расхода задано задание, то берется только это задание.
			Если ЗначениеЗаполнено(текЗаданиеНаПеревозкуГрузаПоСтроке) Тогда
				сткОтбораЗадание.Вставить("ЗаданиеНаПеревозку", текЗаданиеНаПеревозкуГрузаПоСтроке);
			КонецЕсли;
			
			мсвСтрокиЗадания = тбзПоказателиЗадания.НайтиСтроки(сткОтбораЗадание);
			Для каждого СтрокаЗадание Из мсвСтрокиЗадания Цикл
				НоваяСтрока = тбзЗадания.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗадание);
			КонецЦикла;
			
			РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(сткРеквизитыПравила.СпособРаспределения, сткРеквизитыПравила.Показатель, сткРеквизитыПравила.Формула, тбзЗадания, Новый Структура("Рейс, ЗаданиеНаПеревозку"));
			
			сткСуммыПоЗаданиям 		= ШаблонСуммДляРаспределения(); // Накопительная сумма по мере обхода заданий.
			ЗаданияКоличествоСтрок 	= тбзЗадания.Количество();
			ц_Шаг = 1;
			
			// Сумма расходов рейса распределяется по заданиям.
			Для каждого СтрокаЗадание Из тбзЗадания Цикл
				
				текЗаданиеНаПеревозкуГруза = СтрокаЗадание.ЗаданиеНаПеревозку;
				
				// Если распределяется последнее задание.
				Если ц_Шаг = ЗаданияКоличествоСтрок Тогда
					СтрокаЗадание.сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(СтрокаРейс.сткСуммы, сткСуммыПоЗаданиям, Отказ);          // Берется остаток.
				Иначе
					СтрокаЗадание.сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(СтрокаРейс.сткСуммы,СтрокаЗадание.Коэффициент);	// Сумма распределения умножается на коэффициент.
				КонецЕсли;
				сткСуммыПоЗаданиям		= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоЗаданиям,СтрокаЗадание.сткСуммы, Отказ);
				
				// Если в таблице расходов не задан аналитический разрез.
				Если НЕ ЗначениеЗаполнено(текАналитическийРазрезПоСтроке) Тогда

					// Формируется таблица аналитических разрезов по рейсу и заданию.
					тбзАналитическиеРазрезы = тбзПоказателиАналитическиеРазрезы.СкопироватьКолонки();
					
					сткОтбора = Новый Структура();
					сткОтбора.Вставить("Рейс", текРейс);
					сткОтбора.Вставить("ЗаданиеНаПеревозку", текЗаданиеНаПеревозкуГруза);
					
					мсвСтрокиАналитическиеРазрезы 	= тбзПоказателиАналитическиеРазрезы.НайтиСтроки(сткОтбора);
					Для каждого СтрокаАналитическийРазрез Из мсвСтрокиАналитическиеРазрезы Цикл
						текСтрока = тбзАналитическиеРазрезы.Добавить();
						ЗаполнитьЗначенияСвойств(текСтрока, СтрокаАналитическийРазрез);
					КонецЦикла;

					РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(сткРеквизитыПравила.СпособРаспределения, сткРеквизитыПравила.Показатель, сткРеквизитыПравила.Формула, тбзАналитическиеРазрезы, Новый Структура("Рейс, ЗаданиеНаПеревозку,АналитическийРазрез"));
					
					тбзАналитическиеРазрезы.Сортировать("Коэффициент");
					
					// Сумма расходов задания распределяется по аналитическим разрезам.
					сткСуммыПоАналитическимРазрезам	= ШаблонСуммДляРаспределения();
				
					у_Шаг = 1;
					АналитическиеРазрезыКоличествоСтрок = тбзАналитическиеРазрезы.Количество();
					Для каждого СтрокаАналитическийРазрез Из тбзАналитическиеРазрезы Цикл
						
						сткСуммы = Неопределено;
						
						// Если распределяется последняя строка.
						Если у_Шаг = АналитическиеРазрезыКоличествоСтрок Тогда
							сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(СтрокаЗадание.сткСуммы, сткСуммыПоАналитическимРазрезам, Отказ); 			// Берется остаток.
						Иначе
							сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(СтрокаЗадание.сткСуммы, СтрокаАналитическийРазрез.Коэффициент);	// Сумма распределения умножается на коэффициент.
						КонецЕсли;
						
						Если НЕ упСервисныеФункцииКлиентСервер.СтруктураРавнаНулю(сткСуммы) Тогда
							
							СтрокаРаспределение = тбзРаспределениеРасходов.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаАналитическийРазрез);
							СтрокаРаспределение.Валюта         = Валюта;
							СтрокаРаспределение.СтатьяРасходов = СтатьяРасходов;
							СтрокаРаспределение.сткСуммы       = сткСуммы;
							
							сткСуммыПоАналитическимРазрезам    = упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоАналитическимРазрезам, СтрокаРаспределение.сткСуммы, Отказ);
						КонецЕсли;
						
						у_Шаг = у_Шаг + 1;
					КонецЦикла;	
					
				// Если аналитический разрез задан (пока это есть только в прочих расходах)
				Иначе	
					
					// Вся сумма списывается на текущий аналитический разрез.
					СтрокаРаспределение = тбзРаспределениеРасходов.Добавить();
					СтрокаРаспределение.Рейс                = текРейс;
					СтрокаРаспределение.ЗаданиеНаПеревозку  = текЗаданиеНаПеревозкуГруза;
					СтрокаРаспределение.АналитическийРазрез = текАналитическийРазрезПоСтроке;
					СтрокаРаспределение.Валюта              = Валюта;
					СтрокаРаспределение.СтатьяРасходов      = СтатьяРасходов;
					СтрокаРаспределение.сткСуммы            = СтрокаЗадание.сткСуммы;
					
				КонецЕсли;
						
				Если Отказ Тогда
					Прервать;
				КонецЕсли;
				
				ц_Шаг = ц_Шаг + 1;
				
			КонецЦикла;
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
			й_Шаг = й_Шаг + 1;
		КонецЦикла;
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуРаспределенийПоТранспортнымСредствам(тбзПоказателиТС, тбзПоказателиАналитическиеРазрезы, тбзРасходыНаТС, тбзРаспределениеРасходовНаТС, Отказ)
	
	ЕстьКолонкаАналитическийРазрез = Ложь;
	Если НЕ тбзРасходыНаТС.Колонки.Найти("АналитическийРазрез") = Неопределено Тогда
		ЕстьКолонкаАналитическийРазрез = Истина;	
	КонецЕсли;
	
	Для каждого СтрокаРасход Из тбзРасходыНаТС Цикл
		
		АналитическийРазрезПоСтроке 		= Справочники.упАналитическиеРазрезы.ПустаяСсылка();
		Если ЕстьКолонкаАналитическийРазрез Тогда
			АналитическийРазрезПоСтроке  = СтрокаРасход.АналитическийРазрез;
		КонецЕсли;
		
		тбзТранспортныеСредства = тбзПоказателиТС.Скопировать();
		
		// Формула распределения.
		ПравилоРаспределения	= СтрокаРасход.ПравилоРаспределения;
		сткРеквизитыПравила 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоРаспределения, "Формула, СпособРаспределения, Показатель");
		
		// По формуле рассчитываются коэффициенты для  ТС.
		РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(сткРеквизитыПравила.СпособРаспределения, сткРеквизитыПравила.Показатель, сткРеквизитыПравила.Формула, тбзТранспортныеСредства, Новый Структура("ТранспортноеСредство"));
		
		сткСуммыПоТС	 = ШаблонСуммДляРаспределения();	// Накопительная сумма по мере обхода рейсов.
		ТСКоличествоСтрок = тбзТранспортныеСредства.Количество();
		й_Шаг = 1; 
		
		сткСуммыПоСтатьеРасходов	= СтрокаРасход.сткСуммы; 
		СтатьяРасходов 		= СтрокаРасход.СтатьяРасходов;
		Валюта				= СтрокаРасход.Валюта;
		
		// Сумма расходов распределяется по ТС.
		Для каждого СтрокаТС Из тбзТранспортныеСредства Цикл
			
			ТранспортноеСредство = СтрокаТС.ТранспортноеСредство;
			
			// Если распределяется сумма по последнему ТС.
			Если й_Шаг = ТСКоличествоСтрок Тогда
				СтрокаТС.сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(сткСуммыПоСтатьеРасходов, сткСуммыПоТС, Отказ); 			// Берется остаток.
			Иначе
				СтрокаТС.сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(сткСуммыПоСтатьеРасходов, СтрокаТС.Коэффициент); 	// Сумма распределения умножается на коэффициент.
			КонецЕсли;
			сткСуммыПоТС		= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоТС, СтрокаТС.сткСуммы, Отказ);
			
			// Если в таблице расходов не задан аналитический разрез.
			Если НЕ ЗначениеЗаполнено(АналитическийРазрезПоСтроке) Тогда
				
				// Формируется таблица аналитических разрезов по рейсу и заданию.
				тбзАналитическиеРазрезы = тбзПоказателиАналитическиеРазрезы.СкопироватьКолонки();
				
				сткОтбора = Новый Структура();
				сткОтбора.Вставить("ТранспортноеСредство", ТранспортноеСредство);
				
				мсвСтрокиАналитическиеРазрезы 	= тбзПоказателиАналитическиеРазрезы.НайтиСтроки(сткОтбора);
				Для каждого СтрокаАналитическийРазрез Из мсвСтрокиАналитическиеРазрезы Цикл
					НоваяСтрока = тбзАналитическиеРазрезы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока , СтрокаАналитическийРазрез);
				КонецЦикла;
				
				РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(сткРеквизитыПравила.СпособРаспределения, сткРеквизитыПравила.Показатель, сткРеквизитыПравила.Формула, тбзАналитическиеРазрезы, Новый Структура("ТранспортноеСредство, АналитическийРазрез"));
				
				// Сумма расходов задания распределяется по аналитическим разрезам.
				сткСуммыПоАналитическимРазрезам	= ШаблонСуммДляРаспределения();
				
				у_Шаг = 1;
				АналитическиеРазрезыКоличествоСтрок = тбзАналитическиеРазрезы.Количество();
				Для каждого СтрокаАналитическийРазрез Из тбзАналитическиеРазрезы Цикл
					
					сткСуммы = Неопределено;
					
					// Если распределяется последняя строка.
					Если у_Шаг = АналитическиеРазрезыКоличествоСтрок Тогда
						сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(СтрокаТС.сткСуммы, сткСуммыПоАналитическимРазрезам, Отказ); 			// Берется остаток.
					Иначе
						сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(СтрокаТС.сткСуммы, СтрокаАналитическийРазрез.Коэффициент);	// Сумма распределения умножается на коэффициент.
					КонецЕсли;
					
					Если НЕ упСервисныеФункцииКлиентСервер.СтруктураРавнаНулю(сткСуммы) Тогда
						
						СтрокаРаспределение = тбзРаспределениеРасходовНаТС.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаАналитическийРазрез);
						СтрокаРаспределение.Валюта 		= Валюта;
						СтрокаРаспределение.СтатьяРасходов = СтатьяРасходов;
						СтрокаРаспределение.сткСуммы 		= сткСуммы;
						
						сткСуммыПоАналитическимРазрезам	= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоАналитическимРазрезам, СтрокаРаспределение.сткСуммы, Отказ);
					КонецЕсли;
					
					у_Шаг = у_Шаг + 1;
				КонецЦикла;	
				
			// Если аналитический разрез задан (пока это есть только в прочих расходах)
			Иначе	
				
				// Вся сумма списывается на текущий аналитический разрез.
				СтрокаРаспределение = тбзРаспределениеРасходовНаТС.Добавить();
				СтрокаРаспределение.ТранспортноеСредство = ТранспортноеСредство;
				СтрокаРаспределение.АналитическийРазрез	= АналитическийРазрезПоСтроке;
				СтрокаРаспределение.Валюта 			= Валюта;
				СтрокаРаспределение.СтатьяРасходов 	= СтатьяРасходов;
				СтрокаРаспределение.сткСуммы 			= СтрокаТС.сткСуммы;
				
			КонецЕсли;
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
			й_Шаг = й_Шаг + 1;
		КонецЦикла;
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуРаспределенийПриИспользованииУправленияТранспортом(мсвРейсы, тбзРасходы, тбзРаспределение, РаспределениеРасходовНаТС, Отказ)
	
	Если РаспределениеРасходовНаТС Тогда
		ИмяОбъекта = "ТранспортноеСредство";	
	Иначе	
		ИмяОбъекта = "Рейс";	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаРаспределенияРасходовУправлениеТранспортом(РаспределениеРасходовНаТС);
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	
	тбзПоказатели = Запрос.Выполнить().Выгрузить();
	
	// Объекты распределения, это пока либо рейсы либо транспортные средства
	тбзОбъектыРаспределения = тбзПоказатели.Скопировать();
	тбзОбъектыРаспределения.Колонки.Добавить("Коэффициент");
	тбзОбъектыРаспределения.Колонки.Добавить("сткСуммы");
				
	Для каждого СтрокаРасход Из тбзРасходы Цикл
			
			// Формула распределения.
			ПравилоРаспределения	= СтрокаРасход.ПравилоРаспределения;
			сткРеквизитыПравила 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоРаспределения, "Формула, СпособРаспределения, Показатель");
		
			// По формуле рассчитываются коэффициенты для  рейсов.
			РассчитатьКоэффициентыРаспределенияПропорциональноПараметру(сткРеквизитыПравила.СпособРаспределения, сткРеквизитыПравила.Показатель, сткРеквизитыПравила.Формула, тбзОбъектыРаспределения, Новый Структура(ИмяОбъекта));
						
			сткСуммыПоОбъектам = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ШаблонСуммДляРаспределения());	// Накопительная сумма по мере обхода объектов распределения.
			КоличествоСтрокОбъектов = тбзОбъектыРаспределения.Количество();
			й_Шаг = 1; 
			
			сткСуммыПоСтатьеРасходов	= СтрокаРасход.сткСуммы; 
			СтатьяРасходов 		= СтрокаРасход.СтатьяРасходов;
			Валюта				= СтрокаРасход.Валюта;
			
			// Сумма расходов распределяется по рейсам.
			Для каждого СтрокаОбъект Из тбзОбъектыРаспределения Цикл
				
				// Если распределяется сумма по последнему рейсу.
				Если й_Шаг = КоличествоСтрокОбъектов Тогда
					СтрокаОбъект.сткСуммы = упСервисныеФункцииКлиентСервер.РазностьСтруктур(сткСуммыПоСтатьеРасходов, сткСуммыПоОбъектам, Отказ); 		// Берется остаток.
				Иначе
					СтрокаОбъект.сткСуммы = упСервисныеФункцииКлиентСервер.УмножитьСтруктуНаЗначение(сткСуммыПоСтатьеРасходов, СтрокаОбъект.Коэффициент); 	// Сумма распределения умножается на коэффициент.
				КонецЕсли;
				
				Если НЕ упСервисныеФункцииКлиентСервер.СтруктураРавнаНулю(СтрокаОбъект.сткСуммы) Тогда
					сткСуммыПоОбъектам	= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСуммыПоОбъектам, СтрокаОбъект.сткСуммы, Отказ);
					
					СтрокаРаспределение = тбзРаспределение.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРаспределение, СтрокаОбъект);
					СтрокаРаспределение.Валюта 			= Валюта;
					СтрокаРаспределение.СтатьяРасходов 	= СтатьяРасходов;
					
				КонецЕсли;	
														
				й_Шаг = й_Шаг + 1;
			КонецЦикла;
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
КонецПроцедуры

Функция СвернутьТаблицуССуммамиВСтруктуре(тбзРаспределение, ИменаПолей, сткСуммыШаблон, Отказ)
	
	тбзРаспределениеСвернутое = тбзРаспределение.Скопировать();
	
	Если тбзРаспределение.Количество() >  0 Тогда

		тбзРаспределениеСвернутое.Свернуть(ИменаПолей,"сткСуммы");
		
		Для каждого Строка Из тбзРаспределениеСвернутое Цикл
			
			сткПоиска = Новый Структура(ИменаПолей);
			ЗаполнитьЗначенияСвойств(сткПоиска, Строка);
			мсвСтрокРаспределения = тбзРаспределение.НайтиСтроки(сткПоиска);
			
			сткСумма = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткСуммыШаблон);
			
			Для каждого СтрокаРаспределение Из мсвСтрокРаспределения Цикл
				
				сткСумма	= упСервисныеФункцииКлиентСервер.СложитьСтруктуры(сткСумма, СтрокаРаспределение.сткСуммы, Отказ);
				
				Если Отказ Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение расходов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Строка.сткСуммы = сткСумма;
			
		КонецЦикла;
	КонецЕсли;
	Возврат тбзРаспределениеСвернутое;
КонецФункции

Функция ТекстЗапросаРаспределенияРасходов(ИспользоватьДопАналитическийРазрезПриРаспределенииРасходов)
	
	Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Все работы по рейсу
	|ВЫБРАТЬ
	|	тбРаботы.Рейс КАК Рейс,
	|	тбРаботы.Идентификатор КАК Идентификатор,
	|	тбРаботы.Отменена КАК Отменена,
	|	тбРаботы.Задание КАК Задание,
	|	тбРаботы.Операция КАК Операция,
	|	тбРаботы.ГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК тбРаботы
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Документы выполнявшие изменения в работах рейса
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРаботыПоРейсу.Рейс КАК Рейс,
	|	упРаботыПоРейсу.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втРегистраторы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ ИСТИНА
	|	И упРаботыПоРейсу.Рейс В  (&мсвРейсы)
	|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Подсчет количества работ и отмененных работ по рейсам/заданиям
	|ВЫБРАТЬ
	|	втРаботы.Рейс КАК Рейс,
	|	втРаботы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА втРаботы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ КАК ГрузовоеМесто,
	|	СУММА(ВЫБОР
	|		КОГДА втРаботы.Отменена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтменена,
	|	СУММА(1) КАК КоличествоОпераций
	|ПОМЕСТИТЬ втОтмененныеЗадания
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ ЛОЖЬ
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Рейс,
	|	втРаботы.Задание,
	|	ВЫБОР
	|		КОГДА втРаботы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Не отмененные грузовые места по рейсам
	|ВЫБРАТЬ
	|	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов,
	|	упРейсЗадания.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаданиям
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упРейсЗадания.Ссылка = втОтмененныеЗадания.Рейс
	|		И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК тбПеревозчикПоРейсу
	|	ПО тбПеревозчикПоРейсу.Рейс = упРейсЗадания.Ссылка
	|ГДЕ ИСТИНА
	|	И упРейсЗадания.Ссылка В (&мсвРейсы)
	|	И (ЛОЖЬ
	|		ИЛИ НЕ (ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0))
	|		ИЛИ ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) = 0)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРегистраторы.Рейс КАК Рейс,
	|	упКорректировкаРейсаЗадания.Задание КАК Задание,
	|	упКорректировкаРейсаЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упКорректировкаРейсаЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упКорректировкаРейсаЗадания.КоличествоГрузов КАК КоличествоГрузов,
	|	втРегистраторы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
	|ИЗ
	|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	|	ПО ИСТИНА
	|		И упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
	|		И упКорректировкаРейсаЗадания.Ссылка.Рейс = втРегистраторы.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упКорректировкаРейсаЗадания.Ссылка.Рейс = втОтмененныеЗадания.Рейс
	|		И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК тбПеревозчикПоРейсу
	|	ПО тбПеревозчикПоРейсу.Рейс = втРегистраторы.Рейс
	|ГДЕ ЛОЖЬ
	|	ИЛИ НЕ (ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0))
	|	ИЛИ ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) = 0
	|;
	|//{Запрос: 4, -15 ////////////////////////////////////////
	|// Характеристики грузовых мест по заданиям
	|ВЫБРАТЬ
	|	тбГрузовыеМеста.Рейс КАК Рейс,
	|	тбГрузовыеМеста.ТранспортноеСредство КАК ТранспортноеСредство,
	|	тбГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	тбГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	СУММА(тбГрузовыеМеста.Масса) КАК Масса,
	|	СУММА(тбГрузовыеМеста.Объем) КАК Объем,
	|	СУММА(тбГрузовыеМеста.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(тбГрузовыеМеста.КоличествоМест) КАК КоличествоМест,
	|	СУММА(тбГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(тбГрузовыеМеста.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	(ВЫБРАТЬ
	|		упРейсЗадания.Рейс КАК Рейс,
	|		упРейсЗадания.ТранспортноеСредство КАК ТранспортноеСредство,
	|		упРейсЗадания.Задание КАК ЗаданиеНаПеревозку,
	|		ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) КАК Масса,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) КАК Объем,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) КАК КоличествоМест,
	|		1 КАК КоличествоГрузов,
	|		упРейсЗадания.Задание.ОценочнаяСтоимость КАК ОценочнаяСтоимостьГруза
	|	ИЗ
	|		втГрузовыеМестаПоЗаданиям КАК упРейсЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания
	|				ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|			,
	|			ЗаданиеНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.Задание КАК Задание
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ГДЕ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упРейсЗадания.Рейс КАК Рейс,
	|		упРейсЗадания.ТранспортноеСредство КАК ТранспортноеСредство,
	|		упРейсЗадания.Задание КАК ЗаданиеНаПеревозку,
	|		упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, ЕСТЬNULL(упГрузовыеМеста.Масса, 0))) КАК Масса,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, ЕСТЬNULL(упГрузовыеМеста.Объем, 0))) КАК Объем,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упГрузовыеМеста.КоличествоЗагрузочныхМетров, 0))) КАК КоличествоЗагрузочныхМетров,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, ЕСТЬNULL(упГрузовыеМеста.КоличествоМест, 0))) КАК КоличествоМест,
	|		упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упГрузовыеМеста.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимостьГруза
	|	ИЗ
	|		втГрузовыеМестаПоЗаданиям КАК упРейсЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ПО упРейсЗадания.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания
	|				ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|			,
	|			ЗаданиеНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.Задание КАК Задание
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ГДЕ ЛОЖЬ
	|		ИЛИ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса)
	|		ИЛИ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упРейсЗадания.Рейс КАК Рейс,
	|		упРейсЗадания.ТранспортноеСредство КАК ТранспортноеСредство,
	|		упРейсЗадания.Задание КАК ЗаданиеНаПеревозку,
	|		ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) КАК Масса,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) КАК Объем,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) КАК КоличествоМест,
	|		1 КАК КоличествоГрузов,
	|		упРейсЗадания.Задание.ОценочнаяСтоимость КАК ОценочнаяСтоимостьГруза
	|	ИЗ
	|		втГрузовыеМестаПоЗаданиям КАК упРейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|		ПО упРейсЗадания.Задание = ГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания
	|				ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|			,
	|			ЗаданиеНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.Задание КАК Задание
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ГДЕ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)) КАК тбГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	тбГрузовыеМеста.Рейс,
	|	тбГрузовыеМеста.ТранспортноеСредство,
	|	тбГрузовыеМеста.ЗаданиеНаПеревозку,
	|	тбГрузовыеМеста.ГрузовоеМесто
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|// Оценочная стоимость грузов рейса
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Рейс КАК Рейс,
	|	СУММА(втГрузовыеМеста.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза
	|ПОМЕСТИТЬ втРейсОценочнаяСтоимостьГруза
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Рейс
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|// Маршрут рейса
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.Адрес КАК Адрес
	|ПОМЕСТИТЬ втМаршрутРейса
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&мсвРейсы)) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ НЕ (упМаршрутПоРейсуСрезПоследних.Отменена)
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|// Точки погрузки/разгрузки грузовых мест и индекс точки разгрузки
	|ВЫБРАТЬ
	|	РаботыПогрузки.Рейс КАК Рейс,
	|	РаботыПогрузки.Задание КАК Задание,
	|	РаботыПогрузки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	МаршрутРейсаПогрузка.Адрес КАК АдресПогрузки,
	|	МаршрутРейсаРазгрузка.Адрес КАК АдресРазгрузки,
	|	ЕСТЬNULL(МаршрутРейсаРазгрузка.СтрокаНомер, 0) КАК ПорядковыйНомерТочкиРазгрузки
	|ПОМЕСТИТЬ втАдресаПогрузкиРазгрузкиПоГрузовымМестам
	|ИЗ
	|	втРаботы КАК РаботыПогрузки
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК РаботыРазгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМаршрутРейса КАК МаршрутРейсаРазгрузка
	|		ПО ИСТИНА
	|			И РаботыРазгрузки.Рейс = МаршрутРейсаРазгрузка.Рейс
	|			И РаботыРазгрузки.Идентификатор = МаршрутРейсаРазгрузка.Идентификатор
	|	ПО ИСТИНА
	|		И РаботыПогрузки.Рейс = РаботыРазгрузки.Рейс
	|		И (ЛОЖЬ
	|			ИЛИ РаботыРазгрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|			ИЛИ РаботыРазгрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|		И РаботыПогрузки.Задание = РаботыРазгрузки.Задание
	|		И РаботыПогрузки.ГрузовоеМесто = РаботыРазгрузки.ГрузовоеМесто
	|		И НЕ (РаботыРазгрузки.Отменена)
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМаршрутРейса КАК МаршрутРейсаПогрузка
	|	ПО ИСТИНА
	|		И РаботыПогрузки.Рейс = МаршрутРейсаПогрузка.Рейс
	|		И РаботыПогрузки.Идентификатор = МаршрутРейсаПогрузка.Идентификатор
	|ГДЕ ИСТИНА
	|	И НЕ (РаботыПогрузки.Отменена)
	|	И (ЛОЖЬ
	|		ИЛИ РаботыПогрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|		ИЛИ РаботыПогрузки.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|// Индексы первых точек разгрузок по заданиям
	|ВЫБРАТЬ
	|	втАдресаПогрузкиРазгрузки.Рейс КАК Рейс,
	|	втАдресаПогрузкиРазгрузки.Задание КАК Задание,
	|	МИНИМУМ(втАдресаПогрузкиРазгрузки.ПорядковыйНомерТочкиРазгрузки) КАК ПорядковыйНомерТочкиРазгрузки
	|ПОМЕСТИТЬ втАдресаРазгрузкиПоЗаданиям
	|ИЗ
	|	втАдресаПогрузкиРазгрузкиПоГрузовымМестам КАК втАдресаПогрузкиРазгрузки
	|СГРУППИРОВАТЬ ПО
	|	втАдресаПогрузкиРазгрузки.Рейс,
	|	втАдресаПогрузкиРазгрузки.Задание
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|//Адреса погрузок и первых точек разгрузок по заданиям
	|ВЫБРАТЬ
	|	втАдресаПогрузкиРазгрузки.Рейс КАК Рейс,
	|	втАдресаПогрузкиРазгрузки.Задание КАК Задание,
	|	втАдресаПогрузкиРазгрузки.АдресПогрузки КАК АдресПогрузки,
	|	втАдресаПогрузкиРазгрузки.АдресРазгрузки КАК АдресРазгрузки,
	|	СУММА(1) КАК КоличествоЗаданийВТочкеРазгрузки
	|ПОМЕСТИТЬ втАдресаПогрузкиРазгрузкиПоЗаданиям
	|ИЗ
	|	втАдресаПогрузкиРазгрузкиПоГрузовымМестам КАК втАдресаПогрузкиРазгрузки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдресаРазгрузкиПоЗаданиям КАК втТочкиРазгрузкиПоЗаданиям
	|	ПО ИСТИНА
	|		И втАдресаПогрузкиРазгрузки.Рейс = втТочкиРазгрузкиПоЗаданиям.Рейс
	|		И втАдресаПогрузкиРазгрузки.Задание = втТочкиРазгрузкиПоЗаданиям.Задание
	|		И втАдресаПогрузкиРазгрузки.ПорядковыйНомерТочкиРазгрузки = втТочкиРазгрузкиПоЗаданиям.ПорядковыйНомерТочкиРазгрузки
	|СГРУППИРОВАТЬ ПО
	|	втАдресаПогрузкиРазгрузки.Рейс,
	|	втАдресаПогрузкиРазгрузки.Задание,
	|	втАдресаПогрузкиРазгрузки.АдресПогрузки,
	|	втАдресаПогрузкиРазгрузки.АдресРазгрузки
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|// Различные адреса погрузки/разгрузки
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тбАдреса.АдресПогрузки КАК АдресНачальнойТочки,
	|	тбАдреса.АдресРазгрузки КАК АдресКонечнойТочки
	|ИЗ
	|	(ВЫБРАТЬ
	|		втАдресаПогрузкиРазгрузки.АдресПогрузки КАК АдресПогрузки,
	|		втАдресаПогрузкиРазгрузки.АдресРазгрузки КАК АдресРазгрузки
	|	ИЗ
	|		втАдресаПогрузкиРазгрузкиПоГрузовымМестам КАК втАдресаПогрузкиРазгрузки
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втАдресаПоЗаданиям.АдресПогрузки КАК АдресПогрузки,
	|		втАдресаПоЗаданиям.АдресРазгрузки КАК АдресРазгрузки
	|	ИЗ
	|		втАдресаПогрузкиРазгрузкиПоЗаданиям КАК втАдресаПоЗаданиям) КАК тбАдреса
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|// Количество заданий в адресах разгрузки
	|ВЫБРАТЬ
	|	втАдресаПогрузкиРазгрузкиПоЗаданиямТ.АдресРазгрузки КАК АдресРазгрузки,
	|	СУММА(втАдресаПогрузкиРазгрузкиПоЗаданиямТ.КоличествоЗаданийВТочкеРазгрузки) КАК КоличествоЗаданийВТочкеРазгрузки,
	|	втАдресаПогрузкиРазгрузкиПоЗаданиямТ.Рейс КАК Рейс
	|ПОМЕСТИТЬ втКоличествоЗаданийВАдресахРазгрузки
	|ИЗ
	|	втАдресаПогрузкиРазгрузкиПоЗаданиям КАК втАдресаПогрузкиРазгрузкиПоЗаданиямТ
	|СГРУППИРОВАТЬ ПО
	|	втАдресаПогрузкиРазгрузкиПоЗаданиямТ.АдресРазгрузки,
	|	втАдресаПогрузкиРазгрузкиПоЗаданиямТ.Рейс
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРасходыОбороты.Рейс КАК Рейс,
	|	упРасходыОбороты.СуммаФактСНДСОборот КАК СуммаФактСНДСОборот,
	|	упРасходыОбороты.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	упРасходыОбороты.АналитическийРазрез КАК АналитическийРазрез
	|ПОМЕСТИТЬ втФактическиеРасходы
	|ИЗ
	|	РегистрНакопления.упРасходы.Обороты(
	|		,
	|		,
	|		,
	|		Рейс В (&мсвРейсы)) КАК упРасходыОбороты
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втФактическиеРасходыТ.Рейс КАК Рейс,
	|	втФактическиеРасходыТ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	СУММА(втФактическиеРасходыТ.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
	|ПОМЕСТИТЬ втФактическиеРасходыПоЗаданиям
	|ИЗ
	|	втФактическиеРасходы КАК втФактическиеРасходыТ
	|СГРУППИРОВАТЬ ПО
	|	втФактическиеРасходыТ.Рейс,
	|	втФактическиеРасходыТ.ЗаданиеНаПеревозку
	|;
	|//{Запрос: 14 ////////////////////////////////////////
	|// Значения показателей распределения по заданиям
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Рейс КАК Рейс,
	|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	СУММА(втГрузовыеМеста.Масса) КАК Масса,
	|	СУММА(втГрузовыеМеста.Объем) КАК Объем,
	|	СУММА(втГрузовыеМеста.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втГрузовыеМеста.КоличествоМест) КАК КоличествоМест,
	|	СУММА(втГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	|	СУММА(втГрузовыеМеста.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	0 КАК Расстояние,
	|	0 КАК Время,
	|	1 КАК ПлановыеРасходыПоРейсу,
	|	МИНИМУМ(втАдресаПогрузкиРазгрузкиПоЗаданиямТ.АдресПогрузки) КАК АдресПогрузки,
	|	МИНИМУМ(втАдресаПогрузкиРазгрузкиПоЗаданиямТ.АдресРазгрузки) КАК АдресРазгрузки,
	|	МИНИМУМ(ЕСТЬNULL(втКоличествоЗаданийВАдресахРазгрузкиТ.КоличествоЗаданийВТочкеРазгрузки, 0)) КАК КоличествоЗаданийВТочкеРазгрузки,
	|	СУММА(ЕСТЬNULL(втФактическиеРасходыПоЗаданиямТ.СуммаФактСНДСОборот, 0)) КАК ФактическиеРасходыПоРейсу
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаПогрузкиРазгрузкиПоЗаданиям КАК втАдресаПогрузкиРазгрузкиПоЗаданиямТ
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.Рейс = втАдресаПогрузкиРазгрузкиПоЗаданиямТ.Рейс
	|		И втГрузовыеМеста.ЗаданиеНаПеревозку = втАдресаПогрузкиРазгрузкиПоЗаданиямТ.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоЗаданийВАдресахРазгрузки КАК втКоличествоЗаданийВАдресахРазгрузкиТ
	|	ПО ИСТИНА
	|		И втАдресаПогрузкиРазгрузкиПоЗаданиямТ.Рейс = втКоличествоЗаданийВАдресахРазгрузкиТ.Рейс
	|		И втАдресаПогрузкиРазгрузкиПоЗаданиямТ.АдресРазгрузки = втКоличествоЗаданийВАдресахРазгрузкиТ.АдресРазгрузки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|	ПО упРейс.Ссылка = втГрузовыеМеста.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРасходыПоЗаданиям КАК втФактическиеРасходыПоЗаданиямТ
	|	ПО ИСТИНА
	|		И втФактическиеРасходыПоЗаданиямТ.ЗаданиеНаПеревозку = втГрузовыеМеста.ЗаданиеНаПеревозку
	|		И втФактическиеРасходыПоЗаданиямТ.Рейс = втГрузовыеМеста.Рейс
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Рейс,
	|	упРейс.НаправлениеДеятельности,
	|	втГрузовыеМеста.ЗаданиеНаПеревозку
	|;
	|//{Запрос: 15 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втФактическиеРасходыТ.Рейс КАК Рейс,
	|	СУММА(втФактическиеРасходыТ.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
	|ПОМЕСТИТЬ втФактическиеРасходыПоРейсам
	|ИЗ
	|	втФактическиеРасходы КАК втФактическиеРасходыТ
	|СГРУППИРОВАТЬ ПО
	|	втФактическиеРасходыТ.Рейс
	|;
	|//{Запрос: 16 ////////////////////////////////////////
	|// Значение показателей распределения по рейсам
	|ВЫБРАТЬ
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Масса, упПлановыеПараметрыРейса.Масса), 0) КАК Масса,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Объем, упПлановыеПараметрыРейса.Объем), 0) КАК Объем,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.КоличествоМест, упПлановыеПараметрыРейса.КоличествоМест), 0) КАК КоличествоМест,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.КоличествоЗагрузочныхМетров, упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров), 0) КАК КоличествоЗагрузочныхМетров,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упФактическиеПараметрыРейса.Пробег, 0) <> 0
	|			ТОГДА упФактическиеПараметрыРейса.Пробег
	|		ИНАЧЕ ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Расстояние, упПлановыеПараметрыРейса.Расстояние), 0)
	|	КОНЕЦ КАК Расстояние,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Время, упПлановыеПараметрыРейса.Время), 0) КАК Время,
	|	ЕСТЬNULL(упВсегоРасходыПоРейсу.СуммаПланСНДС, 0) КАК ПлановыеРасходыПоРейсу,
	|	ЕСТЬNULL(втРейсОценочнаяСтоимостьГруза.ОценочнаяСтоимостьГруза, 0) КАК ОценочнаяСтоимостьГруза,
	|	ЕСТЬNULL(втФактическиеРасходы.СуммаФактСНДСОборот, 0) КАК ФактическиеРасходыПоРейсу,
	|	упРейс.ВидПеревозки.ОбращатьсяКМаршрутизаторуПриРасчетеРасходов КАК ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов,
	|	упРейс.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|	ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	|	ПО ИСТИНА
	|		И &УчитыватьФактическиеПараметры
	|		И упРейс.Ссылка = упФактическиеПараметрыРейса.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВсегоРасходыПоРейсу КАК упВсегоРасходыПоРейсу
	|	ПО упРейс.Ссылка = упВсегоРасходыПоРейсу.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРейсОценочнаяСтоимостьГруза КАК втРейсОценочнаяСтоимостьГруза
	|	ПО упРейс.Ссылка = втРейсОценочнаяСтоимостьГруза.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРасходыПоРейсам КАК втФактическиеРасходы
	|	ПО упРейс.Ссылка = втФактическиеРасходы.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК тбПеревозчикПоРейсу
	|	ПО упРейс.Ссылка = тбПеревозчикПоРейсу.Рейс
	|ГДЕ упРейс.Ссылка В  (&мсвРейсы)
	|;
	|//{Запрос: 17 ////////////////////////////////////////
	|// Значение показателей распределения по рейсам
	|ВЫБРАТЬ
	|	втРейсы.*
	|ИЗ
	|	втРейсы КАК втРейсы
	|;
	|//{Запрос: 18 ////////////////////////////////////////
	|// Значение показателей распределения по транспортным средствам
	|ВЫБРАТЬ
	|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	СУММА(втРейсы.Масса) КАК Масса,
	|	СУММА(втРейсы.Объем) КАК Объем,
	|	СУММА(втРейсы.КоличествоМест) КАК КоличествоМест,
	|	СУММА(втРейсы.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втРейсы.Расстояние) КАК Расстояние,
	|	СУММА(втРейсы.Время) КАК Время,
	|	СУММА(втРейсы.ПлановыеРасходыПоРейсу) КАК ПлановыеРасходыПоРейсу,
	|	СУММА(втРейсы.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	СУММА(втРейсы.ФактическиеРасходыПоРейсу) КАК ФактическиеРасходыПоРейсу
	|ИЗ
	|	втРейсы КАК втРейсы
	|ГДЕ втРейсы.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	втРейсы.ТранспортноеСредство
	|";
		
	Текст = Текст + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	Если ИспользоватьДопАналитическийРазрезПриРаспределенииРасходов Тогда
		
		Текст = Текст + "
		|//{Запрос: 19 ////////////////////////////////////////
		|// Аналитические разрезы
		|ВЫБРАТЬ
		|	тбГрузовыеМеста.Рейс КАК Рейс,
		|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	упРейс.Подразделение КАК Подразделение,
		|	тбГрузовыеМеста.ТранспортноеСредство КАК ТранспортноеСредство,
		|	тбГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	ЕСТЬNULL(тбТоварныйСостав.АналитическийРазрез, ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)) КАК АналитическийРазрез,
		|	СУММА(ВЫБОР
		|		КОГДА тбГрузовыеМеста.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА 0
		|		КОГДА тбТоварныйСостав.Ссылка ЕСТЬ NULL
		|			ТОГДА тбГрузовыеМеста.Масса
		|		КОГДА тбТоварныйСостав.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА тбТоварныйСостав.Номенклатура.Масса * тбТоварныйСостав.Количество * тбГрузовыеМеста.КоличествоГрузов
		|		ИНАЧЕ тбТоварныйСостав.УпаковкаНоменклатуры.Масса * тбТоварныйСостав.Количество * тбГрузовыеМеста.КоличествоГрузов
		|	КОНЕЦ) КАК Масса,
		|	СУММА(ВЫБОР
		|		КОГДА тбГрузовыеМеста.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА 0
		|		КОГДА тбТоварныйСостав.Ссылка ЕСТЬ NULL
		|			ТОГДА тбГрузовыеМеста.Объем
		|		КОГДА тбТоварныйСостав.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА тбТоварныйСостав.Номенклатура.Объем * тбТоварныйСостав.Количество * тбГрузовыеМеста.КоличествоГрузов
		|		ИНАЧЕ тбТоварныйСостав.УпаковкаНоменклатуры.Объем * тбТоварныйСостав.Количество * тбГрузовыеМеста.КоличествоГрузов
		|	КОНЕЦ) КАК Объем,
		|	СУММА(ВЫБОР
		|		КОГДА тбГрузовыеМеста.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА 0
		|		КОГДА тбТоварныйСостав.Ссылка ЕСТЬ NULL
		|			ТОГДА тбГрузовыеМеста.ОценочнаяСтоимостьГруза
		|		ИНАЧЕ тбТоварныйСостав.Сумма * тбГрузовыеМеста.КоличествоГрузов
		|	КОНЕЦ) КАК ОценочнаяСтоимостьГруза,
		|	МИНИМУМ(ЕСТЬNULL(втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.АдресПогрузки, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))) КАК АдресПогрузки,
		|	МИНИМУМ(ЕСТЬNULL(втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.АдресРазгрузки, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))) КАК АдресРазгрузки
		|ПОМЕСТИТЬ втАналитическиеРазрезы
		|ИЗ
		|	втГрузовыеМеста КАК тбГрузовыеМеста
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК тбТоварныйСостав
		|	ПО тбГрузовыеМеста.ГрузовоеМесто = тбТоварныйСостав.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаПогрузкиРазгрузкиПоГрузовымМестам КАК втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ
		|	ПО ИСТИНА
		|		И тбГрузовыеМеста.ГрузовоеМесто = втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.ГрузовоеМесто
		|		И тбГрузовыеМеста.ЗаданиеНаПеревозку = втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.Задание
		|		И тбГрузовыеМеста.Рейс = втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.Рейс
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
		|	ПО упРейс.Ссылка = тбГрузовыеМеста.Рейс
		|СГРУППИРОВАТЬ ПО
		|	тбГрузовыеМеста.Рейс,
		|	упРейс.НаправлениеДеятельности,
		|	упРейс.Подразделение,
		|	тбГрузовыеМеста.ЗаданиеНаПеревозку,
		|	тбТоварныйСостав.АналитическийРазрез,
		|	тбГрузовыеМеста.ТранспортноеСредство
		|;
		|//{Запрос: 20 ////////////////////////////////////////
		|// Количество аналитических разрезов
		|ВЫБРАТЬ
		|	втАналитическиеРазрезы.Рейс КАК Рейс,
		|	втАналитическиеРазрезы.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втАналитическиеРазрезы.АналитическийРазрез) КАК КоличествоАналитическихРазрезов
		|ПОМЕСТИТЬ втКоличествоАналитическихРазрезов
		|ИЗ
		|	втАналитическиеРазрезы КАК втАналитическиеРазрезы
		|СГРУППИРОВАТЬ ПО
		|	втАналитическиеРазрезы.Рейс,
		|	втАналитическиеРазрезы.ЗаданиеНаПеревозку
		|;
		|//{Запрос: 21 ////////////////////////////////////////
		|// Количество аналитических разрезов в точках разгрузки
		|ВЫБРАТЬ
		|	втАналитическиеРазрезыТ.АдресРазгрузки КАК АдресРазгрузки,
		|	втАналитическиеРазрезыТ.Рейс КАК Рейс,
		|	СУММА(ЕСТЬNULL(втКоличествоАналитическихРазрезовТ.КоличествоАналитическихРазрезов, 0)) КАК КоличествоАналитическихРазрезов
		|ПОМЕСТИТЬ втКоличествоАналитическихРазрезовВТочкеРазгрузки
		|ИЗ
		|	втАналитическиеРазрезы КАК втАналитическиеРазрезыТ
		|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоАналитическихРазрезов КАК втКоличествоАналитическихРазрезовТ
		|	ПО ИСТИНА
		|		И втАналитическиеРазрезыТ.Рейс = втКоличествоАналитическихРазрезовТ.Рейс
		|		И втАналитическиеРазрезыТ.ЗаданиеНаПеревозку = втКоличествоАналитическихРазрезовТ.ЗаданиеНаПеревозку
		|СГРУППИРОВАТЬ ПО
		|	втАналитическиеРазрезыТ.АдресРазгрузки,
		|	втАналитическиеРазрезыТ.Рейс
		|;
		|//{Запрос: 22 ////////////////////////////////////////
		|// Значения показателей аналитических разрезов
		|ВЫБРАТЬ
		|	втАналитическиеРазрезы.Рейс КАК Рейс,
		|	втАналитическиеРазрезы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	втАналитическиеРазрезы.Подразделение КАК Подразделение,
		|	втАналитическиеРазрезы.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втАналитическиеРазрезы.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	втАналитическиеРазрезы.АналитическийРазрез КАК АналитическийРазрез,
		|	втАналитическиеРазрезы.Масса КАК Масса,
		|	втАналитическиеРазрезы.Объем КАК Объем,
		|	втАналитическиеРазрезы.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
		|	ВЫБОР
		|		КОГДА НЕ втКоличествоАналитическихРазрезов.КоличествоАналитическихРазрезов ЕСТЬ NULL
		|				И втКоличествоАналитическихРазрезов.КоличествоАналитическихРазрезов > 0
		|			ТОГДА втГрузовыеМеста.КоличествоЗагрузочныхМетров / втКоличествоАналитическихРазрезов.КоличествоАналитическихРазрезов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
		|	ВЫБОР
		|		КОГДА НЕ втКоличествоАналитическихРазрезов.КоличествоАналитическихРазрезов ЕСТЬ NULL
		|				И втКоличествоАналитическихРазрезов.КоличествоАналитическихРазрезов > 0
		|			ТОГДА втГрузовыеМеста.КоличествоМест / втКоличествоАналитическихРазрезов.КоличествоАналитическихРазрезов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМест,
		|	0 КАК Расстояние,
		|	0 КАК Время,
		|	1 КАК ПлановыеРасходыПоРейсу,
		|	втАналитическиеРазрезы.АдресПогрузки КАК АдресПогрузки,
		|	втАналитическиеРазрезы.АдресРазгрузки КАК АдресРазгрузки,
		|	ЕСТЬNULL(втКоличествоАналитическихРазрезовВТочкеРазгрузкиТ.КоличествоАналитическихРазрезов, 0) КАК КоличествоАналитическихРазрезовВТочкеРазгрузки,
		|	ЕСТЬNULL(втФактическиеРасходы.СуммаФактСНДСОборот, 0) КАК ФактическиеРасходыПоРейсу
		|ИЗ
		|	втАналитическиеРазрезы КАК втАналитическиеРазрезы
		|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМеста
		|	ПО ИСТИНА
		|		И втАналитическиеРазрезы.Рейс = втГрузовыеМеста.Рейс
		|		И втАналитическиеРазрезы.ЗаданиеНаПеревозку = втГрузовыеМеста.ЗаданиеНаПеревозку
		|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоАналитическихРазрезовВТочкеРазгрузки КАК втКоличествоАналитическихРазрезовВТочкеРазгрузкиТ
		|	ПО ИСТИНА
		|		И втАналитическиеРазрезы.Рейс = втКоличествоАналитическихРазрезовВТочкеРазгрузкиТ.Рейс
		|		И втАналитическиеРазрезы.АдресРазгрузки = втКоличествоАналитическихРазрезовВТочкеРазгрузкиТ.АдресРазгрузки
		|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоАналитическихРазрезов КАК втКоличествоАналитическихРазрезов
		|	ПО ИСТИНА
		|		И втАналитическиеРазрезы.Рейс = втКоличествоАналитическихРазрезов.Рейс
		|		И втАналитическиеРазрезы.ЗаданиеНаПеревозку = втКоличествоАналитическихРазрезов.ЗаданиеНаПеревозку
		|	ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРасходы КАК втФактическиеРасходы
		|	ПО ИСТИНА
		|		И втФактическиеРасходы.АналитическийРазрез = втАналитическиеРазрезы.АналитическийРазрез
		|		И втФактическиеРасходы.ЗаданиеНаПеревозку = втАналитическиеРазрезы.ЗаданиеНаПеревозку
		|		И втФактическиеРасходы.Рейс = втАналитическиеРазрезы.Рейс
		|";

		
	Иначе
		
		Текст = Текст + "
		|//{Запрос: 19 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втФактическиеРасходыТ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	втФактическиеРасходыТ.Рейс КАК Рейс,
		|	СУММА(втФактическиеРасходыТ.СуммаФактСНДСОборот) КАК СуммаФактСНДСОборот
		|ПОМЕСТИТЬ втФактическиеРасходыПоЗаданию
		|ИЗ
		|	втФактическиеРасходы КАК втФактическиеРасходыТ
		|СГРУППИРОВАТЬ ПО
		|	втФактическиеРасходыТ.ЗаданиеНаПеревозку,
		|	втФактическиеРасходыТ.Рейс
		|;
		|//{Запрос: 20 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(втАдресаПогрузкиРазгрузкиПоГрузовымМестам.АдресРазгрузки, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)) КАК АдресРазгрузки,
		|	втГрузовыеМеста.Рейс КАК Рейс,
		|	СУММА(1) КАК КоличествоАналитическихРазрезов
		|ПОМЕСТИТЬ втКоличествоГрузовыхМестВТочкахРазгрузки
		|ИЗ
		|	втГрузовыеМеста КАК втГрузовыеМеста
		|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаПогрузкиРазгрузкиПоГрузовымМестам КАК втАдресаПогрузкиРазгрузкиПоГрузовымМестам
		|	ПО ИСТИНА
		|		И втГрузовыеМеста.ГрузовоеМесто = втАдресаПогрузкиРазгрузкиПоГрузовымМестам.ГрузовоеМесто
		|		И втГрузовыеМеста.ЗаданиеНаПеревозку = втАдресаПогрузкиРазгрузкиПоГрузовымМестам.Задание
		|		И втГрузовыеМеста.Рейс = втАдресаПогрузкиРазгрузкиПоГрузовымМестам.Рейс
		|СГРУППИРОВАТЬ ПО
		|	втАдресаПогрузкиРазгрузкиПоГрузовымМестам.АдресРазгрузки,
		|	втГрузовыеМеста.Рейс
		|;
		|//{Запрос: 21 ////////////////////////////////////////
		|// Грузовые места
		|ВЫБРАТЬ
		|	втГрузовыеМеста.Рейс КАК Рейс,
		|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	упРейс.Подразделение КАК Подразделение,
		|	втГрузовыеМеста.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	втГрузовыеМеста.Масса КАК Масса,
		|	втГрузовыеМеста.Объем КАК Объем,
		|	0 КАК Расстояние,
		|	0 КАК Время,
		|	0 КАК ПлановыеРасходыПоРейсу,
		|	0 КАК ФактическиеРасходыПоРейсу,
		|	втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
		|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
		|	втГрузовыеМеста.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
		|	ЕСТЬNULL(втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.АдресПогрузки, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)) КАК АдресПогрузки,
		|	ЕСТЬNULL(втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.АдресРазгрузки, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)) КАК АдресРазгрузки,
		|	ЕСТЬNULL(втКоличествоГрузовыхМестВТочкахРазгрузкиТ.КоличествоАналитическихРазрезов, 0) КАК КоличествоАналитическихРазрезовВТочкеРазгрузки,
		|	ЕСТЬNULL(втФактическиеРасходыПоЗаданиюТ.СуммаФактСНДСОборот, 0) КАК ФактическиеРасходыПоРейсу1
		|ИЗ
		|	втГрузовыеМеста КАК втГрузовыеМеста
		|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаПогрузкиРазгрузкиПоГрузовымМестам КАК втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ
		|	ПО ИСТИНА
		|		И втГрузовыеМеста.ГрузовоеМесто = втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.ГрузовоеМесто
		|		И втГрузовыеМеста.ЗаданиеНаПеревозку = втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.Задание
		|		И втГрузовыеМеста.Рейс = втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.Рейс
		|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоГрузовыхМестВТочкахРазгрузки КАК втКоличествоГрузовыхМестВТочкахРазгрузкиТ
		|	ПО ИСТИНА
		|		И втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.АдресРазгрузки = втКоличествоГрузовыхМестВТочкахРазгрузкиТ.АдресРазгрузки
		|		И втАдресаПогрузкиРазгрузкиПоГрузовымМестамТ.Рейс = втКоличествоГрузовыхМестВТочкахРазгрузкиТ.Рейс
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
		|	ПО упРейс.Ссылка = втГрузовыеМеста.Рейс
		|	ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРасходыПоЗаданию КАК втФактическиеРасходыПоЗаданиюТ
		|	ПО ИСТИНА
		|		И втФактическиеРасходыПоЗаданиюТ.ЗаданиеНаПеревозку = втГрузовыеМеста.ЗаданиеНаПеревозку
		|		И втФактическиеРасходыПоЗаданиюТ.Рейс = втГрузовыеМеста.Рейс
		|";

		
	КонецЕсли;
		
	Возврат Текст;			
	
КонецФункции

Функция ТекстЗапросаРаспределенияРасходовУправлениеТранспортом(РаспределениеРасходовНаТС = Ложь)
	
	Если РаспределениеРасходовНаТС Тогда
		Текст = "ВЫБРАТЬ
		        |	упРейс.Ссылка КАК Рейс,
		        |	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
			   |	упРейс.Подразделение КАК Подразделение,
		        |	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка) КАК ЗаданиеНаПеревозку,
		        |	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		        |	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) КАК Масса,
		        |	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
		        |	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
		        |	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
		        |	ЕСТЬNULL(упПлановыеПараметрыРейса.Расстояние, 0) КАК Расстояние,
		        |	ЕСТЬNULL(упПлановыеПараметрыРейса.Время, 0) КАК Время,
		        |	ЕСТЬNULL(упВсегоРасходыПоРейсу.СуммаПланСНДС, 0) КАК ПлановыеРасходыПоРейсу,
		        |	0 КАК ОценочнаяСтоимостьГруза,
			   | 0 КАК ФактическиеРасходыПоРейсу
		        |ИЗ
		        |	Документ.упРейс КАК упРейс
		        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
		        |		ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
		        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВсегоРасходыПоРейсу КАК упВсегоРасходыПоРейсу
		        |		ПО упРейс.Ссылка = упВсегоРасходыПоРейсу.Рейс
		        |ГДЕ
		        |	упРейс.Ссылка В(&мсвРейсы)";
	Иначе
		Текст = "ВЫБРАТЬ
		        |	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
		        |	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
			   |	упРейс.Подразделение КАК Подразделение,
		        |	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		        |	СУММА(ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0)) КАК Масса,
		        |	СУММА(ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0)) КАК Объем,
		        |	СУММА(ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0)) КАК КоличествоМест,
		        |	СУММА(ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров,
		        |	СУММА(ЕСТЬNULL(упПлановыеПараметрыРейса.Расстояние, 0)) КАК Расстояние,
		        |	СУММА(ЕСТЬNULL(упПлановыеПараметрыРейса.Время, 0)) КАК Время,
		        |	СУММА(ЕСТЬNULL(упВсегоРасходыПоРейсу.СуммаПланСНДС, 0)) КАК ПлановыеРасходыПоРейсу,
		        |	СУММА(0) КАК ОценочнаяСтоимостьГруза,
			   | СУММА(0) КАК ФактическиеРасходыПоРейсу
		        |ИЗ
		        |	Документ.упРейс КАК упРейс
		        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
		        |		ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
		        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВсегоРасходыПоРейсу КАК упВсегоРасходыПоРейсу
		        |		ПО упРейс.Ссылка = упВсегоРасходыПоРейсу.Рейс
		        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, ) КАК тбПеревозчикПоРейсу
		        |		ПО упРейс.Ссылка = тбПеревозчикПоРейсу.Рейс
		        |ГДЕ
		        |	упРейс.Ссылка В(&мсвРейсы)
		        |	И тбПеревозчикПоРейсу.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
		        |
		        |СГРУППИРОВАТЬ ПО
		        |	тбПеревозчикПоРейсу.ТранспортноеСредство,
		        |	упРейс.НаправлениеДеятельности,
			   |	упРейс.Подразделение";

	КонецЕсли;
	Возврат Текст;	
КонецФункции

#КонецОбласти

// Возвращает структуру с данными расхода с разрезом по рейсу.
//
// Параметры:
//  Рейс		 - ДокументСсылка.упРейс - Ссылка на документы.
// 
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПлановыеРасходыПоРейсу(Рейс) Экспорт
	
	сткРезультат = Новый Структура("СуммаПланБезНДС, СуммаПланСНДС, СуммаПланВалютаБезНДС, СуммаПланВалютаСНДС",0,0,0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРасходыОбороты.СуммаПланБезНДСОборот КАК СуммаПланБезНДС,
	               |	упРасходыОбороты.СуммаПланСНДСОборот КАК СуммаПланСНДС,
	               |	упРасходыОбороты.СуммаПланВалютаБезНДСОборот КАК СуммаПланВалютаБезНДС,
	               |	упРасходыОбороты.СуммаПланВалютаСНДСОборот КАК СуммаПланВалютаСНДС
	               |ИЗ
	               |	РегистрНакопления.упРасходы.Обороты(, , , Рейс = &Рейс) КАК упРасходыОбороты";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, текВыборка);
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		Если НЕ ВедетсяВалютныйУчет Тогда
			сткРезультат.СуммаПланВалютаБезНДС	= 0;	
			сткРезультат.СуммаПланВалютаСНДС	= 0;
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Возвращает структуру с данными расхода по регистратору рейс.
//
// Параметры:
//  Рейс		 - ДокументСсылка.упРейс - Ссылка на документы.
// 
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПлановыеРасходыПоРейсуВведенныеНеДокументомРейс(Рейс) Экспорт
	
	сткРезультат = Новый Структура("СуммаПланБезНДС, СуммаПланСНДС, СуммаПланВалютаБезНДС, СуммаПланВалютаСНДС",0,0,0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРасходыОбороты.СуммаПланБезНДСОборот КАК СуммаПланБезНДС,
	               |	упРасходыОбороты.СуммаПланСНДСОборот КАК СуммаПланСНДС,
	               |	упРасходыОбороты.СуммаПланВалютаБезНДСОборот КАК СуммаПланВалютаБезНДС,
	               |	упРасходыОбороты.СуммаПланВалютаСНДСОборот КАК СуммаПланВалютаСНДС
	               |ИЗ
	               |	РегистрНакопления.упРасходы.Обороты(, , Регистратор, Рейс = &Рейс) КАК упРасходыОбороты
	               |ГДЕ
	               |	НЕ упРасходыОбороты.Регистратор = &Регистратор";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Регистратор", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	Если текВыборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, текВыборка);
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		Если НЕ ВедетсяВалютныйУчет Тогда
			сткРезультат.СуммаПланВалютаБезНДС	= 0;	
			сткРезультат.СуммаПланВалютаСНДС	= 0;
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Возвращает структуру с данными расхода по документу рейс.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документы.
// 
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция ПлановыеРасходыВсегоПоРейсу(Рейс) Экспорт
	
	сткРезультат = Новый Структура("СуммаПланБезНДС, СуммаПланСНДС, СуммаПланВалютаБезНДС, СуммаПланВалютаСНДС",0,0,0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упВсегоРасходыПоРейсу.СуммаПланСНДС,
	               |	упВсегоРасходыПоРейсу.СуммаПланБезНДС,
	               |	упВсегоРасходыПоРейсу.СуммаПланВалютаБезНДС,
	               |	упВсегоРасходыПоРейсу.СуммаПланВалютаСНДС
	               |ИЗ
	               |	РегистрСведений.упВсегоРасходыПоРейсу КАК упВсегоРасходыПоРейсу
	               |ГДЕ
	               |	упВсегоРасходыПоРейсу.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, текВыборка);
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		Если НЕ ВедетсяВалютныйУчет Тогда
			сткРезультат.СуммаПланВалютаБезНДС	= 0;	
			сткРезультат.СуммаПланВалютаСНДС	= 0;
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Возвращает таблицу плановых расходов по документу "упПредложениеПеревозчика".
//
// Параметры:
//  ПредложениеПеревозчика - ДокументСсылка.упПредложениеПеревозчика - Ссылка на документы.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ПлановыеРасходыПоПредложениюПеревозчика(ПредложениеПеревозчика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПредложениеПеревозчикаПлановыеРасходы.СтатьяРасходов,
	               |	упПредложениеПеревозчикаПлановыеРасходы.ПравилоРаспределения,
	               |	СУММА(упПредложениеПеревозчикаПлановыеРасходы.Сумма) КАК Сумма,
	               |	упПредложениеПеревозчикаПлановыеРасходы.Валюта,
	               |	СУММА(упПредложениеПеревозчикаПлановыеРасходы.Количество) КАК Количество,
	               |	СУММА(упПредложениеПеревозчикаПлановыеРасходы.СуммаНДС) КАК СуммаНДС
	               |ИЗ
	               |	Документ.упПредложениеПеревозчика.ПлановыеРасходы КАК упПредложениеПеревозчикаПлановыеРасходы
	               |ГДЕ
	               |	упПредложениеПеревозчикаПлановыеРасходы.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упПредложениеПеревозчикаПлановыеРасходы.СтатьяРасходов,
	               |	упПредложениеПеревозчикаПлановыеРасходы.ПравилоРаспределения,
	               |	упПредложениеПеревозчикаПлановыеРасходы.Валюта";
	Запрос.УстановитьПараметр("Ссылка", ПредложениеПеревозчика);
	тбзРезультат	= Запрос.Выполнить().Выгрузить();
	
	Возврат тбзРезультат;
			
КонецФункции

// Функция - Плановые расходы по выполненным заданиям рейсов
//
// Параметры:
//  мсвРейсы			 - Массив	 - массив рейсов
//  ВедетсяВалютныйУчет	 - Булево	 - Истина, если ведется валютный учет
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица расходов с колонками:
//  * Рейс - ДокументСсылка.упРейс - рейс.
//  * Задание - ДокументСсылка.упЗаданиеНаПеревозкуГруза - задание.
//  * СтатьяРасходов - СправочникСсылка.упСтатьиДоходовРасходов - статья расходов.
//  * СтавкаНДС - ПеречислениеСсылка.упСтавкиНДС - ставка НДС.
//  * ПравилоРаспределения - СправочникСсылка.упПравилаРаспределенияРасходов - правила распределения.
//  * Валюта - СправочникСсылка.Валюты - валюта.
//  * Количество - Число - количество.
//  * Сумма - Число - сумма.
//
Функция ПлановыеРасходыПоВыполненнымЗаданиямРейсов(мсвРейсы, ВедетсяВалютныйУчет) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	упРейсЗадания.Задание КАК Задание,
	               	|	упРейсЗадания.Ссылка.Ссылка КАК Рейс
	               	|ПОМЕСТИТЬ втВыполненныеЗадания
	               	|ИЗ
	               	|	Документ.упРейс.Задания КАК упРейсЗадания
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	               	|		ПО упРейсЗадания.Задание = упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ
	               	|ГДЕ
	               	|	упРейсЗадания.Ссылка В(&мсвРейсы)
	               	|	И упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполнено)
	               	|
	               	|ОБЪЕДИНИТЬ ВСЕ
	               	|
	               	|ВЫБРАТЬ
	               	|	упКорректировкаРейсаЗадания.Задание,
	               	|	упКорректировкаРейсаЗадания.Ссылка.Рейс
	               	|ИЗ
	               	|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	               	|		ПО упКорректировкаРейсаЗадания.Задание = упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ
	               	|ГДЕ
	               	|	упКорректировкаРейсаЗадания.Ссылка.Рейс В(&мсвРейсы)
	               	|	И упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполнено)
	               	|;
	               	|
	               	|////////////////////////////////////////////////////////////////////////////////
	               	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	               	|	втЗадания.Задание,
	               	|	втЗадания.Рейс
	               	|ПОМЕСТИТЬ втЗаданияПоРейсам
	               	|ИЗ
	               	|	втВыполненныеЗадания КАК втЗадания
	               	|;
	               	|
	               	|////////////////////////////////////////////////////////////////////////////////
	               	|ВЫБРАТЬ
	               	|	упПеревозчикПоРейсуСрезПоследних.Рейс КАК Рейс,
	               	|	упПеревозчикПоРейсуСрезПоследних.Соглашение КАК Соглашение,
	               	|	ЕСТЬNULL(упГруппыТарифов.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта
	               	|ПОМЕСТИТЬ втСоглашенияПеревозчика
	               	|ИЗ
	               	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&мсвРейсы)) КАК упПеревозчикПоРейсуСрезПоследних
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
	               	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГруппыТарифов КАК упГруппыТарифов
	               	|			ПО упСоглашенияСПеревозчиками.ГруппаТарифов = упГруппыТарифов.Ссылка
	               	|		ПО упПеревозчикПоРейсуСрезПоследних.Соглашение = упСоглашенияСПеревозчиками.Ссылка
	               	|;
	               	|
	               	|////////////////////////////////////////////////////////////////////////////////
	               	|ВЫБРАТЬ
	               	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов,
	               	|	втСоглашенияПеревозчика.Валюта,
	               	|	упСтатьиДоходовРасходов.СтавкаНДС,
	               	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	               	|	втСоглашенияПеревозчика.Рейс,
	               	|	1 КАК Количество,
	               	|	упСоглашенияСПеревозчикамиСтатьиРасходов.ЗаполнятьПоЗаданиям
	               	|ПОМЕСТИТЬ втРасходыПоСоглашению
	               	|ИЗ
	               	|	втСоглашенияПеревозчика КАК втСоглашенияПеревозчика
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК упСоглашенияСПеревозчикамиСтатьиРасходов
	               	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	               	|			ПО упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	               	|		ПО втСоглашенияПеревозчика.Соглашение = упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка
	               	|;
	               	|
	               	|////////////////////////////////////////////////////////////////////////////////
	               	|ВЫБРАТЬ
	               	|	упРаботыПоРейсуСрезПоследних.Рейс,
	               	|	упРаботыПоРейсуСрезПоследних.Задание,
	               	|	упДополнительныеОперации.СтатьяРасходов,
	               	|	упСтатьиДоходовРасходов.СтавкаНДС,
	               	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	               	|	втСоглашенияПеревозчика.Валюта,
	               	|	упРаботыПоРейсуСрезПоследних.КоличествоДопОпераций КАК Количество
	               	|ПОМЕСТИТЬ втРасходыПоДопОперациям
	               	|ИЗ
	               	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс В (&мсвРейсы)) КАК упРаботыПоРейсуСрезПоследних
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упДополнительныеОперации КАК упДополнительныеОперации
	               	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	               	|			ПО упДополнительныеОперации.СтатьяДоходов = упСтатьиДоходовРасходов.Ссылка
	               	|				И упДополнительныеОперации.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	               	|		ПО упРаботыПоРейсуСрезПоследних.ДополнительнаяОперация = упДополнительныеОперации.Ссылка
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСоглашенияПеревозчика КАК втСоглашенияПеревозчика
	               	|		ПО упРаботыПоРейсуСрезПоследних.Рейс = втСоглашенияПеревозчика.Рейс
	               	|ГДЕ
	               	|	упРаботыПоРейсуСрезПоследних.Выполнена
	               	|;
	               	|
	               	|////////////////////////////////////////////////////////////////////////////////
	               	|ВЫБРАТЬ
	               	|	втЗаданияПоРейсам.Рейс,
	               	|	втЗаданияПоРейсам.Задание,
	               	|	втРасходыПоСоглашению.СтатьяРасходов,
	               	|	втРасходыПоСоглашению.ПравилоРаспределения,
	               	|	втРасходыПоСоглашению.Валюта,
	               	|	втРасходыПоСоглашению.СтавкаНДС,
	               	|	втРасходыПоСоглашению.Количество КАК Количество,
	               	|	0 КАК Сумма
	               	|ПОМЕСТИТЬ втРасходы
	               	|ИЗ
	               	|	втРасходыПоСоглашению КАК втРасходыПоСоглашению
	               	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияПоРейсам КАК втЗаданияПоРейсам
	               	|		ПО втРасходыПоСоглашению.Рейс = втЗаданияПоРейсам.Рейс
	               	|ГДЕ
	               	|	втРасходыПоСоглашению.ЗаполнятьПоЗаданиям
	               	|
	               	|ОБЪЕДИНИТЬ ВСЕ
	               	|
	               	|ВЫБРАТЬ
	               	|	втРасходыПоСоглашению.Рейс,
	               	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка),
	               	|	втРасходыПоСоглашению.СтатьяРасходов,
	               	|	втРасходыПоСоглашению.ПравилоРаспределения,
	               	|	втРасходыПоСоглашению.Валюта,
	               	|	втРасходыПоСоглашению.СтавкаНДС,
	               	|	втРасходыПоСоглашению.Количество,
	               	|	0
	               	|ИЗ
	               	|	втРасходыПоСоглашению КАК втРасходыПоСоглашению
	               	|ГДЕ
	               	|	НЕ втРасходыПоСоглашению.ЗаполнятьПоЗаданиям
	               	|
	               	|ОБЪЕДИНИТЬ ВСЕ
	               	|
	               	|ВЫБРАТЬ
	               	|	втРасходыПоДопОперациям.Рейс,
	               	|	втРасходыПоДопОперациям.Задание,
	               	|	втРасходыПоДопОперациям.СтатьяРасходов,
	               	|	втРасходыПоДопОперациям.ПравилоРаспределения,
	               	|	втРасходыПоДопОперациям.Валюта,
	               	|	втРасходыПоДопОперациям.СтавкаНДС,
	               	|	втРасходыПоДопОперациям.Количество,
	               	|	0
	               	|ИЗ
	               	|	втРасходыПоДопОперациям КАК втРасходыПоДопОперациям";
	Запрос.Текст =  Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов();				
	Если ВедетсяВалютныйУчет Тогда
		Запрос.Текст =  Запрос.Текст + 	"ВЫБРАТЬ
		|	втРасходы.Рейс,
		|	втРасходы.Задание,
		|	втРасходы.СтатьяРасходов,
		|	втРасходы.ПравилоРаспределения,
		|	втРасходы.Валюта,
		|	втРасходы.СтавкаНДС,
		|	СУММА(втРасходы.Количество) КАК Количество,
		|	СУММА(втРасходы.Сумма) КАК Сумма
		|ИЗ
		|	втРасходы КАК втРасходы
		|
		|СГРУППИРОВАТЬ ПО
		|	втРасходы.СтатьяРасходов,
		|	втРасходы.Рейс,
		|	втРасходы.Задание,
		|	втРасходы.СтавкаНДС,
		|	втРасходы.Валюта,
		|	втРасходы.ПравилоРаспределения";
		
	Иначе					
		Запрос.Текст =  Запрос.Текст + 	"ВЫБРАТЬ
		|	втРасходы.Рейс,
		|	втРасходы.Задание,
		|	втРасходы.СтатьяРасходов,
		|	втРасходы.ПравилоРаспределения,
		|	втРасходы.СтавкаНДС,
		|	СУММА(втРасходы.Количество) КАК Количество,
		|	СУММА(втРасходы.Сумма) КАК Сумма
		|ИЗ
		|	втРасходы КАК втРасходы
		|
		|СГРУППИРОВАТЬ ПО
		|	втРасходы.СтатьяРасходов,
		|	втРасходы.Рейс,
		|	втРасходы.Задание,
		|	втРасходы.СтавкаНДС,
		|	втРасходы.ПравилоРаспределения";								
		
	КонецЕсли;				
	
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	тбзПлановыеРасходы = Запрос.Выполнить().Выгрузить();
	
	Возврат тбзПлановыеРасходы;
	
КонецФункции

#Область РБП

// Процедура формирует движения по регистру "упПрочиеРасходы".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//  Ссылка - ДокументСсылка - Ссылка на объект.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  Реквизиты - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
// 
Процедура СформироватьРасходыБудущихПериодов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	ДвиженияРасходы = Движения.упПрочиеРасходы;
	ДвиженияРасходы.Записывать 	= Истина;
	тбзПрочиеРасходы = ДополнительныеСвойства.ПрочиеРасходы;
	тбзРезультатРаспределения = ДополнительныеСвойства.РезультатРаспределения;
	
	// Списываются расходы будущих периодов.
	Для каждого Строка Из тбзПрочиеРасходы Цикл
		ДвижениеПрочиеРасходы = ДвиженияРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеПрочиеРасходы, Строка);
		
		ДвижениеПрочиеРасходы.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеПрочиеРасходы.Организация = Реквизиты.Организация;
		
		ДвижениеПрочиеРасходы.Период = Строка.НачалоПериода;
		
		ДвижениеПрочиеРасходы.Регистратор = Ссылка;
		Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
			ДвижениеПрочиеРасходы.Валюта = Строка.Валюта;
			ДвижениеПрочиеРасходы.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			ДвижениеПрочиеРасходы.СуммаСНДС = Строка.Сумма;
			ДвижениеПрочиеРасходы.СуммаВалютаБезНДС = Строка.СуммаВалюта - Строка.СуммаВалютаНДС;
			ДвижениеПрочиеРасходы.СуммаВалютаСНДС   = Строка.СуммаВалюта;
		Иначе	
			ДвижениеПрочиеРасходы.Валюта = Справочники.Валюты.ПустаяСсылка();
			ДвижениеПрочиеРасходы.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			ДвижениеПрочиеРасходы.СуммаСНДС = Строка.Сумма;
			ДвижениеПрочиеРасходы.СуммаВалютаБезНДС = 0;
			ДвижениеПрочиеРасходы.СуммаВалютаСНДС   = 0;
		КонецЕсли;
	КонецЦикла;
			
	Для каждого Строка Из тбзРезультатРаспределения Цикл
		ДвижениеПрочиеРасходы = ДвиженияРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеПрочиеРасходы, Строка);
		ДвижениеПрочиеРасходы.ВидДвижения = ВидДвиженияНакопления.Приход;
		ДвижениеПрочиеРасходы.Организация = Реквизиты.Организация;
		ДвижениеПрочиеРасходы.Период = Строка.Дата;
		ДвижениеПрочиеРасходы.Регистратор = Ссылка;
		
		Если ДополнительныеСвойства.ВедетсяВалютныйУчет Тогда
			ДвижениеПрочиеРасходы.Валюта = Строка.Валюта;
			ДвижениеПрочиеРасходы.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			ДвижениеПрочиеРасходы.СуммаСНДС = Строка.Сумма;
			ДвижениеПрочиеРасходы.СуммаВалютаБезНДС = Строка.СуммаВалюта - Строка.СуммаВалютаНДС;
			ДвижениеПрочиеРасходы.СуммаВалютаСНДС = Строка.СуммаВалюта;
		Иначе	
			ДвижениеПрочиеРасходы.Валюта = Справочники.Валюты.ПустаяСсылка();
			ДвижениеПрочиеРасходы.СуммаБезНДС = Строка.Сумма - Строка.СуммаНДС;
			ДвижениеПрочиеРасходы.СуммаСНДС = Строка.Сумма;
			ДвижениеПрочиеРасходы.СуммаВалютаБезНДС = 0;
			ДвижениеПрочиеРасходы.СуммаВалютаСНДС = 0;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеТаблицРасходов

// Процедура - Заполняет таблицы для формирования движений по расходам,
//  пересчитывает и заполняет итоговые суммы документа,
//  формирует аналитические разрезы по прочим расходам.
//
// Параметры:
//  Объект - ДокументОбъект	 - документ объект.
//  Отказ	 - Булево			 - Истина, в случае ошибки при выполнении.
//
Процедура ЗаполнитьДоходыРасходы(Объект, Отказ) Экспорт
	
	ДополнительныеСвойства	    = Объект.ДополнительныеСвойства;
	ИспользуетсяУчетРасходовНаТС = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетРасходовНаТС");
	ДополнительныеСвойства.Вставить("ИспользуетсяУчетРасходовНаТС", ИспользуетсяУчетРасходовНаТС);
	ВедетсяВалютныйУчет		    = ДополнительныеСвойства.ВедетсяВалютныйУчет;

	Если ТипЗнч(Объект) = Тип("ДокументОбъект.упПоступлениеАгрегатов") Тогда
		ЗаполнитьДоходыРасходы_ПоступлениеАгрегатов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упПрочиеРасходы") Тогда	
		ЗаполнитьДоходыРасходы_ПрочиеРасходы(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упРейс") Тогда
		ЗаполнитьДоходыРасходы_Рейс(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упИсходящаяПретензия") Тогда		
		ЗаполнитьДоходыРасходы_ИсходящаяПретензия(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упВходящаяПретензия") Тогда			
		ЗаполнитьДоходыРасходы_ВходящаяПретензия(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упСлужебноеРасследование") Тогда
		ЗаполнитьДоходыРасходы_СлужебноеРасследование(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упЗаявкаНаРемонтИТОТС") Тогда
		ЗаполнитьДоходыРасходы_ЗаявкаНаРемонтИТОТС(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упРемонтИТОТС") Тогда
		ЗаполнитьДоходыРасходы_РемонтИТОТС(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упФактическиеРасходы") Тогда	
		ЗаполнитьДоходыРасходы_ФактическиеРасходы(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упФактическиеДоходы") Тогда		
		ЗаполнитьДоходыРасходы_ФактическиеДоходы(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упСчетНаОплатуЗаказчику") Тогда	
		ЗаполнитьДоходыРасходы_СчетНаОплатуЗаказчику(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упЗаявкаНаПеревозкуГруза") Тогда		
		ЗаполнитьДоходыРасходы_ЗаявкаНаПеревозкуГруза(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упПодтверждениеЗаявкиНаТС") Тогда		
		ЗаполнитьДоходыРасходы_ПодтверждениеЗаявкиНаТС(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);	
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упРаспределениеПрочихРасходов") Тогда		
		ЗаполнитьДоходыРасходы_РаспределениеПрочихРасходов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);		
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упРаспределениеПрямыхРасходов") Тогда		
		ЗаполнитьДоходыРасходы_РаспределениеПрямыхРасходов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);			
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.упСписаниеПрочихРасходов") Тогда		
		ЗаполнитьДоходыРасходы_СписаниеПрочихРасходов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ);				
		
	КонецЕсли;
			
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ПоступлениеАгрегатов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	тбзАгрегаты = Объект.Агрегаты.Выгрузить(,"Агрегат, Валюта, СтатьяРасходов, Сумма");
	тбзПрочиеРасходы = Неопределено;
	
	Если тбзАгрегаты.Количество() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбАгрегаты.Агрегат КАК Агрегат,
		|	тбАгрегаты.Валюта КАК Валюта,
		|	тбАгрегаты.СтатьяРасходов КАК СтатьяРасходов,
		|	тбАгрегаты.Сумма КАК Сумма
		|ПОМЕСТИТЬ втАгрегаты
		|ИЗ
		|	&Агрегаты КАК тбАгрегаты
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПоступлениеАгрегатовАгрегаты.Агрегат КАК ОбъектАналитическогоРазреза,
		|	упПоступлениеАгрегатовАгрегаты.Валюта КАК Валюта,
		|	упПоступлениеАгрегатовАгрегаты.СтатьяРасходов КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	упПоступлениеАгрегатовАгрегаты.Сумма КАК Сумма,
		|	0 КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ПОМЕСТИТЬ втПрочиеРасходы
		|ИЗ
		|	втАгрегаты КАК упПоступлениеАгрегатовАгрегаты
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО упПоступлениеАгрегатовАгрегаты.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ГДЕ НЕ (упПоступлениеАгрегатовАгрегаты.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка))
		|";
	
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
		
		Запрос.УстановитьПараметр("Агрегаты", тбзАгрегаты);
		Запрос.УстановитьПараметр("Партнер", Объект.Поставщик);
		
		тбзПрочиеРасходы = Запрос.Выполнить().Выгрузить();
		
		ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);
	
		ПересчитатьСуммыВВалютуРеглУчета(тбзПрочиеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
	
		тбзПрочиеРасходы.Свернуть(КолонкиГруппировки("АналитическийРазрез"), КолонкиСуммирования());
	
		// Косвенные расходы и РБП.
		ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ПрочиеРасходы(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	тбзОбщиеРасходы  = Объект.Расходы.Выгрузить(,"СтатьяРасходов, ПравилоРаспределения, АналитическийРазрез, Валюта, Сумма, СуммаНДС");
	тбзПрочиеРасходы = Неопределено;
	тбзПрямыеРасходы = Неопределено;
	тбзПрямыеРасходыНаТС = Неопределено;
	
	Если тбзОбщиеРасходы.Количество() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|// Расходы, с выбранным объектом аналитического разреза.
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	тбРасходы.Валюта КАК Валюта,
		|	тбРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|	тбРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
		|	тбРасходы.АналитическийРазрез КАК АналитическийРазрез,
		|	тбРасходы.СуммаНДС КАК СуммаНДС,
		|	тбРасходы.Сумма КАК Сумма
		|ПОМЕСТИТЬ втРасходы
		|ИЗ
		|	&Расходы КАК тбРасходы
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Расходы дополняются вариантом расходов и видом аналитики. 
		|ВЫБРАТЬ
		|	втРасходы.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	втРасходы.Валюта КАК Валюта,
		|	втРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|	втРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
		|	втРасходы.АналитическийРазрез КАК АналитическийРазрез,
		|	втРасходы.СуммаНДС КАК СуммаНДС,
		|	втРасходы.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ПОМЕСТИТЬ втРасходыПоВидамРасходов
		|ИЗ
		|	втРасходы КАК втРасходы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО втРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Прямые расходы.
		|ВЫБРАТЬ
		|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
		|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
		|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
		|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
		|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
		|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы
		|ИЗ
		|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
		|ГДЕ втРасходыПоВидамРасходов.ЭтоПрямыеРасходы
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|// Прочие расходы (косвенные расходы и РБП).
		|ВЫБРАТЬ
		|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
		|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
		|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
		|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
		|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
		|	втРасходыПоВидамРасходов.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
		|	втРасходыПоВидамРасходов.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
		|	втРасходыПоВидамРасходов.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	втРасходыПоВидамРасходов.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
		|	втРасходыПоВидамРасходов.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
		|ПОМЕСТИТЬ втПрочиеРасходы
		|ИЗ
		|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
		|ГДЕ НЕ (втРасходыПоВидамРасходов.ЭтоПрямыеРасходы)
		|";
		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
		
		Запрос.УстановитьПараметр("Расходы",	тбзОбщиеРасходы);
		
		// По "Виду прочих расходов" определяется объект аналитического разреза 
		Если Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВидуПеревозки Тогда
			ОбъектАналитическогоРазреза = Объект.ВидПеревозки;
		
		ИначеЕсли Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВодителюЭкспедитору Тогда	
			ОбъектАналитическогоРазреза = Объект.Водитель;
			
		ИначеЕсли Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоТранспортномуСредству Тогда		
			ОбъектАналитическогоРазреза = Объект.ТранспортноеСредство;
			
		Иначе
			ОбъектАналитическогоРазреза = Документы.упРейс.ПустаяСсылка();
			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ОбъектАналитическогоРазреза", 			ОбъектАналитическогоРазреза);
		
		мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
		тбзПрямыеРасходы = мсвРезультатЗапроса[2].Выгрузить();
		тбзПрочиеРасходы = мсвРезультатЗапроса[6].Выгрузить();
		
		ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);
			
		сткИтогиПрямыеРасходы = Неопределено;
		сткИтогиПрочиеРасходы = Неопределено;
		
		// Пересчет в валюту управленческого учета.
		ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтогиПрямыеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
		ПересчитатьСуммыВВалютуУпрУчета(тбзПрочиеРасходы, Ложь, сткИтогиПрочиеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
		
		// Подсчет итогов расходов по документу.
		Объект.СуммаРасходы    = сткИтогиПрямыеРасходы.Сумма + сткИтогиПрочиеРасходы.Сумма;
		Объект.СуммаРасходыНДС = сткИтогиПрямыеРасходы.СуммаНДС + сткИтогиПрочиеРасходы.СуммаНДС;
		
		// Таблицы для формирования движений.
		КолонкиСуммирования = "Сумма, СуммаБезНДС, СуммаНДС, СуммаВалюта, СуммаБезНДСВалюта, СуммаВалютаНДС";
		
		тбзПрямыеРасходы.Свернуть("СтатьяРасходов, ПравилоРаспределения, АналитическийРазрез, Валюта", КолонкиСуммирования());
		тбзПрочиеРасходы.Свернуть("СтатьяРасходов, ПравилоРаспределения, АналитическийРазрез, Валюта", КолонкиСуммирования());
		
		тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
		ЭтоФактическиеРасходы = Истина;
		Для каждого Строка Из тбзПрямыеРасходы Цикл
			ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
		КонецЦикла;
		
		тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
		тбзПрямыеРасходы.Колонки.Добавить("Задание", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
		
	КонецЕсли;
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// Косвенные расходы и РБП.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);

		
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ЗаявкаНаПеревозкуГруза(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	 	 	
	сткИтоги = Неопределено;
	
	тбзТабличнаяЧасть = Объект.ПлановыеДоходы.Выгрузить(,"СтатьяДоходов, Валюта, Сумма, СуммаНДС, СкидкаСумма");
	Если ВедетсяВалютныйУчет Тогда
		ПересчитатьСуммыВВалютуУпрУчета(тбзТабличнаяЧасть, , сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
		Объект.СуммаДоходы = сткИтоги.Сумма;
		Объект.СуммаДоходыНДС = сткИтоги.СуммаНДС;
		Объект.СуммаДоходыСкидка	= сткИтоги.СуммаСкидка;
		тбзТабличнаяЧасть.Свернуть("СтатьяДоходов, Валюта", "Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	Иначе
		тбзТабличнаяЧасть.Свернуть("СтатьяДоходов, Валюта", "Сумма, СуммаНДС");
	КонецЕсли;	
	
	// Плановые доходы.
	ДополнительныеСвойства.Вставить("ПлановыеДоходы", тбзТабличнаяЧасть);
		
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_СчетНаОплатуЗаказчику(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	 	 	
	сткИтоги = Неопределено;
	
	тбзТабличнаяЧасть = Объект.РасшифровкаПлатежа.Выгрузить(,"ОбъектРасчетов, СтатьяДоходов, Валюта, Сумма, СуммаНДС");
	Если ВедетсяВалютныйУчет Тогда
		ПересчитатьСуммыВВалютуУпрУчета(тбзТабличнаяЧасть, Ложь, сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
		Объект.Сумма = сткИтоги.Сумма;
		Объект.СуммаНДС = сткИтоги.СуммаНДС;
		тбзТабличнаяЧасть.Свернуть("ОбъектРасчетов, СтатьяДоходов, Валюта", "Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	Иначе
		тбзТабличнаяЧасть.Свернуть("ОбъектРасчетов, СтатьяДоходов, Валюта", "Сумма, СуммаНДС");
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", тбзТабличнаяЧасть);
		
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_Рейс(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	тбзПрямыеРасходы = Объект.ПлановыеРасходы.Выгрузить(,"СтатьяРасходов, Задание, ПравилоРаспределения, Валюта, Сумма, СуммаНДС");
	
	сткИтоги = Неопределено;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
	
	Объект.СуммаРасходы = сткИтоги.Сумма;
	Объект.СуммаРасходыНДС = сткИтоги.СуммаНДС;
		
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки("Задание"), КолонкиСуммирования());
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	
	ЭтоФактическиеРасходы = Ложь;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
		
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();

	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ФактическиеДоходы(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	 	 	
	сткИтоги = Неопределено;

	тбзТабличнаяЧасть = Объект.Доходы.Выгрузить(,"ЗаявкаНаПеревозкуГруза, СтатьяДоходов, Валюта, Сумма, СуммаНДС, СкидкаСумма");
	
	Если Объект.СпособВводаФактическихДоходов <> Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда
		
		Для каждого Строка Из тбзТабличнаяЧасть Цикл
			Строка.ЗаявкаНаПеревозкуГруза = Объект.ЗаявкаНаПеревозкуГруза;
		КонецЦикла;
		
	КонецЕсли;
	
	ПересчитатьСуммыВВалютуУпрУчета(тбзТабличнаяЧасть, , сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
	
	Объект.СуммаДоходы = сткИтоги.Сумма;
	Объект.СуммаДоходыНДС = сткИтоги.СуммаНДС;
	Объект.СуммаДоходыСкидка	= сткИтоги.СуммаСкидка;

	тбзТабличнаяЧасть.Свернуть("ЗаявкаНаПеревозкуГруза, СтатьяДоходов, Валюта", "Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	
	тбзТабличнаяЧасть.Колонки.Добавить("сткСуммы");
	тбзТабличнаяЧасть.Колонки.Добавить("ХозяйственнаяОперация");
	
	ЭтоФактическиеДоходы = Истина;
	
	РеквизитыЗаявок = Неопределено;
	
	Если Ложь
		Или НЕ ЗначениеЗаполнено(Объект.Подразделение)
		Или НЕ ЗначениеЗаполнено(Объект.НаправлениеДеятельности) 
	Тогда
		мсвЗаявки = тбзТабличнаяЧасть.ВыгрузитьКолонку("ЗаявкаНаПеревозкуГруза");
		мсвЗаявки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаявки);
	     РеквизитыЗаявок = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(мсвЗаявки, "Подразделение, НаправлениеДеятельности");
	КонецЕсли;
		
	тбзДоходыПредприятия = ТаблицаДоходыРасходыПредприятия();
	
	Для каждого Строка Из тбзТабличнаяЧасть Цикл
		
		ЗаполнитьСтрокуТаблицыДоходов(Строка, ЭтоФактическиеДоходы, ВедетсяВалютныйУчет); 		
		Строка.ХозяйственнаяОперация = Перечисления.упХозяйственныеОперации.ОтражениеПрямыхДоходов;
		
		// Заполняется строка доходов предприятия.
		НоваяСтрока  = тбзДоходыПредприятия.Добавить();
		НоваяСтрока.Организация = Объект.Организация;
		НоваяСтрока.ХозяйственнаяОперация = Перечисления.упХозяйственныеОперации.ОтражениеПрямыхДоходов;
		НоваяСтрока.СтатьяДоходовРасходов = Строка.СтатьяДоходов;
		
		ЗаявкаНаПеревозкуГруза = Строка.ЗаявкаНаПеревозкуГруза;
		
		Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
			НоваяСтрока.Подразделение = Объект.Подразделение;
		Иначе
			сткРеквизиты = РеквизитыЗаявок.Получить(ЗаявкаНаПеревозкуГруза);
			Если сткРеквизиты <> Неопределено Тогда
				НоваяСтрока.Подразделение = сткРеквизиты.Подразделение;	
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.НаправлениеДеятельности) Тогда
			НоваяСтрока.НаправлениеДеятельности = Объект.НаправлениеДеятельности;
		Иначе
			сткРеквизиты = РеквизитыЗаявок.Получить(ЗаявкаНаПеревозкуГруза);
			Если сткРеквизиты <> Неопределено Тогда
				НоваяСтрока.НаправлениеДеятельности = сткРеквизиты.НаправлениеДеятельности;	
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Доходы  = Строка.Сумма;
		НоваяСтрока.Расходы = 0;
			
	КонецЦикла;
	
	тбзДоходыПредприятия.Свернуть("Организация, Подразделение, НаправлениеДеятельности, СтатьяДоходовРасходов, ХозяйственнаяОперация", "Доходы, Расходы");
	
	// Фактические доходы.
	ДополнительныеСвойства.Вставить("Доходы", тбзТабличнаяЧасть);
	
	// Доходы предприятия.
	ДополнительныеСвойства.Вставить("ДоходыРасходыПредприятия", тбзДоходыПредприятия);
		
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ФактическиеРасходы(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	сткИтоги = Неопределено;
	тбзРасходы = Объект.Расходы.Выгрузить(,"Рейс, Задание, СтатьяРасходов, ПравилоРаспределения, Валюта, Сумма, СуммаНДС");
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Расходы, с выбранным объектом аналитического разреза.
	|ВЫБРАТЬ
	|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбРасходы.Валюта КАК Валюта,
	|	тбРасходы.Рейс КАК Рейс,
	|	тбРасходы.Задание КАК Задание,
	|	тбРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	тбРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	тбРасходы.СуммаНДС КАК СуммаНДС,
	|	тбРасходы.Сумма КАК Сумма
	|ПОМЕСТИТЬ втРасходы
	|ИЗ
	|	&Расходы КАК тбРасходы
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Расходы дополняются вариантом расходов и видом аналитики. 
	|ВЫБРАТЬ
	|	втРасходы.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходы.Валюта КАК Валюта,
	|	втРасходы.Рейс КАК Рейс,
	|	втРасходы.Задание КАК Задание,
	|	втРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходы.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходы.СуммаНДС КАК СуммаНДС,
	|	втРасходы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ПОМЕСТИТЬ втРасходыПоВидамРасходов
	|ИЗ
	|	втРасходы КАК втРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО втРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Прямые расходы.
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Рейс КАК Рейс,
	|	втРасходыПоВидамРасходов.Задание КАК Задание,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ втРасходыПоВидамРасходов.ЭтоПрямыеРасходы
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Прочие расходы (косвенные расходы и РБП).
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
	|	втРасходыПоВидамРасходов.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втРасходыПоВидамРасходов.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втРасходыПоВидамРасходов.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втРасходыПоВидамРасходов.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ НЕ (втРасходыПоВидамРасходов.ЭтоПрямыеРасходы)
	|";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
	
	Запрос.УстановитьПараметр("Расходы", тбзРасходы);
	
	// По фактическим расходам пока не задаются аналитические разрезы, поэтому передается любой пустой.
	Запрос.УстановитьПараметр("ОбъектАналитическогоРазреза", Справочники.упВидыПеревозок.ПустаяСсылка());
	
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	тбзПрямыеРасходы = мсвРезультатЗапроса[2].Выгрузить();
	тбзПрочиеРасходы = мсвРезультатЗапроса[6].Выгрузить();
	
	ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);
	
	сткИтогиПрямыеРасходы = Неопределено;
	сткИтогиПрочиеРасходы = Неопределено;
	
	// Пересчет в валюту управленческого учета.
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтогиПрямыеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрочиеРасходы, Ложь, сткИтогиПрочиеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
	
	// Подсчет итогов расходов по документу.
	Объект.СуммаРасходы    = сткИтогиПрямыеРасходы.Сумма + сткИтогиПрочиеРасходы.Сумма;
	Объект.СуммаРасходыНДС = сткИтогиПрямыеРасходы.СуммаНДС + сткИтогиПрочиеРасходы.СуммаНДС;
	
	// Таблицы для формирования движений.
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам  Тогда
		КолонкиГруппировки = КолонкиГруппировки("Рейс, Задание");
	Иначе
		КолонкиГруппировки = КолонкиГруппировки("Задание");
	КонецЕсли;
	
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки, КолонкиСуммирования());
	тбзПрочиеРасходы.Свернуть(КолонкиГруппировки("АналитическийРазрез"), КолонкиСуммирования());
	
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	ЭтоФактическиеРасходы = Истина;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// Косвенные расходы и РБП.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ПодтверждениеЗаявкиНаТС(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)

	сткИтоги = Неопределено;
	
	тбзПрямыеРасходы = упРаспределениеРасходов.ПлановыеРасходыПоПредложениюПеревозчика(Объект.Основание);	
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы,Ложь , сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки(), КолонкиСуммирования());
	
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	ЭтоФактическиеРасходы = Ложь;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
			
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	тбзПрямыеРасходы.Колонки.Добавить("Задание", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);


КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_РемонтИТОТС(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)

	сткИтоги = Неопределено;
	СтатьяРасходовВидРемонта       = ДополнительныеСвойства.СтатьяРасходов;
	ПравилоРаспределенияВидРемонта = ДополнительныеСвойства.ПравилоРаспределения;
	
	// Плановые расходы.
	тбзПлановыеРасходы = ТаблицаРасходов();
	Если ДополнительныеСвойства.ВариантРасходов = Перечисления.упВариантыРасходов.Прямые Тогда
		
		НоваяСтрока                      = тбзПлановыеРасходы.Добавить();
		НоваяСтрока.СтатьяРасходов       = СтатьяРасходовВидРемонта;
		НоваяСтрока.ПравилоРаспределения = ПравилоРаспределенияВидРемонта;
		НоваяСтрока.Валюта               = Объект.Валюта;
		НоваяСтрока.Сумма                = Объект.СуммаРасходыПлан;
		НоваяСтрока.СуммаНДС             = Объект.СуммаРасходыПланНДС;
		НоваяСтрока.СуммаБезНДС          = Объект.СуммаРасходыПлан - Объект.СуммаРасходыПланНДС;
		НоваяСтрока.СуммаВалюта          = Объект.СуммаРасходыПланВалюта;
		НоваяСтрока.СуммаВалютаНДС       = Объект.СуммаРасходыПланНДСВалюта;
		НоваяСтрока.СуммаБезНДСВалюта    = Объект.СуммаРасходыПланВалюта - Объект.СуммаРасходыПланНДСВалюта;
		
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	ФормироватьФактическиеРасходы = Ложь
							  Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
							  Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен;
							  
	Если ФормироватьФактическиеРасходы Тогда
	
		// Собственные запчасти, ГСМ, агрегаты и доп. оборудование.
		тбзРасходыСобственныеМатериалы = ТаблицаРасходов();
		
		// Распределить рассчитанные суммы остатков по заданным статьям расходов,
		// пропорционально количеству.
		Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
			
			Если ВедетсяВалютныйУчет Тогда
				ВалютаРегламентногоУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
			Иначе
				ВалютаРегламентногоУчета = Справочники.Валюты.ПустаяСсылка();	
			КонецЕсли;
			
			ЗапчастиПоСтатьямРасходов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ЗапчастиПоСтатьямРасходов");
			
			Для каждого Материал Из ДополнительныеСвойства.ОстаткиМатериалов Цикл
				
				СтатьяРасходов = Справочники.упСтатьиДоходовРасходов.ПустаяСсылка();
				
				Если ЗапчастиПоСтатьямРасходов <> Неопределено Тогда
					
					мсвСтрокиЗапчасти    = ЗапчастиПоСтатьямРасходов.НайтиСтроки(Новый Структура("Материал, Склад", Материал.Материал, Материал.Склад));
					мсвСтатьиРасходов    = ЗапчастиПоСтатьямРасходов.ВыгрузитьКолонку("СтатьяРасходов");
					мсвСтатьиРасходов    = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвСтатьиРасходов);
					ПравилаРаспределения = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвСтатьиРасходов, "ПравилаРаспределения");
					
					// Если это не запчасть.
					Если мсвСтрокиЗапчасти.Количество() = 0 Тогда
						СтатьяРасходов = СтатьяРасходовВидРемонта;
						
						НоваяСтрока = тбзРасходыСобственныеМатериалы.Добавить();
						НоваяСтрока.СтатьяРасходов       = СтатьяРасходов;
						НоваяСтрока.ПравилоРаспределения = ПравилоРаспределенияВидРемонта;
						НоваяСтрока.Валюта               = ВалютаРегламентногоУчета;
						НоваяСтрока.Сумма                = Материал.СуммаСНДС;
						НоваяСтрока.СуммаНДС             = Материал.СуммаСНДС - Материал.СуммаБезНДС;
						
						// Если по запчасти только одна статья расходов в табличной части.
					ИначеЕсли мсвСтрокиЗапчасти.Количество() = 1 Тогда
						СтатьяРасходов = мсвСтрокиЗапчасти[0].СтатьяРасходов;
						
						НоваяСтрока = тбзРасходыСобственныеМатериалы.Добавить();
						НоваяСтрока.СтатьяРасходов       = СтатьяРасходов;
						НоваяСтрока.ПравилоРаспределения = ПравилоРаспределенияВидРемонта;
						НоваяСтрока.Валюта               = ВалютаРегламентногоУчета;
						НоваяСтрока.Сумма                = Материал.СуммаСНДС;
						НоваяСтрока.СуммаНДС             = Материал.СуммаСНДС - Материал.СуммаБезНДС;
						
						// Если по запчасти задано более чем одна статья расходов.
					Иначе
						
						мсвКоэффициенты = Новый Массив;
						Для каждого Строка Из мсвСтрокиЗапчасти Цикл
							мсвКоэффициенты.Добавить(Строка.Количество);
						КонецЦикла;
						
						мсвРаспределениеСуммыСНДС   = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(Материал.СуммаСНДС, мсвКоэффициенты);
						мсвРаспределениеСуммыБезНДС = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(Материал.СуммаБезНДС, мсвКоэффициенты);
						
						Для й = 0 По мсвСтрокиЗапчасти.Количество() - 1 Цикл
							СтрокаЗапчасти  = мсвСтрокиЗапчасти[й];
							СуммаСНДС       = мсвРаспределениеСуммыСНДС[й];
							СумммаБезНДС    = мсвРаспределениеСуммыБезНДС[й];
							
							НоваяСтрока = тбзРасходыСобственныеМатериалы.Добавить();
							НоваяСтрока.СтатьяРасходов       = СтрокаЗапчасти.СтатьяРасходов;
							НоваяСтрока.ПравилоРаспределения = ПравилаРаспределения.Получить(СтрокаЗапчасти.СтатьяРасходов);
							НоваяСтрока.Валюта               = ВалютаРегламентногоУчета;
							НоваяСтрока.Сумма                = СуммаСНДС;
							НоваяСтрока.СуммаНДС             = СуммаСНДС - СумммаБезНДС;
							
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла; 
		
		КонецЕсли;
		
		тбзЗапчасти = Объект.Запчасти.Выгрузить();
		тбзРаботы   = Объект.Работы.Выгрузить();
		тбзШины     = Объект.ШиныУзлыАгрегаты.Выгрузить();;
		тбзСливыГСМ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РасходыСливыГСМ");
		Если тбзСливыГСМ = Неопределено Тогда
			тбзСливыГСМ = ТаблицаРасходов();
		КонецЕсли;
		тбзДополнительноеОборудование = Объект.ДополнительноеОборудование.Выгрузить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|
		|//{Запрос: 0 ////////////////////////////////////////
		|// Расходы на собственные запчасти, ГСМ, агрегаты  .
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	&ОбъектАналитическогоРазрезаТранспортноеСредство КАК ОбъектАналитическогоРазрезаТранспортноеСредство,
		|	тбРасходыСобственныеМатериалы.Валюта КАК Валюта,
		|	тбРасходыСобственныеМатериалы.СтатьяРасходов КАК СтатьяРасходов,
		|	тбРасходыСобственныеМатериалы.СуммаНДС КАК СуммаНДС,
		|	тбРасходыСобственныеМатериалы.Сумма КАК Сумма
		|ПОМЕСТИТЬ втРасходыСобственныеМатериалы
		|ИЗ
		|	&РасходыСобственныеМатериалы КАК тбРасходыСобственныеМатериалы
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Расходы на приобретенные запчасти.
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	&ОбъектАналитическогоРазрезаТранспортноеСредство КАК ОбъектАналитическогоРазрезаТранспортноеСредство,
		|	тбЗапчастиПриобретенные.Валюта КАК Валюта,
		|	тбЗапчастиПриобретенные.СтатьяРасходов КАК СтатьяРасходов,
		|	тбЗапчастиПриобретенные.СуммаНДС КАК СуммаНДС,
		|	тбЗапчастиПриобретенные.Сумма КАК Сумма
		|ПОМЕСТИТЬ втЗапчасти
		|ИЗ
		|	&Запчасти КАК тбЗапчастиПриобретенные
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Расходы на приобретенные запчасти.
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	&ОбъектАналитическогоРазрезаТранспортноеСредство КАК ОбъектАналитическогоРазрезаТранспортноеСредство,
		|	тбРаботы.Валюта КАК Валюта,
		|	тбРаботы.СтатьяРасходов КАК СтатьяРасходов,
		|	тбРаботы.СуммаНДС КАК СуммаНДС,
		|	тбРаботы.Сумма КАК Сумма
		|ПОМЕСТИТЬ втРаботы
		|ИЗ
		|	&Работы КАК тбРаботы
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|// Расходы на работы.
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	&ОбъектАналитическогоРазрезаТранспортноеСредство КАК ОбъектАналитическогоРазрезаТранспортноеСредство,
		|	тбШины.Валюта КАК Валюта,
		|	&СтатьяРасходов КАК СтатьяРасходов,
		|	тбШины.СуммаНДС КАК СуммаНДС,
		|	тбШины.Сумма КАК Сумма
		|ПОМЕСТИТЬ втШины
		|ИЗ
		|	&Шины КАК тбШины
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|// Расходы - сливы ГСМ.
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	&ОбъектАналитическогоРазрезаТранспортноеСредство КАК ОбъектАналитическогоРазрезаТранспортноеСредство,
		|	тбСливыГСМ.Валюта КАК Валюта,
		|	тбСливыГСМ.СтатьяРасходов КАК СтатьяРасходов,
		|	тбСливыГСМ.СуммаНДС КАК СуммаНДС,
		|	тбСливыГСМ.Сумма КАК Сумма
		|ПОМЕСТИТЬ втСливыГСМ
		|ИЗ
		|	&СливыГСМ КАК тбСливыГСМ
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|// Расходы - сливы ГСМ.
		|ВЫБРАТЬ
		|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	&ОбъектАналитическогоРазрезаТранспортноеСредство КАК ОбъектАналитическогоРазрезаТранспортноеСредство,
		|	тбДополнительноеОборудование.Валюта КАК Валюта,
		|	&СтатьяРасходов КАК СтатьяРасходов,
		|	тбДополнительноеОборудование.СуммаНДС КАК СуммаНДС,
		|	тбДополнительноеОборудование.Сумма КАК Сумма
		|ПОМЕСТИТЬ втДополнительноеОборудование
		|ИЗ
		|	&ДополнительноеОборудование КАК тбДополнительноеОборудование
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|// Расходы дополняются вариантом расходов и видом аналитики.
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|				И упСтатьиДоходовРасходов.ТранспортноеСредство
		|				И ТИПЗНАЧЕНИЯ(тбРасходыСобственныеМатериалы.ОбъектАналитическогоРазреза) <> ТИП(Справочник.упТранспортныеСредства)
		|			ТОГДА тбРасходыСобственныеМатериалы.ОбъектАналитическогоРазрезаТранспортноеСредство
		|		ИНАЧЕ тбРасходыСобственныеМатериалы.ОбъектАналитическогоРазреза
		|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
		|	тбРасходыСобственныеМатериалы.Валюта КАК Валюта,
		|	тбРасходыСобственныеМатериалы.СтатьяРасходов КАК СтатьяРасходов,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	тбРасходыСобственныеМатериалы.СуммаНДС КАК СуммаНДС,
		|	тбРасходыСобственныеМатериалы.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ПОМЕСТИТЬ втРасходыПоВидамРасходов
		|ИЗ
		|	втРасходыСобственныеМатериалы КАК тбРасходыСобственныеМатериалы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО тбРасходыСобственныеМатериалы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|				И упСтатьиДоходовРасходов.ТранспортноеСредство
		|				И ТИПЗНАЧЕНИЯ(тбЗапчасти.ОбъектАналитическогоРазреза) <> ТИП(Справочник.упТранспортныеСредства)
		|			ТОГДА тбЗапчасти.ОбъектАналитическогоРазрезаТранспортноеСредство
		|		ИНАЧЕ тбЗапчасти.ОбъектАналитическогоРазреза
		|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
		|	тбЗапчасти.Валюта КАК Валюта,
		|	тбЗапчасти.СтатьяРасходов КАК СтатьяРасходов,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	тбЗапчасти.СуммаНДС КАК СуммаНДС,
		|	тбЗапчасти.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ИЗ
		|	втЗапчасти КАК тбЗапчасти
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО тбЗапчасти.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|				И упСтатьиДоходовРасходов.ТранспортноеСредство
		|				И ТИПЗНАЧЕНИЯ(тбРаботы.ОбъектАналитическогоРазреза) <> ТИП(Справочник.упТранспортныеСредства)
		|			ТОГДА тбРаботы.ОбъектАналитическогоРазрезаТранспортноеСредство
		|		ИНАЧЕ тбРаботы.ОбъектАналитическогоРазреза
		|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
		|	тбРаботы.Валюта КАК Валюта,
		|	тбРаботы.СтатьяРасходов КАК СтатьяРасходов,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	тбРаботы.СуммаНДС КАК СуммаНДС,
		|	тбРаботы.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ИЗ
		|	втРаботы КАК тбРаботы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО тбРаботы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|				И упСтатьиДоходовРасходов.ТранспортноеСредство
		|				И ТИПЗНАЧЕНИЯ(тбШины.ОбъектАналитическогоРазреза) <> ТИП(Справочник.упТранспортныеСредства)
		|			ТОГДА тбШины.ОбъектАналитическогоРазрезаТранспортноеСредство
		|		ИНАЧЕ тбШины.ОбъектАналитическогоРазреза
		|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
		|	тбШины.Валюта КАК Валюта,
		|	тбШины.СтатьяРасходов КАК СтатьяРасходов,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	тбШины.СуммаНДС КАК СуммаНДС,
		|	тбШины.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ИЗ
		|	втШины КАК тбШины
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО тбШины.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|				И упСтатьиДоходовРасходов.ТранспортноеСредство
		|				И ТИПЗНАЧЕНИЯ(тбСливыГСМ.ОбъектАналитическогоРазреза) <> ТИП(Справочник.упТранспортныеСредства)
		|			ТОГДА тбСливыГСМ.ОбъектАналитическогоРазрезаТранспортноеСредство
		|		ИНАЧЕ тбСливыГСМ.ОбъектАналитическогоРазреза
		|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
		|	тбСливыГСМ.Валюта КАК Валюта,
		|	тбСливыГСМ.СтатьяРасходов КАК СтатьяРасходов,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	тбСливыГСМ.СуммаНДС КАК СуммаНДС,
		|	тбСливыГСМ.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ИЗ
		|	втСливыГСМ КАК тбСливыГСМ
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО тбСливыГСМ.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИСТИНА
		|				И упСтатьиДоходовРасходов.ТранспортноеСредство
		|				И ТИПЗНАЧЕНИЯ(тбДополнительноеОборудование.ОбъектАналитическогоРазреза) <> ТИП(Справочник.упТранспортныеСредства)
		|			ТОГДА тбДополнительноеОборудование.ОбъектАналитическогоРазрезаТранспортноеСредство
		|		ИНАЧЕ тбДополнительноеОборудование.ОбъектАналитическогоРазреза
		|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
		|	тбДополнительноеОборудование.Валюта КАК Валюта,
		|	тбДополнительноеОборудование.СтатьяРасходов КАК СтатьяРасходов,
		|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
		|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
		|	тбДополнительноеОборудование.СуммаНДС КАК СуммаНДС,
		|	тбДополнительноеОборудование.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПрямыеРасходы,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
		|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
		|				ИЛИ упСтатьиДоходовРасходов.Агрегат
		|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НуженАналитическийРазрез,
		|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
		|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
		|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
		|ИЗ
		|	втДополнительноеОборудование КАК тбДополнительноеОборудование
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО тбДополнительноеОборудование.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|;
		|//{Запрос: 7 ////////////////////////////////////////
		|// Прямые фактические расходы.
		|ВЫБРАТЬ
		|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
		|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
		|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
		|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
		|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
		|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы
		|ИЗ
		|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
		|ГДЕ втРасходыПоВидамРасходов.ЭтоПрямыеРасходы
		|;
		|//{Запрос: 8 ////////////////////////////////////////
		|// Прочие расходы (косвенные расходы и РБП).
		|ВЫБРАТЬ
		|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
		|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
		|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
		|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
		|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
		|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
		|	втРасходыПоВидамРасходов.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
		|	втРасходыПоВидамРасходов.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
		|	втРасходыПоВидамРасходов.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
		|	втРасходыПоВидамРасходов.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
		|	втРасходыПоВидамРасходов.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
		|ПОМЕСТИТЬ втПрочиеРасходы
		|ИЗ
		|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
		|ГДЕ НЕ (втРасходыПоВидамРасходов.ЭтоПрямыеРасходы)
		|";

		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
		
		Если Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство Тогда
			ОбъектАналитическогоРазреза = Объект.ТранспортноеСредство;	
			ОбъектАналитическогоРазрезаТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
			
		ИначеЕсли Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
			ОбъектАналитическогоРазреза = Объект.ДополнительноеОборудованиеРемонта;		
			ОбъектАналитическогоРазрезаТранспортноеСредство = Объект.ТранспортноеСредствоОбъектаРемонта;
			
		ИначеЕсли Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда		
			ОбъектАналитическогоРазреза = Объект.Агрегат;	
			ОбъектАналитическогоРазрезаТранспортноеСредство = Объект.ТранспортноеСредствоОбъектаРемонта;
			
		КонецЕсли; 
		
		
		Запрос.УстановитьПараметр("РасходыСобственныеМатериалы", тбзРасходыСобственныеМатериалы);
		Запрос.УстановитьПараметр("Запчасти",                    тбзЗапчасти);
		Запрос.УстановитьПараметр("Работы",                      тбзРаботы);
		Запрос.УстановитьПараметр("Шины",                        тбзШины);
		Запрос.УстановитьПараметр("СливыГСМ",                    тбзСливыГСМ);
		Запрос.УстановитьПараметр("ДополнительноеОборудование",  тбзДополнительноеОборудование);
		Запрос.УстановитьПараметр("ОбъектАналитическогоРазреза", ОбъектАналитическогоРазреза);
		Запрос.УстановитьПараметр("ОбъектАналитическогоРазрезаТранспортноеСредство", ОбъектАналитическогоРазрезаТранспортноеСредство);
		Запрос.УстановитьПараметр("СтатьяРасходов",              СтатьяРасходовВидРемонта);
		
		мсвРезультат = Запрос.ВыполнитьПакет();
		
		тбзПрямыеРасходы = мсвРезультат[7].Выгрузить();
		тбзПрочиеРасходы = мсвРезультат[11].Выгрузить();
		
		ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);
				
		сткИтогиПрямыеРасходы = Неопределено;
		сткИтогиПрочиеРасходы = Неопределено;
			
		// Пересчет в валюту управленческого учета.
		ДатаПересчетаВалюты = ?(ЗначениеЗаполнено(Объект.НачалоРемонтаФакт), Объект.НачалоРемонтаФакт, Объект.Дата);
		ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтогиПрямыеРасходы, ДатаПересчетаВалюты, ВедетсяВалютныйУчет);
		ПересчитатьСуммыВВалютуУпрУчета(тбзПрочиеРасходы, Ложь, сткИтогиПрочиеРасходы, ДатаПересчетаВалюты, ВедетсяВалютныйУчет);
			
		// Для корректных итогов по документу добавляются суммы расходов на ГСМ.
		// Так как расходы по ГСМ списываются по мере потребления топлива,
		// а общая сумма документа должна включать в себя расходы и на заправку.
		тбзГСМ  = Объект.ГСМ.Выгрузить();
		
		сткИтогиГСМ = Неопределено;
		
		ПересчитатьСуммыВВалютуУпрУчета(тбзГСМ, Ложь , сткИтогиГСМ, ?(ЗначениеЗаполнено(Объект.НачалоРемонтаФакт), Объект.НачалоРемонтаФакт, Объект.Дата), ВедетсяВалютныйУчет);

		// Подсчет итогов расходов по документу.
		Объект.СуммаРасходы    = сткИтогиПрямыеРасходы.Сумма + сткИтогиПрочиеРасходы.Сумма + сткИтогиГСМ.СуммаНДС;
		Объект.СуммаРасходыНДС = сткИтогиПрямыеРасходы.СуммаНДС + сткИтогиПрочиеРасходы.СуммаНДС + сткИтогиГСМ.СуммаНДС;

		// Таблицы для формирования движений.
		тбзПрямыеРасходы.Свернуть(КолонкиГруппировки(), КолонкиСуммирования());
		тбзПрочиеРасходы.Свернуть(КолонкиГруппировки("АналитическийРазрез"), КолонкиСуммирования());
		
	Иначе
		тбзПрямыеРасходы = ТаблицаРасходов();	
	КонецЕсли;	
		
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	
	// Заполняется структура сумм, для распределения расходов.
	ЭтоФактическиеРасходы = Истина;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	// К прямым фактическим расходам добавляются плановые расходы.
	ЭтоФактическиеРасходы = Ложь;
	Для каждого Строка Из тбзПлановыеРасходы Цикл
		НоваяСтрока = тбзПрямыеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		ЗаполнитьСтрокуТаблицыРасходов(НоваяСтрока, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	Если тбзПрямыеРасходы.Колонки.Найти("Задание") = Неопределено Тогда
		тбзПрямыеРасходы.Колонки.Добавить("Задание", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	КонецЕсли;
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// Косвенные расходы и РБП.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);

	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ЗаявкаНаРемонтИТОТС(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)

	тбзРасходы = Объект.Работы.Выгрузить(,"СтатьяРасходов, Валюта, Сумма, СуммаНДС");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Расходы на собственные запчасти, ГСМ, агрегаты  .
	|ВЫБРАТЬ
	|	тбРасходы.Валюта КАК Валюта,
	|	тбРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	тбРасходы.СуммаНДС КАК СуммаНДС,
	|	тбРасходы.Сумма КАК Сумма
	|ПОМЕСТИТЬ втРасходы
	|ИЗ
	|	&Расходы КАК тбРасходы
	|;
	|// Расходы дополняются правилом распределения
	|ВЫБРАТЬ
	|	тбРасходы.Валюта КАК Валюта,
	|	тбРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ЕстьNull(упСтатьиДоходовРасходов.ПравилаРаспределения, ЗНАЧЕНИЕ(Справочник.упПравилаРаспределенияРасходов.ПустаяСсылка)) КАК ПравилоРаспределения,
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	тбРасходы.СуммаНДС КАК СуммаНДС,
	|	тбРасходы.Сумма КАК Сумма
	|ИЗ
	|	втРасходы КАК тбРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО тбРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка";
	
	Запрос.УстановитьПараметр("Расходы", тбзРасходы);
	
	тбзПрямыеРасходы = Запрос.Выполнить().Выгрузить();
	
	сткИтоги = Неопределено;
	ДатаПересчетаИтогов = ?(ЗначениеЗаполнено(Объект.НачалоРемонтаПлан), Объект.НачалоРемонтаПлан, Объект.Дата);
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь , сткИтоги, ДатаПересчетаИтогов, ВедетсяВалютныйУчет);
	Объект.СуммаРасходыПлан    = сткИтоги.Сумма;
	Объект.СуммаРасходыПланНДС = сткИтоги.СуммаНДС;
	
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	
	// Заполняется структура сумм, для распределения расходов.
	ЭтоФактическиеРасходы = Ложь;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;

	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	тбзПрямыеРасходы.Колонки.Добавить("Задание", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);

	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_СлужебноеРасследование(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Ущерб.
	|ВЫБРАТЬ
	|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбУщерб.Валюта КАК Валюта,
	|	тбУщерб.СтатьяРасходов КАК СтатьяРасходов,
	|	тбУщерб.СуммаНДС КАК СуммаНДС,
	|	тбУщерб.Сумма КАК Сумма,
	|	тбУщерб.План КАК План
	|ПОМЕСТИТЬ втУщерб
	|ИЗ
	|	&Ущерб КАК тбУщерб
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Расходы по ответственным лицам.
	|ВЫБРАТЬ
	|	тбОтветственныеЛица.Сотрудник КАК ОбъектАналитическогоРазреза,
	|	тбОтветственныеЛица.Валюта КАК Валюта,
	|	тбОтветственныеЛица.СтатьяРасходов КАК СтатьяРасходов,
	|	тбОтветственныеЛица.РазмерШтрафа КАК Сумма
	|ПОМЕСТИТЬ втОтветственныеЛица
	|ИЗ
	|	&ОтветственныеЛица КАК тбОтветственныеЛица
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Расходы дополняются вариантом расходов и видом аналитики.
	|ВЫБРАТЬ
	|	тбУщерб.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбУщерб.Валюта КАК Валюта,
	|	тбУщерб.СтатьяРасходов КАК СтатьяРасходов,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	тбУщерб.СуммаНДС КАК СуммаНДС,
	|	тбУщерб.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	упСтатьиДоходовРасходов.Сотрудник КАК Сотрудник,
	|	тбУщерб.План КАК План,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ПОМЕСТИТЬ втРасходыПоВидамРасходов
	|ИЗ
	|	втУщерб КАК тбУщерб
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО тбУщерб.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА тбОтветственныеЛица.ОбъектАналитическогоРазреза = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|			ТОГДА &ОбъектАналитическогоРазреза
	|		ИНАЧЕ тбОтветственныеЛица.ОбъектАналитическогоРазреза
	|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
	|	тбОтветственныеЛица.Валюта КАК Валюта,
	|	тбОтветственныеЛица.СтатьяРасходов КАК СтатьяРасходов,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	0 КАК СуммаНДС,
	|	- тбОтветственныеЛица.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	упСтатьиДоходовРасходов.Сотрудник КАК Сотрудник,
	|	ЛОЖЬ КАК План,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ИЗ
	|	втОтветственныеЛица КАК тбОтветственныеЛица
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО тбОтветственныеЛица.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Плановые расходы.
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы,
	|	втРасходыПоВидамРасходов.План КАК План
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ ИСТИНА
	|	И втРасходыПоВидамРасходов.План
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|// Прямые фактические расходы.
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы,
	|	втРасходыПоВидамРасходов.План КАК План
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ ИСТИНА
	|	И втРасходыПоВидамРасходов.ЭтоПрямыеРасходы
	|	И НЕ (втРасходыПоВидамРасходов.План)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|// Прочие расходы (косвенные расходы и РБП).
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
	|	втРасходыПоВидамРасходов.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втРасходыПоВидамРасходов.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втРасходыПоВидамРасходов.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втРасходыПоВидамРасходов.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|	ПО втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза = упАналитическиеРазрезы.Сотрудник
	|ГДЕ ИСТИНА
	|	И НЕ (втРасходыПоВидамРасходов.ЭтоПрямыеРасходы)
	|	И НЕ (втРасходыПоВидамРасходов.План)
	|";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
	
	тбзУщерб                    = Объект.Ущерб.Выгрузить();
	тбзОтветственныеЛица        = Объект.ОтветственныеЛица.Выгрузить();
	ОбъектАналитическогоРазреза = Объект.Сотрудник;
	
	Запрос.УстановитьПараметр("Ущерб",                       тбзУщерб);
	Запрос.УстановитьПараметр("ОтветственныеЛица",           тбзОтветственныеЛица);
	Запрос.УстановитьПараметр("ОбъектАналитическогоРазреза", Объект.Сотрудник);
	
	мсвРезультатЗапрос = Запрос.ВыполнитьПакет();
	
	тбзПрямыеПлановыеРасходы = мсвРезультатЗапрос[3].Выгрузить();
	тбзПрямыеРасходы         = мсвРезультатЗапрос[4].Выгрузить();
	тбзПрочиеРасходы         = мсвРезультатЗапрос[8].Выгрузить();
	
	ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);
	
	// Плановые расходы.
	сткИтоги = Неопределено;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеПлановыеРасходы, Ложь, сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
	тбзПрямыеПлановыеРасходы.Свернуть(КолонкиГруппировки(), КолонкиСуммирования());
	
	Объект.СуммаРасходыПлан    = сткИтоги.Сумма;
	Объект.СуммаРасходыПланНДС = сткИтоги.СуммаНДС;
	ДополнительныеСвойства.Вставить("ПлановыеРасходы", тбзПрямыеПлановыеРасходы);
		
	// Прямые фактические расходы.
	сткИтогиПрямыеРасходы = Неопределено;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтогиПрямыеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки(), КолонкиСуммирования());
	
	сткИтогиПрочиеРасходы = Неопределено;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрочиеРасходы, Ложь, сткИтогиПрочиеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
	тбзПрочиеРасходы.Свернуть(КолонкиГруппировки("АналитическийРазрез"), КолонкиСуммирования());
	
	// Заполнение фактических итогов.
	Объект.СуммаРасходы    = сткИтогиПрямыеРасходы.Сумма + сткИтогиПрочиеРасходы.Сумма;
	Объект.СуммаРасходыНДС = сткИтогиПрямыеРасходы.СуммаНДС + сткИтогиПрочиеРасходы.СуммаНДС;
		
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	ЭтоФактическиеРасходы = Истина;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	ЭтоФактическиеРасходы = Ложь;
	Для каждого Строка Из тбзПрямыеПлановыеРасходы Цикл
		НоваяСтрока = тбзПрямыеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		ЗаполнитьСтрокуТаблицыРасходов(НоваяСтрока, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	тбзПрямыеРасходы.Колонки.Добавить("Задание", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// Косвенные расходы и РБП.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);


КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ИсходящаяПретензия(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)

	сткИтоги = Неопределено;
	
	тбзТабличнаяЧасть = Объект.ПлановыеДоходы.Выгрузить(,"СтатьяДоходов, Валюта, Сумма, СуммаНДС");
	Если ВедетсяВалютныйУчет Тогда
		ПересчитатьСуммыВВалютуУпрУчета(тбзТабличнаяЧасть, Ложь, сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
		тбзТабличнаяЧасть.Свернуть("СтатьяДоходов, Валюта", "Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	Иначе
		тбзТабличнаяЧасть.Свернуть("СтатьяДоходов, Валюта", "Сумма, СуммаНДС");
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("ПлановыеДоходы", тбзТабличнаяЧасть);

КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_ВходящаяПретензия(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)

	сткИтоги = Неопределено;
	
	тбзРасходы = Объект.Расходы.Выгрузить(,"СтатьяРасходов, Задание, ПравилоРаспределения, Валюта, Сумма, СуммаНДС");

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Расходы.
	|ВЫБРАТЬ
	|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбзРасходы.Валюта КАК Валюта,
	|	тбзРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	тбзРасходы.Задание КАК Задание,
	|	тбзРасходы.СуммаНДС КАК СуммаНДС,
	|	тбзРасходы.Сумма КАК Сумма
	|ПОМЕСТИТЬ втРасходы
	|ИЗ
	|	&Расходы КАК тбзРасходы
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Расходы дополняются вариантом расходов и видом аналитики.
	|ВЫБРАТЬ
	|	тбРасходы.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбРасходы.Валюта КАК Валюта,
	|	тбРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	тбРасходы.СуммаНДС КАК СуммаНДС,
	|	тбРасходы.Задание КАК Задание,
	|	тбРасходы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ПОМЕСТИТЬ втРасходыПоВидамРасходов
	|ИЗ
	|	втРасходы КАК тбРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО тбРасходы.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Прямые фактические расходы.
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.Задание КАК Задание,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ втРасходыПоВидамРасходов.ЭтоПрямыеРасходы
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Прочие расходы (косвенные расходы и РБП).
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
	|	втРасходыПоВидамРасходов.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втРасходыПоВидамРасходов.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втРасходыПоВидамРасходов.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втРасходыПоВидамРасходов.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ НЕ (втРасходыПоВидамРасходов.ЭтоПрямыеРасходы)
	|";

	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
	
	ОбъектАналитическогоРазреза = ?(ЗначениеЗаполнено(Объект.ДокументОснование), 
								ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ВидПеревозки"),
								Справочники.упВидыПеревозок.ПустаяСсылка());
	Запрос.УстановитьПараметр("ОбъектАналитическогоРазреза", ОбъектАналитическогоРазреза);
	Запрос.УстановитьПараметр("Расходы",                     тбзРасходы);
	
	мсвРезультат = Запрос.ВыполнитьПакет();
	
	тбзПрямыеРасходы   = мсвРезультат[2].Выгрузить();
	тбзПрочиеРасходы   = мсвРезультат[6].Выгрузить();
	
	ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);

	сткИтоги = Неопределено;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтоги, Объект.Дата, ВедетсяВалютныйУчет);
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки("Задание"), КолонкиСуммирования());
	
	сткИтогиПрочиеРасходы = Неопределено;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрочиеРасходы, Ложь, сткИтогиПрочиеРасходы, Объект.Дата, ВедетсяВалютныйУчет);
	тбзПрочиеРасходы.Свернуть(КолонкиГруппировки("АналитическийРазрез"), КолонкиСуммирования());
	
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	ЭтоФактическиеРасходы = Истина;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// Косвенные расходы и РБП.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);


КонецПроцедуры

// Процедура - Заполнить доходы расходы регламентная операция
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Набор движений.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//
Процедура ЗаполнитьДоходыРасходы_РегламентнаяОперация(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт

	сткИтоги = Неопределено;
	
	ВедетсяВалютныйУчет = ДополнительныеСвойства.ВедетсяВалютныйУчет;
	
	тбзПрямыеРасходы = ДополнительныеСвойства.Расходы;
	ПересчитатьСуммыВВалютуУпрУчета(тбзПрямыеРасходы, Ложь, сткИтоги, Реквизиты.Дата, ВедетсяВалютныйУчет);
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки("Задание"), КолонкиСуммирования());
	
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	ЭтоФактическиеРасходы = Истина;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);


КонецПроцедуры

// Процедура - Заполнить доходы расходы путевой лист
//
// Параметры:
//  ДополнительныеСвойства	 - Структура	 - Свойства.
//  Ссылка				 - ДокументСсылка	 - Ссылка на элемент.
//  Движения				 - КоллекцияДвижений - Набор движений.
//  Реквизиты				 - Структура		 - Реквизиты.
//  Отказ					 - Булево			 - Фиксировать изменения.
//
Процедура ЗаполнитьДоходыРасходы_ПутевойЛист(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ) Экспорт
	
	ВедетсяВалютныйУчет = ДополнительныеСвойства.ВедетсяВалютныйУчет;
	
	// ГСМ по ТС.
	Если ТипЗнч(Реквизиты.ГСМ) = Тип("ТаблицаЗначений") Тогда
		тбзГСМ = Реквизиты.ГСМ;
	Иначе	
		тбзГСМ = Реквизиты.ГСМ.Выгрузить(,"ВидГСМ, СуммаРасходовПлан, СуммаРасходовПланНДС, СуммаРасходов, СуммаРасходовНДС");
	КонецЕсли;
	
	// ГСМ по доп. оборудованию.
	Если ТипЗнч(Реквизиты.ГСМДополнительноеОборудование) = Тип("ТаблицаЗначений") Тогда
		тбзГСМДопОборудование = Реквизиты.ГСМДополнительноеОборудование;
	Иначе	
		тбзГСМДопОборудование = Реквизиты.ГСМДополнительноеОборудование.Выгрузить(,"ВидГСМ, РасходИзСобственногоБака, СуммаРасходовПлан, СуммаРасходовПланНДС, СуммаРасходов, СуммаРасходовНДС");
	КонецЕсли;

	тбзВыработкаАгрегатов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ВыработкаАгрегатов");
	Если тбзВыработкаАгрегатов = Неопределено Тогда
		тбзВыработкаАгрегатов = Новый ТаблицаЗначений;
		тбзВыработкаАгрегатов.Колонки.Добавить("Агрегат",        Новый ОписаниеТипов("СправочникСсылка.упШиныУзлыИАгрегаты"));
		тбзВыработкаАгрегатов.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
		тбзВыработкаАгрегатов.Колонки.Добавить("Сумма",          ТипСумма());
	Иначе
		тбзВыработкаАгрегатов.Свернуть("Агрегат, СтатьяРасходов", "Сумма");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Расходы на ГСМ.
	|ВЫБРАТЬ
	|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	&Валюта КАК Валюта,
	|	тбзГСМ.ВидГСМ КАК ВидГСМ,
	|	тбзГСМ.СуммаРасходовПланНДС КАК СуммаПланНДС,
	|	тбзГСМ.СуммаРасходовПлан КАК СуммаПлан,
	|	тбзГСМ.СуммаРасходов КАК Сумма,
	|	тбзГСМ.СуммаРасходовНДС КАК СуммаНДС
	|ПОМЕСТИТЬ втГСМ
	|ИЗ
	|	&ГСМ КАК тбзГСМ
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Расходы на приобретенные запчасти.
	|ВЫБРАТЬ
	|	&ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	&Валюта КАК Валюта,
	|	тбзГСМДопОборудование.ВидГСМ КАК ВидГСМ,
	|	тбзГСМДопОборудование.СуммаРасходовПланНДС КАК СуммаПланНДС,
	|	тбзГСМДопОборудование.СуммаРасходовПлан КАК СуммаПлан,
	|	тбзГСМДопОборудование.СуммаРасходов КАК Сумма,
	|	тбзГСМДопОборудование.СуммаРасходовНДС КАК СуммаНДС,
	|	тбзГСМДопОборудование.РасходИзСобственногоБака КАК РасходИзСобственногоБака
	|ПОМЕСТИТЬ втГСМДопОборудование
	|ИЗ
	|	&ГСМДопОборудование КАК тбзГСМДопОборудование
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Расходы на ГСМ.
	|ВЫБРАТЬ
	|	тбзВыработкаАгрегатов.Агрегат КАК ОбъектАналитическогоРазреза,
	|	&Валюта КАК Валюта,
	|	тбзВыработкаАгрегатов.СтатьяРасходов КАК СтатьяРасходов,
	|	тбзВыработкаАгрегатов.Сумма КАК Сумма,
	|	0 КАК СуммаНДС
	|ПОМЕСТИТЬ втВыработкаАгрегатов
	|ИЗ
	|	&ВыработкаАгрегатов КАК тбзВыработкаАгрегатов
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Расходы дополняются вариантом расходов и видом аналитики.
	|ВЫБРАТЬ
	|	тбГСМ.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбГСМ.ВидГСМ КАК ОбъектСтатьяРасходов,
	|	тбГСМ.Валюта КАК Валюта,
	|	ЕСТЬNULL(упСтатьиДоходовРасходов.Ссылка, ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)) КАК СтатьяРасходов,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	тбГСМ.СуммаНДС КАК СуммаНДС,
	|	тбГСМ.Сумма КАК Сумма,
	|	тбГСМ.СуммаПланНДС КАК СуммаПланНДС,
	|	тбГСМ.СуммаПлан КАК СуммаПлан,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ПОМЕСТИТЬ втРасходыПоВидамРасходов
	|ИЗ
	|	втГСМ КАК тбГСМ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыГСМ КАК упВидыГСМ
	|	ПО тбГСМ.ВидГСМ = упВидыГСМ.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО упВидыГСМ.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбГСМДопОборудование.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбГСМДопОборудование.ВидГСМ КАК ОбъектСтатьяРасходов,
	|	тбГСМДопОборудование.Валюта КАК Валюта,
	|	ЕСТЬNULL(упСтатьиДоходовРасходов.Ссылка, ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)) КАК СтатьяРасходов,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	тбГСМДопОборудование.СуммаНДС КАК СуммаНДС,
	|	тбГСМДопОборудование.Сумма КАК Сумма,
	|	тбГСМДопОборудование.СуммаПланНДС КАК СуммаПланНДС,
	|	тбГСМДопОборудование.СуммаПлан КАК СуммаПлан,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ИЗ
	|	втГСМДопОборудование КАК тбГСМДопОборудование
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыГСМ КАК упВидыГСМ
	|	ПО тбГСМДопОборудование.ВидГСМ = упВидыГСМ.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО упВидыГСМ.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|ГДЕ тбГСМДопОборудование.РасходИзСобственногоБака
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбВыработкаАгрегатов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	тбВыработкаАгрегатов.ОбъектАналитическогоРазреза КАК ОбъектСтатьяРасходов,
	|	тбВыработкаАгрегатов.Валюта КАК Валюта,
	|	тбВыработкаАгрегатов.СтатьяРасходов КАК СтатьяРасходов,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	тбВыработкаАгрегатов.СуммаНДС КАК СуммаНДС,
	|	тбВыработкаАгрегатов.Сумма КАК Сумма,
	|	0 КАК СуммаПланНДС,
	|	0 КАК СуммаПлан,
	|	ВЫБОР
	|		КОГДА упСтатьиДоходовРасходов.ВариантРасходов = ЗНАЧЕНИЕ(Перечисление.упВариантыРАсходов.Прямые)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПрямыеРасходы,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|				ИЛИ упСтатьиДоходовРасходов.ТранспортноеСредство
	|				ИЛИ упСтатьиДоходовРасходов.ВидПеревозки
	|				ИЛИ упСтатьиДоходовРасходов.Агрегат
	|				ИЛИ упСтатьиДоходовРасходов.Сотрудник
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НуженАналитическийРазрез,
	|	упСтатьиДоходовРасходов.ТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство,
	|	упСтатьиДоходовРасходов.ВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	упСтатьиДоходовРасходов.Агрегат КАК НужнаАналитикаАгрегат,
	|	упСтатьиДоходовРасходов.Сотрудник КАК НужнаАналитикаСотрудник
	|ИЗ
	|	втВыработкаАгрегатов КАК тбВыработкаАгрегатов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|	ПО тбВыработкаАгрегатов.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходовТ.ОбъектСтатьяРасходов КАК ОбъектСтатьяРасходов
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходовТ
	|ГДЕ втРасходыПоВидамРасходовТ.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|// Плановые расходы.
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.СуммаПланНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.СуммаПлан КАК Сумма,
	|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ ИСТИНА
	|	И втРасходыПоВидамРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|	И (ЛОЖЬ
	|		ИЛИ втРасходыПоВидамРасходов.СуммаПланНДС <> 0
	|		ИЛИ втРасходыПоВидамРасходов.СуммаПлан <> 0)
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|// Прямые фактические расходы.
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.ЭтоПрямыеРасходы КАК ЭтоПрямыеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ ИСТИНА
	|	И втРасходыПоВидамРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|	И втРасходыПоВидамРасходов.ЭтоПрямыеРасходы
	|	И (ЛОЖЬ
	|		ИЛИ втРасходыПоВидамРасходов.СуммаНДС <> 0
	|		ИЛИ втРасходыПоВидамРасходов.Сумма <> 0)
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|// Прочие расходы (косвенные расходы и РБП).
	|ВЫБРАТЬ
	|	втРасходыПоВидамРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втРасходыПоВидамРасходов.Валюта КАК Валюта,
	|	втРасходыПоВидамРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоВидамРасходов.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка) КАК АналитическийРазрез,
	|	втРасходыПоВидамРасходов.СуммаНДС КАК СуммаНДС,
	|	втРасходыПоВидамРасходов.Сумма КАК Сумма,
	|	втРасходыПоВидамРасходов.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
	|	втРасходыПоВидамРасходов.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втРасходыПоВидамРасходов.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втРасходыПоВидамРасходов.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втРасходыПоВидамРасходов.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ПОМЕСТИТЬ втПрочиеРасходы
	|ИЗ
	|	втРасходыПоВидамРасходов КАК втРасходыПоВидамРасходов
	|ГДЕ ИСТИНА
	|	И втРасходыПоВидамРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|	И НЕ (втРасходыПоВидамРасходов.ЭтоПрямыеРасходы)
	|	И (ЛОЖЬ
	|		ИЛИ втРасходыПоВидамРасходов.СуммаНДС <> 0
	|		ИЛИ втРасходыПоВидамРасходов.Сумма <> 0)
	|";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() + ТекстЗапросаПрочиеРасходыАналитика();
	
	Если ВедетсяВалютныйУчет Тогда
		ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
	Иначе
		ВалютаРеглУчета = Справочники.Валюты.ПустаяСсылка();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Валюта",                      ВалютаРеглУчета);
	Запрос.УстановитьПараметр("ГСМ",                         тбзГСМ);
	Запрос.УстановитьПараметр("ГСМДопОборудование",          тбзГСМДопОборудование);
	Запрос.УстановитьПараметр("ВыработкаАгрегатов",          тбзВыработкаАгрегатов);
	Запрос.УстановитьПараметр("ОбъектАналитическогоРазреза", Реквизиты.ТранспортноеСредство);
	
	мсвРезультат = Запрос.ВыполнитьПакет();
	
	ВыборкаОбъектыБезСтатейРасходов = мсвРезультат[4].Выбрать();
	Пока ВыборкаОбъектыБезСтатейРасходов.Следующий() Цикл
		ТекстСообщения = Нстр("ru = 'Для объекта ""%1"" не удалось определить статью расходов'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВыборкаОбъектыБезСтатейРасходов.ОбъектСтатьяРасходов);
		ЗаписьЖурналаРегистрации(СобытиеОшибкаРаспределенияРасходов(),УровеньЖурналаРегистрации.Предупреждение ,,, ТекстСообщения,РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);	
	КонецЦикла;
	
	тбзПлановыеРасходы = мсвРезультат[5].Выгрузить();
	тбзПрямыеРасходы   = мсвРезультат[6].Выгрузить();
	тбзПрочиеРасходы   = мсвРезультат[10].Выгрузить();
	
	ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы);
	
	ПересчитатьСуммыВВалютуРеглУчета(тбзПлановыеРасходы, Реквизиты.Дата, ВедетсяВалютныйУчет);
	ПересчитатьСуммыВВалютуРеглУчета(тбзПрямыеРасходы, Реквизиты.Дата, ВедетсяВалютныйУчет);
	ПересчитатьСуммыВВалютуРеглУчета(тбзПрочиеРасходы, Реквизиты.Дата, ВедетсяВалютныйУчет);
	
	тбзПрямыеРасходы.Свернуть(КолонкиГруппировки(), КолонкиСуммирования());
	тбзПлановыеРасходы.Свернуть(КолонкиГруппировки(), КолонкиСуммирования());
	тбзПрочиеРасходы.Свернуть(КолонкиГруппировки("АналитическийРазрез"), КолонкиСуммирования());
	
	тбзПрямыеРасходы.Колонки.Добавить("сткСуммы");
	ЭтоФактическиеРасходы = Истина;
	Для каждого Строка Из тбзПрямыеРасходы Цикл
		ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	ЭтоФактическиеРасходы = Ложь;
	Для каждого Строка Из тбзПлановыеРасходы Цикл
		НоваяСтрока = тбзПрямыеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		ЗаполнитьСтрокуТаблицыРасходов(НоваяСтрока, ЭтоФактическиеРасходы, ВедетсяВалютныйУчет); 	
	КонецЦикла;
	
	тбзПрямыеРасходыНаТС = тбзПрямыеРасходы.Скопировать();
	тбзПрямыеРасходы.Колонки.Добавить("Задание", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	
	// Прямые расходы.
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходы);
	
	// Прямые расходы на ТС.
	ДополнительныеСвойства.Вставить("ПрямыеРасходыНаТС", тбзПрямыеРасходыНаТС);
	
	// Косвенные расходы и РБП.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);


КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_РаспределениеПрочихРасходов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	ХозяйственнаяОперация = Перечисления.упХозяйственныеОперации.РаспределениеКосвенныхРасходов;
	
	тбзРасходыПоРейсам = Объект.РасходыПоРейсам.Выгрузить(,"Рейс, АналитическийРазрез, СтатьяРасходов, ПравилоРаспределения, Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	тбзРасходыПоРейсам.Колонки.Добавить("ЗаданиеНаПеревозку");
	тбзРасходыПоРейсам.Колонки.Добавить("ХозяйственнаяОперация");
	тбзРасходыПоРейсам.Колонки.Добавить("Валюта");
	тбзРасходыПоРейсам.Колонки.Добавить("сткСуммы");
	Для каждого Строка Из тбзРасходыПоРейсам Цикл
		Строка.Валюта = Объект.Валюта;
		Строка.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	тбзРасходыПоТС = Объект.РасходыПоТС.Выгрузить(,"АналитическийРазрез, ТранспортноеСредство, СтатьяРасходов, Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	тбзРасходыПоТС.Колонки.Добавить("ХозяйственнаяОперация");
	тбзРасходыПоТС.Колонки.Добавить("Валюта");
	тбзРасходыПоТС.Колонки.Добавить("сткСуммы");
	Для каждого Строка Из тбзРасходыПоТС Цикл
		Строка.Валюта = Объект.Валюта;
		Строка.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	тбзРезультатыФинансовыйРезультат = Объект.РезультатыФинансовыйРезультат.Выгрузить(,"АналитическийРазрез, НаправлениеДеятельности, Организация, Подразделение, Расходы, СтатьяРасходов");
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("ХозяйственнаяОперация");
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("Валюта");
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("Сумма",          ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("СуммаНДС",       ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("СуммаВалюта",    ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("СуммаВалютаНДС", ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("сткСуммы");
	
	Для каждого Строка Из тбзРезультатыФинансовыйРезультат Цикл
		Строка.Валюта = Объект.Валюта;
		Строка.Сумма          = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Объект.Валюта, Строка.Расходы, Объект.Дата);
		Строка.СуммаНДС       = 0;
		Строка.СуммаВалюта    = Строка.Расходы;
		Строка.СуммаВалютаНДС = 0;
		Строка.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	тбзПрочиеРасходы = Объект.ПрочиеРасходы.Выгрузить();
	тбзПрочиеРасходы.Колонки.Добавить("Организация");
	тбзПрочиеРасходы.Колонки.Добавить("ВидДвижения");
	тбзПрочиеРасходы.Колонки.Добавить("СтатьяРасходов");
	тбзПрочиеРасходы.Колонки.Добавить("СуммаБезНДС",       ТипСумма());
	тбзПрочиеРасходы.Колонки.Добавить("СуммаБезНДСВалюта", ТипСумма());
	Для каждого Строка Из тбзПрочиеРасходы Цикл
		Строка.ВидДвижения       = ВидДвиженияНакопления.Расход;
		Строка.Организация       = Объект.Организация;
		Строка.СтатьяРасходов    = Объект.СтатьяРасходов;
		Строка.СуммаБезНДС       = Строка.Сумма - Строка.СуммаНДС;
		Строка.СуммаБезНДСВалюта = Строка.СуммаВалюта - Строка.СуммаВалютаНДС;;
	КонецЦикла;

	ДополнительныеСвойства.Вставить("ПрямыеРасходы",              		тбзРасходыПоРейсам);
	ДополнительныеСвойства.Вставить("РаспределениеРасходовНаТС",          тбзРасходыПоТС);
	ДополнительныеСвойства.Вставить("РаспределениеРасходовНаПредприятие", тбзРезультатыФинансовыйРезультат);
		
	// Прочие расходы.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);

	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_РаспределениеПрямыхРасходов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	// Расходы по рейсам.
	тбзРасходыПоРейсам = Объект.ДанныеРегистраРасходы.Выгрузить(,"Рейс, ЗаданиеНаПеревозку, АналитическийРазрез, СтатьяРасходов, Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	
	ХозяйственнаяОперация = Перечисления.упХозяйственныеОперации.РаспределениеПрямыхРасходов;
	
	//КолонкаЗадание = тбзРасходыПоРейсам.Колонки.Найти("ЗаданиеНаПеревозку");
	//КолонкаЗадание.Имя = "Задание";
	тбзРасходыПоРейсам.Колонки.Добавить("Валюта");
	тбзРасходыПоРейсам.Колонки.Добавить("ХозяйственнаяОперация");
	тбзРасходыПоРейсам.Колонки.Добавить("сткСуммы");
	Для каждого Строка Из тбзРасходыПоРейсам Цикл
		Строка.Валюта  = Объект.Валюта;
		Строка.Сумма          = -Строка.Сумма;
		Строка.СуммаНДС       = -Строка.СуммаНДС;
		Строка.СуммаВалюта    = -Строка.СуммаВалюта;
		Строка.СуммаВалютаНДС = -Строка.СуммаВалютаНДС;
		Строка.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	тбзПрямыеРасходыПоРейсам = Объект.РасходыПоРейсам.Выгрузить(,"Рейс, АналитическийРазрез, СтатьяРасходов, ПравилоРаспределения, Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	тбзПрямыеРасходыПоРейсам.Колонки.Добавить("Задание");
	тбзПрямыеРасходыПоРейсам.Колонки.Добавить("Валюта");
	тбзПрямыеРасходыПоРейсам.Колонки.Добавить("сткСуммы");
	
	Для каждого Строка Из тбзПрямыеРасходыПоРейсам Цикл
		Строка.Валюта  = Объект.Валюта;
		Строка.Задание = Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка();
		Строка.Сумма          = Строка.Сумма;
		Строка.СуммаНДС       = Строка.СуммаНДС;
		Строка.СуммаВалюта    = Строка.СуммаВалюта;
		Строка.СуммаВалютаНДС = Строка.СуммаВалютаНДС;
		//НоваяСтрока.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	// Расходы по ТС.
	тбзРасходыПоТС = Объект.ДанныеРегистраРасходыНаТС.Выгрузить(,"АналитическийРазрез, ТранспортноеСредство, СтатьяРасходов, Сумма, СуммаНДС, СуммаВалюта, СуммаВалютаНДС");
	тбзРасходыПоТС.Колонки.Добавить("Валюта");
	тбзРасходыПоТС.Колонки.Добавить("ХозяйственнаяОперация");
	тбзРасходыПоТС.Колонки.Добавить("сткСуммы");
	Для каждого Строка Из тбзРасходыПоТС Цикл
		Строка.Валюта = Объект.Валюта;
		Строка.Сумма          = -Строка.Сумма;
		Строка.СуммаНДС       = -Строка.СуммаНДС;
		Строка.СуммаВалюта    = -Строка.СуммаВалюта;
		Строка.СуммаВалютаНДС = -Строка.СуммаВалютаНДС;
		Строка.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	Для каждого Строка Из Объект.РасходыПоТС Цикл
		НоваяСтрока = тбзРасходыПоТС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.Валюта  = Объект.Валюта;
		НоваяСтрока.Сумма          = Строка.Сумма;
		НоваяСтрока.СуммаНДС       = Строка.СуммаНДС;
		НоваяСтрока.СуммаВалюта    = Строка.СуммаВалюта;
		НоваяСтрока.СуммаВалютаНДС = Строка.СуммаВалютаНДС;
		НоваяСтрока.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(НоваяСтрока, ВедетсяВалютныйУчет);
	КонецЦикла;

	
	// Расходы на финансовый результат.
	тбзРезультатыФинансовыйРезультат = Объект.РасходыФинансовыйРезультат.ВыгрузитьКолонки("АналитическийРазрез, НаправлениеДеятельности, Организация, Подразделение, Расходы, СтатьяРасходов");
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("Валюта");
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("ХозяйственнаяОперация");
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("Сумма",          ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("СуммаНДС",       ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("СуммаВалюта",    ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("СуммаВалютаНДС", ТипСумма());
	тбзРезультатыФинансовыйРезультат.Колонки.Добавить("сткСуммы");
	
	Для каждого Строка Из Объект.ДанныеРегистраДоходыРасходыПредприятия Цикл
		НоваяСтрока = тбзРезультатыФинансовыйРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.СтатьяРасходов = Строка.СтатьяДоходовРасходов; 
		НоваяСтрока.Валюта         = Объект.Валюта;
		НоваяСтрока.Организация    = Объект.Организация;
		НоваяСтрока.Сумма          = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Объект.Валюта, -Строка.Расходы, Объект.Дата);
		НоваяСтрока.СуммаНДС       = 0;
		НоваяСтрока.СуммаВалюта    = -Строка.Расходы;
		НоваяСтрока.СуммаВалютаНДС = 0;
		НоваяСтрока.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(НоваяСтрока, ВедетсяВалютныйУчет);
	КонецЦикла;
	
	Для каждого Строка Из Объект.РасходыФинансовыйРезультат Цикл
		НоваяСтрока = тбзРезультатыФинансовыйРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Валюта = Объект.Валюта;
		НоваяСтрока.Сумма          = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Объект.Валюта, Строка.Расходы, Объект.Дата);
		НоваяСтрока.СуммаНДС       = 0;
		НоваяСтрока.СуммаВалюта    = Строка.Расходы;
		НоваяСтрока.СуммаВалютаНДС = 0;
		НоваяСтрока.ХозяйственнаяОперация = ХозяйственнаяОперация;
		ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(НоваяСтрока, ВедетсяВалютныйУчет);
	КонецЦикла;

	
	ДополнительныеСвойства.Вставить("РаспределениеРасходовДополнение", тбзРасходыПоРейсам);
	ДополнительныеСвойства.Вставить("ПрямыеРасходы", тбзПрямыеРасходыПоРейсам);
 	ДополнительныеСвойства.Вставить("РаспределениеРасходовНаТС",          тбзРасходыПоТС);
	ДополнительныеСвойства.Вставить("РаспределениеРасходовНаПредприятие", тбзРезультатыФинансовыйРезультат);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);

	
КонецПроцедуры

Процедура ЗаполнитьДоходыРасходы_СписаниеПрочихРасходов(Объект, ДополнительныеСвойства, ВедетсяВалютныйУчет, Отказ)
	
	тбзПрочиеРасходы = Новый ТаблицаЗначений;
	тбзПрочиеРасходы.Колонки.Добавить("ВидДвижения");
	тбзПрочиеРасходы.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата", , ,
									    Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	тбзПрочиеРасходы.Колонки.Добавить("Валюта",       	Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	тбзПрочиеРасходы.Колонки.Добавить("СуммаБезНДС",       ТипСумма());
	тбзПрочиеРасходы.Колонки.Добавить("Сумма",         ТипСумма());
	тбзПрочиеРасходы.Колонки.Добавить("СуммаБезНДСВалюта", ТипСумма());
	тбзПрочиеРасходы.Колонки.Добавить("СуммаВалюта",   ТипСумма());
	
	
	Сумма = 0;
	СуммаНДС = 0;
	СуммаВалюта = 0;
	СуммаВалютаНДС = 0;
	Для каждого Строка Из Объект.Расходы Цикл
		НоваяСтрока = тбзПрочиеРасходы.Добавить();
		НоваяСтрока.ВидДвижения       = ВидДвиженияНакопления.Расход;
		НоваяСтрока.Валюта            = Объект.Валюта;
		НоваяСтрока.Период            = КонецДня(Строка.Дата);
		НоваяСтрока.Сумма             = Строка.Сумма;
		НоваяСтрока.СуммаБезНДС       = Строка.Сумма - Строка.СуммаНДС;
		НоваяСтрока.СуммаВалюта       = Строка.СуммаВалюта;
		НоваяСтрока.СуммаБезНДСВалюта = Строка.СуммаВалюта - Строка.СуммаВалютаНДС;
		Сумма = Сумма + Строка.Сумма;
		СуммаНДС = СуммаНДС + Строка.СуммаНДС;
		СуммаВалюта = СуммаВалюта + Строка.СуммаВалюта;
		СуммаВалютаНДС = СуммаВалютаНДС + Строка.СуммаВалютаНДС;

	КонецЦикла;
	
	Объект.Сумма           = Сумма;
	Объект.СуммаНДС        = СуммаНДС;
	Объект.СуммаВалюта     = СуммаВалюта;
	Объект.СуммаВалютаНДС  = СуммаВалютаНДС;
		
	// Прочие расходы.
	ДополнительныеСвойства.Вставить("ПрочиеРасходы", тбзПрочиеРасходы);
	
	// При распределении учитывать по возможности фактические параметры рейса.
	ДополнительныеСвойства.Вставить("УчитыватьФактическиеПараметры", Истина);

	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ЗаполнитьСтрокуТаблицыДоходов(Строка, ЭтоФактическиеРасходы = Истина, ВедетсяВалютныйУчет)
	
	сткСуммы  = ШаблонСуммДляРаспределения();
	Если ЭтоФактическиеРасходы Тогда
		сткСуммы.Вставить("СуммаФактБезНДС", Строка.Сумма - Строка.СуммаНДС);
		сткСуммы.Вставить("СуммаФактСНДС",   Строка.Сумма);
		Если ВедетсяВалютныйУчет Тогда
			сткСуммы.Вставить("СуммаФактВалютаБезНДС", Строка.СуммаВалюта - Строка.СуммаВалютаНДС);
			сткСуммы.Вставить("СуммаФактВалютаСНДС",   Строка.СуммаВалюта);	
		КонецЕсли;
		
	Иначе	
		сткСуммы.Вставить("СуммаПланБезНДС", Строка.Сумма - Строка.СуммаНДС);
		сткСуммы.Вставить("СуммаПланСНДС",   Строка.Сумма);
		Если ВедетсяВалютныйУчет Тогда
			сткСуммы.Вставить("СуммаПланВалютаБезНДС", Строка.СуммаВалюта - Строка.СуммаВалютаНДС);
			сткСуммы.Вставить("СуммаПланВалютаСНДС",   Строка.СуммаВалюта);	
		КонецЕсли;
	КонецЕсли;
     Строка.сткСуммы = сткСуммы;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуТаблицыРасходов(Строка, ЭтоФактическиеРасходы = Истина, ВедетсяВалютныйУчет)
	
	сткСуммы  = ШаблонСуммДляРаспределения();
	Если ЭтоФактическиеРасходы Тогда
		сткСуммы.Вставить("СуммаФактБезНДС", Строка.СуммаБезНДС);
		сткСуммы.Вставить("СуммаФактСНДС",   Строка.Сумма);
		Если ВедетсяВалютныйУчет Тогда
			сткСуммы.Вставить("СуммаФактВалютаБезНДС", Строка.СуммаБезНДСВалюта);
			сткСуммы.Вставить("СуммаФактВалютаСНДС",   Строка.СуммаВалюта);	
		КонецЕсли;
		
	Иначе	
		сткСуммы.Вставить("СуммаПланБезНДС", Строка.СуммаБезНДС);
		сткСуммы.Вставить("СуммаПланСНДС",   Строка.Сумма);
		Если ВедетсяВалютныйУчет Тогда
			сткСуммы.Вставить("СуммаПланВалютаБезНДС", Строка.СуммаБезНДСВалюта);
			сткСуммы.Вставить("СуммаПланВалютаСНДС",   Строка.СуммаВалюта);	
		КонецЕсли;
	КонецЕсли;
     Строка.сткСуммы = сткСуммы;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуТаблицыРасходовДляРаспределенияПрочихРасходов(Строка, ВедетсяВалютныйУчет)
	
	сткСуммы  = ШаблонСуммДляРаспределения();
	сткСуммы.Вставить("СуммаФактБезНДС", Строка.Сумма - Строка.СуммаНДС);
	сткСуммы.Вставить("СуммаФактСНДС",   Строка.Сумма);
	Если ВедетсяВалютныйУчет Тогда
		сткСуммы.Вставить("СуммаФактВалютаБезНДС", Строка.СуммаВалюта - Строка.СуммаВалютаНДС);
		сткСуммы.Вставить("СуммаФактВалютаСНДС",   Строка.СуммаВалюта);	
	КонецЕсли;

	Строка.сткСуммы = сткСуммы;
КонецПроцедуры

Процедура ЗаполнитьАналитическиеРазрезыПоПрочимРасходам(тбзПрочиеРасходы) Экспорт
	
	Для каждого Строка Из тбзПрочиеРасходы Цикл
		
		// Если нужен аналитический разрез и в справочнике такого еще нет.
		Если Истина
			И Строка.НуженАналитическийРазрез
			И НЕ ЗначениеЗаполнено(Строка.АналитическийРазрез) 
		Тогда
			Если ЗначениеЗаполнено(Строка.ОбъектАналитическогоРазреза) Тогда
				
				// Формируется новый аналитический разрез.
				АналитическийРазрезСправочникОбъект = Справочники.упАналитическиеРазрезы.СоздатьЭлемент();
				
				Если ТипЗнч(Строка.ОбъектАналитическогоРазреза) = Тип("СправочникСсылка.упВидыПеревозок") Тогда
					АналитическийРазрезСправочникОбъект.ВидПеревозки = Строка.ОбъектАналитическогоРазреза;	
					
				ИначеЕсли ТипЗнч(Строка.ОбъектАналитическогоРазреза) = Тип("СправочникСсылка.упСотрудники") Тогда
					АналитическийРазрезСправочникОбъект.Сотрудник = Строка.ОбъектАналитическогоРазреза;	
					
				ИначеЕсли ТипЗнч(Строка.ОбъектАналитическогоРазреза) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
					АналитическийРазрезСправочникОбъект.ТранспортноеСредство = Строка.ОбъектАналитическогоРазреза;	
					
				КонецЕсли;
				
				АналитическийРазрезСправочникОбъект.Наименование = Справочники.упАналитическиеРазрезы.СформироватьНаименование(Строка.ОбъектАналитическогоРазреза);
				АналитическийРазрезСправочникОбъект.ОперацияСистемыПлатон = Ложь;
				
				Попытка
					АналитическийРазрезСправочникОбъект.Записать();	
					АналитическийРазрез = АналитическийРазрезСправочникОбъект.Ссылка;
				Исключение
					Отказ = Истина;
					ОписаниеОшибки = НСтр("ru = 'Не удалось записать аналитический разрез по %1.'");
					ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Строка.ОбъектАналитическогоРазреза);
					ВызватьИсключение ОписаниеОшибки;
				КонецПопытки;
				
				// Заполняется аналитический разрез в таблице прочих расходов.
				Строка.АналитическийРазрез = АналитическийРазрез;
				
				// Заполняются аналитические разрезы у всех строк текущего объекта аналитического разреза,
				// что бы исключить создание повторяющихся аналитических разрезов.
				сткОтбора = Новый Структура();
				сткОтбора.Вставить("ОбъектАналитическогоРазреза", Строка.ОбъектАналитическогоРазреза);
				сткОтбора.Вставить("НуженАналитическийРазрез", Истина);
				сткОтбора.Вставить("АналитическийРазрез", Справочники.упАналитическиеРазрезы.ПустаяСсылка());
				
				мсвСтрокиПрочиеРасходы = тбзПрочиеРасходы.НайтиСтроки(сткОтбора);
				
				Для каждого СтрокаПрочиеРасходы Из мсвСтрокиПрочиеРасходы Цикл
					СтрокаПрочиеРасходы.АналитическийРазрез = АналитическийРазрез;
				КонецЦикла;
			Иначе
				ТекстСообщения = Нстр("ru = 'Для статьи расходов ""%1"" не удалось определить аналитику'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.СтатьяРасходов);
				ЗаписьЖурналаРегистрации(СобытиеОшибкаРаспределенияРасходов(),УровеньЖурналаРегистрации.Предупреждение ,,, ТекстСообщения,РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КолонкиГруппировки(ДополнительнаяГруппировка = "")
	Возврат "СтатьяРасходов, ПравилоРаспределения, Валюта" + ?(ЗначениеЗаполнено(ДополнительнаяГруппировка),", " + ДополнительнаяГруппировка,"");		
КонецФункции

Функция КолонкиСуммирования()
	Возврат "Сумма, СуммаБезНДС, СуммаНДС, СуммаВалюта, СуммаБезНДСВалюта, СуммаВалютаНДС";
КонецФункции

Функция КолонкиПоказателейРаспределения()
	Возврат "Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, Расстояние, Время, ПлановыеРасходыПоРейсу, ОценочнаяСтоимостьГруза, ФактическиеРасходыПоРейсу";
КонецФункции

Функция ТипСумма()
	Возврат Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,2));	
КонецФункции

Процедура ПересчитатьСуммыВВалютуУпрУчета(тбзТабличнаяЧасть, ЭтоДоход = Истина, сткИтоги, Дата, ВедетсяВалютныйУчет) Экспорт
	
	сткИтоги = Новый Структура("Сумма, СуммаНДС, СуммаСкидка",0,0,0);
	
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаВалюта");
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаВалютаНДС");
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаБезНДС");
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаБезНДСВалюта");
				
	Сумма       = 0;
	СуммаНДС    = 0;
	СуммаСкидка = 0;
	
	Для каждого Строка Из тбзТабличнаяЧасть Цикл
		
		Если ВедетсяВалютныйУчет Тогда
				
			// В документе суммы в валюте регл. учета, переносятся в соответствующие реквизиты.
			Строка.СуммаВалюта    = Строка.Сумма;
			Строка.СуммаВалютаНДС = Строка.СуммаНДС;
			
			// Пересчитать суммы в валюту управленческого учета.
			Строка.Сумма          = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СуммаВалюта, Дата);
			Строка.СуммаНДС       = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СуммаВалютаНДС, Дата);
			
			Если НЕ ЭтоДоход Тогда
				Строка.СуммаБезНДС       = Строка.Сумма - Строка.СуммаНДС;	
				Строка.СуммаБезНДСВалюта = Строка.СуммаВалюта - Строка.СуммаВалютаНДС;	
			Иначе
				Строка.СуммаБезНДС       = 0;	
				Строка.СуммаБезНДСВалюта = 0;		
			КонецЕсли;
		     			
		Иначе
			Строка.СуммаБезНДС       = Строка.Сумма - Строка.СуммаНДС;
			
			Строка.СуммаБезНДСВалюта = 0;		
			Строка.СуммаВалютаНДС    = 0;
			Строка.СуммаВалюта       = 0;

		КонецЕсли;
		
		// Пересчитать итоговые суммы управленческого учета, что бы не было отклонений от движений регистра.
		Сумма    = Сумма + Строка.Сумма;
		СуммаНДС = СуммаНДС + Строка.СуммаНДС;
		Если ЭтоДоход Тогда
			Если ВедетсяВалютныйУчет Тогда
				СуммаСкидка = СуммаСкидка + упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СкидкаСумма, Дата);	
			Иначе
				СуммаСкидка = СуммаСкидка + Строка.СкидкаСумма;	
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	сткИтоги.Вставить("Сумма",       Сумма);
	сткИтоги.Вставить("СуммаНДС",    СуммаНДС);
	сткИтоги.Вставить("СуммаСкидка", СуммаСкидка);
		
КонецПроцедуры

Процедура ПересчитатьСуммыВВалютуРеглУчета(тбзТабличнаяЧасть, Дата, ВедетсяВалютныйУчет)
	
	Если ВедетсяВалютныйУчет Тогда
		ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
	Иначе
		ВалютаРеглУчета = Справочники.Валюты.ПустаяСсылка();
	КонецЕсли;
	
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаВалюта");
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаВалютаНДС");
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаБезНДС");
	ДобавитьКолонкуСумма(тбзТабличнаяЧасть, "СуммаБезНДСВалюта");
	
	Для каждого Строка Из тбзТабличнаяЧасть Цикл
		
		Если ВедетсяВалютныйУчет Тогда
		
			// Пересчитать суммы в валюту управленческого учета.
			Строка.СуммаВалюта       = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(ВалютаРеглУчета, Строка.Сумма, Дата);
			Строка.СуммаВалютаНДС    = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(ВалютаРеглУчета, Строка.СуммаНДС, Дата);
			
			Строка.СуммаБезНДС       = Строка.Сумма - Строка.СуммаНДС;	
			Строка.СуммаБезНДСВалюта = Строка.СуммаВалюта - Строка.СуммаВалютаНДС;	
	
		Иначе
			Строка.СуммаБезНДС       = Строка.Сумма - Строка.СуммаНДС;
			Строка.СуммаБезНДСВалюта = 0;		
			Строка.СуммаВалютаНДС    = 0;
			Строка.СуммаВалюта       = 0;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура ДобавитьКолонкуСумма(тбзТаблица, ИмяКолонки)
	
	Если тбзТаблица.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
		тбзТаблица.Колонки.Добавить(ИмяКолонки, ТипСумма());
	КонецЕсли;
	
КонецПроцедуры

Функция СобытиеОшибкаРаспределенияРасходов()
	Возврат НСтр("ru = 'Распределение расходов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
КонецФункции

// Возвращает строку с типовой формулой.
//
// Параметры:
//  Показатель - Значение - значение перечисления для которого необходимо получить имя перечисления.
// 
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьФормулуРаспределения(Показатель) Экспорт
		
	Формула = "%1 / СуммаПоВсемДокументам(""%1"",ТаблицаДокументов)";			
	Формула = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Формула, ОбщегоНазначения.ИмяЗначенияПеречисления(Показатель));
	
	Возврат Формула;
	
КонецФункции	

// Возвращает строку с типовой формулой.
//
// Параметры:
//  Показатель - Значение - значение перечисления для которого необходимо получить имя перечисления.
// 
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ПолучитьФормулуРаспределенияБезСуммы(Показатель)
	
	сткРезультат = Новый Структура();
	
	Формула = "%1 / СуммаПоВсемДокументам";			
	Формула = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Формула, ОбщегоНазначения.ИмяЗначенияПеречисления(Показатель));
	
	ФормулаСумма = "%1";
	ФормулаСумма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ФормулаСумма, ОбщегоНазначения.ИмяЗначенияПеречисления(Показатель));
	
	сткРезультат.Вставить("Формула", Формула);
	сткРезультат.Вставить("ФормулаСумма", ФормулаСумма);
	
	Возврат сткРезультат;
	
КонецФункции

Функция ТекстЗапросаСтатейРасходовПоРейсу()
	
	Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тбТипыТС.СтатьяРасходов,
	|	1 КАК Количество,
	|	тбТипыТС.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения КАК СпособРаспределения,
	|	ЛОЖЬ КАК ЗаполнятьПоЗаданиям,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка) КАК Задание
	|ПОМЕСТИТЬ втСтатьиРасходов
	|ИЗ
	|	Справочник.упТипыТС.СтатьиРасходов КАК тбТипыТС
	|ГДЕ
	|	тбТипыТС.Ссылка = &ТипТС
	|	И тбТипыТС.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов,
	|	1,
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения,
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.ЗаполнятьПоЗаданиям,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК упСоглашенияСПеревозчикамиСтатьиРасходов
	|ГДЕ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка = &Соглашение
	|	И упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов,
	|	упРейсРаботы.КоличествоДопОпераций,
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения,
	|	ЛОЖЬ,
	|	упРейсРаботы.Задание
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК упРейсРаботы
	|ГДЕ
	|	упРейсРаботы.Рейс = &ДокументОснование
	|	И упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтатьиРасходов.СтатьяРасходов,
	|	СУММА(тбСтатьиРасходов.Количество) КАК Количество,
	|	тбСтатьиРасходов.СпособРаспределения,
	|	упСтатьиДоходовРасходов.СтавкаНДС,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	тбСтатьиРасходов.ЗаполнятьПоЗаданиям,
	|	тбСтатьиРасходов.Задание
	|ИЗ
	|	втСтатьиРасходов КАК тбСтатьиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|		ПО тбСтатьиРасходов.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСтатьиРасходов.СтатьяРасходов,
	|	тбСтатьиРасходов.СпособРаспределения,
	|	упСтатьиДоходовРасходов.СтавкаНДС,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения,
	|	тбСтатьиРасходов.ЗаполнятьПоЗаданиям,
	|	тбСтатьиРасходов.Задание";
					  
	Возврат Текст;
		
КонецФункции

Функция ТекстЗапросаСтатейРасходовПоЗаявкеНаТС()
	
	Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаТС.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|ГДЕ
	|	упЗаявкаНаТС.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тбТипыТС.СтатьяРасходов,
	|	1 КАК Количество,
	|	тбТипыТС.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения КАК СпособРаспределения,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка) КАК Задание
	|ПОМЕСТИТЬ втСтатьиРасходов
	|ИЗ
	|	Справочник.упТипыТС.СтатьиРасходов КАК тбТипыТС
	|ГДЕ
	|	тбТипыТС.Ссылка = &ТипТС
	|	И тбТипыТС.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов,
	|	1,
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК упСоглашенияСПеревозчикамиСтатьиРасходов
	|ГДЕ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка = &Соглашение
	|	И упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов,
	|	упРейсРаботы.КоличествоДопОпераций,
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения,
	|	упРейсРаботы.Задание
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					втРейсы.Рейс
	|				ИЗ
	|					втРейсы)) КАК упРейсРаботы
	|ГДЕ
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтатьиРасходов.СтатьяРасходов,
	|	СУММА(тбСтатьиРасходов.Количество) КАК Количество,
	|	тбСтатьиРасходов.СпособРаспределения,
	|	упСтатьиДоходовРасходов.СтавкаНДС,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	тбСтатьиРасходов.Задание
	|ИЗ
	|	втСтатьиРасходов КАК тбСтатьиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|		ПО тбСтатьиРасходов.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСтатьиРасходов.СтатьяРасходов,
	|	тбСтатьиРасходов.СпособРаспределения,
	|	упСтатьиДоходовРасходов.СтавкаНДС,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения,
	|	тбСтатьиРасходов.Задание";
	Возврат Текст;

КонецФункции

Функция ТекстЗапросаСтатейРасходовПоПредложениюПеревозчика()
	
	Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПредложениеПеревозчика.ЗаявкаНаТС.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тбТипыТС.СтатьяРасходов,
	|	1 КАК Количество,
	|	тбТипыТС.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения КАК СпособРаспределения,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка) КАК Задание
	|ПОМЕСТИТЬ втСтатьиРасходов
	|ИЗ
	|	Справочник.упТипыТС.СтатьиРасходов КАК тбТипыТС
	|ГДЕ
	|	тбТипыТС.Ссылка = &ТипТС
	|	И тбТипыТС.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов,
	|	1,
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения,
	|	ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками.СтатьиРасходов КАК упСоглашенияСПеревозчикамиСтатьиРасходов
	|ГДЕ
	|	упСоглашенияСПеревозчикамиСтатьиРасходов.Ссылка = &Соглашение
	|	И упСоглашенияСПеревозчикамиСтатьиРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов,
	|	упРейсРаботы.КоличествоДопОпераций,
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов.ПравилаРаспределения.СпособРаспределения,
	|	упРейсРаботы.Задание
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК упРейсРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	|		ПО упРейсРаботы.Рейс = втРейсы.Рейс
	|ГДЕ
	|	упРейсРаботы.ДополнительнаяОперация.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.упСтатьиДоходовРасходов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтатьиРасходов.СтатьяРасходов,
	|	СУММА(тбСтатьиРасходов.Количество) КАК Количество,
	|	тбСтатьиРасходов.СпособРаспределения,
	|	упСтатьиДоходовРасходов.СтавкаНДС,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения КАК ПравилоРаспределения,
	|	тбСтатьиРасходов.Задание
	|ИЗ
	|	втСтатьиРасходов КАК тбСтатьиРасходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	|		ПО тбСтатьиРасходов.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСтатьиРасходов.СтатьяРасходов,
	|	тбСтатьиРасходов.СпособРаспределения,
	|	упСтатьиДоходовРасходов.СтавкаНДС,
	|	упСтатьиДоходовРасходов.ПравилаРаспределения,
	|	тбСтатьиРасходов.Задание";
					  
	Возврат Текст;
		
КонецФункции

Функция ТекстЗапросаПрочиеРасходыАналитика()
	
	Текст = "
	|// Прочие расходы (косвенные расходы и РБП) без заданной аналитики,
	|// для которых нужны аналитические разрезы
	|ВЫБРАТЬ
	|	втПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	втПрочиеРасходы.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втПрочиеРасходы.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втПрочиеРасходы.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втПрочиеРасходы.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втПрочиеРасходы.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ПОМЕСТИТЬ втПодборАналитики
	|ИЗ
	|	втПрочиеРасходы КАК втПрочиеРасходы
	|ГДЕ ИСТИНА
	|	И втПрочиеРасходы.НуженАналитическийРазрез
	|	И втПрочиеРасходы.АналитическийРазрез = ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)
	|;
	|//{Запрос:  ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбАналитикаПоСтатьямРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	тбАналитикаПоСтатьямРасходов.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	МАКСИМУМ(тбАналитикаПоСтатьямРасходов.АналитическийРазрез) КАК АналитическийРазрез
	|ПОМЕСТИТЬ втАналитикаПоСтатьямРасходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		втПодборАналитики.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|		втПодборАналитики.СтатьяРасходов КАК СтатьяРасходов,
	|		упАналитическиеРазрезы.Ссылка КАК АналитическийРазрез
	|	ИЗ
	|		втПодборАналитики КАК втПодборАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|		ПО втПодборАналитики.ОбъектАналитическогоРазреза = упАналитическиеРазрезы.Агрегат
	|	ГДЕ ИСТИНА
	|		И втПодборАналитики.НужнаАналитикаАгрегат
	|		И втПодборАналитики.ОбъектАналитическогоРазреза <> ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)
	|		И НЕ (упАналитическиеРазрезы.ПометкаУдаления)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втПодборАналитики.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|		втПодборАналитики.СтатьяРасходов КАК СтатьяРасходов,
	|		упАналитическиеРазрезы.Ссылка КАК АналитическийРазрез
	|	ИЗ
	|		втПодборАналитики КАК втПодборАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|		ПО втПодборАналитики.ОбъектАналитическогоРазреза = упАналитическиеРазрезы.ТранспортноеСредство
	|	ГДЕ ИСТИНА
	|		И втПодборАналитики.НужнаАналитикаТранспортноеСредство
	|		И втПодборАналитики.ОбъектАналитическогоРазреза <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|		И НЕ (упАналитическиеРазрезы.ПометкаУдаления)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втПодборАналитики.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|		втПодборАналитики.СтатьяРасходов КАК СтатьяРасходов,
	|		упАналитическиеРазрезы.Ссылка КАК АналитическийРазрез
	|	ИЗ
	|		втПодборАналитики КАК втПодборАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|		ПО втПодборАналитики.ОбъектАналитическогоРазреза = упАналитическиеРазрезы.ВидПеревозки
	|	ГДЕ ИСТИНА
	|		И втПодборАналитики.НужнаАналитикаВидПеревозки
	|		И втПодборАналитики.ОбъектАналитическогоРазреза <> ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)
	|		И НЕ (упАналитическиеРазрезы.ПометкаУдаления)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втПодборАналитики.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|		втПодборАналитики.СтатьяРасходов КАК СтатьяРасходов,
	|		упАналитическиеРазрезы.Ссылка КАК АналитическийРазрез
	|	ИЗ
	|		втПодборАналитики КАК втПодборАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	|		ПО втПодборАналитики.ОбъектАналитическогоРазреза = упАналитическиеРазрезы.Сотрудник
	|	ГДЕ ИСТИНА
	|		И втПодборАналитики.НужнаАналитикаСотрудник
	|		И втПодборАналитики.ОбъектАналитическогоРазреза <> ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|		И НЕ (упАналитическиеРазрезы.ПометкаУдаления)) КАК тбАналитикаПоСтатьямРасходов
	|СГРУППИРОВАТЬ ПО
	|	тбАналитикаПоСтатьямРасходов.СтатьяРасходов,
	|	тбАналитикаПоСтатьямРасходов.ОбъектАналитическогоРазреза
	|;
	|//{Запрос:  ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеРасходы.ОбъектАналитическогоРазреза КАК ОбъектАналитическогоРазреза,
	|	втПрочиеРасходы.Валюта КАК Валюта,
	|	втПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	втПрочиеРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ЕстьNull(втПрочиеРасходы.АналитическийРазрез, ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)) КАК АналитическийРазрез,
	|	втПрочиеРасходы.СуммаНДС КАК СуммаНДС,
	|	втПрочиеРасходы.Сумма КАК Сумма,
	|	втПрочиеРасходы.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
	|	втПрочиеРасходы.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втПрочиеРасходы.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втПрочиеРасходы.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втПрочиеРасходы.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ИЗ
	|	втПрочиеРасходы КАК втПрочиеРасходы
	|ГДЕ ЛОЖЬ
	|	ИЛИ НЕ (втПрочиеРасходы.НуженАналитическийРазрез)
	|	ИЛИ втПрочиеРасходы.АналитическийРазрез <> ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втПрочиеРасходы.НужнаАналитикаАгрегат
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(втПрочиеРасходы.ОбъектАналитическогоРазреза) = ТИП(Справочник.упШиныУзлыИАгрегаты)
	|					ТОГДА втПрочиеРасходы.ОбъектАналитическогоРазреза
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)
	|			КОНЕЦ
	|		КОГДА втПрочиеРасходы.НужнаАналитикаВидПеревозки
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(втПрочиеРасходы.ОбъектАналитическогоРазреза) = ТИП(Справочник.упВидыПеревозок)
	|					ТОГДА втПрочиеРасходы.ОбъектАналитическогоРазреза
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)
	|			КОНЕЦ
	|		КОГДА втПрочиеРасходы.НужнаАналитикаСотрудник
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(втПрочиеРасходы.ОбъектАналитическогоРазреза) = ТИП(Справочник.упСотрудники)
	|					ТОГДА втПрочиеРасходы.ОбъектАналитическогоРазреза
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|			КОНЕЦ
	|		КОГДА втПрочиеРасходы.НужнаАналитикаТранспортноеСредство
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(втПрочиеРасходы.ОбъектАналитическогоРазреза) = ТИП(Справочник.упТранспортныеСредства)
	|					ТОГДА втПрочиеРасходы.ОбъектАналитическогоРазреза
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК ОбъектАналитическогоРазреза,
	|	втПрочиеРасходы.Валюта КАК Валюта,
	|	втПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	втПрочиеРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ЕстьNull(втАналитикаПоСтатьямРасходов.АналитическийРазрез, ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)) КАК АналитическийРазрез,
	|	втПрочиеРасходы.СуммаНДС КАК СуммаНДС,
	|	втПрочиеРасходы.Сумма КАК Сумма,
	|	втПрочиеРасходы.НуженАналитическийРазрез КАК НуженАналитическийРазрез,
	|	втПрочиеРасходы.НужнаАналитикаАгрегат КАК НужнаАналитикаАгрегат,
	|	втПрочиеРасходы.НужнаАналитикаВидПеревозки КАК НужнаАналитикаВидПеревозки,
	|	втПрочиеРасходы.НужнаАналитикаСотрудник КАК НужнаАналитикаСотрудник,
	|	втПрочиеРасходы.НужнаАналитикаТранспортноеСредство КАК НужнаАналитикаТранспортноеСредство
	|ИЗ
	|	втПрочиеРасходы КАК втПрочиеРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаПоСтатьямРасходов КАК втАналитикаПоСтатьямРасходов
	|	ПО ИСТИНА
	|		И втПрочиеРасходы.ОбъектАналитическогоРазреза = втАналитикаПоСтатьямРасходов.ОбъектАналитическогоРазреза
	|		И втПрочиеРасходы.СтатьяРасходов = втАналитикаПоСтатьямРасходов.СтатьяРасходов
	|ГДЕ ИСТИНА
	|	И втПрочиеРасходы.НуженАналитическийРазрез
	|	И втПрочиеРасходы.АналитическийРазрез = ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)
	|";
					  
	Возврат Текст;
		
КонецФункции

Функция ШаблонСуммДляРаспределения()
	
	сткСуммыШаблон	= Новый Структура();
	сткСуммыШаблон.Вставить("СуммаПланБезНДС",       0);
	сткСуммыШаблон.Вставить("СуммаПланСНДС",         0);
	сткСуммыШаблон.Вставить("СуммаПланВалютаБезНДС", 0);
	сткСуммыШаблон.Вставить("СуммаПланВалютаСНДС",   0);
	сткСуммыШаблон.Вставить("СуммаФактБезНДС",       0);
	сткСуммыШаблон.Вставить("СуммаФактСНДС",         0);
	сткСуммыШаблон.Вставить("СуммаФактВалютаБезНДС", 0);
	сткСуммыШаблон.Вставить("СуммаФактВалютаСНДС",   0);
	
	Возврат сткСуммыШаблон;
	
КонецФункции

Функция ШаблонСуммДляРаспределенияПрочихРасходовПоРейсам()
	
	сткСуммыШаблон	= Новый Структура();
	сткСуммыШаблон.Вставить("Сумма",          0);
	сткСуммыШаблон.Вставить("СуммаВалюта",    0);
	сткСуммыШаблон.Вставить("СуммаВалютаНДС", 0);
	сткСуммыШаблон.Вставить("СуммаНДС",       0);
		
	Возврат сткСуммыШаблон;

	
КонецФункции

Функция ТаблицаРасходов()
	
	тбзРасходы = Новый ТаблицаЗначений;
	тбзРасходы.Колонки.Добавить("Задание",              Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
	тбзРасходы.Колонки.Добавить("СтатьяРасходов",       Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
	тбзРасходы.Колонки.Добавить("АналитическийРазрез",  Новый ОписаниеТипов("СправочникСсылка.упАналитическиеРазрезы"));
	тбзРасходы.Колонки.Добавить("ПравилоРаспределения", Новый ОписаниеТипов("СправочникСсылка.упПравилаРаспределенияРасходов"));
	тбзРасходы.Колонки.Добавить("Валюта",               Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	тбзРасходы.Колонки.Добавить("Сумма",                ТипСумма());
	тбзРасходы.Колонки.Добавить("СуммаНДС",             ТипСумма());
	тбзРасходы.Колонки.Добавить("СуммаБезНДС",          ТипСумма());
	тбзРасходы.Колонки.Добавить("СуммаВалюта",          ТипСумма());
	тбзРасходы.Колонки.Добавить("СуммаВалютаНДС",       ТипСумма());
	тбзРасходы.Колонки.Добавить("СуммаБезНДСВалюта",    ТипСумма());
	
	Возврат тбзРасходы;
	
КонецФункции

// Функция - Таблица доходы расходы предприятия
// 
// Возвращаемое значение:
//   - 
//
Функция ТаблицаДоходыРасходыПредприятия() Экспорт
	
	тбзРезультат = Новый ТаблицаЗначений;
	тбзРезультат.Колонки.Добавить("Организация",             Новый ОписаниеТипов("СправочникСсылка.Организации"));
	тбзРезультат.Колонки.Добавить("Подразделение",           Новый ОписаниеТипов("СправочникСсылка.упСтруктураПредприятия"));
	тбзРезультат.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.упНаправленияДеятельности"));
	тбзРезультат.Колонки.Добавить("СтатьяДоходовРасходов",   Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
	тбзРезультат.Колонки.Добавить("ХозяйственнаяОперация",   Новый ОписаниеТипов("ПеречислениеСсылка.упХозяйственныеОперации"));
	тбзРезультат.Колонки.Добавить("Доходы",  ТипСумма());
	тбзРезультат.Колонки.Добавить("Расходы", ТипСумма());

	Возврат тбзРезультат;
	
КонецФункции

Функция ТекстЗапросаБазаДляРаспределенияРасходов()
	
	Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРезультат.ТранспортноеСредство КАК ТранспортноеСредство,
	|	&Организация КАК Организация,
	|	втРезультат.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРезультат.Подразделение КАК Подразделение,
	|	втРезультат.СтатьяРасходов КАК СтатьяРасходов,
	|	&Валюта КАК Валюта,
	|	втРезультат.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРезультатОтбора
	|ИЗ
	|	&Результат КАК втРезультат
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРезультатОтбораТ.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсыОтбор
	|ИЗ
	|	втРезультатОтбора КАК втРезультатОтбораТ
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Все работы по рейсу
	|ВЫБРАТЬ
	|	тбРаботы.Рейс КАК Рейс,
	|	тбРаботы.Идентификатор КАК Идентификатор,
	|	тбРаботы.Отменена КАК Отменена,
	|	тбРаботы.Задание КАК Задание,
	|	тбРаботы.Операция КАК Операция,
	|	тбРаботы.ГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсыОтбор.Рейс КАК Рейс
	|			ИЗ
	|				втРейсыОтбор КАК втРейсыОтбор)) КАК тбРаботы
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Документы выполнявшие изменения в работах рейса
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРаботыПоРейсу.Рейс КАК Рейс,
	|	упРаботыПоРейсу.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втРегистраторы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыОтбор КАК втРейсыОтбор
	|	ПО упРаботыПоРейсу.Рейс = втРейсыОтбор.Рейс
	|ГДЕ упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|// Подсчет количества работ и отмененных работ по рейсам/заданиям
	|ВЫБРАТЬ
	|	втРаботы.Рейс КАК Рейс,
	|	втРаботы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА втРаботы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ КАК ГрузовоеМесто,
	|	СУММА(ВЫБОР
	|		КОГДА втРаботы.Отменена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтменена,
	|	СУММА(1) КАК КоличествоОпераций
	|ПОМЕСТИТЬ втОтмененныеЗадания
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ ЛОЖЬ
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Рейс,
	|	втРаботы.Задание,
	|	ВЫБОР
	|		КОГДА втРаботы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|// Не отмененные грузовые места по рейсам
	|ВЫБРАТЬ
	|	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов,
	|	упРейсЗадания.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаданиям
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыОтбор КАК втРейсыОтбор
	|	ПО упРейсЗадания.Ссылка = втРейсыОтбор.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упРейсЗадания.Ссылка = втОтмененныеЗадания.Рейс
	|		И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК тбПеревозчикПоРейсу
	|	ПО тбПеревозчикПоРейсу.Рейс = упРейсЗадания.Ссылка
	|ГДЕ ЛОЖЬ
	|	ИЛИ ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) <> ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	|	ИЛИ ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) = 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРегистраторы.Рейс КАК Рейс,
	|	упКорректировкаРейсаЗадания.Задание КАК Задание,
	|	упКорректировкаРейсаЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упКорректировкаРейсаЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упКорректировкаРейсаЗадания.КоличествоГрузов КАК КоличествоГрузов,
	|	втРегистраторы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
	|ИЗ
	|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	|	ПО ИСТИНА
	|		И упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
	|		И упКорректировкаРейсаЗадания.Ссылка.Рейс = втРегистраторы.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упКорректировкаРейсаЗадания.Ссылка.Рейс = втОтмененныеЗадания.Рейс
	|		И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК тбПеревозчикПоРейсу
	|	ПО тбПеревозчикПоРейсу.Рейс = втРегистраторы.Рейс
	|ГДЕ ЛОЖЬ
	|	ИЛИ НЕ (ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0))
	|	ИЛИ ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) = 0
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|// Характеристики грузовых мест по заданиям
	|ВЫБРАТЬ
	|	тбГрузовыеМеста.Рейс КАК Рейс,
	|	тбГрузовыеМеста.ТранспортноеСредство КАК ТранспортноеСредство,
	|	тбГрузовыеМеста.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	тбГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	СУММА(тбГрузовыеМеста.Масса) КАК Масса,
	|	СУММА(тбГрузовыеМеста.Объем) КАК Объем,
	|	СУММА(тбГрузовыеМеста.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(тбГрузовыеМеста.КоличествоМест) КАК КоличествоМест,
	|	СУММА(тбГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(тбГрузовыеМеста.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	(ВЫБРАТЬ
	|		упРейсЗадания.Рейс КАК Рейс,
	|		упРейсЗадания.ТранспортноеСредство КАК ТранспортноеСредство,
	|		упРейсЗадания.Задание КАК ЗаданиеНаПеревозку,
	|		ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) КАК Масса,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) КАК Объем,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров,
	|		ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) КАК КоличествоМест,
	|		1 КАК КоличествоГрузов,
	|		упРейсЗадания.Задание.ОценочнаяСтоимость КАК ОценочнаяСтоимостьГруза
	|	ИЗ
	|		втГрузовыеМестаПоЗаданиям КАК упРейсЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания
	|				ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|			,
	|			ЗаданиеНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.Задание КАК Задание
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ГДЕ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упРейсЗадания.Рейс КАК Рейс,
	|		упРейсЗадания.ТранспортноеСредство КАК ТранспортноеСредство,
	|		упРейсЗадания.Задание КАК ЗаданиеНаПеревозку,
	|		упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) КАК Масса,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) КАК Объем,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) КАК КоличествоМест,
	|		упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов,
	|		упРейсЗадания.КоличествоГрузов * ЕСТЬNULL(упГрузовыеМеста.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимостьГруза
	|	ИЗ
	|		втГрузовыеМестаПоЗаданиям КАК упРейсЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ПО упРейсЗадания.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания
	|				ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|			,
	|			ЗаданиеНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.Задание КАК Задание
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ГДЕ ЛОЖЬ
	|		ИЛИ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса)
	|		ИЛИ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упРейсЗадания.Рейс КАК Рейс,
	|		упРейсЗадания.ТранспортноеСредство КАК ТранспортноеСредство,
	|		упРейсЗадания.Задание КАК ЗаданиеНаПеревозку,
	|		ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) КАК Масса,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) КАК Объем,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров,
	|		ГрузовыеМеста.КоличествоГрузов * ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) КАК КоличествоМест,
	|		1 КАК КоличествоГрузов,
	|		упРейсЗадания.Задание.ОценочнаяСтоимость КАК ОценочнаяСтоимостьГруза
	|	ИЗ
	|		втГрузовыеМестаПоЗаданиям КАК упРейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|		ПО упРейсЗадания.Задание = ГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|			,
	|			ЗаявкаНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания
	|				ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|			,
	|			ЗаданиеНаПеревозку В (
	|				ВЫБРАТЬ
	|					втЗадания.Задание КАК Задание
	|				ИЗ
	|					втГрузовыеМестаПоЗаданиям КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО ИСТИНА
	|			И упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ГДЕ упРейсЗадания.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)) КАК тбГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	тбГрузовыеМеста.Рейс,
	|	тбГрузовыеМеста.ТранспортноеСредство,
	|	тбГрузовыеМеста.ЗаданиеНаПеревозку,
	|	тбГрузовыеМеста.ГрузовоеМесто
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|// Оценочная стоимость грузов рейса
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Рейс КАК Рейс,
	|	СУММА(втГрузовыеМеста.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза
	|ПОМЕСТИТЬ втРейсОценочнаяСтоимостьГруза
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Рейс
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|// Фактические расходы
	|ВЫБРАТЬ
	|	упРасходыОбороты.Рейс КАК Рейс,
	|	упРасходыОбороты.СуммаФактСНДСОборот КАК СуммаФактСНДСОборот
	|ПОМЕСТИТЬ втФактическиеРасходы
	|ИЗ
	|	РегистрНакопления.упРасходы.Обороты(
	|		,
	|		,
	|		,
	|		(Рейс, СтатьяРасходов) В (
	|				ВЫБРАТЬ
	|				втРейсыОтбор.Рейс КАК Рейс,
	|				втРейсыОтбор.СтатьяРасходов КАК СтатьяРасходов
	|			ИЗ
	|				втРезультатОтбора КАК втРейсыОтбор)) КАК упРасходыОбороты
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|// Значение показателей распределения по рейсам
	|ВЫБРАТЬ
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	тбПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.НачалоВыполнения, упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения), ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоРейса,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Масса, упПлановыеПараметрыРейса.Масса), 0) КАК Масса,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Объем, упПлановыеПараметрыРейса.Объем), 0) КАК Объем,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.КоличествоМест, упПлановыеПараметрыРейса.КоличествоМест), 0) КАК КоличествоМест,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.КоличествоЗагрузочныхМетров, упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров), 0) КАК КоличествоЗагрузочныхМетров,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упФактическиеПараметрыРейса.Пробег, 0) <> 0
	|			ТОГДА упФактическиеПараметрыРейса.Пробег
	|		ИНАЧЕ ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Расстояние, упПлановыеПараметрыРейса.Расстояние), 0)
	|	КОНЕЦ КАК Расстояние,
	|	ЕСТЬNULL(ЕСТЬNULL(упФактическиеПараметрыРейса.Время, упПлановыеПараметрыРейса.Время), 0) КАК Время,
	|	ЕСТЬNULL(упВсегоРасходыПоРейсу.СуммаПланСНДС, 0) КАК ПлановыеРасходыПоРейсу,
	|	ЕСТЬNULL(втРейсОценочнаяСтоимостьГруза.ОценочнаяСтоимостьГруза, 0) КАК ОценочнаяСтоимостьГруза,
	|	ЕСТЬNULL(втРасходы.СуммаФактСНДСОборот, 0) КАК ФактическиеРасходыПоРейсу,
	|	упРейс.ВидПеревозки.ОбращатьсяКМаршрутизаторуПриРасчетеРасходов КАК ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов,
	|	упРейс.Подразделение КАК Подразделение,
	|	упРейс.Организация КАК Организация,
	|	упРейс.ВидПеревозки КАК ВидПеревозки,
	|	тбПеревозчикПоРейсу.Водитель1 КАК Сотрудник
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсыОтбор КАК втРейсыОтбор
	|	ПО упРейс.Ссылка = втРейсыОтбор.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|	ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	|	ПО упРейс.Ссылка = упФактическиеПараметрыРейса.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВсегоРасходыПоРейсу КАК упВсегоРасходыПоРейсу
	|	ПО упРейс.Ссылка = упВсегоРасходыПоРейсу.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРейсОценочнаяСтоимостьГруза КАК втРейсОценочнаяСтоимостьГруза
	|	ПО упРейс.Ссылка = втРейсОценочнаяСтоимостьГруза.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРасходы КАК втРасходы
	|	ПО упРейс.Ссылка = втРасходы.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		) КАК тбПеревозчикПоРейсу
	|	ПО упРейс.Ссылка = тбПеревозчикПоРейсу.Рейс
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсыТ.Рейс КАК Рейс,
	|	упПрицепыТранспортныхСредствТ.Прицеп КАК Прицеп,
	|	упПрицепыТранспортныхСредствТ.Период КАК Период,
	|	упПрицепыТранспортныхСредствТ.Операция КАК Операция
	|ПОМЕСТИТЬ втПрицепы
	|ИЗ
	|	втРейсы КАК втРейсыТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств КАК упПрицепыТранспортныхСредствТ
	|	ПО ИСТИНА
	|		И упПрицепыТранспортныхСредствТ.ТранспортноеСредство = втРейсыТ.ТранспортноеСредство
	|		И втРейсыТ.НачалоРейса >= упПрицепыТранспортныхСредствТ.Период
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрицепыТ.Прицеп КАК ТранспортноеСредство,
	|	втПрицепыТ.Рейс КАК Рейс,
	|	ИСТИНА КАК ЭтоПрицеп
	|ПОМЕСТИТЬ втПрицепыИТранспортныеСредстваПоРейсам
	|ИЗ
	|	втПрицепы КАК втПрицепыТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		МАКСИМУМ(втПрицепыТ.Период) КАК Период,
	|		втПрицепыТ.Прицеп КАК Прицеп,
	|		втПрицепыТ.Рейс КАК Рейс
	|	ИЗ
	|		втПрицепы КАК втПрицепыТ
	|	СГРУППИРОВАТЬ ПО
	|		втПрицепыТ.Прицеп,
	|		втПрицепыТ.Рейс) КАК ВложенныйЗапрос
	|	ПО ИСТИНА
	|		И ВложенныйЗапрос.Прицеп = втПрицепыТ.Прицеп
	|		И ВложенныйЗапрос.Рейс = втПрицепыТ.Рейс
	|		И ВложенныйЗапрос.Период = втПрицепыТ.Период
	|ГДЕ втПрицепыТ.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРейсы.Рейс КАК Рейс,
	|	ЛОЖЬ КАК ЭтоПрицеп
	|ИЗ
	|	втРейсы КАК втРейсы
	|ГДЕ втРейсы.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсыТ.ВидПеревозки КАК ВидПеревозки,
	|	втРейсыТ.ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРасходов КАК ВидПеревозкиОбращатьсяКМаршрутизаторуПриРасчетеРас,
	|	втРейсыТ.Время КАК Время,
	|	втРейсыТ.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	втРейсыТ.КоличествоМест КАК КоличествоМест,
	|	втРейсыТ.Масса КАК Масса,
	|	втРейсыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРейсыТ.НачалоРейса КАК НачалоРейса,
	|	втРейсыТ.Объем КАК Объем,
	|	втРейсыТ.Организация КАК Организация,
	|	втРейсыТ.ОценочнаяСтоимостьГруза КАК ОценочнаяСтоимостьГруза,
	|	втРейсыТ.ПлановыеРасходыПоРейсу КАК ПлановыеРасходыПоРейсу,
	|	втРейсыТ.Подразделение КАК Подразделение,
	|	втРейсыТ.Расстояние КАК Расстояние,
	|	втРейсыТ.Рейс КАК Рейс,
	|	втРейсыТ.Сотрудник КАК Сотрудник,
	|	втРейсыТ.ФактическиеРасходыПоРейсу КАК ФактическиеРасходыПоРейсу,
	|	втПрицепыИТранспортныеСредстваПоРейсамТ.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ЕСТЬNULL(втПрицепыИТранспортныеСредстваПоРейсамТ.ЭтоПрицеп, ЛОЖЬ) КАК ЭтоПрицеп
	|ПОМЕСТИТЬ втРейсыСТСиПрицепами
	|ИЗ
	|	втРейсы КАК втРейсыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПрицепыИТранспортныеСредстваПоРейсам КАК втПрицепыИТранспортныеСредстваПоРейсамТ
	|	ПО втПрицепыИТранспортныеСредстваПоРейсамТ.Рейс = втРейсыТ.Рейс
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|// Значение показателей распределения по рейсам
	|ВЫБРАТЬ
	|	втРейсы.*
	|ИЗ
	|	втРейсыСТСиПрицепами КАК втРейсы
	|;
	|//{Запрос: 14 ////////////////////////////////////////
	|// Значение показателей распределения по транспортным средствам
	|ВЫБРАТЬ
	|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
	|	СУММА(втРейсы.Масса) КАК Масса,
	|	СУММА(втРейсы.Объем) КАК Объем,
	|	СУММА(втРейсы.КоличествоМест) КАК КоличествоМест,
	|	СУММА(втРейсы.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втРейсы.Расстояние) КАК Расстояние,
	|	СУММА(втРейсы.Время) КАК Время,
	|	СУММА(втРейсы.ПлановыеРасходыПоРейсу) КАК ПлановыеРасходыПоРейсу,
	|	СУММА(втРейсы.ФактическиеРасходыПоРейсу) КАК ФактическиеРасходыПоРейсу,
	|	СУММА(втРейсы.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	втРейсы.Сотрудник КАК Сотрудник,
	|	втРейсы.ВидПеревозки КАК ВидПеревозки,
	|	втРейсы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРейсы.Подразделение КАК Подразделение,
	|	втРейсы.ЭтоПрицеп КАК ЭтоПрицеп
	|ИЗ
	|	втРейсыСТСиПрицепами КАК втРейсы
	|ГДЕ втРейсы.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	втРейсы.ТранспортноеСредство,
	|	втРейсы.Сотрудник,
	|	втРейсы.ВидПеревозки,
	|	втРейсы.НаправлениеДеятельности,
	|	втРейсы.Подразделение,
	|	втРейсы.ЭтоПрицеп
	|;
	|//{Запрос: 15 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втРейсыТ.Время) КАК Время,
	|	СУММА(втРейсыТ.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втРейсыТ.КоличествоМест) КАК КоличествоМест,
	|	СУММА(втРейсыТ.Масса) КАК Масса,
	|	втРейсыТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СУММА(втРейсыТ.Объем) КАК Объем,
	|	втРейсыТ.Организация КАК Организация,
	|	СУММА(втРейсыТ.ОценочнаяСтоимостьГруза) КАК ОценочнаяСтоимостьГруза,
	|	СУММА(втРейсыТ.ФактическиеРасходыПоРейсу) КАК ФактическиеРасходыПоРейсу,
	|	СУММА(втРейсыТ.ПлановыеРасходыПоРейсу) КАК ПлановыеРасходыПоРейсу,
	|	втРейсыТ.Подразделение КАК Подразделение,
	|	СУММА(втРейсыТ.Расстояние) КАК Расстояние,
	|	втРейсыТ.Сотрудник КАК Сотрудник,
	|	втРейсыТ.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРейсыТ.ВидПеревозки КАК ВидПеревозки,
	|	втРейсыТ.ЭтоПрицеп КАК ЭтоПрицеп
	|ИЗ
	|	втРейсыСТСиПрицепами КАК втРейсыТ
	|СГРУППИРОВАТЬ ПО
	|	втРейсыТ.НаправлениеДеятельности,
	|	втРейсыТ.Организация,
	|	втРейсыТ.Подразделение,
	|	втРейсыТ.Сотрудник,
	|	втРейсыТ.ТранспортноеСредство,
	|	втРейсыТ.ВидПеревозки,
	|	втРейсыТ.ЭтоПрицеп
	|";
	
	Возврат Текст;
	
	
КонецФункции

Функция ТекстЗапросаОборотыПоРасходам(ЗаполнитьРасходыПоРейсам, ЗаполнитьРасходыПоТС, ЗаполнитьРасходыФинансовыйРезультат)
	
	Текст =
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втРезультат.ТранспортноеСредство КАК ТранспортноеСредство,
	|	&Организация КАК Организация,
	|	втРезультат.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРезультат.Подразделение КАК Подразделение,
	|	втРезультат.СтатьяРасходов КАК СтатьяРасходов,
	|	&Валюта КАК Валюта,
	|	втРезультат.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРезультатОтбора
	|ИЗ
	|	&Результат КАК втРезультат
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходыПоРейсам.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоРейсам.Валюта КАК Валюта,
	|	втРасходыПоРейсам.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втРасходыПоРейсам.Рейс КАК Рейс,
	|	втРасходыПоРейсам.Рейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРасходыПоРейсам.СуммаФактСНДСОборот - втРасходыПоРейсам.СуммаФактБезНДСОборот КАК СуммаНДС,
	|	втРасходыПоРейсам.СуммаФактВалютаСНДСОборот - втРасходыПоРейсам.СуммаФактВалютаБезНДСОборот КАК СуммаВалютаНДС,
	|	втРасходыПоРейсам.СуммаФактВалютаСНДСОборот КАК СуммаВалюта,
	|	втРасходыПоРейсам.СуммаФактСНДСОборот КАК Сумма,
	|	втРасходыПоРейсам.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	РегистрНакопления.упРасходы.Обороты(
	|		&НачалоПериода,
	|		&ОкончаниеПериода,
	|		,
	|		(Рейс, СтатьяРасходов, Валюта) В (
	|		ВЫБРАТЬ
	|		втРезультатОтбора.Рейс КАК Рейс,
	|		втРезультатОтбора.СтатьяРасходов КАК СтатьяРасходов,
	|		втРезультатОтбора.Валюта КАК Валюта
	|		ИЗ
	|		втРезультатОтбора КАК втРезультатОтбора)) КАК втРасходыПоРейсам
	|ГДЕ &ЗаполнитьРасходыПоРейсам
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходыПоРейсам.АналитическийРазрез КАК АналитическийРазрез,
	|	втРасходыПоРейсам.Валюта КАК Валюта,
	|	втРасходыПоРейсам.СтатьяРасходов КАК СтатьяРасходов,
	|	втРасходыПоРейсам.ТранспортноеСредство КАК ТранспортноеСредство,
	|	втРасходыПоРейсам.СуммаФактСНДСОборот - втРасходыПоРейсам.СуммаФактБезНДСОборот КАК СуммаНДС,
	|	втРасходыПоРейсам.СуммаФактВалютаСНДСОборот - втРасходыПоРейсам.СуммаФактВалютаБезНДСОборот КАК СуммаВалютаНДС,
	|	втРасходыПоРейсам.СуммаФактВалютаСНДСОборот КАК СуммаВалюта,
	|	втРасходыПоРейсам.СуммаФактСНДСОборот КАК Сумма
	|ИЗ
	|	РегистрНакопления.упРасходыНаТС.Обороты(
	|		&НачалоПериода,
	|		&ОкончаниеПериода,
	|		,
	|		(ТранспортноеСредство, СтатьяРасходов, Валюта) В (
	|		ВЫБРАТЬ
	|		втРезультатОтбора.ТранспортноеСредство КАК ТранспортноеСредство,
	|		втРезультатОтбора.СтатьяРасходов КАК СтатьяРасходов,	
	|		втРезультатОтбора.Валюта КАК Валюта
	|		ИЗ
	|		втРезультатОтбора КАК втРезультатОтбора)) КАК втРасходыПоРейсам
	|ГДЕ &ЗаполнитьРасходыПоТС
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходыПоРейсам.Организация КАК Организация,
	|	втРасходыПоРейсам.Подразделение КАК Подразделение,
	|	втРасходыПоРейсам.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втРасходыПоРейсам.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	втРасходыПоРейсам.РасходыОборот КАК Расходы
	|ИЗ
	|	РегистрНакопления.упДоходыРасходыПредприятия.Обороты(
	|		&НачалоПериода,
	|		&ОкончаниеПериода,
	|		,
	|		(Организация, Подразделение,
	|		НаправлениеДеятельности, СтатьяДоходовРасходов) В (
	|		ВЫБРАТЬ
	|		втРезультатОтбора.Организация КАК Организация,
	|		втРезультатОтбора.Подразделение КАК Подразделение,
	|		втРезультатОтбора.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		втРезультатОтбора.СтатьяРасходов КАК СтатьяРасходов
	|		ИЗ
	|		втРезультатОтбора КАК втРезультатОтбора)) КАК втРасходыПоРейсам
	|ГДЕ &ЗаполнитьРасходыФинансовыйРезультат
	|";

	Возврат Текст;
	
КонецФункции

#КонецОбласти