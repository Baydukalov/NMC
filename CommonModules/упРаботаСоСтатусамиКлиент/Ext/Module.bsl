
#Область ПрограммныйИнтерфейс

// Процедура устанавливает новый статус документам.
//
// Параметры:
//  мсвДокументов - Массив - Ссылки на элементы.
//	Статус - ПеречислениеСсылка - Ссылка на элемент.
//
Процедура УстановитьСтатусДокументам(мсвДокументов, Статус) Экспорт
	
	мсвУдачных = упРаботаСоСтатусами.УстановитьСтатусДокументам(мсвДокументов, Статус);
	
	Если мсвУдачных.Количество() = 0 Тогда 
		ОписаниеОповещенияПользователя = Новый ОписаниеОповещения("ОткрытьЖурналРегистрации", ЭтотОбъект);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Установка статуса.'"),ОписаниеОповещенияПользователя, НСтр("ru = 'Не удалось установить статус документам'"), БиблиотекаКартинок.Ошибка32);
	ИначеЕсли мсвУдачных.Количество() = 1 Тогда 	
		ПоказатьОповещениеПользователя(НСтр("ru = 'Установка статуса.'"), ПолучитьНавигационнуюСсылку(мсвУдачных[0]), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Установлен статус документу %1'"), мсвУдачных[0]), БиблиотекаКартинок.Информация32);
	Иначе	
		стрПредставлениеДокументов = "";
		Для Каждого текДокумент Из мсвУдачных Цикл 
			стрПредставлениеДокументов = стрПредставлениеДокументов + текДокумент + "; ";
		КонецЦикла;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Установка статуса.'"),, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Установлен статус документам %1'"), стрПредставлениеДокументов), БиблиотекаКартинок.Информация32);
	КонецЕсли;	

КонецПроцедуры

// Процедура - Открыть журнал регистрации
//
// Параметры:
//  ДополнительныеПараметры	 - Структура	 - параметры
//
Процедура ОткрытьЖурналРегистрации(ДополнительныеПараметры) Экспорт
	упСервисныеФункцииКлиент.ОткрытьЖурналРегистрации(ДополнительныеПараметры);
КонецПроцедуры


#КонецОбласти