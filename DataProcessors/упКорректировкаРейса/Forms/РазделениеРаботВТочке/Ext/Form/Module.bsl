#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторТочки = Параметры.Идентификатор;
	
	Если Параметры.Свойство("РедактированиеМаршрута") Тогда
		тбзОперации = Параметры.Работы.Выгрузить(Новый Структура("Идентификатор", ИдентификаторТочки), "Операция, ТипОперации");
		тбзОперации.Колонки.Добавить("КоличествоРабот");
		тбзОперации.ЗаполнитьЗначения(1, "КоличествоРабот");
		тбзОперации.Свернуть("Операция, ТипОперации", "КоличествоРабот");
		тбзОперации.Колонки.Добавить("СоздаватьОперацию");
		тбзОперации.ЗаполнитьЗначения(0, "СоздаватьОперацию");
	Иначе	
		тбзОперации = Параметры.Работы.Выгрузить(Новый Структура("Идентификатор, Выполнена, Отменена", ИдентификаторТочки, Ложь, Ложь), "Операция, ТипОперации, СтрокаДокумента");
		тбзОперации.Колонки.Добавить("КоличествоРабот");
		тбзОперации.Колонки.Добавить("СоздаватьОперацию");
		тбзОперации.ЗаполнитьЗначения(1, "КоличествоРабот");
		
		Для каждого СтрокаОперация Из тбзОперации Цикл
			СтрокаОперация.СоздаватьОперацию = ?(СтрокаОперация.СтрокаДокумента, 0, 1);
		КонецЦикла;
		тбзОперации.Свернуть("Операция, ТипОперации", "КоличествоРабот, СоздаватьОперацию");
		
	КонецЕсли;
		
	СписокОпераций.Загрузить(тбзОперации);
	
	Элементы.СоздатьТочку.Доступность = Ложь;
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокОперацийПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	
	КоличествоСтрок = СписокОпераций.Количество();
	
	КоличествоВыделенныхСтрок = 0;
	Для каждого Строка Из СписокОпераций Цикл
		Если Строка.Пометка Тогда
			КоличествоВыделенныхСтрок = КоличествоВыделенныхСтрок + 1;	
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СоздатьТочку.Доступность = КоличествоВыделенныхСтрок > 0 И НЕ КоличествоВыделенныхСтрок = КоличествоСтрок;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьТочку(Команда)
	
	сткРазделитьРаботыТочки = Новый Структура("ИдентификаторТочки",ИдентификаторТочки);
	
	мсвОперации = Новый Массив;
	
	Для каждого СтрокаОперация Из СписокОпераций Цикл
		Если СтрокаОперация.Пометка Тогда
			мсвОперации.Добавить(СтрокаОперация.Операция);		
		КонецЕсли;
	КонецЦикла;
	
	сткРазделитьРаботыТочки.Вставить("СписокОпераций", мсвОперации);
	
	Закрыть(сткРазделитьРаботыТочки);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

#КонецОбласти