#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Рейс 		= Параметры.Рейс;
	сткСтатусРейса	= упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(Рейс);
	СтатусРейса	= сткСтатусРейса.Статус;
	ТолькоПроведенныеДокументы = Истина;
	ЗаполнитьСписокДокументов();
	ЗаблокироватьЭлементы(Истина)
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если НЕ ТолькоПроведенныеДокументы Тогда
		ЗаполнитьСписокДокументов();
		ЗаблокироватьЭлементы(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = Рейс Тогда
		
		ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
		ИндексТекущейСтроки = -1;
		Если ТекущиеДанные <> Неопределено Тогда
			ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);
		КонецЕсли;
	
		мсвСобытий = СтрРазделить(ИмяСобытия, упСервисныеФункцииКлиент.РазделительИменСобытий());
		
		ИзменилсяМаршрут = мсвСобытий.Найти("ИзменилсяМаршрут") <> Неопределено;
		ОбновитьРаботы	 = мсвСобытий.Найти("ОбновитьРаботы") <> Неопределено;
		
		Если ИзменилсяМаршрут ИЛИ ОбновитьРаботы Тогда
			ЗаполнитьСписокДокументов();
			УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки);
		КонецЕсли;
		ЗаблокироватьЭлементы();
			
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные 		= Элементы.СписокДокументов.ТекущиеДанные;
	
	Если Истина
		И Поле.Имя = "СписокДокументовДокумент"
		И ЗначениеЗаполнено(ТекущиеДанные.Документ)	Тогда 
		
		Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.упПрохождениеТочки") Тогда
		     ОткрытьФорму("Документ.упПрохождениеТочки.Форма.ФормаДокумента", Новый Структура("Ключ", ТекущиеДанные.Документ), ЭтаФорма);		
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.упПроисшествиеВПути") Тогда	
			ОткрытьФорму("Документ.упПроисшествиеВПути.Форма.ФормаДокумента", Новый Структура("Ключ", ТекущиеДанные.Документ), ЭтаФорма);		
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.упКорректировкаРейса") Тогда		
			ОткрытьФорму("Документ.упКорректировкаРейса.Форма.ФормаДокумента", Новый Структура("Ключ", ТекущиеДанные.Документ), ЭтаФорма);		
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.упРегистрацияОтложенныхРаботПоРейсу") Тогда		
			ОткрытьФорму("Документ.упРегистрацияОтложенныхРаботПоРейсу.Форма.ФормаДокумента", Новый Структура("Ключ", ТекущиеДанные.Документ), ЭтаФорма);			
		КонецЕсли;			
	
	ИначеЕсли Истина
			И Поле.Имя = "СписокДокументовАдрес"
			И ЗначениеЗаполнено(ТекущиеДанные.Адрес) Тогда	
		ОткрытьФорму("Справочник.упАдреса.Форма.ФормаЭлемента", Новый Структура("Ключ", ТекущиеДанные.Адрес), ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПометкаПриИзменении(Элемент)
	
	ТекущиеДанные 		= Элементы.СписокДокументов.ТекущиеДанные;
	ИндексТекущейСтроки	= СписокДокументов.Индекс(ТекущиеДанные);
		
	Если ТекущиеДанные.Пометка Тогда
		
		// Установить пометки у всех строк находящихся ниже текущей, до не пройденных точек.
		КоличествоСтрок = СписокДокументов.Количество();
		
		Для й = ИндексТекущейСтроки + 1 По КоличествоСтрок - 1 Цикл
			Строка = СписокДокументов[й];		
			Если Строка.Обработана = 1 Тогда
				Прервать;
			ИначеЕсли Строка.ПометкаУдаления Тогда
				Продолжить;
			Иначе	
				Строка.Пометка = Истина;	
			КонецЕсли;
		КонецЦикла;
		
	Иначе	
		
		// Снять пометки у всех строк находящихся выше текущей.
		й = ИндексТекущейСтроки - 1;
		Пока й >= 0 Цикл
			Строка = СписокДокументов[й];		
			Строка.Пометка = Ложь;
			й = й - 1;
		КонецЦикла;
		
	КонецЕсли;
	ЗаблокироватьЭлементы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьПрохождениеТочки(Команда)
		
	Отказ = Ложь;
	ТекстОшибки = "";
	ДокументСсылка = СформироватьПрохождениеТочкиНаСервере(Рейс, ТекстОшибки, Отказ);
	
	Если Не Отказ Тогда
		ОткрытьФорму("Документ.упПрохождениеТочки.Форма.ФормаДокумента", Новый Структура("Ключ", ДокументСсылка), ЭтаФорма);
	Иначе
		Если ТекстОшибки = "" Тогда
			ТекстОшибки = НСтр("ru = 'Прохождение точки не сформировано.'");		    
		КонецЕсли; 
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура СформироватьРегистрациюОтложенныхРабот(Команда)
	Отказ = Ложь;
	ТекстОшибки = "";
	ДокументСсылка = СформироватьРегистрациюОтложенныхРаботНаСервере(Рейс, ТекстОшибки, Отказ);
	
	Если Не Отказ Тогда
		ОткрытьФорму("Документ.упРегистрацияОтложенныхРаботПоРейсу.Форма.ФормаДокумента", Новый Структура("Ключ", ДокументСсылка), ЭтаФорма);
	Иначе
		Если ТекстОшибки = "" Тогда
			ТекстОшибки = НСтр("ru = 'Регистрация отложенных работ не сформирована.'");		    
		КонецЕсли; 
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПроведение(Команда)
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	ИндексТекущейСтроки = -1;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);
	КонецЕсли;

	ТекстОшибки = "";
	Отказ = Ложь;
	ИзменитьДокументыНаСервере("ОтменитьПроведение", ТекстОшибки, Отказ);
	УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки);	
	ЗаполнитьСписокДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	ИндексТекущейСтроки = -1;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИндексТекущейСтроки", ИндексТекущейСтроки);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПометитьНаУдалениеДокументы", ЭтаФорма, ДополнительныеПараметры);
	ТекстСообщения = Нстр("ru = 'Пометить документы на удаление?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокДокументов(Команда)
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	ИндексТекущейСтроки = -1;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);
	КонецЕсли;
	ЗаполнитьСписокДокументов();
	УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки);
КонецПроцедуры

&НаКлиенте
Процедура ДокументПометитьНаУдаление(Команда)
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);	
		
		ДокументСсылка	= ТекущиеДанные.Документ;
		
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда
			ДополнительныеПараметры = Новый Структура("Документ, ИндексТекущейСтроки", ДокументСсылка, ИндексТекущейСтроки);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПометитьНаУдаление", ЭтаФорма, ДополнительныеПараметры);
			Если ТекущиеДанные.ПометкаУдаления  Тогда
				ТекстСообщения = Нстр("ru = 'Снять с ""%1"" пометку на удаление?'");
			Иначе
				ТекстСообщения = Нстр("ru = 'Пометить ""%1"" на удаление?'");
			КонецЕсли;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументСсылка);	
			ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументПровести(Команда)
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);
		ДокументСсылка	= ТекущиеДанные.Документ;
		
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда
			ДокументЗаписать(ДокументСсылка, "Провести");
		КонецЕсли;
		
		УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДокументОтменитьПроведение(Команда)
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);
		ДокументСсылка	= ТекущиеДанные.Документ;
		
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда
			ДокументЗаписать(ДокументСсылка, "ОтменитьПроведение");
		КонецЕсли;
		
		УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьСписокДокументов()

	СписокДокументов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|//Количество выполненных/отмененных работ по регистраторам
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ВЫБОР
	|		КОГДА упРаботыПоРейсуТ.Выполнена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ВыполненоРабот,
	|	СУММА(ВЫБОР
	|		КОГДА упРаботыПоРейсуТ.Отменена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ОтмененоРабот,
	|	СУММА(ВЫБОР
	|		КОГДА упРаботыПоРейсуТ.Отложена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ОтложеноРабот,
	|	упРаботыПоРейсуТ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсуТ
	|ГДЕ ИСТИНА
	|	И упРаботыПоРейсуТ.Рейс = &Рейс
	|	И НЕ (упРаботыПоРейсуТ.Регистратор ССЫЛКА Документ.упРейс)
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсуТ.Регистратор
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПрохождениеТочкиТ.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА НЕ упПрохождениеТочкиТ.ОкончаниеВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА упПрохождениеТочкиТ.ОкончаниеВыполнения
	|		ИНАЧЕ упПрохождениеТочкиТ.Дата
	|	КОНЕЦ КАК ПериодФормированияДвижений,
	|	упПрохождениеТочкиТ.АдресТочки КАК Адрес,
	|	упПрохождениеТочкиТ.ИдентификаторТочки КАК ИдентификаторТочки,
	|	упПрохождениеТочкиТ.Изменил КАК Изменил,
	|	упПрохождениеТочкиТ.ИсточникСозданияОбъекта КАК ИсточникСозданияОбъекта,
	|	упПрохождениеТочкиТ.Автор КАК Автор,
	|	упПрохождениеТочкиТ.Проведен КАК Проведен,
	|	упПрохождениеТочкиТ.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ втДокументы
	|ИЗ
	|	Документ.упПрохождениеТочки КАК упПрохождениеТочкиТ
	|ГДЕ упПрохождениеТочкиТ.Рейс = &Рейс
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упПроисшествиеВПутиТ.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА НЕ упПроисшествиеВПутиТ.ОкончаниеВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА упПроисшествиеВПутиТ.ОкончаниеВыполнения
	|		ИНАЧЕ упПроисшествиеВПутиТ.Дата
	|	КОНЕЦ КАК ПериодФормированияДвижений,
	|	упПроисшествиеВПутиТ.АдресПроисшествия КАК Адрес,
	|	упПроисшествиеВПутиТ.ИдентификаторТочки КАК ИдентификаторТочки,
	|	упПроисшествиеВПутиТ.Изменил КАК Изменил,
	|	упПроисшествиеВПутиТ.ИсточникСозданияОбъекта КАК ИсточникСозданияОбъекта,
	|	упПроисшествиеВПутиТ.Автор КАК Автор,
	|	упПроисшествиеВПутиТ.Проведен КАК Проведен,
	|	упПроисшествиеВПутиТ.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.упПроисшествиеВПути КАК упПроисшествиеВПутиТ
	|ГДЕ упПроисшествиеВПутиТ.Рейс = &Рейс
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упКорректировкаРейсаТ.Ссылка КАК Ссылка,
	|	упКорректировкаРейсаТ.Дата КАК ПериодФормированияДвижений,
	|	ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка) КАК Адрес,
	|	"""" КАК ИдентификаторТочки,
	|	упКорректировкаРейсаТ.Изменил КАК Изменил,
	|	упКорректировкаРейсаТ.ИсточникСозданияОбъекта КАК ИсточникСозданияОбъекта,
	|	упКорректировкаРейсаТ.Автор КАК Автор,
	|	упКорректировкаРейсаТ.Проведен КАК Проведен,
	|	упКорректировкаРейсаТ.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.упКорректировкаРейса КАК упКорректировкаРейсаТ
	|ГДЕ упКорректировкаРейсаТ.Рейс = &Рейс
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упРегистрацияОтложенныхРаботПоРейсуТ.Ссылка КАК Ссылка,
	|	упРегистрацияОтложенныхРаботПоРейсуТ.Дата КАК ПериодФормированияДвижений,
	|	ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка) КАК Адрес,
	|	"""" КАК ИдентификаторТочки,
	|	упРегистрацияОтложенныхРаботПоРейсуТ.Изменил КАК Изменил,
	|	упРегистрацияОтложенныхРаботПоРейсуТ.ИсточникСозданияОбъекта КАК ИсточникСозданияОбъекта,
	|	упРегистрацияОтложенныхРаботПоРейсуТ.Автор КАК Автор,
	|	упРегистрацияОтложенныхРаботПоРейсуТ.Проведен КАК Проведен,
	|	упРегистрацияОтложенныхРаботПоРейсуТ.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.упРегистрацияОтложенныхРаботПоРейсу КАК упРегистрацияОтложенныхРаботПоРейсуТ
	|ГДЕ упРегистрацияОтложенныхРаботПоРейсуТ.Рейс = &Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//Регистраторы выполнявшие движения по маршруту
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(упМаршрутПоРейсу.Период, втДокументыТ.ПериодФормированияДвижений) КАК Период,
	|	ВЫБОР
	|		КОГДА втДокументыТ.Проведен
	|			ТОГДА 1
	|		КОГДА втДокументыТ.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА втДокументыТ.ИсточникСозданияОбъекта = ЗНАЧЕНИЕ(Перечисление.упИсточникиСозданияОбъекта.МобильныйКлиент)
	|			ТОГДА 0
	|		КОГДА втДокументыТ.ИсточникСозданияОбъекта = ЗНАЧЕНИЕ(Перечисление.упИсточникиСозданияОбъекта.ИнтеграцияСВнешнейСистемой)
	|			ТОГДА 1
	|		КОГДА втДокументыТ.ИсточникСозданияОбъекта = ЗНАЧЕНИЕ(Перечисление.упИсточникиСозданияОбъекта.Пользователь)
	|			ТОГДА 2
	|		КОГДА втДокументыТ.ИсточникСозданияОбъекта = ЗНАЧЕНИЕ(Перечисление.упИсточникиСозданияОбъекта.ВнешнийПользователь)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК ИсточникСозданияИндексКартинки,
	|	втДокументыТ.Ссылка КАК Ссылка,
	|	втДокументыТ.Адрес КАК Адрес,
	|	втДокументыТ.Автор КАК Автор,
	|	втДокументыТ.ИсточникСозданияОбъекта КАК ИсточникСозданияОбъекта,
	|	втДокументыТ.Изменил КАК Изменил,
	|	втДокументыТ.ПометкаУдаления КАК ПометкаУдаления,
	|	втДокументыТ.Проведен КАК Проведен,
	|	ВЫБОР
	|		КОГДА НЕ упМаршрутПоРейсу.Адрес ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРегистраторМаршрута
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	втДокументы КАК втДокументыТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|	ПО ИСТИНА
	|		И втДокументыТ.Ссылка = упМаршрутПоРейсу.Регистратор
	|		И упМаршрутПоРейсу.Рейс = &Рейс
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК СтрокаНомер,
	|	втМаршрут.Ссылка КАК Документ,
	|	втМаршрут.Адрес КАК Адрес,
	|	втМаршрут.Период КАК ДатаДвижений,
	|	втМаршрут.ИсточникСозданияИндексКартинки КАК ИсточникСозданияИндексКартинки,
	|	втМаршрут.Автор КАК Автор,
	|	втМаршрут.Изменил КАК Изменил,
	|	ЕСТЬNULL(втРаботы.ВыполненоРабот, 0) КАК ВыполненоРабот,
	|	ЕСТЬNULL(втРаботы.ОтмененоРабот, 0) КАК ОтмененоРабот,
	|	ЕСТЬNULL(втРаботы.ОтложеноРабот, 0) КАК ОтложеноРабот,
	|	0 КАК Обработана,
	|	втМаршрут.ИндексКартинки КАК ИндексКартинки,
	|	втМаршрут.ПометкаУдаления КАК ПометкаУдаления,
	|	втМаршрут.Проведен КАК Проведен,
	|	ЛОЖЬ КАК Отложена,
	|	втМаршрут.ЭтоРегистраторМаршрута КАК ЭтоРегистраторМаршрута
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРаботы КАК втРаботы
	|	ПО втМаршрут.Ссылка = втРаботы.Регистратор
	|ГДЕ ЛОЖЬ
	|	ИЛИ (ИСТИНА
	|		И &ТолькоПроведенные
	|		И втМаршрут.Проведен)
	|	ИЛИ НЕ (&ТолькоПроведенные)
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	тбМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	NULL КАК Документ,
	|	тбМаршрут.Адрес КАК Адрес,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДвижений,
	|	4 КАК ИсточникСозданияИндексКартинки,
	|	NULL КАК Автор,
	|	NULL КАК Изменил,
	|	0 КАК ВыполненоРабот,
	|	0 КАК ОтмененоРабот,
	|	0 КАК ОтложеноРабот,
	|	1 КАК Обработана,
	|	3 КАК ИндексКартинки,
	|	ЛОЖЬ КАК ПометкаУдаления,
	|	ЛОЖЬ КАК Проведен,
	|	тбМаршрут.Отложена КАК Отложена,
	|	ЛОЖЬ КАК ЭтоРегистраторМаршрута
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс = &Рейс) КАК тбМаршрут
	|ГДЕ ИСТИНА
	|	И НЕ (тбМаршрут.Пройдена)
	|	И НЕ (тбМаршрут.Отменена)
	|УПОРЯДОЧИТЬ ПО
	|	Обработана,
	|	ДатаДвижений,
	|	СтрокаНомер
	|";

	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("ТолькоПроведенные", ТолькоПроведенныеДокументы);
	
	СписокДокументов.Загрузить(Запрос.Выполнить().Выгрузить());
	ЗаблокироватьЭлементы(Истина);
				
КонецПроцедуры

&НаСервере
Функция СформироватьПрохождениеТочкиНаСервере(Рейс, ТекстОшибки, Отказ)
	
	Возврат Документы.упРейс.СформироватьСледующееПрохождениеТочки(Рейс, ТекстОшибки, Отказ);
	
КонецФункции

&НаСервере
Функция СформироватьРегистрациюОтложенныхРаботНаСервере(Рейс, ТекстОшибки, Отказ)
	
	Возврат Документы.упРегистрацияОтложенныхРаботПоРейсу.СформироватьРегистрацияОтложенныхРаботПоРейсу(Рейс, ТекстОшибки, Отказ);
	
КонецФункции


&НаСервере
Процедура ИзменитьДокументыНаСервере(Режим = "ОтменитьПроведение", ТекстОшибки, Отказ)
	
	мсвДокументы = Новый Массив;
	КоличествоСтрок	= СписокДокументов.Количество();
	
	й = КоличествоСтрок - 1;
	Пока й >=0 Цикл
		
		СтрокаДокумент = СписокДокументов[й];
		Если СтрокаДокумент.Пометка Тогда
			мсвДокументы.Добавить(СтрокаДокумент.Документ);	
		КонецЕсли;
		й = й - 1;
	КонецЦикла;
	
	ПометкаУдаления = Ложь;
	РежимЗаписи 	 = РежимЗаписиДокумента.ОтменаПроведения;	
	
	Если Режим = "ПометитьНаУдаление" Тогда	
		ПометкаУдаления = Истина;
	КонецЕсли;
	
	Если НЕ Режим = Неопределено Тогда
		
		упСервисныеФункции.ЗаписатьМассивДокументов(мсвДокументы, РежимЗаписи, ПометкаУдаления, ТекстОшибки, Отказ);
	
		ЗаполнитьСписокДокументов();

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы(ЗаблокироватьКоманды = Ложь)
	
	Если СтатусРейса = Перечисления.упСтатусыРейса.Завершен Тогда
		Элементы.СписокДокументов.ТолькоПросмотр = Истина;
		Элементы.ФормаПометитьНаУдаление.Доступность = Ложь;
		Элементы.ФормаОтменитьПроведение.Доступность = Ложь;
		Элементы.ФормаОбновитьСписокДокументов.Доступность = Ложь;
		Элементы.ФормаСформироватьПрохождениеТочки.Доступность = Ложь;
	ИначеЕсли ЗаблокироватьКоманды Тогда	
		Элементы.ФормаПометитьНаУдаление.Доступность = Ложь;
		Элементы.ФормаОтменитьПроведение.Доступность = Ложь;
	Иначе
		мсвПомеченныеСтроки = СписокДокументов.НайтиСтроки(Новый Структура("Пометка", Истина));
		Если мсвПомеченныеСтроки.Количество() > 0 Тогда
			Элементы.ФормаПометитьНаУдаление.Доступность = Истина;
			Элементы.ФормаОтменитьПроведение.Доступность = Истина;
		Иначе
			Элементы.ФормаПометитьНаУдаление.Доступность = Ложь;
			Элементы.ФормаОтменитьПроведение.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДокументЗаписать(ДокументСсылка, Режим)
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		Попытка
			Если Режим = "ПометитьНаУдаление" Тогда
				ДокументОбъект.ПометкаУдаления = НЕ ДокументОбъект.ПометкаУдаления;
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			ИначеЕсли Режим = "ОтменитьПроведение" Тогда	
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			ИначеЕсли	Режим = "Провести" Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			Отказ = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументСсылка,,,Отказ);
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,ДокументСсылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
		
	Иначе	
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
	КонецЕсли;
	
	ЗаполнитьСписокДокументов();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПометитьНаУдаление(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТекущийДокумент = ДополнительныеПараметры.Документ;
		ДокументЗаписать(ТекущийДокумент, "ПометитьНаУдаление");
		УстановитьТекущуюСтрокуПоИндексу(ДополнительныеПараметры.ИндексТекущейСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПометитьНаУдалениеДокументы(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТекстОшибки = "";
		Отказ = Ложь;
		ИзменитьДокументыНаСервере("ПометитьНаУдаление", ТекстОшибки, Отказ); 
		ЗаполнитьСписокДокументов();
		УстановитьТекущуюСтрокуПоИндексу(ДополнительныеПараметры.ИндексТекущейСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоПроведенныеДокументыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ИндексТекущейСтроки = СписокДокументов.Индекс(ТекущиеДанные);	
	КонецЕсли;

	ЗаполнитьСписокДокументов();
	
	УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтрокуПоИндексу(ИндексТекущейСтроки)
	Если Истина
		И ИндексТекущейСтроки < СписокДокументов.Количество() 
		И НЕ ИндексТекущейСтроки = -1 Тогда
		ТекущаяСтрока = СписокДокументов.Получить(ИндексТекущейСтроки);
		Элементы.СписокДокументов.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти