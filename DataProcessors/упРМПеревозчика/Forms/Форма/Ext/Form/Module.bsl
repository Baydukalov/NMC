#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(ПараметрыСеанса.ТекущийВнешнийПользователь) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.упПрохождениеТочки) Тогда
		Элементы.ПрохождениеТочек.Видимость = Ложь;
	КонецЕсли;
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли; 
	
	УчитыватьМассу                       = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	УчитыватьОбъем                       = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	УчитыватьКоличествоМест              = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	
	ОбъектАвторизации = ВнешниеПользователи.ТекущийВнешнийПользователь().ОбъектАвторизации;
	
	УстановитьЗаголовки();
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СменаСтатуса" И ТипЗнч(Источник) = тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		Элементы.ЗаявкиНаТС.Обновить();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаявкиНаТСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	
	Если Поле.Имя = "ЗаявкиНаТСПредложение" Тогда
		СтандартнаяОбработка = Ложь;
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.Предложение);
		ОткрытьФорму("Документ.упПредложениеПеревозчика.Форма.ФормаДокумента",сткПараметры);
	ИначеЕсли Поле.Имя = "ЗаявкиНаТСРейс" Тогда
		СтандартнаяОбработка = Ложь;
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.Рейс);
		ОткрытьФорму("Документ.упРейс.Форма.ФормаДокумента",сткПараметры);		
	ИначеЕсли Поле.Имя = "ЗаявкиНаТСПодтверждениеЗаявкиНаТС" Тогда
		СтандартнаяОбработка = Ложь;
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.ПодтверждениеЗаявкиНаТС);
		ОткрытьФорму("Документ.упПодтверждениеЗаявкиНаТС.Форма.ФормаДокумента",сткПараметры);			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаявкиНаТСПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ПрохождениеТочек, "Рейс", Неопределено, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ПрохождениеТочек, "Рейс", ТекущиеДанные.Рейс, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьПредложениеПеревозчика(Команда)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		упСервисныеФункцииКлиент.СформироватьПредложениеПеревозчика(ТекущиеДанные.Ссылка);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПодтверждение(Команда)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		упСервисныеФункцииКлиент.СформироватьПодтверждениеЗаявкиНаТС(ТекущиеДанные.Ссылка);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПрохождениеТочки(Команда)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		Рейс = ТекущиеДанные.Рейс;
		Если Не ЗначениеЗаполнено(Рейс) Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'По документу ""%1"" нет сформированного рейса.'"), ТекущиеДанные.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли; 
		
		Отказ = Ложь;
		ТекстОшибки = "";
		текПрохождениеТочки = СформироватьПрохождениеТочкиНаСервере(Рейс, ТекстОшибки, Отказ);
		
		Если Не Отказ Тогда
			ОткрытьФорму("Документ.упПрохождениеТочки.Форма.ФормаДокумента", Новый Структура("Ключ", текПрохождениеТочки));
		Иначе
			Если ТекстОшибки = "" Тогда
				ТекстОшибки = НСтр("ru = 'Прохождение точки не сформировано.'");		    
			КонецЕсли; 		
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьПрохождениеТочкиНаСервере(Рейс, ТекстОшибки, Отказ)
	
	Возврат Документы.упРейс.СформироватьСледующееПрохождениеТочки(Рейс, ТекстОшибки, Отказ);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест);
	
	Если УчитыватьМассу Тогда
		Элементы.ЗаявкиНаТСМасса.Заголовок     = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСМасса.Заголовок, сткПредставления.Масса);
		Элементы.ЗаявкиНаТСМассаФакт.Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСМассаФакт.Заголовок, сткПредставления.Масса);
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		Элементы.ЗаявкиНаТСОбъем.Заголовок     = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСОбъем.Заголовок, сткПредставления.Объем);
		Элементы.ЗаявкиНаТСОбъемФакт.Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСОбъемФакт.Заголовок, сткПредставления.Объем);
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда         
		Элементы.ЗаявкиНаТСКоличествоМест.Заголовок     = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСКоличествоМест.Заголовок, сткПредставления.КоличествоМест);
		Элементы.ЗаявкиНаТСКоличествоМестФакт.Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСКоличествоМестФакт.Заголовок, сткПредставления.КоличествоМест);
	КонецЕсли;
	
	Если ВедетсяВалютныйУчет Тогда
		Элементы.ЗаявкиНаТСГруппаРасходы.Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"), Элементы.ЗаявкиНаТСГруппаРасходы.Заголовок, ВалютаУпрУчета);
	КонецЕсли;
	
КонецПроцедуры // УстановитьЗаголовки()

&НаСервереБезКонтекста
Процедура ЗаявкиНаТСПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ЕстьПлановоеВремяРейса = Ложь;	
	ЕстьФактическоеВремяРейса = Ложь;
	Для каждого текСтрока Из Строки Цикл
		ДанныеСтроки = текСтрока.Значение.Данные;
		Если Не ЕстьПлановоеВремяРейса Тогда
			Если Не ДанныеСтроки.Свойство("ПлановоеВремяРейса") Тогда
				Прервать;
			Иначе
				ЕстьПлановоеВремяРейса = Истина;
			КонецЕсли;
		КонецЕсли; 
		Если ЗначениеЗаполнено(ДанныеСтроки.ПлановоеНачалоВыполнения) Или ЗначениеЗаполнено(ДанныеСтроки.ПлановоеОкончаниеВыполнения) Тогда
			ДанныеСтроки.ПлановоеВремяРейса = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(ДанныеСтроки.ПлановоеНачалоВыполнения, ДанныеСтроки.ПлановоеОкончаниеВыполнения); 
		КонецЕсли; 
		
		Если Не ЕстьФактическоеВремяРейса Тогда
			Если Не ДанныеСтроки.Свойство("ФактическоеВремяРейса") Тогда
				Прервать;
			Иначе
				ЕстьФактическоеВремяРейса = Истина;
			КонецЕсли;
		КонецЕсли; 
		Если ЗначениеЗаполнено(ДанныеСтроки.НачалоВыполненияФакт) Или ЗначениеЗаполнено(ДанныеСтроки.ОкончаниеВыполненияФакт) Тогда
			ДанныеСтроки.ФактическоеВремяРейса = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(ДанныеСтроки.НачалоВыполненияФакт, ДанныеСтроки.ОкончаниеВыполненияФакт); 
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти