#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	ТекущийПользователь = Пользователи.ТекущийПользователь();
		
	ИнициализироватьКомпоновщикНастроек();
		
	УстановитьПараметрыПланированиеРемонтовНаСервере();
	
	УстановитьВидПланировщика();
	
	СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ПустаяСсылка");
	
	Если НЕ ПараметрыПланированиеРемонтов = Неопределено Тогда
		СпособПланированияРемонтныхРабот = ПараметрыПланированиеРемонтов.СпособПланированияРемонтныхРабот;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРемонтов, "СпособПланированияРемонтныхРабот", СпособПланированияРемонтныхРабот, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРемонтов, "Гараж", Гараж, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Настройки.Получить("Гараж")) Тогда
		ОбновитьДанныеПланировщикаНаСервере();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Ложь
		Или ИмяСобытия = "ЗаписьДокументаРемонтИТОТС"
		Или ИмяСобытия = "ЗаписьДокументаЗаявкаРемонтИТОТС" Тогда
		
		Элементы.СписокРемонтов.Обновить();
		ОбновитьДанныеПланировщикаНаСервере();	
		
	ИначеЕсли ИмяСобытия = "ИзменениеСтатусаРемонтИТОТС" Тогда
		
		СсылкаНаДокумент = Параметр.Ссылка;	
		СтатусДокумента =  Параметр.Статус;	
		ВидРемонтаДокумента = Параметр.ВидРемонта;
		
		ИскомыеСтроки = СписокЭлементовПланировщика.НайтиСтроки(Новый Структура("Документ", СсылкаНаДокумент));
		
		Если ИскомыеСтроки.Количество() Тогда
			
			НайденнаяСтрока = ИскомыеСтроки[0];
			
			НайденнаяСтрока.Статус = СтатусДокумента;
			НайденнаяСтрока.ИндексСтатуса = ПолучитьПорядокСтатусаРемонта(СтатусДокумента);
			НайденнаяСтрока.ВидРемонта = ВидРемонтаДокумента;
			
			ТекущийДокумент   = НайденнаяСтрока.Документ;
			ТекущееТС         = НайденнаяСтрока.ТранспортноеСредство;
			ТекущийВидРемонта = НайденнаяСтрока.ВидРемонта;
			КартинкаСтатусДокумента = НазначитьКартинкуДокументуРемонт(НайденнаяСтрока.ИндексСтатуса);
			КартинкаТипРемонта = НайденнаяСтрока.Картинка;
			
			Элементы.СписокРемонтов.Обновить();
			ОбновитьДанныеПланировщикаНаСервере();
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГаражПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРемонтов, "Гараж", Гараж, ВидСравненияКомпоновкиДанных.Равно);
	ОбновитьДанныеПланировщикаНаСервере();

КонецПроцедуры

#Область ЭлементыФормы_Планировщик

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)
	
	ВыделенныеЭлементы = Элементы.Планировщик.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() Тогда
		ЭлементПланировщика = ВыделенныеЭлементы[0];
		ИскомыеСтроки = СписокЭлементовПланировщика.НайтиСтроки(Новый Структура("Документ", ЭлементПланировщика.Значение));
		Если ИскомыеСтроки.Количество() Тогда
			НайденнаяСтрока = ИскомыеСтроки[0];
			ТекущийДокумент   = НайденнаяСтрока.Документ;
			ТекущееТС         = НайденнаяСтрока.ТранспортноеСредство;
			ТекущийВидРемонта = НайденнаяСтрока.ВидРемонта;
			КартинкаСтатусДокумента = НазначитьКартинкуДокументуРемонт(НайденнаяСтрока.ИндексСтатуса);
			КартинкаТипРемонта = НайденнаяСтрока.Картинка;
		КонецЕсли; 
	Иначе
		ТекущийДокумент   = Неопределено;
		ТекущееТС         = Неопределено;
		ТекущийВидРемонта = Неопределено;
		КартинкаСтатусДокумента = БиблиотекаКартинок.упПустаяКартинка;
		КартинкаТипРемонта = БиблиотекаКартинок.Информация;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элементы.ФормаУстановитьИнтервалМесяц.Пометка Тогда
		Возврат;
	КонецЕсли; 
	МестоРемонта = Значения.Получить("МестаРемонта");
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("МестоРемонта"        , МестоРемонта);
	сткПараметры.Вставить("НачалоРемонтаПлан"   , Начало);
	сткПараметры.Вставить("ОкончаниеРемонтаПлан", Конец);
	
	СпособПланированияРемонтныхРабот = Неопределено;
	
	Если НЕ ПараметрыПланированиеРемонтов = Неопределено Тогда
		СпособПланированияРемонтныхРабот = ПараметрыПланированиеРемонтов.СпособПланированияРемонтныхРабот;
	КонецЕсли; 
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Если СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ДокументомРемонт") Тогда
		ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", сткПараметры,,,,, Новый ОписаниеОповещения("СозданиеРемонтаЗавершение", ЭтаФорма), РежимОткрытия);
	ИначеЕсли СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ДокументомЗаявкаНаРемонт") Тогда
		ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", сткПараметры,,,,, Новый ОписаниеОповещения("СозданиеРемонтаЗавершение", ЭтаФорма), РежимОткрытия);
	Иначе		
		Если ВидРемонтаЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт(МестоРемонта) Тогда
			ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", сткПараметры,,,,, Новый ОписаниеОповещения("СозданиеРемонтаЗавершение", ЭтаФорма), РежимОткрытия);
		Иначе
			СписокЗначений = Новый СписокЗначений;
			СписокЗначений.Добавить(КодВозвратаДиалога.Да, "Ремонт (ТО)ТС");
			СписокЗначений.Добавить(КодВозвратаДиалога.Нет, "Заявка на ремонт (ТО)ТС");
			СписокЗначений.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаСпособПланирования", ЭтаФорма, сткПараметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Выберете способ планирования ремонта документом - Ремонт и ТО ТС - Заявка на ремонт и ТО ТС, «Отмена» - отказаться от выбора.';"
			+ " en = 'Do you want to continue?'"), СписокЗначений, 0);
		КонецЕсли;		
	КонецЕсли;
	 
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСпособПланирования(Результат, Параметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Отмена Тогда
        Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", Параметры,,,,, Новый ОписаниеОповещения("СозданиеРемонтаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", Параметры,,,,, Новый ОписаниеОповещения("СозданиеРемонтаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    КонецЕсли;

КонецПроцедуры // ПослеЗакрытияВопросаСпособПланирования()

&НаСервереБезКонтекста
Функция ВидРемонтаЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт(МестоРемонта)

	ВидРемонтаПоУмолчанию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МестоРемонта, "ВидРемонтаПоУмолчанию");
	ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРемонтаПоУмолчанию, "ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт");
	Возврат ?(ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт =  Истина, Истина, Ложь);

КонецФункции // ВидРемонтаЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт()

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыделенныеЭлементы = Элементы.Планировщик.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() Тогда
		текРемонт = ВыделенныеЭлементы[0].Значение;
		Если ТипЗнч(текРемонт) = Тип("ДокументСсылка.упРемонтИТОТС")Тогда
			ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", текРемонт), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли ТипЗнч(текРемонт) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС")Тогда
			ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", текРемонт), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	
	Если Элементы.ФормаУстановитьИнтервалМесяц.Пометка Тогда
		СтандартнаяОбработка = Ложь;
		УстановитьИнтервалМесяцСервер(ТекущиеПериодыОтображения[0].Начало + 1296000); 
	КонецЕсли;	
	УстановитьИнтервалыФонаПоГрафикуРаботы(ТекущиеПериодыОтображения[0].Начало, ТекущиеПериодыОтображения[0].Конец);
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Дата, Значения)
	        
	СтандартнаяОбработка = Ложь;
	текРемонт = Элементы.СписокРемонтов.ДанныеСтроки(ПараметрыПеретаскивания.Значение).Ссылка;
	Если Истина
		И ТипЗнч(текРемонт) <> Тип("ДокументСсылка.упРемонтИТОТС")
		И ТипЗнч(текРемонт) <> Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС")
	Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;                 
	                                                                                                             
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Дата, Значения)
	
	СтандартнаяОбработка = Ложь;
	текРемонт = Элементы.СписокРемонтов.ДанныеСтроки(ПараметрыПеретаскивания.Значение).Ссылка;
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("НачалоРемонтаПлан", НачалоЧаса(Дата));
	сткПараметры.Вставить("ОкончаниеРемонтаПлан", КонецЧаса(Дата));                            
	сткПараметры.Вставить("Гараж", Гараж);
	сткПараметры.Вставить("МестоРемонта", Значения.Получить("МестаРемонта"));
	сткПараметры.Вставить("Значения", Значения);
	ЗапланироватьРемонтНаСервере(текРемонт, сткПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена Тогда
		Возврат;
	Иначе
		Планировщик.Элементы.Удалить(Планировщик.Элементы.Найти(ПараметрыПеретаскивания.Значение[0].Значение));
		Элементы.СписокРемонтов.Обновить();
		ОбновитьДанныеПланировщикаНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	ВыделенныеЭлементы = Элементы.Планировщик.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() Тогда
		РедактируемыйЭлемент = ВыделенныеЭлементы[0];
		МестоРемонта = РедактируемыйЭлемент.ЗначенияИзмерений.Получить("МестаРемонта");
		ИскомыеСтроки = СписокЭлементовПланировщика.НайтиСтроки(Новый Структура("Документ", РедактируемыйЭлемент.Значение));
		Если ИскомыеСтроки.Количество() Тогда
			НайденнаяСтрока = ИскомыеСтроки[0];
			Если НайденнаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Начат")
				Или НайденнаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда
				ОтменаРедактирования = Истина;
				//ТекстСообщения = СтрШаблон("ru = 'Ремонт (ТО) находится в статусе ""%1"". Изменение невозможно.'", НайденнаяСтрока.Статус);
				ТекстСообщения = Нстр("ru = 'Ремонт (ТО) находится в статусе ""%1"". Изменение невозможно.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НайденнаяСтрока.Статус);
				ПоказатьПредупреждение(,ТекстСообщения);
				//упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(НСтр(ТекстСообщения));
			ИначеЕсли НайденнаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт")
				ИЛИ НайденнаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте")
				ИЛИ НайденнаяСтрока.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена") Тогда
				ОтменаРедактирования = Истина;
				ТекстСообщения = Нстр("ru = 'Заявка на ремонт (ТО) находится в статусе ""%1"". Изменение невозможно.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НайденнаяСтрока.Статус);
				ПоказатьПредупреждение(,ТекстСообщения);
				
			Иначе
				Ремонт = НайденнаяСтрока.Документ;
				ТекстОшибки = "";
				ИзменитьВремяРемонтаНаСервере(Ремонт, РедактируемыйЭлемент.Начало, РедактируемыйЭлемент.Конец, МестоРемонта, ТекстОшибки);
				Если ТекстОшибки <> "" Тогда
					ОтменаРедактирования = Истина;
					//ПоказатьПредупреждение(, ТекстОшибки);
					упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(, Истина);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры
	
#КонецОбласти 

#Область ЭлементыФормы_СписокРемонтов
	
&НаКлиенте
Процедура СписокРемонтовПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	мсвРемонтов = ПараметрыПеретаскивания.Значение;
	Если ТипЗнч(мсвРемонтов) = Тип("Массив") И мсвРемонтов.Количество() Тогда
		текРемонт = мсвРемонтов[0].Значение;
		СтатусРемонта = ПолучитьСтатусРемонтаНаСервере(текРемонт);
		Если Истина
			И СтатусРемонта <> ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Запланирован")
			И СтатусРемонта <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению") Тогда
			ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			Возврат;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРемонтовПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Отказ = Ложь;
		ПеревестиРемонтВСтатусНовыйНаСервере(ПараметрыПеретаскивания.Значение[0].Значение, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРемонтовОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена Тогда
		Возврат;
	Иначе
		Элементы.СписокРемонтов.Обновить();
		ОбновитьДанныеПланировщикаНаСервере();
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура УказатьПараметрыПланированиеРемонтов(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыПланированиеРемонтов", ПараметрыПланированиеРемонтов);
	ОткрытьФорму("Обработка.упРМПланированиеРемонтов.Форма.ФормаУстановкиПараметров", ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыПланированиеРемонтовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображениеНаДату(Команда)
	
	ТекстЗаголовка = НСтр("ru = 'Укажите дату, входящую в желаемый интервал планирования'");
	ПоказатьВводДаты(Новый ОписаниеОповещения("ВводДатыОтображенияЗавершение", ЭтаФорма), Планировщик.ТекущиеПериодыОтображения[0].Начало, ТекстЗаголовка, ЧастиДаты.Дата); 
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДоступныеМестаРемонта(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Настройки", КомпоновщикНастроек.Настройки);
	
	ОткрытьФорму("Обработка.упРМПланированиеРемонтов.Форма.ФормаНастройкиМестРемонтов", ПараметрыФормы,,,,, Новый ОписаниеОповещения("НастройкаМестРемонтовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПланировщика(Команда)
	
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалДень(Команда)
	
	УстановитьИнтервалДеньСервер();
	УстановитьИнтервалыФонаПоГрафикуРаботы();
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалНеделя(Команда)

	УстановитьИнтервалНеделяСервер();
	УстановитьИнтервалыФонаПоГрафикуРаботы();
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалМесяц(Команда)
	
	УстановитьИнтервалМесяцСервер();
	УстановитьИнтервалыФонаПоГрафикуРаботы();
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВперед(Команда)
	ИзменитьПериод();
КонецПроцедуры

&НаКлиенте
Процедура ПериодНазад(Команда)
	ИзменитьПериод(Ложь);               	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьПериод(СледующийПериод = Истина)
	
	Коэффициент = ?(СледующийПериод, 1, -1);
	
	ТекущиеПериодыОтображения = Планировщик.ТекущиеПериодыОтображения;
	
	Если Элементы.ФормаУстановитьИнтервалДень.Пометка Тогда
			
		ТекущиеПериодыОтображения[0].Начало = ТекущиеПериодыОтображения[0].Начало + Коэффициент * 3600 * 24; 
		ТекущиеПериодыОтображения[0].Конец = ТекущиеПериодыОтображения[0].Конец + Коэффициент * 3600 * 24;

	ИначеЕсли Элементы.ФормаУстановитьИнтервалНеделя.Пометка Тогда	
		
		ТекущиеПериодыОтображения[0].Начало = ТекущиеПериодыОтображения[0].Начало + Коэффициент * 7 * 3600 * 24; 
		ТекущиеПериодыОтображения[0].Конец = ТекущиеПериодыОтображения[0].Конец + Коэффициент * 7 * 3600 * 24;
			
	ИначеЕсли Элементы.ФормаУстановитьИнтервалМесяц.Пометка Тогда		
		
		ТекущиеПериодыОтображения[0].Начало = ДобавитьМесяц(ТекущиеПериодыОтображения[0].Начало, Коэффициент); 
		ТекущиеПериодыОтображения[0].Конец = ДобавитьМесяц(ТекущиеПериодыОтображения[0].Конец, Коэффициент);
		
	КонецЕсли;
	
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыПланированиеРемонтовНаСервере()
	
	ЗаполнитьПараметрыПоУмолчанию = Истина;
	
	КлючОбъекта = "Обработка.упРМПланированиеРемонтов.ПараметрыПланированиеРемонтов";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если НЕ СохраненныеНастройки = Неопределено Тогда
				ПараметрыПланированиеРемонтов = СохраненныеНастройки;
				Если ПараметрыПланированиеРемонтов.Свойство("СпособПланированияРемонтныхРабот") Тогда
					ЗаполнитьПараметрыПоУмолчанию = Ложь;
				Иначе	
					ЗаполнитьПараметрыПоУмолчанию = Истина;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗаполнитьПараметрыПоУмолчанию Тогда
		ПараметрыПланированиеРемонтов = упВариантыНастроек.ПараметрыПланированиеРемонтовПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметрыПланированиеРемонтовНаСервере()

&НаСервере
Процедура УстановитьИнтервалДеньСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоДня(ТекущаяДата), КонецДня(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	ПервыйЭлемент.Формат = "ДФ=""дд ММММ гггг 'г.' (dddd)""";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат  = "ДФ=HH:mm";
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Час;
	Планировщик.КратностьПериодическогоВарианта = 24;
	
	Элементы.ФормаУстановитьИнтервалДень.Пометка   = Истина;
	Элементы.ФормаУстановитьИнтервалНеделя.Пометка = Ложь;
	Элементы.ФормаУстановитьИнтервалМесяц.Пометка  = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтервалНеделяСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Лево;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоНедели(ТекущаяДата), КонецНедели(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	ПервыйЭлемент.Формат  = "ДФ = 'dd.MM'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат  = "ДФ = 'dddd'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = ЦветаСтиля.ЦветФонаПоля;
	НовыйЭлементШкалы.ОтображатьПериодическиеМетки = Истина;
	НовыйЭлементШкалы.Формат  = "ДФ=HH";

	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Неделя;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.ФормаУстановитьИнтервалДень.Пометка   = Ложь;
	Элементы.ФормаУстановитьИнтервалНеделя.Пометка = Истина;
	Элементы.ФормаУстановитьИнтервалМесяц.Пометка  = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтервалМесяцСервер(ТекущаяДата = Неопределено)

	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли; 
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоМесяца(ТекущаяДата), КонецМесяца(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.Формат = "ДФ = 'MMMM yyyy'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.Формат = "ДФ = 'dd'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат = "ДФ = 'ddd'";
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Месяц;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.ФормаУстановитьИнтервалДень.Пометка   = Ложь;
	Элементы.ФормаУстановитьИнтервалНеделя.Пометка = Ложь;
	Элементы.ФормаУстановитьИнтервалМесяц.Пометка  = Истина;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидПланировщика()
	
	УстановитьИнтервалДеньСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПланировщикаНаСервере()	

	СписокЭлементовПланировщика.Очистить();
	
	Планировщик.Измерения.Очистить();
	Планировщик.Элементы.Очистить();
	ГрафикРаботы      = Неопределено;
	ТекущийДокумент   = Неопределено;
	ТекущееТС         = Неопределено;
	ТекущийВидРемонта = Неопределено;
	КартинкаСтатусДокумента = БиблиотекаКартинок.упПустаяКартинка;
	КартинкаТипРемонта = БиблиотекаКартинок.Информация;
	
	Если ЗначениеЗаполнено(Гараж) Тогда
		Элементы.ГруппаОсновная.Доступность = Истина;
	Иначе	
		Элементы.ГруппаОсновная.Доступность = Ложь;
		Возврат;
	КонецЕсли; 
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	НачалоПериода = Планировщик.ТекущиеПериодыОтображения[0].Начало;
	ОкончаниеПериода = Планировщик.ТекущиеПериодыОтображения[0].Конец;
	
	// Установка параметров
	УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "Гараж", Гараж, Истина);
	УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "НачалоПериода", НачалоПериода, Истина);
	УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "ОкончаниеПериода", ОкончаниеПериода, Истина);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Дерево значений, в которое будет получен результат
	Результат = Новый ДеревоЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	НовоеИзмерение = Планировщик.Измерения.Добавить("МестаРемонта");
	НовоеИзмерение.Текст = НСтр("ru = 'Места ремонта'");
	
	ЕстьМестаРемонта = Ложь;
	
	Для каждого ГаражТекущий Из Результат.Строки Цикл
		
		Если ЗначениеЗаполнено(ГаражТекущий.ГрафикРаботы) Тогда
			ГрафикРаботы = ГаражТекущий.ГрафикРаботы;
			УстановитьИнтервалыФонаПоГрафикуРаботы();
		КонецЕсли; 
		
		Для каждого МестоРемонтаТекущее Из ГаражТекущий.Строки Цикл
			
			ЕстьМестаРемонта = Истина;
			
			соотИзмерений = Новый Соответствие;
			НовыйЭлементИзмерения = НовоеИзмерение.Элементы.Добавить(МестоРемонтаТекущее.МестоРемонта); 
			соотИзмерений.Вставить(НовоеИзмерение.Значение, НовыйЭлементИзмерения.Значение); 

			Для каждого текРабота Из МестоРемонтаТекущее.Строки Цикл
				Если ЗначениеЗаполнено(текРабота.ДатаНачала) Тогда
					НовыйЭлемент = ДобавитьЭлементПланировщика(текРабота);
					НовыйЭлемент.ЗначенияИзмерений = Новый ФиксированноеСоответствие(соотИзмерений); 
					
					НоваяСтрока = СписокЭлементовПланировщика.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, текРабота);
					НоваяСтрока.Картинка = НовыйЭлемент.Картинка;
				КонецЕсли; 
			КонецЦикла; 
		КонецЦикла;
	КонецЦикла; 
	
	Элементы.ГруппаОсновная.Доступность = ЕстьМестаРемонта;
	
КонецПроцедуры

&НаСервере
// Устанавливает значение параметра СКД.
//
// Параметры:
//  Настройки - Структура - Настройки.
//  Параметр - Структура - Параметр.
//  Значение - Строка - Значение.
//  Использование - Булево - Признак использования.
//
// Возвращаемое значение:
//  ЗначениеПараметра - Значение параметра.
//
Функция УстановитьПараметрСКД(Настройки, Параметр, Значение, Использование)
	
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
	
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Использование = Использование;
		ЗначениеПараметра.Значение      = Значение;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции // УстановитьПараметрСКД()

&НаСервере
Функция ДобавитьЭлементПланировщика(Работа)
	
	Если Работа.Факт Тогда
		Если ЗначениеЗаполнено(Работа.ДатаОкончания) Тогда // работы завершены
			НовыйЭлемент = Планировщик.Элементы.Добавить(Работа.ДатаНачала, Работа.ДатаОкончания);
			НовыйЭлемент.ЦветФона = WebЦвета.СветлоКоричневый;
		Иначе // работы начаты	
			НовыйЭлемент = Планировщик.Элементы.Добавить(Работа.ДатаНачала, Работа.ДатаНачала + 3600);
			НовыйЭлемент.ЦветФона = WebЦвета.СветлоЗеленый;
		КонецЕсли;
		НовыйЭлемент.Картинка = НазначитьКартинкуЭлементуПланировщика(Работа.ТипРемонта);
	Иначе // работы запланированы	
		НовыйЭлемент = Планировщик.Элементы.Добавить(Работа.ДатаНачала, Работа.ДатаОкончания);
		НовыйЭлемент.ЦветФона = WebЦвета.Желтый;
		НовыйЭлемент.Картинка = НазначитьКартинкуЭлементуПланировщика(Работа.ТипРемонта, Истина);
	КонецЕсли; 
	НовыйЭлемент.Значение = Работа.Документ;
	НовыйЭлемент.Текст    = Работа.ВидРемонтаПредставление; // + ", " + Работа.ТранспортноеСредство;
	
	Возврат НовыйЭлемент;
	
КонецФункции

&НаСервере 
Функция НазначитьКартинкуЭлементуПланировщика(ТипРемонта, План = Ложь)
	
	Если План Тогда
		Если ТипРемонта = Перечисления.упТипыРемонтов.Ремонт Тогда
			Возврат БиблиотекаКартинок.упРемонтПлан;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.ПлановоеТО Тогда
			Возврат БиблиотекаКартинок.упТехосмотрПлан;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.МонтажОборудования Тогда
			Возврат БиблиотекаКартинок.упМонтажПлан;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.МонтажПрицепов Тогда
			Возврат БиблиотекаКартинок.упМонтажПлан;	
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.ЗаменаШин Тогда
			Возврат БиблиотекаКартинок.упЗаменаШинПлан;
		КонецЕсли; 
	Иначе
		Если ТипРемонта = Перечисления.упТипыРемонтов.Ремонт Тогда
			Возврат БиблиотекаКартинок.упРемонт;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.ПлановоеТО Тогда
			Возврат БиблиотекаКартинок.упТехосмотр;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.МонтажОборудования Тогда
			Возврат БиблиотекаКартинок.упМонтаж;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.МонтажПрицепов Тогда
			Возврат БиблиотекаКартинок.упМонтаж;
		ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.ЗаменаШин Тогда
			Возврат БиблиотекаКартинок.упЗаменаШин;
		КонецЕсли; 
	КонецЕсли;
	
КонецФункции

&НаСервере 
Процедура УстановитьИнтервалыФонаПоГрафикуРаботы(Начало = Неопределено, Конец = Неопределено)
	
	Планировщик.ИнтервалыФона.Очистить();
	
	Если Начало = Неопределено Или Конец = Неопределено Тогда
		ПериодОтображения = Планировщик.ТекущиеПериодыОтображения[0];
		Начало = ПериодОтображения.Начало;
		Конец = ПериодОтображения.Конец;
	КонецЕсли; 
	
	тзРасписание = ГрафикиРаботы.РасписанияРаботыНаПериод(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ГрафикРаботы), Начало, Конец);
	Если тзРасписание.Количество() Тогда
		ИнтервалыФона = Планировщик.ИнтервалыФона;
		Для каждого текПериод Из тзРасписание Цикл
			НовыйИнтервал = ИнтервалыФона.Добавить(текПериод.ДатаГрафика + Час(текПериод.ВремяНачала)*3600 + Минута(текПериод.ВремяНачала)*60, текПериод.ДатаГрафика + Час(текПериод.ВремяОкончания)*3600 + Минута(текПериод.ВремяОкончания)*60);
			НовыйИнтервал.Цвет = WebЦвета.Роса;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция НазначитьКартинкуДокументуРемонт(ИндексСтатуса)
	
	Картинка = Неопределено;
	Если ИндексСтатуса = 0 Тогда
		Картинка = БиблиотекаКартинок.упСтатусНовый;
	ИначеЕсли ИндексСтатуса = 1 Тогда
		Картинка = БиблиотекаКартинок.упСтатусКВыполнению;
	ИначеЕсли ИндексСтатуса = 2 Тогда
		Картинка = БиблиотекаКартинок.упСтатусВыполняется;
	ИначеЕсли ИндексСтатуса = 3 Тогда
		Картинка = БиблиотекаКартинок.упСтатусВыполнена;
	ИначеЕсли ИндексСтатуса = 4 Тогда
		Картинка = БиблиотекаКартинок.упСтатусОтменено;	
	КонецЕсли; 
	Возврат Картинка;
	
КонецФункции

&НаСервере
Процедура ИзменитьВремяРемонтаНаСервере(Ремонт, НачалоПериода, КонецПериода, МестоРемонта, ТекстОшибки)
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Ремонт,, УникальныйИдентификатор);
	Исключение
		ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Ремонт);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	НачатьТранзакцию();
	
	РемонтОбъект = Ремонт.ПолучитьОбъект();
	
	Если ТипЗнч(Ремонт) = Тип("ДокументСсылка.упРемонтИТОТС") И РемонтОбъект.Проведен Тогда
		РемонтОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		РемонтОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРемонтаИТОТС.Запланирован);
	КонецЕсли;
	
	РемонтОбъект.НачалоРемонтаПлан = НачалоПериода;
	РемонтОбъект.ОкончаниеРемонтаПлан = КонецПериода;
	РемонтОбъект.Гараж = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МестоРемонта, "Владелец");
	РемонтОбъект.МестоРемонта = МестоРемонта;
	Попытка
		РемонтОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
	КонецПопытки; 
	
	Если ТранзакцияАктивна() Тогда
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;	
	
	РазблокироватьДанныеДляРедактирования(Ремонт, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	СхемаКомпоновкиДанных = Обработки.упРМПланированиеРемонтов.ПолучитьМакет("РемонтныеРаботы");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры // ИнициализироватьКомпоновщикНастроек()

&НаСервере
Функция ПолучитьСтатусРемонтаНаСервере(Ремонт)
	
	текСтатус = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Ремонт);
	Возврат текСтатус;
	
КонецФункции

&НаСервере
Процедура ПеревестиРемонтВСтатусНовыйНаСервере(Ремонт, Отказ)
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Ремонт,, УникальныйИдентификатор);
	Исключение
		ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Ремонт);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		Возврат;
	КонецПопытки;
	
	НачатьТранзакцию();
	
	текРемонтОбъект = Ремонт.ПолучитьОбъект();
	
	Если текРемонтОбъект.Проведен Тогда
		текРемонтОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЕсли;
	
	Если ТипЗнч(текРемонтОбъект) = Тип("ДокументОбъект.упЗаявкаНаРемонтИТОТС") Тогда
	//	текРемонтОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРемонтаИТОТС.Новый);
	//Иначе
		текРемонтОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая)	КонецЕсли;
		
	Попытка
		текРемонтОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение 
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда 
			ТекстСообщения = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		Возврат;
	КонецПопытки;
	
	Если ТранзакцияАктивна() Тогда
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;	
	
	Если Не Отказ Тогда
		ИскомыеСтроки = СписокЭлементовПланировщика.НайтиСтроки(Новый Структура("Документ", Ремонт));
		Для каждого текСтрока Из ИскомыеСтроки Цикл
			СписокЭлементовПланировщика.Удалить(текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
	РазблокироватьДанныеДляРедактирования(Ремонт, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗапланироватьРемонтНаСервере(Ремонт, СтруктураПараметров)
	
	Отказ = Ложь;
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Ремонт,, УникальныйИдентификатор);
	Исключение
		ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Ремонт);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		Возврат;
	КонецПопытки;
	
	сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ремонт, Новый Структура("ВремяРаботПлан, ТипРемонта","ВидРемонта.ВремяРаботПлан", "ВидРемонта.ТипРемонта"));
	Если ЗначениеЗаполнено(сткДанные.ВремяРаботПлан) Тогда
		//СтруктураПараметров.НачалоРемонтаФакт = СтруктураПараметров.НачалоРемонтаПлан + сткДанные.ВремяРаботПлан*3600;
		СтруктураПараметров.ОкончаниеРемонтаПлан = СтруктураПараметров.НачалоРемонтаПлан + сткДанные.ВремяРаботПлан*3600;
		Если СтруктураПараметров.ОкончаниеРемонтаПлан = НачалоЧаса(СтруктураПараметров.ОкончаниеРемонтаПлан) Тогда
			СтруктураПараметров.ОкончаниеРемонтаПлан = СтруктураПараметров.ОкончаниеРемонтаПлан - 1;			
		КонецЕсли;
	КонецЕсли;
	
	текРемонтОбъект = Ремонт.ПолучитьОбъект();
	ЗаполнитьЗначенияСвойств(текРемонтОбъект, СтруктураПараметров);
	Если НЕ ЗначениеЗаполнено(текРемонтОбъект.ВидРемонта) И СтруктураПараметров.Свойство("МестоРемонта") Тогда 
		Если ЗначениеЗаполнено(СтруктураПараметров.МестоРемонта)  Тогда 
			текРемонтОбъект.ВидРемонта = СтруктураПараметров.МестоРемонта.ВидРемонтаПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	Если ТипЗнч(текРемонтОбъект) = Тип("ДокументОбъект.упРемонтИТОТС") Тогда
		текРемонтОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРемонтаИТОТС.Запланирован);
	Иначе
		текРемонтОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению);
	КонецЕсли;
	
	Попытка
		текРемонтОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение 
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда 
			ТекстСообщения = ОписаниеОшибки();
		КонецЕсли;
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Отказ = Истина;
		ЗаписьЖурналаРегистрации("РМ Планирование ремонтов:", УровеньЖурналаРегистрации.Ошибка, , ,ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	Если Не Отказ Тогда
		
		НовыйЭлемент = Планировщик.Элементы.Добавить(текРемонтОбъект.НачалоРемонтаПлан, текРемонтОбъект.ОкончаниеРемонтаПлан);
		НовыйЭлемент.ЦветФона = WebЦвета.Желтый;
		НовыйЭлемент.Картинка = НазначитьКартинкуЭлементуПланировщика(сткДанные.ТипРемонта, Истина);
		НовыйЭлемент.Значение = Ремонт;
		НовыйЭлемент.Текст = текРемонтОбъект.ВидРемонта;
		НовыйЭлемент.ЗначенияИзмерений = СтруктураПараметров.Значения;
		
		НоваяСтрока = СписокЭлементовПланировщика.Добавить();
		НоваяСтрока.Документ = Ремонт;
		НоваяСтрока.ТранспортноеСредство = текРемонтОбъект.ТранспортноеСредство;
		НоваяСтрока.ВидРемонта = текРемонтОбъект.ВидРемонта;
		НоваяСтрока.Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован;
		НоваяСтрока.ИндексСтатуса = 1;
	
	КонецЕсли; 
	
	РазблокироватьДанныеДляРедактирования(Ремонт, УникальныйИдентификатор);
	
КонецПроцедуры                                                           

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров управления ремонтами.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыПланированиеРемонтовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		
		ПараметрыПланированиеРемонтов = Результат;
		упВариантыНастроекВызовСервера.СохранитьВариантНастроек(Результат, "Обработка.упРМПланированиеРемонтов.ПараметрыПланированиеРемонтов", ТекущийПользователь);
		
		СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ПустаяСсылка");
		
		Если НЕ ПараметрыПланированиеРемонтов = Неопределено Тогда
			СпособПланированияРемонтныхРабот = ПараметрыПланированиеРемонтов.СпособПланированияРемонтныхРабот;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРемонтов, "СпособПланированияРемонтныхРабот", СпособПланированияРемонтныхРабот, Истина);
		
	КонецЕсли;
    
КонецПроцедуры // УстановитьПараметрыПланированиеРемонтовЗавершение()

&НаКлиенте
// Процедура завершения после ввода даты.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВводДатыОтображенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Если Элементы.ФормаУстановитьИнтервалДень.Пометка Тогда
			УстановитьИнтервалДеньСервер(Результат);
		ИначеЕсли Элементы.ФормаУстановитьИнтервалНеделя.Пометка Тогда
			УстановитьИнтервалНеделяСервер(Результат);
		ИначеЕсли Элементы.ФормаУстановитьИнтервалМесяц.Пометка Тогда
			УстановитьИнтервалМесяцСервер(Результат);
		КонецЕсли;
		
		ОбновитьДанныеПланировщикаНаСервере();
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрыия формы "ФормаНастройкиМестРемонтов".
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НастройкаМестРемонтовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Результат.Настройки);
		ОбновитьДанныеПланировщикаНаСервере();
	КонецЕсли;
    
КонецПроцедуры // НастройкаЗаданийНаПеревозкуЗавершение()

&НаКлиенте
// Процедура завершения после закрыия формы объекта документа "упРемонтИТОТС".
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СозданиеРемонтаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
// Выполним обращение к данным для получения порядкового номера.
//
// Параметры:
//  СтатусыРемонтаИТОТС  - Перечисление.упСтатусыРемонтаИТОТС - Ссылка на значение.
//
// Возвращаемое значение:
//   Число   - Порядковый номер.
//
Функция ПолучитьПорядокСтатусаРемонта(СтатусыРемонтаИТОТС)
	
	Результат = -1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтатусыРемонтаИТОТС.Порядок КАК Порядок
	|ИЗ
	|	Перечисление.упСтатусыРемонтаИТОТС КАК упСтатусыРемонтаИТОТС
	|ГДЕ
	|	упСтатусыРемонтаИТОТС.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СтатусыРемонтаИТОТС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат = Выборка.Порядок;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьПорядокСтатусаРемонта()

&НаКлиенте
Процедура СписокРемонтовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.СписокРемонтов.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		Если ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда
			ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.Ссылка), ЭтаФорма);
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда
			ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.Ссылка), ЭтаФорма);	
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#КонецОбласти
