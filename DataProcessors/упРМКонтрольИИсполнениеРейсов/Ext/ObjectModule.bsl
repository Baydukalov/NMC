#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
// Нет.
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
	
#Область Планировщик

// Процедура обработки табличной части "Рейсы".
//
Процедура ЗаполнитьДанныеПланировщика(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанныхОтбор, НастройкиКомпоновкиДанныхПорядок, НастройкиПараметрыДанных) Экспорт
	
	ЗаполнитьСписокРейсов(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанныхОтбор, НастройкиКомпоновкиДанныхПорядок, НастройкиПараметрыДанных);
	ЗаполнитьМаршрутыПоРейсам();
	
КонецПроцедуры

Процедура ЗаполнитьСписокРейсов(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанныхОтбор, НастройкиКомпоновкиДанныхПорядок, НастройкиПараметрыДанных)
	
	Рейсы.Очистить();
	
	НастройкиПоУмолчанию 		= СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	КоллекцияЭлементовОтбора	= НастройкиПоУмолчанию.Отбор.Элементы;
	КоллекцияЭлементовОтбора.Очистить();
	ЗаполнитьЭлементыОтбора(КоллекцияЭлементовОтбора, НастройкиКомпоновкиДанныхОтбор);

	КоллекцияЭлементовПорядка	= НастройкиПоУмолчанию.Порядок.Элементы;
	КоллекцияЭлементовПорядка.Очистить();
	Для каждого ЭлементПорядка Из НастройкиКомпоновкиДанныхПорядок.Элементы Цикл
		НовыйЭлемент = КоллекцияЭлементовПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементПорядка);
	КонецЦикла;
	
	КоллекцияПараметров	= НастройкиПоУмолчанию.ПараметрыДанных.Элементы;
	КоллекцияПараметров.Очистить();
	Для каждого ЭлементПараметр Из НастройкиПараметрыДанных.Элементы Цикл
		НовыйЭлемент = КоллекцияПараметров.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементПараметр);
	КонецЦикла;
	
	// Выбранные поля 
	ВыбранныеПоля = НастройкиПоУмолчанию.Выбор.Элементы;
	
	ВыбранноеПоле = ВыбранныеПоля.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("Ссылка");
	
	ВыбранноеПоле = ВыбранныеПоля.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ТранспортноеСредство");
	
	ВыбранноеПоле = ВыбранныеПоля.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("Номер");
		
	Структура 	  	= НастройкиПоУмолчанию.Структура;
	Группировка 	= Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	
	ВыбранныеПоля 	= Группировка.Выбор.Элементы;
	ВыбранноеПоле 	= ВыбранныеПоля.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Использование = Истина;
	
	ПоляПорядка		= Группировка.Порядок.Элементы;
	ПолеПорядка		= ПоляПорядка.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	ПолеПорядка.Использование = Истина;
			
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(НастройкиПоУмолчанию);
		
	// Отключение вывода общих итогов.
	КомпоновщикНастроекКомпоновкиДанных.Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("ГоризонтальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	КомпоновщикНастроекКомпоновкиДанных.Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
		
	// Компоновка макета.
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
	// Инициализация процессора компоновки.
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
		
	// Таблица значений, в которую будет получен результат.
	тбзРезультат = Новый ТаблицаЗначений;
		
	// Получение результата.
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(тбзРезультат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	й_Шаг = 0;
	Для каждого СтрокаРезультат Из тбзРезультат Цикл
		НоваяСтрока = Рейсы.Добавить();
		НоваяСтрока.Номер 					= СтрокаРезультат.Номер;
		НоваяСтрока.Рейс 					= СтрокаРезультат.Ссылка;
		НоваяСтрока.ТранспортноеСредство 	= СтрокаРезультат.ТранспортноеСредство;
		НоваяСтрока.МинНачалоПериода 		= ВерхняяГраницаПериодов();
		НоваяСтрока.МаксКонецПериода 		= НижняяГраницаПериодов();
		НоваяСтрока.СтрокаНомер				= й_Шаг;
		й_Шаг = й_Шаг + 1;
	КонецЦикла;
		
КонецПроцедуры

Процедура ЗаполнитьЭлементыОтбора(НоваяКоллекцияЭлементовОтбора, ГруппаЭлементовОтбора)
	
	Для каждого ЭлементОтбора Из ГруппаЭлементовОтбора.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			НовыйЭлемент = НоваяКоллекцияЭлементовОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));	
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементОтбора);
			ЗаполнитьЭлементыОтбора(НовыйЭлемент.Элементы, ЭлементОтбора); 
		Иначе	
			НовыйЭлемент = НоваяКоллекцияЭлементовОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
		
КонецПроцедуры

Процедура ЗаполнитьМаршрутыПоРейсам()
	
	МаршрутыПоРейсам.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзРейсы.Рейс,
	               |	тбзРейсы.СтрокаНомер
	               |ПОМЕСТИТЬ втРейсы
	               |ИЗ
	               |	&тбзРейсы КАК тбзРейсы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упПрохождениеТочки.ИдентификаторТочки,
	               |	упПрохождениеТочки.Ссылка,
	               |	упПрохождениеТочки.Дата,
	               |	ВЫБОР
	               |		КОГДА упПрохождениеТочки.Проведен
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Проведен,
	               |	упПрохождениеТочки.Рейс
	               |ПОМЕСТИТЬ втДокументыПрохождениеТочки
	               |ИЗ
	               |	Документ.упПрохождениеТочки КАК упПрохождениеТочки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	               |		ПО упПрохождениеТочки.Ссылка = втРейсы.Рейс
	               |ГДЕ
	               |	НЕ упПрохождениеТочки.ПометкаУдаления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДокументыПрохождениеТочки.ИдентификаторТочки,
	               |	МАКСИМУМ(втДокументыПрохождениеТочки.Дата) КАК Дата,
	               |	МАКСИМУМ(втДокументыПрохождениеТочки.Проведен) КАК Проведен,
	               |	втДокументыПрохождениеТочки.Рейс
	               |ПОМЕСТИТЬ втПараметрыВыбораДокументаПрохождениеТочки
	               |ИЗ
	               |	втДокументыПрохождениеТочки КАК втДокументыПрохождениеТочки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втДокументыПрохождениеТочки.ИдентификаторТочки,
	               |	втДокументыПрохождениеТочки.Рейс
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(втДокументыПрохождениеТочки.Ссылка) КАК Ссылка,
	               |	втДокументыПрохождениеТочки.ИдентификаторТочки,
	               |	втПараметрыВыбораДокументаПрохождениеТочки.Рейс
	               |ПОМЕСТИТЬ втВыбранныеДокументы
	               |ИЗ
	               |	втПараметрыВыбораДокументаПрохождениеТочки КАК втПараметрыВыбораДокументаПрохождениеТочки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДокументыПрохождениеТочки КАК втДокументыПрохождениеТочки
	               |		ПО втПараметрыВыбораДокументаПрохождениеТочки.ИдентификаторТочки = втДокументыПрохождениеТочки.ИдентификаторТочки
	               |			И втПараметрыВыбораДокументаПрохождениеТочки.Рейс = втДокументыПрохождениеТочки.Рейс
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втДокументыПрохождениеТочки.ИдентификаторТочки,
	               |	втПараметрыВыбораДокументаПрохождениеТочки.Рейс
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбМаршрут.Рейс КАК Рейс,
	               |	тбМаршрут.Адрес КАК Адрес,
	               |	тбМаршрут.Идентификатор КАК Идентификатор,
	               |	тбМаршрут.ТипТочки КАК ТипТочки,
	               |	тбМаршрут.СтрокаНомер КАК СтрокаНомер,
	               |	тбМаршрут.Пройдена КАК Пройдена,
	               |	тбМаршрут.Адрес.Представление КАК АдресПредставление,
	               |	ВЫБОР
	               |		КОГДА тбМаршрут.ПрибытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА тбМаршрут.ПлановоеПрибытие
	               |		ИНАЧЕ тбМаршрут.ПрибытиеФакт
	               |	КОНЕЦ КАК ПлановоеПрибытие,
	               |	ВЫБОР
	               |		КОГДА тбМаршрут.НачалоРаботФакт = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА тбМаршрут.НачалоРаботПлан
	               |		ИНАЧЕ тбМаршрут.НачалоРаботФакт
	               |	КОНЕЦ КАК НачалоРаботПлан,
	               |	ВЫБОР
	               |		КОГДА тбМаршрут.УбытиеФакт = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА тбМаршрут.ПлановоеУбытие
	               |		ИНАЧЕ тбМаршрут.УбытиеФакт
	               |	КОНЕЦ КАК ПлановоеУбытие,
	               |	тбМаршрут.ПрибытиеФакт КАК ПрибытиеФакт,
	               |	тбМаршрут.НачалоРаботФакт КАК НачалоРаботФакт,
	               |	тбМаршрут.УбытиеФакт КАК УбытиеФакт,
	               |	ЕСТЬNULL(тбПрохождениеТочки.Ссылка, ЗНАЧЕНИЕ(ДОКУМЕНТ.упПрохождениеТочки.ПустаяСсылка)) КАК ПрохождениеТочки
	               |ИЗ
	               |	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	               |			,
	               |			Рейс В
	               |				(ВЫБРАТЬ
	               |					втРейсы.Рейс
	               |				ИЗ
	               |					втРейсы КАК втРейсы)) КАК тбМаршрут
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втВыбранныеДокументы КАК тбПрохождениеТочки
	               |		ПО тбМаршрут.Рейс = тбПрохождениеТочки.Рейс
	               |			И тбМаршрут.Идентификатор = тбПрохождениеТочки.ИдентификаторТочки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	               |		ПО тбМаршрут.Рейс = втРейсы.Рейс
	               |ГДЕ
	               |	НЕ тбМаршрут.Отменена
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	втРейсы.СтрокаНомер,
	               |	СтрокаНомер
	               |ИТОГИ ПО
	               |	Рейс";				   
	Запрос.УстановитьПараметр("тбзРейсы", Рейсы.Выгрузить(,"Рейс, СтрокаНомер"));
	
	НачалоПериодаПланировщика 	= ВерхняяГраницаПериодов();
	КонецПериодаПланировщика 	= НижняяГраницаПериодов();
	текДатаСеанса				= ТекущаяДатаСеанса();

	// Заполняются периоды в точках маршрута
	текВыборкаРейс	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока текВыборкаРейс.Следующий() Цикл
		текВыборкаТочка = текВыборкаРейс.Выбрать();
		
		НачалоПериодаПоРейсу 	= ВерхняяГраницаПериодов();
		КонецПериодаПоРейсу 	= НижняяГраницаПериодов();
		КоличествоТочекПоРейсу	= 0;
		
		Пока текВыборкаТочка.Следующий() Цикл
			КоличествоТочекПоРейсу = КоличествоТочекПоРейсу + 1;
			НоваяСтрокаТочка = МаршрутыПоРейсам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТочка, текВыборкаТочка);
			
			сткПериодПлан = ПериодТочки(текВыборкаТочка.ПлановоеПрибытие, текВыборкаТочка.НачалоРаботПлан, текВыборкаТочка.ПлановоеУбытие);
			НоваяСтрокаТочка.НачалоПериодаПлан 	= сткПериодПлан.ПериодНачало;
			НоваяСтрокаТочка.СтрокаНомер		= КоличествоТочекПоРейсу;
			НоваяСтрокаТочка.КонецПериодаПлан 	= сткПериодПлан.ПериодОкончание;
			
			сткПериодФакт = ПериодТочки(текВыборкаТочка.ПрибытиеФакт, текВыборкаТочка.НачалоРаботФакт, текВыборкаТочка.УбытиеФакт);
			НоваяСтрокаТочка.НачалоПериодаФакт	= сткПериодФакт.ПериодНачало;
			НоваяСтрокаТочка.КонецПериодаФакт	= сткПериодФакт.ПериодОкончание;
			
			Если НЕ НоваяСтрокаТочка.НачалоПериодаПлан = НижняяГраницаПериодов() Тогда
				НачалоПериодаПоРейсу = Мин(НачалоПериодаПоРейсу, НоваяСтрокаТочка.НачалоПериодаПлан);
			КонецЕсли;
			
			Если ОтображатьФакт И НЕ НоваяСтрокаТочка.НачалоПериодаФакт = НижняяГраницаПериодов() Тогда
				НачалоПериодаПоРейсу = Мин(НачалоПериодаПоРейсу, НоваяСтрокаТочка.НачалоПериодаФакт);	
			КонецЕсли;
			
			Если НЕ НоваяСтрокаТочка.КонецПериодаПлан = НижняяГраницаПериодов() Тогда
				КонецПериодаПоРейсу = Макс(КонецПериодаПоРейсу, НоваяСтрокаТочка.КонецПериодаПлан);
			КонецЕсли;
			
			Если ОтображатьФакт И НЕ НоваяСтрокаТочка.КонецПериодаФакт = НижняяГраницаПериодов() Тогда
				КонецПериодаПоРейсу = Макс(КонецПериодаПоРейсу, НоваяСтрокаТочка.КонецПериодаФакт);	
			КонецЕсли;
		КонецЦикла;
		
		мсвСтрокиРейса = Рейсы.НайтиСтроки(Новый Структура("Рейс",текВыборкаРейс.Рейс));
		Если мсвСтрокиРейса.Количество() > 0 Тогда
			мсвСтрокиРейса[0].МинНачалоПериода 			= НачалоПериодаПоРейсу;
			мсвСтрокиРейса[0].МаксКонецПериода 			= КонецПериодаПоРейсу;
			мсвСтрокиРейса[0].КоличествоТочекПоРейсу	= КоличествоТочекПоРейсу;
		КонецЕсли;
		
		НачалоПериодаПланировщика 	= Мин(НачалоПериодаПланировщика, НачалоПериодаПоРейсу);
		КонецПериодаПланировщика 	= Макс(КонецПериодаПланировщика, КонецПериодаПоРейсу);
				
	КонецЦикла;
	
	Если НачалоПериодаПланировщика = ВерхняяГраницаПериодов() Тогда
		НачалоПериодаПланировщика = НачалоДня(текДатаСеанса);
	Иначе
		НачалоПериодаПланировщика = НачалоДня(НачалоПериодаПланировщика);
	КонецЕсли;
	
	Если КонецПериодаПланировщика = НижняяГраницаПериодов() Тогда
		КонецПериодаПланировщика = КонецДня(текДатаСеанса);
	Иначе
		КонецПериодаПланировщика = КонецДня(КонецПериодаПланировщика);
	КонецЕсли;

	Для каждого СтрокаТочка Из МаршрутыПоРейсам Цикл
		
		СтрокаТочка.СтатусТочки = "БезСтатуса";
		
		Если СтрокаТочка.Пройдена Тогда
			СтрокаТочка.СтатусТочки = "Выполнена";
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаТочка.НачалоПериодаФакт) 
			И ЗначениеЗаполнено(СтрокаТочка.КонецПериодаФакт) Тогда
			
			Если ЗначениеЗаполнено(СтрокаТочка.КонецПериодаПлан) 
				И СтрокаТочка.КонецПериодаПлан < СтрокаТочка.КонецПериодаФакт Тогда
				СтрокаТочка.СтатусТочки = "Опоздание";
			КонецЕсли;		
			
		Иначе
			Если ЗначениеЗаполнено(СтрокаТочка.КонецПериодаПлан) 
				И СтрокаТочка.КонецПериодаПлан < текДатаСеанса Тогда
				СтрокаТочка.СтатусТочки = "Опоздание";
			КонецЕсли;	
		КонецЕсли;
			
		Если СтрокаТочка.НачалоПериодаПлан = НижняяГраницаПериодов() Тогда
			СтрокаТочка.НачалоПериодаПлан = НачалоПериодаПланировщика;
		КонецЕсли;
		
		Если СтрокаТочка.КонецПериодаПлан = НижняяГраницаПериодов() Тогда
			СтрокаТочка.КонецПериодаПлан = КонецПериодаПланировщика;
		КонецЕсли;
			
		
	КонецЦикла;
	
	Для каждого СтрокаРейс Из Рейсы Цикл
		Если СтрокаРейс.МинНачалоПериода = ВерхняяГраницаПериодов() Тогда
			СтрокаРейс.МинНачалоПериода = НачалоПериодаПланировщика;
		КонецЕсли;
		Если СтрокаРейс.МаксКонецПериода = НижняяГраницаПериодов() Тогда
			СтрокаРейс.МаксКонецПериода = КонецПериодаПланировщика;
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры

Функция ВерхняяГраницаПериодов()
	Возврат '30000101';
КонецФункции

Функция НижняяГраницаПериодов()
	Возврат  упСервисныеФункцииКлиентСервер.НачалоОтсчета();	
КонецФункции

Функция ПериодТочки(Прибытие, НачалоРабот, Убытие)
	
	ПериодНачало 	= Неопределено;
	ПериодОкончание = Неопределено;
	
	Если ЗначениеЗаполнено(НачалоРабот) Тогда
		ПериодНачало = НачалоРабот;
	Иначе
		Если ЗначениеЗаполнено(Прибытие) Тогда
			ПериодНачало = Прибытие;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Убытие) Тогда
		ПериодОкончание = Убытие;
	КонецЕсли;

	сткВозврата = Новый Структура("ПериодНачало, ПериодОкончание", ПериодНачало, ПериодОкончание);
	Возврат сткВозврата;
КонецФункции

#Конецобласти

#Область ДеревоРаботыПоТекущемуРейсу

// Заполняет дерево работ по переданному рейсу. 
//
Процедура ЗаполнитьДеревоРаботПоРейсу(ИзменитьТолькоВыполненные = Ложь) Экспорт
	АдресаПогрузкиПоРейсу.Очистить();
	упПрохождениеТочки.ЗаполнитьДеревоРаботПоРейсу(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, ТекущийРейс, ИзменитьТолькоВыполненные);
КонецПроцедуры

// Заполняет таблицу адресов для погрузки.
//
Процедура ЗагрузитьАдресаПогрузкиПоРейсу() Экспорт
	Если АдресаПогрузкиПоРейсу.Количество()=0 Тогда
		АдресаПогрузкиПоРейсу = упПрохождениеТочки.ПолучитьАдресаПогрузкиПоРейсу(ТекущийРейс);
	КонецЕсли;
КонецПроцедуры

// Возвращает признак наличия изменений в дереве.
//
Функция ЕстьИзмененияВДеревеИсполненияРейсов() Экспорт
	Возврат упПрохождениеТочки.ЕстьИзмененияВДеревеИсполненияРейсов(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон);		
КонецФункции

// Сохраняем изменения в дереве точек.
//
Процедура ПрименитьИзмененияВДеревеТочекРейса(мсвИндексовИзмененныхТочек = Неопределено, ПерезаполнятьДеревоТочекРейса = Истина) Экспорт
	упПрохождениеТочки.ПрименитьИзмененияВДеревеТочекРейса(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, ТекущийРейс, мсвИндексовИзмененныхТочек, ПерезаполнятьДеревоТочекРейса,,ТоварныйСоставГруза, ИдентификаторТочкиСИзмененнымСоставомГрузов);
	ТоварныйСоставГруза.Очистить();
	ИдентификаторТочкиСИзмененнымСоставомГрузов = Неопределено;
КонецПроцедуры

// Процедура - Восстанивливает точку разгрузки.
//
Процедура ВосстановитьТочкуРазгрузки(сткВозврата) Экспорт
	упПрохождениеТочки.ВосстановитьТочкуРазгрузки(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, сткВозврата);
КонецПроцедуры

#Конецобласти

// Функция - Разрешено изменять товарный состав
// 
// Возвращаемое значение:
//  Булево - Истина, если разршено.
//
Функция НастройкиВидаПеревозки(дрвРаботыПоТекущемуРейсу) Экспорт
	
	РазрешеноИзменятьТоварныйСостав = Ложь;
	РазрешеноИзменятьВГХ = Ложь;
		
	сткЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущийРейс, "ВидПеревозки.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава, 
	                                                                                |ВидПеревозки.РазрешеноДосрочноеУбытиеИзТочкиМаршрута");
	
	РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава	= сткЗначенияРеквизитов.ВидПеревозкиРазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава;
	РазрешеноДосрочноеУбытиеИзТочкиМаршрута	= сткЗначенияРеквизитов.ВидПеревозкиРазрешеноДосрочноеУбытиеИзТочкиМаршрута;
	РазрешеноИзменятьВГХ = ПолучитьФункциональнуюОпцию("упРазрешитьИзменениеПараметровГрузовПриИсполненииРейса");	
	
	Если Ложь
		Или РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава
		Или РазрешеноИзменятьВГХ
	Тогда
		
		ЭтоОсновнаяОперацияДляВидаГрузаГруз = Ложь;
		
		ЭлементыДерева = дрвРаботыПоТекущемуРейсу.Строки;
		Для каждого текТочка Из ЭлементыДерева Цикл
			ЭлементыЗадания = текТочка.Строки;
			Для каждого текЗадание Из ЭлементыЗадания Цикл
				ЭлементыОперации = текЗадание.Строки;
				Для каждого текОперация Из ЭлементыОперации Цикл
					ЭтоОсновнаяОперацияДляВидаГрузаГруз = упПрохождениеТочкиКлиентСервер.ОсновнаяОперацияДляВидаГрузаГруз(текОперация.НазначениеСтроки);
					Если ЭтоОсновнаяОперацияДляВидаГрузаГруз Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ЭтоОсновнаяОперацияДляВидаГрузаГруз Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ЭтоОсновнаяОперацияДляВидаГрузаГруз Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		РазрешеноИзменятьТоварныйСостав = Истина 
									И РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава
									И ЭтоОсновнаяОперацияДляВидаГрузаГруз;
									
		РазрешеноИзменятьВГХ = Истина
							И РазрешеноИзменятьВГХ 
							И ЭтоОсновнаяОперацияДляВидаГрузаГруз;
		
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("РазрешеноИзменятьТоварныйСостав", РазрешеноИзменятьТоварныйСостав);
	Результат.Вставить("РазрешеноИзменятьВГХ", РазрешеноИзменятьВГХ);
	Результат.Вставить("РазрешеноДосрочноеУбытиеИзТочкиМаршрута", РазрешеноДосрочноеУбытиеИзТочкиМаршрута);
	
	Возврат Результат;
	
КонецФункции 

#КонецОбласти

#КонецЕсли