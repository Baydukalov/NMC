#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	мсвМаршрут = Параметры.Маршрут;
	Если ТипЗнч(мсвМаршрут) = Тип("Массив") Тогда
		Для каждого Элемент Из мсвМаршрут Цикл
			НоваяСтрока = Маршрут.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
			НоваяСтрока.ПрибытиеФактТекущее = НоваяСтрока.ПрибытиеФакт;
			НоваяСтрока.НачалоРаботФактТекущее = НоваяСтрока.НачалоРаботФакт;
			НоваяСтрока.УбытиеФактТекущее = НоваяСтрока.УбытиеФакт;
			Если Истина
				И НЕ НоваяСтрока.Выполнена
				И НЕ НоваяСтрока.Отменена Тогда
				НоваяСтрока.Пометка = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура МаршрутПрибытиеПриИзменении(Элемент)
	ТекущиеДанные 		= Элементы.Маршрут.ТекущиеДанные;
	ИндексТекущейСтроки = Маршрут.Индекс(ТекущиеДанные); 	
	ПересчитатьВремя(ИндексТекущейСтроки, "ПрибытиеФакт");
КонецПроцедуры

&НаКлиенте
Процедура МаршрутНачалоРаботПриИзменении(Элемент)
	ТекущиеДанные 		= Элементы.Маршрут.ТекущиеДанные;
	ИндексТекущейСтроки = Маршрут.Индекс(ТекущиеДанные); 	
	ПересчитатьВремя(ИндексТекущейСтроки, "НачалоРаботФакт");
КонецПроцедуры

&НаКлиенте
Процедура МаршрутУбытиеПриИзменении(Элемент)
	ТекущиеДанные 		= Элементы.Маршрут.ТекущиеДанные;
	ИндексТекущейСтроки = Маршрут.Индекс(ТекущиеДанные); 	
	ПересчитатьВремя(ИндексТекущейСтроки, "УбытиеФакт");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	мсвСтрокиМаршрута = Новый Массив;
	сткСтрока = Новый Структура("Пометка,
							|ПрибытиеФакт,
							|НачалоРаботФакт,
							|УбытиеФакт");
	Для каждого Строка Из  Маршрут Цикл
		
		сткНоваяСтрока = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткСтрока);
		ЗаполнитьЗначенияСвойств(сткНоваяСтрока, Строка);
		сткНоваяСтрока.ПрибытиеФакт 		= Строка.ПрибытиеФактТекущее;
		сткНоваяСтрока.НачалоРаботФакт 	= Строка.НачалоРаботФактТекущее;
		сткНоваяСтрока.УбытиеФакт 		= Строка.УбытиеФактТекущее;
		мсвСтрокиМаршрута.Добавить(сткНоваяСтрока);
	
	КонецЦикла;

	Закрыть(мсвСтрокиМаршрута);
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для каждого Строка Из Маршрут Цикл
		Если НЕ Строка.Выполнена И НЕ Строка.Отменена Тогда
			Строка.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для каждого Строка Из Маршрут Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ПересчитатьВремя(ИндексСтроки, ИмяПоля)
	
	СтрокаОтсчета	= Маршрут.Получить(ИндексСтроки);
	Разница 		= СтрокаОтсчета[ИмяПоля + "Текущее"] -  СтрокаОтсчета[ИмяПоля];
	мсвПомеченныеСтроки 	 = Маршрут.НайтиСтроки(Новый Структура("Пометка",Истина));
	КоличествоПомеченныхСтрок = мсвПомеченныеСтроки.Количество();
	
	ОграничениеНаДатуДляСтроки = упСервисныеФункцииВызовСервера.ДатаСеанса() - КоличествоПомеченныхСтрок;
	
	Отказ = Ложь;
	
	Если ИмяПоля = "ПрибытиеФакт" Тогда
		
		Если СтрокаОтсчета.ПрибытиеФактТекущее > ОграничениеНаДатуДляСтроки Тогда
			ТекстСообщения = Нстр("ru = 'Введенная дата является датой будущего периода, дата не должна превышать %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОграничениеНаДатуДляСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Маршрут", ИндексСтроки + 1, "ПрибытиеФактТекущее"),,Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаОтсчета.НачалоРаботФактТекущее = СтрокаОтсчета.НачалоРаботФакт + Разница;
		
		Если СтрокаОтсчета.НачалоРаботФактТекущее < СтрокаОтсчета.ПрибытиеФактТекущее Тогда
			СтрокаОтсчета.НачалоРаботФактТекущее = СтрокаОтсчета.ПрибытиеФактТекущее;
		КонецЕсли;
		
		Если СтрокаОтсчета.НачалоРаботФактТекущее > ОграничениеНаДатуДляСтроки Тогда
			СтрокаОтсчета.НачалоРаботФактТекущее = ОграничениеНаДатуДляСтроки;
		КонецЕсли;
		
		СтрокаОтсчета.УбытиеФактТекущее = СтрокаОтсчета.УбытиеФакт + Разница;
		
		Если СтрокаОтсчета.УбытиеФактТекущее < СтрокаОтсчета.НачалоРаботФактТекущее Тогда
			СтрокаОтсчета.УбытиеФактТекущее = СтрокаОтсчета.НачалоРаботФактТекущее;
		КонецЕсли;
		
		Если СтрокаОтсчета.УбытиеФактТекущее > ОграничениеНаДатуДляСтроки Тогда
			СтрокаОтсчета.УбытиеФактТекущее = ОграничениеНаДатуДляСтроки;
		КонецЕсли;

		
	ИначеЕсли ИмяПоля = "НачалоРаботФакт" Тогда
		
		Если СтрокаОтсчета.НачалоРаботФактТекущее > ОграничениеНаДатуДляСтроки Тогда
			ТекстСообщения = Нстр("ru = 'Введенная дата является датой будущего периода, дата не должна превышать %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОграничениеНаДатуДляСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Маршрут", ИндексСтроки + 1, "НачалоРаботФактТекущее"),,Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаОтсчета.УбытиеФактТекущее = СтрокаОтсчета.УбытиеФакт + Разница;
		
		Если СтрокаОтсчета.УбытиеФактТекущее < СтрокаОтсчета.НачалоРаботФактТекущее Тогда
			СтрокаОтсчета.УбытиеФактТекущее = СтрокаОтсчета.НачалоРаботФактТекущее;
		КонецЕсли;
		
		Если СтрокаОтсчета.УбытиеФактТекущее > ОграничениеНаДатуДляСтроки Тогда
			СтрокаОтсчета.УбытиеФактТекущее = ОграничениеНаДатуДляСтроки;
		КонецЕсли;
	
	Иначе
		
		Если СтрокаОтсчета.УбытиеФактТекущее > ОграничениеНаДатуДляСтроки Тогда
			ТекстСообщения = Нстр("ru = 'Введенная дата является датой будущего периода, дата не должна превышать %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОграничениеНаДатуДляСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Маршрут", ИндексСтроки + 1, "УбытиеФактТекущее"),,Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	ОграничениеНаДатуДляСтроки  = ОграничениеНаДатуДляСтроки + 1;
	УбытиеФактИзПредыдущейТочки = СтрокаОтсчета.УбытиеФактТекущее;
	
	Если НЕ Разница = 0 Тогда
		
		Для ТекущийИндексСтроки = ИндексСтроки + 1 По Маршрут.Количество() - 1 Цикл
								
			Строка = Маршрут.Получить(ТекущийИндексСтроки);
			
			Если НЕ Строка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Строка.ПрибытиеФактТекущее 	= Строка.ПрибытиеФакт + Разница;
			Строка.НачалоРаботФактТекущее = Строка.НачалоРаботФакт + Разница;
			Строка.УбытиеФактТекущее 	= Строка.УбытиеФакт + Разница;
			
			Если УбытиеФактИзПредыдущейТочки >= Строка.ПрибытиеФактТекущее Тогда
				Строка.ПрибытиеФактТекущее = УбытиеФактИзПредыдущейТочки + 1;
			КонецЕсли;
			
			Если Строка.ПрибытиеФактТекущее > ОграничениеНаДатуДляСтроки Тогда
				Строка.ПрибытиеФактТекущее = ОграничениеНаДатуДляСтроки;
			КонецЕсли;
			
			Если Строка.ПрибытиеФактТекущее > Строка.НачалоРаботФактТекущее Тогда
				Строка.НачалоРаботФактТекущее = Строка.ПрибытиеФактТекущее;
			КонецЕсли;
			
			Если Строка.НачалоРаботФактТекущее > ОграничениеНаДатуДляСтроки Тогда
				Строка.НачалоРаботФактТекущее = ОграничениеНаДатуДляСтроки;
			КонецЕсли;
			
			Если Строка.НачалоРаботФактТекущее > Строка.УбытиеФакт Тогда
				Строка.УбытиеФактТекущее = Строка.НачалоРаботФактТекущее;
			КонецЕсли;
			
			Если Строка.УбытиеФактТекущее > ОграничениеНаДатуДляСтроки Тогда
				Строка.УбытиеФактТекущее = ОграничениеНаДатуДляСтроки;
			КонецЕсли;

			УбытиеФактИзПредыдущейТочки = Строка.УбытиеФактТекущее; 
			
			ОграничениеНаДатуДляСтроки = ОграничениеНаДатуДляСтроки + 1;
			
		КонецЦикла;
		     			
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


