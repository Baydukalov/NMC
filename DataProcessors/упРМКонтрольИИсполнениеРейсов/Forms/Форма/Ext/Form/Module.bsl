
#Область ПеременныеМодуляФормы

&НаКлиенте
Перем СлужебныеЭлементыHtml;

#КонецОбласти 

&НаСервере
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//#Если ВебКлиент Тогда
	//	Элементы.ГруппаКарта.Видимость = Ложь;
	//#КонецЕсли
	
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	ЗаполнитьЦветаПланировщика();
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ИспользуетсяСкладскойУчетНаХабах = ПолучитьФункциональнуюОпцию("упИспользуетсяСкладскойУчетНаХабах");
	
	//#Если НЕ ВебКлиент Тогда
	//	Объект.ПоказыватьКарту  = Истина;
	//	ПоказыватьКарту			= Объект.ПоказыватьКарту;
		ЭтоЯндекс = (упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипКарт") = Перечисления.упТипыКарт.ЯндексКарты);
		ПараметрыМаршрутизацииСитиГИД = упМаршрутизация.ПолучитьПараметрыМаршрутизацииСитиГИД();
	//#КонецЕсли
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Объект.ОтображатьФакт = ОтображатьФакт;
	Объект.ПоказыватьКарту = ПоказыватьКарту;
	Объект.ПоказыватьПланировщик = ПоказыватьПланировщик;
	
	ОбновитьВидимость();
	#Если ВебКлиент Тогда
		Элементы.ГруппаКарта.Видимость = Ложь;
	#Иначе
		//Объект.ПоказыватьКарту  = Истина;
		//ПоказыватьКарту			= Объект.ПоказыватьКарту;
	#КонецЕсли
	
	УстановитьПериодПланирования();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ВидПеревозки", ВидПеревозки);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ДатаНачала", ПериодПланированияНачало);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ДатаОкончания", ПериодПланированияОкончание);		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ПроверятьИзмененныйПорядокЗаданий", ПроверятьИзмененныйПорядокЗаданий);		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ПроверятьОпозданияВТочкиЗаданий", ПроверятьОпозданияВТочкиЗаданий);		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ПроверятьРаботыСПроблемами", ПроверятьРаботыСПроблемами);		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ПроверятьНаличиеФайлов", ПроверятьНаличиеФайлов);		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ПроверятьНеотвеченныеСообщенияВодителя", ПроверятьНеотвеченныеСообщенияВодителя);		
		
	Автообновление();
	Если Автообновление Тогда 
		ПодключитьОбработчикОжидания("Автообновление", ПериодАвтообновления);
	КонецЕсли;	
	
	// Исходные данные точки для отрисовки на карте: цвет текста подписи, цвет заполнения внутренней окружности, цвет заполнения внешней окружности.
	ДанныеСтандартнойТочки   = "'#61411E', '#6BCBE8', '#6B8DE8'";
	ДанныеДепо               = "'#61411E', '#FFE12D', '#6B8DE8'";
	ДанныеИсключеннойТочки   = "'#61411E', '#A5A696', '#7A7A6F'";
	ДанныеНарушенийПоВремени = "'#61411E', '#FF2A3C', '#6B8DE8'";
	
	УставновитьВидимостьСкладскойЯчейкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	мсвСобытий = СтрРазделить(ИмяСобытия, упСервисныеФункцииКлиент.РазделительИменСобытий());
	
	Для каждого ИмяОрабатываемогоСобытия Из мсвСобытий Цикл
		Если ИмяОрабатываемогоСобытия = "ОбновитьРаботы" Тогда 
			АвтообновлениеСервер();
		ИначеЕсли ИмяОрабатываемогоСобытия = "ИзменилсяМаршрут" И Объект.ТекущийРейс = Источник Тогда	
			ОбновитьДанныеПоРейсу();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	
	Для Каждого текЭлемент Из Элементы.Планировщик.ВыделенныеЭлементы Цикл 
		Если ТипЗнч(текЭлемент.Значение) = Тип("ДокументСсылка.упПрохождениеТочки") Тогда 
			ПоказатьЗначение(, текЭлемент.Значение);
		Иначе
			сткПараметры  = Новый Структура("ЗначенияЗаполнения", текЭлемент.Значение);
			
			ОткрытьФорму("Документ.упПрохождениеТочки.ФормаОбъекта", сткПараметры);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	ОтменаРедактирования = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)
	
	Для Каждого текЭлемент Из Элементы.Планировщик.ВыделенныеЭлементы Цикл 
		текРейс = текЭлемент.ЗначенияИзмерений["Рейс"];
	КонецЦикла;	
	
	Элементы.СписокРейсов.ТекущаяСтрока = текРейс;
	
	ОбработатьВыборСтрокиСпискаРейсов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	
	мсвПериодов = ТекущиеПериодыОтображения;
	Если мсвПериодов.Количество() > 0 Тогда
		текНачало = мсвПериодов[0].Начало;
		
		текНачалоПериодаПланировщика 	= Планировщик.ТекущиеПериодыОтображения[0].Начало;
		текОкончаниеПериодаПланировщика = Планировщик.ТекущиеПериодыОтображения[0].Конец;
		
		Если текНачалоПериодаПланировщика <= текНачало И  текНачало <=текОкончаниеПериодаПланировщика Тогда
			УстановитьЦветФонаИзмерений(мсвПериодов[0].Начало);		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРейсовПриАктивизацииСтроки(Элемент)

	ПодключитьОбработчикОжидания("ОбработатьВыборСтрокиСпискаРейсов", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуПриАктивизацииСтроки(Элемент)
	Если Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные <> Неопределено Тогда
		Элементы.дрвРаботыПоТекущемуРейсуИзменитьТоварныйСостав.Доступность = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.ЭтоРабота;
		Если Элементы.дрвРаботыПоТекущемуРейсуИзменитьВГХ.Видимость Тогда
			Элементы.дрвРаботыПоТекущемуРейсуИзменитьВГХ.Доступность = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.ЭтоРабота;	
		КонецЕсли;
		Если Элементы.дрвРаботыПоТекущемуРейсуДосрочноеПрохождениеТочки.Видимость Тогда
			Если Ложь
				Или Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")
				Или Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения")
			Тогда
				Элементы.дрвРаботыПоТекущемуРейсуДосрочноеПрохождениеТочки.Доступность = Ложь;	
			Иначе
				Элементы.дрвРаботыПоТекущемуРейсуДосрочноеПрохождениеТочки.Доступность = Истина;	
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		Элементы.дрвРаботыПоТекущемуРейсуИзменитьТоварныйСостав.Доступность = Ложь;
		Если Элементы.дрвРаботыПоТекущемуРейсуИзменитьВГХ.Видимость Тогда
			Элементы.дрвРаботыПоТекущемуРейсуИзменитьВГХ.Доступность = Ложь;
		КонецЕсли;
		Если Элементы.дрвРаботыПоТекущемуРейсуДосрочноеПрохождениеТочки.Видимость Тогда
			Элементы.дрвРаботыПоТекущемуРейсуДосрочноеПрохождениеТочки.Доступность = Ложь;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуВыполненаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	Если Не ТекущиеДанные = Неопределено Тогда
		
		// Проверим предшественников.
		Если ТекущиеДанные.Выполнена = 1 Тогда
			РодительЭлемента = ТекущиеДанные.ПолучитьРодителя();
			Если РодительЭлемента = Неопределено Тогда
				мсвПредшественников = СтрРазделить(ТекущиеДанные.Предшественники, ",");
			Иначе
				СледующийРодитель = РодительЭлемента.ПолучитьРодителя();
				Если СледующийРодитель = Неопределено Тогда
					мсвПредшественников = СтрРазделить(РодительЭлемента.Предшественники, ",");
				Иначе
					РодительЭлемента = СледующийРодитель.ПолучитьРодителя();
					Если РодительЭлемента = Неопределено Тогда
						мсвПредшественников = СтрРазделить(СледующийРодитель.Предшественники, ",");
					Иначе
						СледующийРодитель = РодительЭлемента.ПолучитьРодителя();
						Если СледующийРодитель = Неопределено Тогда
							мсвПредшественников = СтрРазделить(РодительЭлемента.Предшественники, ",");
						Иначе
							мсвПредшественников = Новый Массив;
						КонецЕсли;	
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если мсвПредшественников.Количество() > 0 Тогда
				ЭлементыДерева = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
				Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
					Если (ЭлементДерева.Выполнена = 0 И Не ЭлементДерева.Отменена) И мсвПредшественников.Найти(Строка(ЭлементДерева.ИдентификаторТочки)) <> Неопределено Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(СтрШаблон("ru = 'Точка (работы) не может быть выполнена, пока не выполнены или отменены работы по адресу ""%1""!'", ЭлементДерева.АдресТочки)));
						ТекущиеДанные.Выполнена = 0;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущиеДанные.Выполнена = 2 Тогда
			ТекущиеДанные.Выполнена = 0;
		КонецЕсли;
		
		упПрохождениеТочкиКлиентСервер.УстановитьФлаги(ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута = Ложь;
	
	Если Истина
		И ЗначениеЗаполнено(ТекущиеДанные.Проблема)
		И упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(ТекущиеДанные.Операция)
	Тогда
		ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Проблема, "ЗапрещеноСозданиеНовойТочкиМаршрута");
		Если ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
			ТекущиеДанные.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.АдресРазгрузки) Тогда
		текТочка = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя().ПолучитьРодителя();
		Если Истина
			И ТекущиеДанные.Проблема <> ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение")
			И текТочка.АдресТочки = ТекущиеДанные.АдресРазгрузки
		Тогда
			ТекущиеДанные.АдресРазгрузки  = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
			ТекстСообщения = Нстр("ru = 'Адрес разгрузки не должен совпадать с адресом текущей точки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	СтарыйАдрес = Неопределено;
	СтараяПроблема = Неопределено;
	
	Если НЕ сткДанныеСтрокиРаботаДоРедактирования = Неопределено Тогда
		Если Истина
			И сткДанныеСтрокиРаботаДоРедактирования.Выполнена = ТекущиеДанные.Выполнена 
			И Не ЗначениеЗаполнено(ТекущиеДанные.Проблема) 
			И (НЕ ТекущиеДанные.Отменена 
				ИЛИ (Истина
					И НЕ сткДанныеСтрокиРаботаДоРедактирования.Отменена
					И сткДанныеСтрокиРаботаДоРедактирования.Отменена = ТекущиеДанные.Отменена))
		Тогда
			Возврат;
		КонецЕсли;
		СтарыйАдрес = сткДанныеСтрокиРаботаДоРедактирования.АдресРазгрузки;
		СтараяПроблема = сткДанныеСтрокиРаботаДоРедактирования.Проблема
	КонецЕсли;
	
	КоллекцияТочекМаршрута = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	
	сткДопСвойства = Неопределено;
	
	сткДопСвойства = Новый Структура();
	сткДопСвойства.Вставить("ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре", Ложь);
	сткДопСвойства.Вставить("ЕстьНеОтмененнаяРаботаТара", Ложь);
	сткДопСвойства.Вставить("СтараяПроблема", СтараяПроблема);
	сткДопСвойства.Вставить("СтарыйАдрес", СтарыйАдрес);

	упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(ТекущиеДанные, КоллекцияТочекМаршрута, , СтараяПроблема, СтарыйАдрес, сткДопСвойства, ,Объект.АдресаПогрузкиПоРейсу, );
	
	Если Истина
		И сткДопСвойства <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(сткДопСвойства, "Задание") Тогда
		ВосстановитьТочкуРазгрузки(сткДопСвойства);	
	КонецЕсли;
	
	ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре = сткДопСвойства.ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре;
	ЕстьНеОтмененнаяРаботаТара = сткДопСвойства.ЕстьНеОтмененнаяРаботаТара;
	Если Истина
		И ТекущиеДанные.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара")
		И ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре 
	Тогда
		сткДопСвойства.Вставить("ТекущиеДанные", ТекущиеДанные);
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОтменеГрузовВТаре", ЭтаФорма, сткДопСвойства);
		ПоказатьВопрос(ОписаниеОповещенияВопрос, Нстр("ru = 'Отменяется тара. 
												  |Выполнить отмену грузов в таре?'"), РежимДиалогаВопрос.ДаНет);
	ИначеЕсли Истина 
			И ЗначениеЗаполнено(ТекущиеДанные.Тара)
			И НЕ ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре 
			И ЕстьНеОтмененнаяРаботаТара Тогда	
		сткДопСвойства.Вставить("ТекущиеДанные", ТекущиеДанные);
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОтменеТары", ЭтаФорма, сткДопСвойства);
		ПоказатьВопрос(ОписаниеОповещенияВопрос, Нстр("ru = 'Все грузы в таре отменены. 
												  |Отменить тару?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

	
	//РазвернутьУзлыДереваКонтроляИсполненияРейсов();

КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьПрохождение = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если НЕ Поле.Имя = "дрвРаботыПоТекущемуРейсуВыполнена" 
		И НЕ Поле.Имя = "дрвРаботыПоТекущемуРейсуНазначениеСтроки" 
		   И НЕ Поле.Имя = "дрвРаботыПоТекущемуРейсуКоличество"
		   И НЕ Поле.Имя = "дрвРаботыПоТекущемуРейсуСумма" Тогда
		ОткрытьПрохождение = Истина;
		Если ТекущиеДанные.ЭтоАдрес Тогда
			Если НЕ ТекущиеДанные.ТочкаПройдена 
				И (Поле.Имя = "дрвРаботыПоТекущемуРейсуПрибытие"
					ИЛИ Поле.Имя = "дрвРаботыПоТекущемуРейсуНачалоРабот"
					ИЛИ Поле.Имя = "дрвРаботыПоТекущемуРейсуУбытие"
					ИЛИ Поле.Имя = "дрвРаботыПоТекущемуРейсуСкладскаяЯчейка") Тогда	
				ОткрытьПрохождение = Ложь;
			КонецЕсли;		
		ИначеЕсли ТекущиеДанные.ЭтоРабота ИЛИ ТекущиеДанные.ЭтоПолучениеПередачаДокументов Тогда	
			Если  НЕ ТекущиеДанные.ТочкаПройдена 
					И (Поле.Имя = "дрвРаботыПоТекущемуРейсуПроблема"
						ИЛИ Поле.Имя = "дрвРаботыПоТекущемуРейсуАдресРазгрузки"
						ИЛИ Поле.Имя = "дрвРаботыПоТекущемуРейсуСкладскаяЯчейка") Тогда	
				ОткрытьПрохождение = Ложь;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	Если ОткрытьПрохождение Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ТекущиеДанные.ПрохождениеТочки) Тогда
			сткПараметры = Новый Структура("Ключ",ТекущиеДанные.ПрохождениеТочки);
			Если ТипЗнч(ТекущиеДанные.ПрохождениеТочки) = Тип("ДокументСсылка.упПроисшествиеВПути") Тогда
				ОткрытьФорму("Документ.упПроисшествиеВПути.ФормаОбъекта",сткПараметры);
			Иначе	
				ОткрытьФорму("Документ.упПрохождениеТочки.ФормаОбъекта",сткПараметры);	
			КонецЕсли;
			
		Иначе
			ТекстСообщения = Нстр("ru = 'Для текущей точки пока нет документа ""Прохождение точки""'");
			ПоказатьПредупреждение(,ТекстСообщения);
		КонецЕсли;
	КонецЕсли; 
	
	
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	сткДанныеСтрокиРаботаДоРедактирования = Неопределено;
	Если Истина
		И ТекущиеДанные <>  Неопределено 
		И ТекущиеДанные.ЭтоРабота
	Тогда
		сткДанныеСтрокиРаботаДоРедактирования = Новый Структура();
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("Проблема", ТекущиеДанные.Проблема);
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("Отменена", ТекущиеДанные.Отменена);
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("АдресРазгрузки", ТекущиеДанные.АдресРазгрузки);
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("Выполнена", ТекущиеДанные.Выполнена);
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("Отменена", ТекущиеДанные.Отменена);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуПроблемаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	текОперация = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.Операция;
	СтандартнаяОбработка = Ложь;
	сткПараметры = Новый Структура("Отбор", Новый Структура("ТипОперации", текОперация));
	ОткрытьФорму("Справочник.упПроблемы.ФормаВыбора", сткПараметры, Элемент);	
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуПроблемаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.Проблема = ВыбранноеЗначение;
	
	Если Объект.АдресаПогрузкиПоРейсу.Количество() = 0 Тогда
		ЗагрузитьАдресаПогрузкиПоРейсу();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуКоличествоПриИзменении(Элемент)
	
	текСтрока = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;	
	Если Истина
		И текСтрока <> Неопределено 
		И текСтрока.ЭтоПолучениеПередачаДокументов 
		И текСтрока.КоличествоЭкземпляровДокумента = текСтрока.Количество
	Тогда
		Если ЗначениеЗаполнено(текСтрока.Проблема) Тогда
			текСтрока.Проблема = Неопределено;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПланированияПриИзменении(Элемент)
	УстановитьПериодПланирования();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ДатаНачала", ПериодПланированияНачало);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ДатаОкончания", ПериодПланированияОкончание);
	Автообновление();
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРейсов, "ВидПеревозки", ВидПеревозки);
	УставновитьВидимостьСкладскойЯчейкиНаСервере();
	СформироватьПредставлениеГруппыОтборов();
	Автообновление();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутРейсаHTMLДокументСформирован(Элемент)
	МаршрутРейсаHTMLСформирован = Истина;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда) 
	
	АвтообновлениеСервер();
	
	Период = Планировщик.ТекущиеПериодыОтображения[0];

	УстановитьТекущийПериодПланировщика();
	УстановитьЦветФонаИзмерений(Период.Начало);	

	
КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Автообновление", Автообновление);
	сткПараметры.Вставить("ПериодАвтообновления", ПериодАвтообновления);
	сткПараметры.Вставить("ОтображатьФакт", Объект.ОтображатьФакт);
	сткПараметры.Вставить("ПоказыватьКарту", Объект.ПоказыватьКарту);
	сткПараметры.Вставить("ПоказыватьПланировщик", Объект.ПоказыватьПланировщик);
	сткПараметры.Вставить("ПроверятьНеотвеченныеСообщенияВодителя", ПроверятьНеотвеченныеСообщенияВодителя);
	сткПараметры.Вставить("ПроверятьНаличиеФайлов", ПроверятьНаличиеФайлов);
	сткПараметры.Вставить("ПроверятьРаботыСПроблемами", ПроверятьРаботыСПроблемами);
	сткПараметры.Вставить("ПроверятьОпозданияВТочкиЗаданий", ПроверятьОпозданияВТочкиЗаданий);
	сткПараметры.Вставить("ПроверятьИзмененныйПорядокЗаданий", ПроверятьИзмененныйПорядокЗаданий);
	
	ОткрытьФорму("Обработка.упРМКонтрольИИсполнениеРейсов.Форма.ФормаНастройки", сткПараметры, ЭтотОбъект,,,,Новый ОписаниеОповещения("ОбработатьЗакрытияФормыНастройки", ЭтотОбъект));
	
КонецПроцедуры
	
&НаКлиенте
Процедура СохранитьИсполнениеРейса(Команда)
	
	// Запоминается текущая строка дерева
	мсвИндексов = Новый Массив;
	ПолучитьИндексТекущейСтроки(мсвИндексов, Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные);
	
	// Данные дерева сохраняются в документы "Прохождение точек"
	СохранитьИсполнениеРейсаНаСервере(Неопределено);
	
	// Дерево разворачивается
	РазвернутьУзлыДереваКонтроляИсполненияРейсов();
	
	// Восстанавливается положение текущей строки элемента формы дерева
	Если мсвИндексов.Количество() > 0 Тогда
		ТекущийИндекс = мсвИндексов.Количество()-1;
		УстановитьТекущуюСтроку(мсвИндексов, Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), ТекущийИндекс);		
	КонецЕсли;
	
	ОбновитьДанныеПоРейсу();
	
	ОтобразитьРейсыНаКарте();
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДерево(Команда)
	
	ЭлементыДерева = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыДерева Цикл
		ЭлементыЗадания = текТочка.ПолучитьЭлементы();
		Для каждого текЗадание Из ЭлементыЗадания Цикл
			ЭлементыОперации = текЗадание.ПолучитьЭлементы();
			Для каждого текОперация Из ЭлементыОперации Цикл
				Элементы.дрвРаботыПоТекущемуРейсу.Свернуть(текОперация.ПолучитьИдентификатор());
			КонецЦикла;
			Элементы.дрвРаботыПоТекущемуРейсу.Свернуть(текЗадание.ПолучитьИдентификатор());
		КонецЦикла;
		Элементы.дрвРаботыПоТекущемуРейсу.Свернуть(текТочка.ПолучитьИдентификатор());
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево(Команда)
	
	ЭлементыДерева = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыДерева Цикл
		Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(текТочка.ПолучитьИдентификатор());
		ЭлементыЗадания = текТочка.ПолучитьЭлементы();
		Для каждого текЗадание Из ЭлементыЗадания Цикл
			Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(текЗадание.ПолучитьИдентификатор());
			ЭлементыОперации = текЗадание.ПолучитьЭлементы();
			Для каждого текОперация Из ЭлементыОперации Цикл
				Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(текОперация.ПолучитьИдентификатор());
			КонецЦикла; 
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	упПрохождениеТочкиКлиентСервер.УстановитьФлагиПотомкам(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	упПрохождениеТочкиКлиентСервер.УстановитьФлагиПотомкам(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИсполнениеРейса(Команда)
	ОбновитьДанныеПоРейсу();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПрохождениеТочки(Команда)
	текТипОперации = ПолучитьТипСамойПриоритетнойОперации(Объект.ТекущийРейс, ПолучитьТекущуюТочку().ИдентификаторТочки);
	сткПараметры = Новый Структура("Отбор",Новый Структура("ТипОперации",текТипОперации));
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыВыбораПроблемы",ЭтаФорма);
	ОткрытьФорму("Справочник.упПроблемы.ФормаВыбора",сткПараметры,,,,,ОповещениеОЗакрытии);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТипСамойПриоритетнойОперации(Рейс, ИдентификаторТочки)
	
	Возврат упПрохождениеТочки.ПолучитьТипСамойПриоритетнойОперацииВТочке(Рейс, ИдентификаторТочки);
	
КонецФункции

&НаКлиенте
Функция ПолучитьТекущуюТочку()
	
	текРезультат = Неопределено;
	
	ТекущиеДанные = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	Если ТипЗнч(ТекущиеДанные.НазначениеСтроки) = Тип("СправочникСсылка.упАдреса") Тогда
		текРезультат = ТекущиеДанные;
	ИначеЕсли ТипЗнч(ТекущиеДанные.НазначениеСтроки) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		текРезультат = ТекущиеДанные.ПолучитьРодителя();	 
	ИначеЕсли ТипЗнч(ТекущиеДанные.НазначениеСтроки) = Тип("ПеречислениеСсылка.упТипыОпераций") Тогда	
		текРезультат = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя();	 	
	Иначе
		текРезультат = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя().ПолучитьРодителя();	 		
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьТоварныйСостав(Команда)
	
	ТекущиеДанныеРаботы = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	СтрокаТипОперации = ТекущиеДанныеРаботы.ПолучитьРодителя();
	СтрокаЗадание = СтрокаТипОперации.ПолучитьРодителя();
	СтрокаТочка = СтрокаЗадание.ПолучитьРодителя();
	
	Если Истина
		И ЗначениеЗаполнено(Объект.ИдентификаторТочкиСИзмененнымСоставомГрузов) 
		И Объект.ИдентификаторТочкиСИзмененнымСоставомГрузов <> СтрокаТочка.ИдентификаторТочки 
	Тогда
		ТекстСообщения = Нстр("ru = 'Товарный состав грузовых мест в точке %1 был изменен. 
						 |Перед тем как изменять состав в новой точке, необходимо сохранить изменения. 
						 |Сбросить изменения?'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТочка.НазначениеСтроки);
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьОтветНаВопросОСохраненииТоварногоСостава", ЭтаФорма), ТекстСообщения, РежимДиалогаВопрос.ДаНетОтмена);	
	Иначе	
		ИзменитьТоварныйСоставНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВГХ(Команда)
	
	ТекущиеДанныеРаботы = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	СтрокаТипОперации = ТекущиеДанныеРаботы.ПолучитьРодителя();
	СтрокаЗадание = СтрокаТипОперации.ПолучитьРодителя();
	СтрокаТочка = СтрокаЗадание.ПолучитьРодителя();
	
	Если Истина
		И ЗначениеЗаполнено(Объект.ИдентификаторТочкиСИзмененнымиВГХГрузов) 
		И Объект.ИдентификаторТочкиСИзмененнымиВГХГрузов <> СтрокаТочка.ИдентификаторТочки 
	Тогда
		ТекстСообщения = Нстр("ru = 'ВГХ грузовых мест в точке %1 были изменены. 
						 |Перед тем как изменять ВГХ в новой точке, необходимо сохранить изменения. 
						 |Сбросить изменения?'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТочка.НазначениеСтроки);
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьОтветНаВопросОСохраненииВГХ", ЭтаФорма), ТекстСообщения, РежимДиалогаВопрос.ДаНетОтмена);	
	Иначе	
		ИзменитьВГХНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТоварныйСоставНаКлиенте()
	
	ТекущиеДанныеРаботы = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	ГрузовоеМесто = ТекущиеДанныеРаботы.НазначениеСтроки;
	СтрокаТипОперации = ТекущиеДанныеРаботы.ПолучитьРодителя();
	ТипОперации = СтрокаТипОперации.НазначениеСтроки;
	СтрокаЗадание = СтрокаТипОперации.ПолучитьРодителя();
	Задание = СтрокаЗадание.НазначениеСтроки;
	СтрокаТочка = СтрокаЗадание.ПолучитьРодителя();

	Объект.ИдентификаторТочкиСИзмененнымСоставомГрузов = СтрокаТочка.ИдентификаторТочки;
	
	сткПараметрыФормы = Новый Структура();
	сткПараметрыФормы.Вставить("ГрузовоеМесто", ГрузовоеМесто);
	сткПараметрыФормы.Вставить("КоличествоГрузов", ТекущиеДанныеРаботы.КоличествоГрузов);
	сткПараметрыФормы.Вставить("ЗаданиеНаПеревозкуГруза", Задание);
	сткПараметрыФормы.Вставить("Рейс", Объект.ТекущийРейс);
	сткПараметрыФормы.Вставить("Регистратор", ПредопределенноеЗначение("Документ.упПрохождениеТочки.ПустаяСсылка"));
	сткПараметрыФормы.Вставить("ТоварныйСоставГрузовогоМеста", ПолучитьТоварныйСостав(ГрузовоеМесто));
	сткПараметрыФормы.Вставить("ТипОперации", ТипОперации);
	
	ОповещениеОЗакрытииФормыИзмененияТоварногоСостава = Новый ОписаниеОповещения("ОповещениеОЗакрытииФормыИзмененияТоварногоСостава", ЭтаФорма);
	
	ОткрытьФорму("ОбщаяФорма.упИзменениеТоварногоСостава", сткПараметрыФормы, ЭтаФорма,,,, ОповещениеОЗакрытииФормыИзмененияТоварногоСостава, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВГХНаКлиенте()
	
	ТекущиеДанныеРаботы = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	ГрузовоеМесто = ТекущиеДанныеРаботы.НазначениеСтроки;
	СтрокаТипОперации = ТекущиеДанныеРаботы.ПолучитьРодителя();
	ТипОперации = СтрокаТипОперации.НазначениеСтроки;
	СтрокаЗадание = СтрокаТипОперации.ПолучитьРодителя();
	Задание = СтрокаЗадание.НазначениеСтроки;
	СтрокаТочка = СтрокаЗадание.ПолучитьРодителя();

	Объект.ИдентификаторТочкиСИзмененнымиВГХГрузов = СтрокаТочка.ИдентификаторТочки;
	
	сткПараметрыФормы = Новый Структура();
	сткПараметрыФормы.Вставить("ГрузовоеМесто", ГрузовоеМесто);
	сткПараметрыФормы.Вставить("ЗаданиеНаПеревозкуГруза", Задание);
	сткПараметрыФормы.Вставить("ШиринаГруза", ТекущиеДанныеРаботы.Ширина);
	сткПараметрыФормы.Вставить("ДлинаГруза", ТекущиеДанныеРаботы.Длина);
	сткПараметрыФормы.Вставить("ВысотаГруза", ТекущиеДанныеРаботы.Высота);
	сткПараметрыФормы.Вставить("Масса", ТекущиеДанныеРаботы.Масса);
	сткПараметрыФормы.Вставить("Объем", ТекущиеДанныеРаботы.Объем);
	сткПараметрыФормы.Вставить("КоличествоМест", ТекущиеДанныеРаботы.КоличествоМест);
	сткПараметрыФормы.Вставить("КоличествоЗагрузочныхМетров", ТекущиеДанныеРаботы.КоличествоЗагрузочныхМетров);
	сткПараметрыФормы.Вставить("ИдентификаторРаботы", ТекущиеДанныеРаботы.ИдентификаторРаботы);
	сткПараметрыФормы.Вставить("Операция", ТипОперации);
		 	
	ОповещениеОЗакрытииФормыИзмененияВГХ = Новый ОписаниеОповещения("ОповещениеОЗакрытииФормыИзмененияВГХ", ЭтаФорма);
	
	ОткрытьФорму("ОбщаяФорма.упИзменениеВГХГруза", сткПараметрыФормы, ЭтаФорма,,,, ОповещениеОЗакрытииФормыИзмененияВГХ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДосрочноеПрохождениеТочки(Команда)
	ДосрочноеПрохождениеТочкиНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

#Область ОтображениеДанныхМаршрутаРейса

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтроки(Строка)
	
	Возврат СтрЗаменить(Строка, """", "");
	
КонецФункции // ПредставлениеСтроки()

&НаСервере
// Выпонлим подготовку для отображения маршрута рейса.
//
Процедура ВыполнитьПодготовкуМаршрутаРейса()
	
	ОбновитьМаршрутРаботыНаСервере();
	спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
	ПересчитатьВременнойГрафикМаршрута(, спкДействий);
	
КонецПроцедуры // ВыполнитьПодготовкуМаршрутаРейса()

&НаСервере
Процедура ОбновитьМаршрутРаботыНаСервере()
	
	Маршрут.Очистить();
	Работы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упМаршрут.СтрокаНомер КАК НомерСтроки,
	|	упМаршрут.Адрес,
	|	упМаршрут.Гараж,
	|	упМаршрут.Адрес.Широта КАК Широта,
	|	упМаршрут.Адрес.Долгота КАК Долгота,
	|	упМаршрут.Идентификатор,
	|	упМаршрут.ПлановоеПрибытие,
	|	упМаршрут.ПлановоеУбытие,
	|	упМаршрут.ВремяРабот * 3600 КАК ВремяРабот,
	|	упМаршрут.ТипТочки,
	|	упМаршрут.ТипТочки.Порядок КАК ИндексКартинки,
	|	упМаршрут.ВременноеОкноНачало,
	|	упМаршрут.ВременноеОкноОкончание,
	|	упМаршрут.РасстояниеОтПредыдущейТочки,
	|	упМаршрут.ВремяОтПредыдущейТочки * 3600 КАК ВремяОтПредыдущейТочки,
	|	упМаршрут.ВремяОтдыхаВПутиОтПредыдущейТочки * 3600 КАК ВремяОтдыхаВПутиОтПредыдущейТочки,
	|	упМаршрут.ВремяОжиданияВТочке * 3600 КАК ВремяОжиданияВТочке,
	|	упМаршрут.ПроездНеРассчитан,
	|	упМаршрут.Предшественники,
	|	упМаршрут.Пройдена,
	|	упМаршрут.Отменена
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсРаботы.Идентификатор,
	|	упРейсРаботы.Задание,
	|	упРейсРаботы.Задание.Заказчик КАК Заказчик,
	|	упРейсРаботы.Операция,
	|	упРейсРаботы.Операция.Порядок КАК ТипОперации,
	|	упРейсРаботы.ДополнительнаяОперация,
	|	упРейсРаботы.ВремяРабот * 3600 КАК ВремяРабот,
	|	упРейсРаботы.ВременноеОкноНачало,
	|	упРейсРаботы.ВременноеОкноОкончание,
	|	упРейсРаботы.ГрузовоеМесто,
	|	упРейсРаботы.ВремяОжидания * 3600 КАК ВремяОжидания,
	|	упРейсРаботы.ПлановоеВремяНачалаРабот,
	|	упРейсРаботы.СтрокаНомер,
	|	упРейсРаботы.КоличествоДопОпераций,
	|	упРейсРаботы.КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА упРейсРаботы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И упРейсРаботы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 1
	|		КОГДА упРейсРаботы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(упРейсРаботы.ПлановоеВремяНачалаРабот, СЕКУНДА, упРейсРаботы.ВремяРабот * 3600 - 60) <= упРейсРаботы.ВременноеОкноОкончание
	|			ТОГДА 1
	|		КОГДА упРейсРаботы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				И упРейсРаботы.ПлановоеВремяНачалаРабот >= упРейсРаботы.ВременноеОкноНачало
	|			ТОГДА 1
	|		КОГДА упРейсРаботы.ПлановоеВремяНачалаРабот >= упРейсРаботы.ВременноеОкноНачало
	|				И ДОБАВИТЬКДАТЕ(упРейсРаботы.ПлановоеВремяНачалаРабот, СЕКУНДА, упРейсРаботы.ВремяРабот * 3600 - 60) <= упРейсРаботы.ВременноеОкноОкончание
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПопадаетВоВременноеОкно,
	|	упРейсРаботы.ТипРегламентногоДокумента,
	|	упРейсРаботы.КоличествоЭкземпляровДокумента
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРейсРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.НомерСтроки,
	|	втМаршрут.Адрес,
	|	втМаршрут.Гараж,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМаршрут.Адрес) КАК АдресПредставление,
	|	втМаршрут.Широта,
	|	втМаршрут.Долгота,
	|	втМаршрут.Идентификатор,
	|	втМаршрут.ПлановоеПрибытие,
	|	втМаршрут.ПлановоеУбытие,
	|	втМаршрут.ВремяРабот,
	|	втМаршрут.ТипТочки,
	|	втМаршрут.ИндексКартинки,
	|	втМаршрут.ВременноеОкноНачало,
	|	втМаршрут.ВременноеОкноОкончание,
	|	втМаршрут.РасстояниеОтПредыдущейТочки,
	|	втМаршрут.ВремяОтПредыдущейТочки,
	|	втМаршрут.ВремяОтдыхаВПутиОтПредыдущейТочки,
	|	втМаршрут.ВремяОжиданияВТочке,
	|	ВЫБОР
	|		КОГДА втМаршрут.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И втМаршрут.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 1
	|		КОГДА втМаршрут.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеУбытие, СЕКУНДА, -60) <= втМаршрут.ВременноеОкноОкончание
	|			ТОГДА 1
	|		КОГДА втМаршрут.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеПрибытие, СЕКУНДА, втМаршрут.ВремяОжиданияВТочке + 60) >= втМаршрут.ВременноеОкноНачало
	|			ТОГДА 1
	|		КОГДА ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеУбытие, СЕКУНДА, -60) <= втМаршрут.ВременноеОкноОкончание
	|				И ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеПрибытие, СЕКУНДА, втМаршрут.ВремяОжиданияВТочке + 60) >= втМаршрут.ВременноеОкноНачало
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПопадаетВоВременноеОкно,
	|	втМаршрут.ПроездНеРассчитан,
	|	втМаршрут.Предшественники,
	|	втМаршрут.Пройдена,
	|	втМаршрут.Отменена
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Задание,
	|	втРаботы.Заказчик,
	|	втРаботы.Операция,
	|	втРаботы.ТипОперации,
	|	втРаботы.ДополнительнаяОперация,
	|	втРаботы.ВремяРабот,
	|	втРаботы.ВременноеОкноНачало,
	|	втРаботы.ВременноеОкноОкончание,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.ВремяОжидания,
	|	втРаботы.ПлановоеВремяНачалаРабот,
	|	втРаботы.СтрокаНомер,
	|	втРаботы.КоличествоДопОпераций,
	|	втРаботы.КоличествоГрузов,
	|	ИСТИНА КАК Пометка,
	|	ЕСТЬNULL(упГрузовыеМеста.Масса, 0) * втРаботы.КоличествоГрузов КАК Масса,
	|	ЕСТЬNULL(упГрузовыеМеста.Объем, 0) * втРаботы.КоличествоГрузов КАК Объем,
	|	ЕСТЬNULL(упГрузовыеМеста.КоличествоМест, 0) * втРаботы.КоличествоГрузов КАК КоличествоМест,
	|	ЕСТЬNULL(упГрузовыеМеста.КоличествоЗагрузочныхМетров, 0) * втРаботы.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
	|	втРаботы.ПопадаетВоВременноеОкно,
	|	втРаботы.ТипРегламентногоДокумента,
	|	втРаботы.КоличествоЭкземпляровДокумента
	|ИЗ
	|	втРаботы КАК втРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ПО втРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Задание,
	|	втРаботы.Заказчик,
	|	втРаботы.Операция,
	|	втРаботы.ТипОперации,
	|	втРаботы.ДополнительнаяОперация,
	|	втРаботы.ВремяРабот,
	|	втРаботы.ВременноеОкноНачало,
	|	втРаботы.ВременноеОкноОкончание,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.ВремяОжидания,
	|	втРаботы.ПлановоеВремяНачалаРабот,
	|	втРаботы.СтрокаНомер,
	|	втРаботы.КоличествоДопОпераций,
	|	втРаботы.КоличествоГрузов,
	|	ИСТИНА,
	|	втРаботы.Задание.Масса,
	|	втРаботы.Задание.Объем,
	|	втРаботы.Задание.КоличествоМест,
	|	втРаботы.Задание.КоличествоЗагрузочныхМетров,
	|	втРаботы.ПопадаетВоВременноеОкно,
	|	втРаботы.ТипРегламентногоДокумента,
	|	втРаботы.КоличествоЭкземпляровДокумента
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ
	|	втРаботы.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	втРаботы.СтрокаНомер";

	Запрос.УстановитьПараметр("Рейс", Объект.ТекущийРейс);
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	
	тзМаршрут = Результат[КоличествоТаблиц - 2].Выгрузить();
	Для каждого текТочка Из тзМаршрут Цикл
		НоваяСтрока = Маршрут.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текТочка);
		НоваяСтрока.Координаты = ПредставлениеЧисла(текТочка.Широта) + ";" + ПредставлениеЧисла(текТочка.Долгота) + ";";
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.Позиционирование = Истина;
	КонецЦикла;
	
	тзРаботы = Результат[КоличествоТаблиц - 1].Выгрузить();
	Для каждого текРабота Из тзРаботы Цикл
		НоваяСтрока = Работы.Добавить();
		НоваяСтрока.ИдентификаторРаботы = Новый УникальныйИдентификатор;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текРабота);		
	КонецЦикла;
	
КонецПроцедуры // ОбновитьМаршрутРаботыНаСервере()

&НаСервере
Процедура ПересчитатьВременнойГрафикМаршрута(ТекущаяСтрока = Неопределено, СписокДействий = Неопределено)
	
	Если Маршрут.Количество() Тогда
		
		ИндексТекущейСтроки = 0;
		ПерваяСтрока = Маршрут[0];
		ПерваяСтрока.РасстояниеОтПредыдущейТочки = 0;
		ПерваяСтрока.ВремяОтПредыдущейТочки = 0;
		
		тзМаршрут = Маршрут.Выгрузить(Новый Структура("Пометка", Истина));
		тзРаботы = Работы.Выгрузить(Новый Структура("Пометка", Истина));
		
		Если ТекущаяСтрока <> Неопределено Тогда
			ИзменяемаяСтрока = Маршрут.НайтиПоИдентификатору(ТекущаяСтрока);   
			Если ИзменяемаяСтрока.Пометка Тогда
				ИндексТекущейСтроки = тзМаршрут.Индекс(тзМаршрут.Найти(ИзменяемаяСтрока.НомерСтроки, "НомерСтроки"));
			КонецЕсли;
		КонецЕсли;
		
		Для каждого текТочка Из тзМаршрут Цикл
			ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", текТочка.Идентификатор));
			НайденнаяСтрока = ИскомыеСтроки[0];
			ЗаполнитьЗначенияСвойств(НайденнаяСтрока, текТочка);
		КонецЦикла;
		Для каждого текРабота Из тзРаботы Цикл
			ИскомыеСтроки = Работы.НайтиСтроки(Новый Структура("ИдентификаторРаботы", текРабота.ИдентификаторРаботы));
			ЗаполнитьЗначенияСвойств(ИскомыеСтроки[0], текРабота);
		КонецЦикла;
		Работы.Сортировать("СтрокаНомер");
		
		Для каждого текТочка Из Маршрут Цикл	
			ПопадаетВоВременноеОкно = текТочка.ПопадаетВоВременноеОкно;
			Если Не ЗначениеЗаполнено(текТочка.ВременноеОкноНачало) И Не ЗначениеЗаполнено(текТочка.ВременноеОкноОкончание) Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текТочка.ВременноеОкноНачало) И текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текТочка.ВременноеОкноОкончание) И текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание И текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			Иначе
				текТочка.ПопадаетВоВременноеОкно = Ложь;	
			КонецЕсли;
			Если текТочка.ПопадаетВоВременноеОкно <> ПопадаетВоВременноеОкно Тогда
				Если СписокДействий <> Неопределено И текТочка <> ИзменяемаяСтрока Тогда
					сткИзменений = Новый Структура;
					сткИзменений.Вставить("ИмяПроцедуры", "ИзменитьИконкуМаркера"); 
					сткИзменений.Вставить("ИндексВМассиве", текТочка.ИндексВМассиве); 
					сткИзменений.Вставить("Данные", ("""" + Строка(текТочка.НомерСтроки) + """," + ?(текТочка.ПопадаетВоВременноеОкно, ДанныеСтандартнойТочки, ДанныеНарушенийПоВремени))); 
					сткИзменений.Вставить("Режим", 1); 
					СписокДействий.Добавить(сткИзменений);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		
		Для каждого текРабота Из Работы Цикл
			Если Не ЗначениеЗаполнено(текРабота.ВременноеОкноНачало) И Не ЗначениеЗаполнено(текРабота.ВременноеОкноОкончание) Тогда
				текРабота.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текРабота.ВременноеОкноНачало) И (текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание Тогда
				текРабота.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текРабота.ВременноеОкноОкончание) И текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало Тогда
				текРабота.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало И (текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание Тогда	
				текРабота.ПопадаетВоВременноеОкно = Истина;
			Иначе
				текРабота.ПопадаетВоВременноеОкно = Ложь;
			КонецЕсли;
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьВременнойГрафикМаршрута()

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Строка - значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат	упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецФункции

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	текстСкрипта - строка - тектс на javascript.
//
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОбъекты()
	
	ВыполнитьПроцедуруHTML("clearObjects()");
	
КонецПроцедуры // ОчиститьОбъекты()

&НаКлиенте
Функция ОтобразитьОбъект(Имя = "", Данные = "", Координаты, Режим = 0, ТекстМаркера = "") 
	
	
	Если Режим = 0 Тогда
		мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
		Индекс = ВыполнитьФункциюHTML("addObjectMarker(" + мсвКоординат[0] + ", " + мсвКоординат[1] + ", """ + ПредставлениеСтроки(Имя) + """, " + Данные + ")");
	ИначеЕсли Режим = 1 Или Режим = 2 Тогда
		//Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
		//	ВыполнитьПроцедуруHTML("addPoint(" + мсвКоординат[Сч * 2] + ", " + мсвКоординат[(Сч * 2) + 1] + ")");
		//КонецЦикла;	
		//Индекс = ВыполнитьФункциюHTML("addObjectPolylineBetweenObjects(""" + Данные.Цвет + """, " + ПредставлениеЧисла(Данные.Толщина) + ", " + ПредставлениеЧисла(Данные.Прозрачность) + ")");
		Индекс = ВыполнитьФункциюHTML("addPolylineWithCoordinates(""" + Координаты + """, """ + Данные.Цвет + """, " + ПредставлениеЧисла(Данные.Толщина) + ", " + ПредставлениеЧисла(Данные.Прозрачность) + ")");
	ИначеЕсли Режим = 3 Тогда
		мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
		Индекс = ВыполнитьФункциюHTML("addObjectMarkerWithDivIcon(" + мсвКоординат[0] + ", " + мсвКоординат[1] + ", """ + ПредставлениеСтроки(Имя) + """, """ + ТекстМаркера + """, " + Данные + ")");
	КонецЕсли;	
	
	Возврат Индекс;
	
Конецфункции // ОтобразитьОбъект()

&НаКлиенте
// Процедура выводит объекты на карту.
//
// Параметры:
//  Нет.
//
Процедура ОтобразитьОбъектыНаКарте()
	
	Если МаршрутРейсаHTMLСформирован Тогда
		Сч = 1;
		Для каждого текТочка Из Маршрут Цикл
			Если ЗначениеЗаполнено(текТочка.Гараж) Тогда
				текДанные = ДанныеДепо;
			Иначе
				Если текТочка.ПопадаетВоВременноеОкно Тогда
					текДанные = ДанныеСтандартнойТочки;
				Иначе
					текДанные = ДанныеНарушенийПоВремени;
				КонецЕсли;
			КонецЕсли;
			
			МассивЦвета = СтрРазделить(текДанные,", ",Ложь);
			МассивЦвета[1] = "'#FFFFFF'"; // цвет заполнения внутренней окружности, 
			Если текТочка.Отменена Тогда
				МассивЦвета[1] = "'#FF2A3C'";
			КонецЕсли;
			Если текТочка.Пройдена Тогда
				МассивЦвета[1] = "'#00FF00'";
			КонецЕсли;
			Если НЕ текТочка.ПопадаетВоВременноеОкно Тогда
				МассивЦвета[2] = "'#FF2A3C'"; // цвет заполнения внешней окружности.
			КонецЕсли;
			
			текДанные = СтрСоединить(МассивЦвета,", ");
			текТочка.ИндексВМассиве = ОтобразитьОбъект(текТочка.АдресПредставление, текДанные, текТочка.Координаты, 3, Строка(Сч));
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОтобразитьОбъектыНаКарте()

#КонецОбласти 

&НаКлиенте
Процедура Автообновление()
	
	АвтообновлениеСервер();
	
	Если ПоказыватьПланировщик Тогда
		Период = Планировщик.ТекущиеПериодыОтображения[0];
		УстановитьТекущийПериодПланировщика();
		УстановитьЦветФонаИзмерений(Период.Начало);	
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура АвтообновлениеСервер()
			
	Элементы.СписокРейсов.Обновить();
	
	// Перезаполняется планировщик
	Если ПоказыватьПланировщик Тогда
		текОбъект = РеквизитФормыВЗначение("Объект");
		СхемаКомпоновкиДанных = Элементы.СписокРейсов.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
		НастройкиКомпоновкиДанных = Элементы.СписокРейсов.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
		текОбъект.ЗаполнитьДанныеПланировщика(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных.Отбор, НастройкиКомпоновкиДанных.Порядок, НастройкиКомпоновкиДанных.ПараметрыДанных);
		
		ЗначениеВРеквизитФормы(текОбъект.МаршрутыПоРейсам, "Объект.МаршрутыПоРейсам");
		ЗначениеВРеквизитФормы(текОбъект.Рейсы, "Объект.Рейсы");
		Объект.НачалоПериодаПланировщика = текОбъект.НачалоПериодаПланировщика;
		Объект.КонецПериодаПланировщика = текОбъект.КонецПериодаПланировщика;
		ЗаполнитьПланировщикИсполненияРейсов();
	КонецЕсли; 

КонецПроцедуры	

&НаСервере
Процедура УставновитьВидимостьСкладскойЯчейкиНаСервере()
	сткПараметрыВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПеревозки, "ИспользуетсяСкладскойУчетНаХабах, ПараметрыВводаИнформацииОГрузовыхМестах");
	Элементы.дрвРаботыПоТекущемуРейсуСкладскаяЯчейка.Видимость = Истина
	                                                             И ИспользуетсяСкладскойУчетНаХабах 
													 И ЗначениеЗаполнено(ВидПеревозки)
													 И сткПараметрыВидаПеревозки.ИспользуетсяСкладскойУчетНаХабах
													 И сткПараметрыВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится;
КонецПроцедуры

&НаКлиенте
Функция УстановитьПериодПланирования()
	
	Если ЗначениеЗаполнено(ПериодПланирования) Тогда
		Если ТипЗнч(ПериодПланирования) = Тип("СправочникСсылка.упПериодыПланирования") Тогда
			сткДанныеПериода = ВернутьПериодОтбора(ПериодПланирования);
			ПериодПланированияНачало    = сткДанныеПериода.НачалоПериода;
			ПериодПланированияОкончание = сткДанныеПериода.ОкончаниеПериода;
		Иначе
			ПериодПланированияНачало    = ПериодПланирования.ДатаНачала;
			ПериодПланированияОкончание = ПериодПланирования.Датаокончания;
		КонецЕсли;
	Иначе
		ПериодПланированияНачало    = ТекущаяДата() - 15552000;
		ПериодПланированияОкончание = ТекущаяДата() + 15552000;
	КонецЕсли;

	СформироватьПредставлениеГруппыОтборов();
	
КонецФункции // УстановитьПериодПланирования()

&НаКлиенте 
Процедура СформироватьПредставлениеГруппыОтборов()
	
	ПредставлениеНачалаПериода = Формат(ПериодПланированияНачало, 	"ДФ=""дд:ММ:гггг ЧЧ:мм""");
	ПредставлениеОкончанияПериода = Формат(ПериодПланированияОкончание, "ДФ=""дд:ММ:гггг ЧЧ:мм""");
	НадписьЗаголовокПериода = СтрШаблон("ru = '(%1 - %2)'", ПредставлениеНачалаПериода, ПредставлениеОкончанияПериода);
	
	НадписьЗаголовок = СтрШаблон("ru = 'Период планирования: (%1 - %2); Вид перевозки: %3'",
	                             ПериодПланированияНачало,
		                        ПериодПланированияОкончание,
		                        ?(ЗначениеЗаполнено(ВидПеревозки), Строка(ВидПеревозки), "-"));
	Элементы.ГруппаОтборы.ЗаголовокСвернутогоОтображения = НСтр(НадписьЗаголовок);; 

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьПериодОтбора(ПериодПланирования)
	
	Возврат Справочники.упПериодыПланирования.РассчитатьПериодПланирования(ПериодПланирования);	
	
КонецФункции // ВернутьПериодОтбора()

&НаКлиенте
Процедура ОповещениеОЗакрытииФормыИзмененияТоварногоСостава(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ЗаданиеНаПеревозкуГруза = РезультатЗакрытия.ЗаданиеНаПеревозкуГруза;
		ГрузовоеМестоТекущее = РезультатЗакрытия.ГрузовоеМесто;
		КоличествоГрузов = РезультатЗакрытия.КоличествоГрузов;
		мсвИзмененийСостава = РезультатЗакрытия.МассивИзмененийСостава; 
		Операция = РезультатЗакрытия.Операция; 
		
		Если мсвИзмененийСостава <> Неопределено Тогда
			ЗаполнитьТЧТоварныйСоставГруза(Объект.ИдентификаторТочкиСИзмененнымСоставомГрузов, ЗаданиеНаПеревозкуГруза, ГрузовоеМестоТекущее, Операция, КоличествоГрузов, мсвИзмененийСостава);	
						
			ТекущиеДанные 	= Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	        	КоллекцияТочекМаршрута = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
			
			сткВозврата = Неопределено;
			упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(ТекущиеДанные, КоллекцияТочекМаршрута,,,,,,,,);
			
			Если Истина
				И сткВозврата <> Неопределено 
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(сткВозврата, "Задание")
			Тогда
				ВосстановитьТочкуРазгрузки(сткВозврата);	
			КонецЕсли;
			
			РазвернутьУзлыДереваКонтроляИсполненияРейсов();

		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗакрытииФормыИзмененияВГХ(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ЗаполнитьВГХГруза(Объект.ИдентификаторТочкиСИзмененнымиВГХГрузов, РезультатЗакрытия);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВГХГруза(ИдентификаторТочки, сткВГХ)
	
	ЭлементТочка = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), "ИдентификаторТочки", ИдентификаторТочки);
	ЭлементТочка.ИзмененыПараметры = Истина;
	ЭлементЗадание = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементТочка.ПолучитьЭлементы(), "НазначениеСтроки", сткВГХ.Задание);
	ЭлементЗадание.ИзмененыПараметры = Истина;
	ЭлементОперация = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементЗадание.ПолучитьЭлементы(), "НазначениеСтроки", сткВГХ.Операция);
	ЭлементОперация.ИзмененыПараметры = Истина;
	ЭлементГрузовоеМесто = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементОперация.ПолучитьЭлементы(), "НазначениеСтроки", сткВГХ.ГрузовоеМесто);
	ЗаполнитьЗначенияСвойств(ЭлементГрузовоеМесто, сткВГХ);
	ЭлементГрузовоеМесто.ИзмененыПараметры = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьТЧТоварныйСоставГруза(ИдентификаторТочки, ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, Операция, КоличествоГрузов, мсвИзмененийСостава)
	
	Результат = Новый Структура("ЕстьПроблемы, СписокПроблем", Ложь, "");
	
	СписокПроблем = Новый Массив;
	
	мсвСтрокиГрузовоеМесто = Объект.ТоварныйСоставГруза.НайтиСтроки(Новый Структура("ГрузовоеМестоРодитель", ГрузовоеМесто));
	
	Для каждого СтрокаГрузовоеМесто Из мсвСтрокиГрузовоеМесто Цикл
		Объект.ТоварныйСоставГруза.Удалить(СтрокаГрузовоеМесто);
	КонецЦикла;
	
	Если мсвИзмененийСостава.Количество() > 0 Тогда
		
		Для каждого Строка Из мсвИзмененийСостава Цикл
			НоваяСтрока = Объект.ТоварныйСоставГруза.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ЗаданиеНаПеревозкуГруза = ЗаданиеНаПеревозкуГруза;
			НоваяСтрока.ГрузовоеМестоРодитель = ГрузовоеМесто;
			НоваяСтрока.КоличествоГрузов = КоличествоГрузов;
			Если Истина
				И ЗначениеЗаполнено(НоваяСтрока.Проблема)
				И СписокПроблем.Найти(НоваяСтрока.Проблема) = Неопределено Тогда
				СписокПроблем.Добавить(НоваяСтрока.Проблема);
			КонецЕсли;
		КонецЦикла;
		
		ЕстьПроблемы = СписокПроблем.Количество() > 0;
		
		ЭлементТочка = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), "ИдентификаторТочки", ИдентификаторТочки);
		ЭлементТочка.ЕстьПроблемы = ЕстьПроблемы;
		
		ЭлементЗадание = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементТочка.ПолучитьЭлементы(), "НазначениеСтроки", ЗаданиеНаПеревозкуГруза);
		ЭлементЗадание.ЕстьПроблемы = ЕстьПроблемы;
		
		ЭлементОперация = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементЗадание.ПолучитьЭлементы(), "НазначениеСтроки", Операция);
		ЭлементОперация.ЕстьПроблемы = ЕстьПроблемы;
		
		ЭлементГрузовоеМесто = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементОперация.ПолучитьЭлементы(), "НазначениеСтроки", ГрузовоеМесто);
		ЭлементГрузовоеМесто.ЕстьПроблемы = ЕстьПроблемы;
		ЭлементГрузовоеМесто.СписокПроблем = СтрСоединить(СписокПроблем, ",");
		ЭлементГрузовоеМесто.Отменена = Истина;
		ЭлементГрузовоеМесто.Выполнена = Ложь;
		ЭлементГрузовоеМесто.ГрузРазделен = Истина;
		ЭлементГрузовоеМесто.ИндексКартинки = 12;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьТоварныйСостав(ГрузовоеМесто)
	ТоварныйСостав = РеквизитФормыВЗначение("Объект.ТоварныйСоставГруза");
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТоварныйСостав.Выгрузить(Новый Структура("ГрузовоеМестоРодитель", ГрузовоеМесто)));
КонецФункции

#Область Планировщик

&НаСервере
Процедура ЗаполнитьПланировщикИсполненияРейсов()
	
	Отказ = Ложь;
	
	ЗаполнитьИзмеренияПланировщика();
	
	ЗаполнитьЭлементыИзмеренияПланировщика(Отказ);
	
	Если НЕ Отказ Тогда
		
		Планировщик.Элементы.Очистить();
		
		соотИзмерений = Новый Соответствие;
		соотИзмерений.Вставить("Рейс", Неопределено);
		Если Объект.ОтображатьФакт Тогда 
			соотИзмерений.Вставить("ПланФакт", "");
		КонецЕсли;	
		
		// Заполняются элементы планировщика
		Для каждого СтрокаТочкаМаршрута Из Объект.МаршрутыПоРейсам Цикл
			
			соотИзмерений["Рейс"] 		= СтрокаТочкаМаршрута.Рейс;
			
			Если Объект.ОтображатьФакт Тогда
				соотИзмерений["ПланФакт"] 	= "План";
			КонецЕсли;
			
			НовыйЭлементПланировщика = Планировщик.Элементы.Добавить(СтрокаТочкаМаршрута.НачалоПериодаПлан, СтрокаТочкаМаршрута.КонецПериодаПлан);
			НовыйЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(соотИзмерений);
			НовыйЭлементПланировщика.Значение	= ЗначениеЭлементаПланировщика(СтрокаТочкаМаршрута);
			НовыйЭлементПланировщика.Текст      = ТекстЭлементаПланировщика(СтрокаТочкаМаршрута);
			НовыйЭлементПланировщика.Подсказка  = ПодсказкаЭлементаПланировщика(СтрокаТочкаМаршрута);
			НовыйЭлементПланировщика.ЦветФона	= сткЦвета[СтрокаТочкаМаршрута.СтатусТочки];	
			
			Если Объект.ОтображатьФакт 
				И ЗначениеЗаполнено(СтрокаТочкаМаршрута.НачалоПериодаФакт) 
				И ЗначениеЗаполнено(СтрокаТочкаМаршрута.КонецПериодаФакт) Тогда
				
				соотИзмерений["ПланФакт"] 	= "Факт";
				НовыйЭлементПланировщика = Планировщик.Элементы.Добавить(СтрокаТочкаМаршрута.НачалоПериодаФакт, СтрокаТочкаМаршрута.КонецПериодаФакт);
				НовыйЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(соотИзмерений);
				НовыйЭлементПланировщика.Значение	= ЗначениеЭлементаПланировщика(СтрокаТочкаМаршрута, "Факт");
				НовыйЭлементПланировщика.Текст      = ТекстЭлементаПланировщика(СтрокаТочкаМаршрута);
				НовыйЭлементПланировщика.Подсказка  = ПодсказкаЭлементаПланировщика(СтрокаТочкаМаршрута, "Факт");
				НовыйЭлементПланировщика.ЦветФона	= сткЦвета[СтрокаТочкаМаршрута.СтатусТочки];	
				
			КонецЕсли;
			
		КонецЦикла;
		
		Планировщик.НачалоПериодаОтображения = Объект.НачалоПериодаПланировщика;
		Планировщик.КонецПериодаОтображения  = Объект.КонецПериодаПланировщика;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзмеренияПланировщика()
	Если Объект.ОтображатьФакт Тогда 
		Если Планировщик.Измерения.Количество() = 1 Тогда 
			ИзмерениеПланФакт = Планировщик.Измерения.Добавить("ПланФакт");
			ИзмерениеПланФакт.Текст = "П/Ф";
			ИзмерениеПланФакт.Элементы.Добавить("План");
			ИзмерениеПланФакт.Элементы.Добавить("Факт");
		КонецЕсли;
	Иначе
		Если Планировщик.Измерения.Количество() = 2 Тогда 
			Планировщик.Измерения.Удалить(1);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементыИзмеренияПланировщика(Отказ)
	
	ИзмерениеРейс = Планировщик.Измерения.Найти("Рейс");
	Если НЕ ИзмерениеРейс = Неопределено Тогда
		ИзмерениеРейс.Элементы.Очистить();	
		Для Каждого СтрокаРейс Из Объект.Рейсы Цикл 
			
			// Значение элемента измерения
			ЭлементИзмеренияРейс 			= ИзмерениеРейс.Элементы.Добавить(СтрокаРейс.Рейс);
			
			// Текст элемента измерения
			СообщениеКоличествоТочек		= Нстр("ru = 'Точек:%1'");
			СообщениеКоличествоТочек		= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеКоличествоТочек, СтрокаРейс.КоличествоТочекПоРейсу);
			
			МинДата		= СтрокаРейс.МинНачалоПериода;
			МаксДата	= СтрокаРейс.МаксКонецПериода;
						
			текФорматнаяСтрока = "ДФ=d.MM";
			Если ЗначениеЗаполнено(МинДата) И ЗначениеЗаполнено(МаксДата) Тогда
				СообщениеИнтервал	= Нстр("ru = '(%1-%2)'");
				СообщениеИнтервал	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеИнтервал, Формат(МинДата,текФорматнаяСтрока), Формат(МаксДата,текФорматнаяСтрока));	
			ИначеЕсли ЗначениеЗаполнено(МинДата) Тогда
				СообщениеИнтервал	= Нстр("ru = '%1'");
				СообщениеИнтервал	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеИнтервал, Формат(МинДата,текФорматнаяСтрока));		
			ИначеЕсли ЗначениеЗаполнено(МаксДата) Тогда
				СообщениеИнтервал	= Нстр("ru = '%1'");
				СообщениеИнтервал	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеИнтервал, Формат(МаксДата,текФорматнаяСтрока));			
			КонецЕсли;
			
			ЭлементИзмеренияРейс.Текст 		= "" + СтрокаРейс.Номер + " " + СтрокаРейс.ТранспортноеСредство + Символы.ПС
													+ СообщениеКоличествоТочек + " " + СообщениеИнтервал;
		КонецЦикла;
	Иначе	
		 Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветФонаИзмерений(ДатаПериода)
	
	НачалоДняПериода = НачалоДня(ДатаПериода);
	
	ИзмерениеРейс 	= Планировщик.Измерения.Найти("Рейс");
	ТекущийРейс		= Неопределено;
	Если НЕ Элементы.СписокРейсов.ТекущиеДанные = Неопределено Тогда
		ТекущийРейс		= Элементы.СписокРейсов.ТекущиеДанные.Ссылка;	
	КонецЕсли;
	
	
	Если НЕ ИзмерениеРейс = Неопределено Тогда
		
		ЭлементыИзмерения = ИзмерениеРейс.Элементы;
		Для каждого ЭлементИзмерения Из ЭлементыИзмерения Цикл
			
			текРейс = ЭлементИзмерения.Значение;
			мсвСтрокиРейса = Объект.Рейсы.НайтиСтроки(Новый Структура("Рейс",текРейс));
			Если мсвСтрокиРейса.Количество() > 0 Тогда
				НайденнаяСтрока = мсвСтрокиРейса[0];
				Если НачалоДня(НайденнаяСтрока.МинНачалоПериода) <= НачалоДняПериода
					И НачалоДняПериода <= КонецДня(НайденнаяСтрока.МаксКонецПериода) Тогда
						ЭлементИзмерения.ЦветФона =  сткЦвета["ФонСобытие"];	
				ИначеЕсли НачалоДня(НайденнаяСтрока.МинНачалоПериода) > НачалоДняПериода Тогда	
					ЭлементИзмерения.ЦветФона =  сткЦвета["ФонСобытиеЕщеНеПроизошло"];		
				Иначе
					ЭлементИзмерения.ЦветФона =  сткЦвета["ФонСобытиеПроизошлоРанее"];	
				КонецЕсли;
			КонецЕсли;
			
			Если текРейс = ТекущийРейс Тогда
				 ЭлементИзмерения.ЦветФона =  сткЦвета["ТекущийРейс"];	
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦветаПланировщика()
	сткЦвета	= Новый Структура();
	текОбъект	= РеквизитФормыВЗначение("Объект");
	МакетЦветаОформления = текОбъект.ПолучитьМакет("ЦветаОформления");
	Для каждого Область Из МакетЦветаОформления.Области Цикл
		сткЦвета.Вставить(Область.Имя, Область.ЦветФона);	
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ЗначениеЭлементаПланировщика(ТочкаМаршрута, ТипВремени = "План")
	
	текРезультат = Неопределено;
	
	Если ЗначениеЗаполнено(ТочкаМаршрута.ПрохождениеТочки) Тогда 
		текРезультат          = ТочкаМаршрута.ПрохождениеТочки;
	Иначе	
		Если ТипВремени = "План" Тогда
			текРезультат = Новый Структура();
			текРезультат.Вставить("ИдентификаторТочки", 	ТочкаМаршрута.Идентификатор);
			текРезультат.Вставить("НачалоВыполнения", 		ТочкаМаршрута.ПлановоеПрибытие);
			текРезультат.Вставить("ОкончаниеВыполнения", 	ТочкаМаршрута.ПлановоеУбытие);
			текРезультат.Вставить("АдресТочки", 			ТочкаМаршрута.Адрес);
			текРезультат.Вставить("СтрокаНомер", 			ТочкаМаршрута.СтрокаНомер);
			текРезультат.Вставить("НачалоРабот", 			ТочкаМаршрута.НачалоРаботПлан);
			текРезультат.Вставить("Рейс", 					ТочкаМаршрута.Рейс);
		Иначе	
			текРезультат = ТочкаМаршрута.Идентификатор;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

&НаСервере
Функция ТекстЭлементаПланировщика(ТочкаМаршрута)
	Возврат "" + ТочкаМаршрута.СтрокаНомер + ") " + ТочкаМаршрута.АдресПредставление;	
КонецФункции

&НаСервере
Функция ПодсказкаЭлементаПланировщика(ТочкаМаршрута, ТипВремени = "План")
	текРезультат = "";
	Если ТипВремени = "План" Тогда
		текРезультат = "" + Формат(ТочкаМаршрута.НачалоПериодаПлан, "ДФ=HH:mm") + "-" + Формат(ТочкаМаршрута.КонецПериодаПлан, "ДФ=HH:mm");
	Иначе													
		текРезультат = "" + Формат(ТочкаМаршрута.НачалоПериодаФакт, "ДФ=HH:mm") + "-" + Формат(ТочкаМаршрута.КонецПериодаФакт, "ДФ=HH:mm")
	КонецЕсли;
	текРезультат = текРезультат +  Символы.ПС + ТочкаМаршрута.СтрокаНомер + ") " + ТочкаМаршрута.АдресПредставление;												
	Возврат текРезультат;
КонецФункции

&НаКлиенте
Процедура УстановитьТекущийПериодПланировщика()
	мсвСтрокаРейса = Объект.Рейсы.НайтиСтроки(Новый Структура("Рейс",Объект.ТекущийРейс));
	Если мсвСтрокаРейса.Количество() > 0 Тогда
		КоллекцияПериодовОтображения = Планировщик.ТекущиеПериодыОтображения;
		Если КоллекцияПериодовОтображения.Количество()>0 Тогда
			Планировщик.ТекущиеПериодыОтображения.Очистить();
			текДатаНачало 		= НачалоДня(мсвСтрокаРейса[0].МинНачалоПериода);
			текДатаОкончание 	= КонецДня(мсвСтрокаРейса[0].МинНачалоПериода);
			Период = Планировщик.ТекущиеПериодыОтображения.Добавить(текДатаНачало, текДатаОкончание);
			УстановитьЦветФонаИзмерений(текДатаНачало);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиОповещений

&НаКлиенте
// Процедура завершения после закрытия формы настроек.
//
Процедура ОбработатьЗакрытияФормыНастройки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда 
		Автообновление = Результат.Автообновление;
		ПериодАвтообновления = Результат.ПериодАвтообновления;
		ОтображатьФакт = Результат.ОтображатьФакт;
		Объект.ОтображатьФакт = ОтображатьФакт;

		ПоказыватьКарту = Результат.ПоказыватьКарту;
		Объект.ПоказыватьКарту = ПоказыватьКарту;
		
		ПоказыватьПланировщик = Результат.ПоказыватьПланировщик;
		Объект.ПоказыватьПланировщик = ПоказыватьПланировщик;
		
		ПроверятьРаботыСПроблемами = Результат.ПроверятьРаботыСПроблемами;
		ПроверятьОпозданияВТочкиЗаданий = Результат.ПроверятьОпозданияВТочкиЗаданий;
		ПроверятьНеотвеченныеСообщенияВодителя = Результат.ПроверятьНеотвеченныеСообщенияВодителя;
		ПроверятьНаличиеФайлов = Результат.ПроверятьНаличиеФайлов;
		ПроверятьИзмененныйПорядокЗаданий = Результат.ПроверятьИзмененныйПорядокЗаданий;
				
		Если Автообновление Тогда 
			ПодключитьОбработчикОжидания("Автообновление", ПериодАвтообновления);
		Иначе
			ОтключитьОбработчикОжидания("Автообновление");
		КонецЕсли;
		
		Автообновление();
		ОбновитьВидимость();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
// Процедура обработки ответа пользователя.
//
Процедура ОбработатьОтветНаВопросОСохраненииДереваИсполнения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		мсвИзмененныхПрохожденийТочек = Неопределено;
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
			И ДополнительныеПараметры.Свойство("мсвИзмененныхПрохожденийТочек", мсвИзмененныхПрохожденийТочек) Тогда
				// Данные дерева сохраняются в документы "Прохождение точек"
				СохранитьИсполнениеРейсаНаСервере(мсвИзмененныхПрохожденийТочек, Истина);
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ РезультатВопроса = КодВозвратаДиалога.Отмена  Тогда	
		НовыйТекущийРейс = Неопределено;
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
			И ДополнительныеПараметры.Свойство("НовыйТекущийРейс", НовыйТекущийРейс) Тогда
			Объект.ТекущийРейс 	= НовыйТекущийРейс;
			ОбработатьВыборСтрокиСпискаРейсовСервер();
			РазвернутьУзлыДереваКонтроляИсполненияРейсов();
			ОтобразитьРейсыНаКарте();
		КонецЕсли;	
	Иначе
		ТекущийРейс = Неопределено;
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
			И ДополнительныеПараметры.Свойство("ТекущийРейс", ТекущийРейс) Тогда
			Элементы.СписокРейсов.ТекущаяСтрока = ТекущийРейс;
		КонецЕсли;	

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура обработки ответа пользователя.
//
Процедура ОбработатьОтветНаВопросОСохраненииТоварногоСостава(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ИдентификаторТочкиСИзмененнымСоставомГрузов = Неопределено;
		Объект.ТоварныйСоставГруза.Очистить();
		ИзменитьТоварныйСоставНаКлиенте();	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура обработки ответа пользователя.
//
Процедура ОбработатьОтветНаВопросОСохраненииВГХ(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ИдентификаторТочкиСИзмененнымиВГХГрузов = Неопределено;
		ИзменитьВГХНаКлиенте();	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборСтрокиСпискаРейсов()
	
	// Если выбрана строка с рейсом
	Если Элементы.СписокРейсов.ТекущиеДанные <> Неопределено Тогда
		ТекущийРейс = Объект.ТекущийРейс;
		
		НовыйТекущийРейс = Элементы.СписокРейсов.ТекущиеДанные.Ссылка;
		Если ТекущийРейс <> НовыйТекущийРейс Тогда
			мсвИзмененныхПрохожденийТочек = МассивИзмененныхТочек();
			сткДополнительныеПараметры = Новый Структура("НовыйТекущийРейс, ТекущийРейс, мсвИзмененныхПрохожденийТочек", НовыйТекущийРейс, ТекущийРейс, мсвИзмененныхПрохожденийТочек);
			Если мсвИзмененныхПрохожденийТочек.Количество() <> 0 Тогда
				ТекстСообщения = Нстр("ru = 'Данные по %1 были изменены. Сохранить изменения?'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущийРейс);
				ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьОтветНаВопросОСохраненииДереваИсполнения", ЭтаФорма, сткДополнительныеПараметры), ТекстСообщения, РежимДиалогаВопрос.ДаНетОтмена);	
			Иначе	
				Объект.ТекущийРейс 	= НовыйТекущийРейс;
				ОбновитьДанныеПоРейсу();
				ОтобразитьРейсыНаКарте();
			КонецЕсли;
			
			УстановитьТекущийПериодПланировщика();
		
		КонецЕсли;
	Иначе
			
		Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы().Очистить();
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоРейсу()
	ОбработатьВыборСтрокиСпискаРейсовСервер();
	//РазвернутьУзлыДереваКонтроляИсполненияРейсов();		
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборСтрокиСпискаРейсовСервер()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьАдресаПогрузкиПоРейсу();
	ОбработкаОбъект.ЗаполнитьДеревоРаботПоРейсу();
	ЗначениеВРеквизитФормы(ОбработкаОбъект.АдресаПогрузкиПоРейсу, "Объект.АдресаПогрузкиПоРейсу");
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсу, "Объект.дрвРаботыПоТекущемуРейсу");
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсуЭталон, "Объект.дрвРаботыПоТекущемуРейсуЭталон");
	сткНастройкиВидаПеревозки = ОбработкаОбъект.НастройкиВидаПеревозки(ОбработкаОбъект.дрвРаботыПоТекущемуРейсу);
	Элементы.дрвРаботыПоТекущемуРейсуИзменитьТоварныйСостав.Видимость = сткНастройкиВидаПеревозки.РазрешеноИзменятьТоварныйСостав;
	Элементы.дрвРаботыПоТекущемуРейсуИзменитьВГХ.Видимость = сткНастройкиВидаПеревозки.РазрешеноИзменятьВГХ И НЕ сткНастройкиВидаПеревозки.РазрешеноИзменятьТоварныйСостав;
	Элементы.дрвРаботыПоТекущемуРейсуДосрочноеПрохождениеТочки.Видимость = сткНастройкиВидаПеревозки.РазрешеноДосрочноеУбытиеИзТочкиМаршрута;
	
КонецПроцедуры	

&НаКлиенте
// Процедура завершения после закрытия формы выбора проблемы.
//
Процедура ОбработатьЗакрытиеФормыВыбораПроблемы(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Если Объект.АдресаПогрузкиПоРейсу.Количество() = 0 Тогда
			ЗагрузитьАдресаПогрузкиПоРейсу();
		КонецЕсли;
		ТекущаяТочка = ПолучитьТекущуюТочку();
		упПрохождениеТочкиКлиентСервер.ОтменитьТочку(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), ТекущаяТочка, РезультатЗакрытия, Объект.АдресаПогрузкиПоРейсу, ТекущаяТочка.ПорядокТочки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДосрочноеПрохождениеТочкиНаКлиенте()
	ТекущаяТочка = ПолучитьТекущуюТочку();
	мсвПредшественников = СтрРазделить(ТекущаяТочка.Предшественники,",");
	Отказ = Ложь;
	Если мсвПредшественников.Количество() > 0 Тогда
		ЭлементыДерева = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
		Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
			Если ЭлементДерева.ИдентификаторТочки = ТекущаяТочка.ИдентификаторТочки Тогда
				Прервать;
			КонецЕсли;
			Если Истина
				И ЭлементДерева.Выполнена = 0 
				И Не ЭлементДерева.Отменена 
				И мсвПредшественников.Найти(Строка(ЭлементДерева.ИдентификаторТочки)) <> Неопределено 
			Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(СтрШаблон("ru = 'Точка (работы) не может быть выполнена, пока не выполнены или отменены работы по адресу ""%1""!'", ЭлементДерева.АдресТочки)), ,,,Отказ);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не Отказ Тогда
		сткДополнительныеСвойства = Новый Структура("Рейс", Объект.ТекущийРейс);
		упПрохождениеТочкиКлиентСервер.ОтменитьТочку(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), ТекущаяТочка, ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение"), , ТекущаяТочка.ПорядокТочки, сткДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьАдресаПогрузкиПоРейсу()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьАдресаПогрузкиПоРейсу();
	ЗначениеВРеквизитФормы(ОбработкаОбъект.АдресаПогрузкиПоРейсу, 	"Объект.АдресаПогрузкиПоРейсу");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОтменеТары(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.Вставить("ОтменитьТару", Истина);
		КоллекцияТочекМаршрута = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
		упПрохождениеТочкиКлиентСервер.ОтменитьРаботуПоТаре(ДополнительныеПараметры.ТекущиеДанные, ДополнительныеПараметры, Истина, КоллекцияТочекМаршрута, ДополнительныеПараметры.СтараяПроблема, ДополнительныеПараметры.СтарыйАдрес, Объект.ТекущийРейс);
		РаботыМодифицированы = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОтменеГрузовВТаре(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.Вставить("ОтменитьГрузовыеМестаВТаре", Истина);
		КоллекцияТочекМаршрута = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
		упПрохождениеТочкиКлиентСервер.ОтменитьРаботуПоТаре(ДополнительныеПараметры.ТекущиеДанные, ДополнительныеПараметры, Истина, КоллекцияТочекМаршрута, ДополнительныеПараметры.СтараяПроблема, ДополнительныеПараметры.СтарыйАдрес, Объект.ТекущийРейс);
		РаботыМодифицированы = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОписаниеОповещенияОЗакрытииФормыВводаВремени(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
		
		КоллекцияТочек = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
		
		Для ИндексТочки = 0 По КоллекцияТочек.Количество() - 1 Цикл
			
			ЭлементМассива = РезультатЗакрытия[ИндексТочки];
			
			Если НЕ ЭлементМассива.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементКоллекцииТочек = КоллекцияТочек.Получить(ИндексТочки);
			ЗаполнитьЗначенияСвойств(ЭлементКоллекцииТочек, ЭлементМассива); 
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ДеревоРаботыПоТекущемуРейсу

&НаСервере
Процедура СохранитьИсполнениеРейсаНаСервере(мсвИндексовИзмененныхТочек, ПерезаполнятьДеревоТочекРейса = Истина)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПрименитьИзмененияВДеревеТочекРейса(мсвИндексовИзмененныхТочек, ПерезаполнятьДеревоТочекРейса);
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсу, "Объект.дрвРаботыПоТекущемуРейсу");
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсуЭталон, "Объект.дрвРаботыПоТекущемуРейсуЭталон");
	ЗначениеВРеквизитФормы(ОбработкаОбъект.ТоварныйСоставГруза, "Объект.ТоварныйСоставГруза");
	Объект.ИдентификаторТочкиСИзмененнымСоставомГрузов = Неопределено;
	Объект.ИдентификаторТочкиСИзмененнымиВГХГрузов = Неопределено;
		
КонецПроцедуры

&НаСервере
Функция МассивИзмененныхТочек()
	
	текРезультат = Неопределено;
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	текРезультат = ОбработкаОбъект.ЕстьИзмененияВДеревеИсполненияРейсов();

	Возврат текРезультат;
	
КонецФункции

&НаКлиенте
Процедура РазвернутьУзлыДереваКонтроляИсполненияРейсов()
	АдресаЭлементы = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	Для каждого ЭлементАдрес Из АдресаЭлементы Цикл
		Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(ЭлементАдрес.ПолучитьИдентификатор(), Истина);	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтроку(мсвИндексов, ЭлементыДерева, ТекущийИндексМассива)
	
	ЭлементДерева = ЭлементыДерева.Получить(мсвИндексов[ТекущийИндексМассива]);
	КоллекцияЭлементов = ЭлементДерева.ПолучитьЭлементы();
	Если ТекущийИндексМассива = 0 ИЛИ КоллекцияЭлементов.Количество() = 0 Тогда
		Элементы.дрвРаботыПоТекущемуРейсу.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
	Иначе
		УстановитьТекущуюСтроку(мсвИндексов, КоллекцияЭлементов, ТекущийИндексМассива - 1);
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПолучитьИндексТекущейСтроки(мсвИндексов, ЭлементДерева)
	
	Если НЕ ЭлементДерева = Неопределено Тогда
		Родитель = ЭлементДерева.ПолучитьРодителя();
		
		ЭтоКорневойЭлемент = Ложь;
		
		Если Родитель = Неопределено Тогда
			Родитель = Объект.дрвРаботыПоТекущемуРейсу;
			ЭтоКорневойЭлемент = Истина;
		КонецЕсли;	
		
		текЭлементы = Родитель.ПолучитьЭлементы();
		
		Индекс = текЭлементы.Индекс(ЭлементДерева);
		
		мсвИндексов.Добавить(Индекс);
		
		Если НЕ ЭтоКорневойЭлемент Тогда
			ПолучитьИндексТекущейСтроки(мсвИндексов, Родитель);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьТочкуРазгрузки(сткВозврата)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ВосстановитьТочкуРазгрузки(сткВозврата);
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсу, 	"Объект.дрвРаботыПоТекущемуРейсу");
			
КонецПроцедуры

#КонецОбласти 

#Область КартографическийСервис

&НаСервере
Процедура УстановитьКартографическийСервис()
	HTMLДокумент			= упМаршрутизация.ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса();
	МаршрутРейсаHTMLСформирован = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьКартографическийСервис()
	HTMLДокумент	= упРаботаСКартамиКлиентСервер.ИнициализацияМакетаЗначениямиПоУмолчанию(HTMLДокумент, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРейсыНаКарте()

	Если ПоказыватьКарту Тогда
		Если МаршрутРейсаHTMLСформирован Тогда
			ОтобразитьРейсыНаКартеОбработчик();
		Иначе	
			ПодключитьОбработчикОжидания("ОтобразитьРейсыНаКартеОбработчик",1);		
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРейсыНаКартеОбработчик()

	Если МаршрутРейсаHTMLСформирован Тогда
		ОтключитьОбработчикОжидания("ОтобразитьРейсыНаКартеОбработчик");
		
		ОчиститьОбъекты();
		ВыполнитьПодготовкуМаршрутаРейса();
		ОтобразитьОбъектыНаКарте();
		
		спзВыделенныеРейсы = Новый СписокЗначений();
		спзВыделенныеРейсы.Добавить(Объект.ТекущийРейс);
		текИндексМаршрутаНаКарте = упРаботаСКартамиКлиентСервер.ОтобразитьРейсы(ЭтаФорма, спзВыделенныеРейсы, спзИндексыРейсовНаКарте, "Факт,План");	
		спзИндексыРейсовНаКарте.Очистить();
		спзИндексыРейсовНаКарте.Добавить(текИндексМаршрутаНаКарте);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиВремя(Команда)
	мсвСтрокиМаршрута = Новый Массив;
	сткСтрока = Новый Структура("Выполнена,
							|Отменена,
							|АдресТочки,
							|ПрибытиеФакт,
							|НачалоРаботФакт,
							|УбытиеФакт,
							|ИндексКартинки");
	Для каждого Строка Из  Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы() Цикл
		
		сткНоваяСтрока = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткСтрока);
		ЗаполнитьЗначенияСвойств(сткНоваяСтрока, Строка);
		мсвСтрокиМаршрута.Добавить(сткНоваяСтрока);
				
	КонецЦикла;

	сткПараметры = Новый Структура("Маршрут", мсвСтрокиМаршрута);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОписаниеОповещенияОЗакрытииФормыВводаВремени", ЭтаФорма);
	ОткрытьФорму("Обработка.упРМКонтрольИИсполнениеРейсов.Форма.ФормаВводВремени", сткПараметры,ЭтаФорма, УникальныйИдентификатор, , , ОписаниеОповещенияОЗакрытии);
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура ОбновитьВидимость()
	
	Элементы.ГруппаКарта.Видимость = ПоказыватьКарту;
	Если ПоказыватьКарту Тогда
		УстановитьКартографическийСервис();
		ИнициализироватьКартографическийСервис();
	КонецЕсли;
	Элементы.ГруппаПланировщик.Видимость = ПоказыватьПланировщик;
	Элементы.СписокРейсовЕстьФайлы.Видимость = ПроверятьНаличиеФайлов;
	Элементы.СписокРейсовЕстьПроблемы.Видимость = ПроверятьРаботыСПроблемами;
	Элементы.СписокРейсовЕстьОпозданияПрибытияВТочку.Видимость = ПроверятьОпозданияВТочкиЗаданий;
	Элементы.СписокРейсовЕстьСообщенияОтВодителяНеОтвеченные.Видимость = ПроверятьНеотвеченныеСообщенияВодителя;
	Элементы.СписокРейсовЕстьИзмененныйПорядокЗаданий.Видимость = ПроверятьИзмененныйПорядокЗаданий;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокРейсовПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти 