
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОписаниеОшибки 	= "";
	Отказ 			= Ложь;

	упПрохождениеТочки.ПроверитьСпособПрохожденияМаршрута(ПараметрКоманды, ОписаниеОшибки, Отказ);
	
	Если НЕ Отказ Тогда
		
		текПроисшествиеВПути = СформироватьПроисшествиеВПутиНаСервере(ПараметрКоманды, ОписаниеОшибки, Отказ); 	
		
		Если НЕ Отказ Тогда
			ПараметрыФормы = Новый Структура("Ключ", текПроисшествиеВПути);
			ОткрытьФорму("Документ.упПроисшествиеВПути.Форма.ФормаДокумента", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Происшествие в пути не сформировано.'"),, ОписаниеОшибки, БиблиотекаКартинок.Ошибка32);			
		КонецЕсли;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Происшествие в пути не сформировано.'"),, ОписаниеОшибки, БиблиотекаКартинок.Ошибка32);			
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьПроисшествиеВПутиНаСервере(Рейс, ОписаниеОшибки, Отказ)
	
	текРезультат = Неопределено;
	
	ПрохождениеТочки = упПрохождениеТочки.ПоследнееПрохождениеТочки(Рейс);
	Если ЗначениеЗаполнено(ПрохождениеТочки) Тогда
		текРезультат = Документы.упПроисшествиеВПути.СформироватьНаОснованииПрохожденияТочки(ПрохождениеТочки, ОписаниеОшибки, Отказ);
	Иначе
		 ТекстСообщения = Нстр("ru = 'По рейсу нет пройденных точек. Сформируйте прохождение точки'");
		 ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	 КонецЕсли;
	 
	 Возврат текРезультат;
	
КонецФункции

#КонецОбласти 