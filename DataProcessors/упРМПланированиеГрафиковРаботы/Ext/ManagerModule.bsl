#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Графики работы
//
// Параметры:
//  НачалоПериода					 - 	 - 
//  ОкончаниеПериода				 - 	 - 
//  СхемаКомпоновкиДанных			 - 	 - 
//  КомпоновщикНастроекКомпоновкиДанных	 - 	 - 
//  ТипОбъекта						 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ГрафикиРаботы(НачалоПериода, ОкончаниеПериода, СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных, ТипОбъекта) Экспорт

	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки();
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Объекты = Результат.ВыгрузитьКолонку("Ссылка");
	
	сткРезультат = Новый Структура();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаГрафикиПоОбъекту(ТипОбъекта);
	Запрос.УстановитьПараметр("Объекты", Объекты);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	
	мсвРезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	сткРезультат.Вставить("Графики", мсвРезультатыЗапроса[2].Выгрузить());
	сткРезультат.Вставить("Объекты", мсвРезультатыЗапроса[1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам));
	
	Возврат сткРезультат;
		
КонецФункции

Процедура ЗаполнитьГрафикиРаботПоШаблону(СписокОбъектов, ТипОбъекта, ДатаНачала, ДатаОкончания, ШаблонГрафика, Описание, Автор, ОчищатьТекущиеЗаписи, Отказ) Экспорт
	
	Если СписокОбъектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроконтролироватьУказанныйПериод(ДатаНачала, ДатаОкончания, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Если ОчищатьТекущиеЗаписи Тогда
		УдалитьГрафикиРаботыПоОбъектам(СписокОбъектов, ТипОбъекта, ДатаНачала, ДатаОкончания, , Отказ);		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ШаблонЗаполнения.НомерСтроки КАК НомерДня,
	|	РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ЕСТЬNULL(РасписаниеРаботы.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)),СЕКУНДА) КАК ВремяНачала,
	|	РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ЕСТЬNULL(РасписаниеРаботы.ВремяОкончания, ДАТАВРЕМЯ(1, 1, 1, 23, 59, 59)),СЕКУНДА)  КАК ВремяОкончания
	|ИЗ
	|	Справочник.упШаблоныГрафиковРаботы.ШаблонЗаполнения КАК ШаблонЗаполнения
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упШаблоныГрафиковРаботы.РасписаниеРаботы КАК РасписаниеРаботы
	|	ПО ИСТИНА
	|		И ШаблонЗаполнения.НомерСтроки = РасписаниеРаботы.НомерДня
	|		И ШаблонЗаполнения.Ссылка = РасписаниеРаботы.Ссылка
	|ГДЕ ИСТИНА
	|	И ШаблонЗаполнения.Ссылка = &ШаблонГрафикаРаботы
	|	И ШаблонЗаполнения.ДеньВключенВГрафик
	|";
	Запрос.УстановитьПараметр("ШаблонГрафикаРаботы", ШаблонГрафика);
	
	РасписаниеРаботы = Запрос.Выполнить().Выгрузить();
	
	РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ШаблонГрафика, "СпособЗаполнения, ДлинаЦикла");
	СпособЗаполненияШаблона = РеквизитыШаблона.СпособЗаполнения;
	ДлинаЦикла = РеквизитыШаблона.ДлинаЦикла;
	
	Период = НачалоДня(ДатаНачала);
	
	НомерДня = 0;
	
	Пока Период < ДатаОкончания Цикл
		
		Если СпособЗаполненияШаблона = Перечисления.упСпособыЗаполненияГрафикаРаботы.ПоДнямНедели Тогда
			НомерДня = ДеньНедели(Период);
		Иначе	
			НомерДня = НомерДня + 1;
			Если НомерДня > ДлинаЦикла Тогда
				НомерДня = 1;
			КонецЕсли;
		КонецЕсли;
		
		мсвСтрокиРасписания = РасписаниеРаботы.НайтиСтроки(Новый Структура("НомерДня", НомерДня));
		
		Для каждого СтрокаРасписание Из мсвСтрокиРасписания Цикл
			
			Запись = РегистрыСведений.упГрафикиРаботыСотрудников.СоздатьМенеджерЗаписи();
			
			Для каждого ОбъектТекущий Из СписокОбъектов Цикл
				ТекущаяДатаНачала = Период + СтрокаРасписание.ВремяНачала;
				ТекущаяДатаОкончания = Период + СтрокаРасписание.ВремяОкончания;
				Запись.Сотрудник = ОбъектТекущий;
				Запись.ДатаНачала = ТекущаяДатаНачала;
				Запись.ДатаОкончания = ТекущаяДатаОкончания;
				Запись.Период = Период;
				
				Запись.Прочитать();
				
				Запись.Период = Период;
				Запись.Сотрудник = ОбъектТекущий;
				Запись.ДатаНачала = ТекущаяДатаНачала;
				Запись.ДатаОкончания = ТекущаяДатаОкончания;
				
				Запись.Шаблон = ШаблонГрафика;
				Запись.Автор = Автор;
				Запись.Описание = Описание;
				
				Попытка
					Запись.Записать();
				Исключение
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
					ЗаписьЖурналаРегистрации(Нстр("ru = 'Ошибка при заполнении графиков шаблонами'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				КонецПопытки;
				Если Отказ Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		Период = Период + 3600 * 24;
		
	КонецЦикла;
	
	ПроконтролироватьНаложениеПериодовДействияШаблоновГрафиков(ДатаНачала ,ДатаОкончания, СписокОбъектов, ТипОбъекта, Отказ);	
			
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе	
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьГрафикРаботыПоОбъекту(Объект, ТипОбъекта, ДатаНачалаИсходная = Неопределено, ДатаОкончанияИсходная = Неопределено, ДатаНачала, ДатаОкончания, Описание, Автор, ШаблонГрафика = Неопределено, Цвет, Отказ) Экспорт
	
	Если Истина
		И ЗначениеЗаполнено(ДатаНачалаИсходная)
		И ЗначениеЗаполнено(ДатаОкончанияИсходная)
	Тогда
		ПроконтролироватьУказанныйПериод(ДатаНачалаИсходная, ДатаОкончанияИсходная, Отказ);
	КонецЕсли;
	ПроконтролироватьУказанныйПериод(ДатаНачала, ДатаОкончания, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
			
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	ПараметрыРегистра = ПараметрыРегистра(Объект);
	
	Запись = РегистрыСведений[ПараметрыРегистра.ИмяРегистра].СоздатьМенеджерЗаписи();
	
	// Если редактируется запись, то исходную запись следует удалить.
	Если ДатаНачалаИсходная <> Неопределено Тогда
		УдалитьГрафикРаботыПоОбъекту(Объект, ДатаНачалаИсходная, ДатаОкончанияИсходная, НачалоДня(ДатаНачалаИсходная));
	КонецЕсли;
		
	Период = НачалоДня(ДатаНачала);
	
	ДатаНачалаТекущая = ДатаНачала;
	
	Пока Период < ДатаОкончания Цикл

		Если ДатаОкончания < КонецДня(ДатаНачалаТекущая) Тогда
			ДатаОкончанияТекущая = ДатаОкончания;
		Иначе	
			ДатаОкончанияТекущая = КонецДня(ДатаНачалаТекущая);
		КонецЕсли;
		
		Запись[ПараметрыРегистра.ИмяОбъекта] = Объект;
		Запись.ДатаНачала = ДатаНачалаТекущая;
		Запись.ДатаОкончания = ДатаОкончанияТекущая;
		Запись.Период = Период;
		
		Запись.Прочитать();
		
		Запись[ПараметрыРегистра.ИмяОбъекта] = Объект;
		Запись.ДатаНачала = ДатаНачалаТекущая;
		Запись.ДатаОкончания = ДатаОкончанияТекущая;
		Запись.Период = Период;
		Запись.Шаблон = ?(ШаблонГрафика = Неопределено, Справочники.упШаблоныГрафиковРаботы.ПустаяСсылка(), ШаблонГрафика);
		Запись.Автор = Автор;
		Запись.Описание = Описание;
		Запись.Цвет = Цвет;
		
		Попытка
			Запись.Записать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			ЗаписьЖурналаРегистрации(Нстр("ru = 'Ошибка при заполнении графиков шаблонами'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	
		Период = Период + 3600 * 24;
		ДатаНачалаТекущая = Период;
	КонецЦикла;
	
	ПроконтролироватьНаложениеПериодовДействияШаблоновГрафиков(ДатаНачала ,ДатаОкончания, Объект, ТипОбъекта, Отказ);	
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе	
		ОтменитьТранзакцию();
	КонецЕсли;
			
КонецПроцедуры

Процедура ПроконтролироватьУказанныйПериод(ДатаНачала, ДатаОкончания, Отказ) Экспорт
	
	Если Истина
		И Не Отказ
		И ДатаОкончания < ДатаНачала
	Тогда
		ТекстСообщения = Нстр("ru = 'Дата начала периода больше даты окончания'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	// Проверить, что не редактируется прошлый период.
	Если Истина
		И Не Отказ
		И ДатаНачала < НачалоДня(ТекущаяДатаСеанса())-1
	Тогда
		ТекстСообщения = Нстр("ru = 'Нельзя изменять записи прошлых периодов'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	// Проверить, что не указан период протяженностью более года.
	Если Истина
		И Не Отказ
		И ДатаОкончания - ДатаНачала > 366*24*3600
	Тогда
		ТекстСообщения = Нстр("ru = 'Заданный период более одного года'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
		
КонецПроцедуры

Процедура УдалитьГрафикиРаботыПоОбъектам(СписокОбъектов, ТипОбъекта, ДатаНачала, ДатаОкончания, ВТранзакции = Ложь , Отказ) Экспорт
	
	Если ВТранзакции Тогда
		ПроконтролироватьУказанныйПериод(ДатаНачала, ДатаОкончания, Отказ);
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ВТранзакции Тогда
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);	
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаГрафикиПоОбъектуУдалить(ТипОбъекта);
	Запрос.УстановитьПараметр("Объекты", СписокОбъектов);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецДня(ДатаОкончания));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		УдалитьГрафикРаботыПоОбъекту(Выборка.Ссылка, Выборка.ДатаНачала, Выборка.ДатаОкончания, Выборка.Период);
	КонецЦикла; 
	
	Если ВТранзакции Тогда
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		Иначе	
			ОтменитьТранзакцию();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьГрафикРаботыПоОбъекту(Объект, ДатаНачала, ДатаОкончания, Период) Экспорт
	
	ПараметрыРегистра = ПараметрыРегистра(Объект);
	
	Запись = РегистрыСведений[ПараметрыРегистра.ИмяРегистра].СоздатьМенеджерЗаписи();
	Запись[ПараметрыРегистра.ИмяОбъекта] = Объект;
	Запись.ДатаНачала = ДатаНачала;
	Запись.ДатаОкончания = ДатаОкончания;
	Запись.Период = Период;
	
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		Запись.Удалить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроконтролироватьНаложениеПериодовДействияШаблоновГрафиков(ДатаНачала, ДатаОкончания, СписокОбъектов, ТипОбъекта, Отказ)
	Если ТипЗнч(ТипОбъекта) = Тип("СправочникСсылка.упСотрудники") Тогда
		РегистрыСведений.упГрафикиРаботыСотрудников.ПроконтролироватьНаложениеПериодовДействияШаблоновГрафиков(ДатаНачала ,ДатаОкончания, СписокОбъектов, Отказ);	
	КонецЕсли;
КонецПроцедуры

Функция ПараметрыРегистра(ОбъектЗаписи)
	
	Результат = Новый Структура("ИмяРегистра, ИмяОбъекта", "", "");
	
	Если ТипЗнч(ОбъектЗаписи) = Тип("СправочникСсылка.упСотрудники") Тогда
		Результат.Вставить("ИмяРегистра", "упГрафикиРаботыСотрудников");	
		Результат.Вставить("ИмяОбъекта", "Сотрудник");	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаГрафикиПоОбъекту(ОбъектЗаписи)
	
	Результат = "";
	
	Если ТипЗнч(ОбъектЗаписи) = Тип("СправочникСсылка.упСотрудники") Тогда
		Результат = 
		
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГрафикиРаботы.ДатаНачала КАК ДатаНачала,
		|	ГрафикиРаботы.ДатаОкончания КАК ДатаОкончания,
		|	ГрафикиРаботы.Сотрудник КАК Ссылка,
		|	ГрафикиРаботы.Цвет КАК Цвет,
		|	ГрафикиРаботы.Описание КАК Описание,
		|	ГрафикиРаботы.Шаблон КАК Шаблон,
		|	ГрафикиРаботы.Период КАК Период,
		|	упШаблоныГрафиковРаботыТ.Цвет КАК ЦветШаблона,
		|	упШаблоныГрафиковРаботыТ.СтильМаркера КАК СтильМаркера
		|ПОМЕСТИТЬ втГрафикиРаботы
		|ИЗ
		|	РегистрСведений.упГрафикиРаботыСотрудников КАК ГрафикиРаботы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упШаблоныГрафиковРаботы КАК упШаблоныГрафиковРаботыТ
		|	ПО упШаблоныГрафиковРаботыТ.Ссылка = ГрафикиРаботы.Шаблон
		|ГДЕ ИСТИНА
		|	И ГрафикиРаботы.Сотрудник В (&Объекты)
		|	И ГрафикиРаботы.Период >= &НачалоПериода
		|	И ГрафикиРаботы.Период <= &ОкончаниеПериода
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|		КОГДА втГрафикиРаботыТ.Период ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ) КАК КоличествоИнтервалов,
		|	втГрафикиРаботыТ.Период КАК Период,
		|	упСотрудникиТ.Ссылка КАК Ссылка,
		|	упСотрудникиТ.Наименование КАК Наименование
		|ИЗ
		|	Справочник.упСотрудники КАК упСотрудникиТ
		|	ЛЕВОЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботыТ
		|	ПО упСотрудникиТ.Ссылка = втГрафикиРаботыТ.Ссылка
		|ГДЕ ИСТИНА
		|	И упСотрудникиТ.Ссылка В (&Объекты)
		|СГРУППИРОВАТЬ ПО
		|	втГрафикиРаботыТ.Период,
		|	упСотрудникиТ.Ссылка,
		|	упСотрудникиТ.Наименование
		|УПОРЯДОЧИТЬ ПО
		|	Наименование
		|ИТОГИ
		|	МАКСИМУМ(КоличествоИнтервалов) КАК КоличествоИнтервалов
		|ПО
		|	ОБЩИЕ,
		|	Ссылка
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втГрафикиРаботыТ.ДатаНачала КАК ДатаНачала,
		|	втГрафикиРаботыТ.ДатаОкончания КАК ДатаОкончания,
		|	втГрафикиРаботыТ.Описание КАК Описание,
		|	втГрафикиРаботыТ.Период КАК Период,
		|	втГрафикиРаботыТ.Ссылка КАК Ссылка,
		|	втГрафикиРаботыТ.СтильМаркера КАК СтильМаркера,
		|	втГрафикиРаботыТ.Цвет КАК Цвет,
		|	втГрафикиРаботыТ.ЦветШаблона КАК ЦветШаблона,
		|	втГрафикиРаботыТ.Шаблон КАК Шаблон
		|ИЗ
		|	втГрафикиРаботы КАК втГрафикиРаботыТ
		|";

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаГрафикиПоОбъектуУдалить(ОбъектЗаписи)
	
	Результат = "";
	
	Если ТипЗнч(ОбъектЗаписи) = Тип("СправочникСсылка.упСотрудники") Тогда
		Результат = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГрафикиРаботы.ДатаНачала КАК ДатаНачала,
		|	ГрафикиРаботы.ДатаОкончания КАК ДатаОкончания,
		|	ГрафикиРаботы.Сотрудник КАК Ссылка,
		|	ГрафикиРаботы.Цвет КАК Цвет,
		|	ГрафикиРаботы.Описание КАК Описание,
		|	ГрафикиРаботы.Шаблон КАК Шаблон,
		|	ГрафикиРаботы.Период КАК Период,
		|	упШаблоныГрафиковРаботыТ.Цвет КАК ЦветШаблона,
		|	упШаблоныГрафиковРаботыТ.СтильМаркера КАК СтильМаркера
		|ИЗ
		|	РегистрСведений.упГрафикиРаботыСотрудников КАК ГрафикиРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упШаблоныГрафиковРаботы КАК упШаблоныГрафиковРаботыТ
		|		ПО упШаблоныГрафиковРаботыТ.Ссылка = ГрафикиРаботы.Шаблон
		|ГДЕ ИСТИНА
		|	И ГрафикиРаботы.Сотрудник В (&Объекты)
		|	И ГрафикиРаботы.Период >= &НачалоПериода
		|	И ГрафикиРаботы.Период <= &ОкончаниеПериода
		|";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли