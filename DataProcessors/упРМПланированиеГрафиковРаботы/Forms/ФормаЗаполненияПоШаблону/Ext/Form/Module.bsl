#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Параметры.ТекущийПользователь;
	СписокОбъектовТекущий = Параметры.СписокОбъектов;
	ДатаНачала = Параметры.ДатаНачала;
	Если ДатаНачала < ТекущаяДатаСеанса() Тогда
		ДатаНачала = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	ДатаОкончания = Параметры.ДатаОкончания;
	Действие = Параметры.Действие;
	ТипИзмеренияПланировщика = Параметры.ТипИзмеренияПланировщика;
	
	Если Действие = "Удалить" Тогда
		Элементы.ОчищатьТекущиеЗаписи.Видимость = Ложь;
		Элементы.ШаблонГрафикаРаботы.Видимость = Ложь;
		Элементы.Описание.Видимость = Ложь;
		Элементы.ФормаЗаписатьИЗакрыть.Заголовок = Нстр("ru = 'Удалить и закрыть'");
	КонецЕсли;
	
	Для каждого ОбъектТекущий Из СписокОбъектовТекущий Цикл
		НоваяСтрока = СписокОбъектов.Добавить();
		НоваяСтрока.Объект = ОбъектТекущий.Значение;
		НоваяСтрока.Пометка = ОбъектТекущий.Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Действие = "Удалить" Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ШаблонГрафикаРаботы");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокОтметитьВсе(Команда)
	Для каждого Строка Из СписокОбъектов Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокСнятьВсе(Команда)
	Для каждого Строка Из СписокОбъектов Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Отказ = Ложь;
	Если ПроверитьЗаполнение() Тогда
		Если Действие = "Заполнить" Тогда
			ЗаполнитьГрафикиРаботШаблонамиНаСервере(Отказ);
		Иначе	
			УдалитьГрафикиРаботНаСервере(Отказ);
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			Закрыть(Истина);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПериод(Команда)
	упСервисныеФункцииКлиент.РедактироватьПериод(ЭтаФорма, Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачала", "ДатаОкончания"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьГрафикиРаботШаблонамиНаСервере(Отказ)
	Обработки.упРМПланированиеГрафиковРаботы.ЗаполнитьГрафикиРаботПоШаблону(СписокОтмеченныхОбъектов(), Объект.ТипИзмеренияПланировщика, ДатаНачала, ДатаОкончания, ШаблонГрафикаРаботы, Описание, ТекущийПользователь, ОчищатьТекущиеЗаписи, Отказ);
КонецПроцедуры

&НаСервере
Процедура УдалитьГрафикиРаботНаСервере(Отказ)
	Обработки.упРМПланированиеГрафиковРаботы.УдалитьГрафикиРаботыПоОбъектам(СписокОтмеченныхОбъектов(), Объект.ТипИзмеренияПланировщика, ДатаНачала, ДатаОкончания, Истина, Отказ);	
КонецПроцедуры

&НаСервере
Функция СписокОтмеченныхОбъектов()
	Результат = Новый Массив;
	Для каждого Строка Из СписокОбъектов Цикл
		Если Строка.Пометка Тогда
			Результат.Добавить(Строка.Объект);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат
КонецФункции

#КонецОбласти

