#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.ТипИзмеренияПланировщика = ПредопределенноеЗначение("Справочник.упСотрудники.ПустаяСсылка");	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ИнициализацияПараметровИзмерения();
	
	ИнициализироватьКомпоновщикНастроек();
	УстановитьДоступныеЗначенияВариантовСписков();
	ВосстановитьОбщиеНастройкиФормы();

	УстановитьИнтервалМесяцСервер();
	ЗаполнитьПланировщикНаСервере();
	
	РазрешеноРучноеРедактирование = Ложь;
	Элементы.РазрешеноРучноеРедактирование.Пометка = РазрешеноРучноеРедактирование;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВариантНастроекПриИзменении(Элемент = Неопределено)

	Если ЗначениеЗаполнено(ВариантСписков) Тогда
		сткНастройки = ВернутьВариантНастройкиСпискаОбъектов();
		Если сткНастройки <> Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
		КонецЕсли;
	Иначе
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	ЗаполнитьПланировщикНаСервере();
	СохранитьОбщиеНастройкиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если РазрешеноРучноеРедактирование Тогда
		ОбработатьВыделенныйЭлемент(Элемент);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если РазрешеноРучноеРедактирование Тогда
		сткПараметры = Новый Структура();
		сткПараметры.Вставить("Новая", Ложь);
		сткПараметры.Вставить("ДатаНачала", Начало);
		сткПараметры.Вставить("ДатаОкончания", Конец);
		сткПараметры.Вставить("ТипИзмеренияПланировщика", Объект.ТипИзмеренияПланировщика);
		сткПараметры.Вставить("ТекущийПользователь", ТекущийПользователь);
		сткПараметры.Вставить("ОбъектТекущий", Значения[КонтекстФормы.ИмяИзмерения]);
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыЗаполнитьРучноеИзменение", ЭтаФорма);
		ОткрытьФорму("Обработка.упРМПланированиеГрафиковРаботы.Форма.ФормаЗаполненияРучноеИзменение",сткПараметры,ЭтаФорма, УникальныйИдентификатор,,,ОписаниеОповещенияОЗакрытии);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() Тогда
		ВыделенныйЭлемент = ВыделенныеЭлементы[0];
		УдалитьГрафикРаботыПоОбъекту(ВыделенныйЭлемент.ЗначенияИзмерений[КонтекстФормы.ИмяИзмерения], ВыделенныйЭлемент.Начало, ВыделенныйЭлемент.Конец, Отказ);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если РазрешеноРучноеРедактирование Тогда
		ОбработатьВыделенныйЭлемент(Элемент);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если РазрешеноРучноеРедактирование Тогда
		ОбработатьВыделенныйЭлемент(Элемент);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВариантСписковОчистка(Элемент, СтандартнаяОбработка)
	Планировщик.Элементы.Очистить();
	Планировщик.Измерения.Очистить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьИнтервалДень(Команда)
	
	УстановитьИнтервалДеньСервер();
	ЗаполнитьПланировщикНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалНеделя(Команда)

	УстановитьИнтервалНеделяСервер();
	ЗаполнитьПланировщикНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалМесяц(Команда)
	
	УстановитьИнтервалМесяцСервер();
	ЗаполнитьПланировщикНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура Отбор(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Настройки", КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Настройка отбора списка объектов'"));
	
	ОткрытьФорму("ОбщаяФорма.упФормаНастройкиОтбора", ПараметрыФормы,,,,, Новый ОписаниеОповещения("НастройкаОтбораОбъектовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьВариантНастройкиСпискаОбъектовРемонта(Команда)
	
	СохранитьВариантНастройки(КонтекстФормы.КлючНастройкиСпискаОбъектов, "СохранитьВариантНастройкиСпискаОбъектовНаСервере", ВариантСписков);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройкиСпискаОбъектовРемонта(Команда)
	
	ОткрытьВариантНастройки(КонтекстФормы.КлючНастройкиСпискаОбъектов, "ОткрытьВариантНастройкиСпискаОбъектовНаКлиенте", ВариантСписков);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПланировщика(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("СписокОбъектов", СписокОбъектов);
	сткПараметры.Вставить("Действие", "Заполнить");
	сткПараметры.Вставить("ТекущийПользователь", ТекущийПользователь);
	сткПараметры.Вставить("ТипИзмеренияПланировщика", Объект.ТипИзмеренияПланировщика);
	сткПараметры.Вставить("ДатаНачала", Планировщик.ТекущиеПериодыОтображения[0].Начало);
	сткПараметры.Вставить("ДатаОкончания", Планировщик.ТекущиеПериодыОтображения[0].Конец);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыЗаполнитьПоШаблону", ЭтаФорма);
	ОткрытьФорму("Обработка.упРМПланированиеГрафиковРаботы.Форма.ФормаЗаполненияПоШаблону",сткПараметры,ЭтаФорма, УникальныйИдентификатор,,,ОписаниеОповещенияОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("СписокОбъектов", СписокОбъектов);
	сткПараметры.Вставить("Действие", "Удалить");
	сткПараметры.Вставить("ТекущийПользователь", ТекущийПользователь);
	сткПараметры.Вставить("ТипИзмеренияПланировщика", Объект.ТипИзмеренияПланировщика);
	сткПараметры.Вставить("ДатаНачала", Планировщик.ТекущиеПериодыОтображения[0].Начало);
	сткПараметры.Вставить("ДатаОкончания", Планировщик.ТекущиеПериодыОтображения[0].Конец);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыЗаполнитьПоШаблону", ЭтаФорма);
	ОткрытьФорму("Обработка.упРМПланированиеГрафиковРаботы.Форма.ФормаЗаполненияПоШаблону",сткПараметры,ЭтаФорма, УникальныйИдентификатор,,,ОписаниеОповещенияОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура ПериодВперед(Команда)
	ИзменитьПериод();
КонецПроцедуры

&НаКлиенте
Процедура ПериодНазад(Команда)
	ИзменитьПериод(Ложь);               	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображениеНаДату(Команда)
	ТекстЗаголовка = НСтр("ru = 'Укажите дату, входящую в желаемый интервал планирования'");
	ПоказатьВводДаты(Новый ОписаниеОповещения("ВводДатыОтображенияЗавершение", ЭтаФорма), Планировщик.ТекущиеПериодыОтображения[0].Начало, ТекстЗаголовка, ЧастиДаты.Дата); 
КонецПроцедуры

&НаКлиенте
Процедура РазрешеноРучноеРедактирование(Команда)
	РазрешеноРучноеРедактирование = НЕ РазрешеноРучноеРедактирование;
	Элементы.РазрешеноРучноеРедактирование.Пометка = РазрешеноРучноеРедактирование;
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеРаботыСотрудников(Команда)
	НачалоПериода = Планировщик.ТекущиеПериодыОтображения[0].Начало;
	ОкончаниеПериода = Планировщик.ТекущиеПериодыОтображения[0].Конец;

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СписокОбъектов", СписокОбъектов);
	ПараметрыФормы.Вставить("ДатаНачала", НачалоПериода);
	ПараметрыФормы.Вставить("ДатаОкончания", ОкончаниеПериода);
	ВидШкалы = 0;
	Если Элементы.УстановитьИнтервалНеделя.Пометка Тогда
		ВидШкалы = 1;	
	ИначеЕсли Элементы.УстановитьИнтервалМесяц.Пометка Тогда
		ВидШкалы = 2;	
	КонецЕсли;
	ПараметрыФормы.Вставить("ВидШкалы", ВидШкалы);
	ОткрытьФорму("Отчет.упРасписаниеРаботыСотрудников.Форма", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьИнтервалДеньСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоДня(ТекущаяДата), КонецДня(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	ПервыйЭлемент.Формат = "ДФ=""дд ММММ гггг 'г.' (dddd)""";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат  = "ДФ=HH:mm";
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Час;
	Планировщик.КратностьПериодическогоВарианта = 24;
	
	Элементы.УстановитьИнтервалДень.Пометка   = Истина;
	Элементы.УстановитьИнтервалНеделя.Пометка = Ложь;
	Элементы.УстановитьИнтервалМесяц.Пометка  = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтервалНеделяСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоНедели(ТекущаяДата), КонецНедели(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	ПервыйЭлемент.Формат  = "ДФ = 'dd.MM'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат  = "ДФ = 'dddd'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = ЦветаСтиля.ЦветФонаПоля;
	НовыйЭлементШкалы.ОтображатьПериодическиеМетки = Истина;
	НовыйЭлементШкалы.Формат  = "ДФ=HH";

	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Неделя;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.УстановитьИнтервалДень.Пометка = Ложь;
	Элементы.УстановитьИнтервалНеделя.Пометка = Истина;
	Элементы.УстановитьИнтервалМесяц.Пометка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтервалМесяцСервер(ТекущаяДата = Неопределено)

	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли; 
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоМесяца(ТекущаяДата), КонецМесяца(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.Формат = "ДФ = 'MMMM yyyy'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.Формат = "ДФ = 'dd'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат = "ДФ = 'ddd'";
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Месяц;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.УстановитьИнтервалДень.Пометка   = Ложь;
	Элементы.УстановитьИнтервалНеделя.Пометка = Ложь;
	Элементы.УстановитьИнтервалМесяц.Пометка  = Истина;

КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	Если ПроверитьЗаполнение() Тогда
		ЗаполнитьПланировщикНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПланировщикНаСервере()
	
	Если ЗначениеЗаполнено(ВариантСписков) Тогда

		СхемаКомпоновкиДанных = Обработки.упРМПланированиеГрафиковРаботы.ПолучитьМакет(КонтекстФормы.ИмяМакета);
		
		НачалоПериода = Планировщик.ТекущиеПериодыОтображения[0].Начало;
		ОкончаниеПериода = Планировщик.ТекущиеПериодыОтображения[0].Конец;
		
		сткДанные = Обработки.упРМПланированиеГрафиковРаботы.ГрафикиРаботы(НачалоПериода, ОкончаниеПериода, СхемаКомпоновкиДанных, КомпоновщикНастроек, Объект.ТипИзмеренияПланировщика);
		Графики = сткДанные.Графики;
		Объекты = сткДанные.Объекты;
		
		Измерения = Планировщик.Измерения;
		Измерения.Очистить();
		СписокОбъектов.Очистить();
		
		Измерение = Измерения.Добавить(КонтекстФормы.ИмяИзмерения);
		
		Для каждого СтрокаОбщиеИтоги Из Объекты.Строки Цикл
			Для каждого СтрокаОбъект Из СтрокаОбщиеИтоги.Строки Цикл
				СписокОбъектов.Добавить(СтрокаОбъект.Ссылка);
				ЭлементИзмерения = Измерение.Элементы.Добавить(СтрокаОбъект.Ссылка);
				ЭлементИзмерения.Текст = ?(Элементы.УстановитьИнтервалМесяц.Пометка,  упСервисныеФункции.УбратьНадписьЕщеИзЯчеекПланировщика(СтрокаОбъект.Ссылка, СтрокаОбщиеИтоги.КоличествоИнтервалов), СтрокаОбъект.Ссылка);
			КонецЦикла;
		КонецЦикла;
		
		Планировщик.Элементы.Очистить();
		КартинкиМаркеров = Новый Массив;
		СтилиМаркеров = Новый Массив;
		
		Для каждого Строка Из Графики Цикл
			
			соотИзмерений = Новый Соответствие;
			соотИзмерений.Вставить(КонтекстФормы.ИмяИзмерения, Строка.Ссылка);
			
			НовыйЭлементПланировщика = Планировщик.Элементы.Добавить(Строка.ДатаНачала, Строка.ДатаОкончания);
			НовыйЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(соотИзмерений);
			НовыйЭлементПланировщика.Значение = Строка.Шаблон;
			НовыйЭлементПланировщика.Текст = Строка.Описание;
			//НовыйЭлементПланировщика.Подсказка  = ПодсказкаЭлементаПланировщика(СтрокаТочкаМаршрута);
			Если ЗначениеЗаполнено(Строка.Цвет) Тогда
				НовыйЭлементПланировщика.ЦветФона = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(Строка.Цвет);		
			ИначеЕсли ЗначениеЗаполнено(Строка.ЦветШаблона) Тогда
				НовыйЭлементПланировщика.ЦветФона = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(Строка.ЦветШаблона);			
			КонецЕсли;
			
			Секунд = 3600 * Час(Строка.ДатаОкончания) + 60 * Минута(Строка.ДатаОкончания) + Секунда(Строка.ДатаОкончания) 
			- 3600 * Час(Строка.ДатаНачала) - 60 * Минута(Строка.ДатаНачала) - Секунда(Строка.ДатаНачала);
			
			Часов = Окр(Секунд / 3600, 1);
			
			НовыйЭлементПланировщика.Подсказка = СтрШаблон(Нстр("ru = '%1 ч.(%2:%3-%4:%5)'"), 
			Часов, 
			СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Час(Строка.ДатаНачала), 2, "0"),
			СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Минута(Строка.ДатаНачала), 2, "0"), 
			СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Час(Строка.ДатаОкончания), 2, "0"), 
			СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Минута(Строка.ДатаОкончания), 2, "0")
			);
			
			СтильМаркера = Строка.СтильМаркера;
			Если ЗначениеЗаполнено(СтильМаркера) Тогда
				ИндексКартинки = СтилиМаркеров.Найти(СтильМаркера);
				Если ИндексКартинки = Неопределено Тогда
					
					СтилиМаркеров.Добавить(СтильМаркера);
					
					РеквизитыМаркера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтильМаркера, "Картинка, ХранилищеЗначения");
					Если РеквизитыМаркера.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
						КартинкаМаркера = ПоместитьВоВременноеХранилище(РеквизитыМаркера.ХранилищеЗначения.Получить(), УникальныйИдентификатор);
					Иначе
						КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(РеквизитыМаркера.Картинка);
					КонецЕсли;
					КартинкиМаркеров.Добавить(КартинкаМаркера);
					
				Иначе	
					КартинкаМаркера = КартинкиМаркеров.Получить(ИндексКартинки);	
				КонецЕсли;
				
				НовыйЭлементПланировщика.Картинка = КартинкаМаркера;
			КонецЕсли;
			
		КонецЦикла;
//		упСервисныеФункции.УбратьНадписьЕщеИзЯчеекПланировщика(Планировщик);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияПараметровИзмерения()
	
	КонтекстФормы = Новый Структура();
	
	Если ТипЗнч(Объект.ТипИзмеренияПланировщика) = Тип("СправочникСсылка.упСотрудники") Тогда
		КонтекстФормы.Вставить("ИмяИзмерения", "Сотрудник");
		КонтекстФормы.Вставить("ИмяМакета", "Сотрудники");
	КонецЕсли;             
	
	КонтекстФормы.Вставить("КлючОбъекта", "Обработка.упРМПланированиеГрафиковРаботы");
	КонтекстФормы.Вставить("КлючНастройкиСпискаОбъектов", "НастройкаСпискаОбъектов");
	КонтекстФормы.Вставить("КлючНастройкиОбщие", "ОбщиеНастройки")
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВариантНастройки(Знач ТипНастройки, ИмяОбработчикаОповещения, ВариантНастройки)
	
	сткПараметры = Новый Структура("КлючОбъекта, КлючТекущихНастроек", КонтекстФормы.КлючОбъекта + "." + ТипНастройки, ВариантНастройки);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаСохранения", сткПараметры,,,,,Новый ОписаниеОповещения(ИмяОбработчикаОповещения, ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройки(Знач ТипНастройки, ИмяОбработчикаОповещения, ВариантНастройки)
	
	сткПараметры = Новый Структура("КлючОбъекта, КлючТекущихНастроек", КонтекстФормы.КлючОбъекта + "." + ТипНастройки, ВариантНастройки);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаЗагрузки", сткПараметры,,,,,Новый ОписаниеОповещения(ИмяОбработчикаОповещения, ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	СхемаКомпоновкиДанных = Обработки.упРМПланированиеГрафиковРаботы.ПолучитьМакет(КонтекстФормы.ИмяМакета);
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при закрытии формы настроек отбора списка объектов
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НастройкаОтбораОбъектовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Результат.Настройки);
		ЗаполнитьПланировщикНаСервере();
	КонецЕсли;
    
КонецПроцедуры // НастройкаЗаданийНаПеревозкуЗавершение()

&НаСервере
// Процедура завершения после закрытия формы сохранения настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СохранитьВариантНастройкиСпискаОбъектовНаСервере(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.ВариантСписков = Результат.КлючНастроек;
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("Пользователь", ТекущийПользователь);
		сткНастройки.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроек.Настройки);
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КонтекстФормы.КлючОбъекта + "." + КонтекстФормы.КлючНастройкиСпискаОбъектов, ВариантСписков, сткНастройки);
		УстановитьДоступныеЗначенияВариантовСписков();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы загрузки настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОткрытьВариантНастройкиСпискаОбъектовНаКлиенте(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		УстановитьДоступныеЗначенияВариантовСписков();
		ЭтаФорма.ВариантСписков = Результат.КлючНастроек;
		сткНастройки = ВернутьВариантНастройкиСпискаОбъектов();
		Если сткНастройки <> Неопределено Тогда
			ВариантНастроекПриИзменении();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовСписков()
	
	УстановитьДоступныеЗначенияВариантовНастроек(КонтекстФормы.КлючНастройкиСпискаОбъектов);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьОбщиеНастройкиФормы()
	
	КлючОбъекта =  КонтекстФормы.КлючОбъекта + "." + КонтекстФормы.КлючНастройкиОбщие;
	
	ДоступныеВарианты = ДоступныеЗначенияВариантовНастроек(КлючОбъекта);
	
	Если ДоступныеВарианты <> Неопределено Тогда
		
		Если ДоступныеВарианты.Количество() > 0 Тогда
			Вариант = ДоступныеВарианты[0];	
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Вариант.КлючВарианта);
			Если Истина
				И СохраненныеНастройки <> Неопределено 
				И ТипЗнч(СохраненныеНастройки) = Тип("Структура") 
			Тогда
				ЭтаФорма.КлючОбщихНастроекФормы = Вариант.КлючВарианта;
				ЭтаФорма.ВариантСписков = СохраненныеНастройки.ВариантНастроек;
				сткНастройки = ВернутьВариантНастройкиСпискаОбъектов();
				Если сткНастройки <> Неопределено Тогда
					КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВосстановитьОбщиеНастройкиФормы()

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовНастроек(ТипНастройки)
	
	Если ТипНастройки = КонтекстФормы.КлючНастройкиСпискаОбъектов Тогда
		ПолеФормы = Элементы.ВариантСписков;
	Иначе
		ВызватьИсключение "Неизвестный тип настройки " + ТипНастройки;
	КонецЕсли;
	
	ПолеФормы.СписокВыбора.Очистить();
	
	ДоступныеВарианты = ДоступныеЗначенияВариантовНастроек(КонтекстФормы.КлючОбъекта + "." + ТипНастройки);
	
	Если ДоступныеВарианты <> Неопределено Тогда
		Для каждого СтрокаВарианта Из ДоступныеВарианты Цикл
			Если СтрокаВарианта.ПометкаУдаления Тогда
				ПолеФормы.СписокВыбора.Добавить(СтрокаВарианта.КлючВарианта, СтрокаВарианта.Наименование,, БиблиотекаКартинок.Удалить);
			Иначе
				ПолеФормы.СписокВыбора.Добавить(СтрокаВарианта.КлючВарианта, СтрокаВарианта.Наименование);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры

Функция ДоступныеЗначенияВариантовНастроек(КлючОбъекта)
	
	Результат = Неопределено;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.КлючВарианта КАК КлючВарианта,
	|	упВариантыНастроек.ПометкаУдаления КАК ПометкаУдаления,
	|	упВариантыНастроек.Наименование КАК Наименование
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ ИСТИНА
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ (упВариантыНастроек.ПометкаУдаления)
	|	И (ЛОЖЬ
	|		ИЛИ упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
	|		ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь)
	|";

	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Результат = РезультатЗапроса.Выгрузить();	
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

&НаСервере
Функция ВернутьВариантНастройкиСпискаОбъектов()
	
	Возврат ХранилищаНастроек.упХранилищеНастроек.Загрузить(КонтекстФормы.КлючОбъекта + "." +КонтекстФормы.КлючНастройкиСпискаОбъектов, ВариантСписков);
	
КонецФункции

&НаСервере
Процедура СохранитьОбщиеНастройкиФормы()
	
	сткНастройки = Новый Структура;
	сткНастройки.Вставить("ВариантНастроек", ВариантСписков);
	КлючОбъекта = КонтекстФормы.КлючОбъекта + "." + КонтекстФормы.КлючНастройкиОбщие;
	
	Если ЭтаФорма.КлючОбщихНастроекФормы = "" Тогда
		НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
		НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
		НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
		НовыйВариант.Автор = ТекущийПользователь;
		НовыйВариант.ТолькоДляАвтора = Истина;
		НовыйВариант.Объект = КлючОбъекта;
		НовыйВариант.Записать();
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
	Иначе
		СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, КлючОбщихНастроекФормы);
		Если СохраненныеНастройки <> Неопределено Тогда
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, КлючОбщихНастроекФормы, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СохранитьОбщиеНастройкиФормы()

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыЗаполнитьПоШаблону(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если Истина
		И РезультатЗакрытия <> Неопределено
		И РезультатЗакрытия
	Тогда
		ЗаполнитьПланировщикНаСервере();		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыделенныйЭлемент(Элемент)
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	Если ВыделенныеЭлементы.Количество() Тогда
		ВыделенныйЭлемент = ВыделенныеЭлементы[0];
		сткПараметры = Новый Структура();
		сткПараметры.Вставить("Новая", Истина);
		сткПараметры.Вставить("ДатаНачала", ВыделенныйЭлемент.Начало);
		сткПараметры.Вставить("ДатаОкончания", ВыделенныйЭлемент.Конец);
		сткПараметры.Вставить("ТипИзмеренияПланировщика", Объект.ТипИзмеренияПланировщика);
		сткПараметры.Вставить("Цвет", ВыделенныйЭлемент.ЦветФона);
		сткПараметры.Вставить("Описание", ВыделенныйЭлемент.Текст);
		сткПараметры.Вставить("ТекущийПользователь", ТекущийПользователь);
		сткПараметры.Вставить("ОбъектТекущий", ВыделенныйЭлемент.ЗначенияИзмерений[КонтекстФормы.ИмяИзмерения]);
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыЗаполнитьРучноеИзменение", ЭтаФорма);
		ОткрытьФорму("Обработка.упРМПланированиеГрафиковРаботы.Форма.ФормаЗаполненияРучноеИзменение",сткПараметры,ЭтаФорма, УникальныйИдентификатор,,,ОписаниеОповещенияОЗакрытии);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыЗаполнитьРучноеИзменение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Истина
		И РезультатЗакрытия <> Неопределено
		И РезультатЗакрытия 
	Тогда
		ЗаполнитьПланировщикНаСервере();	
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьГрафикРаботыПоОбъекту(ОбъектТекущий, ДатаНачала, ДатаОкончания, Отказ)
	
	Обработки.упРМПланированиеГрафиковРаботы.ПроконтролироватьУказанныйПериод(ДатаНачала, ДатаОкончания, Отказ);
	Если НЕ Отказ Тогда
		Обработки.упРМПланированиеГрафиковРаботы.УдалитьГрафикРаботыПоОбъекту(ОбъектТекущий, ДатаНачала, ДатаОкончания, НачалоДня(ДатаНачала));	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПериод(СледующийПериод = Истина)
	
	Коэффициент = ?(СледующийПериод, 1, -1);
	
	ТекущиеПериодыОтображения = Планировщик.ТекущиеПериодыОтображения;
	
	Если Элементы.УстановитьИнтервалДень.Пометка Тогда
			
		ТекущиеПериодыОтображения[0].Начало = ТекущиеПериодыОтображения[0].Начало + Коэффициент * 3600 * 24; 
		ТекущиеПериодыОтображения[0].Конец = ТекущиеПериодыОтображения[0].Конец + Коэффициент * 3600 * 24;

	ИначеЕсли Элементы.УстановитьИнтервалНеделя.Пометка Тогда	
		
		ТекущиеПериодыОтображения[0].Начало = ТекущиеПериодыОтображения[0].Начало + Коэффициент * 7 * 3600 * 24; 
		ТекущиеПериодыОтображения[0].Конец = ТекущиеПериодыОтображения[0].Конец + Коэффициент * 7 * 3600 * 24;
			
	ИначеЕсли Элементы.УстановитьИнтервалМесяц.Пометка Тогда		
		
		ТекущиеПериодыОтображения[0].Начало = ДобавитьМесяц(ТекущиеПериодыОтображения[0].Начало, Коэффициент); 
		ТекущиеПериодыОтображения[0].Конец = ДобавитьМесяц(ТекущиеПериодыОтображения[0].Конец, Коэффициент);
		
	КонецЕсли;
	
	ЗаполнитьПланировщикНаСервере();
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после ввода даты.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВводДатыОтображенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Если Элементы.УстановитьИнтервалДень.Пометка Тогда
			УстановитьИнтервалДеньСервер(Результат);
		ИначеЕсли Элементы.УстановитьИнтервалНеделя.Пометка Тогда
			УстановитьИнтервалНеделяСервер(Результат);
		ИначеЕсли Элементы.УстановитьИнтервалМесяц.Пометка Тогда
			УстановитьИнтервалМесяцСервер(Результат);
		КонецЕсли;
		ЗаполнитьПланировщикНаСервере();
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти
