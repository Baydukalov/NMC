#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыКонтроляСовместимости = Параметры.ПараметрыКонтроляСовместимости;
	Если ПараметрыКонтроляСовместимости <> Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыКонтроляСовместимости);
	КонецЕсли;
	
	НастройкаПараметровУчетаГрузов.Загрузить(Параметры.НастройкаПараметровУчетаГрузов.Выгрузить());
	НастройкаПриоритетовРаспределения.Загрузить(Параметры.НастройкаПриоритетовРаспределения.Выгрузить());
	Объект.ПоказыватьКарту 					= Параметры.ПоказыватьКарту;
	Объект.ИспользуетсяТрассировкаРешения 	= Параметры.ИспользуетсяТрассировкаРешения;
	
	СписокВыбора = Элементы.НастройкаПриоритетовРаспределенияПредставление.СписокВыбора;
	Для каждого текНастройка Из НастройкаПараметровУчетаГрузов Цикл
		Если текНастройка.Использование Тогда
			СписокВыбора.Добавить(текНастройка.ИмяПараметра, текНастройка.Представление);
		КонецЕсли;
	КонецЦикла;
	ЭтаФорма.ФОИспользуетсяУчетМестоположенийТранспортныхСредств = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств");
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НастройкаПриоритетовРаспределенияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		текУдалитьТекущуюСтроку = Истина;		
		текИдентификатор 	= Элементы.НастройкаПриоритетовРаспределения.ТекущаяСтрока;
		текСтрока 			= НастройкаПриоритетовРаспределения.НайтиПоИдентификатору(текИдентификатор);
		Если НЕ текСтрока = Неопределено Тогда
			НастройкаПриоритетовРаспределения.Удалить(НастройкаПриоритетовРаспределения.Индекс(текСтрока));
		КонецЕсли;
	Иначе	
		текИмяПараметра = Элементы.НастройкаПриоритетовРаспределения.ТекущиеДанные.ИмяПараметра;
		Если текИмяПараметра = "" Тогда
			ТекстСообщения = Нстр("ru = 'Имя праметра должно быть заполнено'");
			текИдентификатор 	= Элементы.НастройкаПриоритетовРаспределения.ТекущаяСтрока;
			текСтрока 			= НастройкаПриоритетовРаспределения.НайтиПоИдентификатору(текИдентификатор);
			РеквизитФормы 		= ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Приоритеты", НастройкаПриоритетовРаспределения.Индекс(текСтрока) + 1, "ИмяПараметра");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,РеквизитФормы,,Отказ);
		Иначе
			Если НастройкаПриоритетовРаспределения.НайтиСтроки(Новый Структура("ИмяПараметра", текИмяПараметра)).Количество() > 1 Тогда
				ТекстСообщения = Нстр("ru = 'Выбранный параметр уже есть в списке'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПриоритетовРаспределенияПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	мсвСтрокНастройкиПараметров	= НастройкаПараметровУчетаГрузов.НайтиСтроки(Новый Структура("ИмяПараметра",ВыбранноеЗначение));
	Если мсвСтрокНастройкиПараметров.Количество() > 0  Тогда
		Элементы.НастройкаПриоритетовРаспределения.ТекущиеДанные.ИмяПараметра 	= мсвСтрокНастройкиПараметров[0].ИмяПараметра;
		Элементы.НастройкаПриоритетовРаспределения.ТекущиеДанные.Представление 	= мсвСтрокНастройкиПараметров[0].Представление;
		Элементы.НастройкаПриоритетовРаспределения.ТекущиеДанные.Направление 	= мсвСтрокНастройкиПараметров[0].Направление;
	КонецЕсли;
	//Элементы.НастройкаПриоритетовРаспределения.ДобавитьСтроку();
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ПараметрыКонтроляСовместимости = Новый Структура;
	ПараметрыКонтроляСовместимости.Вставить("КонтрольМассы"                      , КонтрольМассы);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольОбъема"                     , КонтрольОбъема);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольКоличестваМест"             , КонтрольКоличестваМест);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольКоличестваЗагрузочныхМетров", КонтрольКоличестваЗагрузочныхМетров);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольГабаритовГрузовогоМеста"    , КонтрольГабаритовГрузовогоМеста);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольРасписанияРаботыТС"         , КонтрольРасписанияРаботыТС);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольМестоположенияТранспорта"   , КонтрольМестоположенияТранспорта);
	ПараметрыКонтроляСовместимости.Вставить("ДоступностьЗон"                     , ДоступностьЗон);
	ПараметрыКонтроляСовместимости.Вставить("ДоступностьРесурсовПартнера"        , ДоступностьРесурсовПартнера);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольТиповКузовов"               , КонтрольТиповКузовов);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольКлассовОпасности"           , КонтрольКлассовОпасности);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольТемпературногоРежима"       , КонтрольТемпературногоРежима);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольДокументов"       			 , КонтрольДокументов);
	ПараметрыКонтроляСовместимости.Вставить("КонтрольРасписанияРаботыВодителей"  , КонтрольРасписанияРаботыВодителей);
	
	сткВоврата = Новый Структура("ПараметрыКонтроляСовместимости, НастройкаПриоритетовРаспределения, ИспользуетсяТрассировкаРешения, ПоказыватьКарту", ПараметрыКонтроляСовместимости, НастройкаПриоритетовРаспределения, Объект.ИспользуетсяТрассировкаРешения, Объект.ПоказыватьКарту);
	Закрыть(сткВоврата);
	
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеРаботыТСПриИзменении(Элемент)
	
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(ЭтаФорма) 
	
	ЭтаФорма.Элементы.КонтрольМестоположенияТранспорта.Доступность = ЭтаФорма.КонтрольРасписанияРаботыТС И ЭтаФорма.ФОИспользуетсяУчетМестоположенийТранспортныхСредств;
	
КонецПроцедуры

#КонецОбласти
