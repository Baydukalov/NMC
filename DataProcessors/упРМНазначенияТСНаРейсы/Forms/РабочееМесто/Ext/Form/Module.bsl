
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Объект.ВидПеревозки                   = Справочники.упВидыПеревозок.Основной;
	Объект.ИспользуетсяТрассировкаРешения = ОбщегоНазначенияВызовСервера.ХранилищеНастроекДанныхФормЗагрузить("упРМНазначенияТСНаРейсы", "ТрассировкаРешения", Ложь);
	АдресВременногоХранилища                    		 = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	ПризнакЗавершенияВыполненияАдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Истина, Новый УникальныйИдентификатор);
	
	УчитыватьМассу                       = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	УчитыватьОбъем                       = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	УчитыватьКоличествоМест              = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	УчитыватьКоличествоЗагрузочныхМетров = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	УчитыватьЛинейныеРазмеры             = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	ВедетсяУчетДокументов 				 = ПолучитьФункциональнуюОпцию("упВедетсяУчетДокументовТранспортныхСредствВодителей");
	
	ВосстановитьНастройки();
	УстановитьЗаголовки();
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьКоличествоДокументовНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СменаСтатуса" И ТипЗнч(Источник) = тип("ДокументСсылка.упРейс") Тогда
		Элементы.Рейсы.Обновить();
		
	ИначеЕсли ИмяСобытия = "СменаСтатуса" И ТипЗнч(Источник) = тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		Элементы.ЗаявкиНаТС.Обновить();	
		
	ИначеЕсли ИмяСобытия = "СформированыЗаявкиНаТС" Тогда
		мсвРейсы = Новый Массив;
		Для каждого текРейс Из Элементы.Рейсы.ВыделенныеСтроки Цикл
			текДанныеСтроки = Элементы.Рейсы.ДанныеСтроки(текРейс);
			Если НЕ текДанныеСтроки = Неопределено Тогда
				мсвРейсы.Добавить(текДанныеСтроки.Ссылка);	
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьКоличествоЗаявокНаТС(мсвРейсы);
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Для каждого текЗаявкаНаТС Из Параметр Цикл
				ОткрытьФорму("Документ.упЗаявкаНаТС.ФормаОбъекта", Новый Структура("Ключ", текЗаявкаНаТС), Источник);
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗаписанТипТС" Тогда	
		
		ОбработатьИзменениеРейса();
	
	ИначеЕсли ИмяСобытия = "ЗаписанТранспортноеСредство" Тогда		
		
		ОбработатьИзменениеРейса();

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

	СохранитьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработатьИзменениеРейса", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваОбъектОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТипыТСОбъектОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВодителиОбъектОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте                      
Процедура ТранспортныеСредстваТипТСОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура МаршрутРейсаДокументСформирован(Элемент)
	МаршрутРейсаHTMLСформирован = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкиНаТСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	
	Если Поле.Имя = "ЗаявкиНаТСПредложение" Тогда
		СтандартнаяОбработка = Ложь;
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.Предложение);
		ОткрытьФорму("Документ.упПредложениеПеревозчика.Форма.ФормаДокумента",сткПараметры);
	ИначеЕсли Поле.Имя = "ЗаявкиНаТСПодтверждение" Тогда
		СтандартнаяОбработка = Ложь;
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.Подтверждение);
		ОткрытьФорму("Документ.упПодтверждениеЗаявкиНаТС.Форма.ФормаДокумента",сткПараметры);	
	ИначеЕсли Поле.Имя = "ЗаявкиНаТСРейс" Тогда
		СтандартнаяОбработка = Ложь;
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.Рейс);
		ОткрытьФорму("Документ.упРейс.Форма.ФормаДокумента",сткПараметры);		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГруппаПодборТСПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ПодключитьОбработчикОжидания("ОбработатьИзменениеРейса", 0.2, Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТипыТСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ТипыТС.ТекущиеДанные;
	Если текСтрока <> Неопределено И текСтрока.ПлановыеРасходыПоРейсу > 0 И (Поле = Элементы.ТипыТСРасшифровкаРасходов Или Поле = Элементы.ТипыТСПлановыеРасходыПоРейсу) Тогда
		 ТабличныйДокумент = СформироватьРасшифровкуРасходов("ТипыТС");
		 Если ТабличныйДокумент <> Неопределено Тогда
			 ТабличныйДокумент.Показать(НСтр("ru = 'Расходы'"));
		 КонецЕсли; 
	     //Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТрассировка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ТранспортныеСредства.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		Если текСтрока.ПлановыеРасходыПоРейсу > 0  
			И (Поле = Элементы.ТранспортныеСредстваРасшифровкаРасходов Или Поле = Элементы.ТранспортныеСредстваПлановыеРасходыПоРейсу) Тогда
			ТабличныйДокумент = СформироватьРасшифровкуРасходов();
			Если ТабличныйДокумент <> Неопределено Тогда
				ТабличныйДокумент.Показать(НСтр("ru = 'Расходы'"));
			КонецЕсли;
		КонецЕсли;
		//Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТрассировка;
		
		Если Поле = Элементы.ТранспортныеСредстваСрокПоДокументамИстекает Тогда
			ТабличныйДокумент = СформироватьПакетДокументов();
			Если ТабличныйДокумент <> Неопределено Тогда
				ТабличныйДокумент.Показать(НСтр("ru = 'Пакет документов'"));
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	текСтрока = Элементы.Водители.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
	
		Если Поле = Элементы.ВодителиСрокПоДокументамИстекает Тогда
			текРейс = Элементы.Рейсы.ТекущаяСтрока;
			текДанныеРейса = Элементы.Рейсы.ДанныеСтроки(текРейс);
			текТранспортноеСредство	= текДанныеРейса.ТранспортноеСредство;
			ТабличныйДокумент = СформироватьПакетДокументов("Водители", текТранспортноеСредство);
			Если ТабличныйДокумент <> Неопределено Тогда
				ТабличныйДокумент.Показать(НСтр("ru = 'Пакет документов'"));
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

#Область Команды_Рейсы

&НаКлиенте
Процедура СформироватьЗаявки(Команда)
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                       	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	
	Если Элементы.Рейсы.ВыделенныеСтроки.Количество() > 0 Тогда
		текРейс = Элементы.Рейсы.ТекущиеДанные.Ссылка;
		сткЗаявкиНаТС = СформироватьЗаявкуНаТС(текРейс, ОписаниеОшибки, Отказ);
		Если Не Отказ Тогда
			Если Не сткЗаявкиНаТС.ЗаявкиНаТС = Неопределено Тогда
				Оповестить("СформированыЗаявкиНаТС", сткЗаявкиНаТС.ЗаявкиНаТС);
			ИначеЕсли сткЗаявкиНаТС.ФормироватьЗаявки Тогда	
				ПараметрыФормы = Новый Структура("Рейс", текРейс);
				ОткрытьФорму("Справочник.упПартнеры.Форма.ФормаВыбораПеревозчики", 
				ПараметрыФормы, 
				ЭтаФорма, 
				УникальныйИдентификатор);	
			КонецЕсли;
		Иначе	
			ПоказатьОповещениеПользователя(НСтр("ru = 'Документ не сформирован.'"),, ОписаниеОшибки, БиблиотекаКартинок.Ошибка32);		
		КонецЕсли;
	Иначе
		ОписаниеОшибки	= Нстр("ru = 'Рейс не выбран.'");
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ не сформирован.'"),, ОписаниеОшибки, БиблиотекаКартинок.Ошибка32);						
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	сткПараметры = Новый Структура("ПараметрыКонтроляСовместимости, НастройкаПриоритетовРаспределения, НастройкаПараметровУчетаГрузов, ИспользуетсяТрассировкаРешения, ПоказыватьКарту",
	                                ПараметрыКонтроляСовместимости,
	                                НастройкаПриоритетовРаспределения,
									НастройкаПараметровУчетаГрузов,
									Объект.ИспользуетсяТрассировкаРешения,
									Объект.ПоказыватьКарту);
	ОткрытьФорму("Обработка.упРМНазначенияТСНаРейсы.Форма.НастройкиРабочегоМеста", сткПараметры, ЭтаФорма,,,,Новый ОписаниеОповещения("ОбработкаОповещенияОЗакрытииФормыНастройкиПриоритетов", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьТранспортНаРейсы(Команда)
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если Не ВыделенныеСтроки.Количество() Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выделено ни одного рейса!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли; 
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("МассивРейсов", ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ВыделенныеСтроки));
	ОткрытьФорму("Обработка.упРМНазначенияТСНаРейсы.Форма.ФормаАвтоматическогоРаспределенияТранспорта", сткПараметры,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);  

КонецПроцедуры

&НаКлиенте
Процедура РаспределитьТипыТСПоРейсам(Команда)
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если Не ВыделенныеСтроки.Количество() Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выделено ни одного рейса!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	мсвРейсов = Новый Массив;
	Для каждого текРейс Из ВыделенныеСтроки Цикл
		текДанныеСтроки = Элементы.Рейсы.ДанныеСтроки(текРейс);
		Если текДанныеСтроки = Неопределено Или ЗначениеЗаполнено(текДанныеСтроки.ТипТС) Тогда
			Продолжить;
		КонецЕсли;
		мсвРейсов.Добавить(текРейс);
	КонецЦикла;
	
	Если мсвРейсов.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'У всех выделенных рейсов уже назначены типы ТС!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли; 
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("МассивРейсов"                     , мсвРейсов);
	сткПараметры.Вставить("ПараметрыКонтроляСовместимости"   , ПараметрыКонтроляСовместимости);
	сткПараметры.Вставить("НастройкаПриоритетовРаспределения", НастройкаПриоритетовРаспределения);
	сткПараметры.Вставить("ТипОперации"                      , "ПодборТиповТС");
	ОткрытьФорму("Обработка.упРМНазначенияТСНаРейсы.Форма.ФормаАвтоматическогоРаспределенияТранспорта", сткПараметры,,,,, Новый ОписаниеОповещения("РаспределитьТипыТСПоРейсамЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьТСПоРейсам(Команда)
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если Не ВыделенныеСтроки.Количество() Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выделено ни одного рейса!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	мсвРейсов = Новый Массив;
	Для каждого текРейс Из ВыделенныеСтроки Цикл
		текДанныеСтроки = Элементы.Рейсы.ДанныеСтроки(текРейс);
		Если текДанныеСтроки = Неопределено Или ЗначениеЗаполнено(текДанныеСтроки.ТранспортноеСредство) Тогда
			Продолжить;
		КонецЕсли;
		мсвРейсов.Добавить(текРейс);
	КонецЦикла;
	
	Если мсвРейсов.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'У всех выделенных рейсов уже назначены транспортные средства!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли; 
		
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("МассивРейсов"                     , мсвРейсов);
	сткПараметры.Вставить("ПараметрыКонтроляСовместимости"   , ПараметрыКонтроляСовместимости);
	сткПараметры.Вставить("НастройкаПриоритетовРаспределения", НастройкаПриоритетовРаспределения);
	сткПараметры.Вставить("ТипОперации"                      , "ПодборТранспортныхСредств");
	ОткрытьФорму("Обработка.упРМНазначенияТСНаРейсы.Форма.ФормаАвтоматическогоРаспределенияТранспорта", сткПараметры,,,,, Новый ОписаниеОповещения("РаспределитьТСПоРейсамЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьВодителейПоРейсам(Команда)
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если Не ВыделенныеСтроки.Количество() Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выделено ни одного рейса!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	мсвРейсов = Новый Массив;
	Для каждого текРейс Из ВыделенныеСтроки Цикл
		текДанныеСтроки = Элементы.Рейсы.ДанныеСтроки(текРейс);
		Если текДанныеСтроки = Неопределено Или Не ЗначениеЗаполнено(текДанныеСтроки.ТранспортноеСредство) Или ЗначениеЗаполнено(текДанныеСтроки.Водитель1) Тогда
			Продолжить;
		КонецЕсли;
		мсвРейсов.Добавить(текРейс);
	КонецЦикла;
	
	Если мсвРейсов.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Назначение водителей возможно только для рейсов с назначенными транспортными средствами и неназначенным водителем!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли; 
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("МассивРейсов"                     , мсвРейсов);
	сткПараметры.Вставить("ПараметрыКонтроляСовместимости"   , ПараметрыКонтроляСовместимости);
	сткПараметры.Вставить("НастройкаПриоритетовРаспределения", НастройкаПриоритетовРаспределения);
	сткПараметры.Вставить("ТипОперации"                      , "ПодборВодителей");
	ОткрытьФорму("Обработка.упРМНазначенияТСНаРейсы.Форма.ФормаАвтоматическогоРаспределенияТранспорта", сткПараметры,,,,, Новый ОписаниеОповещения("РаспределитьВодителейПоРейсамЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПеревозчика(Команда)
	мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
	Отказ = Ложь;
	упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, Новый Структура("Перевозчик, Соглашение, ТипТС, ТранспортноеСредство", 
													ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка"), 
													ПредопределенноеЗначение("Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка"), 
													ПредопределенноеЗначение("Справочник.упТипыТС.ПустаяСсылка"), 
													ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка")), 
													Отказ);
	Элементы.Рейсы.Обновить();
	ОбработатьИзменениеРейса();
	Если НЕ Отказ Тогда
		ПоказатьОповещениеПользователя(Нстр("ru = 'Реквизит ""Перевозчик"" очищен'"),,, БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(Нстр("ru = 'Не удалось очистить реквизит ""Перевозчик""'"),, , БиблиотекаКартинок.Ошибка32);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТипТС(Команда)
	мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
	Отказ = Ложь;
	упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, Новый Структура("ТипТС, ТранспортноеСредство", 
													ПредопределенноеЗначение("Справочник.упТипыТС.ПустаяСсылка"), 
													ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка")), 
													Отказ);
	Элементы.Рейсы.Обновить();
	ОбработатьИзменениеРейса();
	Если НЕ Отказ Тогда
		ПоказатьОповещениеПользователя(Нстр("ru = 'Реквизит ""Тип ТС"" очищен'"),,, БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(Нстр("ru = 'Не удалось очистить реквизит ""Тип ТС""'"),, , БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТранспортноеСредство(Команда)
	мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
	Отказ = Ложь;
	упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, Новый Структура("ТранспортноеСредство", 
													ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка")), 
													Отказ);
	Элементы.Рейсы.Обновить();
	ОбработатьИзменениеРейса();
	Если НЕ Отказ Тогда
		ПоказатьОповещениеПользователя(Нстр("ru = 'Реквизит ""Транспортное средство"" очищен'"),,, БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(Нстр("ru = 'Не удалось очистить реквизит ""Транспортное средство""'"),, , БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВодитель(Команда)
	мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
	Отказ = Ложь;
	упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, Новый Структура("Водитель1", 
													ПредопределенноеЗначение("Справочник.упСотрудники.ПустаяСсылка")), 
													Отказ);
	Элементы.Рейсы.Обновить();
	ОбработатьИзменениеРейса();
	Если НЕ Отказ Тогда
		ПоказатьОповещениеПользователя(Нстр("ru = 'Реквизит ""Водитель"" очищен'"),,, БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(Нстр("ru = 'Не удалось очистить реквизит ""Водитель""'"),, , БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Команды_ТипыТС

&НаКлиенте
Процедура СвернутьДеревоТиповТС(Команда)
	
	ЭлементыДерева = ТипыТС.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыДерева Цикл
		Элементы.ТипыТС.Свернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоТиповТС(Команда)
	
	ЭлементыДерева = ТипыТС.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыДерева Цикл
		Элементы.ТипыТС.Развернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисвоитьРейсуТипТС(Команда)
	
	Если НЕ Элементы.ТипыТС.ТекущиеДанные = Неопределено Тогда
		текПеревозчик	= Элементы.ТипыТС.ТекущиеДанные.Перевозчик;
		текСоглашение 	= ПолучитьСоглашениеПоУмолчанию(текПеревозчик);
		Если ТипЗнч(Элементы.ТипыТС.ТекущиеДанные.Объект) = Тип("СправочникСсылка.упТипыТС") Тогда
			сткНазначение = Новый Структура("ТипТС, Перевозчик, Соглашение", Элементы.ТипыТС.ТекущиеДанные.Объект, текПеревозчик, текСоглашение);			
		Иначе	 
			сткНазначение = Новый Структура("Перевозчик, Соглашение", текПеревозчик, текСоглашение);
		КонецЕсли;	 
		
		мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
		Отказ = Ложь;
		упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, сткНазначение, Отказ);
		Элементы.Рейсы.Обновить();
		ОбработатьИзменениеРейса();
		Если НЕ Отказ Тогда
			ТекстСообщения  = Нстр("ru = 'Выделенным рейсам присвоен тип ТС'");
			ПоказатьОповещениеПользователя(Нстр("ru = 'Присвоен тип ТС'"),,ТекстСообщения, БиблиотекаКартинок.Информация32);
		КонецЕсли;

	Иначе
		ТекстСообщения  = Нстр("ru = 'Выберите перевозчика или тип ТС'");
		ПоказатьОповещениеПользователя(Нстр("ru = 'Тип ТС не присвоен'"),, ТекстСообщения, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоСобственныеПеревозчикиТипыТС(Команда)
	
	ОтбиратьТолькоСобственнныхПеревозчиковТипыТС = Не ОтбиратьТолькоСобственнныхПеревозчиковТипыТС;
	Элементы.ТипыТСТолькоСобственныеПеревозчики.Пометка = ОтбиратьТолькоСобственнныхПеревозчиковТипыТС;
	ОбработатьИзменениеРейса();	
	
КонецПроцедуры

#КонецОбласти

#Область Команды_ТранспортныеСредства

&НаКлиенте
Процедура СвернутьДеревоТранспортныхСредств(Команда)
	
	ЭлементыДерева = ТранспортныеСредства.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыДерева Цикл
		Элементы.ТранспортныеСредства.Свернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоТранспортныхСредств(Команда)
	
	ЭлементыДерева = ТранспортныеСредства.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыДерева Цикл
		Элементы.ТранспортныеСредства.Развернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисвоитьРейсуТС(Команда)
	
	Если НЕ Элементы.ТранспортныеСредства.ТекущиеДанные = Неопределено Тогда
		текДанные = Элементы.ТранспортныеСредства.ТекущиеДанные;
		текПеревозчик	= текДанные.Перевозчик;
		текСоглашение 	= ПолучитьСоглашениеПоУмолчанию(текПеревозчик);
		текКонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(текПеревозчик);
		
		ТранспортноеСредство = Неопределено;
		
		Если ТипЗнч(Элементы.ТранспортныеСредства.ТекущиеДанные.Объект) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
			сткНазначение = Новый Структура("ТранспортноеСредство, ТипТС, Перевозчик, Соглашение, КонтрагентПеревозчика",текДанные.Объект, текДанные.ТипТС, текПеревозчик, текСоглашение, текКонтрагентПеревозчика);			
			ТранспортноеСредство = текДанные.Объект;
		Иначе	 
			сткНазначение = Новый Структура("Перевозчик, Соглашение, КонтрагентПеревозчика", текПеревозчик, текСоглашение, текКонтрагентПеревозчика);
		КонецЕсли;	 
		
		Отказ = Ложь;
		ПрисвоитьРейсуТСНаСервере(ТранспортноеСредство, сткНазначение, Отказ);

		Элементы.Рейсы.Обновить();
		ОбработатьИзменениеРейса();
		Если НЕ Отказ Тогда
			ТекстСообщения  = Нстр("ru = 'Выделенным рейсам присвоено ТС'");
			ПоказатьОповещениеПользователя(Нстр("ru = 'Присвоено ТС'"),,ТекстСообщения, БиблиотекаКартинок.Информация32);
		КонецЕсли;

	Иначе
		ТекстСообщения = Нстр("ru = 'Не выбран перевозчик или ТС'");
		ПоказатьОповещениеПользователя(Нстр("ru = 'ТС не присвоено'"),, ТекстСообщения, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрисвоитьРейсуТСНаСервере(ТранспортноеСредство, сткНазначение, Отказ)
	
	мсвВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	
	Если ПараметрыКонтроляСовместимости.КонтрольДокументов И НЕ ТранспортноеСредство = Неопределено Тогда
		текОтказ = Ложь;
		упРейс.ПроверитьНаличиеНеобходимыхДокуметовПоТС(ТранспортноеСредство, текОтказ);	
	КонецЕсли;
	
	Для каждого текРейс Из мсвВыделенныеСтроки Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текРейс);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
			Возврат;
		КонецПопытки;
	КонецЦикла; 
	
	упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, сткНазначение, Отказ);
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	
КонецПроцедуры


&НаКлиенте
Процедура ТолькоСобственныеПеревозчикиТранспортныеСредства(Команда)
	
	ОтбиратьТолькоСобственнныхПеревозчиковТС = Не ОтбиратьТолькоСобственнныхПеревозчиковТС;
	Элементы.ТранспортныеСредстваТолькоСобственныеПеревозчики.Пометка = ОтбиратьТолькоСобственнныхПеревозчиковТС;
	ОбработатьИзменениеРейса();	
	
КонецПроцедуры

#КонецОбласти

#Область Команды_Водители

&НаКлиенте
Процедура СвернутьДеревоВодителей(Команда)
	
	ЭлементыДерева = Водители.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыДерева Цикл
		Элементы.Водители.Свернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоВодителей(Команда)
	
	ЭлементыДерева = Водители.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыДерева Цикл
		Элементы.Водители.Развернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисвоитьРейсуВодителя(Команда)
	
	Если НЕ Элементы.Водители.ТекущиеДанные = Неопределено Тогда
		текДанные = Элементы.Водители.ТекущиеДанные;
		Если ТипЗнч(текДанные.Объект) = Тип("СправочникСсылка.упСотрудники") Тогда

			Отказ = Ложь;
			Водитель = Элементы.Водители.ТекущиеДанные.Объект;
			мсвТранспортныеСредства = Новый Массив;
			
			мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
			Для каждого Строка Из мсвВыделенныеСтроки Цикл
				мсвТранспортныеСредства.Добавить(Элементы.Рейсы.ДанныеСтроки(Строка).ТранспортноеСредство);
			КонецЦикла;
					
			ПрисвоитьРейсуВодителяНаСервере(Водитель, мсвТранспортныеСредства, Отказ);
			
			Элементы.Рейсы.Обновить();
			Если НЕ Отказ Тогда
				ТекстСообщения  = Нстр("ru = 'Выделенным рейсам присвоен водитель'");
				ПоказатьОповещениеПользователя(Нстр("ru = 'Присвоен водитель'"),,ТекстСообщения, БиблиотекаКартинок.Информация32);
			КонецЕсли;
		Иначе
			ТекстСообщения  = Нстр("ru = 'Выберите водителя'");
			ПоказатьОповещениеПользователя(Нстр("ru = 'Водитель не присвоен'"),, ТекстСообщения, БиблиотекаКартинок.Ошибка32);
		КонецЕсли;	 
	Иначе
		ТекстСообщения  = Нстр("ru = 'Выберите водителя'");
		ПоказатьОповещениеПользователя(Нстр("ru = 'Водитель не присвоен'"),, ТекстСообщения, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрисвоитьРейсуВодителяНаСервере(Водитель, мсвТранспортныеСредства, Отказ)
	
	сткНазначение = Новый Структура("Водитель1",Водитель);			
	мсвВыделенныеСтроки 	= Элементы.Рейсы.ВыделенныеСтроки;
	
	Если ПараметрыКонтроляСовместимости.КонтрольДокументов Тогда
		Для каждого ТранспортноеСредство Из мсвТранспортныеСредства Цикл
			текОтказ = Ложь;
			упРейс.ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель, текОтказ);	
		КонецЦикла;
	КонецЕсли;
	упРейсВызовСервера.НазначитьРеквизитыРейса(мсвВыделенныеСтроки, сткНазначение, Отказ);
	
КонецПроцедуры


#КонецОбласти

#Область Команды_Тендеры

&НаКлиенте
Процедура ОтбиратьТендерыПоРейсу(Команда)
	
	ОтбиратьТендерыПоРейсу = Не ОтбиратьТендерыПоРейсу;
	Элементы.ЗаявкиНаТСОтбиратьТендерыПоРейсу.Пометка = ОтбиратьТендерыПоРейсу;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЗаявкиНаТС, "Рейсы", спзВыделенныеРейсы.ВыгрузитьЗначения());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЗаявкиНаТС, "ОтбиратьТендерыПоРейсу", ОтбиратьТендерыПоРейсу);
	ЗаполнитьКоличествоДокументовНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредложениеПеревозчика(Команда)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		упСервисныеФункцииКлиент.СформироватьПредложениеПеревозчика(ТекущиеДанные.Ссылка);	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПодтверждениеЗаявкиНаОснованииЗаявкиТС(Команда)
	
	ТекущиеДанные = Элементы.ЗаявкиНаТС.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.Предложение) Тогда
			упСервисныеФункцииКлиент.СформироватьПодтверждениеЗаявкиНаТС(ТекущиеДанные.Предложение);		
		Иначе	
			ТекстСообщения = СтрШаблон(НСтр("ru = 'По документу ""%1"" не получено предложение перевозчика.'"), ТекущиеДанные.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура ОтображатьКарту(Команда)
	УправлениеВидимостьюКарты();	      	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ВосстановитьНастройки()
	
	// Настройки динамического списка "Рейсы"
	ПользовательскиеНастройки = ХранилищеСистемныхНастроек.Загрузить("Обработка.упРМНазначенияТСНаРейсы.Форма.Форма.Рейсы/ТекущиеПользовательскиеНастройки");
	Если Не ПользовательскиеНастройки = Неопределено Тогда
		Рейсы.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройки);	
	КонецЕсли;
	
	ЗаполнитьПараметрыКонтроляСовместимости();
	ЗаполнитьНастройкуПараметровУчетаГрузов();
	ЗаполнитьНастройкуПриоритетовРаспределения();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкуПараметровУчетаГрузов()
	
	НастройкаПараметровУчетаГрузов.Очистить();
	
	// Имена параметров должны быть аналогичны полям, присутствующим в выборке доступного транспорта (см. модуль "упПодбор").
	ДобавитьНастройку("Высота"                                      , УчитыватьЛинейныеРазмеры            , 0, НСтр("ru = 'Высота минимальна'"));
	ДобавитьНастройку("Ширина"                                      , УчитыватьЛинейныеРазмеры            , 0, НСтр("ru = 'Ширина минимальная'"));
	ДобавитьНастройку("Длина"                                       , УчитыватьЛинейныеРазмеры            , 0, НСтр("ru = 'Длина минимальна'"));
	ДобавитьНастройку("Грузоподъемность"                            , УчитыватьМассу                      , 0, НСтр("ru = 'Грузоподъемность минимальна'"));
	ДобавитьНастройку("Объем"                                       , УчитыватьОбъем                      , 0, НСтр("ru = 'Объем минимален'"));
	ДобавитьНастройку("КоличествоМест"                              , УчитыватьКоличествоМест             , 0, НСтр("ru = 'Количество мест минимально'"));
	ДобавитьНастройку("КоличествоЗагрузочныхМетров"                 , УчитыватьКоличествоЗагрузочныхМетров, 0, НСтр("ru = 'Количество загрузочных метров минимально'"));
	ДобавитьНастройку("ГрузоподъемностьПроцентЗагрузки"             , УчитыватьМассу                      , 2, НСтр("ru = 'Процент загрузки по массе максимален'"));
	ДобавитьНастройку("ОбъемПроцентЗагрузки"                        , УчитыватьОбъем                      , 2, НСтр("ru = 'Процент загрузки по объёму максимален'"));
	ДобавитьНастройку("КоличествуМестПроцентЗагрузки"               , УчитыватьКоличествоМест             , 2, НСтр("ru = 'Процент загрузки по местам максимален'"));
	ДобавитьНастройку("КоличествоЗагрузочныхМетровПроцентЗагрузки"  , УчитыватьКоличествоЗагрузочныхМетров, 2, НСтр("ru = 'Процент загрузки по кол. метров максимален'"));
	ДобавитьНастройку("РасходыПоРейсу"                              , Истина                              , 0, НСтр("ru = 'Расходы по рейсу минимальны'"));
	ДобавитьНастройку("СобственныйПеревозчик"                       , Истина                              , 2, НСтр("ru = 'Приоритет у собственных перевозчиков'"));
	ДобавитьНастройку("РеальныйПеревозчик"                          , Истина                              , 2, НСтр("ru = 'Приоритет у реальных перевозчиков'"));
	ДобавитьНастройку("ВиртуальныйПеревозчик"                       , Истина                              , 2, НСтр("ru = 'Приоритет у виртуальных перевозчиков'"));
	ДобавитьНастройку("ВедетсяУчетДокументов"                       , ВедетсяУчетДокументов               , 0, НСтр("ru = 'В наличии необходимый пакет документов по транспортному средству и водителю'"));
	ДобавитьНастройку("РейтингПеревозчика"                          , Истина                              , 2, НСтр("ru = 'Рейтинг перевозчика максимален'"));

КонецПроцедуры

&НаСервере
Процедура ДобавитьНастройку(ИмяПараметра, Использование, Направление, Представление)

	Строка = НастройкаПараметровУчетаГрузов.Добавить();
	Строка.ИмяПараметра	 = ИмяПараметра;
	Строка.Использование = Использование;
	// Направление - порядок сортировки: соответствует системному перечислению "НаправлениеПорядкаСхемыЗапроса" (0 - "ПоВозрастанию" и т.д.).
	Строка.Направление   = Направление;
	Строка.Представление = Представление;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкуПриоритетовРаспределения()
	
	НастройкаПриоритетовРаспределения.Очистить();
	тзНастройкиПриоритетовРаспределения = ОбщегоНазначенияВызовСервера.ХранилищеНастроекДанныхФормЗагрузить("упРМНазначенияТСНаРейсы", "НастройкаПриоритетовРаспределения", Неопределено);
	
	// Если настройки не были сохранены
	Если тзНастройкиПриоритетовРаспределения = Неопределено Тогда
		
		// Заполняются настройки по умолчанию
		Для каждого текСтрока Из НастройкаПараметровУчетаГрузов Цикл
			ЗаполнитьЗначенияСвойств(НастройкаПриоритетовРаспределения.Добавить(), текСтрока);
		КонецЦикла;
		
	Иначе
		Для каждого текСтрокаПриоритетСохраненная Из тзНастройкиПриоритетовРаспределения Цикл
			
			мсвСтрокиНастроекУчета = НастройкаПараметровУчетаГрузов.НайтиСтроки(Новый Структура("ИмяПараметра", текСтрокаПриоритетСохраненная.ИмяПараметра));
			
			Если мсвСтрокиНастроекУчета.Количество() > 0 Тогда
				текСтрокаНастройкаУчета = мсвСтрокиНастроекУчета[0];
				
				// Заполняются только используемые настройки
				Если текСтрокаНастройкаУчета.Использование Тогда
					текСтрокаПриоритетРаспределения = НастройкаПриоритетовРаспределения.Добавить();
					ЗаполнитьЗначенияСвойств(текСтрокаПриоритетРаспределения, текСтрокаНастройкаУчета);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыКонтроляСовместимости()
	
	ЗаполнитьПараметрыПоУмолчанию = Истина;
	СписокПараметров = ОбщегоНазначенияВызовСервера.ХранилищеНастроекДанныхФормЗагрузить("упРМНазначенияТСНаРейсы", "ПараметрыКонтроляСовместимости", Неопределено);
	Если СписокПараметров <> Неопределено Тогда
		ПараметрыКонтроляСовместимости = СписокПараметров;	
		ЗаполнитьПараметрыПоУмолчанию = Ложь;
	КонецЕсли;
	
	Если ЗаполнитьПараметрыПоУмолчанию Тогда
		ПараметрыКонтроляСовместимости = Новый Структура;
	КонецЕсли;	
		
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольМассы") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольМассы", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольОбъема") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольОбъема", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольКоличестваМест") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольКоличестваМест", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольКоличестваЗагрузочныхМетров") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольКоличестваЗагрузочныхМетров", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольГабаритовГрузовогоМеста") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольГабаритовГрузовогоМеста", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольРасписанияРаботыТС") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольРасписанияРаботыТС", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольМестоположенияТранспорта") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольМестоположенияТранспорта", Ложь);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("ДоступностьЗон") Тогда
		ПараметрыКонтроляСовместимости.Вставить("ДоступностьЗон", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("ДоступностьРесурсовПартнера") Тогда
		ПараметрыКонтроляСовместимости.Вставить("ДоступностьРесурсовПартнера", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольТиповКузовов") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольТиповКузовов", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольКлассовОпасности") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольКлассовОпасности", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольТемпературногоРежима") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольТемпературногоРежима", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольДокументов") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольДокументов", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольДокументов") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольДокументов", Истина);	
	КонецЕсли;
	Если НЕ ПараметрыКонтроляСовместимости.Свойство("КонтрольРасписанияРаботыВодителей") Тогда
		ПараметрыКонтроляСовместимости.Вставить("КонтрольРасписанияРаботыВодителей", Истина);	
	КонецЕсли;
	
	// проверка доступности выбранного контролирующего показателя
	ПараметрыКонтроляСовместимости.КонтрольМассы                       = (ПараметрыКонтроляСовместимости.КонтрольМассы И УчитыватьМассу);
	ПараметрыКонтроляСовместимости.КонтрольОбъема                      = (ПараметрыКонтроляСовместимости.КонтрольОбъема И УчитыватьОбъем);
	ПараметрыКонтроляСовместимости.КонтрольКоличестваМест              = (ПараметрыКонтроляСовместимости.КонтрольКоличестваМест И УчитыватьКоличествоМест);
	ПараметрыКонтроляСовместимости.КонтрольКоличестваЗагрузочныхМетров = (ПараметрыКонтроляСовместимости.КонтрольКоличестваЗагрузочныхМетров И УчитыватьКоличествоЗагрузочныхМетров);
	ПараметрыКонтроляСовместимости.КонтрольГабаритовГрузовогоМеста     = (ПараметрыКонтроляСовместимости.КонтрольГабаритовГрузовогоМеста И УчитыватьЛинейныеРазмеры);
	
	ПараметрыКонтроляСовместимости.ДоступностьЗон               = (ПараметрыКонтроляСовместимости.ДоступностьЗон И ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон"));
	ПараметрыКонтроляСовместимости.КонтрольТиповКузовов         = (ПараметрыКонтроляСовместимости.КонтрольТиповКузовов И ПолучитьФункциональнуюОпцию("упКонтролироватьТипыКузововДляПогрузкиРазгрузки"));
	ПараметрыКонтроляСовместимости.КонтрольКлассовОпасности     = (ПараметрыКонтроляСовместимости.КонтрольКлассовОпасности И ПолучитьФункциональнуюОпцию("упУчитыватьКлассыОпасностиГрузов"));
	ПараметрыКонтроляСовместимости.КонтрольТемпературногоРежима = (ПараметрыКонтроляСовместимости.КонтрольТемпературногоРежима И ПолучитьФункциональнуюОпцию("упКонтролироватьТемпературныеРежимы"));
	ПараметрыКонтроляСовместимости.КонтрольДокументов			= (ПараметрыКонтроляСовместимости.КонтрольДокументов И ВедетсяУчетДокументов);
	ПараметрыКонтроляСовместимости.ДоступностьРесурсовПартнера  = (ПараметрыКонтроляСовместимости.ДоступностьРесурсовПартнера И ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьРесурсовПартнеру"));
	
	УстановитьПиктограммыКонтроля();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПиктограммыКонтроля()
	
	РодительТипТС = Элементы.ГруппаПиктограммыКонтроляТипыТС;
	КоличествоЭлементов = РодительТипТС.ПодчиненныеЭлементы.Количество();
	Сч = КоличествоЭлементов - 1;
	Пока Сч >= 0 Цикл
		текЭлемент = РодительТипТС.ПодчиненныеЭлементы[Сч];
	    Элементы.Удалить(текЭлемент);
		Сч = Сч - 1;
	КонецЦикла; 
	
	РодительТС = Элементы.ГруппаПиктограммыКонтроляТС;
	КоличествоЭлементов = РодительТС.ПодчиненныеЭлементы.Количество();
	Сч = КоличествоЭлементов - 1;
	Пока Сч >= 0 Цикл
		текЭлемент = РодительТС.ПодчиненныеЭлементы[Сч];
		Элементы.Удалить(текЭлемент);
		Сч = Сч - 1;
	КонецЦикла;  
	
	РодительВодители = Элементы.ГруппаПиктограммыКонтроляВодители;
	КоличествоЭлементов = РодительВодители.ПодчиненныеЭлементы.Количество();
	Сч = КоличествоЭлементов - 1;
	Пока Сч >= 0 Цикл
		текЭлемент = РодительВодители.ПодчиненныеЭлементы[Сч];
		Элементы.Удалить(текЭлемент);
		Сч = Сч - 1;
	КонецЦикла;  

	Если ПараметрыКонтроляСовместимости.КонтрольМассы Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаМассаТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упМасса;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Грузоподъемность'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаМассаТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упМасса;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Грузоподъемность'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольОбъема Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаОбъемТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упОбъем;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Объем'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаОбъемТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упОбъем;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Объем'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольКоличестваМест Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаКоличествоМестТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упКоличествоМест;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Количество мест'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаКоличествоМестТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упКоличествоМест;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Количество мест'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольКоличестваЗагрузочныхМетров Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаКоличествоЗагрузочныхМетровТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упКоличествоЗагрузочныхМетров;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Количество загрузочных метров'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаКоличествоЗагрузочныхМетровТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упКоличествоЗагрузочныхМетров;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Количество загрузочных метров'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольГабаритовГрузовогоМеста Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаГабаритыТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упГабариты;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Габариты грузового места'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаГабаритыТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упГабариты;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Габариты грузового места'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольРасписанияРаботыТС Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаРасписаниеРаботыТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упПериодМесяц;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Расписание работы ТС'");
	КонецЕсли;	
	
	Если ПараметрыКонтроляСовместимости.КонтрольМестоположенияТранспорта Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаМестоположенияТранспорта", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упГеозоны;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Контроль местоположения транспорта'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.ДоступностьЗон Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаДоступностьЗонТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упЗона;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Доступность зон'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаДоступностьЗонТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упЗона;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Доступность зон'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.ДоступностьРесурсовПартнера Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаДоступностьРесурсовПартнераТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упДоступностьТСПеревозчика;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Доступность ТС перевозчиков'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаДоступностьРесурсовПартнераВодители", Тип("ДекорацияФормы"), РодительВодители);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упДоступностьВодителейПеревозчика;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Доступность водителей перевозчиков'");
	КонецЕсли;

	Если ПараметрыКонтроляСовместимости.КонтрольТиповКузовов Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаТипыКузововТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упТипыКузовов;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Типы кузовов для погрузки/разгрузки'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаТипыКузововТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упТипыКузовов;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Типы кузовов для погрузки/разгрузки'");
	КонецЕсли;

	Если ПараметрыКонтроляСовместимости.КонтрольКлассовОпасности Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаКлассыОпасностиТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упРадиация;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Классы опасности'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаКлассыОпасностиТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упРадиация;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Классы опасности'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольТемпературногоРежима Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаТемпературныйРежимТипТС", Тип("ДекорацияФормы"), РодительТипТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упТемпература;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Температурный режим'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаТемпературныйРежимТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упТемпература;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Температурный режим'");
	КонецЕсли;
	
	Если ПараметрыКонтроляСовместимости.КонтрольДокументов Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаДокументТС", Тип("ДекорацияФормы"), РодительТС);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.Документ;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Ведется учет документов'");
		
		НоваяДекорация = Элементы.Добавить("ПиктограммаДокументВодители", Тип("ДекорацияФормы"), РодительВодители);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.Документ;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Ведется учет документов'");
	КонецЕсли;

	Если ПараметрыКонтроляСовместимости.КонтрольРасписанияРаботыВодителей Тогда
		НоваяДекорация = Элементы.Добавить("ПиктограммаРасписаниеРаботыВодителей", Тип("ДекорацияФормы"), РодительВодители);
		НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
		НоваяДекорация.Картинка = БиблиотекаКартинок.упПериодНеделя;
		НоваяДекорация.Высота = 1;
		НоваяДекорация.Ширина = 2;
		НоваяДекорация.Подсказка = НСтр("ru = 'Расписание работы водителей'");
	КонецЕсли;
	
	Элементы.ДекорацияКонтрольТипыТС.Видимость   = (РодительТипТС.ПодчиненныеЭлементы.Количество() > 0);
	Элементы.ДекорацияКонтрольТС.Видимость       = (РодительТС.ПодчиненныеЭлементы.Количество() > 0);
	Элементы.ДекорацияКонтрольВодители.Видимость = (РодительВодители.ПодчиненныеЭлементы.Количество() > 0);
	
КонецПроцедуры

#Область РаботаСЗаявкамиНаТС

&НаКлиенте
Процедура ЗаполнитьКоличествоДокументовНаКлиенте()
	
	Если ОтбиратьТендерыПоРейсу Тогда
		ЗаполнитьКоличествоДокументов(спзВыделенныеРейсы);	
	Иначе
		КоличествоЗаявокНаТС = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоличествоДокументов(мсвРейсы)
	ЗаполнитьКоличествоЗаявокНаТС(мсвРейсы);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоличествоЗаявокНаТС(мсвРейсы)
	Если мсвРейсы.Количество() > 0 Тогда
		КоличествоЗаявокНаТС = упРейс.ПолучитьКоличествоЗаявокНаТСпоРейсу(мсвРейсы);
	Иначе
		КоличествоЗаявокНаТС = 0;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
// Возвращает массив заявок на ТС и флаг необходимости формирования заявок
//
Функция СформироватьЗаявкуНаТС(Рейс, ОписаниеОшибки, Отказ)
	
	Возврат Документы.упРейс.СформироватьЗаявкуНаТСПоКоманде(Рейс, ОписаниеОшибки, Отказ);
	
КонецФункции

&НаСервере
Функция СформироватьПодтверждениеНаСервере(Основание, ОписаниеОшибки, Отказ)
	Возврат Документы.упПодтверждениеЗаявкиНаТС.СформироватьПодтверждениеНаОсновании(Основание, ОписаниеОшибки, Отказ);
КонецФункции 

#КонецОбласти

&НаСервере
Процедура СохранитьНастройки()
	
	тбзНастройки = НастройкаПриоритетовРаспределения.Выгрузить();
	ОбщегоНазначенияВызовСервера.ХранилищеНастроекДанныхФормСохранить("упРМНазначенияТСНаРейсы", "НастройкаПриоритетовРаспределения", тбзНастройки);
	
	ХранилищеСистемныхНастроек.Сохранить("Обработка.упРМНазначенияТСНаРейсы.Форма.Форма.Рейсы/ТекущиеПользовательскиеНастройки",,Рейсы.КомпоновщикНастроек.ПользовательскиеНастройки);
	ХранилищеСистемныхНастроек.Сохранить("Обработка.упРМНазначенияТСНаРейсы.Форма.Форма.Рейсы/КлючТекущихПользовательскихНастроек",,Рейсы.КлючТекущихПользовательскихНастроек);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТипыТС(РассчитыватьРасходы)
	
	ТипыТС.ПолучитьЭлементы().Очистить();
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если Не ВыделенныеСтроки.Количество() Тогда
		НадписьСостояниеРасчета = НСтр("ru = 'В списке рейсов выделен не один рейс'");
		РассчитыватьРасходы = Ложь;
		Возврат;
	КонецЕсли; 
	
	ОбновитьТипыТСНаСервере(РассчитыватьРасходы);
	
	ЭлементыПеревозчики = ТипыТС.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыПеревозчики Цикл
		Элементы.ТипыТС.Развернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьТипыТСНаСервере(РассчитыватьРасходы)
	
	спзДанныеПеревозчиков.Очистить();
	мсвРейсов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы.Рейсы.ВыделенныеСтроки);
	
	Запрос = Новый Запрос;
	нМенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = нМенеджерВременныхТаблиц;
	Запрос.Текст = упПодборТС.ПолучитьТекстЗапросаДоступныеТипыТС();
	Запрос.УстановитьПараметр("МассивРейсов"                       , мсвРейсов);
	Запрос.УстановитьПараметр("ТолькоСобственныеПеревозчики"       , ОтбиратьТолькоСобственнныхПеревозчиковТипыТС);
	Запрос.УстановитьПараметр("КонтрольМассы"                      , ПараметрыКонтроляСовместимости.КонтрольМассы);
	Запрос.УстановитьПараметр("КонтрольОбъема"                     , ПараметрыКонтроляСовместимости.КонтрольОбъема);
	Запрос.УстановитьПараметр("КонтрольКоличестваМест"             , ПараметрыКонтроляСовместимости.КонтрольКоличестваМест);
	Запрос.УстановитьПараметр("КонтрольКоличестваЗагрузочныхМетров", ПараметрыКонтроляСовместимости.КонтрольКоличестваЗагрузочныхМетров);
	Запрос.УстановитьПараметр("КонтрольГабаритовГрузовогоМеста"    , ПараметрыКонтроляСовместимости.КонтрольГабаритовГрузовогоМеста);
	Запрос.УстановитьПараметр("КонтрольДоступностиЗон"             , ПараметрыКонтроляСовместимости.ДоступностьЗон);
	Запрос.УстановитьПараметр("КонтрольТиповКузовов"               , ПараметрыКонтроляСовместимости.КонтрольТиповКузовов);
	Запрос.УстановитьПараметр("КонтрольКлассовОпасности"           , ПараметрыКонтроляСовместимости.КонтрольКлассовОпасности);
	Запрос.УстановитьПараметр("КонтрольТемпературногоРежима"       , ПараметрыКонтроляСовместимости.КонтрольТемпературногоРежима);	
	Запрос.УстановитьПараметр("КонтрольДоступностиТС"              , ПараметрыКонтроляСовместимости.ДоступностьРесурсовПартнера);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		дзТипыТС = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		ЭлементыПеревозчики = ТипыТС.ПолучитьЭлементы();
		Для каждого текПеревозчик Из дзТипыТС.Строки Цикл
			НовыйПеревозчик = ЭлементыПеревозчики.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПеревозчик, текПеревозчик);
			НовыйПеревозчик.Объект = НовыйПеревозчик.Перевозчик;
			
			ЭлементыТипыТС = НовыйПеревозчик.ПолучитьЭлементы();
			Для каждого текТипТС Из текПеревозчик.Строки Цикл
				НовыйТипТС = ЭлементыТипыТС.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйТипТС, текТипТС);
				НовыйТипТС.Объект = НовыйТипТС.ТипТС;
				НовыйТипТС.Габариты = НСтр(СтрШаблон("ru = '%1×%2×%3'", Формат(НовыйТипТС.Ширина, "ЧРД=.; ЧН="), Формат(НовыйТипТС.Длина, "ЧРД=.; ЧН="), Формат(НовыйТипТС.Высота, "ЧРД=.; ЧН=")));
				Если ЗначениеЗаполнено(НовыйПеревозчик.Перевозчик) Тогда
					спзДанныеПеревозчиков.Добавить(Новый Структура("Перевозчик, ТипТС", НовыйПеревозчик.Перевозчик, НовыйТипТС.Объект));
				КонецЕсли; 
			КонецЦикла; 
		КонецЦикла; 
		
	Иначе
		РассчитыватьРасходы = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = нМенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ * ИЗ втКонтроль";
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.ЕстьНарушенияТС Тогда
					НадписьСостояниеРасчета = НСтр("ru = 'Среди выделенных рейсов присутствуют рейсы с назначенными транспортными средствами'");
				ИначеЕсли Выборка.ЕстьПодтвержденныеЗаявки Тогда
					НадписьСостояниеРасчета = НСтр("ru = 'Среди выделенных рейсов присутствуют рейсы с подтвержденными заявками на ТС'");
				ИначеЕсли Выборка.КоличествоВидовПеревозок > 1 Тогда
					НадписьСостояниеРасчета = НСтр("ru = 'Среди выделенных рейсов присутствуют рейсы с разными видами перевозок'");
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТранспортныеСредства(РассчитыватьРасходы)
	
	ТранспортныеСредства.ПолучитьЭлементы().Очистить();
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() <> 1 Тогда
		НадписьСостояниеРасчета = НСтр("ru = 'В списке рейсов выделен не один рейс'");
		РассчитыватьРасходы = Ложь;
		Возврат;
	КонецЕсли; 
	
	ОбновитьТранспортныеСредстваНаСервере();
	
	ЭлементыПеревозчики = ТранспортныеСредства.ПолучитьЭлементы();
	Для каждого текПеревозчик Из ЭлементыПеревозчики Цикл
		Элементы.ТранспортныеСредства.Развернуть(текПеревозчик.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТранспортныеСредстваНаСервере()
	
	спзДанныеПеревозчиков.Очистить();
	мсвРейсов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы.Рейсы.ВыделенныеСтроки);
	ВедетсяУчетДокументов	= ПараметрыКонтроляСовместимости.КонтрольДокументов;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	упПлановыеПараметрыРейса.ЗонаПогрузки,
	               |	упПлановыеПараметрыРейса.ЗонаРазгрузки,
	               |	упПлановыеПараметрыРейса.Рейс,
	               |	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения КАК НачалоРейса,
	               |	упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения КАК ОкончаниеРейса
	               |ИЗ
	               |	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |ГДЕ
	               |	упПлановыеПараметрыРейса.Рейс В(&Рейс)";
	Запрос.УстановитьПараметр("Рейс", мсвРейсов);
	ПараметрыРейсов = Запрос.Выполнить().Выгрузить();
	ЗоныОтправления = ПараметрыРейсов.ВыгрузитьКолонку("ЗонаПогрузки");
	ЗоныПрибытия = ПараметрыРейсов.ВыгрузитьКолонку("ЗонаРазгрузки");
	ГаражиЗон = Справочники.упГаражи.ПодобратьГаражиДляТранспортногоСредства(, ЗоныОтправления, ЗоныПрибытия).Гаражи;
		
	Запрос = Новый Запрос;
	Запрос.Текст = упПодборТС.ПолучитьТекстЗапросаДоступныеТранспортныеСредства();
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("МассивРейсов"                       , мсвРейсов);
	Запрос.УстановитьПараметр("ТолькоСобственныеПеревозчики"       , ОтбиратьТолькоСобственнныхПеревозчиковТС);
	Запрос.УстановитьПараметр("КонтрольМассы"                      , ПараметрыКонтроляСовместимости.КонтрольМассы);
	Запрос.УстановитьПараметр("КонтрольОбъема"                     , ПараметрыКонтроляСовместимости.КонтрольОбъема);
	Запрос.УстановитьПараметр("КонтрольКоличестваМест"             , ПараметрыКонтроляСовместимости.КонтрольКоличестваМест);
	Запрос.УстановитьПараметр("КонтрольКоличестваЗагрузочныхМетров", ПараметрыКонтроляСовместимости.КонтрольКоличестваЗагрузочныхМетров);
	Запрос.УстановитьПараметр("КонтрольГабаритовГрузовогоМеста"    , ПараметрыКонтроляСовместимости.КонтрольГабаритовГрузовогоМеста);
	Запрос.УстановитьПараметр("КонтрольРасписанияРаботыТС"         , ПараметрыКонтроляСовместимости.КонтрольРасписанияРаботыТС);
	Запрос.УстановитьПараметр("КонтрольМестоположенияТранспорта"   , ПараметрыКонтроляСовместимости.КонтрольМестоположенияТранспорта);
	Запрос.УстановитьПараметр("КонтрольДоступностиЗон"             , ПараметрыКонтроляСовместимости.ДоступностьЗон);
	Запрос.УстановитьПараметр("КонтрольДоступностиТС"              , ПараметрыКонтроляСовместимости.ДоступностьРесурсовПартнера);
	Запрос.УстановитьПараметр("КонтрольТиповКузовов"               , ПараметрыКонтроляСовместимости.КонтрольТиповКузовов);
	Запрос.УстановитьПараметр("КонтрольКлассовОпасности"           , ПараметрыКонтроляСовместимости.КонтрольКлассовОпасности);
	Запрос.УстановитьПараметр("КонтрольТемпературногоРежима"       , ПараметрыКонтроляСовместимости.КонтрольТемпературногоРежима);
	Запрос.УстановитьПараметр("КонтрольДокументов"				   , ВедетсяУчетДокументов);
	ГраничныеЗоны = ЗоныОтправления;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ГраничныеЗоны, ЗоныПрибытия, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ГраничныеЗоны, ГаражиЗон.ВыгрузитьКолонку("ЗонаГаража"), Истина);
	Запрос.УстановитьПараметр("ГраничныеЗоны", ГраничныеЗоны);
	мсвРезультат = Запрос.ВыполнитьПакет();
	ИндексИерархияУчетныхЗон = мсвРезультат.ВГраница();
	ИндексПериодыЗанятости = мсвРезультат.ВГраница() - 1;
	ИндексЗапросаПрицепы = мсвРезультат.ВГраница() - 2;
	ИндексЗапросаТС = мсвРезультат.ВГраница() - 3;
	ИндексЗапросаДокументы = мсвРезультат.ВГраница() - 4;
	
	РезультатТранспортноеСредство = мсвРезультат[ИндексЗапросаТС];
	ПериодыЗанятости = мсвРезультат[ИндексПериодыЗанятости].Выгрузить();
	ИерархияУчетныхЗон = мсвРезультат[ИндексИерархияУчетныхЗон].Выгрузить();
	тбзПрицепы	= мсвРезультат[ИндексЗапросаПрицепы].Выгрузить();
	Если Не РезультатТранспортноеСредство.Пустой() Тогда
		
		Если ВедетсяУчетДокументов Тогда
			тбзДокументыСИстекающимСрокомДействия = мсвРезультат[ИндексЗапросаДокументы].Выгрузить();
		КонецЕсли;
		
		дзТранспортныеСредства = РезультатТранспортноеСредство.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		ЭлементыПеревозчики = ТранспортныеСредства.ПолучитьЭлементы();
		Для каждого текПеревозчик Из дзТранспортныеСредства.Строки Цикл
			НовыйПеревозчик = ЭлементыПеревозчики.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПеревозчик, текПеревозчик);
			НовыйПеревозчик.ИндексТипаСостоянияТС = -1;
			НовыйПеревозчик.Объект = НовыйПеревозчик.Перевозчик;
			Если ВедетсяУчетДокументов Тогда
				НовыйПеревозчик.СрокПоДокументамИстекает = 0;
			КонецЕсли;
			
			ЭлементыТранспортныеСредства = НовыйПеревозчик.ПолучитьЭлементы();
			Для каждого текТипТС Из текПеревозчик.Строки Цикл
				Если ПараметрыКонтроляСовместимости.КонтрольРасписанияРаботыТС Тогда
					ТранспортДоступен = Истина;
					Транспорт = текТипТС.ТранспортноеСредство;
					Если ПараметрыРейсов.Количество() = 0 Тогда
						Продолжить;		
					КонецЕсли;
					Для Каждого Рейс Из мсвРейсов Цикл
						ПараметрыРейса = ПараметрыРейсов.Найти(Рейс, "Рейс");
						ЗонаНачала = ПараметрыРейса.ЗонаПогрузки;
						СтрокиГаража = ГаражиЗон.НайтиСтроки(Новый Структура("ТранспортноеСредство, Адрес", Транспорт, ЗонаНачала));
						Если СтрокиГаража.Количество() > 0 Тогда
							ЗонаНачала = СтрокиГаража[0].ЗонаГаража;
						КонецЕсли;
						ЗонаОкончания = ПараметрыРейса.ЗонаРазгрузки;
						СтрокиГаража = ГаражиЗон.НайтиСтроки(Новый Структура("ТранспортноеСредство, Адрес", Транспорт, ЗонаОкончания));
						Если СтрокиГаража.Количество() > 0 Тогда
							ЗонаОкончания = СтрокиГаража[0].ЗонаГаража;
						КонецЕсли;
						// Здесь дублируется проверка занятости транспорта по отношению к запросу, но контроль зон в запросе делать  неудобно
						ТранспортДоступен = упПравилаДоступностиРесурсов.ТранспортДоступенПоЗанятости(Транспорт, ПараметрыРейса.НачалоРейса, ПараметрыРейса.ОкончаниеРейса, ПериодыЗанятости, 
							ПараметрыКонтроляСовместимости.КонтрольМестоположенияТранспорта, ЗонаНачала, ЗонаОкончания, ИерархияУчетныхЗон);
						Если Не ТранспортДоступен Тогда
							Прервать;;
						КонецЕсли; 
					КонецЦикла;
					Если Не ТранспортДоступен Тогда
						Продолжить;
					КонецЕсли; 
				КонецЕсли; 
				
				НовоеТранспортноеСредство = ЭлементыТранспортныеСредства.Добавить();
				ЗаполнитьЗначенияСвойств(НовоеТранспортноеСредство, текТипТС);
				НовоеТранспортноеСредство.ИндексТипаСостоянияТС = -1;
				Если ЗначениеЗаполнено(текТипТС.ТипСостоянияТС) Тогда
					НовоеТранспортноеСредство.ИндексТипаСостоянияТС = Перечисления.упТипыСостоянийТС.Индекс(текТипТС.ТипСостоянияТС);
				КонецЕсли; 
				НовоеТранспортноеСредство.Объект = НовоеТранспортноеСредство.ТранспортноеСредство;
				
				текГабариты = НСтр(СтрШаблон("ru = '%1×%2×%3'", Формат(НовоеТранспортноеСредство.Ширина, "ЧРД=.; ЧН="), Формат(НовоеТранспортноеСредство.Длина, "ЧРД=.; ЧН="), Формат(НовоеТранспортноеСредство.Высота, "ЧРД=.; ЧН=")));
				
				Если текТипТС.КоличествоПрицепов > 0 Тогда
					мсвСтрокиПрицепы = тбзПрицепы.НайтиСтроки(Новый Структура("ТранспортноеСредство", текТипТС.ТранспортноеСредство));	
					Для каждого текХарактеристикаПрицепа Из мсвСтрокиПрицепы Цикл
						текГабариты = текГабариты + ", " + НСтр(СтрШаблон("ru = '%1×%2×%3'", Формат(текХарактеристикаПрицепа.Ширина, "ЧРД=.; ЧН="), Формат(текХарактеристикаПрицепа.Длина, "ЧРД=.; ЧН="), Формат(текХарактеристикаПрицепа.Высота, "ЧРД=.; ЧН=")));	
					КонецЦикла;
				КонецЕсли;
				 		
				НовоеТранспортноеСредство.Габариты = текГабариты;
				Если ЗначениеЗаполнено(НовыйПеревозчик.Перевозчик) Тогда
					спзДанныеПеревозчиков.Добавить(Новый Структура("Перевозчик, ТипТС", НовыйПеревозчик.Перевозчик, НовоеТранспортноеСредство.ТипТС));
				КонецЕсли; 
				
				Если ВедетсяУчетДокументов Тогда
					мсвДокументыСИстекающимСрокомГодности = тбзДокументыСИстекающимСрокомДействия.НайтиСтроки(Новый Структура("ТранспортноеСредство", НовоеТранспортноеСредство.ТранспортноеСредство));
					Если мсвДокументыСИстекающимСрокомГодности.Количество()>0 Тогда
						НовоеТранспортноеСредство.СрокПоДокументамИстекает = 2;
					Иначе	
						НовоеТранспортноеСредство.СрокПоДокументамИстекает = 1;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли; 
КонецПроцедуры

&наКлиенте
Процедура ОбновитьВодителей()
	
	Водители.ПолучитьЭлементы().Очистить();
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли; 
	
	ОбновитьВодителейНаСервере();
	
	ЭлементыВодители = Водители.ПолучитьЭлементы();
	Для каждого текСотрудник Из ЭлементыВодители Цикл
		Элементы.Водители.Развернуть(текСотрудник.ПолучитьИдентификатор());
	КонецЦикла	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВодителейНаСервере()
	
	мсвРейсов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы.Рейсы.ВыделенныеСтроки);
	
	ВедетсяУчетДокументов	= ПараметрыКонтроляСовместимости.КонтрольДокументов;
	
	Запрос = Новый Запрос;
	Запрос.Текст = упПодборТС.ПолучитьТекстЗапросаВодители();	
	Запрос.УстановитьПараметр("МассивРейсов", мсвРейсов);
	Запрос.УстановитьПараметр("КонтрольДоступностиВодителей", ПараметрыКонтроляСовместимости.ДоступностьРесурсовПартнера);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("КонтрольДокументов", ВедетсяУчетДокументов);
	Запрос.УстановитьПараметр("КонтрольРасписанияРаботыВодителей", ПараметрыКонтроляСовместимости.КонтрольРасписанияРаботыВодителей);
	
	мсвРезультат = Запрос.ВыполнитьПакет();
	КоличествоЗапросовВПакете = мсвРезультат.Количество();
	ИндексЗапросаДокументы = КоличествоЗапросовВПакете - 2;
	ИндексЗапросаВодители = КоличествоЗапросовВПакете - 1;
	
	РезультатСотрудники	= мсвРезультат[ИндексЗапросаВодители];
		
	Если НЕ РезультатСотрудники.Пустой() Тогда
		
		Если ВедетсяУчетДокументов Тогда
			тбзДокументыСИстекающимСрокомДействия = мсвРезультат[ИндексЗапросаДокументы].Выгрузить();
		КонецЕсли;
		
		дзВодители = РезультатСотрудники.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		ЭлементыПеревозчики = Водители.ПолучитьЭлементы();
		
		Для каждого текПеревозчик Из дзВодители.Строки Цикл
			НовыйПеревозчик = ЭлементыПеревозчики.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПеревозчик, текПеревозчик);
			НовыйПеревозчик.Объект = НовыйПеревозчик.Перевозчик;
			НовыйПеревозчик.Порядок = 9;
			Если ВедетсяУчетДокументов Тогда
				НовыйПеревозчик.СрокПоДокументамИстекает = 0;
			КонецЕсли;
			
			ЭлементыВодители = НовыйПеревозчик.ПолучитьЭлементы();
			Для каждого текСотрудник Из текПеревозчик.Строки Цикл
				НовыйСотрудник = ЭлементыВодители.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйСотрудник, текСотрудник);
				НовыйСотрудник.Объект = НовыйСотрудник.Сотрудник;
				Если ВедетсяУчетДокументов Тогда
					мсвДокументыСИстекающимСрокомГодности = тбзДокументыСИстекающимСрокомДействия.НайтиСтроки(Новый Структура("Сотрудник, ТранспортноеСредство", текСотрудник.Сотрудник, текСотрудник.ТранспортноеСредство));
					Если мсвДокументыСИстекающимСрокомГодности.Количество()>0 Тогда
						НовыйСотрудник.СрокПоДокументамИстекает = 2;
					Иначе	
						НовыйСотрудник.СрокПоДокументамИстекает = 1;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСоСтекомЗаданий

&НаКлиенте
Процедура ОбработатьИзменениеРейса()
	
	РассчитыватьРасходы = Истина;
	
	Если Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТипыТС Тогда
		
		ОбновитьТипыТС(РассчитыватьРасходы);
		
	ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТранспортныеСредства Тогда
		
		ОбновитьТранспортныеСредства(РассчитыватьРасходы);
		
	ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаВодители Тогда
		
		ОбновитьВодителей();
		РассчитыватьРасходы = Ложь;
		
	ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТендеры Тогда
		
		Элементы.ЗаявкиНаТСОтбиратьТендерыПоРейсу.Пометка = ОтбиратьТендерыПоРейсу;
		
		РассчитыватьРасходы = Ложь;
		ЗаполнитьДанныеПоВыделеннымРейсам();
	    ЗаполнитьКоличествоДокументовНаКлиенте();

		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЗаявкиНаТС, "Рейсы", спзВыделенныеРейсы.ВыгрузитьЗначения());
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЗаявкиНаТС, "ОтбиратьТендерыПоРейсу", ОтбиратьТендерыПоРейсу);
		
	КонецЕсли; 

	// Нужно закомментировать, если требуется предотвращать частые аварийные завершения рабочего процесса
	Если РассчитыватьРасходы Тогда 
		ЗаполнитьДанныеПоВыделеннымРейсам();
		ВыполнитьЗадание();
		ПодключитьОбработчикОжидания("ОбновитьСостояниеЗадачи", 1);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоВыделеннымРейсам()
	
	спзВыделенныеРейсыДанные.Очистить();    // Хранятся данные динамического списка по выделенным рейсам
	спзВыделенныеРейсы.Очистить();
	
	Для каждого текРейс Из Элементы.Рейсы.ВыделенныеСтроки Цикл
		текДанныеСтроки = Элементы.Рейсы.ДанныеСтроки(текРейс);
		Если текДанныеСтроки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		спзВыделенныеРейсы.Добавить(текРейс);
		спзВыделенныеРейсыДанные.Добавить(Новый Структура("Рейс, ПлановыеРасходыПоРейсу", текРейс, текДанныеСтроки.ПлановыеРасходыПоРейсу));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Процедура запускается из обрабочика ожидания.
//
Процедура ОбновитьСостояниеЗадачи()
	
	ЗаданиеВыполнено = Истина;
	ЗаданиеВыполнено = ОбновитьСостояниеЗадачиНаСервере();
	
	Если ЗаданиеВыполнено Тогда 
		ОтключитьОбработчикОжидания("ОбновитьСостояниеЗадачи");
		ОбработатьРезультатВыполненияЗадания();
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСостояниеЗадачи()

&НаСервере
Функция ОбновитьСостояниеЗадачиНаСервере()
	
	ЗаданиеВыполнено = Ложь;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если Задание = Неопределено Или Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда 
		ЗаданиеВыполнено = Истина;
		упСервисныеФункции.ПроверитьВывестиОшибкуФоновогоЗадания(Задание);
	Иначе
		Если ЗначениеЗаполнено(Объект.ПризнакЗавершенияВыполненияАдресВоВременномХранилище) Тогда
			ЗаданиеВыполнено = ПолучитьИзВременногоХранилища(Объект.ПризнакЗавершенияВыполненияАдресВоВременномХранилище);		
		КонецЕсли; 
		
	КонецЕсли;
	
	Возврат ЗаданиеВыполнено;
	
КонецФункции // ОбновитьСостояниеЗадачиНаСервере()

&НаСервере
Процедура ОбработатьРезультатВыполненияЗадания()
	
	сткРезультатЗаполнения = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если Не сткРезультатЗаполнения = Неопределено Тогда
		
		тбзРейсы  = сткРезультатЗаполнения.Рейсы;
		Для каждого СтрокаРейс Из тбзРейсы Цикл
			
			тбзРасшифровка 	= СтрокаРейс.ПлановыеРасходыПоРейсамРасшифровка;
			
			Для каждого СтрокаРасход Из тбзРасшифровка Цикл
				НоваяСтрока 	 = Объект.РейсРасшифровкаПлановыхРасходов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасход);
				НоваяСтрока.Рейс = СтрокаРейс.Рейс;
			КонецЦикла;
			
			тбзПлановыеРасходы = СтрокаРейс.ПлановыеРасходыПоРейсам;
			
			Для каждого СтрокаРасход Из тбзПлановыеРасходы Цикл
				НоваяСтрока 	 = Объект.РейсПлановыеРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасход);
				НоваяСтрока.Рейс = СтрокаРейс.Рейс;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьРасходы();
	
	Если Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТипыТС Тогда
		//Элементы.ГруппаСтраницыКартинкаТипыТС.ТекущаяСтраница = Элементы.СтраницаРассчитанТипыТС;
	ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТранспортныеСредства Тогда
		//Элементы.ГруппаСтраницыКартинкаТранспортныеСредства.ТекущаяСтраница = Элементы.СтраницаРассчитанТранспортныеСредства;
	КонецЕсли;
	НадписьСостояниеРасчета = НСтр("ru = 'Расходы рассчитаны'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасходы()
	
	Если Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТипыТС Тогда
		// Заполнение расходов по типам ТС
		СтрокиПеревозчики = ТипыТС.ПолучитьЭлементы();
		Для каждого СтрокаПеревозчик Из СтрокиПеревозчики Цикл
			
			текПеревозчик = СтрокаПеревозчик.Перевозчик;
			
			СтрокиТипыТС = СтрокаПеревозчик.ПолучитьЭлементы();
			
			Для каждого СтрокаТипТС Из СтрокиТипыТС Цикл
				
				текТипТС = СтрокаТипТС.Объект;
				
				мсвСтрокиРасходов = Объект.РейсПлановыеРасходы.НайтиСтроки(Новый Структура("Перевозчик, ТипТС", текПеревозчик, текТипТС));
				
				максСуммаРасходов = 0;
				
				Для каждого СтрокаРасходов Из мсвСтрокиРасходов Цикл
					Если СтрокаРасходов.Сумма > максСуммаРасходов Тогда
						максСуммаРасходов = СтрокаРасходов.Сумма;
					КонецЕсли;
				КонецЦикла;
				
				СтрокаТипТС.ПлановыеРасходыПоРейсу = максСуммаРасходов;
				СтрокаТипТС.РасшифровкаРасходов = (СтрокаТипТС.ПлановыеРасходыПоРейсу > 0);
				
			КонецЦикла;
		КонецЦикла;
		
	ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТранспортныеСредства Тогда
		// Заполнение расходов по ТС
		СтрокиПеревозчики = ТранспортныеСредства.ПолучитьЭлементы();
		Для каждого СтрокаПеревозчик Из СтрокиПеревозчики Цикл
			
			текПеревозчик = СтрокаПеревозчик.Перевозчик;
			
			СтрокиТипыТС = СтрокаПеревозчик.ПолучитьЭлементы();
			
			Для каждого СтрокаТипТС Из СтрокиТипыТС Цикл
				
				текТипТС = СтрокаТипТС.ТипТС;
				
				мсвСтрокиРасходов = Объект.РейсПлановыеРасходы.НайтиСтроки(Новый Структура("Перевозчик, ТипТС", текПеревозчик, текТипТС));
				
				максСуммаРасходов = 0;
				
				Для каждого СтрокаРасходов Из мсвСтрокиРасходов Цикл
					Если СтрокаРасходов.Сумма > максСуммаРасходов Тогда
						максСуммаРасходов = СтрокаРасходов.Сумма;
					КонецЕсли;
				КонецЦикла;
				
				СтрокаТипТС.ПлановыеРасходыПоРейсу = максСуммаРасходов;
				СтрокаТипТС.РасшифровкаРасходов = (СтрокаТипТС.ПлановыеРасходыПоРейсу > 0);
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадание()
	
	Объект.РейсПлановыеРасходы.Очистить();
	Объект.РейсРасшифровкаПлановыхРасходов.Очистить();
	ЗапуститьПроцессРасчетаРасходов();
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьПроцессРасчетаРасходов()
		
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	Если спзВыделенныеРейсыДанные.Количество() = 0 Или спзДанныеПеревозчиков.Количество() = 0 Тогда
		ПризнакЗавершенияВыполненияАдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Истина, Объект.ПризнакЗавершенияВыполненияАдресВоВременномХранилище);
		Возврат;
	Иначе	
		ПризнакЗавершенияВыполненияАдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Ложь, Объект.ПризнакЗавершенияВыполненияАдресВоВременномХранилище);
	КонецЕсли;
	
	тбзРейсы = Новый ТаблицаЗначений;
	тбзРейсы.Колонки.Добавить("Рейс", Новый ОписаниеТипов("ДокументСсылка.упРейс"));
	тбзРейсы.Колонки.Добавить("ПлановыеРасходыПоРейсу", Новый ОписаниеТипов("Число"));
	Для каждого сткДанные Из спзВыделенныеРейсыДанные Цикл
		СтрокаНовая = тбзРейсы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНовая, сткДанные.Значение);
	КонецЦикла;
	
	тбзПеревозчики = Новый ТаблицаЗначений;
	тбзПеревозчики.Колонки.Добавить("Перевозчик");
	тбзПеревозчики.Колонки.Добавить("ТипТС");
	Для каждого сткДанные Из спзДанныеПеревозчиков Цикл
		СтрокаНовая = тбзПеревозчики.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНовая, сткДанные.Значение);
	КонецЦикла;
	
	тбзПеревозчики.Свернуть("Перевозчик, ТипТС");
	
	Если тбзРейсы.Количество() И тбзПеревозчики.Количество() Тогда
		Если Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТипыТС Тогда
			//Элементы.ГруппаСтраницыКартинкаТипыТС.ТекущаяСтраница = Элементы.СтраницаРассчитываетсяТипыТС;
		ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаТранспортныеСредства Тогда
			//Элементы.ГруппаСтраницыКартинкаТранспортныеСредства.ТекущаяСтраница = Элементы.СтраницаРассчитываетсяТранспортныеСредства;
		ИначеЕсли Элементы.ГруппаПодборТС.ТекущаяСтраница = Элементы.ГруппаВодители Тогда	
			//	
		КонецЕсли;
		НадписьСостояниеРасчета = НСтр("ru = 'Расчет плановых расходов ...'");
	Иначе
		НадписьСостояниеРасчета = "";
		Возврат;
	КонецЕсли; 
	
	ВходныеПараметры = Новый Структура();
	ВходныеПараметры.Вставить("СпособРасчета", 											"РассчитатьРейсыПоПеревозчикам");
	ВходныеПараметры.Вставить("тбзПеревозчики", 										тбзПеревозчики);
	ВходныеПараметры.Вставить("РезультатАдресВоВременномХранилище", 					АдресВременногоХранилища);
	ВходныеПараметры.Вставить("ПризнакЗавершенияВыполненияАдресВоВременномХранилище", 	ПризнакЗавершенияВыполненияАдресВоВременномХранилище);
	
	мсвПараметров = Новый Массив(3);
	мсвПараметров[0] = тбзРейсы;
	мсвПараметров[1] = ВходныеПараметры;
	
	//ФоновоеЗаданиеОтменить = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	//Если Не ФоновоеЗаданиеОтменить = Неопределено Тогда
	//	ФоновоеЗаданиеОтменить.Отменить();
	//КонецЕсли; 
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("упТарификацияВызовСервера.Рассчитать", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Расчет расходов'"));
	ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция СформироватьРасшифровкуРасходов(Источник = "ТранспортныеСредства")
	
	Если Источник = "ТранспортныеСредства" Тогда
		текИденфтикаторСтроки = Элементы.ТранспортныеСредства.ТекущаяСтрока;
		текДанные = ТранспортныеСредства.НайтиПоИдентификатору(текИденфтикаторСтроки);
	Иначе	
		текИденфтикаторСтроки = Элементы.ТипыТС.ТекущаяСтрока;
		текДанные = ТипыТС.НайтиПоИдентификатору(текИденфтикаторСтроки);
	КонецЕсли;
		
	Если Не текДанные = Неопределено Тогда
		Если ТипЗнч(текДанные.Объект) = Тип("СправочникСсылка.упТипыТС") 
			ИЛИ ТипЗнч(текДанные.Объект) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
			
			текПеревозчик 	= текДанные.Перевозчик;
			
			Если Источник = "ТранспортныеСредства" Тогда
				текТипТС = текДанные.ТипТС;
			Иначе	
				текТипТС = текДанные.Объект;
			КонецЕсли;

			СформироватьРасшифровкуПоРейсам(текПеревозчик, текТипТС);
			
			ТабличныйДокумент = Новый ТабличныйДокумент;
		    ТабличныйДокумент.Присоединить(Объект.РезультатТрассировки);
			Возврат ТабличныйДокумент;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция СформироватьПакетДокументов(Источник = "ТранспортныеСредства", ТранспортноеСредствоРейса = Неопределено)
	
	Если Источник = "ТранспортныеСредства" Тогда
		текИдентификаторСтроки = Элементы.ТранспортныеСредства.ТекущаяСтрока;
		текДанные = ТранспортныеСредства.НайтиПоИдентификатору(текИдентификаторСтроки);
	Иначе	
		текИдентификаторСтроки = Элементы.Водители.ТекущаяСтрока;
		текДанные = Водители.НайтиПоИдентификатору(текИдентификаторСтроки);
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Если Не текДанные = Неопределено Тогда
		текОбъект = текДанные.Объект;
		Если Источник = "ТранспортныеСредства" Тогда
			текВодитель 			= Справочники.упСотрудники.ПустаяСсылка();
			текТранспортноеСредство = текОбъект;
		Иначе	
			текВодитель 			= текОбъект;
			текТранспортноеСредство	= ТранспортноеСредствоРейса;
		КонецЕсли;
		
		Объект.РезультатТрассировки.Очистить();
		
		текСхемаКомпоновкиДанных	= Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ПакетДокументов");
			
		текКомпоновщикМакетаКомпоновкиДанных	= Новый КомпоновщикМакетаКомпоновкиДанных;
	
		Настройки = текСхемаКомпоновкиДанных.НастройкиПоУмолчанию;
		ПараметрыДанных = Настройки.ПараметрыДанных;
		ПараметрыДанных.УстановитьЗначениеПараметра("Водитель", текВодитель); 
		ПараметрыДанных.УстановитьЗначениеПараметра("ТранспортноеСредство", текТранспортноеСредство);
		ПараметрыДанных.УстановитьЗначениеПараметра("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
		
		ЗаписьХМЛ = Новый ЗаписьXML;
		ЗаписьХМЛ.ОткрытьФайл("D:\Настройики.xml");
		СериализаторXDTO.ЗаписатьXML(ЗаписьХМЛ, Настройки);
		ЗаписьХМЛ.Закрыть();
		
		ЗаписьХМЛ = Новый ЗаписьXML;
		ЗаписьХМЛ.ОткрытьФайл("D:\Схема.xml");
		СериализаторXDTO.ЗаписатьXML(ЗаписьХМЛ, текСхемаКомпоновкиДанных);
		ЗаписьХМЛ.Закрыть();

		
		текМакетКомпоновкиДанных	= текКомпоновщикМакетаКомпоновкиДанных.Выполнить(текСхемаКомпоновкиДанных, Настройки);
		
		текПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		
		текПроцессорКомпоновкиДанных.Инициализировать(текМакетКомпоновкиДанных);
		
		текПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		
		текПроцессорВыводаРезультата.УстановитьДокумент(Объект.РезультатТрассировки);
		
		текПроцессорВыводаРезультата.Вывести(текПроцессорКомпоновкиДанных);
	
	КонецЕсли;
	
	ТабличныйДокумент.Присоединить(Объект.РезультатТрассировки);
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.ОтображатьСетку = Ложь;

	Возврат ТабличныйДокумент;
	
КонецФункции

#Область КартографическийСервис

&НаСервере
Процедура УстановитьКартографическийСервис()
	МаршрутРейсаHTML			= упМаршрутизация.ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса();
	МаршрутРейсаHTMLСформирован = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьКартографическийСервис()
	МаршрутРейсаHTML	= упРаботаСКартамиКлиентСервер.ИнициализацияМакетаЗначениямиПоУмолчанию(МаршрутРейсаHTML, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРейсыНаКартеОбработчик()

	Если МаршрутРейсаHTMLСформирован Тогда
		ОтключитьОбработчикОжидания("ОтобразитьРейсыНаКартеОбработчик");
		упРаботаСКартамиКлиентСервер.ОтобразитьРейсы(Элементы.МаршрутРейса.Документ, спзВыделенныеРейсы, спзИндексыРейсовНаКарте);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостьюКарты(ПоказыватьКартуЗначение = Неопределено)
	
	Если ПоказыватьКартуЗначение = Неопределено Тогда
		ПоказыватьКарту = НЕ ПоказыватьКарту;
		Объект.ПоказыватьКарту = ПоказыватьКарту;
	КонецЕсли;
	Если ПоказыватьКарту И НЕ КартографическийСервисИнициализирован() Тогда
		УстановитьКартографическийСервис();
		ИнициализироватьКартографическийСервис();
	КонецЕсли;

	Элементы.МаршрутРейсаКонтекстноеМенюОтображатьКарту.Пометка = ПоказыватьКарту;
	Элементы.РейсыКонтекстноеМенюОтображатьКарту.Пометка 		= ПоказыватьКарту;
	Элементы.МаршрутРейса.Видимость 							= ПоказыватьКарту;
	Если НЕ ПоказыватьКарту Тогда
		МаршрутРейсаHTMLСформирован = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КартографическийСервисИнициализирован()

	Возврат НЕ МаршрутРейсаHTML = "";	

КонецФункции // КартографическийСервисИнициализирован()

#КонецОбласти

&НаСервере
Функция ПолучитьСоглашениеПоУмолчанию(Перевозчик)

	Возврат Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(Перевозчик);

КонецФункции // ПолучитьСоглашениеПоУмолчанию()

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		Элементы.РейсыМасса.Заголовок                = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.РейсыМасса.Заголовок, сткПредставления.Масса));
		Элементы.ТипыТСГрузоподъемность.Заголовок    = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТипыТСГрузоподъемность.Заголовок, сткПредставления.Масса));
		Элементы.ТранспортныеСредстваГрузоподъемность.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТранспортныеСредстваГрузоподъемность.Заголовок, сткПредставления.Масса));
	Иначе
		Элементы.РейсыГруппаКолонокМасса.Видимость = Ложь;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		Элементы.РейсыОбъем.Заголовок                = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.РейсыОбъем.Заголовок, сткПредставления.Объем));
		Элементы.ТипыТСОбъем.Заголовок               = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТипыТСОбъем.Заголовок, сткПредставления.Объем));
		Элементы.ТранспортныеСредстваОбъем.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТранспортныеСредстваОбъем.Заголовок, сткПредставления.Объем));
	Иначе
		Элементы.РейсыГруппаКолонокОбъем.Видимость = Ложь;	
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда         
		Элементы.РейсыКоличествоМест.Заголовок                = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.РейсыКоличествоМест.Заголовок, сткПредставления.КоличествоМест));
		Элементы.ТипыТСКоличествоМест.Заголовок               = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТипыТСКоличествоМест.Заголовок, сткПредставления.КоличествоМест));
		Элементы.ТранспортныеСредстваКоличествоМест.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТранспортныеСредстваКоличествоМест.Заголовок, сткПредставления.КоличествоМест));
	Иначе
		Элементы.РейсыГруппаКолонокКоличествоМест.Видимость = Ложь;	
	КонецЕсли;
	Если Не УчитыватьКоличествоЗагрузочныхМетров Тогда
	//	Элементы.РейсыКоличествоЗагрузочныхМетров.Заголовок                = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.РейсыКоличествоЗагрузочныхМетров.Заголовок, сткПредставления.КоличествоЗагрузочныхМетров));
	//	Элементы.ТипыТСКоличествоЗагрузочныхМетров.Заголовок               = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТипыТСКоличествоЗагрузочныхМетров.Заголовок, сткПредставления.КоличествоЗагрузочныхМетров));
	//	Элементы.ТранспортныеСредстваКоличествоЗагрузочныхМетров.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТранспортныеСредстваКоличествоЗагрузочныхМетров.Заголовок, сткПредставления.КоличествоЗагрузочныхМетров));
	//Иначе
		Элементы.РейсыГруппаКолонокКоличествоЗагрузочныхМетров.Видимость = Ложь;	
	КонецЕсли;
	Если УчитыватьЛинейныеРазмеры Тогда
		Элементы.ТипыТСГабариты.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТипыТСГабариты.Заголовок, сткПредставления.КоличествоЗагрузочныхМетров));
		Элементы.ТранспортныеСредстваГабариты.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТранспортныеСредстваГабариты.Заголовок, сткПредставления.КоличествоЗагрузочныхМетров));
	КонецЕсли; 
	
	ВалютаУпр = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	ВалютаУпрПредставление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВалютаУпр, "Наименование");
	Элементы.ТипыТСПлановыеРасходыПоРейсу.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТипыТСПлановыеРасходыПоРейсу.Заголовок, ВалютаУпрПредставление));
	Элементы.ТранспортныеСредстваПлановыеРасходыПоРейсу.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ТранспортныеСредстваПлановыеРасходыПоРейсу.Заголовок, ВалютаУпрПредставление));
	
КонецПроцедуры // УстановитьЗаголовки()

&НаСервере
Процедура СформироватьРасшифровкуПоРейсам(Перевозчик, ТипТС)
	
	Объект.РезультатТрассировки.Очистить();
	
	тбзРасшифровкаРасходов = Новый ТаблицаЗначений;
	тбзРасшифровкаРасходов.Колонки.Добавить("СтатьяРасходов"						, Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
	тбзРасшифровкаРасходов.Колонки.Добавить("ТипИсточникаСтатейРасходов"			, Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(1,0,ДопустимыйЗнак.Неотрицательный)));
	тбзРасшифровкаРасходов.Колонки.Добавить("ТипИсточникаСтатейРасходовОписание"	, Новый ОписаниеТипов("Строка",,,Новый КвалификаторыСтроки(100)));
	тбзРасшифровкаРасходов.Колонки.Добавить("Сумма"								    , Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(16,2)));
	тбзРасшифровкаРасходов.Колонки.Добавить("Рейс"								    , Новый ОписаниеТипов("ДокументСсылка.упРейс"));
	
	ВыделенныеСтроки = Элементы.Рейсы.ВыделенныеСтроки;
	Для каждого текРейс Из ВыделенныеСтроки Цикл
		
		мсвСтрокРасшифровки	= Объект.РейсРасшифровкаПлановыхРасходов.НайтиСтроки(Новый Структура("Рейс, Перевозчик, ТипТС, ТипИсточникаСтатейРасходов", текРейс, Перевозчик, Справочники.упТипыТС.ПустаяСсылка(), 1));
		
		Для каждого текЭлементРасшифровка Из мсвСтрокРасшифровки Цикл
			текСтрокаРасходов = тбзРасшифровкаРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(текСтрокаРасходов, текЭлементРасшифровка);
			текСтрокаРасходов.Рейс  = текРейс; 
		КонецЦикла;					
		
		мсвСтрокРасшифровки	= Объект.РейсРасшифровкаПлановыхРасходов.НайтиСтроки(Новый Структура("Рейс, Перевозчик, ТипТС, ТипИсточникаСтатейРасходов", текРейс, Перевозчик, Справочники.упТипыТС.ПустаяСсылка(), 3));
		
		Для каждого текЭлементРасшифровка Из мсвСтрокРасшифровки Цикл
			текСтрокаРасходов = тбзРасшифровкаРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(текСтрокаРасходов, текЭлементРасшифровка);
			текСтрокаРасходов.Рейс  = текРейс;
		КонецЦикла;					
		
		мсвСтрокРасшифровки	= Объект.РейсРасшифровкаПлановыхРасходов.НайтиСтроки(Новый Структура("Рейс, Перевозчик, ТипТС", текРейс, Перевозчик, ТипТС));
		
		Для каждого текЭлементРасшифровка Из мсвСтрокРасшифровки Цикл
			
			текСтрокаРасходов = тбзРасшифровкаРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(текСтрокаРасходов, текЭлементРасшифровка);
			текСтрокаРасходов.Рейс  = текРейс;
			
		КонецЦикла;					
		
	КонецЦикла;							
	
	текСхемаКомпоновкиДанных	= Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("РасходыРасшифровка");
	
	текКомпоновщикМакетаКомпоновкиДанных	= Новый КомпоновщикМакетаКомпоновкиДанных;
	
	текМакетКомпоновкиДанных	= текКомпоновщикМакетаКомпоновкиДанных.Выполнить(текСхемаКомпоновкиДанных, текСхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	текПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	
	текПроцессорКомпоновкиДанных.Инициализировать(текМакетКомпоновкиДанных, Новый Структура("тбзРасходы", тбзРасшифровкаРасходов), );
	
	текПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	текПроцессорВыводаРезультата.УстановитьДокумент(Объект.РезультатТрассировки);
	
	текПроцессорВыводаРезультата.Вывести(текПроцессорКомпоновкиДанных);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПараметрыКонтроляСовместимости()
	
	ОбщегоНазначенияВызовСервера.ХранилищеНастроекДанныхФормСохранить("упРМНазначенияТСНаРейсы", "ПараметрыКонтроляСовместимости", ПараметрыКонтроляСовместимости);
	УстановитьПиктограммыКонтроля();
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьРезультатыРаспределенияТиповТС(МассивРешений)
	
	Для каждого текРешение Из МассивРешений Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРешение.Рейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр(СтрШаблон("ru='Не удалось заблокировать %1. %2'", текРешение.Рейс, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецПопытки;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Отказ = Ложь;
		текСоглашение = Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(текРешение.Перевозчик);			
		упРейсВызовСервера.НазначитьРеквизитыРейса(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(текРешение.Рейс), Новый Структура("Перевозчик, Соглашение, ТипТС", текРешение.Перевозчик, текСоглашение, текРешение.ТипТС), Отказ);
		Если Отказ Тогда
			ОтменитьТранзакцию();
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Не удалось присвоить тип ТС рейсу %1'", текРешение.Рейс));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);	
			Продолжить;
		КонецЕсли;
		
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЦикла;	
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьРезультатыРаспределенияТС(МассивРешений)
	
	МассивРейсы = Новый Массив;
	Для Каждого ЭлементМассива Из МассивРешений Цикл
		МассивРейсы.Добавить(ЭлементМассива.Рейс);
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПлановыеПараметрыРейса.ЗонаПогрузки,
	               |	упПлановыеПараметрыРейса.ЗонаРазгрузки,
	               |	упПлановыеПараметрыРейса.Рейс,
	               |	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения,
	               |	упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения
	               |ИЗ
	               |	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |ГДЕ
	               |	упПлановыеПараметрыРейса.Рейс В(&Рейс)";
	Запрос.УстановитьПараметр("Рейс", МассивРейсы);
	ПараметрыРейсов = Запрос.Выполнить().Выгрузить();
	Для каждого текРешение Из МассивРешений Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРешение.Рейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр(СтрШаблон("ru='Не удалось заблокировать %1. %2'", текРешение.Рейс, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецПопытки;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Отказ = Ложь;
		текСоглашение = Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(текРешение.Перевозчик);		
		КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(текРешение.Перевозчик);
		РеквизитыРейса = Новый Структура("Перевозчик, КонтрагентПеревозчика, Соглашение, ТипТС, ТранспортноеСредство", текРешение.Перевозчик, КонтрагентПеревозчика, текСоглашение, текРешение.ТипТС, текРешение.ТранспортноеСредство);
		ПарамаетрыРейса = ПараметрыРейсов.Найти(текРешение.Рейс, "Рейс");
		упПодборТС.ПолучитьЭкипажПоУмолчаниюДляРейса(ПарамаетрыРейса.ПлановоеНачалоВыполнения, ПарамаетрыРейса.ПлановоеОкончаниеВыполнения,
			ПараметрыКонтроляСовместимости.КонтрольРасписанияРаботыВодителей, РеквизитыРейса);
		упРейсВызовСервера.НазначитьРеквизитыРейса(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(текРешение.Рейс), РеквизитыРейса, Отказ);
		Если Отказ Тогда
			ОтменитьТранзакцию();
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Не удалось присвоить транспортное средство рейсу %1'", текРешение.Рейс));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);	
			Продолжить;
		КонецЕсли;
		
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЦикла;	
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьРезультатыРаспределенияВодителей(МассивРешений)
	
	Для каждого текРешение Из МассивРешений Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРешение.Рейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр(СтрШаблон("ru='Не удалось заблокировать %1. %2'", текРешение.Рейс, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецПопытки;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Отказ = Ложь;
		упРейсВызовСервера.НазначитьРеквизитыРейса(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(текРешение.Рейс), Новый Структура("Водитель1", текРешение.Водитель1), Отказ);
		Если Отказ Тогда
			ОтменитьТранзакцию();
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Не удалось присвоить водителя рейсу %1'", текРешение.Рейс));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);	
			Продолжить;
		КонецЕсли;
		
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЦикла;	
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	
КонецПроцедуры

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия формы настроек рабочего места.
//
Процедура ОбработкаОповещенияОЗакрытииФормыНастройкиПриоритетов(Параметр, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Параметр) = Тип("Структура") Тогда
		
		ПараметрыКонтроляСовместимости = Параметр.ПараметрыКонтроляСовместимости;
		СохранитьПараметрыКонтроляСовместимости();
		
		ОбновитьЭлементыФормы = "";
		
		Если Не Объект.ИспользуетсяТрассировкаРешения = Параметр.ИспользуетсяТрассировкаРешения Тогда
			ОбновитьЭлементыФормы = "ИспользуетсяТрассировкаРешения";
			Объект.ИспользуетсяТрассировкаРешения = Параметр.ИспользуетсяТрассировкаРешения;
		КонецЕсли;
		
		Если Не Объект.ПоказыватьКарту = Параметр.ПоказыватьКарту Тогда
			ОбновитьЭлементыФормы = ОбновитьЭлементыФормы + ?(СтрДлина(ОбновитьЭлементыФормы)>0,",","") + "ПоказыватьКарту";
			Объект.ПоказыватьКарту = Параметр.ПоказыватьКарту;	
		КонецЕсли;
		
		НастройкаПриоритетовРаспределения.Очистить();
		тбзНастройкаПриоритетовРаспределения = Параметр.НастройкаПриоритетовРаспределения;
		
		Для каждого текНастройка Из тбзНастройкаПриоритетовРаспределения Цикл
			текНоваяСтрока = НастройкаПриоритетовРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(текНоваяСтрока, текНастройка);
		КонецЦикла;
		
		ПодключитьОбработчикОжидания("ОбработатьИзменениеРейса", 0.2, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы автоматического распределения.
//
Процедура РаспределитьТипыТСПоРейсамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Количество() Тогда
			ПрименитьРезультатыРаспределенияТиповТС(Результат);
						
			Элементы.Рейсы.Обновить();
			ОбработатьИзменениеРейса();
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры // РаспределитьТипыТСПоРейсамЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы автоматического распределения транспорта.
//
Процедура РаспределитьТСПоРейсамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Количество() Тогда
			ПрименитьРезультатыРаспределенияТС(Результат);
			
			Элементы.Рейсы.Обновить();
			ОбработатьИзменениеРейса();
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры // РаспределитьТипыТСПоРейсамЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы автоматического распределения транспорта.
//
Процедура РаспределитьВодителейПоРейсамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Количество() Тогда
			ПрименитьРезультатыРаспределенияВодителей(Результат);
			
			Элементы.Рейсы.Обновить();
			ОбработатьИзменениеРейса();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 
