
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипОперации = Параметры.ТипОперации;
	ПараметрыКонтроляСовместимости = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Параметры.ПараметрыКонтроляСовместимости);
	НастройкаПриоритетовРаспределения.Загрузить(Параметры.НастройкаПриоритетовРаспределения.Выгрузить());
	МассивРейсов.ЗагрузитьЗначения(Параметры.МассивРейсов);
	ОбновитьВидимость();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УстановитьПредварительныйОтборПодбираемыхПараметровПриИзменении(Элемент)
	
	Элементы.ГруппаСКД.Доступность = УстановитьПредварительныйОтбор;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для каждого текСтрока Из СписокРейсов Цикл
		текСтрока.Пометка = Истина;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для каждого текСтрока Из СписокРейсов Цикл
		текСтрока.Пометка = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	 СписокРейсов.Очистить();
	 Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПредварительныйОтбор;
	 Элементы.ФормаПодобратьТранспорт.Видимость = Истина;
	 Элементы.ФормаПодобратьТранспорт.КнопкаПоУмолчанию = Истина;
	 Элементы.ФормаПрименить.Видимость = Ложь;
	 Элементы.ФормаНазад.Видимость = Ложь;
		
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ПриЗакрытииНаСервере();
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьТранспорт(Команда)
	
	Если ТипОперации = "ПодборТранспортныхСредств" Тогда
		РаспределитьТСПоРейсамНаСервере();
	ИначеЕсли ТипОперации = "ПодборТиповТС" Тогда
		РаспределитьТипыТСПоРейсамНаСервере();
	ИначеЕсли ТипОперации = "ПодборВодителей" Тогда	
		РаспределитьВодителейПоРейсамНаСервере();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбновитьСостояниеЗадачи", 1);
    Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;	
	Элементы.ФормаПодобратьТранспорт.Видимость = Ложь;
	Элементы.ФормаПрименить.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	мсвРешений = Новый Массив;
	Для каждого текСтрока Из СписокРейсов Цикл
		Если текСтрока.Пометка Тогда
			Если ТипОперации = "ПодборТранспортныхСредств" Тогда
				сткРезультатПодбора = Новый Структура("Рейс, Перевозчик, ТипТС, ТранспортноеСредство", текСтрока.Рейс, текСтрока.Перевозчик, текСтрока.ТипТС, текСтрока.ТранспортноеСредство);
				мсвРешений.Добавить(сткРезультатПодбора);
				
			ИначеЕсли ТипОперации = "ПодборТиповТС" Тогда
				сткРезультатПодбора = Новый Структура("Рейс, Перевозчик, ТипТС", текСтрока.Рейс, текСтрока.Перевозчик, текСтрока.ТипТС);
				мсвРешений.Добавить(сткРезультатПодбора);
				
			ИначеЕсли ТипОперации = "ПодборВодителей" Тогда	
				сткРезультатПодбора = Новый Структура("Рейс, Водитель1", текСтрока.Рейс, текСтрока.Водитель1);
				мсвРешений.Добавить(сткРезультатПодбора);
				
			КонецЕсли; 
			
		КонецЕсли; 
	КонецЦикла; 
	Закрыть(мсвРешений);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимость()
	
	ТекстТипОперации = "";
	
	Если ТипОперации = "ПодборТранспортныхСредств" Тогда
		ТекстТипОперации = "транспортных средств";
		Элементы.СписокРейсовВодитель1.Видимость = Ложь;
		
		СхемаКомпоновкиДанных = Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ОтборТС");
		
	ИначеЕсли ТипОперации = "ПодборТиповТС" Тогда
		ТекстТипОперации = "типов ТС";
		Элементы.СписокРейсовТранспортноеСредство.Видимость = Ложь;
		Элементы.СписокРейсовВодитель1.Видимость            = Ложь;
		
		СхемаКомпоновкиДанных = Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ОтборТипыТС");
		
	ИначеЕсли ТипОперации = "ПодборВодителей" Тогда	
		ТекстТипОперации = "водителей";
		СхемаКомпоновкиДанных = Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ОтборВодители");
		
	КонецЕсли; 
 
	Элементы.НадписьДлительнаяОперация.Заголовок = НСтр(СтрШаблон("ru = 'Подождите, выполняется автоматическое назначение %1 на рейсы ...'", ТекстТипОперации));

	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаКлиенте
// Процедура запускается из обрабочика ожидания.
//
Процедура ОбновитьСостояниеЗадачи()
	
	ЗаданиеВыполнено = Истина;
	ЗаданиеВыполнено = ОбновитьСостояниеЗадачиНаСервере();
	
	Если ЗаданиеВыполнено Тогда 
		ОтключитьОбработчикОжидания("ОбновитьСостояниеЗадачи");
		мсвРешений = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		
		Если мсвРешений <> Неопределено Тогда
			Для каждого текРешение Из мсвРешений Цикл
				ЗаполнитьЗначенияСвойств(СписокРейсов.Добавить(), текРешение);
				Если ПараметрыКонтроляСовместимости.КонтрольДокументов Тогда
					мсвСообщения = Неопределено;
					Если ТипЗнч(текРешение) = Тип("Структура") И текРешение.Свойство("мсвСообщения",мсвСообщения) Тогда
						Для каждого текСообщение Из мсвСообщения Цикл
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(текСообщение.Текст, текСообщение.Ссылка);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;			
		КонецЕсли; 
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультаты;
		Элементы.ФормаНазад.Видимость = Истина;
		Элементы.ФормаПрименить.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСостояниеЗадачи()

&НаСервере
Функция ОбновитьСостояниеЗадачиНаСервере()
	
	ЗаданиеВыполнено = Ложь;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	Если Задание = Неопределено Или Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда 
		ЗаданиеВыполнено = Истина;
		упСервисныеФункции.ПроверитьВывестиОшибкуФоновогоЗадания(Задание);
	Иначе
		мсвСообшений = Задание.ПолучитьСообщенияПользователю(Истина);
		Если мсвСообшений <> Неопределено И мсвСообшений.Количество() Тогда
			Для Каждого текСообщение Из мсвСообшений Цикл
				Текст = текСообщение.Текст;
				Если Текст = "true" Тогда
					ЗаданиеВыполнено = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗаданиеВыполнено;
	
КонецФункции // ОбновитьСостояниеЗадачиНаСервере()

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры // ПриЗакрытииНаСервере()

&НаСервере
Процедура РаспределитьТипыТСПоРейсамНаСервере()
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Новый Массив, Новый УникальныйИдентификатор);
	
	мсвПараметров = Новый Массив;
	мсвПараметров.Добавить(МассивРейсов.ВыгрузитьЗначения());
	мсвПараметров.Добавить(ПараметрыКонтроляСовместимости);
	мсвПараметров.Добавить(НастройкаПриоритетовРаспределения.Выгрузить());
	Если УстановитьПредварительныйОтбор Тогда
		мсвПараметров.Добавить(ПолучитьМассивОбъектов());	
	Иначе
		мсвПараметров.Добавить(Неопределено);
	КонецЕсли; 
	мсвПараметров.Добавить(АдресВременногоХранилища);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("упПодборТС.РаспределитьТипыТСПоРейсам", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Назначение типов ТС на рейсы'"));
	ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьТСПоРейсамНаСервере()
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Новый Массив, УникальныйИдентификатор); //Новый УникальныйИдентификатор);
	
	мсвПараметров = Новый Массив;
	мсвПараметров.Добавить(МассивРейсов.ВыгрузитьЗначения());
	мсвПараметров.Добавить(ПараметрыКонтроляСовместимости);
	мсвПараметров.Добавить(НастройкаПриоритетовРаспределения.Выгрузить());
	Если УстановитьПредварительныйОтбор Тогда
		мсвПараметров.Добавить(ПолучитьМассивОбъектов());
	Иначе
		мсвПараметров.Добавить(Неопределено);
	КонецЕсли; 
	мсвПараметров.Добавить(АдресВременногоХранилища);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("упПодборТС.РаспределитьТСПоРейсам", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Назначение транспортных средств на рейсы'"));
	ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;

КонецПроцедуры

&НаСервере
Процедура РаспределитьВодителейПоРейсамНаСервере()

	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Новый Массив, Новый УникальныйИдентификатор);
	
	мсвПараметров = Новый Массив;
	мсвПараметров.Добавить(МассивРейсов.ВыгрузитьЗначения());
	мсвПараметров.Добавить(ПараметрыКонтроляСовместимости);
	Если УстановитьПредварительныйОтбор Тогда
		мсвПараметров.Добавить(ПолучитьМассивОбъектов());	
	Иначе
		мсвПараметров.Добавить(Неопределено);
	КонецЕсли; 
	мсвПараметров.Добавить(АдресВременногоХранилища);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("упПодборТС.РаспределитьВодителейПоРейсам", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Назначение водителей на рейсы'"));
	ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;
	
КонецПроцедуры 

&НаСервере
Функция ПолучитьМассивОбъектов()
	
	Если ТипОперации = "ПодборТранспортныхСредств" Тогда
		СхемаКомпоновкиДанных = Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ОтборТС");
		
	ИначеЕсли ТипОперации = "ПодборТиповТС" Тогда
		СхемаКомпоновкиДанных = Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ОтборТипыТС");
		
	ИначеЕсли ТипОперации = "ПодборВодителей" Тогда	
		СхемаКомпоновкиДанных = Обработки.упРМНазначенияТСНаРейсы.ПолучитьМакет("ОтборВодители");
		
	КонецЕсли;
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти 