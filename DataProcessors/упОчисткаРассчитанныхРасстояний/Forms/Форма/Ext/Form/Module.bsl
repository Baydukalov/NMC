
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СхемаКомпоновкиДанных = Обработки.упОчисткаРассчитанныхРасстояний.ПолучитьМакет("ОтборЗон");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);

	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить();

КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура УдалитьЗаписи(Команда)
	
	КоличествоЗаписей = ПолучитьРазмерРегистра();
	Если КоличествоЗаписей = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Регистр ""Расстояния между точками"" пустой'"));
		Возврат;
	КонецЕсли; 
	
	ПолнаяОчистка = Истина;
	Для каждого текЭлемент Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если текЭлемент.Использование = Истина Тогда
			ПолнаяОчистка = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПолнаяОчистка Тогда
		ТекстВопроса = НСтр(СтрШаблон("ru = 'Не установлено ни одного элемента отбора. Все записи регистра ""Расстояния между точками"" будут удалены (количество записей: %1)'", КоличествоЗаписей));
		ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьРегистрОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	    Возврат;
	КонецЕсли; 
	
	Отказ = Ложь;
	ИдентификаторЗадания = Неопределено;
	УдалитьЗаписиНаСервере(Ложь, ИдентификаторЗадания, Отказ);
	Если Не Отказ И ИдентификаторЗадания <> Неопределено Тогда 
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ИдентификаторЗадания" , ИдентификаторЗадания);
		ОткрытьФорму("Обработка.упОчисткаРассчитанныхРасстояний.Форма.ФормаИндикаторСостоянияУдаления", сткПараметры,,,,, Новый ОписаниеОповещения("ОчисткаРассчитанныхРасстоянийЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьРазмерРегистра()
	
	Возврат упСервисныеФункции.ПолучитьРазмерРегистра(Метаданные.РегистрыСведений.упРасстоянияМеждуТочками);
	
КонецФункции

&НаСервере
Процедура УдалитьЗаписиНаСервере(ПолнаяОчистка, ИдентификаторЗадания, Отказ)
	
	Если ПолнаяОчистка Тогда
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(Неопределено);
	Иначе
		СхемаКомпоновкиДанных = Обработки.упОчисткаРассчитанныхРасстояний.ПолучитьМакет("ОтборЗон");
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));
		
		// Компоновка макета
		КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		// Инициализация процессора компоновки
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
		
		// Таблица значений, в которую будет получен результат
		Результат = Новый ТаблицаЗначений;
		
		// Получение результата
		ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
		ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
		
		Если Результат.Количество() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Расстояний с установленным отбором не существует'"));
			Отказ = Истина;
			Возврат;
		КонецЕсли; 
		
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(Результат);
	КонецЕсли; 
	
    ФоновоеЗадание = ФоновыеЗадания.Выполнить("упСервисныеФункции.УдалитьЗаписиРегистраРасстоянияМеждуТочками", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Удаление записей регистра ""Расстояния между точками""'"));
	ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
// Очищение строк регистра "упРасстоянияМеждуТочками".
//
Процедура ОчиститьРегистрОтветНаВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		Отказ = Ложь;
		ИдентификаторЗадания = Неопределено;
		УдалитьЗаписиНаСервере(Истина, ИдентификаторЗадания, Отказ);
		Если Не Отказ И ИдентификаторЗадания <> Неопределено Тогда 
			сткПараметры = Новый Структура;
			сткПараметры.Вставить("ИдентификаторЗадания" , ИдентификаторЗадания);
			ОткрытьФорму("Обработка.упОчисткаРассчитанныхРасстояний.Форма.ФормаИндикаторСостоянияУдаления", сткПараметры,,,,, Новый ОписаниеОповещения("ОчисткаРассчитанныхРасстоянийЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Вывод сообщение пользователю после очистки записей.
//
Процедура ОчисткаРассчитанныхРасстоянийЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Очистка записей завершена'"));
	
КонецПроцедуры
	
#КонецОбласти
