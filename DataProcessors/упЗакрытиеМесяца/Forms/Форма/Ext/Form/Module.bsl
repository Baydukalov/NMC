&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаданиеВыполнено = Истина;
	УстановитьПериод(НачалоМесяца(НачалоМесяца(ТекущаяДатаСеанса())-1));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредставлениеПериодаОчистка(Элемент, СтандартнаяОбработка)
		СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура РегламентнаяОперацияНажатие(Элемент)
	
	ИндексРегламентнойОперации = СтрЗаменить(Элемент.Имя, "РегламентнаяОперация", "");
	Попытка 
		ИндексРегламентнойОперации = Число(ИндексРегламентнойОперации) - 1;
		Если ИндексРегламентнойОперации < 0 Тогда
			Возврат;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
		
	СтрокаТаблицы 	= Операции[ИндексРегламентнойОперации];
	
	СписокКоманд 	= КомандыРегламентнойОперации(СтрокаТаблицы);
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтрокаТаблицы", СтрокаТаблицы);
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ОповещениеВыбораИзМеню = Новый ОписаниеОповещения("РегламентнаяОперацияНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВыборИзМеню(ОповещениеВыбораИзМеню, СписокКоманд, Элемент);

КонецПроцедуры	

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПоказатьСписокВыбораПериода(Объект.Период);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УвеличитьПериод(Команда)
	УстановитьПериод(КонецМесяца(Объект.Период)+1);
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьПериод(Команда)
	УстановитьПериод(НачалоМесяца(Объект.Период-1));
КонецПроцедуры

&НаСервере
Процедура УстановитьПериод(ДатаПериода)
	Объект.Период = ДатаПериода;
	ПредставлениеПериода = упЗакрытиеПериода.ПолучитьПредставлениеПериода(Объект.Период);	
	ОбновитьФормуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗакрытие(Команда)
	ОповеститьФормыРегламентныхОпераций(ОтменитьЗакрытиеПериода());
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПериод(Команда)
	ЗакрытьПериодНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьТаблицуОпераций()
	текОперации = РеквизитФормыВЗначение("Операции");
	текОперации.Очистить();
	
	тбзОперации = упЗакрытиеПериода.ТаблицаРегламентныхОпераций(Объект.Период);
	Для каждого СтрокаОперация  Из тбзОперации Цикл
		НоваяСтрока = текОперации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОперация);
	КонецЦикла;
	ЗначениеВРеквизитФормы(текОперации, "Операции");
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьФормыРегламентныхОпераций(Ссылки = Неопределено)
	
	// Ссылки - это массив операций или одна операция
	
	ОповеститьОбИзменении = Ложь;
	
	Если Ссылки <> Неопределено Тогда
		ПараметрыОповещения = Новый Структура("ОбрабатываемыеОперации", Ссылки);
		Оповестить("ОбновитьРегламентныеОперации", ПараметрыОповещения);
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.упРегламентнаяОперация"));
	
КонецПроцедуры

#Область ГрупповыеОперации

&НаКлиенте
Процедура ЗакрытьПериодНаКлиенте()
	
	Результат = ПровестиОперациюЗакрытияНаСервере();
	
	мсвОперации    = Результат.Операции;
	ФоновоеЗадание = Результат.ФоновоеЗадание;
	
	Если ЗначениеЗаполнено(ФоновоеЗадание) Тогда
				
		ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ПрогрессВыполнения", ЭтотОбъект);
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		НастройкиОжидания.ПолучатьРезультат = Истина;
		НастройкиОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
		
		Обработчик = Новый ОписаниеОповещения("ПослеЗакрытияПериода", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПровестиОперациюЗакрытияНаСервере()
	
	сткРезультат = Новый Структура("Операции, ФоновоеЗадание", Неопределено, Неопределено);
	ЗаданиеВыполнено = Ложь;
	мсвОпераций      = Неопределено;
	
	Если НЕ упСервисныеФункции.ЗаданиеЕщеВыполняется(ИдентификаторЗадания) Тогда	
		
		НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Проведение закрытия периода %1'"), ПредставлениеПериода);
		
		ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
		
			
		ПараметрыПроцедуры = Новый Структура("Операции, Период, ФоновоеЗадание", Операции.Выгрузить(), Объект.Период, Истина);
		
		ФоновоеЗадание = ДлительныеОперации.ВыполнитьВФоне("упЗакрытиеПериода.ВыполнитьЗакрытиеПериода", 
		ПараметрыПроцедуры, ПараметрыВыполненияВФоне);
		
		ИдентификаторЗадания = ФоновоеЗадание.ИдентификаторЗадания;
		
		Если ФоновоеЗадание.Статус = "Выполняется" Тогда
			Элементы.ЭтапыЗакрытия.ТекущаяСтраница = Элементы.Ожидание;
		КонецЕсли;
							
		сткРезультат.Вставить("ФоновоеЗадание", ФоновоеЗадание);					
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

&НаСервере
Функция ОтменитьЗакрытиеПериода()
	
	мсвРегламентныеОперации = Новый Массив;
	КоличествоОпераций = Операции.Количество();
	
	Для й_шаг = 1 По КоличествоОпераций Цикл
		
		Строка = Операции[КоличествоОпераций - й_шаг];
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.Статус = Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена
			ИЛИ Строка.Статус = Перечисления.упСтатусыОперацииЗакрытия.Исключена Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		РегламентнаяОперацияОбъект = Строка.Ссылка.ПолучитьОбъект();
		РегламентнаяОперацияОбъект.ОтменитьПроведениеОперацииЗакрытия();
		мсвРегламентныеОперации.Добавить(РегламентнаяОперацияОбъект.Ссылка);
		ЗафиксироватьТранзакцию();
	КонецЦикла;
	
	ОбновитьФормуНаСервере();
	
	Возврат мсвРегламентныеОперации;
	
КонецФункции

#КонецОбласти

#Область ОдиночныеОперации

&НаКлиенте
Процедура ПровестиОперациюЗакрытияРегламентнойОперацииНаКлиенте(Ссылка, ВидОперации)
	
	ФоновоеЗадание = ПровестиОперациюЗакрытияРегламентнойОперацииНаСервере(Ссылка, ВидОперации);
	
	Если ЗначениеЗаполнено(ФоновоеЗадание) Тогда
				
		ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ПрогрессВыполнения", ЭтотОбъект);
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		НастройкиОжидания.ПолучатьРезультат = Истина;
		НастройкиОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
		
		Обработчик = Новый ОписаниеОповещения("ПослеЗакрытияПериода", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПровестиОперациюЗакрытияРегламентнойОперацииНаСервере(Ссылка, ВидОперации)
	
	ЗаданиеВыполнено 	= Ложь;
	ВыполненоУспешно	= Ложь;
	
	ФоновоеЗадание = Неопределено;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		РегламентнаяОперацияОбъект = Ссылка.ПолучитьОбъект();
	Иначе
		РегламентнаяОперацияОбъект             = Документы.упРегламентнаяОперация.СоздатьДокумент();
		РегламентнаяОперацияОбъект.Дата        = КонецМесяца(Объект.Период);
		РегламентнаяОперацияОбъект.ВидОперации = ВидОперации;
	КонецЕсли;
	
	Если НЕ упСервисныеФункции.ЗаданиеЕщеВыполняется(ИдентификаторЗадания) Тогда	
		НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Проведение закрытия периода операции ""%1""'"), ВидОперации);
		ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
					
	
		ПараметрыПроцедуры = Новый Структура("Ссылка, ФоновоеЗадание", РегламентнаяОперацияОбъект.Ссылка, Истина);
		
		ФоновоеЗадание = ДлительныеОперации.ВыполнитьВФоне("упЗакрытиеПериода.ПровестиОперациюЗакрытия", 
		ПараметрыПроцедуры, ПараметрыВыполненияВФоне);
		ИдентификаторЗадания = ФоновоеЗадание.ИдентификаторЗадания;
		
		Если ФоновоеЗадание.Статус = "Выполняется" Тогда
			Элементы.ЭтапыЗакрытия.ТекущаяСтраница = Элементы.Ожидание;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаСервере
Процедура ОтменитьПроведениеНаСервере(Ссылка)
	
	// Транзакция, что бы данные не брались из кэша
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	докРегламентнаяОперацияОбъект = Ссылка.ПолучитьОбъект();
	докРегламентнаяОперацияОбъект.ОтменитьПроведениеОперацииЗакрытия();
    ОбновитьФормуНаСервере();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьОперацию(Ссылка, ВидОперации)
	
	// Транзакция, что бы данные не брались из кэша
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		РегламентнаяОперацияОбъект = Ссылка.ПолучитьОбъект();
	Иначе
		РегламентнаяОперацияОбъект 				= Документы.упРегламентнаяОперация.СоздатьДокумент();
		РегламентнаяОперацияОбъект.Дата       	= КонецМесяца(Объект.Период);
		РегламентнаяОперацияОбъект.ВидОперации	= ВидОперации;
	КонецЕсли;
	
	РегламентнаяОперацияОбъект.ИсключитьОперациюЗакрытия();
	
	ЗафиксироватьТранзакцию();
	
	ОбновитьФормуНаСервере();
		
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Функция КомандыРегламентнойОперации(СтрокаОперации)
	
	СписокКоманд = Новый СписокЗначений;
	
	Выполнена 	= СтрокаОперации.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыОперацииЗакрытия.Выполнена");
	ЕстьСсылка 	= ЗначениеЗаполнено(СтрокаОперации.Ссылка);
	
	Если ЕстьСсылка Тогда
		СписокКоманд.Добавить("ОткрытьДокумент", НСтр("ru = 'Открыть документ'"),, БиблиотекаКартинок.Документ);
	КонецЕсли;

	СписокКоманд.Добавить("Провести", НСтр("ru = 'Выполнить закрытие'"),, БиблиотекаКартинок.Провести);
	
	Если ЕстьСсылка И Выполнена Тогда 
		СписокКоманд.Добавить("Отменить", НСтр("ru = 'ОтменитьПроведение'"),, БиблиотекаКартинок.ОтменаПроведения);	
	КонецЕсли;
	
	Если НЕ СтрокаОперации.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыОперацииЗакрытия.Исключена") Тогда 
		СписокКоманд.Добавить("Исключить", НСтр("ru = 'Исключить'"),, БиблиотекаКартинок.упАктивностьВыключена);	
	КонецЕсли;
	
	Возврат СписокКоманд;
	
КонецФункции // КомандыРегламентнойОперации

&НаКлиенте
Процедура ОбработкаНажатияКнопкиМеню(Команда, Элемент, СтрокаОперации)
	
	Если Команда = "ОткрытьДокумент" Тогда
		сткПараметры = Новый Структура("Ключ", СтрокаОперации.Ссылка);
		ОткрытьФорму("Документ.упРегламентнаяОперация.Форма.ФормаДокумента", сткПараметры, ЭтаФорма);
	ИначеЕсли Команда = "Провести" Тогда
		ПровестиОперациюЗакрытияРегламентнойОперацииНаКлиенте(СтрокаОперации.Ссылка, СтрокаОперации.ВидОперации);
	ИначеЕсли Команда = "Отменить" Тогда
		ОтменитьПроведениеНаСервере(СтрокаОперации.Ссылка);
		ОповеститьФормыРегламентныхОпераций(СтрокаОперации.Ссылка);
	ИначеЕсли Команда = "Исключить" Тогда
		ИсключитьОперацию(СтрокаОперации.Ссылка, СтрокаОперации.ВидОперации);
		ОповеститьФормыРегламентныхОпераций(СтрокаОперации.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора элемента из меню.
//
Процедура РегламентнаяОперацияНажатиеЗавершение(Команда, ДополнительныеПараметры) Экспорт

	Если Команда <> Неопределено Тогда
		ОбработкаНажатияКнопкиМеню(Команда.Значение, ДополнительныеПараметры.Элемент, ДополнительныеПараметры.СтрокаТаблицы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСписокВыбораПериода(ДатаПериода)
	
	СписокПериодов = упСервисныеФункцииКлиентСервер.СписокФиксированныхПериодов(НачалоМесяца(ДатаПериода), ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредставлениеПериодаНачалоВыбораЗавершение", ЭтотОбъект);
	
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокПериодов, Элементы.ПредставлениеПериода);
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора элемента из списка.
//
Процедура ПредставлениеПериодаНачалоВыбораЗавершение(СтруктураПериода, ДополнительныеПараметры) Экспорт
	
	// Установим полученный период
	Если СтруктураПериода <> Неопределено Тогда
		Если СтруктураПериода.Представление = "Ранее..." 
			ИЛИ СтруктураПериода.Представление = "Позже..." Тогда
			ПоказатьСписокВыбораПериода(СтруктураПериода.Значение);
		Иначе	
			ПредставлениеПериода = СтруктураПериода.Представление;
			Объект.Период = КонецМесяца(СтруктураПериода.Значение);	
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуНаКлиенте()
	
	ОбновитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуНаСервере()
	
	Для каждого ЭлементГруппыОперации Из Элементы.ГруппаОперации.ПодчиненныеЭлементы Цикл
		Для каждого ЭлементОперации Из ЭлементГруппыОперации.ПодчиненныеЭлементы Цикл
			ЭлементОперации.Видимость = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого ЭлементГруппыСтатусы Из Элементы.ГруппаСтатусы.ПодчиненныеЭлементы Цикл
		Для каждого ЭлементСтатус Из ЭлементГруппыСтатусы.ПодчиненныеЭлементы Цикл
			ЭлементСтатус.Видимость = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	ЗаполнитьТаблицуОпераций();
		
	й_шаг = 1;
	Для каждого СтрокаОперация Из Операции Цикл
		
		ЭлементОперация = Элементы.Найти("РегламентнаяОперация" + й_шаг);
		ЭлементОперация.Заголовок = СтрокаОперация.ВидОперации;
		текЦветТекста	=  ЦветТекстаРегламентнойОперации(СтрокаОперация);
		ЭлементОперация.ЦветТекста	= текЦветТекста;
		ЭлементОперация.Видимость = Истина;
	
		ЭлементСтатус	= Элементы.Найти("Статус" + й_шаг);
		ЭлементСтатус.Заголовок 	= СтрокаОперация.Статус;
		ЭлементСтатус.ЦветТекста	= текЦветТекста;
		ЭлементСтатус.Видимость = Истина;
		
		ЭлементКартинка	= Элементы.Найти("РекламентаяОперацияКартинка" + й_шаг);
		ЭлементКартинка.Картинка 	= КартинкаРегламентнойОперации(СтрокаОперация); 
		ЭлементКартинка.Видимость = Истина;
			
		й_шаг = й_шаг + 1;
		
	КонецЦикла;
	
	// Расчет статуса периода
	
	Отбор = Новый Структура("Используется", Истина);
	Отбор.Вставить("Статус", Перечисления.упСтатусыОперацииЗакрытия.Выполнена);
	КоличествоВыполнено		= Операции.НайтиСтроки(Отбор).Количество();
	
	Отбор = Новый Структура("Используется", Истина);
	Отбор.Вставить("Статус", Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена);
	КоличествоНеВыполнено	= Операции.НайтиСтроки(Отбор).Количество();
	
	Отбор = Новый Структура("Используется", Истина);
	Отбор.Вставить("Статус", Перечисления.упСтатусыОперацииЗакрытия.ЕстьОшибки);
	КоличествоСОшибками 	= Операции.НайтиСтроки(Отбор).Количество();
	
	Отбор = Новый Структура("Используется", Истина);
	Отбор.Вставить("Статус", Перечисления.упСтатусыОперацииЗакрытия.Исключена);
	КоличествоИсключено     = Операции.НайтиСтроки(Отбор).Количество();
	
	КоличествоОсталось  = КоличествоСОшибками + КоличествоНеВыполнено;

	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		
		Статус = НСтр("ru = 'Не задан период'");
		ЦветСтатуса = ЦветаСтиля.ЦветОсобогоТекста;
		
	ИначеЕсли КоличествоОсталось = 0 Тогда
		
		Статус      = НСтр("ru = 'Выполнено'");
		ЦветСтатуса = WebЦвета.Зеленый;
		
	Иначе
		
		Статус      = НСтр("ru = 'Не выполнено'");
		ЦветСтатуса = WebЦвета.Черный;
		
	КонецЕсли;
	
	Элементы.Статус.ЦветТекста = ЦветСтатуса;
	
	Если НЕ ЗаданиеВыполнено Тогда
		Элементы.ЭтапыЗакрытия.ТекущаяСтраница = Элементы.Ожидание;
	Иначе	
		Элементы.ЭтапыЗакрытия.ТекущаяСтраница = Элементы.Выполнено;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ЦветТекстаРегламентнойОперации(СтрокаОперации)
	
	Если СтрокаОперации.Статус = Перечисления.упСтатусыОперацииЗакрытия.Выполнена Тогда
		Возврат WebЦвета.Зеленый;
	ИначеЕсли СтрокаОперации.Статус = Перечисления.упСтатусыОперацииЗакрытия.ЕстьОшибки Тогда
		Возврат WebЦвета.Красный;
	Иначе
		Возврат ЦветаСтиля.ЦветТекстаФормы;
	КонецЕсли;
КонецФункции

&НаСервере
Функция КартинкаРегламентнойОперации(СтрокаОперации)
	
	Если СтрокаОперации.Статус = Перечисления.упСтатусыОперацииЗакрытия.Выполнена Тогда
		
		Возврат БиблиотекаКартинок.упСтатусВыполнена;
		
	ИначеЕсли СтрокаОперации.Статус = Перечисления.упСтатусыОперацииЗакрытия.ЕстьОшибки Тогда
		
		Возврат БиблиотекаКартинок.упСтатусОтказ;
		
	ИначеЕсли СтрокаОперации.Статус = Перечисления.упСтатусыОперацииЗакрытия.Исключена Тогда
		
		Возврат БиблиотекаКартинок.упСтатусЗакрыт;
		
	ИначеЕсли СтрокаОперации.Статус = Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена Тогда
		
		Возврат БиблиотекаКартинок.упСтатусНовый;
	
		
	Иначе
		Возврат Новый Картинка;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ПрогрессВыполнения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполняется" Тогда
		Прогресс = ПрочитатьПрогресс(Результат.ИдентификаторЗадания);
		Если Прогресс <> Неопределено Тогда
			ПрогрессЗакрытияПериода = Прогресс.Процент;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьПрогресс(ИдентификаторЗадания)
	Возврат ДлительныеОперации.ПрочитатьПрогресс(ИдентификаторЗадания);
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияПериода(ФоновоеЗадание, ДополнительныеПараметры) Экспорт

	Если ФоновоеЗадание.Статус = "Выполнено" Тогда
		
		сткРезультатВыполнения = ПолучитьИзВременногоХранилища(ФоновоеЗадание.АдресРезультата);
		Если ТипЗнч(сткРезультатВыполнения) =  Тип("Структура") Тогда
			ВыполненоУспешно = сткРезультатВыполнения.ВыполненоУспешно;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(сткРезультатВыполнения, "Операции") Тогда
				мсвОпераций      = сткРезультатВыполнения.Операции;
				мсвСсылки = Новый Массив;
				Для каждого ОперацияТекущая Из мсвОпераций Цикл
					мсвСсылки.Добавить(ОперацияТекущая.Ссылка);
				КонецЦикла;
				ОповеститьФормыРегламентныхОпераций(мсвСсылки);
			ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(сткРезультатВыполнения, "Ссылка") Тогда	
				ОповеститьФормыРегламентныхОпераций(сткРезультатВыполнения.Ссылка);
			КонецЕсли;
		КонецЕсли;
		
		ЗаданиеВыполнено = Истина;
		ОбновитьФормуНаКлиенте();
		
	ИначеЕсли ФоновоеЗадание.Статус = "Ошибка" Тогда
		ТекстСообщения = Нстр("ru = 'Не удалось закрыть период. %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФоновоеЗадание.КраткоеПредставлениеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры


#КонецОбласти
