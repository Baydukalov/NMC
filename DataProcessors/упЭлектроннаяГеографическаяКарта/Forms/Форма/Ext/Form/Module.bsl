// Переменная ЧислоПопыток хранит количество попыток сформировать документ HTML.
&НаКлиенте
Перем ЧислоПопыток;

// Флаг подтверждения, используется при не модальном закрытии.
&НаКлиенте
Перем ПодтверждениеЗакрытияФормы, СлужебныеЭлементыHtml;

// Переменная ЭтоПодтверждениеАдреса признак что происходит подтвержедние адреса.
&НаКлиенте
Перем ЭтоПодтверждениеАдреса;

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	спзЦветаУсловногоОформления.Добавить("#FFFFFF");
	спзЦветаУсловногоОформления.Добавить("#1C7ED4");
	спзЦветаУсловногоОформления.Добавить("#1FCCCF");
	спзЦветаУсловногоОформления.Добавить("#DADE00");
	спзЦветаУсловногоОформления.Добавить("#33D41E");
	спзЦветаУсловногоОформления.Добавить("#D1221F");
	спзЦветаУсловногоОформления.Добавить("#1F1FD1");
	спзЦветаУсловногоОформления.Добавить("#DB09C3");
	спзЦветаУсловногоОформления.Добавить("#00FFFF");
	спзЦветаУсловногоОформления.Добавить("#FFFF00");
	спзЦветаУсловногоОформления.Добавить("#00FF00");
	спзЦветаУсловногоОформления.Добавить("#FF0000");
	спзЦветаУсловногоОформления.Добавить("#FF8500");
	спзЦветаУсловногоОформления.Добавить("#FFB4B4");
	спзЦветаУсловногоОформления.Добавить("#C0C0C0");
	спзЦветаУсловногоОформления.Добавить("#0000FF");
	спзЦветаУсловногоОформления.Добавить("#FF00FF");
	спзЦветаУсловногоОформления.Добавить("#000000");
	спзЦветаУсловногоОформления.Добавить("#BBFF2F");	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСтилиПолигонов.ЦветЗаливки КАК ОсновнойЦвет
	|ИЗ
	|	Справочник.упСтилиПолигонов КАК упСтилиПолигонов
	|ГДЕ
	|	НЕ упСтилиПолигонов.ЦветЗаливки В (&ОсновнойЦвет)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСтилиМаркеров.ОсновнойЦвет
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	НЕ упСтилиМаркеров.ОсновнойЦвет В (&ОсновнойЦвет)";
	Запрос.УстановитьПараметр("ОсновнойЦвет", спзЦветаУсловногоОформления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если Не ЗначениеЗаполнено(Выборка.ОсновнойЦвет) Тогда 
			Продолжить;
		КонецЕсли;
		
		НовыйЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		НовыйЭлементУсловногоОформления.Использование = Истина;
		
		НовыйЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", упСервисныеФункцииКлиентСервер.HexСтрВЦвет(Выборка.ОсновнойЦвет));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементУсловногоОформления.Отбор, "ДеревоОбъектов.ОсновнойЦвет", Выборка.ОсновнойЦвет, ВидСравненияКомпоновкиДанных.Равно,, Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		
		ОформляемоеПоле = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Использование = Истина;
		ОформляемоеПоле.Поле          = Новый ПолеКомпоновкиДанных("ДеревоОбъектовОсновнойЦвет");
	КонецЦикла;	
	
	НовыйУровень = ДеревоОбъектов.ПолучитьЭлементы().Добавить();
	НовыйУровень.Объект         = НСтр("ru = 'Адреса'");
	НовыйУровень.ИндексКартинки = 2;
	
	НовыйУровень = ДеревоОбъектов.ПолучитьЭлементы().Добавить();
	НовыйУровень.Объект         = НСтр("ru = 'Зоны'");
	НовыйУровень.ИндексКартинки = 4;
	
	НовыйУровень = ДеревоОбъектов.ПолучитьЭлементы().Добавить();
	НовыйУровень.Объект         = НСтр("ru = 'Маршруты'");
	НовыйУровень.ИндексКартинки = 6;

	КлючAPI = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючЭлектронныхГеографическихКарт");
	
	HTMLДокумент = упМаршрутизация.ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса();	
	
	Структура = упМаршрутизация.ПолучитьКоординатыНачальнойПозиции();
	НачальнаяШирота  = Структура.Широта;
	НачальнаяДолгота = Структура.Долгота;
	НачальныйЗум     = Структура.Зум;
	
	ЭтоЯндекс = (упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипКарт") = Перечисления.упТипыКарт.ЯндексКарты);
	ИспользуетсяГеокодированиеАдресов = упСервисныеФункции.ПолучитьЗначениеКонстанты("упИспользуетсяГеокодированиеАдресов");
	
	НетПраваРедактировать = НЕ Справочники.упАдреса.ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных();
	
	Если Параметры.Свойство("БП_ПодтверждениеАдреса") Тогда 
		НачальнаяШирота  = Параметры.Широта;
		НачальнаяДолгота = Параметры.Долгота;
	КонецЕсли;
	
КонецПроцедуры //ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии(Отказ)
	ЭтоПодтверждениеАдреса = Ложь;
	#Если ВебКлиент Тогда
		ПоказатьПредупреждение(,Нстр("ru = 'Работоспособность рабочего места в веб-клиенте не поддерживается.'"));
		Отказ=Истина;
		Возврат;
	#Иначе
	
	ЧислоПопыток = 0;
	
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%КлючAPI%", СокрЛП(КлючAPI));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Широта%", ПредставлениеЧисла(НачальнаяШирота));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Долгота%", ПредставлениеЧисла(НачальнаяДолгота));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Увеличение%", ПредставлениеЧисла(НачальныйЗум));	
	
	сткКартинок = Новый Структура();
	сткКартинок.Вставить("упМаркерПростойБелый", Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойБелый, Неопределено));
	сткКартинок.Вставить("упМаркерПростойЖелтый", Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЖелтый, Неопределено));
	сткКартинок.Вставить("упМаркерПростойКрасный", Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойКрасный, Неопределено));
	сткКартинок.Вставить("упГеокодируемыйМаркер", Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упГеокодируемыйМаркер, Неопределено));
	сткКартинок.Вставить("упТумблер",              	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упТумблер, Неопределено));	
	
	упСервисныеФункцииВызовСервера.ЗаполнитьДвоичныеДанныеКартинок(сткКартинок);

	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упТумблер.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%toggle%", ИмяВременногоФайла);
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойБелый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерРедактируемогоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойБелый");
	НовыйСохраненныйМаркер.Адрес   = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЖелтый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПервогоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойЖелтый");
	НовыйСохраненныйМаркер.Адрес   = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойКрасный.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПоследнегоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойКрасный");
	НовыйСохраненныйМаркер.Адрес   = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упГеокодируемыйМаркер.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерГеокодирования%", ИмяВременногоФайла);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПередЗакрытием" формы.
//
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	#Если НЕ ВебКлиент Тогда
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		Если ПодтверждениеЗакрытияФормы <> Истина Тогда
			Если РежимРедактирования Тогда
				Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормыЗавершение", ЭтотОбъект);
				Отказ = Истина;
				
				Текст = НСтр("ru = 'При закрытии измененные данные будут потеряны. Сохранить?'");
				ПоказатьВопрос(Оповещение,Текст,РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да,"Сохранить изменения");
			КонецЕсли; //Если РежимРедактирования Тогда
		КонецЕсли;
		
		Если КартаСформирована Тогда 
			Положение = ВыполнитьФункциюHTML("getMapStatus()");
			мсвЧисел = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Положение, ";");
			Если мсвЧисел.Количество() Тогда 
				НачальнаяШирота  = мсвЧисел[0];
				НачальнаяДолгота = мсвЧисел[1];
				НачальныйЗум     = мсвЧисел[2];
			КонецЕсли;	
		КонецЕсли;	
	#КонецЕсли
КонецПроцедуры //ПередЗакрытием()

&НаКлиенте
// Процедура - обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЭтоПодтверждениеАдреса Тогда 
		СтруктураОтвета = Новый Структура();
		Если КартаСформирована Тогда 
			СтруктураОтвета.Вставить("РедактированиеВыполнено", РежимРедактирования=ЛОЖЬ);
		Иначе
			СтруктураОтвета.Вставить("РедактированиеВыполнено", ЛОЖЬ);
		КонецЕсли;
		ОповеститьОВыборе(СтруктураОтвета);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ОбработкаВыбора" формы.
//
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	#Если НЕ ВебКлиент Тогда
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда 
		текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
		Если текДанные.ПолучитьРодителя() = Неопределено Тогда 
			текРодитель = текДанные;
		Иначе	
			текРодитель = текДанные.ПолучитьРодителя();
		КонецЕсли;
		
		Если текРодитель <> Неопределено Тогда 
			ВыборМассиваОбъектовДерева(ВыбранноеЗначение, текРодитель);
		КонецЕсли;
	КонецЕсли;	
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	#Если НЕ ВебКлиент Тогда
	Если ИмяСобытия = "ОтобразитьГеографическиеОбъекты" Тогда 
		Если ТипЗнч(Параметр) = Тип("Массив") И Параметр.Количество() Тогда 
			
			ТипОбъекта = ТипЗнч(Параметр[0]);
			Родитель   = Неопределено;
			Если ТипОбъекта = Тип("СправочникСсылка.упАдреса") Тогда 
				Для Каждого текСтрока Из ДеревоОбъектов.ПолучитьЭлементы() Цикл 
					Если текСтрока.Объект = НСтр("ru = 'Адреса'") Тогда 
						Родитель = текСтрока;
						Прервать;
					КонецЕсли;
				КонецЦикла;	
			ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.упГеографическиеЗоны") Тогда 
				Для Каждого текСтрока Из ДеревоОбъектов.ПолучитьЭлементы() Цикл 
					Если текСтрока.Объект = НСтр("ru = 'Зоны'") Тогда 
						Родитель = текСтрока;
						Прервать;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
			
			Если Родитель <> Неопределено Тогда 
				ВыборМассиваОбъектовДерева(Параметр, Родитель);
			КонецЕсли;	
		КонецЕсли;	
	ИначеЕсли ИмяСобытия = "ВыбраныНачальнаяКонечнаяТочкаМаршрута" Тогда
		Если ЭтоАдресВременногоХранилища(Параметр.АдресВХ) Тогда
			Для Каждого текСтрока Из ДеревоОбъектов.ПолучитьЭлементы() Цикл 
				Если текСтрока.Объект = НСтр("ru = 'Маршруты'") Тогда 
					ВыборМассиваОбъектовДерева(ПолучитьИзВременногоХранилища(Параметр.АдресВХ), текСтрока, Не Параметр.ТолькоПросмотр);
					Прервать;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли; 
	ИначеЕсли ИмяСобытия = "ПодтвердитьНовыеКоординатыНаКарте" Тогда
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			ЭтоПодтверждениеАдреса = Истина;
			Родитель = Неопределено;
			Для Каждого текСтрока Из ДеревоОбъектов.ПолучитьЭлементы() Цикл 
				Если текСтрока.Объект = НСтр("ru = 'Адреса'") Тогда 
					Родитель = текСтрока;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если НЕ Родитель = Неопределено Тогда 
				МассивАдрессов = Новый Массив;
				МассивАдрессов.Добавить(Параметр.Адрес);
				ВыборМассиваОбъектовДерева(МассивАдрессов, Родитель);
				ПодключитьОбработчикОжидания("ВыполнитьОбработкуПодтверждениеАдреса",0.1,Истина);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;	
	#КонецЕсли
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	Отказ = Истина;
	
	текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
	Если текДанные.ПолучитьРодителя() = Неопределено Тогда 
		текРодитель = текДанные;
	Иначе	
		текРодитель = текДанные.ПолучитьРодителя();
	КонецЕсли;
	
	Если текРодитель <> Неопределено Тогда 
		сткПараметров = Новый Структура("МножественныйВыбор, ЗакрыватьПриВыборе", Истина, Ложь);
		
		Если текРодитель.Объект = НСтр("ru = 'Адреса'") Тогда 
			ОткрытьФорму("Справочник.упАдреса.ФормаВыбора", сткПараметров, ЭтотОбъект, УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли текРодитель.Объект = НСтр("ru = 'Зоны'") Тогда 
			ОткрытьФорму("Справочник.упГеографическиеЗоны.ФормаВыбора", сткПараметров, ЭтотОбъект, УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Если РежимРедактирования Тогда 
		текОбъект = ДеревоОбъектов.НайтиПоИдентификатору(РедактируемаяСтрока);
		Если текОбъект <> Неопределено Тогда 
			ОчиститьОбъект(текОбъект.ПолучитьРодителя().Объект);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРеперныеТочки(Команда)
	
	Для Каждого текСтрока Из Элементы.ДеревоОбъектов.ВыделенныеСтроки Цикл 
		текДанные = Элементы.ДеревоОбъектов.ДанныеСтроки(текСтрока);
		Если текДанные.ПолучитьРодителя() = Неопределено Тогда
			Для Каждого текОбъект Из текДанные.ПолучитьЭлементы() Цикл 
				Если текОбъект.Редактировать = НСтр("ru = 'Завершить редактирование'") Тогда 
					Продолжить;
				КонецЕсли;	
				
				Если ЗначениеЗаполнено(текОбъект.ИндексВМассиве) И ЗначениеЗаполнено(текОбъект.Координаты) Тогда 
					ОтобразитьРеперныеТочкиНаКарте(текОбъект, текДанные.Объект);
				КонецЕсли;	
			КонецЦикла;	
		Иначе	
			Если текДанные.Редактировать = НСтр("ru = 'Завершить редактирование'") Тогда 
				Продолжить;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(текДанные.ИндексВМассиве) И ЗначениеЗаполнено(текДанные.Координаты) Тогда 
				ОтобразитьРеперныеТочкиНаКарте(текДанные, текДанные.ПолучитьРодителя().Объект);
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
	ОбъектРодительАдрес = НСтр("ru = 'Адреса'");
	ОбъектРодительЗоны = НСтр("ru = 'Зоны'");
	ОбъектРодительМаршруты = НСтр("ru = 'Маршруты'");
	Если ЗначениеЗаполнено(текДанные.Редактировать) И Поле = Элементы.ДеревоОбъектовРедактировать Тогда 	
		
		Если РежимРедактирования Тогда 
			Если Элементы.ДеревоОбъектов.ТекущаяСтрока = РедактируемаяСтрока Тогда
				
				Если текДанные.Редактировать = НСтр("ru = 'Выбрать команду'") Тогда
					Список = Новый СписокЗначений;
					Список.Добавить("Завершить",,Истина);
					Список.Добавить("Отменить",,Ложь);
					Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМеню",ЭтаФорма);
					ПоказатьВыборИзМеню(Оповещение, Список, Элементы.ДеревоОбъектовРедактировать);					
				Иначе
					СохранитьОбъект(текДанные, текДанные.ПолучитьРодителя().Объект);
					
					РежимРедактирования = Ложь;
					РедактируемаяСтрока = 0;
					
					Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;
					Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = РежимРедактирования;
					Если ЭтоПодтверждениеАдреса=Истина Тогда
						СтруктураОтвета = Новый Структура();
						СтруктураОтвета.Вставить("РедактированиеВыполнено", ИСТИНА);
						Закрыть(СтруктураОтвета);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		Иначе		
			
			текДанныеРодительОбъект = текДанные.ПолучитьРодителя().Объект;
			
			Если НетПраваРедактировать Тогда
				ОбъектПодтвержден = Ложь;
				Если текДанныеРодительОбъект = НСтр("ru = 'Адреса'") Тогда
					ОбъектПодтвержден = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(текДанные.Объект, "Подтвержден");
				ИначеЕсли текДанныеРодительОбъект = НСтр("ru = 'Зоны'") Тогда
					ОбъектПодтвержден = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(текДанные.Объект, "Подтверждена");
				КонецЕсли;				
				Если ОбъектПодтвержден Тогда
					ПоказатьОповещениеПользователя("Редактировать",,"У вас нет права редактировать выбранный элемент",БиблиотекаКартинок.Информация32);
					Возврат;
				КонецЕсли;				
			КонецЕсли;
			
			Элементы.ДеревоОбъектов.Доступность = Ложь;
			РедактироватьОбъект(текДанные, текДанныеРодительОбъект);
			
			РежимРедактирования = Истина;
			РедактируемаяСтрока = Элементы.ДеревоОбъектов.ТекущаяСтрока;
			
			текДанные.Редактировать = НСтр("ru = 'Завершить редактирование'");
			
			Элементы.ДеревоОбъектов.Доступность = Истина;
			
			Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;			
			Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = (текДанныеРодительОбъект = НСтр("ru = 'Зоны'") ИЛИ текДанныеРодительОбъект = НСтр("ru = 'Маршруты'"));
		КонецЕсли;		
	
	ИначеЕсли текДанные.ОпределитьКоординаты И Поле = Элементы.ДеревоОбъектовОпределитьКоординаты Тогда 
		
		СтрокаПоиска = текДанные.Имя;
		ТекущийОбъектРодитель = текДанные.ПолучитьРодителя().Объект;
		Если ТекущийОбъектРодитель = ОбъектРодительАдрес Тогда	
			
			Если ЭтоПодтверждениеАдреса=Истина Тогда
				ПоказатьОповещениеПользователя("Определить координаты",,"Вы подтверждаете адрес, изменить координаты вы не можете",БиблиотекаКартинок.Информация32);
				Возврат;
			КонецЕсли;
			
			ОбъектПодтвержден = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(текДанные.Объект, "Подтвержден");
			
			Если НетПраваРедактировать И ОбъектПодтвержден Тогда 
				ПоказатьОповещениеПользователя("Определить координаты",,"У вас нет права корректировка выбранный элемент",БиблиотекаКартинок.Информация32);
				Возврат;
			КонецЕсли;
			
			#Если ВебКлиент Тогда
				сткКоординаты = упГеокодированиеСервер.НайтиКоординатыАдреса(СтрокаПоиска);
			#Иначе	
				сткКоординаты = упГеокодированиеКлиентСервер.НайтиКоординатыАдреса(СтрокаПоиска);
			#КонецЕсли	
			
			Если НЕ сткКоординаты = Неопределено Тогда
				ПредставлениеШирота = ПредставлениеЧисла(сткКоординаты.Широта);
				ПредставлениеДолгота = ПредставлениеЧисла(сткКоординаты.Долгота);
				ПредставлениеКоординат = ПредставлениеШирота + ";" + ПредставлениеДолгота + ";";
				
				Если РежимРедактирования Тогда
					Ответ = ВыполнитьФункциюHTML("getLatLngObjectMarker(" + ПредставлениеЧисла(текДанные.ИндексВМассиве) + ", " + ПредставлениеШирота + ", " + ПредставлениеДолгота + ")");
					текДанные.Координаты = ПредставлениеЧисла(сткКоординаты.Широта) + ";" + ПредставлениеЧисла(сткКоординаты.Долгота) + ";";					
				Иначе
					Если НЕ текДанные.Координаты = ПредставлениеКоординат Тогда
						УдалитьТочку = ЗначениеЗаполнено(текДанные.Координаты);
						
						текДанные.Редактировать  = НСтр("ru = 'Редактировать'");
						текДанные.Пометка        = Истина;
						текДанные.ИндексКартинки = 2;
						текДанные.Координаты     = ПредставлениеЧисла(сткКоординаты.Широта) + ";" + ПредставлениеЧисла(сткКоординаты.Долгота) + ";";
						СохранитьКоординатыМаркера(текДанные.Объект, сткКоординаты.Широта,сткКоординаты.Долгота);
						упСервисныеФункцииКлиент.ОповеститьОЗаписиОбъекта(текДанные.Объект);
						
						Если УдалитьТочку Тогда
							ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(текДанные.ИндексВМассиве) + ")");
						КонецЕсли;
						
						Индекс = ВыполнитьФункциюHTML("addObjectMarker(" + ПредставлениеШирота + ", " + ПредставлениеДолгота + ", """ + текДанные.Имя + """, " + текДанные.Данные + ")");
						текДанные.ИндексВМассиве = Индекс;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли РежимРедактирования И ТекущийОбъектРодитель = ОбъектРодительЗоны Тогда
			
			ОбъектПодтвержден = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(текДанные.Объект, "Подтверждена");
			
			Если НетПраваРедактировать И ОбъектПодтвержден Тогда 
				ПоказатьОповещениеПользователя("Определить координаты",,"У вас нет права корректировка выбранный элемент",БиблиотекаКартинок.Информация32);
				Возврат;
			КонецЕсли;
			
			ДополнительныйТекст = "";
			Если РежимРедактирования Тогда
				ДополнительныйТекст = " Текущие изменения редактируемого полигона будут сохранены.";
			КонецЕсли;
			ТекущееСообщение = СтрШаблон(НСтр("ru = 'Получить координаты географической зоны от сервиса геокодирования?%1'; en = 'Do you want to continue?'"),ДополнительныйТекст);		
			
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаГеокодированиеГеографическойЗоны", ЭтаФорма);
			ПоказатьВопрос(Оповещение, ТекущееСообщение, Режим, 0);
			
		КонецЕсли;
	Иначе	
		Если текДанные.ПолучитьРодителя() = Неопределено Тогда 
			текСтрока = Элементы.ДеревоОбъектов.ТекущаяСтрока;
			Если Элементы.ДеревоОбъектов.Развернут(текСтрока) Тогда 
				Элементы.ДеревоОбъектов.Свернуть(текСтрока);
			Иначе	
				Элементы.ДеревоОбъектов.Развернуть(текСтрока);
			КонецЕсли;
		Иначе
			ТекущийОбъектРодитель = текДанные.ПолучитьРодителя().Объект;
			Если Истина
				И Не РежимРедактирования 
				И ТекущийОбъектРодитель = ОбъектРодительМаршруты 
			Тогда
				ПараметрыФормы = Новый Структура("ТолькоПросмотр, НачальнаяТочка, КонечнаяТочка", Истина, текДанные.НачальнаяТочка, текДанные.КонечнаяТочка);
				ОткрытьФорму("Обработка.упЭлектроннаяГеографическаяКарта.Форма.ФормаТочкиМаршрута", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли; 
			Если ЗначениеЗаполнено(текДанные.ИндексВМассиве) И ЗначениеЗаполнено(текДанные.Координаты) Тогда 
				ПерейтиКОбъекту(текДанные.ИндексВМассиве, ТекущийОбъектРодитель);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДеревоОбъектовВыбор()

#Область ГеоКодированиеГеографическойЗоны

&НаКлиенте
// Процедура завершения после утвердительно ответа получения геокодирования географической зоны.
//
Процедура ПослеЗакрытияВопросаГеокодированиеГеографическойЗоны(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДанныеГеоКодирования = Неопределено;
		текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
		СтрокаПоиска = текДанные.Имя;
		Если РежимРедактирования Тогда 
			Если Элементы.ДеревоОбъектов.ТекущаяСтрока = РедактируемаяСтрока Тогда
				
				СохранитьОбъект(текДанные, текДанные.ПолучитьРодителя().Объект);
				
				РежимРедактирования = Ложь;
				РедактируемаяСтрока = 0;
				
				Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;
				Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = РежимРедактирования;
				
			КонецЕсли;
		КонецЕсли;
		ВыполнитьГеокодированиеГеографическойЗоны(СтрокаПоиска,текДанные.Объект);
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопросаГеокодированиеГеографическойЗоны()

&НаКлиенте
// Процедура завершения после выбора команды.
//
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		
		РезультатВыбора = ВыбранныйЭлемент.Значение;
		
		Если РезультатВыбора = "Завершить" Тогда
			текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
			СохранитьОбъект(текДанные, текДанные.ПолучитьРодителя().Объект);		
			РежимРедактирования = Ложь;
			РедактируемаяСтрока = 0;		
			Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;
			Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = РежимРедактирования;
		ИначеЕсли РезультатВыбора = "Отменить" Тогда		
			
			текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
			текДанныеРодительОбъект = текДанные.ПолучитьРодителя().Объект;			
			
			Элементы.ДеревоОбъектов.Доступность = Ложь;
			
			Ответ = ВыполнитьФункциюHTML("setObjectPolygon(" + ПредставлениеЧисла(текДанные.ИндексВМассиве) + ", """ + текДанные.Имя + """, " + текДанные.Данные + ")");
			
			ИзменитьВидимостьОбъекта(текДанные.ИндексВМассиве, Ложь, текДанныеРодительОбъект);
			
			текДанные.Координаты = ДанныеГеоКодирования.ИсходныеКоординаты;
			текДанные.ИндексВМассиве = ДанныеГеоКодирования.ИсходныйИндекс;
			
			ИзменитьВидимостьОбъекта(текДанные.ИндексВМассиве, Истина, текДанныеРодительОбъект);
			
			текДанные.Редактировать  = НСтр("ru = 'Редактировать'");
			
			РежимРедактирования = Ложь;
			РедактируемаяСтрока = 0;		
			Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;
			Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = РежимРедактирования;
			Элементы.ДеревоОбъектов.Доступность = Истина;
			
			ДанныеГеоКодирования = Неопределено;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПослеВыбораИзМеню()

&НаКлиенте
// Выполним обращение к сервису геокодирования.
//
// Параметры:
//  ИмяГеоЗоны  - Строка - текст поиска.
//  ГеографическаяЗона  - СправочникСсылка.упГеографическиеЗоны - Ссылка на элемент справочника.
//
Процедура ВыполнитьГеокодированиеГеографическойЗоны(ИмяГеоЗоны,ГеографическаяЗона)
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Выполнить запрос к сервису.'"),,НСтр("ru = 'Выполняется запрос к сервису геокодирования.'"), БиблиотекаКартинок.упГеокодирование);
	//#Если ВебКлиент Тогда
	ПолигонГеографическойЗоны = упГеокодированиеСервер.ПолучитьПолигоныГеографическойЗоны(ИмяГеоЗоны);
	//#Иначе	
	//	ПолигонГеографическойЗоны = упГеокодированиеКлиентСервер.ПолучитьПолигоныГеографическойЗоны(ИмяГеоЗоны);
	//#КонецЕсли
	ПоказатьОповещениеПользователя(НСтр("ru = 'Получен ответ от сервиса.'"),,НСтр("ru = 'Получен ответ от сервиса геокодирования.'"), БиблиотекаКартинок.упГеокодирование);
	Если ПолигонГеографическойЗоны = Неопределено Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Данные от сервиса не получены!'; en = 'Data from the service is not received!'"),,НСтр("ru = 'Данные от сервиса геокодирования не получены!'; en = 'Data from the service geo coding is not received!'"), БиблиотекаКартинок.упГеокодирование);		
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Выбрать вариант ответа.'"),,НСтр("ru = 'Выбирете вариант ответа сервиса геокодирования.'"), БиблиотекаКартинок.упГеокодирование);
		сткПараметры = Новый Структура("Список",ПолигонГеографическойЗоны);
		сткПараметры.Вставить("Ссылка",ГеографическаяЗона);		
		ОткрытьФорму("Обработка.упЭлектроннаяГеографическаяКарта.Форма.ФормаВыбора",сткПараметры, ЭтаФорма,,,,Новый ОписаниеОповещения("ОбработатьВыборОтветаОтГеоСервиса", ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьГеокодированиеГеографическойЗоны()

&НаСервереБезКонтекста
// Выполнить подготовку данных полученных от сервиса геокодирования,
// если точек много уменьшить их количество.
//
// Параметры:
//  СтруктураДанных  - Структура - Отчет от сервиса геокодирования.
//
// Возвращаемое значение:
//   Массив   - Структур ключи долгота и широта, координаты полигона.
//
Функция ПолучитьУпрощенныеКоординаты(СтруктураДанных)
	
	МассивКоординат =  упГеокодированиеКлиентСервер.ВыполнитьОбходОтветаJSON(СтруктураДанных);
	
	Отклонение = СтруктураДанных.Отклонение;
	КоличествоТочекДо = МассивКоординат.Количество();
	Результат = упРаботаСКартами.ВыполнитьУпрощениеПолигональнойЦепи(МассивКоординат, Отклонение);
	КоличествоТочекПосле = 0;
	Если Результат = Неопределено Тогда
		Результат = МассивКоординат;
	Иначе
		КоличествоТочекПосле = Результат.Количество();
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Координаты",Результат);
	Структура.Вставить("КоличествоТочекДо",КоличествоТочекДо);
	Структура.Вставить("КоличествоТочекПосле",КоличествоТочекПосле);
	
	Возврат Структура;
	
КонецФункции // ПолучитьУпрощенныеКоординаты()

&НаКлиенте
// Процедура завершения после получения данных от сервиса геокодирования.
//
Процедура ОбработатьВыборОтветаОтГеоСервиса(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеГеоКодирования = Результат;
	
	Если НЕ Результат = Неопределено Тогда
		ПодключитьОбработчикОжидания("ВыполнитьОбработкуВыбораОтветаОтГеоСервиса",0.1,Истина);
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборОтветаОтГеоСервиса()	

&НаКлиенте
// Обрабатывает данный полученные от сервиса геокодирования.
//
Процедура ВыполнитьОбработкуВыбораОтветаОтГеоСервиса() Экспорт
	
	Если ТипЗнч(ДанныеГеоКодирования) = Тип("Структура") Тогда
		
		РежимРедактирования = Истина;
		РедактируемаяСтрока = Элементы.ДеревоОбъектов.ТекущаяСтрока;
		текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
		
		текДанные.Редактировать = НСтр("ru = 'Выбрать команду'");
		
		текДанныеРодительОбъект = текДанные.ПолучитьРодителя().Объект;
		
		Элементы.ДеревоОбъектов.Доступность = Ложь;
		
		ИзменитьВидимостьОбъекта(текДанные.ИндексВМассиве, Ложь, текДанныеРодительОбъект);
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Выполнить упрощение ответа сервиса.'"),, НСтр("ru = 'Выполнение упрощения полученного ответа для географической зоны'"), БиблиотекаКартинок.упГеокодирование);
		
		Структура = ПолучитьУпрощенныеКоординаты(ДанныеГеоКодирования);
		
		Если Структура.КоличествоТочекПосле = 0 Тогда			
			ПоказатьОповещениеПользователя(НСтр("ru = 'Ответ сервиса не упрощен.'"),,НСтр("ru = 'Алгоритм упрощения полигональной цепи не был применен к ответу сервиса геокодирования.'"),БиблиотекаКартинок.упГеокодирование);
		Иначе
			ТекущееСообщение = СтрШаблон(НСтр("ru = 'Из %1 точек, упрощен до %2 точек, в результате применения алгоритма упрощения, ответ сервиса геокодирования.'"),Структура.КоличествоТочекДо,Структура.КоличествоТочекПосле);			
			ПоказатьОповещениеПользователя(НСтр("ru = 'Выполнено упрощение ответа сервиса.'"),,ТекущееСообщение,БиблиотекаКартинок.упГеокодирование);
		КонецЕсли;	
		
		МассивКоординат = Новый Массив;
		Для Каждого текУгол Из Структура.Координаты Цикл			
			Широта = ПредставлениеЧисла(текУгол.Широта);
			Долгота = ПредставлениеЧисла(текУгол.Долгота);
			МассивКоординат.Добавить(Широта);
			МассивКоординат.Добавить(Долгота);
		КонецЦикла;
		ТекущиеКоординаты = СтрСоединить(МассивКоординат,";");
		ВыполнитьПроцедуруHTML("FillArrayCorner(""" + ТекущиеКоординаты + """)");
		
		ДанныеГеоКодирования.Вставить("ИсходныеКоординаты",текДанные.Координаты);
		ДанныеГеоКодирования.Вставить("ИсходныйИндекс",текДанные.ИндексВМассиве);
		
		текДанные.Координаты = ТекущиеКоординаты;
		ТекущиеКоординаты = Неопределено; // Освободим значение переменной. 
		
		текИндекс = ВыполнитьФункциюHTML("addObjectPolygon(""" + текДанные.Имя + """, " + текДанные.Данные + ")");		
		текДанные.ИндексВМассиве = текИндекс;
		
		РедактироватьОбъект(текДанные, текДанныеРодительОбъект);
		
		Элементы.ДеревоОбъектов.Доступность = Истина;
		
		Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;			
		Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = РежимРедактирования;
		
	КонецЕсли; 
	
КонецПроцедуры // ВыполнитьОбработкуВыбораОтветаОтГеоСервиса()

#КонецОбласти

&НаКлиенте
Процедура ДеревоОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если РежимРедактирования Тогда 
		Возврат;
	КонецЕсли;
	
	текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
	Если текДанные.ПолучитьРодителя() = Неопределено Тогда 
		текРодитель = текДанные;
	Иначе	
		текРодитель = текДанные.ПолучитьРодителя();
	КонецЕсли;
	
	Если текРодитель <> Неопределено Тогда 
		Если текРодитель.Объект = НСтр("ru = 'Адреса'") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыборОбъектаДерева", ЭтотОбъект, текРодитель);
			ОткрытьФорму("Справочник.упАдреса.ФормаВыбора",, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли текРодитель.Объект = НСтр("ru = 'Зоны'") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыборОбъектаДерева", ЭтотОбъект, текРодитель);
			ОткрытьФорму("Справочник.упГеографическиеЗоны.ФормаВыбора",, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли текРодитель.Объект = НСтр("ru = 'Маршруты'") Тогда 
			
			ОткрытьФорму("Обработка.упЭлектроннаяГеографическаяКарта.Форма.ФормаТочкиМаршрута",, ЭтотОбъект, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры // ДеревоОбъектовПередНачаломДобавления()

&НаКлиенте
Процедура ДеревоОбъектовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Для Каждого текСтрока Из Элементы.ДеревоОбъектов.ВыделенныеСтроки Цикл 
		текДанные = Элементы.ДеревоОбъектов.ДанныеСтроки(текСтрока);
		Если текДанные.Пометка Тогда 
			текДанные.Пометка = Ложь;
			
			ИзменитьПометку(текДанные);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовПометкаПриИзменении(Элемент)
	
	текДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
	
	ИзменитьПометку(текДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	
	ЧислоПопыток = ЧислоПопыток + 1;
	// Проверяет на стороне карты сформирован объект Map.
	MapСформирован = ВыполнитьФункциюHTML("ValidateCacheCalls()");
	
	Если MapСформирован = Истина Тогда
		// Объект был сформирован и можно выполнить отложенные действия.
		КартаСформирована = Ложь;
	Иначе
		// Карта не сформирована.
		КартаСформирована = Истина;
		Если ЧислоПопыток >= 2 ИЛИ ЭтоЯндекс Тогда
			// Число попыток больше 1 тогда подключаем отложеное действие.
			ЧислоПопыток = 0;
			ПодключитьОбработчикОжидания("ВыполнитьОтложенноеДействие",0.1,Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если Не КартаСформирована Тогда 
		КартаСформирована                                = Истина;
		Элементы.ДеревоОбъектов.ТолькоПросмотр           = Ложь;
		Элементы.ДеревоОбъектовПометка.ТолькоПросмотр    = Ложь;
		Элементы.ДеревоОбъектовРедактировать.Доступность = Истина;
		Элементы.ДеревоОбъектовОпределитьКоординаты.Доступность = Истина;
		
		Для Каждого текРодитель Из ДеревоОбъектов.ПолучитьЭлементы() Цикл 
			Для Каждого текСтрока Из текРодитель.ПолучитьЭлементы() Цикл 
				Если текСтрока.Пометка И ЗначениеЗаполнено(текСтрока.Координаты) И текСтрока.ИндексВМассиве = 0 Тогда 
					ОтобразитьОбъект(текСтрока, текРодитель.Объект);
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыФункции

&НаКлиенте
// Обработчик действия ожидания т.к. карты Google и Яндекс не грузяться тогдавыполним повторный вызов.
//
Процедура ВыполнитьОтложенноеДействие()Экспорт 

	HTMLДокументДокументСформирован(Неопределено);

КонецПроцедуры // ВыполнитьОтложенноеДействие()

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ - Данные.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Нет.
// 
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ - Данные.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Строка - значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат	упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьОбъект(СтрокаОбъекта, ТипОбъекта)
	
	Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
		МассивКоординат = СтрРазделить(СтрокаОбъекта.Координаты,";");
		Индекс = ВыполнитьФункциюHTML("addObjectMarker(" + МассивКоординат[0] + ", " + МассивКоординат[1] + ", """ + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда
		Если НЕ ПустаяСтрока(СтрокаОбъекта.Координаты) Тогда
			ВыполнитьПроцедуруHTML("FillArrayCorner(""" + СтрокаОбъекта.Координаты + """)");
			Индекс = ВыполнитьФункциюHTML("addObjectPolygon(""" + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
		КонецЕсли;
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда
		Если НЕ ПустаяСтрока(СтрокаОбъекта.Координаты) Тогда
			ВыполнитьПроцедуруHTML("FillArrayPoint(""" + СтрокаОбъекта.Координаты + """)");
			Индекс = ВыполнитьФункциюHTML("addObjectPolyline()");
		КонецЕсли;
	КонецЕсли;	
	
	СтрокаОбъекта.ИндексВМассиве = Индекс;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКОбъекту(ИндексВМассиве, ТипОбъекта)
	
	Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
		ВыполнитьПроцедуруHTML("gotoObjectMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда 
		ВыполнитьПроцедуруHTML("gotoObjectPolygon(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьОбъекта(ИндексВМассиве, Пометка, ТипОбъекта = Неопределено)
	Если Пометка Тогда 
		Если ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда
			ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
			ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве-1) + ")");
			ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве-2) + ")");
		Иначе
			ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		КонецЕсли;
	Иначе	
		Если ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда
			ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
			ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве-1) + ")");
			ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве-2) + ")");
		Иначе
			ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьОбъект(СтрокаОбъекта, ТипОбъекта)
	
	Если ЗначениеЗаполнено(СтрокаОбъекта.Координаты) Тогда 
		Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
			ТекстШирота = "0";
			ТекстДолгота = "0";
			Если ЭтоПодтверждениеАдреса = Истина Тогда 
				ТекстШирота = ПредставлениеЧисла(НачальнаяШирота);
				ТекстДолгота = ПредставлениеЧисла(НачальнаяДолгота);
			КонецЕсли;
			ТекстИсполненияПроцедуры = СтрШаблон("redrawObjectMarker(%1, ""%2"", %3, %4)", ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве), СтрокаОбъекта.Имя, ТекстШирота, ТекстДолгота);
			ВыполнитьПроцедуруHTML(ТекстИсполненияПроцедуры);
		ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда 
			ВыполнитьПроцедуруHTML("redrawObjectPolygon(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """)");
		ИначеЕсли ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда 
			ВыполнитьПроцедуруHTML("redrawObjectPolyline(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """)");
		КонецЕсли;	
	Иначе
		Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
			ВыполнитьПроцедуруHTML("drawObjectMarker(""" + СтрокаОбъекта.Имя + """)");
		ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда 
			ВыполнитьПроцедуруHTML("drawObjectPolygon(""" + СтрокаОбъекта.Имя + """)");
		ИначеЕсли ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда 
			сткКоординаты = ПолучитьНачКонКоординаты(СтрокаОбъекта.НачальнаяТочка, СтрокаОбъекта.КонечнаяТочка);
			ВыполнитьПроцедуруHTML("drawObjectPolyline(" + """" + СтрокаОбъекта.Имя + """," + сткКоординаты.НачШирота + "," + сткКоординаты.НачДолгота + "," + сткКоординаты.КонШирота + "," + сткКоординаты.КонДолгота + ")");
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНачКонКоординаты(НачальнаяТочка, КонечнаяТочка)

	НачальныеКоординаты 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачальнаяТочка, "Широта, Долгота");
	КонечныеКоординаты 		= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КонечнаяТочка, "Широта, Долгота");

	Возврат Новый Структура("НачШирота, НачДолгота, КонШирота, КонДолгота", ПредставлениеЧисла(НачальныеКоординаты.Широта), ПредставлениеЧисла(НачальныеКоординаты.Долгота), ПредставлениеЧисла(КонечныеКоординаты.Широта), ПредставлениеЧисла(КонечныеКоординаты.Долгота));
	
КонецФункции // ()

&НаКлиенте
Процедура СохранитьОбъект(СтрокаОбъекта, ТипОбъекта)
	
	Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
		Ответ = ВыполнитьФункциюHTML("setObjectMarker(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
		Если Ответ = 0 ИЛИ Ответ = "0" Тогда 
			СтрокаОбъекта.Редактировать  = НСтр("ru = 'Указать точку'");
			СтрокаОбъекта.Пометка        = Ложь;
			СтрокаОбъекта.ИндексКартинки = 1;
			СтрокаОбъекта.Координаты     = "";
			СтрокаОбъекта.ИндексВМассиве = 0;
			
			СохранитьКоординатыМаркера(СтрокаОбъекта.Объект, 0, 0);
		Иначе	
			СтрокаОбъекта.Редактировать  = НСтр("ru = 'Редактировать'");
			СтрокаОбъекта.Пометка        = Истина;
			СтрокаОбъекта.ИндексКартинки = 2;
			
			мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ответ, ";");
			
			СтрокаОбъекта.Координаты     = мсвКоординат[1] + ";" + мсвКоординат[2] + ";";
			СтрокаОбъекта.ИндексВМассиве = мсвКоординат[0];
			
			СохранитьКоординатыМаркера(СтрокаОбъекта.Объект, мсвКоординат[1], мсвКоординат[2]);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда 
		Ответ = ВыполнитьФункциюHTML("setObjectPolygon(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
		Если Ответ = 0 ИЛИ Ответ = "0" Тогда 
			СтрокаОбъекта.Редактировать  = НСтр("ru = 'Указать область'");
			СтрокаОбъекта.Пометка        = Ложь;
			СтрокаОбъекта.ИндексКартинки = 3;
			СтрокаОбъекта.Координаты     = "";
			СтрокаОбъекта.ИндексВМассиве = 0;
			
			СохранитьКоординатыОбласти(СтрокаОбъекта.Объект, "");
		Иначе	
			СтрокаОбъекта.Редактировать  = НСтр("ru = 'Редактировать'");
			СтрокаОбъекта.Пометка        = Истина;
			СтрокаОбъекта.ИндексКартинки = 4;
			
			мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ответ, ";");
			
			Координаты = "";
			Для Сч = 0 По ((мсвКоординат.Количество() - 2) / 2) - 1 Цикл 
				Координаты     = Координаты + мсвКоординат[(Сч * 2) + 1] + ";" + мсвКоординат[(Сч * 2) + 2] + ";";
			КонецЦикла;	
			СтрокаОбъекта.Координаты     = Координаты;
			СтрокаОбъекта.ИндексВМассиве = мсвКоординат[0];
			
			СохранитьКоординатыОбласти(СтрокаОбъекта.Объект, Координаты);
		КонецЕсли;
	//	
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда 
		Ответ = ВыполнитьФункциюHTML("setObjectPolyline(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
		Если Ответ = 0 ИЛИ Ответ = "0" Тогда 
			СтрокаОбъекта.Редактировать  = НСтр("ru = 'Указать маршрут'");
			СтрокаОбъекта.Пометка        = Ложь;
			СтрокаОбъекта.ИндексКартинки = 5;
			СтрокаОбъекта.Координаты     = "";
			СтрокаОбъекта.ИндексВМассиве = 0;
			
			УдалитьКоординатыМаршрута(СтрокаОбъекта.НачальнаяТочка, СтрокаОбъекта.КонечнаяТочка);
		Иначе	
			СтрокаОбъекта.Редактировать  = НСтр("ru = 'Редактировать'");
			СтрокаОбъекта.Пометка        = Истина;
			СтрокаОбъекта.ИндексКартинки = 6;
			
			мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ответ, ";");
			
			Координаты = "";
			Расстояние = 0;
			Для Сч = 0 По ((мсвКоординат.Количество() - 2) / 2) - 1 Цикл 
				Координаты	= Координаты + мсвКоординат[(Сч * 2) + 1] + ";" + мсвКоординат[(Сч * 2) + 2] + ";";
				Если Сч <> ((мсвКоординат.Количество() - 2) / 2) - 1 Тогда
					Расстояние	= Расстояние + РассчитатьРасстояние(мсвКоординат[(Сч * 2) + 2], мсвКоординат[(Сч * 2) + 1], мсвКоординат[(Сч * 2) + 4] , мсвКоординат[(Сч * 2) + 3]) //долгота, широта, доглота, широта
				КонецЕсли; 
			КонецЦикла;	
				
			СтрокаОбъекта.Координаты     = Координаты;
			СтрокаОбъекта.ИндексВМассиве = мсвКоординат[0];
			
			сткПараметры = Новый Структура("НачальнаяТочка, КонечнаяТочка, Координаты, Расстояние", СтрокаОбъекта.НачальнаяТочка, СтрокаОбъекта.КонечнаяТочка, Координаты, Расстояние); 
			// Задать вопрос пользователю.
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаСохранениеМаршрута", ЭтаФорма, сткПараметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Сохранить внесенные изменения маршрута?';en = 'Do you want to continue?'"), Режим, 0);			
			// Задать вопрос пользователю.
		КонецЕсли;
	КонецЕсли;
	Если НЕ ТипЗнч(СтрокаОбъекта.Объект) = Тип("Строка") Тогда
		упСервисныеФункцииКлиент.ОповеститьОЗаписиОбъекта(СтрокаОбъекта.Объект);
	КонецЕсли; 
	текРодитель = СтрокаОбъекта.ПолучитьРодителя();
	УстановитьПометкуРодителя(текРодитель);
	
КонецПроцедуры

&НаКлиенте
// Выполнить обработку ответа пользователя, о сохранении маршрута.
//
// Параметры:
//  Результат - РежимДиалогаВопрос - Результат выбора.
//  ПараметрыОтвета  - Структура - Параметры.
//
Процедура ПослеЗакрытияВопросаСохранениеМаршрута(Результат, ПараметрыОтвета) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    Время = 0;
	ПоказатьВводЗначения(Новый ОписаниеОповещения("СохранитьДанныеМаршрутаПослеВводаВремени", ЭтаФорма, ПараметрыОтвета), Время, "Введите время маршрута (с.):", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8)));

КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
// Параметры:
//  ВведенноеЗначение  - Строка - Результат выбора.
//  сткПараметры  - Структура - Параметры.
//
Процедура СохранитьДанныеМаршрутаПослеВводаВремени(ВведенноеЗначение, сткПараметры) Экспорт	
	
	СохранитьКоординатыМаршрута(сткПараметры.НачальнаяТочка, сткПараметры.КонечнаяТочка, сткПараметры.Координаты, сткПараметры.Расстояние, ВведенноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОбъект(ТипОбъекта)
	
	Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
		ВыполнитьПроцедуруHTML("eraseObjectMarker()");
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда 
		ВыполнитьПроцедуруHTML("eraseObjectPolygon()");
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'") Тогда 
		ВыполнитьПроцедуруHTML("eraseObjectPolygon()");
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда 
		ВыполнитьПроцедуруHTML("eraseObjectPolygon()");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРеперныеТочкиНаКарте(СтрокаОбъекта, ТипОбъекта)
	
	Если ТипОбъекта = НСтр("ru = 'Адреса'") Тогда 
		МассивКоординат = СтрРазделить(СтрокаОбъекта.Координаты, ";");
		Индекс = ВыполнитьФункциюHTML("addReperMarker(" + МассивКоординат[0] + ", " + МассивКоординат[1] + ")");
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Зоны'")  Тогда 
		Если НЕ ПустаяСтрока(СтрокаОбъекта.Координаты) Тогда 
			ВыполнитьПроцедуруHTML("FillArrayReperMarker(""" + СтрокаОбъекта.Координаты + """)");
		КонецЕсли;
	ИначеЕсли ТипОбъекта = НСтр("ru = 'Маршруты'") Тогда
		Если НЕ ПустаяСтрока(СтрокаОбъекта.Координаты) Тогда 
			ВыполнитьПроцедуруHTML("FillArrayReperMarker(""" + СтрокаОбъекта.Координаты + """)");
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКоординатыМаркера(Маркер, Широта, Долгота)
	
	текОбъект = Маркер.ПолучитьОбъект();
	текОбъект.Широта  = Широта;
	текОбъект.Долгота = Долгота;
	текОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКоординатыОбласти(Зона, Координаты)
	
	Если Ложь Тогда // Для контекстной подсказки
	    Зона = Справочники.упГеографическиеЗоны.ПустаяСсылка();
	КонецЕсли;
	текОбъект = Зона.ПолучитьОбъект();
	текОбъект.Полигон.Очистить();
	текОбъект.ПолигонПростой.Очистить();
	
	мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
	Для Сч = 0 По ((мсвКоординат.Количество() - 1) / 2) - 1 Цикл 
		НоваяСтрока = текОбъект.Полигон.Добавить();
		НоваяСтрока.Широта  = мсвКоординат[Сч * 2];
		НоваяСтрока.Долгота = мсвКоординат[(Сч * 2) + 1];
	КонецЦикла;	
	
	Если НЕ ДанныеГеоКодирования = Неопределено Тогда
		текОбъект.osm_id = ДанныеГеоКодирования.osm_id;
		текОбъект.Nominatim_place_id = ДанныеГеоКодирования.place_id;
	КонецЕсли;
	текОбъект.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьКоординатыМаршрута(НачальнаяТочка, КонечнаяТочка, Координаты, Расстояние, Время)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачальныеКоординаты 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачальнаяТочка, "Широта, Долгота");
	КонечныеКоординаты 		= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КонечнаяТочка, "Широта, Долгота");
	
	МенеджерЗаписи 					= РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НачальнаяШирота 	= НачальныеКоординаты.Широта;
	МенеджерЗаписи.НачальнаяДолгота = НачальныеКоординаты.Долгота;
	МенеджерЗаписи.КонечнаяШирота 	= КонечныеКоординаты.Широта;
	МенеджерЗаписи.КонечнаяДолгота 	= КонечныеКоординаты.Долгота;
	МенеджерЗаписи.Ограничение = ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка");
	
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		РазницаВоВремени = Время - МенеджерЗаписи.Время;
		МенеджерЗаписи.ВремяПонедельник = ?(МенеджерЗаписи.ВремяПонедельник=0,0,МенеджерЗаписи.ВремяПонедельник+РазницаВоВремени);
		МенеджерЗаписи.ВремяВторник     = ?(МенеджерЗаписи.ВремяВторник=0,0,МенеджерЗаписи.ВремяВторник+РазницаВоВремени);
		МенеджерЗаписи.ВремяСреда       = ?(МенеджерЗаписи.ВремяСреда=0,0,МенеджерЗаписи.ВремяСреда+РазницаВоВремени);
		МенеджерЗаписи.ВремяЧетверг     = ?(МенеджерЗаписи.ВремяЧетверг=0,0,МенеджерЗаписи.ВремяЧетверг+РазницаВоВремени);
		МенеджерЗаписи.ВремяПятница     = ?(МенеджерЗаписи.ВремяПятница=0,0,МенеджерЗаписи.ВремяПятница+РазницаВоВремени);
		МенеджерЗаписи.ВремяСуббота     = ?(МенеджерЗаписи.ВремяСуббота=0,0,МенеджерЗаписи.ВремяСуббота+РазницаВоВремени);
		МенеджерЗаписи.ВремяВоскресенье = ?(МенеджерЗаписи.ВремяВоскресенье=0,0,МенеджерЗаписи.ВремяВоскресенье+РазницаВоВремени);		
	КонецЕсли;
	
	МенеджерЗаписи.Путь		 		= Координаты;
	МенеджерЗаписи.Время 			= Время;
	МенеджерЗаписи.Расстояние 		= Расстояние;
	
	МенеджерЗаписи.Записать(); 
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьКоординатыМаршрута(НачальнаяТочка, КонечнаяТочка)
	
	НачальныеКоординаты 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачальнаяТочка, "Широта, Долгота");
	КонечныеКоординаты 		= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КонечнаяТочка, "Широта, Долгота");
	
	МенеджерЗаписи 					= РегистрыСведений.упРасстоянияМеждуТочками.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НачальнаяШирота 	= НачальныеКоординаты.Широта;
	МенеджерЗаписи.НачальнаяДолгота = НачальныеКоординаты.Долгота;
	МенеджерЗаписи.КонечнаяШирота 	= КонечныеКоординаты.Широта;
	МенеджерЗаписи.КонечнаяДолгота 	= КонечныеКоординаты.Долгота;
	МенеджерЗаписи.Ограничение = ПредопределенноеЗначение("Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка");
	
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьРасстояние(Долгота1, Широта1, Долгота2, Широта2)
	
	текРасстояние = -1;
	
	РадиусЗемли = 6372795;
	ЧислоПи     = 3.141592;
	
	РадДолгота1 = Долгота1 * ЧислоПи / 180;
	РадДолгота2 = Долгота2 * ЧислоПи / 180;
	РадШирота1  = Широта1 * ЧислоПи / 180;
	РадШирота2  = Широта2 * ЧислоПи / 180;
	Дельта      = РадДолгота2 - РадДолгота1;
	
	КосинусШирота1 = Cos(РадШирота1);
	КосинусШирота2 = Cos(РадШирота2);
	КосинусДельта  = Cos(Дельта);
	СинусШирота1   = Sin(РадШирота1);
	СинусШирота2   = Sin(РадШирота2);
	СинусДельта    = Sin(Дельта);
	
	Дуга = ATan(Sqrt(Pow(КосинусШирота2 * СинусДельта, 2) + Pow((КосинусШирота1 * СинусШирота2) - (СинусШирота1 * КосинусШирота2 * КосинусДельта), 2)) / ((СинусШирота1 * СинусШирота2) + (КосинусШирота1 * КосинусШирота2 * КосинусДельта)));
	
	Возврат Дуга * РадиусЗемли;
               
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции

&НаКлиенте
// Процедура завершения после выбора адреса или географической зоны.
//
// Параметры:
//  ВыбранныйОбъект  - Объект - Результат выбора.
//  Родитель  - Объект - родитель выбора.
//
Процедура ВыборОбъектаДерева(ВыбранныйОбъект, Родитель) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныйОбъект) Тогда 
		мсвОбъектов = Новый Массив;
		мсвОбъектов.Добавить(ВыбранныйОбъект);
		
		ВыборМассиваОбъектовДерева(мсвОбъектов, Родитель);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
// Обработка переданного массива данных.
//
// Параметры:
//  ВыбранныйОбъект  - Объект - Результат выбора.
//  Родитель  - Объект - родитель выбора.
//
Процедура ВыборМассиваОбъектовДерева(ВыбранныеОбъекты, Родитель, ТолькоДобавление = Истина)
	
	Если Родитель.Объект = НСтр("ru = 'Маршруты'") Тогда
		Массив = Новый Массив;
		Массив.Добавить(ВыбранныеОбъекты.НачальнаяТочка);
		Массив.Добавить(ВыбранныеОбъекты.КонечнаяТочка);
		мсвОтвет = упМаршрутизация.ПолучитьРасстояниеИВремяМассиваТочек(Массив);
		Путь = Неопределено;
		Если мсвОтвет.Количество() Тогда
			Путь = мсвОтвет[0].ТекстМаршрута;
		КонецЕсли;
		Выборка = Новый Структура();
		Выборка.Вставить("НачальноеПредставление", ВыбранныеОбъекты.НачальнаяТочка);
		Выборка.Вставить("КонечноеПредставление", ВыбранныеОбъекты.КонечнаяТочка);
		Выборка.Вставить("Путь", Путь);
		Маршруты = Родитель.ПолучитьЭлементы();
		Если Не ТолькоДобавление Тогда
			Для Каждого Маршрут Из Маршруты Цикл
				Если Истина
					И Маршрут.НачальнаяТочка = ВыбранныеОбъекты.НачальнаяТочка
					И Маршрут.КонечнаяТочка = ВыбранныеОбъекты.КонечнаяТочка
				Тогда
					НоваяСтрока = Маршрут;
					Прервать;
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
		Если НоваяСтрока = Неопределено Тогда
			НоваяСтрока 				= Маршруты.Добавить();
			НоваяСтрока.Объект 			= "" + Выборка.НачальноеПредставление + " - " + Выборка.КонечноеПредставление;
			НоваяСтрока.Имя 			= НоваяСтрока.Объект;
			НоваяСтрока.Данные 			= """#FF0000"", 3, 1, ""#FF0000"", 0.2";
			НоваяСтрока.ОсновнойЦвет 	= "#FF0000";
			НоваяСтрока.Редактировать 	= НСтр("ru = 'Редактировать'");
			НоваяСтрока.НачальнаяТочка  = ВыбранныеОбъекты.НачальнаяТочка;
			НоваяСтрока.КонечнаяТочка	= ВыбранныеОбъекты.КонечнаяТочка;
			УстановитьПометкуРодителя(Родитель);
		КонецЕсли; 
		НоваяСтрока.Координаты = Выборка.Путь;
		Если ЗначениеЗаполнено(НоваяСтрока.Координаты) Тогда
			Если ЗначениеЗаполнено(НоваяСтрока.ИндексВМассиве) Тогда
				ИзменитьВидимостьОбъекта(НоваяСтрока.ИндексВМассиве, Ложь, НоваяСтрока.Объект);
			КонецЕсли; 
			НоваяСтрока.Пометка = Истина;
			ОтобразитьОбъект(НоваяСтрока, Родитель.Объект);
			НоваяСтрока.ИндексКартинки = 6;
		Иначе	
			НоваяСтрока.ИндексКартинки = 5;
		КонецЕсли;
	Иначе
		Если ВыбранныеОбъекты.Количество() Тогда 
			
			мсвНовыхОбъектов = Новый Массив;
			Для Каждого ВыбранныйОбъект Из ВыбранныеОбъекты Цикл 
				Если спзОбъектов.НайтиПоЗначению(ВыбранныйОбъект) <> Неопределено Тогда 
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Географический объект ""%1"" уже есть в списке объектов.'"), ВыбранныйОбъект),, "ДеревоОбъектов");
					Продолжить;
				КонецЕсли;	
				
				мсвНовыхОбъектов.Добавить(ВыбранныйОбъект);
				спзОбъектов.Добавить(ВыбранныйОбъект);
			КонецЦикла;	
			
			мсвСтруктур = ПолучитьДанныеОбъектов(мсвНовыхОбъектов);
			
			Для Каждого сткВозврат Из мсвСтруктур Цикл 
				
				НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, сткВозврат);
				
				Если НоваяСтрока.Пометка Тогда 
					Если КартаСформирована Тогда 
						ОтобразитьОбъект(НоваяСтрока, Родитель.Объект);
					КонецЕсли;	
				КонецЕсли;
				
				Если сткВозврат.ОпределитьКоординаты  = НСтр("ru = 'Геокодировать'") Тогда 
					НоваяСтрока.ОпределитьКоординаты = Истина;
				Иначе
					НоваяСтрока.ОпределитьКоординаты = Ложь;
				КонецЕсли;
				
			КонецЦикла;	
			
			УстановитьПометкуРодителя(Родитель);
		КонецЕсли;	
		
	КонецЕсли; 
КонецПроцедуры // ВыборМассиваОбъектовДерева()

&НаСервере
Функция ПолучитьДанныеОбъекта(ВыбранныйОбъект)
	
	сткВозврат = Новый Структура("Объект, Редактировать, Пометка, ИндексКартинки, Имя, Координаты, Данные, ОсновнойЦвет, ОпределитьКоординаты");
	сткВозврат.Объект = ВыбранныйОбъект;
	АдресКартинки = "";
	
	ТипЗначения = ТипЗнч(ВыбранныйОбъект);
	
	Если ТипЗначения = Тип("СправочникСсылка.упАдреса") Тогда 
		сткВозврат.Имя            = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Строка(ВыбранныйОбъект), "");
		СтильОтображения = ВыбранныйОбъект.ВидАдреса.СтильМаркера;
		Если Не ЗначениеЗаполнено(СтильОтображения) Тогда 
			СтильОтображения = Справочники.упСтилиМаркеров.Основной;
		КонецЕсли;	
		
		Если СтильОтображения.Картинка = Перечисления.упКартинкиМаркеров.МаркерПоУмолчанию Тогда 
			СсылкаНаИзображение = "";
		ИначеЕсли СтильОтображения.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда 
			мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Стиль", СтильОтображения));
			Если мсвСохраненныйМаркер.Количество() Тогда 
				Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Адрес) Тогда 
					АдресКартинки         = мсвСохраненныйМаркер[0].Адрес;
				КонецЕсли;	
			Иначе
				АдресКартинки = ПоместитьВоВременноеХранилище(СтильОтображения.ХранилищеЗначения.Получить(), УникальныйИдентификатор);
				
				НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
				НовыйСохраненныйМаркер.Стиль = СтильОтображения;
				НовыйСохраненныйМаркер.Адрес = АдресКартинки;
			КонецЕсли;	
		Иначе	
			мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Маркер", СтильОтображения.Картинка));
			Если мсвСохраненныйМаркер.Количество() Тогда 
				Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Адрес) Тогда 
					АдресКартинки         = мсвСохраненныйМаркер[0].Адрес;
				КонецЕсли;	
			Иначе
				Картинка = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(СтильОтображения.Картинка);
				
				АдресКартинки = ПоместитьВоВременноеХранилище(Картинка, УникальныйИдентификатор);
				
				НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
				НовыйСохраненныйМаркер.Маркер = СтильОтображения.Картинка;
				НовыйСохраненныйМаркер.Адрес  = АдресКартинки;
			КонецЕсли;	
		КонецЕсли;	
		
		сткВозврат.Данные         = "'" + АдресКартинки + "', " + ПредставлениеЧисла(СтильОтображения.ПоложениеПривязкиX) + ", " + ПредставлениеЧисла(СтильОтображения.ПоложениеПривязкиY);
		Ширина = СтильОтображения.Ширина;
		Высота = СтильОтображения.Высота;
		МасштабСтиля = СтильОтображения.Масштаб;
		Если МасштабСтиля > 0 И Ширина > 0 И Высота > 0 Тогда
			сткВозврат.Данные = сткВозврат.Данные + ", true, " + ПредставлениеЧисла(Ширина*МасштабСтиля/100) + ", " + ПредставлениеЧисла(Высота*МасштабСтиля/100);
		КонецЕсли; 
		сткВозврат.ОсновнойЦвет   = СтильОтображения.ОсновнойЦвет;
		Если ЗначениеЗаполнено(ВыбранныйОбъект.Широта) ИЛИ ЗначениеЗаполнено(ВыбранныйОбъект.Долгота) Тогда 
			сткВозврат.Редактировать  = НСтр("ru = 'Редактировать'");
			сткВозврат.Пометка        = Истина;
			сткВозврат.ИндексКартинки = 2;
			сткВозврат.Координаты     = ПредставлениеЧисла(ВыбранныйОбъект.Широта) + ";" + ПредставлениеЧисла(ВыбранныйОбъект.Долгота) + ";";
		Иначе
			сткВозврат.Редактировать  = НСтр("ru = 'Указать точку'");
			сткВозврат.Пометка        = Ложь;
			сткВозврат.ИндексКартинки = 1;
		КонецЕсли;
		
		сткВозврат.ОпределитьКоординаты  = НСтр("ru = 'Геокодировать'");
		
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.упГеографическиеЗоны") Тогда 
		сткВозврат.Имя            = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Строка(ВыбранныйОбъект), "");
		СтильОтображения = ВыбранныйОбъект.СтильОтображения;
		Если Не ЗначениеЗаполнено(СтильОтображения) Тогда 
			СтильОтображения = Справочники.упСтилиПолигонов.Основной;
		КонецЕсли;	
		сткВозврат.Данные         = """" + СтильОтображения.ЦветГраницы + """, " + ПредставлениеЧисла(СтильОтображения.ТолщинаГраницы) + ", " + ПредставлениеЧисла(1 - СтильОтображения.ПрозрачностьГраницы / 100) + ", """ + СтильОтображения.ЦветЗаливки + """, " + ПредставлениеЧисла(1 - СтильОтображения.ПрозрачностьЗаливки / 100);
		сткВозврат.ОсновнойЦвет   = СтильОтображения.ЦветЗаливки;
		Если ВыбранныйОбъект.Полигон.Количество() Тогда 
			сткВозврат.Редактировать  = НСтр("ru = 'Редактировать'");
			сткВозврат.Пометка        = Истина;
			сткВозврат.ИндексКартинки = 4;
			сткВозврат.Координаты     = "";
			Для Каждого текУгол Из ВыбранныйОбъект.Полигон Цикл 
				сткВозврат.Координаты     = сткВозврат.Координаты + ПредставлениеЧисла(текУгол.Широта) + ";" + ПредставлениеЧисла(текУгол.Долгота) + ";";
			КонецЦикла;	
		Иначе
			сткВозврат.Редактировать  = НСтр("ru = 'Указать область'");
			сткВозврат.Пометка        = Ложь;
			сткВозврат.ИндексКартинки = 3;
		КонецЕсли;
		
		сткВозврат.ОпределитьКоординаты  = НСтр("ru = 'Геокодировать'");
		
	КонецЕсли;	
	
	Возврат сткВозврат;
	
КонецФункции // ПолучитьДанныеОбъекта()

&НаСервере
Функция ПолучитьДанныеОбъектов(ВыбранныеОбъекты)
	
	мсвСтруктур = Новый Массив;
	Для Каждого ВыбранныйОбъект Из ВыбранныеОбъекты Цикл 
		сткВозврат = ПолучитьДанныеОбъекта(ВыбранныйОбъект);
		мсвСтруктур.Добавить(сткВозврат);
	КонецЦикла;	
	
	Возврат мсвСтруктур;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьПометку(текДанные)
	
	Если текДанные.ПолучитьРодителя() = Неопределено Тогда 
		Для каждого текЭлемент Из текДанные.ПолучитьЭлементы() Цикл 
			Если текЭлемент.Пометка = текДанные.Пометка Тогда 
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(текЭлемент.ИндексВМассиве) И ЗначениеЗаполнено(текЭлемент.Координаты) Тогда 
				текЭлемент.Пометка = текДанные.Пометка;
				ИзменитьВидимостьОбъекта(текЭлемент.ИндексВМассиве, текЭлемент.Пометка, текДанные.Объект);
				
				Если текЭлемент.Пометка Тогда 
					текЭлемент.ИндексКартинки = текЭлемент.ИндексКартинки + 1;
				Иначе	
					текЭлемент.ИндексКартинки = текЭлемент.ИндексКартинки - 1;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;	
		
		УстановитьПометкуРодителя(текДанные);
	Иначе	
		текРодитель = текДанные.ПолучитьРодителя();
		Если ЗначениеЗаполнено(текДанные.ИндексВМассиве) И ЗначениеЗаполнено(текДанные.Координаты) Тогда 
			ИзменитьВидимостьОбъекта(текДанные.ИндексВМассиве, текДанные.Пометка, текРодитель.Объект);
			Если текДанные.Пометка Тогда 
				текДанные.ИндексКартинки = текДанные.ИндексКартинки + 1;
			Иначе	
				текДанные.ИндексКартинки = текДанные.ИндексКартинки - 1;
			КонецЕсли;	
		Иначе
			текДанные.Пометка = Ложь;
		КонецЕсли;
		
		УстановитьПометкуРодителя(текРодитель);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуРодителя(текРодитель)
	
	ВсеПомечены = Истина;
	Для Каждого текЭлемент Из текРодитель.ПолучитьЭлементы() Цикл 
		Если Не текЭлемент.Пометка Тогда 
			ВсеПомечены = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	текРодитель.Пометка = ВсеПомечены;
	
КонецПроцедуры

&НаКлиенте
// Общий обработчик подтверждения закрытия.
//
// Параметры:
//  РезультатВопроса  - КодВозвратаДиалога - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ЗакрытиеФормыЗавершение(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		текДанные = ДеревоОбъектов.НайтиПоИдентификатору(РедактируемаяСтрока);
		Если текДанные <> Неопределено Тогда 
			СохранитьОбъект(текДанные, текДанные.ПолучитьРодителя().Объект);
			
			РежимРедактирования = Ложь;
			РедактируемаяСтрока = 0;
			
			Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность                  = РежимРедактирования;
			Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = РежимРедактирования;
		КонецЕсли;
		
		// Закрываем без дополнительных вопросов.
		ПодтверждениеЗакрытияФормы = Истина;
		Закрыть();
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		// Закрываем без дополнительных вопросов.
		ПодтверждениеЗакрытияФормы = Истина;
		Закрыть();
	Иначе 
		ПодтверждениеЗакрытияФормы = Неопределено;
	КонецЕсли;
	
КонецПроцедуры //ЗакрытиеФормыЗавершение()

&НаКлиенте
// Обрабатывает подтверждение адреса.
//
Процедура ВыполнитьОбработкуПодтверждениеАдреса() Экспорт
	
	Если НЕ КартаСформирована Тогда 
		ПодключитьОбработчикОжидания("ВыполнитьОбработкуПодтверждениеАдреса",1,Истина);
	Иначе
		Для Каждого текСтрока Из ДеревоОбъектов.ПолучитьЭлементы() Цикл
			Если текСтрока.Объект = НСтр("ru = 'Адреса'") Тогда
				Элементы.ДеревоОбъектов.Развернуть(текСтрока.ПолучитьИдентификатор(), Истина);
				Для Каждого текДанные Из текСтрока.ПолучитьЭлементы() Цикл
					текДанныеРодительОбъект = текСтрока.Объект;
					Элементы.ДеревоОбъектов.Доступность = Ложь;
					РедактироватьОбъект(текДанные, текДанныеРодительОбъект);
					РежимРедактирования = Истина;
					РедактируемаяСтрока = текДанные.ПолучитьИдентификатор();
					текДанные.Редактировать = НСтр("ru = 'Завершить редактирование'");
					Элементы.ДеревоОбъектов.Доступность = Истина;
					Элементы.HTMLДокументКонтекстноеМенюОчистить.Доступность = РежимРедактирования;			
					Элементы.ДеревоОбъектовКонтекстноеМенюОтобразитьРеперныеТочки.Доступность = ЛОЖЬ;
					Прервать;
				КонецЦикла;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбработкуПодтверждениеАдреса()

#КонецОбласти 
