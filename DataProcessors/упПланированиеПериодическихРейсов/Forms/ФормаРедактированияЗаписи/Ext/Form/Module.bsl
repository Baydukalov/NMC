
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Дата = Параметры.Дата;
	Расписание = Параметры.Расписание;
    ПеречитатьЗапись();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Действие"    , -1);
	сткВозврат.Вставить("Период"      , Дата);
	сткВозврат.Вставить("КоличествоТС", КоличествоТС);
	сткВозврат.Вставить("ЕстьТС"      , Ложь); 
	
	Если КоличествоТС = 0 И ЗаписьСуществует Тогда
		УдалитьЗаписьРегистра();
		сткВозврат.Действие = 0;
	Иначе
		Если КоличествоТС > 0 Тогда
			ИзменитьЗаписьРегистра();
			Если Не ЗаписьСуществует Тогда
				сткВозврат.ЕстьТС = ЗначениеЗаполнено(упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Расписание, "ТранспортноеСредство"));
			КонецЕсли; 
			сткВозврат.Действие = 1;
		КонецЕсли;
	КонецЕсли;
	
	Закрыть(сткВозврат);
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Действие"    , 0);
	сткВозврат.Вставить("Период"      , Дата);
	сткВозврат.Вставить("КоличествоТС", КоличествоТС);
	
	УдалитьЗаписьРегистра();
	Закрыть(сткВозврат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПеречитатьЗапись()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упГрафикиПериодическихРейсов.КоличествоТС,
	|	упГрафикиПериодическихРейсов.ВремяНачалаРейса
	|ИЗ
	|	РегистрСведений.упГрафикиПериодическихРейсов КАК упГрафикиПериодическихРейсов
	|ГДЕ
	|	упГрафикиПериодическихРейсов.Расписание = &Расписание
	|	И упГрафикиПериодическихРейсов.ДатаНачалаРейса = &Период";
	Запрос.УстановитьПараметр("Расписание", Расписание);
	Запрос.УстановитьПараметр("Период", Дата);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Элементы.ФормаУдалить.Видимость = Ложь;
	Иначе	
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КоличествоТС     = Выборка.КоличествоТС;
		ВремяНачалаРейса = Выборка.ВремяНачалаРейса;
		
		ЗаписьСуществует = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗаписьРегистра()
	
	НаборЗаписей = РегистрыСведений.упГрафикиПериодическихРейсов.СоздатьНаборЗаписей(); 
	
	НаборЗаписей.Отбор.Расписание.Установить(Расписание); 
	НаборЗаписей.Отбор.ДатаНачалаРейса.Установить(Дата); 
	
	НоваяЗапись = НаборЗаписей.Добавить(); 
	НоваяЗапись.Расписание       = Расписание; 
	НоваяЗапись.ДатаНачалаРейса  = Дата; 
	НоваяЗапись.КоличествоТС     = КоличествоТС; 
	НоваяЗапись.Автор            = Пользователи.ТекущийПользователь();
	НоваяЗапись.ВремяНачалаРейса = ВремяНачалаРейса;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры	

&НаСервере
Процедура УдалитьЗаписьРегистра()
	
	НаборЗаписей = РегистрыСведений.упГрафикиПериодическихРейсов.СоздатьНаборЗаписей(); 
	НаборЗаписей.Отбор.Расписание.Установить(Расписание); 
	НаборЗаписей.Отбор.ДатаНачалаРейса.Установить(Дата); 
	НаборЗаписей.Записать();    
	
КонецПроцедуры

#КонецОбласти 
