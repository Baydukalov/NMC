
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Расписание) Тогда
		Расписание = Параметры.Расписание;
	КонецЕсли; 
	
	УстановитьВидПланировщика();
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РасписаниеПриИзменении(Элемент)
	
	ОбновитьДанныеПланировщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	
	Если Элементы.ФормаУстановитьИнтервалМесяц.Пометка Тогда
		СтандартнаяОбработка = Ложь;
		УстановитьИнтервалМесяцСервер(ТекущиеПериодыОтображения[0].Начало + 1296000); 
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элемент.ВыделенныеЭлементы.Количество() Тогда
		сткПараметры = Новый Структура();
		сткПараметры.Вставить("Дата"      , Элемент.ВыделенныеЭлементы[0].Начало);
		сткПараметры.Вставить("Расписание", Элемент.ВыделенныеЭлементы[0].ЗначенияИзмерений.Получить("Расписания"));
		
		сткДополнительныеПараметры = Новый Структура;
		сткДополнительныеПараметры.Вставить("ЗначенияИзмерений", Элемент.ВыделенныеЭлементы[0].ЗначенияИзмерений);
		сткДополнительныеПараметры.Вставить("Идентификатор"    , Элемент.ВыделенныеЭлементы[0].Значение);
		
		ОткрытьФорму("Обработка.упПланированиеПериодическихРейсов.Форма.ФормаРедактированияЗаписи", сткПараметры, ЭтаФорма,,,, Новый ОписаниеОповещения("РедактированиеЗаписиЗавершение", ЭтаФорма, сткДополнительныеПараметры), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не (Начало >= Планировщик.НачалоПериодаОтображения И Начало <= Планировщик.КонецПериодаОтображения) Тогда
		ТекстПредупреждения = НСтр("ru = 'Указанный период находится за границами периода действия расписаний'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли; 
	
	текРасписание = Значения.Получить("Расписания");
	ИскомыеСтроки = СписокИнтервалов.НайтиСтроки(Новый Структура("Расписание, Период", текРасписание, Начало));
	Если ИскомыеСтроки.Количество() Тогда
		текИдентификатор = ИскомыеСтроки[0].Идентификатор;
	Иначе
		текИдентификатор = Неопределено;
	КонецЕсли; 
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Дата"      , Начало);
	сткПараметры.Вставить("Расписание", текРасписание);
	
	сткДополнительныеПараметры = Новый Структура;
	сткДополнительныеПараметры.Вставить("ЗначенияИзмерений", Значения);
	сткДополнительныеПараметры.Вставить("Идентификатор"    , текИдентификатор);
	
	ОткрытьФорму("Обработка.упПланированиеПериодическихРейсов.Форма.ФормаРедактированияЗаписи", сткПараметры, ЭтаФорма,,,, Новый ОписаниеОповещения("РедактированиеЗаписиЗавершение", ЭтаФорма, сткДополнительныеПараметры), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	ОтменаРедактирования = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура УстановитьИнтервалДень(Команда)
	
	УстановитьИнтервалДеньСервер(Планировщик.ТекущиеПериодыОтображения[0].Начало);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалНеделя(Команда)

	УстановитьИнтервалНеделяСервер(Планировщик.ТекущиеПериодыОтображения[0].Начало);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалМесяц(Команда)
	
	УстановитьИнтервалМесяцСервер(Планировщик.ТекущиеПериодыОтображения[0].Начало);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображениеНаДату(Команда)
	
	ТекстЗаголовка = НСтр("ru = 'Укажите дату, входящую в желаемый интервал планирования'");
	ПоказатьВводДаты(Новый ОписаниеОповещения("ВводДатыОтображенияЗавершение", ЭтаФорма), Планировщик.ТекущиеПериодыОтображения[0].Начало, ТекстЗаголовка, ЧастиДаты.Дата); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	
	мсвРасписаний = Новый Массив;
	КоллекцияЭлементов = Планировщик.Измерения[0].Элементы;	
	Для каждого текЭлемент Из КоллекцияЭлементов Цикл
		мсвРасписаний.Добавить(текЭлемент.Значение);
	КонецЦикла; 
	
	Если мсвРасписаний.Количество() Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("МассивРасписаний", мсвРасписаний);
		ОткрытьФорму("Обработка.упПланированиеПериодическихРейсов.Форма.ШаблонЗаполненияПериодов", сткПараметры,,,,, Новый ОписаниеОповещения("ЗаполнениеПоШаблонуЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не выбрано ни одного расписания периодических рейсов!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьИнтервалДеньСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоДня(ТекущаяДата), КонецДня(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	ПервыйЭлемент.Формат = "ДФ=""дд ММММ гггг 'г.' (dddd)""";
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.День;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.ФормаУстановитьИнтервалДень.Пометка   = Истина;
	Элементы.ФормаУстановитьИнтервалНеделя.Пометка = Ложь;
	Элементы.ФормаУстановитьИнтервалМесяц.Пометка  = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтервалНеделяСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоНедели(ТекущаяДата), КонецНедели(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	ПервыйЭлемент.Формат  = "ДФ = 'dd.MM'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ФорматДня = ФорматДняШкалыВремени.ДеньНедели;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат  = "ДФ = 'dddd'";

	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Неделя;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.ФормаУстановитьИнтервалДень.Пометка   = Ложь;
	Элементы.ФормаУстановитьИнтервалНеделя.Пометка = Истина;
	Элементы.ФормаУстановитьИнтервалМесяц.Пометка  = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнтервалМесяцСервер(ТекущаяДата = Неопределено)
	
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	КонецЕсли; 
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоМесяца(ТекущаяДата), КонецМесяца(ТекущаяДата));
	
	ЭлементыШкалыВремени = Планировщик.ШкалаВремени.Элементы;
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Сч = -ЭлементыШкалыВремени.Количество() + 1 по -1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[-Сч]);
	КонецЦикла;
	
	ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ПервыйЭлемент.Кратность = 1;
	ПервыйЭлемент.Формат = "ДФ = 'MMMM yyyy'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.Формат = "ДФ = 'dd'";
	
	НовыйЭлементШкалы = ЭлементыШкалыВремени.Добавить();
	НовыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	НовыйЭлементШкалы.Кратность = 1;
	НовыйЭлементШкалы.ЦветТекста = WebЦвета.Серый;
	НовыйЭлементШкалы.Формат = "ДФ = 'ddd'";
	
	Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина;
	
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Месяц;
	Планировщик.КратностьПериодическогоВарианта = 1;
	
	Элементы.ФормаУстановитьИнтервалДень.Пометка   = Ложь;
	Элементы.ФормаУстановитьИнтервалНеделя.Пометка = Ложь;
	Элементы.ФормаУстановитьИнтервалМесяц.Пометка  = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПланировщика()
	
	УстановитьИнтервалМесяцСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПланировщикаНаСервере()	

	СписокИнтервалов.Очистить();
	
	Планировщик.Измерения.Очистить();
	Планировщик.Элементы.Очистить();
	
	НовоеИзмерение = Планировщик.Измерения.Добавить("Расписания");
	НовоеИзмерение.Текст = НСтр("ru = 'Расписания'");
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упРасписанияПериодическихРейсов.Ссылка,
	|	ВЫБОР
	|		КОГДА упРасписанияПериодическихРейсов.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьТС,
	|	упРасписанияПериодическихРейсов.ДатаНачалаДействия,
	|	упРасписанияПериодическихРейсов.ДатаОкончанияДействия
	|ПОМЕСТИТЬ втРасписания
	|ИЗ
	|	Справочник.упРасписанияПериодическихРейсов КАК упРасписанияПериодическихРейсов
	|ГДЕ
	|	упРасписанияПериодическихРейсов.Активно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасписания.Ссылка КАК Расписание,
	|	втРасписания.ЕстьТС,
	|	упГрафикиПериодическихРейсов.ДатаНачалаРейса КАК Период,
	|	упГрафикиПериодическихРейсов.КоличествоТС,
	|	втРасписания.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	втРасписания.ДатаОкончанияДействия КАК ДатаОкончанияДействия
	|ИЗ
	|	втРасписания КАК втРасписания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упГрафикиПериодическихРейсов КАК упГрафикиПериодическихРейсов
	|		ПО втРасписания.Ссылка = упГрафикиПериодическихРейсов.Расписание
	|
	|УПОРЯДОЧИТЬ ПО
	|	втРасписания.Ссылка
	|ИТОГИ
	|	МИНИМУМ(ДатаНачалаДействия),
	|	МАКСИМУМ(ДатаОкончанияДействия)
	|ПО
	|	ОБЩИЕ,
	|	Расписание";	
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Если ЗначениеЗаполнено(Расписание) Тогда
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("упРасписанияПериодическихРейсов.Ссылка = &Расписание");
	КонецЕсли; 

	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Если ЗначениеЗаполнено(Расписание) Тогда
		Запрос.УстановитьПараметр("Расписание", Расписание);
	КонецЕсли; 
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ДеревоРезультат = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Для каждого текОбщиеИтоги Из ДеревоРезультат.Строки Цикл
			Если ЗначениеЗаполнено(текОбщиеИтоги.ДатаНачалаДействия) Тогда
				Планировщик.НачалоПериодаОтображения = текОбщиеИтоги.ДатаНачалаДействия;
			КонецЕсли; 
			Если ЗначениеЗаполнено(текОбщиеИтоги.ДатаОкончанияДействия) Тогда
				Планировщик.КонецПериодаОтображения = текОбщиеИтоги.ДатаОкончанияДействия;
			КонецЕсли; 
			Для каждого текРасписание Из текОбщиеИтоги.Строки Цикл
				соотИзмерений = Новый Соответствие;
				НовыйЭлементИзмерения = НовоеИзмерение.Элементы.Добавить(текРасписание.Расписание);
				соотИзмерений.Вставить(НовоеИзмерение.Значение, НовыйЭлементИзмерения.Значение); 
				
				Для каждого текДанные Из текРасписание.Строки Цикл
					Если ЗначениеЗаполнено(текДанные.Период) Тогда
						ДобавитьЭлементПланировщика(текДанные, соотИзмерений);
					КонецЕсли; 
				КонецЦикла; 
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементПланировщика(Данные, ЗначенияИзмерений)
	
	НовыйЭлемент = Планировщик.Элементы.Добавить(Данные.Период, КонецДня(Данные.Период));
	НовыйЭлемент.ЦветФона = WebЦвета.СветлоЗеленый;
	НовыйЭлемент.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений);
	Если Данные.ЕстьТС Тогда
		НовыйЭлемент.Картинка = БиблиотекаКартинок.упТС;
	Иначе	
		НовыйЭлемент.Картинка = БиблиотекаКартинок.упТипТС;
	КонецЕсли; 
	
	НовыйЭлемент.Значение = Новый УникальныйИдентификатор;
	НовыйЭлемент.Текст    = Строка(Данные.КоличествоТС);
	
	НоваяСтрока = СписокИнтервалов.Добавить();
	НоваяСтрока.Расписание = ЗначенияИзмерений.Получить("Расписания");
	НоваяСтрока.Период = Данные.Период;
	НоваяСтрока.Идентификатор = НовыйЭлемент.Значение;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементПланировщика(Идентификатор)
	
	ЭлементПланировщика = Планировщик.Элементы.Найти(Идентификатор);
	Планировщик.Элементы.Удалить(ЭлементПланировщика);
	
	ИскомыеСтроки = СписокИнтервалов.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
	Для каждого текСтрока Из ИскомыеСтроки Цикл
		СписокИнтервалов.Удалить(текСтрока);
	КонецЦикла; 
	
КонецПроцедуры

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после ввода даты.
//
Процедура ВводДатыОтображенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Если Элементы.ФормаУстановитьИнтервалДень.Пометка Тогда
			УстановитьИнтервалДеньСервер(Результат);
		ИначеЕсли Элементы.ФормаУстановитьИнтервалНеделя.Пометка Тогда
			УстановитьИнтервалНеделяСервер(Результат);
		ИначеЕсли Элементы.ФормаУстановитьИнтервалМесяц.Пометка Тогда
			УстановитьИнтервалМесяцСервер(Результат);
		КонецЕсли;
		
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы редактирования записей.
//
Процедура РедактированиеЗаписиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		
		Если Результат.Действие = 0 Тогда // удаление элемента
			УдалитьЭлементПланировщика(ДополнительныеПараметры.Идентификатор)
		ИначеЕсли Результат.Действие = 1 Тогда
			Если ЗначениеЗаполнено(ДополнительныеПараметры.Идентификатор) Тогда // изменение элемента
				ЭлементПланировщика = Планировщик.Элементы.Найти(ДополнительныеПараметры.Идентификатор);
				Если ЭлементПланировщика <> Неопределено Тогда
					ЭлементПланировщика.Текст = Строка(Результат.КоличествоТС);
				КонецЕсли; 
			Иначе // создание элемента
				//Если Результат.КоличествоТС > 0 Тогда
				ДобавитьЭлементПланировщика(Результат, Новый Соответствие(ДополнительныеПараметры.ЗначенияИзмерений));
				//КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы заполнения по шаблону перодов.
//
Процедура ЗаполнениеПоШаблонуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ОбновитьДанныеПланировщикаНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 