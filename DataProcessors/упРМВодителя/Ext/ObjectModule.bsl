#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
	
// Аутентификация пользователя. 
//
Процедура Авторизация(сткПараметрыАвторизации, ТекстОшибки = "", Отказ) Экспорт
	
	Водитель = упСервисныеФункцииВызовСервера.Авторизация(сткПараметрыАвторизации, ТекстОшибки);	
	
	Если НЕ ЗначениеЗаполнено(Водитель) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура поиска рейса по сотруднику и при необходимости изменении активности найденного рейса.
//
Процедура ЗаполнитьРейсПоСотруднику(ТекстОшибки = "") Экспорт
	
	сткПараметрыРейса = упРейс.РейсПоСотруднику(Водитель, ТекстОшибки);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткПараметрыРейса);
	ЭтоРейсАктивный 	= Ложь;
	ЭтоРейсКВыполнению 	= Ложь;
	Если ЗначениеЗаполнено(Рейс) Тогда
		ТребуетсяОформлениеПутевогоЛиста = упРейс.ТребуетсяОформлениеПутевогоЛиста(Рейс);
		
		Если ТребуетсяОформлениеПутевогоЛиста Тогда
			ЭтоРейсАктивный = Ложь
						   Или (Истина
							   И сткПараметрыРейса.СтатусРейса = Перечисления.упСтатусыРейса.Выполняется 
							   И НЕ сткПараметрыРейса.СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Новый)
						   Или сткПараметрыРейса.СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Выдан
						   Или сткПараметрыРейса.СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Возвращен;
		Иначе
			ЭтоРейсАктивный = Истина; // Для маршрутного листа рейс всегда активный
		КонецЕсли;
		ЭтоРейсКВыполнению 	= НЕ ЭтоРейсАктивный;
	КонецЕсли;
		
КонецПроцедуры

// Процедура заполнения реквизитов типовыми данными.
//
Процедура Инициализация() Экспорт
	
	Водитель        = Справочники.упСотрудники.ПустаяСсылка();
	Рейс            = Документы.упРейс.ПустаяСсылка();
	ПутевойЛист     = Документы.упПутевойЛист.ПустаяСсылка();
	ЭтоРейсАктивный = Ложь;
	ЭтоРейсКВыполнению = Ложь;
	НомерРейса      = "";
	ПредставлениеМаршрута = "";
	ТребуетсяОформлениеПутевогоЛиста = Ложь;
	ПробегНачало    = 0;
	ПробегОкончание = 0;
	ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
	ГСМ.Очистить();
	
КонецПроцедуры

// Процедура - поиска рейса по штрихкоду.
//
Процедура НайтиМаршрутныйЛистПоШК(ШтрихКод, ТекстОшибки = "", Отказ) Экспорт
	
	РейсПоМаршрутномуЛисту = упСервисныеФункции.ПолучитьДокументПоШК(ШтрихКод, "Документ.упРейс"); 
	
	Если ЗначениеЗаполнено(РейсПоМаршрутномуЛисту) Тогда
		Если НЕ РейсПоМаршрутномуЛисту = Рейс Тогда
			Отказ = Истина;	
			ТекстОшибки = Нстр("ru = 'Маршрутный лист не принадлежит текущему рейсу");
		КонецЕсли;
	Иначе	
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Маршрутный лист со штрихкодом ""%1"" не найден'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ШтрихКод);	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - поиска путевого листа по штрихкоду.
//
Процедура НайтиПутевойЛистПоШК(ШтрихКод, ТекстОшибки = "", Отказ) Экспорт
	
	ПутевойЛист = упСервисныеФункции.ПолучитьДокументПоШК(ШтрихКод, "Документ.упПутевойЛист"); 
	
	Если ЗначениеЗаполнено(ПутевойЛист) Тогда
		сткРеквизитыПутевогоЛиста	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПутевойЛист, "ТранспортноеСредство, ПробегНачало, ПробегОкончание");
		ПробегНачало		= сткРеквизитыПутевогоЛиста.ПробегНачало;
		ПробегОкончание		= сткРеквизитыПутевогоЛиста.ПробегОкончание;
		СтатусПутевогоЛиста = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ПутевойЛист);
		Если упПутевойЛистУчетГСМ.РейсПринадлежитПутевомуЛисту(Рейс, ПутевойЛист) Тогда
			ТранспортноеСредство = сткРеквизитыПутевогоЛиста.ТранспортноеСредство;
		Иначе	
			Отказ = Истина;	
			ТекстОшибки = Нстр("ru = 'Путевой лист не принадлежит текущему рейсу");
		КонецЕсли;
	Иначе	
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Путевой лист со штрихкодом ""%1"" не найден'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ШтрихКод);	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - создает новый путевой лист по рейсу.
//
Процедура СформироватьПутевойЛист(ТекстОшибки = "", Отказ) Экспорт
	Если ЗначениеЗаполнено(Рейс) И НЕ ЗначениеЗаполнено(ПутевойЛист) Тогда
		мсвРейсы = Новый Массив;
		мсвРейсы.Добавить(Рейс);
		ПутевойЛист = упПутевойЛистУчетГСМ.НовыйПутевойЛистПоРейсам(мсвРейсы,,ТекстОшибки,Отказ);
		Если НЕ ЗначениеЗаполнено(ПутевойЛист) Тогда
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'Не удалось сформировать путевой лист по рейсу");
		Иначе
			ПробегНачало	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПутевойЛист, "ПробегНачало");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Заполнение ТЧ "ГСМ" на основании путевого листа.
//
Процедура ЗаполнитьГСМ() Экспорт
	
	Если ЗначениеЗаполнено(ПутевойЛист) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упПутевойЛистГСМ.ВидГСМ,
		               |	упПутевойЛистГСМ.НачОстаток,
		               |	упПутевойЛистГСМ.КонОстаток
		               |ИЗ
		               |	Документ.упПутевойЛист.ГСМ КАК упПутевойЛистГСМ
		               |ГДЕ
		               |	упПутевойЛистГСМ.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упПутевойЛистЗаправкиСливыГСМ.ВидГСМ,
		               |	упПутевойЛистЗаправкиСливыГСМ.Количество,
		               |	упПутевойЛистЗаправкиСливыГСМ.ВидДвижения
		               |ИЗ
		               |	Документ.упПутевойЛист.ЗаправкиСливыГСМ КАК упПутевойЛистЗаправкиСливыГСМ
		               |ГДЕ
		               |	упПутевойЛистЗаправкиСливыГСМ.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ПутевойЛист);
		мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
		ГСМ.Загрузить(мсвРезультатЗапроса[0].Выгрузить());
		Заправки.Загрузить(мсвРезультатЗапроса[1].Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения начального остатка путевого листа.
//
Процедура ВыдатьПутевойЛист(ТекстОшибки = "", Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ПутевойЛист) Тогда
		упПутевойЛистУчетГСМ.ЗаполнитьНачальныеОстатки(ПутевойЛист, ГСМ.Выгрузить(), ПробегНачало, Перечисления.упСтатусыПутевогоЛиста.Выдан, ТекстОшибки, Отказ);	
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения конечного остатка путевого листа.
//
Процедура ЗаполнитьКонечныеОстатки(ТекстОшибки = "", Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ПутевойЛист) Тогда
		упПутевойЛистУчетГСМ.ЗаполнитьКонечныеОстатки(ПутевойЛист, ГСМ.Выгрузить(), Заправки.Выгрузить(), ПробегОкончание, Перечисления.упСтатусыПутевогоЛиста.Возвращен, ТекстОшибки, Отказ);	
	КонецЕсли;

КонецПроцедуры

// Процедура заполнения дерева работ.
//
Процедура ЗаполнитьДеревоРабот(ИзменятьТолькоВыполненные, ТекстОшибки = "", Отказ) Экспорт
	упПрохождениеТочки.ЗаполнитьДеревоРаботПоРейсу(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, Рейс, ИзменятьТолькоВыполненные);
	Отказ = Ложь;
	сткДанныеПоТочке = упПрохождениеТочки.ЗаполнитьДеревоТочкиПервойНеПройденнойТочкой(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, Отказ);
	АдресаПогрузкиПоРейсу.Очистить();
	Если НЕ Отказ Тогда
		ЭтоПерваяТочка    = сткДанныеПоТочке.ЭтоПерваяТочка;
		ЭтоПоследняяТочка = сткДанныеПоТочке.ЭтоПоследняяТочка;
		НачалоВыполнения  = сткДанныеПоТочке.НачалоВыполнения;
		НачалоРабот       = сткДанныеПоТочке.НачалоРабот;
		ОкончаниеВыполнения	= сткДанныеПоТочке.ОкончаниеВыполнения;
		НомерТекущейТочки   = сткДанныеПоТочке.НомерТекущейТочки;
	КонецЕсли;
КонецПроцедуры

// Процедура заполнения дерево по точке.
//
Процедура ЗаполнитьДеревоПоТочке(Сдвиг = 0, НайтиТочку) Экспорт
	Отказ = Ложь;
	текПорядокТочки = Неопределено;
	Если дрвРаботыПоТочке.Строки.Количество()>0 Тогда
		текПорядокТочки = дрвРаботыПоТочке.Строки[0].ПорядокТочки;
	КонецЕсли;
	сткДанныеПоТочке = упПрохождениеТочки.ЗаполнитьДеревоТочкиДаннымиТочкиСоСдвигом(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, текПорядокТочки, Сдвиг,НайтиТочку,Отказ);
	Если НЕ Отказ Тогда
		ЭтоПерваяТочка    = сткДанныеПоТочке.ЭтоПерваяТочка;
		ЭтоПоследняяТочка = сткДанныеПоТочке.ЭтоПоследняяТочка;
		НачалоВыполнения  = сткДанныеПоТочке.НачалоВыполнения;
		НачалоРабот = сткДанныеПоТочке.НачалоРабот;
		ОкончаниеВыполнения	= сткДанныеПоТочке.ОкончаниеВыполнения;
		НомерТекущейТочки   = сткДанныеПоТочке.НомерТекущейТочки;
	КонецЕсли;
КонецПроцедуры

// Вовращае строку не перезаполняемых типовых реквизитов.
// 
Функция ПолучитьСтрокуНеПерезаполняемыхРеквизитов() Экспорт
	
	Возврат "дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, ЭтоПерваяТочка, ЭтоПоследняяТочка, дрвРаботыПоТекущемуРейсуЭталон, ГСМ, Заправки, АдресаПогрузкиПоРейсу";
		
КонецФункции

// Возвращает признак наличия изменений в дереве.
//
Функция ЕстьИзмененияВДеревеИсполненияРейсов() Экспорт
	Возврат упПрохождениеТочки.ЕстьИзмененияВДеревеИсполненияРейсов(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон);		
КонецФункции

// Получаем и заполняем информацию по точке.
//
Процедура ПрименитьИзмененияВДеревеТочекРейса(мсвИндексовИзмененныхТочек = Неопределено, ПерезаполнятьДеревоТочекРейса = Истина, мсвНеправильноЗаполненныхСтрок, ЕстьНеПройденныеТочки) Экспорт
	текПорядокТочки = Неопределено;
	Если дрвРаботыПоТочке.Строки.Количество()>0 Тогда
		текПорядокТочки = дрвРаботыПоТочке.Строки[0].ПорядокТочки;
		Отказ = Ложь;
		упПрохождениеТочки.ПеренестиДанныеТочкиВДерево(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, текПорядокТочки, Отказ); 
		Если НЕ Отказ Тогда
			упПрохождениеТочки.ПрименитьИзмененияВДеревеТочекРейса(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, Рейс, мсвИндексовИзмененныхТочек, ПерезаполнятьДеревоТочекРейса, мсвНеправильноЗаполненныхСтрок);	
			Если мсвНеправильноЗаполненныхСтрок.Количество() > 0 Тогда
				текПорядокТочки	= мсвНеправильноЗаполненныхСтрок[0].ИндексТочки + 1;
				сткДанныеПоТочке	= упПрохождениеТочки.ЗаполнитьДеревоТочкиДаннымиТочкиСоСдвигом(дрвРаботыПоТекущемуРейсу,дрвРаботыПоТочке,текПорядокТочки,,Истина,Отказ);
			Иначе
				сткДанныеПоТочке 	= упПрохождениеТочки.ЗаполнитьДеревоТочкиПервойНеПройденнойТочкой(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, Отказ);
			КонецЕсли;
			Если НЕ Отказ Тогда
					ЭтоПерваяТочка	 	= сткДанныеПоТочке.ЭтоПерваяТочка;
					ЭтоПоследняяТочка 	= сткДанныеПоТочке.ЭтоПоследняяТочка;
					НачалоВыполнения	= сткДанныеПоТочке.НачалоВыполнения;
					НачалоРабот			= сткДанныеПоТочке.НачалоРабот;
					ОкончаниеВыполнения	= сткДанныеПоТочке.ОкончаниеВыполнения;
					ЕстьНеПройденныеТочки = сткДанныеПоТочке.ЕстьНеПройденныеТочки;
					НомерТекущейТочки	= сткДанныеПоТочке.НомерТекущейТочки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - Восстанивливает точку разгрузки.
//
Процедура ВосстановитьТочкуРазгрузки(сткВозврата) Экспорт
	упПрохождениеТочки.ВосстановитьТочкуРазгрузки(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТекущемуРейсуЭталон, сткВозврата);
КонецПроцедуры

// Выделяет все строки дерева значений.
//
Процедура ВыделитьВсе() Экспорт
	текПорядокТочки = Неопределено;
	Если дрвРаботыПоТочке.Строки.Количество()>0 Тогда
		текПорядокТочки = дрвРаботыПоТочке.Строки[0].ПорядокТочки;
		Отказ = Ложь;
		упПрохождениеТочки.ПеренестиДанныеТочкиВДерево(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, текПорядокТочки, Отказ); 
		Если НЕ Отказ Тогда
			упПрохождениеТочки.УстановитьФлагиПотомкам(дрвРаботыПоТекущемуРейсу.Строки, Истина);
			упПрохождениеТочки.ЗаполнитьДеревоТочкиДаннымиТочкиСоСдвигом(дрвРаботыПоТекущемуРейсу, дрвРаботыПоТочке, текПорядокТочки, ,Истина, Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - получает данные по работам указанного рейса.
//
Процедура ЗагрузитьАдресаПогрузкиПоРейсу() Экспорт
	Если АдресаПогрузкиПоРейсу.Количество()=0 Тогда
		АдресаПогрузкиПоРейсу = упПрохождениеТочки.ПолучитьАдресаПогрузкиПоРейсу(Рейс);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли