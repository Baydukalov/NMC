#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	текОбъект	= РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(Объект, Параметры.Объект,,текОбъект.ПолучитьСтрокуНеПерезаполняемыхРеквизитов());

	Элементы.ГруппаКнопкаПутевойЛист.Видимость    = Объект.ТребуетсяОформлениеПутевогоЛиста;
	Элементы.ГруппаКнопкаМаршрутныйЛист.Видимость = НЕ Объект.ТребуетсяОформлениеПутевогоЛиста;

	
	ТекстДополнительногоСообщения  = Нстр("ru = 'Рейс номер: %1'");
	ТекстДополнительногоСообщения  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДополнительногоСообщения, Объект.НомерРейса);
	ТекстДополнительногоСообщения1 = Нстр("ru = 'Представление маршрута:%1'");
	ТекстДополнительногоСообщения1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДополнительногоСообщения1, Объект.ПредставлениеМаршрута);
	
	Если Объект.ТребуетсяОформлениеПутевогоЛиста Тогда
		ТекстПодсказки = Нстр("ru = 'Для оформления путевого листа нажмите кнопку'");	
	Иначе
		ТекстПодсказки = Нстр("ru = 'Для печати маршрутного листа нажмите кнопку'");	
	КонецЕсли;
	ЗакрытьТекущуюФорму = Ложь;
	ФормаЗаблокирована 	= Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = НЕ ЗакрытьТекущуюФорму;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаПечатьМаршрутногоЛистаНажатие(Элемент)

	Если ЗначениеЗаполнено(Объект.Рейс) И НЕ ФормаЗаблокирована Тогда
		мсвОбъектов = Новый Массив;
		мсвОбъектов.Добавить(Объект.Рейс);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Документ.упРейс", "МаршрутныйЛист", мсвОбъектов);
		ЗакрытьФорму();
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПолучитьПутевойЛистНажатие(Элемент)
	
	ПолучитьПутевойЛист();
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыходНажатие(Элемент)
	ЗакрытьФорму();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ПолучитьПутевойЛист()
	
	Отказ = Ложь;
	СформироватьПутевойЛистНаСервере(Отказ);	
		
	Если НЕ Отказ Тогда
		ОткрытьФормуПерехода("ФормаНачальныеДанныеПутевогоЛиста");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПутевойЛистНаСервере(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.ПутевойЛист) Тогда
		ТекстОшибки	= "";
		текОбъект = РеквизитФормыВЗначение("Объект");
		текОбъект.СформироватьПутевойЛист(ТекстОшибки, Отказ);
		ЗначениеВРеквизитФормы(текОбъект, "Объект");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы начальных данных.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт

	сткРезультатПриЗакрытииФормы 	= Неопределено;
	ИмяФормыПерехода 				= Неопределено;	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		сткРезультатПриЗакрытииФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		Если сткРезультатПриЗакрытииФормы.Свойство("ИмяФормыПерехода", ИмяФормыПерехода) Тогда
			
		КонецЕсли;
	КонецЕсли;
	
	мсвСоставИмениФормы = СтрРазделить(ИмяФормы, ".");
	ИмяТекущейФормы 	= мсвСоставИмениФормы[мсвСоставИмениФормы.ВГраница()];
	
	// При возвращении по стеку форм, анализируем имя текущей формы, если дошли до нужного.
	Если ИмяТекущейФормы = ИмяФормыПерехода Тогда
		Активизировать();
		ФормаЗаблокирована = Ложь;
	Иначе	
		ПодключитьОбработчикОжидания("ЗакрытьФормуОбработчикОжидания",0.1,Истина);
	КонецЕсли;
	
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ОткрытьФормуПерехода(ИмяОткрываемойФормы, сткПараметры = Неопределено, ВладелецФормы = Неопределено)
	
	Если НЕ ФормаЗаблокирована Тогда
		ФормаЗаблокирована 	= Истина;
		Если сткПараметры = Неопределено Тогда
			сткПараметры				= Новый Структура();
			сткПараметры.Вставить("Объект", Объект);	
		КонецЕсли;
		ОповещениеОЗакрытии 		= Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы",ЭтаФорма);
		ПолноеИмяОткрываемойФормы 	= СтрЗаменить("Обработка.упРМВодителя.Форма.%1", "%1", ИмяОткрываемойФормы);
		Если ВладелецФормы = Неопределено Тогда
			ВладелецФормы = ЭтаФорма;	
		КонецЕсли;
		ОткрытьФорму(ПолноеИмяОткрываемойФормы, сткПараметры, ВладелецФормы,,Окно,,ОповещениеОЗакрытии);			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(сткВозврата = "")
	Если НЕ ФормаЗаблокирована Тогда
		ЗакрытьТекущуюФорму = Истина;
		Если ЗначениеЗаполнено(сткВозврата) Тогда
			Закрыть(сткВозврата);	
		Иначе
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуОбработчикОжидания() Экспорт
	ЗакрытьТекущуюФорму = Истина;
	Закрыть(сткРезультатПриЗакрытииФормы);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
