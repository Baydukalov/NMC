#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	текОбъект	= РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(Объект, Параметры.Объект,,текОбъект.ПолучитьСтрокуНеПерезаполняемыхРеквизитов());
	
	ТекстДополнительногоСообщения = Нстр("ru = 'Рейс номер: %1'");
	ТекстДополнительногоСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДополнительногоСообщения, Объект.НомерРейса);
	ТекстДополнительногоСообщения1 = Нстр("ru = 'Представление маршрута:%1'");
	ТекстДополнительногоСообщения1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДополнительногоСообщения1, Объект.ПредставлениеМаршрута);
	
	ИспользоватьДополнительныеОтчетыИОбработки = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки");
	
	ТекстПодсказки = Нстр("ru = 'Отсканируйте штрихкод путевого или маршрутного листа'");
	
	Если НЕ Объект.ТребуетсяОформлениеПутевогоЛиста Тогда
		Элементы.КнопкаПовторнаяПечать.ТекстНевыбраннойКартинки = Нстр("ru = 'Печать маршрутного листа'");
	КонецЕсли;

	ЗакрытьТекущуюФорму = Ложь;
	ФормаЗаблокирована 	= Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = НЕ ЗакрытьТекущуюФорму;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаПовторнаяПечатьНажатие(Элемент)
	
	РаспечататьПакетДокументов();

КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыходНажатие(Элемент)
	ЗакрытьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ШтрихКодДокументаПриИзменении(Элемент)
	ОбработатьПолученныйШК(Штрихкод);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// ПодключаемоеОборудование
	Если Истина
		И Источник = "ПодключаемоеОборудование"
		И ВводДоступен()
	Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[ 1 ] = Неопределено Тогда
				ТекКод = Параметр[ 0 ];
			Иначе
				ТекКод = Параметр[ 1 ][ 1 ];
			КонецЕсли;
			ОбработатьПолученныйШК(ТекКод);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОбработатьПолученныйШК(Штрихкод)
	Если ЗначениеЗаполнено(Штрихкод) Тогда
		
		// Если отсканированный ШК принадлежит ПЛ или МЛ активного рейса
		Отказ = Ложь;
		ТекстОшибки = "";
		НайтиПутевойЛистПоШК(ШтрихКод, ТекстОшибки, Отказ);
		Если Отказ Тогда
			ТекстОшибки = "";
			Отказ = Ложь;
			НайтиМаршрутныйЛистПоШК(ШтрихКод, ТекстОшибки, Отказ);
			
			Если НЕ Отказ Тогда
				ОткрытьФормуПерехода("ФормаИсполнениеТочек");
			КонецЕсли;
		Иначе	
			ОткрытьФормуПерехода("ФормаКонечныеДанныеПутевогоЛиста");		
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

&НаСервере
Процедура НайтиПутевойЛистПоШК(ШтрихКод, ТекстОшибки, Отказ)
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.НайтиПутевойЛистПоШК(ШтрихКод, ТекстОшибки, Отказ);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
КонецПроцедуры

&НаСервере
Процедура НайтиМаршрутныйЛистПоШК(ШтрихКод, ТекстОшибки, Отказ)
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.НайтиМаршрутныйЛистПоШК(ШтрихКод, ТекстОшибки, Отказ);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьПакетДокументов()
	
	Если ЗначениеЗаполнено(Объект.ПутевойЛист) Тогда
		мсвОбъектов = Новый Массив;
		мсвОбъектов.Добавить(Объект.ПутевойЛист);
		
		ИмяОбъектаМетаданных = "Документ.упПутевойЛист";
		
		ТипПечатнойФормыПутевогоЛиста = ТипПечатнойФормыПутевогоЛиста();
		
		Если ЗначениеЗаполнено(ТипПечатнойФормыПутевогоЛиста) Тогда
			Если ТипЗнч(ТипПечатнойФормыПутевогоЛиста) = Тип("ПеречислениеСсылка.упТипыПечатныхФормПутевогоЛиста") Тогда
				ИмяМакета = упСервисныеФункцииВызовСервера.ИмяЗначенияПеречисления(ТипПечатнойФормыПутевогоЛиста);
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, мсвОбъектов);
			ИначеЕсли ИспользоватьДополнительныеОтчетыИОбработки Тогда
				ПараметрыПечати = Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Истина);
				ИмяМакета 		= упСервисныеФункцииВызовСервера.ПолучитьИмяМакетаВнешнейПечатнойФормы(ТипПечатнойФормыПутевогоЛиста, ИмяОбъектаМетаданных);
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, мсвОбъектов,ПараметрыПечати);
			КонецЕсли;
		Иначе
			Текстошибки = "У модели транспортного средства не задана печатная форма путевого листа";
		КонецЕсли;
	ИначеЕсли НЕ Объект.ТребуетсяОформлениеПутевогоЛиста И ЗначениеЗаполнено(Объект.Рейс) Тогда	
		мсвОбъектов = Новый Массив;
		мсвОбъектов.Добавить(Объект.Рейс);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Документ.упРейс", "МаршрутныйЛист", мсвОбъектов);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ТипПечатнойФормыПутевогоЛиста()
	
	ТранспортноеСредство = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ПутевойЛист, "ТранспортноеСредство");
	
	Возврат упПутевойЛистУчетГСМ.ПолучитьТипПечатнойФормы(ТранспортноеСредство);
	
КонецФункции

&НаКлиенте
// Процедура завершения после закрытия формы работы с точками.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт

	сткРезультатПриЗакрытииФормы 	= Неопределено;
	ИмяФормыПерехода 				= Неопределено;	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		сткРезультатПриЗакрытииФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		Если сткРезультатПриЗакрытииФормы.Свойство("ИмяФормыПерехода", ИмяФормыПерехода) Тогда
			
		КонецЕсли;
	КонецЕсли;
	
	мсвСоставИмениФормы = СтрРазделить(ИмяФормы, ".");
	ИмяТекущейФормы 	= мсвСоставИмениФормы[мсвСоставИмениФормы.ВГраница()];
	
	// При возвращении по стеку форм, анализируем имя текущей формы, если дошли до нужного.
	Если ИмяТекущейФормы = ИмяФормыПерехода Тогда
		Активизировать();
		ФормаЗаблокирована = Ложь;
	Иначе	
		ПодключитьОбработчикОжидания("ЗакрытьФормуОбработчикОжидания",0.1,Истина);
	КонецЕсли;

	
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ОткрытьФормуПерехода(ИмяОткрываемойФормы, сткПараметры = Неопределено, ВладелецФормы = Неопределено)
	
	Если НЕ ФормаЗаблокирована Тогда
		ФормаЗаблокирована 	= Истина;
		Если сткПараметры = Неопределено Тогда
			сткПараметры				= Новый Структура();
			сткПараметры.Вставить("Объект", Объект);	
		КонецЕсли;
		ОповещениеОЗакрытии 		= Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы",ЭтаФорма);
		ПолноеИмяОткрываемойФормы 	= СтрЗаменить("Обработка.упРМВодителя.Форма.%1", "%1", ИмяОткрываемойФормы);
		Если ВладелецФормы = Неопределено Тогда
			ВладелецФормы = ЭтаФорма;	
		КонецЕсли;
		ОткрытьФорму(ПолноеИмяОткрываемойФормы, сткПараметры, ВладелецФормы,,Окно,,ОповещениеОЗакрытии);			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(сткВозврата = "")
	Если НЕ ФормаЗаблокирована Тогда
		ЗакрытьТекущуюФорму = Истина;
		Если ЗначениеЗаполнено(сткВозврата) Тогда
			Закрыть(сткВозврата);	
		Иначе
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуОбработчикОжидания() Экспорт
	ЗакрытьТекущуюФорму = Истина;
	Закрыть(сткРезультатПриЗакрытииФормы);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
