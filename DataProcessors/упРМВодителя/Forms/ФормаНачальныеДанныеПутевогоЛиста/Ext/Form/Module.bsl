
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	текОбъект	= РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(текОбъект, Параметры.Объект,,текОбъект.ПолучитьСтрокуНеПерезаполняемыхРеквизитов());
	текОбъект.ЗаполнитьГСМ();
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	ТекстДополнительногоСообщения 	= "" + Объект.ПутевойЛист;
	ТекстДополнительногоСообщения1 	= "" + Объект.ТранспортноеСредство;
	ТекстПодсказки = Нстр("ru = 'Введите начальные остатки по путевому листу'");
	ЗакрытьТекущуюФорму	= Ложь;
	ФормаЗаблокирована 	= Ложь;
	ИспользоватьДополнительныеОтчетыИОбработки = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = НЕ ЗакрытьТекущуюФорму;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаПечатьНажатие(Элемент)
	Отказ = Ложь;
	ВыдатьПутевойЛист(Отказ);
	Если НЕ Отказ И НЕ ФормаЗаблокирована Тогда
		РаспечататьПакетДокументов();
		ЗакрытьФорму();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыходНажатие(Элемент)
	ЗакрытьФорму(Новый Структура("ИмяФормыПерехода", "ФормаАвторизации"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура РаспечататьПакетДокументов()
	
	Если ЗначениеЗаполнено(Объект.ПутевойЛист) Тогда
		мсвОбъектов = Новый Массив;
		мсвОбъектов.Добавить(Объект.ПутевойЛист);
		ИмяОбъектаМетаданных	 = "Документ.упПутевойЛист";
		ТипПечатнойФормыПутевогоЛиста = ТипПечатнойФормыПутевогоЛиста();
		
		Если ЗначениеЗаполнено(ТипПечатнойФормыПутевогоЛиста) Тогда
			Если ТипЗнч(ТипПечатнойФормыПутевогоЛиста) = Тип("ПеречислениеСсылка.упТипыПечатныхФормПутевогоЛиста") Тогда
				ИмяМакета = упСервисныеФункцииВызовСервера.ИмяЗначенияПеречисления(ТипПечатнойФормыПутевогоЛиста);
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, мсвОбъектов);
			ИначеЕсли ИспользоватьДополнительныеОтчетыИОбработки Тогда
				ПараметрыПечати = Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Истина);
				ИмяМакета 		= упСервисныеФункцииВызовСервера.ПолучитьИмяМакетаВнешнейПечатнойФормы(ТипПечатнойФормыПутевогоЛиста, ИмяОбъектаМетаданных);
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, мсвОбъектов, ПараметрыПечати);
			КонецЕсли;
		Иначе
			Текстошибки = "У модели транспортного средства не задана печатная форма путевого листа";
		КонецЕсли;
	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ТипПечатнойФормыПутевогоЛиста()
	
	ТранспортноеСредство = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ПутевойЛист, "ТранспортноеСредство");
	
	Возврат упПутевойЛистУчетГСМ.ПолучитьТипПечатнойФормы(ТранспортноеСредство);
	
КонецФункции

&НаСервере
Процедура ВыдатьПутевойЛист(Отказ)
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ВыдатьПутевойЛист(ТекстОшибки, Отказ);
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы.
//
// Параметры:
//  Результат  - РежимДиалогаВопрос - Ответ.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт

	сткРезультатПриЗакрытииФормы 	= Неопределено;
	ИмяФормыПерехода 				= Неопределено;	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		сткРезультатПриЗакрытииФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		Если сткРезультатПриЗакрытииФормы.Свойство("ИмяФормыПерехода", ИмяФормыПерехода) Тогда
			
		КонецЕсли;
	КонецЕсли;
	
	мсвСоставИмениФормы = СтрРазделить(ИмяФормы, ".");
	ИмяТекущейФормы 	= мсвСоставИмениФормы[мсвСоставИмениФормы.ВГраница()];
	
	// При возвращении по стеку форм, анализируем имя текущей формы, если дошли до нужного.
	Если ИмяТекущейФормы = ИмяФормыПерехода Тогда
		Активизировать();
		ФормаЗаблокирована = Ложь;
	Иначе	
		ПодключитьОбработчикОжидания("ЗакрытьФормуОбработчикОжидания",0.1,Истина);
	КонецЕсли;

	
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ЗакрытьФорму(сткВозврата = "")
	Если НЕ ФормаЗаблокирована Тогда
		ЗакрытьТекущуюФорму = Истина;
		Если ЗначениеЗаполнено(сткВозврата) Тогда
			Закрыть(сткВозврата);	
		Иначе
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуОбработчикОжидания() Экспорт
	ЗакрытьТекущуюФорму = Истина;
	Закрыть(сткРезультатПриЗакрытииФормы);
КонецПроцедуры

#КонецОбласти

#КонецОбласти     


