
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗакрытьТекущуюФорму	= Ложь;
	ФормаЗаблокирована 	= Ложь;
	Количество          = Параметры.Количество;
	ВидГСМ              = Параметры.ВидГСМ;
	ВидДвижения         = Параметры.ВидДвижения;
	ПриемникСливаГСМ    = Параметры.ПриемникСливаГСМ;
	ДатаЗаправкиСлива   = ?(ЗначениеЗаполнено(Параметры.ДатаЗаправкиСлива), Параметры.ДатаЗаправкиСлива, ТекущаяДатаСеанса());
	ГСМ                 = Параметры.ГСМ;
	Если НЕ ЗначениеЗаполнено(ВидДвижения) Тогда
		ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка;
	КонецЕсли;
	ДобавитьСтроку	= Параметры.ДобавитьСтроку;	
	Если НЕ ДобавитьСтроку Тогда
		Элементы.КнопкаДобавить.ТекстНевыбраннойКартинки  = Нстр("ru = 'Изменить'");
	КонецЕсли;
	ТекстПодсказки = Нстр("ru = 'Введите данные по движению топлива'");
	ВедетсяУчетГСМ	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетГСМ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбработатьПереключениеВидаДвижения();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = НЕ ЗакрытьТекущуюФорму;	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаВыходНажатие(Элемент)
	сткВозврата	= Новый Структура;
	сткВозврата.Вставить("ИмяФормыПерехода", "ФормаКонечныеДанныеПутевогоЛиста");
	ЗакрытьТекущуюФорму	= Истина;
	Закрыть(сткВозврата);
КонецПроцедуры

&НаКлиенте
Процедура КнопкаДобавитьНажатие(Элемент)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеФормы(Отказ);
	
	Если НЕ Отказ Тогда
		сткВозврата =Новый Структура;
		сткВозврата.Вставить("ДобавитьСтроку", 		ДобавитьСтроку);
		сткВозврата.Вставить("Заправка", 			Истина);
		сткВозврата.Вставить("Количество", 			Количество);
		сткВозврата.Вставить("ВидГСМ", 				ВидГСМ);
		сткВозврата.Вставить("ГСМ", 				ГСМ);
		сткВозврата.Вставить("ВидДвижения", 		ВидДвижения);
		сткВозврата.Вставить("ИмяФормыПерехода", 	"ФормаКонечныеДанныеПутевогоЛиста");
		сткВозврата.Вставить("ДатаЗаправкиСлива", 	ДатаЗаправкиСлива);
		сткВозврата.Вставить("ПриемникСливаГСМ", 	ПриемникСливаГСМ);
		ЗакрытьТекущуюФорму = Истина;
		Закрыть(сткВозврата);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидДвиженияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткПараметры	= Новый Структура();
	мсвВидовДвиженийТоплива =  Новый Массив;
	мсвВидовДвиженийТоплива.Добавить(ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка"));
	мсвВидовДвиженийТоплива.Добавить(ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Слив"));
	сткОтбор		= Новый Структура("Ссылка",мсвВидовДвиженийТоплива);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФормуПерехода("ФормаВыбораВидаДвиженияТоплива", сткПараметры, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуПерехода("ФормаВыбораВидаГСМ",, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПриемникСливаГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка 	= Ложь;
	Если ЗначениеЗаполнено(ВидГСМ) Тогда
		сткПараметры			= Новый Структура();
		сткПараметры.Вставить("ВидГСМ", ВидГСМ);
		ОткрытьФормуПерехода("ФормаВыбораПриемникаСливаТоплива", сткПараметры, Элемент);
	Иначе	
   		ТекстОшибки = Нстр("ru = 'Не заполнено поле ""Вид ГСМ"".'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуПерехода("ФормаВыбораГСМ",, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВидГСМПриИзменении(Элемент)
	ПриемникСливаГСМ = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
// Процедура завершения после закрытия формы выбора.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт

	сткРезультатПриЗакрытииФормы 	= Неопределено;
	ИмяФормыПерехода 				= Неопределено;	
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		сткРезультатПриЗакрытииФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		Если сткРезультатПриЗакрытииФормы.Свойство("ИмяФормыПерехода", ИмяФормыПерехода) Тогда
			
		КонецЕсли;
		
		Если сткРезультатПриЗакрытииФормы.Свойство("ВыбранВидДвижения") Тогда
			Если ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка") Тогда
				ПриемникСливаГСМ = Неопределено;	
			КонецЕсли; 
			ОбработатьПереключениеВидаДвижения();
		КонецЕсли;
	
		Если сткРезультатПриЗакрытииФормы.Свойство("ИдентификаторПриемникаСлива", ИдентификаторПриемникаСлива) Тогда
			сткПараметры			= Новый Структура();
			сткПараметры.Вставить("ВидГСМ", ВидГСМ);
			текОповещениеОЗакрытии 	= Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы",ЭтаФорма);
			Если ИдентификаторПриемникаСлива = "СкладГСМ" Тогда
				Элементы.ГруппаГСМСтраницы.ТекущаяСтраница = Элементы.ГруппаГСМ;
				ОткрытьФорму("Обработка.упРМВодителя.Форма.ФормаВыбораСклада",сткПараметры,Элементы.ПриемникСливаГСМ,,Окно,,текОповещениеОЗакрытии,);	
				Возврат;
			ИначеЕсли ИдентификаторПриемникаСлива = "ТранспортноеСредство"	Тогда	
				Элементы.ГруппаГСМСтраницы.ТекущаяСтраница = Элементы.ГруппаБезГСМ;
				ОткрытьФорму("Обработка.упРМВодителя.Форма.ФормаВыбораТранспортногоСредства",сткПараметры,Элементы.ПриемникСливаГСМ,,Окно,,текОповещениеОЗакрытии,);	
				Возврат;	
			ИначеЕсли ИдентификаторПриемникаСлива = "ДополнительноеОборудование"	Тогда	
				Элементы.ГруппаГСМСтраницы.ТекущаяСтраница = Элементы.ГруппаБезГСМ;
				ОткрытьФорму("Обработка.упРМВодителя.Форма.ФормаВыбораДополнительногоОборудования",сткПараметры,Элементы.ПриемникСливаГСМ,,Окно,,текОповещениеОЗакрытии,);	
				Возврат;	
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	мсвСоставИмениФормы = СтрРазделить(ИмяФормы, ".");
	ИмяТекущейФормы 	= мсвСоставИмениФормы[мсвСоставИмениФормы.ВГраница()];
	
	// При возвращении по стеку форм, анализируем имя текущей формы, если дошли до нужного.
	Если ИмяТекущейФормы = ИмяФормыПерехода Тогда
		Активизировать();
		ФормаЗаблокирована = Ложь;
		ТекущийЭлемент = Элементы.Количество;
	Иначе	
		ПодключитьОбработчикОжидания("ЗакрытьФормуОбработчикОжидания",0.1,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеФормы(Отказ)
		
	Если ВедетсяУчетГСМ И НЕ ЗначениеЗаполнено(ВидГСМ) Тогда
		ТекстОшибки = Нстр("ru = 'Не заполнено поле ""Вид ГСМ""'");
		Отказ = Истина;
    ИначеЕсли НЕ ЗначениеЗаполнено(ВидДвижения) Тогда
		ТекстОшибки = Нстр("ru = 'Не заполнено поле ""Вид движения""'");
		Отказ = Истина;
	ИначеЕсли ЗначениеЗаполнено(ПриемникСливаГСМ)
		И ТипЗнч(ПриемникСливаГСМ) = Тип("СправочникСсылка.упСклады") 
		И НЕ ЗначениеЗаполнено(ГСМ) Тогда	
		ТекстОшибки = Нстр("ru = 'Не заполнено поле ""ГСМ""'");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПереключениеВидаДвижения()
	Если ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка") Тогда
		Элементы.ГруппаЗаправки.ТекущаяСтраница = Элементы.ГруппаЗаправка;	
	Иначе	
		Элементы.ГруппаЗаправки.ТекущаяСтраница = Элементы.ГруппаСлив;
	КонецЕсли;
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ОткрытьФормуПерехода(ИмяОткрываемойФормы, сткПараметры = Неопределено, ВладелецФормы = Неопределено)
	
	Если НЕ ФормаЗаблокирована Тогда
		ФормаЗаблокирована 	= Истина;
		Если сткПараметры = Неопределено Тогда
			сткПараметры				= Новый Структура();
			//сткПараметры.Вставить("Объект", Объект);	
		КонецЕсли;
		ОповещениеОЗакрытии 		= Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы",ЭтаФорма);
		ПолноеИмяОткрываемойФормы 	= СтрЗаменить("Обработка.упРМВодителя.Форма.%1", "%1", ИмяОткрываемойФормы);
		Если ВладелецФормы = Неопределено Тогда
			ВладелецФормы = ЭтаФорма;	
		КонецЕсли;
		ОткрытьФорму(ПолноеИмяОткрываемойФормы, сткПараметры, ВладелецФормы,,Окно,,ОповещениеОЗакрытии);			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуОбработчикОжидания() Экспорт
	ЗакрытьТекущуюФорму = Истина;
	Закрыть(сткРезультатПриЗакрытииФормы);
КонецПроцедуры

#КонецОбласти


#КонецОбласти
