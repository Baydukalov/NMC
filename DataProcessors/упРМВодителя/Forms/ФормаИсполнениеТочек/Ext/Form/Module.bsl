
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	текОбъект	= РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(текОбъект, Параметры.Объект,,текОбъект.ПолучитьСтрокуНеПерезаполняемыхРеквизитов());
	Отказ = Ложь;
	текОбъект.ЗаполнитьДеревоРабот(Ложь, ТекстОшибки, Отказ);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
 	УстановитьДоступностьКнопокВверхВниз();
	Элементы.дрвРаботыПоТочке.ТолькоПросмотр = ТолькоПросмотр;
	ЗакрытьТекущуюФорму	= Ложь;
	ФормаЗаблокирована 	= Ложь;
	ЦветОшибки    = ЦветаСтиля.упЦветТекстаОшибки;
	ЦветПодсказки = ЦветаСтиля.упЦветПодсказки;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТолькоПросмотр = Ложь;
	РазвернутьУзлыДерева();
	АктивизацияПослеНажатияКнопки = Истина;
	ТекущийЭлемент=Элементы.ОбъектНачалоВыполнения;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = НЕ ЗакрытьТекущуюФорму;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаТолькоПросмотрНажатие(Элемент)
	УстановитьТолькоПросмотрДляДерева();	
	АктивизацияПослеНажатияКнопки = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыделитьВсеНажатие(Элемент)
	УстановитьТолькоПросмотрДляДерева();	
	ВыделитьВсе();	
	РазвернутьУзлыДерева();
	УстановитьТолькоПросмотрДляДерева();	
	АктивизацияПослеНажатияКнопки = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВнизНажатие(Элемент)
	СдвинутьсяНаОднуТочкуВниз();
	РазвернутьУзлыДерева();
	АктивизацияПослеНажатияКнопки = Истина;
	Элементы.дрвРаботыПоТочке.ТекущийЭлемент = Элементы.дрвРаботыПоТочкеНазначениеСтроки;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВверхНажатие(Элемент)
	СдвинутьсяНаОднуТочкуВверх();
	РазвернутьУзлыДерева();	
	АктивизацияПослеНажатияКнопки = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыполнитьРейсНажатие(Элемент)
	УстановитьТолькоПросмотрДляДерева();

	// Данные дерева сохраняются в документы "Прохождение точек"
	ЕстьНеПройденныеТочки = Истина;
	СохранитьИсполнениеРейсаНаСервере(Неопределено,,ЕстьНеПройденныеТочки);
	
	Если НЕ ЕстьНеПройденныеТочки И НЕ ФормаЗаблокирована Тогда
		ЗакрытьФорму();	
	КонецЕсли;
	
	// Дерево разворачивается
	РазвернутьУзлыДерева();
	УстановитьТолькоПросмотрДляДерева();
	АктивизацияПослеНажатияКнопки = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыходНажатие(Элемент)
	ЗакрытьФорму(Новый Структура("ИмяФормыПерехода", "ФормаАвторизации"));
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОчиститьПроблемуНажатие(Элемент)
	Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОчиститьАдресНажатие(Элемент)
	АдресРазгрузки	= ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОтменитьТочкуНажатие(Элемент)
	Если Объект.АдресаПогрузкиПоРейсу.Количество() = 0 Тогда
		ЗагрузитьАдресаПогрузкиПоРейсу();
	КонецЕсли;
	упПрохождениеТочкиКлиентСервер.ОтменитьТочку(Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы(), ПолучитьТекущуюТочку(), Проблема, Объект.АдресаПогрузкиПоРейсу, Объект.НомерТекущейТочки);
	ОтменитьТочкуНаСервере();
	РазвернутьУзлыДерева();	
	АктивизацияПослеНажатияКнопки = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПроблемаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Проблема) Тогда
		ТипПроблемы = ПолучитьТипПроблемы(Проблема);
	Иначе
		ТипПроблемы = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПустаяСсылка");	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроблемаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткПараметры	= Новый Структура();
	ТекущиеДанные = Элементы.дрвРаботыПоТочке.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТипОперации = ТекущиеДанные.Операция;
		Если ЗначениеЗаполнено(ТипОперации) Тогда
			сткОтбор = Новый Структура("ТипОперации", ТипОперации);
			сткПараметры.Вставить("Отбор", сткОтбор);
		КонецЕсли;
	КонецЕсли;
	ОткрытьФормуПерехода("ФормаВыбораПроблемы", сткПараметры, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АдресРазгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткПараметры	= Новый Структура();
	мсвАдреса		= ПолучитьАдресаПоМаршруту();
	сткПараметры.Вставить("Адреса", мсвАдреса);
	ОткрытьФормуПерехода("ФормаВыбораАдреса", сткПараметры, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТочкеПриАктивизацииЯчейки(Элемент)
	Если НЕ ТолькоПросмотр Тогда
		Если НЕ АктивизацияПослеНажатияКнопки Тогда
			СохранитьДанныеПередРедактированием();
			РедактироватьПоле = Ложь;
			ОработатьПоле(Элемент.ТекущийЭлемент.Имя, РедактироватьПоле);	
			Если НЕ РедактироватьПоле Тогда
				Элементы.дрвРаботыПоТочке.ТекущийЭлемент = Элементы.дрвРаботыПоТочкеНазначениеСтроки;		
			КонецЕсли; 
		Иначе
			АктивизацияПослеНажатияКнопки = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТочкеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ ТолькоПросмотр Тогда
		Если Поле.Имя = "дрвРаботыПоТочкеВыполнена" Тогда
			ОработатьПоле(Поле.Имя);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТочкеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ИзменениеПроблемыАдресаРазгрузки();

КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТочкеПроблемаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	текОперация = Элементы.дрвРаботыПоТочке.ТекущиеДанные.Операция;
	СтандартнаяОбработка = Ложь;
	сткПараметры = Новый Структура("Отбор", Новый Структура("ТипОперации", текОперация));
	ОткрытьФормуПерехода("ФормаВыбораПроблемы", сткПараметры, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТочкеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.Проблема = ВыбранноеЗначение;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТочкеПередНачаломИзменения(Элемент, Отказ)
	СохранитьДанныеПередРедактированием();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНачалоВыполненияПриИзменении(Элемент)
	УстановитьДанныеТочке("ПрибытиеФакт", Объект.НачалоВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНачалоРаботПриИзменении(Элемент)
	УстановитьДанныеТочке("НачалоРаботФакт", Объект.НачалоРабот);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектОкончаниеВыполненияПриИзменении(Элемент)
	УстановитьДанныеТочке("УбытиеФакт", Объект.ОкончаниеВыполнения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ВыделитьВсе()
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ВыделитьВсе();
    ЗначениеВРеквизитФормы(текОбъект, "Объект");
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТипПроблемы(Проблема)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проблема, "ТипОперации");
КонецФункции

&НаКлиенте
Процедура ОработатьПоле(ИмяПоля, РедактироватьПоле = Ложь)

	ТекущиеДанные = Элементы.дрвРаботыПоТочке.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено  Тогда
		Если НЕ ТекущиеДанные.Отменена И НЕ ТекущиеДанные.ТочкаПройдена И ИмяПоля = "дрвРаботыПоТочкеВыполнена" Тогда
			ТекстОшибки 	= "";
			
			Если ТекущиеДанные.Выполнена = 2 Тогда
				ТекущиеДанные.Выполнена = 0;
			ИначеЕсли ТекущиеДанные.Выполнена = 0 Тогда	
				ТекущиеДанные.Выполнена = 1;
			ИначеЕсли ТекущиеДанные.Выполнена = 1 Тогда	
				ТекущиеДанные.Выполнена = 0;
			КонецЕсли;
			
			Если ТекущиеДанные.Отменена Тогда
				ТекущиеДанные.ИндексКартинкиВыполнено = 3;
			Иначе	
				ТекущиеДанные.ИндексКартинкиВыполнено = ТекущиеДанные.Выполнена;
			КонецЕсли;
				
			упПрохождениеТочкиКлиентСервер.УстановитьФлаги(ТекущиеДанные);
			
		ИначеЕсли ТекущиеДанные.ЭтоРабота 
			И ИмяПоля = "дрвРаботыПоТочкеПроблема"  Тогда	
			ТекстОшибки 	= "";
			Если ЗначениеЗаполнено(ТекущиеДанные.Проблема) Тогда
				ТекущиеДанные.Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
				ИзменениеПроблемыАдресаРазгрузки();
			Иначе
				Если ТипПроблемы =  ТекущиеДанные.Операция ИЛИ НЕ ЗначениеЗаполнено(ТипПроблемы) Тогда
					ТекущиеДанные.Проблема = Проблема;	
					ИзменениеПроблемыАдресаРазгрузки();
				Иначе
					ТекстОшибки = Нстр("ru = 'Проблему с типом ""%1"" нельзя устанавливать операции с типом ""%2""'");
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ТипПроблемы, ТекущиеДанные.Операция );
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТекущиеДанные.ЭтоРабота 
			И упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(ТекущиеДанные.Операция) 
			И ИмяПоля = "дрвРаботыПоТочкеАдресРазгрузки" Тогда	
			ТекстОшибки 	= "";
			Если ЗначениеЗаполнено(ТекущиеДанные.АдресРазгрузки) Тогда
				ТекущиеДанные.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
			Иначе	
				ТекущиеДанные.АдресРазгрузки = АдресРазгрузки;	
			КонецЕсли;
			ИзменениеПроблемыАдресаРазгрузки();
		ИначеЕсли ТекущиеДанные.ЭтоРабота 
			И ТекущиеДанные.Операция = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ВыемкаЦенностей") 
			И ИмяПоля = "дрвРаботыПоТочкеСумма" Тогда	
        	РедактироватьПоле = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СдвинутьсяНаОднуТочкуВниз()
	текОбъект = РеквизитФормыВЗначение("Объект");
	Отказ = Ложь;
	текОбъект.ЗаполнитьДеревоПоТочке(1, Ложь);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	УстановитьДоступностьКнопокВверхВниз();
КонецПроцедуры

&НаСервере
Процедура СдвинутьсяНаОднуТочкуВверх()
	текОбъект = РеквизитФормыВЗначение("Объект");
	Отказ = Ложь;
	текОбъект.ЗаполнитьДеревоПоТочке(-1, Ложь);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	УстановитьДоступностьКнопокВверхВниз();
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКнопокВверхВниз()
	Элементы.КнопкаВверх.Доступность 	= НЕ Объект.ЭтоПерваяТочка;
	Элементы.КнопкаВниз.Доступность 	= НЕ Объект.ЭтоПоследняяТочка;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТолькоПросмотрДляДерева()
	ТолькоПросмотр = НЕ ТолькоПросмотр;
	Элементы.дрвРаботыПоТочке.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.КнопкаТолькоПросмотр.ТекстНевыбраннойКартинки = ?(ТолькоПросмотр, "Редактирование", "Просмотр");
	ТекущийЭлемент = Элементы.Проблема;
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьУзлыДерева()
	Строки = Объект.дрвРаботыПоТочке.ПолучитьЭлементы();
	Для каждого Строка Из Строки Цикл
		ИдентификаторСтроки = Строки.Получить(Строки.Индекс(Строка)).ПолучитьИдентификатор();
		Элементы.дрвРаботыПоТочке.Развернуть(ИдентификаторСтроки, Истина);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанныеПередРедактированием()
	ТекущиеДанные = Элементы.дрвРаботыПоТочке.ТекущиеДанные;
	сткДанныеСтрокиРаботаДоРедактирования = Неопределено;
	Если НЕ ТекущиеДанные = Неопределено 
		И ТекущиеДанные.ЭтоРабота Тогда
		сткДанныеСтрокиРаботаДоРедактирования = Новый Структура();
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("Проблема", 			ТекущиеДанные.Проблема);
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("Отменена", 			ТекущиеДанные.Отменена);
		сткДанныеСтрокиРаботаДоРедактирования.Вставить("АдресРазгрузки", 	ТекущиеДанные.АдресРазгрузки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеПроблемыАдресаРазгрузки()
		
	ТекущиеДанные 	= Элементы.дрвРаботыПоТочке.ТекущиеДанные;
	СтарыйАдрес 	= Неопределено;
	СтараяПроблема = Неопределено;
	
	ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута = Ложь;
	
	Если Истина
		И ЗначениеЗаполнено(ТекущиеДанные.Проблема)
		И упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(ТекущиеДанные.Операция)
	Тогда
		ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Проблема, "ЗапрещеноСозданиеНовойТочкиМаршрута");
		Если ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
			ТекущиеДанные.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(ТекущиеДанные.АдресРазгрузки) Тогда
		текТочка = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя().ПолучитьРодителя();
			
		Если Истина
			И ТекущиеДанные.Проблема <> ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение")
			И текТочка.АдресТочки = ТекущиеДанные.АдресРазгрузки
		Тогда
			ТекущиеДанные.АдресРазгрузки  = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
			ТекстСообщения = Нстр("ru = 'Адрес разгрузки не должен совпадать с адресом текущей точки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;

	Если НЕ сткДанныеСтрокиРаботаДоРедактирования  = Неопределено Тогда
		СтарыйАдрес 	= сткДанныеСтрокиРаботаДоРедактирования.АдресРазгрузки;
		СтараяПроблема 	= сткДанныеСтрокиРаботаДоРедактирования.Проблема
	КонецЕсли;
	
	КоллекцияТочекМаршрута = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	
	Если Объект.АдресаПогрузкиПоРейсу.Количество() = 0 Тогда
		ЗагрузитьАдресаПогрузкиПоРейсу();
	КонецЕсли;
	
	сткВозврата = Неопределено;
	упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(ТекущиеДанные, КоллекцияТочекМаршрута,,СтараяПроблема, СтарыйАдрес, сткВозврата, ТекущиеДанные.ПорядокТочки, Объект.АдресаПогрузкиПоРейсу);
	
	
	Если Истина
		И сткВозврата <> Неопределено
		И сткВозврата.Свойство("Задание")
	Тогда
		ВосстановитьТочкуРазгрузки(сткВозврата);	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВосстановитьТочкуРазгрузки(сткВозврата)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ВосстановитьТочкуРазгрузки(сткВозврата);
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсу, 	"Объект.дрвРаботыПоТекущемуРейсу");
КонецПроцедуры

&НаСервере
Процедура СохранитьИсполнениеРейсаНаСервере(мсвИндексовИзмененныхТочек, ПерезаполнятьДеревоТочекРейса = Истина, ЕстьНеПройденныеТочки)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	мсвНеправильноЗаполненныхСтрок = Новый Массив;
	ОбработкаОбъект.ПрименитьИзмененияВДеревеТочекРейса(мсвИндексовИзмененныхТочек, ПерезаполнятьДеревоТочекРейса, мсвНеправильноЗаполненныхСтрок, ЕстьНеПройденныеТочки);
	Если мсвНеправильноЗаполненныхСтрок.Количество() > 0  Тогда
		ТекстОшибки = мсвНеправильноЗаполненныхСтрок[0].ТекстСообщения;
	КонецЕсли;
	ЗначениеВРеквизитФормы(ОбработкаОбъект,	"Объект");
	УстановитьДоступностьКнопокВверхВниз();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтроку(мсвИндексов, ЭлементыДерева, ТекущийИндексМассива)
	
	ЭлементДерева = ЭлементыДерева.Получить(мсвИндексов[ТекущийИндексМассива]);
	Если ТекущийИндексМассива = 0 Тогда
		Элементы.дрвРаботыПоТочке.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
	Иначе
		УстановитьТекущуюСтроку(мсвИндексов, ЭлементДерева.ПолучитьЭлементы(), ТекущийИндексМассива - 1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИндексТекущейСтроки(мсвИндексов, ЭлементДерева)
	
	Если ЭлементДерева = Неопределено Тогда
		Родитель = ЭлементДерева.ПолучитьРодителя();
		
		ЭтоКорневойЭлемент = Ложь;
		
		Если Родитель = Неопределено Тогда
			Родитель = Объект.дрвРаботыПоТочке;
			ЭтоКорневойЭлемент = Истина;
		КонецЕсли;	
		
		текЭлементы = Родитель.ПолучитьЭлементы();
		
		Индекс = текЭлементы.Индекс(ЭлементДерева);
		
		мсвИндексов.Добавить(Индекс);
		
		Если НЕ ЭтоКорневойЭлемент Тогда
			ПолучитьИндексТекущейСтроки(мсвИндексов, Родитель);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт

	сткРезультатПриЗакрытииФормы 	= Неопределено;
	ИмяФормыПерехода 				= Неопределено;	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		сткРезультатПриЗакрытииФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		Если сткРезультатПриЗакрытииФормы.Свойство("ИмяФормыПерехода", ИмяФормыПерехода) Тогда
			
		КонецЕсли;
	КонецЕсли;
	
	мсвСоставИмениФормы = СтрРазделить(ИмяФормы, ".");
	ИмяТекущейФормы 	= мсвСоставИмениФормы[мсвСоставИмениФормы.ВГраница()];
	
	// При возвращении по стеку форм, анализируем имя текущей формы, если дошли до нужного.
	Если ИмяТекущейФормы = ИмяФормыПерехода Тогда
		Активизировать();
		ФормаЗаблокирована = Ложь;
	Иначе	
		ПодключитьОбработчикОжидания("ЗакрытьФормуОбработчикОжидания",0.1,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДанныеТочке(ИмяПоля, Значение)
	
	КоллекцияТочек = Объект.дрвРаботыПоТочке.ПолучитьЭлементы();
	Если КоллекцияТочек.Количество()>0 Тогда
		КоллекцияТочек[0][ИмяПоля] = Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресаПоМаршруту()
	
	текРезультат = Новый Массив;
	
	Для каждого ЭлементТочка Из Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы() Цикл
		текРезультат.Добавить(ЭлементТочка.АдресТочки);
	КонецЦикла;
		
	Возврат текРезультат;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьАдресаПогрузкиПоРейсу()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьАдресаПогрузкиПоРейсу();
	ЗначениеВРеквизитФормы(ОбработкаОбъект.АдресаПогрузкиПоРейсу, 	"Объект.АдресаПогрузкиПоРейсу");
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТекущуюТочку()
	
	текРезультат = Неопределено;
	
	ТекущиеДанные = Элементы.дрвРаботыПоТочке.ТекущиеДанные;
	
	Если ТипЗнч(ТекущиеДанные.НазначениеСтроки) = Тип("СправочникСсылка.упАдреса") Тогда
		текРезультат = ТекущиеДанные;
	ИначеЕсли ТипЗнч(ТекущиеДанные.НазначениеСтроки) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		текРезультат = ТекущиеДанные.ПолучитьРодителя();	 
	ИначеЕсли ТипЗнч(ТекущиеДанные.НазначениеСтроки) = Тип("ПеречислениеСсылка.упТипыОпераций") Тогда	
		текРезультат = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя();	 	
	Иначе
		текРезультат = ТекущиеДанные.ПолучитьРодителя().ПолучитьРодителя().ПолучитьРодителя();	 		
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

&НаСервере
Процедура ОтменитьТочкуНаСервере()
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ЗаполнитьДеревоПоТочке(0, Ложь);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	УстановитьДоступностьКнопокВверхВниз();
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ОткрытьФормуПерехода(ИмяОткрываемойФормы, сткПараметры = Неопределено, ВладелецФормы = Неопределено)
	
	Если НЕ ФормаЗаблокирована Тогда
		ФормаЗаблокирована 	= Истина;
		Если сткПараметры = Неопределено Тогда
			сткПараметры				= Новый Структура();
			сткПараметры.Вставить("Объект", Объект);	
		КонецЕсли;
		ОповещениеОЗакрытии 		= Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы",ЭтаФорма);
		ПолноеИмяОткрываемойФормы 	= СтрЗаменить("Обработка.упРМВодителя.Форма.%1", "%1", ИмяОткрываемойФормы);
		Если ВладелецФормы = Неопределено Тогда
			ВладелецФормы = ЭтаФорма;	
		КонецЕсли;
		ОткрытьФорму(ПолноеИмяОткрываемойФормы, сткПараметры, ВладелецФормы,,Окно,,ОповещениеОЗакрытии);			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(сткВозврата = "")
	Если НЕ ФормаЗаблокирована Тогда
		ЗакрытьТекущуюФорму = Истина;
		Если ЗначениеЗаполнено(сткВозврата) Тогда
			Закрыть(сткВозврата);	
		Иначе
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуОбработчикОжидания() Экспорт
	ЗакрытьТекущуюФорму = Истина;
	Закрыть(сткРезультатПриЗакрытииФормы);
КонецПроцедуры

#КонецОбласти


#КонецОбласти


