#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстПодсказки 	= Нстр("ru = 'Введите логин и пароль, отсканируйте штрихкод или поднесите карту к считывателю'");
	ТекущийЭлемент 	= Элементы.КнопкаВойти;
	ФормаЗаблокирована 	= Ложь;
	
	РольДоступнаупРазработчик = РольДоступна("упРазработчик");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	// ПодключаемоеОборудование
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект); 
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;
	// Конец ПодключаемоеОборудование
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода" );
	ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт" );
	ОповещенияПриПодключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект); 
   	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец ПодключаемоеОборудование
	
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Истина
		И Источник = "ПодключаемоеОборудование"
		И ВводДоступен() 
	Тогда
		
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[ 1 ] = Неопределено Тогда
				ТекКод = Параметр[ 0 ];
			Иначе
				ТекКод = Параметр[ 1 ][ 1 ];
			КонецЕсли;
			ОбработатьПолученныйШК(ТекКод);
		КонецЕсли;
		
		Если ИмяСобытия  = "TracksData" Тогда
			ТекКод  = Параметр [ 0 ];
			ОбработатьПолученныйМК(ТекКод );
		КонецЕсли;
		
	КонецЕсли;
	// Конец ПодключаемоеОборудование
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаВойтиНажатие(Элемент)
	АвторизоватьсяПоЛогинуПаролюНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ШтрихкодПриИзменении(Элемент)
	ОбработатьПолученныйШК(Штрихкод);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
// Процедура завершения после закрытия формы работы с рейсом.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт
	
	ИнициализацияДанных();	
	Активизировать();
	ФормаЗаблокирована 	= Ложь;

КонецПроцедуры

&НаКлиенте
// Процедура - Подключить оборудование завершение
//
// Параметры:
//  РезультатВыполнения	 - 	 - 
//  Параметры			 - 	 - 
//
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
 
   Если Не РезультатВыполнения.Результат Тогда
      ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
      ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
	 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	 
   Иначе
      ТекстСообщения = НСтр("ru = 'Оборудование подключено.'" );
      ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
   КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура - Отключить оборудование завершение
//
// Параметры:
//  РезультатВыполнения	 - 	 - 
//  Параметры			 - 	 - 
//
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт

   Если Не РезультатВыполнения.Результат Тогда
      ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
      ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
      ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
   Иначе
      ТекстСообщения = НСтр("ru = 'Оборудование отключено.'" );
      ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
   КонецЕсли;

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура АвторизоватьсяПоЛогинуПаролю(Отказ)

	ТекстОшибки = "";
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.Авторизация(Новый Структура("Логин, Пароль",Логин, Пароль), ТекстОшибки, Отказ);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныйШК(Штрихкод)
	Если ЗначениеЗаполнено(Штрихкод) Тогда
		Отказ = Ложь;
		АвторизоватьсяПоШтрихкоду(Штрихкод, Отказ);	
		Если НЕ Отказ Тогда
			ОткрытьФормуРаботыСРейсом();
		КонецЕсли;
		Штрихкод = "";
	КонецЕсли;		
КонецПроцедуры

&НаСервере
Процедура АвторизоватьсяПоШтрихкоду(Штрихкод, Отказ)

	ТекстОшибки = "";
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.Авторизация(Новый Структура("Штрихкод", Штрихкод), ТекстОшибки, Отказ);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныйМК(КодМагнитнойКарты)
	Если ЗначениеЗаполнено(КодМагнитнойКарты) Тогда
		Отказ = Ложь;
		АвторизоватьсяПоКодуМагнитнойКарты(КодМагнитнойКарты, Отказ);	
		Если НЕ Отказ Тогда
			ОткрытьФормуРаботыСРейсом();
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

&НаСервере
Процедура АвторизоватьсяПоКодуМагнитнойКарты(КодМагнитнойКарты, Отказ)

	ТекстОшибки = "";
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.Авторизация(Новый Структура("КодМагнитнойКарты", КодМагнитнойКарты), ТекстОшибки, Отказ);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализацияДанных()
	Логин 	= "";
	Пароль	= "";
	Если РольДоступнаупРазработчик Тогда
		Штрихкод = "";	
	КонецЕсли;
	ИнициализацияДанныхНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИнициализацияДанныхНаСервере()
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.Инициализация();
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРаботыСРейсом()
	текТекстОшибки 		= "";
	ЗаполнитьРейсПоСотруднику(текТекстОшибки);
	Если ЗначениеЗаполнено(Объект.Рейс) Тогда
		Если Объект.ЭтоРейсАктивный Тогда
			ОткрытьФормуПерехода("ФормаРейсАктивный",, ЭтаФорма); 
		ИначеЕсли Объект.ЭтоРейсКВыполнению Тогда 	
			ОткрытьФормуПерехода("ФормаРейсКВыполнению",, ЭтаФорма); 
		КонецЕсли;	
	Иначе
		сткПараметры	= Новый Структура();
		сткПараметры.Вставить("ТекстОшибки",текТекстОшибки);
		ОткрытьФормуПерехода("ФормаОшибка", сткПараметры, ЭтаФорма); 
	КонецЕсли;
	Если РольДоступнаупРазработчик Тогда
		Штрихкод		= "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРейсПоСотруднику(ТекстОшибки = "") 
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ЗаполнитьРейсПоСотруднику(ТекстОшибки);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура АвторизоватьсяПоЛогинуПаролюНаКлиенте()
	Отказ = Ложь;
	АвторизоватьсяПоЛогинуПаролю(Отказ);	
	Если НЕ Отказ Тогда
		Логин 	= "";
		Пароль	= "";
		ОткрытьФормуРаботыСРейсом();
	КонецЕсли;
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ОткрытьФормуПерехода(ИмяОткрываемойФормы, сткПараметры = Неопределено, ВладелецФормы = Неопределено)
	
	Если НЕ ФормаЗаблокирована Тогда
		ФормаЗаблокирована 	= Истина;
		Если сткПараметры = Неопределено Тогда
			сткПараметры = Новый Структура();
			сткПараметры.Вставить("Объект", Объект);	
		КонецЕсли;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы",ЭтаФорма);
		ПолноеИмяОткрываемойФормы = СтрЗаменить("Обработка.упРМВодителя.Форма.%1", "%1", ИмяОткрываемойФормы);
		Если ВладелецФормы = Неопределено Тогда
			ВладелецФормы = ЭтаФорма;	
		КонецЕсли;
		ОткрытьФорму(ПолноеИмяОткрываемойФормы, сткПараметры, ВладелецФормы,,Окно,,ОповещениеОЗакрытии);			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти
