#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	текОбъект	= РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(текОбъект, Параметры.Объект,,текОбъект.ПолучитьСтрокуНеПерезаполняемыхРеквизитов());
	текОбъект.ЗаполнитьГСМ();
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	ТекстДополнительногоСообщения 	= "" + Объект.ПутевойЛист;
	ТекстДополнительногоСообщения1 	= "" + Объект.ТранспортноеСредство;
	ТекстПодсказки = Нстр("ru = 'Введите конечные остатки по путевому листу'");
	
	ЭтоОстатки = Истина;
	ЗакрытьТекущуюФорму	= Ложь;
	ФормаЗаблокирована 	= Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = НЕ ЗакрытьТекущуюФорму;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КнопкаПодтвердитьНажатие(Элемент)
	Отказ = Ложь;
	ПодтвердитьПутевойЛист(Отказ);
	Если НЕ Отказ Тогда
		Если ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками Тогда
			текОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы", ЭтотОбъект);
			сткПараметры	= Новый Структура();
			сткПараметры.Вставить("Объект", Объект);
			ОткрытьФорму("Обработка.упРМВодителя.Форма.ФормаИсполнениеТочек", сткПараметры,ЭтотОбъект,,Окно,,текОповещениеОЗакрытии);
		Иначе	
			ЗакрытьТекущуюФорму = Истина;
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаДобавитьНажатие(Элемент)
	
	текОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы", ЭтотОбъект);
	сткПараметры	= Новый Структура();
	сткПараметры.Вставить("ДобавитьСтроку", Истина);
	ОткрытьФорму("Обработка.упРМВодителя.Форма.ФормаВводаЗаправки", сткПараметры, ЭтотОбъект,, Окно,, текОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаУдалитьНажатие(Элемент)
	ТекущаяСтрока = Элементы.Заправки.ТекущаяСтрока;
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		Объект.Заправки.Удалить(Объект.Заправки.НайтиПоИдентификатору(ТекущаяСтрока));	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаРежимыНажатие(Элемент)
	ЭтоОстатки = НЕ ЭтоОстатки;
	Если ЭтоОстатки Тогда
		Элементы.ГруппаГСМ.ТекущаяСтраница = Элементы.ГруппаОстатки;	
		Элементы.КнопкаРежимы.ТекстНевыбраннойКартинки  = Нстр("ru = 'Заправки'");
	Иначе
		Элементы.КнопкаРежимы.ТекстНевыбраннойКартинки  = Нстр("ru = 'Остатки'");
		Элементы.ГруппаГСМ.ТекущаяСтраница = Элементы.ГруппаЗаправки;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаИзменитьНажатие(Элемент)
	ТекущиеДанные = Элементы.Заправки.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		текОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормы", ЭтотОбъект);
		сткПараметры	= Новый Структура();
		сткПараметры.Вставить("Количество", 		ТекущиеДанные.Количество);
		сткПараметры.Вставить("ВидГСМ", 			ТекущиеДанные.ВидГСМ);
		сткПараметры.Вставить("ВидДвижения", 		ТекущиеДанные.ВидДвижения);
		сткПараметры.Вставить("ДопОборудование", 	ТекущиеДанные.ДопОборудование);
		сткПараметры.Вставить("ДобавитьСтроку", 	Ложь);
		сткПараметры.Вставить("ДатаЗаправкиСлива", 	ТекущиеДанные.ДатаЗаправкиСлива);
		сткПараметры.Вставить("ПриемникСливаГСМ", 	ТекущиеДанные.ПриемникСливаГСМ);
		сткПараметры.Вставить("ГСМ", 				ТекущиеДанные.ГСМ);

		ОткрытьФорму("Обработка.упРМВодителя.Форма.ФормаВводаЗаправки", сткПараметры, ЭтотОбъект,, Окно,, текОповещениеОЗакрытии);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ПодтвердитьПутевойЛист(Отказ)
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ЗаполнитьКонечныеОстатки(ТекстОшибки, Отказ);
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы исполнения точек.
//
Процедура ОбработатьЗакрытиеФормы(Результат, ДополнительныеПараметры) Экспорт

	ИмяФормыПерехода 	= Неопределено;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		сткРезультатПриЗакрытииФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		
		Если сткРезультатПриЗакрытииФормы.Свойство("ИмяФормыПерехода", ИмяФормыПерехода) Тогда
			
		КонецЕсли;
		
		Если сткРезультатПриЗакрытииФормы.Свойство("Заправка") Тогда
			
			СтрокаЗаправки = Неопределено;
			Если сткРезультатПриЗакрытииФормы.ДобавитьСтроку Тогда
				СтрокаЗаправки = Объект.Заправки.Добавить();
			Иначе	
				СтрокаЗаправки = Элементы.Заправки.ТекущиеДанные;	
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтрокаЗаправки, сткРезультатПриЗакрытииФормы);
			
		КонецЕсли;
	КонецЕсли;
	
	мсвСоставИмениФормы = СтрРазделить(ИмяФормы, ".");
	ИмяТекущейФормы 	= мсвСоставИмениФормы[мсвСоставИмениФормы.ВГраница()];
	
	// При возвращении по стеку форм, анализируем имя текущей формы, если дошли до нужного.
	Если ИмяТекущейФормы = ИмяФормыПерехода Тогда
		Активизировать();
		ФормаЗаблокирована = Ложь;
	Иначе	
		ПодключитьОбработчикОжидания("ЗакрытьФормуОбработчикОжидания",0.1,Истина);
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОчиститьКонечныйПробегНажатие(Элемент)
	Объект.ПробегОкончание = 0;
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОчиститьОстаткиНажатие(Элемент)
	
	Для каждого СтрокаГСМ Из Объект.ГСМ Цикл
		
		СтрокаГСМ.КонОстаток = 0;
		
 	КонецЦикла;
	
КонецПроцедуры

#Область ПереходыМеждуФормами

&НаКлиенте
Процедура ЗакрытьФормуОбработчикОжидания() Экспорт
	ЗакрытьТекущуюФорму = Истина;
	Закрыть(сткРезультатПриЗакрытииФормы);
КонецПроцедуры

#КонецОбласти


#КонецОбласти
