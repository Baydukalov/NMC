#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		Объект.Период.Вариант = ВариантСтандартногоПериода.ЭтаНеделя;
	КонецЕсли; 
	
	Объект.Партнеры.ЗагрузитьЗначения(СписокДоступныхПартнеров(ПользователиКлиентСервер.ТекущийПользователь()));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Реализовано не на динамическом списке, потому что у пользователя работающего с РМ
	// урезан набор ролей, списки заполняются в привилегированном режиме.
	ПодключитьОбработчикОжидания("ОбновитьСписокРейсовНаКлиенте",0.1,Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокРейсовПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработатьВыборСтрокиСпискаРейсов", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбновитьСписокРейсовНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПоТекущемуРейсуПриАктивизацииСтроки(Элемент)
	Если Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные <> Неопределено Тогда
		Элементы.дрвРаботыПоТекущемуРейсуТоварныйСостав.Доступность = Ложь
		                                                              Или Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.ЭтоРабота
														  Или ЗначениеЗаполнено(Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные.ПрохождениеТочки);
	Иначе
		Элементы.дрвРаботыПоТекущемуРейсуТоварныйСостав.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ПрисоединенныеФайлы(Команда)
	
	ТекущиеДанные = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда

		Если ТекущиеДанные.ЕстьФайлы Тогда
			ВладелецФайла = Неопределено;
			Если ТекущиеДанные.ЭтоАдрес Тогда
				Если ЗначениеЗаполнено(ТекущиеДанные.ПрохождениеТочки) Тогда
					ВладелецФайла = ТекущиеДанные.ПрохождениеТочки;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(ТекущиеДанные.ЗаданиеЗаявкаНаПеревозкуГруза) Тогда
					ВладелецФайла = ТекущиеДанные.ЗаданиеЗаявкаНаПеревозкуГруза;
				КонецЕсли;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ВладелецФайла) Тогда
			
				сткПараметры = Новый Структура;
				сткПараметры.Вставить("ВладелецФайла",  ВладелецФайла);
				сткПараметры.Вставить("ТолькоПросмотр", Истина);
				
				ОткрытьФорму("ОбщаяФорма.ПрисоединенныеФайлы",сткПараметры, ЭтаФорма, УникальныйИдентификатор,);

				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьСписокРейсовНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДерево(Команда)
	
	ЭлементыДерева = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыДерева Цикл
		ЭлементыЗадания = текТочка.ПолучитьЭлементы();
		Для каждого текЗадание Из ЭлементыЗадания Цикл
			ЭлементыОперации = текЗадание.ПолучитьЭлементы();
			Для каждого текОперация Из ЭлементыОперации Цикл
				Элементы.дрвРаботыПоТекущемуРейсу.Свернуть(текОперация.ПолучитьИдентификатор());
			КонецЦикла;
			Элементы.дрвРаботыПоТекущемуРейсу.Свернуть(текЗадание.ПолучитьИдентификатор());
		КонецЦикла;
		Элементы.дрвРаботыПоТекущемуРейсу.Свернуть(текТочка.ПолучитьИдентификатор());
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево(Команда)
	
	ЭлементыДерева = Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыДерева Цикл
		Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(текТочка.ПолучитьИдентификатор());
		ЭлементыЗадания = текТочка.ПолучитьЭлементы();
		Для каждого текЗадание Из ЭлементыЗадания Цикл
			Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(текЗадание.ПолучитьИдентификатор());
			ЭлементыОперации = текЗадание.ПолучитьЭлементы();
			Для каждого текОперация Из ЭлементыОперации Цикл
				Элементы.дрвРаботыПоТекущемуРейсу.Развернуть(текОперация.ПолучитьИдентификатор());
			КонецЦикла; 
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСостав(Команда)
	ТекущиеДанныеРаботы = Элементы.дрвРаботыПоТекущемуРейсу.ТекущиеДанные;
	
	сткПараметрыФормы = Новый Структура();
	сткПараметрыФормы.Вставить("РежимРаботыФормы", 2);
		
	Если ТекущиеДанныеРаботы.ЭтоРабота Тогда
		ГрузовоеМесто = ТекущиеДанныеРаботы.НазначениеСтроки;
		СтрокаТипОперации = ТекущиеДанныеРаботы.ПолучитьРодителя();
		СтрокаЗадание = СтрокаТипОперации.ПолучитьРодителя();
		ЗаданиеНаПеревозкуГруза = СтрокаЗадание.НазначениеСтроки;

		сткПараметрыФормы.Вставить("ГрузовоеМесто", ГрузовоеМесто);
		сткПараметрыФормы.Вставить("ЗаданиеНаПеревозкуГрузаПредставление", ЗаданиеНаПеревозкуГруза);
	Иначе	
		сткПараметрыФормы.Вставить("ТоварныйСоставГрузовогоМеста", ПолучитьТоварныйСостав(ТекущиеДанныеРаботы.ПрохождениеТочки));
	КонецЕсли;
		
	ОткрытьФорму("ОбщаяФорма.упИзменениеТоварногоСостава", сткПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОбработатьВыборСтрокиСпискаРейсов()
	
	// Если выбрана строка с рейсом
	Если Элементы.СписокРейсов.ТекущиеДанные <> Неопределено Тогда
		ТекущийРейс = Объект.ТекущийРейс;
		
		НовыйТекущийРейс = Элементы.СписокРейсов.ТекущиеДанные.Рейс;
		Если ТекущийРейс <> НовыйТекущийРейс Тогда
			Объект.ТекущийРейс 	= НовыйТекущийРейс;
			ОбновитьДанныеПоРейсу();
		КонецЕсли;
	Иначе
			
		Объект.дрвРаботыПоТекущемуРейсу.ПолучитьЭлементы().Очистить();
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоРейсу()
	ОбработатьВыборСтрокиСпискаРейсовСервер();
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборСтрокиСпискаРейсовСервер()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьДеревоРаботПоРейсу();
	ЗначениеВРеквизитФормы(ОбработкаОбъект.дрвРаботыПоТекущемуРейсу, "Объект.дрвРаботыПоТекущемуРейсу");
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьСписокРейсовНаКлиенте()
	ОбновитьСписокРейсовНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокРейсовНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьСписокРейсов();
	ЗначениеВРеквизитФормы(ОбработкаОбъект.СписокРейсов, "Объект.СписокРейсов");
	ЗначениеВРеквизитФормы(ОбработкаОбъект.ЗаданияПоРейсам, "Объект.ЗаданияПоРейсам");
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокДоступныхПартнеров(Пользователь) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	УпПартнеры_ТорговыеПредставителиТ.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудникиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упПартнеры.ТорговыеПредставители КАК УпПартнеры_ТорговыеПредставителиТ
	|	ПО УпПартнеры_ТорговыеПредставителиТ.ТорговыйПредставитель = упСотрудникиТ.Ссылка
	|ГДЕ упСотрудникиТ.Пользователь = &Пользователь
	|";

	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка);
	КонецЦикла; 
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;

КонецФункции

&НаСервере
Функция ПолучитьТоварныйСостав(ПрохождениеТочки)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Возврат ОбработкаОбъект.ПолучитьТоварныйСостав(ПрохождениеТочки);
	
КонецФункции


#КонецОбласти
