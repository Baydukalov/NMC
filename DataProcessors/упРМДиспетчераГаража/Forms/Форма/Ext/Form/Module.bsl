#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьПараметрыОтбораНаСервере();
	
	// СтандартныеПодсистемы.Печать
	мсвОбъектов = Новый Массив;
	мсвОбъектов.Добавить(Метаданные.Документы.упПутевойЛист);
	
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Вставить("Источники", мсвОбъектов);
	ПараметрыРазмещения.Вставить("КоманднаяПанель", Элементы.РейсыГруппаКомандаПечати);
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.Печать
	
	ИспользоватьДополнительныеОтчетыИОбработки = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки");
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	УстановитьПараметрыОтбораНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Рейсы.ТекущиеДанные;
	
	Если Поле.Имя = "РейсыТранспортноеСредство" Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ТекущиеДанные.ТранспортноеСредство) Тогда
			СтандартнаяОбработка = Ложь;
			сткПараметры = Новый Структура("Ключ",ТекущиеДанные.ТранспортноеСредство);
			ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаЭлемента",сткПараметры);
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "РейсыПутевойЛист" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьТекущийПутевойЛист();	
		
	ИначеЕсли Поле.Имя = "РейсыСсылка" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьТекущийРейс();		
		
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура РейсыПриАктивизацииСтроки(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ГаражПриИзменении(Элемент)
	УстановитьПараметрыОтбораНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ТипИсточника = ТипЗнч(Источник);
	ЭтоТипОбрабатывается = ТипИсточника = Тип("ДокументСсылка.упПутевойЛист") ИЛИ ТипИсточника = Тип("ДокументСсылка.упРейс");
	
	Если ЭтоТипОбрабатывается Тогда
		Элементы.Рейсы.Обновить();
		УстановитьДоступность();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЖурналУчетаПутевыхЛистов(Команда)
	
	ОткрытьФорму("Отчет.упЖурналУчетаДвиженияПутевыхЛистов.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПутевойЛист(Команда)
	ОткрытьТекущийПутевойЛист();	
КонецПроцедуры

&НаКлиенте
Процедура ВыдатьПутевойЛист(Команда)
	
	мсвРейсы = Новый Массив;
	Для каждого текСтрока Из Элементы.Рейсы.ВыделенныеСтроки Цикл
		
		текСтрока = Элементы.Рейсы.ДанныеСтроки(текСтрока);
		
		Если текСтрока.СтатусПутевогоЛиста = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый") Тогда
			мсвРейсы.Добавить(текСтрока.Рейс);	
		КонецЕсли;
				
	КонецЦикла;
	
	Отказ = Ложь;
	мсвПутевойЛист = ВыдатьПутевыеЛистыНаСервере(мсвРейсы, Объект.Гараж, Отказ);
	
	Если НЕ Отказ Тогда
		Если мсвРейсы.Количество() = 1 И мсвПутевойЛист.Количество() > 0 Тогда
			ПараметрыФормы = Новый Структура("Ключ", мсвПутевойЛист[0]);
			ОткрытьФорму("Документ.упПутевойЛист.Форма.ФормаДокумента", ПараметрыФормы);	
		КонецЕсли;
		Для каждого текПутевойЛист Из мсвПутевойЛист Цикл
			Оповестить("СменаСтатуса", ,текПутевойЛист);	
		КонецЦикла;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Произошла ошибка.'"),"e1cib/app/Обработка.ЖурналРегистрации","Подробности см. в журнале регистрации.", БиблиотекаКартинок.Ошибка32);			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПечатьПутевогоЛиста(Команда)
	
	мсвПутевыхЛистов = Новый Массив;
	
	Для каждого Строка Из Элементы.Рейсы.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Рейсы.ДанныеСтроки(Строка);
		
		Если ЗначениеЗаполнено(ДанныеСтроки.ПутевойЛист) Тогда
			мсвПутевыхЛистов.Добавить(ДанныеСтроки.ПутевойЛист);
		КонецЕсли;
	КонецЦикла;

	мсвПечатныхФорм = ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов);
	
	Для каждого ПечатнаяФорма Из мсвПечатныхФорм Цикл
		мсвСылок 					= ПечатнаяФорма.мсвПутевыхЛистов;
		ПечатнаяФормаПутевогоЛиста 	= ПечатнаяФорма.ПечатнаяФормаПутевогоЛиста;
		ИмяМакета 					= ПечатнаяФорма.ИмяМакета;
		ИмяОбъектаМетаданных = "Документ.упПутевойЛист";
		Если ТипЗнч(ПечатнаяФормаПутевогоЛиста) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") 
			И ИспользоватьДополнительныеОтчетыИОбработки Тогда
			//ПараметрыПечати = Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Истина);
			//УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяОбъектаМетаданных, ИмяМакета, мсвСылок, ЭтаФорма, ПараметрыПечати);
			ВыполняемаяКоманда = Новый Структура();
			ВыполняемаяКоманда.Вставить("Идентификатор",СтрЗаменить(ИмяМакета,"ВнешняяПечатнаяФорма.",""));
			ВыполняемаяКоманда.Вставить("Ссылка",ПечатнаяФормаПутевогоЛиста);
			ДополнительныеПараметры = Новый Структура("ВыполняемаяКоманда",ВыполняемаяКоманда);
			ДополнительныеПараметры.Вставить("Форма",ЭтаФорма);
			ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьОткрытиеПечатнойФормыЗавершение(мсвСылок,ДополнительныеПараметры);
		Иначе	
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяОбъектаМетаданных, ИмяМакета, мсвСылок, ЭтаФорма); 
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

//// СтандартныеПодсистемы.Печать
//&НаКлиенте
//Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
//    УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.Рейсы);
//КонецПроцедуры
//// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Рейсы);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Рейсы, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Рейсы);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОткрытьТекущийПутевойЛист()
	ПутевойЛист = Элементы.Рейсы.ТекущиеДанные.ПутевойЛист;
	Если ЗначениеЗаполнено(ПутевойЛист) Тогда
		сткПараметры = Новый Структура("Ключ", ПутевойЛист);
		ОткрытьФорму("Документ.упПутевойЛист.Форма.ФормаДокумента", сткПараметры);
	Иначе	
		ПоказатьОповещениеПользователя(НСтр("ru = 'Невозможно открыть путевой лист.'"),, НСтр("ru = 'По текущему рейсу путевой лист не сформирован.'"), БиблиотекаКартинок.Ошибка32);			
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийРейс()
	Рейс = Элементы.Рейсы.ТекущиеДанные.Рейс;
	Если ЗначениеЗаполнено(Рейс) Тогда
		сткПараметры = Новый Структура("Ключ", Рейс);
		ОткрытьФорму("Документ.упРейс.Форма.ФормаДокумента", сткПараметры);
	КонецЕсли; 	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	Если НЕ Элементы.Рейсы.ТекущиеДанные = Неопределено Тогда
		
		ТекущиеДанные 	= Элементы.Рейсы.ТекущиеДанные;
		ПутевойЛист 	= ТекущиеДанные.ПутевойЛист;
		Статус 			= ТекущиеДанные.СтатусПутевогоЛиста;
		
		Элементы.РейсыВыдатьПутевойЛист.Доступность 								= ЕстьДоступныеДляВыдачиПутевыеЛисты();
		Элементы.РейсыОбработкаупРМДиспетчераСкладаВернутьПутевойЛист.Доступность 	= Статус = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Выдан");
		Элементы.РейсыОбработкаупРМДиспетчераСкладаЗакрытьПутевойЛист.Доступность 	= Статус = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Возвращен");
		Элементы.РейсыОбработкаупРМДиспетчераСкладаЗаправитьТС.Доступность 			= Статус = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый") 
																								И ЗначениеЗаполнено(ПутевойЛист)
																						ИЛИ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Выдан");
		Элементы.РейсыОткрытьПутевойЛист.Доступность 								= ЗначениеЗаполнено(ПутевойЛист);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция ЕстьДоступныеДляВыдачиПутевыеЛисты()
	
	текРезультат = Ложь;
	
	Для каждого КлючСтроки Из Элементы.Рейсы.ВыделенныеСтроки Цикл
		
		  текСтрока = Элементы.Рейсы.ДанныеСтроки(КлючСтроки);
		  
		  текРезультат = текСтрока.СтатусПутевогоЛиста = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый") ИЛИ текРезультат;
		  
		  Если текРезультат Тогда
		  		Прервать;
		  КонецЕсли;
		  		
	КонецЦикла;
	
	Возврат текРезультат;
	
КонецФункции

&НаСервере
Процедура УстановитьПараметрыОтбораНаСервере()
	
	ТекущаяЗонаПогрузки = Неопределено;
	
	Если ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон") Тогда
		сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Гараж, Новый Структура("ЗонаПогрузки", "Адрес.ГеографическаяЗона"));
		ТекущаяЗонаПогрузки = сткДанные.ЗонаПогрузки;
	КонецЕсли;
	
	Если ТекущаяЗонаПогрузки = Неопределено Тогда
		ТекущаяЗонаПогрузки = ПредопределенноеЗначение("Справочник.упГеографическиеЗоны.ПустаяСсылка");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Рейсы, "ЗонаПогрузки", 	ТекущаяЗонаПогрузки);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Рейсы, "Гараж", 	Объект.Гараж);
	
КонецПроцедуры

&НаСервере
Функция ВыдатьПутевыеЛистыНаСервере(мсвРейсы, Гараж, Отказ)
	
	мсвПутевыеЛисты = Новый Массив;
	
	//АдресГаража = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Гараж, "Адрес");
		
	текПутевойЛист = упПутевойЛистУчетГСМ.ВыдатьПутевойЛистПоРейсам(мсвРейсы, Гараж, Отказ);	
	Если НЕ Отказ Тогда
		мсвПутевыеЛисты.Добавить(текПутевойЛист);
	КонецЕсли;
		
	Возврат мсвПутевыеЛисты;
	
КонецФункции

&НаСервере
Функция ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов)
	
	мсвРезультат = упПутевойЛистУчетГСМ.ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов);
			
	Возврат мсвРезультат;
	
КонецФункции


#КонецОбласти
