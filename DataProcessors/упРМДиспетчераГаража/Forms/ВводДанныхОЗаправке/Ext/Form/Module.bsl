#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаЗаправкиСлива		= ТекущаяДатаСеанса();
	ПутевойЛист 			= Параметры.ПутевойЛист;
	ТранспортноеСредство	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПутевойЛист, "ТранспортноеСредство");
	ВидДвиженияТоплива		= Перечисления.упВидыДвиженияТоплива.Заправка;
	ОписаниеОшибки = "";
	ВидГСМ					= Справочники.упМоделиТС.ПолучитьВидГСМПоУмолчанию(ТранспортноеСредство,ОписаниеОшибки, Отказ);
	
	СтатьяРасходов 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидГСМ, "СтатьяРасходов");
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		СтавкаНДС 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "СтавкаНДС");	
	КонецЕсли;
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ВедетсяВалютныйУчет Тогда
		ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета(); 
	Иначе
		ВалютаРеглУчета = Справочники.Валюты.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АЗСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры =Новый Структура("ЭтоАЗС", Истина);
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.упПартнеры.ФормаВыбора", сткПараметры,Элементы.АЗС);
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПриИзменении(Элемент)
	сткСтрока 		= Новый Структура("Сумма, СтавкаНДС, СуммаНДС", Стоимость, СтавкаНДС, СтоимостьНДС);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС");
	СтоимостьНДС	= сткСтрока.СуммаНДС;
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	сткСтрока 		= Новый Структура("Сумма, СтавкаНДС, СуммаНДС", Стоимость, СтавкаНДС, СтоимостьНДС);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС");
	СтоимостьНДС	= сткСтрока.СуммаНДС;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьЗаправкуПутевомуЛисту(Команда)
	
	Отказ = Ложь;
	ОписаниеОшибки	= "";
	
	ДобавитьЗаправкуПутевомуЛистуНаСервере(ПутевойЛист, ВидГСМ, Количество, ВалютаРеглУчета, Стоимость, СтавкаНДС, СтоимостьНДС, ОписаниеОшибки, Отказ, ГСМ, АЗС, ТопливнаяКарта, ДатаЗаправкиСлива);
	Если НЕ Отказ Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заправка успешно добавлена.'"),, ОписаниеОшибки, БиблиотекаКартинок.Успешно32);			
		Оповестить("ИзменилсяСтатус", ,ПутевойЛист);
		Закрыть();
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'При добавлении заправки произошли ошибки.'"),, НСтр("ru = 'Подробности см. в журнале регистрации.'"), БиблиотекаКартинок.Ошибка32);			
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста 
Процедура ДобавитьЗаправкуПутевомуЛистуНаСервере(ПутевойЛист, ВидГСМ, Количество, ВалютаРеглУчета, Стоимость, СтавкаНДС, СтоимостьНДС, ОписаниеОшибки, Отказ, ГСМ, АЗС, ТопливнаяКарта, ДатаЗаправкиСлива)
	упПутевойЛистУчетГСМ.ЗаправитьТранспортноеСредство(ПутевойЛист, ВидГСМ, Количество, ВалютаРеглУчета, Стоимость, СтавкаНДС, СтоимостьНДС, ОписаниеОшибки, Отказ, ГСМ, АЗС, ТопливнаяКарта, ДатаЗаправкиСлива);
КонецПроцедуры

#КонецОбласти