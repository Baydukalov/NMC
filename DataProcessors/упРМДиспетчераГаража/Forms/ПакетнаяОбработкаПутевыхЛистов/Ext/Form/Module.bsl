#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Гараж = Параметры.Гараж;
	
	ИнициализироватьКомпоновщикНастроек();
		
	// СтандартныеПодсистемы.Печать
	мсвСписокОбъектов = Новый Массив;
	мсвСписокОбъектов.Добавить(Метаданные.Документы.упПутевойЛист);
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.СписокПутевыеЛистыДляПечати.КоманднаяПанель, мсвСписокОбъектов);
	// Конец СтандартныеПодсистемы.Печать
	
	ИспользоватьДополнительныеОтчетыИОбработки = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки");

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПерейтиНаСтраницу(Элементы.ОтборТС);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокТранспортныхСредствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.СписокТранспортныхСредств.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "СписокТранспортныхСредствТранспортноеСредство" Тогда
			ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ТранспортноеСредство), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "СписокТранспортныхСредствПутевойЛист" Тогда
			ОткрытьФорму("Документ.упПутевойЛист.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ПутевойЛист), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокСозданныхПутевыхЛистовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "СписокСозданныхПутевыхЛистовТранспортноеСредство" Тогда
			ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ТранспортноеСредство), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "СписокСозданныхПутевыхЛистовПутевойЛист" Тогда
			ОткрытьФорму("Документ.упПутевойЛист.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ПутевойЛист), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокПутевыеЛистыДляПечатиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "СписокПутевыеЛистыДляПечатиТранспортноеСредство" Тогда
			ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ТранспортноеСредство), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "СписокПутевыеЛистыДляПечатиПутевойЛист" Тогда
			ОткрытьФорму("Документ.упПутевойЛист.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ПутевойЛист), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборТСПодобратьТС(Команда)
	ПерейтиНаСтраницу(Элементы.СписокТС);
КонецПроцедуры

&НаКлиенте
Процедура СписокТСНазад(Команда)
	ПерейтиНаСтраницу(Элементы.ОтборТС);
КонецПроцедуры

&НаКлиенте
Процедура СписокТСФормированиеПутевыхЛистов(Команда)
	Если ПроверитьЗаполнение() Тогда
		мсвВыделенныеСтрокиТС = СписокТранспортныхСредств.НайтиСтроки(Новый Структура("Пометка",Истина));
		
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОСозданииПутевыхЛистовНаДату", ЭтаФорма, мсвВыделенныеСтрокиТС);
		СтрокаПутевойЛист = НСтр("ru=';%1 путевой лист;;%1 путевых листа;%1 путевых листов;%1 путевых листа'");
		СтрокаКоличествоПутевыхЛистов = СтрокаСЧислом(СтрокаПутевойЛист, мсвВыделенныеСтрокиТС.Количество(), ВидЧисловогоЗначения.Количественное, "L=ru");
		
		ТекстВопроса = СтрШаблон(НСтр("ru='Создать %1 с датой начала %2?'"), СтрокаКоличествоПутевыхЛистов, НачалоРейса);
		
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОСозданииПутевыхЛистовНаДату(РезультатВопроса, мсвВыделенныеСтрокиТС) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		мсвСтрокиТС = Новый Массив;
		Для каждого Строка Из мсвВыделенныеСтрокиТС Цикл
			сткСтрока = Новый Структура("ТранспортноеСредство, ПечатнаяФормаПутевогоЛиста");
			ЗаполнитьЗначенияСвойств(сткСтрока, Строка);
			мсвСтрокиТС.Добавить(сткСтрока);
		КонецЦикла;
		СформироватьПутевыеЛистыНаДату(мсвСтрокиТС);
		ПерейтиНаСтраницу(Элементы.ПутевыеЛисты);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокТСПечатьДокументов(Команда)
	ПерейтиНаСтраницу(Элементы.ПечатьДокументов);
КонецПроцедуры

&НаКлиенте
Процедура СписокТСОтметитьВсе(Команда)
	Для каждого Строка Из СписокТранспортныхСредств Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокТССнятьВсе(Команда)
	Для каждого Строка Из СписокТранспортныхСредств Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыОтметитьВсе(Команда)
	Для каждого Строка Из СписокСозданныхПутевыхЛистов Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыСнятьВсе(Команда)
	Для каждого Строка Из СписокСозданныхПутевыхЛистов Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыНазад(Команда)
	ПерейтиНаСтраницу(ПредыдущаяСтраницаСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПутевыеЛистыПечатьДокументов(Команда)
	ПерейтиНаСтраницу(Элементы.ПечатьДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДокументовНазад(Команда)
	ПерейтиНаСтраницу(ПредыдущаяСтраницаСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДокументовОтметитьВсе(Команда)
	Для каждого Строка Из СписокПутевыхЛистовДляПечати Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДокументовСнятьВсе(Команда)
	Для каждого Строка Из СписокПутевыхЛистовДляПечати Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПутевогоЛистаПоУмолчанию(Команда)
	
	мсвПутевыхЛистов = Новый Массив;
	
	мсвСтрокиСписка = СписокПутевыхЛистовДляПечати.НайтиСтроки(Новый Структура("Пометка",Истина));
	
	Для каждого Строка Из мсвСтрокиСписка Цикл
		мсвПутевыхЛистов.Добавить(Строка.ПутевойЛист);
	КонецЦикла;
	мсвПечатныхФорм = ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов);

	Для каждого ПечатнаяФорма Из мсвПечатныхФорм Цикл
		мсвСылок                   = ПечатнаяФорма.мсвПутевыхЛистов;
		ПечатнаяФормаПутевогоЛиста = ПечатнаяФорма.ПечатнаяФормаПутевогоЛиста;
		ИмяМакета                  = ПечатнаяФорма.ИмяМакета;
		ИмяОбъектаМетаданных       = "Документ.упПутевойЛист";
		Если Истина
			И ТипЗнч(ПечатнаяФормаПутевогоЛиста) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") 
			И ИспользоватьДополнительныеОтчетыИОбработки 
		Тогда
		     Если НапечататьСразуНаПринтер Тогда
				ПараметрыПечати = Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Истина);
				ИмяМакета       = упСервисныеФункцииВызовСервера.ПолучитьИмяМакетаВнешнейПечатнойФормы(ПечатнаяФормаПутевогоЛиста, ИмяОбъектаМетаданных);
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, мсвСылок, ПараметрыПечати);
			Иначе	
				ВыполняемаяКоманда = Новый Структура();
				ВыполняемаяКоманда.Вставить("Идентификатор",СтрЗаменить(ИмяМакета,"ВнешняяПечатнаяФорма.",""));
				ВыполняемаяКоманда.Вставить("Ссылка",ПечатнаяФормаПутевогоЛиста);
				ВыполняемаяКоманда.Вставить("СразуНаПринтер", НапечататьСразуНаПринтер);
				ДополнительныеПараметры = Новый Структура("ВыполняемаяКоманда",ВыполняемаяКоманда);
				ДополнительныеПараметры.Вставить("Форма",ЭтаФорма);
				ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьОткрытиеПечатнойФормыЗавершение(мсвСылок,ДополнительныеПараметры);
			КонецЕсли;
		Иначе	
			Если НапечататьСразуНаПринтер Тогда
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, мсвСылок); 
			Иначе	
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяОбъектаМетаданных, ИмяМакета, мсвСылок, ЭтаФорма); 
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыдатьПутевыеЛисты(Команда)
	
	ВыдатьПутевыеЛистыНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПутевыеЛисты(Команда)
	
	ОтменитьПутевыеЛистыНаСервере();
	
КонецПроцедуры


// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	ОписаниеКоманды = УправлениеПечатьюКлиентПовтИсп.ОписаниеКомандыПечати(Команда.Имя, ЭтаФорма.Команды.Найти("АдресКомандПечатиВоВременномХранилище").Действие);
	
	мсвПутевыхЛистов = Новый Массив;
	мсвСтрокиСписка = СписокПутевыхЛистовДляПечати.НайтиСтроки(Новый Структура("Пометка",Истина));
	
	Для каждого Строка Из мсвСтрокиСписка Цикл
		мсвПутевыхЛистов.Добавить(Строка.ПутевойЛист);
	КонецЦикла;

	ОписаниеКоманды.СразуНаПринтер = НапечататьСразуНаПринтер;
	ОписаниеКоманды.Вставить("ОбъектыПечати", мсвПутевыхЛистов);
	
	Если ОписаниеКоманды.МенеджерПечати = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" 
		И ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		
		Если НапечататьСразуНаПринтер Тогда
			
			ИмяОбъектаМетаданных	 	= "Документ.упПутевойЛист";
			ПараметрыПечати = Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Истина);
			ИмяМакета       = упСервисныеФункцииВызовСервера.ПолучитьИмяМакетаВнешнейПечатнойФормы(ОписаниеКоманды.Ссылка, ИмяОбъектаМетаданных);
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяОбъектаМетаданных, ИмяМакета, ОписаниеКоманды.ОбъектыПечати, ПараметрыПечати);
		Иначе	
    			МодульДополнительныеОтчетыИОбработкиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДополнительныеОтчетыИОбработкиКлиент");
			МодульДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуПечати(ОписаниеКоманды, ЭтаФорма);
		КонецЕсли;
		
	Иначе		
		Если НапечататьСразуНаПринтер Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати, ОписаниеКоманды.ДополнительныеПараметры);
		Иначе
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати, ЭтаФорма, ОписаниеКоманды);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьВидимостьКомандПанелиНавигации()
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ОтборТС Тогда
		Элементы.НавигацияСтраницы.ТекущаяСтраница    = Элементы.НавигацияОтборТС;
		Элементы.ОтборТСПодобратьТС.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Настройка отбора транспортных средств'");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СписокТС Тогда
		Элементы.НавигацияСтраницы.ТекущаяСтраница                   = Элементы.НавигацияСписокТС;
		Элементы.СписокТСФормированиеПутевыхЛистов.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Список транспортных средств'");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ПутевыеЛисты Тогда
		Элементы.НавигацияСтраницы.ТекущаяСтраница              = Элементы.НавигацияПутевыеЛисты;
		Элементы.ПутевыеЛистыПечатьДокументов.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Формирование путевых листов'");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ПечатьДокументов Тогда	
		Элементы.НавигацияСтраницы.ТекущаяСтраница         = Элементы.НавигацияПечатьДокументов;
		Элементы.ПечатьДокументовЗакрыть.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Печать путевых листов'");
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницу(ЭлементСтраница)
	
	ПредыдущаяСтраница 		= Элементы.Страницы.ТекущаяСтраница;
	
	Если ТипЗнч(ЭлементСтраница) = Тип("Строка") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы[ЭлементСтраница];
	Иначе	
		Элементы.Страницы.ТекущаяСтраница = ЭлементСтраница;	
	КонецЕсли;
	
	ПредыдущаяСтраницаСтрока = ПредыдущаяСтраница.Имя;
	УстановитьВидимостьКомандПанелиНавигации();
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ОтборТС Тогда
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СписокТС Тогда	
		ЗаполнитьСписокТранспортныхСредств();	
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ПутевыеЛисты Тогда		
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ПечатьДокументов Тогда			
		СписокПутевыхЛистовДляПечати.Очистить();
		Если ПредыдущаяСтраница = Элементы.ПутевыеЛисты Тогда
			мсвСтроки = СписокСозданныхПутевыхЛистов.НайтиСтроки(Новый Структура("Пометка",Истина))
		ИначеЕсли ПредыдущаяСтраница = Элементы.СписокТС Тогда	
			мсвСтроки = СписокТранспортныхСредств.НайтиСтроки(Новый Структура("Пометка",Истина))
		КонецЕсли;
		Для каждого Строка Из мсвСтроки Цикл
			Если ЗначениеЗаполнено(Строка.ПутевойЛист) Тогда
				НоваяСтрока = СписокПутевыхЛистовДляПечати.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.Пометка = Истина;
		     КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	СхемаКомпоновкиДанных = Обработки.упРМДиспетчераГаража.ПолучитьМакет("ОтборТС");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТранспортныхСредств()
	
	СхемаКомпоновкиДанных = Обработки.упРМДиспетчераГаража.ПолучитьМакет("ОтборТС");
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Период", ТекущаяДатаСеанса());
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	СписокТранспортныхСредств.Очистить();
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Для каждого Строка Из Результат Цикл
		НоваяСтрока = СписокТранспортныхСредств.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ПутевойЛист = Строка.ПоследнийОткрытыйПутевойЛист;
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПутевыеЛистыНаДату(мсвВыделенныеСтрокиТС)
	
	СписокСозданныхПутевыхЛистов.Очистить();
	
	Для каждого СтрокаТС Из мсвВыделенныеСтрокиТС Цикл
		Отказ = Ложь;
		ПутевойЛист = упПутевойЛистУчетГСМ.СформироватьПутевойЛистПоТранспортномуСредству(СтрокаТС.ТранспортноеСредство, НачалоРейса, Гараж, Отказ);
		
		Если НЕ Отказ Тогда
			НоваяСтрока = СписокСозданныхПутевыхЛистов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТС);
			НоваяСтрока.ПутевойЛист                = ПутевойЛист;
			НоваяСтрока.СтатусПутевогоЛиста        = Перечисления.упСтатусыПутевогоЛиста.Новый;
			НоваяСтрока.ПорядокСтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Индекс(Перечисления.упСтатусыПутевогоЛиста.Новый);
			НоваяСтрока.Пометка = Ложь;
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВыдатьПутевыеЛистыНаСервере()
	
	мсвСтрокиСписка = СписокСозданныхПутевыхЛистов.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого СтрокаПутевойЛист Из мсвСтрокиСписка Цикл
		ОписаниеОшибки = "";
		Отказ = Ложь;
		
		Если СтрокаПутевойЛист.СтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Отменен Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось выдать %1. Ошибка: %2'");
			ОписаниеОшибки = Нстр("ru = 'Нельзя выдавать отмененный путевой лист.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаПутевойЛист.ПутевойЛист, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Продолжить;
		КонецЕсли;
		
		СтатусВыдан = Перечисления.упСтатусыПутевогоЛиста.Выдан;
		упПутевойЛистУчетГСМ.УстановитьСтатусПутевогоЛиста(СтрокаПутевойЛист.ПутевойЛист, СтатусВыдан, ОписаниеОшибки, Отказ);
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось выдать %1. Ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаПутевойЛист.ПутевойЛист, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе	
			СтрокаПутевойЛист.СтатусПутевогоЛиста        = СтатусВыдан;
			СтрокаПутевойЛист.ПорядокСтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Индекс(СтатусВыдан);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьПутевыеЛистыНаСервере()
	
	мсвСтрокиСписка = СписокСозданныхПутевыхЛистов.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого СтрокаПутевойЛист Из мсвСтрокиСписка Цикл
		ОписаниеОшибки = "";
		Отказ = Ложь;
		СтатусОтменен = Перечисления.упСтатусыПутевогоЛиста.Отменен;
		упПутевойЛистУчетГСМ.УстановитьСтатусПутевогоЛиста(СтрокаПутевойЛист.ПутевойЛист, СтатусОтменен, ОписаниеОшибки, Отказ);
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось отменить %1. Ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаПутевойЛист.ПутевойЛист, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе	
			СтрокаПутевойЛист.СтатусПутевогоЛиста        = СтатусОтменен;
			СтрокаПутевойЛист.ПорядокСтатусПутевогоЛиста = Перечисления.упСтатусыПутевогоЛиста.Индекс(СтатусОтменен);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов)
	
	мсвРезультат = упПутевойЛистУчетГСМ.ПечатныеФормПутевыхЛистовПоУмолчанию(мсвПутевыхЛистов);
			
	Возврат мсвРезультат;
	
КонецФункции


#КонецОбласти