
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыМониторинга = Параметры.ПараметрыМониторинга;
	Если ПараметрыМониторинга <> Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыМониторинга);
	КонецЕсли;
	
	Если ПериодОтображенияТрека > 0 Тогда
		ВремяОтображениеТрекаЧасов = Цел(ПериодОтображенияТрека);
		ВремяОтображениеТрекаМинут = Окр((ПериодОтображенияТрека - ВремяОтображениеТрекаЧасов)*60);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	мсвНепроверяемых = Новый Массив;
	Если Не ИспользоватьАвтообновлениеДанных Тогда
		мсвНепроверяемых.Добавить("ПериодАвтообновления");
	КонецЕсли; 
	
	Если Не ОтображатьТрекиДвиженияТранспорта Тогда
		мсвНепроверяемых.Добавить("ПериодОтображенияТрека");
	КонецЕсли;
	
	Если мсвНепроверяемых.Количество() Тогда
		ПроверяемыеРеквизиты = ОбщегоНазначенияКлиентСервер.СократитьМассив(ПроверяемыеРеквизиты, мсвНепроверяемых);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтображатьТрекиДвиженияТранспортаПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтообновлениеДанныхПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ДопустимыйПериодОтсутствияСвязиСТрекеромПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		ПериодОтображенияТрека = ВремяОтображениеТрекаЧасов + ВремяОтображениеТрекаМинут/60;
		
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ОтображатьПоследниеДанныеТрекера"                    , ОтображатьПоследниеДанныеТрекера);
		сткПараметры.Вставить("ОтображатьПоследниеДанныеДатчиков"                   , ОтображатьПоследниеДанныеДатчиков); 
		сткПараметры.Вставить("ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте", ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте);
		сткПараметры.Вставить("ОтображатьСобытияМониторингаНаКарте"                 , ОтображатьСобытияМониторингаНаКарте);
		сткПараметры.Вставить("ОтображатьТрекиДвиженияТранспорта"                   , ОтображатьТрекиДвиженияТранспорта);
		сткПараметры.Вставить("ПериодОтображенияТрека"                              , ПериодОтображенияТрека);
		сткПараметры.Вставить("ИспользоватьАвтообновлениеДанных"                    , ИспользоватьАвтообновлениеДанных);
		сткПараметры.Вставить("ПериодАвтообновления"                                , ПериодАвтообновления);
		сткПараметры.Вставить("ДопустимыйПериодОтсутствияСвязиСТрекером"            , ДопустимыйПериодОтсутствияСвязиСТрекером);
		сткПараметры.Вставить("ОтображатьПериодыОтсутствияСвязи"                    , ОтображатьПериодыОтсутствияСвязи);

		Закрыть(сткПараметры);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните обязательные настройки!'"));
	КонецЕсли;
	
КонецПроцедуры // ЗавершитьРедактирование()

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);

КонецПроцедуры // Отмена()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьНастроекПланирования()
	
	Элементы.ГруппаПериодАвтообновления.Доступность = ИспользоватьАвтообновлениеДанных;
	Элементы.ГруппаТаймерОбновления.Доступность = ИспользоватьАвтообновлениеДанных;
	Элементы.ПериодАвтообновления.АвтоОтметкаНезаполненного = ИспользоватьАвтообновлениеДанных;
	Элементы.ГруппаПериодОтображенияТрека.Доступность = ОтображатьТрекиДвиженияТранспорта;
	Элементы.ГруппаСобытияМониторинга.Доступность = ОтображатьТрекиДвиженияТранспорта;
	Элементы.ГруппаНетСвязи.Доступность = ОтображатьТрекиДвиженияТранспорта;
	Элементы.ГруппаОтображатьПериодыОтсутствияСвязи.Доступность = (ДопустимыйПериодОтсутствияСвязиСТрекером > 0);
	
КонецПроцедуры // УстановитьДоступностьНастроекПланирования()

#КонецОбласти 
