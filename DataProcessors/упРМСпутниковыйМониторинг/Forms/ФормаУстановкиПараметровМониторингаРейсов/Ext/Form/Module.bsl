
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыМониторинга = Параметры.ПараметрыМониторинга;
	Если ПараметрыМониторинга <> Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыМониторинга);
	КонецЕсли;
	
	Если ПериодОтображенияСобытийМониторинга > 0 Тогда
		ВремяОтображениеСобытийМониторингаЧасов = Цел(ПериодОтображенияСобытийМониторинга);
		ВремяОтображениеСобытийМониторингаМинут = Окр((ПериодОтображенияСобытийМониторинга - ВремяОтображениеСобытийМониторингаЧасов)*60);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ИспользоватьАвтообновлениеДанных Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ПериодАвтообновления"));
	КонецЕсли; 
	
	Если НЕ (ОтображатьТрекЦветомПоСкоростиДвиженияРейсы И ОтображатьТрекиДвиженияПоРейсам) Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ЦветоваяШкалаРейсы"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтображатьТрекЦветомПоСкоростиДвиженияПриИзменении(Элемент)	
	
	Элементы.ГруппаПодчиненыеЦветовойШкале.Доступность = ОтображатьТрекЦветомПоСкоростиДвиженияРейсы;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТрекиДвиженияПоРейсамПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры


&НаКлиенте
Процедура ОтображатьСобытияМониторингаПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ДопустимыйПериодОтсутствияСвязиСТрекеромПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтообновлениеДанныхПриИзменении(Элемент)
	
	УстановитьДоступностьНастроекПланирования();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ПериодОтображенияСобытийМониторинга = ВремяОтображениеСобытийМониторингаЧасов + ВремяОтображениеСобытийМониторингаМинут/60;
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ОтображатьПоследниеДанныеТрекера"                    , ОтображатьПоследниеДанныеТрекера);
		сткПараметры.Вставить("ОтображатьПоследниеДанныеДатчиков"                   , ОтображатьПоследниеДанныеДатчиков); 
		сткПараметры.Вставить("ОтображатьТрекиДвиженияПоРейсам"                     , ОтображатьТрекиДвиженияПоРейсам);
		сткПараметры.Вставить("ОтображатьСобытияМониторингаНаКарте"                 , ОтображатьСобытияМониторингаНаКарте);
		сткПараметры.Вставить("ПериодОтображенияСобытийМониторинга"                 , ПериодОтображенияСобытийМониторинга);
		сткПараметры.Вставить("ИспользоватьАвтообновлениеДанных"                    , ИспользоватьАвтообновлениеДанных);
		сткПараметры.Вставить("ПериодАвтообновления"                                , ПериодАвтообновления);
		сткПараметры.Вставить("ДопустимыйПериодОтсутствияСвязиСТрекером"            , ДопустимыйПериодОтсутствияСвязиСТрекером);
		сткПараметры.Вставить("ОтображатьПериодыОтсутствияСвязи"                    , ОтображатьПериодыОтсутствияСвязи);
		сткПараметры.Вставить("ОтображатьТрекЦветомПоСкоростиДвиженияРейсы"         , ОтображатьТрекЦветомПоСкоростиДвиженияРейсы);
		сткПараметры.Вставить("ЦветоваяШкалаРейсы"                                  , ЦветоваяШкалаРейсы);
 
		Закрыть(сткПараметры);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните обязательные настройки!'"));
	КонецЕсли;
	
КонецПроцедуры // ЗавершитьРедактирование()

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);

КонецПроцедуры // Отмена()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьНастроекПланирования()
	
	Элементы.ГруппаПериодАвтообновления.Доступность = ИспользоватьАвтообновлениеДанных;
	Элементы.ГруппаТаймерОбновления.Доступность = ИспользоватьАвтообновлениеДанных;
	Элементы.ПериодАвтообновления.АвтоОтметкаНезаполненного = ИспользоватьАвтообновлениеДанных;
	Элементы.ГруппаСобытияМониторинга.Доступность = ОтображатьТрекиДвиженияПоРейсам;
	Элементы.ГруппаНетСвязи.Доступность = ОтображатьТрекиДвиженияПоРейсам;
	Элементы.ГруппаОтображатьПериодыОтсутствияСвязи.Доступность = (ДопустимыйПериодОтсутствияСвязиСТрекером > 0);
	Элементы.ГруппаПодчиненыеЦветовойШкале.Доступность = ОтображатьТрекЦветомПоСкоростиДвиженияРейсы;
	Элементы.ГруппаЦветоваяШкала.Доступность = ОтображатьТрекиДвиженияПоРейсам;	
	
КонецПроцедуры // УстановитьДоступностьНастроекПланирования()

#КонецОбласти 
