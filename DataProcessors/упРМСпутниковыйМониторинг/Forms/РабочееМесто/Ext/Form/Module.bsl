#Если Не ВебКлиент Тогда

#Область ПеременныеМодуляФормы

// Переменная ЧислоПопыток хранит количество попыток сформировать документ HTML.
// Переменнная ЧислоЗагрузок хранит количество сформированных документов HTML.
&НаКлиенте
Перем ЧислоПопыток, ЧислоЗагрузок, MapСформирован, АктивизацияПоляВСпискеОбъектовВозможна, СлужебныеЭлементыHtml;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ТипКарты = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипКарт");
	ВремяЗапускаОбработки=0.1;
	Если ТипКарты = ПредопределенноеЗначение("Перечисление.упТипыКарт.ЯндексКарты") Тогда
		ВремяЗапускаОбработки=1;
	КонецЕсли;
	СтруктураПараметров = упМаршрутизация.ПолучитьКоординатыНачальнойПозиции();
	НачальнаяШирота  = СтруктураПараметров.Широта;
	НачальнаяДолгота = СтруктураПараметров.Долгота;
	НачальныйЗум     = СтруктураПараметров.Зум;
	
	HTMLДокумент = упМаршрутизация.ПолучитьТекстОбщегоМакетаКартографическогоСервиса();
	
    ВосстановитьОбщиеНастройкиФормы();
	УстановитьПараметрыМониторингаНаСервере();
	
	ВосстановитьНастройкиРежимаРаботыРабочегоМеста();
	УстановитьПараметрыРежимаРабочегоМестаНаСервере();
	УстановитьДоступныеЗначенияВариантовНастроек();
	ОбновитьСписокВидимостиСобытийМониторингаНаСервере();
	Если РежимРабочегоМеста = 0 Тогда // рейсы
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМониторингРейсов;
		ОбновитьСписокРейсовНаСервере();
	ИначеЕсли РежимРабочегоМеста = 1 Тогда // транспорты
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМониторингТранспорта;
		ОбновитьСписокТранспортныхСредствНаСервере();
	КонецЕсли;
	ОбновитьДанныеФормыДляОтображения();
	ОбновитьДоступность(ЭтаФорма);
	упСервисныеФункции.ДобавитьУсловноеОформлениеФормыДляПоляСтиляЛинии(Элементы.СписокТранспортныхСредствЦветНаКарте, "СписокТранспортныхСредств.СтильЛиний");
	упСервисныеФункции.ДобавитьУсловноеОформлениеФормыДляПоляСтиляЛинии(Элементы.СписокРейсовЦветНаКарте, "СписокРейсов.СтильЛиний");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	MapСформирован = Ложь; // карта не сформирована.
	АктивизацияПоляВСпискеОбъектовВозможна = Истина;
	Элементы.СписокРейсов.ТекущаяСтрока = Неопределено;
	Элементы.СписокРейсов.ТекущийЭлемент = Неопределено;
	Элементы.СписокТранспортныхСредств.ТекущаяСтрока = Неопределено;
	Элементы.СписокТранспортныхСредств.ТекущийЭлемент = Неопределено;
	
	// Переопределение текущих элементов таблиц формы для изменения активизации поля при первичной загрузке.
	Элементы.СписокТранспортныхСредств.ТекущийЭлемент = Элементы.СписокТранспортныхСредствПредставление;
	Элементы.СписокРейсов.ТекущийЭлемент = Элементы.СписокРейсовТочекПройдено;
	Элементы.СписокВидимостиСобытийМониторинга.ТекущийЭлемент = Элементы.СписокВидимостиСобытийМониторингаТипСобытия;
	УстановитьПараметрыМакета();
	ЗаполнитьСписокКартинокОбщегоПользования();
	Элементы.ОтобразитьЗоны.Пометка           = ОтображатьЗоны;
	Элементы.ПоказатьУбратьНазванияТС.Пометка = ОтображатьНазвания;
    Элементы.ОтображатьНаправления.Пометка    = ОтображатьНаправления;
	Элементы.ОтобразитьМестаИнтереса.Пометка = ОтображатьМестаИнтереса;
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры //ПриОткрытии()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимРабочегоМеста < 2 И ПараметрыРежима.ИспользоватьАвтообновлениеДанных Тогда
		УстановитьОбработчикОжидания(Ложь);
	КонецЕсли;
	
	СохранитьНастройкиРежимаРаботыРабочегоМеста();
	СохранитьОбщиеНастройкиФормы();
	
КонецПроцедуры //ПередЗакрытием()

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПланированияПриИзменении(Элемент)
	
	ЭтаФорма.НачалоПериода = ПериодИстории.ДатаНачала;
	ЭтаФорма.КонецПериода = ПериодИстории.ДатаОкончания;
	ПериодИсторииДатаНачалаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	
	// Проверяет на стороне карты сформирован ли объект Map.
	MapExists = ВыполнитьФункциюHTML("ValidateCacheCalls()");
	Если MapExists = Истина Тогда
		ОчиститьСообщения();
		
		Если Не КартаСформирована Тогда 
			ЭтаФорма.КартаСформирована = Истина;	
			
			ВыполнитьПроцедуруHTML("displayDirections = " + ?(ОтображатьНаправления, "true", "false") + ";");
			ВыполнитьПроцедуруHTML("displayVehicleNames = " + ?(ОтображатьНазвания, "true", "false") + ";");
			ОтобразитьОбъектыНаКарте();
			МасштабироватьКартуПоОбъектамМониторинга();
			ИзменитьВидимостьЗон();
			ИзменитьВидимостьМестИнтереса(ОтображатьМестаИнтереса);
			ТолькоПросмотр = Ложь;
			УстановитьОбработчикОжидания();
		КонецЕсли;
	Иначе	
		ПодключитьОбработчикОжидания("ВыполнитьОтложенноеДействие", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры //HTMLДокументДокументСформирован()

&НаКлиенте
Процедура HTMLДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ВыполняетОбращениеКДокументу Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьТекущуюСтрокуВСписке();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВидимостиСобытийМониторингаПриАктивизацииПоля(Элемент)
	
	Если КартаСформирована Тогда
		ПодключитьОбработчикОжидания("СписокВидимостиСобытийМониторингаПриАктивизацииПоляОбработчикОжидания", 0.2, Истина);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВидимостиСобытийМониторингаПриАктивизацииПоляОбработчикОжидания()
	
	текСтрока = Элементы.СписокВидимостиСобытийМониторинга.ТекущиеДанные;
	текЭлемент = Элементы.СписокВидимостиСобытийМониторинга.ТекущийЭлемент;
	Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
		Если текЭлемент.Имя = "СписокВидимостиСобытийМониторингаПометка" Тогда
			
			Если РежимРабочегоМеста = 0 Тогда
				Если СписокРейсов.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() Тогда
					текСтрока.Пометка = Не текСтрока.Пометка;
					текСтрока.ПометкаАвтоотключение = Ложь;
					Если текСтрока.Пометка И текСтрока.ОбновитьДанные Тогда
						Если Не текСтрока.НетДанных Тогда
							ОбновитьДанныеСобытийМониторингаРейсов(СписокРейсовСлужебный, текСтрока.ТипСобытия);
							
							мсвКоординатСобытий = Новый Массив;
							КоличествоМаркеров = ВыполнитьФункциюHTML("CheckCountOfTrackingIncidents()");
							спкИднексы = текСтрока.Индексы;
							Если спкИднексы <> Неопределено И спкИднексы.Количество() Тогда
								ИскомыеСтроки =  СписокМаркеров.НайтиСтроки(Новый Структура("СтильМаркера", текСтрока.СтильМаркера));
								Если ИскомыеСтроки.Количество() Тогда
									НайденнаяСтрока = ИскомыеСтроки[0];
									мсвПараметров = Новый Массив;
									мсвПараметров.Добавить(НайденнаяСтрока.ДанныеМаркера);
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ПоложениеПривязкиXМаркера));
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ПоложениеПривязкиYМаркера));
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ШиринаМаркера));
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ВысотаМаркера));
									
									ДанныеМаркера = СтрСоединить(мсвПараметров, ";");
									Для каждого спкИндексовПоТранспорту Из спкИднексы Цикл
										Для каждого ЭлементДанные Из спкИндексовПоТранспорту.Значение Цикл
											сткДанные = ЭлементДанные.Значение;
											Если сткДанные = Неопределено Тогда
												Продолжить;
											КонецЕсли;
											
											мсвКоординатСобытий.Добавить(текСтрока.ПредставлениеТипа);
											мсвКоординатСобытий.Добавить(сткДанные.Период);
											мсвКоординатСобытий.Добавить(сткДанные.Широта);
											мсвКоординатСобытий.Добавить(сткДанные.Долгота);
											мсвКоординатСобытий.Добавить(ДанныеМаркера);
											
											КоличествоМаркеров = КоличествоМаркеров + 1;
											сткДанные.ИндексВМассиве = КоличествоМаркеров; 
										КонецЦикла; 
									КонецЦикла; 
									
									Если мсвКоординатСобытий.Количество() Тогда
										ОтобразитьСобытияМониторингаНаКарте(СтрСоединить(мсвКоординатСобытий, ";"));
									КонецЕсли;
								КонецЕсли; 
							КонецЕсли;
						КонецЕсли; 
					Иначе
						ИзменитьВидимостьСобытийПоТипу(текСтрока);
					КонецЕсли; 
				КонецЕсли;
			ИначеЕсли РежимРабочегоМеста = 1 Тогда
				Если СписокТранспортныхСредств.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() Тогда
					текСтрока.Пометка = Не текСтрока.Пометка;
					текСтрока.ПометкаАвтоотключение = Ложь;
					Если текСтрока.Пометка И текСтрока.ОбновитьДанные Тогда
						Если Не текСтрока.НетДанных Тогда
							ОбновитьДанныеСобытийМониторингаТранспорта(СписокТранспортаСлужебный, текСтрока.ТипСобытия);
							
							мсвКоординатСобытий = Новый Массив;
							КоличествоМаркеров = ВыполнитьФункциюHTML("CheckCountOfTrackingIncidents()");
							спкИднексы = текСтрока.Индексы;
							Если спкИднексы <> Неопределено И спкИднексы.Количество() Тогда
								ИскомыеСтроки =  СписокМаркеров.НайтиСтроки(Новый Структура("СтильМаркера", текСтрока.СтильМаркера));
								Если ИскомыеСтроки.Количество() Тогда
									НайденнаяСтрока = ИскомыеСтроки[0];
									мсвПараметров = Новый Массив;
									мсвПараметров.Добавить(НайденнаяСтрока.ДанныеМаркера);
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ПоложениеПривязкиXМаркера));
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ПоложениеПривязкиYМаркера));
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ШиринаМаркера));
									мсвПараметров.Добавить(ПредставлениеЧисла(НайденнаяСтрока.ВысотаМаркера));
									
									ДанныеМаркера = СтрСоединить(мсвПараметров, ";");
									Для каждого спкИндексовПоТранспорту Из спкИднексы Цикл
										Для каждого ЭлементДанные Из спкИндексовПоТранспорту.Значение Цикл
											сткДанные = ЭлементДанные.Значение;
											Если сткДанные = Неопределено Тогда
												Продолжить;
											КонецЕсли;
											
											мсвКоординатСобытий.Добавить(текСтрока.ПредставлениеТипа);
											мсвКоординатСобытий.Добавить(сткДанные.Период);
											мсвКоординатСобытий.Добавить(сткДанные.Широта);
											мсвКоординатСобытий.Добавить(сткДанные.Долгота);
											мсвКоординатСобытий.Добавить(ДанныеМаркера);
											
											КоличествоМаркеров = КоличествоМаркеров + 1;
											сткДанные.ИндексВМассиве = КоличествоМаркеров; 
										КонецЦикла; 
									КонецЦикла; 
									
									Если мсвКоординатСобытий.Количество() Тогда
										ОтобразитьСобытияМониторингаНаКарте(СтрСоединить(мсвКоординатСобытий, ";"));
									КонецЕсли;
								КонецЕсли; 
							КонецЕсли;
						КонецЕсли; 
					Иначе
						ИзменитьВидимостьСобытийПоТипу(текСтрока);
					КонецЕсли; 
				КонецЕсли;
				
			Иначе
				
			КонецЕсли; 

		КонецЕсли;
	
		Если текЭлемент.Имя = "СписокВидимостиСобытийМониторингаПометка" Тогда
			// изменяем текущий элемент для возможности его повторной активизации
			Элементы.СписокВидимостиСобытийМониторинга.ТекущийЭлемент = Элементы.СписокВидимостиСобытийМониторингаТипСобытия;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимРабочегоМестаПриИзменении(Элемент)
	
	ВосстановитьНастройкиРежимаРаботыРабочегоМеста();
	УстановитьПараметрыРежимаРабочегоМестаНаСервере();
	СписокПоследнихПараметров.Очистить();
	СписокПоследнихДанныхДатчиков.Очистить();
	СписокПериодовОтсутствияСоединения.Очистить();
	Если РежимРабочегоМеста = 0 Тогда // мониторинг рейсов
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМониторингРейсов;
		ОбновитьСписокРейсов(Неопределено);
	ИначеЕсли РежимРабочегоМеста = 1 Тогда // мониторинг транспорта
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМониторингТранспорта;
		ОбновитьСписокТранспортныхСредств(Неопределено);
	КонецЕсли; 
	ОбновитьДоступность(ЭтаФорма);
	МасштабироватьКартуПоОбъектамМониторинга();
	СброситьАнимациюТрека();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНастроекПриИзменении(Элемент)
	
	Если РежимРабочегоМеста = 0 Тогда
		Если ЗначениеЗаполнено(ВариантНастроек) Тогда
			КлючТекущихНастроекМониторингРейсов = ВариантНастроек;
			сткНастройки = ВернутьВариантНастройкиСпискаРейсов();
			
			Если сткНастройки <> Неопределено Тогда
				КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
			КонецЕсли;
		Иначе
			КлючТекущихНастроекМониторингРейсов = "";
			ИнициализироватьКомпоновщикНастроек();
		КонецЕсли;
		
		УстановитьПараметрыРежимаРабочегоМестаНаСервере();
		ОбновитьСписокРейсов(Неопределено);
		
		
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		Если ЗначениеЗаполнено(ВариантНастроек) Тогда
			КлючТекущихНастроекМониторингТранспорта = ВариантНастроек;
			сткНастройки = ВернутьВариантНастройкиСпискаТранспортныхСредств();
			
			Если сткНастройки <> Неопределено Тогда
				КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
			КонецЕсли;
		Иначе
			КлючТекущихНастроекМониторингТранспорта = "";
			ИнициализироватьКомпоновщикНастроек();
		КонецЕсли;
		
		УстановитьПараметрыРежимаРабочегоМестаНаСервере();
		ОбновитьСписокТранспортныхСредств(Неопределено);
		
	Иначе
		
	КонецЕсли;
	МасштабироватьКартуПоОбъектамМониторинга();
	
КонецПроцедуры

#Область СобытияЭлементовРежимаМониторингРейсов

&НаКлиенте
Процедура СписокРейсовПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СписокРейсовПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРейсовПриАктивизацииПоля(Элемент)
	
	Если КартаСформирована Тогда 
		Если АктивизацияПоляВСпискеОбъектовВозможна <> Истина Тогда
			ПодключитьОбработчикОжидания("РазрешитьАктивизациюПоляВСпискеОбъектов", 0.1, Истина);
		Иначе
			ПодключитьОбработчикОжидания("СписокРейсовПриАктивизацииПоляОбработчикОжидания", 0.2, Истина);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбеспечитьВозможностьПовторнойАктивизацииПоля()
	
	текЭлемент = Элементы.СписокРейсов.ТекущийЭлемент;
	Если Ложь
		Или текЭлемент = Неопределено
		Или текЭлемент.Имя = "СписокРейсовПометка" 
		Или текЭлемент.Имя = "СписокРейсовПометкаТрек" 
		Или текЭлемент.Имя = "СписокРейсовПометкаТрекПлан" 
		Или текЭлемент.Имя = "СписокРейсовСлежение" 
		Или текЭлемент.Имя = "СписокРейсовПозиционирование" 
	Тогда
		// изменяем текущий элемент для возможности его повторной активизации
		Элементы.СписокРейсов.ТекущийЭлемент = Элементы.СписокРейсовТочекПройдено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокРейсовПриАктивизацииСтрокиОбработчикОжидания()
	
	Если РежимРабочегоМеста = 0 Тогда
		текСтрока = Элементы.СписокРейсов.ТекущиеДанные;
		Если текСтрока <> Неопределено Тогда
			Если ПараметрыРежима.ОтображатьПоследниеДанныеТрекера Тогда
				ОбновитьПоследниеДанныеТрекера(текСтрока);
			КонецЕсли;
			Если ПараметрыРежима.ОтображатьПоследниеДанныеДатчиков Тогда
				ОбновитьПоследниеДанныеДатчиковНаСервере(текСтрока.ТранспортноеСредство);
			КонецЕсли;
			Если (ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером > 0) И ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи Тогда
				Элементы.СписокПериодовОтсутствияСоединения.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект", текСтрока.Рейс);
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	СброситьАнимациюТрека();

КонецПроцедуры

&НаКлиенте
Процедура СброситьАнимациюТрека()
	
	Если ЭтаФорма.КартаСформирована Тогда
		Если АнимацияЗагружена Тогда
			ВыполнитьПроцедуруHTML("TrackAnimationStop()");
		КонецЕсли; 
		ЭтаФорма.АнимацияЗагружена = Ложь;
		ВыполнитьПроцедуруHTML("ResetTrackAnimation()");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокРейсовПриАктивизацииПоляОбработчикОжидания()
	
	Если РежимРабочегоМеста = 0 Тогда
		текСтрока = Элементы.СписокРейсов.ТекущиеДанные;
		текЭлемент = Элементы.СписокРейсов.ТекущийЭлемент;
		Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
			Если текЭлемент.Имя = "СписокРейсовПометка" Тогда
				Пометка = Не текСтрока.Пометка;
				ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокРейсов.НайтиПоИдентификатору(текИдентификатор);
					текСтрока.Пометка = Пометка;
					Если Не текСтрока.Пометка Тогда
						Если текСтрока.ПометкаТрек Тогда
							текСтрока.ПометкаТрек                   = Ложь;
							текСтрока.ПометкаТрекАвтоотключение     = Истина;
						КонецЕсли;
						Если текСтрока.ПометкаТрекПлан Тогда
							текСтрока.ПометкаТрекПлан               = Ложь;
							текСтрока.ПометкаТрекПланАвтоотключение = Истина;
						КонецЕсли;
					КонецЕсли;
					ИзменитьВидимостьРейса(текСтрока);
					Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
						ИзменитьВидимостьСобытийПоОбъектуМониторинга(текСтрока);
					КонецЕсли; 
				КонецЦикла;
				
				Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте И Не СписокРейсов.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() Тогда
					Для каждого текСтрокаСобытия Из СписокВидимостиСобытийМониторинга Цикл
						Если текСтрокаСобытия.Пометка Тогда
							текСтрокаСобытия.Пометка = Ложь;
							текСтрокаСобытия.ПометкаАвтоотключение = Истина;
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли;
				
			ИначеЕсли текЭлемент.Имя = "СписокРейсовПометкаТрек" Тогда
				Пометка = Не текСтрока.ПометкаТрек;
				ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокРейсов.НайтиПоИдентификатору(текИдентификатор);
					текСтрока.ПометкаТрекРучноеОтключение = Не Пометка;
					
					Если текСтрока.Пометка И текСтрока.КоординатыТрека <> "" Тогда
						текСтрока.ПометкаТрек = Пометка;
						текСтрока.ПометкаТрекАвтоотключение = Ложь;
						ИзменитьВидимостьТрека(текСтрока.ИндексВМассиве, текСтрока.ПометкаТрек);
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли текЭлемент.Имя = "СписокРейсовПометкаТрекПлан" Тогда
				Пометка = Не текСтрока.ПометкаТрекПлан;
				ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокРейсов.НайтиПоИдентификатору(текИдентификатор);
					
					Если текСтрока.Пометка Тогда
						ПерерисоватьТрек = Ложь;
						Если текСтрока.КоординатыТрекаПлан = "" Тогда
							ЗаполнитьПлановыеМаршрутыРейсов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(текСтрока.Рейс));
							ПерерисоватьТрек = Истина;
						КонецЕсли; 	
						текСтрока.ПометкаТрекПлан = Пометка;
						текСтрока.ПометкаТрекПланАвтоотключение = Ложь;
						ИзменитьВидимостьТрекаПлан(текСтрока, ПерерисоватьТрек);
					КонецЕсли; 
				КонецЦикла;
				
			ИначеЕсли текЭлемент.Имя = "СписокРейсовПозиционирование" Тогда
				Если текСтрока.Пометка Тогда
					ЦентрироватьСлойНаКарте(текСтрока.ИндексВМассиве);
				КонецЕсли;
				
			ИначеЕсли текЭлемент.Имя = "СписокРейсовСлежение" Тогда
				Пометка = Не текСтрока.Слежение;
				ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокРейсов.НайтиПоИдентификатору(текИдентификатор);
					текСтрока.Слежение = Пометка;
				КонецЦикла;
				
			КонецЕсли;
			
			ОбеспечитьВозможностьПовторнойАктивизацииПоля();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРейсовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.СписокРейсов.ТекущиеДанные;
	Если текСтрока <> Неопределено И НЕ (Поле = Элементы.СписокРейсовПометка Или Поле = Элементы.СписокРейсовПометкаТрек Или Поле = Элементы.СписокРейсовПометкаТрекПлан
		Или Поле = Элементы.СписокРейсовПозиционирование Или Поле = Элементы.СписокРейсовСлежение Или Поле = Элементы.СписокРейсовДетальныйАнализ) Тогда
		ОткрытьФорму("Документ.упРейс.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Рейс), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СобытияЭлементовРежимаМониторингТранспорта

&НаКлиенте
Процедура СписокТранспортныхСредствПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СписокТранспортныхСредствПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТранспортныхСредствПриАктивизацииПоля(Элемент)
	
	Если КартаСформирована Тогда 
		Если АктивизацияПоляВСпискеОбъектовВозможна <> Истина Тогда
			ПодключитьОбработчикОжидания("РазрешитьАктивизациюПоляВСпискеОбъектов", 0.1, Истина);
		Иначе
			ПодключитьОбработчикОжидания("СписокТранспортныхСредствПриАктивизацииПоляОбработчикОжидания", 0.2, Истина);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТранспортныхСредствПриАктивизацииСтрокиОбработчикОжидания()
	
	Если РежимРабочегоМеста = 1 Тогда
		текСтрока = Элементы.СписокТранспортныхСредств.ТекущиеДанные;
		Если текСтрока <> Неопределено Тогда
			Если ПараметрыРежима.ОтображатьПоследниеДанныеТрекера Тогда
				ОбновитьПоследниеДанныеТрекера(текСтрока);
			КонецЕсли;
			Если ПараметрыРежима.ОтображатьПоследниеДанныеДатчиков Тогда
				ОбновитьПоследниеДанныеДатчиковНаСервере(текСтрока.ТранспортноеСредство);
			КонецЕсли;
			Если (ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером > 0) И ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи Тогда
				Элементы.СписокПериодовОтсутствияСоединения.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект", текСтрока.ТранспортноеСредство);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	СброситьАнимациюТрека();

КонецПроцедуры

&НаКлиенте
Процедура СписокТранспортныхСредствПриАктивизацииПоляОбработчикОжидания()
	
	Если РежимРабочегоМеста = 1 Тогда
		текСтрока = Элементы.СписокТранспортныхСредств.ТекущиеДанные;
		текЭлемент = Элементы.СписокТранспортныхСредств.ТекущийЭлемент;
		Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
			Если текЭлемент.Имя = "СписокТранспортныхСредствПометка" Тогда	
				Пометка = Не текСтрока.Пометка;
				ВыделенныеСтроки = Элементы.СписокТранспортныхСредств.ВыделенныеСтроки;
				
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокТранспортныхСредств.НайтиПоИдентификатору(текИдентификатор);
					Если текСтрока = Неопределено Или текСтрока.Пометка = Пометка Тогда
						Продолжить;
					КонецЕсли;
					
					текСтрока.Пометка = Пометка;
					Если Не текСтрока.Пометка И текСтрока.ПометкаТрек Тогда
						текСтрока.ПометкаТрек               = Ложь;
						текСтрока.ПометкаТрекАвтоотключение = Истина;
					КонецЕсли;
					ИзменитьВидимостьТранспортногоСредства(текСтрока);
					Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
						ИзменитьВидимостьСобытийПоОбъектуМониторинга(текСтрока);
					КонецЕсли;
				КонецЦикла;
				
				Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте И Не СписокТранспортныхСредств.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() Тогда
					Для каждого текСтрокаСобытия Из СписокВидимостиСобытийМониторинга Цикл
						Если текСтрокаСобытия.Пометка Тогда
							текСтрокаСобытия.Пометка = Ложь;
							текСтрокаСобытия.ПометкаАвтоотключение = Истина;
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли;
				
			ИначеЕсли текЭлемент.Имя = "СписокТранспортныхСредствПометкаТрек" Тогда
				Пометка = Не текСтрока.ПометкаТрек;
				ВыделенныеСтроки = Элементы.СписокТранспортныхСредств.ВыделенныеСтроки;
				
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокТранспортныхСредств.НайтиПоИдентификатору(текИдентификатор);
					Если текСтрока = Неопределено Или текСтрока.ПометкаТрек = Пометка Или Не текСтрока.Пометка Или текСтрока.КоординатыТрека = "" Тогда
						Продолжить;
					КонецЕсли;
					
					текСтрока.ПометкаТрек = Пометка;
					текСтрока.ПометкаТрекАвтоотключение = Ложь;
					текСтрока.ПометкаТрекРучноеОтключение = Не Пометка;
					ИзменитьВидимостьТрека(текСтрока.ИндексВМассиве, текСтрока.ПометкаТрек);
				КонецЦикла;
				
			ИначеЕсли текЭлемент.Имя = "СписокТранспортныхСредствСлежение" Тогда
				Пометка = Не текСтрока.Слежение;
				ВыделенныеСтроки = Элементы.СписокТранспортныхСредств.ВыделенныеСтроки;
				
				Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
					текСтрока = СписокТранспортныхСредств.НайтиПоИдентификатору(текИдентификатор);
					текСтрока.Слежение = Пометка;
				КонецЦикла;
				
			ИначеЕсли текЭлемент.Имя = "СписокТранспортныхСредствПозиционирование" Тогда
				Если текСтрока.Пометка Тогда
					ЦентрироватьСлойНаКарте(текСтрока.ИндексВМассиве);
				КонецЕсли;
				
			КонецЕсли;
			
			Если Ложь
				Или текЭлемент.Имя = "СписокТранспортныхСредствПометка"
				Или текЭлемент.Имя = "СписокТранспортныхСредствПометкаТрек" 
				Или текЭлемент.Имя = "СписокТранспортныхСредствСлежение" 
				Или текЭлемент.Имя = "СписокТранспортныхСредствПозиционирование" 
			Тогда
				// изменяем текущий элемент для возможности его повторной активизации
				Элементы.СписокТранспортныхСредств.ТекущийЭлемент = Элементы.СписокТранспортныхСредствПредставление;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТранспортныхСредствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.СписокТранспортныхСредств.ТекущиеДанные;
	Если текСтрока <> Неопределено И НЕ (Поле = Элементы.СписокТранспортныхСредствПометка Или Поле = Элементы.СписокТранспортныхСредствПометкаТрек Или Поле = Элементы.СписокТранспортныхСредствСлежение
		Или Поле = Элементы.СписокТранспортныхСредствПозиционирование) Тогда
		ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ТранспортноеСредство), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

#Область ОбщиеКоманды

&НаКлиенте
Процедура УстановитьПараметрыМониторинга(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыМониторинга", ПараметрыМониторинга);
	ОткрытьФорму("Обработка.упРМСпутниковыйМониторинг.Форма.ФормаУстановкиПараметровМониторинга", ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыМониторингаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
	
&НаКлиенте
Процедура УказатьПараметрыРежима(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыМониторинга", ПараметрыРежима);
	
	Если РежимРабочегоМеста = 0 Тогда
		ОткрытьФорму("Обработка.упРМСпутниковыйМониторинг.Форма.ФормаУстановкиПараметровМониторингаРейсов",
		             ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыМониторингаРейсовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		ОткрытьФорму("Обработка.упРМСпутниковыйМониторинг.Форма.ФормаУстановкиПараметровМониторингаТранспорта",
		             ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыМониторингаТранспортаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура МасштабироватьКартуПоТочкам(Команда)
	
	МасштабироватьКартуПоОбъектамМониторинга()

КонецПроцедуры // МасштабироватьКартуПоТочкам()

&НаКлиенте
Процедура МасштабироватьКартуПоВидимымТочкам(Команда)
	
	МасштабироватьКартуПоОбъектамМониторинга(Истина);

КонецПроцедуры // МасштабироватьКартуПоВидимымТочкам()

&НаКлиенте
Процедура ОтобразитьНаправления(Команда)
	
	ОтображатьНаправления = Не ОтображатьНаправления;
	Элементы.ОтображатьНаправления.Пометка = ОтображатьНаправления;
	ИзменитьВидимостьНаправлений();
	
КонецПроцедуры //ОтобразитьНаправления()

&НаКлиенте
Процедура ОтобразитьНазвания(Команда)
	
	ОтображатьНазвания = Не ОтображатьНазвания;
	Элементы.ПоказатьУбратьНазванияТС.Пометка = ОтображатьНазвания;
	ИзменитьВидимостьНазваний();
	
КонецПроцедуры //ОтобразитьНазвания()

&НаКлиенте
Процедура ОтобразитьЗоны(Команда)
	
	ОтображатьЗоны = Не ОтображатьЗоны;
	Элементы.ОтобразитьЗоны.Пометка = ОтображатьЗоны;
	ИзменитьВидимостьЗон();
	
КонецПроцедуры

&НаКлиенте
Функция ОтобразитьМестаИнтереса(МестоИнтереса)
	
	Данные = "'" + МестоИнтереса.ДанныеМаркера + "', " + МестоИнтереса.Данные;
	
	Индекс = ВыполнитьФункциюHTML("addSiteInterest(" + Данные + ")");
	
	Возврат Индекс;
	
КонецФункции // ОтобразитьМестаИнтереса()

&НаКлиенте
Процедура ИзменитьВидимостьМестаИнтереса(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда
		ВыполнитьПроцедуруHTML("showSiteInterest(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideSiteInterest(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьМестаИнтереса()

&НаКлиенте
Процедура ОтправитьСообщениеВодителю(Команда)
	
	Если РежимРабочегоМеста = 0 Тогда
		текСтрока = Элементы.СписокРейсов.ТекущиеДанные;
		Если текСтрока = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В списке рейсов не указан рейс! Операция невозможна");
			
		ИначеЕсли текСтрока.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен") Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Указанный рейс находится в статусе ""Выполнен""! Операция невозможна");
			
		Иначе
			ЗначенияЗаполнения = Новый Структура("ТипСобытия, ТранспортноеСредство, Рейс, Дата",
			                                      ПредопределенноеЗначение("Справочник.упТипыСобытийМониторинга.СообщениеОтДиспетчера"),
												  текСтрока.ТранспортноеСредство,
												  текСтрока.Рейс,
												  ТекущаяДата());
			сткПараметры       = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.упСобытиеМониторинга.ФормаОбъекта", сткПараметры, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли; 
		
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		текСтрока = Элементы.СписокТранспортныхСредств.ТекущиеДанные;
		Если текСтрока = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В списке транспортных средств не указано транспортное средство! Операция невозможна");
		Иначе
			ЗначенияЗаполнения = Новый Структура("ТипСобытия, ТранспортноеСредство, Дата", ПредопределенноеЗначение("Справочник.упТипыСобытийМониторинга.СообщениеОтДиспетчера"), текСтрока.ТранспортноеСредство, ТекущаяДата());
			сткПараметры       = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.упСобытиеМониторинга.ФормаОбъекта", сткПараметры, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли; 
	КонецЕсли; 
	
	
	
КонецПроцедуры

#КонецОбласти 

#Область РежимМониторингРейсов

&НаКлиенте
Процедура УстановитьВидимостьВыделенныхРейсов(Команда)
	
	ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
	Для каждого текСтрока Из СписокРейсов Цикл
		Если текСтрока.Пометка Тогда
			Если ВыделенныеСтроки.Найти(текСтрока.ПолучитьИдентификатор()) = Неопределено Тогда
				текСтрока.Пометка = Ложь;
				Если текСтрока.ПометкаТрек Тогда
					текСтрока.ПометкаТрек                   = Ложь;
					текСтрока.ПометкаТрекАвтоотключение     = Истина;
				КонецЕсли;
				Если текСтрока.ПометкаТрекПлан Тогда
					текСтрока.ПометкаТрекПлан               = Ложь;
					текСтрока.ПометкаТрекПланАвтоотключение = Истина;
				КонецЕсли;

				ИзменитьВидимостьРейса(текСтрока);
				Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
					ИзменитьВидимостьСобытийПоОбъектуМониторинга(текСтрока);
				КонецЕсли;
			КонецЕсли; 
		Иначе
			Если Не ВыделенныеСтроки.Найти(текСтрока.ПолучитьИдентификатор()) = Неопределено Тогда
				текСтрока.Пометка = Истина;
				ИзменитьВидимостьРейса(текСтрока);
				Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
					ИзменитьВидимостьСобытийПоОбъектуМониторинга(текСтрока);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте И Не СписокРейсов.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() Тогда
		Для каждого текСтрокаСобытия Из СписокВидимостиСобытийМониторинга Цикл
			Если текСтрокаСобытия.Пометка Тогда
				текСтрокаСобытия.Пометка = Ложь;
				текСтрокаСобытия.ПометкаАвтоотключение = Истина;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьАктивизациюПоляВСпискеОбъектов()
	
	ОбеспечитьВозможностьПовторнойАктивизацииПоля();
	АктивизацияПоляВСпискеОбъектовВозможна = Истина;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокРейсов(Команда)
	
	// серверные вызовы
	ОбновитьСписокВидимостиСобытийМониторингаНаСервере();
	
	АктивизацияПоляВСпискеОбъектовВозможна = Ложь;
	ОбновитьСписокРейсовНаСервере();
	ПодключитьОбработчикОжидания("РазрешитьАктивизациюПоляВСпискеОбъектов", 0.1, Истина);
	
	// клиентские вызовы
	ОчиститьОбъекты();
	//Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
	//	ОчиститьСобытияМониторинга();
	//КонецЕсли; 
	ОбновитьДанныеФормыДляОтображения(); //
	ОтобразитьОбъектыНаКарте();
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСписокРейсов(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Настройки", КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Настройка отбора списка рейсов'"));
	
	ОткрытьФорму("ОбщаяФорма.упФормаНастройкиОтбора", ПараметрыФормы,,,,, Новый ОписаниеОповещения("НастройкаРейсовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВариантНастройкиСпискаРейсов(Команда)
	
	сткПараметры  = Новый Структура("КлючОбъекта, КлючТекущихНастроекМониторингТранспорта", "Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаРейсов", КлючТекущихНастроекМониторингРейсов);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаСохранения", сткПараметры,,,,,Новый ОписаниеОповещения("СохранитьВариантНастройкиСпискаРейсовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройкиСпискаРейсов(Команда)
	
	сткПараметры  = Новый Структура("КлючОбъекта, КлючТекущихНастроекМониторингТранспорта", "Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаРейсов", КлючТекущихНастроекМониторингТранспорта);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаЗагрузки", сткПараметры,,,,,Новый ОписаниеОповещения("ОткрытьВариантНастройкиСпискаРейсовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРейс(Команда)
	
	текСтрока = Элементы.СписокРейсов.ТекущиеДанные;
	Если текСтрока = Неопределено Или НЕ ЗначениеЗаполнено(текСтрока.Рейс) Тогда
		ТекстСообщения = НСтр("ru = 'В списке рейсов не выбрана строка с рейсом для изменения'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	Если НЕ упРаботаСоСтатусамиКлиентСервер.РазрешеноРедактироватьМаршрутРейса(текСтрока.Статус,ОписаниеОшибки)  Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Рейс", текСтрока.Рейс);
	сткПараметры.Вставить("НачалоПериодаПланирования", 		текСтрока.ПлановоеНачалоВыполнения);
	сткПараметры.Вставить("ОкончаниеПериодаПланирования",  	текСтрока.ПлановоеОкончаниеВыполнения);
	сткПараметры.Вставить("РежимКорректировки", 			РежимКорректировки(текСтрока.Статус));
	
	сткПараметрыПланирования = Новый Структура();
	сткПараметрыПланирования.Вставить("МетодРешенияЗК", ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗК.ЖадныйАлгоритм"));
	сткПараметрыПланирования.Вставить("ВариантОптимизацииМаршрута", ПредопределенноеЗначение("Перечисление.упВариантыОптимизацииМаршрута.ПоРасстоянию"));
	сткПараметрыПланирования.Вставить("ПространственныйВес", 0.4);
	сткПараметрыПланирования.Вставить("ВременнойВес", 0.4);
	сткПараметрыПланирования.Вставить("ВесСрочности", 0.2);
	сткПараметры.Вставить("ПараметрыПланирования", сткПараметрыПланирования);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаРедактированияРейса", сткПараметры,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ИзменитьРейс()

#КонецОбласти 

#Область РежимМониторингТранспорта

&НаКлиенте
Процедура УстановитьВидимостьВыделенныхТС(Команда)
	
	ВыделенныеСтроки = Элементы.СписокТранспортныхСредств.ВыделенныеСтроки;
	Для каждого текСтрока Из СписокТранспортныхСредств Цикл
		Если текСтрока.Пометка Тогда
			Если ВыделенныеСтроки.Найти(текСтрока.ПолучитьИдентификатор()) = Неопределено Тогда
				текСтрока.Пометка = Ложь;
				ИзменитьВидимостьТранспортногоСредства(текСтрока);
				Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
					ИзменитьВидимостьСобытийПоОбъектуМониторинга(текСтрока);
				КонецЕсли;
			КонецЕсли; 
		Иначе
			Если Не ВыделенныеСтроки.Найти(текСтрока.ПолучитьИдентификатор()) = Неопределено Тогда
				текСтрока.Пометка = Истина;
				ИзменитьВидимостьТранспортногоСредства(текСтрока);
				Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
					ИзменитьВидимостьСобытийПоОбъектуМониторинга(текСтрока);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте И Не СписокТранспортныхСредств.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() Тогда
		Для каждого текСтрокаСобытия Из СписокВидимостиСобытийМониторинга Цикл
			Если текСтрокаСобытия.Пометка Тогда
				текСтрокаСобытия.Пометка = Ложь;
				текСтрокаСобытия.ПометкаАвтоотключение = Истина;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокТранспортныхСредств(Команда)
	
	// серверные вызовы
	ОбновитьСписокВидимостиСобытийМониторингаНаСервере();
	АктивизацияПоляВСпискеОбъектовВозможна = Ложь;
	ОбновитьСписокТранспортныхСредствНаСервере();
	ПодключитьОбработчикОжидания("РазрешитьАктивизациюПоляВСпискеОбъектов", 0.1, Истина);
	
	// клиентские вызовы
	ОчиститьОбъекты();
	//Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
	//    ОчиститьСобытияМониторинга();
	//КонецЕсли; 
	ОбновитьДанныеФормыДляОтображения(); //
	ОтобразитьОбъектыНаКарте();
	ОбновитьДоступность(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСписокТранспортныхСредств(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Настройки", КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Настройка отбора списка транспортных средств'"));
	
	ОткрытьФорму("ОбщаяФорма.упФормаНастройкиОтбора", ПараметрыФормы,,,,, Новый ОписаниеОповещения("НастройкаТранспортныхСредствЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВариантНастройкиСпискаТранспортныхСредств(Команда)
	
	сткПараметры  = Новый Структура("КлючОбъекта, КлючТекущихНастроекМониторингТранспорта", "Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаТранспортныхСредств", КлючТекущихНастроекМониторингТранспорта);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаСохранения", сткПараметры,,,,,Новый ОписаниеОповещения("СохранитьВариантНастройкиСпискаТранспортныхСредствЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройкиСпискаТранспортныхСредств(Команда)
	
	сткПараметры  = Новый Структура("КлючОбъекта, КлючТекущихНастроекМониторингТранспорта", "Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаТранспортныхСредств", КлючТекущихНастроекМониторингТранспорта);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаЗагрузки", сткПараметры,,,,,Новый ОписаниеОповещения("ОткрытьВариантНастройкиСпискаТранспортныхСредствЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти 

#Область МестаИнтереса

&НаКлиенте
Процедура КомандаОтобразитьМестаИнтереса(Команда)
	
	Если НЕ СписокМестаИнтереса.Количество() Тогда
		ОбновитьСписокМестИнтересаНаСервере();
	КонецЕсли; 
	
	ОтображатьМестаИнтереса = НЕ ОтображатьМестаИнтереса;
	
	Элементы.ОтобразитьМестаИнтереса.Пометка = ОтображатьМестаИнтереса;
	ИзменитьВидимостьМестИнтереса(ОтображатьМестаИнтереса);
	
КонецПроцедуры // КомандаОтобразитьМестаИнтереса()

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область МестаИнтересаОбработка

&НаКлиенте
Процедура ОбработатьМаркер(ЭлементКоллекции, Префикс = "", Режим = 0)
	
	Стиль = "";
	текКартинка = ЭлементКоллекции["КартинкаМаркера" + Префикс];
	Если ЭлементКоллекции["ЭтоКартинка" + Префикс] Тогда
		мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Маркер",  текКартинка));
		Если мсвСохраненныйМаркер.Количество() Тогда 
			Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Файл) Тогда 
				Стиль = мсвСохраненныйМаркер[0].Файл;
			КонецЕсли;	
		Иначе	
			АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = текКартинка;
			НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			Стиль = НовыйСохраненныйМаркер.Файл; 
		КонецЕсли;	
	Иначе 
		мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Стиль",  ЭлементКоллекции["СтильМаркера" + Префикс]));
		Если мсвСохраненныйМаркер.Количество() Тогда
			Стиль = мсвСохраненныйМаркер[0].Файл;
		Иначе
			АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");	
			ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Стиль = ЭлементКоллекции["СтильМаркера" + Префикс];
			НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			Стиль = НовыйСохраненныйМаркер.Файл;
		КонецЕсли;
	КонецЕсли;
	Если Режим = 0 Тогда
		ЭлементКоллекции["ДанныеМаркера" + Префикс] = Стиль;
	Иначе		
		ЭлементКоллекции["ДанныеМаркера" + Префикс] = "'" + Стиль + "', " + ЭлементКоллекции["ДанныеМаркера" + Префикс];
	КонецЕсли;

	
КонецПроцедуры // ОбработатьМаркер()


&НаКлиенте
Процедура ИзменитьВидимостьМестИнтереса(Пометка = Ложь)
	
	Если Пометка Тогда
		
		ОбновитьМестаИнтересаНаКлиенте();
		
		Для каждого МестоИнтереса Из СписокМестаИнтереса Цикл
			Если МестоИнтереса.ИндексВМассиве = 0 Тогда
				МестоИнтереса.ИндексВМассиве = ОтобразитьМестаИнтереса(МестоИнтереса);
			Иначе
				ИзменитьВидимостьМестаИнтереса(МестоИнтереса.ИндексВМассиве, Истина);
			КонецЕсли;
		КонецЦикла;		
	Иначе
		Для каждого МестоИнтереса Из СписокМестаИнтереса Цикл
			Если МестоИнтереса.ИндексВМассиве > 0 Тогда
				ИзменитьВидимостьМестаИнтереса(МестоИнтереса.ИндексВМассиве, Ложь);
			КонецЕсли; 
		КонецЦикла;	
	КонецЕсли; 	
	
КонецПроцедуры // ИзменитьВидимостьМестИнтереса()

&НаКлиенте
// Процедура обновляет Места Интереса для отображения
//
// Параметры:
//  Нет.
//
Процедура ОбновитьМестаИнтересаНаКлиенте()
	
	Для каждого МестоИнтереса Из СписокМестаИнтереса Цикл
		ОбработатьМаркер(МестоИнтереса);
	КонецЦикла;
	
КонецПроцедуры // ОбновитьМестаИнтересаНаКлиенте()

&НаСервере
// Заполним список мест интереса
//
// Параметры:
//  Нет.
//
Процедура ОбновитьСписокМестИнтересаНаСервере()
	
	МестаИнтереса = ПараметрыМониторинга.МестаИнтереса;
	Если Не МестаИнтереса.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СписокМестаИнтереса.Очистить();
	
	Запрос = Новый Запрос;                       
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МестаИнтереса.Ссылка КАК МестоИнтереса,
	|	МестаИнтереса.Адрес КАК Адрес,
	|	МестаИнтереса.Адрес.Долгота КАК Долгота,
	|	МестаИнтереса.Адрес.Широта КАК Широта,
	|	МестаИнтереса.Адрес.Представление КАК ПредставлениеАдрес,
	|	МестаИнтереса.Ссылка.Представление КАК ПредставлениеМестоИнтереса,
	|	МестаИнтереса.Ссылка.СтильМаркера КАК СтильМаркера
	|ПОМЕСТИТЬ втМестаИнтереса
	|ИЗ
	|	Справочник.упМестаИнтереса.Адреса КАК МестаИнтереса
	|ГДЕ
	|	МестаИнтереса.Ссылка В(&МестаИнтереса)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтилиМаркеров.Ссылка КАК Стиль,
	|	упСтилиМаркеров.Картинка,
	|	упСтилиМаркеров.ХранилищеЗначения,
	|	упСтилиМаркеров.ПоложениеПривязкиX,
	|	упСтилиМаркеров.ПоложениеПривязкиY,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Ширина
	|		ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК Ширина,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Высота
	|		ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК Высота
	|ПОМЕСТИТЬ втСтильМаркераПоУмолчанию
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	упСтилиМаркеров.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.Основной)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМестаИнтереса.МестоИнтереса КАК МестоИнтереса,
	|	втМестаИнтереса.ПредставлениеМестоИнтереса КАК ПредставлениеМестоИнтереса,
	|	втМестаИнтереса.Адрес КАК Адрес,
	|	втМестаИнтереса.ПредставлениеАдрес КАК ПредставлениеАдрес,
	|	втМестаИнтереса.Долгота КАК Долгота,
	|	втМестаИнтереса.Широта КАК Широта,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Стиль
	|		ИНАЧЕ втМестаИнтереса.СтильМаркера
	|	КОНЕЦ КАК СтильМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Картинка
	|		ИНАЧЕ упСтилиМаркеров.Картинка
	|	КОНЕЦ КАК КартинкаМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.ХранилищеЗначения
	|		ИНАЧЕ упСтилиМаркеров.ХранилищеЗначения
	|	КОНЕЦ КАК ХранилищеЗначенияМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.ПоложениеПривязкиX
	|		ИНАЧЕ упСтилиМаркеров.ПоложениеПривязкиX
	|	КОНЕЦ КАК ПоложениеПривязкиXМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.ПоложениеПривязкиY
	|		ИНАЧЕ упСтилиМаркеров.ПоложениеПривязкиY
	|	КОНЕЦ КАК ПоложениеПривязкиYМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Ширина
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упСтилиМаркеров.Масштаб = 0
	|					ТОГДА упСтилиМаркеров.Ширина
	|				ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|			КОНЕЦ
	|	КОНЕЦ КАК ШиринаМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Высота
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упСтилиМаркеров.Масштаб = 0
	|					ТОГДА упСтилиМаркеров.Высота
	|				ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|			КОНЕЦ
	|	КОНЕЦ КАК ВысотаМаркера
	|ИЗ
	|	втМестаИнтереса КАК втМестаИнтереса
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|		ПО втМестаИнтереса.СтильМаркера = упСтилиМаркеров.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтильМаркераПоУмолчанию КАК втСтильМаркераПоУмолчанию
	|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("МестаИнтереса", МестаИнтереса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = СписокМестаИнтереса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
		НоваяСтрока.Пометка = Истина;
		
		Если Выборка.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
			НоваяСтрока.КартинкаМаркера = Выборка.ХранилищеЗначенияМаркера.Получить();
		Иначе
			НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркера);
			НоваяСтрока.ЭтоКартинка = Истина;
		КонецЕсли;
		
		ДанныеМаркера = СтрШаблон("'%1;%2;%3;%4;%5;%6;%7;%8'", Выборка.ПредставлениеМестоИнтереса,
			Выборка.ПредставлениеАдрес,
			ПредставлениеЧисла(Выборка.Долгота),
			ПредставлениеЧисла(Выборка.Широта),
			ПредставлениеЧисла(НоваяСтрока.ПоложениеПривязкиXМаркера),
			ПредставлениеЧисла(НоваяСтрока.ПоложениеПривязкиYМаркера),
			ПредставлениеЧисла(НоваяСтрока.ШиринаМаркера),
			ПредставлениеЧисла(НоваяСтрока.ВысотаМаркера));
		
		НоваяСтрока.Данные = ДанныеМаркера;
		
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСписокМестИнтересаНаСервере()

#КонецОбласти 

#Область РаботаСКартой

&НаКлиенте
// Обработчик действия ожидания т.к. карты Google и Яндекс не грузяться тогдавыполним повторный вызов.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьОтложенноеДействие()

	HTMLДокументДокументСформирован(Элементы.HTMLДокумент);

КонецПроцедуры // ВыполнитьОтложенноеДействие()

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ - Данные.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Нет.
// 
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ - Данные.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Строка - значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецФункции

&НаКлиенте
Процедура ИзменитьТекущуюСтрокуВСписке()

	КоличествоВыделенныхМаркеров = ВыполнитьФункциюHTML("CheckCountOfSelectedMarkers()");
	Если КоличествоВыделенныхМаркеров Тогда
		ПредставлениеВыделенныхТочек = ВыполнитьФункциюHTML("getSelectedMarkers()");
		мсвТочек = СтрРазделить(ПредставлениеВыделенныхТочек, ";id=", Ложь);
		Если мсвТочек.Количество() Тогда
			текЭлемент = Число(мсвТочек[0]);
			Если РежимРабочегоМеста = 0 Тогда
				ИскомыеСтроки = СписокРейсов.НайтиСтроки(Новый Структура("ИндексВМассиве", текЭлемент));
				Если ИскомыеСтроки.Количество() Тогда
					Элементы.СписокРейсов.ТекущаяСтрока = ИскомыеСтроки[0].ПолучитьИдентификатор()
				КонецЕсли;
			ИначеЕсли РежимРабочегоМеста = 1 Тогда
				ИскомыеСтроки = СписокТранспортныхСредств.НайтиСтроки(Новый Структура("ИндексВМассиве", текЭлемент));
				Если ИскомыеСтроки.Количество() Тогда
					Элементы.СписокТранспортныхСредств.ТекущаяСтрока = ИскомыеСтроки[0].ПолучитьИдентификатор()
				КонецЕсли;
			Иначе
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;	

КонецПроцедуры // ИзменитьТекущуюСтрокуВСписке()

&НаКлиенте
// Выполнить установку начальных параметров макета.
//
// Параметры:
//  Нет.
//
Процедура УстановитьПараметрыМакета()
		
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%КлючAPI%",    СокрЛП(КлючAPI));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Широта%",     ПредставлениеЧисла(НачальнаяШирота));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Долгота%",    ПредставлениеЧисла(НачальнаяДолгота));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Увеличение%", ПредставлениеЧисла(НачальныйЗум));
	
	сткКартинок = Новый Структура();
	сткКартинок.Вставить("упМаркерПростойБелый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойБелый, Неопределено));
	сткКартинок.Вставить("упМаркерПростойЖелтый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЖелтый, Неопределено));
	сткКартинок.Вставить("упМаркерПростойКрасный", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойКрасный, Неопределено));
	сткКартинок.Вставить("упГеокодируемыйМаркер", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упГеокодируемыйМаркер, Неопределено));
	сткКартинок.Вставить("упМаркерПростойЗеленый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЗеленый, Неопределено));
	сткКартинок.Вставить("упТумблер",             	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упТумблер, Неопределено));
	
	упСервисныеФункцииВызовСервера.ЗаполнитьДвоичныеДанныеКартинок(сткКартинок);
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упТумблер.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%toggle%", ИмяВременногоФайла);
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойБелый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерРедактируемогоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойБелый");
	НовыйСохраненныйМаркер.Адрес  = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЖелтый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПервогоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойЖелтый");
	НовыйСохраненныйМаркер.Адрес  = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойКрасный.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПоследнегоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойКрасный");
	НовыйСохраненныйМаркер.Адрес  = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упГеокодируемыйМаркер.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерГеокодирования%", ИмяВременногоФайла);
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЗеленый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерДвиженияТС%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойЗеленый");
	НовыйСохраненныйМаркер.Адрес  = ИмяВременногоФайла;
	
КонецПроцедуры //УстановитьПараметрыМакета()

&НаКлиенте
Процедура ОтобразитьОбъектыНаКарте()
	
	Если КартаСформирована Тогда
		ОбновитьСписокМаркеровНаКлиенте();
		
		Если РежимРабочегоМеста = 0 Тогда
			ОтобразитьРейсыНаКарте();
		ИначеЕсли РежимРабочегоМеста = 1 Тогда
			ОтобразитьТранспортНаКарте();
		Иначе
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры // ОтобразитьОбъектыНаКарте()

&НаКлиенте
Процедура ОтобразитьТранспортНаКарте()
	
	ОчиститьСобытияМониторинга();
	//ОчиститьОбъекты();
	Если СписокТранспортныхСредств.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	соотДанные = ПолучитьСоответствиеДанныхМаркеров();
	
	мсвКоординат = Новый Массив;
	мсвКоординатПозиционирования = Новый Массив;
	КоличествоМаркеров = ВыполнитьФункциюHTML("CheckCountOfMarkers()");
	ОтображатьТекущиеСостояния = ПараметрыРежима.ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте;
	Для каждого Строка Из СписокТранспортныхСредств Цикл
		Широта  = ПредставлениеЧисла(Строка.Широта);
		Долгота = ПредставлениеЧисла(Строка.Долгота);
		Если Строка.Обновлена Тогда
			ДанныеМаркера = соотДанные.Получить(Строка.СтильМаркера);
			
			// основной маркер
			мсвПараметров = Новый Массив;
			мсвПараметров.Добавить(Широта);
			мсвПараметров.Добавить(Долгота);
			мсвПараметров.Добавить(ДанныеМаркера);
			мсвПараметров.Добавить(?(ОтображатьТекущиеСостояния, СписокКартинок[Строка.ИндексСостояния], ""));
			мсвПараметров.Добавить(ПредставлениеЧисла(Строка.ИндексВМассиве));
			мсвПараметров.Добавить(?(Строка.Пометка, "true", "false"));
			мсвПараметров.Добавить(?(Строка.ПометкаТрек, "true", "false"));
			
			мсвКоординат.Добавить(СтрСоединить(мсвПараметров, ";")); 
			
			// представление объекта
			мсвКоординат.Добавить(ПредставлениеСтроки(Строка.Представление));
			
			// направление движения
			мсвПараметров = Новый Массив;
			мсвПараметров.Добавить(СписокКартинок[5]);
			мсвПараметров.Добавить(ПредставлениеЧисла(Строка.НаправлениеДвижения));
			
			мсвКоординат.Добавить(СтрСоединить(мсвПараметров, ";"));
			
			// трек
			//Если Строка.ПометкаТрек Тогда
			//	Строка.Обновлена = Ложь;
			//	Если ЗначениеЗаполнено(Строка.КоординатыТрека) Тогда
			//		мсвКоординат.Добавить(Строка.КоординатыТрека);
			//	Иначе
			//		мсвКоординат.Добавить("null"); 
			//	КонецЕсли;
			//Иначе
			//	мсвКоординат.Добавить("null");
			//КонецЕсли;
			мсвКоординат.Добавить(?(ЗначениеЗаполнено(Строка.КоординатыТрека), Строка.КоординатыТрека, "null"));
			Строка.Обновлена = Ложь;
			
			Если Строка.ИндексВМассиве = 0 Тогда
				КоличествоМаркеров = КоличествоМаркеров + 1;
				Строка.ИндексВМассиве = КоличествоМаркеров;
			КонецЕсли; 
		КонецЕсли;
		
		Если Строка.Слежение Тогда
			мсвКоординатПозиционирования.Добавить(Широта);
			мсвКоординатПозиционирования.Добавить(Долгота);
		КонецЕсли; 
	КонецЦикла;
	
	Если мсвКоординат.Количество() Тогда
		ОтобразитьДанныеНаКарте(СтрСоединить(мсвКоординат, "###"));
		
		Если мсвКоординатПозиционирования.Количество() Тогда
			ЦентрироватьТочкиНаКарте(СтрСоединить(мсвКоординатПозиционирования, ";") + ";");
		КонецЕсли;
	КонецЕсли;
	
	// события мониторинга
	мсвКоординатСобытий = Новый Массив;
	Если СписокТранспортныхСредств.Количество() И мсвКоординат.Количество() Тогда
		КоличествоМаркеров = 0;
		
		Для каждого Строка Из СписокВидимостиСобытийМониторинга Цикл
			Если Строка.Пометка Тогда
				спкИднексы = Строка.Индексы;
				Если спкИднексы = Неопределено Или Не спкИднексы.Количество() Тогда
					Продолжить;
				КонецЕсли;
				ДанныеМаркера = соотДанные.Получить(Строка.СтильМаркера);
				
				Для каждого спкИндексовПоТранспорту Из спкИднексы Цикл
					Для каждого ЭлементДанные Из спкИндексовПоТранспорту.Значение Цикл
						сткДанные = ЭлементДанные.Значение;
						Если сткДанные = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						мсвКоординатСобытий.Добавить(Строка.ПредставлениеТипа);
						мсвКоординатСобытий.Добавить(сткДанные.Период);
						мсвКоординатСобытий.Добавить(сткДанные.Широта);
						мсвКоординатСобытий.Добавить(сткДанные.Долгота);
						мсвКоординатСобытий.Добавить(ДанныеМаркера);
						
						КоличествоМаркеров = КоличествоМаркеров + 1;
						сткДанные.ИндексВМассиве = КоличествоМаркеров; 
					КонецЦикла; 
				КонецЦикла;                                                                
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
	Если мсвКоординатСобытий.Количество() Тогда
		ОтобразитьСобытияМониторингаНаКарте(СтрСоединить(мсвКоординатСобытий, ";"));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРейсыНаКарте()
	
	ОчиститьСобытияМониторинга();
	//ОчиститьОбъекты();
	Если Не СписокРейсов.Количество() Тогда
		Возврат;
	КонецЕсли; 
	
	мсвКоординат = Новый Массив;
	мсвКоординатПозиционирования = Новый Массив;
	КоличествоМаркеров = ВыполнитьФункциюHTML("CheckCountOfMarkers()");
	
	соотДанные = ПолучитьСоответствиеДанныхМаркеров();
	
	Для каждого Строка Из СписокРейсов Цикл
		Широта  = ПредставлениеЧисла(Строка.Широта);
		Долгота = ПредставлениеЧисла(Строка.Долгота);
		
		Если Строка.Обновлена Тогда
			ДанныеМаркера = соотДанные.Получить(Строка.СтильМаркера);
			
			// основной маркер			
			мсвПараметров = Новый Массив;
			мсвПараметров.Добавить(Широта);
			мсвПараметров.Добавить(Долгота);
			мсвПараметров.Добавить(ДанныеМаркера);
			мсвПараметров.Добавить(ПредставлениеЧисла(Строка.ИндексВМассиве));
			мсвПараметров.Добавить(?(Строка.Пометка, "true", "false"));
			мсвПараметров.Добавить(?(Строка.ПометкаТрек, "true", "false"));
			
			мсвКоординат.Добавить(СтрСоединить(мсвПараметров, ";")); 
			
			// представление объекта
			мсвКоординат.Добавить(ПредставлениеСтроки(Строка.Представление));
			
			// направление движения
			мсвПараметров = Новый Массив;
			мсвПараметров.Добавить(СписокКартинок[5]);
			мсвПараметров.Добавить(ПредставлениеЧисла(Строка.НаправлениеДвижения));
			
			мсвКоординат.Добавить(СтрСоединить(мсвПараметров, ";"));
			
			// трек
			Если Строка.ПометкаТрек Тогда
				Строка.Обновлена = Ложь;
				Если ЗначениеЗаполнено(Строка.КоординатыТрека) Тогда
					мсвКоординат.Добавить(Строка.КоординатыТрека);
				Иначе
					мсвКоординат.Добавить("null"); 
				КонецЕсли;
			Иначе
				мсвКоординат.Добавить("null");
			КонецЕсли;
			
			Если Строка.ИндексВМассиве = 0 Тогда
				КоличествоМаркеров = КоличествоМаркеров + 1;
				Строка.ИндексВМассиве = КоличествоМаркеров;
			КонецЕсли; 
			Строка.Обновлена = Ложь;
		КонецЕсли;
		
		Если Строка.Слежение Тогда
			мсвКоординатПозиционирования.Добавить(Широта);
			мсвКоординатПозиционирования.Добавить(Долгота);
		КонецЕсли;
	КонецЦикла;
	
	Если мсвКоординат.Количество() Тогда
		ОтобразитьДанныеПоРейсамНаКарте(СтрСоединить(мсвКоординат, "###"));
		
		Если мсвКоординатПозиционирования.Количество() Тогда
			ЦентрироватьТочкиНаКарте(СтрСоединить(мсвКоординатПозиционирования, ";") + ";");
		КонецЕсли;
	КонецЕсли;
	
	// события мониторинга
	мсвКоординатСобытий = Новый Массив;
	Если СписокРейсов.Количество() И мсвКоординат.Количество() Тогда
		КоличествоМаркеров = 0;
		Для каждого Строка Из СписокВидимостиСобытийМониторинга Цикл
			Если Строка.Пометка Тогда
				спкИднексы = Строка.Индексы;
				Если спкИднексы = Неопределено Или Не спкИднексы.Количество() Тогда
					Продолжить;
				КонецЕсли;
				ДанныеМаркера = соотДанные.Получить(Строка.СтильМаркера);
				
				Для каждого спкИндексовПоТранспорту Из спкИднексы Цикл
					Для каждого ЭлементДанные Из спкИндексовПоТранспорту.Значение Цикл
						сткДанные = ЭлементДанные.Значение;
						Если сткДанные = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						мсвКоординатСобытий.Добавить(Строка.ПредставлениеТипа);
						мсвКоординатСобытий.Добавить(сткДанные.Период);
						мсвКоординатСобытий.Добавить(сткДанные.Широта);
						мсвКоординатСобытий.Добавить(сткДанные.Долгота);
						мсвКоординатСобытий.Добавить(ДанныеМаркера);
						
						КоличествоМаркеров = КоличествоМаркеров + 1;
						сткДанные.ИндексВМассиве = КоличествоМаркеров; 
					КонецЦикла; 
				КонецЦикла; 
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
	Если мсвКоординатСобытий.Количество() Тогда
		ОтобразитьСобытияМониторингаНаКарте(СтрСоединить(мсвКоординатСобытий, ";"));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьЗоны(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда 
		ВыполнитьПроцедуруHTML("showZone(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideZone(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьТрека(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда 
		ВыполнитьПроцедуруHTML("showTrack(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideTrack(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьТрекаПлан(ТекущаяСтрока, ПерерисоватьТрек = Ложь)
	
	ИндексВМассиве = ТекущаяСтрока.ИндексВМассиве;
	Если ТекущаяСтрока.ПометкаТрекПлан Тогда 
		Если ПерерисоватьТрек Тогда
			ВыполнитьПроцедуруHTML("showPlanTrack(" + ПредставлениеЧисла(ИндексВМассиве) + ", true, """ + ТекущаяСтрока.КоординатыТрекаПлан + """)");
		Иначе
			ВыполнитьПроцедуруHTML("showPlanTrack(" + ПредставлениеЧисла(ИндексВМассиве) + ", false)");
		КонецЕсли;
	Иначе	
		ВыполнитьПроцедуруHTML("hidePlanTrack(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьТранспортногоСредства(ТекущаяСтрока)
	
	Если ТекущаяСтрока.Пометка Тогда 
		ВыполнитьПроцедуруHTML("showVehicleMarker(" + ПредставлениеЧисла(ТекущаяСтрока.ИндексВМассиве) + ", " + ?(ТекущаяСтрока.ПометкаТрекАвтоотключение, "true", "false") + ")");
		ТекущаяСтрока.ПометкаТрек = ТекущаяСтрока.ПометкаТрекАвтоотключение;
	Иначе	
		ВыполнитьПроцедуруHTML("hideVehicleMarker(" + ПредставлениеЧисла(ТекущаяСтрока.ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьРейса(ТекущаяСтрока)
	
	Если ТекущаяСтрока.Пометка Тогда 
		ВыполнитьПроцедуруHTML("showVehicleMarker(" + ПредставлениеЧисла(ТекущаяСтрока.ИндексВМассиве) + ", " + ?(ТекущаяСтрока.ПометкаТрекАвтоотключение, "true", "false") + ")");
		ТекущаяСтрока.ПометкаТрек = ТекущаяСтрока.ПометкаТрекАвтоотключение;
		ТекущаяСтрока.ПометкаТрекПлан = ТекущаяСтрока.ПометкаТрекПланАвтоотключение;
	Иначе	
		ВыполнитьПроцедуруHTML("hideVehicleMarker(" + ПредставлениеЧисла(ТекущаяСтрока.ИндексВМассиве) + ")");
	КонецЕсли;	
	ИзменитьВидимостьТрекаПлан(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьСобытийПоТипу(ТекущаяСтрока)
	
	спкИднексы = ТекущаяСтрока.Индексы;
	Если спкИднексы.Количество() Тогда
		мсвИндексов = Новый Массив();
		Для каждого спкИндексовПоТранспорту Из спкИднексы Цикл
			Сч = 0;
			Для каждого ЭлементДанные Из спкИндексовПоТранспорту.Значение Цикл
				сткДанные = ЭлементДанные.Значение;
				Если РежимРабочегоМеста = 0 Тогда
					Если сткДанные = Неопределено Или Не СписокРейсов[Сч].Пометка Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли РежимРабочегоМеста = 1 Тогда
					Если сткДанные = Неопределено Или Не СписокТранспортныхСредств[Сч].Пометка Тогда
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				мсвИндексов.Добавить(ПредставлениеЧисла(сткДанные.ИндексВМассиве));
			КонецЦикла; 
		КонецЦикла; 
		Если мсвИндексов.Количество() Тогда
			стрИндексы = СтрСоединить(мсвИндексов, ";");
			Если ТекущаяСтрока.Пометка Тогда 
				ВыполнитьПроцедуруHTML("showIncidentMarkers(""" + стрИндексы + """)");
			Иначе	
				ВыполнитьПроцедуруHTML("hideIncidentMarkers(""" + стрИндексы + """)");
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьСобытийПоОбъектуМониторинга(ТекущаяСтрока)
	
	Индекс  = ТекущаяСтрока.ИндексВМассиве;
	Если Индекс = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ИндексыИзмененияОтображения = Новый Массив();
	Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
		Если текСтрока.НетДанных Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не текСтрока.Пометка Тогда
			Если текСтрока.ПометкаАвтоотключение Тогда
				текСтрока.ПометкаАвтоотключение = Ложь;
				текСтрока.Пометка = Истина;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли; 
		
		ЭлементИндексовПоТранспорту = текСтрока.Индексы[Индекс - 1];
		Если Не ЭлементИндексовПоТранспорту = Неопределено Тогда
			Для каждого ЭлементДанных Из ЭлементИндексовПоТранспорту.Значение Цикл
				Если ЗначениеЗаполнено(ЭлементДанных.Значение) Тогда
					ИндексыИзмененияОтображения.Добавить(ПредставлениеЧисла(ЭлементДанных.Значение.ИндексВМассиве));
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла; 
	
	Если ИндексыИзмененияОтображения.Количество() Тогда
		СтрокаИндексов = СтрСоединить(ИндексыИзмененияОтображения, ";");
		Если ТекущаяСтрока.Пометка Тогда 
			ВыполнитьПроцедуруHTML("showIncidentMarkers(""" + СтрокаИндексов + """)");
		Иначе	
			ВыполнитьПроцедуруHTML("hideIncidentMarkers(""" + СтрокаИндексов + """)");
		КонецЕсли; 
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
// Процедура очищает все зоны на карте
//
Процедура ОчиститьЗоны()
	
	ВыполнитьПроцедуруHTML("clearZones()");
	
КонецПроцедуры // ОчиститьРейсы()

&НаКлиенте
Функция ОтобразитьЗону(Данные)
	
	Индекс = ВыполнитьФункциюHTML("addZone(" + Данные + ")");
	Возврат Индекс;
	
КонецФункции // ОтобразитьЗону()

&НаКлиенте
Процедура ИзменитьВидимостьНаправлений()
	
	ВыполнитьПроцедуруHTML("displayDirections = " + ?(ОтображатьНаправления, "true", "false") + ";");
	
	МассивИндексов = Новый Массив;
	Если РежимРабочегоМеста = 0 Тогда
		Для каждого текСтрока Из СписокРейсов Цикл
			Если текСтрока.Пометка Тогда
				МассивИндексов.Добавить(ПредставлениеЧисла(текСтрока.ИндексВМассиве));
			КонецЕсли; 
		КонецЦикла; 
		
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		Для каждого текСтрока Из СписокТранспортныхСредств Цикл
			Если текСтрока.Пометка Тогда
				МассивИндексов.Добавить(ПредставлениеЧисла(текСтрока.ИндексВМассиве));
			КонецЕсли; 
		КонецЦикла;
		
	Иначе
	КонецЕсли; 
	
	Если МассивИндексов.Количество() Тогда
		СтрокаИндексов = СтрСоединить(МассивИндексов, ";");
		ВыполнитьПроцедуруHTML(?(ОтображатьНаправления, "showDirections", "hideDirections") + "(""" + СтрокаИндексов + """)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьНазваний()
	
	ВыполнитьПроцедуруHTML("displayVehicleNames = " + ?(ОтображатьНазвания, "true", "false") + ";");
	
	МассивИндексов = Новый Массив;
	Если РежимРабочегоМеста = 0 Тогда
		Для каждого текСтрока Из СписокРейсов Цикл
			Если текСтрока.Пометка Тогда
				МассивИндексов.Добавить(ПредставлениеЧисла(текСтрока.ИндексВМассиве));
			КонецЕсли; 
		КонецЦикла; 
		
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		Для каждого текСтрока Из СписокТранспортныхСредств Цикл
			Если текСтрока.Пометка Тогда
				МассивИндексов.Добавить(ПредставлениеЧисла(текСтрока.ИндексВМассиве));
			КонецЕсли; 
		КонецЦикла;
		
	Иначе
	КонецЕсли; 
	
	Если МассивИндексов.Количество() Тогда
		СтрокаИндексов = СтрСоединить(МассивИндексов, ";");
		ВыполнитьПроцедуруHTML(?(ОтображатьНазвания, "showVehicleNames", "hideVehicleNames") + "(""" + СтрокаИндексов + """)");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
// Процедура позиционирует слой на карте
//
// Параметры:
//  ИндексВМассиве - Число - Индекс в массиве объектов(слоев, рейсов), помещенных на карту.
//  Режим          - Число - числовое представление режима отображения (0 - точки, 1 - линии, 2 - маршруты, 3 - рейсы).
//
Процедура ЦентрироватьСлойНаКарте(ИндексВМассиве,  Режим = 0)
	
	Если Режим = 0 Тогда
		ВыполнитьПроцедуруHTML("gotoObjectMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		
	ИначеЕсли Режим = 1 Или Режим = 2 Тогда
		ВыполнитьПроцедуруHTML("gotoObjectPolygon(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		
	ИначеЕсли Режим = 3 Тогда
		ВыполнитьПроцедуруHTML("gotoTrip(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	
	КонецЕсли; 
	
КонецПроцедуры // ЦентрироватьСлойНаКарте()

&НаКлиенте
Процедура ЦентрироватьТочкиНаКарте(Координаты)
	
	ВыполнитьПроцедуруHTML("FillArrayPoint(""" + Координаты + """)");	
	ВыполнитьПроцедуруHTML("fitMapToBounds()");
	
	//мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
	//Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
	//	ВыполнитьПроцедуруHTML("addPoint(" + мсвКоординат[Сч * 2] + ", " + мсвКоординат[(Сч * 2) + 1] + ")");
	//КонецЦикла;
	//ВыполнитьПроцедуруHTML("fitMapToBounds()");
	
КонецПроцедуры // ЦентрироватьНерассчитанныйРейсНаКарте()

&НаКлиенте
Процедура МасштабироватьКартуПоОбъектамМониторинга(ТолькоВидимые = Ложь)
	
	Если НЕ КартаСформирована Тогда
		Возврат;
	КонецЕсли;
	
	мсвКоординат = Новый Массив;
	Если РежимРабочегоМеста = 0 Тогда
		Если СписокРейсов.Количество() Тогда
			Для каждого текСтрока Из СписокРейсов Цикл
				Если ТолькоВидимые И Не текСтрока.Пометка Тогда
					Продолжить;
				КонецЕсли;
				
				Если текСтрока.Широта <> 0 И текСтрока.Долгота <> 0 Тогда
					мсвКоординат.Добавить(ПредставлениеЧисла(текСтрока.Широта));
					мсвКоординат.Добавить(ПредставлениеЧисла(текСтрока.Долгота));
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
		
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		Если СписокТранспортныхСредств.Количество() Тогда
			Для каждого текСтрока Из СписокТранспортныхСредств Цикл
				Если ТолькоВидимые И Не текСтрока.Пометка Тогда
					Продолжить;
				КонецЕсли;
				
				Если текСтрока.Широта <> 0 И текСтрока.Долгота <> 0 Тогда
					мсвКоординат.Добавить(ПредставлениеЧисла(текСтрока.Широта));
					мсвКоординат.Добавить(ПредставлениеЧисла(текСтрока.Долгота));
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
		
	Иначе
		
	КонецЕсли; 
	
	Если мсвКоординат.Количество() Тогда
		ЦентрироватьТочкиНаКарте(СтрСоединить(мсвКоординат, ";"));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьДанныеНаКарте(Данные)
	
	ВыполнитьПроцедуруHTML("addVehicleTrackingData("""+ Данные + """)");
	
КонецПроцедуры // ОтобразитьДанныеНаКарте()

&НаКлиенте
Процедура ОтобразитьДанныеПоРейсамНаКарте(Данные)
	
	ВыполнитьПроцедуруHTML("addTripTrackingData("""+ Данные + """)");
	
КонецПроцедуры // ОтобразитьДанныеНаКарте()

&НаКлиенте
Процедура ОтобразитьСобытияМониторингаНаКарте(Данные)
	
	ВыполнитьПроцедуруHTML("addTrackingIncidents("""+ Данные + """)");
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСобытияМониторинга()
	
	ВыполнитьПроцедуруHTML("clearTrackingIncidents()");
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОбъекты()
	
	ВыполнитьПроцедуруHTML("clearVehicleTrackingData()");

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьЗон()
	
	Если ОтображатьЗоны Тогда
		соотДанныеЗон = Новый Соответствие;
		мсвЗон        = Новый Массив;
		Для каждого текЗона Из СписокЗон Цикл
			Если Не текЗона.Пометка И текЗона.ИндексВМассиве > 0 Тогда 
				ИзменитьВидимостьЗоны(текЗона.ИндексВМассиве, Ложь);
			ИначеЕсли текЗона.Пометка И	текЗона.ИндексВМассиве > 0 Тогда
				ИзменитьВидимостьЗоны(текЗона.ИндексВМассиве, Истина);
			ИначеЕсли текЗона.Пометка Тогда
				Если ЗначениеЗаполнено(текЗона.Данные) Тогда
					текЗона.ИндексВМассиве = ОтобразитьЗону(текЗона.Данные);
				Иначе	
					соотДанныеЗон.Вставить(текЗона.Зона, текЗона); 
					мсвЗон.Добавить(текЗона.Зона);
				КонецЕсли;  
			КонецЕсли; 
		КонецЦикла; 
		
		Если мсвЗон.Количество() Тогда
			мсвДанныеЗон = ПолучитьДанныеЗон(мсвЗон);
			Для каждого текДанныеЗоны Из мсвДанныеЗон Цикл
				текСтрока = соотДанныеЗон.Получить(текДанныеЗоны.Зона);
				текСтрока.Данные = текДанныеЗоны.Данные;
				текСтрока.ИндексВМассиве = ОтобразитьЗону(текСтрока.Данные);
			КонецЦикла; 
		КонецЕсли; 
		
	Иначе
		Для каждого текЗона Из СписокЗон Цикл
			Если текЗона.ИндексВМассиве > 0 Тогда
				ИзменитьВидимостьЗоны(текЗона.ИндексВМассиве, Ложь); 
			КонецЕсли; 
		КонецЦикла;
		
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбновлениеДанных

&НаКлиенте
Процедура АктуализироватьДанныеФормы()
	
	Если РежимИстории Тогда
		Возврат;
	КонецЕсли; 
	АктивизацияПоляВСпискеОбъектовВозможна = Ложь;
	Если РежимРабочегоМеста = 0 Тогда
		// рейсы
		ОбновитьСписокРейсовНаСервере(Ложь);
		ОтобразитьОбъектыНаКарте();
		ОбновитьДоступность(ЭтаФорма);
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		// транспорты
		ОбновитьСписокТранспортныхСредствНаСервере(Ложь);
		ОтобразитьОбъектыНаКарте();
		ОбновитьДоступность(ЭтаФорма);
	Иначе
		
	КонецЕсли; 
	ПодключитьОбработчикОжидания("РазрешитьАктивизациюПоляВСпискеОбъектов", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокМаркеровНаКлиенте()
	
	Для каждого ЭлементКоллекции Из СписокМаркеров Цикл
		
		Стиль = "";
		текКартинка = ЭлементКоллекции.КартинкаМаркера;
		Если ЭлементКоллекции.ЭтоКартинка Тогда
			мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Маркер", текКартинка));
			Если мсвСохраненныйМаркер.Количество() Тогда 
				Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Файл) Тогда 
					Стиль = мсвСохраненныйМаркер[0].Файл;
				КонецЕсли;	
			Иначе	
				АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
				ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
				
				НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
				НовыйСохраненныйМаркер.Маркер = текКартинка;
				НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
				Стиль = НовыйСохраненныйМаркер.Файл; 
			КонецЕсли;	
		Иначе 
			мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Стиль",  ЭлементКоллекции.СтильМаркера));
			Если мсвСохраненныйМаркер.Количество() Тогда
				Стиль = мсвСохраненныйМаркер[0].Файл;
			Иначе
				АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");	
				ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
				
				НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
				НовыйСохраненныйМаркер.Стиль = ЭлементКоллекции.СтильМаркера;
				НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
				Стиль = НовыйСохраненныйМаркер.Файл;
			КонецЕсли;
		КонецЕсли;
		ЭлементКоллекции.ДанныеМаркера = Стиль;	
		
	КонецЦикла;

КонецПроцедуры // ОбновитьСписокМаркеровНаКлиенте()

&НаКлиенте
Процедура ЗаполнитьСписокКартинокОбщегоПользования()
	
	СписокКартинок.Очистить();
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
	БиблиотекаКартинок.упСостояниеТранспортаСвободно.Записать(ИмяВременногоФайла);
	СписокКартинок.Добавить(ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
	БиблиотекаКартинок.упСостояниеТранспортаВРейсе.Записать(ИмяВременногоФайла);
	СписокКартинок.Добавить(ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
	БиблиотекаКартинок.упСостояниеТранспортаРемонт.Записать(ИмяВременногоФайла);
	СписокКартинок.Добавить(ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
	БиблиотекаКартинок.упСостояниеТранспортаНедоступно.Записать(ИмяВременногоФайла);
	СписокКартинок.Добавить(ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
	БиблиотекаКартинок.упСостояниеТранспортаПрочее.Записать(ИмяВременногоФайла);
	СписокКартинок.Добавить(ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
	БиблиотекаКартинок.упНаправлениеДвижения.Записать(ИмяВременногоФайла);
	СписокКартинок.Добавить(ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВидимостиСобытийМониторингаНаСервере()
	
	СписокВидимостиСобытийМониторинга.Очистить();
	Если Не ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте Тогда
		Возврат;
	КонецЕсли; 
	
	мсвИсключений = Новый Массив;
	мсвИсключений.Добавить(Справочники.упТипыСобытийМониторинга.ОтсутствиеЛицензииНаПодключение);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упТипыСобытийМониторинга.Ссылка КАК ТипСобытия,
	|	ПРЕДСТАВЛЕНИЕ(упТипыСобытийМониторинга.Ссылка) КАК ПредставлениеТипа,
	|	ВЫБОР
	|		КОГДА упТипыСобытийМониторинга.СтильМаркера = ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.СобытиеМониторинга)
	|		ИНАЧЕ упТипыСобытийМониторинга.СтильМаркера
	|	КОНЕЦ КАК СтильМаркера,
	|	ИСТИНА КАК Пометка
	|ИЗ
	|	Справочник.упТипыСобытийМониторинга КАК упТипыСобытийМониторинга,
	|	Константа.упИспользуетсяАндроидКлиент КАК упИспользуетсяАндроидКлиент
	|ГДЕ
	|	упТипыСобытийМониторинга.Активно
	|	И НЕ упТипыСобытийМониторинга.ПометкаУдаления
	|	И (упИспользуетсяАндроидКлиент.Значение
	|			ИЛИ НЕ упИспользуетсяАндроидКлиент.Значение
	|				И НЕ упТипыСобытийМониторинга.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.упВидыСобытийМониторинга.СобытиеAndroidКлиента))
	|	И НЕ упТипыСобытийМониторинга.Ссылка В (&МассивИсключений)
	|
	|УПОРЯДОЧИТЬ ПО
	|	упТипыСобытийМониторинга.Ссылка";
	Запрос.УстановитьПараметр("МассивИсключений", мсвИсключений);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		мсвМаркеров = Новый Массив;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокВидимостиСобытийМониторинга.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ПредставлениеТипа = ПредставлениеСтроки(НоваяСтрока.ПредставлениеТипа);
			НоваяСтрока.НетДанных = Истина;
			мсвМаркеров.Добавить(Выборка.СтильМаркера);
		КонецЦикла;
	КонецЕсли;
	Если СписокВидимостиСобытийМониторинга.Количество() Тогда
		ОбновитьСписокМаркеровНаСервере(мсвМаркеров, Истина);
	КонецЕсли; 
	
КонецПроцедуры

Функция ПолучитьКонецПериода()
	
	Если РежимИстории и ЗначениеЗаполнено(КонецПериода) Тогда
		Результат = КонецПериода;
	Иначе
		Результат = ТекущаяДата();
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНачалоПериода()
	
	Если РежимИстории Тогда
		Результат = НачалоПериода;
	Иначе
		Если РежимРабочегоМеста = 1 Тогда // транспорт
			Результат = ТекущаяДата() - (ПараметрыРежима.ПериодОтображенияТрека * 3600);
		Иначе
			Результат = Дата(1,1,1);
		КонецЕсли; 
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьСписокРейсовНаСервере(ОбновлятьПолностью = Истина)
	
	Элементы.СписокРейсовПоследнееОбновление.Видимость = Не РежимИстории;
	//Элементы.СписокРейсовПозиционирование.Видимость = Не РежимИстории;
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	СписокСтатусовМониторинга = Новый Массив;
	СписокСтатусовМониторинга.Добавить(Перечисления.упСтатусыРейса.Выполнен);
	СписокСтатусовМониторинга.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	СписокСтатусовМониторинга.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);
	упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(НастройкиКомпоновки.Отбор, "Статус", СписокСтатусовМониторинга,,, Ложь, Не РежимИстории);
	упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(НастройкиКомпоновки.Отбор, "Возраст", 4, ВидСравненияКомпоновкиДанных.Меньше,, Ложь, Не РежимИстории);
	упСервисныеФункции.УстановитьЗначениеПараметраКомпоновки(НастройкиКомпоновки.ПараметрыДанных, "НачалоПериода", ПолучитьНачалоПериода());
	упСервисныеФункции.УстановитьЗначениеПараметраКомпоновки(НастройкиКомпоновки.ПараметрыДанных, "КонецПериода", ПолучитьКонецПериода());
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	Если ОбновлятьПолностью Тогда
		СписокПоследнихПараметров.Очистить();
		СписокПоследнихДанныхДатчиков.Очистить();
		СписокРейсов.Очистить();
		СписокРейсовСлужебный.Очистить();
	КонецЕсли;
	СписокПериодовОтсутствияСоединения.Очистить();
	
	// Установка параметров
	упСервисныеФункции.УстановитьЗначениеПараметраКомпоновки(НастройкиКомпоновки.ПараметрыДанных, "ТекущаяДата", ТекущаяДатаСеанса(), Истина);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	мсвОбновления = Новый Массив;
	
	КоличествоВыполненныхРейсов = 0;
	мсвОтображаемыеРейсы = Новый Массив;
	Для каждого текРейс Из Результат Цикл
		мсвОтображаемыеРейсы.Добавить(текРейс.Рейс);
		Если ОбновлятьПолностью Тогда
			СоздатьСтрокуСпискаРейсов(текРейс);
			мсвОбновления.Добавить(текРейс.Рейс);
		Иначе
			ЭлементСписка = СписокРейсовСлужебный.НайтиПоЗначению(текРейс.Рейс);
			Если ЭлементСписка = Неопределено Тогда
				СоздатьСтрокуСпискаРейсов(текРейс);
				мсвОбновления.Добавить(текРейс.Рейс);
			Иначе
				ТекущаяСтрока = СписокРейсов[СписокРейсовСлужебный.Индекс(ЭлементСписка)];
				Если ТекущаяСтрока.Широта <> текРейс.Широта Или ТекущаяСтрока.Долгота <> текРейс.Долгота Или ТекущаяСтрока.Статус <> текРейс.Статус Тогда
					ЗаполнитьЗначенияСвойств(ТекущаяСтрока, текРейс);
					ТекущаяСтрока.Обновлена = Истина;
					мсвОбновления.Добавить(текРейс.Рейс);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		Если Не текРейс.Статус = Перечисления.упСтатусыРейса.Выполняется Тогда
			КоличествоВыполненныхРейсов = КоличествоВыполненныхРейсов + 1;
		КонецЕсли; 
	КонецЦикла;
	
	Если Не ОбновлятьПолностью Тогда
		Для Каждого ЭлементСписка Из СписокРейсовСлужебный Цикл
			Если мсвОтображаемыеРейсы.Найти(ЭлементСписка.Значение) = Неопределено Тогда
				СписокРейсов.Удалить(СписокРейсовСлужебный.Индекс(ЭлементСписка));
				СписокРейсовСлужебный.Удалить(ЭлементСписка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	Если мсвОбновления.Количество() Тогда
		ОбновитьСписокМаркеровНаСервере(Результат.ВыгрузитьКолонку("СтильМаркера"), Ложь);
		Если Не ОбновлятьПолностью Тогда
			ОбновитьДанныеФормыДляОтображения(мсвОбновления);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоРейсам(РейсыДляОбновления = Неопределено)
	
	Если РейсыДляОбновления = Неопределено Тогда
		РейсыДляОбновления = СписокРейсов.Выгрузить(, "Рейс").ВыгрузитьКолонку(0);
	КонецЕсли; 
	ДопустимыйОбъемПревышен = Ложь;
	Если ПараметрыРежима.ОтображатьТрекиДвиженияПоРейсам Тогда
		ДопустимыйОбъемПревышен = ОбновитьДанныеТрековПоРейсам(РейсыДляОбновления);
	КонецЕсли;
	Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте И Не ДопустимыйОбъемПревышен Тогда
		ОбновитьДанныеСобытийМониторингаРейсов(РейсыДляОбновления);
	КонецЕсли;
	упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "Рейс", РейсыДляОбновления);

КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокТранспортныхСредствНаСервере(ОбновлятьПолностью = Истина)
	
	Элементы.СписокТранспортныхСредствПоследнееОбновление.Видимость = Не РежимИстории;
	Элементы.СписокТранспортныхСредствСостояние.Видимость = Не РежимИстории;
	//Элементы.СписокТранспортныхСредствПозиционирование.Видимость = Не РежимИстории;
	Элементы.СписокТранспортныхСредствСлежение.Видимость = Не РежимИстории;
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	Если Не РежимИстории Тогда
		ДатаНачала = Дата(1,1,1);
	Иначе
		ДатаНачала = ПолучитьНачалоПериода();
	КонецЕсли; 
	упСервисныеФункции.УстановитьЗначениеПараметраКомпоновки(НастройкиКомпоновки.ПараметрыДанных, "НачалоПериода", ДатаНачала);
	упСервисныеФункции.УстановитьЗначениеПараметраКомпоновки(НастройкиКомпоновки.ПараметрыДанных, "КонецПериода", ПолучитьКонецПериода());
	Если ОбновлятьПолностью Тогда
		СписокПоследнихПараметров.Очистить();
		СписокПоследнихДанныхДатчиков.Очистить();
		СписокТранспортныхСредств.Очистить();
		СписокТранспортаСлужебный.Очистить();
	КонецЕсли;
	СписокПериодовОтсутствияСоединения.Очистить();
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	мсвОбновления = Новый Массив;
	мсвОтображаемыйТранспорт = Новый Массив;
	
	Для каждого текТС Из Результат Цикл
		мсвОтображаемыйТранспорт.Добавить(текТС.ТранспортноеСредство);
		Если ОбновлятьПолностью Тогда
			СоздатьСтрокуСпискаТранспортныхСредств(текТС);
			мсвОбновления.Добавить(текТС.ТранспортноеСредство);
		Иначе
			ЭлементСписка = СписокТранспортаСлужебный.НайтиПоЗначению(текТС.ТранспортноеСредство);
			Если ЭлементСписка = Неопределено Тогда
				СоздатьСтрокуСпискаТранспортныхСредств(текТС);
				мсвОбновления.Добавить(текТС.ТранспортноеСредство);
			Иначе
				ТекущаяСтрока = СписокТранспортныхСредств[СписокТранспортаСлужебный.Индекс(ЭлементСписка)];
				Если ТекущаяСтрока.Широта <> текТС.Широта Или ТекущаяСтрока.Долгота <> текТС.Долгота Или ТекущаяСтрока.Состояние <> текТС.Состояние Тогда
					ЗаполнитьЗначенияСвойств(ТекущаяСтрока, текТС);
					ТекущаяСтрока.Обновлена = Истина;
					мсвОбновления.Добавить(текТС.ТранспортноеСредство);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	Если Не ОбновлятьПолностью Тогда
		Для Каждого ЭлементСписка Из СписокТранспортаСлужебный Цикл
			Если мсвОтображаемыйТранспорт.Найти(ЭлементСписка.Значение) = Неопределено Тогда
				СписокТранспортныхСредств.Удалить(СписокРейсовСлужебный.Индекс(ЭлементСписка));
				СписокТранспортаСлужебный.Удалить(ЭлементСписка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	Если мсвОбновления.Количество() Тогда
		ОбновитьСписокМаркеровНаСервере(Результат.ВыгрузитьКолонку("СтильМаркера"), Ложь);
		ОбновитьДанныеФормыДляОтображения(мсвОбновления);
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоТранспорту(ТранспортыДляОбновления = Неопределено)
	
	Если ТранспортыДляОбновления = Неопределено Тогда
		ТранспортыДляОбновления = СписокТранспортныхСредств.Выгрузить(, "ТранспортноеСредство").ВыгрузитьКолонку(0);
	КонецЕсли; 
	ДопустимыйОбъемПревышен = Ложь;
	Если ПараметрыРежима.ОтображатьТрекиДвиженияТранспорта Тогда
		ДопустимыйОбъемПревышен = ОбновитьДанныеТрековПоТранспорту(ТранспортыДляОбновления);
	КонецЕсли;
	Если ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте И Не ДопустимыйОбъемПревышен Тогда
		ОбновитьДанныеСобытийМониторингаТранспорта(ТранспортыДляОбновления);
	КонецЕсли;
	упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "ТранспортноеСредство", ТранспортыДляОбновления);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьСтрокуСпискаРейсов(СтрокаДанных)
	
	НоваяСтрока = СписокРейсов.Добавить();
	НоваяСтрока.ДетальныйАнализ = НСтр("ru = 'Анализ'");
	НоваяСтрока.Представление = СтрокаДанных.ТранспортноеСредство;
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
	Если ЗначениеЗаполнено(СтрокаДанных.Широта) И ЗначениеЗаполнено(СтрокаДанных.Долгота) Тогда 
		НоваяСтрока.Пометка         = Истина;
		//НоваяСтрока.ПометкаТрек     = Истина;
		НоваяСтрока.Обновлена       = Истина;
	КонецЕсли;
	
	сткХарактеристикиЛинии = Новый Структура;
	сткХарактеристикиЛинии.Вставить("Цвет",         СтрокаДанных.ЦветЛинии);
	сткХарактеристикиЛинии.Вставить("Толщина",      СтрокаДанных.ТолщинаЛинии);
	сткХарактеристикиЛинии.Вставить("Прозрачность", СтрокаДанных.ПрозрачностьЛинии);
	НоваяСтрока.ХарактеристикиЛинии = сткХарактеристикиЛинии;
	СписокРейсовСлужебный.Добавить(СтрокаДанных.Рейс);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьСтрокуСпискаТранспортныхСредств(СтрокаДанных)
	
	НоваяСтрока = СписокТранспортныхСредств.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
	Если ЗначениеЗаполнено(СтрокаДанных.Широта) И ЗначениеЗаполнено(СтрокаДанных.Долгота) Тогда 
		НоваяСтрока.Пометка     = Истина;
		//НоваяСтрока.ПометкаТрек = Истина;
		НоваяСтрока.Обновлена   = Истина;
	КонецЕсли;
	
	сткХарактеристикиЛинии = Новый Структура;
	сткХарактеристикиЛинии.Вставить("Цвет",         СтрокаДанных.ЦветЛинии);
	сткХарактеристикиЛинии.Вставить("Толщина",      СтрокаДанных.ТолщинаЛинии);
	сткХарактеристикиЛинии.Вставить("Прозрачность", СтрокаДанных.ПрозрачностьЛинии);
	НоваяСтрока.ХарактеристикиЛинии = сткХарактеристикиЛинии;
	СписокТранспортаСлужебный.Добавить(СтрокаДанных.ТранспортноеСредство);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокМаркеровНаСервере(МассивСтилей, ОбновлятьПолностью)
	
	Если ОбновлятьПолностью Тогда
		СписокМаркеров.Очистить();
	Иначе
		ИспользованныеСтили = СписокМаркеров.Выгрузить(, "СтильМаркера").ВыгрузитьКолонку("СтильМаркера");
		МассивСтилей = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивСтилей, ИспользованныеСтили);
		Если МассивСтилей.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтилиМаркеров.Ссылка КАК СтильМаркера,
	|	упСтилиМаркеров.Картинка КАК КартинкаМаркера,
	|	упСтилиМаркеров.ХранилищеЗначения КАК ХранилищеЗначенияМаркера,
	|	упСтилиМаркеров.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркера,
	|	упСтилиМаркеров.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Ширина
	|		ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК ШиринаМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Высота
	|		ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК ВысотаМаркера
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	упСтилиМаркеров.Ссылка В(&МассивСтилей)";
	Запрос.УстановитьПараметр("МассивСтилей", МассивСтилей);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокМаркеров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если Выборка.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
				НоваяСтрока.КартинкаМаркера = Выборка.ХранилищеЗначенияМаркера.Получить();
			Иначе
				НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркера);
				НоваяСтрока.ЭтоКартинка = Истина;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСписокМаркеровНаСервере()

&НаСервере
Функция ОбновитьДанныеТрековПоТранспорту(МассивОбновления)
	
	ДопустимыйОбъемПревышен = Ложь;
	Толщина      = 2;
	Прозрачность = 0.5;
	
	ОтображатьТрекЦветомПоСкоростиДвиженияТС = ПараметрыРежима.ОтображатьТрекЦветомПоСкоростиДвиженияТС И ПараметрыРежима.ОтображатьТрекиДвиженияТранспорта;
	ЦветоваяШкалаТС = ПараметрыРежима.ЦветоваяШкалаТС;
	
	Если ОтображатьТрекЦветомПоСкоростиДвиженияТС И ЗначениеЗаполнено(ЦветоваяШкалаТС) Тогда
		Толщина      = ПараметрыРежима.ЦветоваяШкалаТС.Толщина;
		Прозрачность = ПараметрыРежима.ЦветоваяШкалаТС.Прозрачность;
	КонецЕсли;
	
	Толщина      = ПредставлениеЧисла(Толщина);
	Прозрачность = ПредставлениеЧисла(Прозрачность);
	
	ТаблицаИнтервалов = Справочники.упЦветовыеШкалы.ПолучитьИнтервалыШкалы(ЦветоваяШкалаТС);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
	Запрос.УстановитьПараметр("ТЗ", ТаблицаИнтервалов);
	Запрос.Выполнить();	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаИнтервалов.До КАК ЧИСЛО(15, 2)) КАК До,
	|	ВЫРАЗИТЬ(ТаблицаИнтервалов.Цвет КАК СТРОКА(10)) КАК Цвет,
	|	ВЫРАЗИТЬ(ТаблицаИнтервалов.От КАК ЧИСЛО(15, 2)) КАК От
	|ПОМЕСТИТЬ вт_Интервалы
	|ИЗ
	|	ТЗ КАК ТаблицаИнтервалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ #КоличествоТочек#
	|	упДанныеТрекеров.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упДанныеТрекеров.Широта КАК Широта,
	|	упДанныеТрекеров.Долгота КАК Долгота,
	|	упДанныеТрекеров.Период КАК Период,
	|	ЕСТЬNULL(вт_Интервалы.Цвет, ""#0000FF"") КАК Цвет,
	|	ЕСТЬNULL(вт_Интервалы.От, 0) КАК Интервал,
	|	упДанныеТрекеров.Скорость,
	|	упДанныеТрекеров.ПредставлениеКоординаты
	|ИЗ
	|	РегистрСведений.упДанныеТрекеров КАК упДанныеТрекеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Интервалы КАК вт_Интервалы
	|		ПО (ВЫБОР
	|				КОГДА упДанныеТрекеров.Скорость >= вт_Интервалы.От
	|						И упДанныеТрекеров.Скорость < вт_Интервалы.До
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	упДанныеТрекеров.Период >= &НачалоПериода
	|	И упДанныеТрекеров.Период <= &КонецПериода
	|	И упДанныеТрекеров.ТранспортноеСредство В(&МассивОбновления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТранспортноеСредство,
	|	Период УБЫВ
	|ИТОГИ ПО
	|	ТранспортноеСредство";
	ДопустимоеКоличествоТочек = ПолучитьДопустимоеКоличествоТочекТрекера();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#КоличествоТочек#", XMLСтрока(ДопустимоеКоличествоТочек));
	Запрос.УстановитьПараметр("НачалоПериода", ПолучитьНачалоПериода());
	Запрос.УстановитьПараметр("КонецПериода", ПолучитьКонецПериода());
	Запрос.УстановитьПараметр("МассивОбновления", МассивОбновления);
	Результат = Запрос.Выполнить();
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	СписокПериодовОтсутствияСоединения.Очистить();
	Для Каждого Транспорт Из МассивОбновления Цикл
		ЭлементСписка = СписокТранспортаСлужебный.НайтиПоЗначению(Транспорт);
		Если ЭлементСписка = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ТекущаяСтрока = СписокТранспортныхСредств[СписокТранспортаСлужебный.Индекс(ЭлементСписка)];
		ТекущаяСтрока.КоординатыТрека = "";
		ТекущаяСтрока.Обновлена = Истина;
	КонецЦикла; 
	ДопустимыйПериодОтсутствияСвязиСТрекером = ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером;
	ДопустимыйПериодОтсутствияСвязиСТрекеромЗадан = ДопустимыйПериодОтсутствияСвязиСТрекером > 0;
	Если Не Результат.Пустой() Тогда
		ВыводТрекаРазрешен = Результат.Выбрать().Количество() < ДопустимоеКоличествоТочек;
		Если Не ВыводТрекаРазрешен Тогда
			ДопустимыйОбъемПревышен = Истина;
			ТекстСообщения = НСтр("ru = 'Достигнуто максимальное количество загружаемых данных трекера. Уменьшите количество отобранных объектов или период.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			Пока Выборка.Следующий() Цикл
				ЭлементСписка = СписокТранспортаСлужебный.НайтиПоЗначению(Выборка.ТранспортноеСредство);
				Если ЭлементСписка = Неопределено Тогда
					Продолжить;
				КонецЕсли; 
				ТекущаяСтрока = СписокТранспортныхСредств[СписокТранспортаСлужебный.Индекс(ЭлементСписка)];
				МассивКоординат = Новый Массив;
				предШирота  = Неопределено;
				предДолгота = Неопределено;
				предЦвет    = "#0000FF";
				предПериод  = '39991231';
				//Если Не ВыводТрекаРазрешен Тогда
				//	Продолжить;
				//КонецЕсли; 
				ВыборкаКоординат = Выборка.Выбрать();
				Если ВыборкаКоординат.Количество() > 1 Тогда
					Если Не ТекущаяСтрока.ПометкаТрекАвтоотключение И Не ТекущаяСтрока.ПометкаТрекРучноеОтключение Тогда
						ТекущаяСтрока.ПометкаТрек = Истина;
					КонецЕсли; 
					МассивХарактеристикЛинии = Новый Массив;
					ХарактеристикиЛинии = ТекущаяСтрока.ХарактеристикиЛинии;
					МассивХарактеристикЛинии.Добавить(ХарактеристикиЛинии.Цвет);
					МассивХарактеристикЛинии.Добавить(ПредставлениеЧисла(ХарактеристикиЛинии.Толщина));
					МассивХарактеристикЛинии.Добавить(ПредставлениеЧисла(ХарактеристикиЛинии.Прозрачность));
					ТекущийЦветовойИнтервал = Неопределено;
					ИзменитьЦвет = Ложь;
					Пока ВыборкаКоординат.Следующий() Цикл
						Если предШирота = ВыборкаКоординат.Широта И предДолгота = ВыборкаКоординат.Долгота Тогда
							Продолжить;
						КонецЕсли;
						Если ТекущийЦветовойИнтервал = Неопределено Тогда
							ТекущийЦветовойИнтервал = ВыборкаКоординат.Интервал;
						КонецЕсли;
						Если ОтображатьТрекЦветомПоСкоростиДвиженияТС И НЕ ТекущийЦветовойИнтервал = ВыборкаКоординат.Интервал Тогда
							// Проверка на цветовую шкалу интервалов.
							ТекущийЦветовойИнтервал = ВыборкаКоординат.Интервал;
							ИзменитьЦвет = Истина;					
						КонецЕсли;
						Если ДопустимыйПериодОтсутствияСвязиСТрекеромЗадан Тогда
							Если ВыборкаКоординат.Период - ДопустимыйПериодОтсутствияСвязиСТрекером * 60 > предПериод  Тогда
								Если МассивКоординат.Количество() = 2 Тогда
									МассивКоординат.Добавить(МассивКоординат[0]);
									МассивКоординат.Добавить(МассивКоординат[1]);
								КонецЕсли;							
								КоординатыПоследнейТочки = МассивКоординат[МассивКоординат.ВГраница()];
								ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивХарактеристикЛинии);
								ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";") + "//";
								МассивКоординат.Очистить();
								
								// Отрезок отсутствия связи покажем другим цветом
								МассивКоординат.Добавить(КоординатыПоследнейТочки);
								МассивКоординат.Добавить(ВыборкаКоординат.ПредставлениеКоординаты);
								СвойстваЛинииРазрыва = СвойстваЛинииРазрыва(Прозрачность);
								ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, СвойстваЛинииРазрыва);
								МассивКоординат.Добавить("null");
								ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";") + "//";
								МассивКоординат.Очистить();
								
								Если ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи Тогда
									НовыйРазрыв = СписокПериодовОтсутствияСоединения.Добавить();
									НовыйРазрыв.Объект = ТекущаяСтрока.ТранспортноеСредство;
									НовыйРазрыв.НачалоПериода = предПериод;
									НовыйРазрыв.КонецПериода  = ВыборкаКоординат.Период;
									НовыйРазрыв.ВремяРазрыва  = НСтр(СтрШаблон("ru = 'с %1 в течении %2'", предПериод, упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремениДоСекунды(ВыборкаКоординат.Период - предПериод)));								
								КонецЕсли; 
							КонецЕсли; 
							предПериод = ВыборкаКоординат.Период;
						КонецЕсли; 
						МассивКоординат.Добавить(ВыборкаКоординат.ПредставлениеКоординаты);
						Если ИзменитьЦвет Тогда
							ИзменитьЦвет = Ложь;
							МассивЦветовойШкалы = Новый Массив;
							МассивЦветовойШкалы.Добавить(предЦвет);
							МассивЦветовойШкалы.Добавить(Толщина);
							МассивЦветовойШкалы.Добавить(Прозрачность);
							
							Если МассивКоординат.Количество() = 2 Тогда
								МассивКоординат.Добавить(МассивКоординат[0]);
								МассивКоординат.Добавить(МассивКоординат[1]);
							КонецЕсли;
							
							ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивЦветовойШкалы);
							ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";") + "//";
							МассивКоординат.Очистить();
							МассивКоординат.Добавить(ВыборкаКоординат.ПредставлениеКоординаты);
						КонецЕсли;					
						
						предШирота  = ВыборкаКоординат.Широта;
						предДолгота = ВыборкаКоординат.Долгота;
						предЦвет    = ВыборкаКоординат.Цвет;
						
					КонецЦикла;
					
					Если МассивКоординат.Количество() = 2 Тогда
						МассивКоординат.Добавить(МассивКоординат[0]);
						МассивКоординат.Добавить(МассивКоординат[1]);
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивХарактеристикЛинии);
						ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";");
					Иначе
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивХарактеристикЛинии);
						ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";");
					КонецЕсли;
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли; 
	СписокПериодовОтсутствияСоединения.Сортировать("НачалоПериода УБЫВ");
	Возврат ДопустимыйОбъемПревышен;

КонецФункции

&НаСервере
Функция ОбновитьДанныеТрековПоРейсам(МассивОбновления)
	
	ДопустимыйОбъемПревышен = Ложь;
	Толщина      = 2;
	Прозрачность = 0.5;
	
	ОтображатьТрекЦветомПоСкоростиДвиженияРейсы = ПараметрыРежима.ОтображатьТрекЦветомПоСкоростиДвиженияРейсы И ПараметрыРежима.ОтображатьТрекиДвиженияПоРейсам;
	ЦветоваяШкалаРейсы = ПараметрыРежима.ЦветоваяШкалаРейсы;
	
	Если ОтображатьТрекЦветомПоСкоростиДвиженияРейсы И ЗначениеЗаполнено(ЦветоваяШкалаРейсы) Тогда
		Толщина      = ПараметрыРежима.ЦветоваяШкалаРейсы.Толщина;
		Прозрачность = ПараметрыРежима.ЦветоваяШкалаРейсы.Прозрачность;
	КонецЕсли;
	
	Толщина      = ПредставлениеЧисла(Толщина);
	Прозрачность = ПредставлениеЧисла(Прозрачность);
	
	ТаблицаИнтервалов = Справочники.упЦветовыеШкалы.ПолучитьИнтервалыШкалы(ЦветоваяШкалаРейсы);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
	Запрос.УстановитьПараметр("ТЗ", ТаблицаИнтервалов);
	Запрос.Выполнить();	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаИнтервалов.До КАК ЧИСЛО(15, 2)) КАК До,
	|	ВЫРАЗИТЬ(ТаблицаИнтервалов.Цвет КАК СТРОКА(10)) КАК Цвет,
	|	ВЫРАЗИТЬ(ТаблицаИнтервалов.От КАК ЧИСЛО(15, 2)) КАК От
	|ПОМЕСТИТЬ вт_Интервалы
	|ИЗ
	|	ТЗ КАК ТаблицаИнтервалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	упМаршрутПоРейсу.Адрес.Широта КАК Широта,
	|	упМаршрутПоРейсу.Адрес.Долгота КАК Долгота,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упМаршрутПоРейсу.Адрес) КАК Адрес,
	|	МАКСИМУМ(упМаршрутПоРейсу.СтрокаНомер) КАК СтрокаНомер
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(&КонецПериода, Рейс В (&МассивОбновления)) КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Пройдена
	|	
	|СГРУППИРОВАТЬ ПО
	|	упМаршрутПоРейсу.Рейс,
	|	упМаршрутПоРейсу.Адрес.Широта,
	|	упМаршрутПоРейсу.Адрес.Долгота,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упМаршрутПоРейсу.Адрес)
	|	
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсу.Рейс,
	|	СтрокаНомер
	|ИТОГИ ПО
	|	Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ #КоличествоТочек#
	|	упДанныеТрекеров.Рейс КАК Рейс,
	|	упДанныеТрекеров.Широта КАК Широта,
	|	упДанныеТрекеров.Долгота КАК Долгота,
	|	упДанныеТрекеров.Период КАК Период,
	|	ЕСТЬNULL(вт_Интервалы.Цвет, ""#0000FF"") КАК Цвет,
	|	ЕСТЬNULL(вт_Интервалы.От, 0) КАК Интервал,
	|	упДанныеТрекеров.ПредставлениеКоординаты
	|ИЗ
	|	РегистрСведений.упДанныеТрекеров КАК упДанныеТрекеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Интервалы КАК вт_Интервалы
	|		ПО (ВЫБОР
	|				КОГДА упДанныеТрекеров.Скорость >= вт_Интервалы.От
	|						И упДанныеТрекеров.Скорость < вт_Интервалы.До
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	упДанныеТрекеров.Рейс В(&МассивОбновления)
	|	И упДанныеТрекеров.Период >= &НачалоПериода
	|	И упДанныеТрекеров.Период <= &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	Рейс,
	|	Период
	|ИТОГИ ПО
	|	Рейс";
	ДопустимоеКоличествоТочек = ПолучитьДопустимоеКоличествоТочекТрекера();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#КоличествоТочек#", XMLСтрока(ДопустимоеКоличествоТочек));
	Если РежимИстории Тогда
		Запрос.УстановитьПараметр("НачалоПериода", ПолучитьНачалоПериода());
	Иначе
		Запрос.УстановитьПараметр("НачалоПериода", Дата("00010101"));
	КонецЕсли; 
	Запрос.УстановитьПараметр("КонецПериода", ПолучитьКонецПериода());
	Запрос.УстановитьПараметр("МассивОбновления", МассивОбновления);
	Результат = Запрос.ВыполнитьПакет();
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	ДопустимыйПериодОтсутствияСвязиСТрекером = ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером;
	ДопустимыйПериодОтсутствияСвязиСТрекеромЗадан = ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером > 0;
	КоличествоТаблиц = Результат.Количество();
	Для Каждого Рейс Из МассивОбновления Цикл
		ЭлементСписка = СписокРейсовСлужебный.НайтиПоЗначению(Рейс);
		Если ЭлементСписка = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ТекущаяСтрока = СписокРейсов[СписокРейсовСлужебный.Индекс(ЭлементСписка)];
		ТекущаяСтрока.КоординатыТрека = "";
		ТекущаяСтрока.Обновлена = Истина;
	КонецЦикла; 
	Если Не Результат[КоличествоТаблиц - 1].Пустой() Тогда
		ВыводТрекаРазрешен = Результат[КоличествоТаблиц - 1].Выбрать().Количество() < ДопустимоеКоличествоТочек;
		Если Не ВыводТрекаРазрешен Тогда 
			ДопустимыйОбъемПревышен = Истина;
			ТекстСообщения = НСтр("ru = 'Достигнуто максимальное количество загружаемых данных трекера. Уменьшите количество отобранных объектов или период.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ВыборкаТочек = Неопределено;
			Если Не Результат[КоличествоТаблиц - 2].Пустой() Тогда
				ВыборкаТочек = Результат[КоличествоТаблиц - 2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			КонецЕсли;
			Выборка = Результат[КоличествоТаблиц - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			Пока Выборка.Следующий() Цикл
				ЭлементСписка = СписокРейсовСлужебный.НайтиПоЗначению(Выборка.Рейс);
				Если ЭлементСписка = Неопределено Тогда
					Продолжить;
				КонецЕсли; 
				ТекущаяСтрока = СписокРейсов[СписокРейсовСлужебный.Индекс(ЭлементСписка)];
				МассивКоординат = Новый Массив;
				предШирота  = Неопределено;
				предДолгота = Неопределено;
				предЦвет = "#0000FF";
				предПериод  = '39991231';
				ТекущаяСтрока.КоординатыТрека = "";
				ВыборкаКоординат = Выборка.Выбрать();
				Если ВыборкаКоординат.Количество() > 1 Тогда
					Если Не ТекущаяСтрока.ПометкаТрекАвтоотключение И Не ТекущаяСтрока.ПометкаТрекРучноеОтключение Тогда
						ТекущаяСтрока.ПометкаТрек = Истина;
					КонецЕсли;
					
					МассивХарактеристикЛинии = Новый Массив;
					ХарактеристикиЛинии = ТекущаяСтрока.ХарактеристикиЛинии;
					МассивХарактеристикЛинии.Добавить(ХарактеристикиЛинии.Цвет);
					МассивХарактеристикЛинии.Добавить(ПредставлениеЧисла(ХарактеристикиЛинии.Толщина));
					МассивХарактеристикЛинии.Добавить(ПредставлениеЧисла(ХарактеристикиЛинии.Прозрачность));
					
					ТекущийЦветовойИнтервал = Неопределено;
					ИзменитьЦвет = Ложь;
					
					Пока ВыборкаКоординат.Следующий() Цикл
						Если предШирота = ВыборкаКоординат.Широта И предДолгота = ВыборкаКоординат.Долгота Тогда
							Продолжить;
						КонецЕсли; 
						Если ТекущийЦветовойИнтервал = Неопределено Тогда
							ТекущийЦветовойИнтервал = ВыборкаКоординат.Интервал;
						КонецЕсли;
						
						Если ОтображатьТрекЦветомПоСкоростиДвиженияРейсы И НЕ ТекущийЦветовойИнтервал = ВыборкаКоординат.Интервал Тогда
							// Проверка на цветовую шкалу интервалов.
							ТекущийЦветовойИнтервал = ВыборкаКоординат.Интервал;
							ИзменитьЦвет = Истина;
						КонецЕсли;
						
						Если ДопустимыйПериодОтсутствияСвязиСТрекеромЗадан Тогда
							Если ВыборкаКоординат.Период - ДопустимыйПериодОтсутствияСвязиСТрекером * 60 > предПериод  Тогда
								Если МассивКоординат.Количество() = 2 Тогда
									МассивКоординат.Добавить(МассивКоординат[0]);
									МассивКоординат.Добавить(МассивКоординат[1]);
								КонецЕсли;
								КоординатыПоследнейТочки = МассивКоординат[МассивКоординат.ВГраница()];
								ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивХарактеристикЛинии);
								МассивКоординат.Добавить("null");
								ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";") + "//";
								МассивКоординат.Очистить();
								
								// Отрезок отсутствия связи покажем другим цветом
								МассивКоординат.Добавить(КоординатыПоследнейТочки);
								МассивКоординат.Добавить(ВыборкаКоординат.ПредставлениеКоординаты);
								СвойстваЛинииРазрыва = СвойстваЛинииРазрыва(Прозрачность);
								ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, СвойстваЛинииРазрыва);
								МассивКоординат.Добавить("null");
								ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";") + "//";
								МассивКоординат.Очистить();

								Если ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи Тогда
									НовыйРазрыв = СписокПериодовОтсутствияСоединения.Добавить();
									НовыйРазрыв.Объект = ТекущаяСтрока.Рейс;
									НовыйРазрыв.НачалоПериода = предПериод;
									НовыйРазрыв.КонецПериода  = ВыборкаКоординат.Период;
									НовыйРазрыв.ВремяРазрыва  = НСтр(СтрШаблон("ru = 'с %1 в течении %2'", предПериод, упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремениДоСекунды(ВыборкаКоординат.Период - предПериод)));								
								КонецЕсли; 
							КонецЕсли; 
							предПериод = ВыборкаКоординат.Период;
						КонецЕсли;
						МассивКоординат.Добавить(ВыборкаКоординат.ПредставлениеКоординаты);
						Если ИзменитьЦвет Тогда
							ИзменитьЦвет = Ложь;
							МассивЦветовойШкалы = Новый Массив;
							МассивЦветовойШкалы.Добавить(предЦвет);
							МассивЦветовойШкалы.Добавить(Толщина);
							МассивЦветовойШкалы.Добавить(Прозрачность);
							Если МассивКоординат.Количество() = 2 Тогда
								МассивКоординат.Добавить(МассивКоординат[0]);
								МассивКоординат.Добавить(МассивКоординат[1]);
							КонецЕсли;
							ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивЦветовойШкалы);
							МассивКоординат.Добавить("null");
							ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";") + "//";
							МассивКоординат.Очистить();
							МассивКоординат.Добавить(ВыборкаКоординат.ПредставлениеКоординаты);
						КонецЕсли;
						
						предШирота  = ВыборкаКоординат.Широта;
						предДолгота = ВыборкаКоординат.Долгота;
						предЦвет    = ВыборкаКоординат.Цвет;
					КонецЦикла;
					
					Если МассивКоординат.Количество() = 2 Тогда
						МассивКоординат.Добавить(МассивКоординат[0]);
						МассивКоординат.Добавить(МассивКоординат[1]);
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивХарактеристикЛинии);
						МассивКоординат.Добавить("null");
						ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";");
		
					Иначе
						МассивПройденных = Новый Массив;
						Если Не ВыборкаТочек = Неопределено Тогда
							Если ВыборкаТочек.НайтиСледующий(Новый Структура("Рейс", Выборка.Рейс)) Тогда
								ВыборкаПройденных = ВыборкаТочек.Выбрать();
								Пока ВыборкаПройденных.Следующий() Цикл
									МассивПройденных.Добавить(ПредставлениеЧисла(ВыборкаПройденных.Широта));
									МассивПройденных.Добавить(ПредставлениеЧисла(ВыборкаПройденных.Долгота));
									МассивПройденных.Добавить(ПредставлениеЧисла(ВыборкаПройденных.СтрокаНомер));
									МассивПройденных.Добавить(ПредставлениеСтроки(ВыборкаПройденных.Адрес));
								КонецЦикла; 							
							КонецЕсли;
						КонецЕсли; 
						
						ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивКоординат, МассивХарактеристикЛинии);
						Если МассивПройденных.Количество() Тогда
							//МассивКоординат.Добавить(СтрСоединить(МассивПройденных, ","));
							МассивКоординат.Добавить(СтрСоединить(МассивПройденных, "\\"));
						Иначе
							МассивКоординат.Добавить("null");
						КонецЕсли; 
						ТекущаяСтрока.КоординатыТрека = ТекущаяСтрока.КоординатыТрека + СтрСоединить(МассивКоординат, ";");
					КонецЕсли;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	СписокПериодовОтсутствияСоединения.Сортировать("НачалоПериода УБЫВ");
	Возврат ДопустимыйОбъемПревышен;

КонецФункции

&НаСервере
Функция СвойстваЛинииРазрыва(Знач Прозрачность)
	
	Перем СвойстваЛинииРазрыва;
	
	СвойстваЛинииРазрыва = Новый Массив;
	СвойстваЛинииРазрыва.Добавить("#A0A0FF");
	СвойстваЛинииРазрыва.Добавить(1);
	СвойстваЛинииРазрыва.Добавить(Прозрачность);
	Возврат СвойстваЛинииРазрыва;

КонецФункции

&НаСервере
Функция ПолучитьДопустимоеКоличествоТочекТрекера()
	
	Если ПараметрыМониторинга.ДопустимыйОбъемДанныхТрекера = 1 Тогда
		Результат = 100000;
	ИначеЕсли ПараметрыМониторинга.ДопустимыйОбъемДанныхТрекера = 2 Тогда
		Результат = 200000;
	Иначе
		Результат = 50000;
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеСобытийМониторингаТранспорта(МассивОбновления, ТипСобытия = Неопределено)
	
	Если ТипСобытия = Неопределено Тогда
		мсвТиповСобытий = СписокВидимостиСобытийМониторинга.Выгрузить(Новый Структура("Пометка", Истина), "ТипСобытия").ВыгрузитьКолонку("ТипСобытия");
		Если Не мсвТиповСобытий.Количество() Тогда
			Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
				текСтрока.Индексы = Новый СписокЗначений;
				текСтрока.НетДанных      = Ложь;
				текСтрока.ОбновитьДанные = Истина;
			КонецЦикла;
			Возврат;
		КонецЕсли; 
	Иначе
		мсвТиповСобытий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипСобытия);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упДатыСобытий.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упДатыСобытий.Период,
	|	упДатыСобытий.ТипСобытия КАК ТипСобытия,
	|	упДатыСобытий.Событие.Широта КАК Широта,
	|	упДатыСобытий.Событие.Долгота КАК Долгота
	|ИЗ
	|	РегистрСведений.упДатыСобытий КАК упДатыСобытий
	|ГДЕ
	|	упДатыСобытий.Период >= &НачалоПериода
	|	И упДатыСобытий.Период <= &КонецПериода
	|	И упДатыСобытий.ТранспортноеСредство В(&МассивОбновления)
	|	И упДатыСобытий.ТипСобытия В(&ТипыСобытий)
	|ИТОГИ ПО
	|	ТипСобытия,
	|	ТранспортноеСредство";
	Запрос.УстановитьПараметр("НачалоПериода", ПолучитьНачалоПериода());
	Запрос.УстановитьПараметр("КонецПериода", ПолучитьКонецПериода());
	Запрос.УстановитьПараметр("МассивОбновления", МассивОбновления);
	Запрос.УстановитьПараметр("ТипыСобытий"     , мсвТиповСобытий);
	Результат = Запрос.Выполнить();
	
	Если ТипСобытия = Неопределено Тогда
		Если Результат.Пустой() Тогда		
			Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
				текСтрока.Индексы = Новый СписокЗначений;
				текСтрока.ОбновитьДанные = Истина;
			КонецЦикла;
			
		Иначе
			ВыборкаТиповСобытий = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
				Если Не текСтрока.Пометка Тогда
					текСтрока.Индексы = Новый СписокЗначений;
					текСтрока.ОбновитьДанные = Истина;
					Продолжить;
				КонецЕсли; 
				
				текСтрока.ОбновитьДанные = Ложь;
				спкИднексы = Новый СписокЗначений;
				Если ВыборкаТиповСобытий.НайтиСледующий(текСтрока.ТипСобытия, "ТипСобытия") Тогда
					Если текСтрока.ПометкаАвтоотключение Тогда
						текСтрока.Пометка = Истина;
					КонецЕсли; 
					текСтрока.НетДанных = Ложь;
					
					ВыборкаТранспортныхСредств = ВыборкаТиповСобытий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Для каждого текСтрокаТС Из СписокТранспортныхСредств Цикл
						спкИндексовПоТранспорту = Новый СписокЗначений;
						Если текСтрокаТС.Пометка Тогда
							Если ВыборкаТранспортныхСредств.НайтиСледующий(текСтрокаТС.ТранспортноеСредство, "ТранспортноеСредство") Тогда
								Выборка = ВыборкаТранспортныхСредств.Выбрать();
								Пока Выборка.Следующий() Цикл
									сткДанные = Новый Структура("ИндексВМассиве, Широта, Долгота, Период", 0, ПредставлениеЧисла(Выборка.Широта), ПредставлениеЧисла(Выборка.Долгота), Строка(Выборка.Период));
									спкИндексовПоТранспорту.Добавить(сткДанные);
								КонецЦикла; 
							КонецЕсли
						Иначе
							спкИндексовПоТранспорту.Добавить(Неопределено);
						КонецЕсли;
						спкИднексы.Добавить(спкИндексовПоТранспорту);
					КонецЦикла; 
				Иначе
					текСтрока.НетДанных = Истина;
					Если текСтрока.Пометка Тогда
						текСтрока.Пометка = Ложь;
					КонецЕсли; 
				КонецЕсли;
				текСтрока.Индексы = спкИднексы;
			КонецЦикла; 
			
		КонецЕсли; 
	Иначе
		ИскомыеСтроки = СписокВидимостиСобытийМониторинга.НайтиСтроки(Новый Структура("ТипСобытия", ТипСобытия));
		Если Результат.Пустой() Тогда
			Для каждого текСтрока Из ИскомыеСтроки Цикл
				текСтрока.Индексы = Новый СписокЗначений;
				текСтрока.НетДанных = Истина;
				текСтрока.ОбновитьДанные = Ложь;
			КонецЦикла;
			
		Иначе
			ВыборкаТиповСобытий = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Для каждого текСтрока Из ИскомыеСтроки Цикл
				текСтрока.ОбновитьДанные = Ложь;
				спкИднексы = Новый СписокЗначений;
				Если ВыборкаТиповСобытий.НайтиСледующий(текСтрока.ТипСобытия, "ТипСобытия") Тогда
					текСтрока.НетДанных = Ложь;
					
					ВыборкаТранспортныхСредств = ВыборкаТиповСобытий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Для каждого текСтрокаТС Из СписокТранспортныхСредств Цикл
						спкИндексовПоТранспорту = Новый СписокЗначений;
						Если текСтрокаТС.Пометка Тогда
							Если ВыборкаТранспортныхСредств.НайтиСледующий(текСтрокаТС.ТранспортноеСредство, "ТранспортноеСредство") Тогда
								Выборка = ВыборкаТранспортныхСредств.Выбрать();
								Пока Выборка.Следующий() Цикл
									сткДанные = Новый Структура("ИндексВМассиве, Широта, Долгота, Период", 0, ПредставлениеЧисла(Выборка.Широта), ПредставлениеЧисла(Выборка.Долгота), Строка(Выборка.Период));
									спкИндексовПоТранспорту.Добавить(сткДанные);
								КонецЦикла; 
							КонецЕсли
						Иначе
							спкИндексовПоТранспорту.Добавить(Неопределено);
						КонецЕсли;
						спкИднексы.Добавить(спкИндексовПоТранспорту);
					КонецЦикла; 
				КонецЕсли;
				текСтрока.Индексы = спкИднексы;
			КонецЦикла; 
			
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеСобытийМониторингаРейсов(МассивОбновления, ТипСобытия = Неопределено)
	
	Если ТипСобытия = Неопределено Тогда
		мсвТиповСобытий = СписокВидимостиСобытийМониторинга.Выгрузить(Новый Структура("Пометка", Истина), "ТипСобытия").ВыгрузитьКолонку("ТипСобытия");
		Если Не мсвТиповСобытий.Количество() Тогда
			Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
				текСтрока.Индексы = Новый СписокЗначений;
				текСтрока.НетДанных      = Ложь;
				текСтрока.ОбновитьДанные = Истина;
			КонецЦикла;
			Возврат;
		КонецЕсли; 
	Иначе
		мсвТиповСобытий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипСобытия);
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсу.Рейс,
	|	упПеревозчикПоРейсу.ТранспортноеСредство
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&МассивОбновления)) КАК упПеревозчикПоРейсу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	упДатыСобытий.Период,
	|	упДатыСобытий.ТипСобытия КАК ТипСобытия,
	|	упДатыСобытий.Событие.Широта КАК Широта,
	|	упДатыСобытий.Событие.Долгота КАК Долгота
	|ИЗ
	|	РегистрСведений.упДатыСобытий КАК упДатыСобытий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	|		ПО упДатыСобытий.Рейс = втРейсы.Рейс
	|			И упДатыСобытий.ТранспортноеСредство = втРейсы.ТранспортноеСредство
	|			И (упДатыСобытий.ТипСобытия В (&ТипыСобытий))
	|ГДЕ
	|	упДатыСобытий.Период >= &НачалоПериода
	|	И упДатыСобытий.Период <= &КонецПериода
	|ИТОГИ ПО
	|	ТипСобытия,
	|	Рейс";
	Запрос.УстановитьПараметр("МассивОбновления", МассивОбновления);
	Запрос.УстановитьПараметр("ТипыСобытий"     , мсвТиповСобытий);
	Запрос.УстановитьПараметр("НачалоПериода", ПолучитьНачалоПериода());
	Запрос.УстановитьПараметр("КонецПериода", ПолучитьКонецПериода());
	Результат = Запрос.Выполнить();
	Если ТипСобытия = Неопределено Тогда
		Если Результат.Пустой() Тогда		
			Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
				текСтрока.Индексы = Новый СписокЗначений;
				текСтрока.ОбновитьДанные = Истина;
			КонецЦикла;
		Иначе
			ВыборкаТиповСобытий = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Для каждого текСтрока Из СписокВидимостиСобытийМониторинга Цикл
				Если Не текСтрока.Пометка Тогда
					текСтрока.Индексы = Новый СписокЗначений;
					текСтрока.ОбновитьДанные = Истина;
					Продолжить;
				КонецЕсли; 
				
				текСтрока.ОбновитьДанные = Ложь;
				спкИднексы = Новый СписокЗначений;
				Если ВыборкаТиповСобытий.НайтиСледующий(текСтрока.ТипСобытия, "ТипСобытия") Тогда
					Если текСтрока.ПометкаАвтоотключение Тогда
						текСтрока.Пометка = Истина;
					КонецЕсли; 
					текСтрока.НетДанных = Ложь;
					
					ВыборкаРейсов = ВыборкаТиповСобытий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Для каждого текСтрокаРейс Из СписокРейсов Цикл
						спкИндексовПоРейсам = Новый СписокЗначений;
						Если текСтрокаРейс.Пометка Тогда
							Если ВыборкаРейсов.НайтиСледующий(текСтрокаРейс.Рейс, "Рейс") Тогда
								Выборка = ВыборкаРейсов.Выбрать();
								Пока Выборка.Следующий() Цикл
									сткДанные = Новый Структура("ИндексВМассиве, Широта, Долгота, Период", 0, ПредставлениеЧисла(Выборка.Широта), ПредставлениеЧисла(Выборка.Долгота), Строка(Выборка.Период));
									спкИндексовПоРейсам.Добавить(сткДанные);
								КонецЦикла; 
							КонецЕсли
						Иначе
							спкИндексовПоРейсам.Добавить(Неопределено);
						КонецЕсли;
						спкИднексы.Добавить(спкИндексовПоРейсам);
					КонецЦикла; 
				Иначе
					текСтрока.НетДанных = Истина;
					Если текСтрока.Пометка Тогда
						текСтрока.Пометка = Ложь;
					КонецЕсли;
				КонецЕсли;
				текСтрока.Индексы = спкИднексы;
			КонецЦикла; 
			
		КонецЕсли; 
		
	Иначе
		ИскомыеСтроки = СписокВидимостиСобытийМониторинга.НайтиСтроки(Новый Структура("ТипСобытия", ТипСобытия));
		Если Результат.Пустой() Тогда
			Для каждого текСтрока Из ИскомыеСтроки Цикл
				текСтрока.Индексы = Новый СписокЗначений;
				текСтрока.НетДанных = Истина;
				текСтрока.ОбновитьДанные = Ложь;
			КонецЦикла;
			
		Иначе
			ВыборкаТиповСобытий = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Для каждого текСтрока Из ИскомыеСтроки Цикл
				
				текСтрока.ОбновитьДанные = Ложь;
				спкИднексы = Новый СписокЗначений;
				Если ВыборкаТиповСобытий.НайтиСледующий(текСтрока.ТипСобытия, "ТипСобытия") Тогда
					текСтрока.НетДанных = Ложь;
					
					ВыборкаРейсов = ВыборкаТиповСобытий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Для каждого текСтрокаРейс Из СписокРейсов Цикл
						спкИндексовПоРейсам = Новый СписокЗначений;
						Если текСтрокаРейс.Пометка Тогда						
							Если ВыборкаРейсов.НайтиСледующий(текСтрокаРейс.Рейс, "Рейс") Тогда
								Выборка = ВыборкаРейсов.Выбрать();
								Пока Выборка.Следующий() Цикл
									сткДанные = Новый Структура("ИндексВМассиве, Широта, Долгота, Период", 0, ПредставлениеЧисла(Выборка.Широта), ПредставлениеЧисла(Выборка.Долгота), Строка(Выборка.Период));
									спкИндексовПоРейсам.Добавить(сткДанные);
								КонецЦикла; 
							КонецЕсли
						Иначе
							спкИндексовПоРейсам.Добавить(Неопределено);
						КонецЕсли;
						спкИднексы.Добавить(спкИндексовПоРейсам);
					КонецЦикла; 
				КонецЕсли;
				текСтрока.Индексы = спкИднексы;
			КонецЦикла; 
			
		КонецЕсли; 
		
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыеМаршрутыРейсов(МассивРейсов)
	
	соотПлановыеМаршрутыРейсов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Рейс КАК Рейс,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упМаршрутПоРейсу.Адрес) КАК АдресПредставление,
	|	упМаршрутПоРейсу.Адрес.Широта КАК Широта,
	|	упМаршрутПоРейсу.Адрес.Долгота КАК Долгота,
	|	упМаршрутПоРейсу.СтрокаНомер
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Регистратор В(&МассивРейсов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут1.СтрокаНомер,
	|	втМаршрут1.Рейс КАК Рейс,
	|	втМаршрут1.АдресПредставление КАК АдресПредставление,
	|	втМаршрут1.Широта КАК НачальнаяШирота,
	|	втМаршрут1.Долгота КАК НачальнаяДолгота,
	|	ЕСТЬNULL(втМаршрут2.Широта, 0) КАК КонечнаяШирота,
	|	ЕСТЬNULL(втМаршрут2.Долгота, 0) КАК КонечнаяДолгота,
	|	ЕСТЬNULL(упРасстоянияМеждуТочками.Путь, """") КАК Путь
	|ИЗ
	|	втМаршрут КАК втМаршрут1
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМаршрут КАК втМаршрут2
	|		ПО втМаршрут1.Рейс = втМаршрут2.Рейс
	|			И (втМаршрут1.СтрокаНомер + 1 = втМаршрут2.СтрокаНомер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	|		ПО втМаршрут1.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
	|			И втМаршрут1.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
	|			И (втМаршрут2.Широта = упРасстоянияМеждуТочками.КонечнаяШирота)
	|			И (втМаршрут2.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота)
	|			И (упРасстоянияМеждуТочками.ПопыткаРасчета = 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут1.Рейс,
	|	втМаршрут1.СтрокаНомер
	|ИТОГИ ПО
	|	Рейс";
	Запрос.УстановитьПараметр("МассивРейсов", МассивРейсов);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			ЭлементСписка = СписокРейсовСлужебный.НайтиПоЗначению(Выборка.Рейс);
			Если ЭлементСписка = Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			
			ТекущаяСтрока = СписокРейсов[СписокРейсовСлужебный.Индекс(ЭлементСписка)];
			
			МассивКоординат = Новый Массив;
			МассивТочек = Новый Массив;
			ВыборкаКоординат = Выборка.Выбрать();
			Пока ВыборкаКоординат.Следующий() Цикл
				МассивТочек.Добавить(ПредставлениеЧисла(ВыборкаКоординат.НачальнаяШирота));
				МассивТочек.Добавить(ПредставлениеЧисла(ВыборкаКоординат.НачальнаяДолгота));
				МассивТочек.Добавить(ВыборкаКоординат.СтрокаНомер);
				МассивТочек.Добавить(ПредставлениеСтроки(ВыборкаКоординат.АдресПредставление));
				
				Если ЗначениеЗаполнено(ВыборкаКоординат.Путь) Тогда
					МассивКоординатПути = СтрРазделить(ВыборкаКоординат.Путь, ";", Ложь);
					Для каждого текКоордината Из МассивКоординатПути Цикл
						МассивКоординат.Добавить(ПредставлениеЧисла(текКоордината));
					КонецЦикла;
				ИначеЕсли ВыборкаКоординат.КонечнаяШирота <> 0 И ВыборкаКоординат.КонечнаяДолгота <> 0 Тогда	 
					МассивКоординат.Добавить(ПредставлениеЧисла(ВыборкаКоординат.КонечнаяШирота));
					МассивКоординат.Добавить(ПредставлениеЧисла(ВыборкаКоординат.КонечнаяДолгота));
				КонецЕсли; 
			КонецЦикла;
			
			МассивКоординат.Добавить("#FF880D");
			МассивКоординат.Добавить(3);
			МассивКоординат.Добавить(0);
			МассивКоординат.Добавить(СтрСоединить(МассивТочек, "//"));

			ТекущаяСтрока.КоординатыТрекаПлан = СтрСоединить(МассивКоординат, ";");
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоследниеДанныеТрекера(ТекущаяСтрока)
	
	СписокПоследнихПараметров.Очистить();
	
	НоваяСтрока = СписокПоследнихПараметров.Добавить();
	НоваяСтрока.ИндексКартинки = ?(ТекущаяСтрока.ТипУстройства = ПредопределенноеЗначение("Перечисление.упТипУстройства.АндроидКлиент"), 0, 1);
	НоваяСтрока.Параметр = НСтр("ru = 'Трекер'");
	НоваяСтрока.Значение = ТекущаяСтрока.Трекер;

	НоваяСтрока = СписокПоследнихПараметров.Добавить();
	НоваяСтрока.ИндексКартинки = 2;
	НоваяСтрока.Параметр = НСтр("ru = 'Широта/долгота'");
	НоваяСтрока.Значение = СтрШаблон("(%1;%2)", ТекущаяСтрока.Широта, ТекущаяСтрока.Долгота);
	
	НоваяСтрока = СписокПоследнихПараметров.Добавить();
	НоваяСтрока.ИндексКартинки = 3;
	НоваяСтрока.Параметр = НСтр("ru = 'Скорость'");
	НоваяСтрока.Значение = ТекущаяСтрока.Скорость;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПоследниеДанныеДатчиковНаСервере(ТранспортноеСредство)
	
	СписокПоследнихДанныхДатчиков.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упДанныеДатчиковСрезПоследних.Период,
	|	упДанныеДатчиковСрезПоследних.Датчик,
	|	упДанныеДатчиковСрезПоследних.Вход,
	|	упДанныеДатчиковСрезПоследних.Значение,
	|	ПРЕДСТАВЛЕНИЕ(упДанныеДатчиковСрезПоследних.РасшифровкаЗначения) КАК РасшифровкаЗначения
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(&КонецПериода, ТранспортноеСредство = &ТранспортноеСредство) КАК упДанныеДатчиковСрезПоследних";
	Запрос.УстановитьПараметр("КонецПериода", ПолучитьКонецПериода());
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокПоследнихДанныхДатчиков.Добавить();
			НоваяСтрока.Период = Выборка.Период;
			НоваяСтрока.Датчик = Выборка.Датчик;
			НоваяСтрока.Значение = СтрШаблон("%1 %2", ?(ЗначениеЗаполнено(Выборка.Значение), Выборка.Значение, Выборка.Вход), Выборка.РасшифровкаЗначения);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеЗон(МассивЗон)
	
    мсвДанныеЗон = Новый Массив;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка,
	|	упГеографическиеЗоны.Наименование,
	|	ЕСТЬNULL(упСтилиПолигонов.ЦветГраницы, упСтильОсновной.ЦветГраницы) КАК ЦветГраницы,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ТолщинаГраницы, упСтильОсновной.ТолщинаГраницы) КАК ТолщинаГраницы,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ПрозрачностьГраницы, упСтильОсновной.ПрозрачностьГраницы) КАК ПрозрачностьГраницы,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ЦветЗаливки, упСтильОсновной.ЦветЗаливки) КАК ЦветЗаливки,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ПрозрачностьЗаливки, упСтильОсновной.ПрозрачностьЗаливки) КАК ПрозрачностьЗаливки
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтилиПолигонов КАК упСтилиПолигонов
	|		ПО упГеографическиеЗоны.СтильОтображения = упСтилиПолигонов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтилиПолигонов КАК упСтильОсновной
	|		ПО (упСтильОсновной.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиПолигонов.Основной))
	|ГДЕ
	|	упГеографическиеЗоны.Ссылка В(&МассивЗон)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Полигон.Ссылка КАК Ссылка,
	|	Полигон.Широта,
	|	Полигон.Долгота
	|ИЗ
	|	Справочник.упГеографическиеЗоны.Полигон КАК Полигон
	|ГДЕ
	|	Полигон.Ссылка В(&МассивЗон)
	|ИТОГИ ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("МассивЗон", МассивЗон);
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[1].Пустой() Тогда   
		ВыборкаДанных = Результат[0].Выгрузить();
		Выборка       = Результат[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Пока Выборка.Следующий() Цикл
			ВыборкаКоординат = Выборка.Выбрать();
			Координаты = "";
			Пока ВыборкаКоординат.Следующий() Цикл
				Координаты = Координаты + ПредставлениеЧисла(ВыборкаКоординат.Широта) + ";" + ПредставлениеЧисла(ВыборкаКоординат.Долгота) + ";";			
			КонецЦикла;
			Если Координаты = "" Тогда
				Продолжить;
			КонецЕсли; 
			
			ИскомаяСтрока = ВыборкаДанных.Найти(Выборка.Ссылка, "Ссылка");
			Если Не ИскомаяСтрока = Неопределено Тогда
				текПредставлениеЗоны = ИскомаяСтрока.Наименование;
				Данные  =  """" + Координаты + """, """ + ПредставлениеСтроки(текПредставлениеЗоны) + """, """ + ИскомаяСтрока.ЦветГраницы + """, " + ПредставлениеЧисла(ИскомаяСтрока.ТолщинаГраницы) 
				                             + ", " + ПредставлениеЧисла(1 - ИскомаяСтрока.ПрозрачностьГраницы / 100)
				                             + ", """ + ИскомаяСтрока.ЦветЗаливки
				                             + """, " + ПредставлениеЧисла(1 - ИскомаяСтрока.ПрозрачностьЗаливки / 100);
											 
											 
				мсвДанныеЗон.Добавить(Новый Структура("Зона, Данные", Выборка.Ссылка, Данные));											 
			 КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат мсвДанныеЗон;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Получить координаты строкой в формате.
//
// Параметры:
//  Координата - Число - Географическая координата.
//
// Возвращаемое значение:
//  Строка - Представление координаты.
//
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	// Возврат СтрЗаменить("" + Координата, ",", "."); // так в 2 раза быстрее, но подходит только для координат
	
КонецФункции //ПредставлениеЧисла()

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтроки(ЗНАЧ Строка)
	
	Строка = СтрЗаменить(Строка, """", "");
	Строка = СтрЗаменить(Строка, ";", "");
	Строка = СтрЗаменить(Строка, "###", "");
	Возврат Строка;
	
КонецФункции // ПредставлениеСтроки()

Функция	ИзменитьФорматВременногоФайла(Имя)
	
	Возврат "file://" + СтрЗаменить(Имя,"\","/");
	
КонецФункции // ИзменитьФорматВременногоФайла()

&НаКлиенте
Функция ПолучитьСоответствиеДанныхМаркеров()
	
	соотДанные = Новый Соответствие;
	Для каждого текСтрока Из СписокМаркеров Цикл
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(текСтрока.ДанныеМаркера);
		мсвПараметров.Добавить(ПредставлениеЧисла(текСтрока.ПоложениеПривязкиXМаркера));
		мсвПараметров.Добавить(ПредставлениеЧисла(текСтрока.ПоложениеПривязкиYМаркера));
		мсвПараметров.Добавить(ПредставлениеЧисла(текСтрока.ШиринаМаркера));
		мсвПараметров.Добавить(ПредставлениеЧисла(текСтрока.ВысотаМаркера));
		
		соотДанные.Вставить(текСтрока.СтильМаркера, СтрСоединить(мсвПараметров, ";"));
	КонецЦикла;
	
	Возврат соотДанные;
	
КонецФункции

#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура вызывается при закрытии формы настроек списка транспортных средств.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НастройкаТранспортныхСредствЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Результат.Настройки);
		ОбновитьСписокТранспортныхСредств(Неопределено);
	КонецЕсли;
    
КонецПроцедуры // НастройкаТранспортныхСредствЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы настроек списка рейсов.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НастройкаРейсовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Результат.Настройки);
		ОбновитьСписокРейсов(Неопределено);
	КонецЕсли;
    
КонецПроцедуры // НастройкаТранспортныхСредствЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров мониторинга рейсов.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыМониторингаРейсовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОбновитьУстановкуДоступности = Ложь;
		ОбновитьДанныеФормы          = Ложь;
		
		Если Ложь
			Или ПараметрыРежима.ИспользоватьАвтообновлениеДанных <> Результат.ИспользоватьАвтообновлениеДанных
			Или ПараметрыРежима.ПериодАвтообновления <> Результат.ПериодАвтообновления 
		Тогда
			УстановитьОбработчикОжидания(Ложь);
			УстановитьОбработчикОжидания(Результат.ИспользоватьАвтообновлениеДанных);
			ОбновитьУстановкуДоступности = Истина;
		КонецЕсли;
		
		Если Ложь
			Или Не ОбновитьУстановкуДоступности 
			Или ПараметрыРежима.ОтображатьПоследниеДанныеТрекера <> Результат.ОтображатьПоследниеДанныеТрекера
			Или ПараметрыРежима.ОтображатьПоследниеДанныеДатчиков <> Результат.ОтображатьПоследниеДанныеДатчиков 
			Или ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте <> Результат.ОтображатьСобытияМониторингаНаКарте 
		Тогда
			ОбновитьУстановкуДоступности = Истина;
		КонецЕсли;
		
		Если Ложь
			Или ПараметрыРежима.ОтображатьТрекиДвиженияПоРейсам <> Результат.ОтображатьТрекиДвиженияПоРейсам 
			Или ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте <> Результат.ОтображатьСобытияМониторингаНаКарте
			Или ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером <> Результат.ДопустимыйПериодОтсутствияСвязиСТрекером
			Или ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи <> Результат.ОтображатьПериодыОтсутствияСвязи 
			Или ПараметрыРежима.ЦветоваяШкалаРейсы <> Результат.ЦветоваяШкалаРейсы 
			Или ПараметрыРежима.ПериодОтображенияСобытийМониторинга <> Результат.ПериодОтображенияСобытийМониторинга 
		Тогда
			ОбновитьУстановкуДоступности = Истина;
			ОбновитьДанныеФормы          = Истина;
		КонецЕсли;
		
		ПараметрыРежима = Результат;
		
		Если ОбновитьУстановкуДоступности Тогда
			ОбновитьДоступность(ЭтаФорма);
		КонецЕсли;
		
		Если ОбновитьДанныеФормы Тогда
			ОбновитьСписокРейсов(Неопределено);
		КонецЕсли; 
		
		упВариантыНастроекВызовСервера.СохранитьВариантНастроек(Результат, "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторингаРейсов", ТекущийПользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров мониторинга транспорта.
//// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыМониторингаТранспортаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОбновитьУстановкуДоступности = Ложь;
		ОбновитьДанныеФормы          = Ложь;
		
		Если Ложь
			Или ПараметрыРежима.ИспользоватьАвтообновлениеДанных <> Результат.ИспользоватьАвтообновлениеДанных
			Или ПараметрыРежима.ПериодАвтообновления <> Результат.ПериодАвтообновления 
		Тогда
			УстановитьОбработчикОжидания(Результат.ИспользоватьАвтообновлениеДанных);
			ОбновитьУстановкуДоступности = Истина;
		КонецЕсли;
		
		Если Ложь
			Или Не ОбновитьУстановкуДоступности 
			Или ПараметрыРежима.ОтображатьПоследниеДанныеТрекера <> Результат.ОтображатьПоследниеДанныеТрекера
			Или ПараметрыРежима.ОтображатьПоследниеДанныеДатчиков <> Результат.ОтображатьПоследниеДанныеДатчиков 
			Или ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте <> Результат.ОтображатьСобытияМониторингаНаКарте 
		Тогда
			ОбновитьУстановкуДоступности = Истина;
		КонецЕсли;
		
		Если Ложь
			Или ПараметрыРежима.ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте <> Результат.ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте
			Или ПараметрыРежима.ОтображатьТрекиДвиженияТранспорта <> Результат.ОтображатьТрекиДвиженияТранспорта 
			Или ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте <> Результат.ОтображатьСобытияМониторингаНаКарте
			Или ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером <> Результат.ДопустимыйПериодОтсутствияСвязиСТрекером
			Или ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи <> Результат.ОтображатьПериодыОтсутствияСвязи 
			Или ПараметрыРежима.ПериодОтображенияТрека <> Результат.ПериодОтображенияТрека 
			Или ПараметрыРежима.ЦветоваяШкалаТС <> Результат.ЦветоваяШкалаТС 
			Или ПараметрыРежима.ПериодОтображенияСобытийМониторинга <> Результат.ПериодОтображенияСобытийМониторинга 
		Тогда
			ОбновитьУстановкуДоступности = Истина;
			ОбновитьДанныеФормы          = Истина;
		КонецЕсли;
		
		ПараметрыРежима = Результат;
		ОтображатьТекущиеСостояния = ПараметрыРежима.ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте;
		
		Если ОбновитьУстановкуДоступности Тогда
			ОбновитьДоступность(ЭтаФорма);
		КонецЕсли;
		
		Если ОбновитьДанныеФормы Тогда
			ОбновитьСписокТранспортныхСредств(Неопределено);
		КонецЕсли; 
		
		упВариантыНастроекВызовСервера.СохранитьВариантНастроек(Результат, "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторингаТранспорта", ТекущийПользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров мониторинга.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыМониторингаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Не ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(ПараметрыМониторинга.СписокЗон, Результат.СписокЗон) Тогда
			СписокЗон.Очистить();
			Для каждого текЗона Из Результат.СписокЗон Цикл
				НоваяСтрока = СписокЗон.Добавить();
				НоваяСтрока.Пометка = Истина;
				НоваяСтрока.Зона = текЗона;
			КонецЦикла;
			Если ОтображатьЗоны Тогда
				ОчиститьЗоны();
				ИзменитьВидимостьЗон();
			КонецЕсли;
		КонецЕсли; 	
		
		МассивыИдентичны = ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(ПараметрыМониторинга.МестаИнтереса,Результат.МестаИнтереса);
		
		//Если ПараметрыМониторинга.КоличествоВидимыхРейсов <> Результат.КоличествоВидимыхРейсов Тогда
		//	Если Результат.КоличествоВидимыхРейсов = 0 Тогда
		//		Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Видимость = Ложь;
		//		Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Видимость = Ложь;
		//	Иначе	
		//		Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Видимость = Истина;
		//		Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Видимость = Истина;
		//	КонецЕсли;
		//КонецЕсли; 
		
		ПараметрыМониторинга = Результат;
		упВариантыНастроекВызовСервера.СохранитьВариантНастроек(Результат, "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторинга", ТекущийПользователь);
		
		Если НЕ МассивыИдентичны Тогда
			// Исходный массив и отредактированный не равны, удалим все места на карте.
			// Получим даные для новых и отобразим если потребуется.
			ВыполнитьПроцедуруHTML("ClearSiteInterest()");
			ОбновитьСписокМестИнтересаНаСервере();
			ОбновитьМестаИнтересаНаКлиенте();
			ИзменитьВидимостьМестИнтереса(ОтображатьМестаИнтереса);
		КонецЕсли;
		
		
	КонецЕсли;
    
КонецПроцедуры // УстановитьПараметрыМониторингаЗавершение()

#КонецОбласти 

#Область СохранениеНастроекИПараметров

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	Если РежимРабочегоМеста = 0 Тогда
		СхемаКомпоновкиДанных = Обработки.упРМСпутниковыйМониторинг.ПолучитьМакет("Рейсы");
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		СхемаКомпоновкиДанных = Обработки.упРМСпутниковыйМониторинг.ПолучитьМакет("ТранспортныеСредства");
	Иначе
		
	КонецЕсли; 
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры // ИнициализироватьКомпоновщикНастроек()

&НаСервере
Процедура УстановитьПараметрыМониторингаНаСервере()
	
	КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторинга";
	ПараметрыМониторинга = Новый Структура;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено Тогда
				ПараметрыМониторинга = СохраненныеНастройки;
				Для каждого текЗона Из ПараметрыМониторинга.СписокЗон Цикл
					НоваяСтрока = СписокЗон.Добавить();
					НоваяСтрока.Пометка = Истина;
					НоваяСтрока.Зона = текЗона;
				КонецЦикла;
				ОбновитьСписокМестИнтересаНаСервере();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыМониторингаПоУмолчанию = упВариантыНастроек.ПараметрыСпутниковогоМониторингаПоУмолчанию();
	Для Каждого КлючИЗначение Из ПараметрыМониторингаПоУмолчанию Цикл
		Если Не ПараметрыМониторинга.Свойство(КлючИЗначение.Ключ) Тогда
			ПараметрыМониторинга.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УстановитьПараметрыМониторингаНаСервере()

&НаСервере
Процедура УстановитьПараметрыРежимаРабочегоМестаНаСервере()
	
	ЗаполнитьПараметрыПоУмолчанию = Истина;
	
	Если РежимРабочегоМеста = 0 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторингаРейсов";
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторингаТранспорта";
	Иначе
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.ПараметрыМониторингаИстория";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено Тогда
				ПараметрыРежима = СохраненныеНастройки;
				ЗаполнитьПараметрыПоУмолчанию = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыРежимаПоУмолчанию = Новый Структура;
	Если РежимРабочегоМеста = 0 Тогда
		ПараметрыРежимаПоУмолчанию = упВариантыНастроек.ПараметрыСпутниковогоМониторингаРежимРейсыПоУмолчанию();
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		ПараметрыРежимаПоУмолчанию = упВариантыНастроек.ПараметрыСпутниковогоМониторингаРежимТранспортПоУмолчанию();
	КонецЕсли;
	//
	//Если РежимРабочегоМеста = 0 Тогда
	//	ПараметрыРежимаПоУмолчанию.Вставить("РежимИстории"                                        , Ложь);
	//	ПараметрыРежимаПоУмолчанию.Вставить("НачалоПериода"                                       , Неопределено);
	//	ПараметрыРежимаПоУмолчанию.Вставить("КонецПериода"                                        , Неопределено);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьПоследниеДанныеТрекера"                    , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьПоследниеДанныеДатчиков"                   , Истина); 
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьТрекиДвиженияПоРейсам"                     , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьСобытияМониторингаНаКарте"                 , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ВремяОтображениеТрекаМинут"                          , 0);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ВремяОтображениеТрекаЧасов"                          , 2);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ИспользоватьАвтообновлениеДанных"                    , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ПериодАвтообновления"                                , 120); // в секундах
	//	ПараметрыРежимаПоУмолчанию.Вставить("ДопустимыйПериодОтсутствияСвязиСТрекером"            , 10); // в минутах
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьПериодыОтсутствияСвязи"                    , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьТрекЦветомПоСкоростиДвиженияРейсы"         , Ложь);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ЦветоваяШкалаРейсы", Справочники.упЦветовыеШкалы.ПустаяСсылка());
	//ИначеЕсли РежимРабочегоМеста = 1 Тогда
	//	ПараметрыРежимаПоУмолчанию.Вставить("РежимИстории"                                        , Ложь);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ПериодОтображенияТрека"                              , 2);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьПоследниеДанныеТрекера"                    , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьПоследниеДанныеДатчиков"                   , Истина); 
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьТекущиеСостоянияТранспортныхСредствНаКарте", Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьСобытияМониторингаНаКарте"                 , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьТрекиДвиженияТранспорта"                   , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ВремяОтображениеТрекаМинут"                          , 0);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ВремяОтображениеТрекаЧасов"                          , 2);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ИспользоватьАвтообновлениеДанных"                    , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ПериодАвтообновления"                                , 120);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ДопустимыйПериодОтсутствияСвязиСТрекером"            , 10);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьПериодыОтсутствияСвязи"                    , Истина);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ОтображатьТрекЦветомПоСкоростиДвиженияТС"            , Ложь);
	//	ПараметрыРежимаПоУмолчанию.Вставить("ЦветоваяШкалаТС", Справочники.упЦветовыеШкалы.ПустаяСсылка());
	//КонецЕсли;
	
	Если ЭтаФорма.ПараметрыРежима = Неопределено Тогда
		ЭтаФорма.ПараметрыРежима = Новый Структура;
	КонецЕсли; 
	Для Каждого КлючИЗначение Из ПараметрыРежимаПоУмолчанию Цикл
		Если Ложь
			Или ЗаполнитьПараметрыПоУмолчанию
			Или Не ЭтаФорма.ПараметрыРежима.Свойство(КлючИЗначение.Ключ) 
		Тогда
			ЭтаФорма.ПараметрыРежима.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры // УстановитьПараметрыРежимаРабочегоМестаНаСервере()

&НаСервере
Процедура СохранитьОбщиеНастройкиФормы()
	
	сткНастройки = Новый Структура;
	сткНастройки.Вставить("ОтображатьЗоны"          , ОтображатьЗоны);
	сткНастройки.Вставить("ОтображатьНазвания"      , ОтображатьНазвания);
	сткНастройки.Вставить("ОтображатьНаправления"   , ОтображатьНаправления);
	сткНастройки.Вставить("РежимРабочегоМеста"      , РежимРабочегоМеста);
	сткНастройки.Вставить("ОтображатьМестаИнтереса" , ОтображатьМестаИнтереса);
	сткНастройки.Вставить("НачалоПериода", НачалоПериода);
	сткНастройки.Вставить("КонецПериода", КонецПериода);
	сткНастройки.Вставить("РежимИстории", РежимИстории);
	                                           
	КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.ОбщиеНастройки";
	
	Если КлючОбщихНастроекФормы = "" Тогда
		НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
		НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
		НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
		НовыйВариант.Автор = ТекущийПользователь;
		НовыйВариант.ТолькоДляАвтора = Истина;
		НовыйВариант.Объект = КлючОбъекта;
		НовыйВариант.Записать();
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
	Иначе
		СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, КлючОбщихНастроекФормы);
		Если СохраненныеНастройки <> Неопределено Тогда
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, КлючОбщихНастроекФормы, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СохранитьОбщиеНастройкиФормы()

Процедура СохранитьНастройкиРежимаРаботыРабочегоМеста()
	
	сткНастройки = Новый Структура;
	Если РежимРабочегоМеста = 0 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкиМониторингРейсов";
		сткНастройки.Вставить("ВариантНастроекМониторингРейсов"    , КлючТекущихНастроекМониторингРейсов);
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкиМониторингТранспорта";
		сткНастройки.Вставить("ВариантНастроекМониторингТранспорта", КлючТекущихНастроекМониторингТранспорта);
	КонецЕсли; 
	Если КлючНастроекРежима = "" Тогда
		НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
		НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
		НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
		НовыйВариант.Автор = ТекущийПользователь;
		НовыйВариант.ТолькоДляАвтора = Истина;
		НовыйВариант.Объект = КлючОбъекта;
		НовыйВариант.Записать();
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
	Иначе
		СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, КлючНастроекРежима);
		Если СохраненныеНастройки <> Неопределено Тогда
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, КлючНастроекРежима, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СохранитьОбщиеНастройкиФормы()

&НаСервере
Процедура ВосстановитьОбщиеНастройкиФормы()
	
	КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.ОбщиеНастройки";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено И ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
				КлючОбщихНастроекФормы = Выборка.КлючВарианта;
				ЭтаФорма.ОтображатьЗоны        = СохраненныеНастройки.ОтображатьЗоны;
				ЭтаФорма.ОтображатьНазвания    = СохраненныеНастройки.ОтображатьНазвания;
				ЭтаФорма.ОтображатьНаправления = СохраненныеНастройки.ОтображатьНаправления;
				ЭтаФорма.РежимРабочегоМеста    = СохраненныеНастройки.РежимРабочегоМеста;
				ЭтаФорма.ОтображатьМестаИнтереса = СохраненныеНастройки.ОтображатьМестаИнтереса;
				Если СохраненныеНастройки.Свойство("НачалоПериода") Тогда
					ЭтаФорма.РежимИстории = СохраненныеНастройки.РежимИстории;
					ЭтаФорма.НачалоПериода = СохраненныеНастройки.НачалоПериода;
					ЭтаФорма.КонецПериода = СохраненныеНастройки.КонецПериода;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВосстановитьОбщиеНастройкиФормы()

&НаСервере
Процедура ВосстановитьНастройкиРежимаРаботыРабочегоМеста()
	
	Если РежимРабочегоМеста = 0 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкиМониторингРейсов";
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкиМониторингТранспорта";
	КонецЕсли;
	
	КомпоновщикИнициализирован = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено И ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
				КлючНастроекРежима = Выборка.КлючВарианта;
				
				ИнициализироватьКомпоновщикНастроек();
				КомпоновщикИнициализирован = Истина;
				
				Если РежимРабочегоМеста = 0 Тогда
					ЭтаФорма.КлючТекущихНастроекМониторингРейсов     = СохраненныеНастройки.ВариантНастроекМониторингРейсов;
				ИначеЕсли РежимРабочегоМеста = 1 Тогда
					ЭтаФорма.КлючТекущихНастроекМониторингТранспорта = СохраненныеНастройки.ВариантНастроекМониторингТранспорта;
				КонецЕсли;
				
				сткНастройки = ВернутьВариантНастройки();
				Если сткНастройки <> Неопределено Тогда
					КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли; 
	
КонецПроцедуры // ВосстановитьОбщиеНастройкиФормы()

&НаСервере
Функция ВернутьВариантНастройки()
	
	сткНастройки = Неопределено;
	Если РежимРабочегоМеста = 0 Тогда
		ВариантНастроек = КлючТекущихНастроекМониторингРейсов;
		сткНастройки = ВернутьВариантНастройкиСпискаРейсов();
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		ВариантНастроек = КлючТекущихНастроекМониторингТранспорта;
		сткНастройки = ВернутьВариантНастройкиСпискаТранспортныхСредств();
	Иначе
		ВариантНастроек = КлючТекущихНастроекИстория;
	КонецЕсли; 
	
	Возврат сткНастройки;
	
КонецФункции				

&НаСервере
Функция ВернутьВариантНастройкиСпискаРейсов()
	
	Возврат ХранилищаНастроек.упХранилищеНастроек.Загрузить("Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаРейсов", КлючТекущихНастроекМониторингРейсов);
	
КонецФункции // ВернутьВариантНастройкиСпискаЗаданий()

&НаСервере
Функция ВернутьВариантНастройкиСпискаТранспортныхСредств()
	
	Возврат ХранилищаНастроек.упХранилищеНастроек.Загрузить("Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаТранспортныхСредств", КлючТекущихНастроекМониторингТранспорта);
	
КонецФункции // ВернутьВариантНастройкиСпискаЗаданий()

&НаКлиенте
// Процедура завершения после закрытия формы загрузки настроек по ТС.
//
Процедура ОткрытьВариантНастройкиСпискаТранспортныхСредствЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		КлючТекущихНастроекМониторингТранспорта = Результат.КлючНастроек;
		сткНастройки = ВернутьВариантНастройкиСпискаТранспортныхСредств();
		
		Если сткНастройки <> Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
			
			ОбновитьСписокТранспортныхСредств(Неопределено);
			Если Элементы.ВариантНастроек.СписокВыбора.НайтиПоЗначению(КлючТекущихНастроекМониторингТранспорта) <> Неопределено Тогда
				ВариантНастроек = КлючТекущихНастроекМониторингТранспорта;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОткрытьВариантНастройкиСпискаТранспортныхСредствЗавершение()

&НаСервере
// Процедура завершения после закрытия формы сохранения настроек по ТС.
//
Процедура СохранитьВариантНастройкиСпискаТранспортныхСредствЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КлючТекущихНастроекМониторингТранспорта = Результат.КлючНастроек;
		
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("Пользователь", ТекущийПользователь);
		сткНастройки.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроек.Настройки);
		ХранилищаНастроек.упХранилищеНастроек.Сохранить("Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаТранспортныхСредств", КлючТекущихНастроекМониторингТранспорта, сткНастройки);
		
		УстановитьДоступныеЗначенияВариантовНастроек();
		Если Элементы.ВариантНастроек.СписокВыбора.НайтиПоЗначению(КлючТекущихНастроекМониторингТранспорта) <> Неопределено Тогда
			ВариантНастроек = КлючТекущихНастроекМониторингТранспорта;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // СохранитьВариантНастройкиСпискаЗаданийНаСервере()

&НаКлиенте
// Процедура завершения после закрытия формы загрузки настроек списка рейсов.
//
Процедура ОткрытьВариантНастройкиСпискаРейсовЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		КлючТекущихНастроекМониторингРейсов = Результат.КлючНастроек;
		сткНастройки = ВернутьВариантНастройкиСпискаРейсов();
		
		Если сткНастройки <> Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
			
			Если Элементы.ВариантНастроек.СписокВыбора.НайтиПоЗначению(КлючТекущихНастроекМониторингРейсов) <> Неопределено Тогда
				ВариантНастроек = КлючТекущихНастроекМониторингРейсов;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОткрытьВариантНастройкиСпискаРейсовЗавершение()

&НаСервере
// Процедура завершения после закрытия формы сохранения настроек списка рейсов.
//
Процедура СохранитьВариантНастройкиСпискаРейсовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КлючТекущихНастроекМониторингРейсов = Результат.КлючНастроек;
		
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("Пользователь", ТекущийПользователь);
		сткНастройки.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроек.Настройки);
		ХранилищаНастроек.упХранилищеНастроек.Сохранить("Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаРейсов", КлючТекущихНастроекМониторингРейсов, сткНастройки);
		
		УстановитьДоступныеЗначенияВариантовНастроек();
		Если Элементы.ВариантНастроек.СписокВыбора.НайтиПоЗначению(КлючТекущихНастроекМониторингРейсов) <> Неопределено Тогда
			ВариантНастроек = КлючТекущихНастроекМониторингРейсов;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // СохранитьВариантНастройкиСпискаРейсовЗавершение()

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовНастроек()
	
	Если РежимРабочегоМеста = 0 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаРейсов";
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкаСпискаТранспортныхСредств";
	Иначе
		КлючОбъекта = "Обработка.упРМСпутниковыйМониторинг.НастройкаИстории";
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.КлючВарианта,
	|	упВариантыНастроек.Наименование,
	|	упВариантыНастроек.ПометкаУдаления
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Объект = &Объект
	|	И (упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь)";
	Запрос.УстановитьПараметр("Объект"             , КлючОбъекта);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	ДоступныеВариантов = Запрос.Выполнить().Выгрузить();
	
	Элементы.ВариантНастроек.СписокВыбора.Очистить();
	Для каждого Строка Из ДоступныеВариантов Цикл
		Если Строка.ПометкаУдаления Тогда
			Элементы.ВариантНастроек.СписокВыбора.Добавить(Строка.КлючВарианта, Строка.Наименование,, БиблиотекаКартинок.Удалить);
		Иначе
			Элементы.ВариантНастроек.СписокВыбора.Добавить(Строка.КлючВарианта, Строка.Наименование);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // УстановитьДоступныеЗначенияВариантовНастроек()

#КонецОбласти 

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступность(ЭтаФорма)
	
	ПараметрыРежима = ЭтаФорма.ПараметрыРежима;
	РежимРабочегоМеста = ЭтаФорма.РежимРабочегоМеста;
	Элементы = ЭтаФорма.Элементы;
	Если РежимРабочегоМеста = 0 Тогда
		ШаблонСтроки = "всего: %1";
		Если Не ЭтаФорма.РежимИстории Тогда
			ШаблонСтроки = ШаблонСтроки + ", выполненных за последние 4 ч.: %2";
		КонецЕсли; 
		Элементы.ГруппаСписокРейсов.Заголовок = НСтр(СтрШаблон("ru = 'Рейсы (" + ШаблонСтроки + ")'; hu = '%1%2'", ЭтаФорма.СписокРейсов.Количество(), 
			ЭтаФорма.КоличествоВыполненныхРейсов));
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		Элементы.ГруппаСписокТранспортныхСредств.Заголовок = НСтр(СтрШаблон("ru = 'Транспортные средства (%1 ед.)'", ЭтаФорма.СписокТранспортныхСредств.Количество()));
	Иначе
		
	КонецЕсли;
	Если РежимРабочегоМеста = 0 Тогда
		Элементы.СписокДанныеТрекера.Видимость                  = ПараметрыРежима.ОтображатьПоследниеДанныеТрекера;
		Элементы.СписокДанныеДатчиков.Видимость                 = ПараметрыРежима.ОтображатьПоследниеДанныеДатчиков;
		Элементы.СписокПериодовОтсутствияСоединения.Видимость   = (ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером > 0) И ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи;
		Элементы.СписокРейсовПометкаТрек.Видимость              = ПараметрыРежима.ОтображатьТрекиДвиженияПоРейсам;
		Элементы.СписокРейсовПометкаТрекПлан.Видимость          = ПараметрыРежима.ОтображатьТрекиДвиженияПоРейсам;
		Элементы.СписокРейсовСлежение.Видимость                 = Элементы.СписокРейсовСлежение.Видимость И ПараметрыРежима.ИспользоватьАвтообновлениеДанных;
	ИначеЕсли РежимРабочегоМеста = 1 Тогда
		Элементы.СписокДанныеТрекера.Видимость                  = ПараметрыРежима.ОтображатьПоследниеДанныеТрекера;
		Элементы.СписокДанныеДатчиков.Видимость                 = ПараметрыРежима.ОтображатьПоследниеДанныеДатчиков;
		Элементы.СписокПериодовОтсутствияСоединения.Видимость   = (ПараметрыРежима.ДопустимыйПериодОтсутствияСвязиСТрекером > 0) И ПараметрыРежима.ОтображатьПериодыОтсутствияСвязи;
		Элементы.СписокТранспортныхСредствПометкаТрек.Видимость = ПараметрыРежима.ОтображатьТрекиДвиженияТранспорта;
		Элементы.СписокТранспортныхСредствСлежение.Видимость    = Элементы.СписокТранспортныхСредствСлежение.Видимость И ПараметрыРежима.ИспользоватьАвтообновлениеДанных;
	КонецЕсли;
	Элементы.ГруппаТипыСобытий.Видимость = ПараметрыРежима.ОтображатьСобытияМониторингаНаКарте;
	Элементы.ГруппаПериод.Доступность = ЭтаФорма.РежимИстории;
	Элементы.СписокРейсовГруппаКолонокПодРейсом.Видимость = Не ЭтаФорма.РежимИстории;
	Элементы.СписокРейсовГруппаКолонокПодПометкой.Видимость = Не ЭтаФорма.РежимИстории;
	Элементы.РежимИстория.Пометка = ЭтаФорма.РежимИстории;
	
КонецПроцедуры

&НаКлиенте
// Выполнить работу с обработчиком ожидания
//
// Параметры:
//  Нет.
//
Процедура УстановитьОбработчикОжидания(Включить = Истина)
	
	Если Включить Тогда
		ПериодАвтообновления = ПараметрыРежима.ПериодАвтообновления;
		ПодключитьОбработчикОжидания("АктуализироватьДанныеФормы", ПараметрыРежима.ПериодАвтообновления);
	Иначе
		ОтключитьОбработчикОжидания("АктуализироватьДанныеФормы");
	КонецЕсли;
	
КонецПроцедуры //УстановитьОбработчикОжидания()

&НаСервере
Процедура ОбновитьДанныеФормыДляОтображения(ОбъектыДляОтбора = Неопределено)
	
	Пока Истина Цикл
		ЭлементОтбора = упСервисныеФункции.НайтиЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "Период");
		Если ЭлементОтбора = Неопределено Тогда
			Прервать;
		КонецЕсли; 
		СписокСобытийМониторинга.Отбор.Элементы.Удалить(ЭлементОтбора);
	КонецЦикла; 
	Если РежимИстории Тогда
		упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "Период", ПолучитьНачалоПериода(), ВидСравненияКомпоновкиДанных.БольшеИлиРавно,, Ложь, РежимИстории);
		упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "Период", ПолучитьКонецПериода(), ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,, Ложь, РежимИстории);
	Иначе
		Если ПараметрыРежима.ПериодОтображенияСобытийМониторинга > 0 Тогда
			упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "Период", ТекущаяДатаСеанса() - ПараметрыРежима.ПериодОтображенияСобытийМониторинга*3600, ВидСравненияКомпоновкиДанных.Больше,, Ложь, Истина);
		КонецЕсли;
	КонецЕсли;
	
	упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "Рейс",,,,, Ложь);
	упСервисныеФункции.НайтиДобавитьЭлементОтбораКомпоновки(СписокСобытийМониторинга.Отбор, "ТранспортноеСредство",,,,, Ложь);
	Если РежимРабочегоМеста = 0 Тогда // рейсы
		ОбновитьОтображениеПоРейсам(ОбъектыДляОтбора);
	ИначеЕсли РежимРабочегоМеста = 1 Тогда // транспорт
		ОбновитьОтображениеПоТранспорту(ОбъектыДляОтбора);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПериодИсторииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПериодИстории.ДатаНачала = НачалоПериода;
	ПериодИстории.ДатаОкончания = КонецПериода;
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
// Параметры:
//  Результат  - Дата - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыборСтандартногоПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.НачалоПериода = Результат.НачалоПериода;
		ЭтаФорма.КонецПериода = КонецДня(Результат.КонецПериода);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодИсторииДатаНачалаПриИзменении(Элемент = Неопределено)
	
	Если РежимРабочегоМеста = 0 Тогда // рейсы
		ОбновитьСписокРейсов(Неопределено);
	ИначеЕсли РежимРабочегоМеста = 1 Тогда // транспорт
		ОбновитьСписокТранспортныхСредств(Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РежимИстория(Команда)
	
	РежимИстории = Не РежимИстории;
	ОбновитьДоступность(ЭтаФорма);
	Если РежимИстории Тогда
		Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
			ЭтаФорма.НачалоПериода = ПолучитьНачалоПериода();
			ЭтаФорма.КонецПериода = ПолучитьКонецПериода();
		КонецЕсли; 
	КонецЕсли; 
	РежимРабочегоМестаПриИзменении(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьПериодВОтдельномОкне(Команда)
	
	сткПараметры = Новый Структура("НачалоПериода, КонецПериода, ПроверкаЗаполнения", НачалоПериода, КонецПериода, Истина);
	ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораПроизвольногоПериода", сткПараметры,,,,, Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 

КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.НачалоПериода = Результат.НачалоПериода;
		ЭтаФорма.КонецПериода = Результат.КонецПериода;
		ПериодИсторииДатаНачалаПриИзменении();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АнимацияТрекаСтарт(Команда)
	
	ПроверитьЗагрузитьАнимациюТрека();
	Если ЭтаФорма.АнимацияЗагружена Тогда
		ВыполнитьПроцедуруHTML("TrackAnimationStart()");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗагрузитьАнимациюТрека()
	
	Если Не ЭтаФорма.АнимацияЗагружена Тогда
		Если РежимРабочегоМеста = 0 Тогда
			ТекущаяСтрока = Элементы.СписокРейсов.ТекущаяСтрока;
			Если ТекущаяСтрока <> Неопределено Тогда
				ДанныеОбъекта = СписокРейсов.НайтиПоИдентификатору(ТекущаяСтрока);
			КонецЕсли; 
		Иначе
			ТекущаяСтрока = Элементы.СписокТранспортныхСредств.ТекущаяСтрока;
			Если ТекущаяСтрока <> Неопределено Тогда
				ДанныеОбъекта = СписокТранспортныхСредств.НайтиПоИдентификатору(ТекущаяСтрока);
			КонецЕсли; 
		КонецЕсли; 
		Если ДанныеОбъекта <> Неопределено Тогда
			СтрокаКоординатТочек = ДанныеОбъекта.КоординатыТрека;
			Если ЗначениеЗаполнено(СтрокаКоординатТочек) Тогда
				СтрокаКоординатВремени = "20000";
				РежимТранспорта = ?(РежимРабочегоМеста=0, "false", "true");
				ВыполнитьПроцедуруHTML("SetTrackAnimation(""" + СтрокаКоординатТочек + """, """ + СтрокаКоординатВремени + """," + РежимТранспорта + ")");
				ЭтаФорма.АнимацияЗагружена = Истина;
			Иначе
				// У рейсов может быть пустой строка координат точек
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АнимацияТрекаВНачало(Команда)
	
	ПроверитьЗагрузитьАнимациюТрека();
	Если ЭтаФорма.АнимацияЗагружена Тогда
		ВыполнитьПроцедуруHTML("TrackAnimationMoveTo(0)");
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура АнимацияТрекаПауза(Команда)
	
	ПроверитьЗагрузитьАнимациюТрека();
	Если ЭтаФорма.АнимацияЗагружена Тогда
		//ВыполнитьПроцедуруHTML("TrackAnimationPause()");
		ВыполнитьПроцедуруHTML("TrackAnimationStop()");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АнимацияТрекаВКонец(Команда)
	
	ПроверитьЗагрузитьАнимациюТрека();
	Если ЭтаФорма.АнимацияЗагружена Тогда
		ВыполнитьПроцедуруHTML("TrackAnimationMoveTo(1000)");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АнимацияТрекаСтоп(Команда)
	
	Если АнимацияЗагружена Тогда
		//ВыполнитьПроцедуруHTML("TrackAnimationStop()");
		СброситьАнимациюТрека();
		ПроверитьЗагрузитьАнимациюТрека();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Функция РежимКорректировки(Статус)
	
	Результат = Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению") 
				ИЛИ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполняется");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#КонецЕсли 