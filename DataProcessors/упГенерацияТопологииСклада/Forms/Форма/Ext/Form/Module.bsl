
#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Объект.РазделительСтеллажи = "-";
	Объект.РазделительЯрусы = "-";
	Объект.РазделительПозиции = "-";
	Объект.РазделительПроезды = "-";
	
	Объект.ТипСимволовСтеллажи = "Числовой";
	Объект.ТипСимволовЯрусы = "Числовой";
	Объект.ТипСимволовПозиции = "Числовой";
	Объект.ТипСимволовПроезды = "Числовой";
	
	УстановитьПорядокНаФорме();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементовФормы(Неопределено);
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура ВариантГенерацииПриИзменении(Элемент)
	
	УстановитьПредставлениеОбразца();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "ПриИзменении" элемента.
//
Процедура ИспользоватьОбъектСкладаПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		УстановитьПредставлениеОбразца();
		УстановитьДоступностьЭлементовФормы(Элемент.Имя);
	КонецЕсли;
	
КонецПроцедуры // ИспользоватьОбъектСкладаПриИзменении()

&НаКлиенте
// Обработчик события "ПриИзменении" элемента.
//
Процедура ТипыСимволовПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"ТипСимволов","");
		УстановитьТипСимволов(ТекущееИмяОбъекта);
	КонецЕсли;
	
КонецПроцедуры // ТипыСимволовПриИзменении()

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "ПостояннаяДлинаНомера".
//
Процедура ПостояннаяДлинаНомераПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"ПостояннаяДлинаНомера","");
		УстановитьПостояннуюДлинуНомера(ТекущееИмяОбъекта);
	КонецЕсли;
	
КонецПроцедуры // ПостояннаяДлинаНомераПриИзменении()

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "КоличествоСимволов".
//
Процедура КоличествоСимволовПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"КоличествоСимволов","");
		УстановитьКоличествоСимволов(ТекущееИмяОбъекта);
	КонецЕсли;

КонецПроцедуры // КоличествоСимволовПриИзменении()

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "МатрицаСимволов".
//
Процедура МатрицаСимволовПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"МатрицаСимволов","");
		УстановитьМатрицуСимволов(ТекущееИмяОбъекта);
	КонецЕсли;
	
КонецПроцедуры // МатрицаСимволовПриИзменении()

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "НомерНачало".
//
Процедура НомерНачалоПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"НомерНачало","");
		УстановитьНомерНачало(ТекущееИмяОбъекта);
	КонецЕсли;
	
КонецПроцедуры // НомерНачалоПриИзменении()

&НаКлиенте
// Обработчик события "Регулирование" элемента "НомерНачало".
//
Процедура НомерНачалоРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"НомерНачало","");
		РегулированиеНомерНачало(ТекущееИмяОбъекта, Направление);
	КонецЕсли;
	
КонецПроцедуры // НомерНачалоРегулирование()

&НаКлиенте
// Обработчик события "ПриИзменении" элемента "НомерОкончания".
//
Процедура НомерОкончанияПриИзменении(Элемент)
	
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"НомерОкончания","");
		УстановитьНомерОкончания(ТекущееИмяОбъекта);
	КонецЕсли;
	
КонецПроцедуры // НомерОкончанияПриИзменении()

&НаКлиенте
// Обработчик события "Регулирование" элемента "НомерОкончания".
//
Процедура НомерОкончанияРегулирование(Элемент,Направление,СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Если НЕ Элемент = Неопределено Тогда
		ТекущееИмяОбъекта = СтрЗаменить(Элемент.Имя,"НомерОкончания","");
		РегулированиеНомерОкончания(ТекущееИмяОбъекта,Направление);
	КонецЕсли;	
	
КонецПроцедуры // НомерОкончанияРегулирование() 

&НаКлиенте
// Обработчик события "ПриИзменении" элемента формы "Разделитель".
//
Процедура РазделительПриИзменении(Элемент)
	
	УстановитьПредставлениеОбразца();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
// Обработчик комадны генерации топологии склада.
//
Процедура кмдГенерировать(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	ПараметрыГенерации = Новый Структура();
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса",ЭтаФорма,ПараметрыГенерации);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Выполнить генерацию топологии склада?';"
	+ " en = 'Do you want to continue?'"), Режим, 0);	
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Запускает генерацию топологии склада после утвердительного ответа пользователя.
//
// Параметры:
//  Результат - РежимДиалогаВопрос - Ответ.
//  ПараметрыОтвета - Структура - Параметры.
//
Процедура ПослеЗакрытияВопроса(Результат,ПараметрыОтвета) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если ПроверитьЗаполнение() Тогда 
			Отказ = Ложь;
			
			Если Объект.Проезды Тогда 
				мсвПроезды = ВыполнитьФормированиеМассиваСимволов("Проезды", Отказ);
			Иначе
				мсвПроезды = Новый Массив;
				мсвПроезды.Добавить("");
			КонецЕсли;	
			
			Если Объект.Стеллажи Тогда 
				мсвСтеллажи = ВыполнитьФормированиеМассиваСимволов("Стеллажи", Отказ);
			Иначе
				мсвСтеллажи = Новый Массив;
				мсвСтеллажи.Добавить("");
			КонецЕсли;	
			
			Если Объект.Ярусы Тогда 
				мсвЯрусы = ВыполнитьФормированиеМассиваСимволов("Ярусы", Отказ);
			Иначе
				мсвЯрусы = Новый Массив;
				мсвЯрусы.Добавить("");
			КонецЕсли;	
			
			Если Объект.Позиции Тогда 
				мсвПозиции = ВыполнитьФормированиеМассиваСимволов("Позиции", Отказ);
			Иначе
				мсвПозиции = Новый Массив;
				мсвПозиции.Добавить("");
			КонецЕсли;	
			
			Если НЕ Отказ Тогда				
				ПоказатьОповещениеПользователя(НСтр("ru = 'Генерация топологии склада...'"),, НСтр("ru = 'Начата генерация ячеек.'"), БиблиотекаКартинок.Информация32);				
				Элементы.кмдГенерировать.Доступность = Ложь;
				ЗапуститьГенерациюЭлементовТопологииСклада(мсвПроезды,мсвСтеллажи,мсвЯрусы,мсвПозиции);
				Оповещение = Новый ОписаниеОповещения("ПослеЗавершинияГенерацииТопологииСклада",ЭтаФорма,ПараметрыОтвета);
				ПоказатьПредупреждение(Оповещение,НСтр("ru = 'Генерация топологии склада завершина'"),60,НСтр("ru = 'Генерация топологии склада...'"));
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопроса()

&НаКлиенте
// Процедура завершения после генерации топологии склада.
//
// Параметры:
//  Результат - Структура - Пармерты выполнения.
//
Процедура ПослеЗавершинияГенерацииТопологииСклада(Результат) Экспорт
	
	Элементы.кмдГенерировать.Доступность = Истина;
	ПоказатьОповещениеПользователя(НСтр("ru = 'Генерация топологии склада...'"),, НСтр("ru = 'Генерация топологии склада завершина.'"), БиблиотекаКартинок.Информация32);
	
КонецПроцедуры // ПослеЗавершинияГенерацииТопологииСклада()

&НаСервере
Процедура ЗапуститьГенерациюЭлементовТопологииСклада(Проезды,Стеллажи,Ярусы,Позиции)
		
	СтруктураОбъекта = Новый Структура("Склад,ЗамещатьСуществующиеЯчейки,КоличествоЯчеек,
		|Проезды,Стеллажи,Ярусы,Позиции,РазделительПроезды,РазделительСтеллажи,
		|РазделительЯрусы,РазделительПозиции,ВариантГенерации");
	
	СтруктураОбъекта.Вставить("ВариантГенерации",ВариантГенерации);	
	ЗаполнитьЗначенияСвойств(СтруктураОбъекта,Объект);
	
	ГенерироватьЯчейки(СтруктураОбъекта,Проезды,Стеллажи,Ярусы,Позиции);
	
КонецПроцедуры // ЗапуститьГенерациюЭлементовТопологииСклада()

#Область ГенерацияЯчеек

&НаСервере
// Выполнить генерацию элементов справочника.
//
// Параметры:
//  СтруктураОбъекта - Структура - Данны.
//  МассивПроезды - Массив - список проездов, номера.
//  МассивСтеллажи - Массив - список номеров стеллажей.
//  МассивЯрусы - Массив - список номеров ярусов.
//  МассивПозиции - Массив - список номеров позиций.
//
Процедура ГенерироватьЯчейки(СтруктураОбъекта,МассивПроезды,МассивСтеллажи,МассивЯрусы,МассивПозиции)Экспорт
	
	СтруктураДополнительныеРеквизиты = Новый Структура;
	МассивСтелажейПредыдущихПроездов = Новый Массив;
	МассивТекущихПроездов = Новый Массив;
	КоличествоЯчеек = 1;
	Для Каждого ТекущийПроезд Из МассивПроезды Цикл
		ПротивоположныйПроезд = "";		
		ТекущийНомерПроезда = МассивПроезды.Найти(ТекущийПроезд)+1;
		Если ТекущийНомерПроезда > 1 Тогда
			Если ТекущийНомерПроезда%2 = 1 Тогда 
				ПротивоположныйПроезд = МассивТекущихПроездов[ТекущийНомерПроезда-2];
			КонецЕсли;
		КонецЕсли;
		
		СтруктураДополнительныеРеквизиты.Вставить("ПротивоположныйПроезд", ПротивоположныйПроезд);		
		
		МассивСсылокСтелажей = Новый Массив;
		Для Каждого ТекущийСтеллаж Из МассивСтеллажи Цикл			
			СтеллажСправа = "";
			СтеллажСлева = "";
			СтеллажНапротив = "";			
			ТекущийНомерСтеллажа = МассивСтеллажи.Найти(ТекущийСтеллаж)+1;
			Если ЗначениеЗаполнено(ПротивоположныйПроезд) Тогда 
				СтеллажНапротив = МассивСтелажейПредыдущихПроездов[ТекущийНомерСтеллажа-1];
			КонецЕсли;
			Если ТекущийНомерСтеллажа > 1 Тогда
				Если ТекущийНомерПроезда%2 = 1 Тогда 
					СтеллажСлева  = МассивСсылокСтелажей[ТекущийНомерСтеллажа-2];
				Иначе	
					СтеллажСправа = МассивСсылокСтелажей[ТекущийНомерСтеллажа-2];
				КонецЕсли;
			КонецЕсли;
			СтруктураДополнительныеРеквизиты.Вставить("СтеллажСправа",СтеллажСправа);
			СтруктураДополнительныеРеквизиты.Вставить("СтеллажСлева",СтеллажСлева);
			СтруктураДополнительныеРеквизиты.Вставить("СтеллажНапротив",СтеллажНапротив);
			СтруктураДополнительныеРеквизиты.Вставить("Генерация",Истина);			
			Для Каждого текЯрус Из МассивЯрусы Цикл
				КрайнийИндекс = МассивПозиции.ВГраница();
				Для ТекущийНомер = 0 По КрайнийИндекс Цикл					
					ТекущаяЯчейка = СоздатьЭлементСправочника(СтруктураОбъекта, ТекущийПроезд, ТекущийСтеллаж, текЯрус, МассивПозиции[ТекущийНомер], СтруктураДополнительныеРеквизиты);
					КоличествоЯчеек = КоличествоЯчеек+1;
				КонецЦикла;				
			КонецЦикла;
			МассивСсылокСтелажей.Добавить(ТекущаяЯчейка.Стеллаж);
		КонецЦикла;
		
		МассивТекущихПроездов.Добавить(ТекущаяЯчейка.Проезд);
		
		МассивСтелажейПредыдущихПроездов = Новый Массив;
		Для Каждого текСтеллажСсылка Из МассивСсылокСтелажей Цикл 
			МассивСтелажейПредыдущихПроездов.Добавить(текСтеллажСсылка);
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры // ГенерироватьЯчейки()

&НаСервереБезКонтекста
// Получить номер ячейки и штрихкод.
//
// Параметры:
//  СтруктураОбъекта - Структура - параметры получения номера.
//  Проезд - Строка - номер проезда.
//  Стеллаж - Строка - номер стеллажа.
//  Ярус - Строка - номер яруса.
//  Позиция - Строка - номер позиции.
//  Штрихкод - Строка - штрихкод ячейки.
//
// Возвразаемое значение:
//  Строка - Число - номер ячейки.
//
Функция СформироватьНомерЯчейки(СтруктураОбъекта,Проезд,Стеллаж,Ярус,Позиция,Штрихкод)
	
	Номер = "";
	Штрихкод = "";
	
	Если СтруктураОбъекта.Проезды Тогда 
		Номер = Номер + Проезд;
		Штрихкод = Штрихкод + Проезд;
		Если СтруктураОбъекта.Стеллажи ИЛИ СтруктураОбъекта.Ярусы ИЛИ СтруктураОбъекта.Позиции Тогда 
			Номер = Номер + СтруктураОбъекта.РазделительПроезды;
		КонецЕсли;	
	КонецЕсли;
	
	Если СтруктураОбъекта.Стеллажи Тогда 
		Номер = Номер + Стеллаж;
		Штрихкод = Штрихкод + Стеллаж;
		Если СтруктураОбъекта.Ярусы ИЛИ СтруктураОбъекта.Позиции Тогда 
			Номер = Номер + СтруктураОбъекта.РазделительСтеллажи;
		КонецЕсли;	
	КонецЕсли;
	
	Если СтруктураОбъекта.ВариантГенерации Тогда		
		Если СтруктураОбъекта.Позиции Тогда 
			Номер = Номер + Позиция;
			Штрихкод = Штрихкод + Позиция;
			Если СтруктураОбъекта.Ярусы Тогда 
				Номер = Номер + СтруктураОбъекта.РазделительПозиции;
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураОбъекта.Ярусы Тогда 
			Номер = Номер + Ярус;
			Штрихкод = Штрихкод + Ярус;
		КонецЕсли;
	Иначе
		Если СтруктураОбъекта.Ярусы Тогда 
			Номер = Номер + Ярус;
			Штрихкод = Штрихкод + Ярус;
			Если СтруктураОбъекта.Позиции Тогда 
				Номер = Номер + СтруктураОбъекта.РазделительЯрусы;
			КонецЕсли;	
		КонецЕсли;
		
		Если СтруктураОбъекта.Позиции Тогда 
			Номер = Номер + Позиция;
			Штрихкод = Штрихкод + Позиция;
		КонецЕсли;
	КонецЕсли;
		
	Возврат Номер;
	
КонецФункции // СформироватьНомерЯчейки()

&НаСервереБезКонтекста
// Выполнить создание элементов справочника.
//
// Параметры:
//  СтруктураОбъекта - Структура - параметры для обработки.
//  Проезд    - Строка - номер проезда.
//  Стеллаж   - Строка - номер стеллажа.
//  Ярус      - Строка - номер яруса.
//  Позиция   - Строка - номер позиции.
//  ДополнительныеСвойства - Структура - Свойства обработки.
//
// Вовращаемое значение:
//  СправочникСсылка.усЯчейки - ссылка на созданный элемент.
//
Функция СоздатьЭлементСправочника(СтруктураОбъекта,Проезд,Стеллаж,Ярус,Позиция,ДополнительныеСвойства)
	
	Штрихкод = "";
	Адрес = СформироватьНомерЯчейки(СтруктураОбъекта,Проезд,Стеллаж,Ярус,Позиция,Штрихкод);
	
	Если ПустаяСтрока(Адрес) Тогда
		ТекстСообщения = НСтр("ru='Складская ячейка не сформирована получен пустой код ячейки, измените параметры генерации '");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Справочники.упСкладскиеЯчейки.ПустаяСсылка();
	КонецЕсли;
	
	СсылкаНаЭлементСклада = Справочники.упСкладскиеЯчейки.НайтиЯчейкуНаСкладеПоКоду(СтруктураОбъекта.Склад,Адрес);
	
	Если СсылкаНаЭлементСклада.Пустая() Тогда		
		ОбъектСклада = Справочники.упСкладскиеЯчейки.СоздатьЭлемент();	
	ИначеЕсли СтруктураОбъекта.ЗамещатьСуществующиеЯчейки Тогда		
		ОбъектСклада = СсылкаНаЭлементСклада.ПолучитьОбъект();		
		ОбъектСклада.ПометкаУдаления = Ложь;		
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru='Ячейка %1 существует в структуре склада'"),СокрЛП(Адрес));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат СсылкаНаЭлементСклада;		
	КонецЕсли;
	
	ОбъектСклада.Код = СокрЛП(Адрес);
	ОбъектСклада.Штрихкод = СокрЛП(Штрихкод);
	ОбъектСклада.Владелец = СтруктураОбъекта.Склад;	
	ОбъектСклада.Проезд = Проезд;
	ОбъектСклада.Стеллаж = Стеллаж;
	ОбъектСклада.Ярус = Ярус;
	ОбъектСклада.Позиция = Позиция;
	
	Для Каждого ТекущееСвойство Из ДополнительныеСвойства Цикл 
		ОбъектСклада.ДополнительныеСвойства.Вставить(ТекущееСвойство.Ключ, ТекущееСвойство.Значение);
	КонецЦикла;
	
	СсылкаНаЭлементСклада = Справочники.упСкладскиеЯчейки.ПустаяСсылка();
	
	Попытка
		Если ОбъектСклада.ПроверитьЗаполнение() Тогда
			ОбъектСклада.Записать();
			СсылкаНаЭлементСклада = ОбъектСклада.Ссылка;
		КонецЕсли;
	Исключение		
		ТекстСообщения = СтрШаблон(НСтр("ru='Элемент ""%1%"" не записан: %2%'"),СокрЛП(Адрес),ИнформацияОбОшибке().Описание);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;               
	
	Возврат СсылкаНаЭлементСклада;
	
КонецФункции // СоздатьЭлементСправочника()

#КонецОбласти

&НаСервере
// Выполним переход для формирования порядка.
//
Процедура УстановитьПорядокНаФорме()
		
	УстановитьПредставлениеОбразца();
	
КонецПроцедуры // УстановитьПорядокНаФорме()

&НаСервере
// Выполним формирование текста примера штрихкода.
//
Процедура УстановитьПредставлениеОбразца()
	
	ТекущийПример = Объект.Пример;	
	Объект.Пример = "";
	Штрихкод = "";
	Объект.КоличествоЯчеек = 1;
	
	Если ЭтоАдресВременногоХранилища(ПримерШтрихкода) Тогда
		УдалитьИзВременногоХранилища(ПримерШтрихкода);
		ПримерШтрихкода = "";
	КонецЕсли;
	
	Если Объект.Проезды Тогда 
		Объект.Пример = Объект.Пример + Объект.НомерОкончанияПроезды;
		Штрихкод = Штрихкод + Объект.НомерОкончанияПроезды;
		Объект.КоличествоЯчеек = Объект.КоличествоЯчеек * Объект.КоличествоПроезды;
		Если Объект.Стеллажи ИЛИ Объект.Ярусы ИЛИ Объект.Позиции Тогда 
			Объект.Пример = Объект.Пример + Объект.РазделительПроезды;
		КонецЕсли;	
	КонецЕсли;
	
	Если Объект.Стеллажи Тогда 
		Объект.Пример = Объект.Пример + Объект.НомерОкончанияСтеллажи;
		Штрихкод = Штрихкод + Объект.НомерОкончанияСтеллажи;
		Объект.КоличествоЯчеек = Объект.КоличествоЯчеек * Объект.КоличествоСтеллажи;
		Если Объект.Ярусы ИЛИ Объект.Позиции Тогда 
			Объект.Пример = Объект.Пример + Объект.РазделительСтеллажи;
		КонецЕсли;	
	КонецЕсли;	
	
	Если ВариантГенерации Тогда
		Если Объект.Позиции Тогда 
			Объект.Пример = Объект.Пример + Объект.НомерОкончанияПозиции;
			Штрихкод = Штрихкод + Объект.НомерОкончанияПозиции;
			Объект.КоличествоЯчеек = Объект.КоличествоЯчеек * Объект.КоличествоПозиции;
			Если Объект.Ярусы  Тогда 
				Объект.Пример = Объект.Пример + Объект.РазделительПозиции;
			КонецЕсли;
		КонецЕсли;		
		Если Объект.Ярусы Тогда 
			Объект.Пример = Объект.Пример + Объект.НомерОкончанияЯрусы;
			Штрихкод = Штрихкод + Объект.НомерОкончанияЯрусы;
			Объект.КоличествоЯчеек = Объект.КоличествоЯчеек * Объект.КоличествоЯрусы;
		КонецЕсли;
	Иначе
		Если Объект.Ярусы Тогда 
			Объект.Пример = Объект.Пример + Объект.НомерОкончанияЯрусы;
			Штрихкод = Штрихкод + Объект.НомерОкончанияЯрусы;
			Объект.КоличествоЯчеек = Объект.КоличествоЯчеек * Объект.КоличествоЯрусы;
			Если Объект.Позиции Тогда 
				Объект.Пример = Объект.Пример + Объект.РазделительЯрусы;
			КонецЕсли;
		КонецЕсли;		
		Если Объект.Позиции Тогда 
			Объект.Пример = Объект.Пример + Объект.НомерОкончанияПозиции;
			Штрихкод = Штрихкод + Объект.НомерОкончанияПозиции;
			Объект.КоличествоЯчеек = Объект.КоличествоЯчеек * Объект.КоличествоПозиции;
		КонецЕсли;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.Пример) Тогда 
		Если ПустаяСтрока(ПримерШтрихкода) ИЛИ НЕ ТекущийПример = Объект.Пример Тогда 
			ПараметрыШтрихкода = Новый Структура;
			ПараметрыШтрихкода.Вставить("Ширина",120);
			ПараметрыШтрихкода.Вставить("Высота",40);
			ПараметрыШтрихкода.Вставить("ТипКода",4);
			ПараметрыШтрихкода.Вставить("ОтображатьТекст",Истина);
			ПараметрыШтрихкода.Вставить("РазмерШрифта",10);
			ПараметрыШтрихкода.Вставить("Штрихкод",Штрихкод);			
			ШтрихКодКартинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);			
			ПримерШтрихкода = ПоместитьВоВременноеХранилище(ШтрихКодКартинка,УникальныйИдентификатор);
		КонецЕсли;	
	Иначе
		ПримерШтрихкода = "";
	КонецЕсли;	
	
КонецПроцедуры // УстановитьПредставлениеОбразца()

&НаКлиенте
// Определим доступность элементов формы.
//
Процедура УстановитьДоступностьЭлементовФормы(Элемент)	
	Если Элемент = "Проезды" ИЛИ Элемент = Неопределено Тогда 
		Элементы.ТипСимволовПроезды.Доступность                = Объект.Проезды;
		Элементы.ПостояннаяДлинаНомераПроезды.Доступность = Объект.Проезды;		
		Элементы.КоличествоСимволовПроезды.Доступность           = Объект.Проезды И Объект.ПостояннаяДлинаНомераПроезды;
		Элементы.МатрицаСимволовПроезды.Доступность           = Объект.Проезды И Объект.ПостояннаяДлинаНомераПроезды И Объект.ТипСимволовПроезды = "Алфавитно-цифровой (лат.)";		
		Элементы.ГруппаПроездыКоличество.Доступность         = Объект.Проезды;
		Элементы.РазделительПроезды.Доступность              = Объект.Проезды;		
	КонецЕсли;	
	Если Элемент = "Стеллажи" ИЛИ Элемент = Неопределено Тогда 
		Элементы.ТипСимволовСтеллажи.Доступность                = Объект.Стеллажи;
		Элементы.ПостояннаяДлинаНомераСтеллажи.Доступность = Объект.Стеллажи;		
		Элементы.КоличествоСимволовСтеллажи.Доступность           = Объект.Стеллажи И Объект.ПостояннаяДлинаНомераСтеллажи;
		Элементы.МатрицаСимволовСтеллажи.Доступность           = Объект.Стеллажи И Объект.ПостояннаяДлинаНомераСтеллажи И Объект.ТипСимволовСтеллажи = "Алфавитно-цифровой (лат.)";		
		Элементы.ГруппаСтеллажиКоличество.Доступность         = Объект.Стеллажи;
		Элементы.РазделительСтеллажи.Доступность              = Объект.Стеллажи;
	КонецЕсли;	
	Если Элемент = "Ярусы" ИЛИ Элемент = Неопределено Тогда 
		Элементы.ТипСимволовЯрусы.Доступность                = Объект.Ярусы;
		Элементы.ПостояннаяДлинаНомераЯрусы.Доступность = Объект.Ярусы;		
		Элементы.КоличествоСимволовЯрусы.Доступность           = Объект.Ярусы И Объект.ПостояннаяДлинаНомераЯрусы;
		Элементы.МатрицаСимволовЯрусы.Доступность           = Объект.Ярусы И Объект.ПостояннаяДлинаНомераЯрусы И Объект.ТипСимволовЯрусы = "Алфавитно-цифровой (лат.)";
		Элементы.ГруппаЯрусыКоличество.Доступность         = Объект.Ярусы;
		Элементы.РазделительЯрусы.Доступность              = Объект.Ярусы;
	КонецЕсли;	
	Если Элемент = "Позиции" ИЛИ Элемент = Неопределено Тогда 
		Элементы.ТипСимволовПозиции.Доступность                = Объект.Позиции;
		Элементы.ПостояннаяДлинаНомераПозиции.Доступность = Объект.Позиции;		
		Элементы.КоличествоСимволовПозиции.Доступность           = Объект.Позиции И Объект.ПостояннаяДлинаНомераПозиции;
		Элементы.МатрицаСимволовПозиции.Доступность           = Объект.Позиции И Объект.ПостояннаяДлинаНомераПозиции И Объект.ТипСимволовПозиции = "Алфавитно-цифровой (лат.)";
		Элементы.ГруппаПозицииКоличество.Доступность         = Объект.Позиции;
		Элементы.РазделительПозиции.Доступность              = Объект.Позиции;
	КонецЕсли;	
КонецПроцедуры // УстановитьДоступностьЭлементовФормы()

&НаКлиенте
// Выполним обработку события "ПриИзменении" для элемента "ТипСимволов".
//
// Параметры:
//  Элемент - Строка - имя элемента номера.
//
Процедура УстановитьТипСимволов(Элемент)
	
   	УстановитьДоступностьЭлементовФормы(Элемент);	
	Если Объект["ТипСимволов" + Элемент] = "Числовой" Тогда 
		Объект["МатрицаСимволов" + Элемент] = Ложь;		
		Попытка
			Если Объект["ПостояннаяДлинаНомера" + Элемент] Тогда 
				Объект["НомерНачало" + Элемент] = Формат(Число(Объект["НомерНачало" + Элемент]), "ЧЦ=" + Объект["КоличествоСимволов" + Элемент] + "; ЧВН=; ЧН=; ЧГ=");
				Объект["НомерОкончания" + Элемент] = Формат(Число(Объект["НомерОкончания" + Элемент]), "ЧЦ=" + Объект["КоличествоСимволов" + Элемент] + "; ЧВН=; ЧН=; ЧГ=");
			Иначе	
				Объект["НомерНачало" + Элемент] = Формат(Число(Объект["НомерНачало" + Элемент]), "ЧН=; ЧГ=");
				Объект["НомерОкончания" + Элемент] = Формат(Число(Объект["НомерОкончания" + Элемент]), "ЧН=; ЧГ=");
			КонецЕсли;
			ОтобразитьКоличествоСимволов(Элемент);
		Исключение	
			Объект["НомерНачало" + Элемент] = "1";
			Объект["НомерОкончания" + Элемент] = "1";			
			Объект["Количество" + Элемент] = 1;
		КонецПопытки;		
		УстановитьПредставлениеОбразца();
	ИначеЕсли Объект["ТипСимволов" + Элемент] = "Алфавитно-цифровой (лат.)" Тогда 
		Если ПустаяСтрока(Объект["НомерНачало" + Элемент])И ПустаяСтрока(Объект["НомерОкончания" + Элемент]) Тогда
			Объект["НомерНачало" + Элемент] = "A";
			Объект["НомерОкончания" + Элемент] = "A";			
			Объект["Количество" + Элемент] = 1;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры // УстановитьТипСимволов()

&НаКлиенте
// Выполнить обработку события "ПриИзменении" для элемента "ПостояннаяДлинаНомера".
//
// Параметры:
//  Элемент - Строка - имя элемента номера.
//
Процедура УстановитьПостояннуюДлинуНомера(Элемент)
	
   	УстановитьДоступностьЭлементовФормы(Элемент);	
	Если Объект["ПостояннаяДлинаНомера" + Элемент] Тогда 
		Объект["КоличествоСимволов" + Элемент] = Макс(СтрДлина(СокрЛП(Объект["НомерНачало" + Элемент])), СтрДлина(СокрЛП(Объект["НомерОкончания" + Элемент])), 1);
		УстановитьКоличествоСимволов(Элемент);
	Иначе
		Объект["КоличествоСимволов" + Элемент] = 1;
		Объект["МатрицаСимволов" + Элемент] = Ложь;		
		ОтобразитьКоличествоСимволов(Элемент);
	КонецЕсли;	
	
КонецПроцедуры // УстановитьПостояннуюДлинуНомера()

&НаКлиенте
// Выполнить обработку события "ПриИзменении" для элемента "КоличествоСимволов".
//
// Параметры:
//  Элемент	- Строка - имя элемента номера.
//
Процедура УстановитьКоличествоСимволов(Элемент)

	Если Объект["ТипСимволов" + Элемент] = "Числовой" Тогда 
		Объект["НомерНачало" + Элемент] = Прав("00000" + СокрЛП(Объект["НомерНачало" + Элемент]), Объект["КоличествоСимволов" + Элемент]);
		Объект["НомерОкончания" + Элемент] = Прав("00000" + СокрЛП(Объект["НомерОкончания" + Элемент]), Объект["КоличествоСимволов" + Элемент]);
	Иначе	
		Объект["НомерНачало" + Элемент] = Прав("AAAAA" + СокрЛП(Объект["НомерНачало" + Элемент]), Объект["КоличествоСимволов" + Элемент]);
		Объект["НомерОкончания" + Элемент] = Прав("AAAAA" + СокрЛП(Объект["НомерОкончания" + Элемент]), Объект["КоличествоСимволов" + Элемент]);
	КонецЕсли;	
	
	ОтобразитьКоличествоСимволов(Элемент);
		
	УстановитьПредставлениеОбразца();
	
КонецПроцедуры // УстановитьКоличествоСимволов()

&НаКлиенте
// Выполнить обработку события "ПриИзменении" для элемента "МатрицаСимволов".
//
// Параметры:
//  Элемент	- Строка - имя элемента номера.
//
Процедура УстановитьМатрицуСимволов(Элемент)
	
	ОтобразитьКоличествоСимволов(Элемент);
	
КонецПроцедуры // УстановитьМатрицуСимволов()

&НаКлиенте
// Выполнить обработку события "ПриИзменении" для элемента "НомерНачало".
//
// Параметры:
//  Элемент	- Строка - имя элемента номера.
//
Процедура УстановитьНомерНачало(Элемент)

	Объект["НомерНачало" + Элемент] = СокрЛП(Объект["НомерНачало" + Элемент]);
	
	Если СтрДлина(Объект["НомерНачало" + Элемент]) > СтрДлина(Объект["НомерОкончания" + Элемент])
		ИЛИ (СтрДлина(Объект["НомерНачало" + Элемент]) = СтрДлина(Объект["НомерОкончания" + Элемент])
		И Объект["НомерНачало" + Элемент] > Объект["НомерОкончания" + Элемент]) Тогда 
		
		Объект["НомерОкончания" + Элемент] = Объект["НомерНачало" + Элемент];
		Объект["Количество" + Элемент] = 1;
		УстановитьПредставлениеОбразца();
		
		Если Объект["ПостояннаяДлинаНомера" + Элемент] И СтрДлина(Объект["НомерНачало" + Элемент]) <> Объект["КоличествоСимволов" + Элемент] Тогда 
			Объект["КоличествоСимволов" + Элемент] = СтрДлина(Объект["НомерНачало" + Элемент]);
		КонецЕсли;	
	ИначеЕсли Объект["ПостояннаяДлинаНомера" + Элемент] И СтрДлина(Объект["НомерНачало" + Элемент]) <> Объект["КоличествоСимволов" + Элемент] Тогда 
		Объект["КоличествоСимволов" + Элемент] = СтрДлина(Объект["НомерНачало" + Элемент]);
		
		УстановитьКоличествоСимволов(Элемент);
	Иначе
		ОтобразитьКоличествоСимволов(Элемент);
	КонецЕсли;	
	
КонецПроцедуры // УстановитьНомерНачало()

&НаКлиенте
// Выполнить обработку события "Регулирование" для элемента "НомерНачало".
//
// Параметры:
//  Элемент	- Строка - имя элемента номера.
//  Направление	- Число - направление регулирования.
//
Процедура РегулированиеНомерНачало(Элемент, Направление)

	Если Направление = 1 Тогда 
		Номер = ПерейтиКСледующемуНомеру(Объект["НомерНачало"+Элемент],Объект["ТипСимволов"+Элемент]="Числовой","НомерНачало"+Элемент);
	Иначе
		Номер = ПерейтиКПредыдущемуНомеру(Объект["НомерНачало"+Элемент],Объект["ПостояннаяДлинаНомера"+Элемент],"НомерНачало"+Элемент);
	КонецЕсли;
	
	Если НЕ Номер = Объект["НомерНачало"+Элемент] Тогда 
		Объект["НомерНачало"+Элемент] = Номер;
		УстановитьНомерНачало(Элемент);
	КонецЕсли;	
	
КонецПроцедуры // РегулированиеНомерНачало()

&НаКлиенте
// Выполнить обработку события "ПриИзменении" для элемента "НомерОкончания".
//
// Параметры:
//  Элемент - Строка - имя элемента номера.
//
Процедура УстановитьНомерОкончания(Элемент)

	Объект["НомерОкончания"+Элемент] = СокрЛП(Объект["НомерОкончания"+Элемент]);
	
	Если СтрДлина(Объект["НомерНачало"+Элемент]) > СтрДлина(Объект["НомерОкончания"+Элемент])
		ИЛИ (СтрДлина(Объект["НомерНачало"+Элемент]) = СтрДлина(Объект["НомерОкончания"+Элемент])
		И Объект["НомерНачало"+Элемент] > Объект["НомерОкончания"+Элемент]) Тогда 
		
		Объект["НомерНачало"+Элемент] = Объект["НомерОкончания"+Элемент];
		Объект["Количество"+Элемент] = 1;
		УстановитьПредставлениеОбразца();		
		Если Объект["ПостояннаяДлинаНомера"+Элемент] И  НЕ СтрДлина(Объект["НомерОкончания"+Элемент]) = Объект["КоличествоСимволов"+Элемент] Тогда 
			Объект["КоличествоСимволов"+Элемент] = СтрДлина(Объект["НомерОкончания"+Элемент]);
		КонецЕсли;	
	ИначеЕсли Объект["ПостояннаяДлинаНомера"+Элемент] И НЕ СтрДлина(Объект["НомерОкончания"+Элемент]) = Объект["КоличествоСимволов"+Элемент] Тогда 
		Объект["КоличествоСимволов"+Элемент] = СтрДлина(Объект["НомерОкончания"+Элемент]);		
		УстановитьКоличествоСимволов(Элемент);
	Иначе	
		ОтобразитьКоличествоСимволов(Элемент);		
		УстановитьПредставлениеОбразца();
	КонецЕсли;	
	
КонецПроцедуры // УстановитьНомерОкончания()

&НаКлиенте
// Выполнить обработку события "Регулирование" для элемента "НомерОкончания".
//
// Параметры:
//  Элемент	- Строка - имя элемента номера.
//  Направление	- Число - направление регулирования.
//
Процедура РегулированиеНомерОкончания(Элемент,Направление)

	Если Направление = 1 Тогда 
		Номер = ПерейтиКСледующемуНомеру(Объект["НомерОкончания"+Элемент],Объект["ТипСимволов"+Элемент]="Числовой","НомерОкончания"+Элемент);
	Иначе
		Номер = ПерейтиКПредыдущемуНомеру(Объект["НомерОкончания"+Элемент],Объект["ПостояннаяДлинаНомера"+Элемент],"НомерОкончания"+Элемент);
	КонецЕсли;
	
	Если НЕ Номер = Объект["НомерОкончания"+Элемент] Тогда 
		Объект["НомерОкончания"+Элемент] = Номер;
		УстановитьНомерОкончания(Элемент);
	КонецЕсли;	
	
КонецПроцедуры // РегулированиеНомерОкончания()

&НаКлиенте
// Выполнить установку количества номеров.
//
// Параметры:
//  Элемент - Строка - имя элемента номера.
//
Процедура ОтобразитьКоличествоСимволов(Элемент)
	
	ВыполнитьФормированиеМассиваСимволов(Элемент, Ложь);
	
КонецПроцедуры // ОтобразитьКоличествоСимволов()

&НаКлиенте
// Выпонлим формирование следующего буквеного номера.
//
// Параметры:
//  ТекущийНомер - Строка - текущий номер.
//  Числовой - Булево - признак формирования числового номера.
//  ИмяЭлемента - Строка - имя редактируемого элемента.
//
// Возвращаемое значение:
//  Строка - следующий номер.
//
Функция ПерейтиКСледующемуНомеру(ТекущийНомер,Числовой,ИмяЭлемента)
	НаборЦифр = "1234567890";
	НаборЦифр_0 = "0123456789";
	НаборАлфавита = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";	
	НовыйНомер = "";
	Для Сч = 1 По 5 Цикл
		ПереходНаРазряд = Ложь;
		ТекущийСимвол = "";
		ТекущаяДлинаНомера = СтрДлина(ТекущийНомер);
		Если НЕ Сч > ТекущаяДлинаНомера Тогда
			ТекущийСимвол = Лев(Прав(ТекущийНомер,Сч),1);
		КонецЕсли;		
		Если Найти(НаборЦифр,ТекущийСимвол) > 0 Тогда 
			Тип = НаборЦифр_0;
		ИначеЕсли Найти(НаборАлфавита,ТекущийСимвол) > 0 Тогда 
			Тип = НаборАлфавита;
		ИначеЕсли Не ЗначениеЗаполнено(ТекущийСимвол) Тогда 	
			Если Числовой Тогда 
				Тип = НаборЦифр_0;
			Иначе
				Тип = НаборАлфавита;
			КонецЕсли;
		Иначе	
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),ТекущаяДлинаНомера-Сч+1);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект."+ИмяЭлемента);
			Возврат ТекущийНомер;
		КонецЕсли;		
		Если Тип = НаборЦифр_0 Тогда 
			Если ЗначениеЗаполнено(ТекущийСимвол) Тогда 
				ТекущийСимволОт = "0";
			Иначе	
				ТекущийСимволОт = "1";
			КонецЕсли;	
			ТекущийСимволДо = "9";
		Иначе
			ТекущийСимволОт = "A";
			ТекущийСимволДо = "Z";
		КонецЕсли;
		Если ТекущийСимвол = ТекущийСимволДо Тогда 
			ТекущийСимвол = ТекущийСимволОт;
			ПереходНаРазряд = Истина;
		Иначе 
			ТекущийСимвол = Сред(Тип,Найти(Тип,ТекущийСимвол)+1,1);
		КонецЕсли;
		Если ПереходНаРазряд Тогда 
			НовыйНомер = ТекущийСимвол+НовыйНомер;
		Иначе
			Возврат Лев(ТекущийНомер,ТекущаяДлинаНомера-Сч)+ТекущийСимвол+НовыйНомер;
		КонецЕсли;
	КонецЦикла;	// Для Сч = 1 По 5 Цикл
		
	Возврат ТекущийНомер;
	
КонецФункции // ПерейтиКСледующемуНомеру()

&НаКлиенте
// Выполним формирование предыдущего буквенного номера.
//
// Параметры:
//  ТекущийНомер - Строка - текущий номер.
//  ФиксированнаяДлина - Булево - фиксированная длина номера.
//  ИмяЭлемента - Строка - имя редактируемого элемента.
//
// Возвращаемое значение:
//  Строка - предыдущий номер.
//
Функция ПерейтиКПредыдущемуНомеру(ТекущийНомер,ФиксированнаяДлина,ИмяЭлемента)
	НаборЦифр = "1234567890";
	НаборЦифр_0 = "0123456789";
	НаборАлфавита = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	НовыйНомер = "";
	Для Сч = 1 По 5 Цикл
		ПереходНаРазряд = Ложь;
		ТекущийСимвол = "";
		ТекущаяДлинаНомера = СтрДлина(ТекущийНомер);
		Если НЕ Сч > ТекущаяДлинаНомера Тогда
			ТекущийСимвол = Лев(Прав(ТекущийНомер,Сч),1);
		КонецЕсли;
		Если Найти(НаборЦифр, ТекущийСимвол) > 0 Тогда 
			Тип = НаборЦифр_0;
		ИначеЕсли Найти(НаборАлфавита, ТекущийСимвол) > 0 Тогда 
			Тип = НаборАлфавита;
		ИначеЕсли Не ЗначениеЗаполнено(ТекущийСимвол) Тогда 	
			Возврат ТекущийНомер;
		Иначе
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),ТекущаяДлинаНомера-Сч+1);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект."+ИмяЭлемента);
			Возврат ТекущийНомер;
		КонецЕсли;		
		Если Тип = НаборЦифр_0 Тогда 
			ТекущийСимволОт = "0";
			ТекущийСимволДо = "9";
		Иначе
			ТекущийСимволОт = "A";
			ТекущийСимволДо = "Z";
		КонецЕсли;		
		Если ТекущийСимвол = ТекущийСимволОт Тогда 
			Если ЗначениеЗаполнено(Лев(ТекущийНомер,ТекущаяДлинаНомера-Сч)) Тогда 
				ТекущийСимвол = ТекущийСимволДо;
				ПереходНаРазряд = Истина;
			Иначе	
				Возврат ТекущийНомер;
			КонецЕсли;	
		Иначе 
			ТекущийСимвол = Сред(Тип,Найти(Тип, ТекущийСимвол)-1,1);
			Если НЕ ФиксированнаяДлина
				И НЕ ЗначениеЗаполнено(Лев(ТекущийНомер,ТекущаяДлинаНомера-Сч))
				И (ТекущийСимвол = "0" ИЛИ ТекущийСимвол = "A") Тогда 
				ТекущийСимвол = "";
			КонецЕсли;	
		КонецЕсли;		
		Если ПереходНаРазряд Тогда 
			НовыйНомер = ТекущийСимвол + НовыйНомер;
		Иначе
			Возврат Лев(ТекущийНомер,ТекущаяДлинаНомера-Сч)+ТекущийСимвол+НовыйНомер;			
		КонецЕсли;
	КонецЦикла;	// Для Сч = 1 По 5 Цикл
		
	Возврат ТекущийНомер;
	
КонецФункции // ПерейтиКПредыдущемуНомеру()

&НаКлиенте
// Выполним формирование массива номеров.
//
// Параметры:
//  Элемент	- Строка - имя элемента номера.
//  Отказ - Булево - признак произошедшей ошибки.
//
// Возвращаемое значение:
//  Массив - массив строк с номерами.
//
Функция ВыполнитьФормированиеМассиваСимволов(Элемент, Отказ)
	
	Объект["Количество"+Элемент] = 0;
	
	НумерацияОт = ВРег(СокрЛП(Объект["НомерНачало"+Элемент]));
	НумерацияДо = ВРег(СокрЛП(Объект["НомерОкончания"+Элемент]));
	
	МассивНомеров = Новый Массив;
	
	Если Объект["ТипСимволов"+Элемент] = "Числовой" Тогда 
		Попытка
			ЧислоОт = Число(НумерацияОт);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указано неверное значение'"),,"Объект.НомерНачало"+Элемент,, Отказ);
			Возврат МассивНомеров;
		КонецПопытки;	
		
		Попытка
			ЧислоДо = Число(НумерацияДо);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указано неверное значение'"),,"Объект.НомерОкончания"+Элемент,, Отказ);
			Возврат МассивНомеров;
		КонецПопытки;	
		
		Если ЧислоОт > ЧислоДо Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Начало диапзона больше окончания'"),,"Объект.НомерОкончания"+Элемент,, Отказ);
			Возврат МассивНомеров;
		КонецЕсли;	
		
		Объект["Количество"+Элемент] = ЧислоДо-ЧислоОт+1;
		
		Для Сч = ЧислоОт По ЧислоДо Цикл 
			Если Объект["ПостояннаяДлинаНомера"+Элемент] Тогда 
				ТекущийНомер = Формат(Сч, "ЧЦ="+Объект["КоличествоСимволов"+Элемент]+"; ЧВН=; ЧН=; ЧГ=");
			Иначе	
				ТекущийНомер = Формат(Сч, "ЧН=; ЧГ=");
			КонецЕсли;	
			МассивНомеров.Добавить(ТекущийНомер);
		КонецЦикла;
		
	Иначе		
		ТекущийНомер = НумерацияОт;
		Пока Истина Цикл
			Если СтрДлина(ТекущийНомер) > СтрДлина(НумерацияДо)
				ИЛИ (СтрДлина(ТекущийНомер) = СтрДлина(НумерацияДо) И ТекущийНомер > НумерацияДо) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указано неверное значение'"),,"Объект.НомерОкончания"+Элемент,, Отказ);
				МассивНомеров.Очистить();
				Возврат МассивНомеров;
			КонецЕсли;			
			МассивНомеров.Добавить(ТекущийНомер);			
			Если ТекущийНомер = НумерацияДо Тогда 
				Прервать;
			КонецЕсли;			
			ТекущийНомер = ПерейтиКСледующемуНомеруВДиапазоне(Элемент, ТекущийНомер, НумерацияОт, НумерацияДо, Объект["МатрицаСимволов"+Элемент], Отказ);			
			Если Отказ Тогда 
				МассивНомеров.Очистить();
				Возврат МассивНомеров;
			КонецЕсли;
		КонецЦикла;		
		Объект["Количество"+Элемент] = МассивНомеров.Количество();
	КонецЕсли;
	
	Возврат МассивНомеров;
			
КонецФункции // ВыполнитьФормированиеМассиваСимволов()

&НаКлиенте
// Выпонлить формирование следующего буквенного номера в диапазоне номеров.
//
// Параметры:
//  Элемент - Строка - имя элемента номера.
//  ТекущийНомер - Строка - текущий номер.
//  НумерацияОт - Строка - начало диапазона нумерации.
//  НумерацияДо - Строка - конец диапазона нумерации.
//  Матрица - Булево - признак построения матрицы номеров.
//  Отказ - Булево - признак произошедшей ошибки.
//
// Возвращаемое значение:
//  Строка - следующий номер.
//
Функция ПерейтиКСледующемуНомеруВДиапазоне(Элемент,ТекущийНомер,НумерацияОт,НумерацияДо,Матрица,Отказ)
	
	НаборЦифр = "1234567890";
	НаборЦифр_0 = "0123456789";
	НаборАлфавита = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";	
	НовыйНомер = "";
	
	Для Сч = 1 По СтрДлина(НумерацияДо) Цикл
		
		ПереходНаРазряд = Ложь;		
		ТекущийСимвол = Лев(Прав(ТекущийНомер,Сч),1);
		ТекущийСимволОт = Лев(Прав(НумерацияОт,Сч),1);
		ТекущийСимволДо = Лев(Прав(НумерацияДо,Сч),1);
		
		Если Матрица И ТекущийСимволДо < ТекущийСимволОт Тогда 
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),СтрДлина(НумерацияДо)-Сч+1);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.НомерОкончания"+Элемент,,Отказ);
			Возврат НовыйНомер;
		КонецЕсли;	
		
		Если Найти(НаборЦифр,ТекущийСимвол) > 0 Тогда 
			Если Найти(НаборЦифр,ТекущийСимволДо) = 0 Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),СтрДлина(НумерацияДо)-Сч+1);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.НомерОкончания"+Элемент,,Отказ);
				Возврат НовыйНомер;
			КонецЕсли;
			Тип = НаборЦифр_0;
		ИначеЕсли Найти(НаборАлфавита,ТекущийСимвол) > 0 Тогда 
			Если Найти(НаборАлфавита, ТекущийСимволДо) = 0 Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),СтрДлина(НумерацияДо)-Сч+1);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.НомерОкончания"+Элемент,,Отказ);
				Возврат НовыйНомер;
			КонецЕсли;
			Тип = НаборАлфавита;
		ИначеЕсли Не Матрица И Не ЗначениеЗаполнено(ТекущийСимвол) Тогда 	
			Если Найти(НаборЦифр, ТекущийСимволДо) > 0 Тогда 
				Тип = НаборЦифр_0;
			ИначеЕсли Найти(НаборАлфавита, ТекущийСимволДо) > 0 Тогда 
				Тип = НаборАлфавита;
			ИначеЕсли Не ЗначениеЗаполнено(ТекущийСимволДо) Тогда 	
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указано неверное значение'"),, "Объект.НомерОкончания"+Элемент,,Отказ);
				Возврат НовыйНомер;
			Иначе	
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),СтрДлина(НумерацияДо)-Сч+1);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.НомерОкончания"+Элемент,,Отказ);
				Возврат НовыйНомер;
			КонецЕсли;
		Иначе
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Указано неверное значение (%1 символ)'"),СтрДлина(НумерацияОт)-Сч+1);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.НомерНачало"+Элемент,,Отказ);
			Возврат НовыйНомер;
		КонецЕсли;
		
		Если НЕ Матрица Тогда 
			Если НЕ Лев(ТекущийНомер,СтрДлина(ТекущийНомер)-Сч) = Лев(НумерацияДо,СтрДлина(НумерацияДо)-Сч) Тогда 
				Если Тип = НаборЦифр_0 Тогда 
					Если ЗначениеЗаполнено(Лев(ТекущийНомер,СтрДлина(ТекущийНомер)-Сч)) Тогда 
						ТекущийСимволОт = "0";
					Иначе	
						ТекущийСимволОт = "1";
					КонецЕсли;	
					ТекущийСимволДо = "9";
				Иначе                            
					ТекущийСимволОт = "A";
					ТекущийСимволДо = "Z";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;		
		Если ТекущийСимвол = ТекущийСимволДо Тогда 
			ТекущийСимвол = ТекущийСимволОт;
			ПереходНаРазряд = Истина;
		Иначе 
			ТекущийСимвол = Сред(Тип, Найти(Тип, ТекущийСимвол) + 1, 1);
		КонецЕсли;		
		Если ПереходНаРазряд Тогда 
			НовыйНомер = ТекущийСимвол + НовыйНомер;
		Иначе 
			Возврат Лев(ТекущийНомер,СтрДлина(ТекущийНомер)-Сч)+ТекущийСимвол+НовыйНомер;		
		КонецЕсли;
	КонецЦикла;	
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указано неверное значение'"),,"Объект.НомерОкончания"+Элемент,,Отказ);
	Возврат НовыйНомер;
	
КонецФункции // ПерейтиКСледующемуНомеруВДиапазоне()

#КонецОбласти 
