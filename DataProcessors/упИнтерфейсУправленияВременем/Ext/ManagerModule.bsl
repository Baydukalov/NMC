#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу с расписание работ.
//
// Параметры:
//  ГрафикРаботы  - СправочникСсылка - Ссылка на объект.
//  НачалоПериода  - Дата - Начало.
//  КонецПериода  - Дата - Окончание.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат выполнения.
//
Функция ПолучитьВременныеОкнаДоступности(ГрафикРаботы, НачалоПериода, КонецПериода) Экспорт
	
	Если Не ЗначениеЗаполнено(КонецПериода) Тогда
		КонецПериода = '21001231';
	КонецЕсли;  	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаПериодыДоступности();
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Календарь", ГрафикРаботы);
	тзПериодыДоступности = Запрос.Выполнить().Выгрузить();
	
	Если тзПериодыДоступности.Количество() > 1 Тогда
		тзПериодыДоступности = ОбъединитьПериодыДоступности(тзПериодыДоступности);
	КонецЕсли;
	
	Возврат тзПериодыДоступности;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбъединитьПериодыДоступности(ТаблицаРасписаний)
	
	тзВременныхОкон = ТаблицаРасписаний.СкопироватьКолонки();
	КоличествоСтрок = ТаблицаРасписаний.Количество();
	
	текСтрока = ТаблицаРасписаний[0];
	НачалоОкна = текСтрока.НачалоПериода;
	КонецОкна = текСтрока.КонецПериода;
	Сч = 1;
	Пока Сч < КоличествоСтрок Цикл
		текСтрока = ТаблицаРасписаний[Сч];
		Если КонецОкна + 1 = текСтрока.НачалоПериода Тогда
			КонецОкна = текСтрока.КонецПериода;
		Иначе
			НоваяСтрока = тзВременныхОкон.Добавить();
			НоваяСтрока.НачалоПериода = НачалоОкна;
			НоваяСтрока.КонецПериода = КонецОкна;
			НачалоОкна = текСтрока.НачалоПериода;
			КонецОкна = текСтрока.КонецПериода;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	НоваяСтрока = тзВременныхОкон.Добавить();
	НоваяСтрока.НачалоПериода = НачалоОкна;
	НоваяСтрока.КонецПериода = КонецОкна;	
	
	Возврат тзВременныхОкон;
	
КонецФункции // ОбъединитьПериодыДоступности()

функция ПолучитьТекстЗапросаПериодыДоступности()
	
	Возврат
	"ВЫБРАТЬ
	|	ШаблонЗаполнения.Ссылка КАК ГрафикРаботы,
	|	МАКСИМУМ(ШаблонЗаполнения.НомерСтроки) КАК ДлинаЦикла
	|ПОМЕСТИТЬ втДлинаЦиклаГрафиков
	|ИЗ
	|	Справочник.Календари.ШаблонЗаполнения КАК ШаблонЗаполнения
	|ГДЕ
	|	ШаблонЗаполнения.Ссылка = &Календарь
	|
	|СГРУППИРОВАТЬ ПО
	|	ШаблонЗаполнения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Календари.Ссылка КАК ГрафикРаботы,
	|	ДанныеПроизводственногоКалендаря.Дата КАК ДатаГрафика,
	|	ДанныеПроизводственногоКалендаря.ДатаПереноса,
	|	ВЫБОР
	|		КОГДА ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПредпраздничныйДень
	|ПОМЕСТИТЬ втСведенияПоКалендарю
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|		ПО (Календари.Ссылка = &Календарь)
	|			И ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = Календари.ПроизводственныйКалендарь
	|			И (ДанныеПроизводственногоКалендаря.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ) И &КонецПериода)
	|			И (ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|				ИЛИ ДанныеПроизводственногоКалендаря.ДатаПереноса <> ДАТАВРЕМЯ(1, 1, 1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалендарныеГрафики.Календарь КАК ГрафикРаботы,
	|	КалендарныеГрафики.ДатаГрафика КАК ДатаГрафика,
	|	РАЗНОСТЬДАТ(Календари.ДатаОтсчета, КалендарныеГрафики.ДатаГрафика, ДЕНЬ) + 1 КАК ДнейОтДатыОтсчета,
	|	СведенияПоКалендарю.ПредпраздничныйДень,
	|	СведенияПоКалендарю.ДатаПереноса
	|ПОМЕСТИТЬ втДниВключенныеВГрафик
	|ИЗ
	|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|		ПО (Календари.Ссылка = &Календарь)
	|			И КалендарныеГрафики.Календарь = Календари.Ссылка
	|			И (КалендарныеГрафики.ДатаГрафика МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ) И &КонецПериода)
	|			И (КалендарныеГрафики.ДеньВключенВГрафик)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияПоКалендарю КАК СведенияПоКалендарю
	|		ПО (СведенияПоКалендарю.ГрафикРаботы = КалендарныеГрафики.Календарь)
	|			И (СведенияПоКалендарю.ДатаГрафика = КалендарныеГрафики.ДатаГрафика)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДниВключенныеВГрафик.ГрафикРаботы,
	|	ДниВключенныеВГрафик.ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДниВключенныеВГрафик.РезультатДеленияПоМодулю = 0
	|			ТОГДА ДниВключенныеВГрафик.ДлинаЦикла
	|		ИНАЧЕ ДниВключенныеВГрафик.РезультатДеленияПоМодулю
	|	КОНЕЦ КАК НомерДня,
	|	ДниВключенныеВГрафик.ПредпраздничныйДень
	|ПОМЕСТИТЬ втДатыНомераДней
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|		ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|		ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|		ДниВключенныеВГрафик.ДлинаЦикла КАК ДлинаЦикла,
	|		ДниВключенныеВГрафик.ДнейОтДатыОтсчета - ДниВключенныеВГрафик.ЦелаяЧастьРезультатаДеления * ДниВключенныеВГрафик.ДлинаЦикла КАК РезультатДеленияПоМодулю
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|			ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|			ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|			ДниВключенныеВГрафик.ДнейОтДатыОтсчета КАК ДнейОтДатыОтсчета,
	|			ДлинаЦиклов.ДлинаЦикла КАК ДлинаЦикла,
	|			(ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) - ВЫБОР
	|				КОГДА (ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) > ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ЦелаяЧастьРезультатаДеления
	|		ИЗ
	|			втДниВключенныеВГрафик КАК ДниВключенныеВГрафик
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|				ПО ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
	|					И (Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоЦикламПроизвольнойДлины))
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДлинаЦиклаГрафиков КАК ДлинаЦиклов
	|				ПО ДниВключенныеВГрафик.ГрафикРаботы = ДлинаЦиклов.ГрафикРаботы) КАК ДниВключенныеВГрафик) КАК ДниВключенныеВГрафик
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДниВключенныеВГрафик.ГрафикРаботы,
	|	ДниВключенныеВГрафик.ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДниВключенныеВГрафик.ДатаПереноса ЕСТЬ NULL 
	|			ТОГДА ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаГрафика)
	|		ИНАЧЕ ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаПереноса)
	|	КОНЕЦ,
	|	ДниВключенныеВГрафик.ПредпраздничныйДень
	|ИЗ
	|	втДниВключенныеВГрафик КАК ДниВключенныеВГрафик
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|		ПО ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
	|ГДЕ
	|	Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоНеделям)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДниВключенныеВГрафик.ГрафикРаботы,
	|	ДниВключенныеВГрафик.ДатаГрафика,
	|	ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяНачала, РасписанияРаботы.ВремяНачала) КАК ВремяНачала,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяОкончания, РасписанияРаботы.ВремяОкончания) = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 23, 59, 59)
	|		ИНАЧЕ ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяОкончания, РасписанияРаботы.ВремяОкончания)
	|	КОНЕЦ КАК ВремяОкончания
	|ПОМЕСТИТЬ втРасписанияРаботы
	|ИЗ
	|	втДатыНомераДней КАК ДниВключенныеВГрафик
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботы
	|		ПО (РасписанияРаботы.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы)
	|			И (РасписанияРаботы.НомерДня = ДниВключенныеВГрафик.НомерДня)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботыПредпраздничногоДня
	|		ПО (РасписанияРаботыПредпраздничногоДня.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы)
	|			И (РасписанияРаботыПредпраздничногоДня.НомерДня = 0)
	|			И (ДниВключенныеВГрафик.ПредпраздничныйДень)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяНачала) * 3600 + МИНУТА(РасписанияРаботы.ВремяНачала) * 60 + СЕКУНДА(РасписанияРаботы.ВремяНачала)), РасписанияРаботы.ДатаГрафика) КАК НачалоПериода,
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяОкончания) * 3600 + МИНУТА(РасписанияРаботы.ВремяОкончания) * 60 + СЕКУНДА(РасписанияРаботы.ВремяОкончания)), КОНЕЦПЕРИОДА(РасписанияРаботы.ДатаГрафика, ДЕНЬ)) КАК КонецПериода
	|ИЗ
	|	втРасписанияРаботы КАК РасписанияРаботы
	|ГДЕ
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяОкончания) * 3600 + МИНУТА(РасписанияРаботы.ВремяОкончания) * 60 + СЕКУНДА(РасписанияРаботы.ВремяОкончания)), КОНЕЦПЕРИОДА(РасписанияРаботы.ДатаГрафика, ДЕНЬ)) > &НачалоПериода
	|	И ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяНачала) * 3600 + МИНУТА(РасписанияРаботы.ВремяНачала) * 60 + СЕКУНДА(РасписанияРаботы.ВремяНачала)), РасписанияРаботы.ДатаГрафика) < &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	КонецПериода";
	
КонецФункции

#КонецОбласти

#КонецЕсли
