
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПроверкаЗаполнения") Тогда
		ПроверкаЗаполнения =  Параметры.ПроверкаЗаполнения;
		Элементы.НачалоПериода.АвтоОтметкаНезаполненного = ПроверкаЗаполнения;
		Элементы.КонецПериода.АвтоОтметкаНезаполненного = ПроверкаЗаполнения;
	КонецЕсли; 
	
	НачалоПериода = Параметры.НачалоПериода;
	ВремяНачалаПериода = '00010101000000' + Час(Параметры.НачалоПериода)*3600 + Минута(Параметры.НачалоПериода)*60 + Секунда(Параметры.НачалоПериода);
	КонецПериода  = Параметры.КонецПериода;
	ВремяКонцаПериода = '00010101000000' + Час(Параметры.КонецПериода)*3600 + Минута(Параметры.КонецПериода)*60 + Секунда(Параметры.КонецПериода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ДатаНачала = НачалоПериода + Час(ВремяНачалаПериода)*3600 + Минута(ВремяНачалаПериода)*60 + Секунда(ВремяНачалаПериода);
	ДатаОкончания = КонецПериода + Час(ВремяКонцаПериода)*3600 + Минута(ВремяКонцаПериода)*60 + Секунда(ВремяКонцаПериода);
	
	Если ПроверкаЗаполнения И Не ЗначениеЗаполнено(НачалоПериода) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите начало периода'"), , "НачалоПериода");
	    Возврат;
	КонецЕсли; 
	
	Если ПроверкаЗаполнения И Не ЗначениеЗаполнено(КонецПериода) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите окончание периода'"), , "КонецПериода");
	    Возврат;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда
		Если ДатаНачала > ДатаОкончания Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата начала периода больше чем дата окончания периода'"), , "НачалоПериода");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура("НачалоПериода, КонецПериода");
	РезультатВыбора.НачалоПериода = ДатаНачала; 
	РезультатВыбора.КонецПериода = ДатаОкончания; 
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти
