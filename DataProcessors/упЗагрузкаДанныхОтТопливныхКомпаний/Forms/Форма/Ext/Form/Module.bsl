&НаКлиенте
Перем ПараметрыОбработчика;

&НаКлиенте
Перем ПодтверждениеЗакрытияФормы;

&НаКлиенте
Перем ФормаДлительнойОперации;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТопливнаяКомпания = Перечисления.упТопливныеКомпании.ЗначениеПоУмолчанию();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаАктивностейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТаблицаАктивностейСотрудник" Тогда
		Строка = ТаблицаДанныхПоТопливу.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если НЕ Строка = Неопределено Тогда
			Ключ = Строка["Сотрудник"];
			Если ЗначениеЗаполнено(Ключ) Тогда
				ПараметрыФормы = Новый Структура("Ключ", Ключ);
				ОткрытьФорму("Справочник.упСотрудники.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "ТаблицаАктивностейРейс" Тогда	
		Строка = ТаблицаДанныхПоТопливу.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если НЕ Строка = Неопределено Тогда
			Ключ = Строка["Рейс"];
			Если ЗначениеЗаполнено(Ключ) Тогда
				ПараметрыФормы = Новый Структура("Ключ", Ключ);
				ОткрытьФорму("Документ.упРейс.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайла(Команда)
	ЗагрузитьДанныеИзФайлаКлиент(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицуИзФайла(Команда)
	ЗагрузитьДанныеИзФайлаКлиент(Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для каждого Строка Из ТаблицаДанныхПоТопливу Цикл
		Строка.Флаг = Истина;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для каждого Строка Из ТаблицаДанныхПоТопливу Цикл
		Строка.Флаг = Ложь;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьТопливныеКарты(Команда)
	
	Если ПолучитьПомеченныеДанные() Тогда
		мсвТаблицаСопоставленияТопливныхКарт = ТаблицаСопоставленияТопливныхКарт();
		ПараметрыФормы = Новый Структура("ТопливныеКарты", мсвТаблицаСопоставленияТопливныхКарт);
		ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСопоставленияТопливныхКарт", ЭтаФорма);
		ОткрытьФорму("Обработка.упЗагрузкаДанныхОтТопливныхКомпаний.Форма.ФормаСопоставленияТопливныхКарт", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,ОбработчикЗакрытияФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьГСМ(Команда)
	Если ПолучитьПомеченныеДанные() Тогда
		мсвТаблицаСопоставления = ТаблицаСопоставленияГСМ();
		ПараметрыФормы = Новый Структура("ГСМ", мсвТаблицаСопоставления);
		ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСопоставленияГСМ", ЭтаФорма);
		ОткрытьФорму("Обработка.упЗагрузкаДанныхОтТопливныхКомпаний.Форма.ФормаСопоставленияГСМ", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,ОбработчикЗакрытияФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьВалюту(Команда)
	Если ПолучитьПомеченныеДанные() Тогда
		мсвТаблицаСопоставления = ТаблицаСопоставленияВалюты();
		ПараметрыФормы = Новый Структура("Валюты", мсвТаблицаСопоставления);
		ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыСопоставленияВалют", ЭтаФорма);
		ОткрытьФорму("Обработка.упЗагрузкаДанныхОтТопливныхКомпаний.Форма.ФормаСопоставленияВалют", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,ОбработчикЗакрытияФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеВРегистр(Команда)
	ЗагрузитьДанныеВРегистрКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеВРегистрИЗакрытьФорму(Команда)
	ЗагрузитьДанныеВРегистрКлиент(Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Функция ПолучитьПомеченныеДанные()

	Результат = Истина;
	НайденныеСтроки = ТаблицаДанныхПоТопливу.НайтиСтроки(Новый Структура("Флаг", Истина));
	Если НайденныеСтроки.Количество() = 0 Тогда
		ТекстСообщения = Нстр("ru = 'Установите флаг(пометку) в строках для сопоставления данных.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Результат = Ложь;
	КонецЕсли;
	Возврат Результат;

КонецФункции // ПолучитьПомеченныеДанные()

&НаКлиенте
Процедура ЗагрузитьДанныеВРегистрКлиент(ЗакрытьФорму = Ложь)
	
	Отказ = Ложь;
	ТекстОшибки = "";
	
	// Проверка таблицы
	Сч = 1;
	Для каждого Строка Из ТаблицаДанныхПоТопливу Цикл
		Если Строка.Флаг Тогда
			Если НЕ ЗначениеЗаполнено(Строка.ТопливнаяКарта) Тогда
				ТекстСообщения = Нстр("ru = 'Не заполнена топливная карта. Используйте команды сопоставления.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТаблицаДанныхПоТопливу", Сч, "ТопливнаяКарта"),, Отказ);
			ИначеЕсли НЕ ЗначениеЗаполнено(Строка.ВидГСМ) Тогда	
				ТекстСообщения = Нстр("ru = 'Не заполнен вид ГСМ. Используйте команды сопоставления.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТаблицаДанныхПоТопливу", Сч, "ВидГСМ"),, Отказ);
			ИначеЕсли НЕ ЗначениеЗаполнено(Строка.Валюта) Тогда	
				ТекстСообщения = Нстр("ru = 'Не заполна валюта. Используйте команды сопоставления.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТаблицаДанныхПоТопливу", Сч, "Валюта"),, Отказ);	
			КонецЕсли;
		КонецЕсли;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;

	// Загрузка данных
	Если НЕ Отказ Тогда
		ЗагрузитьДанныеВРегистрНаСервере(ТекстОшибки, Отказ);	
		Если Отказ Тогда
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);	
		КонецЕсли;
	КонецЕсли;
	
	Если Не Отказ И ЗакрытьФорму Тогда
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОкончаниеПомещенияФайла(Результат, АдресВременногоХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат = Истина Тогда
		Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ВыбранноеИмяФайла));
		ТекущееРасширение = "xml";
		Если ТопливнаяКомпания = ПредопределенноеЗначение("Перечисление.упТопливныеКомпании.Роснефть") Тогда
			ТекущееРасширение = "xls";
		КонецЕсли;
		Если Расширение = ТекущееРасширение Тогда
			ФоновоеЗадание 	= Ложь;
			ТекстСообщения		= "";
			ЗагрузитьДанныеФайлаНаСервере(АдресВременногоХранилища, Расширение, ФоновоеЗадание, ТекстСообщения);
			Если ФоновоеЗадание Тогда
				ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
				ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте", 1, Истина);
				ПараметрыОбработчика.МаксимальныйИнтервал = 5;
				ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, ФоновоеЗаданиеИдентификатор);
			КонецЕсли;	
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстСообщения, Истина);
			КонецЕсли;
		Иначе
			ТекстСообщения = СтрШаблон(Нстр("ru = 'Выбранный тип файла не поддерживается. Выберите файл с расширением %1'"), ТекущееРасширение);
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстСообщения, Истина);
		КонецЕсли;
	КонецЕсли;	
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайлаКлиент(ОткрыватьДиалогВыбора = Истина)
	
	Если ОткрыватьДиалогВыбора Тогда
		Интерактивно 	= Истина;
		ПолноеИмяФайла 	= "";	
		Если ПодключитьРасширениеРаботыСФайлами() Тогда
			ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
			Если ТопливнаяКомпания = ПредопределенноеЗначение("Перечисление.упТопливныеКомпании.Роснефть") Тогда
				ДиалогВыбораФайла.Фильтр                      = НСтр("ru='Данные по топливу (*.xls)|*.xls'");
			Иначе
				ДиалогВыбораФайла.Фильтр                      = НСтр("ru='Данные по топливу (*.xml)|*.xml'");
			КонецЕсли;
			ДиалогВыбораФайла.Заголовок                   = Заголовок;
			ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
			ДиалогВыбораФайла.Расширение                  = "";
			ДиалогВыбораФайла.ИндексФильтра               = 0;
			ДиалогВыбораФайла.ПолноеИмяФайла              = "";
			ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
			
			Если ДиалогВыбораФайла.Выбрать() Тогда
				ПолноеИмяФайла = ДиалогВыбораФайла.ПолноеИмяФайла;
			КонецЕсли;
			
			ВыбранныйФайл 	= ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПолноеИмяФайла);
			Если ЗначениеЗаполнено(ВыбранныйФайл.Имя) Тогда
				ПолноеИмяФайла = ВыбранныйФайл.ПолноеИмя;
				Интерактивно = Ложь;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Интерактивно 	= Ложь;
	КонецЕсли;
	АдресВременногоХранилища = "";
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПриОкончаниеПомещенияФайла", ЭтотОбъект);
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, АдресВременногоХранилища, ПолноеИмяФайла, Интерактивно);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеФайлаНаСервере(АдресВременногоХранилища, Расширение, ФоновоеЗадание, ТекстСообщения)

	ИмяВременногоФайла	= ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные 		= ПолучитьИЗВременногоХранилища(АдресВременногоХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ПараметрыВызоваСервера = Новый Структура();
	ПараметрыВызоваСервера.Вставить("ИмяВременногоФайла", 			ИмяВременногоФайла);
	ПараметрыВызоваСервера.Вставить("ТопливнаяКомпания", 			ТопливнаяКомпания);
	СхемаКомпоновки = Неопределено;
	Если ТопливнаяКомпания = Перечисления.упТопливныеКомпании.Лукойл Тогда
		СхемаКомпоновки = Обработки.упЗагрузкаДанныхОтТопливныхКомпаний.ПолучитьМакет("ДанныеЛукойл");
	КонецЕсли;
	ПараметрыВызоваСервера.Вставить("СхемаДанныеТопливнойКомпании", СхемаКомпоновки);	
	ПараметрыВызоваСервера.Вставить("НастройкиСхемыДанных", 		Неопределено);
	
	РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
	"Обработки.упЗагрузкаДанныхОтТопливныхКомпаний.ЗагрузитьДанныеИзФайла",
	ПараметрыВызоваСервера, 
	НСтр("ru = 'Подсистема ЗагрузкаДанныхИзФайла: Выполнение серверного метода загрузка данных из файла'"));
	
	Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
		сткДанныеИзТахографа = ПолучитьИзВременногоХранилища(РезультатФоновогоЗадания.АдресХранилища);
		ТекстСообщения = сткДанныеИзТахографа.ТекстОшибки;
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗаполнитьДанныеФормыНаСервере(сткДанныеИзТахографа);
		КонецЕсли;
	Иначе
		ФоновоеЗадание = Истина;
		ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
		ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте()
	Результат = ФоновоеЗаданиеЗагрузкаФайлаПолучитьРезультат();
	Если Результат.ФоновоеЗаданиеВыполнено Тогда
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ФоновоеЗаданиеИдентификатор Тогда
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		КонецЕсли;
		сткДанныеИзТахографа = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		Если сткДанныеИзТахографа.Свойство("ТекстОшибки") И НЕ ПустаяСтрока(сткДанныеИзТахографа.ТекстОшибки) Тогда
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(сткДанныеИзТахографа.ТекстОшибки, Истина);
		КонецЕсли;
		ЗаполнитьДанныеФормыНаСервере(сткДанныеИзТахографа);
		УстановитьДоступность();
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ФоновоеЗаданиеЗагрузкаФайлаПолучитьРезультат()
	Результат = Новый Структура;
	Результат.Вставить("ФоновоеЗаданиеВыполнено", Ложь);
	Результат.ФоновоеЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеФормыНаСервере(сткДанныеТахографа)
	
	Сотрудник 	= Справочники.упСотрудники.ПустаяСсылка();
	НомерКарты	= "";
	ИмяВодителя	= "";
	ФамилияВодителя		= "";
	ОрганВыдавшийКарту	= "";
	ДатаВыпускаКарты 			= '00010101';
	ДатаНачалаДействияКарты 	= '00010101';
	ДатаОкончанияДействияКарты 	= '00010101';
	ТаблицаДанныхПоТопливу.Очистить();
		
	//	ЗаполнитьЗначенияСвойств(ЭтаФорма, сткДанныеТахографа.ДанныеИдентификации);
	//	Сотрудник		= сткДанныеТахографа.Сотрудник;
	//	КартаТахографа	= сткДанныеТахографа.КартаТахографа;
	
	//	Если ЗначениеЗаполнено(Сотрудник)
	//		И ЗначениеЗаполнено(КартаТахографа) Тогда
	//		СопоставленныйСотрудник = Сотрудник;
	//КонецЕсли;
	
	Для каждого Строка Из сткДанныеТахографа.ТаблицаДанныхПоТопливу Цикл
		НоваяСтрока = ТаблицаДанныхПоТопливу.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	Элементы.ОбновитьТаблицуИзФайла.Доступность 				= ЗначениеЗаполнено(ПолноеИмяФайла);
	//Элементы.ТаблицаДанныхСопоставитьВодителей.Доступность = ЗначениеЗаполнено(Сотрудник) 
	//																И НЕ СопоставленныйСотрудник = Сотрудник;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВРегистрНаСервере(ТекстОшибки, Отказ)
	Обработки.упЗагрузкаДанныхОтТопливныхКомпаний.ЗаписатьДанныеВРегистр(ТаблицаДанныхПоТопливу.Выгрузить(), ТекстОшибки, Отказ);
КонецПроцедуры

&НаСервере
Функция ТаблицаСопоставленияТопливныхКарт()
	мсвРезультат = Неопределено;
	
	тбзТопливныеКарты = ТаблицаДанныхПоТопливу.Выгрузить(Новый Структура("Флаг", Истина),"ТопливнаяКомпания, ТопливнаяКартаНомер, ВладелецКарты, ТопливнаяКарта, Сотрудник, ТранспортноеСредство");
	тбзТопливныеКарты.Свернуть("ТопливнаяКомпания, ТопливнаяКартаНомер, ВладелецКарты, ТопливнаяКарта, Сотрудник, ТранспортноеСредство");
	
	мсвРезультат = ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзТопливныеКарты);
	
	Возврат мсвРезультат;
	
КонецФункции

&НаСервере
Функция ТаблицаСопоставленияВалюты()
	мсвРезультат = Неопределено;
	
	тбзВалюты = ТаблицаДанныхПоТопливу.Выгрузить(Новый Структура("Флаг", Истина),"ВалютаЦифровойКод, Валюта");
	тбзВалюты.Свернуть("ВалютаЦифровойКод, Валюта");
	
	мсвРезультат = ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзВалюты);
	
	Возврат мсвРезультат;
	
КонецФункции

&НаСервере
Функция ТаблицаСопоставленияГСМ()
	мсвРезультат = Неопределено;
	
	тбзГСМ = ТаблицаДанныхПоТопливу.Выгрузить(Новый Структура("Флаг", Истина),"ВидГСМНаименование, ГСМНаименование, ГСМ, ВидГСМ");
	тбзГСМ.Свернуть("ВидГСМНаименование, ГСМНаименование, ГСМ, ВидГСМ");
	
	мсвРезультат = ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзГСМ);
	
	Возврат мсвРезультат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыСопоставленияГСМ(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
		Для каждого Строка Из РезультатЗакрытия Цикл
			Если ЗначениеЗаполнено(Строка.ВидГСМ) Тогда
				мсвСтрокиДанныхПоТопливу = ТаблицаДанныхПоТопливу.НайтиСтроки( Новый Структура("ВидГСМНаименование", Строка.ВидГСМНаименование));	
				Для каждого СтрокаДанныхПоТопливу Из мсвСтрокиДанныхПоТопливу Цикл
					ЗаполнитьЗначенияСвойств(СтрокаДанныхПоТопливу, Строка, "ВидГСМ");
				КонецЦикла;
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ГСМ) Тогда
				мсвСтрокиДанныхПоТопливу = ТаблицаДанныхПоТопливу.НайтиСтроки( Новый Структура("ГСМНаименование", Строка.ГСМНаименование));	
				Для каждого СтрокаДанныхПоТопливу Из мсвСтрокиДанныхПоТопливу Цикл
					ЗаполнитьЗначенияСвойств(СтрокаДанныхПоТопливу, Строка, "ГСМ");
				КонецЦикла;
			КонецЕсли;

		КонецЦикла;
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыСопоставленияВалют(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
		Для каждого Строка Из РезультатЗакрытия Цикл
			Если ЗначениеЗаполнено(Строка.Валюта) Тогда
				мсвСтрокиДанныхПоТопливу = ТаблицаДанныхПоТопливу.НайтиСтроки( Новый Структура("ВалютаЦифровойКод", Строка.ВалютаЦифровойКод));	
				Для каждого СтрокаДанныхПоТопливу Из мсвСтрокиДанныхПоТопливу Цикл
					ЗаполнитьЗначенияСвойств(СтрокаДанныхПоТопливу, Строка, "Валюта");
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыСопоставленияТопливныхКарт(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
		Для каждого Строка Из РезультатЗакрытия Цикл
			Если ЗначениеЗаполнено(Строка.ТопливнаяКарта) Тогда
				мсвСтрокиДанныхПоТопливу = ТаблицаДанныхПоТопливу.НайтиСтроки( Новый Структура("ТопливнаяКартаНомер", Строка.ТопливнаяКартаНомер));	
				Для каждого СтрокаДанныхПоТопливу Из мсвСтрокиДанныхПоТопливу Цикл
					ЗаполнитьЗначенияСвойств(СтрокаДанныхПоТопливу, Строка, "ТопливнаяКарта, Сотрудник, ТранспортноеСредство");
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти