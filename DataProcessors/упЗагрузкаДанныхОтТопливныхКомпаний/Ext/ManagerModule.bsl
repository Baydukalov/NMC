#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Процедура - Загружает данные из файла, по возможности сопоставляя сотрудника, карту тахографа и рейс.
//
// Параметры:
//  ПараметрыВызоваСервера	 - Структура - параметры
//  АдресХранилища			 - Строка - Адрес временного хранилища, куда поместили файл
//
Процедура ЗагрузитьДанныеИзФайла(ПараметрыВызоваСервера, АдресХранилища) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ТекстОшибки", "");
	Отказ = Ложь;
	
	ИмяВременногоФайла 	= ПараметрыВызоваСервера.ИмяВременногоФайла;
	ТопливнаяКомпания	= ПараметрыВызоваСервера.ТопливнаяКомпания;
	СхемаКомпоновки		= ПараметрыВызоваСервера.СхемаДанныеТопливнойКомпании;
	НастройкиСхемы		= ПараметрыВызоваСервера.НастройкиСхемыДанных;
	
	Если ТопливнаяКомпания = Перечисления.упТопливныеКомпании.Лукойл Тогда
		ЧтениеXML = Новый ЧтениеXML;
		Попытка
			ЧтениеXML.ОткрытьФайл(ИмяВременногоФайла);
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ТекстСообщения = Нстр("ru = 'При загрузке произошла ошибка:%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекстОшибки);
			сткРезультат.Вставить("ТекстОшибки", ТекстСообщения);
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,, ТекстОшибки);
			Отказ = Истина;
		КонецПопытки;   
		
		Если НЕ Отказ Тогда
			
			сткДанныеПоТопливу		= СформироватьТаблицуДанныхПоТопливу(СхемаКомпоновки);
			сткСоответствиеПолей 	= сткДанныеПоТопливу.СоответствиеПолей;
			тбзДанныеПоТопливу	 	= сткДанныеПоТопливу.ТаблицаДанныхПоТопливу;
			
			ЗаполнитьТаблицуДанныхПоТопливу(ЧтениеXML, тбзДанныеПоТопливу, ТопливнаяКомпания, сткСоответствиеПолей);
			сткРезультат.Вставить("ТаблицаДанныхПоТопливу",ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзДанныеПоТопливу));
		Иначе
			сткРезультат.Вставить("ТаблицаДанныхПоТопливу",Новый Массив);
		КонецЕсли;
		
		АдресХранилища = ПоместитьВоВременноеХранилище(сткРезультат, АдресХранилища);
		
	ИначеЕсли ТопливнаяКомпания = Перечисления.упТопливныеКомпании.Роснефть Тогда
		ТекстОшибки = "";
		тбзДанныеПоТопливу = ПрочитатьДанныеФайлаРоснефть(ИмяВременногоФайла, ТопливнаяКомпания, Отказ, ТекстОшибки);
		сткРезультат.Вставить("ТекстОшибки", ТекстОшибки);
		Если Отказ Тогда
			сткРезультат.Вставить("ТаблицаДанныхПоТопливу",Новый Массив);
		Иначе
			сткРезультат.Вставить("ТаблицаДанныхПоТопливу",ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзДанныеПоТопливу));
		КонецЕсли;
		АдресХранилища = ПоместитьВоВременноеХранилище(сткРезультат, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

Функция ПрочитатьДанныеФайлаРоснефть(ИмяФайла, ТопливнаяКомпания, Отказ, ТекстОшибки)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТаблицаДанныхПоТопливу = Новый ТаблицаЗначений;
	Попытка
		ТабличныйДокумент.Прочитать(ИмяФайла);
	Исключение
		ТекстСообщения = Нстр("ru = 'Ошибка подключения к Microsoft Excel: %1'");
		ОписаниеОшибки = ОписаниеОшибки();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОписаниеОшибки);		
		ЗаписьЖурналаРегистрации("Загрузка данных от топливных компаний", УровеньЖурналаРегистрации.Ошибка,,,ТекстСообщения);
		ТекстОшибки = ТекстСообщения;
		Отказ = Истина;
	КонецПопытки;
	
	Если НЕ Отказ Тогда
		КоличествоСтрок = ТабличныйДокумент.ВысотаТаблицы;
		ЭтоОбщийИтог = Ложь;
		ЭтоДанные = Ложь;
		ТаблицаОперацийПоКартам = Новый ТаблицаЗначений;
		ПараметрыДаты = Новый КвалификаторыДаты(ЧастиДаты.Дата);
		ПараметрыВремя = Новый КвалификаторыДаты(ЧастиДаты.Время);

		ТаблицаОперацийПоКартам.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата",,,,ПараметрыДаты),"Дата");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Время",Новый ОписаниеТипов("Дата",,,,ПараметрыВремя),"Время");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Количество");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Цена",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Цена");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Сумма");
		ТаблицаОперацийПоКартам.Колонки.Добавить("ЦенаСоСкидкой",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"ЦенаСоСкидкой");
		ТаблицаОперацийПоКартам.Колонки.Добавить("СуммаСоСкидкой",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"СуммаСоСкидкой");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Услуга",Новый ОписаниеТипов("Строка"),"Услуга");
		ТаблицаОперацийПоКартам.Колонки.Добавить("Операция",Новый ОписаниеТипов("Строка"),"Операция");
		ТаблицаОперацийПоКартам.Колонки.Добавить("НомерКарты",Новый ОписаниеТипов("Строка"),"НомерКарты");
		ТаблицаОперацийПоКартам.Колонки.Добавить("ДержательКарты",Новый ОписаниеТипов("Строка"),"ДержательКарты");
		ТаблицаОперацийПоКартам.Колонки.Добавить("ГСМНаименование",Новый ОписаниеТипов("Строка"),"ГСМНаименование");
		ТаблицаОперацийПоКартам.Колонки.Добавить("ГСМ",Новый ОписаниеТипов("СправочникСсылка.упГСМ"),"ГСМ");
		ТаблицаОперацийПоКартам.Колонки.Добавить("ТопливнаяКарта",Новый ОписаниеТипов("СправочникСсылка.упТопливныеКарты"),"ТопливнаяКарта");
		ТаблицаОперацийПоКартам.Колонки.Добавить("НомерТранзакции",Новый ОписаниеТипов("Строка"),"НомерТранзакции");
		
		СоответствиеКарты = Новый Соответствие;
		СоответствиеГСМ = Новый Соответствие;
		МассивОшибок = Новый Массив;
		ЭтоОперацииПоКарте = Ложь;
		Для Сч = 0 По КоличествоСтрок Цикл
			Попытка          
				ТекстПервойЯчейки = СокрЛП(ТабличныйДокумент.Область(Сч,1,Сч,1).Text);
				Если ПустаяСтрока(ТекстПервойЯчейки)Тогда
					ЭтоДанные = Ложь;
					Продолжить;
				ИначеЕсли СтрЧислоВхождений(ТекстПервойЯчейки, "Операции по карте") Тогда
					ЭтоОперацииПоКарте = Истина;
					//Это начало данных, сделаем еще шаг.
					Сч = Сч + 1; // обойти шапку таблицы.
					ЭтоДанные = Истина;
					Продолжить;
				ИначеЕсли НРег(ТекстПервойЯчейки) = НРег("Общий итог") Тогда
					// Конец данных.          
					ЭтоОбщийИтог = Истина;    
					Прервать;
				ИначеЕсли СтрЧислоВхождений(ТекстПервойЯчейки, "Итого по") Тогда
					ЭтоДанные = Ложь;                    
					Продолжить;
				КонецЕсли;
				
				Если ЭтоДанные Тогда
					ДатаЗаписи = СокрЛП(ТабличныйДокумент.Область(Сч, 1,Сч, 1).Text);
					Если СтрЧислоВхождений(ДатаЗаписи, ",") Тогда
						ДатаЗаписи = СтрЗаменить(ДатаЗаписи, ",", ".");
					КонецЕсли;
					
					ВремяЗаписи = СокрЛП(ТабличныйДокумент.Область(Сч, 2,Сч, 2).Text);
					Количество = СокрЛП(ТабличныйДокумент.Область(Сч, 3,Сч, 3).Text);
					Цена = СокрЛП(ТабличныйДокумент.Область(Сч, 4,Сч, 4).Text);
					Сумма = СокрЛП(ТабличныйДокумент.Область(Сч, 5,Сч, 5).Text);
					ЦенаСоСкидкой = СокрЛП(ТабличныйДокумент.Область(Сч, 6,Сч, 6).Text);
					СуммаСоСкидкой = СокрЛП(ТабличныйДокумент.Область(Сч, 7,Сч, 7).Text);
					Услуга = СокрЛП(ТабличныйДокумент.Область(Сч, 8,Сч, 8).Text);
					Операция = СокрЛП(ТабличныйДокумент.Область(Сч, 9,Сч, 9).Text);
					НомерКарты = СокрЛП(ТабличныйДокумент.Область(Сч, 10,Сч, 10).Text);
					ДержательКарты = СокрЛП(ТабличныйДокумент.Область(Сч, 11,Сч, 11).Text);
					
					НоваяСтрока = ТаблицаОперацийПоКартам.Добавить();
					Попытка
						НоваяСтрока.Дата = Дата(ДатаЗаписи + " 00:00:00");
					Исключение
						НоваяСтрока.Дата = Дата("00010101000000");
						ТекстСообщения = Нстр("ru = 'При получении даты в строке № %1, произошла ошибка: %2'");
						ОписаниеОшибки = ОписаниеОшибки();
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Сч, ОписаниеОшибки);		
						МассивОшибок.Добавить(ТекстСообщения);
					КонецПопытки;
					Попытка
						ДлинаВремени = СтрДлина(ВремяЗаписи);
						Нуль = ?(ДлинаВремени=7, "0", "");
						НоваяСтрока.Время = Дата(СтрШаблон("01.01.0001 %1", Нуль) + ВремяЗаписи);
					Исключение
						НоваяСтрока.Время = Дата("00010101000000");
						ТекстСообщения = Нстр("ru = 'При получении времени в строке № %1, произошла ошибка: %2'");
						ОписаниеОшибки = ОписаниеОшибки();
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Сч, ОписаниеОшибки);		
						МассивОшибок.Добавить(ТекстСообщения);
					КонецПопытки;
					НоваяСтрока.Период = НоваяСтрока.Дата + (НоваяСтрока.Время - Дата("00010101000000"));
					НоваяСтрока.Количество = Количество;
					НоваяСтрока.Цена = Цена;
					НоваяСтрока.Сумма = Сумма;
					НоваяСтрока.ЦенаСоСкидкой = ЦенаСоСкидкой;
					НоваяСтрока.СуммаСоСкидкой = СуммаСоСкидкой;
					НоваяСтрока.Услуга = Услуга;
					НоваяСтрока.Операция = Операция;
					НоваяСтрока.НомерКарты = НомерКарты;
					НоваяСтрока.ДержательКарты = ДержательКарты;
					
					НоваяСтрока.ГСМНаименование = Услуга;
					ГСМ = СоответствиеГСМ.Получить(Услуга);
					Если ГСМ = Неопределено Тогда
						ГСМ = Справочники.упГСМ.НайтиПоНаименованию(Услуга);
						СоответствиеГСМ.Вставить(Услуга, ГСМ);
					КонецЕсли;
					НоваяСтрока.ГСМ = ГСМ;
					
					ТопливнаяКарта = СоответствиеКарты.Получить(НомерКарты);
					Если ТопливнаяКарта = Неопределено Тогда
						ТопливнаяКарта = Справочники.упТопливныеКарты.НайтиПоКоду(НомерКарты);
						СоответствиеКарты.Вставить(НомерКарты, ТопливнаяКарта);
					КонецЕсли;
					НоваяСтрока.ТопливнаяКарта = ТопливнаяКарта;
					
					//<Дата+Время+НомерКарты>, 20180112083833C257321032, где С - сокращение от card:
					НомерТранзакции = Формат(НоваяСтрока.Период,"ДФ=yyyyMMddHHmmss") + "C" + НомерКарты;
					НоваяСтрока.НомерТранзакции = НомерТранзакции;
				КонецЕсли;
			Исключение
				ТекстСообщения = Нстр("ru = 'При получении данных в строке № %1, произошла ошибка: %2'");
				ОписаниеОшибки = ОписаниеОшибки();
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Сч, ОписаниеОшибки);		
				МассивОшибок.Добавить(ТекстСообщения);
			КонецПопытки;
		КонецЦикла;
		Если МассивОшибок.Количество() Тогда
			ТекстОшибки = СтрСоединить(МассивОшибок,Символы.ПС);			
			Отказ = (МассивОшибок.Количество() = ТаблицаОперацийПоКартам.Количество());
		КонецЕсли;
		Если НЕ ЭтоОперацииПоКарте Тогда
			ТекстОшибки = Нстр("ru = 'Выбранный табличный документ не содержит областей с операциями по карте.'");
			Отказ = Истина;
		КонецЕсли;
		
		
		Если Отказ = Ложь И ТаблицаОперацийПоКартам.Количество() Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "ВЫБРАТЬ * ПОМЕСТИТЬ ТЗ ИЗ &ТЗ КАК ТЗ";
			Запрос.УстановитьПараметр("ТЗ", ТаблицаОперацийПоКартам);
			Запрос.Выполнить();
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВЫРАЗИТЬ(вртДанных.Период КАК ДАТА) КАК Период,
			|	ВЫРАЗИТЬ(вртДанных.НомерКарты КАК СТРОКА(25)) КАК НомерКарты
			|ПОМЕСТИТЬ втДанные
			|ИЗ
			|	ТЗ КАК вртДанных
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	упТопливныеКарты.Ссылка КАК Ссылка,
			|	упТопливныеКарты.Код КАК Код
			|ПОМЕСТИТЬ вт_ТопличныеКарты
			|ИЗ
			|	Справочник.упТопливныеКарты КАК упТопливныеКарты
			|ГДЕ
			|	упТопливныеКарты.Код В
			|			(ВЫБРАТЬ
			|				втДанные.НомерКарты КАК НомерКарты
			|			ИЗ
			|				втДанные КАК втДанные)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втДанные.Период КАК Период,
			|	втДанные.НомерКарты КАК НомерКарты,
			|	ЕСТЬNULL(вт_ТопличныеКарты.Ссылка, ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка)) КАК ТопливнаяКарта
			|ПОМЕСТИТЬ вт_Данные
			|ИЗ
			|	втДанные КАК втДанные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ТопличныеКарты КАК вт_ТопличныеКарты
			|		ПО втДанные.НомерКарты = вт_ТопличныеКарты.Код
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	вт_Данные.Период КАК Период,
			|	вт_Данные.НомерКарты КАК НомерКарты,
			|	вт_Данные.ТопливнаяКарта КАК ТопливнаяКарта,
			|	МАКСИМУМ(упВыдачаТопливныхКарт.Период) КАК ПериодВыдачи
			|ПОМЕСТИТЬ вт_Выданные
			|ИЗ
			|	вт_Данные КАК вт_Данные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВыдачаТопливныхКарт КАК упВыдачаТопливныхКарт
			|		ПО вт_Данные.ТопливнаяКарта = упВыдачаТопливныхКарт.ТопливнаяКарта
			|			И вт_Данные.Период >= упВыдачаТопливныхКарт.Период
			|
			|СГРУППИРОВАТЬ ПО
			|	вт_Данные.Период,
			|	вт_Данные.ТопливнаяКарта,
			|	вт_Данные.НомерКарты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	вт_Выданные.Период КАК Период,
			|	вт_Выданные.НомерКарты КАК НомерКарты,
			|	вт_Выданные.ТопливнаяКарта КАК ТопливнаяКарта,
			|	вт_Выданные.ПериодВыдачи КАК ПериодВыдачи,
			|	ЕСТЬNULL(упВыдачаТопливныхКарт.Водитель, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель,
			|	ЕСТЬNULL(упВыдачаТопливныхКарт.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство
			|ИЗ
			|	вт_Выданные КАК вт_Выданные
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВыдачаТопливныхКарт КАК упВыдачаТопливныхКарт
			|		ПО вт_Выданные.ТопливнаяКарта = упВыдачаТопливныхКарт.ТопливнаяКарта
			|			И вт_Выданные.ПериодВыдачи = упВыдачаТопливныхКарт.Период
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ТЗ";
			
			ТаблицаВыданныхКарт = Запрос.Выполнить().Выгрузить();
			
			Запрос.МенеджерВременныхТаблиц.Закрыть();
			
			ТаблицаДанныхПоТопливу = Новый ТаблицаЗначений;
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)),"Период");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ТопливнаяКомпания",Новый ОписаниеТипов("ПеречислениеСсылка.упТопливныеКомпании"),"ТопливнаяКомпания");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("НомерТранзакции",Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(25)),"НомерТранзакции");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ВалютаЦифровойКод",Новый ОписаниеТипов("Строка"),"ВалютаЦифровойКод");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Количество");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("СкидкаСумма",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"СкидкаСумма");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("СуммаБезНДС",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"СуммаБезНДС");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("СуммаСНДС",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"СуммаСНДС");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ТопливнаяКартаНомер",Новый ОписаниеТипов("Строка"),"ТопливнаяКартаНомер");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ВладелецКарты",Новый ОписаниеТипов("Строка"),"ВладелецКарты");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ГСМНаименование",Новый ОписаниеТипов("Строка"),"ГСМНаименование");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ВидГСМНаименование",Новый ОписаниеТипов("Строка"),"ВидГСМНаименование");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("СтавкаНДС",Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"),"СтавкаНДС");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ТипТранзакции",Новый ОписаниеТипов("ПеречислениеСсылка.упТипыТранзакцийТопливныхКомпаний"),"ТипТранзакции");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ОрганизацияЗаправщик",Новый ОписаниеТипов("Строка"),"ОрганизацияЗаправщик");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("СкидкаПроцент",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"СкидкаПроцент");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ОперативныйРежим",Новый ОписаниеТипов("Булево"),"ОперативныйРежим");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Страна",Новый ОписаниеТипов("Строка"),"Страна");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Регион",Новый ОписаниеТипов("Строка"),"Регион");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Город",Новый ОписаниеТипов("Строка"),"Город");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Улица",Новый ОписаниеТипов("Строка"),"Улица");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("АЗС",Новый ОписаниеТипов("Строка"),"АЗС");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Валюта",Новый ОписаниеТипов("СправочникСсылка.Валюты"),"Валюта");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Курс",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,3)),"Курс");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Сотрудник",Новый ОписаниеТипов("СправочникСсылка.упСотрудники"),"Сотрудник");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ТранспортноеСредство",Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"),"ТранспортноеСредство");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ТопливнаяКарта",Новый ОписаниеТипов("СправочникСсылка.упТопливныеКарты"),"ТопливнаяКарта");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ГСМ",Новый ОписаниеТипов("СправочникСсылка.упГСМ"),"ГСМ");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("ВидГСМ",Новый ОписаниеТипов("СправочникСсылка.упВидыГСМ"),"ВидГСМ");
			ТаблицаДанныхПоТопливу.Колонки.Добавить("Флаг",Новый ОписаниеТипов("Булево"),"Флаг");
			
			ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
			ВалютаЦифровойКод = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ВалютаУпрУчета, "Код");
			СоответствиеВидГСМ = Новый Соответствие;
			Для каждого ТекущаяОперацияПоКарте Из ТаблицаОперацийПоКартам Цикл
				НоваяСтрока = ТаблицаДанныхПоТопливу.Добавить();
				НоваяСтрока.Период = ТекущаяОперацияПоКарте.Период;
				НоваяСтрока.ТопливнаяКомпания = ТопливнаяКомпания;
				НоваяСтрока.ТопливнаяКарта = ТекущаяОперацияПоКарте.ТопливнаяКарта;
				НоваяСтрока.НомерТранзакции = ТекущаяОперацияПоКарте.НомерТранзакции;
				НоваяСтрока.ВалютаЦифровойКод = ВалютаЦифровойКод;
				НоваяСтрока.Валюта = ВалютаУпрУчета;
				НоваяСтрока.Количество = ТекущаяОперацияПоКарте.Количество;
				НоваяСтрока.СкидкаСумма = ТекущаяОперацияПоКарте.Сумма - ТекущаяОперацияПоКарте.СуммаСоСкидкой;
				НоваяСтрока.СуммаБезНДС = ТекущаяОперацияПоКарте.Сумма;
				НоваяСтрока.СуммаСНДС = ТекущаяОперацияПоКарте.Сумма;
				НоваяСтрока.ТопливнаяКартаНомер = ТекущаяОперацияПоКарте.НомерКарты;
				НоваяСтрока.ВладелецКарты = ТекущаяОперацияПоКарте.ДержательКарты;
				
				ВидГСМНаименование = СоответствиеВидГСМ.Получить(ТекущаяОперацияПоКарте.ГСМ);
				Если ВидГСМНаименование = Неопределено Тогда
					ВидГСМНаименование = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ТекущаяОперацияПоКарте.ГСМ, "Владелец");
					СоответствиеВидГСМ.Вставить(ТекущаяОперацияПоКарте.ГСМ, ВидГСМНаименование);
				КонецЕсли;
				НоваяСтрока.ВидГСМНаименование = ВидГСМНаименование;
				НоваяСтрока.ВидГСМ = ВидГСМНаименование;
				НоваяСтрока.ГСМ = ТекущаяОперацияПоКарте.ГСМ;
				НоваяСтрока.ГСМНаименование = ТекущаяОперацияПоКарте.ГСМНаименование;
				НоваяСтрока.СтавкаНДС = Перечисления.упСтавкиНДС.ПустаяСсылка();
				НоваяСтрока.ТипТранзакции = Перечисления.упТипыТранзакцийТопливныхКомпаний.Покупка;
				Если ТекущаяОперацияПоКарте.Сумма >= 0 Тогда
					НоваяСтрока.ТипТранзакции = Перечисления.упТипыТранзакцийТопливныхКомпаний.Возврат;
				КонецЕсли;
				НоваяСтрока.ОрганизацияЗаправщик = "";
				НоваяСтрока.СкидкаПроцент = 0;
				Если ТекущаяОперацияПоКарте.Сумма > 0 И НоваяСтрока.СкидкаСумма > 0 Тогда
					НоваяСтрока.СкидкаПроцент = (НоваяСтрока.СкидкаСумма / ТекущаяОперацияПоКарте.Сумма) * 100;
				КонецЕсли;
				НоваяСтрока.АЗС = ТекущаяОперацияПоКарте.Операция;
				НоваяСтрока.Курс = 1;
				
				НоваяСтрока.Сотрудник = Справочники.упСотрудники.ПустаяСсылка();
				НоваяСтрока.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
				Отбор = Новый Структура();
				Отбор.Вставить("Период", ТекущаяОперацияПоКарте.Период);
				Отбор.Вставить("НомерКарты", ТекущаяОперацияПоКарте.НомерКарты);
				НайденныеСтроки = ТаблицаВыданныхКарт.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() Тогда
					ВыданнаяКарта = НайденныеСтроки[0];
					НоваяСтрока.Сотрудник = ВыданнаяКарта.Водитель;
					НоваяСтрока.ТранспортноеСредство = ВыданнаяКарта.ТранспортноеСредство;
				КонецЕсли;
				Если НоваяСтрока.ТипТранзакции = Перечисления.упТипыТранзакцийТопливныхКомпаний.Возврат Тогда
					Если НоваяСтрока.Количество > 0 Тогда
						НоваяСтрока.Количество = НоваяСтрока.Количество * -1;
					КонецЕсли;
					Если НоваяСтрока.СуммаБезНДС > 0 Тогда
						НоваяСтрока.СуммаБезНДС = НоваяСтрока.СуммаБезНДС * -1;
						НоваяСтрока.СуммаСНДС = НоваяСтрока.СуммаБезНДС;
					КонецЕсли;
					Если НоваяСтрока.СкидкаСумма > 0 Тогда
						НоваяСтрока.СкидкаСумма = НоваяСтрока.СкидкаСумма * -1;
					КонецЕсли;
				ИначеЕсли НоваяСтрока.ТипТранзакции = Перечисления.упТипыТранзакцийТопливныхКомпаний.Покупка Тогда
					Если НоваяСтрока.Количество < 0 Тогда
						НоваяСтрока.Количество = НоваяСтрока.Количество * -1;
					КонецЕсли;
					Если НоваяСтрока.СуммаБезНДС < 0 Тогда
						НоваяСтрока.СуммаБезНДС = НоваяСтрока.СуммаБезНДС * -1;
						НоваяСтрока.СуммаСНДС = НоваяСтрока.СуммаБезНДС;
					КонецЕсли;
					Если НоваяСтрока.СкидкаСумма < 0 Тогда
						НоваяСтрока.СкидкаСумма = НоваяСтрока.СкидкаСумма * -1;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	ТабличныйДокумент = Неопределено;
	
	Возврат ТаблицаДанныхПоТопливу;
	
КонецФункции // ПрочитатьДанныеФайлаРоснефть()

// Процедура - Сформировать топливные карты
//
// Параметры:
//  тбСопоставленийТопливныхКарт - ТаблицаЗначение	 - таблица данных по топливным картам.
//  Отказ						 - Булево	 - Истина, если произошла ошибка
//
Процедура СформироватьТопливныеКарты(тбСопоставленийТопливныхКарт, Отказ) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбСопоставленийТопливныхКарт.ИндексСтроки,
	               |	тбСопоставленийТопливныхКарт.ТопливнаяКартаНомер,
	               |	тбСопоставленийТопливныхКарт.ТопливнаяКомпания,
	               |	тбСопоставленийТопливныхКарт.ТопливнаяКарта,
	               |	тбСопоставленийТопливныхКарт.Сотрудник,
	               |	тбСопоставленийТопливныхКарт.ТранспортноеСредство
	               |ПОМЕСТИТЬ втСопоставленийТопливныхКарт
	               |ИЗ
	               |	&тбСопоставленийТопливныхКарт КАК тбСопоставленийТопливныхКарт
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСопоставленийТопливныхКарт.ИндексСтроки,
	               |	втСопоставленийТопливныхКарт.ТопливнаяКартаНомер,
	               |	втСопоставленийТопливныхКарт.ТопливнаяКомпания,
	               |	втСопоставленийТопливныхКарт.ТопливнаяКарта,
	               |	втСопоставленийТопливныхКарт.Сотрудник,
	               |	втСопоставленийТопливныхКарт.ТранспортноеСредство,
	               |	ЕСТЬNULL(упТопливныеКарты.Ссылка, ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка)) КАК ТопливнаяКарта,
	               |	ЕСТЬNULL(упВыдачаТопливныхКартСрезПоследних.Водитель, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК СотрудникПоКарте,
	               |	ЕСТЬNULL(упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредствоПоКарте,
	               |	упТопливныеКарты.Код
	               |ИЗ
	               |	втСопоставленийТопливныхКарт КАК втСопоставленийТопливныхКарт
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты КАК упТопливныеКарты
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних КАК упВыдачаТопливныхКартСрезПоследних
	               |			ПО упТопливныеКарты.Ссылка = упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта
	               |		ПО втСопоставленийТопливныхКарт.ТопливнаяКарта = упТопливныеКарты.Ссылка
	               |			И втСопоставленийТопливныхКарт.ТопливнаяКомпания = упТопливныеКарты.ТопливнаяКомпания";
	Запрос.УстановитьПараметр("тбСопоставленийТопливныхКарт", тбСопоставленийТопливныхКарт);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Если транспортная карта не задана
		Если НЕ ЗначениеЗаполнено(Выборка.ТопливнаяКарта) Тогда
			
			
			НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
			
			// Сформировать новую топливную карту с номером из файла
			ТопливнаяКартаОбъект = Справочники.упТопливныеКарты.СоздатьЭлемент();
			ТопливнаяКартаОбъект.Код 				= СокрЛП(Выборка.ТопливнаяКартаНомер);
			ТопливнаяКартаОбъект.ТопливнаяКомпания 	= Выборка.ТопливнаяКомпания;
					
			Попытка
				ТопливнаяКартаОбъект.Записать();
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ,,,Отказ);
				ЗаписьЖурналаРегистрации("Ошибка при создании топливной карты", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецПопытки;
			
			// Выдать ее указанному сотруднику и транспортному средству
			Если Не Отказ 
					И (ЗначениеЗаполнено(Выборка.Сотрудник)
						ИЛИ ЗначениеЗаполнено(Выборка.ТранспортноеСредство)) Тогда
				Запись	= РегистрыСведений.упВыдачаТопливныхКарт.СоздатьМенеджерЗаписи();
				Запись.Период				= ТекущаяДатаСеанса();
				Запись.ТопливнаяКарта		= ТопливнаяКартаОбъект.Ссылка;
				Запись.Водитель				= Выборка.Сотрудник;
				Запись.ТранспортноеСредство	= Выборка.ТранспортноеСредство;
		
				Попытка
					Запись.Записать(Истина);
				Исключение
					Прервать;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					
					ТекстСообщения = Нстр("ru = 'Ошибка при выдаче топливной карты:%1; Номер топливной карты:%2'");	
					ТекстСообщения = СтрШаблон(ТекстСообщения, ТекстОшибки, Выборка.ТопливнаяКартаНомер);
					
					ЗаписьЖурналаРегистрации("Ошибка при выдаче топливной карты", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					
				КонецПопытки;
				
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				ЗафиксироватьТранзакцию();
				
				Строка = тбСопоставленийТопливныхКарт.Получить(Выборка.ИндексСтроки);
				Строка.ТопливнаяКарта = ТопливнаяКартаОбъект.Ссылка;
				
				ТекстСообщения = Нстр("ru = 'Сформирована топливная карта %1.'");
				мсвСообщений = Новый Массив;
				Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
					Сообщение = Нстр("ru = 'сотруднику:%1'");	
					Сообщение = СтрШаблон(Сообщение, Выборка.Сотрудник);
					мсвСообщений.Добавить(Сообщение);
				КонецЕсли;
				Если ЗначениеЗаполнено(Выборка.ТранспортноеСредство) Тогда
					Сообщение = Нстр("ru = 'на транспортное средство:%1'");	
					Сообщение = СтрШаблон(Сообщение, Выборка.ТранспортноеСредство);
					мсвСообщений.Добавить(Сообщение);
				КонецЕсли;
				
				Если мсвСообщений.Количество() > 0 Тогда
					ТекстСообщения =  ТекстСообщения + Нстр("ru = ' Выдана %2'");
					ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ТопливнаяКартаНомер, ?(мсвСообщений.Количество()>0,"("+СтрСоединить(мсвСообщений, ", ") + ")",""));
				Иначе
					ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ТопливнаяКартаНомер);
				КонецЕсли;
				
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ТопливнаяКартаОбъект.Ссылка);

			Иначе	
				ОтменитьТранзакцию();
				Прервать;
			КонецЕсли;
				
		Иначе
			// Если номер карты из файла и номер указанной карты не совпдают
			Если НЕ Выборка.ТопливнаяКартаНомер = Выборка.Код Тогда
				
				// Заменить номер карты
				ТопливнаяКартаОбъект = Выборка.ТопливнаяКарта.ПолучитьОбъект();
				ТопливнаяКартаОбъект.Код = СокрЛП(Выборка.ТопливнаяКартаНомер);
				
				Попытка
					ТопливнаяКартаОбъект.Записать();
				Исключение
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ,,,Отказ);
					ЗаписьЖурналаРегистрации("Ошибка при обновлениии данных топливной карты", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				КонецПопытки;
			КонецЕсли;
			
			// Если не совпадают сотрудник или транспортное средство,
			// предполагаем что карта с уникальным номером выдается только на одного сотрудника/транспортное средство
			// и эта ситуация исключение связанное с неверно занесенными данными
					
			Если НЕ Отказ
				И (НЕ Выборка.Сотрудник = Выборка.СотрудникПоКарте 
				ИЛИ НЕ Выборка.ТранспортноеСредство = Выборка.ТранспортноеСредствоПоКарте) Тогда
				// Выдать карту текущей датой на сотрудника и транспортное средство
				
				Запись	= РегистрыСведений.упВыдачаТопливныхКарт.СоздатьМенеджерЗаписи();
				Запись.Период				= ТекущаяДатаСеанса();
				Запись.ТопливнаяКарта		= Выборка.ТопливнаяКарта;
				Запись.Водитель				= Выборка.Сотрудник;
				Запись.ТранспортноеСредство	= Выборка.ТранспортноеСредство;
				
				Попытка
					Запись.Записать(Истина);
					ТекстСообщения = Нстр("ru = 'Обновлены данные по топливной карте %1.'");
					
					мсвСообщений = Новый Массив;
					Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
						Сообщение = Нстр("ru = 'сотруднику:%1'");	
						Сообщение = СтрШаблон(Сообщение, Выборка.Сотрудник);
						мсвСообщений.Добавить(Сообщение);
					КонецЕсли;
					Если ЗначениеЗаполнено(Выборка.ТранспортноеСредство) Тогда
						Сообщение = Нстр("ru = 'на транспортное средство:%1'");	
						Сообщение = СтрШаблон(Сообщение, Выборка.ТранспортноеСредство);
						мсвСообщений.Добавить(Сообщение);
					КонецЕсли;
					
					Если мсвСообщений.Количество() > 0 Тогда
						ТекстСообщения =  ТекстСообщения + Нстр("ru = ' Выдана %2'");
						ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ТопливнаяКартаНомер, ?(мсвСообщений.Количество()>0,"("+СтрСоединить(мсвСообщений, ", ") + ")",""));
					Иначе
						ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ТопливнаяКартаНомер);
					КонецЕсли;

					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					
				Исключение
					Прервать;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					
					ТекстСообщения = Нстр("ru = 'Ошибка при выдаче топливной карты:%1; Номер топливной карты:%2'");	
					ТекстСообщения = СтрШаблон(ТекстСообщения, ТекстОшибки, Выборка.ТопливнаяКартаНомер);
					
					ЗаписьЖурналаРегистрации("Ошибка при выдаче топливной карты", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					
				КонецПопытки;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры

// Процедура - Записать данные в регистр
//
// Параметры:
//  ТаблицаДанныхПоТопливу	 - ТаблицаЗначений - заполненная таблица данных по топливу.
//  ТекстСообщения		 - Строка - текст ошибки в случае неудачи.
//  Отказ				 - Булево - Истина в случае ошибки.
//
Процедура ЗаписатьДанныеВРегистр(ТаблицаДанныхПоТопливу, ТекстСообщения, Отказ) Экспорт
	
	СобытиеНачало	= Нстр("ru = 'ЗагрузкаДанныхОтТопливныхКомпаний.Начало'");
	СобытиеКонец 	= Нстр("ru = 'ЗагрузкаДанныхОтТопливныхКомпаний.Конец'");
		
	Этап	= Нстр("ru = 'Загружаются/изменяются записи данных от топливных компаний'");
	ЗаписьЖурналаРегистрации(СобытиеНачало, УровеньЖурналаРегистрации.Информация,,, Этап);	
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	КоличествоЗаписей = 0;					
	
	мсвСтрокиТаблицы = ТаблицаДанныхПоТопливу.НайтиСтроки(Новый Структура("Флаг",Истина));
	
	Для каждого Строка Из мсвСтрокиТаблицы Цикл
		
		Запись	= РегистрыСведений.упДанныеОтТопливныхКомпаний.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		Если Строка.ТопливнаяКомпания = Перечисления.упТопливныеКомпании.Роснефть Тогда
			Запись.Операция = Строка.АЗС;
			Запись.АЗС = "";
		КонецЕсли;
		Попытка
			Запись.Записать(Истина);
			КоличествоЗаписей = КоличествоЗаписей + 1;
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ТекстСообщения = Нстр("ru = 'Ошибка:%1; Запись по топливной компании:%2; номер транзакции:%3; валюта:%4'");	
			ТекстСообщения = СтрШаблон(ТекстСообщения, ТекстОшибки, Строка.ТопливнаяКомпания, Строка.НомерТранзакции, Строка.Валюта);
			
			ЗаписьЖурналаРегистрации("Загрузка данных от топливных компаний", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Прервать;
		КонецПопытки;
	КонецЦикла;
		
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
	
	Комментарий = Нстр("ru = 'Загружено %1 записей'");	
	Комментарий = СтрШаблон(Комментарий, КоличествоЗаписей);
	ЗаписьЖурналаРегистрации(СобытиеКонец, УровеньЖурналаРегистрации.Информация,,, Комментарий);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункции

Процедура ЗаполнитьТаблицуДанныхПоТопливу(ЧтениеXML, тбзДанныеПоТопливу, ТопливнаяКомпания, сткСоответствиеПолей)
	
	Если ТопливнаяКомпания = Перечисления.упТопливныеКомпании.Лукойл Тогда
		НоваяСтрока = Неопределено;
		ИмяЭлемента	= "";
		
		Пока ЧтениеXML.Прочитать() Цикл
			
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				
				ИмяЭлемента = ЧтениеXML.Имя;
				Если ЧтениеXML.Имя = "node" Тогда
				    НоваяСтрока = тбзДанныеПоТопливу.Добавить();
					НоваяСтрока.ТопливнаяКомпания = ТопливнаяКомпания;
				КонецЕсли;

			ИначеЕсли ЧтениеXML.ИмеетЗначение Тогда	
				
				ИмяПоля = "";
				Если сткСоответствиеПолей.Свойство(ИмяЭлемента, ИмяПоля) Тогда
					НоваяСтрока[ИмяПоля] = ЧтениеXML.Значение;
				КонецЕсли;
				
			КонецЕсли;	
		КонецЦикла;
		
		ЗаполнитьПоляТаблицыДанныхПоТопливуЛукойл(тбзДанныеПоТопливу);
		
	КонецЕсли;
			
КонецПроцедуры

Функция СформироватьТаблицуДанныхПоТопливу(СхемаКомпоновкиДанных)
	
	сткРезультат = Новый Структура();
	
	сткСоответствиеПолей 	= Новый Структура();
	тбзДанныепоТопливу 		= Новый ТаблицаЗначений;
	
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных["ДанныеПоТопливу"];
	ПоляНабораДанных = НаборДанных.Поля;
	Для каждого ПолеНабораДанных Из ПоляНабораДанных Цикл
		сткСоответствиеПолей.Вставить(ПолеНабораДанных.Поле, ПолеНабораДанных.ПутьКДанным);
		тбзДанныепоТопливу.Колонки.Добавить(ПолеНабораДанных.ПутьКДанным, ПолеНабораДанных.ТипЗначения);
	КонецЦикла;
	
	сткРезультат.Вставить("СоответствиеПолей", 			сткСоответствиеПолей);
	сткРезультат.Вставить("ТаблицаДанныхПоТопливу", 	тбзДанныепоТопливу);
	
	Возврат сткРезультат;
		
КонецФункции

Функция СоответствиеТипыТранзакций()
	Результат = Новый Соответствие;
	Результат.Вставить("Покупка.", Перечисления.упТипыТранзакцийТопливныхКомпаний.Покупка);
	Результат.Вставить("Покупка. Возврат.", Перечисления.упТипыТранзакцийТопливныхКомпаний.Возврат);
	
	Возврат  Результат;
КонецФункции

Функция СоответствиеСтавокНДС()
	Результат = Новый Соответствие;
	Результат.Вставить("0", Перечисления.упСтавкиНДС.НДС0);
	Результат.Вставить("10", Перечисления.упСтавкиНДС.НДС10);
	Результат.Вставить("18", Перечисления.упСтавкиНДС.НДС18);
		
	Возврат  Результат;
КонецФункции

Процедура ЗаполнитьПоляТаблицыДанныхПоТопливуЛукойл(тбзДанныеПоТопливу) 
	
	соотТипыТранзакций 	= СоответствиеТипыТранзакций();
	соотСтавкиНДС		= СоответствиеСтавокНДС();
		
	Для каждого Строка Из тбзДанныеПоТопливу Цикл
		Строка.ТипТранзакции 		= соотТипыТранзакций.Получить(Строка.ТипТранзакцииНаименование);
		Строка.СтавкаНДС	 		= соотСтавкиНДС.Получить(Строка.СтавкаНДСНаименование);
		Строка.ОперативныйРежим 	= ?(Строка.ОперативныйРежимНаименование = "online", Истина, Ложь);
		упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуБезНДС(Строка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	втДанныеПоТопливу.ТопливнаяКомпания,
	               |	втДанныеПоТопливу.НомерТранзакции,
	               |	втДанныеПоТопливу.ВалютаЦифровойКод,
	               |	втДанныеПоТопливу.Количество,
	               |	втДанныеПоТопливу.СкидкаСумма,
	               |	втДанныеПоТопливу.СуммаБезНДС,
	               |	втДанныеПоТопливу.СуммаСНДС,
	               |	втДанныеПоТопливу.ТопливнаяКартаНомер,
	               |	втДанныеПоТопливу.ВладелецКарты,
	               |	втДанныеПоТопливу.ГСМНаименование,
	               |	втДанныеПоТопливу.ВидГСМНаименование,
	               |	втДанныеПоТопливу.СтавкаНДСНаименование,
	               |	втДанныеПоТопливу.ТипТранзакцииНаименование,
	               |	втДанныеПоТопливу.ОперативныйРежимНаименование,
	               |	втДанныеПоТопливу.СтавкаНДС,
	               |	втДанныеПоТопливу.ТипТранзакции,
	               |	втДанныеПоТопливу.ОрганизацияЗаправщик,
	               |	втДанныеПоТопливу.СкидкаПроцент,
	               |	втДанныеПоТопливу.ОперативныйРежим,
	               |	втДанныеПоТопливу.Курс,
	               |	втДанныеПоТопливу.Страна,
	               |	втДанныеПоТопливу.Регион,
	               |	втДанныеПоТопливу.Город,
	               |	втДанныеПоТопливу.Улица,
	               |	втДанныеПоТопливу.АЗС,
	               |	втДанныеПоТопливу.Период
	               |ПОМЕСТИТЬ втДанныеПоТопливу
	               |ИЗ
	               |	&тбДанныеПоТопливу КАК втДанныеПоТопливу
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДанныеПоТопливу.НомерТранзакции,
	               |	втДанныеПоТопливу.ВладелецКарты,
	               |	МАКСИМУМ(ЕСТЬNULL(Валюты.Ссылка, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))) КАК Валюта,
	               |	МАКСИМУМ(ЕСТЬNULL(упТопливныеКарты.Ссылка, ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка))) КАК ТопливнаяКарта,
	               |	МАКСИМУМ(ЕСТЬNULL(упВидыГСМ.Ссылка, ЗНАЧЕНИЕ(Справочник.упВидыГСМ.ПустаяСсылка))) КАК ВидГСМ,
	               |	МАКСИМУМ(ЕСТЬNULL(упГСМ.Ссылка, ЗНАЧЕНИЕ(Справочник.упГСМ.ПустаяСсылка))) КАК ГСМ,
	               |	МАКСИМУМ(ЕСТЬNULL(упВыдачаТопливныхКартСрезПоследних.Водитель, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))) КАК Сотрудник,
	               |	МАКСИМУМ(ЕСТЬNULL(упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))) КАК ТранспортноеСредство
	               |ПОМЕСТИТЬ втТопливныеКарты
	               |ИЗ
	               |	втДанныеПоТопливу КАК втДанныеПоТопливу
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	               |		ПО втДанныеПоТопливу.ВалютаЦифровойКод = Валюты.Код
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты КАК упТопливныеКарты
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних КАК упВыдачаТопливныхКартСрезПоследних
	               |			ПО упТопливныеКарты.Ссылка = упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта
	               |		ПО втДанныеПоТопливу.ТопливнаяКартаНомер = упТопливныеКарты.Код
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыГСМ КАК упВидыГСМ
	               |		ПО втДанныеПоТопливу.ВидГСМНаименование = упВидыГСМ.Наименование
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГСМ КАК упГСМ
	               |		ПО втДанныеПоТопливу.ГСМНаименование = упГСМ.Наименование
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втДанныеПоТопливу.НомерТранзакции,
	               |	втДанныеПоТопливу.ВладелецКарты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТопливныеКарты.НомерТранзакции,
	               |	МАКСИМУМ(ЕСТЬNULL(упСотрудники.Ссылка, втТопливныеКарты.Сотрудник)) КАК Сотрудник,
	               |	МАКСИМУМ(ЕСТЬNULL(упТранспортныеСредства.Ссылка, втТопливныеКарты.ТранспортноеСредство)) КАК ТранспортноеСредство
	               |ПОМЕСТИТЬ втСотрудникиТранспортныеСредства
	               |ИЗ
	               |	втТопливныеКарты КАК втТопливныеКарты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСотрудники КАК упСотрудники
	               |		ПО (втТопливныеКарты.Сотрудник = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	               |			И втТопливныеКарты.ВладелецКарты = упСотрудники.Наименование
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ПО (втТопливныеКарты.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	               |			И втТопливныеКарты.ВладелецКарты = упТранспортныеСредства.РегистрационныйНомер
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТопливныеКарты.НомерТранзакции
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДанныеПоТопливу.ТопливнаяКомпания,
	               |	втДанныеПоТопливу.НомерТранзакции,
	               |	втДанныеПоТопливу.ВалютаЦифровойКод,
	               |	втДанныеПоТопливу.Количество,
	               |	втДанныеПоТопливу.СкидкаСумма,
	               |	втДанныеПоТопливу.СуммаБезНДС,
	               |	втДанныеПоТопливу.СуммаСНДС,
	               |	втДанныеПоТопливу.ТопливнаяКартаНомер,
	               |	втДанныеПоТопливу.ВладелецКарты,
	               |	втДанныеПоТопливу.ГСМНаименование,
	               |	втДанныеПоТопливу.ВидГСМНаименование,
	               |	втДанныеПоТопливу.СтавкаНДСНаименование,
	               |	втДанныеПоТопливу.ТипТранзакцииНаименование,
	               |	втДанныеПоТопливу.ОперативныйРежимНаименование,
	               |	втДанныеПоТопливу.СтавкаНДС,
	               |	втДанныеПоТопливу.ТипТранзакции,
	               |	втДанныеПоТопливу.ОрганизацияЗаправщик,
	               |	втДанныеПоТопливу.СкидкаПроцент,
	               |	втДанныеПоТопливу.ОперативныйРежим,
	               |	втДанныеПоТопливу.Курс,
	               |	втДанныеПоТопливу.Страна,
	               |	втДанныеПоТопливу.Регион,
	               |	втДанныеПоТопливу.Город,
	               |	втДанныеПоТопливу.Улица,
	               |	втДанныеПоТопливу.АЗС,
	               |	ЕСТЬNULL(втТопливныеКарты.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
	               |	втТопливныеКарты.ТопливнаяКарта,
	               |	втТопливныеКарты.ВидГСМ,
	               |	втТопливныеКарты.ГСМ,
	               |	втСотрудникиТранспортныеСредства.Сотрудник,
	               |	втСотрудникиТранспортныеСредства.ТранспортноеСредство,
	               |	втДанныеПоТопливу.Период
	               |ПОМЕСТИТЬ втДанныеПоТопливуСДопПолями
	               |ИЗ
	               |	втДанныеПоТопливу КАК втДанныеПоТопливу
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втТопливныеКарты КАК втТопливныеКарты
	               |		ПО втДанныеПоТопливу.НомерТранзакции = втТопливныеКарты.НомерТранзакции
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втСотрудникиТранспортныеСредства КАК втСотрудникиТранспортныеСредства
	               |		ПО втДанныеПоТопливу.НомерТранзакции = втСотрудникиТранспортныеСредства.НомерТранзакции
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДанныеПоТопливуСДопПолями.ТопливнаяКомпания,
	               |	втДанныеПоТопливуСДопПолями.НомерТранзакции,
	               |	втДанныеПоТопливуСДопПолями.ВалютаЦифровойКод,
	               |	втДанныеПоТопливуСДопПолями.Количество,
	               |	втДанныеПоТопливуСДопПолями.СкидкаСумма,
	               |	ВЫБОР
	               |		КОГДА втДанныеПоТопливуСДопПолями.ТипТранзакции = ЗНАЧЕНИЕ(Перечисление.упТипыТранзакцийТопливныхКомпаний.Возврат)
	               |			ТОГДА -1
	               |		ИНАЧЕ 1
	               |	КОНЕЦ * втДанныеПоТопливуСДопПолями.СуммаБезНДС КАК СуммаБезНДС,
	               |	ВЫБОР
	               |		КОГДА втДанныеПоТопливуСДопПолями.ТипТранзакции = ЗНАЧЕНИЕ(Перечисление.упТипыТранзакцийТопливныхКомпаний.Возврат)
	               |			ТОГДА -1
	               |		ИНАЧЕ 1
	               |	КОНЕЦ * втДанныеПоТопливуСДопПолями.СуммаСНДС КАК СуммаСНДС,
	               |	втДанныеПоТопливуСДопПолями.СуммаСНДС КАК СуммаСНДС1,
	               |	втДанныеПоТопливуСДопПолями.ТопливнаяКартаНомер,
	               |	втДанныеПоТопливуСДопПолями.ВладелецКарты,
	               |	втДанныеПоТопливуСДопПолями.ГСМНаименование,
	               |	втДанныеПоТопливуСДопПолями.ВидГСМНаименование,
	               |	втДанныеПоТопливуСДопПолями.СтавкаНДСНаименование,
	               |	втДанныеПоТопливуСДопПолями.ТипТранзакцииНаименование,
	               |	втДанныеПоТопливуСДопПолями.ОперативныйРежимНаименование,
	               |	втДанныеПоТопливуСДопПолями.СтавкаНДС,
	               |	втДанныеПоТопливуСДопПолями.ТипТранзакции,
	               |	втДанныеПоТопливуСДопПолями.ОрганизацияЗаправщик,
	               |	втДанныеПоТопливуСДопПолями.СкидкаПроцент,
	               |	втДанныеПоТопливуСДопПолями.ОперативныйРежим,
	               |	втДанныеПоТопливуСДопПолями.Курс,
	               |	втДанныеПоТопливуСДопПолями.Страна,
	               |	втДанныеПоТопливуСДопПолями.Регион,
	               |	втДанныеПоТопливуСДопПолями.Город,
	               |	втДанныеПоТопливуСДопПолями.Улица,
	               |	втДанныеПоТопливуСДопПолями.АЗС,
	               |	втДанныеПоТопливуСДопПолями.Валюта,
	               |	втДанныеПоТопливуСДопПолями.ТопливнаяКарта,
	               |	втДанныеПоТопливуСДопПолями.ВидГСМ,
	               |	втДанныеПоТопливуСДопПолями.ГСМ,
	               |	втДанныеПоТопливуСДопПолями.Сотрудник,
	               |	втДанныеПоТопливуСДопПолями.ТранспортноеСредство,
	               |	втДанныеПоТопливуСДопПолями.Период,
	               |	ВЫБОР
	               |		КОГДА НЕ упДанныеОтТопливныхКомпанийСрезПоследних.ТопливнаяКомпания ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		КОГДА втДанныеПоТопливуСДопПолями.ТопливнаяКарта = ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка)
	               |				ИЛИ втДанныеПоТопливуСДопПолями.ВидГСМ = ЗНАЧЕНИЕ(Справочник.упВидыГСМ.ПустаяСсылка)
	               |				ИЛИ втДанныеПоТопливуСДопПолями.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Флаг
	               |ИЗ
	               |	втДанныеПоТопливуСДопПолями КАК втДанныеПоТопливуСДопПолями
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеОтТопливныхКомпаний.СрезПоследних КАК упДанныеОтТопливныхКомпанийСрезПоследних
	               |		ПО втДанныеПоТопливуСДопПолями.ТопливнаяКомпания = упДанныеОтТопливныхКомпанийСрезПоследних.ТопливнаяКомпания
	               |			И втДанныеПоТопливуСДопПолями.НомерТранзакции = упДанныеОтТопливныхКомпанийСрезПоследних.НомерТранзакции
	               |			И втДанныеПоТопливуСДопПолями.Валюта = упДанныеОтТопливныхКомпанийСрезПоследних.Валюта";
	Запрос.УстановитьПараметр("тбДанныеПоТопливу", тбзДанныеПоТопливу);
	тбзДанныеПоТопливу = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли