
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Подтвержден") Тогда
		ЭтаФорма.Заголовок = "Подтвержденные географические зоны";
		СписокГеозон.ТекстЗапроса = ПолучитьТекстДляПодтвержденных();
	ИначеЕсли Параметры.Свойство("ПометкаУдаления") Тогда
		Если Параметры.ПометкаУдаления Тогда
			ЭтаФорма.Заголовок = "Недействующие географические зоны";
		Иначе
			ЭтаФорма.Заголовок = "Действующие географические зоны";
		КонецЕсли;	
		СписокГеозон.ТекстЗапроса = ПолучитьТекстДляПометкиУдаления(Параметры.ПометкаУдаления);
	ИначеЕсли Параметры.Свойство("СписокНегеокодированных") Тогда
		ЭтаФорма.Заголовок = "Негеокодированные географические зоны";
		СписокГеозон.ТекстЗапроса = ПолучитьТекстДляНегеокодированных();
	ИначеЕсли Параметры.Свойство("СПобочнымиКоэффициентами") Тогда
		ЭтаФорма.Заголовок = "Географические зоны с побочными коэффициентами";
		СписокГеозон.ТекстЗапроса = ПолучитьТекстДляЗонСПобочнымиКоэффициентами();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстДляПодтвержденных()
	
	Текст = "ВЫБРАТЬ
	        |	упГеографическиеЗоны.Ссылка,
	        |	упГеографическиеЗоны.Наименование,
	        |	упГеографическиеЗоны.Подтверждена
	        |ИЗ
	        |	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	        |ГДЕ
	        |	упГеографическиеЗоны.Подтверждена = ИСТИНА"	 ;
	
	Возврат Текст;
	
КонецФункции	

&НаСервере
Функция ПолучитьТекстДляНегеокодированных()
	
	Текст = "ВЫБРАТЬ
	        |	упГеографическиеЗоны.Наименование,
	        |	упГеографическиеЗоны.Подтверждена,
	        |	упГеографическиеЗоны.Ссылка
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		упГеографическиеЗоны.Ссылка КАК Ссылка,
	        |		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(упГеографическиеЗоныПолигон.НомерСтроки, 0)) КАК КоличествоСтрок
	        |	ИЗ
	        |		Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	        |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны.Полигон КАК упГеографическиеЗоныПолигон
	        |			ПО (упГеографическиеЗоныПолигон.Ссылка = упГеографическиеЗоны.Ссылка)
	        |	ГДЕ
	        |		упГеографическиеЗоны.ПометкаУдаления = ЛОЖЬ
	        |	
	        |	СГРУППИРОВАТЬ ПО
	        |		упГеографическиеЗоны.Ссылка) КАК ВложенныйЗапрос
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	        |		ПО ВложенныйЗапрос.Ссылка = упГеографическиеЗоны.Ссылка
	        |ГДЕ
	        |	ВложенныйЗапрос.КоличествоСтрок < 3"	 ;
	
	Возврат Текст;
	
КонецФункции	

&НаСервере
Функция ПолучитьТекстДляЗонСПобочнымиКоэффициентами()
	
	Текст = "ВЫБРАТЬ
	        |	упГеографическиеЗоны.Наименование,
	        |	упГеографическиеЗоны.Подтверждена,
	        |	упГеографическиеЗоны.Ссылка
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		упГеографическиеЗоны.Ссылка КАК Ссылка,
	        |		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ упГеографическиеЗоныКоэффициентыВремени.НомерСтроки) КАК КоличествоСтрок
	        |	ИЗ
	        |		Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	        |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны.КоэффициентыВремени КАК упГеографическиеЗоныКоэффициентыВремени
	        |			ПО (упГеографическиеЗоныКоэффициентыВремени.Ссылка = упГеографическиеЗоны.Ссылка)
	        |	ГДЕ
	        |		упГеографическиеЗоны.ПометкаУдаления = ЛОЖЬ
	        |	
	        |	СГРУППИРОВАТЬ ПО
	        |		упГеографическиеЗоны.Ссылка) КАК ВложенныйЗапрос
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	        |		ПО ВложенныйЗапрос.Ссылка = упГеографическиеЗоны.Ссылка
	        |ГДЕ
	        |	ВложенныйЗапрос.КоличествоСтрок > 0"	 ;
	
	Возврат Текст;
	
КонецФункции	

&НаСервере
Функция ПолучитьТекстДляПометкиУдаления(ПометкаУдаления)
	
	Текст = "ВЫБРАТЬ
	        |	упГеографическиеЗоны.Ссылка,
	        |	упГеографическиеЗоны.Наименование,
	        |	упГеографическиеЗоны.Подтверждена
	        |ИЗ
	        |	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	        |ГДЕ
	        |	упГеографическиеЗоны.ПометкаУдаления = ИСТИНА"	 ;
			
			Если Не ПометкаУдаления Тогда
				Текст = СтрЗаменить(Текст, "ИСТИНА", "ЛОЖЬ");
			КонецЕсли;	
	
	Возврат Текст;
КонецФункции	

&НаКлиенте
Процедура ОбъектВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СсылкаНаЭлемент = Элементы.СписокГеозон.ТекущиеДанные.Ссылка;
	ПараметрыФормы = Новый Структура("Ключ", СсылкаНаЭлемент);
	ОткрытьФорму("Справочник.упГеографическиеЗоны.Форма.ФормаЭлемента", ПараметрыФормы); 

КонецПроцедуры
