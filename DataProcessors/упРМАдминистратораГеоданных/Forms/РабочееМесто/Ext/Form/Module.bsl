
#Если Не ВебКлиент Тогда
	
#Область ПеременныеМодуляФормы

// Нет.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы
// Нет.
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьВсе(Команда)
	
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры // ОбновитьВсе()

&НаКлиенте
Процедура ОбщееДействие_ОчиститьИсториюСервисаМаршрутизации(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	ТекущиеПараметры = Новый Структура("Команда",Команда.Имя);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчиститьИсториюСервисаМаршрутизации", ЭтаФорма, ТекущиеПараметры);
	ТекстСообщения = СтрШаблон(НСтр("ru = 'После выполнения команды данные о использовании маршрутизатора %1 будут безвозвратно удалены. Продолжить выполнение?';"
	+ " en = 'Do you want to continue?'"),"");
	ПоказатьВопрос(Оповещение, ТекстСообщения, Режим, 0);	
	
КонецПроцедуры // ОбщееДействие_ОчиститьИсториюСервисаМаршрутизации()

&НаКлиенте
Процедура ОбщееДействие_ОчиститьИсториюСервисаГеокодирования(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	ТекущиеПараметры = Новый Структура("Команда",Команда.Имя);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчиститьИсториюСервисаГеокодирования", ЭтаФорма, ТекущиеПараметры);
	ТекстСообщения = СтрШаблон(НСтр("ru = 'После выполнения команды данные о использовании Сервиса Геокодирования %1 будут безвозвратно удалены. Продолжить выполнение?';"
	+ " en = 'Do you want to continue?'"),"");
	ПоказатьВопрос(Оповещение, ТекстСообщения, Режим, 0);	
	
КонецПроцедуры // ОбщееДействие_ОчиститьИсториюСервисаГеокодирования()

&НаКлиенте
Процедура СМ_ОчиститьВсюИсторию(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	ТекущиеПараметры = Новый Структура();
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчиститьВсюИсториюСервисаМаршрутизации", ЭтаФорма, ТекущиеПараметры);
	ТекстСообщения = НСтр("ru = 'После выполнения команды все данные статистики о использовании сервисов маршрутизации будут безвозвратно удалены. Продолжить выполнение?';"
	+ " en = 'Do you want to continue?'");
	ПоказатьВопрос(Оповещение, ТекстСообщения, Режим, 0);	
	
КонецПроцедуры

&НаКлиенте
Процедура СГ_ОчиститьВсюИсторию(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	ТекущиеПараметры = Новый Структура();
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчиститьВсюИсториюСервисаГеокодирования", ЭтаФорма, ТекущиеПараметры);
	ТекстСообщения = НСтр("ru = 'После выполнения команды все данные статистики о использовании Сервиса Геокодирования будут безвозвратно удалены. Продолжить выполнение?';"
	+ " en = 'Do you want to continue?'");
	ПоказатьВопрос(Оповещение, ТекстСообщения, Режим, 0);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДинамическиеСписки()
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Структура = Новый Структура;
	Структура.Вставить("НачалоМесяца",НачалоМесяца(ТекущаяДата));
	Структура.Вставить("КонецМесяца",КонецМесяца(ТекущаяДата));
	Структура.Вставить("НачалоНедели",НачалоНедели(ТекущаяДата));
	Структура.Вставить("КонецНедели",КонецНедели(ТекущаяДата));
	Структура.Вставить("НачалоТрехДней",НачалоДня(НачалоДня(ТекущаяДата)-259200)); // 259200 - трое суток 60*60*24*3=259200
	Структура.Вставить("КонецТрехДней",КонецДня(ТекущаяДата));
	Структура.Вставить("НачалоСегодня",НачалоДня(ТекущаяДата));
	Структура.Вставить("КонецСегодня",КонецДня(ТекущаяДата));
	
	Для каждого ТекущийПараметр Из Структура Цикл
		// СервисыМаршрутизации
		СервисыМаршрутизации.Параметры.УстановитьЗначениеПараметра(ТекущийПараметр.Ключ,ТекущийПараметр.Значение);
		// СервисыГеокодирования
		СервисыГеокодирования.Параметры.УстановитьЗначениеПараметра(ТекущийПараметр.Ключ,ТекущийПараметр.Значение);
	КонецЦикла;
	
	Элементы.СервисыМаршрутизации.Обновить();
	Элементы.СервисыГеокодирования.Обновить();
	
	// Выполним удаление ранее созданных команд и кнопок размещенных на форме.
	Для каждого ТекущийСервис Из ТаблицаСервисов Цикл
		Элементы.Удалить(Элементы[ТекущийСервис.ИмяЭлемента]);
		Команды.Удалить(Команды[ТекущийСервис.Команда]);
	КонецЦикла;
	
	ТаблицаСервисов.Очистить();
	
	Если СервисыМаршрутизации.ПроизвольныйЗапрос Тогда
		Запрос = Новый Запрос(СервисыМаршрутизации.ТекстЗапроса);
		Для каждого ТекущийПараметр Из СервисыМаршрутизации.Параметры.Элементы Цикл
			Запрос.УстановитьПараметр(Строка(ТекущийПараметр.Параметр),ТекущийПараметр.Значение);
		КонецЦикла;
		ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
		ТаблицаЗначений.Свернуть("Маршрутизатор");
		Для каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
			Представление = "Очистить "+СтрЗаменить(Строка(ТекущаяСтрока.Маршрутизатор),".", "");
			ИмяКоманды = "СМ_" + СтрЗаменить(Представление," ","");
			ИмяЭлемента = "Кнопка_СервисыqМаршрутизации_" + ИмяКоманды;
			// Добавляем новую команду
			НоваяКоманда = Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Действие = "ОбщееДействие_ОчиститьИсториюСервисаМаршрутизации";
			НоваяКоманда.Заголовок = СтрШаблон("Очистить историю (%1)",Представление);
			// Сохраним Имя команды и элемента формы
			НоваяСтрока = ТаблицаСервисов.Добавить();
			НоваяСтрока.СсылкаНаТип = ТекущаяСтрока.Маршрутизатор;
			НоваяСтрока.Команда = ИмяКоманды;
			НоваяСтрока.ИмяЭлемента = ИмяЭлемента;
			// Добавляем новую кнопку
			ЭлементКнопкаПодменю = Элементы.Добавить(ИмяЭлемента,Тип("КнопкаФормы"),Элементы.СервисыМаршрутизацииОчистить);
			ЭлементКнопкаПодменю.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
			ЭлементКнопкаПодменю.ИмяКоманды = ИмяКоманды;
			ЭлементКнопкаПодменю.Заголовок = Представление;			
		КонецЦикла;
	КонецЕсли;
	
	Если СервисыГеокодирования.ПроизвольныйЗапрос Тогда
		Запрос = Новый Запрос(СервисыГеокодирования.ТекстЗапроса);
		Для каждого ТекущийПараметр Из СервисыГеокодирования.Параметры.Элементы Цикл
			Запрос.УстановитьПараметр(Строка(ТекущийПараметр.Параметр),ТекущийПараметр.Значение);
		КонецЦикла;
		ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
		ТаблицаЗначений.Свернуть("ТипГеокодера");
		Для каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
			Представление = "Очистить "+СтрЗаменить(Строка(ТекущаяСтрока.ТипГеокодера),".", "");
			ИмяКоманды = "СГ_" + СтрЗаменить(Представление," ","");
			ИмяЭлемента = "Кнопка_СервисыqГеокодирования_" + ИмяКоманды;
			// Добавляем новую команду
			НоваяКоманда = Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Действие = "ОбщееДействие_ОчиститьИсториюСервисаГеокодирования";
			НоваяКоманда.Заголовок = СтрШаблон("Очистить историю (%1)",Представление);
			// Сохраним Имя команды и элемента формы
			НоваяСтрока = ТаблицаСервисов.Добавить();
			НоваяСтрока.СсылкаНаТип = ТекущаяСтрока.ТипГеокодера;
			НоваяСтрока.Команда = ИмяКоманды;
			НоваяСтрока.ИмяЭлемента = ИмяЭлемента;
			// Добавляем новую кнопку
			ЭлементКнопкаПодменю = Элементы.Добавить(ИмяЭлемента,Тип("КнопкаФормы"),Элементы.СервисыГеокодированияОчистить);
			ЭлементКнопкаПодменю.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
			ЭлементКнопкаПодменю.ИмяКоманды = ИмяКоманды;
			ЭлементКнопкаПодменю.Заголовок = Представление;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьТаблицуАдресов();
	ЗаполнитьТаблицуГеозон();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОчиститьВсюИсториюСервисаМаршрутизации(Результат, ТекущиеПараметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    ВыполнитьОчисткуВсейСтатистикиМаршрутизации(ТекущиеПараметры);

КонецПроцедуры // ПослеЗакрытияВопросаОчиститьВсюИсториюСервисаМаршрутизации()

&НаКлиенте
Процедура ПослеЗакрытияВопросаОчиститьВсюИсториюСервисаГеокодирования(Результат, ТекущиеПараметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    ВыполнитьОчисткуВсейСтатистикиГеокодирования(ТекущиеПараметры);

КонецПроцедуры // ПослеЗакрытияВопросаОчиститьВсюИсториюСервисаГеокодирования()

&НаКлиенте
Процедура ПослеЗакрытияВопросаОчиститьИсториюСервисаГеокодирования(Результат, ТекущиеПараметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    ВыполнитьОчисткуСтатистикиГеокодирования(ТекущиеПараметры);

КонецПроцедуры // ПослеЗакрытияВопросаОчиститьИсториюСервисаГеокодирования()

&НаКлиенте
Процедура ПослеЗакрытияВопросаОчиститьИсториюСервисаМаршрутизации(Результат, ТекущиеПараметры) Экспорт
	
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    ВыполнитьОчисткуСтатистикиМаршрутизации(ТекущиеПараметры);

КонецПроцедуры // ПослеЗакрытияВопросаОчиститьИсториюСервисаМаршрутизации()

&НаСервере
// Выпнолим перезапись данных РС упСтатистикаОбращенийКСервисамГеокодирования чтобы очистить его по отбору.
//
// Параметры:
//  ТекущиеПараметры  - Структура - Ключ имя команды вызова.
//
Процедура ВыполнитьОчисткуСтатистикиГеокодирования(ТекущиеПараметры)
	
	Команда = ТекущиеПараметры.Команда;	
	ТекущийОтбор = Новый Структура("Команда",Команда);
	НайденныеСтроки = ТаблицаСервисов.НайтиСтроки(ТекущийОтбор);
	ЗапуститьОбновление = Ложь;
	Если НайденныеСтроки.Количество() Тогда
		УстановитьПривилегированныйРежим(Истина);
		ТекущийТип = НайденныеСтроки[0].СсылкаНаТип;
		Если ЗначениеЗаполнено(ТекущийТип) И ТипЗнч(ТекущийТип) = ТИП("ПеречислениеСсылка.упТипыГеокодеров") Тогда
			НаборЗаписей = РегистрыСведений.упСтатистикаОбращенийКСервисамГеокодирования.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ТипГеокодера.Установить(ТекущийТип);
			Попытка
				НаборЗаписей.Записать(Истина);
				ЗапуститьОбновление = Истина;
			Исключение
				ЗапуститьОбновление = Ложь;
			КонецПопытки;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если ЗапуститьОбновление Тогда
		ОбновитьДинамическиеСписки();
	КонецЕсли;	
	
КонецПроцедуры // ВыполнитьОчисткуСтатистикиГеокодирования()

&НаСервере
// Выпнолим перезапись данных РС упСтатистикаОбращенийКСервисамМаршрутизации чтобы очистить его по отбору.
//
// Параметры:
//  ТекущиеПараметры  - Структура - Ключ имя команды вызова.
//
Процедура ВыполнитьОчисткуСтатистикиМаршрутизации(ТекущиеПараметры)
	
	Команда = ТекущиеПараметры.Команда;	
	ТекущийОтбор = Новый Структура("Команда",Команда);
	НайденныеСтроки = ТаблицаСервисов.НайтиСтроки(ТекущийОтбор);
	ЗапуститьОбновление = Ложь;
	Если НайденныеСтроки.Количество() Тогда
		УстановитьПривилегированныйРежим(Истина);
		ТекущийТип = НайденныеСтроки[0].СсылкаНаТип;
		Если ЗначениеЗаполнено(ТекущийТип) И ТипЗнч(ТекущийТип) = ТИП("ПеречислениеСсылка.упТипыМаршрутизаторов") Тогда
			НаборЗаписей = РегистрыСведений.упСтатистикаОбращенийКСервисамМаршрутизации.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ТипМаршрутизатора.Установить(ТекущийТип);
			Попытка
				НаборЗаписей.Записать(Истина);
				ЗапуститьОбновление = Истина;
			Исключение
				ЗапуститьОбновление = Ложь;
			КонецПопытки;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если ЗапуститьОбновление Тогда
		ОбновитьДинамическиеСписки();
	КонецЕсли;	
	
КонецПроцедуры // ВыполнитьОчисткуСтатистикиМаршрутизации()

&НаСервере
// Выпнолим перезапись данных РС упСтатистикаОбращенийКСервисамМаршрутизации чтобы очистить его.
//
// Параметры:
//  ТекущиеПараметры  - Структура - Ключ имя команды вызова.
//
Процедура ВыполнитьОчисткуВсейСтатистикиМаршрутизации(ТекущиеПараметры)
	
	ЗапуститьОбновление = Ложь;	
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = РегистрыСведений.упСтатистикаОбращенийКСервисамМаршрутизации.СоздатьНаборЗаписей();	
	Попытка
		НаборЗаписей.Записать(Истина);
		ЗапуститьОбновление = Истина;
	Исключение
		ЗапуститьОбновление = Ложь;
	КонецПопытки;	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЗапуститьОбновление Тогда
		ОбновитьДинамическиеСписки();
	КонецЕсли;	
	
КонецПроцедуры // ВыполнитьОчисткуВсейСтатистикиМаршрутизации()

&НаСервере
// Выпнолим перезапись данных РС упСтатистикаОбращенийКСервисамГеокодирования чтобы очистить его.
//
// Параметры:
//  ТекущиеПараметры  - Структура - Ключ имя команды вызова.
//
Процедура ВыполнитьОчисткуВсейСтатистикиГеокодирования(ТекущиеПараметры)
	
	ЗапуститьОбновление = Ложь;	
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = РегистрыСведений.упСтатистикаОбращенийКСервисамМаршрутизации.СоздатьНаборЗаписей();	
	Попытка
		НаборЗаписей.Записать(Истина);
		ЗапуститьОбновление = Истина;
	Исключение
		ЗапуститьОбновление = Ложь;
	КонецПопытки;	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЗапуститьОбновление Тогда
		ОбновитьДинамическиеСписки();
	КонецЕсли;	
	
КонецПроцедуры // ВыполнитьОчисткуВсейСтатистикиГеокодирования()

#Область Адреса

&НаСервере
Процедура ЗаполнитьТаблицуАдресов()
	
	 СписокАдресов.Очистить();
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	упАдреса.Ссылка,
	 |	упАдреса.ПометкаУдаления,
	 |	упАдреса.Наименование,
	 |	упАдреса.ГеографическаяЗона,
	 |	упАдреса.Долгота,
	 |	упАдреса.Подтвержден,
	 |	упАдреса.Широта
	 |ПОМЕСТИТЬ втАдреса
	 |ИЗ
	 |	Справочник.упАдреса КАК упАдреса
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	СУММА(ВЫБОР
	 |			КОГДА втАдреса.ПометкаУдаления
	 |				ТОГДА 0
	 |			ИНАЧЕ 1
	 |		КОНЕЦ) КАК ВсегоДействующихАдресов,
	 |	СУММА(ВЫБОР
	 |			КОГДА втАдреса.Долгота + втАдреса.Широта = 0
	 |					И НЕ втАдреса.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоНегеокодированных,
	 |	СУММА(ВЫБОР
	 |			КОГДА втАдреса.Подтвержден
	 |					И НЕ втАдреса.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоНеПодтвержденных,
	 |	СУММА(ВЫБОР
	 |			КОГДА втАдреса.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоПомеченныхНаУдаление,
	 |	СУММА(ВЫБОР
	 |			КОГДА втАдреса.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	 |					И НЕ втАдреса.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК НеопределеныГеозоны
	 |ИЗ
	 |	втАдреса КАК втАдреса
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втАдреса.Ссылка) КАК ЗадублированыНаименования
	 |ИЗ
	 |	втАдреса КАК втАдреса
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса1
	 |		ПО втАдреса.Ссылка <> втАдреса1.Ссылка
	 |			И втАдреса.Наименование = втАдреса1.Наименование
	 |ГДЕ
	 |	втАдреса.ПометкаУдаления = ЛОЖЬ
	 |	И втАдреса1.ПометкаУдаления = ЛОЖЬ
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втАдреса.Ссылка) КАК ЗадублированыКоординаты
	 |ИЗ
	 |	втАдреса КАК втАдреса
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса1
	 |		ПО втАдреса.Ссылка <> втАдреса1.Ссылка
	 |			И втАдреса.Долгота = втАдреса1.Долгота
	 |			И втАдреса.Широта = втАдреса1.Широта
	 |ГДЕ
	 |	втАдреса.ПометкаУдаления = ЛОЖЬ
	 |	И втАдреса1.ПометкаУдаления = ЛОЖЬ";
	 
	 
	 НовыеДанные = СписокАдресов.Добавить();
	 Результат = Запрос.ВыполнитьПакет();
	 ЗаполнитьЗначенияСвойств(НовыеДанные,Результат[1].Выгрузить()[0]);
	 НовыеДанные.ЗадублированыНаименования = Результат[2].Выгрузить()[0].ЗадублированыНаименования;
	 НовыеДанные.ЗадублированыКоординаты = Результат[3].Выгрузить()[0].ЗадублированыКоординаты;
	 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСпискаНегеокодированных(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Значение = "ОткрытьСписокНегеокодированных" Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("Долгота", 0);
			Отбор.Вставить("Широта",  0);
			ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
			Форма = ПолучитьФорму("Справочник.упАдреса.ФормаСписка",ПараметрыОткрытия);
			Форма.Открыть();
		ИначеЕсли Результат.Значение = "Геокодировать" Тогда
			АдресаДляГеокодирования = ПолучитьАдресаДляГеокодирования();
			ГеокодироватьАдреса(АдресаДляГеокодирования);
			ОбновитьДинамическиеСписки();
		КонецЕсли; // При "Отмена" ничего не делаем.
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресаДляГеокодирования() 
	
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	упАдреса.Ссылка КАК Адрес
	 |ИЗ
	 |	Справочник.упАдреса КАК упАдреса
	 |ГДЕ
	 |	упАдреса.Долгота + упАдреса.Широта = 0";
	 
	 Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Адрес");
	 
	 Возврат Результат;
	 
КонецФункции	

&НаКлиенте
Процедура ГеокодироватьАдреса(АдресаДляГеокодирования)
	
	Для Каждого Адрес Из АдресаДляГеокодирования Цикл
		#Если НЕ ВебКлиент Тогда
			сткКоординаты = упГеокодированиеКлиентСервер.НайтиКоординатыАдреса(Адрес);
		#Иначе	
			сткКоординаты = упГеокодированиеСервер.НайтиКоординатыАдреса(Адрес);
		#КонецЕсли	
		
		Если НЕ сткКоординаты = Неопределено Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Геокодирование'"),, НСтр("ru = 'Не удалось определить координаты '")+Адрес, БиблиотекаКартинок.Ошибка32);
		Иначе
			Попытка
				ЗаполнитьАдресДанными(Адрес,сткКоординаты);
			Исключение	
				ПоказатьОповещениеПользователя(НСтр("ru = 'Геокодирование'"),, НСтр("ru = 'Не удалось записать адрес '")+Адрес, БиблиотекаКартинок.Ошибка32);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьАдресДанными(Адрес,сткКоординаты)
	
	АдресОбъект = Адрес.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств(АдресОбъект, сткКоординаты);
	
	Если Не ЗначениеЗаполнено(АдресОбъект.ПолноеНаименование) Тогда 
		АдресОбъект.ПолноеНаименование = АдресОбъект.ПолноеПредставление;
	КонецЕсли;
	
	АдресОбъект.Записать();
		
КонецПроцедуры
			
&НаКлиенте
Процедура СписокАдресовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокАдресовВсегоДействующихАдресов" И Элемент.ТекущиеДанные.ВсегоДействующихАдресов > 0 Тогда
		
		Отбор = Новый Структура("ПометкаУдаления", Ложь);
		ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
		Форма = ПолучитьФорму("Справочник.упАдреса.ФормаСписка",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокАдресовКоличествоНегеокодированных" И Элемент.ТекущиеДанные.КоличествоНегеокодированных > 0 Тогда
		
		Список = Новый СписокЗначений;
		Список.Добавить("ОткрытьСписокНегеокодированных",НСтр("ru = 'Открыть список негеокодированных'"));
		Список.Добавить("Геокодировать", НСтр("ru = 'Геокодировать'"));
		Оповещение = Новый ОписаниеОповещения("ОбработкаСпискаНегеокодированных", ЭтотОбъект);
		ПоказатьВыборИзМеню(Оповещение, Список, Элементы.СписокАдресовКоличествоНегеокодированных);
		
	ИначеЕсли Поле.Имя = "СписокАдресовКоличествоНеПодтвержденных" И Элемент.ТекущиеДанные.КоличествоНеПодтвержденных > 0 Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Подтвержден", Истина);
		Отбор.Вставить("ПометкаУдаления", Ложь);
		ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
		Форма = ПолучитьФорму("Справочник.упАдреса.ФормаСписка",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокАдресовКоличествоПомеченныхНаУдаление" И Элемент.ТекущиеДанные.КоличествоПомеченныхНаУдаление > 0 Тогда
		
		Отбор = Новый Структура("ПометкаУдаления", Истина);
		ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
		Форма = ПолучитьФорму("Справочник.упАдреса.ФормаСписка",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокАдресовНеопределеныГеозоны" И Элемент.ТекущиеДанные.НеопределеныГеозоны > 0 Тогда
		
		Список = Новый СписокЗначений;
		Список.Добавить("ОткрытьСписокНеОпределеныГеозоны", НСтр("ru = 'Открыть список с неопределенными геозонами'"));
		Список.Добавить("ОпределитьГеозоны", НСтр("ru = 'Определить геозоны'"));
		Оповещение = Новый ОписаниеОповещения("ОбработкаСпискаНеОпределеныГеозоны", ЭтотОбъект);
		ПоказатьВыборИзМеню(Оповещение, Список, Элементы.СписокАдресовНеопределеныГеозоны);
		
	ИначеЕсли Поле.Имя = "СписокАдресовЗадублированыКоординаты" И Элемент.ТекущиеДанные.ЗадублированыКоординаты > 0 Тогда
		
		Форма = ПолучитьФорму("Обработка.ПоискИУдалениеДублей.Форма.ПоискДублей");
		Форма.ОбластьПоискаДублей = "Справочник.упАдреса";
		Форма.упДополнительныеПараметры = "Координаты совпадают";
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокАдресовЗадублированыНаименования" И Элемент.ТекущиеДанные.ЗадублированыНаименования > 0 Тогда
		
		Форма = ПолучитьФорму("Обработка.ПоискИУдалениеДублей.Форма.ПоискДублей");
		Форма.ОбластьПоискаДублей = "Справочник.упАдреса";
		Форма.упДополнительныеПараметры = "Наименование совпадает";
		Форма.Открыть();
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСпискаНеОпределеныГеозоны(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Значение = "ОткрытьСписокНеОпределеныГеозоны" Тогда
			Форма = ПолучитьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ФормаСпискаСправочникаупАдресаБезГеозон");
			Форма.Открыть();
		ИначеЕсли Результат.Значение = "ОпределитьГеозоны" Тогда
			АдресаДляОпределенияГеозон = ПолучитьАдресаДляОпределенияГеозон();
			ОпределитьГеозоны(АдресаДляОпределенияГеозон);
			ОбновитьДинамическиеСписки();
		КонецЕсли; // При "Отмена" ничего не делаем.
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресаДляОпределенияГеозон() 
	
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	упАдреса.Ссылка КАК Адрес
	 |ИЗ
	 |	Справочник.упАдреса КАК упАдреса
	 |ГДЕ
	 |	упАдреса.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)";
	 
	 Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Адрес");
	 
	 Возврат Результат;
	 
КонецФункции	

&НаКлиенте
Процедура ОпределитьГеозоны(АдресаДляОпределенияГеозон)
	
	Для Каждого Адрес Из АдресаДляОпределенияГеозон Цикл
		АдресНеГеокодирован = ПроверитьКоординаты(Адрес);
		Если АдресНеГеокодирован Тогда 
			Массив = Новый Массив;
			Массив.Добавить(Адрес);
			ГеокодироватьАдреса(Массив);
			АдресНеГеокодирован = ПроверитьКоординаты(Адрес);
			Если АдресНеГеокодирован Тогда 
				Продолжить;
			КонецЕсли;	
		КонецЕсли;
		
		ГеозонаОпределена = ОпределитьГеозонуАдреса(Адрес);
		
		Если Не ГеозонаОпределена Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось определить географическую зону для адреса'")+Адрес);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПроверитьКоординаты(Адрес)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упАдреса.Ссылка
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Долгота + упАдреса.Широта = 0
	|	И упАдреса.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Адрес);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции	

&НаСервереБезКонтекста
Функция ОпределитьГеозонуНаСервере(Долгота, Широта)

	Возврат Справочники.упГеографическиеЗоны.ВернутьГеографическуюЗону(Долгота, Широта);
	
КонецФункции

&НаСервере
Функция ОпределитьГеоЗонуАдреса(Адрес)
	
	    АдресОбъект = Адрес.ПолучитьОбъект();
		сткГеозона = ОпределитьГеозонуНаСервере(АдресОбъект.Долгота, АдресОбъект.Широта);
		Если сткГеозона <> Неопределено Тогда 
			АдресОбъект.ГеографическаяЗона = сткГеозона.Зона;
			Если ЗначениеЗаполнено(сткГеозона.Страна) И сткГеозона.Страна <> АдресОбъект.Страна Тогда 
				АдресОбъект.Страна = сткГеозона.Страна;
				АдресОбъект.Регион = Неопределено;
			КонецЕсли;	
			Если ЗначениеЗаполнено(сткГеозона.Регион) Тогда 
				АдресОбъект.Регион = сткГеозона.Регион;
			КонецЕсли;
			Попытка
				АдресОбъект.Записать();
				Возврат Истина;
			Исключение
				Возврат Ложь;
			КонецПопытки;	
		Иначе
			Возврат Ложь;
		КонецЕсли;	
		
КонецФункции

#КонецОбласти

&НаСервере
Процедура ЗаполнитьТаблицуГеозон()
	
	 СписокГеозон.Очистить();
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ РАЗЛИЧНЫЕ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(упГеографическиеЗоныПолигон.НомерСтроки, 0)) КАК НомерСтроки,
	 |	упГеографическиеЗоны.Ссылка КАК Ссылка,
	 |	упГеографическиеЗоны.Наименование,
	 |	упГеографическиеЗоны.Подтверждена,
	 |	упГеографическиеЗоны.ПометкаУдаления
	 |ПОМЕСТИТЬ втГеозоны
	 |ИЗ
	 |	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны.Полигон КАК упГеографическиеЗоныПолигон
	 |		ПО (упГеографическиеЗоныПолигон.Ссылка = упГеографическиеЗоны.Ссылка)
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	упГеографическиеЗоны.Ссылка,
	 |	упГеографическиеЗоны.Наименование,
	 |	упГеографическиеЗоны.Подтверждена,
	 |	упГеографическиеЗоны.ПометкаУдаления
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	СУММА(ВЫБОР
	 |			КОГДА втГеозоны.ПометкаУдаления
	 |				ТОГДА 0
	 |			ИНАЧЕ 1
	 |		КОНЕЦ) КАК ВсегоДействующихГеозон,
	 |	СУММА(ВЫБОР
	 |			КОГДА втГеозоны.НомерСтроки < 3
	 |					И НЕ втГеозоны.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоНегеокодированных,
	 |	СУММА(ВЫБОР
	 |			КОГДА втГеозоны.Подтверждена
	 |					И НЕ втГеозоны.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоПодтвержденных,
	 |	СУММА(ВЫБОР
	 |			КОГДА втГеозоны.ПометкаУдаления
	 |				ТОГДА 1
	 |			ИНАЧЕ 0
	 |		КОНЕЦ) КАК КоличествоПомеченныхНаУдаление
	 |ИЗ
	 |	втГеозоны КАК втГеозоны
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втГеозоны.Ссылка) КАК ЗадублированыНаименования
	 |ИЗ
	 |	втГеозоны КАК втГеозоны
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГеозоны КАК втГеозоны1
	 |		ПО втГеозоны.Наименование = втГеозоны1.Наименование
	 |			И втГеозоны.Ссылка <> втГеозоны1.Ссылка
	 |ГДЕ
	 |	втГеозоны.ПометкаУдаления = ЛОЖЬ
	 |			И втГеозоны1.ПометкаУдаления = ЛОЖЬ";
	 
	 
	 НовыеДанные = СписокГеозон.Добавить();
	 Результат = Запрос.ВыполнитьПакет();
	 ЗаполнитьЗначенияСвойств(НовыеДанные,Результат[1].Выгрузить()[0]);
	 НовыеДанные.ЗадублированыНаименования = Результат[2].Выгрузить()[0].ЗадублированыНаименования;
	 ФОКоэффициентыРасчетаВремениВПути = Константы.упУчитыватьКоэффициентыРасчетаВремениВПути.Получить();
	 
	 //Если ФОКоэффициентыРасчетаВремениВПути Тогда
	 //    Запрос.Текст = "ВЫБРАТЬ
	 //                   |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ упГеографическиеЗоныКоэффициентыВремени.Ссылка.Ссылка) КАК СПобочнымиКоэффициентами
	 //                   |ИЗ
	 //                   |	Справочник.упГеографическиеЗоны.КоэффициентыВремени КАК упГеографическиеЗоныКоэффициентыВремени
	 //                   |ГДЕ
	 //                   |	упГеографическиеЗоныКоэффициентыВремени.НомерСтроки > 0";
	 //   Результат = Запрос.Выполнить();
	 //   Если Результат.Пустой() Тогда
	 //   	НовыеДанные.СПобочнымиКоэффициентами = 0;
	 //   Иначе
	 //   	НовыеДанные.СПобочнымиКоэффициентами = Результат.Выгрузить()[0].СПобочнымиКоэффициентами;
	 //   КонецЕсли;	
	 //Иначе	 
	 	НовыеДанные.СПобочнымиКоэффициентами = 0;
	 //КонецЕсли;
	 
КонецПроцедуры
	
&НаКлиенте
Процедура СписокГеозонВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СписокГеозонВсегоГеозон" И Элемент.ТекущиеДанные.ВсегоДействующихГеозон > 0 Тогда
		
		ПараметрыОткрытия = Новый Структура("ПометкаУдаления", Ложь);
		Форма = ПолучитьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ФормаСпискаСправочникаупГеозоныУдаленные",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокГеозонКоличествоНегеокодированных" И Элемент.ТекущиеДанные.КоличествоНегеокодированных > 0 Тогда
		
		ПараметрыОткрытия = Новый Структура("СписокНегеокодированных");
		Форма = ПолучитьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ФормаСпискаСправочникаупГеозоныУдаленные",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокГеозонКоличествоПодтвержденных" И Элемент.ТекущиеДанные.КоличествоПодтвержденных > 0 Тогда
		
		ПараметрыОткрытия = Новый Структура("Подтвержден");
		Форма = ПолучитьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ФормаСпискаСправочникаупГеозоныУдаленные",ПараметрыОткрытия);
		Форма.Открыть();                   
		
	ИначеЕсли Поле.Имя = "СписокГеозонКоличествоПомеченныхНаУдаление" И Элемент.ТекущиеДанные.КоличествоПомеченныхНаУдаление > 0 Тогда
		
		ПараметрыОткрытия = Новый Структура("ПометкаУдаления", Истина);
		Форма = ПолучитьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ФормаСпискаСправочникаупГеозоныУдаленные",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокГеозонСПобочнымиКоэффициентами" И Элемент.ТекущиеДанные.СПобочнымиКоэффициентами > 0 Тогда
		
		ПараметрыОткрытия = Новый Структура("СПобочнымиКоэффициентами");
		Форма = ПолучитьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ФормаСпискаСправочникаупГеозоныУдаленные",ПараметрыОткрытия);
		Форма.Открыть();
		
	ИначеЕсли Поле.Имя = "СписокГеозонЗадублированыНаименования" И Элемент.ТекущиеДанные.ЗадублированыНаименования > 0 Тогда
		
		Форма = ПолучитьФорму("Обработка.ПоискИУдалениеДублей.Форма.ПоискДублей");
		Форма.ОбластьПоискаДублей = "Справочник.упГеографическиеЗоны";
		Форма.Открыть();
				
	КонецЕсли;
	
КонецПроцедуры




&НаСервере
Процедура СформироватьОтчетОЗагрузке(ФоновоеЗадание)
	
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
				"Обработки.упРМАдминистратораГеоданных.ПолучитьСписокЗонИРодителей",, 
				НСтр("ru = 'Поиск пересечений геозон'"));
		
		Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		Иначе 
			ФоновоеЗадание = Истина;
			ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		КонецЕсли;

КонецПроцедуры

	
&НаКлиенте
Процедура ПроверитьПересечениеГеозон(Команда)
	
	ФоновоеЗадание = Ложь;
	СформироватьОтчетОЗагрузке(ФоновоеЗадание);
	
	Если ФоновоеЗадание Тогда
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, ФоновоеЗаданиеИдентификатор);
		//ПодключитьОбработчикОжидания("ФоновоеЗаданиеСозданиеОтчетаНаКлиенте",1, Истина);
		//ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		//ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		//ПараметрыОбработчика.МаксимальныйИнтервал = 5;
	Иначе
		ПараметрыОткрытия = Новый Структура("АдресХранилища",ФоновоеЗаданиеАдресХранилища);
		ОткрытьФорму("Обработка.упРМАдминистратораГеоданных.Форма.ДанныеПоПересечениямГеозон",ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти 

#КонецЕсли 