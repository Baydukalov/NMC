#Область ПеременныеМодуляФормы

// Переменные для ускорения работы с html
&НаКлиенте
Перем СлужебныеЭлементыHtml;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.ЗаявкаНаПеревозку) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана заявка на перевозку! Работа с формой запрещена.'");
	КонецЕсли;
	
	ЭтаФорма.УчитыватьМассу                             = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем                             = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест                    = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьКоличествоЗагрузочныхМетров       = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	
	ЭтаФорма.ЗаявкаНаПеревозку = Параметры.ЗаявкаНаПеревозку;
	Попытка
		//ЗаблокироватьДанныеДляРедактирования(ЭтаФорма.ЗаявкаНаПеревозку,, УникальныйИдентификатор);
		ЗаблокироватьДанныеФормыДляРедактирования();
	Исключение
		ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        ЗаявкаНаПеревозку);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	ВерсияДанных = ПолучитьВерсиюЗаявки();
	
	ЭтаФорма.ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ВыполнитьПервоначальноеЗаполнениеПоказателейЗагрузки();
	УстановитьПараметрыНастроекФормыНаСервере();
	ОбновитьЭтапыПеревозкиНаСервере();
	ИзменитьДоступностьЭлементов();
	
	Если Не ЭтаФорма.ЗапрещенаРаботаССетевымГрафом Тогда
		ЭтаФорма.HTMLСетевойГраф = упСетевойГраф.ПолучитьТекстОбщегоМакетаСетевогоГрафа();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЗаявкаНаПеревозку Тогда
		мсвИзмененныеЭтапы = Неопределено;
		Если ИмяСобытия = "Справочник.упГрузовыеМеста.Создание" И Параметр = ЗаявкаНаПеревозку Тогда
			мсвИзмененныеЭтапы = ДобавитьГрузовоеМестоНаСервере(Источник);
		ИначеЕсли ИмяСобытия = "Справочник.упГрузовыеМеста.Запись" И Параметр = ЗаявкаНаПеревозку Тогда
			мсвИзмененныеЭтапы = ИзменитьВГХГрузовогоМестаНаСервере(Источник);
		КонецЕсли; 
		Если ЗначениеЗаполнено(мсвИзмененныеЭтапы) Тогда
			Для каждого текЭтап Из мсвИзмененныеЭтапы Цикл
				ИзменитьРеброНаГрафе(ЭтапыПеревозки.НайтиПоИдентификатору(текЭтап));
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLСетевойГрафДокументСформирован(Элемент)
	
	Если Не ЭтаФорма.СетевойГрафСформирован Тогда
		ЭтаФорма.СетевойГрафСформирован = Истина;
		ОбновитьДанныеОтображенияГрафа();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура HTMLСетевойГрафПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	#Если НЕ ВебКлиент Тогда
		
		Если ВыполняетОбращениеКДокументу Тогда
			Возврат;
		КонецЕсли;
		
		мсвСтруктур = ПолучитьНовыеРебра();
		Для каждого текДанные Из мсвСтруктур Цикл
			ДобавитьЭтапПеревозки(текДанные);
		КонецЦикла;
		
		ПоказатьСообщенияПользователю();
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПеревозкиПриАктивизацииСтроки(Элемент)
	
	СтрокаЭтап = Элемент.ТекущиеДанные;
	Если Не СтрокаЭтап = Неопределено Тогда
		Элементы.ГрузовыеМестаЭтапов.ОтборСтрок = Новый ФиксированнаяСтруктура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", СтрокаЭтап.НомерВЦепиПоставок, СтрокаЭтап.НомерВЦепиПоставокКонец);
		Элементы.ГруппаГрузовыеМестаЭтапов.Доступность = Не ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ГрузовыеМеста.ТекущиеДанные;
	текЭлемент = Элементы.ГрузовыеМеста.ТекущийЭлемент;
	Если текЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если текСтрока <> Неопределено Тогда 
		Если текЭлемент.Имя = Элементы.ГрузовыеМестаПометка.Имя  Тогда
			текСтрока.Пометка = Не текСтрока.Пометка;
		ИначеЕсли текЭлемент.Имя = "ГрузовыеМестаГрузовоеМесто" Тогда	
			сткПараметры = Новый Структура("Ключ", текСтрока.ГрузовоеМесто);
			ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", сткПараметры, Элемент, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПеревозкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ЭтапыПеревозки.ТекущиеДанные;
	текЭлемент = Элементы.ЭтапыПеревозки.ТекущийЭлемент;
	Если текЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если текСтрока <> Неопределено Тогда 
		Если текЭлемент.Имя = Элементы.ЭтапыПеревозкиПометка.Имя  Тогда
			текСтрока.Пометка = Не текСтрока.Пометка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаЭтаповВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ГрузовыеМестаЭтапов.ТекущиеДанные;
	текЭлемент = Элементы.ГрузовыеМестаЭтапов.ТекущийЭлемент;
	Если текЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если текСтрока <> Неопределено Тогда 
		Если текЭлемент.Имя = Элементы.ГрузовыеМестаЭтаповПометка.Имя  Тогда
			текСтрока.Пометка = Не текСтрока.Пометка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПеревозкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	текСтрока = Элементы.ЭтапыПеревозки.ТекущиеДанные;
	текЭлемент = Элементы.ЭтапыПеревозки.ТекущийЭлемент;
	Если текЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если текСтрока <> Неопределено Тогда 
		Если текЭлемент.Имя = Элементы.ЭтапыПеревозкиПометка.Имя  Тогда
			текСтрока.Пометка = Не текСтрока.Пометка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкиПеревозкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		сткПараметры = Новый Структура("Ключ", ТекущиеДанные.Адрес);
		ОткрытьФорму("Справочник.упАдреса.ФормаОбъекта", сткПараметры, Элемент, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура УстановитьПараметры(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыНастроекФормы", ПараметрыНастроекФормы);
	ОткрытьФорму("Обработка.упРМПланированиеЗаданийНаПеревозку.Форма.ФормаУстановкиПараметровРедактированияСхемы", ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#Область КомандыКарты

&НаКлиенте
Процедура ДобавитьТочкуНаГрафе(Команда)

	ОткрытьФорму("Справочник.упАдреса.ФормаВыбора",, ЭтаФорма,,,, Новый ОписаниеОповещения("ВыбратьАдресДляВершиныГрафаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭтапНаГрафе(Команда)
	
	ЭтаФорма.РежимДобавленияЭтапаНаГрафе = Не ЭтаФорма.РежимДобавленияЭтапаНаГрафе;
	Элементы.ФормаСоздатьЭтапНаГрафе.Пометка = ЭтаФорма.РежимДобавленияЭтапаНаГрафе;
	Если ЭтаФорма.РежимДобавленияЭтапаНаГрафе Тогда
		Элементы.ФормаСоздатьЭтапНаГрафе.Картинка = БиблиотекаКартинок.Изменить;
		ВключитьРежимСозданияРеберГрафа();
	Иначе	
		Элементы.ФормаСоздатьЭтапНаГрафе.Картинка = БиблиотекаКартинок.СоздатьЭлементСписка;
		ВыключитьРежимСозданияРеберГрафа();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыбранноеНаГрафе(Команда)
	
	мсвИдентификаторовТочек = Новый Массив;
	мсвИдентификаторовЭтапов = ПолучитьВыделенныеРебраСГрафа();
	Для каждого текИдентификатор Из мсвИдентификаторовЭтапов Цикл
		ИскомыеЭтапы = ЭтапыПеревозки.НайтиСтроки(Новый Структура("ИндексРебраГрафа", текИдентификатор));
		Если ИскомыеЭтапы.Количество() Тогда
			НайденныйЭтап = ИскомыеЭтапы[0]; 
			Если ЗначениеЗаполнено(НайденныйЭтап.ЗаданиеНаПеревозку) Тогда
				ТекстСообщения = НСтр(СтрШаблон("ru = 'По этапу %1 - %2 сформировано задание на перевозку груза. Отмена запрещена.'", НайденныйЭтап.НомерВЦепиПоставок, НайденныйЭтап.НомерВЦепиПоставокКонец));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
			ИначеЕсли НайденныйЭтап.Отменен Тогда
				НайденныйЭтап.Отменен = Ложь;
				ИзменитьРеброНаГрафе(НайденныйЭтап);
				мсвИдентификаторовТочек.Добавить(НайденныйЭтап.НомерВЦепиПоставок);
				мсвИдентификаторовТочек.Добавить(НайденныйЭтап.НомерВЦепиПоставокКонец);
							
			Иначе
				ОтменитьЭтапПеревозки(НайденныйЭтап);
				мсвИдентификаторовТочек.Добавить(НайденныйЭтап.НомерВЦепиПоставок);
				мсвИдентификаторовТочек.Добавить(НайденныйЭтап.НомерВЦепиПоставокКонец);
				
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	Если мсвИдентификаторовТочек.Количество() Тогда
		мсвИдентификаторовТочек = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвИдентификаторовТочек);
		
		// вспомогательные массивы для определения состояния вершин
		МассивОтмененныхВершин = Новый Массив;
		МассивВершинСЗаданиями = Новый Массив;
		МассивВершинСВыполняющимсяЗаданиями = Новый Массив;
		МассивОбычныхВершин = Новый Массив;
		Для каждого СтрокаЭтап Из ЭтапыПеревозки Цикл
			Если СтрокаЭтап.Отменен Или ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку) Тогда
				Если СтрокаЭтап.Отменен Тогда
					МассивОтмененныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
					МассивОтмененныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку) Тогда
					Если ЗаданиеВыполняется(СтрокаЭтап.СтатусЗадания) Тогда
						МассивВершинСВыполняющимсяЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
						МассивВершинСВыполняющимсяЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);					
					Иначе
						МассивВершинСЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
						МассивВершинСЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);					
					КонецЕсли; 
				КонецЕсли;
			Иначе
				МассивОбычныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
				МассивОбычныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);
			КонецЕсли; 
		КонецЦикла;
		
		Для каждого текИдентификатор Из мсвИдентификаторовТочек Цикл
			ИскомыеТочки = ТочкиПеревозки.НайтиСтроки(Новый Структура("ИндексВершиныГрафа", Число(текИдентификатор)));
			Если ИскомыеТочки.Количество() Тогда
				НайденнаяТочка = ИскомыеТочки[0];
				ДанныеВершиныГрафа = Неопределено;
				Если ИспользуютсяКартинкиДляВершинНаГрафе(ПараметрыНастроекФормы) Тогда
					ИскомыеСтроки = СписокМаркеровВидовАдресов.НайтиСтроки(Новый Структура("ВидАдреса", НайденнаяТочка.ВидАдреса));
					Если ИскомыеСтроки.Количество() Тогда
						текМаркерВидаАдреса = ИскомыеСтроки[0];
						ДанныеВершиныГрафа = текМаркерВидаАдреса.ДанныеВершиныГрафа;
					КонецЕсли;
				КонецЕсли;  
				ИзменитьВершинуГрафа(НайденнаяТочка, ДанныеВершиныГрафа,  МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин);
			КонецЕсли;  
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СтабилизироватьГраф(Команда)
	
	ВыполнитьПроцедуруHTML("stabilizeGraph()");
	
КонецПроцедуры

&НаКлиенте
Процедура МасштабироватьГраф(Команда)
	
	ВыполнитьПроцедуруHTML("fitGraph()");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьОбластьТочкиПеревозки(Команда)

	ОтображатьТочкиПеревозки = Не ОтображатьТочкиПеревозки;
	Элементы.ФормаОтобразитьОбластьТочкиПеревозки.Пометка = ОтображатьТочкиПеревозки;
	Элементы.ГруппаТочкиПеревозки.Видимость = ОтображатьТочкиПеревозки;
	
КонецПроцедуры

&НаКлиенте
Процедура НормализоватьГрузовыеПотоки(Команда)
	мсвИзмененныеЭтапы = НормализоватьГрузовыеПотокиНаСервере();
	Если ЗначениеЗаполнено(мсвИзмененныеЭтапы) Тогда
		Для каждого текЭтап Из мсвИзмененныеЭтапы Цикл
			ИзменитьРеброНаГрафе(ЭтапыПеревозки.НайтиПоИдентификатору(текЭтап));
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область КомандыЭтапов

&НаКлиенте
Процедура ВыбратьВсеЭтапы(Команда)
	
	Для каждого текЭтап Из ЭтапыПеревозки Цикл
		Если Не ЗначениеЗаполнено(текЭтап.ЗаданиеНаПеревозку) Тогда
			текЭтап.Пометка = Истина;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьВыборВсехЭтапов(Команда)
	
	Для каждого текЭтап Из ЭтапыПеревозки Цикл
		текЭтап.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВыбранныйГрузВЭтапы(Команда)

	Отказ = Ложь;
	ПроверитьВыборСтрок(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ПеренестиВыбранныйГрузВЭтапыНаСервере();
	
	СнятьВыборСоСтрокТаблицФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВыбранныйГрузИзЭтапов(Команда)
	
	Отказ = Ложь;
	ПроверитьВыборСтрок(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	УдалитьВыбранныйГрузИзЭтаповНаСервере();
	
	СнятьВыборСоСтрокТаблицФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВыбранныйГрузИзЭтапа(Команда)
	
	текСтрока = Элементы.ЭтапыПеревозки.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		ИскомыеСтроки = ГрузовыеМестаЭтапов.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец, Пометка", текСтрока.НомерВЦепиПоставок, текСтрока.НомерВЦепиПоставокКонец, Истина));
		Если ИскомыеСтроки.Количество() Тогда
			Для каждого текГруз Из ИскомыеСтроки Цикл
				ГрузовыеМестаЭтапов.Удалить(текГруз);
			КонецЦикла; 
			ОбновитьПараметрыГрузовПоЭтапуПеревозки(текСтрока.ПолучитьИдентификатор()); // серверный вызов
			ИзменитьРеброНаГрафе(текСтрока);
		Иначе
			ТекстСообщения = НСтр("ru = 'В списке грузов по этапу отсутствуют выбранные строки!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ГрузовыеМестаЭтапов");
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыборСтрок(Отказ);
	
	ЕстьПомеченныеГрузы = ПроверитьНаличиеПомеченныхГрузов();
	
	Если Не ЕстьПомеченныеГрузы Тогда
		ТекстСообщения = НСтр("ru = 'В общем списке грузов отсутствуют выбранные строки!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ГрузовыеМеста",, Отказ);
		Возврат;
	КонецЕсли; 
	
	// проверка на наличие помеченных этапов
	ЕстьПомеченныеЭтапы = Ложь;
	Для каждого текЭтап Из ЭтапыПеревозки Цикл
		Если текЭтап.Пометка Тогда
			ЕстьПомеченныеЭтапы = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Если Не ЕстьПомеченныеЭтапы Тогда
		ТекстСообщения = НСтр("ru = 'В списке этапов отсутствуют выбранные строки!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ЭтапыПеревозки",, Отказ);
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция	ПроверитьНаличиеПомеченныхГрузов()
	
	// проверка на наличие помеченных грузов
	ЕстьПомеченныеГрузы = Ложь;
	Для каждого текГруз Из ГрузовыеМеста Цикл
		Если текГруз.Пометка Тогда
			ЕстьПомеченныеГрузы = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ЕстьПомеченныеГрузы;
	
КонецФункции

&НаКлиенте
Процедура СнятьВыборСоСтрокТаблицФормы()
	
	Для каждого текЭтап Из ЭтапыПеревозки Цикл
		Если текЭтап.Пометка Тогда
			ИзменитьРеброНаГрафе(текЭтап);
			текЭтап.Пометка = Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого текГруз Из ГрузовыеМеста Цикл
		Если текГруз.Пометка Тогда
			текГруз.Пометка = Ложь;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#Область КомандыГрузовыхМест

&НаКлиенте
Процедура ВыбратьВсеГрузы(Команда)
	
	Для каждого текГруз Из ГрузовыеМеста Цикл
		текГруз.Пометка = Истина;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьВыборВсехГрузов(Команда)
	
	Для каждого текГруз Из ГрузовыеМеста Цикл
		текГруз.Пометка = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтрокуГруза(Команда)
	
	текСтрока = Элементы.ГрузовыеМеста.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения команды требуется выбрать строку из списка грузов.'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	Если текСтрока.КоличествоГрузов < 2 Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения команды количество груза должно быть больше одного.'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстПодсказки = НСтр("ru = 'Введите количество груза в новой строке'");
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ВводКоличестваГрузаВНовойСтрокеЗавершение", ЭтаФорма) , 1, ТекстПодсказки, 5, 0); 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГрузовоеМесто(Команда)
	
	ТекстВопроса = НСтр("ru = 'Добавить в список общих грузов новое грузовое место?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("ПослеОтветаНаВопросОДобавленииГрузовогоМеста", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьГрузовоеМесто(Команда)
	
	Отказ = Ложь;
	ЕстьПомеченныеГрузы = ПроверитьНаличиеПомеченныхГрузов();
	
	Если Не ЕстьПомеченныеГрузы Тогда
		ТекстСообщения = НСтр("ru = 'В общем списке грузов отсутствуют выбранные строки!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ГрузовыеМеста",, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ТекстВопроса = НСтр(СтрШаблон("ru = 'Выбранные грузовые места будут удалены из списка общих грузов и из этапов, по которым не запланировано заданий на перевозку. Все равно продолжить?'"));
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить("Да", НСтр("ru = 'Да'"));
		СписокКнопок.Добавить("Отмена", НСтр("ru = 'Отмена'"));
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеОтветаНаВопросОбУдаленииГрузовыхМест", ЭтаФорма), ТекстВопроса, СписокКнопок);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьГрузовоеМесто(Команда)
	ТекущиеДанные = Элементы.ГрузовыеМеста.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено  Тогда
		сткПараметры = Новый Структура("ЗаявкаНаПеревозку, Ключ", ЗаявкаНаПеревозку, ТекущиеДанные.ГрузовоеМесто);
		ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", сткПараметры, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область КомандыГрузовыхМестЭтапов

&НаКлиенте
Процедура ВыбратьВсеГрузыПоЭтапу(Команда)

	Для каждого текГруз Из ГрузовыеМестаЭтапов Цикл
		текГруз.Пометка = Истина;
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьВыборВсехГрузовПоЭтапу(Команда)
	
	Для каждого текГруз Из ГрузовыеМестаЭтапов Цикл
		текГруз.Пометка = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтрокуГрузаЭтапа(Команда)
	
	текСтрока = Элементы.ГрузовыеМестаЭтапов.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения команды требуется выбрать строку из списка грузов по этапу.'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	Если текСтрока.КоличествоГрузов < 2 Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения команды количество груза должно быть больше одного.'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстПодсказки = НСтр("ru = 'Введите количество груза в новой строке'");
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ВводКоличестваГрузаПоЭтапуВНовойСтрокеЗавершение", ЭтаФорма) , 1, ТекстПодсказки, 5, 0); 
	
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура УдалитьНепосредственноОтмененныеЭтапы(Команда)
	
	мсвКУдалению = Новый Массив;
	мсвПредставленийЭтапов = Новый Массив;
	Для каждого текЭтап Из ЭтапыПеревозки Цикл
		Если текЭтап.Отменен И (Не ЭтаФорма.РежимКорректировки Или текЭтап.НоваяСтрока) Тогда
			мсвКУдалению.Добавить(текЭтап.ПолучитьИдентификатор());
			мсвПредставленийЭтапов.Добавить(ПолучитьПредставлениеЭтапаНаКлиенте(текЭтап));
		КонецЕсли;  	
	КонецЦикла; 
	
	Если мсвКУдалению.Количество() Тогда		
		ТекстВопроса = НСтр(СтрШаблон("ru = 'Доступные к непосредственному удалению этапы (%1) будут безвозвратно удалены. Все равно продолжить?'", СтрСоединить(мсвПредставленийЭтапов, " ,")));
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить("Да", НСтр("ru = 'Да'"));
		СписокКнопок.Добавить("УдалитьСТочками", НСтр("ru = 'Удалить с точками'"));
		СписокКнопок.Добавить("Отмена", НСтр("ru = 'Отмена'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЭтапыКУдалению", мсвКУдалению);
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеОтветаНаВопросОНепосредственномУдаленииЭтапов", ЭтаФорма, ДополнительныеПараметры), ТекстВопроса, СписокКнопок);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В списке этапов перевозки отсутствуют отмененные этапы, доступные к непосредственному удалению.'"));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)

	Отказ = Ложь;
	
	// проверка заполнения обязательных реквизитов в этапах перевозок
	ПроверитьЗаполнениеОбязательныхРеквизитов(Отказ);
	
	Если Не Отказ Тогда
		
		Если Не ГрузовыеПотокиНормализованы() Тогда
			ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросНормализоватьГрузовыеПотоки", ЭтаФорма);
			ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Грузовые потоки не нормализованы. Завершить редактирование?'"), РежимДиалогаВопрос.ДаНет);
		Иначе
			ЗавершитьРедактированиеНаКлиенте()
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось завершить редактирование этапов перевозки.'"));
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактированиеНаКлиенте()
	
	Отказ = Ложь;
	
	ЗаписатьИзмененияВЦепиПоставокНаСервере(Отказ);
			
	Если Отказ Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось завершить редактирование этапов перевозки.'"));
	Иначе
		Оповестить("РедактированиеЭтаповПеревозки",,ЗаявкаНаПеревозку);	
		Оповестить("СменаСтатуса",,ЗаявкаНаПеревозку);	
		ОповеститьОбИзменении(ЗаявкаНаПеревозку);
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьИзмененияВЦепиПоставокНаСервере(Отказ)
	
	Если РежимКорректировки Тогда
		СоздатьКорректировкуЭтаповПеревозки(Отказ);
	Иначе
		ЗаписатьИзмененияВЦепиПоставокВЗаявкеНаПеревозку(Отказ);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКорректировкуЭтаповПеревозки(Отказ)
	
	тзЭтапыДляЗаписи = ПодготовитьТаблицуЭтаповПеревозки();
	
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ЗаявкаНаПеревозку);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеЭтапыПеревозки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозку);
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, ЗаявкаНаПеревозку, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭтапыДляЗаписи"        , тзЭтапыДляЗаписи);
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозку);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭтапыДляЗаписи.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ЭтапыДляЗаписи.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ЭтапыДляЗаписи.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыДляЗаписи.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыДляЗаписи.АдресОтправления КАК АдресОтправления,
	|	ЭтапыДляЗаписи.АдресПолучения КАК АдресПолучения,
	|	ЭтапыДляЗаписи.ВидПеревозки КАК ВидПеревозки,
	|	ЭтапыДляЗаписи.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ЭтапыДляЗаписи.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ЭтапыДляЗаписи.Заказчик КАК Заказчик,
	|	ЭтапыДляЗаписи.ЗонаОтправления КАК ЗонаОтправления,
	|	ЭтапыДляЗаписи.ЗонаПолучения КАК ЗонаПолучения,
	|	ЭтапыДляЗаписи.КоличествоГрузов КАК КоличествоГрузов,
	|	ЭтапыДляЗаписи.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	ЭтапыДляЗаписи.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ЭтапыДляЗаписи.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЭтапыДляЗаписи.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	ЭтапыДляЗаписи.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ЭтапыДляЗаписи.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	ЭтапыДляЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЭтапыДляЗаписи.Организация КАК Организация,
	|	ЭтапыДляЗаписи.Отменен КАК Отменен,
	|	ЭтапыДляЗаписи.Отправитель КАК Отправитель,
	|	ЭтапыДляЗаписи.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	ЭтапыДляЗаписи.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	ЭтапыДляЗаписи.Подразделение КАК Подразделение,
	|	ЭтапыДляЗаписи.Получатель КАК Получатель,
	|	ЭтапыДляЗаписи.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	ЭтапыДляЗаписи.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	ЭтапыДляЗаписи.Приоритет КАК Приоритет,
	|	ЭтапыДляЗаписи.Соглашение КАК Соглашение,
	|	ЭтапыДляЗаписи.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	ЭтапыДляЗаписи.Тара КАК Тара,
	|	ЭтапыДляЗаписи.ЧасовойПояс КАК ЧасовойПояс,
	|	ЭтапыДляЗаписи.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втЭтапыДокумента
	|ИЗ
	|	&ЭтапыДляЗаписи КАК ЭтапыДляЗаписи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерВЦепиПоставок,
	|	НомерВЦепиПоставокКонец,
	|	ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.АдресОтправления КАК АдресОтправления,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.АдресПолучения КАК АдресПолучения,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ВидПеревозки КАК ВидПеревозки,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Заказчик КАК Заказчик,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЗонаОтправления КАК ЗонаОтправления,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЗонаПолучения КАК ЗонаПолучения,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Организация КАК Организация,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Отменен КАК Отменен,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Отправитель КАК Отправитель,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Подразделение КАК Подразделение,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Получатель КАК Получатель,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Приоритет КАК Приоритет,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Соглашение КАК Соглашение,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Тара КАК Тара,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЧасовойПояс КАК ЧасовойПояс,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втМоглиИзменитьсяПодготовка
	|ИЗ
	|	РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(, ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза) КАК упПлановыеЭтапыПеревозкиСрезПоследних
	|ГДЕ
	|	(упПлановыеЭтапыПеревозкиСрезПоследних.НомерВЦепиПоставок, упПлановыеЭтапыПеревозкиСрезПоследних.НомерВЦепиПоставокКонец) В
	|			(ВЫБРАТЬ
	|				втЭтапыДокумента.НомерВЦепиПоставок,
	|				втЭтапыДокумента.НомерВЦепиПоставокКонец
	|			ИЗ
	|				втЭтапыДокумента КАК втЭтапыДокумента)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерВЦепиПоставок,
	|	НомерВЦепиПоставокКонец,
	|	ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЭтапыПеревозкиСГрузами.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиСГрузами.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ втНеотмененные
	|ИЗ
	|	втМоглиИзменитьсяПодготовка КАК втЭтапыПеревозкиСГрузами
	|ГДЕ
	|	НЕ втЭтапыПеревозкиСГрузами.Отменен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМоглиИзмениться.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втМоглиИзмениться.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втМоглиИзмениться.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втМоглиИзмениться.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втМоглиИзмениться.АдресОтправления КАК АдресОтправления,
	|	втМоглиИзмениться.АдресПолучения КАК АдресПолучения,
	|	втМоглиИзмениться.ВидПеревозки КАК ВидПеревозки,
	|	втМоглиИзмениться.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втМоглиИзмениться.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втМоглиИзмениться.Заказчик КАК Заказчик,
	|	втМоглиИзмениться.ЗонаОтправления КАК ЗонаОтправления,
	|	втМоглиИзмениться.ЗонаПолучения КАК ЗонаПолучения,
	|	втМоглиИзмениться.КоличествоГрузов КАК КоличествоГрузов,
	|	втМоглиИзмениться.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втМоглиИзмениться.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втМоглиИзмениться.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втМоглиИзмениться.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втМоглиИзмениться.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втМоглиИзмениться.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втМоглиИзмениться.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втМоглиИзмениться.Организация КАК Организация,
	|	втМоглиИзмениться.Отменен КАК Отменен,
	|	втМоглиИзмениться.Отправитель КАК Отправитель,
	|	втМоглиИзмениться.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втМоглиИзмениться.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втМоглиИзмениться.Подразделение КАК Подразделение,
	|	втМоглиИзмениться.Получатель КАК Получатель,
	|	втМоглиИзмениться.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втМоглиИзмениться.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втМоглиИзмениться.Приоритет КАК Приоритет,
	|	втМоглиИзмениться.Соглашение КАК Соглашение,
	|	втМоглиИзмениться.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втМоглиИзмениться.Тара КАК Тара,
	|	втМоглиИзмениться.ЧасовойПояс КАК ЧасовойПояс,
	|	втМоглиИзмениться.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втМоглиИзмениться
	|ИЗ
	|	втМоглиИзменитьсяПодготовка КАК втМоглиИзмениться
	|ГДЕ
	|	НЕ(втМоглиИзмениться.Отменен
	|				И (втМоглиИзмениться.НомерВЦепиПоставок, втМоглиИзмениться.НомерВЦепиПоставокКонец) В
	|					(ВЫБРАТЬ
	|						втНеотмененные.НомерВЦепиПоставок,
	|						втНеотмененные.НомерВЦепиПоставокКонец
	|					ИЗ
	|						втНеотмененные))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМоглиИзмениться.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втМоглиИзмениться.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втМоглиИзмениться.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ ИсключитьНеизмененные
	|ИЗ
	|	втМоглиИзмениться КАК втМоглиИзмениться
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЭтапыДокумента КАК втЭтапыДокумента
	|		ПО втМоглиИзмениться.ГрузовоеМесто = втЭтапыДокумента.ГрузовоеМесто
	|			И втМоглиИзмениться.НомерВЦепиПоставок = втЭтапыДокумента.НомерВЦепиПоставок
	|			И втМоглиИзмениться.НомерВЦепиПоставокКонец = втЭтапыДокумента.НомерВЦепиПоставокКонец
	|			И втМоглиИзмениться.АдресОтправления = втЭтапыДокумента.АдресОтправления
	|			И втМоглиИзмениться.АдресПолучения = втЭтапыДокумента.АдресПолучения
	|			И втМоглиИзмениться.ГрафикРаботыАдресаОтправления = втЭтапыДокумента.ГрафикРаботыАдресаОтправления
	|			И втМоглиИзмениться.ГрафикРаботыАдресаПолучения = втЭтапыДокумента.ГрафикРаботыАдресаПолучения
	|			И втМоглиИзмениться.Заказчик = втЭтапыДокумента.Заказчик
	|			И втМоглиИзмениться.ЗонаОтправления = втЭтапыДокумента.ЗонаОтправления
	|			И втМоглиИзмениться.ЗонаПолучения = втЭтапыДокумента.ЗонаПолучения
	|			И втМоглиИзмениться.КоличествоГрузов = втЭтапыДокумента.КоличествоГрузов
	|			И втМоглиИзмениться.КонтактноеЛицоЗаказчика = втЭтапыДокумента.КонтактноеЛицоЗаказчика
	|			И втМоглиИзмениться.КонтактноеЛицоОтправителя = втЭтапыДокумента.КонтактноеЛицоОтправителя
	|			И втМоглиИзмениться.КонтактноеЛицоПолучателя = втЭтапыДокумента.КонтактноеЛицоПолучателя
	|			И втМоглиИзмениться.КонтрагентЗаказчика = втЭтапыДокумента.КонтрагентЗаказчика
	|			И втМоглиИзмениться.КонтрагентОтправителя = втЭтапыДокумента.КонтрагентОтправителя
	|			И втМоглиИзмениться.КонтрагентПолучателя = втЭтапыДокумента.КонтрагентПолучателя
	|			И втМоглиИзмениться.НаправлениеДеятельности = втЭтапыДокумента.НаправлениеДеятельности
	|			И втМоглиИзмениться.Организация = втЭтапыДокумента.Организация
	|			И втМоглиИзмениться.Отменен = втЭтапыДокумента.Отменен
	|			И втМоглиИзмениться.Отправитель = втЭтапыДокумента.Отправитель
	|			И втМоглиИзмениться.ОтправлениеГрузаПо = втЭтапыДокумента.ОтправлениеГрузаПо
	|			И втМоглиИзмениться.ОтправлениеГрузаС = втЭтапыДокумента.ОтправлениеГрузаС
	|			И втМоглиИзмениться.Подразделение = втЭтапыДокумента.Подразделение
	|			И втМоглиИзмениться.Получатель = втЭтапыДокумента.Получатель
	|			И втМоглиИзмениться.ПолучениеГрузаПо = втЭтапыДокумента.ПолучениеГрузаПо
	|			И втМоглиИзмениться.ПолучениеГрузаС = втЭтапыДокумента.ПолучениеГрузаС
	|			И втМоглиИзмениться.Приоритет = втЭтапыДокумента.Приоритет
	|			И втМоглиИзмениться.Соглашение = втЭтапыДокумента.Соглашение
	|			И втМоглиИзмениться.СхемаМультимодальнойПеревозки = втЭтапыДокумента.СхемаМультимодальнойПеревозки
	|			И втМоглиИзмениться.Тара = втЭтапыДокумента.Тара
	|			И втМоглиИзмениться.ЧасовойПояс = втЭтапыДокумента.ЧасовойПояс
	|			И втМоглиИзмениться.ТипПеревозки = втЭтапыДокумента.ТипПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыДокумента.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыДокумента.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЭтапыДокумента.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыДокумента.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыДокумента.АдресОтправления КАК АдресОтправления,
	|	втЭтапыДокумента.АдресПолучения КАК АдресПолучения,
	|	втЭтапыДокумента.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыДокумента.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыДокумента.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыДокумента.Заказчик КАК Заказчик,
	|	втЭтапыДокумента.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыДокумента.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыДокумента.КоличествоГрузов КАК КоличествоГрузов,
	|	втЭтапыДокумента.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЭтапыДокумента.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыДокумента.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЭтапыДокумента.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыДокумента.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыДокумента.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втЭтапыДокумента.Организация КАК Организация,
	|	втЭтапыДокумента.Отменен КАК Отменен,
	|	втЭтапыДокумента.Отправитель КАК Отправитель,
	|	втЭтапыДокумента.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыДокумента.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыДокумента.Подразделение КАК Подразделение,
	|	втЭтапыДокумента.Получатель КАК Получатель,
	|	втЭтапыДокумента.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыДокумента.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыДокумента.Приоритет КАК Приоритет,
	|	втЭтапыДокумента.Соглашение КАК Соглашение,
	|	втЭтапыДокумента.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыДокумента.Тара КАК Тара,
	|	втЭтапыДокумента.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыДокумента.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втНовыеИзмененные
	|ИЗ
	|	втЭтапыДокумента КАК втЭтапыДокумента
	|ГДЕ
	|	НЕ (втЭтапыДокумента.НомерВЦепиПоставок, втЭтапыДокумента.НомерВЦепиПоставокКонец, втЭтапыДокумента.ГрузовоеМесто) В
	|				(ВЫБРАТЬ
	|					ИсключитьНеизмененные.НомерВЦепиПоставок,
	|					ИсключитьНеизмененные.НомерВЦепиПоставокКонец,
	|					ИсключитьНеизмененные.ГрузовоеМесто
	|				ИЗ
	|					ИсключитьНеизмененные)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыДокумента.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЭтапыДокумента.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыДокумента.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыДокумента.АдресОтправления КАК АдресОтправления,
	|	втЭтапыДокумента.АдресПолучения КАК АдресПолучения,
	|	втЭтапыДокумента.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыДокумента.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыДокумента.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыДокумента.Заказчик КАК Заказчик,
	|	втЭтапыДокумента.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыДокумента.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыДокумента.КоличествоГрузов КАК КоличествоГрузов,
	|	втЭтапыДокумента.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЭтапыДокумента.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыДокумента.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЭтапыДокумента.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыДокумента.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыДокумента.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втЭтапыДокумента.Организация КАК Организация,
	|	втЭтапыДокумента.Отменен КАК Отменен,
	|	втЭтапыДокумента.Отправитель КАК Отправитель,
	|	втЭтапыДокумента.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыДокумента.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыДокумента.Подразделение КАК Подразделение,
	|	втЭтапыДокумента.Получатель КАК Получатель,
	|	втЭтапыДокумента.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыДокумента.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыДокумента.Приоритет КАК Приоритет,
	|	втЭтапыДокумента.Соглашение КАК Соглашение,
	|	втЭтапыДокумента.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыДокумента.Тара КАК Тара,
	|	втЭтапыДокумента.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыДокумента.ТипПеревозки КАК ТипПеревозки
	|ИЗ
	|	втНовыеИзмененные КАК втЭтапыДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Удаленные.ГрузовоеМесто,
	|	Удаленные.НомерВЦепиПоставок,
	|	Удаленные.НомерВЦепиПоставокКонец,
	|	Удаленные.АдресОтправления,
	|	Удаленные.АдресПолучения,
	|	Удаленные.ВидПеревозки,
	|	Удаленные.ГрафикРаботыАдресаОтправления,
	|	Удаленные.ГрафикРаботыАдресаПолучения,
	|	Удаленные.Заказчик,
	|	Удаленные.ЗонаОтправления,
	|	Удаленные.ЗонаПолучения,
	|	Удаленные.КоличествоГрузов,
	|	Удаленные.КонтактноеЛицоЗаказчика,
	|	Удаленные.КонтактноеЛицоОтправителя,
	|	Удаленные.КонтактноеЛицоПолучателя,
	|	Удаленные.КонтрагентЗаказчика,
	|	Удаленные.КонтрагентОтправителя,
	|	Удаленные.КонтрагентПолучателя,
	|	Удаленные.НаправлениеДеятельности,
	|	Удаленные.Организация,
	|	ИСТИНА,
	|	Удаленные.Отправитель,
	|	Удаленные.ОтправлениеГрузаПо,
	|	Удаленные.ОтправлениеГрузаС,
	|	Удаленные.Подразделение,
	|	Удаленные.Получатель,
	|	Удаленные.ПолучениеГрузаПо,
	|	Удаленные.ПолучениеГрузаС,
	|	Удаленные.Приоритет,
	|	Удаленные.Соглашение,
	|	Удаленные.СхемаМультимодальнойПеревозки,
	|	Удаленные.Тара,
	|	Удаленные.ЧасовойПояс,
	|	Удаленные.ТипПеревозки
	|ИЗ
	|	втМоглиИзмениться КАК Удаленные
	|ГДЕ
	|	НЕ (Удаленные.НомерВЦепиПоставок, Удаленные.НомерВЦепиПоставокКонец, Удаленные.ГрузовоеМесто) В
	|				(ВЫБРАТЬ
	|					ИсключитьНеизмененные.НомерВЦепиПоставок,
	|					ИсключитьНеизмененные.НомерВЦепиПоставокКонец,
	|					ИсключитьНеизмененные.ГрузовоеМесто
	|				ИЗ
	|					ИсключитьНеизмененные КАК ИсключитьНеизмененные
	|		
	|				ОБЪЕДИНИТЬ ВСЕ
	|		
	|				ВЫБРАТЬ
	|					втНовыеИзмененные.НомерВЦепиПоставок,
	|					втНовыеИзмененные.НомерВЦепиПоставокКонец,
	|					втНовыеИзмененные.ГрузовоеМесто
	|				ИЗ
	|					втНовыеИзмененные КАК втНовыеИзмененные)";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
	    КорректировкаЭтапов = Документы.упКорректировкаЭтаповПеревозки.СоздатьДокумент();
		КорректировкаЭтапов.Дата = ТекущаяДатаСеанса();
		КорректировкаЭтапов.Автор = Пользователи.ТекущийПользователь();
		КорректировкаЭтапов.ЗаявкаНаПеревозкуГруза = ЗаявкаНаПеревозку;
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(КорректировкаЭтапов.ИзмененияПлановПеревозки.Добавить(), Выборка);
		КонецЦикла; 
		
		Попытка
			КорректировкаЭтапов.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Не удалось изменить цепь поставок (%1): %2.'", ЗаявкаНаПеревозку, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		КонецПопытки;
		
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли; 
	Иначе
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Цепь поставки не была изменена.'"),,,, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияВЦепиПоставокВЗаявкеНаПеревозку(Отказ)
	
	тзЭтапыДляЗаписи = ПодготовитьТаблицуЭтаповПеревозки(Истина);
	
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ЗаявкаНаПеревозку);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеЭтапыПеревозки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозку);
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, ЗаявкаНаПеревозку, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	СтатусЗаявки = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатус(ЗаявкаНаПеревозку);
	
	ЗаявкаНаПеревозкуОбъект = ЗаявкаНаПеревозку.ПолучитьОбъект();
	 
	Если тзЭтапыДляЗаписи.Количество() = 0 Тогда
		ЗаявкаНаПеревозкуОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
	Иначе
		Если СтатусЗаявки = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая Тогда
			ЗаявкаНаПеревозкуОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок);
		КонецЕсли;
		ЗаявкаНаПеревозкуОбъект.ДополнительныеСвойства.Вставить("ЭтапыМультимодальнойПеревозки", тзЭтапыДляЗаписи);
	КонецЕсли; 
	
	Попытка
		ЗаявкаНаПеревозкуОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Не удалось изменить цепь поставок (%1): %2.'", ЗаявкаНаПеревозку, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецПопытки;
	
	Если ТранзакцияАктивна() Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьТаблицуЭтаповПеревозки(ИсключитьОтмененные = Ложь)
	
	тзЭтапыДляЗаписи = Документы.упЗаявкаНаПеревозкуГруза.СтруктураТаблицыЭтапыПеревозки();
	Для каждого текЭтап Из ЭтапыПеревозки Цикл
		Если ИсключитьОтмененные И текЭтап.Отменен Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текЭтап.ЗаданиеНаПеревозку) Тогда
			Продолжить;
		КонецЕсли;
				
		ИскомыеГрузы = ГрузовыеМестаЭтапов.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", текЭтап.НомерВЦепиПоставок, текЭтап.НомерВЦепиПоставокКонец));
		Для каждого текГруз Из ИскомыеГрузы Цикл
			НоваяСтрока = тзЭтапыДляЗаписи.Добавить();
			НоваяСтрока.ЗаявкаНаПеревозкуГруза = ЗаявкаНаПеревозку; 
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текЭтап,, "КоличествоГрузов"); 
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текГруз); 
			НоваяСтрока.Изменен = Истина;
		КонецЦикла; 
	КонецЦикла; 
	
	Если тзЭтапыДляЗаписи.Количество() = 0 Тогда
		// обработать возможную отмену заявки, с вопросом
		
	КонецЕсли;
	
	Возврат тзЭтапыДляЗаписи;
	
КонецФункции	

&НаСервере
Процедура ОбновитьПараметрыГрузовПоЭтапуПеревозки(ИдентификаторЭтапа)
	
	СтрокаЭтап = ЭтапыПеревозки.НайтиПоИдентификатору(ИдентификаторЭтапа);
	Если СтрокаЭтап = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ГрузовыеМестаЭтапа = ГрузовыеМестаЭтапов.Выгрузить(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", СтрокаЭтап.НомерВЦепиПоставок, СтрокаЭтап.НомерВЦепиПоставокКонец));
	Если ГрузовыеМестаЭтапа.Количество() = 0 Тогда
		СтрокаЭтап.КоличествоГрузов = 0;
		СтрокаЭтап.Масса = 0;
		СтрокаЭтап.Объем = 0;
		СтрокаЭтап.КоличествоМест = 0;
		СтрокаЭтап.КоличествоЗагрузочныхМетров = 0;
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапа", ГрузовыеМестаЭтапа);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ГрузовыеМестаЭтапа.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапа.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапа.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапа.Тара КАК Тара,
	|	ГрузовыеМестаЭтапа.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМестаЭтапа.Масса * ГрузовыеМестаЭтапа.КоличествоГрузов КАК Масса,
	|	ГрузовыеМестаЭтапа.Объем * ГрузовыеМестаЭтапа.КоличествоГрузов КАК Объем,
	|	ГрузовыеМестаЭтапа.КоличествоМест * ГрузовыеМестаЭтапа.КоличествоГрузов КАК КоличествоМест,
	|	ГрузовыеМестаЭтапа.КоличествоЗагрузочныхМетров * ГрузовыеМестаЭтапа.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	&ГрузовыеМестаЭтапа КАК ГрузовыеМестаЭтапа
	|;";
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + упРасчетПоказателейЗагрузки.ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПараметрыИтог";
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтрокаЭтап, Выборка);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ОбновитьПараметрыГрузовогоМеста()
	
	мсвИзмененныеЭтапы = Новый Массив;
	
	ПомеченныеГрузы = ГрузовыеМеста.Выгрузить(Новый Структура("Пометка", Истина));
	ПомеченныеГрузы.Свернуть("ГрузовоеМесто, Тара, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров","КоличествоГрузов");
	
	ПомеченныеЭтапы = ЭтапыПеревозки.Выгрузить(Новый Структура("ЗаданиеНаПеревозку", Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()), "НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа");
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГрузовыеМеста", ПомеченныеГрузы);
	Запрос.УстановитьПараметр("ЭтапыПеревозки", ПомеченныеЭтапы);
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить());

	ТекстЗапроса = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Включенные этапы перевозки
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ИндексРебраГрафа КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//////////////////////////////////////////////////
	|// Включенные грузовые места общего списка грузов
	|ВЫБРАТЬ
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.Тара КАК Тара,
	|	ГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМеста.Масса КАК Масса,
	|	ГрузовыеМеста.Объем КАК Объем,
	|	ГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втПомеченныеГрузы
	|ИЗ
	|	&ГрузовыеМеста КАК ГрузовыеМеста
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//////////////////////////////////////////////////
	|// Грузовые места по этапам
	|ВЫБРАТЬ
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапов.Тара КАК Тара,
	|	ГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМестаЭтапов.Масса КАК Масса,
	|	ГрузовыеМестаЭтапов.Объем КАК Объем,
	|	ГрузовыеМестаЭтапов.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтаповПодготовка
	|ИЗ
	|	&ГрузовыеМестаЭтапов КАК ГрузовыеМестаЭтапов
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|//////////////////////////////////////////////////
	|// Свернутые грузовые места по этапам.
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	СУММА(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Масса) КАК Масса,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Объем) КАК Объем,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоМест) КАК КоличествоМест,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов
	|ИЗ
	|	втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтапов
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|//////////////////////////////////////////////////
	|// Грузовые места(в том числе и новые) по всем включенным этапам
	|ВЫБРАТЬ
	|	втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.Идентификатор КАК Идентификатор,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.Масса, втГрузовыеМестаЭтапов.Масса) * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК Масса,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.Объем, втГрузовыеМестаЭтапов.Объем) * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК Объем,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.КоличествоМест, втГрузовыеМестаЭтапов.КоличествоМест) * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоМест,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.КоличествоЗагрузочныхМетров, втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров) * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоЗагрузочныхМетров,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.Масса, втГрузовыеМестаЭтапов.Масса)) КАК МассаГруз,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.Объем, втГрузовыеМестаЭтапов.Объем)) КАК ОбъемГруз,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.КоличествоМест, втГрузовыеМестаЭтапов.КоличествоМест)) КАК КоличествоМестГруз,
	|	МАКСИМУМ(ЕСТЬNULL(втПомеченныеГрузы.КоличествоЗагрузочныхМетров, втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров)) КАК КоличествоЗагрузочныхМетровГруз,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов КАК втГрузовыеМестаЭтапов
	|	ПО ИСТИНА
	|		И втЭтапыПеревозки.НомерВЦепиПоставок = втГрузовыеМестаЭтапов.НомерВЦепиПоставок
	|		И втЭтапыПеревозки.НомерВЦепиПоставокКонец = втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПомеченныеГрузы КАК втПомеченныеГрузы
	|	ПО ИСТИНА
	|		И втПомеченныеГрузы.ГрузовоеМесто = втГрузовыеМестаЭтапов.ГрузовоеМесто
	|		И втПомеченныеГрузы.Тара = втГрузовыеМестаЭтапов.Тара
	|СГРУППИРОВАТЬ ПО
	|	втЭтапыПеревозки.НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.Идентификатор,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;";

	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + упРасчетПоказателейЗагрузки.ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец,
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПараметрыИтог
	|
	|СГРУППИРОВАТЬ ПО
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец
	|;
	|
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза.Порядок КАК ИндексКартинки,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.МассаГруз КАК Масса,
	|	втГрузовыеМеста.ОбъемГруз КАК Объем,
	|	втГрузовыеМеста.КоличествоМестГруз КАК КоличествоМест,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетровГруз КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста";

	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	ВыборкаГрузовыеМестаЭтапов = РезультатЗапроса[КоличествоТаблиц - 1].Выбрать();
	
	Пока ВыборкаГрузовыеМестаЭтапов.Следующий() Цикл
		сткОтбор = Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ГрузовоеМесто, Тара");
		ЗаполнитьЗначенияСвойств(сткОтбор, ВыборкаГрузовыеМестаЭтапов);
		ИскомыеГрузовыеМестаЭтапов = ГрузовыеМестаЭтапов.НайтиСтроки(сткОтбор);
		Если ИскомыеГрузовыеМестаЭтапов.Количество() Тогда
			НайденныоеГрузовоеМестоЭтапа = ИскомыеГрузовыеМестаЭтапов[0];
			ЗаполнитьЗначенияСвойств(НайденныоеГрузовоеМестоЭтапа, ВыборкаГрузовыеМестаЭтапов, "Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров");
		КонецЕсли; 	
	КонецЦикла;
	
	ВыборкаЭтапы = РезультатЗапроса[КоличествоТаблиц - 2].Выбрать();
	Пока ВыборкаЭтапы.Следующий() Цикл
		ИскомыеЭтапы = ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", ВыборкаЭтапы.НомерВЦепиПоставок, ВыборкаЭтапы.НомерВЦепиПоставокКонец));
		Если ИскомыеЭтапы.Количество() Тогда
			НайденныйЭтап = ИскомыеЭтапы[0];
			ЗаполнитьЗначенияСвойств(НайденныйЭтап, ВыборкаЭтапы, "Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров");
			мсвИзмененныеЭтапы.Добавить(НайденныйЭтап.ПолучитьИдентификатор());
		КонецЕсли; 
	КонецЦикла;
		
	Возврат мсвИзмененныеЭтапы;
	
КонецФункции

&НаСервере
Функция ОбновитьПараметрыЭтапов()
	
	мсвИзмененныеЭтапы = Новый Массив;
	
	ПомеченныеЭтапы = ЭтапыПеревозки.Выгрузить(Новый Структура("Пометка", Истина), "НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа");
		
	Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("ЭтапыПеревозки", ПомеченныеЭтапы);
	//Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить(Новый Структура("Пометка", Истина)));
	
	Запрос.УстановитьПараметр("ЭтапыПеревозки", ЭтапыПеревозки.Выгрузить());
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить());

	ТекстЗапроса = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Включенные этапы перевозки
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ИндексРебраГрафа КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//////////////////////////////////////////
	|// Грузовые места по этапам
	|ВЫБРАТЬ
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапов.Тара КАК Тара,
	|	ГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМестаЭтапов.Масса КАК Масса,
	|	ГрузовыеМестаЭтапов.Объем КАК Объем,
	|	ГрузовыеМестаЭтапов.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтаповПодготовка
	|ИЗ
	|	&ГрузовыеМестаЭтапов КАК ГрузовыеМестаЭтапов
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//////////////////////////////////////////
	|// Свернутые грузовые места по этапам.
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	СУММА(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Масса) КАК Масса,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Объем) КАК Объем,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоМест) КАК КоличествоМест,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов
	|ИЗ
	|	втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтапов
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|//////////////////////////////////////////
	|// Грузовые места(в том числе и новые) по всем включенным этапам
	|ВЫБРАТЬ
	|	втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.Идентификатор КАК Идентификатор,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Масса * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК Масса,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Объем * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК Объем,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоМест * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоМест,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров * втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоЗагрузочныхМетров,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Масса) КАК МассаГруз,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Объем) КАК ОбъемГруз,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоМест) КАК КоличествоМестГруз,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетровГруз,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов КАК втГрузовыеМестаЭтапов
	|	ПО ИСТИНА
	|		И втЭтапыПеревозки.НомерВЦепиПоставок = втГрузовыеМестаЭтапов.НомерВЦепиПоставок
	|		И втЭтапыПеревозки.НомерВЦепиПоставокКонец = втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец
	|СГРУППИРОВАТЬ ПО
	|	втЭтапыПеревозки.НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.Идентификатор,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;";

	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + упРасчетПоказателейЗагрузки.ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец,
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПараметрыИтог
	|
	|СГРУППИРОВАТЬ ПО
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец";

	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	ВыборкаЭтапы = РезультатЗапроса[КоличествоТаблиц - 1].Выбрать();
	Пока ВыборкаЭтапы.Следующий() Цикл
		ИскомыеЭтапы = ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", ВыборкаЭтапы.НомерВЦепиПоставок, ВыборкаЭтапы.НомерВЦепиПоставокКонец));
		Если ИскомыеЭтапы.Количество() Тогда
			НайденныйЭтап = ИскомыеЭтапы[0];
			ЗаполнитьЗначенияСвойств(НайденныйЭтап, ВыборкаЭтапы, "Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КоличествоГрузов");
			мсвИзмененныеЭтапы.Добавить(НайденныйЭтап.ПолучитьИдентификатор());
		КонецЕсли; 
	КонецЦикла;
		
	Возврат мсвИзмененныеЭтапы;
	
КонецФункции

&НаСервере
Функция ПеренестиВыбранныйГрузВЭтапыНаСервере(ТолькоПомеченныеЭтапы = Истина)
	
	мсвИзмененныеЭтапы = Новый Массив;
	
	ПомеченныеГрузы = ГрузовыеМеста.Выгрузить(Новый Структура("Пометка", Истина));
	ПомеченныеГрузы.Свернуть("ГрузовоеМесто, Тара, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров","КоличествоГрузов");
	
	Если ТолькоПомеченныеЭтапы Тогда
		ПомеченныеЭтапы = ЭтапыПеревозки.Выгрузить(Новый Структура("Пометка", Истина), "НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа");
	Иначе
		ПомеченныеЭтапы = ЭтапыПеревозки.Выгрузить(Новый Структура("ЗаданиеНаПеревозку", Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()), "НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГрузовыеМеста", ПомеченныеГрузы);
	Запрос.УстановитьПараметр("ЭтапыПеревозки", ПомеченныеЭтапы);
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить());
	ТекстЗапроса = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Включенные этапы перевозки
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ИндексРебраГрафа КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//////////////////////////////////////////////////////////
	|// Включенные грузовые места общего списка грузов
	|ВЫБРАТЬ
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.Тара КАК Тара,
	|	ГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМеста.Масса КАК Масса,
	|	ГрузовыеМеста.Объем КАК Объем,
	|	ГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втПомеченныеГрузы
	|ИЗ
	|	&ГрузовыеМеста КАК ГрузовыеМеста
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//////////////////////////////////////////////////////////
	|// Грузовые места по этапам
	|ВЫБРАТЬ
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапов.Тара КАК Тара,
	|	ГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМестаЭтапов.Масса КАК Масса,
	|	ГрузовыеМестаЭтапов.Объем КАК Объем,
	|	ГрузовыеМестаЭтапов.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтаповПодготовка
	|ИЗ
	|	&ГрузовыеМестаЭтапов КАК ГрузовыеМестаЭтапов
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|//////////////////////////////////////////////////////////
	|// Свернутые грузовые места по этапам.
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	СУММА(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Масса) КАК Масса,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Объем) КАК Объем,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоМест) КАК КоличествоМест,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов
	|ИЗ
	|	втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтапов
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|//////////////////////////////////////////////////////////
	|// Грузовые места(в том числе и новые) по всем включенным этапам
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ВложенныйЗапрос.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ВложенныйЗапрос.Идентификатор КАК Идентификатор,
	|	ВложенныйЗапрос.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ВложенныйЗапрос.Тара КАК Тара,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(ВложенныйЗапрос.Масса * ВложенныйЗапрос.КоличествоГрузов) КАК Масса,
	|	МАКСИМУМ(ВложенныйЗапрос.Объем * ВложенныйЗапрос.КоличествоГрузов) КАК Объем,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоМест * ВложенныйЗапрос.КоличествоГрузов) КАК КоличествоМест,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоЗагрузочныхМетров * ВложенныйЗапрос.КоличествоГрузов) КАК КоличествоЗагрузочныхМетров,
	|	МАКСИМУМ(ВложенныйЗапрос.Масса) КАК МассаГруз,
	|	МАКСИМУМ(ВложенныйЗапрос.Объем) КАК ОбъемГруз,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоМест) КАК КоличествоМестГруз,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетровГруз,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	(// К каждому этапу прикрепляется включенное грузовое место
	|		ВЫБРАТЬ
	|		втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|		втЭтапыПеревозки.Идентификатор КАК Идентификатор,
	|		втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втГрузовыеМеста.Тара КАК Тара,
	|		втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|		втГрузовыеМеста.Масса КАК Масса,
	|		втГрузовыеМеста.Объем КАК Объем,
	|		втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|		втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|	ИЗ
	|		втЭтапыПеревозки КАК втЭтапыПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПомеченныеГрузы КАК втГрузовыеМеста
	|		ПО ИСТИНА
	|	ОБЪЕДИНИТЬ ВСЕ
	|	// Все уже существующие грузовые места по включенным этапам
	|		ВЫБРАТЬ
	|		втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|		втЭтапыПеревозки.Идентификатор КАК Идентификатор,
	|		втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втГрузовыеМестаЭтапов.Тара КАК Тара,
	|		втГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов,
	|		втГрузовыеМестаЭтапов.Масса КАК Масса,
	|		втГрузовыеМестаЭтапов.Объем КАК Объем,
	|		втГрузовыеМестаЭтапов.КоличествоМест КАК КоличествоМест,
	|		втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|	ИЗ
	|		втЭтапыПеревозки КАК втЭтапыПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов КАК втГрузовыеМестаЭтапов
	|		ПО ИСТИНА
	|			И втЭтапыПеревозки.НомерВЦепиПоставок = втГрузовыеМестаЭтапов.НомерВЦепиПоставок
	|			И втЭтапыПеревозки.НомерВЦепиПоставокКонец = втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.НомерВЦепиПоставок,
	|	ВложенныйЗапрос.НомерВЦепиПоставокКонец,
	|	ВложенныйЗапрос.Идентификатор,
	|	ВложенныйЗапрос.ГрузовоеМесто,
	|	ВложенныйЗапрос.Тара
	|;";

	
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + упРасчетПоказателейЗагрузки.ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец,
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПараметрыИтог
	|
	|СГРУППИРОВАТЬ ПО
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец
	|;
	|
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза.Порядок КАК ИндексКартинки,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.МассаГруз КАК Масса,
	|	втГрузовыеМеста.ОбъемГруз КАК Объем,
	|	втГрузовыеМеста.КоличествоМестГруз КАК КоличествоМест,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетровГруз КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто.ТипГруза.ВидГруза.Порядок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара,
	|	втГрузовыеМестаЭтапов.КоличествоГрузов,
	|	втГрузовыеМестаЭтапов.Масса,
	|	втГрузовыеМестаЭтапов.Объем,
	|	втГрузовыеМестаЭтапов.КоличествоМест,
	|	втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров
	|ИЗ
	|	втГрузовыеМестаЭтапов КАК втГрузовыеМестаЭтапов
	|ГДЕ
	|	НЕ (втГрузовыеМестаЭтапов.НомерВЦепиПоставок, втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец) В
	|				(ВЫБРАТЬ
	|					втЭтапыПеревозки.НомерВЦепиПоставок,
	|					втЭтапыПеревозки.НомерВЦепиПоставокКонец
	|				ИЗ
	|					втЭтапыПеревозки КАК втЭтапыПеревозки)";
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	ГрузовыеМестаЭтапов.Загрузить(РезультатЗапроса[КоличествоТаблиц - 1].Выгрузить());
	ВыборкаЭтапы = РезультатЗапроса[КоличествоТаблиц - 2].Выбрать();
	Пока ВыборкаЭтапы.Следующий() Цикл
		ИскомыеЭтапы = ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", ВыборкаЭтапы.НомерВЦепиПоставок, ВыборкаЭтапы.НомерВЦепиПоставокКонец));
		Если ИскомыеЭтапы.Количество() Тогда
			НайденныйЭтап = ИскомыеЭтапы[0];
			ЗаполнитьЗначенияСвойств(НайденныйЭтап, ВыборкаЭтапы);
			мсвИзмененныеЭтапы.Добавить(НайденныйЭтап.ПолучитьИдентификатор());
		КонецЕсли; 
	КонецЦикла;
	Модифицированность = Истина;
	
	Возврат мсвИзмененныеЭтапы;
	
КонецФункции

&НаСервере
Функция УдалитьВыбранныйГрузИзЭтаповНаСервере(ТолькоПомеченныеЭтапы = Истина)
	
	мсвИзмененныеЭтапы = Новый Массив;
	
	ПомеченныеГрузы = ГрузовыеМеста.Выгрузить(Новый Структура("Пометка", Истина));
	ПомеченныеГрузы.Свернуть("ГрузовоеМесто, Тара, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров","КоличествоГрузов");
	
	Если ТолькоПомеченныеЭтапы Тогда
		ПомеченныеЭтапы = ЭтапыПеревозки.Выгрузить(Новый Структура("Пометка", Истина), "НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа");
	Иначе
		ПомеченныеЭтапы = ЭтапыПеревозки.Выгрузить(Новый Структура("ЗаданиеНаПеревозку", Документы.упЗаданиеНаПеревозкуГруза.ПустаяСсылка()), "НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа");
	КонецЕсли; 
	
	Если ПомеченныеГрузы.Количество() = 0 Или ПомеченныеЭтапы.Количество() = 0 Тогда
		Возврат мсвИзмененныеЭтапы;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГрузовыеМеста", ПомеченныеГрузы);
	Запрос.УстановитьПараметр("ЭтапыПеревозки", ПомеченныеЭтапы);
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить());
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ИндексРебраГрафа КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.Тара КАК Тара,
	|	ГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМеста.Масса КАК Масса,
	|	ГрузовыеМеста.Объем КАК Объем,
	|	ГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втПомеченныеГрузы
	|ИЗ
	|	&ГрузовыеМеста КАК ГрузовыеМеста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапов.Тара КАК Тара,
	|	ГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМестаЭтапов.Масса КАК Масса,
	|	ГрузовыеМестаЭтапов.Объем КАК Объем,
	|	ГрузовыеМестаЭтапов.КоличествоМест КАК КоличествоМест,
	|	ГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтаповПодготовка
	|ИЗ
	|	&ГрузовыеМестаЭтапов КАК ГрузовыеМестаЭтапов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	СУММА(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Масса) КАК Масса,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.Объем) КАК Объем,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоМест) КАК КоличествоМест,
	|	МАКСИМУМ(втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов
	|ИЗ
	|	втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтапов
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ВложенныйЗапрос.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ВложенныйЗапрос.Идентификатор КАК Идентификатор,
	|	ВложенныйЗапрос.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ВложенныйЗапрос.Тара КАК Тара,
	|	СУММА(ВложенныйЗапрос.КоличествоГрузов) КАК КоличествоГрузов,
	|	МАКСИМУМ(ВложенныйЗапрос.Масса) КАК Масса,
	|	МАКСИМУМ(ВложенныйЗапрос.Объем) КАК Объем,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоМест) КАК КоличествоМест,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ втГрузовыеМестаПодготовка
	|ИЗ
	|	(ВЫБРАТЬ
	|		втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|		втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|		втЭтапыПеревозки.Идентификатор КАК Идентификатор,
	|		втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|		втГрузовыеМеста.Тара КАК Тара,
	|		- втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|		втГрузовыеМеста.Масса КАК Масса,
	|		втГрузовыеМеста.Объем КАК Объем,
	|		втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|		втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|	ИЗ
	|		втЭтапыПеревозки КАК втЭтапыПеревозки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПомеченныеГрузы КАК втГрузовыеМеста
	|			ПО (ИСТИНА)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втЭтапыПеревозки.НомерВЦепиПоставок,
	|		втЭтапыПеревозки.НомерВЦепиПоставокКонец,
	|		втЭтапыПеревозки.Идентификатор,
	|		втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|		втГрузовыеМестаЭтапов.Тара,
	|		втГрузовыеМестаЭтапов.КоличествоГрузов,
	|		втГрузовыеМестаЭтапов.Масса,
	|		втГрузовыеМестаЭтапов.Объем,
	|		втГрузовыеМестаЭтапов.КоличествоМест,
	|		втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров
	|	ИЗ
	|		втЭтапыПеревозки КАК втЭтапыПеревозки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов КАК втГрузовыеМестаЭтапов
	|			ПО втЭтапыПеревозки.НомерВЦепиПоставок = втГрузовыеМестаЭтапов.НомерВЦепиПоставок
	|				И втЭтапыПеревозки.НомерВЦепиПоставокКонец = втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.НомерВЦепиПоставок,
	|	ВложенныйЗапрос.НомерВЦепиПоставокКонец,
	|	ВложенныйЗапрос.Идентификатор,
	|	ВложенныйЗапрос.ГрузовоеМесто,
	|	ВложенныйЗапрос.Тара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.Идентификатор КАК Идентификатор,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.Масса КАК Масса,
	|	втГрузовыеМеста.Объем КАК Объем,
	|	втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втГрузовыеМестаПодготовка КАК втГрузовыеМеста
	|ГДЕ
	|	втГрузовыеМеста.КоличествоГрузов > 0
	|;";
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + упРасчетПоказателейЗагрузки.ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец,
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов
	|ПОМЕСТИТЬ втПараметрыЭтапов
	|ИЗ
	| " + ИмяИтоговойТаблицы + " КАК втПараметрыИтог
	|
	|СГРУППИРОВАТЬ ПО
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец
	|;
	|
	|ВЫБРАТЬ
	|	втПараметрыЭтапов.НомерВЦепиПоставок,
	|	втПараметрыЭтапов.НомерВЦепиПоставокКонец,
	|	втПараметрыЭтапов.Объем КАК Объем,
	|	втПараметрыЭтапов.КоличествоМест КАК КоличествоМест,
	|	втПараметрыЭтапов.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	втПараметрыЭтапов.Масса КАК Масса,
	|	втПараметрыЭтапов.КоличествоГрузов КАК КоличествоГрузов
	|ИЗ
	|	втПараметрыЭтапов КАК втПараметрыЭтапов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	втГрузовыеМестаПодготовка КАК втГрузовыеМеста
	|ГДЕ
	|	втГрузовыеМеста.КоличествоГрузов <= 0
	|	И НЕ (втГрузовыеМеста.НомерВЦепиПоставок, втГрузовыеМеста.НомерВЦепиПоставокКонец) В
	|				(ВЫБРАТЬ
	|					втПараметрыЭтапов.НомерВЦепиПоставок,
	|					втПараметрыЭтапов.НомерВЦепиПоставокКонец
	|				ИЗ
	|					втПараметрыЭтапов КАК втПараметрыЭтапов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.Масса КАК Масса,
	|	втГрузовыеМеста.Объем КАК Объем,
	|	втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара,
	|	втГрузовыеМестаЭтапов.КоличествоГрузов,
	|	втГрузовыеМестаЭтапов.Масса,
	|	втГрузовыеМестаЭтапов.Объем,
	|	втГрузовыеМестаЭтапов.КоличествоМест,
	|	втГрузовыеМестаЭтапов.КоличествоЗагрузочныхМетров
	|ИЗ
	|	втГрузовыеМестаЭтапов КАК втГрузовыеМестаЭтапов
	|ГДЕ
	|	НЕ (втГрузовыеМестаЭтапов.НомерВЦепиПоставок, втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец) В
	|				(ВЫБРАТЬ
	|					втЭтапыПеревозки.НомерВЦепиПоставок,
	|					втЭтапыПеревозки.НомерВЦепиПоставокКонец
	|				ИЗ
	|					втЭтапыПеревозки КАК втЭтапыПеревозки)";
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	ГрузовыеМестаЭтапов.Загрузить(РезультатЗапроса[КоличествоТаблиц - 1].Выгрузить());
	ВыборкаЭтапы = РезультатЗапроса[КоличествоТаблиц - 2].Выбрать();
	Пока ВыборкаЭтапы.Следующий() Цикл
		ИскомыеЭтапы = ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", ВыборкаЭтапы.НомерВЦепиПоставок, ВыборкаЭтапы.НомерВЦепиПоставокКонец));
		Если ИскомыеЭтапы.Количество() Тогда
			НайденныйЭтап = ИскомыеЭтапы[0];
			ЗаполнитьЗначенияСвойств(НайденныйЭтап, ВыборкаЭтапы);
			мсвИзмененныеЭтапы.Добавить(НайденныйЭтап.ПолучитьИдентификатор());
		КонецЕсли; 
	КонецЦикла;
	Модифицированность = Истина;
	
	Возврат мсвИзмененныеЭтапы;
	
КонецФункции

&НаСервере
Процедура УдалитьГрузовыеМестаНаСервере()
	
	СообщитьОбОшибке = Ложь;
	мсвКУдалению = Новый Массив;
	Для каждого текГруз Из ГрузовыеМеста Цикл
		Если текГруз.Пометка Тогда
			Если ГрузовыеМестаЭтапов.НайтиСтроки(Новый Структура("ГрузовоеМесто", текГруз.ГрузовоеМесто)).Количество() = 0 Тогда // груз остался в этапах
				мсвКУдалению.Добавить(текГруз);
			Иначе
				СообщитьОбОшибке = Истина;	
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
 
	Для каждого текСтрокаГруз Из мсвКУдалению Цикл
		ГрузовыеМеста.Удалить(текСтрокаГруз);
	КонецЦикла;
	
	Если СообщитьОбОшибке Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Часть грузовых мест не может быть удалена, так как они включены в этапы с сформированными заданиями на перевозку.'"));
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЭтапПеревозки(СтрокаЭтап)
	
	СтрокаЭтап.Отменен = Истина;
	ИзменитьРеброНаГрафе(СтрокаЭтап);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыАдресаНаСервере(Адрес)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Адрес, Новый Структура("ВидАдреса, ПредставлениеАдреса", "ВидАдреса", "Наименование"));
	
КонецФункции

&НаСервере
Процедура УстановитьПараметрыНастроекФормыНаСервере()
	
	ЗаполнитьПараметрыПоУмолчанию = Истина;
	
	КлючОбъекта = "Обработка.упРМПланированиеЗаданий.ФормаРедактированияЭтаповПеревозки.ПараметрыФормы";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если НЕ СохраненныеНастройки = Неопределено Тогда
				ЭтаФорма.ПараметрыНастроекФормы = СохраненныеНастройки;				
				ЗаполнитьПараметрыПоУмолчанию = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗаполнитьПараметрыПоУмолчанию Тогда
		ЭтаФорма.ПараметрыНастроекФормы = упВариантыНастроек.ПараметрыНастроекФормыРедактированияЭтаповПеревозкиПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметрыПланированиеРемонтовНаСервере()

&НаСервере
Процедура ОбновитьЭтапыПеревозкиНаСервере()
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
	МассивСтатусов.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс);
	МассивСтатусов.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению);
	МассивСтатусов.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполняется);
	МассивСтатусов.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВПути);
	МассивСтатусов.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполнено);
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упСтатусыЗаданий.Статус КАК СтатусЗадания,
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозку,
	|	упЗаданиеНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения КАК АдресПолучения,
	|	упЗаданиеНаПеревозкуГруза.ВидПеревозки КАК ВидПеревозки,
	|	упЗаданиеНаПеревозкуГруза.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	упЗаданиеНаПеревозкуГруза.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	упЗаданиеНаПеревозкуГруза.Заказчик КАК Заказчик,
	|	упЗаданиеНаПеревозкуГруза.ЗонаОтправления КАК ЗонаОтправления,
	|	упЗаданиеНаПеревозкуГруза.ЗонаПолучения КАК ЗонаПолучения,
	|	упЗаданиеНаПеревозкуГруза.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	упЗаданиеНаПеревозкуГруза.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	упЗаданиеНаПеревозкуГруза.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	упЗаданиеНаПеревозкуГруза.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	упЗаданиеНаПеревозкуГруза.Организация КАК Организация,
	|	упЗаданиеНаПеревозкуГруза.Отправитель КАК Отправитель,
	|	упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	упЗаданиеНаПеревозкуГруза.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	упЗаданиеНаПеревозкуГруза.Подразделение КАК Подразделение,
	|	упЗаданиеНаПеревозкуГруза.Получатель КАК Получатель,
	|	упЗаданиеНаПеревозкуГруза.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	упЗаданиеНаПеревозкуГруза.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	упЗаданиеНаПеревозкуГруза.Приоритет КАК Приоритет,
	|	упЗаданиеНаПеревозкуГруза.Соглашение КАК Соглашение,
	|	упЗаданиеНаПеревозкуГруза.ЧасовойПояс КАК ЧасовойПояс
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(, ) КАК упСтатусыЗаданий
	|		ПО (упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозку)
	|			И упЗаданиеНаПеревозкуГруза.Ссылка = упСтатусыЗаданий.Документ
	|			И (упСтатусыЗаданий.Статус В (&МассивСтатусов))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Период КАК Период,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Регистратор КАК Регистратор,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.АдресОтправления КАК АдресОтправления,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.АдресПолучения КАК АдресПолучения,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ВидПеревозки КАК ВидПеревозки,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Заказчик КАК Заказчик,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЗонаОтправления КАК ЗонаОтправления,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЗонаПолучения КАК ЗонаПолучения,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Организация КАК Организация,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Отменен КАК Отменен,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Отправитель КАК Отправитель,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Подразделение КАК Подразделение,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Получатель КАК Получатель,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Приоритет КАК Приоритет,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Соглашение КАК Соглашение,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.Тара КАК Тара,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ЧасовойПояс КАК ЧасовойПояс,
	|	упПлановыеЭтапыПеревозкиСрезПоследних.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втЭтапыПеревозкиСГрузамиПодготовка
	|ИЗ
	|	РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(, ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозку) КАК упПлановыеЭтапыПеревозкиСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЭтапыПеревозкиСГрузами.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиСГрузами.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ втНеотмененные
	|ИЗ
	|	втЭтапыПеревозкиСГрузамиПодготовка КАК втЭтапыПеревозкиСГрузами
	|ГДЕ
	|	НЕ втЭтапыПеревозкиСГрузами.Отменен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозки.Период КАК Период,
	|	втЭтапыПеревозки.Регистратор КАК Регистратор,
	|	втЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.АдресОтправления КАК АдресОтправления,
	|	втЭтапыПеревозки.АдресПолучения КАК АдресПолучения,
	|	втЭтапыПеревозки.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыПеревозки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыПеревозки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыПеревозки.Заказчик КАК Заказчик,
	|	втЭтапыПеревозки.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыПеревозки.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыПеревозки.КоличествоГрузов КАК КоличествоГрузов,
	|	втЭтапыПеревозки.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЭтапыПеревозки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыПеревозки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЭтапыПеревозки.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыПеревозки.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыПеревозки.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыПеревозки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втЭтапыПеревозки.Организация КАК Организация,
	|	втЭтапыПеревозки.Отменен КАК Отменен,
	|	втЭтапыПеревозки.Отправитель КАК Отправитель,
	|	втЭтапыПеревозки.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыПеревозки.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыПеревозки.Подразделение КАК Подразделение,
	|	втЭтапыПеревозки.Получатель КАК Получатель,
	|	втЭтапыПеревозки.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыПеревозки.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыПеревозки.Приоритет КАК Приоритет,
	|	втЭтапыПеревозки.Соглашение КАК Соглашение,
	|	втЭтапыПеревозки.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыПеревозки.Тара КАК Тара,
	|	втЭтапыПеревозки.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыПеревозки.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втЭтапыПеревозкиСГрузами
	|ИЗ
	|	втЭтапыПеревозкиСГрузамиПодготовка КАК втЭтапыПеревозки
	|ГДЕ
	|	НЕ(втЭтапыПеревозки.Отменен
	|				И (втЭтапыПеревозки.НомерВЦепиПоставок, втЭтапыПеревозки.НомерВЦепиПоставокКонец) В
	|					(ВЫБРАТЬ
	|						втНеотмененные.НомерВЦепиПоставок,
	|						втНеотмененные.НомерВЦепиПоставокКонец
	|					ИЗ
	|						втНеотмененные))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.АдресОтправления КАК АдресОтправления,
	|	втЭтапыПеревозки.АдресПолучения КАК АдресПолучения,
	|	втЭтапыПеревозки.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыПеревозки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыПеревозки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыПеревозки.Заказчик КАК Заказчик,
	|	втЭтапыПеревозки.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыПеревозки.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыПеревозки.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЭтапыПеревозки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыПеревозки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЭтапыПеревозки.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыПеревозки.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыПеревозки.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыПеревозки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втЭтапыПеревозки.Организация КАК Организация,
	|	втЭтапыПеревозки.Отменен КАК Отменен,
	|	втЭтапыПеревозки.Отправитель КАК Отправитель,
	|	втЭтапыПеревозки.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыПеревозки.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыПеревозки.Подразделение КАК Подразделение,
	|	втЭтапыПеревозки.Получатель КАК Получатель,
	|	втЭтапыПеревозки.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыПеревозки.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыПеревозки.Приоритет КАК Приоритет,
	|	втЭтапыПеревозки.Соглашение КАК Соглашение,
	|	втЭтапыПеревозки.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыПеревозки.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыПеревозки.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втЭтапыПеревозкиПодготовка
	|ИЗ
	|	втЭтапыПеревозкиСГрузами КАК втЭтапыПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втЗадания.ЗаданиеНаПеревозку, ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)) КАК ЗаданиеНаПеревозку,
	|	ЕСТЬNULL(втЗадания.СтатусЗадания, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ПустаяСсылка)) КАК СтатусЗадания,
	|	втЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЕСТЬNULL(втЗадания.АдресОтправления, втЭтапыПеревозки.АдресОтправления) КАК АдресОтправления,
	|	ЕСТЬNULL(втЗадания.АдресПолучения, втЭтапыПеревозки.АдресПолучения) КАК АдресПолучения,
	|	ЕСТЬNULL(втЗадания.ВидПеревозки, втЭтапыПеревозки.ВидПеревозки) КАК ВидПеревозки,
	|	ЕСТЬNULL(втЗадания.ГрафикРаботыАдресаОтправления, втЭтапыПеревозки.ГрафикРаботыАдресаОтправления) КАК ГрафикРаботыАдресаОтправления,
	|	ЕСТЬNULL(втЗадания.ГрафикРаботыАдресаПолучения, втЭтапыПеревозки.ГрафикРаботыАдресаПолучения) КАК ГрафикРаботыАдресаПолучения,
	|	ЕСТЬNULL(втЗадания.Заказчик, втЭтапыПеревозки.Заказчик) КАК Заказчик,
	|	ЕСТЬNULL(втЗадания.ЗонаОтправления, втЭтапыПеревозки.ЗонаОтправления) КАК ЗонаОтправления,
	|	ЕСТЬNULL(втЗадания.ЗонаПолучения, втЭтапыПеревозки.ЗонаПолучения) КАК ЗонаПолучения,
	|	ЕСТЬNULL(втЗадания.КонтактноеЛицоЗаказчика, втЭтапыПеревозки.КонтактноеЛицоЗаказчика) КАК КонтактноеЛицоЗаказчика,
	|	ЕСТЬNULL(втЗадания.КонтактноеЛицоОтправителя, втЭтапыПеревозки.КонтактноеЛицоОтправителя) КАК КонтактноеЛицоОтправителя,
	|	ЕСТЬNULL(втЗадания.КонтактноеЛицоПолучателя, втЭтапыПеревозки.КонтактноеЛицоПолучателя) КАК КонтактноеЛицоПолучателя,
	|	ЕСТЬNULL(втЗадания.КонтрагентЗаказчика, втЭтапыПеревозки.КонтрагентЗаказчика) КАК КонтрагентЗаказчика,
	|	ЕСТЬNULL(втЗадания.КонтрагентОтправителя, втЭтапыПеревозки.КонтрагентОтправителя) КАК КонтрагентОтправителя,
	|	ЕСТЬNULL(втЗадания.КонтрагентПолучателя, втЭтапыПеревозки.КонтрагентПолучателя) КАК КонтрагентПолучателя,
	|	ЕСТЬNULL(втЗадания.НаправлениеДеятельности, втЭтапыПеревозки.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втЗадания.Организация, втЭтапыПеревозки.Организация) КАК Организация,
	|	втЭтапыПеревозки.Отменен КАК Отменен,
	|	ЕСТЬNULL(втЗадания.Отправитель, втЭтапыПеревозки.Отправитель) КАК Отправитель,
	|	ЕСТЬNULL(втЗадания.ОтправлениеГрузаПо, втЭтапыПеревозки.ОтправлениеГрузаПо) КАК ОтправлениеГрузаПо,
	|	ЕСТЬNULL(втЗадания.ОтправлениеГрузаС, втЭтапыПеревозки.ОтправлениеГрузаС) КАК ОтправлениеГрузаС,
	|	ЕСТЬNULL(втЗадания.Подразделение, втЭтапыПеревозки.Подразделение) КАК Подразделение,
	|	ЕСТЬNULL(втЗадания.Получатель, втЭтапыПеревозки.Получатель) КАК Получатель,
	|	ЕСТЬNULL(втЗадания.ПолучениеГрузаПо, втЭтапыПеревозки.ПолучениеГрузаПо) КАК ПолучениеГрузаПо,
	|	ЕСТЬNULL(втЗадания.ПолучениеГрузаС, втЭтапыПеревозки.ПолучениеГрузаС) КАК ПолучениеГрузаС,
	|	ЕСТЬNULL(втЗадания.Приоритет, втЭтапыПеревозки.Приоритет) КАК Приоритет,
	|	ЕСТЬNULL(втЗадания.Соглашение, втЭтапыПеревозки.Соглашение) КАК Соглашение,
	|	втЭтапыПеревозки.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	ЕСТЬNULL(втЗадания.ЧасовойПояс, втЭтапыПеревозки.ЧасовойПояс) КАК ЧасовойПояс,
	|	втЭтапыПеревозки.ТипПеревозки КАК ТипПеревозки
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	втЭтапыПеревозкиПодготовка КАК втЭтапыПеревозки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	|		ПО втЭтапыПеревозки.НомерВЦепиПоставок = втЗадания.НомерВЦепиПоставок
	|			И втЭтапыПеревозки.НомерВЦепиПоставокКонец = втЗадания.НомерВЦепиПоставокКонец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиСГрузами.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиСГрузами.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозкиСГрузами.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЭтапыПеревозкиСГрузами.Тара КАК Тара,
	|	втЭтапыПеревозкиСГрузами.КоличествоГрузов КАК КоличествоГрузов,
	|	втЭтапыПеревозкиСГрузами.Отменен КАК Отменен
	|ПОМЕСТИТЬ втГрузовыеМестаПодготовка
	|ИЗ
	|	втЭтапыПеревозкиСГрузами КАК втЭтапыПеревозкиСГрузами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЭтапыПеревозки КАК втЭтапыПеревозки
	|		ПО втЭтапыПеревозкиСГрузами.НомерВЦепиПоставок = втЭтапыПеревозки.НомерВЦепиПоставок
	|			И втЭтапыПеревозкиСГрузами.НомерВЦепиПоставокКонец = втЭтапыПеревозки.НомерВЦепиПоставокКонец
	|			И (втЭтапыПеревозки.ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЭтапыПеревозки.НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец,
	|	упГрузовыеМеста.ГрузовоеМесто,
	|	упГрузовыеМеста.Тара,
	|	упГрузовыеМеста.КоличествоГрузов,
	|	втЭтапыПеревозки.Отменен
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упГрузовыеМеста
	|		ПО втЭтапыПеревозки.ЗаданиеНаПеревозку = упГрузовыеМеста.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза.Порядок КАК ИндексКартинки,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.Отменен КАК Отменен,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, втГрузовыеМеста.ГрузовоеМесто.КоличествоМест) * втГрузовыеМеста.КоличествоГрузов КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, втГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров) * втГрузовыеМеста.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, втГрузовыеМеста.ГрузовоеМесто.Объем) * втГрузовыеМеста.КоличествоГрузов КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, втГрузовыеМеста.ГрузовоеМесто.Масса) * втГрузовыеМеста.КоличествоГрузов КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, втГрузовыеМеста.ГрузовоеМесто.КоличествоМест) КАК КоличествоМестГруз,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, втГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетровГруз,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, втГрузовыеМеста.ГрузовоеМесто.Объем) КАК ОбъемГруз,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, втГрузовыеМеста.ГрузовоеМесто.Масса) КАК МассаГруз,
	|	0 КАК Длина,
	|	0 КАК Ширина,
	|	0 КАК Высота,
	|	0 КАК Сумма,
	|	0 КАК СуммаНДС,
	|	0 КАК ОценочнаяСтоимость,
	|	0 КАК СуммаИнкассацииПлан,
	|	0 КАК КоличествоГрузовыхМест
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втГрузовыеМестаПодготовка КАК втГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку = &ЗаявкаНаПеревозку) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО втГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|ГДЕ
	|	ВЫРАЗИТЬ(&ЗаявкаНаПеревозку КАК Документ.упЗаявкаНаПеревозкуГруза).ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.ГрузовоеМесто.ТипГруза.ВидГруза.Порядок,
	|	втГрузовыеМеста.Тара,
	|	втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.Отменен,
	|	втГрузовыеМеста.ГрузовоеМесто.КоличествоМест * втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров * втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.ГрузовоеМесто.Объем * втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.ГрузовоеМесто.Масса * втГрузовыеМеста.КоличествоГрузов,
	|	втГрузовыеМеста.ГрузовоеМесто.КоличествоМест,
	|	втГрузовыеМеста.ГрузовоеМесто.КоличествоЗагрузочныхМетров,
	|	втГрузовыеМеста.ГрузовоеМесто.Объем,
	|	втГрузовыеМеста.ГрузовоеМесто.Масса,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	втГрузовыеМестаПодготовка КАК втГрузовыеМеста
	|ГДЕ
	|	ВЫРАЗИТЬ(&ЗаявкаНаПеревозку КАК Документ.упЗаявкаНаПеревозкуГруза).ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|;";
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		ТекстЗапроса = ТекстЗапроса + упРасчетПоказателейЗагрузки.ПолучитьТекстЗапросаРасчетПараметровВложенныхГрузов();
		ИмяИтоговойТаблицы = "втПараметрыИтог";
	Иначе	
		ИмяИтоговойТаблицы = "втГрузовыеМеста";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	втПараметрыИтог.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	СУММА(втПараметрыИтог.КоличествоГрузов) КАК КоличествоГрузов,
	|	СУММА(ВЫРАЗИТЬ(втПараметрыИтог.КоличествоМест КАК ЧИСЛО(8, 0))) КАК КоличествоМест,
	|	СУММА(втПараметрыИтог.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	СУММА(втПараметрыИтог.Объем) КАК Объем,
	|	СУММА(втПараметрыИтог.Масса) КАК Масса
	|ПОМЕСТИТЬ втИтогиПоЭтапу
	|ИЗ
	|	" + ИмяИтоговойТаблицы + " КАК втПараметрыИтог 
	|
	|СГРУППИРОВАТЬ ПО
	|	втПараметрыИтог.НомерВЦепиПоставок,
	|	втПараметрыИтог.НомерВЦепиПоставокКонец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГруза.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &ЗаявкаНаПеревозку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ИндексВершиныГрафа КАК ИндексВершиныГрафа,
	|	ВложенныйЗапрос.Адрес КАК Адрес,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВложенныйЗапрос.Адрес) КАК ПредставлениеАдреса,
	|	ВложенныйЗапрос.Адрес.ВидАдреса КАК ВидАдреса,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВложенныйЗапрос.Адрес) КАК КоличествоРазличных
	|ИЗ
	|	(ВЫБРАТЬ
	|		втЭтапыПеревозки.НомерВЦепиПоставок КАК ИндексВершиныГрафа,
	|		втЭтапыПеревозки.АдресОтправления КАК Адрес
	|	ИЗ
	|		втЭтапыПеревозки КАК втЭтапыПеревозки
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		втЭтапыПеревозки.НомерВЦепиПоставокКонец,
	|		втЭтапыПеревозки.АдресПолучения
	|	ИЗ
	|		втЭтапыПеревозки КАК втЭтапыПеревозки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ИндексВершиныГрафа,
	|	ВложенныйЗапрос.Адрес,
	|	ВложенныйЗапрос.Адрес.ВидАдреса
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИндексВершиныГрафа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.ИндексКартинки КАК ИндексКартинки,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.Отменен КАК Отменен,
	|	втГрузовыеМеста.КоличествоМестГруз КАК КоличествоМест,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетровГруз КАК КоличествоЗагрузочныхМетров,
	|	втГрузовыеМеста.ОбъемГруз КАК Объем,
	|	втГрузовыеМеста.МассаГруз КАК Масса
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|
	|УПОРЯДОЧИТЬ ПО
	|	втГрузовыеМеста.ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	МАКСИМУМ(втГрузовыеМеста.ИндексКартинки) КАК ИндексКартинки,
	|	МАКСИМУМ(втГрузовыеМеста.КоличествоМестГруз) КАК КоличествоМест,
	|	МАКСИМУМ(втГрузовыеМеста.КоличествоЗагрузочныхМетровГруз) КАК КоличествоЗагрузочныхМетров,
	|	МАКСИМУМ(втГрузовыеМеста.ОбъемГруз) КАК Объем,
	|	МАКСИМУМ(втГрузовыеМеста.МассаГруз) КАК Масса,
	|	МАКСИМУМ(втГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.ГрузовоеМесто,
	|	втГрузовыеМеста.Тара
	|
	|УПОРЯДОЧИТЬ ПО
	|	втГрузовыеМеста.ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозки.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втЭтапыПеревозки.СтатусЗадания КАК СтатусЗадания,
	|	втЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозки.АдресОтправления КАК АдресОтправления,
	|	втЭтапыПеревозки.АдресПолучения КАК АдресПолучения,
	|	втЭтапыПеревозки.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыПеревозки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыПеревозки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыПеревозки.Заказчик КАК Заказчик,
	|	втЭтапыПеревозки.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыПеревозки.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыПеревозки.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЭтапыПеревозки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыПеревозки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЭтапыПеревозки.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыПеревозки.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыПеревозки.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыПеревозки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втЭтапыПеревозки.Организация КАК Организация,
	|	втЭтапыПеревозки.Отменен КАК Отменен,
	|	втЭтапыПеревозки.Отправитель КАК Отправитель,
	|	втЭтапыПеревозки.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыПеревозки.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыПеревозки.Подразделение КАК Подразделение,
	|	втЭтапыПеревозки.Получатель КАК Получатель,
	|	втЭтапыПеревозки.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыПеревозки.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыПеревозки.Приоритет КАК Приоритет,
	|	втЭтапыПеревозки.Соглашение КАК Соглашение,
	|	втЭтапыПеревозки.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыПеревозки.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыПеревозки.ТипПеревозки КАК ТипПеревозки,
	|	ВЫБОР
	|		КОГДА втЭтапыПеревозки.ТипПеревозки = ЗНАЧЕНИЕ(Перечисление.упТипыПеревозок.ПустаяСсылка)
	|			ТОГДА 10
	|		ИНАЧЕ втЭтапыПеревозки.ТипПеревозки.Порядок
	|	КОНЕЦ КАК ИндексКартинкиСтрок,
	|	ЕСТЬNULL(втИтогиПоЭтапу.КоличествоГрузов, 0) КАК КоличествоГрузов,
	|	ЕСТЬNULL(втИтогиПоЭтапу.КоличествоМест, 0) КАК КоличествоМест,
	|	ЕСТЬNULL(втИтогиПоЭтапу.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(втИтогиПоЭтапу.Масса, 0) КАК Масса,
	|	ЕСТЬNULL(втИтогиПоЭтапу.Объем, 0) КАК Объем
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИтогиПоЭтапу КАК втИтогиПоЭтапу
	|		ПО втЭтапыПеревозки.НомерВЦепиПоставок = втИтогиПоЭтапу.НомерВЦепиПоставок
	|			И втЭтапыПеревозки.НомерВЦепиПоставокКонец = втИтогиПоЭтапу.НомерВЦепиПоставокКонец";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозку", ЗаявкаНаПеревозку);
	Запрос.УстановитьПараметр("МассивСтатусов", МассивСтатусов);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	Если РезультатЗапроса[КоличествоТаблиц - 1].Пустой() Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствуют плановые этапы перевозки! Редактирование цепи поставок невозможно.'");
	КонецЕсли;
	
	ВыборкаЭтапы         = РезультатЗапроса[КоличествоТаблиц - 1].Выбрать();
	ВыборкаГрузы         = РезультатЗапроса[КоличествоТаблиц - 2].Выбрать();
	ВыборкаГрузыПоЭтапам = РезультатЗапроса[КоличествоТаблиц - 3].Выбрать();
	ВыборкаТочки         = РезультатЗапроса[КоличествоТаблиц - 4].Выбрать();
	ВыборкаШапка         = РезультатЗапроса[КоличествоТаблиц - 5].Выбрать();
	ВыборкаШапка.Следующий();
	
	ЭтаФорма.РежимКорректировки = Ложь;
	ЭтаФорма.РежимНеВводятсяГрузы = (ВыборкаШапка.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится);
	
	Пока ВыборкаЭтапы.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаЭтапы.АдресОтправления) Или Не ЗначениеЗаполнено(ВыборкаЭтапы.АдресПолучения) Тогда
			ВызватьИсключение НСтр("ru = 'Для редактирования цепи поставок не должно быть этапов с неуказанными адресами!'");
		КонецЕсли; 
		
		НовыйЭтап = ЭтапыПеревозки.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйЭтап, ВыборкаЭтапы); 
		НовыйЭтап.ПредставлениеСтроки = ПолучитьПредставлениеЭтапа(НовыйЭтап);
		НовыйЭтап.ИндексРебраГрафа = ПолучитьИндексРебраГрафа();
		Если Не РежимКорректировки И ЗначениеЗаполнено(НовыйЭтап.ЗаданиеНаПеревозку) Тогда
			РежимКорректировки = Истина;
		КонецЕсли; 
	КонецЦикла; 
	
	Пока ВыборкаГрузы.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ГрузовыеМеста.Добавить(), ВыборкаГрузы);  
	КонецЦикла;
	
	Пока ВыборкаГрузыПоЭтапам.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ГрузовыеМестаЭтапов.Добавить(), ВыборкаГрузыПоЭтапам);  
	КонецЦикла;
	
	мсвВидовАдресов = Новый Массив;
	максВершина = 0;
	Пока ВыборкаТочки.Следующий() Цикл
		Если ВыборкаТочки.КоличествоРазличных > 1 Тогда
			ЭтаФорма.ЗапрещенаРаботаССетевымГрафом = Истина;
			//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(СтрШаблон("ru = 'Обнаружены нарушения в структуре сетевого графа. Вершина с номером %1 назначена разным адресам! Интерактивная работа с графом будет запрещена.'", ВыборкаТочки.ИндексВершиныГрафа)));
			ВызватьИсключение НСтр(СтрШаблон("ru = 'Обнаружены нарушения в структуре сетевого графа. Вершина с номером %1 назначена разным адресам! Редактирование цепи поставок невозможно.'", ВыборкаТочки.ИндексВершиныГрафа));
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(ТочкиПеревозки.Добавить(), ВыборкаТочки);  
		мсвВидовАдресов.Добавить(ВыборкаТочки.ВидАдреса);
		максВершина = Макс(максВершина, ВыборкаТочки.ИндексВершиныГрафа);
	КонецЦикла;
	МаксИндексВершиныГрафа = максВершина;
	
	//Если ЭтаФорма.ЗапрещенаРаботаССетевымГрафом Тогда
	//	ИзменитьДоступностьЭлементов();
	//Иначе
	Если ИспользуютсяКартинкиДляВершинНаГрафе(ПараметрыНастроекФормы) Тогда
		мсвВидовАдресов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВидовАдресов);
		ОбновитьСписокМаркеровНаСервере(мсвВидовАдресов, Истина);	
	КонецЕсли; 
	//КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ДобавитьГрузовоеМестоНаСервере(НовоеГрузовоеМесто)
	
	мсвИзмененныеЭтапы = Новый Массив;
	
	ИскомыеГрузы = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", НовоеГрузовоеМесто));
	Если ИскомыеГрузы.Количество() Тогда
		РедактируемаяСтрока = ИскомыеГрузы[0];
	Иначе
		РедактируемаяСтрока = ГрузовыеМеста.Добавить();
		РедактируемаяСтрока.КоличествоГрузов = 1;
		РедактируемаяСтрока.ГрузовоеМесто = НовоеГрузовоеМесто;
	КонецЕсли; 
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НовоеГрузовоеМесто, Новый Структура("Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, ИндексКартинки", "Масса", "Объем", "КоличествоМест", "КоличествоЗагрузочныхМетров", "ТипГруза.ВидГруза.Порядок"));
	ЗаполнитьЗначенияСвойств(РедактируемаяСтрока, сткРеквизиты);
	
	Если РежимНеВводятсяГрузы Тогда
		РедактируемаяСтрока.Пометка = Истина;
		мсвИзмененныеЭтапы = ПеренестиВыбранныйГрузВЭтапыНаСервере(Ложь);
		РедактируемаяСтрока.Пометка = Ложь; 
	КонецЕсли; 
	Модифицированность = Истина;
	
	Возврат мсвИзмененныеЭтапы;
	
КонецФункции

&НаСервере
Функция ИзменитьВГХГрузовогоМестаНаСервере(ГрузовоеМесто)
	
	мсвИзмененныеЭтапы = Новый Массив;
	
	ИскомыеГрузы = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", ГрузовоеМесто));
	Если ИскомыеГрузы.Количество() Тогда
		РедактируемаяСтрока = ИскомыеГрузы[0];
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ГрузовоеМесто, Новый Структура("Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, ИндексКартинки", "Масса", "Объем", "КоличествоМест", "КоличествоЗагрузочныхМетров", "ТипГруза.ВидГруза.Порядок"));
		ЗаполнитьЗначенияСвойств(РедактируемаяСтрока, сткРеквизиты);
		РедактируемаяСтрока.Пометка = Истина;
		мсвИзмененныеЭтапы = ОбновитьПараметрыГрузовогоМеста();
		РедактируемаяСтрока.Пометка = Ложь; 
		Модифицированность = Истина;
	КонецЕсли; 
	
	Возврат мсвИзмененныеЭтапы;
		
КонецФункции

&НаСервере
Процедура ВыполнитьПервоначальноеЗаполнениеПоказателейЗагрузки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест);
	Если УчитыватьМассу Тогда
		ЭтаФорма.ЕИ_Масса = сткПредставления.Масса;
		прМасса = НСтр(СтрШаблон("ru = 'Масса (%1)'", сткПредставления.Масса));
		Элементы.ГрузовыеМестаМасса.Заголовок = прМасса;
		Элементы.ГрузовыеМестаЭтаповМасса.Заголовок = прМасса;
		Элементы.ЭтапыПеревозкиМасса.Заголовок = прМасса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ЭтаФорма.ЕИ_Объем = сткПредставления.Объем;
		прОбъем = НСтр(СтрШаблон("ru = 'Объем (%1)'",  сткПредставления.Объем));
		Элементы.ГрузовыеМестаОбъем.Заголовок = прОбъем;
		Элементы.ГрузовыеМестаЭтаповОбъем.Заголовок = прОбъем;
		Элементы.ЭтапыПеревозкиОбъем.Заголовок = прОбъем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		ЭтаФорма.ЕИ_Грузов = сткПредставления.КоличествоМест;
		прКоличествоМест = НСтр(СтрШаблон("ru = 'Мест (%1)'", сткПредставления.КоличествоМест));
		Элементы.ГрузовыеМестаКоличествоМест.Заголовок = прКоличествоМест;
		Элементы.ГрузовыеМестаЭтаповКоличествоМест.Заголовок = прКоличествоМест;
		Элементы.ЭтапыПеревозкиКоличествоМест.Заголовок = прКоличествоМест;
	КонецЕсли;
		
КонецПроцедуры // ВыполнитьПервоначальноеЗаполнениеПоказателейЗагрузки()

&НаСервере
Функция ПолучитьВерсиюЗаявки()
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаНаПеревозку, "ВерсияДанных");
	
КонецФункции

&НаСервере
Процедура ИзменитьДоступностьЭлементов()
	
	Если ЭтаФорма.ЗапрещенаРаботаССетевымГрафом Тогда
		Элементы.HTMLСетевойГраф.Видимость              = Ложь;
		Элементы.ФормаДобавитьТочкуНаГрафе.Видимость    = Ложь;
		Элементы.ФормаСоздатьЭтапНаГрафе.Видимость      = Ложь;
		Элементы.ФормаУдалитьВыбранноеНаГрафе.Видимость = Ложь;	
		Элементы.СтабилизироватьГраф.Видимость          = Ложь;	
	КонецЕсли; 
	
	Если ЭтаФорма.РежимНеВводятсяГрузы Тогда
		Элементы.ЭтапыПеревозкиКоманднаяПанель.Видимость = Ложь;
		Элементы.ЭтапыПеревозкиПометка.Видимость = Ложь;
		Элементы.ЭтапыПеревозкиВидПеревозки.Видимость = Ложь;
		Элементы.ГруппаГрузовыеМестаЭтапов.Видимость = Ложь;
		Элементы.ГрузовыеМестаКомандыВводятсяГрузы.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокМаркеровНаСервере(МассивВидовАдресов, ОбновлятьПолностью)
	
	Если ОбновлятьПолностью Тогда
		СписокМаркеровВидовАдресов.Очистить();
		Если МассивВидовАдресов.Количество() = 0 Тогда
			Для каждого текТочка Из ТочкиПеревозки Цикл
				МассивВидовАдресов.Добавить(текТочка.ВидАдреса);
			КонецЦикла;
			МассивВидовАдресов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивВидовАдресов);
		КонецЕсли; 
		
	Иначе
		ИспользованныеВиды = СписокМаркеровВидовАдресов.Выгрузить(, "ВидАдреса").ВыгрузитьКолонку("ВидАдреса");
		МассивВидовАдресов = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивВидовАдресов, ИспользованныеВиды);
		Если МассивВидовАдресов.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтилиМаркеров.Ссылка КАК Стиль,
	|	упСтилиМаркеров.Картинка КАК Картинка,
	|	упСтилиМаркеров.ХранилищеЗначения КАК ХранилищеЗначения
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	упСтилиМаркеров.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.Основной)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упВидыАдресов.Ссылка КАК ВидАдреса,
	|	упВидыАдресов.СтильВершиныГрафа КАК СтильВершиныГрафа,
	|	упВидыАдресов.СтильВершиныГрафа.Картинка КАК КартинкаВершиныГрафа,
	|	упВидыАдресов.СтильВершиныГрафа.ХранилищеЗначения КАК ХранилищеЗначенияВершиныГрафа
	|ИЗ
	|	Справочник.упВидыАдресов КАК упВидыАдресов
	|ГДЕ
	|	упВидыАдресов.Ссылка В(&ВидыАдресов)";
	Запрос.УстановитьПараметр("ВидыАдресов", МассивВидовАдресов);
	Результат = Запрос.ВыполнитьПакет();
	МаркерПоУмолчанию = Результат[0].Выбрать();
	МаркерПоУмолчанию.Следующий();
	
	Если Не Результат[1].Пустой() Тогда
		Выборка = Результат[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокМаркеровВидовАдресов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если ЗначениеЗаполнено(Выборка.СтильВершиныГрафа) Тогда
				Если Выборка.КартинкаВершиныГрафа = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаВершиныГрафа = Выборка.ХранилищеЗначенияВершиныГрафа.Получить();
				Иначе
					НоваяСтрока.КартинкаВершиныГрафа = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаВершиныГрафа);
					НоваяСтрока.ЭтоКартинкаВершиныГрафа = Истина;
				КонецЕсли;
			Иначе
				НоваяСтрока.СтильВершиныГрафа = МаркерПоУмолчанию.Стиль;
				Если МаркерПоУмолчанию.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаВершиныГрафа = МаркерПоУмолчанию.ХранилищеЗначения.Получить();
				Иначе
					НоваяСтрока.КартинкаВершиныГрафа = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(МаркерПоУмолчанию.Картинка);
					НоваяСтрока.ЭтоКартинкаВершиныГрафа = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСписокМаркеровНаСервере()

&НаКлиенте
Процедура ОбновитьДанныеОтображенияГрафа()
	
	УстановитьПараметрыГрафа();
	ОтобразитьСетевойГраф();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭтапПеревозки(ДанныеНовогоЭтапа)
	
	сткИдентификаторы = ДобавитьЭтапПеревозкиНаСервере(ДанныеНовогоЭтапа);
	
	ТекущийЭтап = ЭтапыПеревозки.НайтиПоИдентификатору(сткИдентификаторы.ИдентификаторЭтапа);
	ИзменитьРеброНаГрафе(ТекущийЭтап);
	
	// при добавлении нового этапа статус вершин, связанных с этапом, может измениться
	ТочкаОтправления = ТочкиПеревозки.НайтиПоИдентификатору(сткИдентификаторы.ИдентификаторОтправления);
	ДанныеВершиныГрафа = Неопределено;
	ИскомыеСтроки = СписокМаркеровВидовАдресов.НайтиСтроки(Новый Структура("ВидАдреса", ТочкаОтправления.ВидАдреса));
	Если ИскомыеСтроки.Количество() Тогда
		текМаркерВидаАдреса = ИскомыеСтроки[0];
		ОбработатьМаркер(текМаркерВидаАдреса, "ВершиныГрафа");
		ДанныеВершиныГрафа = текМаркерВидаАдреса.ДанныеВершиныГрафа;
	КонецЕсли;
	ИзменитьВершинуГрафа(ТочкаОтправления, ДанныеВершиныГрафа, Новый Массив, Новый Массив, Новый Массив, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТочкаОтправления.ИндексВершиныГрафа));
	
	ТочкаПолучения = ТочкиПеревозки.НайтиПоИдентификатору(сткИдентификаторы.ИдентификаторПолучения);
	Если ТочкаОтправления.ВидАдреса <> ТочкаПолучения.ВидАдреса Тогда
		ДанныеВершиныГрафа = Неопределено;
		ИскомыеСтроки = СписокМаркеровВидовАдресов.НайтиСтроки(Новый Структура("ВидАдреса", ТочкаПолучения.ВидАдреса));
		Если ИскомыеСтроки.Количество() Тогда
			текМаркерВидаАдреса = ИскомыеСтроки[0];
			ОбработатьМаркер(текМаркерВидаАдреса, "ВершиныГрафа");
			ДанныеВершиныГрафа = текМаркерВидаАдреса.ДанныеВершиныГрафа;
		КонецЕсли;
	КонецЕсли;
	ИзменитьВершинуГрафа(ТочкаПолучения, ДанныеВершиныГрафа, Новый Массив, Новый Массив, Новый Массив, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТочкаПолучения.ИндексВершиныГрафа));
	ИзменитьКомандуСозданияРеберГрафа();
	
КонецПроцедуры

&НаСервере
Функция ДобавитьЭтапПеревозкиНаСервере(ДанныеНовогоЭтапа);
	
	НовыйЭтап = ЭтапыПеревозки.Добавить();
	НовыйЭтап.НоваяСтрока             = Истина;
	НовыйЭтап.НомерВЦепиПоставок      = ДанныеНовогоЭтапа.НомерВЦепиПоставок;
	НовыйЭтап.НомерВЦепиПоставокКонец = ДанныеНовогоЭтапа.НомерВЦепиПоставокКонец;
	НовыйЭтап.ПредставлениеСтроки     = ПолучитьПредставлениеЭтапа(НовыйЭтап);
	НовыйЭтап.ИндексРебраГрафа        = ДанныеНовогоЭтапа.ИндексРебраГрафа;
	
	ТочкаОтправления = ТочкиПеревозки.НайтиСтроки(Новый Структура("ИндексВершиныГрафа", НовыйЭтап.НомерВЦепиПоставок))[0];
	НовыйЭтап.АдресОтправления = ТочкаОтправления.Адрес;
	ТочкаПолучения = ТочкиПеревозки.НайтиСтроки(Новый Структура("ИндексВершиныГрафа", НовыйЭтап.НомерВЦепиПоставокКонец))[0];	
	НовыйЭтап.АдресПолучения = ТочкаПолучения.Адрес;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АдресОтправления" , НовыйЭтап.АдресОтправления);
	Запрос.УстановитьПараметр("АдресПолучения"   , НовыйЭтап.АдресПолучения);
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозку", ЭтаФорма.ЗаявкаНаПеревозку);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГруза.ВидПеревозки.ТипПеревозки КАК ТипПеревозки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упЗаявкаНаПеревозкуГруза.ВидПеревозки.ТипПеревозки, ЗНАЧЕНИЕ(Перечисление.упТипыПеревозок.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упТипыПеревозок.ПустаяСсылка)
	|			ТОГДА 10
	|		ИНАЧЕ упЗаявкаНаПеревозкуГруза.ВидПеревозки.ТипПеревозки.Порядок
	|	КОНЕЦ КАК ИндексКартинкиСтрок,
	|	упЗаявкаНаПеревозкуГруза.ЧасовойПояс КАК ЧасовойПояс,
	|	упЗаявкаНаПеревозкуГруза.Приоритет КАК Приоритет,
	|	упЗаявкаНаПеревозкуГруза.Подразделение КАК Подразделение,
	|	упЗаявкаНаПеревозкуГруза.Заказчик КАК Заказчик,
	|	упЗаявкаНаПеревозкуГруза.ВидПеревозки КАК ВидПеревозки,
	|	упЗаявкаНаПеревозкуГруза.Организация КАК Организация
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &ЗаявкаНаПеревозку
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упАдреса.ГеографическаяЗона КАК ГеографическаяЗона
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Ссылка = &АдресОтправления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упАдреса.ГеографическаяЗона КАК ГеографическаяЗона
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Ссылка = &АдресПолучения";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличестовТаблиц = РезультатЗапроса.Количество();
	ВыборкаЗонаПолучения = РезультатЗапроса[КоличестовТаблиц - 1].Выбрать();
	Если ВыборкаЗонаПолучения.Следующий() Тогда
		НовыйЭтап.ЗонаПолучения = ВыборкаЗонаПолучения.ГеографическаяЗона;
	КонецЕсли;
	ВыборкаЗонаОтправления = РезультатЗапроса[КоличестовТаблиц - 2].Выбрать();
	Если ВыборкаЗонаОтправления.Следующий() Тогда
		НовыйЭтап.ЗонаОтправления = ВыборкаЗонаОтправления.ГеографическаяЗона;
	КонецЕсли;
	ВыборкаДанные = РезультатЗапроса[КоличестовТаблиц - 3].Выбрать();
	Если ВыборкаДанные.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НовыйЭтап, ВыборкаДанные);
	КонецЕсли;
	
	Если РежимНеВводятсяГрузы Тогда
		Для каждого текГруз Из ГрузовыеМеста Цикл
			НоваяСтрока = ГрузовыеМестаЭтапов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текГруз);
			НоваяСтрока.НомерВЦепиПоставок      = НовыйЭтап.НомерВЦепиПоставок;
			НоваяСтрока.НомерВЦепиПоставокКонец = НовыйЭтап.НомерВЦепиПоставокКонец;
		КонецЦикла;
		ОбновитьПараметрыГрузовПоЭтапуПеревозки(НовыйЭтап.ПолучитьИдентификатор());
		
	ИначеЕсли ПараметрыНастроекФормы.АвтоматическиОпределятьГрузовойСоставНовогоЭтапа Тогда
		тзИскомыеПредшественники = ГрузовыеМестаЭтапов.Выгрузить(Новый Структура("НомерВЦепиПоставокКонец", НовыйЭтап.НомерВЦепиПоставок));
		Если тзИскомыеПредшественники.Количество() Тогда
			тзИскомыеПредшественники.Свернуть("ГрузовоеМесто, Тара, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров", "КоличествоГрузов");
			Для каждого текСтрока Из тзИскомыеПредшественники Цикл
				НоваяСтрока = ГрузовыеМестаЭтапов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
				НоваяСтрока.НомерВЦепиПоставок      = НовыйЭтап.НомерВЦепиПоставок;
				НоваяСтрока.НомерВЦепиПоставокКонец = НовыйЭтап.НомерВЦепиПоставокКонец;
				
				Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяКоличественныйУчетГрузов") Тогда
					НоваяСтрока.КоличествоГрузов = 1;
				КонецЕсли; 
			КонецЦикла;
			ОбновитьПараметрыГрузовПоЭтапуПеревозки(НовыйЭтап.ПолучитьИдентификатор());
		КонецЕсли; 
	КонецЕсли; 
	Модифицированность = Истина;
	
	Возврат Новый Структура("ИдентификаторЭтапа, ИдентификаторОтправления, ИдентификаторПолучения", НовыйЭтап.ПолучитьИдентификатор(), ТочкаОтправления.ПолучитьИдентификатор(), ТочкаПолучения.ПолучитьИдентификатор());
	
КонецФункции

&НаСервере
Функция ПолучитьПредставлениеЭтапа(СтрокаЭтап)
	
	Возврат НСтр(СтрШаблон("ru = '%1 - %2'", СтрокаЭтап.НомерВЦепиПоставок, СтрокаЭтап.НомерВЦепиПоставокКонец));
	
КонецФункции

&НаКлиенте
Функция ПолучитьПредставлениеЭтапаНаКлиенте(СтрокаЭтап)
	
	Возврат НСтр(СтрШаблон("ru = '%1 - %2'", СтрокаЭтап.НомерВЦепиПоставок, СтрокаЭтап.НомерВЦепиПоставокКонец));
	
КонецФункции

&НаСервере
Функция ПолучитьИндексРебраГрафа()
	
	Возврат Строка(Новый УникальныйИдентификатор);
	
КонецФункции	

&НаКлиенте
Функция ПолучитьЛегендуРебраЭтапа(СтрокаЭтап)
	
	текТекстЛегенды = "";
	
	Если СтрокаЭтап.КоличествоГрузов > 0 Тогда
		Текст = СтрШаблон("ru = 'Грузов: %1\n%2%3%4%5'",
						СтрокаЭтап.КоличествоГрузов,
						?(УчитыватьМассу И СтрокаЭтап.Масса > 0, "Масса: " + Строка(СтрокаЭтап.Масса) + " " + ЭтаФорма.ЕИ_Масса + "\n", ""),
						?(УчитыватьОбъем И СтрокаЭтап.Объем > 0, "Объем: " + Строка(СтрокаЭтап.Объем) + " " + ЭтаФорма.ЕИ_Объем + "\n", ""),
						?(УчитыватьКоличествоМест И СтрокаЭтап.КоличествоМест > 0, "Мест: " + Строка(СтрокаЭтап.КоличествоМест) + " " + ЭтаФорма.ЕИ_Грузов + "\n", ""),
						?(УчитыватьКоличествоЗагрузочныхМетров И СтрокаЭтап.КоличествоЗагрузочныхМетров > 0, "Метров: " + Строка(СтрокаЭтап.КоличествоЗагрузочныхМетров) + "\n", ""));
		текТекстЛегенды = НСтр(Текст);					
	КонецЕсли;
	
	Возврат текТекстЛегенды;
	
КонецФункции

&НаКлиенте
Функция ПолучитьЦветГраницыВершиныГрафа(СтрокаТочка, МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин)
	
	текЦвет = "#2b7ce9"; // цвет по умолчанию (светло-синий)
	Если Ложь
		Или Не МассивОбычныхВершин.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено
		Или (Не МассивОтмененныхВершин.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено И Не МассивВершинСЗаданиями.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено)
	Тогда
		Возврат текЦвет;
	КонецЕсли;
	
	Если Не МассивОтмененныхВершин.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено Тогда
		Возврат "#FF0000"; // красный
	КонецЕсли;	
	
	Если Не МассивВершинСЗаданиями.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено Тогда
		Возврат "#C0C0C0"; // серый недоступный;
	КонецЕсли; 
	
	Если Не МассивВершинСВыполняющимсяЗаданиями.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено Тогда
		Возврат "#339966"; // зеленый (для выполняющихся заданий)
	КонецЕсли;
	
	Возврат текЦвет;
	
КонецФункции
		
&НаКлиенте
Функция РазрешитьВыборВершиныНаГрафе(СтрокаТочка, МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин)
	
	Возврат ?(Не МассивВершинСЗаданиями.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено
				И Не МассивВершинСВыполняющимсяЗаданиями.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено
				И МассивОтмененныхВершин.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено
				И МассивОбычныхВершин.Найти(СтрокаТочка.ИндексВершиныГрафа) = Неопределено, "false", "true");
	
КонецФункции

&НаКлиенте
Функция ПолучитьЦветРебраЭтапа(СтрокаЭтап)
	
	текЦвет = "#2b7ce9"; // цвет по умолчанию (светло-синий)
	Если ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку) Тогда
		Если ЗаданиеВыполняется(СтрокаЭтап.СтатусЗадания) Тогда
			текЦвет = "#339966"; // зеленый (для выполняющихся заданий)			
		Иначе
			текЦвет = "#C0C0C0"; // серый недоступный
		КонецЕсли;
	ИначеЕсли СтрокаЭтап.Отменен Тогда
		текЦвет = "#FF0000"; // красный
	КонецЕсли; 
	
	Возврат текЦвет;
	
КонецФункции

&НаКлиенте
Функция РазрешитьВыборЭтапаНаГрафе(СтрокаЭтап)
	
	 Возврат ?(ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку), "false", "true");
	
КонецФункции

&НаКлиенте
Функция СкрытьЭтапНаГрафе(СтрокаЭтап)
	
	 Возврат ?(Не ПараметрыНастроекФормы.ПоказыватьОтмененныеЭтапыПеревозкиНаГрафе И СтрокаЭтап.Отменен, "true", "false");
	
 КонецФункции
 
&НаКлиенте
Функция ПолучитьСтатусЭтапа(СтрокаЭтап)
	
	СтатусЭтапа = "new";
	Если СтрокаЭтап.Отменен Тогда
		СтатусЭтапа = "cancelled";
	ИначеЕсли ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку) Тогда
		Если ЗаданиеВыполняется(СтрокаЭтап.СтатусЗадания) Тогда
			СтатусЭтапа = "executed";
		Иначе
			СтатусЭтапа = "planned";
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат СтатусЭтапа;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаданиеВыполняется(СтатусЗадания)
	
	Возврат Не (Ложь
			Или СтатусЗадания = ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое")
			Или СтатусЗадания = ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию")
			Или СтатусЗадания = ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс"));
	
КонецФункции

#Область РаботаСКартой

&НаКлиенте
Функция ИзменитьФорматВременногоФайла(Имя)
	
	Возврат "file://" + СтрЗаменить(Имя,"\","/");
	
КонецФункции // ИзменитьФорматВременногоФайла()

&НаКлиенте
Процедура ОтобразитьСетевойГраф()
	
	ИспользуютсяКартинки = ИспользуютсяКартинкиДляВершинНаГрафе(ПараметрыНастроекФормы);
	Если ИспользуютсяКартинки Тогда
		ОбновитьСписокМаркеровНаКлиенте();
		Если ТочкиПеревозки.Количество() Тогда
			соотДанные = Новый Соответствие;
			Для каждого текСтрока Из СписокМаркеровВидовАдресов Цикл
				соотДанные.Вставить(текСтрока.ВидАдреса, Новый Структура("ДанныеВершиныГрафа", текСтрока.ДанныеВершиныГрафа));
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли; 

	// вспомогательные массивы для определения состояния вершин
	МассивОтмененныхВершин = Новый Массив;
	МассивВершинСЗаданиями = Новый Массив;
	МассивВершинСВыполняющимсяЗаданиями = Новый Массив;
	МассивОбычныхВершин = Новый Массив;
	
	МассивДанныхРебер = Новый Массив;
	Для каждого СтрокаЭтап Из ЭтапыПеревозки Цикл
		МассивДанныхРебер.Добавить(СтрокаЭтап.ИндексРебраГрафа); // id
		МассивДанныхРебер.Добавить(ПолучитьЛегендуРебраЭтапа(СтрокаЭтап)); // title
		МассивДанныхРебер.Добавить(ПредставлениеЧисла(СтрокаЭтап.НомерВЦепиПоставок)); //from
		МассивДанныхРебер.Добавить(ПредставлениеСтроки(СтрокаЭтап.НомерВЦепиПоставокКонец)); // to
		МассивДанныхРебер.Добавить(ПолучитьЦветРебраЭтапа(СтрокаЭтап)); // color
		МассивДанныхРебер.Добавить(?(СтрокаЭтап.КоличествоГрузов > 0, "false", "true")); // useDashes: ребро этапа без грузов рисуем пунктирной линией
		МассивДанныхРебер.Добавить(РазрешитьВыборЭтапаНаГрафе(СтрокаЭтап)); // chosen
		МассивДанныхРебер.Добавить(СкрытьЭтапНаГрафе(СтрокаЭтап)); // hidden
		МассивДанныхРебер.Добавить(ПолучитьСтатусЭтапа(СтрокаЭтап)); // state
		
		Если СтрокаЭтап.Отменен Или ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку) Тогда
			Если СтрокаЭтап.Отменен Тогда
				МассивОтмененныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
				МассивОтмененныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаЭтап.ЗаданиеНаПеревозку) Тогда
				Если ЗаданиеВыполняется(СтрокаЭтап.СтатусЗадания) Тогда
					МассивВершинСВыполняющимсяЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
					МассивВершинСВыполняющимсяЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);
				Иначе
					МассивВершинСЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
					МассивВершинСЗаданиями.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);
				КонецЕсли;
			КонецЕсли;
		Иначе
			МассивОбычныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставок);
			МассивОбычныхВершин.Добавить(СтрокаЭтап.НомерВЦепиПоставокКонец);
		КонецЕсли; 
	КонецЦикла;
	
	МассивДанныхВершин = Новый Массив;
	Для каждого СтрокаТочка Из ТочкиПеревозки Цикл
		МассивДанныхВершин.Добавить(ПредставлениеЧисла(СтрокаТочка.ИндексВершиныГрафа)); // id 
		МассивДанныхВершин.Добавить(ПредставлениеСтроки(СтрокаТочка.ПредставлениеАдреса));  // title
		Если ИспользуютсяКартинки Тогда
			ИскомаяСтрока = соотДанные.Получить(СтрокаТочка.ВидАдреса);
			МассивДанныхВершин.Добавить(ИскомаяСтрока.ДанныеВершиныГрафа); // image
		Иначе
			МассивДанныхВершин.Добавить(Null); // image
		КонецЕсли;
		МассивДанныхВершин.Добавить(ПолучитьЦветГраницыВершиныГрафа(СтрокаТочка, МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин)); // borderColor
		МассивДанныхВершин.Добавить(РазрешитьВыборВершиныНаГрафе(СтрокаТочка, МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин)); // chosen
	КонецЦикла;
	
	ВыполнитьПроцедуруHTML("createNetwork(""" + СтрСоединить(МассивДанныхВершин, ";") + ";" + """, """ + СтрСоединить(МассивДанныхРебер, ";") + ";" + """)");
	
КонецПроцедуры 

&НаКлиенте
Процедура ИзменитьВершинуГрафа(СтрокаТочка, Знач ДанныеВершиныГрафа,  МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин)
	
	Если Не ЗначениеЗаполнено(ДанныеВершиныГрафа) Тогда
		ДанныеВершиныГрафа = null;	
	КонецЕсли; 
	
	ВыполнитьПроцедуруHTML("editGraphNode(""" + СтрокаТочка.ИндексВершиныГрафа + """, """ // id
			+ ПредставлениеСтроки(СтрокаТочка.ПредставлениеАдреса) + """, """         // title
			+ ДанныеВершиныГрафа + """, """                                             // image
			+ ПолучитьЦветГраницыВершиныГрафа(СтрокаТочка, МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин) + """, " // borderColor
			+ РазрешитьВыборВершиныНаГрафе(СтрокаТочка, МассивОтмененныхВершин, МассивВершинСЗаданиями, МассивВершинСВыполняющимсяЗаданиями, МассивОбычныхВершин) + ")"); // chosen
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеброНаГрафе(СтрокаЭтап)
	
	ВыполнитьПроцедуруHTML("editGraphEdge(""" + СтрокаЭтап.ИндексРебраГрафа + """, """ // id
			+ ПолучитьЛегендуРебраЭтапа(СтрокаЭтап) + """, "                         // title
			+ ПредставлениеЧисла(СтрокаЭтап.НомерВЦепиПоставок) + ", "               // from
			+ ПредставлениеСтроки(СтрокаЭтап.НомерВЦепиПоставокКонец) + ", """       // to
			+ ПолучитьЦветРебраЭтапа(СтрокаЭтап) + """, "                            // color
			+ ?(СтрокаЭтап.КоличествоГрузов > 0, "false", "true") + ", "             // useDashes
			+ РазрешитьВыборЭтапаНаГрафе(СтрокаЭтап) + ", "                          // chosen
			+ СкрытьЭтапНаГрафе(СтрокаЭтап) + ", """                                 // hidden
			+ ПолучитьСтатусЭтапа(СтрокаЭтап) + """)");                              // state  

КонецПроцедуры

&НаКлиенте
Процедура УдалитьВершинуГрафа(Идентификатор)
	
	ВыполнитьПроцедуруHTML("removeNode(""" + Идентификатор + """)");

КонецПроцедуры

&НаКлиенте
Процедура УдалитьРеброГрафа(Идентификатор)

	ВыполнитьПроцедуруHTML("removeEdge(""" + Идентификатор + """)");

КонецПроцедуры

&НаКлиенте
Процедура ВключитьРежимСозданияРеберГрафа()
	
	ВыполнитьПроцедуруHTML("enableGraphEdgeMode()");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыключитьРежимСозданияРеберГрафа()
	
	ВыполнитьПроцедуруHTML("disableGraphEdgeMode()");
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНовыеРебра()
	
	мсвСтруктур = Новый Массив;
	мсвНомеров = Новый Массив;
	СтрокаНомеров = ВыполнитьФункциюHTML("getAddedEdges()");
	Если ЗначениеЗаполнено(СтрокаНомеров) Тогда
		мсвНомеров = СтрРазделить(СтрокаНомеров, ";");
	КонецЕсли; 
	
	Если мсвНомеров.Количество() Тогда
		Сч = 0;
		Пока Сч < мсвНомеров.Количество()/3 Цикл
			мсвСтруктур.Добавить(Новый Структура("ИндексРебраГрафа, НомерВЦепиПоставок, НомерВЦепиПоставокКонец", мсвНомеров[Сч*3], мсвНомеров[Сч*3 + 1], мсвНомеров[Сч*3 + 2]));
			Сч = Сч + 1;
		КонецЦикла;		
	КонецЕсли;  
	
	Возврат мсвСтруктур;

КонецФункции // ПолучитьНовыеРебра()

&НаКлиенте
Процедура ПоказатьСообщенияПользователю()
	
	мсвУведомлений = Новый Массив;
	СтрокаУведомлений = ВыполнитьФункциюHTML("getNotifications()");
	Если ЗначениеЗаполнено(СтрокаУведомлений) Тогда
		мсвУведомлений = СтрРазделить(СтрокаУведомлений, "//");
	КонецЕсли; 
	
	Если мсвУведомлений.Количество() Тогда
		Для каждого текУведомление Из мсвУведомлений Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(текУведомление));
		КонецЦикла;
		
		ИзменитьКомандуСозданияРеберГрафа();
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКомандуСозданияРеберГрафа()
	
	Если ЭтаФорма.РежимДобавленияЭтапаНаГрафе Тогда
		ЭтаФорма.РежимДобавленияЭтапаНаГрафе = Ложь;
		Элементы.ФормаСоздатьЭтапНаГрафе.Картинка = БиблиотекаКартинок.СоздатьЭлементСписка;
		Элементы.ФормаСоздатьЭтапНаГрафе.Пометка = Ложь;
		ВыключитьРежимСозданияРеберГрафа();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьВыделенныеРебраСГрафа()
	
	мсвИдентификаторов = Новый Массив;
	СтрокаИдентификаторов = ВыполнитьФункциюHTML("getSelectedEdgesFromGraph()");
	Если ЗначениеЗаполнено(СтрокаИдентификаторов) Тогда
		мсвИдентификаторов = СтрРазделить(СтрокаИдентификаторов, ";");
	КонецЕсли;
	
	Возврат мсвИдентификаторов;

КонецФункции

&НаКлиенте
Процедура ОбновитьСписокМаркеровНаКлиенте()
	
	Для каждого текВидАдреса Из СписокМаркеровВидовАдресов Цикл
		Если текВидАдреса.ИндексВМассивеВершиныГрафа = 0 Тогда
			ОбработатьМаркер(текВидАдреса, "ВершиныГрафа");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ОбновитьСписокМаркеровНаКлиенте()

&НаКлиенте
Процедура ОбработатьМаркер(ЭлементКоллекции, Префикс = "")
	#Если НЕ ВебКлиент Тогда
	Стиль = "";
	текКартинка = ЭлементКоллекции["Картинка" + Префикс];
	Если ЭлементКоллекции["ЭтоКартинка" + Префикс] Тогда
		мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Маркер",  текКартинка));
		Если мсвСохраненныйМаркер.Количество() Тогда 
			Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Файл) Тогда 
				Стиль = мсвСохраненныйМаркер[0].Файл;
			КонецЕсли;	
		Иначе	
			АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = текКартинка;
			НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			Стиль = НовыйСохраненныйМаркер.Файл; 
		КонецЕсли;	
	Иначе 
		мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Стиль",  ЭлементКоллекции["Стиль" + Префикс]));
		Если мсвСохраненныйМаркер.Количество() Тогда
			Стиль = мсвСохраненныйМаркер[0].Файл;
		Иначе
			АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");	
			ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Стиль = ЭлементКоллекции["Стиль" + Префикс];
			НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			Стиль = НовыйСохраненныйМаркер.Файл;
		КонецЕсли;
	КонецЕсли;
	ЭлементКоллекции["Данные" + Префикс] = Стиль;

	#КонецЕсли
КонецПроцедуры // ОбработатьМаркер()

&НаКлиенте
Процедура УстановитьПараметрыГрафа()
	
	ФигураВершиныГрафа = ПараметрыНастроекФормы.ФигураВершиныГрафа;
	Если ФигураВершиныГрафа = 1 Тогда
		ФигураВершиныГрафа = "ellipse";
	ИначеЕсли ФигураВершиныГрафа = 2 Тогда
		ФигураВершиныГрафа = "circle";
	ИначеЕсли ФигураВершиныГрафа = 3 Тогда
		ФигураВершиныГрафа = "box";
	ИначеЕсли ФигураВершиныГрафа = 4 Тогда
		ФигураВершиныГрафа = "image";
	Иначе
		ФигураВершиныГрафа = "circularImage";
	КонецЕсли; 
	
	ВыполнитьПроцедуруHTML("setGraphProperties(" + ?(ПараметрыНастроекФормы.ВключатьВЗаголовокВершиныНаГрафеНаименованиеАдреса, "true", "false")
												+ ", " + ?(ПараметрыНастроекФормы.ВыводитьИнформациюОПараметрахГрузовНаРебрахГрафа, "true", "false")
												+ ", " + ?(ПараметрыНастроекФормы.ИспользоватьИерархическоеПредставлениеГрафа, "true", "false")
												+ ", """ + ФигураВершиныГрафа
												+ """," + ?(РежимНеВводятсяГрузы, "true", "false") + ")");
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Нет.
// 
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml, "HTMLСетевойГраф");
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml, "HTMLСетевойГраф");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИспользуютсяКартинкиДляВершинНаГрафе(ПараметрыНастроекФормы)
	
	Возврат ПараметрыНастроекФормы.ФигураВершиныГрафа = 4 Или ПараметрыНастроекФормы.ФигураВершиныГрафа = 5; //фигура - картинка или картинка в круге

КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции // ПредставлениеЧисла()

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтроки(Знач Строка)
	
	Строка = СтрЗаменить(Строка, """", "");
	Строка = СтрЗаменить(Строка, ";", "");
	Возврат Строка;
	
КонецФункции // ПредставлениеСтроки()

#КонецОбласти

#Область ОбработчикиОжидания

// Нет.

#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров формы.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		Если ИспользуютсяКартинкиДляВершинНаГрафе(Результат) И Не ИспользуютсяКартинкиДляВершинНаГрафе(ПараметрыНастроекФормы) Тогда // нужно инициализировать картинки
			Если Не ЭтаФорма.ЗапрещенаРаботаССетевымГрафом Тогда
				//мсвВидовАдресов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВидовАдресов);
				ОбновитьСписокМаркеровНаСервере(Новый Массив, Истина);	
			КонецЕсли; 
		КонецЕсли; 
		
		ПараметрыНастроекФормы = Результат;
		упВариантыНастроекВызовСервера.СохранитьВариантНастроек(Результат, "Обработка.упРМПланированиеЗаданий.ФормаРедактированияЭтаповПеревозки.ПараметрыФормы", ТекущийПользователь);
		
		ОбновитьДанныеОтображенияГрафа();
	КонецЕсли;
    
КонецПроцедуры // УстановитьПараметрыЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы выбора адреса для вершина графа.
//
// Параметры:
//  Результат  - СправочникСсылка.упАдреса, Неопределено - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыбратьАдресДляВершиныГрафаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		МаксИндексВершиныГрафа = МаксИндексВершиныГрафа + 1;
		
		НоваяТочка = ТочкиПеревозки.Добавить();
		НоваяТочка.Адрес = Результат;
		НоваяТочка.ИндексВершиныГрафа = МаксИндексВершиныГрафа;
		сткРеквизиты = ПолучитьРеквизитыАдресаНаСервере(НоваяТочка.Адрес);
		НоваяТочка.ВидАдреса = сткРеквизиты.ВидАдреса;
		НоваяТочка.ПредставлениеАдреса = сткРеквизиты.ПредставлениеАдреса;
				
		ДанныеВершиныГрафа = Неопределено;
		Если ИспользуютсяКартинкиДляВершинНаГрафе(ПараметрыНастроекФормы) Тогда
			ОбновитьСписокМаркеровНаСервере(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоваяТочка.ВидАдреса), Ложь);
			ИскомыеСтроки = СписокМаркеровВидовАдресов.НайтиСтроки(Новый Структура("ВидАдреса", НоваяТочка.ВидАдреса));
			Если ИскомыеСтроки.Количество() Тогда
				текМаркерВидаАдреса = ИскомыеСтроки[0];
				ОбработатьМаркер(текМаркерВидаАдреса, "ВершиныГрафа");
				ДанныеВершиныГрафа = текМаркерВидаАдреса.ДанныеВершиныГрафа;
			КонецЕсли; 		
		КонецЕсли;  
		ИзменитьВершинуГрафа(НоваяТочка, ДанныеВершиныГрафа, Новый Массив, Новый Массив, Новый Массив,  ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоваяТочка.ИндексВершиныГрафа));
		СтабилизироватьГраф(Неопределено);
		ИзменитьКомандуСозданияРеберГрафа();
		
		Модифицированность = Истина;
	КонецЕсли;
    
КонецПроцедуры // ВыбратьАдресДляВершиныГрафаЗавершение()

&НаКлиенте
// Процедура вызывается при ответе на вопрос о снятии пометки отмены с этапа.
//
// Параметры:
//  Ответ 
//  ДополнительныеПараметры  - Структура.
//
Процедура _ПослеОтветаНаВопросОСнятииПометкиОтменыСЭтапа(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не Ответ = Неопределено Тогда
		Если Ответ = "Да" Тогда
			текЭтап = ЭтапыПеревозки.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторЭтапа);
			Если Не текЭтап = Неопределено Тогда
				текЭтап.Отменен = Ложь;
				ИзменитьРеброНаГрафе(текЭтап);
				ДополнительныеПараметры.мсвИдентификаторовТочек.Добавить(текЭтап.НомерВЦепиПоставок);
				ДополнительныеПараметры.мсвИдентификаторовТочек.Добавить(текЭтап.НомерВЦепиПоставокКонец);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при ответе на вопрос о непосредственном удалении этапов.
//
// Параметры:
//  Ответ 
//  ДополнительныеПараметры  - Структура.
//
Процедура ПослеОтветаНаВопросОНепосредственномУдаленииЭтапов(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не Ответ = Неопределено Тогда
		Если Ответ = "Да" Или Ответ = "УдалитьСТочками" Тогда
			мсвЭтапыКУдалению = ДополнительныеПараметры.ЭтапыКУдалению;
			Для каждого текИдентификаторЭтапа Из мсвЭтапыКУдалению Цикл
				текЭтап = ЭтапыПеревозки.НайтиПоИдентификатору(текИдентификаторЭтапа);
				УдалитьРеброГрафа(текЭтап.ИндексРебраГрафа);
				
				ИскомыеГрузы = ГрузовыеМестаЭтапов.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", текЭтап.НомерВЦепиПоставок, текЭтап.НомерВЦепиПоставокКонец));
				Для каждого текГруз Из ИскомыеГрузы Цикл
					ГрузовыеМестаЭтапов.Удалить(текГруз);
				КонецЦикла; 
				
				ЭтапыПеревозки.Удалить(текЭтап);
			КонецЦикла;		
			
			Если Ответ = "УдалитьСТочками" Тогда
				мсвТочкиКУдалению = Новый Массив;
				новМаксИндекс = 0;
				Для каждого текТочка Из ТочкиПеревозки Цикл
					Если Истина
						И ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок", текТочка.ИндексВершиныГрафа)).Количество() = 0
						И ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставокКонец", текТочка.ИндексВершиныГрафа)).Количество() = 0
					Тогда
						мсвТочкиКУдалению.Добавить(текТочка);
					Иначе
						новМаксИндекс = Макс(новМаксИндекс, текТочка.ИндексВершиныГрафа);
					КонецЕсли; 
					
				КонецЦикла; 
				
				Если мсвТочкиКУдалению.Количество() Тогда
					Для каждого текТочка Из мсвТочкиКУдалению Цикл
						УдалитьВершинуГрафа(текТочка.ИндексВершиныГрафа);
						ТочкиПеревозки.Удалить(текТочка)
					КонецЦикла; 
					
					ЭтаФорма.МаксИндексВершиныГрафа = новМаксИндекс;
				КонецЕсли; 
			КонецЕсли;			
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при ответе на вопрос об удалении грузовых мест.
//
// Параметры:
//  Ответ 
//  ДополнительныеПараметры  - Структура.
//
Процедура ПослеОтветаНаВопросОбУдаленииГрузовыхМест(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не Ответ = Неопределено Тогда
		Если Ответ = "Да" Тогда
			мсвИзмененныеЭтапы = УдалитьВыбранныйГрузИзЭтаповНаСервере(Ложь);
			УдалитьГрузовыеМестаНаСервере();
			
			Для каждого текЭтап Из мсвИзмененныеЭтапы Цикл
				ИзменитьРеброНаГрафе(ЭтапыПеревозки.НайтиПоИдентификатору(текЭтап));
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при ответе на вопрос о добавлении грузового места
//
// Параметры:
//  Ответ 
//  ДополнительныеПараметры  - Структура.
//
Процедура ПослеОтветаНаВопросОДобавленииГрузовогоМеста(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		сткПараметры = Новый Структура("ЗаявкаНаПеревозку, СчетчикГрузов", ЗаявкаНаПеревозку, ГрузовыеМеста.Количество() + 1);
		ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", сткПараметры, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после немодального ввода числового значения. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВводКоличестваГрузаВНовойСтрокеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		текСтрока = Элементы.ГрузовыеМеста.ТекущиеДанные;
		КоличествоСтарое = текСтрока.КоличествоГрузов;
		Количество = Результат;
		Отказ = Ложь;
		ТекстСообщения = "";
		ВводКоличестваГрузаОбработка(КоличествоСтарое, Количество, Отказ, ТекстСообщения);
		
		Если Отказ Тогда
			ПоказатьПредупреждение(,ТекстСообщения);
		Иначе
			текИндекс = ГрузовыеМеста.Индекс(текСтрока);
			НоваяСтрока = ГрузовыеМеста.Вставить(текИндекс + 1);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			
			НоваяСтрока.КоличествоГрузов = Количество;
			текСтрока.КоличествоГрузов = КоличествоСтарое - НоваяСтрока.КоличествоГрузов;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры // ВводКоличестваГрузаВНовойСтрокеЗавершение()

&НаКлиенте
// Процедура завершения после немодального ввода числового значения. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВводКоличестваГрузаПоЭтапуВНовойСтрокеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		текСтрока = Элементы.ГрузовыеМестаЭтапов.ТекущиеДанные;
		КоличествоСтарое = текСтрока.КоличествоГрузов;
		Количество = Результат;
		Отказ = Ложь;
		ТекстСообщения = "";
		ВводКоличестваГрузаОбработка(КоличествоСтарое, Количество, Отказ, ТекстСообщения);
		
		Если Отказ Тогда
			ПоказатьПредупреждение(,ТекстСообщения);
		Иначе
			текИндекс = ГрузовыеМестаЭтапов.Индекс(текСтрока);
			НоваяСтрока = ГрузовыеМестаЭтапов.Вставить(текИндекс + 1);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			
			НоваяСтрока.КоличествоГрузов = Количество;
			текСтрока.КоличествоГрузов = КоличествоСтарое - НоваяСтрока.КоличествоГрузов;
		КонецЕсли;

	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВводКоличестваГрузаОбработка(Знач КоличествоСтарое, Знач Количество, Отказ, ТекстСообщения)
	
	Если Количество = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Количество груза в новой строке не может быть равно нулю.'");
		Отказ = Истина;
	ИначеЕсли Количество < 0 Тогда	
		ТекстСообщения = НСтр("ru = 'Количество груза в новой строке не может быть отрицательным.'");
		Отказ = Истина;
	ИначеЕсли Количество >= КоличествоСтарое Тогда
		ТекстСообщения = НСтр("ru = 'Количество груза в новой строке должно быть меньше количества груза в текущей.'");
		Отказ = Истина;
	КонецЕсли; 

КонецПроцедуры // ВводКоличестваГрузаПоЭтапуВНовойСтрокеЗавершение()

&НаКлиенте
Процедура ОбработатьОтветНаВопросНормализоватьГрузовыеПотоки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗавершитьРедактированиеНаКлиенте();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура ПроверитьЗаполнениеОбязательныхРеквизитов(Отказ)
	
	сткПоляДляПроверки = Новый Структура();
	сткПоляДляПроверки.Вставить("ВидПеревозки", "Вид перевозки");
	сткПоляДляПроверки.Вставить("АдресОтправления", "Адрес отправления");
	сткПоляДляПроверки.Вставить("АдресПолучения", "Адрес получения");
	сткПоляДляПроверки.Вставить("ЗонаОтправления", "Зона отправления");
	сткПоляДляПроверки.Вставить("ЗонаПолучения", "Зона получения");
	сткПоляДляПроверки.Вставить("Организация", "Организация");
		
	Для каждого СтрокаЭтапПеревозки Из ЭтапыПеревозки Цикл
		Для каждого ПолеДляПроверки Из сткПоляДляПроверки Цикл
			Если Не ЗначениеЗаполнено(СтрокаЭтапПеревозки[ПолеДляПроверки.Ключ]) Тогда
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено'"), ПолеДляПроверки.Значение);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыПеревозки", ЭтапыПеревозки.Индекс(СтрокаЭтапПеревозки) + 1, ПолеДляПроверки.Ключ),, Отказ);
			КонецЕсли;
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ГрузовыеПотокиНормализованы()
	
	Результат = Истина;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Включенные этапы перевозки
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ЭтапыПеревозки.ЕстьЗадание КАК ЕстьЗадание,
	|	ЭтапыПеревозки.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//////////////////////////////////////////
	|// Грузовые места по этапам
	|ВЫБРАТЬ
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапов.Тара КАК Тара,
	|	ГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМестаЭтаповПодготовка
	|ИЗ
	|	&ГрузовыеМестаЭтапов КАК ГрузовыеМестаЭтапов
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//////////////////////////////////////////
	|// Свернутые грузовые места по этапам.
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	СУММА(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов
	|ИЗ
	|	втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтапов
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиТ.Идентификатор КАК Идентификатор,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтаповПодготовкаТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтаповПодготовкаТ.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМестаЭтаповПодготовкаТ.Тара КАК Тара,
	|	втЭтапыПеревозкиТ.ЕстьЗадание КАК ЕстьЗадание
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов10
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозкиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтаповПодготовкаТ
	|	ПО ИСТИНА
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставок = втГрузовыеМестаЭтаповПодготовкаТ.НомерВЦепиПоставок
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец = втГрузовыеМестаЭтаповПодготовкаТ.НомерВЦепиПоставокКонец
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредыдущийЭтап.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втПредыдущийЭтап.Тара КАК Тара,
	|	МАКСИМУМ(втПредыдущийЭтап.КоличествоГрузов) КАК ПредыдущийЭтапКоличествоГрузов,
	|	втПредыдущийЭтап.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставок,
	|	СУММА(втСледующийЭтап.КоличествоГрузов) КАК СледующийЭтапКоличествоГрузов
	|ИЗ
	|	втГрузовыеМестаЭтапов10 КАК втПредыдущийЭтап
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов10 КАК втСледующийЭтап
	|	ПО ИСТИНА
	|		И втСледующийЭтап.ГрузовоеМесто = втПредыдущийЭтап.ГрузовоеМесто
	|		И втСледующийЭтап.Тара = втПредыдущийЭтап.Тара
	|		И втСледующийЭтап.НомерВЦепиПоставок = втПредыдущийЭтап.НомерВЦепиПоставокКонец
	|ГДЕ НЕ втСледующийЭтап.ЕстьЗадание
	|СГРУППИРОВАТЬ ПО
	|	втПредыдущийЭтап.ГрузовоеМесто,
	|	втПредыдущийЭтап.Тара,
	|	втПредыдущийЭтап.НомерВЦепиПоставокКонец
	|ИМЕЮЩИЕ СУММА(втСледующийЭтап.КоличествоГрузов) <> МАКСИМУМ(втПредыдущийЭтап.КоличествоГрузов)
	|";

	Запрос.УстановитьПараметр("ЭтапыПеревозки", ЭтапыПеревозкиСПризнакомНаличияЗадания());
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить(,"НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ГрузовоеМесто, Тара, КоличествоГрузов"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = РезультатЗапроса.Пустой();
	Если Не Результат Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = Нстр("ru = 'Точка %1;%2%3 Количество грузов на входе:%4, на выходе:%5'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, 
			Выборка.НомерВЦепиПоставок,
			?(ЗначениеЗаполнено(Выборка.ГрузовоеМесто),СтрШаблон(Нстр("ru = 'Грузовое место:%1;'"),Выборка.ГрузовоеМесто),""),
			?(ЗначениеЗаполнено(Выборка.Тара),СтрШаблон(Нстр("ru = 'Тара:%1;'"),Выборка.Тара),""),
			Выборка.ПредыдущийЭтапКоличествоГрузов, 
			Выборка.СледующийЭтапКоличествоГрузов);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЭтапыПеревозкиСПризнакомНаличияЗадания()
	
	тбзЭтапыПеревозки = ЭтапыПеревозки.Выгрузить(,"НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ИндексРебраГрафа, ЗаданиеНаПеревозку");
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ЭтапыПеревозки.ИндексРебраГрафа КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЭтапыПеревозкиНачало.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиНачало.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ втЭтапыОкончание
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозкиНачало
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЭтапыПеревозки КАК втЭтапыПеревозкиКонец
	|	ПО втЭтапыПеревозкиНачало.НомерВЦепиПоставокКонец = втЭтапыПеревозкиКонец.НомерВЦепиПоставок
	|ГДЕ втЭтапыПеревозкиКонец.НомерВЦепиПоставок ЕСТЬ NULL
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиТ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втЭтапыПеревозкиТ.Идентификатор КАК Идентификатор,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ВЫБОР
	|		КОГДА втЭтапыОкончаниеТ.НомерВЦепиПоставок ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоОкончание,
	|	ЛОЖЬ КАК ЕстьЗадание
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозкиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЭтапыОкончание КАК втЭтапыОкончаниеТ
	|	ПО ИСТИНА
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставок = втЭтапыОкончаниеТ.НомерВЦепиПоставок
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец = втЭтапыОкончаниеТ.НомерВЦепиПоставокКонец
	|;
	|";
	Запрос.УстановитьПараметр("ЭтапыПеревозки", тбзЭтапыПеревозки);

	тбзЭтапыПеревозки = Запрос.Выполнить().Выгрузить();
	
	мсвЭтапыОкончания = тбзЭтапыПеревозки.НайтиСтроки(Новый Структура("ЭтоОкончание", Истина));
	
	Для каждого СтрокаОкончание Из мсвЭтапыОкончания Цикл
		ЗаполнитьПризнакЕстьЗадание(тбзЭтапыПеревозки, СтрокаОкончание, Ложь);
	КонецЦикла;
	
	Возврат тбзЭтапыПеревозки;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьПризнакЕстьЗадание(тбзЭтапыПеревозки, ТекущийЭтап, ЕстьЗаданиеНаПредыдущемЭтапе)
	
	ЕстьЗаданиеНаПредыдущемЭтапе = Ложь
							Или ЗначениеЗаполнено(ТекущийЭтап.ЗаданиеНаПеревозку)
							Или ЕстьЗаданиеНаПредыдущемЭтапе;
	
	ТекущийЭтап.ЕстьЗадание = ЕстьЗаданиеНаПредыдущемЭтапе;
	
	мсвСтрокиПредыдущиеЭтапы = тбзЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставокКонец", ТекущийЭтап.НомерВЦепиПоставок));
	
	Для каждого СтрокаЭтап Из мсвСтрокиПредыдущиеЭтапы Цикл
		ЗаполнитьПризнакЕстьЗадание(тбзЭтапыПеревозки, СтрокаЭтап, ЕстьЗаданиеНаПредыдущемЭтапе);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НормализоватьГрузовыеПотокиНаСервере()
	
	// Пересчитать количество грузов на этапах не нормализованных точек
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"
	|
	|//{Запрос: 0 ////////////////////////////////////////
	|// Включенные этапы перевозки
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ЭтапыПеревозки.ЕстьЗадание КАК ЕстьЗадание,
	|	ЭтапыПеревозки.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Грузовые места по этапам
	|ВЫБРАТЬ
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМестаЭтапов.Тара КАК Тара,
	|	ГрузовыеМестаЭтапов.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМестаЭтаповПодготовка
	|ИЗ
	|	&ГрузовыеМестаЭтапов КАК ГрузовыеМестаЭтапов
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Свернутые грузовые места по этапам.
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара КАК Тара,
	|	СУММА(втГрузовыеМестаЭтапов.КоличествоГрузов) КАК КоличествоГрузов
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов
	|ИЗ
	|	втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтапов
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставок,
	|	втГрузовыеМестаЭтапов.НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтапов.ГрузовоеМесто,
	|	втГрузовыеМестаЭтапов.Тара
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиТ.Идентификатор КАК Идентификатор,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втГрузовыеМестаЭтаповПодготовкаТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаЭтаповПодготовкаТ.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМестаЭтаповПодготовкаТ.Тара КАК Тара,
	|	втЭтапыПеревозкиТ.ЕстьЗадание КАК ЕстьЗадание
	|ПОМЕСТИТЬ втГрузовыеМестаЭтапов10
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозкиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтаповПодготовка КАК втГрузовыеМестаЭтаповПодготовкаТ
	|	ПО ИСТИНА
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставок = втГрузовыеМестаЭтаповПодготовкаТ.НомерВЦепиПоставок
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец = втГрузовыеМестаЭтаповПодготовкаТ.НомерВЦепиПоставокКонец
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредыдущийЭтап.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втПредыдущийЭтап.Тара КАК Тара,
	|	втПредыдущийЭтап.НомерВЦепиПоставокКонец КАК НомерТочкиВЦепиПоставок,
	|	МАКСИМУМ(втПредыдущийЭтап.КоличествоГрузов) КАК КоличествоГрузовВходТочка,
	|	СУММА(ЕСТЬNULL(втСледующийЭтап.КоличествоГрузов, 0)) КАК КоличествоГрузовВыходТочка,
	|	МАКСИМУМ(ЕСТЬNULL(втСледующийЭтап.ЕстьЗадание, ЛОЖЬ)) КАК ЕстьЗадание,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА втСледующийЭтап.КоличествоГрузов ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ) КАК ЭтоКонечнаяТочка
	|ПОМЕСТИТЬ втКоличествоГрузовВТочках
	|ИЗ
	|	втГрузовыеМестаЭтапов10 КАК втПредыдущийЭтап
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов10 КАК втСледующийЭтап
	|	ПО ИСТИНА
	|		И втСледующийЭтап.ГрузовоеМесто = втПредыдущийЭтап.ГрузовоеМесто
	|		И втСледующийЭтап.Тара = втПредыдущийЭтап.Тара
	|		И втСледующийЭтап.НомерВЦепиПоставок = втПредыдущийЭтап.НомерВЦепиПоставокКонец
	|СГРУППИРОВАТЬ ПО
	|	втПредыдущийЭтап.ГрузовоеМесто,
	|	втПредыдущийЭтап.Тара,
	|	втПредыдущийЭтап.НомерВЦепиПоставокКонец
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущийЭтап.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втТекущийЭтап.Тара КАК Тара,
	|	втТекущийЭтап.Идентификатор КАК Идентификатор,
	|	втТекущийЭтап.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втТекущийЭтап.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втТекущийЭтап.КоличествоГрузов КАК КоличествоГрузовВходТочкаОкончание,
	|	ЕСТЬNULL(втКоличествоГрузовВТочках.КоличествоГрузовВыходТочка, втТекущийЭтап.КоличествоГрузов) КАК КоличествоГрузовВыходТочкаОкончание,
	|	//	ВЫБОР
	|	//		КОГДА втНеНормализованныеТочкиТ.ГрузовоеМесто ЕСТЬ NULL
	|	//			ТОГДА ЛОЖЬ
	|	//		ИНАЧЕ ИСТИНА
	|	//	КОНЕЦ КАК ТочкаОкончанияНеНормализована,
	|	ВЫБОР
	|		КОГДА втПредыдущийЭтап.ГрузовоеМесто ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНачальнаяТочка,
	|	втКоличествоГрузовВТочках.ЭтоКонечнаяТочка КАК ЭтоКонечнаяТочка,
	|	ЛОЖЬ КАК Пометка
	|ИЗ
	|	втГрузовыеМестаЭтапов10 КАК втТекущийЭтап
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаЭтапов10 КАК втПредыдущийЭтап
	|	ПО ИСТИНА
	|		И втТекущийЭтап.ГрузовоеМесто = втПредыдущийЭтап.ГрузовоеМесто
	|		И втТекущийЭтап.Тара = втПредыдущийЭтап.Тара
	|		И втТекущийЭтап.НомерВЦепиПоставок = втПредыдущийЭтап.НомерВЦепиПоставокКонец
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоГрузовВТочках КАК втКоличествоГрузовВТочках
	|	ПО ИСТИНА
	|		И втТекущийЭтап.НомерВЦепиПоставокКонец = втКоличествоГрузовВТочках.НомерТочкиВЦепиПоставок
	|		И втТекущийЭтап.ГрузовоеМесто = втКоличествоГрузовВТочках.ГрузовоеМесто
	|		И втТекущийЭтап.Тара = втКоличествоГрузовВТочках.Тара
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втНеНормализованныеТочкиТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втНеНормализованныеТочкиТ.Тара КАК Тара
	|ИЗ
	|	втКоличествоГрузовВТочках КАК втНеНормализованныеТочкиТ
	|ГДЕ ИСТИНА
	|	И НЕ (втНеНормализованныеТочкиТ.ЕстьЗадание)
	|	И втНеНормализованныеТочкиТ.КоличествоГрузовВходТочка <> втНеНормализованныеТочкиТ.КоличествоГрузовВыходТочка
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаЭтаповТ.*
	|ИЗ
	|	втГрузовыеМестаЭтапов10 КАК втГрузовыеМестаЭтаповТ
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоГрузов
	|ИТОГИ
	|ПО
	|	ГрузовоеМесто,
	|	Тара,
	|	НомерВЦепиПоставок
	|";
	Запрос.УстановитьПараметр("ЭтапыПеревозки", ЭтапыПеревозкиСПризнакомНаличияЗадания());
	Запрос.УстановитьПараметр("ГрузовыеМестаЭтапов", ГрузовыеМестаЭтапов.Выгрузить(,"НомерВЦепиПоставок, НомерВЦепиПоставокКонец, ГрузовоеМесто, Тара, КоличествоГрузов"));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	тбзЭтапы = Новый ТаблицаЗначений;
	тбзЭтапы.Колонки.Добавить("НомерВЦепиПоставок");
	тбзЭтапы.Колонки.Добавить("НомерВЦепиПоставокКонец");
		
	// Если есть не нормализованные точки этапов
	Если Не РезультатЗапроса[КоличествоТаблиц - 2].Пустой() Тогда
		ЭтапыГрузовыхМестДляНормализации = РезультатЗапроса[КоличествоТаблиц - 3].Выгрузить();
		СписокГрузовНенормализованныхЭтапов = РезультатЗапроса[КоличествоТаблиц - 2].Выгрузить();
		дрвЭтапыГрузовыхМестОтсортированные = РезультатЗапроса[КоличествоТаблиц - 1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		// Обход грузовых мест имеющих не нормализованные этапы
		Для каждого СтрокаГрузовоеМесто Из СписокГрузовНенормализованныхЭтапов Цикл
						
			ГрузовоеМестоТекущее = СтрокаГрузовоеМесто.ГрузовоеМесто;
			ТараТекущая = СтрокаГрузовоеМесто.Тара;
			
			// Начальные точки для грузового места
			мсвНачальныеТочкиЭтаповГрузовыхМест = ЭтапыГрузовыхМестДляНормализации.НайтиСтроки(Новый Структура("ЭтоНачальнаяТочка, ГрузовоеМесто, Тара", Истина, ГрузовоеМестоТекущее, ТараТекущая));
			
			// Совершается обход всех веток грузового места.
			Для каждого Точка Из мсвНачальныеТочкиЭтаповГрузовыхМест Цикл
				НомерВЦепиПоставокКонец = Точка.НомерВЦепиПоставокКонец;
				КоличествоГрузовВходТочкаОкончание = Точка.КоличествоГрузовВходТочкаОкончание;
				КоличествоГрузовВыходТочкаОкончание = Точка.КоличествоГрузовВыходТочкаОкончание;
				ПревышениеКоличестваВыходаНадВходом = КоличествоГрузовВыходТочкаОкончание - КоличествоГрузовВходТочкаОкончание;
				НормализоватьЭтапыТочкиОкончаниеНаВыходе(ЭтапыГрузовыхМестДляНормализации, дрвЭтапыГрузовыхМестОтсортированные, ГрузовоеМестоТекущее, ТараТекущая, НомерВЦепиПоставокКонец, ПревышениеКоличестваВыходаНадВходом, тбзЭтапы);
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЕсли;
			
	// Пересчитать итоговые показатели на этапах
	ОбновитьПараметрыЭтапов();
	
	Модифицированность = Истина;
	
	тбзЭтапы.Свернуть("НомерВЦепиПоставок, НомерВЦепиПоставокКонец");
	мсвИзмененныеЭтапы = Новый Массив;
	
	Для каждого СтрокаЭтап Из тбзЭтапы Цикл
		ИскомыеЭтапы = ЭтапыПеревозки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок, НомерВЦепиПоставокКонец", СтрокаЭтап.НомерВЦепиПоставок, СтрокаЭтап.НомерВЦепиПоставокКонец));
		Если ИскомыеЭтапы.Количество() Тогда
			НайденныйЭтап = ИскомыеЭтапы[0];
			мсвИзмененныеЭтапы.Добавить(НайденныйЭтап.ПолучитьИдентификатор());
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвИзмененныеЭтапы);
	
КонецФункции

Процедура НормализоватьЭтапыТочкиОкончаниеНаВыходе(ЭтапыГрузовыхМестДляНормализации, дрвЭтапыСортированныеПоКоличествуГрузовых, ГрузовоеМесто, Тара, НомерВЦепиПоставок, ПревышениеКоличестваВыходаНадВходом, тбзЭтапы)
		
	Если ПревышениеКоличестваВыходаНадВходом < 0 Тогда
		
		// Увеличить
		КоличествоГрузовыхМестОтклонение = -ПревышениеКоличестваВыходаНадВходом;
		
		СтрокиГрузовоеМесто = дрвЭтапыСортированныеПоКоличествуГрузовых.Строки.НайтиСтроки(Новый Структура("ГрузовоеМесто", ГрузовоеМесто));
		Если СтрокиГрузовоеМесто.Количество() > 0 Тогда
			СтрокиТара = СтрокиГрузовоеМесто[0].Строки.НайтиСтроки(Новый Структура("Тара", Тара));
			Если СтрокиТара.Количество() > 0 Тогда
				СтрокиНомерВЦепиПоставок = СтрокиТара[0].Строки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок", НомерВЦепиПоставок));
				Если СтрокиНомерВЦепиПоставок.Количество() > 0 Тогда
					Для каждого СтрокаЭтап Из СтрокиНомерВЦепиПоставок[0].Строки Цикл
						
						Если СтрокаЭтап.ЕстьЗадание Тогда
							Продолжить;
						КонецЕсли;
								
						// Количество увеличивается в строке с наименьшим количеством
						НомерВЦепиПоставокКонец = СтрокаЭтап.НомерВЦепиПоставокКонец;
						
						сткОтбор = Новый Структура("ГрузовоеМесто, Тара, НомерВЦепиПоставок, НомерВЦепиПоставокКонец", ГрузовоеМесто, Тара, НомерВЦепиПоставок, НомерВЦепиПоставокКонец);
						
						мсвСтроки = ЭтапыГрузовыхМестДляНормализации.НайтиСтроки(сткОтбор);
						Для каждого СтрокаГрузовоеМесто Из мсвСтроки Цикл
							СтрокаГрузовоеМесто.КоличествоГрузовВходТочкаОкончание = СтрокаГрузовоеМесто.КоличествоГрузовВходТочкаОкончание + КоличествоГрузовыхМестОтклонение;
						КонецЦикла;
						
						мсвСтроки = ГрузовыеМестаЭтапов.НайтиСтроки(сткОтбор);
						Для каждого СтрокаГрузовоеМесто Из мсвСтроки Цикл
							СтрокаГрузовоеМесто.КоличествоГрузов = СтрокаГрузовоеМесто.КоличествоГрузов + КоличествоГрузовыхМестОтклонение;
						КонецЦикла;
						
						СтрокаЭтап = тбзЭтапы.Добавить();
						СтрокаЭтап.НомерВЦепиПоставок = НомерВЦепиПоставок;
						СтрокаЭтап.НомерВЦепиПоставокКонец = НомерВЦепиПоставокКонец;
						
						Прервать;
						
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ПревышениеКоличестваВыходаНадВходом > 0 Тогда
		// Уменьшить
		
		КоличествоГрузовыхМестОтклонение = ПревышениеКоличестваВыходаНадВходом;
		
		СтрокиГрузовоеМесто = дрвЭтапыСортированныеПоКоличествуГрузовых.Строки.НайтиСтроки(Новый Структура("ГрузовоеМесто", ГрузовоеМесто));
		Если СтрокиГрузовоеМесто.Количество() > 0 Тогда
			СтрокиТара = СтрокиГрузовоеМесто[0].Строки.НайтиСтроки(Новый Структура("Тара", Тара));
			Если СтрокиТара.Количество() > 0 Тогда
				СтрокиНомерВЦепиПоставок = СтрокиТара[0].Строки.НайтиСтроки(Новый Структура("НомерВЦепиПоставок", НомерВЦепиПоставок));
				Если СтрокиНомерВЦепиПоставок.Количество() > 0 Тогда
					
					Для каждого СтрокаЭтап Из СтрокиНомерВЦепиПоставок[0].Строки Цикл
						
						Если СтрокаЭтап.ЕстьЗадание Тогда
							Продолжить;
						КонецЕсли;
						
						Если СтрокаЭтап.КоличествоГрузов > 1 Тогда
							
							КоличествоГрузов = Макс(1, СтрокаЭтап.КоличествоГрузов - КоличествоГрузовыхМестОтклонение);
							КоличествоДельта = СтрокаЭтап.КоличествоГрузов - КоличествоГрузов;
							СтрокаЭтап.КоличествоГрузов = КоличествоГрузов;
							
							НомерВЦепиПоставокКонец = СтрокаЭтап.НомерВЦепиПоставокКонец;
							
							сткОтбор = Новый Структура("ГрузовоеМесто, Тара, НомерВЦепиПоставок, НомерВЦепиПоставокКонец", ГрузовоеМесто, Тара, НомерВЦепиПоставок, НомерВЦепиПоставокКонец);
							
							СтрокаЭтап = тбзЭтапы.Добавить();
							СтрокаЭтап.НомерВЦепиПоставок = НомерВЦепиПоставок;
							СтрокаЭтап.НомерВЦепиПоставокКонец = НомерВЦепиПоставокКонец;
							
							мсвСтроки = ЭтапыГрузовыхМестДляНормализации.НайтиСтроки(сткОтбор);
							Для каждого СтрокаГрузовоеМесто Из мсвСтроки Цикл
								СтрокаГрузовоеМесто.КоличествоГрузовВходТочкаОкончание = КоличествоГрузов;
							КонецЦикла;
							
							мсвСтроки = ГрузовыеМестаЭтапов.НайтиСтроки(сткОтбор);
							Для каждого СтрокаГрузовоеМесто Из мсвСтроки Цикл
								СтрокаГрузовоеМесто.КоличествоГрузов = КоличествоГрузов;
							КонецЦикла;
							
							ПревышениеКоличестваВыходаНадВходом = ПревышениеКоличестваВыходаНадВходом - КоличествоДельта;
							
						КонецЕсли;
						
						Если ПревышениеКоличестваВыходаНадВходом = 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
		
	мсвТочкиЭтаповГрузовыхМест = ЭтапыГрузовыхМестДляНормализации.НайтиСтроки(Новый Структура("ГрузовоеМесто, Тара, НомерВЦепиПоставок", ГрузовоеМесто, Тара, НомерВЦепиПоставок));
	Для каждого Точка Из мсвТочкиЭтаповГрузовыхМест Цикл
		НомерВЦепиПоставокКонец = Точка.НомерВЦепиПоставокКонец;
		КоличествоГрузовТочкаОкончанияВход = Точка.КоличествоГрузовВходТочкаОкончание;
		КоличествоГрузовТочкаОкончанияВыход = Точка.КоличествоГрузовВыходТочкаОкончание;
		ПревышениеКоличестваВыходаНадВходом = КоличествоГрузовТочкаОкончанияВыход - КоличествоГрузовТочкаОкончанияВход;
		НормализоватьЭтапыТочкиОкончаниеНаВыходе(ЭтапыГрузовыхМестДляНормализации, дрвЭтапыСортированныеПоКоличествуГрузовых, ГрузовоеМесто, Тара, НомерВЦепиПоставокКонец, ПревышениеКоличестваВыходаНадВходом, тбзЭтапы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 