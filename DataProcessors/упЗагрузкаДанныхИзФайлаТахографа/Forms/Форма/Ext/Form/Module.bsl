&НаКлиенте
Перем ПараметрыОбработчика;

&НаКлиенте
Перем ПодтверждениеЗакрытияФормы;

&НаКлиенте
Перем ФормаДлительнойОперации;

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАктивностейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТаблицаАктивностейСотрудник" Тогда
		Строка = ТаблицаАктивностей.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если НЕ Строка = Неопределено Тогда
			Ключ = Строка["Сотрудник"];
			Если ЗначениеЗаполнено(Ключ) Тогда
				ПараметрыФормы = Новый Структура("Ключ", Ключ);
				ОткрытьФорму("Справочник.упСотрудники.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "ТаблицаАктивностейРейс" Тогда	
		Строка = ТаблицаАктивностей.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если НЕ Строка = Неопределено Тогда
			Ключ = Строка["Рейс"];
			Если ЗначениеЗаполнено(Ключ) Тогда
				ПараметрыФормы = Новый Структура("Ключ", Ключ);
				ОткрытьФорму("Документ.упРейс.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайла(Команда)
	ЗагрузитьДанныеИзФайлаКлиент(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицуИзФайла(Команда)
	ЗагрузитьДанныеИзФайлаКлиент(Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для каждого Строка Из ТаблицаАктивностей Цикл
		Строка.Флаг = Истина;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для каждого Строка Из ТаблицаАктивностей Цикл
		Строка.Флаг = Ложь;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьВодителей(Команда)
	СопоставитьВодителейНаСервере();
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеВРегистр(Команда)
	
	ЗагрузитьДанныеВРегистрКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеВРегистрИЗакрытьФорму(Команда)
	
	ЗагрузитьДанныеВРегистрКлиент(Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвТипыСотрудников = Новый Массив;
	мсвТипыСотрудников.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Водитель"));
	мсвТипыСотрудников.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.ВодительЭкспедитор"));
	Отбор = Новый Структура("ТипСотрудника", мсвТипыСотрудников);
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ОткрытьФорму("Справочник.упСотрудники.ФормаВыбора", ПараметрыФормы, Элемент, УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ЗагрузитьДанныеВРегистрКлиент(ЗакрытьФорму = Ложь)
	
	Отказ = Ложь;
	ТекстОшибки = "";
	ЗагрузитьДанныеВРегистрНаСервере(ТекстОшибки, Отказ);
	Если Отказ Тогда
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);	
	Иначе
		мсвОтмеченныеСтроки = ТаблицаАктивностей.НайтиСтроки(Новый Структура("Флаг",Истина));
		мсвРейсы = Новый Массив;
		Для каждого Строка Из мсвОтмеченныеСтроки Цикл
			ТекущийРейс = Строка.Рейс;
			Если ЗначениеЗаполнено(ТекущийРейс) 
					И мсвРейсы.Найти(ТекущийРейс) = Неопределено Тогда
				Оповестить("ИзменилсяМаршрут",,ТекущийРейс);
				мсвРейсы.Добавить(ТекущийРейс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не Отказ И ЗакрытьФорму Тогда
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОкончаниеПомещенияФайла(Результат, АдресВременногоХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат = Истина Тогда
		Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ВыбранноеИмяФайла));
		Если Расширение = "esm" 
				ИЛИ Расширение = "ddd" Тогда
			ФоновоеЗадание 	= Ложь;
			ТекстСообщения		= "";
			ЗагрузитьДанныеФайлаНаСервере(АдресВременногоХранилища, Расширение, ФоновоеЗадание, ТекстСообщения);
			Если ФоновоеЗадание Тогда
				ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
				ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте", 1, Истина);
				ПараметрыОбработчика.МаксимальныйИнтервал = 5;
				ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, ФоновоеЗаданиеИдентификатор);
			КонецЕсли;	
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстСообщения, Истина);
			КонецЕсли;
		Иначе
			ТекстСообщения = Нстр("ru = 'Выбранный тип файла не поддерживается. Выберите файл с расширением esm или ddd'");
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстСообщения, Истина);
		КонецЕсли;
	КонецЕсли;	
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайлаКлиент(ОткрыватьДиалогВыбора = Истина)
	
	Если ОткрыватьДиалогВыбора Тогда
		Интерактивно 	= Истина;
		ПолноеИмяФайла 	= "";	
		Если ПодключитьРасширениеРаботыСФайлами() Тогда
			ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
			ДиалогВыбораФайла.Фильтр                      = НСтр("ru='Данные тахографа (*.ddd)|*.ddd|Данные тахографа (*.esm)|*.esm'");
			ДиалогВыбораФайла.Заголовок                   = Заголовок;
			ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
			ДиалогВыбораФайла.Расширение                  = "";
			ДиалогВыбораФайла.ИндексФильтра               = 0;
			ДиалогВыбораФайла.ПолноеИмяФайла              = "";
			ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
			
			Если ДиалогВыбораФайла.Выбрать() Тогда
				ПолноеИмяФайла = ДиалогВыбораФайла.ПолноеИмяФайла;
			КонецЕсли;
			
			ВыбранныйФайл 	= ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПолноеИмяФайла);
			Если ЗначениеЗаполнено(ВыбранныйФайл.Имя) Тогда
				ПолноеИмяФайла = ВыбранныйФайл.ПолноеИмя;
				Интерактивно = Ложь;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Интерактивно 	= Ложь;
	КонецЕсли;
	АдресВременногоХранилища = "";
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПриОкончаниеПомещенияФайла", ЭтотОбъект);
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, АдресВременногоХранилища, ПолноеИмяФайла, Интерактивно);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеФайлаНаСервере(АдресВременногоХранилища, Расширение, ФоновоеЗадание, ТекстСообщения)

	ИмяВременногоФайла	= ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные 		= ПолучитьИЗВременногоХранилища(АдресВременногоХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ПараметрыВызоваСервера = Новый Структура();
	ПараметрыВызоваСервера.Вставить("ИмяВременногоФайла", ИмяВременногоФайла);
	//ПараметрыВызоваСервера.Вставить("ТаблицаАктивностей", Объект.ТаблицаАктивностей);
	
	РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
	"Обработки.упЗагрузкаДанныхИзФайлаТахографа.ЗагрузитьДанныеИзФайла",
	ПараметрыВызоваСервера, 
	НСтр("ru = 'Подсистема ЗагрузкаДанныхИзФайла: Выполнение серверного метода загрузка данных из файла'"));
	
	Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
		сткДанныеИзТахографа = ПолучитьИзВременногоХранилища(РезультатФоновогоЗадания.АдресХранилища);
		ТекстСообщения = сткДанныеИзТахографа.ТекстОшибки;
		Если НЕ ЗначениеЗаполнено(ТекстСообщения) Тогда
			ЗаполнитьДанныеФормыНаСервере(сткДанныеИзТахографа);
		КонецЕсли;
	Иначе
		ФоновоеЗадание = Истина;
		ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
		ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте()
	Результат = ФоновоеЗаданиеЗагрузкаФайлаПолучитьРезультат();
	Если Результат.ФоновоеЗаданиеВыполнено Тогда
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ФоновоеЗаданиеИдентификатор Тогда
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		КонецЕсли;
		сткДанныеИзТахографа = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		ЗаполнитьДанныеФормыНаСервере(сткДанныеИзТахографа);
		УстановитьДоступность();
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ФоновоеЗаданиеЗагрузкаФайлаПолучитьРезультат()
	Результат = Новый Структура;
	Результат.Вставить("ФоновоеЗаданиеВыполнено", Ложь);
	Результат.ФоновоеЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеФормыНаСервере(сткДанныеТахографа)
	
	Сотрудник 	= Справочники.упСотрудники.ПустаяСсылка();
	НомерКарты	= "";
	ИмяВодителя	= "";
	ФамилияВодителя		= "";
	ОрганВыдавшийКарту	= "";
	ДатаВыпускаКарты 			= '00010101';
	ДатаНачалаДействияКарты 	= '00010101';
	ДатаОкончанияДействияКарты 	= '00010101';
	ТаблицаАктивностей.Очистить();
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, сткДанныеТахографа.ДанныеИдентификации);
	Сотрудник		= сткДанныеТахографа.Сотрудник;
	КартаТахографа	= сткДанныеТахографа.КартаТахографа;
	
	Если ЗначениеЗаполнено(Сотрудник)
		И ЗначениеЗаполнено(КартаТахографа) Тогда
		СопоставленныйСотрудник = Сотрудник;
	КонецЕсли;
	
	Для каждого Строка Из сткДанныеТахографа.ТаблицаАктивностей Цикл
		НоваяСтрока = ТаблицаАктивностей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	Элементы.ОбновитьТаблицуИзФайла.Доступность 				= ЗначениеЗаполнено(ПолноеИмяФайла);
	Элементы.ТаблицаАктивностейСопоставитьВодителей.Доступность = ЗначениеЗаполнено(Сотрудник) 
																	И НЕ СопоставленныйСотрудник = Сотрудник;
КонецПроцедуры

&НаСервере
Процедура СопоставитьВодителейНаСервере()
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстОшибки = "";
		Отказ = Ложь;
		сткПараметрыКарты = Новый Структура();
		сткПараметрыКарты.Вставить("НомерКарты", 				НомерКарты);
		сткПараметрыКарты.Вставить("ИмяВодителя", 				ИмяВодителя);
		сткПараметрыКарты.Вставить("ФамилияВодителя", 			ФамилияВодителя);
		сткПараметрыКарты.Вставить("ОрганВыдавшийКарту", 		ОрганВыдавшийКарту);
		сткПараметрыКарты.Вставить("ДатаВыпускаКарты", 			ДатаВыпускаКарты);
		сткПараметрыКарты.Вставить("ДатаНачалаДействияКарты", 	ДатаНачалаДействияКарты);
		сткПараметрыКарты.Вставить("ДатаОкончанияДействияКарты",ДатаОкончанияДействияКарты);
		КартаТахографа = Обработки.упЗагрузкаДанныхИзФайлаТахографа.СформироватьКартуТахографаПоСотруднику(сткПараметрыКарты,Сотрудник, ТекстОшибки, Отказ);
		Если НЕ Отказ Тогда
			Для каждого Строка Из ТаблицаАктивностей Цикл
				Строка.Сотрудник = Сотрудник;
			КонецЦикла;
			тбзАктивностей = Обработки.упЗагрузкаДанныхИзФайлаТахографа.ПодобратьРейсы(ТаблицаАктивностей.Выгрузить());
			ТаблицаАктивностей.Загрузить(тбзАктивностей);
			СопоставленныйСотрудник = Сотрудник;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеВРегистрНаСервере(ТекстОшибки, Отказ)
	Обработки.упЗагрузкаДанныхИзФайлаТахографа.ЗаписатьДанныеАктивностиВРегистр(ТаблицаАктивностей.Выгрузить(), ТекстОшибки, Отказ);
КонецПроцедуры

#КонецОбласти