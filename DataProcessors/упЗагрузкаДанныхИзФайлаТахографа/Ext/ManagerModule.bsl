#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Процедура - Загружает данные из файла, по возможности сопоставляя сотрудника, карту тахографа и рейс.
//
// Параметры:
//  ПараметрыВызоваСервера	 - Структура - параметры
//  АдресХранилища			 - Строка - Адрес временного хранилища, куда поместили файл
//
Процедура ЗагрузитьДанныеИзФайла(ПараметрыВызоваСервера, АдресХранилища) Экспорт
	
	ИмяВременногоФайла 	= ПараметрыВызоваСервера.ИмяВременногоФайла;
	
	ДвоичныеДанные 		= Новый ДвоичныеДанные(ИмяВременногоФайла);
	сткДанныеТахографа 	= упСервисныеФункции.РасшифроватьФайлТахографа(ДвоичныеДанные);
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("ТекстОшибки", "");
	сткРезультат.Вставить("Сотрудник", 		Справочники.упСотрудники.ПустаяСсылка());
	сткРезультат.Вставить("КартаТахографа", Справочники.упКартыТахографа.ПустаяСсылка());
	
	Если НЕ ЗначениеЗаполнено(сткДанныеТахографа.ТекстОшибки) Тогда
		
		ТаблицаАктивностей = сткДанныеТахографа.ТаблицаАктивностей;
		ТаблицаАктивностей.Колонки.Добавить("Сотрудник", 	Новый ОписаниеТипов("СправочникСсылка.упСотрудники"));
		ТаблицаАктивностей.Колонки.Добавить("Флаг", 		Новый ОписаниеТипов("Булево"));
		
		// Сотрудник
		сткДанныеСотрудника = СотрудникПоНомеруКарты(сткДанныеТахографа.ДанныеИдентификации.НомерКарты);
		Сотрудник 		= сткДанныеСотрудника.Сотрудник;
		КартаТахографа 	= сткДанныеСотрудника.КартаТахографа;
								
		Для каждого СтрокаАктивности Из ТаблицаАктивностей Цикл
			
			Если ЗначениеЗаполнено(Сотрудник) Тогда
				СтрокаАктивности.Сотрудник 	= Сотрудник;
			КонецЕсли;	
			СтрокаАктивности.Период		= МестноеВремя(СтрокаАктивности.Период, ЧасовойПоясСеанса());
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Сотрудник) Тогда
			тбзАктивностей		  = ПодобратьРейсы(ТаблицаАктивностей);			
			мсвТаблицаАктивностей = ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзАктивностей);
		Иначе
			мсвТаблицаАктивностей = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаАктивностей);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
			Сотрудник = СотрудникПоФамилииИмени(сткДанныеТахографа.ДанныеИдентификации.ФамилияВодителя, сткДанныеТахографа.ДанныеИдентификации.ИмяВодителя); 
			сткРезультат.Сотрудник = Сотрудник;
		КонецЕсли;
		
		сткРезультат.Вставить("ДанныеИдентификации", 	сткДанныеТахографа.ДанныеИдентификации);
		сткРезультат.Вставить("ТаблицаАктивностей", 	мсвТаблицаАктивностей);
		сткРезультат.Вставить("Сотрудник",				Сотрудник);
		сткРезультат.Вставить("КартаТахографа",			КартаТахографа);
	Иначе
		ТекстСообщения = Нстр("ru = 'При загрузке произошла ошибка:%1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, сткДанныеТахографа.ТекстОшибки);
		сткРезультат.Вставить("ТекстОшибки", ТекстСообщения);
	КонецЕсли;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(сткРезультат, АдресХранилища);
	
КонецПроцедуры

// Функция - Поиск сотрудник по фамилии имени, значения могут быть заданы как на кириллице, так и транслитом.
//
// Параметры:
//  Фамилия	 - Строка	 - фамилия.
//  Имя		 - Строка	 - имя.
// 
// Возвращаемое значение:
//  Справочник.упСотрудники - ссылка на сотрудника, в случае удачного поиска.
//
Функция СотрудникПоФамилииИмени(Фамилия, Имя) Экспорт
	
	Результат = Справочники.упСотрудники.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСотрудники.Ссылка
	               |ИЗ
	               |	Справочник.упСотрудники КАК упСотрудники
	               |ГДЕ
	               |	упСотрудники.Наименование ПОДОБНО &НаименованиеЛатиница
	               |	И НЕ упСотрудники.ПометкаУдаления";
	
	ФамилияИмя = СокрЛП(Фамилия) + " " + СокрЛП(Имя);
	СтрокаДляПоискаЛатиница = "%" + ФамилияИмя + "%";
	
	// Сначала проверяется латиница	
	Запрос.УстановитьПараметр("НаименованиеЛатиница", СтрокаДляПоискаЛатиница);
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЛатиницаВСтроке(ФамилияИмя,," ") Тогда
		
		СтрокаДляПоискаКириллица = упСервисныеФункцииКлиентСервер.СтрокаКириллицей(ФамилияИмя);
			
		Если НЕ ПустаяСтрока(СтрокаДляПоискаКириллица) Тогда
			
			СтрокаДляПоискаКириллица = "%" + СтрокаДляПоискаКириллица + "%";
			
			Запрос.Текст = Запрос.Текст +"
			|ОБЪЕДИНИТЬ ВСЕ
			|";
			
			Запрос.Текст = Запрос.Текст +"ВЫБРАТЬ
			                             |	упСотрудники.Ссылка
			                             |ИЗ
			                             |	Справочник.упСотрудники КАК упСотрудники
			                             |ГДЕ
			                             |	упСотрудники.Наименование ПОДОБНО &НаименованиеКириллица
			                             |	И НЕ упСотрудники.ПометкаУдаления";
			
			Запрос.УстановитьПараметр("НаименованиеКириллица", СтрокаДляПоискаКириллица);
			
		КонецЕсли;
	КонецЕсли;
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

// Функция - Заполняет колонку рейс в таблице, подбирается не отмененный рейс по сотруднику с максимальной датой
//           фактического выполнения, которая меньше периода записи таблицы.
//
// Параметры:
//  ТаблицаАктивностей	 - ТаблицаЗначений - таблица активностей
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с заполненными рейсами
//
Функция ПодобратьРейсы(ТаблицаАктивностей) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	втТаблицаАктивностей.Период,
	               |	втТаблицаАктивностей.Сотрудник,
	               |	втТаблицаАктивностей.СлотСчитывающегоУстройства,
	               |	втТаблицаАктивностей.СтатусУправления,
	               |	втТаблицаАктивностей.ПоложениеКарточки,
	               |	втТаблицаАктивностей.Флаг,
	               |	втТаблицаАктивностей.РежимДеятельности
	               |ПОМЕСТИТЬ втТаблицаАктивностей
	               |ИЗ
	               |	&тбТаблицаАктивностей КАК втТаблицаАктивностей
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТаблицаАктивностей.Период,
	               |	втТаблицаАктивностей.Сотрудник,
	               |	МАКСИМУМ(упПеревозчикПоРейсуСрезПоследних.Рейс) КАК Рейс
	               |ПОМЕСТИТЬ втРейсы
	               |ИЗ
	               |	втТаблицаАктивностей КАК втТаблицаАктивностей
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |			ПО упПеревозчикПоРейсуСрезПоследних.Рейс = упСтатусыРейсовСрезПоследних.Документ
	               |				И (упСтатусыРейсовСрезПоследних.Статус В (&СписокСтатусов))
	               |		ПО (НЕ втТаблицаАктивностей.Сотрудник = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	               |			И (втТаблицаАктивностей.Сотрудник = упПеревозчикПоРейсуСрезПоследних.Водитель1
	               |				ИЛИ втТаблицаАктивностей.Сотрудник = упПеревозчикПоРейсуСрезПоследних.Водитель2
	               |				ИЛИ втТаблицаАктивностей.Сотрудник = упПеревозчикПоРейсуСрезПоследних.Экспедитор)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	               |		ПО (упПеревозчикПоРейсуСрезПоследних.Рейс = упФактическиеПараметрыРейса.Рейс)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |		ПО (упПеревозчикПоРейсуСрезПоследних.Рейс = упПлановыеПараметрыРейса.Рейс)
	               |ГДЕ
	               |	ЕСТЬNULL(упФактическиеПараметрыРейса.НачалоВыполнения, ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1))) <= втТаблицаАктивностей.Период
	               |	И ВЫБОР
	               |			КОГДА упФактическиеПараметрыРейса.ОкончаниеВыполнения ЕСТЬ NULL
	               |				ТОГДА втТаблицаАктивностей.Период <= ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59))
	               |			КОГДА упФактическиеПараметрыРейса.ОкончаниеВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	               |				ТОГДА втТаблицаАктивностей.Период <= ДОБАВИТЬКДАТЕ(упФактическиеПараметрыРейса.НачалоВыполнения, СЕКУНДА, РАЗНОСТЬДАТ(ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)), ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)), СЕКУНДА))
	               |			ИНАЧЕ втТаблицаАктивностей.Период <= упФактическиеПараметрыРейса.ОкончаниеВыполнения
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТаблицаАктивностей.Период,
	               |	втТаблицаАктивностей.Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТаблицаАктивностей.Период,
	               |	втТаблицаАктивностей.Сотрудник,
	               |	втТаблицаАктивностей.СлотСчитывающегоУстройства,
	               |	втТаблицаАктивностей.СтатусУправления,
	               |	втТаблицаАктивностей.ПоложениеКарточки,
	               |	втТаблицаАктивностей.РежимДеятельности,
	               |	втРейсы.Рейс,
	               |	упТахографДанныеОДеятельностиВодителей.РежимДеятельности ЕСТЬ NULL КАК Флаг
	               |ИЗ
	               |	втТаблицаАктивностей КАК втТаблицаАктивностей
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	               |		ПО втТаблицаАктивностей.Период = втРейсы.Период
	               |			И втТаблицаАктивностей.Сотрудник = втРейсы.Сотрудник
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упТахографДанныеОДеятельностиВодителей КАК упТахографДанныеОДеятельностиВодителей
	               |		ПО втТаблицаАктивностей.Период = упТахографДанныеОДеятельностиВодителей.Период
	               |			И втТаблицаАктивностей.Сотрудник = упТахографДанныеОДеятельностиВодителей.Водитель";
	Запрос.УстановитьПараметр("тбТаблицаАктивностей", ТаблицаАктивностей);
	мсвСтатусов = Новый Массив;
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.Выполнен);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.Завершен);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);
	
	Запрос.УстановитьПараметр("СписокСтатусов", мсвСтатусов);
	Возврат Запрос.Выполнить().Выгрузить();		
КонецФункции

// Функция - Сформировать карту тахографа по сотруднику.
//
// Параметры:
//  сткПараметрыКарты	 - Структура - параметры карты из файла.
//  Сотрудник			 - СправочникСсылка.упСотрудники - сотрудник.
//  ТекстОшибки			 - Строка - текст ошибки в случае неудачи.
//  Отказ				 - Булево - Истина в случае ошибки.
// 
// Возвращаемое значение:
//   СправочникСсылка.упКартыТахографа - ссылка на карту соответствующую заданным параметрам.
//
Функция СформироватьКартуТахографаПоСотруднику(сткПараметрыКарты, Сотрудник, ТекстОшибки, Отказ) Экспорт
	
	Результат = Справочники.упКартыТахографа.ПустаяСсылка();
	
	НомерКарты 	= "";
	
	Отказ 		= Ложь;
	Если НЕ сткПараметрыКарты.Свойство("НомерКарты", НомерКарты) Тогда
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Не задан номер карты'");
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упКартыТахографа.Ссылка
		|ИЗ
		|	Справочник.упКартыТахографа КАК упКартыТахографа
		|ГДЕ
		|	упКартыТахографа.Код = &Код";
		Запрос.УстановитьПараметр("Код", НомерКарты);
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			КартаТахографа = Выборка.Ссылка;		
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(КартаТахографа) Тогда
			КартаТахографаОбъект = КартаТахографа.ПолучитьОбъект();
			ТекстСообщения = Нстр("ru = 'Карта тахографа с номером %1 по сотруднику %2 обновлена'");
		Иначе	
			КартаТахографаОбъект = Справочники.упКартыТахографа.СоздатьЭлемент();	
			ТекстСообщения = Нстр("ru = 'Добвалена карта тахографа с номером %1 по сотруднику %2.'");
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(КартаТахографаОбъект, сткПараметрыКарты);
		КартаТахографаОбъект.Код 			= НомерКарты;
		КартаТахографаОбъект.Сотрудник	= Сотрудник; 	
		КартаТахографаОбъект.Имя			= сткПараметрыКарты.ИмяВодителя; 	
		КартаТахографаОбъект.Фамилия		= сткПараметрыКарты.ФамилияВодителя; 	
		
		Попытка
			КартаТахографаОбъект.Записать();
			Результат = КартаТахографаОбъект.Ссылка;
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, КартаТахографаОбъект,,,Отказ);
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,КартаТахографаОбъект, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;

		Если НЕ Отказ Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НомерКарты, Сотрудник);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Результат);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура - Записать данные активности в регистр
//
// Параметры:
//  ТаблицаАктивностей	 - ТаблицаЗначений - заполненная таблица активностей по сотруднику.
//  ТекстСообщения		 - Строка - текст ошибки в случае неудачи.
//  Отказ				 - Булево - Истина в случае ошибки.
//
Процедура ЗаписатьДанныеАктивностиВРегистр(ТаблицаАктивностей, ТекстСообщения, Отказ) Экспорт
	
	СобытиеНачало	= Нстр("ru = 'ЗагрузкаИзТахографа.Начало'");
	СобытиеКонец 	= Нстр("ru = 'ЗагрузкаИзТахографа.Конец'");
		
	Этап	= Нстр("ru = 'Загружаются/изменяются записи данных тахографа'");
	ЗаписьЖурналаРегистрации(СобытиеНачало, УровеньЖурналаРегистрации.Информация,,, Этап);	
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	КоличествоЗаписей = 0;					
	
	мсвСтрокиТаблицы = ТаблицаАктивностей.НайтиСтроки(Новый Структура("Флаг",Истина));
	
	Для каждого Строка Из мсвСтрокиТаблицы Цикл
		
		Запись	= РегистрыСведений.упТахографДанныеОДеятельностиВодителей.СоздатьМенеджерЗаписи();
		Запись.Водитель	= Строка.Сотрудник;
		Запись.Период	= Строка.Период;
		ЗаполнитьЗначенияСвойств(Запись, Строка,,"Период");
						
		Попытка
			Запись.Записать(Истина);
			КоличествоЗаписей = КоличествоЗаписей + 1;
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ТекстСообщения = Нстр("ru = 'Ошибка:%1; Запись по водителю:%2'");	
			ТекстСообщения = СтрШаблон(ТекстСообщения, ТекстОшибки, Строка.Сотрудник);
			
			ЗаписьЖурналаРегистрации("Загрузка данных из тахографа", УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Прервать;
		КонецПопытки;
	КонецЦикла;
		
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
	
	Комментарий = Нстр("ru = 'Загружено %1 записей'");	
	Комментарий = СтрШаблон(Комментарий, КоличествоЗаписей);
	ЗаписьЖурналаРегистрации(СобытиеКонец, УровеньЖурналаРегистрации.Информация,,, Комментарий);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункции

Функция СотрудникПоНомеруКарты(НомерКарты)
	
	сткРезультат = Новый Структура("КартаТахографа, Сотрудник", Справочники.упКартыТахографа.ПустаяСсылка(), Справочники.упСотрудники.ПустаяСсылка());
	Результат = Справочники.упСотрудники.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упКартыТахографа.Сотрудник,
	               |	упКартыТахографа.Ссылка КАК КартаТахографа
	               |ИЗ
	               |	Справочник.упКартыТахографа КАК упКартыТахографа
	               |ГДЕ
	               |	упКартыТахографа.Код = &НомерКарты
	               |	И НЕ упКартыТахографа.ПометкаУдаления";
	Запрос.УстановитьПараметр("НомерКарты", НомерКарты);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(сткРезультат, Выборка);
	КонецЦикла; 
	
	Возврат сткРезультат;
		
КонецФункции

#КонецОбласти

#КонецЕсли