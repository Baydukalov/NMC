#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

&НаСервере
// Функция актуализирует показатели расчета расстояний между адресами.
//
// Параметры:
//  МассивЗаданий             - Массив - документы с типом "ДокументСсылка.упЗаданиеНаПеревозкуГруза".
//  МассивТранспортныхСредств - Массив - справочники с типом "СправочникСсылка.упТранспортныеСредства".
//
// Возвращаемое значение:
//   Структура - Структура с показателями состояния.
//
Функция ПолучитьСостояниеМассиваРасстояний(МассивЗаданий, МассивТранспортныхСредств) Экспорт
	
	РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД = ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД");
	Если РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1 1
		|ИЗ
		|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
		|ГДЕ
		|	упГеографическиеЗоны.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД
		|";
		Если Запрос.Выполнить().Пустой() Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСТр("ru = 'Не найдено географических зон с включенным признаком ""Рассчитывать расстояния с учетом дополнительных ограничений СитиГИД""!'"));
		КонецЕсли; 
	КонецЕсли; 
	сткСостояние = Новый Структура();
	ПериодАктуальности = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПериодАктуальностиРасчета");
	ОграниченияМаршрутизатора = упМаршрутизация.ПолучитьТаблицуОграниченийМаршрутизатора();
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Гаражи.Гараж.Адрес КАК Адрес
	|ПОМЕСТИТЬ втГаражи
	|ИЗ
	|	Справочник.упТранспортныеСредства.Гаражи КАК Гаражи
	|ГДЕ
	|	Гаражи.Ссылка В(&ТранспортныеСредства)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения КАК АдресПолучения,
	|	упЗаданиеНаПеревозкуГруза.ВидПеревозки.ВидТочкиОтправления КАК ВидАдресаОтправления,
	|	упЗаданиеНаПеревозкуГруза.Заказчик КАК Заказчик
	|ПОМЕСТИТЬ втИсходныеТочкиПодготовка
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка В(&Задания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втИсходныеТочки.Заказчик.АдресВозвратаДокументов КАК АдресВозвратаДокументов
	|ПОМЕСТИТЬ АдресаВозврата
	|ИЗ
	|	втИсходныеТочкиПодготовка КАК втИсходныеТочки
	|ГДЕ
	|	втИсходныеТочки.Заказчик.АдресВозвратаДокументов <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втИсходныеТочки.АдресОтправления КАК Адрес,
	|	втИсходныеТочки.АдресОтправления.ВидАдреса = втИсходныеТочки.ВидАдресаОтправления КАК ПриоритетноеПостроениеМассиваРасстояний
	|ПОМЕСТИТЬ втТочкиПодготовка
	|ИЗ
	|	втИсходныеТочкиПодготовка КАК втИсходныеТочки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втИсходныеТочки.АдресПолучения,
	|	ЛОЖЬ
	|ИЗ
	|	втИсходныеТочкиПодготовка КАК втИсходныеТочки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГаражи.Адрес,
	|	ИСТИНА
	|ИЗ
	|	втГаражи КАК втГаражи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АдресаВозврата.АдресВозвратаДокументов,
	|	ЛОЖЬ
	|ИЗ
	|	АдресаВозврата КАК АдресаВозврата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втТочки.Адрес) КАК Адрес,
	|	втТочки.Адрес.Широта КАК Широта,
	|	втТочки.Адрес.Долгота КАК Долгота,
	|	МАКСИМУМ(втТочки.ПриоритетноеПостроениеМассиваРасстояний
	|			ИЛИ втТочки.Адрес.ВидАдреса.ПриоритетноеПостроениеМассиваРасстояний) КАК ПриоритетноеПостроениеМассиваРасстояний,
	|	МАКСИМУМ(ЕСТЬNULL(упГеографическиеЗоны.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД, ЛОЖЬ)) КАК ДопОграниченияСитиГИД
	|ПОМЕСТИТЬ втАдреса
	|ИЗ
	|	втТочкиПодготовка КАК втТочки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	|			ПО (упИерархияГеографическихЗон.Родитель = упГеографическиеЗоны.Ссылка)
	|		ПО (втТочки.Адрес.ГеографическаяЗона = упИерархияГеографическихЗон.Зона)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТочки.Адрес.Широта,
	|	втТочки.Адрес.Долгота
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдресаПоЗонамНачало.Широта КАК НачальнаяШирота,
	|	втАдресаПоЗонамНачало.Долгота КАК НачальнаяДолгота,
	|	втАдресаПоЗонамНачало.ДопОграниченияСитиГИД
	|		И втАдресаПоЗонамОкончание.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД,
	|	втАдресаПоЗонамОкончание.Широта КАК КонечнаяШирота,
	|	втАдресаПоЗонамОкончание.Долгота КАК КонечнаяДолгота
	|ПОМЕСТИТЬ втМассивОтрезков
	|ИЗ
	|	втАдреса КАК втАдресаПоЗонамНачало
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдресаПоЗонамОкончание
	|		ПО (втАдресаПоЗонамНачало.Широта <> втАдресаПоЗонамОкончание.Широта
	|				ИЛИ втАдресаПоЗонамНачало.Долгота <> втАдресаПоЗонамОкончание.Долгота)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачальнаяШирота,
	|	НачальнаяДолгота,
	|	КонечнаяШирота,
	|	КонечнаяДолгота
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ОграниченияМаршрутизатора.Ограничение КАК Перечисление.упОграниченияМаршрутизаторов) КАК Ограничение
	|ПОМЕСТИТЬ ОграниченияМаршрутизатора
	|ИЗ
	|	&ОграниченияМаршрутизатора КАК ОграниченияМаршрутизатора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМассивОтрезков.НачальнаяШирота КАК НачальнаяШирота,
	|	втМассивОтрезков.НачальнаяДолгота КАК НачальнаяДолгота,
	|	втМассивОтрезков.КонечнаяШирота КАК КонечнаяШирота,
	|	втМассивОтрезков.КонечнаяДолгота КАК КонечнаяДолгота,
	|	ОграниченияМаршрутизатора.Ограничение КАК Ограничение,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.ПопыткаРасчета ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА упРасстоянияМеждуТочками.ПопыткаРасчета > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НеРассчитано,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упРасстоянияМеждуТочками.ДатаИзменения, &ДатаРасчета) < &ДатаРасчета
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Просрочено
	|ПОМЕСТИТЬ втРезультат
	|ИЗ
	|	втМассивОтрезков КАК втМассивОтрезков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОграниченияМаршрутизатора КАК ОграниченияМаршрутизатора
	|		ПО (втМассивОтрезков.ДопОграниченияСитиГИД
	|				ИЛИ ОграниченияМаршрутизатора.Ограничение = ЗНАЧЕНИЕ(Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	|		ПО втМассивОтрезков.НачальнаяШирота = упРасстоянияМеждуТочками.НачальнаяШирота
	|			И втМассивОтрезков.НачальнаяДолгота = упРасстоянияМеждуТочками.НачальнаяДолгота
	|			И втМассивОтрезков.КонечнаяШирота = упРасстоянияМеждуТочками.КонечнаяШирота
	|			И втМассивОтрезков.КонечнаяДолгота = упРасстоянияМеждуТочками.КонечнаяДолгота
	|			И (ОграниченияМаршрутизатора.Ограничение = упРасстоянияМеждуТочками.Ограничение)
	|			И (упРасстоянияМеждуТочками.ПопыткаРасчета = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдреса.Адрес КАК Адрес,
	|	втАдреса.ПриоритетноеПостроениеМассиваРасстояний КАК ПриоритетноеПостроениеМассиваРасстояний,
	|	втАдреса.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
	|ИЗ
	|	втРезультат КАК втРезультат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	|		ПО втРезультат.НачальнаяШирота = втАдреса.Широта
	|			И втРезультат.НачальнаяДолгота = втАдреса.Долгота
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдреса.Адрес,
	|	втАдреса.ПриоритетноеПостроениеМассиваРасстояний,
	|	втАдреса.ДопОграниченияСитиГИД
	|ИЗ
	|	втРезультат КАК втРезультат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	|		ПО втРезультат.КонечнаяШирота = втАдреса.Широта
	|			И втРезультат.КонечнаяДолгота = втАдреса.Долгота
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдреса.Адрес КАК Адрес,
	|	втАдреса.ПриоритетноеПостроениеМассиваРасстояний КАК ПриоритетноеПостроениеМассиваРасстояний,
	|	втАдреса.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
	|ИЗ
	|	втРезультат КАК втРезультат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	|		ПО втРезультат.НачальнаяШирота = втАдреса.Широта
	|			И втРезультат.НачальнаяДолгота = втАдреса.Долгота
	|ГДЕ
	|	втРезультат.НеРассчитано > 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдреса.Адрес,
	|	втАдреса.ПриоритетноеПостроениеМассиваРасстояний,
	|	втАдреса.ДопОграниченияСитиГИД
	|ИЗ
	|	втРезультат КАК втРезультат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	|		ПО втРезультат.КонечнаяШирота = втАдреса.Широта
	|			И втРезультат.КонечнаяДолгота = втАдреса.Долгота
	|ГДЕ
	|	втРезультат.НеРассчитано > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдреса.Адрес КАК Адрес,
	|	втАдреса.ПриоритетноеПостроениеМассиваРасстояний КАК ПриоритетноеПостроениеМассиваРасстояний,
	|	втАдреса.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
	|ИЗ
	|	втРезультат КАК втРезультат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	|		ПО втРезультат.НачальнаяШирота = втАдреса.Широта
	|			И втРезультат.НачальнаяДолгота = втАдреса.Долгота
	|ГДЕ
	|	втРезультат.НеРассчитано + втРезультат.Просрочено > 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдреса.Адрес,
	|	втАдреса.ПриоритетноеПостроениеМассиваРасстояний,
	|	втАдреса.ДопОграниченияСитиГИД
	|ИЗ
	|	втРезультат КАК втРезультат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАдреса КАК втАдреса
	|		ПО втРезультат.КонечнаяШирота = втАдреса.Широта
	|			И втРезультат.КонечнаяДолгота = втАдреса.Долгота
	|ГДЕ
	|	втРезультат.НеРассчитано + втРезультат.Просрочено > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втРезультат.НеРассчитано) КАК НеРассчитано,
	|	СУММА(1) КАК Всего,
	|	СУММА(втРезультат.Просрочено) КАК Просрочено
	|ИЗ
	|	втРезультат КАК втРезультат";
	Запрос.УстановитьПараметр("Задания", МассивЗаданий);
	Запрос.УстановитьПараметр("ТранспортныеСредства", МассивТранспортныхСредств);
	Запрос.УстановитьПараметр("ОграниченияМаршрутизатора", ОграниченияМаршрутизатора);
	Если ПериодАктуальности = 0 Тогда 
		Запрос.УстановитьПараметр("ДатаРасчета", '00010101');
	Иначе	
		Запрос.УстановитьПараметр("ДатаРасчета", ТекущаяДатаСеанса() - (ПериодАктуальности * 86400));
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	РассчитаноКоличество = 0;
	НеРассчитаноКоличество = 0;
	ПросроченоКоличество = 0;
	Рассчитано = 0;
	НеРассчитано = 0;
	Просрочено = 0;
	Если Не Результат[КоличествоТаблиц - 1].Пустой() Тогда
		Выборка = Результат[КоличествоТаблиц - 1].Выбрать();
		Выборка.Следующий();
		РассчитаноКоличество = Выборка.Всего - Выборка.НеРассчитано;
		НеРассчитаноКоличество = Выборка.НеРассчитано;
		ПросроченоКоличество = Выборка.Просрочено;
		Рассчитано = 100 * РассчитаноКоличество / Выборка.Всего;
		НеРассчитано = 100 * НеРассчитаноКоличество / Выборка.Всего;
		Просрочено = 100 * ПросроченоКоличество / Выборка.Всего;
	КонецЕсли;
	сткСостояние.Вставить("РассчитаноКоличество", РассчитаноКоличество);
	сткСостояние.Вставить("НеРассчитаноКоличество", НеРассчитаноКоличество);
	сткСостояние.Вставить("ПросроченоКоличество", ПросроченоКоличество);
	сткСостояние.Вставить("Рассчитано", Рассчитано);
	сткСостояние.Вставить("НеРассчитано", НеРассчитано);
	сткСостояние.Вставить("Просрочено", Просрочено);
	ТаблицаВсехАдресов = Результат[КоличествоТаблиц - 4].Выгрузить();
	сткСостояние.Вставить("ВсеАдреса", ТаблицаВсехАдресов);
	ТаблицаНерассчитанныхАдресов = Результат[КоличествоТаблиц - 3].Выгрузить();
	сткСостояние.Вставить("НерассчитанныеРасстояния", ТаблицаНерассчитанныхАдресов);
	ТаблицаПросроченныхАдресов = Результат[КоличествоТаблиц - 2].Выгрузить();
	сткСостояние.Вставить("ПросроченныеРасстояния", ТаблицаПросроченныхАдресов);
	
	Возврат сткСостояние;
	
КонецФункции // ПолучитьСостояниеМассиваРасстояний()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
