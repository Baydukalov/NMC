
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ИдентификаторЗадания = Параметры.ИдентификаторЗадания;
	АдресВременногоХранилища = Параметры.АдресВременногоХранилища;
	ТекущийЦикл = НСтр("ru = 'Подготовка данных ...'", КодЯзыка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьПроцентВыполнения", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершениеСозданияРейсов" Тогда
		Если Параметр = Ложь Тогда
			ТекущийЦикл = НСтр("ru = 'Автоматическое планирование не удалось'", КодЯзыка);
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(НСтр("ru = 'Автоматическое планирование не удалось'"), Истина, Ложь);
		Иначе
			ОтключитьОбработчикОжидания("ОбновитьПроцентВыполнения");
			ТекущийЦикл = НСтр("ru = 'Автоматическое планирование успешно завершено'", КодЯзыка);
			Если Не ВыведеныСообщенияПользователю Тогда
				// Закрываем форму асинхронно для исправления ошибки с крашем клиентского приложения 
				// из-за длинной последовательности оповещений между родителской формой 
				// и формой автоматического планирования рейсов (текущей).
				ПодключитьОбработчикОжидания("ЗакрытьОтложенно", 0.5, Истина);
			Иначе
				Элементы.ФормаЖурналСобытий.Видимость = Истина;
			КонецЕсли; 
		КонецЕсли; 
		Элементы.ФормаЗакрыть.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьОтложенно()
	Закрыть(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Процедура запускается из обрабочика ожидания.
// Инициирует обновление индикаторов. 
//
Процедура ОбновитьПроцентВыполнения()
	
	ВсеЗаданияВыполнены = Истина;
	Попытка
		ВсеЗаданияВыполнены = ОбновитьИндикаторыВыполненияФоновыхЗаданийНаКлиенте();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ВсеЗаданияВыполнены = Ложь;
		ОтключитьОбработчикОжидания("ОбновитьПроцентВыполнения");
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ОписаниеОшибки, Истина, Ложь);
	КонецПопытки; 
	
	Если ВсеЗаданияВыполнены Тогда 
		ОтключитьОбработчикОжидания("ОбновитьПроцентВыполнения");
		Оповестить("ОбработкаРешенияЗМТ", АдресВременногоХранилища);
	КонецЕсли;
	
КонецПроцедуры // ОбновитьПроцентВыполнения()

&НаСервере
Функция ОбновитьИндикаторыВыполненияФоновыхЗаданийНаСервере()
	
	выхДатаНачала = ЭтаФорма.ДатаНачала;
	
	ЗаданияВыполнены = Ложь;
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный));
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	мсвСообшений = Задание.ПолучитьСообщенияПользователю(Истина);
	выхДатаНачала = Задание.Начало + (ТекущаяДатаСеанса() - ТекущаяДата());
	Если мсвСообшений <> Неопределено Тогда
		Если мсвСообшений.Количество() Тогда
			Для Каждого текСообщение Из мсвСообшений Цикл
				Текст = текСообщение.Текст;
				Если Лев(Текст, 3) = "###" Тогда 
					КоличествоВыполнено = ОписаниеТиповЧисло.ПривестиЗначение(СтрЗаменить(Текст, "###", ""));
					Индикатор = Макс(Индикатор, КоличествоВыполнено);
				ИначеЕсли Лев(Текст, 3) = "///" Тогда
					ШагНовый = ОписаниеТиповЧисло.ПривестиЗначение(СтрЗаменить(Текст, "///", ""));
					Если ШагНовый > Шаг Тогда
						Индикатор = 0;
					КонецЕсли;
					Шаг = ШагНовый;
				ИначеЕсли Лев(Текст, 3) = "[[[" Тогда
					КоличествоШагов = ОписаниеТиповЧисло.ПривестиЗначение(СтрЗаменить(Текст, "[[[", ""));
				ИначеЕсли СтрНайти(Текст, "curcycle_") > 0 Тогда
					ЦиклПланирования = ОписаниеТиповЧисло.ПривестиЗначение(СтрЗаменить(Текст, "curcycle_", ""));
				ИначеЕсли СтрНайти(Текст, "maxcycle_") > 0 Тогда
					КоличествоЦиклов = ОписаниеТиповЧисло.ПривестиЗначение(СтрЗаменить(Текст, "maxcycle_", ""));
				ИначеЕсли СтрНайти(Текст, "operation_") > 0 Тогда
					ТекущаяОперация = СтрЗаменить(Текст, "operation_", "");	
				Иначе
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
					ВыведеныСообщенияПользователю = Истина;
				КонецЕсли;
			КонецЦикла;
			Если Шаг > 0 Тогда
				Шагомер = НСтр(СтрШаблон("ru = 'Выполнение итерации (шаг %1 из %2)'", Шаг, КоличествоШагов), КодЯзыка);
				Элементы.Индикатор.Видимость = Истина;
			КонецЕсли;
			Если КоличествоЦиклов = 1  Тогда
				ТекущийЦикл = НСтр("ru = 'Циклическое планирование отключено'", КодЯзыка);
			Иначе	
				ТекущийЦикл = НСтр(СтрШаблон("ru = 'Цикл планирования (%1 из %2)'", ЦиклПланирования, КоличествоЦиклов), КодЯзыка);
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	Если Задание = Неопределено Или Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда 
		ЗаданияВыполнены = Истина;
		Индикатор = 100;
		Если Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			ВызватьИсключение ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
		КонецЕсли; 
	КонецЕсли;
	
	Если ЗаданияВыполнены Тогда
		ФоновыеЗадания.ОжидатьЗавершения(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Задание), 1000);
		ТекущийЦикл = НСтр("ru = 'Создание рейсов ...'", КодЯзыка);
		ТекущаяОперация = "";
		Шагомер = "";
		Элементы.Индикатор.Видимость = Ложь;
	КонецЕсли;
	Возврат ЗаданияВыполнены;
	
КонецФункции // ОбновитьИндикаторыВыполненияФоновыхЗаданийНаСервере()

&НаКлиенте
Функция ОбновитьИндикаторыВыполненияФоновыхЗаданийНаКлиенте()
	
	ВсеЗаданияВыполнены = ОбновитьИндикаторыВыполненияФоновыхЗаданийНаСервере();
	Если ВсеЗаданияВыполнены Тогда 
		Элементы.ФормаОтмена.Доступность = Ложь;
	КонецЕсли;
	Возврат ВсеЗаданияВыполнены;
	
КонецФункции // ОбновитьИндикаторыВыполненияФоновыхЗаданийНаСервере()

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры // ПриЗакрытииНаСервере()

&НаКлиенте
Процедура ЖурналСобытий(Команда)
	
	ОтборПоУровню = Новый СписокЗначений;
	ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	ОтборПоУровню.Добавить("Предупреждение", "Предупреждение");
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
	ПараметрыФормы.Вставить("ДатаНачала", ДатаНачала);
	ПараметрыФормы.Вставить("Пользователь", ИмяПользователя());
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы);
	Закрыть();

КонецПроцедуры

#КонецОбласти 
