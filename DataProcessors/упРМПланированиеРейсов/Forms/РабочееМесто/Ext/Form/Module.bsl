
#Область ПеременныеМодуляФормы

&НаКлиенте
Перем РезультатПланирования;

&НаКлиенте
// 0 - множественное выделение выключено.
// 1 - множественное выделение.
// 2 - множественное снятие выделения.
//
Перем РежимВыделенияНаКарте;

&НаКлиенте
Перем ПроизведенПереходПоСсылке;

&НаКлиенте
// При выделении маркера на карте позиционирование на строке списка заданий не должно вызывать вызов события "ПриАктивизацииСтроки".
Перем РазрешитьАктивизациюСтроки;

&НаКлиенте
Перем РазрешитьСобытиеВыборВСпискеЗаданий;

&НаКлиенте
Перем РазрешитьСобытиеВыборВДеревеРейсов;

&НаКлиенте
Перем ПутьКФайлу_СостояниеСрочногоГруза;

// Переменные для ускорения работы с html
&НаКлиенте
Перем СлужебныеЭлементыHtml;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Параметры.Свойство("ВидТранспортнойУслуги", ЭтаФорма.ВидТранспортнойУслуги);
	Если Не ЗначениеЗаполнено(ЭтаФорма.ВидТранспортнойУслуги) Тогда
		ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов;
	КонецЕсли;
	РежимФормированияКонтейнеров = (ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами);
	ВидТранспортнойУслугиПредставление = ОбщегоНазначения.ИмяЗначенияПеречисления(ВидТранспортнойУслуги);
	ОбновитьВидРабочегоМеста();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();	
	КодЯзыка     = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ВидПеревозки = Справочники.упВидыПеревозок.Основной;
	
	текНачалоДня = НачалоДня(ТекущаяДатаСеанса());
	НачалоПериодаПланирования    = текНачалоДня;
	ОкончаниеПериодаПланирования = текНачалоДня + 86400;
		
	УчитыватьМассу                             = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	УчитыватьОбъем                             = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	УчитыватьКоличествоМест                    = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	УчитыватьКоличествоЗагрузочныхМетров       = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	ИспользуютсяЭлектронныеГеографическиеКарты = Не РежимФормированияКонтейнеров И ПолучитьФункциональнуюОпцию("упИспользуютсяЭлектронныеГеографическиеКарты");
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
		ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	УстановитьДоступностьЭлектроннойКарты(Истина);
	
	ИнициализироватьКомпоновщикНастроек();
	ВыполнитьПервоначальноеЗаполнениеПоказателейЗагрузки();
	
	ВосстановитьОбщиеНастройкиФормы();
    УстановитьДоступныеЗначенияВариантовСписков();
	УстановитьДоступныеЗначенияВариантовПланирования();
	УстановитьПараметрыПланированияНаСервере();
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		ОбновитьТаблицуЗаданийНаСервере();
		ВосстановитьВариантНабораДоступныхТС();
		ОбновитьДеревоРейсовНаСервере();
	КонецЕсли; 
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		ОбновитьСписокМестИнтересаНаСервере();
		ОбновитьСписокНезависимыхМаркеровНаСервере();
	КонецЕсли; 
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	Если Не ИспользуютсяЭлектронныеГеографическиеКарты Тогда // для передачи на клиент полной таблицы заданий
		ОбновитьТаблицуЗаданийНаСервере();
		ВосстановитьВариантНабораДоступныхТС();
		ОбновитьДеревоРейсовНаСервере();
	КонецЕсли;
	
	РазрешитьСобытиеВыборВСпискеЗаданий  = Истина;
	РазрешитьСобытиеВыборВДеревеРейсов   = Истина; 
	РазрешитьАктивизациюСтроки           = Истина;
	
	РежимОтображенияКарты = 0;
	ЧислоПопыток = 0;
	ПроизведенПереходПоСсылке = Ложь;
	
	#Если ВебКлиент Тогда
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ПоказатьПредупреждение(,Нстр("ru = 'Работоспособность рабочего места при использовании электронных географических карт в веб-клиенте не поддерживается.'"));
			Отказ=Истина;
			Возврат;
		КонецЕсли; 
		
	#Иначе	
		
		// Переопределение текущих элементов таблиц формы для изменения активизации поля при первичной загрузке.
		Элементы.СписокЗаданий.ТекущийЭлемент = Элементы.СписокЗаданийПредставление;
		Элементы.ДеревоРейсы.ТекущийЭлемент = Элементы.ДеревоРейсыПредставление;
		Элементы.ПоказателиЗагрузкиСпискаЗаданий.ТекущийЭлемент = Элементы.ПоказателиЗагрузкиСпискаЗаданийКатегория;
		//Элементы.АдресаОперацийСКонтейнерами.ТекущийЭлемент = Элементы.АдресаОперацийСКонтейнерамиАдресОтправления;
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			
			Элементы.ОтобразитьЗоны.Пометка = ОтображатьЗоны;
			Элементы.ОтобразитьМестаИнтереса.Пометка = ОтображатьМестаИнтереса;		
			Элементы.ВключитьКластеризацию.Пометка = ИспользоватьКластеризацию;		
			
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Широта%", ПредставлениеЧисла(НачальнаяШирота));
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Долгота%", ПредставлениеЧисла(НачальнаяДолгота));
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Увеличение%", ПредставлениеЧисла(НачальныйЗум));	
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			БиблиотекаКартинок.упТумблер.Записать(ИмяВременногоФайла);
			ИмяВременногоФайла = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%toggle%", ИмяВременногоФайла);
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			БиблиотекаКартинок.упМаркерПростойБелый.Записать(ИмяВременногоФайла);
			ИмяВременногоФайла = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерРедактируемогоУгла%", ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = БиблиотекаКартинок.упМаркерПростойБелый;
			НовыйСохраненныйМаркер.Файл   = ИмяВременногоФайла;
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			ПустаяКартинка = Новый Картинка;
			ПустаяКартинка.Записать(ИмяВременногоФайла);
			БиблиотекаКартинок.упМаркерПростойЖелтый.Записать(ИмяВременногоФайла);
			ИмяВременногоФайла = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПервогоУгла%", ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = БиблиотекаКартинок.упМаркерПростойЖелтый;
			НовыйСохраненныйМаркер.Файл   = ИмяВременногоФайла;
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
			ПустаяКартинка.Записать(ИмяВременногоФайла);
			БиблиотекаКартинок.упМаркерПростойКрасный.Записать(ИмяВременногоФайла);
			ИмяВременногоФайла = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПоследнегоУгла%", ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = БиблиотекаКартинок.упМаркерПростойКрасный;
			НовыйСохраненныйМаркер.Файл   = ИмяВременногоФайла;
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			БиблиотекаКартинок.упГеокодируемыйМаркер.Записать(ИмяВременногоФайла);
			HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерГеокодирования%", ИзменитьФорматВременногоФайла(ИмяВременногоФайла));
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
			БиблиотекаКартинок.упСостояниеСрочныйГруз.Записать(ИмяВременногоФайла);
			ИмяВременногоФайла = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			ПутьКФайлу_СостояниеСрочногоГруза = ИмяВременногоФайла;
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = БиблиотекаКартинок.упСостояниеСрочныйГруз;
			НовыйСохраненныйМаркер.Файл   = ИмяВременногоФайла;
		Иначе
			//СписокЗаданий
			Элементы.СписокЗаданий.Обновить();
		КонецЕсли;
		
		//ИзменитьДоступностьРейсов();
		ОбновитьДоступность();
		ПодключитьОбработчикОжидания("ПроверкаВыделенияВсехЗаданий", 1, Ложь);
		
	#КонецЕсли
	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ПроверкаВыделенияВсехЗаданий()
	
	#Если НЕ ВебКлиент Тогда	
	Если Истина
		И Не ВыделениеВсехСтрокОбработано
		И Элементы.СписокЗаданий.ВыделенныеСтроки.Количество() = СписокЗаданий.Количество() 
	Тогда
		ЗапуститьСписокЗаданийПриАктивизацииСтроки();
		//СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания();
		ЭтаФорма.ВыделениеВсехСтрокОбработано = Истина;
	КонецЕсли; 
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	#Если НЕ ВебКлиент Тогда	
		мсвСобытий = СтрРазделить(ИмяСобытия, упСервисныеФункцииКлиент.РазделительИменСобытий());
		Для каждого ИмяОбрабатываемогоСобытия Из мсвСобытий Цикл
			Если Ложь
				Или ИмяОбрабатываемогоСобытия = "ЗаписьДокументаупРейс"
				Или ИмяОбрабатываемогоСобытия = "ИзменилсяМаршрут"
			Тогда
				Если ИмяОбрабатываемогоСобытия = "ЗаписьДокументаупРейс" Тогда
					// Запись рейса или корректировки рейса
					Рейс = Параметр.Ссылка;
				Иначе
					Рейс = Источник;
				КонецЕсли; 
				текСтрокаДерева = Неопределено;
				мсвЗаданий = Новый Массив;
				ИндексСтрокиРейса = Истина;
				ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
				Для ИндексСтрокиЦикла = 0 По ЭлементыДерева.Количество() - 1 Цикл
					ЭлементРейс = ЭлементыДерева[ИндексСтрокиЦикла];
					Если ЭлементРейс.Рейс = Рейс Тогда
						ЭлементРейс = ЭлементыДерева[ИндексСтрокиЦикла];
						текСтрокаДерева = ЭлементРейс;
						ИндексСтрокиРейса = ИндексСтрокиЦикла;
						ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
						Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
							мсвЗаданий.Добавить(ЭлементЗадание.ЗаданиеНаПеревозку);
						КонецЦикла;
						
						Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
							// удаление объектов рейса с карты
							Если текСтрокаДерева.ИндексВМассивеМаршрут > 0 Тогда
								УдалитьОтображениеРейса(текСтрокаДерева.ИндексВМассивеМаршрут);
								текСтрокаДерева.ИндексВМассивеМаршрут = 0;
							КонецЕсли;
							ОчиститьМаркерыРейсов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИндексСтрокиРейса));
						КонецЕсли;
						
						сткДополнительныеРеквизиты = Новый Структура;
						сткДополнительныеРеквизиты.Вставить("НомерЦветовойСхемы", текСтрокаДерева.НомерЦветовойСхемы);
						ОбновитьВетвьДереваРейсовНаСервере(Рейс, ИндексСтрокиЦикла, сткДополнительныеРеквизиты);
						Прервать;
					ИначеЕсли Истина
						И Параметр <> Неопределено
						И Не ЗначениеЗаполнено(ЭлементРейс.Рейс) 
						И ЭлементРейс.Перевозчик = Параметр.Перевозчик 
						И ЭлементРейс.ТипТС = Параметр.ТипТС 
						И ЭлементРейс.ТранспортноеСредство = Параметр.ТранспортноеСредство
					Тогда
						текСтрокаДерева = ЭлементРейс;
						ИндексСтрокиРейса = ИндексСтрокиЦикла;
						мсвЗаданий = ВернутьЗаданияРейсовНаСервере(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Рейс));
						Прервать;
					КонецЕсли;
				КонецЦикла; 
				Если текСтрокаДерева <> Неопределено Тогда
					Элементы.ДеревоРейсы.Развернуть(ЭлементыДерева.Получить(ИндексСтрокиРейса).ПолучитьИдентификатор(), Ложь);
					
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ОтобразитьРейсыНаКарте();
					КонецЕсли;
					
					ЭлементыРейса = текСтрокаДерева.ПолучитьЭлементы();
					Для каждого ЭлементРейса Из ЭлементыРейса Цикл
						мсвЗаданий.Добавить(ЭлементРейса.ЗаданиеНаПеревозку);
					КонецЦикла; 
					
					мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
					мсвОтправлений = Новый Массив;
					Для каждого Задание Из мсвЗаданий Цикл
						ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозку", Задание));
						КоличествоСтрок = ИскомыеСтроки.Количество();
						текИндекс = 0;
						Сч = 0;
						Пока Сч < КоличествоСтрок Цикл
							ИскомаяСтрока = ИскомыеСтроки.Получить(Сч);
							Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
								мсвОтправлений.Добавить(ИскомаяСтрока.АдресОтправления);
								ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеПолучения, Ложь);
								Если РежимОтображенияКарты = 1 Тогда // линии
									ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеЛиния, Ложь);
								ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
									ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеМаршрут, Ложь);
								КонецЕсли;
							КонецЕсли;
							текИндекс = СписокЗаданий.Индекс(ИскомаяСтрока);
							УдалитьСтрокуСпискаЗаданий(ИскомаяСтрока);
							Сч = Сч + 1;
						КонецЦикла;
					КонецЦикла;
					Если мсвОтправлений.Количество() Тогда
						ОбработатьСостояниеОтправлений(мсвОтправлений);
					КонецЕсли;
					
					мсвОтборов = Новый Массив;
					Отбор = Новый Структура;
					Отбор.Вставить("ИмяПоля"       , "ЗаданиеНаПеревозку");
					Отбор.Вставить("ПравоеЗначение", мсвЗаданий);
					Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);
					мсвОтборов.Добавить(Отбор);
					ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь); 
					//сткНастройки = ВернутьВариантНастройкиСпискаЗаданий();
					//Если сткНастройки <> Неопределено Тогда
					//	КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
					//КонецЕсли;
					
					НовыеСтроки = Новый Массив;
					КоличествоСтрок = СписокЗаданий.Количество();
					Пока КоличествоДобавленныхСтрок > 0 Цикл
						ИндексСтрокиРейса = КоличествоСтрок - КоличествоДобавленныхСтрок;
						ТекущаяСтрока = СписокЗаданий[ИндексСтрокиРейса];
						НовыеСтроки.Добавить(ТекущаяСтрока);
						КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
						СписокЗаданий.Сдвинуть(ИндексСтрокиРейса, текИндекс - ИндексСтрокиРейса);
					КонецЦикла;
					
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ОтобразитьОбъектыНаКарте(НовыеСтроки);
					КонецЕсли;
					ОбновитьДоступность();
					
				КонецЕсли;
				
			ИначеЕсли ИмяОбрабатываемогоСобытия = "ЗаписьДокументаупЗаданиеНаПеревозкуГруза" Тогда	
				
				Задание = Параметр.Ссылка;
				ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозку", Задание));
				КоличествоСтрок = ИскомыеСтроки.Количество();
				Если КоличествоСтрок > 0 Тогда
					текИндекс = 0;
					Сч = 0;
					мсвОтправлений = Новый Массив;
					Пока Сч < КоличествоСтрок Цикл
						ИскомаяСтрока = ИскомыеСтроки.Получить(Сч);
						
						Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
							мсвОтправлений.Добавить(ИскомаяСтрока.АдресОтправления);
							ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеПолучения, Ложь);
							Если РежимОтображенияКарты = 1 Тогда // линии
								ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеЛиния, Ложь);
							ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
								ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеМаршрут, Ложь);
							КонецЕсли;
						КонецЕсли;
						текИндекс = СписокЗаданий.Индекс(ИскомаяСтрока);
						УдалитьСтрокуСпискаЗаданий(ИскомаяСтрока);
						Сч = Сч + 1;
					КонецЦикла;
					
					Если мсвОтправлений.Количество() Тогда
						ОбработатьСостояниеОтправлений(мсвОтправлений);
					КонецЕсли;
					
					мсвОтборов = Новый Массив;
					Отбор = Новый Структура;
					Отбор.Вставить("ИмяПоля"       , "ЗаданиеНаПеревозку");
					Отбор.Вставить("ПравоеЗначение", Задание);
					Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.Равно);
					мсвОтборов.Добавить(Отбор);
					ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь); 
					
					НовыеСтроки = Новый Массив;
					КоличествоСтрок = СписокЗаданий.Количество();
					Пока КоличествоДобавленныхСтрок > 0 Цикл
						ИндексСтрокиРейса = КоличествоСтрок - КоличествоДобавленныхСтрок;
						ТекущаяСтрока = СписокЗаданий[ИндексСтрокиРейса];
						НовыеСтроки.Добавить(ТекущаяСтрока);
						КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
						СписокЗаданий.Сдвинуть(ИндексСтрокиРейса, текИндекс - ИндексСтрокиРейса);
					КонецЦикла;
					
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ОтобразитьОбъектыНаКарте(НовыеСтроки);
					КонецЕсли; 
					ОбновитьДоступность();
				КонецЕсли;
				
			ИначеЕсли ИмяОбрабатываемогоСобытия = "ОбработкаРешенияЗМТ" Тогда	
				
				АдресВременногоХранилища = Параметр;
				Если АдресВременногоХранилища <> "" Тогда
					Отказ = Ложь;
					ТекстОшибки = "";
					СформироватьРейсыНаСервере(АдресВременногоХранилища, Отказ, ТекстОшибки);
					РезультатПланирования = Не Отказ;
					ПодключитьОбработчикОжидания("ЗавершениеИзмененияМаршрутаРейсаСЗадержкой", 1, Истина);
					//Оповестить("ЗавершениеСозданияРейсов", Не Отказ); 
				КонецЕсли;		
				
			ИначеЕсли ИмяОбрабатываемогоСобытия = "ОбработкаРешенияЗК" Тогда	
				
				Если ТипЗнч(Параметр) = Тип("Массив") И Параметр.Количество() Тогда
					Для каждого текРешение Из Параметр Цикл
						АдресВременногоХранилища = текРешение.АдресВременногоХранилища;
						Рейс                     = текРешение.Рейс;
						Если АдресВременногоХранилища <> "" Тогда
							сткВозврат = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
							Если сткВозврат <> Неопределено И ТипЗнч(сткВозврат) = Тип("Структура") Тогда
								ТекстОшибки = "";
								ИзменитьПорядокОбъездаТочекРейса(сткВозврат, Рейс, ПараметрыПланирования, ТекстОшибки);
								Если ТекстОшибки <> "" Тогда
									ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
								КонецЕсли;
							КонецЕсли; 
						КонецЕсли;	
					КонецЦикла;
					Если ТекстОшибки = "" Тогда
						Оповестить("ЗавершениеИзмененияМаршрутаРейса"); 
					КонецЕсли;
				КонецЕсли;	
				
			ИначеЕсли ИмяОбрабатываемогоСобытия = "ИзменениеРежимаИспользованияЭлектронныхГеографическихКарт" Тогда	
				
				Обработчик = Новый ОписаниеОповещения("ИзменениеРежимаИспользованияЭлектронныхГеографическихКартЗавершение", ЭтотОбъект);
				ТекстВопроса = НСтр("ru = 'Изменен режим использования электронных географических карт. Для продолжения работы с рабочим местом планирования требуется перезапуск обработки'");
				ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОК, 120); 
				
			КонецЕсли; 
			
		КонецЦикла;
		
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеИзмененияМаршрутаРейсаСЗадержкой()
	
	Оповестить("ЗавершениеСозданияРейсов", РезультатПланирования); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	#Если НЕ ВебКлиент Тогда
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		СохранитьОбщиеНастройкиФормы();
	#КонецЕсли	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

#Область ЭлементыФормы_АдресаОперацийСКонтейнерами

&НаКлиенте
Процедура АдресаОперацийСКонтейнерамиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	#Если НЕ ВебКлиент Тогда
	текСтрока = Элементы.АдресаОперацийСКонтейнерами.ТекущиеДанные;
	текЭлемент = Элементы.АдресаОперацийСКонтейнерами.ТекущийЭлемент;
	Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
		Если текЭлемент.Имя = Элементы.АдресаОперацийСКонтейнерамиПометка.Имя Тогда
			Если ИдентификаторПомеченнойСтрокиФК >= 0 Тогда
				текПомеченная = АдресаОперацийСКонтейнерами.НайтиПоИдентификатору(ИдентификаторПомеченнойСтрокиФК);
				Если Не текПомеченная = Неопределено И текПомеченная <> текСтрока Тогда
					текПомеченная.Пометка = Ложь;
				КонецЕсли;
			КонецЕсли; 
			
			текСтрока.Пометка = Не текСтрока.Пометка;
			Если текСтрока.Пометка Тогда
				ЭтаФорма.АдресОтправленияКонтейнера = текСтрока.АдресОтправления;
				ЭтаФорма.АдресПолученияКонтейнера   = текСтрока.АдресПолучения;
				ИдентификаторПомеченнойСтрокиФК     = текСтрока.ПолучитьИдентификатор();
			Иначе
				ЭтаФорма.АдресОтправленияКонтейнера = Неопределено;
				ЭтаФорма.АдресПолученияКонтейнера   = Неопределено;
				ИдентификаторПомеченнойСтрокиФК     = -1;
			КонецЕсли;
			Элементы.СписокЗаданий.ОтборСтрок = Новый ФиксированнаяСтруктура("АдресОтправления, АдресПолучения", АдресОтправленияКонтейнера, АдресПолученияКонтейнера);
			
			ОбновитьПоказателиПоВыбраннымСтрокамСпискаЗаданий();
			ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
			ОбновитьДоступность();
		КонецЕсли;	
	КонецЕсли;
	#КонецЕсли	
	
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыФормы_СписокЗаданий

&НаКлиенте
Процедура СписокЗаданийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("СписокЗаданийВыборОбработчикОжидания", 0.1, Истина);
	
	//Если РазрешитьСобытиеВыборВСпискеЗаданий Тогда
		текСтрока = Элементы.СписокЗаданий.ТекущиеДанные;
		Если Истина
			И текСтрока <> Неопределено 
			И НЕ (Ложь
			Или Поле = Элементы.СписокЗаданийВыбрано 
			Или Поле = Элементы.СписокЗаданийАктивно 
			Или Поле = Элементы.СписокЗаданийПометка
			Или Поле = Элементы.СписокЗаданийПозиционирование 
			Или Поле = Элементы.СписокЗаданийПозиционированиеНаТочкеПолучения) 
			Тогда
			ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ЗаданиеНаПеревозку), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	//КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийВыборОбработчикОжидания()
	
	#Если НЕ ВебКлиент Тогда
	текСтрока = Элементы.СписокЗаданий.ТекущиеДанные;
	текЭлемент = Элементы.СписокЗаданий.ТекущийЭлемент;
	Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
		Если текЭлемент.Имя = Элементы.СписокЗаданийВыбрано.Имя Тогда
			Если Ложь
				Или Не ЗначениеЗаполнено(текСтрока.ПредыдущееЗадание) 
				Или текСтрока.ПредыдущееЗадание = текСтрока.ЗаданиеНаПеревозку 
			Тогда
				ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
				Выбрано = НЕ текСтрока.Выбрано;
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					мсвОтправлений = Новый Массив;
					Для каждого Строка Из ИскомыеСтроки Цикл
						Строка.Выбрано = Выбрано;
						Если Строка.Геокодировано Тогда 
							Режим = ?(Выбрано, 2, 1);
							ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения, Режим, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
							мсвОтправлений.Добавить(Строка.АдресОтправления);
						КонецЕсли;
						СвязанныеЗадания = СвязанныеСтрокиЗаданий(Строка, СписокЗаданий);
						Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
							СвязанноеЗадание.Выбрано = Выбрано;
							Если СвязанноеЗадание.Геокодировано Тогда 
								ИзменитьИконкуМаркераПоВидуАдреса(СвязанноеЗадание.ВидАдресаПолучения, СвязанноеЗадание.ИндексВМассивеПолучения, Режим, ОпределитьКоэффициентПересчета(СвязанноеЗадание), (ВыделятьСрочныеГрузы И СвязанноеЗадание.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
							КонецЕсли; 
						КонецЦикла;
					КонецЦикла;
					Если мсвОтправлений.Количество() Тогда
						мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
					КонецЕсли;
					Для каждого текОтправление Из мсвОтправлений Цикл
						Если Выбрано Тогда
							ИзменитьИконкуОтправления(текОтправление, 2); 
						Иначе
							ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Выбрано", текОтправление, Истина));
							Если ИскомыеСтроки.Количество() = 0 Тогда
								УбратьВыборМаркера(текОтправление);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла; 
				Иначе
					Для каждого Строка Из ИскомыеСтроки Цикл
						Строка.Выбрано = Выбрано;
					КонецЦикла;
				КонецЕсли; 
				ОбновитьПоказателиПоВыбраннымСтрокам();
			КонецЕсли; 
		ИначеЕсли текЭлемент.Имя = Элементы.СписокЗаданийПометка.Имя Тогда
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
			Пометка = НЕ текСтрока.Пометка;
			мсвОтправлений = Новый Массив;
			Для каждого Строка Из ИскомыеСтроки Цикл
				Строка.Пометка = Пометка;
				мсвОтправлений.Добавить(Строка.АдресОтправления);
				ИзменитьВидимостьПоСтрокеСпискаЗаданий(Строка);
			КонецЦикла; 
			Если мсвОтправлений.Количество() Тогда
				мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
			КонецЕсли;
			Для каждого текОтправление Из мсвОтправлений Цикл
				Если Пометка Тогда
					ИзменитьВидимостьТочкиОтправления(текОтправление, Пометка);
				Иначе
					ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Пометка", текОтправление, Истина));
					Если ИскомыеСтроки.Количество() = 0 Тогда
						ИзменитьВидимостьТочкиОтправления(текОтправление, Пометка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли текЭлемент.Имя = Элементы.СписокЗаданийАктивно.Имя Тогда
			Если Ложь
				Или Не ЗначениеЗаполнено(текСтрока.ПредыдущееЗадание) 
				Или текСтрока.ПредыдущееЗадание = текСтрока.ЗаданиеНаПеревозку 
			Тогда
				ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
				Активно = НЕ текСтрока.Активно;
				Для каждого Строка Из ИскомыеСтроки Цикл
					Если Строка.ТипПеревозки <> 3 Тогда
						Строка.Активно = Активно;
					КонецЕсли;
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ИзменитьПрозрачностьМаркера(Строка.ИндексВМассивеПолучения, ?(Строка.Активно, 0, 50));
					КонецЕсли;
					СвязанныеЗадания = СвязанныеСтрокиЗаданий(Строка, СписокЗаданий);
					Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
						СвязанноеЗадание.Активно = Активно;
						Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
							ИзменитьПрозрачностьМаркера(СвязанноеЗадание.ИндексВМассивеПолучения, ?(СвязанноеЗадание.Активно, 0, 50));
						КонецЕсли; 
					КонецЦикла;
				КонецЦикла;
			КонецЕсли; 
		ИначеЕсли текЭлемент.Имя = Элементы.СписокЗаданийПозиционирование.Имя Тогда
			Если текСтрока.Пометка Тогда
				ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текСтрока.АдресОтправления));
				Если ИскомыеСтроки.Количество() Тогда
					ЦентрироватьСлойНаКарте(ИскомыеСтроки[0].ИндексВМассиве);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли текЭлемент.Имя = Элементы.СписокЗаданийПозиционированиеНаТочкеПолучения.Имя Тогда
			Если текСтрока.Пометка Тогда
				ЦентрироватьСлойНаКарте(текСтрока.ИндексВМассивеПолучения);
			КонецЕсли;
		КонецЕсли;
		Если Ложь
			Или текЭлемент.Имя = Элементы.СписокЗаданийВыбрано.Имя
			Или текЭлемент.Имя = Элементы.СписокЗаданийПометка.Имя
			Или текЭлемент.Имя = Элементы.СписокЗаданийАктивно.Имя
			Или текЭлемент.Имя = Элементы.СписокЗаданийПозиционирование.Имя
			Или текЭлемент.Имя = Элементы.СписокЗаданийПозиционированиеНаТочкеПолучения.Имя 
		Тогда
			РазрешитьСобытиеВыборВСпискеЗаданий = Ложь;
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПриАктивизацииСтроки(Элемент)
	
	#Если НЕ ВебКлиент Тогда	
		Если РазрешитьАктивизациюСтроки Тогда
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				Если КартаСформирована И СписокЗаданийСформирован Тогда
					// Обработка активизации строки запускается с некоторой задержкой из-за несвоевременного.
					// Обновления массива выделенных строк таблицы формы при снятии выделения с зажатой клавишей Ctrl.	
					ЗапуститьСписокЗаданийПриАктивизацииСтроки();
					//СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания();
				КонецЕсли;
			Иначе
				ЗапуститьСписокЗаданийПриАктивизацииСтроки();
				//СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания();
			КонецЕсли;
			ЭтаФорма.ВыделениеВсехСтрокОбработано = Ложь;
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если ИспользуютсяЭлектронныеГеографическиеКарты И Не КартаСформирована Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ПараметрыПеретаскивания.Значение.Количество() Тогда
		ПерваяСтрокаМассива = ПараметрыПеретаскивания.Значение[0];
		Если ТипЗнч(ПерваяСтрокаМассива) = Тип("Число") Тогда
			// перетаскивание заданий в списке заданий
		ИначеЕсли ПерваяСтрокаМассива.Свойство("Рейс") Тогда
			Отказ = Ложь;
			Для каждого СтрокаРейса Из ПараметрыПеретаскивания.Значение Цикл
				Если СтрокаРейса.Уровень = 0 Тогда
					Если Не СтрокаРейса.ДоступноИзменениеМаршрута Тогда
						Отказ = Истина;
						Прервать;
					КонецЕсли;
				Иначе
					текРодитель = СтрокаРейса.ПолучитьРодителя();
					Если Не текРодитель.ДоступноИзменениеМаршрута Тогда
						Отказ = Истина;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; 
			
			Если Отказ Тогда
				// запрет удаления заданий из рейса
				ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаданийПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ПараметрыПеретаскивания.Значение.Количество() Тогда
		ПерваяСтрокаМассива = ПараметрыПеретаскивания.Значение[0];
		Если ТипЗнч(ПерваяСтрокаМассива) = Тип("Число") Тогда
			// перетаскивание заданий в списке заданий
		ИначеЕсли ТипЗнч(ПерваяСтрокаМассива) = Тип("ДанныеФормыЭлементДерева") И ПерваяСтрокаМассива.Свойство("Рейс") Тогда
			// перетаскивание заданий из дерева рейсов
			ПеренестиСтрокиИзДереваРейсовВСписокЗаданийНаКлиенте(ПараметрыПеретаскивания.Значение);
		КонецЕсли;
		//ПервыйЗапуск = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыФормы_ПоказателиЗагрузки
	
#КонецОбласти 

#Область ЭлементыФормы_ДеревоРейсы

&НаКлиенте
Процедура ДеревоРейсыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("ДеревоРейсыВыборОбработчикОжидания", 0.1, Истина);

	//Если РазрешитьСобытиеВыборВДеревеРейсов Тогда
		текСтрока = Элементы.ДеревоРейсы.ТекущиеДанные;
		Если текСтрока <> Неопределено
			И НЕ (Ложь
			Или Поле = Элементы.ДеревоРейсыВыбран 
			Или Поле = Элементы.ДеревоРейсыПометка 
			Или Поле = Элементы.ДеревоРейсыАктивно
			Или Поле = Элементы.ДеревоРейсыПозиционирование
			Или Поле = Элементы.ДеревоРейсФильтрПоАдресам) 
		Тогда
			СтандартнаяОбработка = Ложь;
			Если ЗначениеЗаполнено(текСтрока.Рейс) Тогда
				Если текСтрока.Уровень = 0 Тогда
					ОткрытьФорму("Документ.упРейс.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Рейс), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				ИначеЕсли текСтрока.Уровень = 1 Тогда
					ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ЗаданиеНаПеревозку), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(текСтрока.ТранспортноеСредство) Тогда
				ПоказатьЗначение(, текСтрока.ТранспортноеСредство);
			ИначеЕсли ЗначениеЗаполнено(текСтрока.ТипТС) Тогда
				ПоказатьЗначение(, текСтрока.ТипТС);
			КонецЕсли; 
		КонецЕсли;
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРейсыВыборОбработчикОжидания()
	
	РазрешитьСобытиеВыборВДеревеРейсов = Истина;
	#Если НЕ ВебКлиент Тогда
	текСтрока = Элементы.ДеревоРейсы.ТекущиеДанные;
	текЭлемент = Элементы.ДеревоРейсы.ТекущийЭлемент;
	Если текЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если текСтрока <> Неопределено Тогда 
		Если Истина
			И ЗначениеЗаполнено(текСтрока.Рейс)
			И КартыНеИспользуютсяИлиКартаСформирована()
		Тогда
			Если текСтрока.Уровень = 0 Тогда
				Если текЭлемент.Имя = Элементы.ДеревоРейсыВыбран.Имя И текСтрока.ДоступноИзменениеМаршрута Тогда
					текСтрока.Выбран = Не текСтрока.Выбран;
					Если ИспользуютсяЭлектронныеГеографическиеКарты И текСтрока.Геокодирован И текСтрока.НомерЦветовойСхемы > 0 Тогда
						текЦветоваяСхема = ЦветовыеСхемы[текСтрока.НомерЦветовойСхемы - 1];
					КонецЕсли; 
					ЭлементыЗадания = текСтрока.ПолучитьЭлементы();
					Для каждого текЗадание Из ЭлементыЗадания Цикл
						текЗадание.Выбран = текСтрока.Выбран;
						Если ИспользуютсяЭлектронныеГеографическиеКарты И текСтрока.Геокодирован И текСтрока.НомерЦветовойСхемы > 0 Тогда
							Если текЗадание.Выбран Тогда
								ИзменитьИконкуМаркера(текЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыбранного + ",0,0", 2);
							Иначе
								Если текЗадание.Выделен Тогда
									ИзменитьИконкуМаркера(текЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
								Иначе
									ИзменитьИконкуМаркера(текЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла; 
					ОбновитьПоказателиПоВыбраннымСтрокам();
				ИначеЕсли текЭлемент.Имя = Элементы.ДеревоРейсыПометка.Имя Тогда
					текСтрока.Пометка = Не текСтрока.Пометка;    
					ИзменитьВидимостьРейса(текСтрока);
					ЭлементыЗадания = текСтрока.ПолучитьЭлементы();
					стрИндексов = "";
					Для каждого текАдресОтправления Из текСтрока.АдресаОтправлений Цикл
						сткДанныеАдреса = текАдресОтправления.Значение;
						Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
							стрИндексов = стрИндексов + ПредставлениеЧисла(сткДанныеАдреса.ИндексВМассиве) + ";";
						КонецЕсли;
					КонецЦикла; 
					Для каждого текЗадание Из ЭлементыЗадания Цикл
						текЗадание.Пометка = текСтрока.Пометка;
						Если текЗадание.ИндексВМассиве > 0 Тогда
							стрИндексов = стрИндексов + ПредставлениеЧисла(текЗадание.ИндексВМассиве) + ";";
						КонецЕсли;
					КонецЦикла; 
					Если ЗначениеЗаполнено(стрИндексов) Тогда
						ИзменитьВидимостьОбъектов(стрИндексов, текСтрока.Пометка, Ложь);
					КонецЕсли;
				ИначеЕсли текЭлемент.Имя = Элементы.ДеревоРейсыПозиционирование.Имя Тогда
					Если текСтрока.Пометка Тогда
						ЦентрироватьСлойНаКарте(текСтрока.ИндексВМассивеМаршрут, 3);
					КонецЕсли;
				ИначеЕсли текЭлемент.Имя = Элементы.ДеревоРейсФильтрПоАдресам.Имя Тогда
					Если РежимФормированияКонтейнеров Тогда
						ИзменитьФильтрПоАдресам(текСтрока);
					КонецЕсли; 
				КонецЕсли;
			Иначе
				Если текЭлемент.Имя = Элементы.ДеревоРейсыВыбран.Имя Тогда
					текРодитель = текСтрока.ПолучитьРодителя();
					Если текРодитель.ДоступноИзменениеМаршрута Тогда
						текСтрока.Выбран = НЕ текСтрока.Выбран;
						текРодитель = текСтрока.ПолучитьРодителя();
						Если ИспользуютсяЭлектронныеГеографическиеКарты И текРодитель.Геокодирован И текРодитель.НомерЦветовойСхемы > 0 Тогда
							текЦветоваяСхема = ЦветовыеСхемы[текРодитель.НомерЦветовойСхемы - 1];
						КонецЕсли;
						Если текСтрока.Выбран Тогда
							текРодитель.Выбран = Истина;
							Если ИспользуютсяЭлектронныеГеографическиеКарты И текРодитель.Геокодирован И текЦветоваяСхема <> Неопределено Тогда
								ИзменитьИконкуМаркера(текСтрока.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыбранного + ",0,0", 2);
							КонецЕсли;
							СвязанныеЗадания = СвязанныеСтрокиЗаданий(текСтрока, текРодитель.ПолучитьЭлементы());
							Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
								СвязанноеЗадание.Выбран = Истина;
								Если ИспользуютсяЭлектронныеГеографическиеКарты И текРодитель.Геокодирован И текЦветоваяСхема <> Неопределено Тогда
									ИзменитьИконкуМаркера(СвязанноеЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыбранного + ",0,0", 2);
								КонецЕсли;
							КонецЦикла;
						Иначе
							Если ИспользуютсяЭлектронныеГеографическиеКарты И текРодитель.Геокодирован Тогда
								Если текЦветоваяСхема <> Неопределено Тогда
									Если текСтрока.Выделен Тогда
										ИзменитьИконкуМаркера(текСтрока.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
									Иначе
										ИзменитьИконкуМаркера(текСтрока.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
									КонецЕсли;
								КонецЕсли; 
								СвязанныеЗадания = СвязанныеСтрокиЗаданий(текСтрока, текРодитель.ПолучитьЭлементы());
								Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
									СвязанноеЗадание.Выбран = Ложь;
									Если текЦветоваяСхема <> Неопределено Тогда
										Если СвязанноеЗадание.Выделен Тогда
											ИзменитьИконкуМаркера(СвязанноеЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
										Иначе
											ИзменитьИконкуМаркера(СвязанноеЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
										КонецЕсли;
									КонецЕсли; 
								КонецЦикла;
							КонецЕсли;
							ЭлементыЗадания = текРодитель.ПолучитьЭлементы();
							ОбновитьРодителя = Истина;
							Для каждого текЗадание Из ЭлементыЗадания Цикл
								Если текЗадание.Выбран Тогда
									ОбновитьРодителя = Ложь;
								КонецЕсли;
							КонецЦикла; 
							Если ОбновитьРодителя Тогда
								текРодитель.Выбран = Ложь;
							КонецЕсли;
						КонецЕсли;
						ОбновитьПоказателиПоВыбраннымСтрокам();
					КонецЕсли;
				ИначеЕсли текЭлемент.Имя = Элементы.ДеревоРейсыПозиционирование.Имя Тогда
					текРодитель = текСтрока.ПолучитьРодителя();
					Если текРодитель.Пометка Тогда
						ЦентрироватьСлойНаКарте(текСтрока.ИндексВМассиве);
					КонецЕсли;
				ИначеЕсли текЭлемент.Имя = Элементы.ДеревоРейсФильтрПоАдресам.Имя Тогда
					Если РежимФормированияКонтейнеров Тогда
						текРодитель = текСтрока.ПолучитьРодителя();
						ИзменитьФильтрПоАдресам(текРодитель);
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли; 
		Если текЭлемент.Имя = Элементы.ДеревоРейсыАктивно.Имя Тогда
			Если текСтрока.РазрешитьАвтопланирование Тогда
				текСтрока.Активно = Не текСтрока.Активно;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	Если Ложь
		Или текЭлемент.Имя = Элементы.ДеревоРейсыВыбран.Имя
		Или текЭлемент.Имя = Элементы.ДеревоРейсыПометка.Имя
		Или текЭлемент.Имя = Элементы.ДеревоРейсыАктивно.Имя
		Или текЭлемент.Имя = Элементы.ДеревоРейсыПозиционирование.Имя
	Тогда
		РазрешитьСобытиеВыборВДеревеРейсов = Ложь;
	КонецЕсли;	 
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Функция КартыНеИспользуютсяИлиКартаСформирована()
#Если НЕ ВебКлиент Тогда
	Возврат Ложь
		Или Не ИспользуютсяЭлектронныеГеографическиеКарты
		Или (ИспользуютсяЭлектронныеГеографическиеКарты И КартаСформирована);
#КонецЕсли
КонецФункции

&НаКлиенте                                                                                              
Процедура ДеревоРейсыПриАктивизацииСтроки(Элемент)
	
	#Если НЕ ВебКлиент Тогда
	Если РазрешитьАктивизациюСтроки Тогда
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			Если КартаСформирована Тогда
				//// Обработка активизации строки запускается с некоторой задержкой из-за несвоевременного.
				//// Обновления массива выделенных строк таблицы формы при снятии выделения с зажатой клавишей Ctrl.
				ПодключитьОбработчикОжидания("ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина); 
				//ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания();
			КонецЕсли;
		Иначе
			ПодключитьОбработчикОжидания("ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина); 
			//ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания();
		КонецЕсли;
	КонецЕсли;
#КонецЕсли	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРейсыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;  
	Если Строка = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты И Не КартаСформирована Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ПараметрыПеретаскивания.Значение.Количество() Тогда
		СтрокаПриемник = ДеревоРейсы.НайтиПоИдентификатору(Строка);
		Отказ = Ложь;
		Если ЗначениеЗаполнено(СтрокаПриемник.Рейс) Тогда
			Если СтрокаПриемник.Уровень = 0 Тогда
				Если Не РежимКорректировки И Не СтрокаПриемник.ДоступноИзменениеМаршрута Тогда
					Отказ = Истина;
				КонецЕсли;
			Иначе
				текРодитель = СтрокаПриемник.ПолучитьРодителя();
				Если Не РежимКорректировки И Не текРодитель.ДоступноИзменениеМаршрута Тогда
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
			
			// Перетаскиваем из дерева в дерево рейсы
			Если НЕ Отказ И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
				Для каждого идСтроки Из ПараметрыПеретаскивания.Значение Цикл
					СтрокаДерева = ДеревоРейсы.НайтиПоИдентификатору(идСтроки);
					Если СтрокаДерева.Рейс = СтрокаПриемник.Рейс Тогда
						Отказ = Истина;
						Прервать;
					КонецЕсли; 
				КонецЦикла;
				
			КонецЕсли;
			
			Если Отказ Тогда
				// запрет добавления новых заданий в рейс
				ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			КонецЕсли;
			
		КонецЕсли; 
		
		// Перетаскиваем из дерева в дерево рейсы
		Если НЕ Отказ И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
			
			Если РежимФормированияКонтейнеров Тогда
				//Отказ = Истина;
				ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
				Возврат;
			КонецЕсли; 
			
			// Проверить разрешено ли редактирование рейса
			мсвСтатусыРейсов 		= Новый Массив;
			мсвИдентификаторовСтрок = ПараметрыПеретаскивания.Значение;
			Для каждого ИдентификаторСтроки Из мсвИдентификаторовСтрок Цикл
				ЭлементКоллекции 	= ДеревоРейсы.НайтиПоИдентификатору(ИдентификаторСтроки);
				
				Если ЗначениеЗаполнено(ЭлементКоллекции.СтатусРейса) 
					И мсвСтатусыРейсов.Найти(ЭлементКоллекции.СтатусРейса) = Неопределено Тогда
					мсвСтатусыРейсов.Добавить(ЭлементКоллекции.СтатусРейса);
				КонецЕсли;
			КонецЦикла;
			
			Если мсвСтатусыРейсов.Количество() > 0 Тогда
				Отказ = НЕ РазрешеноКорректироватьРейсы(мсвСтатусыРейсов);	
			КонецЕсли;
			
			Если Отказ Тогда
				// запрет добавления новых заданий в рейс
				ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
			КонецЕсли;
			
		КонецЕсли;		
	КонецЕсли; 

КонецПроцедуры
                                                                                         
&НаКлиенте                                                                                                          
Процедура ДеревоРейсыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	                                                                                                      
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ПараметрыПеретаскивания.Значение.Количество() Тогда
		Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
			Если Строка <> Неопределено Тогда
				// перетаскивание внутри дерева рейсов
				ПеренестиСтрокиВнутриДереваРейсовНаКлиенте(ПараметрыПеретаскивания.Значение, Строка);                 
			КонецЕсли;
			
		ИначеЕсли ПараметрыПеретаскивания.Значение[0].Свойство("Выделено") Тогда // перетаскивание из списка заданий на перевозку
			ПеренестиСтрокиИзСпискаЗаданийВРейсНаКлиенте(ПараметрыПеретаскивания.Значение, Строка);
			
		КонецЕсли;
		ПервыйЗапуск = Истина;
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте                                                                                                          
Процедура ИзменитьФильтрПоАдресам(текСтрока)
	
	ЭтаФорма.АдресОтправленияКонтейнера = текСтрока.Адрес;
	ЭтаФорма.АдресПолученияКонтейнера   = текСтрока.АдресПолучения;
	
	ИскомыеСтроки = АдресаОперацийСКонтейнерами.НайтиСтроки(Новый Структура("АдресОтправления, АдресПолучения", АдресОтправленияКонтейнера, АдресПолученияКонтейнера));
	Если ИскомыеСтроки.Количество() Тогда
		НайденнаяСтрока = ИскомыеСтроки[0];
	Иначе
		НайденнаяСтрока = АдресаОперацийСКонтейнерами.Добавить();
		НайденнаяСтрока.АдресОтправления = АдресОтправленияКонтейнера;
		НайденнаяСтрока.АдресПолучения = АдресПолученияКонтейнера;
	КонецЕсли; 
	
	Если НайденнаяСтрока.Пометка Тогда
		Возврат;
	КонецЕсли; 
	
	Если ИдентификаторПомеченнойСтрокиФК >= 0 Тогда
		текПомеченная = АдресаОперацийСКонтейнерами.НайтиПоИдентификатору(ИдентификаторПомеченнойСтрокиФК);
		Если Не текПомеченная = Неопределено Тогда
			текПомеченная.Пометка = Ложь;
		КонецЕсли;
	КонецЕсли; 
	
	НайденнаяСтрока.Пометка = Истина;
	ИдентификаторПомеченнойСтрокиФК = НайденнаяСтрока.ПолучитьИдентификатор();
	Элементы.СписокЗаданий.ОтборСтрок = Новый ФиксированнаяСтруктура("АдресОтправления, АдресПолучения", АдресОтправленияКонтейнера, АдресПолученияКонтейнера);
	
	ОбновитьПоказателиПоВыбраннымСтрокамСпискаЗаданий();
	ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
	ОбновитьДоступность();
	
	Элементы.АдресаОперацийСКонтейнерами.ТекущаяСтрока = ИдентификаторПомеченнойСтрокиФК;
	
КонецПроцедуры
					

#КонецОбласти 

#Область ЭлементыФормы_HTMLДокумент

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	
#Если НЕ ВебКлиент Тогда	
	Если Не ПроизведенПереходПоСсылке Тогда
				
		// Проверяет на стороне карты сформирован ли объект Map.
		MapExists = ВыполнитьФункциюHTML("ValidateCacheCalls()");
		Если MapExists = Истина Тогда
			ОчиститьСообщения();
			
			Если Не КартаСформирована Тогда 
				КартаСформирована = Истина;	

				//Элементы.СписокЗаданий.ВыделенныеСтроки.Очистить();
				//// Вывод объектов точек адресов на карте без позиционирования существенно ускоряет процесс прорисовки карты.
				ВыполнитьПроцедуруHTML("setUsePositioning(false)");
				ОтобразитьОбъектыНаКарте();
				ОтобразитьРейсыНаКарте();
				ИзменитьВидимостьЗон(ОтображатьЗоны);
				ИзменитьВидимостьМестИнтереса(ОтображатьМестаИнтереса);
				
				ТолькоПросмотр = Ложь;
			КонецЕсли;
		Иначе
			ПодключитьОбработчикОжидания("ВыполнитьОтложенноеДействие", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
#КонецЕсли		
КонецПроцедуры

&НаКлиенте
Процедура HTMLДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
#Если НЕ ВебКлиент Тогда		
	Если ВыполняетОбращениеКДокументу Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроизведенПереходПоСсылке Или ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда // переход по ссылке на другой адрес, стандартная работа с картой больше не обрабатывается
		ПроизведенПереходПоСсылке = Истина;
	Иначе
		ОбновитьСостояниеМаркеров();
		ИндексРейсаВДереве = ВыполнитьФункциюHTML("GetIndexOfDoubleClickedTrip()");
		Если ИндексРейсаВДереве <> Неопределено Тогда
			Элементы.ДеревоРейсы.ТекущаяСтрока = ДеревоРейсы.ПолучитьЭлементы()[ИндексРейсаВДереве].ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЕсли;
#КонецЕсли		
КонецПроцедуры
	
#КонецОбласти 

#Область ЭлементыФормы_Прочие

&НаКлиенте
Процедура ПериодПланированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элементы.ПериодПланирования.ВыбиратьТип = Ложь;
	спзТипов = Новый СписокЗначений;
	спзТипов.Добавить(0, "Указать произвольный период...");
	спзТипов.Добавить(1, "Выбрать период планирования");
	ПоказатьВыборИзМеню(Новый ОписаниеОповещения("ВыборТипаПериодаПланированияЗавершение", ЭтаФорма), спзТипов, Элементы.ПериодПланирования);
	
КонецПроцедуры

&НаКлиенте                                                                                                     
Процедура ВидПеревозкиПриИзменении(Элемент)
	
	УстановитьДоступныеЗначенияВариантовСписков();
	УстановитьДоступныеЗначенияВариантовПланирования();
	
	ВариантНастроекПриИзменении();
	ВариантПланированияПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> ВидПеревозки Тогда
		 ЭтаФорма.ВариантСписков = Неопределено;
		 ЭтаФорма.ВариантПланирования = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНастроекПриИзменении(Элемент = Неопределено)

	Если ЗначениеЗаполнено(ВариантСписков) Тогда
		сткНастройки = ВернутьВариантНастройкиСпискаЗаданий();
		Если сткНастройки <> Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
		КонецЕсли;
	Иначе
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	ВосстановитьВариантНабораДоступныхТС();
	УстановитьПараметрыПланированияНаСервере();
	
	// серверные вызовы
	ОбновитьТаблицуЗаданийНаСервере();
	ОбновитьДеревоРейсовНаСервере();
	
	// клиентские вызовы
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		ОчиститьОбъекты();
		ОтобразитьОбъектыНаКарте();
		ВыполнитьПроцедуруHTML("clearTrips()");
		ОтобразитьРейсыНаКарте();
		ИзменитьВидимостьЗон(ОтображатьЗоны);
	КонецЕсли;
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПланированияПриИзменении(Элемент = Неопределено)
	
	//Если ЗначениеЗаполнено(ВариантПланирования) Тогда
	//	ЭтаФорма.ТекущийВариантПланирования = ВариантПланирования;
	//Иначе
	//	ЭтаФорма.ТекущийВариантПланирования = "";
	//КонецЕсли;
	УстановитьПараметрыПланированияНаСервере();
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияГеокодированиеАдресовНажатие(Элемент)
	
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаГеокодированияАдресов",,,,,, Новый ОписаниеОповещения("ДекорацияГеокодированиеАдресовНажатиеЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Оповестить("ГеокодироватьАдресаЗаданийНаПеревозку", СписокНегеокодированныхАдресов);
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

#Область КоманднаяПанельФормы

&НаКлиенте
// Команда выводит информацию об используемом браузере (user agent), который отображает содержимое HTML-документа.
//
// Параметры:
//  Команда  - Элемент - Команда формы.
//
Процедура СообщитьИнформациюПрограммыПросмотра(Команда)
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Элементы.HTMLДокумент.ИнформацияПрограммыПросмотра);
	
КонецПроцедуры // СообщитьИнформациюПрограммыПросмотра()

#КонецОбласти 

#Область КоманднаяПанельОбщиеНастройки

&НаКлиенте
Процедура УказатьПроизвольныйПериод(Команда)
	
	сткПараметры = Новый Структура("НачалоПериода, КонецПериода, ПроверкаЗаполнения", НачалоПериодаПланирования, ОкончаниеПериодаПланирования, Истина);
	ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораПроизвольногоПериода", сткПараметры,,,,, Новый ОписаниеОповещения("ВыборСтандартногоПериодаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодПланирования(Команда)
	
	ОткрытьФорму("Справочник.упПериодыПланирования.ФормаВыбора",,,,,, Новый ОписаниеОповещения("ВыборПериодаПланированияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

#КонецОбласти 

#Область КоманднаяПанельСпискаЗаданий

&НаКлиенте
Процедура РазбитьСтрокуСпискаЗаданий(Команда)
	
	текСтрока = Элементы.СписокЗаданий.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения команды требуется выбрать строку из списка заданий'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	Если текСтрока.КоличествоГрузов < 2 Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения команды количество груза должно быть больше одного'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли;

	ТекстПодсказки = НСтр("ru = 'Введите количество груза в новой строке'");
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ВводКоличестваГрузаВНовойСтрокеЗавершение", ЭтаФорма) , 1, ТекстПодсказки, 5, 0); 
	
КонецПроцедуры // РазбитьСтрокуСпискаЗаданий()

&НаКлиенте
Процедура ПодготовитьМассивРасстояний(Команда)
	
	Если СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	мсвТС = Неопределено;
	мсвЗаданий = Неопределено;
	ПолучитьУникальныеТранспортыИЗадания(мсвТС, мсвЗаданий);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТранспортыДляРасчетаРасстояний", мсвТС);
	ПараметрыФормы.Вставить("ЗаданияДляРасчетаРасстояний", мсвЗаданий);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаПодготовкиМассиваРасстояний", ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьУникальныеТранспортыИЗадания(мсвТС, мсвЗаданий)
	
	мсвТС = СписокТранспорта.Выгрузить(, "ТранспортноеСредство").ВыгрузитьКолонку("ТранспортноеСредство");
	мсвТС = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвТС);
	мсвЗаданий = СписокЗаданий.Выгрузить(Новый Структура("Геокодировано", Истина), "ЗаданиеНаПеревозку").ВыгрузитьКолонку("ЗаданиеНаПеревозку");
	мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
	Возврат мсвТС;

КонецФункции

&НаКлиенте
Процедура НастройкаЗаданийНаПеревозку(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Настройки", КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Настройка отбора списка заданий на перевозку'"));
	
	ОткрытьФорму("ОбщаяФорма.упФормаНастройкиОтбора", ПараметрыФормы,,,,, Новый ОписаниеОповещения("НастройкаЗаданийНаПеревозкуЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // НастройкаЗаданийНаПеревозку()

&НаКлиенте
Процедура МасштабироватьКартуПоТочкам(Команда)
	
	Если НЕ КартаСформирована Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокЗаданий.Количество() Тогда
		Координаты = ПолучитьКоординатыТочекСтрокойНаСервере();
		Если ЗначениеЗаполнено(Координаты) Тогда
			ЦентрироватьТочкиНаКарте(Координаты);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // МасштабироватьКартуПоТочкам()

&НаКлиенте
Процедура МасштабироватьКартуПоВидимымТочкам(Команда)
	
	Если Не КартаСформирована Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокЗаданий.Количество() Тогда
		Координаты = ПолучитьКоординатыТочекСтрокойНаСервере(Истина);
		Если ЗначениеЗаполнено(Координаты) Тогда
			ЦентрироватьТочкиНаКарте(Координаты);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // МасштабироватьКартуПоВидимымТочкам()

&НаКлиенте
Процедура НазначитьПеревозкуТранспортнойКомпании(Команда)
	
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	мсвСтруктурЗаданий = Новый Массив;
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		текСтрока = СписокЗаданий.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если текСтрока <> Неопределено Тогда
			мсвСтруктурЗаданий.Добавить(Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов", текСтрока.ЗаданиеНаПеревозку, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов));
		КонецЕсли;
	КонецЦикла;	
	
	Если мсвСтруктурЗаданий.Количество() Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("МассивСтруктурЗаданий", мсвСтруктурЗаданий);
		сткПараметры.Вставить("РазбиватьЗаданияПоГрузовымМестам", РазбиватьЗаданияПоГрузовымМестам);
		ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаНазначенияПеревозокТранспортнойКомпании", сткПараметры,,,,,Новый ОписаниеОповещения("НазначитьПеревозкуТранспортнойКомпанииЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры // НазначитьПеревозкуТранспортнойКомпании()

&НаКлиенте
Процедура ВыбратьВыделенныеСтрокиСпискаЗаданий(Команда)
	
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	ВыбратьСтрокиСпискаЗаданий(ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
//Процедура ВыбратьСтрокиСпискаЗаданий(Знач ВыделенныеСтроки)
Процедура ВыбратьСтрокиСпискаЗаданий(ВыделенныеСтроки)
	
	//Перем ИдентификаторСтроки, мсвОтправлений, текОтправление, текСтрока;
	//Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
	//	Элементы.СписокЗаданий.ВыделенныеСтроки.Добавить(ВыделеннаяСтрока);
	//КонецЦикла;	
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		мсвОтправлений = Новый Массив;
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			Если ТипЗнч(ИдентификаторСтроки) = Тип("Число") Тогда
				текСтрока = СписокЗаданий.НайтиПоИдентификатору(ИдентификаторСтроки);
			Иначе
				текСтрока = ИдентификаторСтроки;
			КонецЕсли; 
			Если текСтрока <> Неопределено И Не текСтрока.Выбрано Тогда
				текСтрока.Выбрано = Истина;
				мсвОтправлений.Добавить(текСтрока.АдресОтправления);
				Если текСтрока.Геокодировано Тогда 
					ИзменитьИконкуМаркераПоВидуАдреса(текСтрока.ВидАдресаПолучения, текСтрока.ИндексВМассивеПолучения, 2, ОпределитьКоэффициентПересчета(текСтрока),
					(ВыделятьСрочныеГрузы И текСтрока.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		Если мсвОтправлений.Количество() Тогда
			мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
		КонецЕсли;
		Для каждого текОтправление Из мсвОтправлений Цикл
			ИзменитьИконкуОтправления(текОтправление, 2);
		КонецЦикла; 
		
	Иначе
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			Если ТипЗнч(ИдентификаторСтроки) = Тип("Число") Тогда
				текСтрока = СписокЗаданий.НайтиПоИдентификатору(ИдентификаторСтроки);
			Иначе
				текСтрока = ИдентификаторСтроки;
			КонецЕсли; 
			Если текСтрока <> Неопределено И Не текСтрока.Выбрано Тогда
				текСтрока.Выбрано = Истина;
			КонецЕсли;
		КонецЦикла; 
		
	КонецЕсли; 
	ОбновитьПоказателиПоВыбраннымСтрокам();
	//ПодключитьОбработчикОжидания("СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина); 

КонецПроцедуры // ВыбратьВыделенныеСтрокиСпискаЗаданий()

&НаКлиенте
Процедура УбратьВыборСВыделенныхСтрокСпискаЗаданий(Команда)
	
	Если Не СписокЗаданий.Количество()  Тогда
		Возврат;
	КонецЕсли; 
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	//Если ВыделенныеСтроки.Количество() <= 1 Тогда
	//	ВыделенныеСтроки = Новый Массив;
	//	Для Каждого СтрокаДерева Из СписокЗаданий Цикл
	//		ВыделенныеСтроки.Добавить(СтрокаДерева);
	//	КонецЦикла;
	//КонецЕсли; 
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		мсвОтправлений = Новый Массив;
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			Если ТипЗнч(ИдентификаторСтроки) = Тип("Число") Тогда
				текСтрока = СписокЗаданий.НайтиПоИдентификатору(ИдентификаторСтроки);
			Иначе
				текСтрока = ИдентификаторСтроки;
			КонецЕсли; 
			Если текСтрока.Выбрано Тогда
				текСтрока.Выбрано = Ложь;
				Если текСтрока.Геокодировано Тогда
					ИзменитьИконкуМаркераПоВидуАдреса(текСтрока.ВидАдресаПолучения, текСтрока.ИндексВМассивеПолучения, 1, ОпределитьКоэффициентПересчета(текСтрока), (ВыделятьСрочныеГрузы И текСтрока.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
					мсвОтправлений.Добавить(текСтрока.АдресОтправления);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		Если мсвОтправлений.Количество() Тогда
			мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
		КонецЕсли;
		Для каждого текОтправление Из мсвОтправлений Цикл
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Выбрано", текОтправление, Истина));
			Если ИскомыеСтроки.Количество() = 0 Тогда
				УбратьВыборМаркера(текОтправление);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			Если ТипЗнч(ИдентификаторСтроки) = Тип("Число") Тогда
				текСтрока = СписокЗаданий.НайтиПоИдентификатору(ИдентификаторСтроки);
			Иначе
				текСтрока = ИдентификаторСтроки;
			КонецЕсли; 
			Если текСтрока.Выбрано Тогда
				текСтрока.Выбрано = Ложь;
			КонецЕсли;
		КонецЦикла; 
		
	КонецЕсли;
	ОбновитьПоказателиПоВыбраннымСтрокам();
	
КонецПроцедуры // УбратьВыборСВыделенныхСтрокСпискаЗаданий()

&НаКлиенте
Процедура СохранитьВариантНастройки(Знач ТипНастройки, ИмяОбработчикаОповещения, ТекушийВариантНастройки)
	
	сткПараметры = Новый Структура("КлючОбъекта, КлючТекущихНастроек, ВидПеревозки", "Обработка.упРМПланированиеРейсов." + ТипНастройки + "." + ВидТранспортнойУслугиПредставление, ТекушийВариантНастройки, ВидПеревозки);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаСохранения", сткПараметры,,,,,Новый ОписаниеОповещения(ИмяОбработчикаОповещения, ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры // СохранитьВариантНастройкиСпискаЗаданий() 

&НаКлиенте
Процедура СохранитьВариантНастройкиСпискаЗаданий(Команда)
	
	СохранитьВариантНастройки("НастройкаСпискаЗаданий", "СохранитьВариантНастройкиСпискаЗаданийНаСервере", ВариантСписков);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВариантНастройкиПланирования(Команда)
	
	СохранитьВариантНастройки("ПараметрыПланирования", "СохранитьВариантНастройкиПланированияНаСервере", ВариантПланирования);
	
КонецПроцедуры // ОткрытьВариантНастройкиСпискаЗаданий()

&НаКлиенте
Процедура ОткрытьВариантНастройки(Знач ТипНастройки, ИмяОбработчикаОповещения, ВариантНастройки)
	
	сткПараметры = Новый Структура("КлючОбъекта, КлючТекущихНастроек, ВидПеревозки", "Обработка.упРМПланированиеРейсов." + ТипНастройки + "." + ВидТранспортнойУслугиПредставление, ВариантНастройки, ВидПеревозки);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаЗагрузки", сткПараметры,,,,,Новый ОписаниеОповещения(ИмяОбработчикаОповещения, ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры // СохранитьВариантНастройкиСпискаЗаданий() 

&НаКлиенте
Процедура ОткрытьВариантНастройкиСпискаЗаданий(Команда)
	
	ОткрытьВариантНастройки("НастройкаСпискаЗаданий", "ОткрытьВариантНастройкиСпискаЗаданийНаКлиенте", ВариантСписков);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройкиПланирования(Команда)
	
	ОткрытьВариантНастройки("ПараметрыПланирования", "ОткрытьВариантНастройкиПланированияНаКлиенте", ВариантПланирования);
	
КонецПроцедуры // ОткрытьВариантНастройкиСпискаЗаданий()

&НаКлиенте
Процедура ОбновитьТаблицуЗаданий(Команда = Неопределено)

	ОбновитьДеревоРейсов = Ложь;
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого текРейс Из ЭлементыРейсы Цикл
		Если ЗначениеЗаполнено(текРейс.Рейс) Тогда
			ОбновитьДеревоРейсов = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ОбновитьДанныеИОтображение(ОбновитьДеревоРейсов);

КонецПроцедуры

&НаКлиенте
Процедура ДополнитьТаблицуЗаданийНовыми(Команда)
	
	Если Не ЗначениеЗаполнено(ДатаОбновленияСпискаЗаданий) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	//ТекстОшибки = "";
	мсвОтправлений = Новый Массив;
	сткИндексыУдаляемыхОбъектовКарты = Новый Структура;
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыМаркеров", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("МассивОтправлений", мсвОтправлений);

	
	///////////////////////////////////////////////
	// серверные вызовы
	///////////////////////////////////////////////
	ДополнитьТаблицуЗаданийНовымиНаСервере(сткИндексыУдаляемыхОбъектовКарты, Отказ);
	
	///////////////////////////////////////////////
	// клиентские вызовы
	///////////////////////////////////////////////
	Если Не Отказ Тогда
		// 1. очистка старых объектов с карты
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда 
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров Цикл
				УдалитьОбъект(текИндексКарты);
			КонецЦикла;
		КонецЕсли; 
		
		// 2. добавляем объекты на карту по новым строкам в списке заданий
		мсвОтправлений = сткИндексыУдаляемыхОбъектовКарты.МассивОтправлений;
		НовыеСтроки = Новый Массив;
		КоличествоСтрок = СписокЗаданий.Количество();
		Пока КоличествоДобавленныхСтрок > 0 Цикл
			ИндексСтроки = КоличествоСтрок - КоличествоДобавленныхСтрок;
			ТекущаяСтрока = СписокЗаданий[ИндексСтроки];
			мсвОтправлений.Добавить(ТекущаяСтрока.АдресОтправления);
			НовыеСтроки.Добавить(ТекущаяСтрока);
			КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
		КонецЦикла; 
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ИзменитьВидимостьЗон(ОтображатьЗоны);
			Если мсвОтправлений.Количество() Тогда
				мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
				Для каждого текОтправление Из мсвОтправлений Цикл
					ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текОтправление));
					Для каждого текСтрока Из ИскомыеСтроки Цикл
						Если Не текСтрока.Пометка Тогда
							ИзменитьВидимостьОбъекта(текСтрока.ИндексВМассиве, Истина);
						КонецЕсли; 
					КонецЦикла; 
				КонецЦикла;
			КонецЕсли;
			
			Если НовыеСтроки.Количество() Тогда
				ОтобразитьОбъектыНаКарте(НовыеСтроки);
			КонецЕсли;
		КонецЕсли;
		ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
		ОбновитьДоступность();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицуЗаданийНовымиНаСервере(сткИндексыУдаляемыхОбъектовКарты, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упСтатусыЗаданийНаПеревозкуГруза.Документ КАК ЗаданиеНаПеревозку
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза КАК упСтатусыЗаданийНаПеревозкуГруза
	|ГДЕ
	|	упСтатусыЗаданийНаПеревозкуГруза.Период > &Период
	|	И упСтатусыЗаданийНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиКПланированию.Задание КАК ЗаданиеНаПеревозку,
	|	ОстаткиКПланированию.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ОстаткиКПланированию.КоличествоКПланированиюОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.упЗаданияКПланированию.Остатки(
	|			,
	|			Задание В
	|				(ВЫБРАТЬ
	|					втЗадания.ЗаданиеНаПеревозку
	|				ИЗ
	|					втЗадания)) КАК ОстаткиКПланированию
	|ГДЕ
	|	ОстаткиКПланированию.КоличествоКПланированиюОстаток > 0";
	Запрос.УстановитьПараметр("Период", ДатаОбновленияСпискаЗаданий);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	ТаблицаНовыхЗаданий = РезультатЗапроса.Выгрузить();
	мсвЗаданий = ТаблицаНовыхЗаданий.ВыгрузитьКолонку("ЗаданиеНаПеревозку");
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		мсвГрузовыхМест = ТаблицаНовыхЗаданий.ВыгрузитьКолонку("ГрузовоеМесто");
	КонецЕсли; 
	
	// 1. ищем существующие задания в списке, удаляем их, запоминая данные для очистки с карты
	Для каждого текЗадание Из мсвЗаданий Цикл
		//Если РазбиватьЗаданияПоГрузовымМестам Тогда
		//	сткПоиска = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов", текСтрока.Задание, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов);
		//Иначе
		сткПоиска = Новый Структура("ЗаданиеНаПеревозку", текЗадание);
		//КонецЕсли; 
		СтрокиЗадания = СписокЗаданий.НайтиСтроки(сткПоиска);
		Если СтрокиЗадания.Количество() > 0 Тогда
			Для каждого текСтрока Из СтрокиЗадания Цикл
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					сткИндексыУдаляемыхОбъектовКарты.МассивОтправлений.Добавить(текСтрока.АдресОтправления);
					сткИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеПолучения);
				КонецЕсли;
				СписокЗаданий.Удалить(текСтрока);
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла;
	
	// 2. обновляем список заданий с учетом новых заданий и варианта списка
	мсвОтборов = Новый Массив;
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяПоля"       , "ЗаданиеНаПеревозку");
	Отбор.Вставить("ПравоеЗначение", мсвЗаданий);
	Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);
	мсвОтборов.Добавить(Отбор);
	
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяПоля"       , "ГрузовоеМесто");
		Отбор.Вставить("ПравоеЗначение", мсвГрузовыхМест);
		Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);				
		мсвОтборов.Добавить(Отбор);
	КонецЕсли;
	ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеИОтображение(Знач ОбновитьДеревоРейсов = Истина)
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Если ОтображатьЗоны Тогда
			ИзменитьВидимостьЗон();
		КонецЕсли;	
	КонецЕсли;
	ОбновитьТаблицуЗаданийНаСервере();
	Если ОбновитьДеревоРейсов Тогда
		ОбновитьДеревоРейсовНаСервере();
	КонецЕсли;
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		ОбновитьОтображениеНаКарте(ОбновитьДеревоРейсов);
		
		Если ОтображатьЗоны Тогда
			ИзменитьВидимостьЗон(Истина);
		КонецЕсли;
	КонецЕсли;
	ОбновитьПоказателиПоВыбраннымСтрокам();
	ОбновитьДоступность();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеНаКарте(Знач ОбновитьРейсы = Истина)
	
	// клиентские вызовы
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		ОчиститьОбъекты();
		ОтобразитьОбъектыНаКарте();
		Если ОбновитьРейсы Тогда
			ВыполнитьПроцедуруHTML("clearTrips()");
			ОтобразитьРейсыНаКарте();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОбновитьТаблицуЗаданий()

&НаКлиенте
Процедура УстановитьФильтрПоАдресуОтправления(Команда)
	
	Если СписокЗаданий.Количество() Тогда
		сткПараметры = Новый Структура("СписокОтправлений", СписокОтправлений);
		ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаОтбораАдресовОтправления", сткПараметры,,,,, Новый ОписаниеОповещения("УстановкаФильтраПоАдресуОтправленияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры // УстановитьФильтрПоАдресуОтправления()

&НаКлиенте
Процедура УстановитьВидимостьВыделенныхТочек(Команда)
	
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	
	мсвОтправлений = Новый Массив;
	Если ВыделенныеСтроки.Количество() = СписокЗаданий.Количество() Тогда
		Для каждого Строка Из СписокЗаданий Цикл
			Если Строка.Пометка Или Не Строка.Геокодировано Тогда
				Продолжить;
			Иначе
				Строка.Пометка = Истина;
				ИзменитьВидимостьПоСтрокеСпискаЗаданий(Строка);
				мсвОтправлений.Добавить(Строка.АдресОтправления);
			КонецЕсли;
		КонецЦикла;
		
		Если мсвОтправлений.Количество() Тогда
			мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
		КонецЕсли;
		Для каждого текОтправление Из мсвОтправлений Цикл
			ИзменитьВидимостьТочкиОтправления(текОтправление, Истина);
		КонецЦикла;
		
	Иначе
		мсвОтправленийСкрыть = Новый Массив;
		Для каждого Строка Из СписокЗаданий Цикл
			Если Строка.Выделено Тогда
				Если Строка.Пометка Или Не Строка.Геокодировано Тогда
					Продолжить;
				Иначе
					Строка.Пометка = Истина;
					ИзменитьВидимостьПоСтрокеСпискаЗаданий(Строка);
					мсвОтправлений.Добавить(Строка.АдресОтправления);
				КонецЕсли;
			Иначе
				Если Не Строка.Пометка Тогда
					Продолжить;
				Иначе
					Строка.Пометка = Ложь;
					ИзменитьВидимостьПоСтрокеСпискаЗаданий(Строка);
					мсвОтправленийСкрыть.Добавить(Строка.АдресОтправления);
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла; 
		
		Если мсвОтправлений.Количество() Тогда
			мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
		КонецЕсли;
		Для каждого текОтправление Из мсвОтправлений Цикл
			ИзменитьВидимостьТочкиОтправления(текОтправление, Истина);
		КонецЦикла;
		
		Если мсвОтправленийСкрыть.Количество() Тогда
			мсвОтправленийСкрыть = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправленийСкрыть);
		КонецЕсли;
		
		Для каждого текОтправление Из мсвОтправленийСкрыть Цикл
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Пометка", текОтправление, Истина));
			Если ИскомыеСтроки.Количество() = 0 Тогда
				ИзменитьВидимостьТочкиОтправления(текОтправление, Ложь);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьВыделенныхТочек()

&НаКлиенте
Процедура УстановитьАктивностьВыделенныхТочек(Команда)
	
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = СписокЗаданий.Количество() Тогда
		Для каждого Строка Из СписокЗаданий Цикл
			Если Строка.Активно Или Строка.ТипПеревозки = 3 Тогда
				Продолжить;
			Иначе
				Строка.Активно = Истина;
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					ИзменитьПрозрачностьМаркера(Строка.ИндексВМассивеПолучения, 0);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	Иначе
		Для каждого Строка Из СписокЗаданий Цикл
			Если Строка.Выделено Тогда
				Если Ложь
					Или Строка.Активно 
					Или Строка.ТипПеревозки = 3 
				Тогда
					Продолжить;
				Иначе
					Строка.Активно = Истина;
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ИзменитьПрозрачностьМаркера(Строка.ИндексВМассивеПолучения, 0);
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если Не Строка.Активно Тогда
					Продолжить;
				Иначе
					Строка.Активно = Ложь;
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ИзменитьПрозрачностьМаркера(Строка.ИндексВМассивеПолучения, 50);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // УстановитьАктивностьВыделенныхТочек() 	

#Область КонтекстноеМенюСпискаЗаданий

&НаКлиенте
Процедура МасштабироватьКартуПоВыделеннымТочкам(Команда)
	
	Если НЕ КартаСформирована Тогда
		Возврат;
	КонецЕсли;
	
	Координаты = "";
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	Если СписокЗаданий.Количество() И ВыделенныеСтроки.Количество() Тогда
		Для каждого текИдентификатор Из ВыделенныеСтроки Цикл
			текСтрока = СписокЗаданий.НайтиПоИдентификатору(текИдентификатор);
			Если текСтрока.Пометка Тогда
				Координаты = Координаты + текСтрока.КоординатыПолучения;
			КонецЕсли;			
		КонецЦикла; 
	КонецЕсли;
	Если ЗначениеЗаполнено(Координаты) Тогда
		ЦентрироватьТочкиНаКарте(Координаты);
	КонецЕсли;
	
КонецПроцедуры // МасштабироватьКартуПоВыделеннымТочкам()

&НаКлиенте
Процедура ВыключитьДоступностьНевыделенныхТочек(Команда)
	
	НевыделенныеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Активно, Выделено", Истина, Ложь));
	
	Для каждого Строка Из НевыделенныеСтроки Цикл
		Строка.Активно = Ложь;
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ИзменитьПрозрачностьМаркера(Строка.ИндексВМассивеПолучения, 50);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // ВыключитьДоступностьНевыделенныхТочек()

&НаКлиенте
Процедура СкрытьНевыделенныеТочки(Команда)
	
	НевыделенныеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Пометка, Выделено", Истина, Ложь));
	мсвОтправлений = Новый Массив;
	Для каждого Строка Из НевыделенныеСтроки Цикл
		Строка.Пометка = Ложь;
		мсвОтправлений.Добавить(Строка.АдресОтправления);
		ИзменитьВидимостьПоСтрокеСпискаЗаданий(Строка);
	КонецЦикла; 
	Если мсвОтправлений.Количество() Тогда
		мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
	КонецЕсли;
	Для каждого текОтправление Из мсвОтправлений Цикл
		ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Пометка", текОтправление, Истина));
		Если ИскомыеСтроки.Количество() = 0 Тогда
			ИзменитьВидимостьТочкиОтправления(текОтправление, Ложь);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СкрытьНевыделенныеТочки()

#КонецОбласти 

#КонецОбласти 

#Область КоманднаяПанельДереваРейсов

&НаКлиенте
Процедура ОткрытьФормуВыбораДоступныхТС(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан вид перевозки'")); 
		Возврат;
	КонецЕсли;

	//Организации = ВернутьМассивОрганизаций();
	сткПараметры = Новый Структура;
	//сткПараметры.Вставить("Организации"     , Новый ФиксированныйМассив(Организации)); // Больше не используется
	сткПараметры.Вставить("ВидПеревозки"                , ВидПеревозки);
	сткПараметры.Вставить("РежимФормированияКонтейнеров", РежимФормированияКонтейнеров);
	сткПараметры.Вставить("НачалоПериода"               , НачалоПериодаПланирования);
	сткПараметры.Вставить("ОкончаниеПериода"            , ОкончаниеПериодаПланирования);
	сткПараметры.Вставить("МножественныйВыбор"          , Истина);
	
	Если СписокТранспорта.Количество() = 0 Тогда
		ВосстановитьВариантНабораДоступныхТС();
	КонецЕсли;	
		
	сткПараметры.Вставить("ТаблицаРезультат", СписокТранспорта);
	
	ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбораДоступныхТС", сткПараметры,,,,, Новый ОписаниеОповещения("ВыборДоступныхТСЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ОткрытьФормуВыбораДоступныхТС()

&НаКлиенте
Процедура ВыбратьВыделенныеСтрокиДереваРейсов(Команда)
	
	ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
	ВыбратьСтрокиДереваРейсов(ВыделенныеСтрокиДерева);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтрокиДереваРейсов(Знач ВыделенныеСтрокиДерева)
	
	Перем ИдентификаторСтрокиДерева, текЗадание, текРодитель, текСтрока, текЦветоваяСхема, ЭлементыЗадания;
	
	Для каждого ИдентификаторСтрокиДерева Из ВыделенныеСтрокиДерева Цикл
		Если ТипЗнч(ИдентификаторСтрокиДерева) = Тип("Число") Тогда
			текСтрока = ДеревоРейсы.НайтиПоИдентификатору(ИдентификаторСтрокиДерева);
		Иначе
			текСтрока = ИдентификаторСтрокиДерева;
		КонецЕсли; 
		Если ЗначениеЗаполнено(текСтрока.Рейс) И текСтрока.ДоступноИзменениеМаршрута Тогда
			текСтрока.Выбран = Истина;
			Если текСтрока.Уровень = 0 Тогда
				Если ИспользуютсяЭлектронныеГеографическиеКарты И текСтрока.НомерЦветовойСхемы > 0 Тогда
					текЦветоваяСхема = ЦветовыеСхемы[текСтрока.НомерЦветовойСхемы - 1];
				КонецЕсли;
				ЭлементыЗадания = текСтрока.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					текЗадание.Выбран = Истина;
					Если ИспользуютсяЭлектронныеГеографическиеКарты И текЦветоваяСхема <> Неопределено Тогда
						ИзменитьИконкуМаркера(текЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыбранного + ",0,0", 2);
					КонецЕсли;
				КонецЦикла; 
			Иначе	
				текРодитель = текСтрока.ПолучитьРодителя();
				текРодитель.Выбран = Истина;
				Если ИспользуютсяЭлектронныеГеографическиеКарты И текРодитель.НомерЦветовойСхемы > 0 Тогда
					текЦветоваяСхема = ЦветовыеСхемы[текРодитель.НомерЦветовойСхемы - 1];
					ИзменитьИконкуМаркера(текСтрока.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыбранного + ",0,0", 2);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	ОбновитьПоказателиПоВыбраннымСтрокам();

КонецПроцедуры // ВыбратьВыделенныеСтрокиДереваРейсов()

&НаКлиенте
Процедура УбратьВыборСВыделенныхСтрокДереваРейсов(Команда)
	
	ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
	//Если ВыделенныеСтрокиДерева.Количество() <= 1 Тогда
	//	ВыделенныеСтрокиДерева = Новый Массив;
	//	Для Каждого СтрокаДерева Из ДеревоРейсы.ПолучитьЭлементы() Цикл
	//		ВыделенныеСтрокиДерева.Добавить(СтрокаДерева);
	//	КонецЦикла;
	//КонецЕсли; 
	Для каждого ИдентификаторСтрокиДерева Из ВыделенныеСтрокиДерева Цикл
		Если ТипЗнч(ИдентификаторСтрокиДерева) = Тип("Число") Тогда
			текСтрока = ДеревоРейсы.НайтиПоИдентификатору(ИдентификаторСтрокиДерева);
		Иначе
			текСтрока = ИдентификаторСтрокиДерева;
		КонецЕсли; 
		Если ЗначениеЗаполнено(текСтрока.Рейс) Тогда
			текСтрока.Выбран = Ложь;
			Если текСтрока.Уровень = 0 Тогда
				Если ИспользуютсяЭлектронныеГеографическиеКарты И текСтрока.НомерЦветовойСхемы > 0 Тогда
					текЦветоваяСхема = ЦветовыеСхемы[текСтрока.НомерЦветовойСхемы - 1];
				КонецЕсли;
				ЭлементыЗадания = текСтрока.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					текЗадание.Выбран = Ложь;
					Если ИспользуютсяЭлектронныеГеографическиеКарты И текЦветоваяСхема <> Неопределено Тогда
						Если текЗадание.Выделен Тогда
							ИзменитьИконкуМаркера(текЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
						Иначе
							ИзменитьИконкуМаркера(текЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе	
				текРодитель = текСтрока.ПолучитьРодителя();
				Если ИспользуютсяЭлектронныеГеографическиеКарты И текРодитель.НомерЦветовойСхемы > 0 Тогда
					текЦветоваяСхема = ЦветовыеСхемы[текРодитель.НомерЦветовойСхемы - 1];
				КонецЕсли;
				ОбновитьРодителя = Истина;
				ЭлементыЗадания = текРодитель.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					Если текЗадание.Выбран Тогда
						ОбновитьРодителя = Ложь;
					КонецЕсли;
				КонецЦикла; 
				Если ОбновитьРодителя Тогда
					текРодитель.Выбран = Ложь;
				КонецЕсли;
				Если ИспользуютсяЭлектронныеГеографическиеКарты И текЦветоваяСхема <> Неопределено Тогда
					Если текСтрока.Выделен Тогда
						ИзменитьИконкуМаркера(текСтрока.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
					Иначе
						ИзменитьИконкуМаркера(текСтрока.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	ОбновитьПоказателиПоВыбраннымСтрокам();
		
КонецПроцедуры // УбратьВыборСВыделенныхСтрокДереваРейсов()

&НаКлиенте
Процедура ИзменитьРейс(Команда)
	
	текСтрока = Элементы.ДеревоРейсы.ТекущиеДанные;
	Если текСтрока = Неопределено Или НЕ ЗначениеЗаполнено(текСтрока.Рейс) Тогда
		ТекстСообщения = НСтр("ru = 'В дереве рейсов не выбрана строка с рейсом для изменения'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если текСтрока.Уровень = 1 Тогда
		текСтрока = текСтрока.ПолучитьРодителя();
	КонецЕсли;
	
	Если Не (текСтрока.ДоступноИзменениеМаршрута И текСтрока.Геокодирован) Тогда
		ТекстСообщения = НСтр("ru = 'Для выбранного рейса недоступно изменение маршрута'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли; 	

	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Рейс", текСтрока.Рейс);
	сткПараметры.Вставить("НачалоПериодаПланирования", НачалоПериодаПланирования);
	сткПараметры.Вставить("ОкончаниеПериодаПланирования", ОкончаниеПериодаПланирования);
	сткПараметры.Вставить("ПараметрыПланирования", ПараметрыПланирования);
	сткПараметры.Вставить("РежимКорректировки", РежимКорректировки);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаРедактированияРейса", сткПараметры,,,,, Новый ОписаниеОповещения("ИзменениеРейсаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ИзменитьРейс()

&НаКлиенте
Процедура ОбновитьДеревоРейсов(Команда)
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		// клиентские вызовы
		ОчиститьРейсы();
	КонецЕсли; 
	
	// серверные вызовы
	ОбновитьДеревоРейсовНаСервере();
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		// клиентские вызовы
		ОтобразитьРейсыНаКарте();
		Элементы.ДеревоРейсы.Обновить();
	КонецЕсли; 
	
	ОбновитьПоказателиПоВыбраннымСтрокам();
	ОбновитьДоступность();
	
КонецПроцедуры // ОбновитьДеревоРейсов()

&НаКлиенте
Процедура СпланироватьРейсы(Команда)
	
	Отказ = Ложь;
	
	Если НЕ (УчитыватьМассу Или УчитыватьОбъем Или УчитыватьКоличествоМест Или УчитыватьКоличествоЗагрузочныхМетров) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В конфигурации не включено использование ни одного из доступных контролируемых показателей (масса, объем, груз. места или загр. метры)! Автоматическое планирование невозможно'"),,,, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидПеревозки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан вид перевозки'"),, "ВидПеревозки",, Отказ);
	КонецЕсли;
	
	Если СписокТранспорта.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указаны доступные транспортные средства'"),, "ДеревоРейсыОткрытьФормуВыбораДоступныхТС",, Отказ);
	КонецЕсли;
	
	Если ПараметрыПланирования.МетодРешенияЗМТ = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗМТ.МеханизмСбереженийКларкаРайта") Тогда
		Если ПараметрыПланирования.ОграничитьКоличествоРейсов И Не ЗначениеЗаполнено(ПараметрыПланирования.КоличествоРейсов) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указано количество формируемых рейсов'"),, "ДеревоРейсыУстановитьПараметрыПланирования",, Отказ);
		КонецЕсли;
		Если ПараметрыПланирования.ОграничитьКоличествоТочекВРейсе И Не ЗначениеЗаполнено(ПараметрыПланирования.КоличествоТочекВРейсе) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указано количество точек в рейсе'"),, "ДеревоРейсыУстановитьПараметрыПланирования",, Отказ);
		КонецЕсли;
		Если ПараметрыПланирования.ОграничитьРазмерГруза И Не ЗначениеЗаполнено(ПараметрыПланирования.РазмерГруза) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан предельный размер груза'"),, "ДеревоРейсыУстановитьПараметрыПланирования",, Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТочкаОтсчета) И НачалоПериодаПланирования < ТекущаяДатаСеансаНаСервере() Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Допускается только будущий период планирования'"),, "ПериодПланирования",, Отказ);
		КонецЕсли;
	ИначеЕсли ПараметрыПланирования.МетодРешенияЗМТ = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗМТ.ЗональноеПланирование") Тогда
		Если ПараметрыПланирования.ОграничиватьСписокЗон И ПараметрыПланирования.ДоступныеЗоны.Количество() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указаны доступные зоны для зонального планирования'"),, "ДеревоРейсыУстановитьПараметрыПланирования",, Отказ);
		КонецЕсли;	
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	АдресВременногоХранилища = ""; 
	РешитьЗадачуМаршрутизацииТранспортаНаСервере(ИдентификаторЗадания, АдресВременногоХранилища, Отказ);
	Если Не Отказ Тогда
		Если ИдентификаторЗадания <> Неопределено Тогда 
			сткПараметры = Новый Структура;
			сткПараметры.Вставить("ИдентификаторЗадания" , ИдентификаторЗадания);
			сткПараметры.Вставить("АдресВременногоХранилища" , АдресВременногоХранилища);
			ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаАвтоматическогоПланированияРейсов", сткПараметры,,,,, Новый ОписаниеОповещения("АвтоматическоеПланированиеРейсовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		КонецЕсли;
	КонецЕсли; 
			
КонецПроцедуры

&НаСервере
Функция ТекущаяДатаСеансаНаСервере()
	
	Возврат ТекущаяДатаСеанса();

КонецФункции // СпланироватьРейсы()

&НаКлиенте
Процедура РаспределитьЗаданияПоПериодическимРейсам(Команда)
	
	Если СписокЗаданий.Количество() = 0 Тогда 
		ТекстСообщения = НСтр("ru = 'В списке заданий отсутствуют задания для распределения по периодическим рейсам'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Будет произведена попытка распределения заданий по периодическим рейсам. Продолжить?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("РаспределитьЗаданияПоПериодическимРейсамВопросЗавершение", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры // РаспределитьЗаданияПоПериодическимРейсам()

&НаКлиенте
Процедура УстановитьПараметрыПланирования(Команда)
	
	Если Не (УчитыватьМассу Или УчитыватьОбъем Или УчитыватьКоличествоМест Или УчитыватьКоличествоЗагрузочныхМетров) Тогда
		ТекстСообщения = НСтр("ru = 'В конфигурации не включено использование ни одного из доступных контролируемых показателей (масса, объем, груз. места или загр. метры)! Установка параметров невозможна'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан вид перевозки'"));
		Возврат;
	КонецЕсли;
	
	ОбщийСписокТранспорта = Новый СписокЗначений;
	СписокПартнеров = Новый СписокЗначений;
	Для каждого текТранспорт Из СписокТранспорта Цикл
		Если ЗначениеЗаполнено(текТранспорт.ТранспортноеСредство) Тогда
			ОбщийСписокТранспорта.Добавить(текТранспорт.ТранспортноеСредство);
		ИначеЕсли ЗначениеЗаполнено(текТранспорт.ТипТС) Тогда
			ОбщийСписокТранспорта.Добавить(текТранспорт.ТипТС);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текТранспорт.Перевозчик) Тогда
			СписокПартнеров.Добавить(текТранспорт.Перевозчик);
		КонецЕсли; 
	КонецЦикла;
	
	Для каждого текЗадание Из СписокЗаданий Цикл
		СписокПартнеров.Добавить(текЗадание.Отправитель);
		СписокПартнеров.Добавить(текЗадание.Получатель);
	КонецЦикла; 
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбщийСписокТранспорта);
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокПартнеров);
	ИндексПустого = СписокПартнеров.НайтиПоЗначению(ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка"));
	Если ИндексПустого <> Неопределено Тогда
		СписокПартнеров.Удалить(ИндексПустого);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыПланирования", ПараметрыПланирования);
	ПараметрыФормы.Вставить("РежимКорректировки", РежимКорректировки);
	ПараметрыФормы.Вставить("КлючВарианта", ВариантПланирования);
	ПараметрыФормы.Вставить("СписокТранспорта", ОбщийСписокТранспорта);
	ПараметрыФормы.Вставить("СписокПартнеров", СписокПартнеров);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаУстановкиПараметровПланирования", ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыПланированияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВыбранныйГрузВРейс(Команда)
	
	СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания();
	СтрокаПриемник = Элементы.ДеревоРейсы.ТекущиеДанные;
	Если СтрокаПриемник = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Выберите рейс для изменения'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;

	Если СтрокаПриемник.Уровень = 1 Тогда
		СтрокаПриемник = СтрокаПриемник.ПолучитьРодителя();
	КонецЕсли;
	
	Если Не РежимКорректировки И ЗначениеЗаполнено(СтрокаПриемник.Рейс) И Не СтрокаПриемник.ДоступноИзменениеМаршрута Тогда
		ТекстСообщения = НСтр("ru = 'Для выбранного рейса недоступно изменение маршрута'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ТекстОшибки = "";
	
	Если РежимКорректировки Тогда
		мсвЗаданий = Новый Массив;
		ИскомыеСтрокиСпискаЗаданий = СписокЗаданий.НайтиСтроки(Новый Структура("Выбрано", Истина));
		Для каждого текСтрока Из ИскомыеСтрокиСпискаЗаданий Цикл
			мсвЗаданий.Добавить(текСтрока.ЗаданиеНаПеревозку);
		КонецЦикла;
		ИДСтрокиНазначения = СтрокаПриемник.ПолучитьИдентификатор();
		ОткрытьКорректировкуСДобавлениемЗаданий(ИДСтрокиНазначения, мсвЗаданий);
		ЗаданияКУдалениюИзСписка = Новый Массив;
		Возврат;
	КонецЕсли; 
	сткИндексыУдаляемыхОбъектовКарты = Новый Структура;
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыРейсов"       , Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыМаркеров"     , Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыТочекМаршрута", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("МассивОтправлений"   , Новый Массив);
	ЗаданияКУдалениюИзСписка = ПеренестиВыбранныйГрузВРейсНаСервере(СтрокаПриемник.ПолучитьИдентификатор(), сткИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ);
	Если Отказ Тогда
		//Если ТекстОшибки = "" Тогда
		//	ТекстОшибки = НСтр("ru = 'Произошла неисправимая ошибка. Рекомендуется обновить данные формы'");
		//КонецЕсли; 
		//ПоказатьПредупреждение(, ТекстОшибки);
		ОповеститьПользователяОбОшибке(, ТекстОшибки);
	Иначе
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			// очистка старых объектов с карты
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов Цикл
				УдалитьОтображениеРейса(текИндексКарты);
			КонецЦикла; 
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров Цикл
				УдалитьОбъект(текИндексКарты);
			КонецЦикла;
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута Цикл
				УдалитьОбъект(текИндексКарты, Истина);
			КонецЦикла;
			
			Если сткИндексыУдаляемыхОбъектовКарты.МассивОтправлений.Количество() Тогда
				Для каждого текОтправление Из сткИндексыУдаляемыхОбъектовКарты.МассивОтправлений Цикл
					ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Выбрано", текОтправление, Истина));
					Если ИскомыеСтроки.Количество() = 0 Тогда
						УбратьВыборМаркера(текОтправление);
					КонецЕсли;
				КонецЦикла;
				ОбработатьСостояниеОтправлений(сткИндексыУдаляемыхОбъектовКарты.МассивОтправлений);
			КонецЕсли;
		КонецЕсли; 
		
		//мсвОтправлений = Новый Массив;
		//НовыеСтроки = Новый Массив;
		//КоличествоСтрок = СписокЗаданий.Количество();
		//Пока КоличествоДобавленныхСтрок > 0 Цикл
		//	ИндексСтроки = КоличествоСтрок - КоличествоДобавленныхСтрок;
		//	ТекущаяСтрока = СписокЗаданий[ИндексСтроки];
		//	мсвОтправлений.Добавить(ТекущаяСтрока.АдресОтправления);
		//	НовыеСтроки.Добавить(ТекущаяСтрока);
		//	КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
		//КонецЦикла;
		
		ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
		Для каждого текИндекс Из СписокИндексов Цикл   
			ИДТекущейСтроки = ЭлементыРейсы.Получить(текИндекс.Значение).ПолучитьИдентификатор();
			Элементы.ДеревоРейсы.Развернуть(ИДТекущейСтроки, Ложь);
		КонецЦикла;
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			//Если мсвОтправлений.Количество() Тогда
			//	мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
			//	Для каждого текОтправление Из мсвОтправлений Цикл
			//		ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текОтправление));
			//		Для каждого текСтрока Из ИскомыеСтроки Цикл
			//			Если Не текСтрока.Пометка Тогда
			//				ИзменитьВидимостьОбъекта(текСтрока.ИндексВМассиве, Истина);
			//			КонецЕсли; 
			//		КонецЦикла; 
			//	КонецЦикла;
			//КонецЕсли;
			//
			//Если НовыеСтроки.Количество() Тогда
			//	ОтобразитьОбъектыНаКарте(НовыеСтроки);
			//КонецЕсли;
			Если СписокИндексов.Количество() Тогда
				ОтобразитьРейсыНаКарте();
			КонецЕсли;
		Иначе
			СписокИндексов.Очистить();
		КонецЕсли;
		
		ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
		Элементы.ДеревоРейсы.ВыделенныеСтроки.Очистить();
		ОбновитьПоказателиПоВыбраннымСтрокам();
		ОбновитьДоступность();
		
		Элементы.ДеревоРейсы.ТекущаяСтрока = ИДТекущейСтроки;
	КонецЕсли;
	
	ПервыйЗапуск = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКорректировкуСДобавлениемЗаданий(Знач ИДСтрокиНазначения, Знач мсвЗаданий)
	
	СтрокаПриемник = ДеревоРейсы.НайтиПоИдентификатору(ИДСтрокиНазначения);
	Если СтрокаПриемник.Уровень = 1 Тогда
		СтрокаПриемник = СтрокаПриемник.ПолучитьРодителя();
	КонецЕсли;
	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	ИндексСтроки = ЭлементыДерева.Индекс(СтрокаПриемник);
	СписокИндексов.Добавить(ИндексСтроки);
	текРейс = СтрокаПриемник.Рейс;
	ПозицияПогрузки = Неопределено;
	ПозицияРазгрузки = Неопределено;
	СтрокиЛучших = ЛучшиеРейсы.НайтиСтроки(Новый Структура("Рейс", текРейс));
	Если СтрокиЛучших.Количество() > 0 Тогда
		ЭлементСпискаЛучших = СтрокиЛучших[0];
		ПозицияПогрузки = ЭлементСпискаЛучших.ПозицияПогрузки;
		ПозицияРазгрузки = ЭлементСпискаЛучших.ПозицияРазгрузки;
	КонецЕсли; 
	ПараметрыФормы = Новый Структура("Основание, ДобавляемыеЗадания, ПредпочитаемаяПозицияПогрузки, ПредпочитаемаяПозицияРазгрузки", текРейс, мсвЗаданий, ПозицияПогрузки, ПозицияРазгрузки);
	ОткрытьФорму("Документ.упКорректировкаРейса.Форма.ФормаДокумента", ПараметрыФормы,, текРейс);

КонецПроцедуры // ПеренестиВыбранныйГрузВРейс()

&НаКлиенте
Процедура УдалитьВыбранныйГрузИзРейсов(Команда)
	
	ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания();
	Отказ = Ложь;
	ТекстОшибки = "";
	сткИндексыУдаляемыхОбъектовКарты = Новый Структура;
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыРейсов", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыМаркеров", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыТочекМаршрута", Новый Массив);
	
	мсвРейсов = Новый Массив;
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		Если ЭлементРейс.Выбран Тогда
			мсвЗаданий = Новый Массив;
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				Если ЭлементРейс.ИндексВМассивеМаршрут > 0 Тогда
					сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(ЭлементРейс.ИндексВМассивеМаршрут);
				КонецЕсли;
				Для каждого текАдресОтправления Из ЭлементРейс.АдресаОтправлений Цикл
					сткДанныеАдреса = текАдресОтправления.Значение;
					Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
						сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(сткДанныеАдреса.ИндексВМассиве);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
			Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
				Если ЭлементЗадание.Выбран Тогда
					Если РазбиватьЗаданияПоГрузовымМестам Тогда
						сткДанныеОГрузе = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
						ЗаполнитьЗначенияСвойств(сткДанныеОГрузе, ЭлементЗадание);
						мсвЗаданий.Добавить(сткДанныеОГрузе);
					Иначе	
						мсвЗаданий.Добавить(ЭлементЗадание.ЗаданиеНаПеревозку);
					КонецЕсли;
				КонецЕсли;
				Если ИспользуютсяЭлектронныеГеографическиеКарты И ЭлементЗадание.ИндексВМассиве > 0 Тогда
					сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(ЭлементЗадание.ИндексВМассиве);
				КонецЕсли;
			КонецЦикла;
			сткДанныеПоРейсу = Новый Структура("Рейс, Индекс, ДополнительныеПараметры, МассивЗаданий", ЭлементРейс.Рейс, ЭлементыРейсы.Индекс(ЭлементРейс), Новый Структура("НомерЦветовойСхемы", ЭлементРейс.НомерЦветовойСхемы), мсвЗаданий);
			мсвРейсов.Добавить(сткДанныеПоРейсу);
		КонецЕсли;
	КонецЦикла; 
	
	Если мсвРейсов.Количество() Тогда
		ПеренестиСтрокиИзРейсаВСписокЗаданийНаСервере(мсвРейсов, сткИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ);
		Если Отказ Тогда
			//Если ТекстОшибки = "" Тогда
			//	ТекстОшибки = НСтр("ru = 'Произошла неисправимая ошибка. Рекомендуется обновить данные формы'");
			//КонецЕсли; 
			//ПоказатьПредупреждение(, ТекстОшибки);
			ОповеститьПользователяОбОшибке(, ТекстОшибки);
		Иначе
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				// очистка старых объектов с карты
				Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов Цикл
					УдалитьОтображениеРейса(текИндексКарты);
				КонецЦикла; 
				Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров Цикл
					УдалитьОбъект(текИндексКарты);
				КонецЦикла;
				Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута Цикл
					УдалитьОбъект(текИндексКарты, Истина);
				КонецЦикла;
			КонецЕсли; 
					
			мсвОтправлений = Новый Массив;
			НовыеСтроки = Новый Массив;
			КоличествоСтрок = СписокЗаданий.Количество();
			Пока КоличествоДобавленныхСтрок > 0 Цикл
				ИндексСтроки = КоличествоСтрок - КоличествоДобавленныхСтрок;
				ТекущаяСтрока = СписокЗаданий[ИндексСтроки];
				мсвОтправлений.Добавить(ТекущаяСтрока.АдресОтправления);
				НовыеСтроки.Добавить(ТекущаяСтрока);
				КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
			КонецЦикла;
			
			Для каждого текИндекс Из СписокИндексов Цикл
				Элементы.ДеревоРейсы.Развернуть(ДеревоРейсы.ПолучитьЭлементы().Получить(текИндекс.Значение).ПолучитьИдентификатор(), Ложь);
			КонецЦикла; 
			
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				Если мсвОтправлений.Количество() Тогда
					мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
					Для каждого текОтправление Из мсвОтправлений Цикл
						ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текОтправление));
						Для каждого текСтрока Из ИскомыеСтроки Цикл
							Если Не текСтрока.Пометка Тогда
								ИзменитьВидимостьОбъекта(текСтрока.ИндексВМассиве, Истина);
							КонецЕсли; 
						КонецЦикла; 
					КонецЦикла;
				КонецЕсли;
				
				Если НовыеСтроки.Количество() Тогда
					ОтобразитьОбъектыНаКарте(НовыеСтроки);
				Иначе
					ОтобразитьОбъектыНаКарте();
				КонецЕсли;
				Если СписокИндексов.Количество() Тогда
					ОтобразитьРейсыНаКарте();
				КонецЕсли;
			Иначе
				СписокИндексов.Очистить();
			КонецЕсли;
			
			ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
			Элементы.ДеревоРейсы.ВыделенныеСтроки.Очистить();
			ОбновитьПоказателиПоВыбраннымСтрокам();
			ОбновитьДоступность();
		КонецЕсли;
	Иначе
		ПредставлениеСообщения = СтрШаблон("ru = 'Не выбрано ни одного %1 для исключения из рейсов'", ?(РазбиватьЗаданияПоГрузовымМестам, "грузового места", "задания на перевозку"));
		ПоказатьПредупреждение(, НСтр(ПредставлениеСообщения));
	КонецЕсли; 
	
	ПервыйЗапуск = Истина;
	
КонецПроцедуры // УдалитьВыбранныйГрузИзРейсов()

&НаКлиенте
Процедура СвернутьДеревоРейсов(Команда)
	
	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого текРейс Из ЭлементыДерева Цикл
		Элементы.ДеревоРейсы.Свернуть(текРейс.ПолучитьИдентификатор());
	КонецЦикла; 
	
КонецПроцедуры // СвернутьДеревоРейсов()

&НаКлиенте
Процедура РазвернутьДеревоРейсов(Команда)
	
	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого текРейс Из ЭлементыДерева Цикл
		Элементы.ДеревоРейсы.Развернуть(текРейс.ПолучитьИдентификатор());
	КонецЦикла;	
	
КонецПроцедуры // РазвернутьДеревоРейсов()

&НаКлиенте
Процедура ОтобразитьПредыдущуюГруппуРейсов(Команда)
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Если ЭлементыРейсы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	КоличествоВидимых = ПараметрыПланирования.КоличествоВидимыхРейсов;
	
	Если КоличествоРейсов <= КоличествоВидимых Тогда
		// отобразим невидимые рейсы
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если Не текРейс.Пометка И ЗначениеЗаполнено(текРейс.Рейс) И текРейс.Геокодирован Тогда
				СписокИндексов.Добавить(ЭлементыРейсы.Индекс(текРейс));
				текРейс.Пометка = Истина;
			КонецЕсли; 
		КонецЦикла; 
		
	Иначе
		ИндексПервогоВидимого = 0;
		
		// ищем первый видимый рейс
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если текРейс.Пометка Тогда
				ИндексПервогоВидимого = ЭлементыРейсы.Индекс(текРейс);
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если Не текРейс.Пометка Тогда
				Продолжить;
			КонецЕсли;
			стрИндексов = "";
			Для каждого текАдресОтправления Из текРейс.АдресаОтправлений Цикл
				сткДанныеАдреса = текАдресОтправления.Значение;
				Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
					стрИндексов = стрИндексов + ПредставлениеЧисла(сткДанныеАдреса.ИндексВМассиве) + ";";
				КонецЕсли;
			КонецЦикла; 
			
			ЭлементыЗадания = текРейс.ПолучитьЭлементы();
			Для каждого текЗадание Из ЭлементыЗадания Цикл
				Если текЗадание.ИндексВМассиве > 0 Тогда
					стрИндексов = стрИндексов + ПредставлениеЧисла(текЗадание.ИндексВМассиве) + ";";
				КонецЕсли;
			КонецЦикла; 
			
			текРейс.Пометка = Ложь;    
			ИзменитьВидимостьРейса(текРейс);
			Если ЗначениеЗаполнено(стрИндексов) Тогда
				ИзменитьВидимостьОбъектов(стрИндексов, текРейс.Пометка, Ложь);
			КонецЕсли;
		КонецЦикла; 
		
		текИндекс = ИндексПервогоВидимого - 1;
		Пока КоличествоВидимых > 0 Цикл
			Если текИндекс = ИндексПервогоВидимого Тогда
				Прервать;
			КонецЕсли; 
			Если текИндекс < 0 Тогда
				текИндекс = ЭлементыРейсы.Количество() - 1;
			КонецЕсли; 
			
			текРейс = ЭлементыРейсы.Получить(текИндекс);
			Если Не текРейс.Пометка И ЗначениеЗаполнено(текРейс.Рейс) И текРейс.Геокодирован Тогда
				СписокИндексов.Добавить(ЭлементыРейсы.Индекс(текРейс));
				КоличествоВидимых = КоличествоВидимых - 1;
				текРейс.Пометка = Истина;
			КонецЕсли; 
			
			текИндекс = текИндекс - 1;
		КонецЦикла; 
		
	КонецЕсли; 
	
	Если СписокИндексов.Количество() Тогда
		ИдентификаторСтроки = ЭлементыРейсы.Получить(СписокИндексов[СписокИндексов.Количество() - 1].Значение).ПолучитьИдентификатор();
		ОтобразитьРейсыНаКарте();
		Элементы.ДеревоРейсы.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСледующуюГруппуРейсов(Команда)
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Если ЭлементыРейсы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	КоличествоВидимых = ПараметрыПланирования.КоличествоВидимыхРейсов;
	
	Если КоличествоРейсов <= КоличествоВидимых Тогда
		// отобразим невидимые рейсы
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если Не текРейс.Пометка И ЗначениеЗаполнено(текРейс.Рейс) И текРейс.Геокодирован Тогда
				СписокИндексов.Добавить(ЭлементыРейсы.Индекс(текРейс));
				текРейс.Пометка = Истина;
			КонецЕсли; 
		КонецЦикла; 
		
	Иначе
		ИндексПоследнегоВидимого = 0;
		
		// ищем последний видимый рейс
		Сч = ЭлементыРейсы.Количество() - 1;
		Пока Сч >= 0 Цикл
			текРейс = ЭлементыРейсы.Получить(Сч);
			Если текРейс.Пометка Тогда
				ИндексПоследнегоВидимого = ЭлементыРейсы.Индекс(текРейс);
				Прервать;
			КонецЕсли; 
			Сч = Сч - 1;
		КонецЦикла; 
		
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если Не текРейс.Пометка Тогда
				Продолжить;
			КонецЕсли;
			стрИндексов = "";
			Для каждого текАдресОтправления Из текРейс.АдресаОтправлений Цикл
				сткДанныеАдреса = текАдресОтправления.Значение;
				Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
					стрИндексов = стрИндексов + ПредставлениеЧисла(сткДанныеАдреса.ИндексВМассиве) + ";";
				КонецЕсли;
			КонецЦикла; 
			
			ЭлементыЗадания = текРейс.ПолучитьЭлементы();
			Для каждого текЗадание Из ЭлементыЗадания Цикл
				Если текЗадание.ИндексВМассиве > 0 Тогда
					стрИндексов = стрИндексов + ПредставлениеЧисла(текЗадание.ИндексВМассиве) + ";";
				КонецЕсли;
			КонецЦикла; 
			
			текРейс.Пометка = Ложь;    
			ИзменитьВидимостьРейса(текРейс);
			Если ЗначениеЗаполнено(стрИндексов) Тогда
				ИзменитьВидимостьОбъектов(стрИндексов, текРейс.Пометка, Ложь);
			КонецЕсли;
		КонецЦикла; 
		
		текИндекс = ?(ИндексПоследнегоВидимого = 0, 0, ИндексПоследнегоВидимого + 1);
		КоличествоСтрок = ЭлементыРейсы.Количество();
		ЦиклНачат = Ложь;
		Пока КоличествоВидимых > 0 Цикл
			Если текИндекс > КоличествоСтрок - 1 Тогда
				текИндекс = 0;
			КонецЕсли; 
			
			Если текИндекс = ИндексПоследнегоВидимого Тогда
				Если ЦиклНачат Тогда
					Прервать;
				Иначе 
					ЦиклНачат = Истина;	
				КонецЕсли; 
			КонецЕсли; 
			
			текРейс = ЭлементыРейсы.Получить(текИндекс);
			Если Не текРейс.Пометка И ЗначениеЗаполнено(текРейс.Рейс) И текРейс.Геокодирован Тогда
				СписокИндексов.Добавить(ЭлементыРейсы.Индекс(текРейс));
				КоличествоВидимых = КоличествоВидимых - 1;
				текРейс.Пометка = Истина;
			КонецЕсли; 
			
			текИндекс = текИндекс + 1;
		КонецЦикла; 
		
	КонецЕсли; 
	
	Если СписокИндексов.Количество() Тогда
		ИдентификаторСтроки = ЭлементыРейсы.Получить(СписокИндексов[0].Значение).ПолучитьИдентификатор();
		ОтобразитьРейсыНаКарте();
		Элементы.ДеревоРейсы.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли
	
КонецПроцедуры

#Область КонтекстноеМенюДереваРейсов

&НаКлиенте
Процедура ОптимизироватьВыделенныеРейсы(Команда)
	
	ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
	Если ВыделенныеСтрокиДерева.Количество() Тогда
		мсвРейсов = Новый Массив;
		Для каждого ИдентификаторСтроки Из ВыделенныеСтрокиДерева Цикл
			текСтрокаДерева = ДеревоРейсы.НайтиПоИдентификатору(ИдентификаторСтроки);
			Если текСтрокаДерева <> Неопределено И ЗначениеЗаполнено(текСтрокаДерева.Рейс) Тогда
				Если текСтрокаДерева.Уровень = 0 Тогда
					Если текСтрокаДерева.ДоступноИзменениеМаршрута И текСтрокаДерева.Геокодирован Тогда
						мсвРейсов.Добавить(текСтрокаДерева);
					КонецЕсли;
				Иначе
					текРодитель = текСтрокаДерева.ПолучитьРодителя();
					Если текРодитель.ДоступноИзменениеМаршрута И текРодитель.Геокодирован Тогда
						мсвРейсов.Добавить(текРодитель);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;  
		Если мсвРейсов.Количество() Тогда
			Отказ = ТипЗнч(ПараметрыПланирования.МетодРешенияЗК) = Тип("СправочникСсылка.упАлгоритмыОптимизации") И Не ПроверитьКорректностьПоставляемогоАлгоритмаОптимизации(ПараметрыПланирования.МетодРешенияЗК);
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			мсвРейсов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвРейсов);
			
			мсвИдентификаторов = Новый Массив;
			сткПериодПланирования = Новый Структура;
			сткПериодПланирования.Вставить("НачалоПериодаПланирования"   , НачалоПериодаПланирования);
			сткПериодПланирования.Вставить("ОкончаниеПериодаПланирования", ОкончаниеПериодаПланирования);
			Для каждого текДанные Из мсвРейсов Цикл
				ИдентификаторЗадания = Неопределено;
				ИдентификаторСтроки  = текДанные.ПолучитьИдентификатор();
				АдресВременногоХранилища = ""; 
				
				ПересчитатьМаршрутНаСервере(текДанные.Рейс, ПараметрыПланирования, ИдентификаторЗадания, АдресВременногоХранилища, УникальныйИдентификатор, сткПериодПланирования);
				Если ИдентификаторЗадания <> Неопределено Тогда 
					сткДанныеРейса = Новый Структура;
					сткДанныеРейса.Вставить("Рейс"                    , текДанные.Рейс);
					сткДанныеРейса.Вставить("ИдентификаторЗадания"    , ИдентификаторЗадания);
					сткДанныеРейса.Вставить("ИдентификаторСтроки"     , ИдентификаторСтроки);
					сткДанныеРейса.Вставить("АдресВременногоХранилища", АдресВременногоХранилища);
					мсвИдентификаторов.Добавить(сткДанныеРейса);
				КонецЕсли;
			КонецЦикла; 
			Если мсвИдентификаторов.Количество() Тогда
				сткПараметры = Новый Структура;
				сткПараметры.Вставить("МассивИдентификаторов"           , мсвИдентификаторов);
				сткПараметры.Вставить("Метод"                           , ПараметрыПланирования.МетодРешенияЗК);
				сткПараметры.Вставить("РасчетИзФормыРедактированияРейса", Ложь);
				ПараметрыФормы = Новый Структура();
				ПараметрыФормы.Вставить("ПараметрыОптимизации", сткПараметры);
				ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаОптимизацииМаршрута", ПараметрыФормы,,,,, Новый ОписаниеОповещения("ОптимизацияМаршрутаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьКорректностьПоставляемогоАлгоритмаОптимизации(АлгоритмОптимизации)
	
	Возврат Справочники.упАлгоритмыОптимизации.ПроверитьКорректностьПоставляемогоАлгоритмаОптимизации(АлгоритмОптимизации);

КонецФункции

&НаКлиенте
Процедура СкрытьНевыделенныеРейсы(Команда)
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого текРейс Из ЭлементыРейсы Цикл
		
		Если Не текРейс.Пометка Или текРейс.Выделен Тогда
			Продолжить;
		КонецЕсли;
		стрИндексов = "";
		Для каждого текАдресОтправления Из текРейс.АдресаОтправлений Цикл
			сткДанныеАдреса = текАдресОтправления.Значение;
			Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
				стрИндексов = стрИндексов + ПредставлениеЧисла(сткДанныеАдреса.ИндексВМассиве) + ";";
			КонецЕсли;
		КонецЦикла; 
		
		РейсВыделен = Ложь;
		ЭлементыЗадания = текРейс.ПолучитьЭлементы();
		Для каждого текЗадание Из ЭлементыЗадания Цикл
			Если текЗадание.Выделен Тогда
				РейсВыделен = Истина;
				Прервать;
			КонецЕсли;
			Если текЗадание.ИндексВМассиве > 0 Тогда
				стрИндексов = стрИндексов + ПредставлениеЧисла(текЗадание.ИндексВМассиве) + ";";
			КонецЕсли;
		КонецЦикла; 
		
		Если Не РейсВыделен Тогда
			текРейс.Пометка = НЕ текРейс.Пометка;    
			ИзменитьВидимостьРейса(текРейс);
			Если ЗначениеЗаполнено(стрИндексов) Тогда
				ИзменитьВидимостьОбъектов(стрИндексов, текРейс.Пометка, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКомментарийРейса(Команда)
	ТекущиеДанные = Элементы.ДеревоРейсы.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Рейс) Тогда
		текРейс	= ТекущиеДанные.Рейс;
		Комментарий	 = ПолучитьКомментарий(текРейс);
		Подсказка	 =  Нстр("ru = 'Введите комментарий для %1'");
		Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Подсказка, текРейс);
		сткДополнительныеПараметры = Новый Структура("Рейс, КомментарийДоИзменения", текРейс, Комментарий);
		текОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьВводСтрокиДляКомментарияПоРейсу",ЭтаФорма, сткДополнительныеПараметры);
		ПоказатьВводСтроки(текОписаниеОповещенияОЗавершении, Комментарий, Подсказка, , Истина);	
	Иначе
		ТекстСообщения = Нстр("ru = 'Комментарий указывается только для рейса'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#Область КоманднаяПанельКарты

&НаКлиенте
Процедура УстановитьРежимМножественногоВыделенияНаКарте(Команда)
	
	Если КартаСформирована Тогда
		Если РежимВыделенияНаКарте = 1 Тогда
			РежимВыделенияНаКарте = 0;
			Элементы.УстановитьРежимМножественногоВыделенияНаКарте.Пометка = Ложь;
			УстановитьРежимМножественногоВыделения(Ложь);
		Иначе	
			Если РежимВыделенияНаКарте = 2 Тогда
				УстановитьРежимМножественногоСнятияВыделения(Ложь);
			КонецЕсли;
			РежимВыделенияНаКарте = 1;
			Элементы.УстановитьРежимМножественногоВыделенияНаКарте.Пометка = Истина;
			Элементы.УстановитьРежимМножественногоСнятияВыделенияНаКарте.Пометка = Ложь;
			УстановитьРежимМножественногоВыделения(Истина);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры // УстановитьРежимМножественногоВыделенияНаКарте()

&НаКлиенте
Процедура УстановитьРежимМножественногоСнятияВыделенияНаКарте(Команда)
	
	Если КартаСформирована Тогда
		Если РежимВыделенияНаКарте = 2 Тогда 
			РежимВыделенияНаКарте = 0;
			Элементы.УстановитьРежимМножественногоСнятияВыделенияНаКарте.Пометка = Ложь;
			УстановитьРежимМножественногоСнятияВыделения(Ложь);
		Иначе	
			Если РежимВыделенияНаКарте = 1 Тогда
				УстановитьРежимМножественногоВыделения(Ложь);
			КонецЕсли;
			РежимВыделенияНаКарте = 2;
			Элементы.УстановитьРежимМножественногоСнятияВыделенияНаКарте.Пометка = Истина;
			Элементы.УстановитьРежимМножественногоВыделенияНаКарте.Пометка = Ложь;
			УстановитьРежимМножественногоСнятияВыделения(Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьРежимМножественногоСнятияВыделенияНаКарте()

&НаКлиенте
Процедура УстановитьРежимОтображенияКартыТочки(Команда)
	
	Если ИспользоватьКластеризацию Тогда		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрещено использовать команду при включенной кластеризации точек на карте'", КодЯзыка));		
		Возврат;
	КонецЕсли; 
	
	Если РежимОтображенияКарты > 0 Тогда
		СкрытьОбъектыПриИзмененииРежимаОтображенияКарты(РежимОтображенияКарты);
		РежимОтображенияКарты = 0;
	КонецЕсли;
	Элементы.УстановитьРежимОтображенияКартыТочки.Пометка = Истина;
	Элементы.УстановитьРежимОтображенияКартыЛинии.Пометка = Ложь;
	Элементы.УстановитьРежимОтображенияКартыМаршруты.Пометка = Ложь;
	
КонецПроцедуры // УстановитьРежимОтображенияКартыТочки()

&НаКлиенте
Процедура УстановитьРежимОтображенияКартыЛинии(Команда)
	
	Если ИспользоватьКластеризацию Тогда		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрещено использовать команду при включенной кластеризации точек на карте'", КодЯзыка));		
		Возврат;
	КонецЕсли; 
	
	Если РежимОтображенияКарты = 2 Тогда
		СкрытьОбъектыПриИзмененииРежимаОтображенияКарты(2);
	КонецЕсли;
	РежимОтображенияКарты = 1;
	ОбработатьИзменениеРежимаОтображенияКарты();
	Элементы.УстановитьРежимОтображенияКартыТочки.Пометка = Ложь;
	Элементы.УстановитьРежимОтображенияКартыЛинии.Пометка = Истина;
	Элементы.УстановитьРежимОтображенияКартыМаршруты.Пометка = Ложь;
	
КонецПроцедуры // УстановитьРежимОтображенияКартыЛинии()

&НаКлиенте
Процедура УстановитьРежимОтображенияКартыМаршруты(Команда)
	
	Если ИспользоватьКластеризацию Тогда		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрещено использовать команду при включенной кластеризации точек на карте'", КодЯзыка));		
		Возврат;
	КонецЕсли; 
	
	Если РежимОтображенияКарты = 1 Тогда
		СкрытьОбъектыПриИзмененииРежимаОтображенияКарты(1);
	КонецЕсли;
	РежимОтображенияКарты = 2;
	ОбработатьИзменениеРежимаОтображенияКарты();
	Элементы.УстановитьРежимОтображенияКартыТочки.Пометка = Ложь;
	Элементы.УстановитьРежимОтображенияКартыЛинии.Пометка = Ложь;
	Элементы.УстановитьРежимОтображенияКартыМаршруты.Пометка = Истина;
	
КонецПроцедуры // УстановитьРежимОтображенияКартыМаршруты()

&НаКлиенте
Процедура ВключитьКластеризацию(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Или СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ИспользоватьКластеризацию = Не ИспользоватьКластеризацию;
	Если ИспользоватьКластеризацию Тогда
		УстановитьРежимКластеризации();
		Элементы.ВключитьКластеризацию.Пометка = Истина;
	Иначе
		УбратьРежимКластеризации();
		Элементы.ВключитьКластеризацию.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ВключитьКластеризацию()

&НаКлиенте
Процедура ВключитьТеплокартуМасса(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Или СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Измерение) Или Измерение = "weight" Тогда
		ВключенаТеплокарта = Не ВключенаТеплокарта;
	КонецЕсли;
	ЭтаФорма.Измерение = "weight";	
	Если ВключенаТеплокарта Тогда
		УбратьРежимТеплокарты();
		УстановитьРежимТеплокарты();
		ВключитьКомандуИзмеренияТеплокарты(Измерение);
	Иначе	
		УбратьРежимТеплокарты();
		Элементы.ВключитьТеплокартуМасса.Пометка = Ложь;
		ЭтаФорма.Измерение = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьТеплокартуОбъем(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Или СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Измерение) Или Измерение = "volume" Тогда
		ВключенаТеплокарта = Не ВключенаТеплокарта;
	КонецЕсли;
	ЭтаФорма.Измерение = "volume";	
	Если ВключенаТеплокарта Тогда
		УбратьРежимТеплокарты();
		УстановитьРежимТеплокарты();
		ВключитьКомандуИзмеренияТеплокарты(Измерение);
	Иначе	
		УбратьРежимТеплокарты();
		Элементы.ВключитьТеплокартуОбъем.Пометка = Ложь;
		ЭтаФорма.Измерение = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьТеплокартуКоличествоМест(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Или СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Измерение) Или Измерение = "quantityCargo" Тогда
		ВключенаТеплокарта = Не ВключенаТеплокарта;
	КонецЕсли;
	ЭтаФорма.Измерение = "quantityCargo";	
	Если ВключенаТеплокарта Тогда
		УбратьРежимТеплокарты();
		УстановитьРежимТеплокарты();
		ВключитьКомандуИзмеренияТеплокарты(Измерение);
	Иначе	
		УбратьРежимТеплокарты();
		Элементы.ВключитьТеплокартуКоличествоМест.Пометка = Ложь;
		ЭтаФорма.Измерение = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьТеплокартуКоличествоЗагрузочныхМетров(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Или СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Измерение) Или Измерение = "quantityMeters" Тогда
		ВключенаТеплокарта = Не ВключенаТеплокарта;
	КонецЕсли;
	ЭтаФорма.Измерение = "quantityMeters";	
	
	Если ВключенаТеплокарта Тогда
		УбратьРежимТеплокарты();
		УстановитьРежимТеплокарты();
		ВключитьКомандуИзмеренияТеплокарты(Измерение);
	Иначе	
		УбратьРежимТеплокарты();
		Элементы.ВключитьТеплокартуКоличествоЗагрузочныхМетров.Пометка = Ложь;
		ЭтаФорма.Измерение = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьТеплокартуВременныеОкнаКСуткам(Команда)
	
	Если Не ЗначениеЗаполнено(ВидПеревозки) Или СписокЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Измерение) Или Измерение = "timeWindows" Тогда
		ВключенаТеплокарта = Не ВключенаТеплокарта;
	КонецЕсли;
	ЭтаФорма.Измерение = "timeWindows";

	Если ВключенаТеплокарта Тогда
		УбратьРежимТеплокарты();
		УстановитьРежимТеплокарты();
		ВключитьКомандуИзмеренияТеплокарты(Измерение);
	Иначе	
		УбратьРежимТеплокарты();
		Элементы.ВключитьТеплокартуВременныеОкнаКПериодуПланирования.Пометка = Ложь;
		ЭтаФорма.Измерение = "";
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьКомандуИзмеренияТеплокарты(Измерение)
	
	Элементы.ВключитьТеплокартуМасса.Пометка                             = ?(Измерение = "weight", Истина, Ложь);
	Элементы.ВключитьТеплокартуОбъем.Пометка                             = ?(Измерение = "volume", Истина, Ложь);
	Элементы.ВключитьТеплокартуКоличествоМест.Пометка                    = ?(Измерение = "quantityCargo", Истина, Ложь);
	Элементы.ВключитьТеплокартуКоличествоЗагрузочныхМетров.Пометка       = ?(Измерение = "quantityMeters", Истина, Ложь);
	Элементы.ВключитьТеплокартуВременныеОкнаКПериодуПланирования.Пометка = ?(Измерение = "timeWindows", Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьЗоны(Команда)
	
	Если Не СписокЗон.Количество() Тогда
		ОбновитьСписокЗонНаСервере();
	КонецЕсли; 
	
	ОтображатьЗоны = Не ОтображатьЗоны;
	Если ОтображатьЗоны Тогда
		ИзменитьВидимостьЗон(Истина);
		Элементы.ОтобразитьЗоны.Пометка = Истина;
	Иначе
		ИзменитьВидимостьЗон();
		Элементы.ОтобразитьЗоны.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтобразитьМестаИнтереса(Команда)
	
	Если НЕ СписокМестаИнтереса.Количество() Тогда
		ОбновитьСписокМестИнтересаНаСервере();
	КонецЕсли; 
	
	ОтображатьМестаИнтереса = НЕ ОтображатьМестаИнтереса;
	
	Элементы.ОтобразитьМестаИнтереса.Пометка = ОтображатьМестаИнтереса;
	ИзменитьВидимостьМестИнтереса(ОтображатьМестаИнтереса);
	
КонецПроцедуры // КомандаОтобразитьМестаИнтереса()

&НаКлиенте
Процедура УстановитьВидимыеЗоны(Команда)
	
	Если ОтображатьЗоны  Тогда
		сткПараметры = Новый Структура("СписокЗон", СписокЗон);
		ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаУстановкиВидимостиГеозон", сткПараметры,,,,, Новый ОписаниеОповещения("УстановитьВидимыеЗоныЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьПересекающиесяВТочкахРейсы(Команда)
	
	ВыделитьПересекающиесяВТочкахРейсыНаКлиенте();
	
	
КонецПроцедуры // ВыделитьПересекающиесяВТочкахРейсы()

&НаКлиенте
Процедура ВыделитьПересекающиесяВТочкахЗаявкиРазныхРейсов(Команда)
	
	ВыделитьПересекающиесяВТочкахРейсыНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьПересекающиесяВТочкахРейсыНаКлиенте(ПересечениеЗаявок = Ложь)
	
	мсвРейсов  = Новый Массив;
	соотРейсов = Новый Соответствие;
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		Если ЗначениеЗаполнено(ЭлементРейс.Рейс) И ЭлементРейс.Пометка Тогда
			мсвРейсов.Добавить(ЭлементРейс.Рейс);
			соотРейсов.Вставить(ЭлементРейс.Рейс, ЭлементРейс.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла; 
	
	Если мсвРейсов.Количество() > 1 Тогда
		мсвПересекающихсяРейсов = ВернутьПересекающиесяВТочкахРейсыНаСервере(мсвРейсов, ПересечениеЗаявок);
		Если мсвПересекающихсяРейсов.Количество() Тогда
			ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
			ВыделенныеСтрокиДерева.Очистить();
			Координаты = "";
			Для каждого текРейс Из мсвПересекающихсяРейсов Цикл
				ИдентификаторСтроки = соотРейсов.Получить(текРейс);
				ВыделенныеСтрокиДерева.Добавить(ИдентификаторСтроки);
				
				ИскомаяСтрока = ДеревоРейсы.НайтиПоИдентификатору(ИдентификаторСтроки);
				Координаты = Координаты + ИскомаяСтрока.КоординатыТочек;
				ЭлементыЗадания = ИскомаяСтрока.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					Координаты = Координаты + текЗадание.КоординатыТочек;
				КонецЦикла; 
			КонецЦикла; 
			ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания();
			ЦентрироватьТочкиНаКарте(Координаты);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АвтоматическаяМаршрутизация

&НаСервере
Процедура РаспределитьЗаданияПоПериодическимРейсамНаСервере(Отказ)
	
	ОписаниеОшибки = "";
	мсвЗаданий = СписокЗаданий.Выгрузить(, "ЗаданиеНаПеревозку").ВыгрузитьКолонку("ЗаданиеНаПеревозку");
	мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
	
    СхемаЗапроса = Новый СхемаЗапроса;
	ТекстЗапроса = упКомбинаторнаяОптимизация.ПолучитьТекстЗапросаРаспределенияЗаданийПоПериодическимРейсам();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
	ПакетЗапросов.Удалить(ПакетЗапросов.Количество() - 1);
	
	ЗапросВыбораСхемыЗапроса = ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ИтоговаяВыборка.Задание КАК Задание,
	|	ИтоговаяВыборка.Рейс КАК Рейс,
	|	ИтоговаяВыборка.Расписание КАК Расписание,
	|	ИтоговаяВыборка.МаршрутСледования КАК МаршрутСледования,
	|	ИтоговаяВыборка.Перевозчик КАК Перевозчик,
	|	ИтоговаяВыборка.Соглашение КАК Соглашение,
	|	ИтоговаяВыборка.ТипТС КАК ТипТС,
	|	ИтоговаяВыборка.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ИтоговаяВыборка.ПлановоеНачалоРейса КАК ПлановоеНачалоРейса,
	|	ИтоговаяВыборка.Порядок КАК Порядок,
	|	ИтоговаяВыборка.МаксимальноеКоличествоТочекВРейсе КАК МаксимальноеКоличествоТочекВРейсе,
	|	ИтоговаяВыборка.АдресОтправления КАК АдресОтправления,
	|	ИтоговаяВыборка.АдресПолучения КАК АдресПолучения,
	|	втОстатки.Масса КАК Масса,
	|	втОстатки.Объем КАК Объем,
	|	втОстатки.КоличествоМест КАК КоличествоМест,
	|	втОстатки.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	(ВЫБРАТЬ
	|		втАктивныеРейсы.Задание КАК Задание,
	|		втАктивныеРейсы.Рейс КАК Рейс,
	|		втАктивныеРейсы.ПериодическоеРасписание КАК Расписание,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(втАктивныеРейсы.ПериодическоеРасписание.ИзменятьМаршрутСледованияРейса, ЛОЖЬ) = ИСТИНА
	|				ТОГДА втАктивныеРейсы.ПериодическоеРасписание.МаршрутСледования
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упТиповыеМаршруты.ПустаяСсылка)
	|		КОНЕЦ КАК МаршрутСледования,
	|		NULL КАК Перевозчик,
	|		NULL КАК Соглашение,
	|		втАктивныеРейсы.ТипТС КАК ТипТС,
	|		втАктивныеРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
	|		втАктивныеРейсы.ДатаНачалаРейса КАК ПлановоеНачалоРейса,
	|		втАктивныеРейсы.МаксимальноеКоличествоТочекВРейсе КАК МаксимальноеКоличествоТочекВРейсе,
	|		втАктивныеРейсы.АдресОтправления КАК АдресОтправления,
	|		втАктивныеРейсы.АдресПолучения КАК АдресПолучения,
	|		0 КАК Порядок
	|	ИЗ
	|		втАктивныеРейсы15 КАК втАктивныеРейсы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втГрафикНовыйРейсов.Задание,
	|		NULL,
	|		втГрафикНовыйРейсов.ПериодическоеРасписание,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(втГрафикНовыйРейсов.ПериодическоеРасписание.ИзменятьМаршрутСледованияРейса, ЛОЖЬ) = ИСТИНА
	|				ТОГДА втГрафикНовыйРейсов.ПериодическоеРасписание.МаршрутСледования
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упТиповыеМаршруты.ПустаяСсылка)
	|		КОНЕЦ,
	|		втГрафикНовыйРейсов.Перевозчик,
	|		втГрафикНовыйРейсов.Соглашение,
	|		втГрафикНовыйРейсов.ТипТС,
	|		втГрафикНовыйРейсов.ТранспортноеСредство,
	|		втГрафикНовыйРейсов.ПлановоеНачалоРейса,
	|		втГрафикНовыйРейсов.МаксимальноеКоличествоТочекВРейсе,
	|		втГрафикНовыйРейсов.АдресОтправления,
	|		втГрафикНовыйРейсов.АдресПолучения,
	|		1
	|	ИЗ
	|		втГрафикНовыйРейсов10 КАК втГрафикНовыйРейсов) КАК ИтоговаяВыборка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО ИтоговаяВыборка.Задание = втОстатки.Задание
	|
	|УПОРЯДОЧИТЬ ПО
	|	втОстатки.Объем УБЫВ,
	|	втОстатки.Масса УБЫВ,
	|	втОстатки.КоличествоМест УБЫВ,
	|	втОстатки.КоличествоЗагрузочныхМетров УБЫВ,
	|	ИтоговаяВыборка.Порядок,
	|	ИтоговаяВыборка.ПлановоеНачалоРейса
	|ИТОГИ ПО
	|	Задание";
	ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ЗапросВыбораСхемыЗапроса = ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	втХарактеристикиТС.ТипТС,
	|	втХарактеристикиТС.ТранспортноеСредство,
	|	втХарактеристикиТС.Грузоподъемность,
	|	втХарактеристикиТС.Объем,
	|	втХарактеристикиТС.КоличествоМест,
	|	втХарактеристикиТС.КоличествоЗагрузочныхМетров
	|ИЗ 
	|	втХарактеристикиТС КАК втХарактеристикиТС";
	ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);

	ЗапросВыбораСхемыЗапроса = ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАктивныеРейсы.Рейс,
	|	втАктивныеРейсы.Масса,
	|	втАктивныеРейсы.Объем,
	|	втАктивныеРейсы.КоличествоМест,
	|	втАктивныеРейсы.КоличествоЗагрузочныхМетров
	|ИЗ 
	|   втАктивныеРейсы05 КАК втАктивныеРейсы";
	ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ЗапросВыбораСхемыЗапроса = ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втГрафикНовыйРейсов.ПериодическоеРасписание КАК Расписание,
	|	втГрафикНовыйРейсов.ПлановоеНачалоРейса,
	|   втГрафикНовыйРейсов.ОстатокТС 
	|ИЗ 
	|   втГрафикНовыйРейсов10 КАК втГрафикНовыйРейсов
	|УПОРЯДОЧИТЬ ПО
	|   ПериодическоеРасписание,
	|   ПлановоеНачалоРейса";
	ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);

	// Для подсчета количества точек по маршруту рейса
	ЗапросВыбораСхемыЗапроса = ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	втМаршрутыПоРейсам.Рейс КАК Рейс,
	|	втМаршрутыПоРейсам.Адрес
	|ИЗ
	|	втМаршрутыПоРейсам КАК втМаршрутыПоРейсам
	|ИТОГИ ПО
	|	Рейс";
	ЗапросВыбораСхемыЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("МассивЗаданий", мсвЗаданий);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоПериодаПланирования);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = МассивРезультатов.Количество();
	Если Не МассивРезультатов[КоличествоРезультатов - 4].Пустой() Тогда
		дзРезультатРаспределения = МассивРезультатов[КоличествоРезультатов - 5].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		тзЗагрузкаПоТС           = МассивРезультатов[КоличествоРезультатов - 4].Выгрузить();
		тзЗагрузкаПоРейсам       = МассивРезультатов[КоличествоРезультатов - 3].Выгрузить();
		тзРасписание             = МассивРезультатов[КоличествоРезультатов - 2].Выгрузить();
		дзАдресаПоРейсам		 = МассивРезультатов[КоличествоРезультатов - 1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		// Таблица для формирования распределения заданий по рейсам
		тзРаспределение = Новый ТаблицаЗначений;
		тзРаспределение.Колонки.Добавить("Рейс");
		тзРаспределение.Колонки.Добавить("Перевозчик");
		тзРаспределение.Колонки.Добавить("Соглашение");
		тзРаспределение.Колонки.Добавить("ТипТС");
		тзРаспределение.Колонки.Добавить("ТранспортноеСредство");
		тзРаспределение.Колонки.Добавить("Водитель1");
		тзРаспределение.Колонки.Добавить("Расписание");
		тзРаспределение.Колонки.Добавить("МаршрутСледования");
		тзРаспределение.Колонки.Добавить("ПлановоеНачалоРейса");
		тзРаспределение.Колонки.Добавить("МассивЗаданий");
		тзРаспределение.Колонки.Добавить("МассивАдресовТочек");
		тзРаспределение.Колонки.Добавить("МассаОстаток", Новый ОписаниеТипов("Число"));
		тзРаспределение.Колонки.Добавить("ОбъемОстаток", Новый ОписаниеТипов("Число"));
		тзРаспределение.Колонки.Добавить("КоличествоМестОстаток", Новый ОписаниеТипов("Число"));
		тзРаспределение.Колонки.Добавить("КоличествоЗагрузочныхМетровОстаток", Новый ОписаниеТипов("Число"));
		тзРаспределение.Колонки.Добавить("ОстатокТС", Новый ОписаниеТипов("Число"));
		тзРаспределение.Колонки.Добавить("КоличествоТочекОстаток", Новый ОписаниеТипов("Число"));
		
		Для каждого стрЗадание Из дзРезультатРаспределения.Строки Цикл
			ЗаданиеРаспределено = Ложь;
			Для каждого стрРаспределение Из стрЗадание.Строки Цикл
				Если ЗаданиеРаспределено Тогда
					Прервать;
				КонецЕсли;
				
				// Если удалось подобрать активный рейс для задания.
				Если ЗначениеЗаполнено(стрРаспределение.Рейс) Тогда
					ИскомаяСтрока = тзРаспределение.Найти(стрРаспределение.Рейс, "Рейс");
					
					Если ИскомаяСтрока = Неопределено Тогда
						НоваяСтрока = тзРаспределение.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, стрРаспределение);
						НоваяСтрока.МассивЗаданий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(стрРаспределение.Задание);
						
						мсвСтрокЗагрузки = тзЗагрузкаПоТС.НайтиСтроки(Новый Структура("ТипТС, ТранспортноеСредство", стрРаспределение.ТипТС, стрРаспределение.ТранспортноеСредство));
						
						ЗагрузкаПоРейсу = тзЗагрузкаПоРейсам.Найти(стрРаспределение.Рейс, "Рейс");
						
						Если мсвСтрокЗагрузки.Количество() Тогда
							НоваяСтрока.МассаОстаток                       = мсвСтрокЗагрузки[0].Грузоподъемность - ЗагрузкаПоРейсу.Масса - стрРаспределение.Масса;
							НоваяСтрока.ОбъемОстаток                       = мсвСтрокЗагрузки[0].Объем - ЗагрузкаПоРейсу.Объем - стрРаспределение.Объем;
							НоваяСтрока.КоличествоМестОстаток              = мсвСтрокЗагрузки[0].КоличествоМест - ЗагрузкаПоРейсу.КоличествоМест - стрРаспределение.КоличествоМест;
							НоваяСтрока.КоличествоЗагрузочныхМетровОстаток = мсвСтрокЗагрузки[0].КоличествоЗагрузочныхМетров - ЗагрузкаПоРейсу.КоличествоЗагрузочныхМетров - стрРаспределение.КоличествоЗагрузочныхМетров;
						КонецЕсли;
						
						// Если есть ограничение на количество точек в рейсе.
						Если стрРаспределение.МаксимальноеКоличествоТочекВРейсе > 0 Тогда
							
							мсвАдресаТочекВРейсе = Новый Массив;
							мсвСтрокиАдресаПоРейсу = дзАдресаПоРейсам.Строки.НайтиСтроки(Новый Структура("Рейс", стрРаспределение.Рейс));
							КоличествоТочекДобавляемыхЗаданием 	= 0;
													
							Если мсвСтрокиАдресаПоРейсу.Количество()>0 Тогда
								мсвАдресаТочекВРейсе = мсвСтрокиАдресаПоРейсу[0].Строки.ВыгрузитьКолонку("Адрес");
								КоличествоТочекДобавляемыхЗаданием 	= мсвАдресаТочекВРейсе.Количество();
							КонецЕсли;
							
							Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресОтправления) = Неопределено Тогда
								мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресОтправления);
								КоличествоТочекДобавляемыхЗаданием 	= КоличествоТочекДобавляемыхЗаданием + 1;
							КонецЕсли;
							
							Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресПолучения) = Неопределено Тогда
								мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресПолучения);
								КоличествоТочекДобавляемыхЗаданием 	= КоличествоТочекДобавляемыхЗаданием + 1;
							КонецЕсли;

							НоваяСтрока.МассивАдресовТочек 		= мсвАдресаТочекВРейсе;
							НоваяСтрока.КоличествоТочекОстаток 	= стрРаспределение.МаксимальноеКоличествоТочекВРейсе - КоличествоТочекДобавляемыхЗаданием;
																					
						КонецЕсли;
												
						Прервать;
					Иначе 
						
						мсвАдресаТочекВРейсе 	= Новый Массив;
						ОстатокКоличествоТочек	= 0;
												
						// Если есть ограничение на количество точек в рейсе.
						Если стрРаспределение.МаксимальноеКоличествоТочекВРейсе > 0 Тогда
                        	мсвАдресаТочекВРейсе 				= ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ИскомаяСтрока.МассивАдресовТочек);
							КоличествоТочекДобавляемыхЗаданием 	= 0;
							
							Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресОтправления) = Неопределено Тогда
								мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресОтправления);
								КоличествоТочекДобавляемыхЗаданием 	= КоличествоТочекДобавляемыхЗаданием + 1;
							КонецЕсли;
							
							Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресПолучения) = Неопределено Тогда
								мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресПолучения);
								КоличествоТочекДобавляемыхЗаданием 	= КоличествоТочекДобавляемыхЗаданием + 1;
							КонецЕсли;

							ОстатокКоличествоТочек = ИскомаяСтрока.КоличествоТочекОстаток - КоличествоТочекДобавляемыхЗаданием;
							
							Если ОстатокКоличествоТочек < 0 Тогда
								Продолжить;
							КонецЕсли;
						КонецЕсли;	
												
						Если ИскомаяСтрока.МассаОстаток < стрРаспределение.Масса
							Или ИскомаяСтрока.ОбъемОстаток < стрРаспределение.Объем
							Или ИскомаяСтрока.КоличествоМестОстаток < стрРаспределение.КоличествоМест
							Или ИскомаяСтрока.КоличествоЗагрузочныхМетровОстаток < стрРаспределение.КоличествоЗагрузочныхМетров Тогда
							Продолжить;
						Иначе
							ИскомаяСтрока.МассаОстаток                       = ИскомаяСтрока.МассаОстаток - стрРаспределение.Масса;
							ИскомаяСтрока.ОбъемОстаток                       = ИскомаяСтрока.ОбъемОстаток - стрРаспределение.Объем;
							ИскомаяСтрока.КоличествоМестОстаток              = ИскомаяСтрока.КоличествоМестОстаток - стрРаспределение.КоличествоМест;
							ИскомаяСтрока.КоличествоЗагрузочныхМетровОстаток = ИскомаяСтрока.КоличествоЗагрузочныхМетровОстаток - стрРаспределение.КоличествоЗагрузочныхМетров;
						КонецЕсли;	 
						ИскомаяСтрока.МассивАдресовТочек 		= мсвАдресаТочекВРейсе;
						ИскомаяСтрока.КоличествоТочекОстаток 	= ОстатокКоличествоТочек;
						ИскомаяСтрока.МассивЗаданий.Добавить(стрРаспределение.Задание);
						Прервать;
					КонецЕсли;
					
				// Иначе анализируются графики периодическийх рейсов и по ним формируются распределения в рейсы.
				Иначе
					ИскомыеСтроки = тзРаспределение.НайтиСтроки(Новый Структура("Расписание, ПлановоеНачалоРейса", стрРаспределение.Расписание, стрРаспределение.ПлановоеНачалоРейса));
					Если ИскомыеСтроки.Количество() Тогда
						
						ПодходящаяСтрока = Неопределено;
						СоздатьНовуюСтроку = Ложь;
						Для каждого текСтрока Из ИскомыеСтроки Цикл
							
							мсвАдресаТочекВРейсе 	= Новый Массив;
							ОстатокКоличествоТочек	= 0;
						
							Если стрРаспределение.МаксимальноеКоличествоТочекВРейсе > 0 Тогда
						
								// Если есть ограничение на количество точек в рейсе.
								Если стрРаспределение.МаксимальноеКоличествоТочекВРейсе > 0 Тогда
									мсвАдресаТочекВРейсе 				= ОбщегоНазначенияКлиентСервер.СкопироватьМассив(текСтрока.МассивАдресовТочек);
									КоличествоТочекДобавляемыхЗаданием 	= 0;
									
									Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресОтправления) = Неопределено Тогда
										мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресОтправления);
										КоличествоТочекДобавляемыхЗаданием = КоличествоТочекДобавляемыхЗаданием + 1;
									КонецЕсли;
									
									Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресПолучения) = Неопределено Тогда
										мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресПолучения);
										КоличествоТочекДобавляемыхЗаданием = КоличествоТочекДобавляемыхЗаданием + 1;
									КонецЕсли;
									
									ОстатокКоличествоТочек = текСтрока.КоличествоТочекОстаток - КоличествоТочекДобавляемыхЗаданием;
									
									Если ОстатокКоличествоТочек < 0 Тогда
										Продолжить;
									КонецЕсли;
								КонецЕсли;	
															
							КонецЕсли; 
							
							Если текСтрока.МассаОстаток >= стрРаспределение.Масса
								И текСтрока.ОбъемОстаток >= стрРаспределение.Объем
								И текСтрока.КоличествоМестОстаток >= стрРаспределение.КоличествоМест
								И текСтрока.КоличествоЗагрузочныхМетровОстаток >= стрРаспределение.КоличествоЗагрузочныхМетров Тогда
								
								ПодходящаяСтрока = текСтрока;
								Прервать;
							КонецЕсли;	
						КонецЦикла;
						
						Если ПодходящаяСтрока = Неопределено Тогда
							Для каждого текСтрока Из ИскомыеСтроки Цикл
								Если текСтрока.ОстатокТС > 0 Тогда
									СоздатьНовуюСтроку = Истина;
									Прервать;
								КонецЕсли;	
							КонецЦикла;
						КонецЕсли; 
						
						Если СоздатьНовуюСтроку Тогда
							НоваяСтрока = тзРаспределение.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, стрРаспределение);
							НоваяСтрока.МассивЗаданий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(стрРаспределение.Задание);
							
							мсвСтрокЗагрузки = тзЗагрузкаПоТС.НайтиСтроки(Новый Структура("ТипТС, ТранспортноеСредство", стрРаспределение.ТипТС, стрРаспределение.ТранспортноеСредство));
							Если мсвСтрокЗагрузки.Количество() Тогда
								НоваяСтрока.МассаОстаток                       = мсвСтрокЗагрузки[0].Грузоподъемность - стрРаспределение.Масса;
								НоваяСтрока.ОбъемОстаток                       = мсвСтрокЗагрузки[0].Объем - стрРаспределение.Объем;
								НоваяСтрока.КоличествоМестОстаток              = мсвСтрокЗагрузки[0].КоличествоМест - стрРаспределение.КоличествоМест;
								НоваяСтрока.КоличествоЗагрузочныхМетровОстаток = мсвСтрокЗагрузки[0].КоличествоЗагрузочныхМетров - стрРаспределение.КоличествоЗагрузочныхМетров;
							КонецЕсли;
							
							Для каждого текСтрока Из ИскомыеСтроки Цикл
								текСтрока.ОстатокТС = текСтрока.ОстатокТС - 1;
							КонецЦикла;
							НоваяСтрока.ОстатокТС = текСтрока.ОстатокТС;
							
							мсвАдресаТочекВРейсе 	= Новый Массив;
							ОстатокКоличествоТочек	= 0;
							Если стрРаспределение.МаксимальноеКоличествоТочекВРейсе > 0 Тогда
							
								мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресОтправления);
								КоличествоТочекДобавляемыхЗаданием 	= 1;
								
								Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресПолучения) = Неопределено Тогда
									мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресПолучения);
									КоличествоТочекДобавляемыхЗаданием 	= КоличествоТочекДобавляемыхЗаданием + 1;
								КонецЕсли;
								
								ОстатокКоличествоТочек = стрРаспределение.МаксимальноеКоличествоТочекВРейсе - КоличествоТочекДобавляемыхЗаданием;
								
							КонецЕсли;
							НоваяСтрока.МассивАдресовТочек 		= мсвАдресаТочекВРейсе;
							НоваяСтрока.КоличествоТочекОстаток 	= ОстатокКоличествоТочек;
							
							ЗаданиеРаспределено = Истина;
							Продолжить;
						КонецЕсли; 
						
						Если Не ПодходящаяСтрока = Неопределено Тогда
							ПодходящаяСтрока.МассаОстаток                       = ПодходящаяСтрока.МассаОстаток - стрРаспределение.Масса;
							ПодходящаяСтрока.ОбъемОстаток                       = ПодходящаяСтрока.ОбъемОстаток - стрРаспределение.Объем;
							ПодходящаяСтрока.КоличествоМестОстаток              = ПодходящаяСтрока.КоличествоМестОстаток - стрРаспределение.КоличествоМест;
							ПодходящаяСтрока.КоличествоЗагрузочныхМетровОстаток = ПодходящаяСтрока.КоличествоЗагрузочныхМетровОстаток - стрРаспределение.КоличествоЗагрузочныхМетров;
							ПодходящаяСтрока.МассивАдресовТочек 				= мсвАдресаТочекВРейсе;
							ПодходящаяСтрока.КоличествоТочекОстаток 			= ОстатокКоличествоТочек;
							
							ПодходящаяСтрока.МассивЗаданий.Добавить(стрРаспределение.Задание);
							ЗаданиеРаспределено = Истина;
							Продолжить;
						КонецЕсли;
											
					Иначе
						
						НоваяСтрока = тзРаспределение.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, стрРаспределение);
						НоваяСтрока.МассивЗаданий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(стрРаспределение.Задание);
						
						мсвСтрокЗагрузки = тзЗагрузкаПоТС.НайтиСтроки(Новый Структура("ТипТС, ТранспортноеСредство", стрРаспределение.ТипТС, стрРаспределение.ТранспортноеСредство));
						Если мсвСтрокЗагрузки.Количество() Тогда
							НоваяСтрока.МассаОстаток                       = мсвСтрокЗагрузки[0].Грузоподъемность - стрРаспределение.Масса;
							НоваяСтрока.ОбъемОстаток                       = мсвСтрокЗагрузки[0].Объем - стрРаспределение.Объем;
							НоваяСтрока.КоличествоМестОстаток              = мсвСтрокЗагрузки[0].КоличествоМест - стрРаспределение.КоличествоМест;
							НоваяСтрока.КоличествоЗагрузочныхМетровОстаток = мсвСтрокЗагрузки[0].КоличествоЗагрузочныхМетров - стрРаспределение.КоличествоЗагрузочныхМетров;
							
							мсвАдресаТочекВРейсе 	= Новый Массив;
							ОстатокКоличествоТочек	= 0;
							Если стрРаспределение.МаксимальноеКоличествоТочекВРейсе > 0 Тогда
							
								мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресОтправления);
								КоличествоТочекДобавляемыхЗаданием 	= 1;
								
								Если мсвАдресаТочекВРейсе.Найти(стрРаспределение.АдресПолучения) = Неопределено Тогда
									мсвАдресаТочекВРейсе.Добавить(стрРаспределение.АдресПолучения);
									КоличествоТочекДобавляемыхЗаданием 	= КоличествоТочекДобавляемыхЗаданием + 1;
								КонецЕсли;
								
								ОстатокКоличествоТочек = стрРаспределение.МаксимальноеКоличествоТочекВРейсе - КоличествоТочекДобавляемыхЗаданием;
								
							КонецЕсли;
							НоваяСтрока.МассивАдресовТочек 		= мсвАдресаТочекВРейсе;
							НоваяСтрока.КоличествоТочекОстаток 	= ОстатокКоличествоТочек;
							
							
							ОстаткиПоРасписанию = тзРасписание.НайтиСтроки(Новый Структура("Расписание, ПлановоеНачалоРейса", стрРаспределение.Расписание, стрРаспределение.ПлановоеНачалоРейса));
							Если ОстаткиПоРасписанию.Количество() Тогда
								Для каждого текСтрока Из ОстаткиПоРасписанию Цикл
									текСтрока.ОстатокТС = текСтрока.ОстатокТС - 1;
								КонецЦикла;
								НоваяСтрока.ОстатокТС = текСтрока.ОстатокТС;
							КонецЕсли;
						КонецЕсли;
						Прервать;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла; 
		КонецЦикла; 
		
		Если тзРаспределение.Количество() > 0 Тогда
			сткПериодПланирования = Новый Структура;
			сткПериодПланирования.Вставить("НачалоПериодаПланирования"   , '00010101');
			сткПериодПланирования.Вставить("ОкончаниеПериодаПланирования", '00010101');
			Для каждого текРаспределение Из тзРаспределение Цикл
				Если ЗначениеЗаполнено(текРаспределение.Рейс) Тогда // добавление задания в существующий рейс
					Документы.упРейс.ДобавитьЗаданияВРейс(текРаспределение.МассивЗаданий, Неопределено, текРаспределение.Рейс, сткПериодПланирования, ОписаниеОшибки, Отказ);
				Иначе // создание нового рейса
					РеквизитыПоУмолчанию = Новый Структура;
					РеквизитыПоУмолчанию.Вставить("Перевозчик");
					РеквизитыПоУмолчанию.Вставить("ТипТС");
					РеквизитыПоУмолчанию.Вставить("Соглашение");
					РеквизитыПоУмолчанию.Вставить("ТранспортноеСредство");
					РеквизитыПоУмолчанию.Вставить("Водитель1");
					РеквизитыПоУмолчанию.Вставить("Расписание");
					ЗаполнитьЗначенияСвойств(РеквизитыПоУмолчанию, текРаспределение);
					РеквизитыПоУмолчанию.Вставить("Автор",ПользователиКлиентСервер.ТекущийПользователь());
					
					ДополнительныеПараметры = Новый Структура;
					ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", текРаспределение.ПлановоеНачалоРейса);
					ДополнительныеПараметры.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей", Ложь);
					ДополнительныеПараметры.Вставить("МаршрутСледования", текРаспределение.МаршрутСледования);
					Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(текРаспределение.МассивЗаданий, Неопределено, РеквизитыПоУмолчанию, ДополнительныеПараметры, ОписаниеОшибки, Отказ);
				КонецЕсли; 
			КонецЦикла; 
			
			Если Не Отказ Тогда
				ОбновитьТаблицуЗаданийНаСервере();
				ОбновитьДеревоРейсовНаСервере();
			КонецЕсли; 
		Иначе
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // РаспределитьЗаданияПоПериодическимРейсамНаСервере();

&НаСервере
Процедура РешитьЗадачуМаршрутизацииТранспортаНаСервере(ИдентификаторЗадания, АдресВременногоХранилища, Отказ)
	
	Перем РеквизитКоличество;
	
	Отказ = (ТипЗнч(ПараметрыПланирования.МетодРешенияЗМТ) = Тип("СправочникСсылка.упАлгоритмыОптимизации") И Не Справочники.упАлгоритмыОптимизации.ПроверитьКорректностьПоставляемогоАлгоритмаОптимизации(ПараметрыПланирования.МетодРешенияЗМТ));
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗадания = СписокЗаданий.Выгрузить(Новый Структура("Геокодировано, Активно", Истина, Истина), "ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
		тзЗадания.Свернуть("ЗаданиеНаПеревозку, ГрузовоеМесто", "КоличествоГрузов"); 
		Если тзЗадания.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		мсвЗаданий = Неопределено;
	Иначе	
		мсвЗаданий = СписокЗаданий.Выгрузить(Новый Структура("Геокодировано, Активно", Истина, Истина), "ЗаданиеНаПеревозку").ВыгрузитьКолонку("ЗаданиеНаПеревозку");
		Если мсвЗаданий.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		тзЗадания = Неопределено;
	КонецЕсли;
	
	сткПараметрыПланирования = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыПланирования);
	сткПараметрыПланирования.Вставить("НачалоПериодаПланирования"       , НачалоПериодаПланирования);
	сткПараметрыПланирования.Вставить("ОкончаниеПериодаПланирования"    , ОкончаниеПериодаПланирования);
	сткПараметрыПланирования.Вставить("РазбиватьЗаданияПоГрузовымМестам", РазбиватьЗаданияПоГрузовымМестам);
	Если сткПараметрыПланирования.ГрузоваяЕдиница = 0 Тогда 
		сткПараметрыПланирования.ГрузоваяЕдиница = "Масса";
	ИначеЕсли сткПараметрыПланирования.ГрузоваяЕдиница = 1 Тогда 
		сткПараметрыПланирования.ГрузоваяЕдиница = "Объем";
	ИначеЕсли сткПараметрыПланирования.ГрузоваяЕдиница = 2 Тогда 
		сткПараметрыПланирования.ГрузоваяЕдиница = "КоличествоМест";
	ИначеЕсли сткПараметрыПланирования.ГрузоваяЕдиница = 3 Тогда 
		сткПараметрыПланирования.ГрузоваяЕдиница = "КоличествоЗагрузочныхМетров";
	КонецЕсли;
	
	ВыбранныеТранспорты = СписокТранспорта.Выгрузить(Новый Массив);
	сткПоиска = Новый Структура("Перевозчик, ТипТС, ТранспортноеСредство");
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		ЗаполнитьЗначенияСвойств(сткПоиска, ЭлементРейс); 
		Если ЭлементРейс.Активно И Не ЗначениеЗаполнено(ЭлементРейс.Рейс) Тогда
			ИскомыеСтроки = СписокТранспорта.НайтиСтроки(сткПоиска);
			ЗаполнитьЗначенияСвойств(ВыбранныеТранспорты.Добавить(), ИскомыеСтроки[0]); 
		КонецЕсли; 
	КонецЦикла; 
	//мсвРейсов = Новый Массив;
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		Если ЗначениеЗаполнено(ЭлементРейс.Рейс) Тогда
			////Если ЭлементРейс.Активен Тогда
			//	мсвРейсов.Добавить(ЭлементРейс.Рейс);
			////КонецЕсли;
			ЗаполнитьЗначенияСвойств(сткПоиска, ЭлементРейс); 
			ИскомыеСтроки = ВыбранныеТранспорты.НайтиСтроки(сткПоиска);
			Если ИскомыеСтроки.Количество() Тогда
				ИскомаяСтрока = ИскомыеСтроки[0];
				Если ЗначениеЗаполнено(ИскомаяСтрока.ТранспортноеСредство) И ИскомаяСтрока.ПланироватьЦиклически Тогда
					РеквизитКоличество = "КоличествоЦиклов";
				Иначе
					РеквизитКоличество = "Количество";
				КонецЕсли; 
				ИскомаяСтрока[РеквизитКоличество] = ИскомаяСтрока[РеквизитКоличество] - 1;
				
				Если ИскомаяСтрока[РеквизитКоличество] < 1 Тогда
					ВыбранныеТранспорты.Удалить(ИскомаяСтрока);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ВыбранныеТранспорты.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нет активных (лампочка) свободных транспортных средств для автоматического планирования'"),,,, Отказ);
		Возврат;
	КонецЕсли; 
	
	//Если мсвРейсов.Количество() > 1 Тогда
	//	// Среди рейсов с пересекающимися маршрутами (в точках получения) выбираем первый попавшийся.
	//	мсвПересекающихсяРейсов = Документы.упРейс.ВернутьПересекающиесяВТочкахРейсы(мсвРейсов);
	//	Если мсвПересекающихсяРейсов.Количество() Тогда
	//		мсвПересекающихсяРейсов.Удалить(0);
	//		мсвРейсов = ОбщегоНазначенияКлиентСервер.СократитьМассив(мсвРейсов, мсвПересекающихсяРейсов);
	//	КонецЕсли; 
	//КонецЕсли;
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Новый Массив, УникальныйИдентификатор);
	
	мсвПараметров = Новый Массив;
	мсвПараметров.Добавить(ПараметрыПланирования.МетодРешенияЗМТ);
	мсвПараметров.Добавить(мсвЗаданий);
	мсвПараметров.Добавить(тзЗадания);
	мсвПараметров.Добавить(ВыбранныеТранспорты);
	мсвПараметров.Добавить(Новый Массив);   // массив рейсов
	мсвПараметров.Добавить(сткПараметрыПланирования);
	мсвПараметров.Добавить(ВидПеревозки);
	мсвПараметров.Добавить(АдресВременногоХранилища);
	мсвПараметров.Добавить(ВариантПланирования);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("упКомбинаторнаяОптимизация.РешитьЗадачуМаршрутизацииТранспорта", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Решение ЗМТ'"));
	ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;

КонецПроцедуры // РешитьЗадачуМаршрутизацииТранспортаНаСервере()

&НаСервере
Процедура СформироватьРейсыНаСервере(АдресВременногоХранилища, Отказ, ТекстОшибки)
	
	МассивРешений = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если ПараметрыПланирования.МетодРешенияЗМТ = Перечисления.упМетодыРешенияЗМТ.МеханизмСбереженийКларкаРайта Или ТипЗнч(ПараметрыПланирования.МетодРешенияЗМТ) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		Если МассивРешений.Количество() Тогда
			ТекстОшибки = "";
			Для каждого текРешенияПоДепо Из МассивРешений Цикл
				мсвРешений = текРешенияПоДепо.МассивРешений;
				
				Для каждого текРешение Из мсвРешений Цикл
					
					Если ЗначениеЗаполнено(текРешение.Рейс) Тогда
						
					Иначе	
						тзЗадания = Новый ТаблицаЗначений;
						тзЗадания.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
						тзЗадания.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
						тзЗадания.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
						
						тзТочек = текРешение.ТаблицаТочек;
						Для каждого текТочка Из тзТочек Цикл
							Для каждого текТочкаСостава Из текТочка.Состав Цикл
								Если текТочкаСостава.ИсключитьПослеПланирования Тогда
									Продолжить;
								КонецЕсли; 
								НоваяСтрока = тзЗадания.Добавить();
								НоваяСтрока.ЗаданиеНаПеревозку = текТочкаСостава.Задание;
								НоваяСтрока.ГрузовоеМесто = текТочкаСостава.ГрузовоеМесто;
								НоваяСтрока.КоличествоГрузов = текТочкаСостава.КоличествоГрузов;
							КонецЦикла; 
						КонецЦикла; 
						
						Если ПараметрыПланирования.УчитыватьВременныеОкна Тогда
							тзАдресов = тзТочек.Скопировать(,"Адрес, ВременноеОкноНачало, ВременноеОкноОкончание, ПолнаяДоступность, БезРабот");
							
							тзАдресов.Свернуть("Адрес, ВременноеОкноНачало, ВременноеОкноОкончание, ПолнаяДоступность, БезРабот");
							КоличествоСтрок = тзАдресов.Количество();
							Сч = КоличествоСтрок - 1;
							текАдрес = Неопределено;
							Пока Сч >= 0 Цикл
								текСтрока = тзАдресов[Сч];
								Если текАдрес = Неопределено Или текАдрес <> текСтрока.Адрес Тогда
									текАдрес = текСтрока.Адрес;
								Иначе
									тзАдресов.Удалить(Сч);
								КонецЕсли; 
								Сч = Сч - 1;
							КонецЦикла; 
						Иначе
							тзАдресов = тзТочек.Скопировать(,"Адрес, БезРабот");
							тзАдресов.Свернуть("Адрес, БезРабот");
						КонецЕсли;
						// В случае рейса без разгрузок депо должно быть последним, а оно всегда правильно добавляется в упРейс.ДобавитьРаботыПоЗаданиюВМаршрут
						//СтрокаАдресаДепо = тзАдресов.Вставить(0);
						//СтрокаАдресаДепо.Адрес = текРешенияПоДепо.Депо;
						Если тзАдресов.Количество() Тогда
							сткРеквизитыПоУмолчанию = Новый Структура("Перевозчик, ТипТС, ТранспортноеСредство");
							ЗаполнитьЗначенияСвойств(сткРеквизитыПоУмолчанию, текРешение);
							Если ЗначениеЗаполнено(сткРеквизитыПоУмолчанию.Перевозчик) Тогда
								сткРеквизитыПоУмолчанию.Вставить("КонтрагентПеревозчика", Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(сткРеквизитыПоУмолчанию.Перевозчик));	
							КонецЕсли;
							сткРеквизитыПоУмолчанию.Вставить("Автор", ПользователиКлиентСервер.ТекущийПользователь());
							сткРеквизитыПоУмолчанию.Вставить("ОграничениеМаршрутизатора", текРешение.Ограничение);
							сткРеквизитыПоУмолчанию.Вставить("СкладскиеВорота", текРешение.СкладскиеВорота);
							ДополнительныеПараметры = Новый Структура;
							ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", текРешение.ПлановоеНачалоВыполнения);
							ДополнительныеПараметры.Вставить("УчитыватьВременныеОкна"   , ПараметрыПланирования.УчитыватьВременныеОкна);
							//Если ПараметрыПланирования.УчитыватьВременныеОкна Тогда
							//	ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", текРешение.ПлановоеНачалоВыполнения);
							//	ДополнительныеПараметры.Вставить("УчитыватьВременныеОкна"  , Истина);
							//Иначе
							//	ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", НачалоПериодаПланирования);
							//	ДополнительныеПараметры.Вставить("УчитыватьВременныеОкна"  , Ложь);
							//КонецЕсли;
							ДополнительныеПараметры.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей", ПараметрыПланирования.УчитыватьГрафикиРаботыОсновныхВодителей);
							Если тзАдресов.Количество() > 1 Тогда
								ДополнительныеПараметры.Вставить("ТаблицаАдресов", тзАдресов);
							КонецЕсли; 
							мсвРейсов = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(Неопределено, тзЗадания, сткРеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ); 
							Если Отказ Тогда
								ЗаписьЖурналаРегистрации("Создание рейса", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		Если МассивРешений.Количество() Тогда
			ТекстОшибки = "";
			Для каждого текРешение Из МассивРешений Цикл
				сткРеквизитыПоУмолчанию = Новый Структура("Перевозчик, ТипТС, ТранспортноеСредство");
				ЗаполнитьЗначенияСвойств(сткРеквизитыПоУмолчанию, текРешение);
				сткРеквизитыПоУмолчанию.Вставить("Автор",ПользователиКлиентСервер.ТекущийПользователь());
				Если ЗначениеЗаполнено(сткРеквизитыПоУмолчанию.Перевозчик) Тогда
					сткРеквизитыПоУмолчанию.Вставить("КонтрагентПеревозчика", Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(сткРеквизитыПоУмолчанию.Перевозчик));	
				КонецЕсли;
				тзЗадания = Новый ТаблицаЗначений;
				тзЗадания.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
				тзЗадания.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
				тзЗадания.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
				
				Для каждого текТочкаСостава Из текРешение.Состав Цикл
					НоваяСтрока = тзЗадания.Добавить();
					НоваяСтрока.ЗаданиеНаПеревозку = текТочкаСостава.Задание;
					НоваяСтрока.ГрузовоеМесто = текТочкаСостава.ГрузовоеМесто;
					НоваяСтрока.КоличествоГрузов = текТочкаСостава.КоличествоГрузов;
				КонецЦикла; 
				
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", НачалоПериодаПланирования);
				ДополнительныеПараметры.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей", ПараметрыПланирования.УчитыватьГрафикиРаботыОсновныхВодителей);
				ДополнительныеПараметры.Вставить("УчитыватьВременныеОкна"  , Ложь);
				мсвРейсов = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(Неопределено, тзЗадания, сткРеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ); 
				Если Отказ Тогда
					ЗаписьЖурналаРегистрации("Создание рейса", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры // СформироватьРейсыНаСервере()

&НаСервере
Процедура УстановитьПараметрыПланированияНаСервере()
	
	ПараметрыПланирования = Новый Структура;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		КлючОбъекта = "Обработка.упРМПланированиеРейсов.ПараметрыПланирования." + ВидТранспортнойУслугиПредставление;
		//Запрос = Новый Запрос;
		//Запрос.Текст = 
		//"ВЫБРАТЬ
		//|	упВариантыНастроек.КлючВарианта
		//|ИЗ
		//|	Справочник.упВариантыНастроек КАК упВариантыНастроек
		//|ГДЕ
		//|   упВариантыНастроек.ВидПеревозки = &ВидПеревозки
		//|	И упВариантыНастроек.Автор = &Автор
		//|	И упВариантыНастроек.КлючВарианта = &КлючВарианта
		//|	И упВариантыНастроек.Объект = &Объект
		//|	И НЕ упВариантыНастроек.ПометкаУдаления";
		//Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		//Запрос.УстановитьПараметр("КлючВарианта", ВариантПланирования);
		//Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
		//Запрос.УстановитьПараметр("Объект", КлючОбъекта);
		//Выборка = Запрос.Выполнить().Выбрать();
		//Пока Выборка.Следующий() Цикл
		//	ПараметрыПланирования = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
		//	Если ПараметрыПланирования <> Неопределено Тогда
		//		ПараметрыПланирования = ПараметрыПланирования.Результат;
		//		Прервать;
		//	КонецЕсли;
		//КонецЦикла;
		ПараметрыПланирования = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, ВариантПланирования);
		Если ПараметрыПланирования <> Неопределено Тогда
			ПараметрыПланирования = ПараметрыПланирования.Результат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПоУмолчанию = упВариантыНастроек.ПараметрыПланированияРейсовПоУмолчанию();
	упСервисныеФункцииКлиентСервер.ДобавитьНедостающиеЭлементыСтруктуру(ПараметрыПоУмолчанию, ПараметрыПланирования);
	
	// проверка доступности выбранного контролирующего показателя
	ДоступныеПоказателиКонтроля = Новый Массив;
	Если УчитыватьМассу Тогда
		ДоступныеПоказателиКонтроля.Добавить(0);
	КонецЕсли; 
	Если УчитыватьОбъем Тогда
		ДоступныеПоказателиКонтроля.Добавить(1);
	КонецЕсли; 
	Если УчитыватьКоличествоМест Тогда
		ДоступныеПоказателиКонтроля.Добавить(2);
	КонецЕсли; 
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		ДоступныеПоказателиКонтроля.Добавить(3);
	КонецЕсли;
	
	Если ДоступныеПоказателиКонтроля.Количество() Тогда
		Если ДоступныеПоказателиКонтроля.Найти(ПараметрыПланирования.ГрузоваяЕдиница) = Неопределено Тогда
			ПараметрыПланирования.ГрузоваяЕдиница = ДоступныеПоказателиКонтроля[0];
		КонецЕсли;
		Если ДоступныеПоказателиКонтроля.Найти(ПараметрыПланирования.ПоказательКрупногоГруза) = Неопределено Тогда
			ПараметрыПланирования.ПоказательКрупногоГруза = ДоступныеПоказателиКонтроля[0];
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПланирования.ПоказательКрупногоГруза = 0 Тогда 
		ПоказательКрупногоГруза = "Масса";
	ИначеЕсли ПараметрыПланирования.ПоказательКрупногоГруза = 1 Тогда 
		ПоказательКрупногоГруза = "Объем";	
	ИначеЕсли ПараметрыПланирования.ПоказательКрупногоГруза = 2 Тогда 
		ПоказательКрупногоГруза = "КоличествоМест";	
	Иначе 
		ПоказательКрупногоГруза = "КоличествоЗагрузочныхМетров";		
	КонецЕсли;
	ВыделятьКрупныеГрузы = ПараметрыПланирования.ВыделятьКрупныеГрузы;
	РазмерКрупногоГруза = ПараметрыПланирования.РазмерКрупногоГруза;
	ВыделятьСрочныеГрузы = ПараметрыПланирования.ВыделятьСрочныеГрузы;
	
	Если ПараметрыПланирования.КоличествоВидимыхРейсов = 0 Тогда
		Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Видимость = Ложь;
		Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Видимость = Ложь;
	Иначе	
		Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Видимость = Истина;
		Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбновлениеИнтерфейса

&НаКлиенте
Процедура ИзменитьДоступностьРейсов()
	
	Если РежимФормированияКонтейнеров Тогда
		ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
		
		Для каждого ЭлементРейс Из ЭлементыДерева Цикл
			Если ЗначениеЗаполнено(ЭлементРейс.Рейс) Тогда
				Если Ложь
					Или ЭлементРейс.СтатусРейса = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению")
					Или Не (ЭлементРейс.Адрес = АдресОтправленияКонтейнера И ЭлементРейс.АдресПолучения = АдресПолученияКонтейнера)
				Тогда
					ЭлементРейс.ДоступноИзменениеМаршрута =	Ложь;
					ЭлементРейс.Выбран = Ложь;
				Иначе
					ЭлементРейс.ДоступноИзменениеМаршрута =	Истина;
				КонецЕсли;
				
				ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					текЗадание.ДоступноИзменениеМаршрута = ЭлементРейс.ДоступноИзменениеМаршрута;
					текЗадание.Выбран = Ложь;
				КонецЦикла; 
			Иначе
				ЭлементРейс.ДоступноИзменениеМаршрута =	Истина;
			КонецЕсли; 
		КонецЦикла;	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлектроннойКарты(ПервыйЗапуск = Ложь)
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		ТолькоПросмотр = Истина; // пока карта не загрузится, ни одно из действий не доступно
		Структура = упМаршрутизация.ПолучитьКоординатыНачальнойПозиции();
		НачальнаяШирота  = Структура.Широта;
		НачальнаяДолгота = Структура.Долгота;
		НачальныйЗум     = Структура.Зум;
		
		HTMLДокумент = упМаршрутизация.ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса();		
		КоличествоТочекДляАвтоматическогоИспользованияКластеризации = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоличествоТочекДляАвтоматическогоИспользованияКластеризации");
		УстановитьВидимостьЭлементовКарты(Истина);
		
	Иначе
		УстановитьВидимостьЭлементовКарты(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидРабочегоМеста()
	
	Если РежимФормированияКонтейнеров Тогда
		Элементы.ГруппаПарыАдресов.Видимость                      = Истина;
		Элементы.СпланироватьРейсы.Видимость                      = Ложь;
		Элементы.РаспределитьЗаданияПоПериодическимРейсам.Видимость = Ложь;
		Элементы.ОткрытьВариантНастройкиПланирования.Видимость    = Ложь;
		Элементы.СохранитьВариантНастройкиПланирования.Видимость  = Ложь;
		Элементы.ОткрытьФормуВыбораДоступныхТС.Заголовок          = НСтр("ru = 'Контейнеры'");
		Элементы.ОткрытьФормуВыбораДоступныхТС.Картинка           = БиблиотекаКартинок.упКонтейнер;
		Элементы.РежимКорректировки.Видимость                     = Ложь;
		Элементы.ГруппаКомандыКорректировки.Видимость             = Ложь;
		Элементы.ГруппаВыборВариантаПланирования.Видимость        = Ложь;
		Элементы.ОптимизироватьВыделенныеРейсы.Видимость          = Ложь;
		Элементы.ИзменитьРейс.Видимость                           = Ложь;
		Элементы.ПодготовитьМассивРасстояний.Видимость            = Ложь;
		Элементы.НазначитьПеревозкуТранспортнойКомпании.Видимость = Ложь;
		Элементы.ОтобразитьМестаИнтереса.Видимость                = Ложь;
		
		Элементы.СписокЗаданийАктивно.Видимость                   = Ложь;
		
		Элементы.УстановитьАктивностьВыделенныхТочек.Видимость    = Ложь;
		Элементы.УстановитьФильтрПоАдресуОтправления.Видимость    = Ложь;
		
		Элементы.ДеревоРейсыПредставление.Заголовок               = НСтр("ru = 'Контейнер / Владелец'");
		Элементы.ДеревоРейсыНомерВПорядкеЛучшихРейсов.Видимость   = Ложь;
		Элементы.ДеревоРейсыАктивно.Видимость                     = Ложь;
		Элементы.ДеревоРейсФильтрПоАдресам.Видимость              = Истина;
		Элементы.ДеревоРейсыПлановоеНачалоВыполнения.Заголовок    = НСтр("ru = 'Начало работ'");
		
		Элементы.ПеренестиВыбранныйГрузВРейс.Заголовок            = НСтр("ru = 'Перенести выбранный груз в контейнер'");
		Элементы.УдалитьВыбранныйГрузИзРейсов.Заголовок           = НСтр("ru = 'Перенести выбранный груз из контейнера'");
		Элементы.СвернутьДеревоРейсов.Заголовок                   = НСтр("ru = 'Свернуть дерево контейнеров'");
		Элементы.РазвернутьДеревоРейсов.Заголовок                 = НСтр("ru = 'Развернуть дерево контейнеров'");
		
		Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Заголовок = НСтр("ru = 'Отобразить предыдущую группу контейнеров'");
		Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Заголовок  = НСтр("ru = 'Отобразить следующую группу контейнеров'");
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.РазрешеноФормированиеКонтейнеров", Истина);
		Элементы.ВидПеревозки.ПараметрыВыбора = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НовыйПараметр));
	Иначе	
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовКарты(Включить = Ложь)
	
	Элементы.ГруппаКарта.Видимость = Включить;
	Элементы.СписокЗаданийПометка.Видимость = Включить;
	Элементы.УстановитьВидимостьВыделенныхТочек.Видимость = Включить;
	Элементы.СписокЗаданийКонтекстноеМенюМасштабироватьКартуПоВыделеннымТочкам.Видимость = Включить;
	Элементы.СписокЗаданийКонтекстноеМенюСкрытьНевыделенныеТочки.Видимость = Включить;
	
	Элементы.СписокЗаданийПозиционированиеНаТочкеПолучения.Видимость = Включить;
	Элементы.СписокЗаданийПозиционирование.Видимость                 = Включить;
	
	Элементы.ДеревоРейсыПометка.Видимость                     = Включить;
	Элементы.ДеревоРейсыПозиционирование.Видимость            = Включить;
	Элементы.ДеревоРейсыЦветНаКарте.Видимость                 = Включить;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеОтображения()
	
	КоличествоАдресовОтправления = 0;
	КоличествоАдресовПолучения = 0;
	
	Если СписокЗаданий.Количество() Тогда
		тзАдреса = СписокЗаданий.Выгрузить(, "АдресОтправления, АдресПолучения");
		
		Если РежимФормированияКонтейнеров Тогда
			
			сткПоискПометить = Неопределено;
			Если ИдентификаторПомеченнойСтрокиФК >=0  Тогда
				текПомеченная = АдресаОперацийСКонтейнерами.НайтиПоИдентификатору(ИдентификаторПомеченнойСтрокиФК);
			    Если Не текПомеченная = Неопределено Тогда
					сткПоискПометить = Новый Структура("АдресОтправления, АдресПолучения", текПомеченная.АдресОтправления, текПомеченная.АдресПолучения);				
				КонецЕсли; 
			КонецЕсли; 
			
			тзАдреса.Свернуть("АдресОтправления, АдресПолучения");
			АдресаОперацийСКонтейнерами.Загрузить(тзАдреса);
			
			Если Не сткПоискПометить = Неопределено Тогда
				ИскомыеАдреса = АдресаОперацийСКонтейнерами.НайтиСтроки(сткПоискПометить);
				Если ИскомыеАдреса.Количество() Тогда
					ИскомыеАдреса[0].Пометка = Истина;
					ИдентификаторПомеченнойСтрокиФК = ИскомыеАдреса[0].ПолучитьИдентификатор();
					Элементы.АдресаОперацийСКонтейнерами.ТекущаяСтрока = ИдентификаторПомеченнойСтрокиФК;
				Иначе // строка была удалена
					ИдентификаторПомеченнойСтрокиФК = -1;
					АдресОтправленияКонтейнера = Неопределено;
					АдресПолученияКонтейнера = Неопределено;
				КонецЕсли; 
			КонецЕсли; 
			
		Иначе
			мсвАдресов = тзАдреса.ВыгрузитьКолонку("АдресОтправления");
			КоличествоАдресовОтправления = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвАдресов).Количество();
			мсвАдресов = тзАдреса.ВыгрузитьКолонку("АдресПолучения");
			КоличествоАдресовПолучения = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвАдресов).Количество();
		КонецЕсли; 
		
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступность()
	
	// обновление отображаемых надписей и заголовков
	Если РежимФормированияКонтейнеров Тогда
		ЭтаФорма.Заголовок                   = НСтр(СтрШаблон("ru = 'Формирование контейнеров (%1)'", упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(НачалоПериодаПланирования, ОкончаниеПериодаПланирования)));
		ЭтаФорма.НадписьЗаголовок            = НСтр(СтрШаблон("ru = 'Заданий (%1): %2'", ?(РазбиватьЗаданияПоГрузовымМестам, "грузовых мест", "заданий на перевозку"), 
													СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, АдресПолучения", АдресОтправленияКонтейнера, АдресПолученияКонтейнера)).Количество()));
		ЭтаФорма.НадписьЗаголовокРейсы       = НСтр(СтрШаблон("ru ='Всего контейнеров: %1, сформированных: %2 шт.'", СписокТранспорта.Итог("Количество"), КоличествоРейсов));
		ЭтаФорма.НадписьЗаголовокПарыАдресов = НСтр(СтрШаблон("ru ='Фильтр по адресам (всего %1)'", АдресаОперацийСКонтейнерами.Количество()));
		
		ИзменитьДоступностьРейсов();
	Иначе
		ЭтаФорма.Заголовок = НСтр(СтрШаблон("ru = 'Планирование рейсов (%1)'", упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(НачалоПериодаПланирования, ОкончаниеПериодаПланирования)));
		ЭтаФорма.НадписьЗаголовок = НСтр(СтрШаблон("ru = 'Заданий (%1): %2; адресов отправления: %3, получения: %4'", ?(РазбиватьЗаданияПоГрузовымМестам, "грузовых мест", "заданий на перевозку"), СписокЗаданий.Количество(), КоличествоАдресовОтправления, КоличествоАдресовПолучения));
		ЭтаФорма.НадписьЗаголовокРейсы = НСтр(СтрШаблон("ru ='Рейсов: %1, транспорта: %2 ед.'", КоличествоРейсов, СписокТранспорта.Итог("Количество")));
		
		Элементы.РежимКорректировки.Пометка = РежимКорректировки;
		Элементы.ОтображениеПлановыхМаршрутов.Пометка = ОтображениеПлановыхМаршрутов;
		Элементы.УдалитьВыбранныйГрузИзРейсов.Доступность = Не РежимКорректировки;
		Элементы.ГруппаКомандыПланирования.Видимость = Не РежимКорректировки;
		Элементы.ГруппаКомандыКорректировки.Видимость = РежимКорректировки;
		Элементы.ВыделитьПересекающиесяВТочкахРейсы.Доступность = Не РежимКорректировки;
		Элементы.ВыбратьВыделенныеСтрокиДереваРейсов.Доступность = Не РежимКорректировки;
		Элементы.УбратьВыборСВыделенныхСтрокДереваРейсов.Доступность = Не РежимКорректировки;
		Элементы.ОптимизироватьВыделенныеРейсы.Доступность = Не РежимКорректировки;
		Элементы.УстановитьАктивностьВыделенныхТочек.Доступность = Не РежимКорректировки;
		Элементы.НазначитьПеревозкуТранспортнойКомпании.Доступность = Не РежимКорректировки;
		Элементы.ПодготовитьМассивРасстояний.Доступность = Не РежимКорректировки;
		Элементы.ДеревоРейсыНомерВПорядкеЛучшихРейсов.Видимость = РежимКорректировки И ЛучшиеРейсы.Количество() > 0;
	КонецЕсли; 
	

КонецПроцедуры // ОбновитьВидимость()

&НаКлиенте
Процедура ОбновитьПредставлениеПоказателейЗагрузкиРейсов()

	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	
	Для каждого ЭлементРейс Из ЭлементыДерева Цикл
		Если УчитыватьМассу Тогда
			МассаСтарая = ЭлементРейс.Масса;
			МассаНовая  = Макс(ЭлементРейс.МассаПослеПриращения, ЭлементРейс.МассаВозвратовПослеПриращения);
			ЭлементРейс.МассаПослеПриращения = МассаНовая;
			МассаПроцентЗагрузки = Строка(Окр(МассаСтарая/ЭлементРейс.МассаМакс*100));
			Если МассаСтарая = МассаНовая Тогда
				ЭлементРейс.МассаПредставление = Строка(МассаСтарая);
				ЭлементРейс.МассаПроцентЗагрузки = МассаПроцентЗагрузки;
			Иначе	
				Представление = СтрШаблон("ru = '%1 (%2%3)'", Строка(МассаСтарая), ?(МассаНовая > МассаСтарая, "↑", "↓"), Строка(МассаНовая));
				ЭлементРейс.МассаПредставление = НСтр(Представление, КодЯзыка);
				ПредставлениеПроцентЗагрузки = СтрШаблон("ru = '%1 (%2%3%%)'", МассаПроцентЗагрузки, ?(МассаНовая > МассаСтарая, "↑", "↓"), Строка(Окр(МассаНовая/ЭлементРейс.МассаМакс*100)));
				ЭлементРейс.МассаПроцентЗагрузки = НСтр(ПредставлениеПроцентЗагрузки, КодЯзыка);
			КонецЕсли;
		КонецЕсли;
		Если УчитыватьОбъем Тогда
			ОбъемСтарый = ЭлементРейс.Объем;
			ОбъемНовый  = Макс(ЭлементРейс.ОбъемПослеПриращения, ЭлементРейс.ОбъемВозвратовПослеПриращения);
			ЭлементРейс.ОбъемПослеПриращения = ОбъемНовый;
			ОбъемПроцентЗагрузки = Строка(Окр(ОбъемСтарый/ЭлементРейс.ОбъемМакс*100));
			Если ОбъемСтарый = ОбъемНовый Тогда
				ЭлементРейс.ОбъемПредставление = Строка(ОбъемСтарый);
				ЭлементРейс.ОбъемПроцентЗагрузки = ОбъемПроцентЗагрузки;
			Иначе	
				Представление = СтрШаблон("ru = '%1 (%2%3)'", Строка(ОбъемСтарый), ?(ОбъемНовый > ОбъемСтарый, "↑", "↓"), Строка(ОбъемНовый));
				ЭлементРейс.ОбъемПредставление = НСтр(Представление, КодЯзыка);
				ПредставлениеПроцентЗагрузки = СтрШаблон("ru = '%1 (%2%3%%)'", ОбъемПроцентЗагрузки, ?(ОбъемНовый > ОбъемСтарый, "↑", "↓"), Строка(Окр(ОбъемНовый/ЭлементРейс.ОбъемМакс*100)));
				ЭлементРейс.ОбъемПроцентЗагрузки = НСтр(ПредставлениеПроцентЗагрузки, КодЯзыка);
			КонецЕсли;
		КонецЕсли;
		Если УчитыватьКоличествоМест Тогда
			КоличествоМестСтарое = ЭлементРейс.КоличествоМест;
			КоличествоМестНовое  = Макс(ЭлементРейс.КоличествоМестПослеПриращения, ЭлементРейс.КоличествоМестВозвратовПослеПриращения);
			ЭлементРейс.КоличествоМестПослеПриращения = КоличествоМестНовое;
			КоличествоМестПроцентЗагрузки = Строка(Окр(КоличествоМестСтарое/ЭлементРейс.КоличествоМестМакс*100));
			Если КоличествоМестСтарое = КоличествоМестНовое Тогда
				ЭлементРейс.КоличествоМестПредставление = Строка(КоличествоМестСтарое);
				ЭлементРейс.КоличествоМестПроцентЗагрузки = КоличествоМестПроцентЗагрузки;
			Иначе	
				Представление = СтрШаблон("ru = '%1 (%2%3)'", Строка(КоличествоМестСтарое), ?(КоличествоМестНовое > КоличествоМестСтарое, "↑", "↓"), Строка(КоличествоМестНовое));
				ЭлементРейс.КоличествоМестПредставление = НСтр(Представление, КодЯзыка);
				ПредставлениеПроцентЗагрузки = СтрШаблон("ru = '%1 (%2%3%%)'", КоличествоМестПроцентЗагрузки, ?(КоличествоМестНовое > КоличествоМестСтарое, "↑", "↓"), Строка(Окр(КоличествоМестНовое/ЭлементРейс.КоличествоМестМакс*100)));
				ЭлементРейс.КоличествоМестПроцентЗагрузки = НСтр(ПредставлениеПроцентЗагрузки, КодЯзыка);
			КонецЕсли;
		КонецЕсли;
		Если УчитыватьКоличествоЗагрузочныхМетров Тогда
			КоличествоЗагрузочныхМетровСтарое = ЭлементРейс.КоличествоЗагрузочныхМетров;
			КоличествоЗагрузочныхМетровНовое  = Макс(ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения, ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения);
			ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения = КоличествоЗагрузочныхМетровНовое;
			КоличествоЗагрузочныхМетровПроцентЗагрузки = Строка(Окр(КоличествоЗагрузочныхМетровСтарое/ЭлементРейс.КоличествоЗагрузочныхМетровМакс*100));
			Если КоличествоЗагрузочныхМетровСтарое = КоличествоЗагрузочныхМетровНовое Тогда
				ЭлементРейс.КоличествоЗагрузочныхМетровПредставление = Строка(КоличествоЗагрузочныхМетровСтарое);
				ЭлементРейс.КоличествоЗагрузочныхМетровПроцентЗагрузки = КоличествоЗагрузочныхМетровПроцентЗагрузки;
			Иначе	
				Представление = СтрШаблон("ru = '%1 (%2%3)'", Строка(КоличествоЗагрузочныхМетровСтарое), ?(КоличествоЗагрузочныхМетровНовое > КоличествоЗагрузочныхМетровСтарое, "↑", "↓"), Строка(КоличествоЗагрузочныхМетровНовое));
				ЭлементРейс.КоличествоЗагрузочныхМетровПредставление = НСтр(Представление, КодЯзыка);
				ПредставлениеПроцентЗагрузки = СтрШаблон("ru = '%1 (%2%3%%)'", КоличествоЗагрузочныхМетровПроцентЗагрузки, 
				?(КоличествоЗагрузочныхМетровНовое > КоличествоЗагрузочныхМетровСтарое, "↑", "↓"), 
				Строка(Окр(КоличествоЗагрузочныхМетровНовое/ЭлементРейс.КоличествоЗагрузочныхМетровМакс*100)));
				ЭлементРейс.КоличествоЗагрузочныхМетровПроцентЗагрузки = НСтр(ПредставлениеПроцентЗагрузки, КодЯзыка);
			КонецЕсли;
		КонецЕсли;
		
		ОценочнаяСтоимостьСтарая = ЭлементРейс.ОценочнаяСтоимость;
		ОценочнаяСтоимостьНовая  = ЭлементРейс.ОценочнаяСтоимостьПослеПриращения;
		Если ОценочнаяСтоимостьСтарая = ОценочнаяСтоимостьНовая Тогда
			ЭлементРейс.ОценочнаяСтоимостьПредставление = Строка(ОценочнаяСтоимостьСтарая);
		Иначе	
			Представление = СтрШаблон("ru = '%1 (%2%3)'", Строка(ОценочнаяСтоимостьСтарая), ?(ОценочнаяСтоимостьНовая > ОценочнаяСтоимостьСтарая, "↑", "↓"), Строка(ОценочнаяСтоимостьНовая));
			ЭлементРейс.ОценочнаяСтоимостьПредставление = НСтр(Представление, КодЯзыка);
		КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры // ОбновитьПредставлениеПоказателейЗагрузкиРейсов()

&НаСервере
Процедура ВыполнитьПервоначальноеЗаполнениеПоказателейЗагрузки()
	
	ПоказателиЗагрузки.Очистить();
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест);
	Если УчитыватьМассу Тогда
		прМасса = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ПоказателиЗагрузкиСпискаЗаданийМасса.Заголовок, сткПредставления.Масса), КодЯзыка);
		Элементы.ПоказателиЗагрузкиСпискаЗаданийМасса.Заголовок = прМасса;
		Элементы.ПоказателиЗагрузкиДереваРейсовМасса.Заголовок = прМасса;
		Элементы.СписокЗаданийМасса.Заголовок = прМасса;
		Элементы.ДеревоРейсыМассаПредставление.Заголовок = прМасса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		прОбъем = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ПоказателиЗагрузкиСпискаЗаданийОбъем.Заголовок, сткПредставления.Объем), КодЯзыка);
		Элементы.ПоказателиЗагрузкиСпискаЗаданийОбъем.Заголовок = прОбъем;
		Элементы.ПоказателиЗагрузкиДереваРейсовОбъем.Заголовок = прОбъем;
		Элементы.СписокЗаданийОбъем.Заголовок = прОбъем;
		Элементы.ДеревоРейсыОбъемПредставление.Заголовок = прОбъем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		прКоличествоМест = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ПоказателиЗагрузкиСпискаЗаданийКоличествоМест.Заголовок, сткПредставления.КоличествоМест), КодЯзыка);
		Элементы.ПоказателиЗагрузкиСпискаЗаданийКоличествоМест.Заголовок = прКоличествоМест;
		Элементы.ПоказателиЗагрузкиДереваРейсовКоличествоМест.Заголовок = прКоличествоМест;
		Элементы.СписокЗаданийКоличествоМест.Заголовок = прКоличествоМест;
		Элементы.ДеревоРейсыКоличествоМестПредставление.Заголовок = прКоличествоМест;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") И ЗначениеЗаполнено(ВалютаУпрУчета) Тогда
		прОценочнаяСтоимость = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.ПоказателиЗагрузкиСпискаЗаданийОценочнаяСтоимость.Заголовок, ВалютаУпрУчета), КодЯзыка);
		Элементы.ПоказателиЗагрузкиСпискаЗаданийОценочнаяСтоимость.Заголовок = прОценочнаяСтоимость;
		Элементы.ПоказателиЗагрузкиДереваРейсовОценочнаяСтоимость.Заголовок = прОценочнаяСтоимость;
		Элементы.СписокЗаданийОценочнаяСтоимость.Заголовок = прОценочнаяСтоимость;
		Элементы.ДеревоРейсыОценочнаяСтоимостьПредставление.Заголовок = прОценочнаяСтоимость;		
	КонецЕсли; 
	
	НоваяСтрока = ПоказателиЗагрузки.Добавить();
	НоваяСтрока.Категория = НСтр("ru = 'Выделено:'", КодЯзыка);
	НоваяСтрока.СтрокаДляСпискаЗаданий = Истина;
	
	НоваяСтрока = ПоказателиЗагрузки.Добавить();
	НоваяСтрока.Категория = НСтр("ru = 'Выбрано:'", КодЯзыка);
	НоваяСтрока.СтрокаДляСпискаЗаданий = Истина;
	НоваяСтрока.Выбрано = Истина;
	
	НоваяСтрока = ПоказателиЗагрузки.Добавить();
	НоваяСтрока.Категория = НСтр("ru = 'Выделено:'", КодЯзыка);
	НоваяСтрока.РасстояниеПредставление = НСтр("ru = '0 км'");
	НоваяСтрока.ВремяПредставление      = НСтр("ru = '0 мин.'");
	НоваяСтрока.СтрокаДляСпискаЗаданий = Ложь;
	
	НоваяСтрока = ПоказателиЗагрузки.Добавить();
	НоваяСтрока.Категория = НСтр("ru = 'Выбрано:'", КодЯзыка);
	НоваяСтрока.РасстояниеПредставление = НСтр("ru = '0 км'");
	НоваяСтрока.ВремяПредставление      = НСтр("ru = '0 мин.'");
	НоваяСтрока.СтрокаДляСпискаЗаданий = Ложь;
	НоваяСтрока.Выбрано = Истина;
	
	Элементы.ПоказателиЗагрузкиСпискаЗаданий.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("СтрокаДляСпискаЗаданий", Истина));
	Элементы.ПоказателиЗагрузкиДереваРейсов.ОтборСтрок  = Новый ФиксированнаяСтруктура(Новый Структура("СтрокаДляСпискаЗаданий", Ложь));
	
КонецПроцедуры // ВыполнитьПервоначальноеЗаполнениеПоказателейЗагрузки()
	
#КонецОбласти 

#Область ОбновлениеТаблицФормы

&НаКлиенте
Процедура ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий()
	
	Если СписокЗаданий.Количество() Тогда
		ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
		КоличествоВыделенныхСтрок = ВыделенныеСтроки.Количество();
	Иначе
		КоличествоВыделенныхСтрок = 0;
	КонецЕсли;
	
	МассаГруза                       = 0;
	ОбъемГруза                       = 0;
	КоличествоМестГруза              = 0;
	КоличествоЗагрузочныхМетровГруза = 0;
	КоличествоЕдиниц                 = 0;
	ОценочнаяСтоимость               = 0;
	
	Если КоличествоВыделенныхСтрок Тогда 
		Для каждого Строка Из ВыделенныеСтроки Цикл
			текДанные  = СписокЗаданий.НайтиПоИдентификатору(Строка);
			
			Если НЕ текДанные = Неопределено Тогда
				МассаГруза                       = МассаГруза + текДанные.Масса;
				ОбъемГруза                       = ОбъемГруза + текДанные.Объем;
				КоличествоМестГруза              = КоличествоМестГруза + текДанные.КоличествоМест;
				КоличествоЗагрузочныхМетровГруза = КоличествоЗагрузочныхМетровГруза + текДанные.КоличествоЗагрузочныхМетров;
				ОценочнаяСтоимость               = ОценочнаяСтоимость + текДанные.ОценочнаяСтоимость;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	СтрокаВыделенныхЗаданий = ПоказателиЗагрузки[0];
	СтрокаВыделенныхЗаданий.КоличествоЕдиниц            = КоличествоВыделенныхСтрок;
	СтрокаВыделенныхЗаданий.Масса                       = МассаГруза;
	СтрокаВыделенныхЗаданий.Объем                       = ОбъемГруза;
	СтрокаВыделенныхЗаданий.КоличествоМест              = КоличествоМестГруза;
	СтрокаВыделенныхЗаданий.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетровГруза;
	СтрокаВыделенныхЗаданий.ОценочнаяСтоимость          = ОценочнаяСтоимость;
	
КонецПроцедуры // ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий()

&НаКлиенте
Процедура ОбновитьПоказателиПоВыбраннымСтрокамСпискаЗаданий()
	
	Если РежимФормированияКонтейнеров Тогда
		ВыбранныеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выбрано, АдресОтправления, АдресПолучения", Истина, АдресОтправленияКонтейнера, АдресПолученияКонтейнера));
	Иначе
		ВыбранныеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выбрано", Истина));
	КонецЕсли; 
	
	КоличествоВыбранныхСтрок = ВыбранныеСтроки.Количество();
	МассаГруза                       = 0;
	ОбъемГруза                       = 0;
	КоличествоМестГруза              = 0;
	КоличествоЗагрузочныхМетровГруза = 0;
	КоличествоЕдиниц                 = 0;
	ОценочнаяСтоимостьГруза          = 0;
	
	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	// обнуление показателей загрузки с приращениями
	Для каждого ЭлементРейс Из ЭлементыДерева Цикл
		Если ЭлементРейс.Выбран Тогда // выбранные рейсы будут пересчитаны в процедуре ОбновитьПоказателиПоВыбраннымСтрокамДереваРейсов
			ЭлементРейс.МассаПослеПриращения                       = 0; 
			ЭлементРейс.ОбъемПослеПриращения                       = 0;
			ЭлементРейс.КоличествоМестПослеПриращения              = 0;
			ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения = 0;
		Иначе
			ЭлементРейс.МассаПослеПриращения                       = ЭлементРейс.Масса; 
			ЭлементРейс.ОбъемПослеПриращения                       = ЭлементРейс.Объем;
			ЭлементРейс.КоличествоМестПослеПриращения              = ЭлементРейс.КоличествоМест;
			ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения = ЭлементРейс.КоличествоЗагрузочныхМетров;
		КонецЕсли; 
		
		ЭлементРейс.ОценочнаяСтоимостьПослеПриращения          = ЭлементРейс.ОценочнаяСтоимость;
		
		МассаВозвратов                       = 0;
		ОбъемВозвратов                       = 0;
		КоличествоМестВозвратов              = 0;
		КоличествоЗагрузочныхМетровВозвратов = 0;
		ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
		Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
			Если ЭлементЗадание.ЭтоВозвратГруза Тогда
				МассаВозвратов                       = МассаВозвратов + ЭлементЗадание.Масса;
				ОбъемВозвратов                       = ОбъемВозвратов + ЭлементЗадание.Объем;
				КоличествоМестВозвратов              = КоличествоМестВозвратов + ЭлементЗадание.КоличествоМест;
				КоличествоЗагрузочныхМетровВозвратов = КоличествоЗагрузочныхМетровВозвратов + ЭлементЗадание.КоличествоЗагрузочныхМетров;
			КонецЕсли; 
		КонецЦикла; 
		
		ЭлементРейс.МассаВозвратовПослеПриращения                       = МассаВозвратов;
		ЭлементРейс.ОбъемВозвратовПослеПриращения                       = ОбъемВозвратов;
		ЭлементРейс.КоличествоМестВозвратовПослеПриращения              = КоличествоМестВозвратов;
		ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения = КоличествоЗагрузочныхМетровВозвратов;	
	КонецЦикла; 
	
	Если КоличествоВыбранныхСтрок Тогда 
		
		Сч = 0;
		Пока Сч < КоличествоВыбранныхСтрок Цикл
			текДанные = ВыбранныеСтроки[Сч];
			
			МассаГруза                       = МассаГруза + текДанные.Масса;
			ОбъемГруза                       = ОбъемГруза + текДанные.Объем;
			КоличествоМестГруза              = КоличествоМестГруза + текДанные.КоличествоМест;
			КоличествоЗагрузочныхМетровГруза = КоличествоЗагрузочныхМетровГруза + текДанные.КоличествоЗагрузочныхМетров;
			ОценочнаяСтоимостьГруза          = ОценочнаяСтоимостьГруза + текДанные.ОценочнаяСтоимость;
			
			Для каждого ЭлементРейс Из ЭлементыДерева Цикл
				
				Если РежимФормированияКонтейнеров И ЗначениеЗаполнено(ЭлементРейс.Рейс) И Не ЭлементРейс.ДоступноИзменениеМаршрута Тогда
					Продолжить;
				КонецЕсли; 
				
				Если УчитыватьМассу Тогда
					Если текДанные.ЭтоВозвратГруза Тогда
						ЭлементРейс.МассаВозвратовПослеПриращения = ЭлементРейс.МассаВозвратовПослеПриращения + текДанные.Масса;
					Иначе
						ЭлементРейс.МассаПослеПриращения = ЭлементРейс.МассаПослеПриращения + текДанные.Масса;
					КонецЕсли; 
				КонецЕсли;
				Если УчитыватьОбъем Тогда
					Если текДанные.ЭтоВозвратГруза Тогда
						ЭлементРейс.ОбъемВозвратовПослеПриращения = ЭлементРейс.ОбъемВозвратовПослеПриращения + текДанные.Объем;
					Иначе
						ЭлементРейс.ОбъемПослеПриращения = ЭлементРейс.ОбъемПослеПриращения + текДанные.Объем;
					КонецЕсли; 
				КонецЕсли;
				Если УчитыватьКоличествоМест Тогда
					Если текДанные.ЭтоВозвратГруза Тогда
						ЭлементРейс.КоличествоМестВозвратовПослеПриращения = ЭлементРейс.КоличествоМестВозвратовПослеПриращения + текДанные.КоличествоМест;
					Иначе
						ЭлементРейс.КоличествоМестПослеПриращения = ЭлементРейс.КоличествоМестПослеПриращения + текДанные.КоличествоМест;
					КонецЕсли;
				КонецЕсли;
				Если УчитыватьКоличествоЗагрузочныхМетров Тогда
					Если текДанные.ЭтоВозвратГруза Тогда
						ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения = ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения + текДанные.КоличествоЗагрузочныхМетров;
					Иначе
						ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения = ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения + текДанные.КоличествоЗагрузочныхМетров;
					КонецЕсли;
				КонецЕсли;
				ЭлементРейс.ОценочнаяСтоимостьПослеПриращения = ЭлементРейс.ОценочнаяСтоимостьПослеПриращения + текДанные.ОценочнаяСтоимость;
				
			КонецЦикла; 
			
			Сч = Сч + 1;
		КонецЦикла;	
	КонецЕсли;
	
	СтрокаВыбранныхЗаданий = ПоказателиЗагрузки[1];
	СтрокаВыбранныхЗаданий.КоличествоЕдиниц            = КоличествоВыбранныхСтрок;
	СтрокаВыбранныхЗаданий.Масса                       = МассаГруза;
	СтрокаВыбранныхЗаданий.Объем                       = ОбъемГруза;
	СтрокаВыбранныхЗаданий.КоличествоМест              = КоличествоМестГруза;
	СтрокаВыбранныхЗаданий.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетровГруза;
	СтрокаВыбранныхЗаданий.ОценочнаяСтоимость          = ОценочнаяСтоимостьГруза;
	
КонецПроцедуры // ОбновитьПоказателиПоВыбраннымСтрокамСпискаЗаданий()

&НаКлиенте
Процедура ОбновитьПоказателиПоВыделеннымСтрокамДереваРейсов()
	
	ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
	КоличествоВыделенныхСтрок = ВыделенныеСтрокиДерева.Количество();
	МассаГруза                       = 0;
	ОбъемГруза                       = 0;
	КоличествоМестГруза              = 0;
	КоличествоЗагрузочныхМетровГруза = 0;
	КоличествоЕдиниц                 = 0;
	Расстояние                       = 0;
	Время                            = 0;
	ОценочнаяСтоимостьГруза          = 0;
	
	Если КоличествоВыделенныхСтрок Тогда 
		
		мсвСтрокДерева = Новый Массив;
		мсвУдаляемыхИдентификаторов = Новый Массив;
		Для каждого Строка Из ВыделенныеСтрокиДерева Цикл
			текСтрока = ДеревоРейсы.НайтиПоИдентификатору(Строка);
			Если текСтрока <> Неопределено И текСтрока.Уровень = 0 Тогда
				ЭлементыЗадания = текСтрока.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					мсвУдаляемыхИдентификаторов.Добавить(текЗадание.ПолучитьИдентификатор());
				КонецЦикла;	
			КонецЕсли;
		КонецЦикла; 
		ВыделенныеСтрокиКопия = ОбщегоНазначенияКлиентСервер.СократитьМассив(ВыделенныеСтрокиДерева, мсвУдаляемыхИдентификаторов);
		Для каждого Строка Из ВыделенныеСтрокиКопия Цикл
			текДанные = ДеревоРейсы.НайтиПоИдентификатору(Строка);
			Если текДанные <> Неопределено Тогда
				Если РежимФормированияКонтейнеров И ЗначениеЗаполнено(текДанные.Рейс) И Не текДанные.ДоступноИзменениеМаршрута Тогда
					Продолжить;
				КонецЕсли; 
				
				МассаГруза                       = МассаГруза + текДанные.Масса;
				ОбъемГруза                       = ОбъемГруза + текДанные.Объем;
				КоличествоМестГруза              = КоличествоМестГруза + текДанные.КоличествоМест;
				КоличествоЗагрузочныхМетровГруза = КоличествоЗагрузочныхМетровГруза + текДанные.КоличествоЗагрузочныхМетров;
				ОценочнаяСтоимостьГруза          = ОценочнаяСтоимостьГруза + текДанные.ОценочнаяСтоимость;
				Если текДанные.Уровень = 0 Тогда
					КоличествоЕдиниц = КоличествоЕдиниц + текДанные.ПолучитьЭлементы().Количество();
					Расстояние = Расстояние + текДанные.Расстояние;
					Время      = Время + текДанные.Время;
				Иначе
					КоличествоЕдиниц = КоличествоЕдиниц + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
			
	КонецЕсли;
	
	СтрокаВыделенныхЗаданий = ПоказателиЗагрузки[2];
	СтрокаВыделенныхЗаданий.КоличествоЕдиниц            = КоличествоЕдиниц;
	СтрокаВыделенныхЗаданий.Масса                       = МассаГруза;
	СтрокаВыделенныхЗаданий.Объем                       = ОбъемГруза;
	СтрокаВыделенныхЗаданий.КоличествоМест              = КоличествоМестГруза;
	СтрокаВыделенныхЗаданий.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетровГруза;
	СтрокаВыделенныхЗаданий.РасстояниеПредставление     = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(Расстояние);
	СтрокаВыделенныхЗаданий.ВремяПредставление          = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Время*3600);
	СтрокаВыделенныхЗаданий.ОценочнаяСтоимость          = ОценочнаяСтоимостьГруза;
	
КонецПроцедуры // ОбновитьПоказателиПоВыделеннымСтрокамДереваРейсов()

&НаКлиенте
Процедура ОбновитьПоказателиПоВыбраннымСтрокамДереваРейсов()
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	
	// накапливаем итоговые приращения (ключ - исключаемый рейс, значение - число)
	Если УчитыватьМассу Тогда
		соотПриращенияМассы = Новый Соответствие;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		соотПриращенияОбъема = Новый Соответствие;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		соотПриращенияКоличестваМест = Новый Соответствие;
	КонецЕсли;
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		соотПриращенияКоличестваЗагрузочныхМетров = Новый Соответствие;
	КонецЕсли;
	соотПриращенияОценочнойСтоимости = Новый Соответствие;
	
	МассаГрузаВсего                       = 0;
	ОбъемГрузаВсего                       = 0;
	КоличествоМестГрузаВсего              = 0;
	КоличествоЗагрузочныхМетровГрузаВсего = 0;
	ОценочнаяСтоимостьГрузаВсего          = 0;
	КоличествоЕдиницВсего                 = 0;
	Расстояние                            = 0;
	Время                                 = 0;
	
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		Если ЭлементРейс.Выбран Тогда
			ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
			МассаГруза                       = 0;
			ОбъемГруза                       = 0;
			КоличествоМестГруза              = 0;
			КоличествоЗагрузочныхМетровГруза = 0;
			МассаВозвратовГруза                       = 0;
			ОбъемВозвратовГруза                       = 0;
			КоличествоМестВозвратовГруза              = 0;
			КоличествоЗагрузочныхМетровВозвратовГруза = 0;
			
			МассаНевозвратов                       = 0;
			ОбъемНевозвратов                       = 0;
			КоличествоМестНевозвратов              = 0;
			КоличествоЗагрузочныхМетровНевозвратов = 0;
			
			ОценочнаяСтоимостьГруза          = 0;
			Расстояние = Расстояние + ЭлементРейс.Расстояние;
			Время      = Время + ЭлементРейс.Время;
			Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
				Если ЭлементЗадание.Выбран Тогда
					Если УчитыватьМассу Тогда
						Если ЭлементЗадание.ЭтоВозвратГруза Тогда
							ЭлементРейс.МассаВозвратовПослеПриращения = ЭлементРейс.МассаВозвратовПослеПриращения - ЭлементЗадание.Масса;
							МассаВозвратовГруза = МассаВозвратовГруза + ЭлементЗадание.Масса;
						Иначе	
							МассаГруза      = МассаГруза + ЭлементЗадание.Масса;
						КонецЕсли; 
						МассаГрузаВсего = МассаГрузаВсего + ЭлементЗадание.Масса;
					КонецЕсли;
					Если УчитыватьОбъем Тогда
						Если ЭлементЗадание.ЭтоВозвратГруза Тогда
							ЭлементРейс.ОбъемВозвратовПослеПриращения = ЭлементРейс.ОбъемВозвратовПослеПриращения - ЭлементЗадание.Объем;
							ОбъемВозвратовГруза = ОбъемВозвратовГруза + ЭлементЗадание.Объем;							
						Иначе	
							ОбъемГруза      = ОбъемГруза + ЭлементЗадание.Объем;
						КонецЕсли;
						ОбъемГрузаВсего = ОбъемГрузаВсего + ЭлементЗадание.Объем;
					КонецЕсли;
					Если УчитыватьКоличествоМест Тогда
						Если ЭлементЗадание.ЭтоВозвратГруза Тогда
							ЭлементРейс.КоличествоМестВозвратовПослеПриращения = ЭлементРейс.КоличествоМестВозвратовПослеПриращения - ЭлементЗадание.КоличествоМест;
							КоличествоМестВозвратовГруза = КоличествоМестГруза + ЭлементЗадание.КоличествоМест;
						Иначе	
							КоличествоМестГруза      = КоличествоМестГруза + ЭлементЗадание.КоличествоМест;
						КонецЕсли;
						КоличествоМестГрузаВсего = КоличествоМестГрузаВсего + ЭлементЗадание.КоличествоМест;
					КонецЕсли;
					Если УчитыватьКоличествоЗагрузочныхМетров Тогда
						Если ЭлементЗадание.ЭтоВозвратГруза Тогда
							ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения = ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения - ЭлементЗадание.КоличествоЗагрузочныхМетров;
							КоличествоЗагрузочныхМетровВозвратовГруза = КоличествоЗагрузочныхМетровВозвратовГруза + ЭлементЗадание.КоличествоЗагрузочныхМетров;
						Иначе	
							КоличествоЗагрузочныхМетровГруза      = КоличествоЗагрузочныхМетровГруза + ЭлементЗадание.КоличествоЗагрузочныхМетров;
						КонецЕсли;
						КоличествоЗагрузочныхМетровГрузаВсего = КоличествоЗагрузочныхМетровГрузавсего + ЭлементЗадание.КоличествоЗагрузочныхМетров;
					КонецЕсли;
					ЭлементРейс.ОценочнаяСтоимостьПослеПриращения = ЭлементРейс.ОценочнаяСтоимостьПослеПриращения - ЭлементЗадание.ОценочнаяСтоимость;
					ОценочнаяСтоимостьГруза = ОценочнаяСтоимостьГруза + ЭлементЗадание.ОценочнаяСтоимость ;
					ОценочнаяСтоимостьГрузаВсего = ОценочнаяСтоимостьГрузаВсего + ЭлементЗадание.ОценочнаяСтоимость ;
					
					КоличествоЕдиницВсего = КоличествоЕдиницВсего + 1;
				КонецЕсли; 
				
				Если Не ЭлементЗадание.ЭтоВозвратГруза Тогда
					Если УчитыватьМассу Тогда
						МассаНевозвратов = МассаНевозвратов + ЭлементЗадание.Масса;
					КонецЕсли;
					Если УчитыватьОбъем Тогда
						ОбъемНевозвратов = ОбъемНевозвратов + ЭлементЗадание.Объем;
					КонецЕсли;
					Если УчитыватьКоличествоМест Тогда
						КоличествоМестНевозвратов = КоличествоМестНевозвратов + ЭлементЗадание.КоличествоМест;
					КонецЕсли;
					Если УчитыватьКоличествоЗагрузочныхМетров Тогда
						КоличествоЗагрузочныхМетровНевозвратов = КоличествоЗагрузочныхМетровНевозвратов + ЭлементЗадание.КоличествоЗагрузочныхМетров;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; 
			
			ЭлементРейс.МассаПослеПриращения = Макс(ЭлементРейс.МассаПослеПриращения, Макс(МассаНевозвратов - МассаГруза, ЭлементРейс.МассаВозвратовПослеПриращения));
			ЭлементРейс.ОбъемПослеПриращения = Макс(ОбъемНевозвратов - ОбъемГруза, ЭлементРейс.ОбъемВозвратовПослеПриращения);
			ЭлементРейс.КоличествоМестПослеПриращения = Макс(КоличествоМестНевозвратов - КоличествоМестГруза, ЭлементРейс.КоличествоМестВозвратовПослеПриращения);
			ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения = Макс(КоличествоЗагрузочныхМетровНевозвратов - КоличествоЗагрузочныхМетровГруза, ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения);
			
			Если УчитыватьМассу Тогда
				соотПриращенияМассы.Вставить(ЭлементРейс.Рейс, Новый Структура("МассаГруза, МассаВозвратовГруза", МассаГруза, МассаВозвратовГруза));
			КонецЕсли;
			Если УчитыватьОбъем Тогда
				соотПриращенияОбъема.Вставить(ЭлементРейс.Рейс, Новый Структура("ОбъемГруза, ОбъемВозвратовГруза", ОбъемГруза, ОбъемВозвратовГруза));
			КонецЕсли;
			Если УчитыватьКоличествоМест Тогда
				соотПриращенияКоличестваМест.Вставить(ЭлементРейс.Рейс, Новый Структура("КоличествоМестГруза, КоличествоМестВозвратовГруза", КоличествоМестГруза, КоличествоМестВозвратовГруза));
			КонецЕсли;
			Если УчитыватьКоличествоЗагрузочныхМетров Тогда
				соотПриращенияКоличестваЗагрузочныхМетров.Вставить(ЭлементРейс.Рейс, Новый Структура("КоличествоЗагрузочныхМетровГруза, КоличествоЗагрузочныхМетровВозвратовГруза", КоличествоЗагрузочныхМетровГруза, КоличествоЗагрузочныхМетровВозвратовГруза));
			КонецЕсли;
			соотПриращенияОценочнойСтоимости.Вставить(ЭлементРейс.Рейс, ОценочнаяСтоимостьГруза);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		Если УчитыватьМассу Тогда
			Для каждого текЗначение Из соотПриращенияМассы Цикл
				Если ЭлементРейс.Рейс <> текЗначение.Ключ Тогда
					ЭлементРейс.МассаПослеПриращения         = ЭлементРейс.МассаПослеПриращения + текЗначение.Значение.МассаГруза;
					ЭлементРейс.МассаВозвратовПослеПриращения = ЭлементРейс.МассаВозвратовПослеПриращения + текЗначение.Значение.МассаВозвратовГруза;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		Если УчитыватьОбъем Тогда
			Для каждого текЗначение Из соотПриращенияОбъема Цикл
				Если ЭлементРейс.Рейс <> текЗначение.Ключ Тогда
					ЭлементРейс.ОбъемПослеПриращения         = ЭлементРейс.ОбъемПослеПриращения + текЗначение.Значение.ОбъемГруза;
					ЭлементРейс.ОбъемВозвратовПослеПриращения = ЭлементРейс.ОбъемВозвратовПослеПриращения + текЗначение.Значение.ОбъемВозвратовГруза;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		Если УчитыватьКоличествоМест Тогда
			Для каждого текЗначение Из соотПриращенияКоличестваМест Цикл
				Если ЭлементРейс.Рейс <> текЗначение.Ключ Тогда
					ЭлементРейс.КоличествоМестПослеПриращения         = ЭлементРейс.КоличествоМестПослеПриращения + текЗначение.Значение.КоличествоМестГруза;
					ЭлементРейс.КоличествоМестВозвратовПослеПриращения = ЭлементРейс.КоличествоМестВозвратовПослеПриращения + текЗначение.Значение.КоличествоМестВозвратовГруза;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		Если УчитыватьКоличествоЗагрузочныхМетров Тогда
			Для каждого текЗначение Из соотПриращенияКоличестваЗагрузочныхМетров Цикл
				Если ЭлементРейс.Рейс <> текЗначение.Ключ Тогда
					ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения         = ЭлементРейс.КоличествоЗагрузочныхМетровПослеПриращения + текЗначение.Значение.КоличествоЗагрузочныхМетровГруза;
					ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения = ЭлементРейс.КоличествоЗагрузочныхМетровВозвратовПослеПриращения + текЗначение.Значение.КоличествоЗагрузочныхМетровВозвратовГруза;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		
		Для каждого текЗначение Из соотПриращенияОценочнойСтоимости Цикл
			Если ЭлементРейс.Рейс <> текЗначение.Ключ Тогда
				ЭлементРейс.ОценочнаяСтоимостьПослеПриращения = ЭлементРейс.ОценочнаяСтоимостьПослеПриращения + текЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	СтрокаВыбранныхЗаданий = ПоказателиЗагрузки[3];
	СтрокаВыбранныхЗаданий.КоличествоЕдиниц            = КоличествоЕдиницВсего;
	СтрокаВыбранныхЗаданий.Масса                       = МассаГрузаВсего;
	СтрокаВыбранныхЗаданий.Объем                       = ОбъемГрузаВсего;
	СтрокаВыбранныхЗаданий.КоличествоМест              = КоличествоМестГрузаВсего;
	СтрокаВыбранныхЗаданий.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетровГрузаВсего;
	СтрокаВыбранныхЗаданий.РасстояниеПредставление     = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(Расстояние);
	СтрокаВыбранныхЗаданий.ВремяПредставление          = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Время*3600);
	СтрокаВыбранныхЗаданий.ОценочнаяСтоимость          = ОценочнаяСтоимостьГрузаВсего;
	
КонецПроцедуры // ОбновитьПоказателиПоВыбраннымСтрокамДереваРейсов()

&НаКлиенте
Процедура ОбновитьПоказателиПоВыбраннымСтрокам()
	
	ОбновитьПоказателиПоВыбраннымСтрокамСпискаЗаданий();
	ОбновитьПоказателиПоВыбраннымСтрокамДереваРейсов();
	ОбновитьПредставлениеПоказателейЗагрузкиРейсов();
	
КонецПроцедуры // ОбновитьПоказателиПоВыбраннымСтрокам()

&НаКлиенте
// Процедура обновляет Места Интереса для отображения
//
// Параметры:
//  Нет.
//
Процедура ОбновитьМестаИнтересаНаКлиенте()
	
	Для каждого МестоИнтереса Из СписокМестаИнтереса Цикл
		ОбработатьМаркер(МестоИнтереса);
	КонецЦикла;
	
КонецПроцедуры // ОбновитьМестаИнтересаНаКлиенте()

&НаСервере
// Заполним список мест интереса
//
// Параметры:
//  Нет.
//
Процедура ОбновитьСписокМестИнтересаНаСервере()
	
	МестаИнтереса = ПараметрыПланирования.МестаИнтереса;
	Если Не МестаИнтереса.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СписокМестаИнтереса.Очистить();
	
	Запрос = Новый Запрос;                       
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МестаИнтереса.Ссылка КАК МестоИнтереса,
	|	МестаИнтереса.Адрес КАК Адрес,
	|	МестаИнтереса.Адрес.Долгота КАК Долгота,
	|	МестаИнтереса.Адрес.Широта КАК Широта,
	|	МестаИнтереса.Адрес.Представление КАК ПредставлениеАдрес,
	|	МестаИнтереса.Ссылка.Представление КАК ПредставлениеМестоИнтереса,
	|	МестаИнтереса.Ссылка.СтильМаркера КАК СтильМаркера
	|ПОМЕСТИТЬ втМестаИнтереса
	|ИЗ
	|	Справочник.упМестаИнтереса.Адреса КАК МестаИнтереса
	|ГДЕ
	|	МестаИнтереса.Ссылка В(&МестаИнтереса)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтилиМаркеров.Ссылка КАК Стиль,
	|	упСтилиМаркеров.Картинка,
	|	упСтилиМаркеров.ХранилищеЗначения,
	|	упСтилиМаркеров.ПоложениеПривязкиX,
	|	упСтилиМаркеров.ПоложениеПривязкиY,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Ширина
	|		ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК Ширина,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Высота
	|		ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК Высота
	|ПОМЕСТИТЬ втСтильМаркераПоУмолчанию
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	упСтилиМаркеров.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.Основной)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМестаИнтереса.МестоИнтереса КАК МестоИнтереса,
	|	втМестаИнтереса.ПредставлениеМестоИнтереса КАК ПредставлениеМестоИнтереса,
	|	втМестаИнтереса.Адрес КАК Адрес,
	|	втМестаИнтереса.ПредставлениеАдрес КАК ПредставлениеАдрес,
	|	втМестаИнтереса.Долгота КАК Долгота,
	|	втМестаИнтереса.Широта КАК Широта,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Стиль
	|		ИНАЧЕ втМестаИнтереса.СтильМаркера
	|	КОНЕЦ КАК СтильМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Картинка
	|		ИНАЧЕ упСтилиМаркеров.Картинка
	|	КОНЕЦ КАК КартинкаМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.ХранилищеЗначения
	|		ИНАЧЕ упСтилиМаркеров.ХранилищеЗначения
	|	КОНЕЦ КАК ХранилищеЗначенияМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.ПоложениеПривязкиX
	|		ИНАЧЕ упСтилиМаркеров.ПоложениеПривязкиX
	|	КОНЕЦ КАК ПоложениеПривязкиXМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.ПоложениеПривязкиY
	|		ИНАЧЕ упСтилиМаркеров.ПоложениеПривязкиY
	|	КОНЕЦ КАК ПоложениеПривязкиYМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Ширина
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упСтилиМаркеров.Масштаб = 0
	|					ТОГДА упСтилиМаркеров.Ширина
	|				ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|			КОНЕЦ
	|	КОНЕЦ КАК ШиринаМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Ссылка ЕСТЬ NULL 
	|			ТОГДА втСтильМаркераПоУмолчанию.Высота
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упСтилиМаркеров.Масштаб = 0
	|					ТОГДА упСтилиМаркеров.Высота
	|				ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|			КОНЕЦ
	|	КОНЕЦ КАК ВысотаМаркера
	|ИЗ
	|	втМестаИнтереса КАК втМестаИнтереса
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|		ПО втМестаИнтереса.СтильМаркера = упСтилиМаркеров.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтильМаркераПоУмолчанию КАК втСтильМаркераПоУмолчанию
	|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("МестаИнтереса", МестаИнтереса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = СписокМестаИнтереса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
		НоваяСтрока.Пометка = Истина;
		
		Если Выборка.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
			НоваяСтрока.КартинкаМаркера = Выборка.ХранилищеЗначенияМаркера.Получить();
		Иначе
			НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркера);
			НоваяСтрока.ЭтоКартинка = Истина;
		КонецЕсли;
		
		ДанныеМаркера = СтрШаблон("'%1;%2;%3;%4;%5;%6;%7;%8'", Выборка.ПредставлениеМестоИнтереса,
			Выборка.ПредставлениеАдрес,
			ПредставлениеЧисла(Выборка.Долгота),
			ПредставлениеЧисла(Выборка.Широта),
			ПредставлениеЧисла(НоваяСтрока.ПоложениеПривязкиXМаркера),
			ПредставлениеЧисла(НоваяСтрока.ПоложениеПривязкиYМаркера),
			ПредставлениеЧисла(НоваяСтрока.ШиринаМаркера),
			ПредставлениеЧисла(НоваяСтрока.ВысотаМаркера));
		
		НоваяСтрока.Данные = ДанныеМаркера;
		
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСписокМестИнтересаНаСервере()

&НаКлиенте
// Процедура обновляет цветовые схемы для отображения маршрута и точек рейса
//
// Параметры:
//  Нет.
//
Процедура ОбновитьЦветовыеСхемыНаКлиенте()
	
	Для каждого текЦветоваяСхема Из ЦветовыеСхемы Цикл
		ОбработатьМаркер(текЦветоваяСхема,, 1);
		ОбработатьМаркер(текЦветоваяСхема, "Выделенного", 1);
		ОбработатьМаркер(текЦветоваяСхема, "Выбранного", 1);
	КонецЦикла;
	
КонецПроцедуры // ОбновитьЦветовыеСхемыНаКлиенте()

&НаСервере
Процедура ОбновитьЦветовыеСхемыНаСервере()
	
	ЦветовыеСхемы.Очистить();
	упСервисныеФункции.УдалитьУсловноеОформлениеФормыПоПрефиксу(ЭтаФорма);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЦветовыеСхемы.СтильМаркера,
	|	упЦветовыеСхемы.СтильМаркера.Картинка КАК КартинкаМаркера,
	|	упЦветовыеСхемы.СтильМаркера.ХранилищеЗначения КАК ХранилищеЗначенияМаркера,
	|	упЦветовыеСхемы.СтильМаркера.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркера,
	|	упЦветовыеСхемы.СтильМаркера.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркера,
	|	упЦветовыеСхемы.СтильВыделенногоМаркера КАК СтильМаркераВыделенного,
	|	упЦветовыеСхемы.СтильВыделенногоМаркера.Картинка КАК КартинкаМаркераВыделенного,
	|	упЦветовыеСхемы.СтильВыделенногоМаркера.ХранилищеЗначения КАК ХранилищеЗначенияМаркераВыделенного,
	|	упЦветовыеСхемы.СтильВыделенногоМаркера.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркераВыделенного,
	|	упЦветовыеСхемы.СтильВыделенногоМаркера.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркераВыделенного,
	|	упЦветовыеСхемы.СтильВыбранногоМаркера КАК СтильМаркераВыбранного,
	|	упЦветовыеСхемы.СтильВыбранногоМаркера.Картинка КАК КартинкаМаркераВыбранного,
	|	упЦветовыеСхемы.СтильВыбранногоМаркера.ХранилищеЗначения КАК ХранилищеЗначенияМаркераВыбранного,
	|	упЦветовыеСхемы.СтильВыбранногоМаркера.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркераВыбранного,
	|	упЦветовыеСхемы.СтильВыбранногоМаркера.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркераВыбранного,
	|	упЦветовыеСхемы.СтильЛинии.Цвет КАК ЦветЛинии,
	|	упЦветовыеСхемы.СтильЛинии.Толщина КАК ТолщинаЛинии,
	|	упЦветовыеСхемы.СтильЛинии.Прозрачность КАК ПрозрачностьЛинии,
	|	упЦветовыеСхемы.Приоритет КАК Приоритет
	|ИЗ
	|	Справочник.упЦветовыеСхемы КАК упЦветовыеСхемы
	|ГДЕ
	|	НЕ упЦветовыеСхемы.ПометкаУдаления
	|	И упЦветовыеСхемы.ИспользоватьДляОтображенияМаршрута
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ЦветовыеСхемы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если Выборка.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
				НоваяСтрока.КартинкаМаркера = Выборка.ХранилищеЗначенияМаркера.Получить();
			Иначе
				НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркера);
				НоваяСтрока.ЭтоКартинка = Истина;
			КонецЕсли;
			Если Выборка.КартинкаМаркераВыделенного = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
				НоваяСтрока.КартинкаМаркераВыделенного = Выборка.ХранилищеЗначенияМаркераВыделенного.Получить();
			Иначе
				НоваяСтрока.КартинкаМаркераВыделенного = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркераВыделенного);
				НоваяСтрока.ЭтоКартинкаВыделенного = Истина;
			КонецЕсли;
			Если Выборка.КартинкаМаркераВыбранного = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
				НоваяСтрока.КартинкаМаркераВыбранного = Выборка.ХранилищеЗначенияМаркераВыбранного.Получить();
			Иначе
				НоваяСтрока.КартинкаМаркераВыбранного = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркераВыбранного);
				НоваяСтрока.ЭтоКартинкаВыбранного = Истина;
			КонецЕсли;
			УстановитьОформлениеМаркераИЛинииВСтрокеТаблицы(Выборка, Выборка, НоваяСтрока);
		КонецЦикла; 
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	упСтилиМаркеров.Ссылка КАК СтильМаркера,
		|	упСтилиМаркеров.Картинка КАК КартинкаМаркера,
		|	упСтилиМаркеров.ХранилищеЗначения КАК ХранилищеЗначенияМаркера,
		|	упСтилиМаркеров.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркера,
		|	упСтилиМаркеров.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркера
		|ИЗ
		|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
		|ГДЕ
		|	упСтилиМаркеров.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.Основной)
		|;
		|ВЫБРАТЬ
		|	упСтилиЛиний.Цвет КАК ЦветЛинии,
		|	упСтилиЛиний.Толщина КАК ТолщинаЛинии,
		|	упСтилиЛиний.Прозрачность КАК ПрозрачностьЛинии
		|ИЗ
		|	Справочник.упСтилиЛиний КАК упСтилиЛиний
		|ГДЕ
		|	упСтилиЛиний.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиЛиний.Основной)";
		Выборка = Запрос.ВыполнитьПакет();
		ДанныеМаркера = Выборка[0].Выбрать();
		ДанныеМаркера.Следующий();
		ДанныеЛинии = Выборка[1].Выбрать();
		ДанныеЛинии.Следующий();
		НоваяСтрока = ЦветовыеСхемы.Добавить();
		НоваяСтрока.СтильМаркера = ДанныеМаркера.СтильМаркера;
		НоваяСтрока.СтильМаркераВыделенного = ДанныеМаркера.СтильМаркера;
		НоваяСтрока.СтильМаркераВыбранного = ДанныеМаркера.СтильМаркера;
		Если ДанныеМаркера.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
			НоваяСтрока.КартинкаМаркера = ДанныеМаркера.ХранилищеЗначенияМаркера.Получить();
		Иначе
			НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(ДанныеМаркера.КартинкаМаркера);
			НоваяСтрока.ЭтоКартинка = Истина;
			НоваяСтрока.ЭтоКартинкаВыделенного = Истина;
			НоваяСтрока.ЭтоКартинкаВыбранного = Истина;
		КонецЕсли;
		НоваяСтрока.КартинкаМаркераВыделенного = НоваяСтрока.КартинкаМаркера;
		НоваяСтрока.КартинкаМаркераВыбранного  = НоваяСтрока.КартинкаМаркера;
		УстановитьОформлениеМаркераИЛинииВСтрокеТаблицы(ДанныеЛинии, ДанныеМаркера, НоваяСтрока);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОформлениеМаркераИЛинииВСтрокеТаблицы(Знач ДанныеЛинии, Знач ДанныеМаркера, Знач СтрокаЦветовойСхемы)
	
	СтрокаЦветовойСхемы.ДанныеМаркера            = ПредставлениеЧисла(ДанныеМаркера.ПоложениеПривязкиXМаркера) + ", " + ПредставлениеЧисла(ДанныеМаркера.ПоложениеПривязкиYМаркера);
	СтрокаЦветовойСхемы.ДанныеМаркераВыделенного = СтрокаЦветовойСхемы.ДанныеМаркера;
	СтрокаЦветовойСхемы.ДанныеМаркераВыбранного  = СтрокаЦветовойСхемы.ДанныеМаркера;
	
	сткХарактеристикиЛинии = Новый Структура;
	сткХарактеристикиЛинии.Вставить("Цвет",         ДанныеЛинии.ЦветЛинии);
	сткХарактеристикиЛинии.Вставить("Толщина",      ДанныеЛинии.ТолщинаЛинии);
	сткХарактеристикиЛинии.Вставить("Прозрачность", ДанныеЛинии.ПрозрачностьЛинии);
	СтрокаЦветовойСхемы.ХарактеристикиЛинии = сткХарактеристикиЛинии;
	
	ПолеФормы = Элементы.ДеревоРейсыЦветНаКарте;
	ПутьКДаннымНомераЦветовойСхемы = "ДеревоРейсы.НомерЦветовойСхемы";
	ЭлементУсловноеОформление = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловноеОформление.Представление = "# Цвет строки " + ЭтаФорма.УсловноеОформление.Элементы.Количество();
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭлементУсловноеОформление.Отбор, ПутьКДаннымНомераЦветовойСхемы, ЦветовыеСхемы.Индекс(СтрокаЦветовойСхемы) + 1);
	ПолеУсловногоОформления = ЭлементУсловноеОформление.Поля.Элементы.Добавить();
	ПолеУсловногоОформления.Поле = Новый ПолеКомпоновкиДанных(ПолеФормы.Имя);
	ПолеУсловногоОформления.Использование = Истина;
	ЭлементУсловноеОформление.Оформление.УстановитьЗначениеПараметра("ЦветФона", ДанныеЛинии.ЦветЛинии);
	//ЭлементУсловноеОформление.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ДанныеЛинии.ЦветЛинии);
	//ЭлементУсловноеОформление.Оформление.УстановитьЗначениеПараметра("Текст", "*");
	ЭлементУсловноеОформление.Использование = Истина;
	
КонецПроцедуры // ОбновитьЦветовыеСхемыНаСервере()

&НаСервере
// Процедура обновляет таблицу географических зон
//
Процедура ОбновитьСписокЗонНаСервере()
	
	МассивЗоны = Новый Массив;
	Для каждого ТекущееЗадание Из СписокЗаданий Цикл
		// Получим все зоны заданий.
		Если МассивЗоны.Найти(ТекущееЗадание.ЗонаОтправления) = Неопределено Тогда
			МассивЗоны.Добавить(ТекущееЗадание.ЗонаОтправления);
		КонецЕсли;
		Если МассивЗоны.Найти(ТекущееЗадание.ЗонаПолучения) = Неопределено Тогда
			МассивЗоны.Добавить(ТекущееЗадание.ЗонаПолучения);
		КонецЕсли;
	КонецЦикла;	
	
	ТекущийСписокЗоны = СписокЗон.Выгрузить();
	ТекущийСписокЗоны.ЗаполнитьЗначения(Ложь, "Пометка");
	СписокЗон.Очистить();
	Для каждого ТекущаяЗона Из МассивЗоны Цикл
		НоваяСтрока = СписокЗон.Добавить();
		ОтборПоЗонам = Новый Структура;
		ОтборПоЗонам.Вставить("Зона",ТекущаяЗона);
		НайденныеСтроки = ТекущийСписокЗоны.НайтиСтроки(ОтборПоЗонам);
		Если НайденныеСтроки.Количество() Тогда
			НайденнаяСтрока = НайденныеСтроки[0];
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока); 
			ТекущийСписокЗоны.Удалить(НайденнаяСтрока);
		Иначе
			НоваяСтрока.Зона = ТекущаяЗона; 
		КонецЕсли;
		НоваяСтрока.Пометка = Истина;
	КонецЦикла;	
	
	Для каждого СтрокаОсталасьПослеПроверки Из ТекущийСписокЗоны Цикл
		Если НЕ СтрокаОсталасьПослеПроверки.ИндексВМассиве = 0 Тогда
			НоваяСтрока = СписокЗон.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОсталасьПослеПроверки);
		КонецЕсли;
	КонецЦикла; 
	
	ТаблицаИзХранилища = Неопределено;
	
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		КлючОбъекта = "Обработка.упРМПланированиеРейсов.СписокЗон." + ВидТранспортнойУслугиПредставление;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упВариантыНастроек.КлючВарианта
		|ИЗ
		|	Справочник.упВариантыНастроек КАК упВариантыНастроек
		|ГДЕ
		|   упВариантыНастроек.ВидПеревозки = &ВидПеревозки
		|	И упВариантыНастроек.Автор = &Автор
		|	И упВариантыНастроек.Объект = &Объект
		|	И НЕ упВариантыНастроек.ПометкаУдаления";
		Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
		Запрос.УстановитьПараметр("Объект", КлючОбъекта);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				СохраненныеЗоны = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
				Если СохраненныеЗоны <> Неопределено Тогда
					ТаблицаИзХранилища = СохраненныеЗоны.Результат;
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ТаблицаИзХранилища = Неопределено Тогда
		Для каждого СтрокаЗоныСПометкой Из ТаблицаИзХранилища Цикл
			ПометкаЗоны = Истина;
			Если МассивЗоны.Найти(СтрокаЗоныСПометкой.Зона) = Неопределено Тогда
				ПометкаЗоны = Ложь;
			КонецЕсли;	
			ОтборПоЗонам = Новый Структура;
			ОтборПоЗонам.Вставить("Зона",СтрокаЗоныСПометкой.Зона);
			НайденныеСтроки = СписокЗон.НайтиСтроки(ОтборПоЗонам);
			Для каждого СтрокаСпискаЗон Из НайденныеСтроки Цикл
				СтрокаСпискаЗон.Пометка = ?(ПометкаЗоны,СтрокаЗоныСПометкой.Пометка,Ложь);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
// Процедура обновляет таблицу заданий на перевозку грузов.
//
// Параметры:
//  МассивОтборов - Массив - Массив со структурами, содержащими данные о элементах отбора.
//    * Структура:
//      ** ИмяПоля        - Строка - Представление.
//      ** ПравоеЗначение - Произвольный - Значение отбора.
//      ** ВидСравнения   - ВидСравненияКомпоновкиДанных - Сравнение.
//  ОбновлятьПолностью - Булево - Признак, влияющий на характер обновления списка заданий 
//                                ("Истина" - таблица с заданиями (груз. местами) полностью перезаполняется, 
//                                 "Ложь" - в список заданий добавляются.
//                                  новые строки в соответствии с переданными отборами).
//
Процедура ОбновитьТаблицуЗаданийНаСервере(МассивОтборов = Неопределено, ОбновлятьПолностью = Истина, ДобавлениеЗаданий = Ложь)
	
	СписокЗаданийСформирован = Ложь; // признак требуется для запрета выполнения операций с объектами карты до полного заполнения списка заданий и отображения точек на карте
	СнимокСохраняемыхСвойствЗаданий = СнимокСохраняемыхСвойствЗаданий();
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		сткРеквизитыВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПеревозки, "ПараметрыВводаИнформацииОГрузовыхМестах, ВариантФормированияМаршрута");
		РазбиватьЗаданияПоГрузовымМестам = (сткРеквизитыВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку
		                                     Или сткРеквизитыВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса);
		ЭтоСамовывоз = (сткРеквизитыВидаПеревозки.ВариантФормированияМаршрута = Перечисления.упВариантыФормированияМаршрута.Самовывоз = Истина);									 
	Иначе
		РазбиватьЗаданияПоГрузовымМестам = Ложь;
		ЭтоСамовывоз                     = Ложь;
	КонецЕсли;
	
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		ИмяРеквизита = "ГрузовоеМесто";
		Элементы.РазбитьСтрокуСпискаЗаданий.Видимость = Истина;
	Иначе
		ИмяРеквизита = "ЗаданиеНаПеревозку";
		Элементы.РазбитьСтрокуСпискаЗаданий.Видимость = Ложь;
	КонецЕсли;
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	
	// Установка отбора
	Если ОбновлятьПолностью Тогда
		СписокЗаданий.Очистить();
		СписокОтправлений.Очистить();
		СписокНегеокодированныхАдресов.Очистить();
		ПервыйЗапуск = Истина;
	Иначе
		МассивОтборовУдалить = Новый Массив;
		Для каждого Отбор Из МассивОтборов Цикл
			КоличествоОтборов = КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество();
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор, Отбор.ИмяПоля, Отбор.ПравоеЗначение, Отбор.ВидСравнения,, Истина);
			Если КоличествоОтборов = КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() Тогда
			   МассивОтборовУдалить.Добавить(Отбор);
			КонецЕсли; 
		КонецЦикла;
		
		Если МассивОтборовУдалить.Количество() Тогда
			МассивОтборов = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивОтборов, МассивОтборовУдалить);
		КонецЕсли; 
	КонецЕсли;
	
	// Установка параметров
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "РазбиватьЗаданияПоГрузовымМестам", РазбиватьЗаданияПоГрузовымМестам, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "НачалоПериода", НачалоПериодаПланирования, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "КонецПериода", ОкончаниеПериодаПланирования, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "ВидПеревозки", ВидПеревозки, Истина);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));
	упСервисныеФункции.НайтиДобавитьЭлементНастроекКомпоновкиПоПолю(КомпоновщикНастроек.Настройки.Выбор, "ПредыдущееЗадание");
	ЭлементОтбораКУдалению = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(КомпоновщикНастроек.Настройки.Отбор.Элементы, "ПредыдущееЗадание");
	Если ЭлементОтбораКУдалению <> Неопределено Тогда
		КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбораКУдалению);
	КонецЕсли; 
	упСервисныеФункции.НайтиДобавитьЭлементНастроекКомпоновкиПоПолю(КомпоновщикНастроек.Настройки.Выбор, "ПолеУпорядочивания1");
	упСервисныеФункции.НайтиДобавитьЭлементНастроекКомпоновкиПоПолю(КомпоновщикНастроек.Настройки.Выбор, "ПолеУпорядочивания2");
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	ДатаОбновленияСпискаЗаданий = ТекущаяДатаСеанса();
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Если Не РежимФормированияКонтейнеров И ЗначениеЗаполнено(ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе) Тогда
		Если Не ОбновлятьПолностью И РазбиватьЗаданияПоГрузовымМестам Тогда
			ТекущиеГрузы = СписокЗаданий.Выгрузить(, "ГрузовоеМесто").ВыгрузитьКолонку("ГрузовоеМесто")
		Иначе
			ТекущиеГрузы = Неопределено;
		КонецЕсли; 
		
		// Добавим зависимые задания
		ВременныеНастройки = КомпоновщикНастроек.ПолучитьНастройки();
		ВременныеНастройки.Отбор.Элементы.Очистить();
		ЗагруженныеЗадания = Результат.ВыгрузитьКолонку("ЗаданиеНаПеревозку");
		СписокЗначенийЛ = Новый СписокЗначений;
		СписокЗначенийЛ.ЗагрузитьЗначения(ЗагруженныеЗадания);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ВременныеНастройки.Отбор, "ПредыдущееЗадание", СписокЗначенийЛ, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		Если Не ТекущиеГрузы = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ВременныеНастройки.Отбор, "ГрузовоеМесто", ТекущиеГрузы, ВидСравненияКомпоновкиДанных.НеВСписке,, Истина);
		КонецЕсли; 
		ДополнитьСписокЗаданийДополнительнымиЗаданиями(СхемаКомпоновкиДанных, ВременныеНастройки, Результат);
		
		// Добавим основные задания
		ВременныеНастройки = КомпоновщикНастроек.ПолучитьНастройки();
		ВременныеНастройки.Отбор.Элементы.Очистить();
		ЗагруженныеЗадания = Результат.ВыгрузитьКолонку("ПредыдущееЗадание");
		СписокЗначенийЛ = Новый СписокЗначений;
		СписокЗначенийЛ.ЗагрузитьЗначения(ЗагруженныеЗадания);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ВременныеНастройки.Отбор, "ЗаданиеНаПеревозку", СписокЗначенийЛ, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		Если Не ТекущиеГрузы = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ВременныеНастройки.Отбор, "ГрузовоеМесто", ТекущиеГрузы, ВидСравненияКомпоновкиДанных.НеВСписке,, Истина);
		КонецЕсли; 
		ДополнитьСписокЗаданийДополнительнымиЗаданиями(СхемаКомпоновкиДанных, ВременныеНастройки, Результат);
		Результат.Сортировать("ПолеУпорядочивания1, ПолеУпорядочивания2");
	КонецЕсли;
	
	МаксШирота  = -180;
	МаксДолгота = -90;
	МинШирота   =  180;
	МинДолгота  =  90;
	
	ИспользуютсяМультимодальныеПеревозки = ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки");
	ИспользуетсяКоличественныйУчетГрузов = ПолучитьФункциональнуюОпцию("упИспользуетсяКоличественныйУчетГрузов");
	
	Для каждого текЗадание Из Результат Цикл
		НоваяСтрока = СписокЗаданий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текЗадание);
		Представление = СтрШаблон("ru = '%1 (%2)%3'", ?(РазбиватьЗаданияПоГрузовымМестам, текЗадание.ПредставлениеГруза, "Груз по заданию"),
	                                                 ?(ИспользуетсяКоличественныйУчетГрузов, текЗадание.КоличествоГрузов, 1),
													 ?(ИспользуютсяМультимодальныеПеревозки И текЗадание.НомерВЦепиПоставок > 0, СтрШаблон(" <этап %1-%2>", текЗадание.НомерВЦепиПоставок, текЗадание.НомерВЦепиПоставокКонец), ""));
		НоваяСтрока.Представление = НСтр(Представление, КодЯзыка);
		НоваяСтрока.КоличествоГрузов	= ?(ИспользуетсяКоличественныйУчетГрузов, текЗадание.КоличествоГрузов, 1);
		Если ЗначениеЗаполнено(НоваяСтрока.ГрафикОтправления) И Не ЗначениеЗаполнено(НоваяСтрока.ОтправлениеГрузаС) И Не ЗначениеЗаполнено(НоваяСтрока.ОтправлениеГрузаПо) Тогда
			НоваяСтрока.ВременноеОкноОтправленияПредставление = НСтр("ru = 'по графику работы'");
		Иначе	
			НоваяСтрока.ВременноеОкноОтправленияПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(НоваяСтрока.ОтправлениеГрузаС, НоваяСтрока.ОтправлениеГрузаПо);
		КонецЕсли;

		Если ЗначениеЗаполнено(НоваяСтрока.ГрафикПолучения) И Не ЗначениеЗаполнено(НоваяСтрока.ПолучениеГрузаС) И Не ЗначениеЗаполнено(НоваяСтрока.ПолучениеГрузаПо) Тогда
			НоваяСтрока.ВременноеОкноПолученияПредставление = НСтр("ru = 'по графику работы'");
		Иначе	
			НоваяСтрока.ВременноеОкноПолученияПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(НоваяСтрока.ПолучениеГрузаС, НоваяСтрока.ПолучениеГрузаПо);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(текЗадание.КоординатыОтправления) Тогда
			НовыйНегеокодированныйАдрес = СписокНегеокодированныхАдресов.Добавить();
			НовыйНегеокодированныйАдрес.ЗаданиеНаПеревозку = текЗадание.ЗаданиеНаПеревозку;
			НовыйНегеокодированныйАдрес.Объект = текЗадание.АдресОтправления;			
		КонецЕсли; 	
		
		Если Не ЗначениеЗаполнено(текЗадание.КоординатыПолучения) Тогда
			НовыйНегеокодированныйАдрес = СписокНегеокодированныхАдресов.Добавить();
			НовыйНегеокодированныйАдрес.ЗаданиеНаПеревозку = текЗадание.ЗаданиеНаПеревозку;
			НовыйНегеокодированныйАдрес.Объект = текЗадание.АдресПолучения;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяСтрока.КоординатыОтправления) И ЗначениеЗаполнено(НоваяСтрока.КоординатыПолучения) Тогда
			НоваяСтрока.Пометка = Истина;
			НоваяСтрока.Геокодировано = Истина;
			МинШирота  = Мин(МинШирота, текЗадание.ШиротаТочкиОтправления, текЗадание.ШиротаТочкиПолучения);
			МаксШирота = Макс(МаксШирота, текЗадание.ШиротаТочкиОтправления, текЗадание.ШиротаТочкиПолучения);
			МинДолгота  = Мин(МинДолгота, текЗадание.ДолготаТочкиОтправления, текЗадание.ДолготаТочкиПолучения);
			МаксДолгота = Макс(МаксДолгота, текЗадание.ДолготаТочкиОтправления, текЗадание.ДолготаТочкиПолучения);
		Иначе
			НоваяСтрока.Пометка       = Ложь;
			НоваяСтрока.Геокодировано = Ложь;
			НоваяСтрока.Активно       = Ложь;
		КонецЕсли;
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			сткХарактеристикиЛинии = Новый Структура;
			сткХарактеристикиЛинии.Вставить("Цвет",         текЗадание.ЦветЛинии);
			сткХарактеристикиЛинии.Вставить("Толщина",      текЗадание.ТолщинаЛинии);
			сткХарактеристикиЛинии.Вставить("Прозрачность", текЗадание.ПрозрачностьЛинии);
			НоваяСтрока.ХарактеристикиЛинии = сткХарактеристикиЛинии;
		КонецЕсли; 
	КонецЦикла;
	
	КоличествоДобавленныхСтрок = Результат.Количество();
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		
		тзВидыАдресовОтправления = Результат.Скопировать(, "ВидАдресаОтправления");
		тзВидыАдресовОтправления.Свернуть("ВидАдресаОтправления");
		мсвВидовАдресовОтправления = тзВидыАдресовОтправления.ВыгрузитьКолонку("ВидАдресаОтправления");
		тзВидыАдресовПолучения = Результат.Скопировать(, "ВидАдресаПолучения");
		тзВидыАдресовПолучения.Свернуть("ВидАдресаПолучения");
		мсвВидовАдресовПолучения   = тзВидыАдресовПолучения.ВыгрузитьКолонку("ВидАдресаПолучения");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвВидовАдресовОтправления, мсвВидовАдресовПолучения);
		мсвВидовАдресов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВидовАдресовОтправления);
		ОбновитьСписокМаркеровНаСервере(мсвВидовАдресов, ОбновлятьПолностью);
		
		тзОтправлений = Результат.Скопировать(, "АдресОтправления, ВидАдресаОтправления, ИмяОтправления, КоординатыОтправления");
		тзОтправлений.Свернуть("АдресОтправления, ВидАдресаОтправления, ИмяОтправления, КоординатыОтправления");
		
		Для каждого текОтправление Из тзОтправлений Цикл
			АдресаОтправлений = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текОтправление.АдресОтправления));
			Если АдресаОтправлений.Количество() = 0 Тогда
				НоваяСтрока = СписокОтправлений.Добавить();
				НоваяСтрока.Адрес = текОтправление.АдресОтправления;
				НоваяСтрока.ВидАдреса = текОтправление.ВидАдресаОтправления;
				НоваяСтрока.Имя = текОтправление.ИмяОтправления;
				//Если ЗначениеЗаполнено(текОтправление.ШиротаТочкиОтправления) И ЗначениеЗаполнено(текОтправление.ДолготаТочкиОтправления) Тогда
				Если ЗначениеЗаполнено(текОтправление.КоординатыОтправления) Тогда
					//НоваяСтрока.Координаты = ПредставлениеЧисла(текОтправление.ШиротаТочкиОтправления) + ";" + ПредставлениеЧисла(текОтправление.ДолготаТочкиОтправления);
					НоваяСтрока.Координаты = текОтправление.КоординатыОтправления;
					НоваяСтрока.Геокодировано = Истина;
					НоваяСтрока.Пометка       = Истина;
				Иначе
					НоваяСтрока.Геокодировано = Ложь;
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла; 
		Если ОбновлятьПолностью Или ДобавлениеЗаданий Тогда
			КоординатыПозиционирования = ПредставлениеЧисла(МаксШирота) + ";" + ПредставлениеЧисла(МаксДолгота) + ";" + ПредставлениеЧисла(МинШирота) + ";" + ПредставлениеЧисла(МинДолгота) + ";";
		КонецЕсли; 
		
		Если СписокНегеокодированныхАдресов.Количество() Тогда
			тзНегеокодированныеАдреса = СписокНегеокодированныхАдресов.Выгрузить();
			тзНегеокодированныеАдреса.Свернуть("ЗаданиеНаПеревозку, Объект");
			СписокНегеокодированныхАдресов.Загрузить(тзНегеокодированныеАдреса);
		КонецЕсли;
		
		// При большом количестве точек к планированию использование отображения линий и маршрутов между адресами отправления и получения.
		// Критически увеличивает время отрисовки карты.
		Если СписокЗаданий.Количество() < 300 Тогда
			Элементы.ГруппаРежимОтображенияКарты.Видимость = Истина;
		Иначе
			Элементы.ГруппаРежимОтображенияКарты.Видимость = Ложь;
			РежимОтображенияКарты = 0;
		КонецЕсли;
		
		Если Не ИспользоватьКластеризацию И КоличествоТочекДляАвтоматическогоИспользованияКластеризации > 0 И СписокЗаданий.Количество() >= КоличествоТочекДляАвтоматическогоИспользованияКластеризации Тогда
			ИспользоватьКластеризацию = Истина;
			Элементы.ВключитьКластеризацию.Пометка = Истина;
		КонецЕсли;
		
		КоличествоНегеокодированныхАдресов = СписокНегеокодированныхАдресов.Количество();
		Если КоличествоНегеокодированныхАдресов Тогда	
			Элементы.ГруппаГеокодированиеАдресов.Видимость = Истина;
			Элементы.ДекорацияГеокодированиеАдресов.Доступность = Истина;
			НадписьЗаголовок = СтрШаблон(НСтр("ru = 'Количество негеокодированных адресов: %1'", КодЯзыка), КоличествоНегеокодированныхАдресов);
			Элементы.ДекорацияГеокодированиеАдресов.Заголовок = НадписьЗаголовок;
		Иначе
			Элементы.ГруппаГеокодированиеАдресов.Видимость = Ложь;
			Элементы.ДекорацияГеокодированиеАдресов.Доступность = Ложь;
		КонецЕсли;
		
		Если ОтображатьЗоны Тогда
			ОбновитьСписокЗонНаСервере();
		Иначе
			СписокЗон.Очистить();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ОбновлятьПолностью Тогда
		Для каждого Отбор Из МассивОтборов Цикл
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(КомпоновщикНастроек.Настройки.Отбор, Отбор.ИмяПоля);
		КонецЦикла; 
	КонецЕсли;
	
	Для Каждого СтрокаЗадания Из СписокЗаданий Цикл
		ВосстановитьСохраняемыеСвойстваЗадания(СтрокаЗадания, СнимокСохраняемыхСвойствЗаданий);
	КонецЦикла;
	
	Если РежимФормированияКонтейнеров И ОбновлятьПолностью Тогда
		ИдентификаторПомеченнойСтрокиФК = -1;
		АдресОтправленияКонтейнера = Неопределено;
		АдресПолученияКонтейнера   = Неопределено;
		Элементы.СписокЗаданий.ОтборСтрок = Новый ФиксированнаяСтруктура("АдресОтправления, АдресПолучения", АдресОтправленияКонтейнера, АдресПолученияКонтейнера);			
	КонецЕсли; 
	ПодготовитьДанныеОтображения();
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьСписокЗаданийДополнительнымиЗаданиями(СхемаКомпоновкиДанных, ВременныеНастройки, Результат)
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, ВременныеНастройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	ДополнительныеЗадания = Новый ТаблицаЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(ДополнительныеЗадания);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	Если ДополнительныеЗадания.НайтиСтроки(Новый Структура("АдресПолучения", Null)).Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Некоторые связанные с отобранными задания не попали в заданный интервал планирования!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	Для Каждого СтрокаЗадания Из ДополнительныеЗадания Цикл
		КлючУникальностиСписка = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто");
		ЗаполнитьЗначенияСвойств(КлючУникальностиСписка, СтрокаЗадания); 
		Если Результат.НайтиСтроки(КлючУникальностиСписка).Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), СтрокаЗадания); 
		КонецЕсли;
	КонецЦикла;
	СтрокиКУдалению = Результат.НайтиСтроки(Новый Структура("АдресПолучения", Null));
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		Результат.Удалить(СтрокаКУдалению);
	КонецЦикла;

КонецПроцедуры // ОбновитьТаблицуЗаданийНаСервере()

&НаСервере
Процедура ОбновитьСписокМаркеровНаСервере(МассивВидовАдресов, ОбновлятьПолностью)
	
	Если ОбновлятьПолностью Тогда
		СписокМаркеровВидовАдресов.Очистить();
	Иначе
		ИспользованныеВиды = СписокМаркеровВидовАдресов.Выгрузить(, "ВидАдреса").ВыгрузитьКолонку("ВидАдреса");
		МассивВидовАдресов = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивВидовАдресов, ИспользованныеВиды);
		Если МассивВидовАдресов.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтилиМаркеров.Ссылка КАК Стиль,
	|	упСтилиМаркеров.Картинка,
	|	упСтилиМаркеров.ХранилищеЗначения,
	|	упСтилиМаркеров.ПоложениеПривязкиX,
	|	упСтилиМаркеров.ПоложениеПривязкиY,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Ширина
	|		ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК Ширина,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Высота
	|		ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК Высота
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	упСтилиМаркеров.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиМаркеров.Основной)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упВидыАдресов.Ссылка КАК ВидАдреса,
	|	упВидыАдресов.СтильМаркера,
	|	упВидыАдресов.СтильМаркера.Картинка КАК КартинкаМаркера,
	|	упВидыАдресов.СтильМаркера.ХранилищеЗначения КАК ХранилищеЗначенияМаркера,
	|	упВидыАдресов.СтильМаркера.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркера,
	|	упВидыАдресов.СтильМаркера.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркера,
	|	ВЫБОР
	|		КОГДА упВидыАдресов.СтильМаркера.Масштаб = 0
	|			ТОГДА упВидыАдресов.СтильМаркера.Ширина
	|		ИНАЧЕ упВидыАдресов.СтильМаркера.Ширина * упВидыАдресов.СтильМаркера.Масштаб / 100
	|	КОНЕЦ КАК ШиринаМаркера,
	|	ВЫБОР
	|		КОГДА упВидыАдресов.СтильМаркера.Масштаб = 0
	|			ТОГДА упВидыАдресов.СтильМаркера.Высота
	|		ИНАЧЕ упВидыАдресов.СтильМаркера.Высота * упВидыАдресов.СтильМаркера.Масштаб / 100
	|	КОНЕЦ КАК ВысотаМаркера,
	|	упВидыАдресов.СтильВыделенногоМаркера КАК СтильМаркераВыделенного,
	|	упВидыАдресов.СтильВыделенногоМаркера.Картинка КАК КартинкаМаркераВыделенного,
	|	упВидыАдресов.СтильВыделенногоМаркера.ХранилищеЗначения КАК ХранилищеЗначенияМаркераВыделенного,
	|	упВидыАдресов.СтильВыделенногоМаркера.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркераВыделенного,
	|	упВидыАдресов.СтильВыделенногоМаркера.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркераВыделенного,
	|	ВЫБОР
	|		КОГДА упВидыАдресов.СтильВыделенногоМаркера.Масштаб = 0
	|			ТОГДА упВидыАдресов.СтильВыделенногоМаркера.Ширина
	|		ИНАЧЕ упВидыАдресов.СтильВыделенногоМаркера.Ширина * упВидыАдресов.СтильВыделенногоМаркера.Масштаб / 100
	|	КОНЕЦ КАК ШиринаМаркераВыделенного,
	|	ВЫБОР
	|		КОГДА упВидыАдресов.СтильВыделенногоМаркера.Масштаб = 0
	|			ТОГДА упВидыАдресов.СтильВыделенногоМаркера.Высота
	|		ИНАЧЕ упВидыАдресов.СтильВыделенногоМаркера.Высота * упВидыАдресов.СтильВыделенногоМаркера.Масштаб / 100
	|	КОНЕЦ КАК ВысотаМаркераВыделенного,
	|	упВидыАдресов.СтильВыбранногоМаркера КАК СтильМаркераВыбранного,
	|	упВидыАдресов.СтильВыбранногоМаркера.Картинка КАК КартинкаМаркераВыбранного,
	|	упВидыАдресов.СтильВыбранногоМаркера.ХранилищеЗначения КАК ХранилищеЗначенияМаркераВыбранного,
	|	упВидыАдресов.СтильВыбранногоМаркера.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркераВыбранного,
	|	упВидыАдресов.СтильВыбранногоМаркера.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркераВыбранного,
	|	ВЫБОР
	|		КОГДА упВидыАдресов.СтильВыбранногоМаркера.Масштаб = 0
	|			ТОГДА упВидыАдресов.СтильВыбранногоМаркера.Ширина
	|		ИНАЧЕ упВидыАдресов.СтильВыбранногоМаркера.Ширина * упВидыАдресов.СтильВыбранногоМаркера.Масштаб / 100
	|	КОНЕЦ КАК ШиринаМаркераВыбранного,
	|	ВЫБОР
	|		КОГДА упВидыАдресов.СтильВыбранногоМаркера.Масштаб = 0
	|			ТОГДА упВидыАдресов.СтильВыбранногоМаркера.Высота
	|		ИНАЧЕ упВидыАдресов.СтильВыбранногоМаркера.Высота * упВидыАдресов.СтильВыбранногоМаркера.Масштаб / 100
	|	КОНЕЦ КАК ВысотаМаркераВыбранного
	|ИЗ
	|	Справочник.упВидыАдресов КАК упВидыАдресов
	|ГДЕ
	|	упВидыАдресов.Ссылка В(&ВидыАдресов)";
	Запрос.УстановитьПараметр("ВидыАдресов", МассивВидовАдресов);
	Результат = Запрос.ВыполнитьПакет();
	МаркерПоУмолчанию = Результат[0].Выбрать();
	МаркерПоУмолчанию.Следующий();
	
	Если Не Результат[1].Пустой() Тогда
		Выборка = Результат[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокМаркеровВидовАдресов.Добавить();
						
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если ЗначениеЗаполнено(Выборка.СтильМаркера) Тогда
				Если Выборка.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаМаркера = Выборка.ХранилищеЗначенияМаркера.Получить();
				Иначе
					НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркера);
					НоваяСтрока.ЭтоКартинка = Истина;
				КонецЕсли;
			Иначе
				НоваяСтрока.СтильМаркера = МаркерПоУмолчанию.Стиль;
				Если МаркерПоУмолчанию.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаМаркера = МаркерПоУмолчанию.ХранилищеЗначения.Получить();
				Иначе
					НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(МаркерПоУмолчанию.Картинка);
					НоваяСтрока.ЭтоКартинка = Истина;
				КонецЕсли;
				НоваяСтрока.ПоложениеПривязкиXМаркера = МаркерПоУмолчанию.ПоложениеПривязкиX;
				НоваяСтрока.ПоложениеПривязкиYМаркера = МаркерПоУмолчанию.ПоложениеПривязкиY;
				НоваяСтрока.ШиринаМаркера = МаркерПоУмолчанию.Ширина;
				НоваяСтрока.ВысотаМаркера = МаркерПоУмолчанию.Высота;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.СтильМаркераВыделенного) Тогда
				Если Выборка.КартинкаМаркераВыделенного = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаМаркераВыделенного = Выборка.ХранилищеЗначенияМаркераВыделенного.Получить();
				Иначе
					НоваяСтрока.КартинкаМаркераВыделенного = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркераВыделенного);
					НоваяСтрока.ЭтоКартинкаВыделенного = Истина;
				КонецЕсли;
			Иначе
				НоваяСтрока.СтильМаркераВыделенного = МаркерПоУмолчанию.Стиль;
				Если МаркерПоУмолчанию.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаМаркераВыделенного = МаркерПоУмолчанию.ХранилищеЗначения.Получить();
				Иначе
					НоваяСтрока.КартинкаМаркераВыделенного = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(МаркерПоУмолчанию.Картинка);
					НоваяСтрока.ЭтоКартинкаВыделенного = Истина;
				КонецЕсли;
				НоваяСтрока.ПоложениеПривязкиXМаркераВыделенного = МаркерПоУмолчанию.ПоложениеПривязкиX;
				НоваяСтрока.ПоложениеПривязкиYМаркераВыделенного = МаркерПоУмолчанию.ПоложениеПривязкиY;
				НоваяСтрока.ШиринаМаркераВыделенного = МаркерПоУмолчанию.Ширина;
				НоваяСтрока.ВысотаМаркераВыделенного = МаркерПоУмолчанию.Высота;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.СтильМаркераВыбранного) Тогда
				Если Выборка.КартинкаМаркераВыбранного = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаМаркераВыбранного = Выборка.ХранилищеЗначенияМаркераВыбранного.Получить();
				Иначе
					НоваяСтрока.КартинкаМаркераВыбранного = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркераВыбранного);
					НоваяСтрока.ЭтоКартинкаВыбранного = Истина;
				КонецЕсли;
			Иначе
				НоваяСтрока.СтильМаркераВыбранного = МаркерПоУмолчанию.Стиль;
				Если МаркерПоУмолчанию.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
					НоваяСтрока.КартинкаМаркераВыбранного = МаркерПоУмолчанию.ХранилищеЗначения.Получить();
				Иначе
					НоваяСтрока.КартинкаМаркераВыбранного = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(МаркерПоУмолчанию.Картинка);
					НоваяСтрока.ЭтоКартинкаВыбранного = Истина;
				КонецЕсли;
				НоваяСтрока.ПоложениеПривязкиXМаркераВыбранного = МаркерПоУмолчанию.ПоложениеПривязкиX;
				НоваяСтрока.ПоложениеПривязкиYМаркераВыбранного = МаркерПоУмолчанию.ПоложениеПривязкиY;
				НоваяСтрока.ШиринаМаркераВыбранного = МаркерПоУмолчанию.Ширина;
				НоваяСтрока.ВысотаМаркераВыбранного = МаркерПоУмолчанию.Высота;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСписокМаркеровНаСервере()

&НаСервере
Процедура ОбновитьСписокНезависимыхМаркеровНаСервере()
	
	МассивСтилей = Новый Массив;
	МассивСтилей.Добавить(Справочники.упСтилиМаркеров.ТранспортноеСредство);
	СписокНезависимыхМаркеров.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтилиМаркеров.Ссылка КАК СтильМаркера,
	|	упСтилиМаркеров.Картинка КАК КартинкаМаркера,
	|	упСтилиМаркеров.ХранилищеЗначения КАК ХранилищеЗначенияМаркера,
	|	упСтилиМаркеров.ПоложениеПривязкиX КАК ПоложениеПривязкиXМаркера,
	|	упСтилиМаркеров.ПоложениеПривязкиY КАК ПоложениеПривязкиYМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Ширина
	|		ИНАЧЕ упСтилиМаркеров.Ширина * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК ШиринаМаркера,
	|	ВЫБОР
	|		КОГДА упСтилиМаркеров.Масштаб = 0
	|			ТОГДА упСтилиМаркеров.Высота
	|		ИНАЧЕ упСтилиМаркеров.Высота * упСтилиМаркеров.Масштаб / 100
	|	КОНЕЦ КАК ВысотаМаркера
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	упСтилиМаркеров.Ссылка В(&МассивСтилей)";
	Запрос.УстановитьПараметр("МассивСтилей", МассивСтилей);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокНезависимыхМаркеров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если Выборка.КартинкаМаркера = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда
				НоваяСтрока.КартинкаМаркера = Выборка.ХранилищеЗначенияМаркера.Получить();
			Иначе
				НоваяСтрока.КартинкаМаркера = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(Выборка.КартинкаМаркера);
				НоваяСтрока.ЭтоКартинка = Истина;
				//ИмяВременногоФайла = ПолучитьИмяВременногоФайла("png");
				//НоваяСтрока.КартинкаМаркера.Записать(ИмяВременногоФайла);
				//НоваяСтрока.ДанныеМаркера = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСписокМаркеровНаСервере()

&НаСервере
Функция СнимокСохраняемыхСвойствРейсов()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ТипТС");
	Результат.Колонки.Добавить("ТранспортноеСредство");
	Результат.Колонки.Добавить("Рейс");
	Результат.Колонки.Добавить("Активно");
	Результат.Колонки.Добавить("Пометка");
	Для Каждого СтрокаДерева Из ДеревоРейсы.ПолучитьЭлементы() Цикл
		Если Ложь
			Или Не СтрокаДерева.Активно 
			Или Не СтрокаДерева.Пометка
		Тогда
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), СтрокаДерева); 
		КонецЕсли; 
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СнимокСохраняемыхСвойствЗаданий()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ЗаданиеНаПеревозку");
	Результат.Колонки.Добавить("Активно");
	Результат.Колонки.Добавить("Пометка");
	Для Каждого СтрокаЗадания Из СписокЗаданий Цикл
		Если СтрокаЗадания.Геокодировано И (Не СтрокаЗадания.Активно Или Не СтрокаЗадания.Пометка) Тогда
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), СтрокаЗадания); 
		КонецЕсли; 
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СтатусыРейсов()
	
	Результат = Новый Массив;
	Если РежимКорректировки Тогда
		Результат.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
		Результат.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	Иначе
		Результат.Добавить(Перечисления.упСтатусыРейса.Новый);
		Результат.Добавить(Перечисления.упСтатусыРейса.КПланированиюТС);
		Результат.Добавить(Перечисления.упСтатусыРейса.СформированыЗаявкиНаТС);
		Результат.Добавить(Перечисления.упСтатусыРейса.НазначеноТС);
		Результат.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

&НаСервере
// Процедура обновляет дерево рейсов
//
// Параметры:
//  Нет.
//
Процедура ОбновитьДеревоРейсовНаСервере()
	
	сткРеквизиты = Новый Структура();
	сткРеквизиты.Вставить("АвтоматическиДобавлятьЗаданияНаВозврат");
	сткРеквизиты.Вставить("АвтоматическиДобавлятьЗаданияИнкассации");
	сткЗначениеРеквизитов = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(ВидПеревозки, сткРеквизиты);
	
	ЭтаФорма.АвтоматическиДобавлятьЗаданияНаВозврат = сткЗначениеРеквизитов.АвтоматическиДобавлятьЗаданияНаВозврат;
	ЭтаФорма.АвтоматическиДобавлятьЗаданияИнкассации = сткЗначениеРеквизитов.АвтоматическиДобавлятьЗаданияИнкассации;
	КоличествоРейсов = 0;
	СнимокСохраняемыхСвойствРейсов = СнимокСохраняемыхСвойствРейсов();
	ДеревоРейсы.ПолучитьЭлементы().Очистить();
	Элементы.ДеревоРейсы.ВыделенныеСтроки.Очистить();
	ОбновитьЦветовыеСхемыНаСервере();
	
	тзТранспортныеСредства = ДанныеФормыВЗначение(СписокТранспорта, Тип("ТаблицаЗначений"));
	Запрос = Новый Запрос;
	Запрос.Текст =
"
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	СписокТС.Перевозчик КАК Перевозчик,
|	СписокТС.ТипТС КАК ТипТС,
|	ВЫРАЗИТЬ(СписокТС.ТранспортноеСредство КАК Справочник.упТранспортныеСредства) КАК ТранспортноеСредство,
|	СписокТС.Количество КАК Количество,
|	СписокТС.КоличествоЦиклов КАК КоличествоЦиклов,
|	СписокТС.ПланироватьЦиклически
|ПОМЕСТИТЬ втТранспортныеСредства03
|ИЗ
|	&ТранспортныеСредства КАК СписокТС
|;
|ВЫБРАТЬ
|	Т.Перевозчик КАК Перевозчик,
|	Т.ТипТС КАК ТипТС,
|	Т.ТранспортноеСредство КАК ТранспортноеСредство,
|	ВЫБОР
|		КОГДА НЕ Т.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка) И Т.ПланироватьЦиклически И Т.КоличествоЦиклов > 1
|		ТОГДА Т.КоличествоЦиклов 
|		ИНАЧЕ Т.Количество
|	КОНЕЦ КАК Количество
|ПОМЕСТИТЬ втТранспортныеСредства05
|ИЗ
|	втТранспортныеСредства03 КАК Т
|ГДЕ НЕ &РежимКорректировки
|ОБЪЕДИНИТЬ
|ВЫБРАТЬ
|	упТранспортныеСредства.Партнер КАК Перевозчик,
|	упТранспортныеСредства.ТипТС КАК ТипТС,
|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
|	1 КАК Количество
|ИЗ
|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
|ГДЕ &РежимКорректировки
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упПрицепы.ТранспортноеСредство КАК ТранспортноеСредство,
|	СУММА(упПрицепы.Прицеп.Грузоподъемность) КАК Грузоподъемность,
|	СУММА(упПрицепы.Прицеп.Объем) КАК Объем,
|	СУММА(упПрицепы.Прицеп.КоличествоМест) КАК КоличествоМест,
|	СУММА(упПрицепы.Прицеп.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
|ПОМЕСТИТЬ ХарактеристикиПрицепов
|ИЗ
|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
|			,
|			ТранспортноеСредство В
|				(ВЫБРАТЬ
|					втТранспортныеСредства05.ТранспортноеСредство КАК ТранспортноеСредство
|				ИЗ
|					втТранспортныеСредства05 КАК втТранспортныеСредства05)) КАК упПрицепы
|ГДЕ
|	упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
|
|СГРУППИРОВАТЬ ПО
|	упПрицепы.ТранспортноеСредство
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втТранспортныеСредства.Перевозчик КАК Перевозчик,
|	втТранспортныеСредства.ТипТС КАК ТипТС,
|	втТранспортныеСредства.ТранспортноеСредство КАК ТранспортноеСредство,
|	втТранспортныеСредства.Количество КАК Количество,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность) = 0
|			КОНЕЦ
|			ТОГДА 999999999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность)
|			КОНЕЦ
|	КОНЕЦ КАК МассаМакс,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем) = 0
|			КОНЕЦ
|			ТОГДА 999999999999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем)
|			КОНЕЦ
|	КОНЕЦ КАК ОбъемМакс,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест) = 0
|			КОНЕЦ
|			ТОГДА 99999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест)
|			КОНЕЦ
|	КОНЕЦ КАК КоличествоМестМакс,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров) = 0
|			КОНЕЦ
|			ТОГДА 99999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров)
|			КОНЕЦ
|	КОНЕЦ КАК КоличествоЗагрузочныхМетровМакс,
|	ЕСТЬNULL(УпУчетныеПараметрыТранспортныхСредств_СрезПоследних.ПринятоКУчету, ЛОЖЬ) КАК ПринятоКУчету,
|	ЕСТЬNULL(упТранспортныеСредства.СобственноеТранспортноеСредство, ЛОЖЬ) КАК СобственноеТранспортноеСредство,
|	ВЫБОР
|		КОГДА упТранспортныеСредства.СобственноеТранспортноеСредство = ИСТИНА
|				И НЕ ЕСТЬNULL(УпУчетныеПараметрыТранспортныхСредств_СрезПоследних.ПринятоКУчету, ЛОЖЬ)
|				И НЕ упТранспортныеСредства.ВидТранспорта = ЗНАЧЕНИЕ(Перечисление.упВидыТранспорта.Контейнер)
|			ТОГДА ""(неучетное) "" + упТранспортныеСредства.Наименование
|		ИНАЧЕ упТранспортныеСредства.Наименование
|	КОНЕЦ КАК ПредставлениеТС,
|	ВЫБОР
|		КОГДА упТранспортныеСредства.СобственноеТранспортноеСредство = ИСТИНА
|				И НЕ ЕСТЬNULL(УпУчетныеПараметрыТранспортныхСредств_СрезПоследних.ПринятоКУчету, ЛОЖЬ)
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК Активно,
|	УпСостоянияТранспортныхСредств_СрезПоследнихТ.СостояниеТС КАК СостояниеТС
|ПОМЕСТИТЬ втТранспортныеСредства10
|ИЗ
|	втТранспортныеСредства05 КАК втТранспортныеСредства
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
|		ПО втТранспортныеСредства.ТранспортноеСредство = упТранспортныеСредства.Ссылка
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК упТипыТС
|		ПО втТранспортныеСредства.ТипТС = упТипыТС.Ссылка
|		ЛЕВОЕ СОЕДИНЕНИЕ ХарактеристикиПрицепов КАК ХарактеристикиПрицепов
|		ПО втТранспортныеСредства.ТранспортноеСредство = ХарактеристикиПрицепов.ТранспортноеСредство
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(, ) КАК УпУчетныеПараметрыТранспортныхСредств_СрезПоследних
|		ПО (УпУчетныеПараметрыТранспортныхСредств_СрезПоследних.ТранспортноеСредство = втТранспортныеСредства.ТранспортноеСредство)
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСостоянияТранспортныхСредств.СрезПоследних(, ) КАК УпСостоянияТранспортныхСредств_СрезПоследнихТ
|		ПО (УпСостоянияТранспортныхСредств_СрезПоследнихТ.ТранспортноеСредство = втТранспортныеСредства.ТранспортноеСредство)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	упРейс.Ссылка КАК Рейс,
|	упПеревозчикПоРейсу.Перевозчик КАК Перевозчик,
|	упПеревозчикПоРейсу.ТипТС КАК ТипТС,
|	упПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
|	упСтатусыРейсовСрезПоследних.Статус.Порядок КАК ИндексКартинки,
|	упСтатусыРейсовСрезПоследних.Статус КАК СтатусРейса
|ПОМЕСТИТЬ втРейсы05
|ИЗ
|	Документ.упРейс КАК упРейс
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, ) КАК упСтатусыРейсовСрезПоследних
|		ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
|			И (упРейс.ВидПеревозки = &ВидПеревозки)
|			И (упСтатусыРейсовСрезПоследних.Статус В (&СтатусыРейсов))
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, ) КАК упПеревозчикПоРейсу
|		ПО упРейс.Ссылка = упПеревозчикПоРейсу.Рейс

|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	ЕСТЬNULL(втРейсы.Рейс, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)) КАК Рейс,
|	ЕСТЬNULL(втРейсы.Перевозчик, втТранспортныеСредства.Перевозчик) КАК Перевозчик,
|	ЕСТЬNULL(втРейсы.ТипТС, втТранспортныеСредства.ТипТС) КАК ТипТС,
|	ЕСТЬNULL(втРейсы.ТранспортноеСредство, втТранспортныеСредства.ТранспортноеСредство) КАК ТранспортноеСредство,
|	втТранспортныеСредства.МассаМакс КАК МассаМакс,
|	втТранспортныеСредства.ОбъемМакс КАК ОбъемМакс,
|	втТранспортныеСредства.КоличествоМестМакс КАК КоличествоМестМакс,
|	втТранспортныеСредства.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Расстояние, 0) КАК Расстояние,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Время, 0) КАК Время,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ВремяВПути, 0) КАК ВремяВПути,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ЗонаПогрузки, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаПогрузки,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ЗонаРазгрузки, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)) КАК ЗонаРазгрузки, 
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) КАК Масса,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
|	ВЫБОР
|		КОГДА ЕСТЬNULL(втРейсы.Перевозчик, втТранспортныеСредства.Перевозчик) = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
|			ТОГДА 2
|		КОГДА ЕСТЬNULL(втРейсы.ТранспортноеСредство, втТранспортныеСредства.ТранспортноеСредство) = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ КАК Порядок,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеНачалоВыполнения,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеОкончаниеВыполнения,
|	втРейсы.ИндексКартинки КАК ИндексКартинки,
|	втРейсы.СтатусРейса КАК СтатусРейса,
|	втТранспортныеСредства.ПринятоКУчету КАК ПринятоКУчету,
|	втТранспортныеСредства.СобственноеТранспортноеСредство КАК СобственноеТранспортноеСредство,
|	втТранспортныеСредства.ПредставлениеТС КАК ПредставлениеТС,
|	втТранспортныеСредства.Активно КАК Активно,
|	втТранспортныеСредства.СостояниеТС КАК СостояниеТС
|ПОМЕСТИТЬ втРейсы15
|ИЗ
|	втТранспортныеСредства10 КАК втТранспортныеСредства
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы05 КАК втРейсы
|		ПО втТранспортныеСредства.Перевозчик = втРейсы.Перевозчик
|			И втТранспортныеСредства.ТипТС = втРейсы.ТипТС
|			И втТранспортныеСредства.ТранспортноеСредство = втРейсы.ТранспортноеСредство
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
|		ПО (втРейсы.Рейс = упПлановыеПараметрыРейса.Рейс)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗЛИЧНЫЕ
|	втРейсы.Перевозчик КАК Перевозчик,
|	втРейсы.ТипТС КАК ТипТС,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство
|ПОМЕСТИТЬ втКомбинацииТСВРейсах
|ИЗ
|	втРейсы15 КАК втРейсы
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втКомбинации.Перевозчик КАК Перевозчик,
|	втКомбинации.ТипТС КАК ТипТС,
|	втКомбинации.ТранспортноеСредство КАК ТранспортноеСредство,
|	КОЛИЧЕСТВО(втРейсы.Рейс) КАК Количество
|ПОМЕСТИТЬ втКоличествоТСВРейсах
|ИЗ
|	втКомбинацииТСВРейсах КАК втКомбинации
|		ЛЕВОЕ СОЕДИНЕНИЕ втРейсы15 КАК втРейсы
|		ПО втКомбинации.Перевозчик = втРейсы.Перевозчик
|			И втКомбинации.ТипТС = втРейсы.ТипТС
|			И втКомбинации.ТранспортноеСредство = втРейсы.ТранспортноеСредство
|
|СГРУППИРОВАТЬ ПО
|	втКомбинации.Перевозчик,
|	втКомбинации.ТипТС,
|	втКомбинации.ТранспортноеСредство
|;
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	Задания.Рейс КАК Рейс,
|	Задания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозку,
|	Задания.Задание КАК ЗаданиеНаПеревозку,
|	Задания.ГрузовоеМесто КАК ГрузовоеМесто,
|	МАКСИМУМ(Задания.КоличествоГрузов) КАК КоличествоГрузов,
|	Задания.Задание.Номер КАК НомерЗадания,
|	Задания.Задание.АдресПолучения.Широта КАК Широта,
|	Задания.Задание.АдресПолучения.Долгота КАК Долгота,
|	Задания.Задание.АдресПолучения КАК Адрес
|ПОМЕСТИТЬ втЗаданияПодготовка
|ИЗ
|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
|		, Рейс В (ВЫБРАТЬ Рейс ИЗ втРейсы15)
|		) КАК Задания

|ГДЕ ИСТИНА
|		И Задания.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка),
|								ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка),
|								ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей), 
|								ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
|	И НЕ Задания.Отменена 
|	И НЕ Задания.Выполнена
|СГРУППИРОВАТЬ ПО
|	Задания.Рейс,
|	Задания.Задание,
|	Задания.ГрузовоеМесто
|;
|
|ВЫБРАТЬ
|	Задания.Рейс,
|	Задания.ЗаданиеНаПеревозку,
|	Задания.ГрузовоеМесто КАК ГрузовоеМесто,
|	Задания.КоличествоГрузов КАК КоличествоГрузов,
|	Задания.НомерЗадания,
|	Задания.Широта,
|	Задания.Долгота,
|	Задания.Адрес,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упПараметрыЗаданияНаПеревозку.КоличествоМест) * Задания.КоличествоГрузов КАК КоличествоМест,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров) * Задания.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упПараметрыЗаданияНаПеревозку.Объем) * Задания.КоличествоГрузов КАК Объем,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упПараметрыЗаданияНаПеревозку.Масса) * Задания.КоличествоГрузов КАК Масса,
|	Задания.ГрузовоеМесто.ОценочнаяСтоимость * Задания.КоличествоГрузов КАК ОценочнаяСтоимость
|ПОМЕСТИТЬ втЗадания
|ИЗ
|	втЗаданияПодготовка КАК Задания
|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку В (ВЫБРАТЬ втЗадания.ЗаявкаНаПеревозку ИЗ втЗаданияПодготовка КАК втЗадания ГДЕ НЕ втЗадания.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
|		ПО Задания.ЗаявкаНаПеревозку = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
|			И Задания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(, ЗаданиеНаПеревозку В (ВЫБРАТЬ втЗадания.ЗаданиеНаПеревозку ИЗ втЗаданияПодготовка КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
|		ПО Задания.ЗаданиеНаПеревозку = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
|			И Задания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
|ГДЕ ВЫРАЗИТЬ(&ВидПеревозки КАК Справочник.упВидыПеревозок).ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку),
|			ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса),
|			ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса))
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	Задания.Рейс КАК Рейс,
|	Задания.ЗаданиеНаПеревозку,
|	Задания.ГрузовоеМесто,
|	Задания.КоличествоГрузов КАК КоличествоГрузов,
|	Задания.НомерЗадания,
|	Задания.Широта,
|	Задания.Долгота,
|	Задания.Адрес,
|	Задания.ЗаданиеНаПеревозку.КоличествоМест,
|	Задания.ЗаданиеНаПеревозку.КоличествоЗагрузочныхМетров,
|	Задания.ЗаданиеНаПеревозку.Объем,
|	Задания.ЗаданиеНаПеревозку.Масса,
|	Задания.ЗаданиеНаПеревозку.ОценочнаяСтоимость
|ИЗ
|	втЗаданияПодготовка КАК Задания
|ГДЕ ВЫРАЗИТЬ(&ВидПеревозки КАК Справочник.упВидыПеревозок).ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВидыТочекОтправления.ВидТочкиОтправления
|ПОМЕСТИТЬ втВидыТочекОтправления
|ИЗ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
|ГДЕ
|	ВидыТочекОтправления.Ссылка = &ВидПеревозки
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втЗадания.ЗаданиеНаПеревозку,
|	МАКСИМУМ(ВЫБОР КОГДА втВидыТочекОтправления1.ВидТочкиОтправления ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ) КАК ОтправлениеДепо,
|	МАКСИМУМ(ВЫБОР КОГДА втВидыТочекОтправления2.ВидТочкиОтправления ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ) КАК ПолучениеДепо
|ПОМЕСТИТЬ втПоказателиАдресов	
|ИЗ втЗадания КАК втЗадания
|ЛЕВОЕ СОЕДИНЕНИЕ втВидыТочекОтправления КАК втВидыТочекОтправления1
|ПО втЗадания.ЗаданиеНаПеревозку.АдресОтправления.ВидАдреса = втВидыТочекОтправления1.ВидТочкиОтправления
|ЛЕВОЕ СОЕДИНЕНИЕ втВидыТочекОтправления КАК втВидыТочекОтправления2
|ПО втЗадания.ЗаданиеНаПеревозку.АдресПолучения.ВидАдреса = втВидыТочекОтправления2.ВидТочкиОтправления
|СГРУППИРОВАТЬ ПО
|	втЗадания.ЗаданиеНаПеревозку
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	Маршрут.Рейс КАК Рейс,
|	Маршрут.СтрокаНомер КАК НомерСтроки,
|	Маршрут.Адрес КАК Адрес,
|	Маршрут.ВремяРабот КАК ВремяРабот,
|	Маршрут.ВремяОжиданияВТочке КАК ВремяОжидания,
|	Маршрут.ВремяОжиданияВТочкеПослеРабот КАК ВремяОжиданияПослеРабот
|ПОМЕСТИТЬ втМаршрут05
|ИЗ
|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
|			,
|			Рейс В
|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
|					втРейсы15.Рейс КАК Рейс
|				ИЗ
|					втРейсы15 КАК втРейсы15)) КАК Маршрут
|ГДЕ
|	НЕ Маршрут.Пройдена И НЕ Маршрут.Отменена
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втМаршрут.Рейс КАК Рейс,
|	втМаршрут.НомерСтроки КАК НомерСтроки,
|	втМаршрут.Адрес КАК НачальнаяТочка,
|	втМаршрутСлед.Адрес КАК КонечнаяТочка
|ПОМЕСТИТЬ втМаршрут10
|ИЗ
|	втМаршрут05 КАК втМаршрут
|		ЛЕВОЕ СОЕДИНЕНИЕ втМаршрут05 КАК втМаршрутСлед
|		ПО втМаршрут.Рейс = втМаршрутСлед.Рейс
|			И (втМаршрут.НомерСтроки + 1 = втМаршрутСлед.НомерСтроки)
|;
|// 820т17аипв
|ВЫБРАТЬ
|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
|	МАКСИМУМ(ЗаданиеОсновноеГрузовыеМеста.Ссылка) КАК ОсновноеЗадание
|ПОМЕСТИТЬ ЗависимыеЗадания
|ИЗ
|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
|		ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
|		ПО ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза
|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.НомерВЦепиПоставок)
|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза)
|			И (ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе)
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК ЗаданияОсновные
|		ПО ЗаданиеОсновноеГрузовыеМеста.Ссылка = ЗаданияОсновные.ЗаданиеНаПеревозку
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК ЗаданияЗависимые
|		ПО ЗаданиеЗависимоеГрузовыеМеста.Ссылка = ЗаданияЗависимые.ЗаданиеНаПеревозку
|ГДЕ
|	ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки = &ВидПеревозки
|	И ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ВидПеревозки = &ВидПеревозки
|СГРУППИРОВАТЬ ПО
|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка
|;
|ВЫБРАТЬ
|	упДанныеТрекеров.ТранспортноеСредство КАК Транспорт,
|	упДанныеТрекеров.Рейс КАК Рейс,
|	упДанныеТрекеров.Широта КАК Широта,
|	упДанныеТрекеров.Долгота КАК Долгота,
|	упДанныеТрекеров.Период КАК Период,
|	упДанныеТрекеров.ПредставлениеКоординаты
|ИЗ
|	РегистрСведений.упДанныеТрекеров.СрезПоследних(, ТранспортноеСредство В (ВЫБРАТЬ ТранспортноеСредство ИЗ втТранспортныеСредства10)) КАК упДанныеТрекеров
|ГДЕ &РежимКорректировки
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРейсы.Рейс КАК Рейс,
|	втРейсы.Перевозчик КАК Перевозчик,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.Перевозчик) КАК ПеревозчикПредставление,
|	втРейсы.ТипТС КАК ТипТС,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТипТС) КАК ТипТСПредставление,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
|	1 КАК Количество,
|	втРейсы.Расстояние КАК Расстояние,
|	втРейсы.Время КАК Время,
|	втРейсы.ВремяВПути КАК ВремяВПути,
|	втМаршрут.ВремяРабот КАК ВремяРабот,
|	втМаршрут.ВремяОжидания КАК ВремяОжидания,
|	втМаршрут.ВремяОжиданияПослеРабот КАК ВремяОжиданияПослеРабот,
|	втРейсы.Масса КАК Масса,
|	втРейсы.Объем КАК Объем,
|	втРейсы.КоличествоМест КАК КоличествоМест,
|	втРейсы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
|	втРейсы.МассаМакс КАК МассаМакс,
|	втРейсы.ОбъемМакс КАК ОбъемМакс,
|	втРейсы.КоличествоМестМакс КАК КоличествоМестМакс,
|	втРейсы.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс,
|	втРейсы.Порядок КАК Порядок,
|	втРейсы.ИндексКартинки КАК ИндексКартинки,
|	втРейсы.СтатусРейса КАК СтатусРейса,
|	ВЫБОР
|		КОГДА втРейсы.СтатусРейса = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению)
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК ДоступноИзменениеМаршрута,
|	втРейсы.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения,
|	втРейсы.ПлановоеОкончаниеВыполнения КАК ПлановоеОкончаниеВыполнения,
|	втРейсы.ПринятоКУчету КАК ПринятоКУчету,
|	втРейсы.СобственноеТранспортноеСредство КАК СобственноеТранспортноеСредство,
|	втРейсы.ПредставлениеТС КАК ТранспортноеСредствоПредставление,
|	втРейсы.Активно КАК Активно,
|	втРейсы.СостояниеТС КАК СостояниеТС,
|	втРейсы.ЗонаПогрузки,
|	втРейсы.ЗонаРазгрузки
|ИЗ
|	втРейсы15 КАК втРейсы
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
|			втМаршрут.Рейс КАК Рейс,
|			СУММА(втМаршрут.ВремяРабот) КАК ВремяРабот,
|			СУММА(втМаршрут.ВремяОжидания) КАК ВремяОжидания,
|			СУММА(втМаршрут.ВремяОжиданияПослеРабот) КАК ВремяОжиданияПослеРабот
|		ИЗ
|			втМаршрут05 КАК втМаршрут
|		
|		СГРУППИРОВАТЬ ПО
|			втМаршрут.Рейс) КАК втМаршрут
|		ПО втРейсы.Рейс = втМаршрут.Рейс
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	NULL,
|	втТранспортныеСредства.Перевозчик,
|	ПРЕДСТАВЛЕНИЕ(втТранспортныеСредства.Перевозчик),
|	втТранспортныеСредства.ТипТС,
|	ПРЕДСТАВЛЕНИЕ(втТранспортныеСредства.ТипТС),
|	втТранспортныеСредства.ТранспортноеСредство,
|	втТранспортныеСредства.Количество - ЕСТЬNULL(втКоличествоТСВРейсах.Количество, 0),
|	NULL,
|	0,
|	0,
|	0,
|	0,
|	0,
|	0,
|	0,
|	0,
|	0,
|	втТранспортныеСредства.МассаМакс,
|	втТранспортныеСредства.ОбъемМакс,
|	втТранспортныеСредства.КоличествоМестМакс,
|	втТранспортныеСредства.КоличествоЗагрузочныхМетровМакс,
|	ВЫБОР
|		КОГДА втТранспортныеСредства.Перевозчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
|			ТОГДА 2
|		КОГДА втТранспортныеСредства.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ,
|	99,
|	NULL,
|	ЛОЖЬ,
|	NULL,
|	NULL,
|	втТранспортныеСредства.ПринятоКУчету,
|	втТранспортныеСредства.СобственноеТранспортноеСредство,
|	втТранспортныеСредства.ПредставлениеТС,
|	втТранспортныеСредства.Активно,
|	втТранспортныеСредства.СостояниеТС,
|	NULL,
|	NULL
|ИЗ
|	втТранспортныеСредства10 КАК втТранспортныеСредства
|		ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТСВРейсах КАК втКоличествоТСВРейсах
|		ПО втТранспортныеСредства.Перевозчик = втКоличествоТСВРейсах.Перевозчик
|			И втТранспортныеСредства.ТипТС = втКоличествоТСВРейсах.ТипТС
|			И втТранспортныеСредства.ТранспортноеСредство = втКоличествоТСВРейсах.ТранспортноеСредство
|ГДЕ
|	втТранспортныеСредства.Количество - ЕСТЬNULL(втКоличествоТСВРейсах.Количество, 0) > 0
|
|УПОРЯДОЧИТЬ ПО
|	втРейсы.Порядок,
|	Перевозчик,
|	втРейсы.ТипТС,
|	втРейсы.ТранспортноеСредство,
|	втРейсы.ИндексКартинки
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	втЗадания.Рейс КАК Рейс,
|	втЗадания.Адрес КАК Адрес,
|	втЗадания.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
|	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЗадания.ГрузовоеМесто) КАК ГрузовоеМестоПредставление,
|	втЗадания.КоличествоГрузов КАК КоличествоГрузов,
|	втЗадания.НомерЗадания КАК НомерЗадания,
|	МАКСИМУМ(ЭлементыМаршрутов.НомерСтроки) КАК ПорядокОбъезда,
|	втЗадания.КоличествоМест КАК КоличествоМест,
|	втЗадания.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
|	втЗадания.Объем КАК Объем,
|	втЗадания.Масса КАК Масса,
|	втЗадания.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
|	МИНИМУМ(ВЫБОР
|			КОГДА Работы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
|					И Работы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
|				ТОГДА 1
|			КОГДА Работы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
|					И ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, Работы.ВремяРабот * 3600 - 60) <= Работы.ВременноеОкноОкончание
|				ТОГДА 1
|			КОГДА Работы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
|					И ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, 60) >= Работы.ВременноеОкноНачало
|				ТОГДА 1
|			КОГДА ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, 60) >= Работы.ВременноеОкноНачало
|					И ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, Работы.ВремяРабот * 3600 - 60) <= Работы.ВременноеОкноОкончание
|				ТОГДА 1
|			ИНАЧЕ 0
|	КОНЕЦ) КАК ПопадаетВоВременноеОкно,
|	ЗависимыеЗадания.ОсновноеЗадание КАК ПредыдущееЗадание,
|	ВЫБОР КОГДА ИСТИНА
|			И втЗадания.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
|			И НЕ втПоказателиАдресов.ОтправлениеДепо И втПоказателиАдресов.ПолучениеДепо
|		ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ ЭтоВозвратГруза,
|	втЗадания.ЗаданиеНаПеревозку.АдресОтправления КАК АдресОтправления
|ИЗ
|	втЗадания КАК втЗадания
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
|				,
|				Рейс В
|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
|						втРейсы15.Рейс КАК Рейс
|					ИЗ
|						втРейсы15 КАК втРейсы15)) КАК Работы
|		ПО втЗадания.Рейс = Работы.Рейс
|			И втЗадания.ЗаданиеНаПеревозку = Работы.Задание
|			И (втЗадания.ГрузовоеМесто В (Работы.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)))
|		ЛЕВОЕ СОЕДИНЕНИЕ ЗависимыеЗадания КАК ЗависимыеЗадания
|		ПО втЗадания.ЗаданиеНаПеревозку = ЗависимыеЗадания.ЗависимоеЗадание
|		ЛЕВОЕ СОЕДИНЕНИЕ втМаршрут10 КАК ЭлементыМаршрутов
|		ПО (ЭлементыМаршрутов.КонечнаяТочка = втЗадания.Адрес)
|			И (ЭлементыМаршрутов.Рейс = втЗадания.Рейс)
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоказателиАдресов КАК втПоказателиАдресов
|		ПО втЗадания.ЗаданиеНаПеревозку = втПоказателиАдресов.ЗаданиеНаПеревозку
|
|СГРУППИРОВАТЬ ПО
|	ЗависимыеЗадания.ОсновноеЗадание,
|	втЗадания.Рейс,
|	втЗадания.Адрес,
|	втЗадания.ЗаданиеНаПеревозку,
|	втЗадания.ГрузовоеМесто,
|	втЗадания.КоличествоГрузов,
|	втЗадания.НомерЗадания,
|	втЗадания.КоличествоМест,
|	втЗадания.КоличествоЗагрузочныхМетров,
|	втЗадания.Объем,
|	втЗадания.Масса,
|	втЗадания.ОценочнаяСтоимость,
|ВЫБОР КОГДА ИСТИНА
|			И втЗадания.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
|			И НЕ втПоказателиАдресов.ОтправлениеДепо И втПоказателиАдресов.ПолучениеДепо
|		ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втМаршрут.Рейс КАК Рейс,
|	втМаршрут.НомерСтроки КАК НомерСтроки,
|	втМаршрут.НачальнаяТочка КАК Адрес,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМаршрут.НачальнаяТочка) КАК ПредставлениеАдреса,
|	втМаршрут.НачальнаяТочка.Широта КАК Широта,
|	втМаршрут.НачальнаяТочка.Долгота КАК Долгота,
|	втМаршрут.КонечнаяТочка.Широта КАК ШиротаКонечная,
|	втМаршрут.КонечнаяТочка.Долгота КАК ДолготаКонечная,
|	ЕСТЬNULL(упРасстоянияМеждуТочками.Путь, """") КАК Путь,
|	ВЫБОР
|		КОГДА втМаршрут.НачальнаяТочка.Широта = 0
|				И втМаршрут.НачальнаяТочка.Долгота = 0
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК Геокодирован
|ИЗ
|	втМаршрут10 КАК втМаршрут
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
|		ПО втМаршрут.НачальнаяТочка.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
|			И втМаршрут.НачальнаяТочка.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
|			И втМаршрут.КонечнаяТочка.Широта = упРасстоянияМеждуТочками.КонечнаяШирота
|			И втМаршрут.КонечнаяТочка.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота
|			И втМаршрут.Рейс.ОграничениеМаршрутизатора = упРасстоянияМеждуТочками.Ограничение
|";
	
	Запрос.УстановитьПараметр("ТранспортныеСредства", тзТранспортныеСредства);
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("РежимКорректировки", РежимКорректировки);
	Запрос.УстановитьПараметр("СтатусыРейсов", СтатусыРейсов());
	Результат = Запрос.ВыполнитьПакет();
	
	КоличествоТаблиц = Результат.Количество();
	ТекущиеКоординаты = Результат[КоличествоТаблиц - 4].Выгрузить();
	тзРейсы = Результат[КоличествоТаблиц - 3].Выгрузить();
	тзЗадания = Результат[КоличествоТаблиц - 2].Выгрузить();
	тзМаршруты = Результат[КоличествоТаблиц - 1].Выгрузить();
	МаршрутыРейсов.Очистить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(тзМаршруты, МаршрутыРейсов);
	
	Если НЕ РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗадания.Свернуть("Рейс, Адрес, ЗаданиеНаПеревозку, НомерЗадания, ПорядокОбъезда", "КоличествоМест, КоличествоЗагрузочныхМетров, Объем, Масса, ОценочнаяСтоимость, ПопадаетВоВременноеОкно");
	КонецЕсли; 
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	сткПоиска = Новый Структура;
	сткПоиска.Вставить("Перевозчик");
	сткПоиска.Вставить("ТипТС");
	сткПоиска.Вставить("ТранспортноеСредство");
	
	Дерево = РеквизитФормыВЗначение("ДеревоРейсы");
	ПараметрыКарты = Неопределено;
	КоличествоВидимых = ?(ПараметрыПланирования.КоличествоВидимыхРейсов > 0, ПараметрыПланирования.КоличествоВидимыхРейсов, 1000);
	Для каждого СтрокаРейс Из тзРейсы Цикл
		Если Не ЗначениеЗаполнено(СтрокаРейс.Рейс) И РежимКорректировки Тогда 
			Продолжить;
		КонецЕсли; 
		НоваяСтрокаРейс = Дерево.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРейс, СтрокаРейс);
		
		ПредставлениеСтроки = ПолучитьПредставлениеСтрокиДереваРейсов(СтрокаРейс);		
		НоваяСтрокаРейс.Представление = НСтр(ПредставлениеСтроки, КодЯзыка);
		НоваяСтрокаРейс.Геокодирован = Истина;
		НоваяСтрокаРейс.Активно = СтрокаРейс.Активно;	
		НоваяСтрокаРейс.РазрешитьАвтопланирование = СтрокаРейс.Активно;	
		НоваяСтрокаРейс.ПопадаетВоВременноеОкно = Истина;
		НоваяСтрокаРейс.Уровень = 0;
		ВосстановитьСохраняемыеСвойстваРейса(НоваяСтрокаРейс, СнимокСохраняемыхСвойствРейсов);
		Если ЗначениеЗаполнено(НоваяСтрокаРейс.Рейс) Тогда
			
			НоваяСтрокаРейс.Активно = Ложь;
			НоваяСтрокаРейс.РазрешитьАвтопланирование = Ложь;
			КоличествоРейсов = КоличествоРейсов + 1;
			ИскомыеЗадания = тзЗадания.Скопировать(Новый Структура("Рейс", СтрокаРейс.Рейс));
			Если УчитыватьМассу Тогда
				НоваяСтрокаРейс.Масса = ИскомыеЗадания.Итог("Масса");
				НоваяСтрокаРейс.МассаПредставление = Строка(НоваяСтрокаРейс.Масса);
				Если НоваяСтрокаРейс.МассаМакс > 0 Тогда
					НоваяСтрокаРейс.МассаПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.Масса/НоваяСтрокаРейс.МассаМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьМассу Тогда
				//НоваяСтрокаРейс.Масса = ИскомыеЗадания.Итог("Масса");
				НоваяСтрокаРейс.Масса = СтрокаРейс.Масса;
				НоваяСтрокаРейс.МассаПредставление = Строка(НоваяСтрокаРейс.Масса);
				Если НоваяСтрокаРейс.МассаМакс > 0 Тогда
					НоваяСтрокаРейс.МассаПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.Масса/НоваяСтрокаРейс.МассаМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьОбъем Тогда
				//НоваяСтрокаРейс.Объем = ИскомыеЗадания.Итог("Объем");
				НоваяСтрокаРейс.Объем = СтрокаРейс.Объем;
				НоваяСтрокаРейс.ОбъемПредставление = Строка(НоваяСтрокаРейс.Объем);
				Если НоваяСтрокаРейс.ОбъемМакс > 0 Тогда
					НоваяСтрокаРейс.ОбъемПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.Объем/НоваяСтрокаРейс.ОбъемМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьКоличествоМест Тогда
				//НоваяСтрокаРейс.КоличествоМест = ИскомыеЗадания.Итог("КоличествоМест");
				НоваяСтрокаРейс.КоличествоМест = СтрокаРейс.КоличествоМест;
				НоваяСтрокаРейс.КоличествоМестПредставление = Строка(НоваяСтрокаРейс.КоличествоМест);
				Если НоваяСтрокаРейс.КоличествоМестМакс > 0 Тогда
					НоваяСтрокаРейс.КоличествоМестПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.КоличествоМест/НоваяСтрокаРейс.КоличествоМестМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьКоличествоЗагрузочныхМетров Тогда
				//НоваяСтрокаРейс.КоличествоЗагрузочныхМетров = ИскомыеЗадания.Итог("КоличествоЗагрузочныхМетров");
				НоваяСтрокаРейс.КоличествоЗагрузочныхМетров = СтрокаРейс.КоличествоЗагрузочныхМетров;
				НоваяСтрокаРейс.КоличествоЗагрузочныхМетровПредставление = Строка(НоваяСтрокаРейс.КоличествоЗагрузочныхМетров);
				Если НоваяСтрокаРейс.КоличествоЗагрузочныхМетровМакс > 0 Тогда
					НоваяСтрокаРейс.КоличествоЗагрузочныхМетровПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.КоличествоЗагрузочныхМетров/НоваяСтрокаРейс.КоличествоЗагрузочныхМетровМакс*100));
				КонецЕсли; 
			КонецЕсли;
			НоваяСтрокаРейс.ОценочнаяСтоимость = ИскомыеЗадания.Итог("ОценочнаяСтоимость");
			НоваяСтрокаРейс.ОценочнаяСтоимостьПредставление = Строка(НоваяСтрокаРейс.ОценочнаяСтоимость);
			
			ИскомыеЗадания.Сортировать("ПорядокОбъезда");
			Для каждого СтрокаЗадание Из ИскомыеЗадания Цикл // задания на перевозку (грузовые места)
				НоваяСтрокаЗадание = НоваяСтрокаРейс.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаЗадание, СтрокаЗадание);
				НоваяСтрокаЗадание.СтатусРейса = СтрокаРейс.СтатусРейса;
				НоваяСтрокаЗадание.ДоступноИзменениеМаршрута = СтрокаРейс.ДоступноИзменениеМаршрута;
				НоваяСтрокаЗадание.ИндексКартинки = 99; // отмена вывода картинки строки для заданий
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					НоваяСтрокаЗадание.Представление = СтрШаблон("%1 (%2)", СтрокаЗадание.ГрузовоеМестоПредставление, СтрокаЗадание.КоличествоГрузов); 
				Иначе
					НоваяСтрокаЗадание.Представление = НСтр(СтрШаблон("ru = '№ %1'", СтрокаЗадание.НомерЗадания), КодЯзыка); 
				КонецЕсли;
				
				Если УчитыватьМассу Тогда
					НоваяСтрокаЗадание.МассаПредставление = Строка(НоваяСтрокаЗадание.Масса);
				КонецЕсли;
				Если УчитыватьОбъем Тогда
					НоваяСтрокаЗадание.ОбъемПредставление = Строка(НоваяСтрокаЗадание.Объем);
				КонецЕсли;
				Если УчитыватьКоличествоМест Тогда
					НоваяСтрокаЗадание.КоличествоМестПредставление = Строка(НоваяСтрокаЗадание.КоличествоМест);
				КонецЕсли;
				Если УчитыватьКоличествоЗагрузочныхМетров Тогда
					НоваяСтрокаЗадание.КоличествоЗагрузочныхМетровПредставление = Строка(НоваяСтрокаЗадание.КоличествоЗагрузочныхМетров);
				КонецЕсли;
				НоваяСтрокаЗадание.ОценочнаяСтоимостьПредставление = Строка(НоваяСтрокаЗадание.ОценочнаяСтоимость);
				
				НоваяСтрокаЗадание.Уровень = 1;
				
				Если НоваяСтрокаРейс.ПопадаетВоВременноеОкно И НЕ СтрокаЗадание.ПопадаетВоВременноеОкно Тогда
					НоваяСтрокаРейс.ПопадаетВоВременноеОкно = Ложь;
				КонецЕсли;
				
				Если РежимФормированияКонтейнеров Тогда
					НоваяСтрокаРейс.АдресПолучения = СтрокаЗадание.Адрес;
				КонецЕсли; 
			КонецЦикла; 
			
			тзМаршрутРейса = тзМаршруты.Скопировать(Новый Структура("Рейс", СтрокаРейс.Рейс));
			тзМаршрутРейса.Сортировать("НомерСтроки");
			СтрокаТекущихКоординат = ТекущиеКоординаты.Найти(СтрокаРейс.Рейс, "Рейс");
			Если СтрокаТекущихКоординат <> Неопределено Тогда
				НоваяСтрокаРейс.ТекущаяШирота = СтрокаТекущихКоординат.Широта;
				НоваяСтрокаРейс.ТекущаяДолгота = СтрокаТекущихКоординат.Долгота;
				СтрокаКоординатВМаршруте = тзМаршрутРейса.Вставить(0);
				СтрокаКоординатВМаршруте.НомерСтроки = 0;
				СтрокаКоординатВМаршруте.Геокодирован = Истина;
				ЗаполнитьЗначенияСвойств(СтрокаКоординатВМаршруте, СтрокаТекущихКоординат); 
				СтрокаКоординатВМаршруте.ШиротаКонечная = тзМаршрутРейса[1].Широта;
				СтрокаКоординатВМаршруте.ДолготаКонечная = тзМаршрутРейса[1].Долгота;
				НоваяСтрокаРейс.ДатаТекущихКоординат = СтрокаТекущихКоординат.Период;
				//// Долго выполняется, поэтому отключил вычисление маршрута
				//МаршрутОтТекущейТочки = упМаршрутизация.ПолучитьРасстояниеМеждуКоординатамиПоТипуКарты(ПараметрыКарты, СтрокаТекущихКоординат.Широта, СтрокаТекущихКоординат.Долгота,
				//	СтрокаКоординатВМаршруте.ШиротаКонечная, СтрокаКоординатВМаршруте.ДолготаКонечная);
				//СтрокаКоординатВМаршруте.Путь = МаршрутОтТекущейТочки.Путь;
			КонецЕсли; 
			КоординатыЛиний = "";
			
			НоваяСтрокаРейс.КоличествоТочек = тзМаршрутРейса.Количество();
			Если НоваяСтрокаРейс.КоличествоТочек > 0 Тогда
				НачалоМаршрута = тзМаршрутРейса[0];
				НоваяСтрокаРейс.Адрес = НачалоМаршрута.Адрес;
				НоваяСтрокаРейс.ПредставлениеАдреса = НачалоМаршрута.ПредставлениеАдреса;
				Если НачалоМаршрута.Геокодирован Тогда
					текКоординаты = ПредставлениеЧисла(НачалоМаршрута.Широта) + ";" + ПредставлениеЧисла(НачалоМаршрута.Долгота) + ";";
					сткДанныеАдреса = Новый Структура("ПредставлениеАдреса, ИндексВМассиве, Координаты", НачалоМаршрута.ПредставлениеАдреса, 0, текКоординаты);
					НоваяСтрокаРейс.АдресаОтправлений.Добавить(сткДанныеАдреса);
				Иначе	
					НоваяСтрокаРейс.Геокодирован = Ложь;
					НовыйНегеокодированныйАдрес = СписокНегеокодированныхАдресов.Добавить();
					НовыйНегеокодированныйАдрес.Объект = НачалоМаршрута.Адрес;
				КонецЕсли;
			КонецЕсли;
			
			//СтрокаМаршрута = МаршрутыРейсов.Добавить();
			Если тзМаршрутРейса.Количество() < 2 Тогда
				НоваяСтрокаРейс.Геокодирован = Ложь;
				НоваяСтрокаРейс.Активно      = Ложь;
			Иначе
				Для каждого ТочкаМаршрута Из тзМаршрутРейса Цикл
					ИскомыеСтроки = НоваяСтрокаРейс.Строки.НайтиСтроки(Новый Структура("Адрес", ТочкаМаршрута.Адрес));
					Если ТочкаМаршрута.Геокодирован Тогда
						Если НоваяСтрокаРейс.Геокодирован Тогда 
							текКоординаты = ПредставлениеЧисла(ТочкаМаршрута.Широта) + ";" + ПредставлениеЧисла(ТочкаМаршрута.Долгота) + ";";
						КонецЕсли;
					Иначе
						НовыйНегеокодированныйАдрес = СписокНегеокодированныхАдресов.Добавить();
						НовыйНегеокодированныйАдрес.Объект = ТочкаМаршрута.Адрес;	
						НоваяСтрокаРейс.Геокодирован = Ложь;                                             
					КонецЕсли;
					Если ИскомыеСтроки.Количество() Тогда
						Для каждого НайденнаяСтрока Из ИскомыеСтроки Цикл
							//НайденнаяСтрока.КоординатыТочек = текКоординаты;
							НайденнаяСтрока.Пометка = Истина;
							Если ТочкаМаршрута.Геокодирован И НоваяСтрокаРейс.Геокодирован Тогда
								НайденнаяСтрока.КоординатыТочек = текКоординаты;
							КонецЕсли;
							НайденнаяСтрока.ПредставлениеАдреса = ТочкаМаршрута.ПредставлениеАдреса;
						КонецЦикла; 
					Иначе
						сткДанныеАдреса = Новый Структура("ПредставлениеАдреса, ИндексВМассиве, Координаты", ТочкаМаршрута.ПредставлениеАдреса, 0, "");
						Если ТочкаМаршрута.Геокодирован Тогда
							сткДанныеАдреса.Координаты = текКоординаты;
						КонецЕсли;
						НоваяСтрокаРейс.АдресаОтправлений.Добавить(сткДанныеАдреса);
					КонецЕсли;
					
					Если НоваяСтрокаРейс.Геокодирован Тогда
						Если ЗначениеЗаполнено(ТочкаМаршрута.Путь) Тогда
							КоординатыЛиний = КоординатыЛиний + ТочкаМаршрута.Путь + "//";
						ИначеЕсли ЗначениеЗаполнено(ТочкаМаршрута.ШиротаКонечная) И ЗначениеЗаполнено(ТочкаМаршрута.ДолготаКонечная) Тогда  
							КоординатыЛиний = КоординатыЛиний + текКоординаты + ПредставлениеЧисла(ТочкаМаршрута.ШиротаКонечная) + ";" + ПредставлениеЧисла(ТочкаМаршрута.ДолготаКонечная) + ";" + "//";
						КонецЕсли;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;	
			
			Для каждого текЗадание Из НоваяСтрокаРейс.Строки Цикл
				Если Не ЗначениеЗаполнено(текЗадание.КоординатыТочек) Тогда
					НоваяСтрокаРейс.Геокодирован = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НоваяСтрокаРейс.Геокодирован Тогда
				КоординатыЛиний = Лев(КоординатыЛиний, СтрДлина(КоординатыЛиний) - 2);
				НоваяСтрокаРейс.КоординатыЛиний = КоординатыЛиний;
			КонецЕсли;
			
			НоваяСтрокаРейс.Пометка = (НоваяСтрокаРейс.Геокодирован И КоличествоВидимых > 0);
			
			Если НоваяСтрокаРейс.Пометка Тогда
			    КоличествоВидимых = КоличествоВидимых - 1;
			КонецЕсли; 
		КонецЕсли;
		ЗаполнитьПоляПредставленийСтрокиМаршрута(НоваяСтрокаРейс);
	КонецЦикла;
	ЗначениеВДанныеФормы(Дерево, ДеревоРейсы);

КонецПроцедуры

&НаСервере
Функция ПолучитьПредставлениеСтрокиДереваРейсов(СтрокаРейс)
	
	ПредставлениеСтроки =
	СтрШаблон("ru = '%1%2 / %3'", 
	?(СтрокаРейс.Количество > 1, ?(ЗначениеЗаполнено(СтрокаРейс.ТранспортноеСредство), СтрШаблон("[%1 цкл.] ", СтрокаРейс.Количество), СтрШаблон("[%1 ед.] ", СтрокаРейс.Количество)), ""),
	?(ЗначениеЗаполнено(СтрокаРейс.ТранспортноеСредство), СтрокаРейс.ТранспортноеСредствоПредставление, СтрокаРейс.ТипТСПредставление),
	?(ЗначениеЗаполнено(СтрокаРейс.Перевозчик), СтрокаРейс.ПеревозчикПредставление, ?(РежимФормированияКонтейнеров, НСтр("ru = 'Вирт. владелец'"), НСтр("ru = 'Вирт. перевозчик'"))));
	Возврат ПредставлениеСтроки;

КонецФункции

&НаСервере
Процедура ЗаполнитьПоляПредставленийСтрокиМаршрута(Знач НоваяСтрокаРейс)
	
	НоваяСтрокаРейс.РасстояниеПредставление    = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(НоваяСтрокаРейс.Расстояние);
	НоваяСтрокаРейс.ВремяПредставление         = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(НоваяСтрокаРейс.Время*3600);
	НоваяСтрокаРейс.ВремяВПутиПредставление    = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(НоваяСтрокаРейс.ВремяВПути*3600);
	НоваяСтрокаРейс.ВремяОжиданияПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(НоваяСтрокаРейс.ВремяОжидания*3600);
	НоваяСтрокаРейс.ВремяОжиданияПослеРаботПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(НоваяСтрокаРейс.ВремяОжиданияПослеРабот*3600);
	НоваяСтрокаРейс.ВремяРаботПредставление    = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(НоваяСтрокаРейс.ВремяРабот*3600);

КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраняемыеСвойстваЗадания(Знач НоваяСтрока, Знач СнимокСохраняемыхСвойств)
	
	Если Ложь Тогда // Для контекстной подсказки
	    СнимокСохраняемыхСвойств = Новый ТаблицаЗначений;
	КонецЕсли;
	КлючПоискаВСнимке = Новый Структура("ЗаданиеНаПеревозку");
	ЗаполнитьЗначенияСвойств(КлючПоискаВСнимке, НоваяСтрока); 
	СтрокиСнимка = СнимокСохраняемыхСвойств.НайтиСтроки(КлючПоискаВСнимке);
	Если СтрокиСнимка.Количество() > 0 Тогда
		СтрокаСнимка = СтрокиСнимка[0];
		Если Не СтрокаСнимка.Активно Тогда
			НоваяСтрока.Активно = Ложь;
		КонецЕсли; 
		Если Не СтрокаСнимка.Пометка Тогда
			НоваяСтрока.Пометка = Ложь;
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраняемыеСвойстваРейса(Знач НоваяСтрока, Знач СнимокСохраняемыхСвойств)
	
	Если Ложь Тогда // Для контекстной подсказки
	    СнимокСохраняемыхСвойств = Новый ТаблицаЗначений;
	КонецЕсли;
	КлючПоискаВСнимке = Новый Структура("ТипТС, ТранспортноеСредство, Рейс");
	ЗаполнитьЗначенияСвойств(КлючПоискаВСнимке, НоваяСтрока); 
	СтрокиСнимка = СнимокСохраняемыхСвойств.НайтиСтроки(КлючПоискаВСнимке);
	Если СтрокиСнимка.Количество() > 0 Тогда
		СтрокаСнимка = СтрокиСнимка[0];
		Если Не СтрокаСнимка.Активно Тогда
			НоваяСтрока.Активно = Ложь;
		КонецЕсли; 
		Если Не СтрокаСнимка.Пометка Тогда
			НоваяСтрока.Пометка = Ложь;
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьВетвиДереваРейсовНаСервере(МассивОбъектов)
	
	тзРейсыДляОбновления = Новый ТаблицаЗначений;
	тзРейсыДляОбновления.Колонки.Добавить("Рейс");
	тзРейсыДляОбновления.Колонки.Добавить("ИндексСтроки");
	тзРейсыДляОбновления.Колонки.Добавить("НомерЦветовойСхемы", Новый ОписаниеТипов("Число"));
	
	дзРейсы = РеквизитФормыВЗначение("ДеревоРейсы");
	Если Ложь Тогда // Для контекстной подсказки
	    дзРейсы = Новый ДеревоЗначений;
	КонецЕсли;
	Для каждого ОбъектМассива Из МассивОбъектов Цикл
		ИскомыеСтроки = дзРейсы.Строки.НайтиСтроки(Новый Структура(ИмяРеквизита,ОбъектМассива), Истина);
		Для каждого НайденнаяСтрока Из ИскомыеСтроки Цикл
			текРейс = НайденнаяСтрока.Родитель;
			НоваяСтрока = тзРейсыДляОбновления.Добавить();
			НоваяСтрока.Рейс         = текРейс.Рейс; 
			НоваяСтрока.ИндексСтроки = дзРейсы.Строки.Индекс(текРейс); 
			НоваяСтрока.НомерЦветовойСхемы = текРейс.НомерЦветовойСхемы; 
		КонецЦикла; 
	КонецЦикла;
	
	тзРейсыДляОбновления.Свернуть("Рейс, НомерЦветовойСхемы, ИндексСтроки");
	тзРейсыДляОбновления.Сортировать("ИндексСтроки");
	Если СписокИндексов.Количество() Тогда
		ИндексРедактируемогоРейса = СписокИндексов[0].Значение;
	Иначе
		ИндексРедактируемогоРейса = -1;
	КонецЕсли; 
	
	Смещение = 0; // так как виртуальные средства одного типа ТС и перевозчика храняться в свернутом виде, возможны случаи удаления строк без добавления новых
	Для каждого текРейс Из тзРейсыДляОбновления Цикл
		сткДополнительныеРеквизиты = Новый Структура;
		сткДополнительныеРеквизиты.Вставить("НомерЦветовойСхемы", текРейс.НомерЦветовойСхемы); 
		сткДополнительныеРеквизиты.Вставить("Смещение", 0);
		ОбновитьВетвьДереваРейсовНаСервере(текРейс.Рейс, текРейс.ИндексСтроки + Смещение, сткДополнительныеРеквизиты);
		Смещение = Смещение + сткДополнительныеРеквизиты.Смещение;
		Если ИндексРедактируемогоРейса >= 0 И текРейс.ИндексСтроки < ИндексРедактируемогоРейса Тогда
			ИндексРедактируемогоРейса = ИндексРедактируемогоРейса + Смещение;
		КонецЕсли;
	КонецЦикла; 
	Если ИндексРедактируемогоРейса >= 0 Тогда
		СписокИндексов[0].Значение = ИндексРедактируемогоРейса;
	КонецЕсли;
	
КонецПроцедуры // ОбновитьВетвиДереваРейсовНаСервере()

&НаСервере                                                     
Процедура ОбновитьВетвьДереваРейсовНаСервере(Рейс, ИндексВставки, ДополнительныеРеквизиты = Неопределено)

	Запрос = Новый Запрос;
	Запрос.Текст =
"
|ВЫБРАТЬ
|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство,
|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.Грузоподъемность, 0)) КАК Грузоподъемность,
|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.Объем, 0)) КАК Объем,
|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.КоличествоМест, 0)) КАК КоличествоМест,
|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров
|ПОМЕСТИТЬ ХарактеристикиПрицепов
|ИЗ
|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(, ) КАК упПрицепы
|		ПО упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = упПрицепы.ТранспортноеСредство
|ГДЕ
|	упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
|
|СГРУППИРОВАТЬ ПО
|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упРейс.Ссылка КАК Рейс,
|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)) КАК Перевозчик,
|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТипТС, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)) КАК ТипТС,
|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Расстояние, 0) КАК Расстояние,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Время, 0) КАК Время,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ВремяВПути, 0) КАК ВремяВПути,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеНачалоВыполнения,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеОкончаниеВыполнения,
|	упПлановыеПараметрыРейса.ЗонаПогрузки,
|	упПлановыеПараметрыРейса.ЗонаРазгрузки,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) КАК Масса,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность) = 0
|			КОНЕЦ
|			ТОГДА 999999999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность)
|			КОНЕЦ
|	КОНЕЦ КАК МассаМакс,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем) = 0
|			КОНЕЦ
|			ТОГДА 999999999999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем)
|			КОНЕЦ
|	КОНЕЦ КАК ОбъемМакс,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест) = 0
|			КОНЕЦ
|			ТОГДА 99999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест)
|			КОНЕЦ
|	КОНЕЦ КАК КоличествоМестМакс,
|	ВЫБОР
|		КОГДА ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров) = 0
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров) = 0
|			КОНЕЦ
|			ТОГДА 99999999
|		ИНАЧЕ ВЫБОР
|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров)
|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров)
|			КОНЕЦ
|	КОНЕЦ КАК КоличествоЗагрузочныхМетровМакс,
|	УпСостоянияТранспортныхСредств_СрезПоследнихТ.СостояниеТС КАК СостояниеТС
|ПОМЕСТИТЬ втРейсы05
|ИЗ
|	Документ.упРейс КАК упРейс
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
|		ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
|		ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
|		ПО (упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = упТранспортныеСредства.Ссылка)
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК упТипыТС
|		ПО (упПеревозчикПоРейсуСрезПоследних.ТипТС = упТипыТС.Ссылка)
|		ЛЕВОЕ СОЕДИНЕНИЕ ХарактеристикиПрицепов КАК ХарактеристикиПрицепов
|		ПО (упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = ХарактеристикиПрицепов.ТранспортноеСредство)
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСостоянияТранспортныхСредств.СрезПоследних(, ) КАК УпСостоянияТранспортныхСредств_СрезПоследнихТ
|		ПО (УпСостоянияТранспортныхСредств_СрезПоследнихТ.ТранспортноеСредство = упТранспортныеСредства.Ссылка)
|ГДЕ
|	упРейс.Ссылка = &Рейс
|	И упРейс.ВидПеревозки = &ВидПеревозки
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	упСтатусыРейсов.Документ КАК Рейс,
|	упСтатусыРейсов.Статус КАК Статус
|ПОМЕСТИТЬ втСтатусы
|ИЗ
|	РегистрСведений.упСтатусыРейсов.СрезПоследних(
|			,
|			Документ В
|				(ВЫБРАТЬ
|					втРейсы05.Рейс КАК Рейс
|				ИЗ
|					втРейсы05 КАК втРейсы05)) КАК упСтатусыРейсов
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРейсы.Рейс КАК Рейс,
|	втРейсы.Перевозчик КАК Перевозчик,
|	втРейсы.ТипТС КАК ТипТС,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
|	втРейсы.Расстояние КАК Расстояние,
|	втРейсы.Время КАК Время,
|	втРейсы.ВремяВПути КАК ВремяВПути,
|	втРейсы.Масса КАК Масса,
|	втРейсы.Объем КАК Объем,
|	втРейсы.КоличествоМест КАК КоличествоМест,
|	втРейсы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
|	втРейсы.МассаМакс КАК МассаМакс,
|	втРейсы.ОбъемМакс КАК ОбъемМакс,
|	втРейсы.КоличествоМестМакс КАК КоличествоМестМакс,
|	втРейсы.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс,
|	втСтатусы.Статус.Порядок КАК ИндексКартинки,
|	втСтатусы.Статус КАК СтатусРейса,
|	втРейсы.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения,
|	втРейсы.ПлановоеОкончаниеВыполнения КАК ПлановоеОкончаниеВыполнения,
|	втРейсы.СостояниеТС КАК СостояниеТС,
|	втРейсы.ЗонаПогрузки,
|	втРейсы.ЗонаРазгрузки
|ПОМЕСТИТЬ втРейсы15
|ИЗ
|	втРейсы05 КАК втРейсы
|		ЛЕВОЕ СОЕДИНЕНИЕ втСтатусы КАК втСтатусы
|		ПО втРейсы.Рейс = втСтатусы.Рейс
|			И (втСтатусы.Статус В (&СтатусыРейсов))
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРейсы.Перевозчик КАК Перевозчик,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.Перевозчик) КАК ПеревозчикПредставление,
|	втРейсы.ТипТС КАК ТипТС,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТипТС) КАК ТипТСПредставление,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТранспортноеСредство) КАК ТранспортноеСредствоПредставление,
|	втРейсы.МассаМакс КАК МассаМакс,
|	втРейсы.ОбъемМакс КАК ОбъемМакс,
|	втРейсы.КоличествоМестМакс КАК КоличествоМестМакс,
|	втРейсы.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс
|ИЗ
|	втРейсы05 КАК втРейсы
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатусы КАК втСтатусы
|		ПО втРейсы.Рейс = втСтатусы.Рейс
|			И (втСтатусы.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен))
|;
|ВЫБРАТЬ
|	втРейсы.Рейс КАК Рейс,
|	втРейсы.Перевозчик КАК Перевозчик,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.Перевозчик) КАК ПеревозчикПредставление,
|	втРейсы.ТипТС КАК ТипТС,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТипТС) КАК ТипТСПредставление,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТранспортноеСредство) КАК ТранспортноеСредствоПредставление,
|	втРейсы.Расстояние КАК Расстояние,
|	втРейсы.Время КАК Время,
|	втРейсы.ВремяВПути КАК ВремяВПути,
|	втРейсы.МассаМакс КАК МассаМакс,
|	втРейсы.ОбъемМакс КАК ОбъемМакс,
|	втРейсы.КоличествоМестМакс КАК КоличествоМестМакс,
|	втРейсы.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс,
|	втРейсы.ИндексКартинки КАК ИндексКартинки,
|	втРейсы.СтатусРейса КАК СтатусРейса,
|	втРейсы.ЗонаПогрузки,
|	втРейсы.ЗонаРазгрузки,
|	ВЫБОР
|		КОГДА втРейсы.СтатусРейса = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению)
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК ДоступноИзменениеМаршрута,
|	втРейсы.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения,
|	втРейсы.ПлановоеОкончаниеВыполнения КАК ПлановоеОкончаниеВыполнения
|ИЗ
|	втРейсы15 КАК втРейсы
|;
|
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	Задания.Рейс КАК Рейс,
|	Задания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозку,
|	Задания.Задание КАК ЗаданиеНаПеревозку,
|	Задания.ГрузовоеМесто КАК ГрузовоеМесто,
|	МАКСИМУМ(Задания.КоличествоГрузов) КАК КоличествоГрузов,
|	Задания.Задание.Номер КАК НомерЗадания,
|	Задания.Задание.АдресПолучения.Широта КАК Широта,
|	Задания.Задание.АдресПолучения.Долгота КАК Долгота,
|	Задания.Задание.АдресПолучения КАК Адрес
|ПОМЕСТИТЬ втЗаданияПодготовка
|ИЗ
|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
|		, Рейс В (ВЫБРАТЬ Рейс ИЗ втРейсы15)
|		) КАК Задания

|ГДЕ ИСТИНА
|		И Задания.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка),
|								ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка),
|								ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей), 
|								ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
|	И НЕ Задания.Отменена 
|	И НЕ Задания.Выполнена
|СГРУППИРОВАТЬ ПО
|	Задания.Рейс,
|	Задания.Задание,
|	Задания.ГрузовоеМесто
|;
|
|ВЫБРАТЬ
|	Задания.Рейс,
|	Задания.ЗаданиеНаПеревозку,
|	Задания.ГрузовоеМесто КАК ГрузовоеМесто,
|	Задания.КоличествоГрузов КАК КоличествоГрузов,
|	Задания.НомерЗадания,
|	Задания.Широта,
|	Задания.Долгота,
|	Задания.Адрес,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упПараметрыЗаданияНаПеревозку.КоличествоМест) * Задания.КоличествоГрузов КАК КоличествоМест,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров) * Задания.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упПараметрыЗаданияНаПеревозку.Объем) * Задания.КоличествоГрузов КАК Объем,
|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упПараметрыЗаданияНаПеревозку.Масса) * Задания.КоличествоГрузов КАК Масса,
|	Задания.ГрузовоеМесто.ОценочнаяСтоимость * Задания.КоличествоГрузов КАК ОценочнаяСтоимость
|ПОМЕСТИТЬ втЗадания
|ИЗ
|	втЗаданияПодготовка КАК Задания
|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку В (ВЫБРАТЬ втЗадания.ЗаявкаНаПеревозку ИЗ втЗаданияПодготовка КАК втЗадания ГДЕ НЕ втЗадания.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
|		ПО Задания.ЗаявкаНаПеревозку = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
|			И Задания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(, ЗаданиеНаПеревозку В (ВЫБРАТЬ втЗадания.ЗаданиеНаПеревозку ИЗ втЗаданияПодготовка КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
|		ПО Задания.ЗаданиеНаПеревозку = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
|			И Задания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
|ГДЕ ВЫРАЗИТЬ(&ВидПеревозки КАК Справочник.упВидыПеревозок).ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку),
|			ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса),
|			ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса))
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	Задания.Рейс КАК Рейс,
|	Задания.ЗаданиеНаПеревозку,
|	Задания.ГрузовоеМесто,
|	Задания.КоличествоГрузов КАК КоличествоГрузов,
|	Задания.НомерЗадания,
|	Задания.Широта,
|	Задания.Долгота,
|	Задания.Адрес,
|	Задания.ЗаданиеНаПеревозку.КоличествоМест,
|	Задания.ЗаданиеНаПеревозку.КоличествоЗагрузочныхМетров,
|	Задания.ЗаданиеНаПеревозку.Объем,
|	Задания.ЗаданиеНаПеревозку.Масса,
|	Задания.ЗаданиеНаПеревозку.ОценочнаяСтоимость
|ИЗ
|	втЗаданияПодготовка КАК Задания
|ГДЕ ВЫРАЗИТЬ(&ВидПеревозки КАК Справочник.упВидыПеревозок).ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВидыТочекОтправления.ВидТочкиОтправления
|ПОМЕСТИТЬ втВидыТочекОтправления
|ИЗ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
|ГДЕ
|	ВидыТочекОтправления.Ссылка = &ВидПеревозки
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втЗадания.ЗаданиеНаПеревозку,
|	МАКСИМУМ(ВЫБОР КОГДА втВидыТочекОтправления1.ВидТочкиОтправления ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ) КАК ОтправлениеДепо,
|	МАКСИМУМ(ВЫБОР КОГДА втВидыТочекОтправления2.ВидТочкиОтправления ЕСТЬ NULL ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ) КАК ПолучениеДепо
|ПОМЕСТИТЬ втПоказателиАдресов	
|ИЗ втЗадания КАК втЗадания
|ЛЕВОЕ СОЕДИНЕНИЕ втВидыТочекОтправления КАК втВидыТочекОтправления1
|ПО втЗадания.ЗаданиеНаПеревозку.АдресОтправления.ВидАдреса = втВидыТочекОтправления1.ВидТочкиОтправления
|ЛЕВОЕ СОЕДИНЕНИЕ втВидыТочекОтправления КАК втВидыТочекОтправления2
|ПО втЗадания.ЗаданиеНаПеревозку.АдресПолучения.ВидАдреса = втВидыТочекОтправления2.ВидТочкиОтправления
|СГРУППИРОВАТЬ ПО
|	втЗадания.ЗаданиеНаПеревозку
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	Маршрут.Рейс КАК Рейс,
|	Маршрут.СтрокаНомер КАК НомерСтроки,
|	Маршрут.Адрес КАК Адрес,
|	Маршрут.ВремяРабот КАК ВремяРабот,
|	Маршрут.ВремяОжиданияВТочке КАК ВремяОжидания,
|	Маршрут.ВремяОжиданияВТочкеПослеРабот КАК ВремяОжиданияПослеРабот
|ПОМЕСТИТЬ втМаршрут05
|ИЗ
|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
|			,
|			Рейс В
|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
|					втРейсы15.Рейс КАК Рейс
|				ИЗ
|					втРейсы15 КАК втРейсы15)) КАК Маршрут
|ГДЕ
|	НЕ Маршрут.Пройдена И НЕ Маршрут.Отменена
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втМаршрут.Рейс КАК Рейс,
|	втМаршрут.НомерСтроки КАК НомерСтроки,
|	втМаршрут.Адрес КАК НачальнаяТочка,
|	втМаршрутСлед.Адрес КАК КонечнаяТочка
|ПОМЕСТИТЬ втМаршрут10
|ИЗ
|	втМаршрут05 КАК втМаршрут
|		ЛЕВОЕ СОЕДИНЕНИЕ втМаршрут05 КАК втМаршрутСлед
|		ПО втМаршрут.Рейс = втМаршрутСлед.Рейс
|			И (втМаршрут.НомерСтроки + 1 = втМаршрутСлед.НомерСтроки)
|;
|// 820т17аипв
|////////////////////////////////////////////////////////////////////////////////
|// Эта временная таблица сделана для обхода неоптимального плана запроса в запросе создания таблицы ЗависимыеЗадания
|ВЫБРАТЬ
|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
|	ЗаданиеОсновноеГрузовыеМеста.Ссылка КАК ОсновноеЗадание
|ПОМЕСТИТЬ
|	ЗависимыеЗадания1
|ИЗ
|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
|		ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
|		И ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки = &ВидПеревозки
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
|		ПО ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза
|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.НомерВЦепиПоставок)
|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза)
|			И (ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе)
|			И ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ВидПеревозки = &ВидПеревозки
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК ЗаданияОсновные
|		ПО (ЗаданиеОсновноеГрузовыеМеста.Ссылка = ЗаданияОсновные.ЗаданиеНаПеревозку)
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК ЗаданияЗависимые
|		ПО (ЗаданиеЗависимоеГрузовыеМеста.Ссылка = ЗаданияЗависимые.ЗаданиеНаПеревозку)
|;
| 
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ЗависимыеЗадания.ЗависимоеЗадание,
|	МАКСИМУМ(ЗависимыеЗадания.ОсновноеЗадание) КАК ОсновноеЗадание
|ПОМЕСТИТЬ ЗависимыеЗадания
|ИЗ ЗависимыеЗадания1  КАК ЗависимыеЗадания
|
|СГРУППИРОВАТЬ ПО
|	ЗависимыеЗадания.ЗависимоеЗадание
|;
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРейсы.Перевозчик КАК Перевозчик,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.Перевозчик) КАК ПеревозчикПредставление,
|	втРейсы.ТипТС КАК ТипТС,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТипТС) КАК ТипТСПредставление,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТранспортноеСредство) КАК ТранспортноеСредствоПредставление,
|	втРейсы.МассаМакс КАК МассаМакс,
|	втРейсы.ОбъемМакс КАК ОбъемМакс,
|	втРейсы.КоличествоМестМакс КАК КоличествоМестМакс,
|	втРейсы.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс,
|	втРейсы.СостояниеТС КАК СостояниеТС,
|	ВЫБОР
|		КОГДА втРейсы.ТранспортноеСредство.СобственноеТранспортноеСредство = ИСТИНА
|				И НЕ ЕСТЬNULL(УпУчетныеПараметрыТранспортныхСредств_СрезПоследних.ПринятоКУчету, ЛОЖЬ)
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК Активно
|ИЗ
|	втРейсы05 КАК втРейсы
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатусы КАК втСтатусы
|		ПО втРейсы.Рейс = втСтатусы.Рейс
|			И (втСтатусы.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен))
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(, ) КАК УпУчетныеПараметрыТранспортныхСредств_СрезПоследних
|		ПО (УпУчетныеПараметрыТранспортныхСредств_СрезПоследних.ТранспортноеСредство = втРейсы.ТранспортноеСредство)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРейсы.Рейс КАК Рейс,
|	втРейсы.Перевозчик КАК Перевозчик,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.Перевозчик) КАК ПеревозчикПредставление,
|	втРейсы.ТипТС КАК ТипТС,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТипТС) КАК ТипТСПредставление,
|	втРейсы.ТранспортноеСредство КАК ТранспортноеСредство,
|	ПРЕДСТАВЛЕНИЕ(втРейсы.ТранспортноеСредство) КАК ТранспортноеСредствоПредставление,
|	втРейсы.Расстояние КАК Расстояние,
|	втРейсы.Время КАК Время,
|	втРейсы.ВремяВПути КАК ВремяВПути,
|	втМаршрут.ВремяРабот КАК ВремяРабот,
|	втМаршрут.ВремяОжидания КАК ВремяОжидания,
|	втМаршрут.ВремяОжиданияПослеРабот КАК ВремяОжиданияПослеРабот,
|	втРейсы.Масса КАК Масса,
|	втРейсы.Объем КАК Объем,
|	втРейсы.КоличествоМест КАК КоличествоМест,
|	втРейсы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
|	втРейсы.МассаМакс КАК МассаМакс,
|	втРейсы.ОбъемМакс КАК ОбъемМакс,
|	втРейсы.КоличествоМестМакс КАК КоличествоМестМакс,
|	втРейсы.КоличествоЗагрузочныхМетровМакс КАК КоличествоЗагрузочныхМетровМакс,
|	втРейсы.ИндексКартинки КАК ИндексКартинки,
|	втРейсы.СтатусРейса КАК СтатусРейса,
|	ВЫБОР
|		КОГДА втРейсы.СтатусРейса = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КВыполнению)
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК ДоступноИзменениеМаршрута,
|	втРейсы.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения,
|	втРейсы.ПлановоеОкончаниеВыполнения КАК ПлановоеОкончаниеВыполнения,
|	втРейсы.СостояниеТС КАК СостояниеТС,
|	втРейсы.ЗонаПогрузки,
|	втРейсы.ЗонаРазгрузки,
|	1 КАК Количество
|ИЗ
|	втРейсы15 КАК втРейсы
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
|			втМаршрут.Рейс КАК Рейс,
|			СУММА(втМаршрут.ВремяРабот) КАК ВремяРабот,
|			СУММА(втМаршрут.ВремяОжидания) КАК ВремяОжидания,
|			СУММА(втМаршрут.ВремяОжиданияПослеРабот) КАК ВремяОжиданияПослеРабот
|		ИЗ
|			втМаршрут05 КАК втМаршрут
|		
|		СГРУППИРОВАТЬ ПО
|			втМаршрут.Рейс) КАК втМаршрут
|		ПО втРейсы.Рейс = втМаршрут.Рейс
|;//////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втЗадания.Рейс КАК Рейс,
|	втЗадания.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
|	втЗадания.Адрес КАК Адрес,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЗадания.Адрес) КАК ПредставлениеАдреса,
|	втЗадания.Широта КАК Широта,
|	втЗадания.Долгота КАК Долгота,
|	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЗадания.ГрузовоеМесто) КАК ГрузовоеМестоПредставление,
|	втЗадания.КоличествоГрузов КАК КоличествоГрузов,
|	втЗадания.НомерЗадания КАК НомерЗадания,
|	МАКСИМУМ(ЭлементыМаршрутов.НомерСтроки) КАК ПорядокОбъезда,
|	втЗадания.КоличествоМест КАК КоличествоМест,
|	втЗадания.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
|	втЗадания.Объем КАК Объем,
|	втЗадания.Масса КАК Масса,
|	втЗадания.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
|	МИНИМУМ(ВЫБОР
|		КОГДА Работы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
|			И Работы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
|			ТОГДА 1
|		КОГДА Работы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
|			И ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, Работы.ВремяРабот * 3600 - 60) <= Работы.ВременноеОкноОкончание
|			ТОГДА 1
|		КОГДА Работы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
|			И ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, 60) >= Работы.ВременноеОкноНачало
|			ТОГДА 1
|		КОГДА ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, 60) >= Работы.ВременноеОкноНачало
|			И ДОБАВИТЬКДАТЕ(Работы.ПлановоеВремяНачалаРабот, СЕКУНДА, Работы.ВремяРабот * 3600 - 60) <= Работы.ВременноеОкноОкончание
|			ТОГДА 1
|		ИНАЧЕ 0
|	КОНЕЦ) КАК ПопадаетВоВременноеОкно,
|	ЗависимыеЗадания.ОсновноеЗадание КАК ПредыдущееЗадание,
|	ВЫБОР КОГДА ИСТИНА
|			И втЗадания.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
|			И НЕ втПоказателиАдресов.ОтправлениеДепо И втПоказателиАдресов.ПолучениеДепо
|		ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ ЭтоВозвратГруза,
|	втЗадания.ЗаданиеНаПеревозку.АдресОтправления КАК АдресОтправления
|ИЗ
|	втЗадания КАК втЗадания
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
|		,
|		Рейс В (
|			ВЫБРАТЬ РАЗЛИЧНЫЕ
|				втРейсы15.Рейс КАК Рейс
|			ИЗ
|				втРейсы15 КАК втРейсы15)) КАК Работы
|	ПО втЗадания.Рейс = Работы.Рейс
|		И втЗадания.ЗаданиеНаПеревозку = Работы.Задание
|		И втЗадания.ГрузовоеМесто В (Работы.ГрузовоеМесто,
|				ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
|	ЛЕВОЕ СОЕДИНЕНИЕ ЗависимыеЗадания КАК ЗависимыеЗадания
|	ПО втЗадания.ЗаданиеНаПеревозку = ЗависимыеЗадания.ЗависимоеЗадание
|	ЛЕВОЕ СОЕДИНЕНИЕ втМаршрут10 КАК ЭлементыМаршрутов
|	ПО ЭлементыМаршрутов.КонечнаяТочка = втЗадания.Адрес
|		И ЭлементыМаршрутов.Рейс = втЗадания.Рейс
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоказателиАдресов КАК втПоказателиАдресов
|		ПО втЗадания.ЗаданиеНаПеревозку = втПоказателиАдресов.ЗаданиеНаПеревозку
|
|СГРУППИРОВАТЬ ПО
|	ЗависимыеЗадания.ОсновноеЗадание,
|	втЗадания.Рейс,
|	втЗадания.ЗаданиеНаПеревозку,
|	втЗадания.Адрес,
|	втЗадания.Широта,
|	втЗадания.Долгота,
|	втЗадания.ГрузовоеМесто,
|	втЗадания.КоличествоГрузов,
|	втЗадания.НомерЗадания,
|	втЗадания.КоличествоМест,
|	втЗадания.КоличествоЗагрузочныхМетров,
|	втЗадания.Объем,
|	втЗадания.Масса,
|	втЗадания.ОценочнаяСтоимость,
|	ВЫБОР КОГДА ИСТИНА
|			И втЗадания.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)
|			И НЕ втПоказателиАдресов.ОтправлениеДепо И втПоказателиАдресов.ПолучениеДепо
|		ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ
|
|
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втМаршрут.Рейс КАК Рейс,
|	втМаршрут.НомерСтроки КАК НомерСтроки,
|	втМаршрут.НачальнаяТочка КАК Адрес,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМаршрут.НачальнаяТочка) КАК ПредставлениеАдреса,
|	втМаршрут.НачальнаяТочка.Широта КАК Широта,
|	втМаршрут.НачальнаяТочка.Долгота КАК Долгота,
|	втМаршрут.КонечнаяТочка.Широта КАК ШиротаКонечная,
|	втМаршрут.КонечнаяТочка.Долгота КАК ДолготаКонечная,
|	ЕСТЬNULL(упРасстоянияМеждуТочками.Путь, """") КАК Путь,
|	ВЫБОР
|		КОГДА втМаршрут.НачальнаяТочка.Широта = 0
|				И втМаршрут.НачальнаяТочка.Долгота = 0
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК Геокодирован
|ИЗ
|	втМаршрут10 КАК втМаршрут
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
|		ПО втМаршрут.НачальнаяТочка.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
|			И втМаршрут.НачальнаяТочка.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
|			И втМаршрут.КонечнаяТочка.Широта = упРасстоянияМеждуТочками.КонечнаяШирота
|			И втМаршрут.КонечнаяТочка.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота
|			И (ВЫРАЗИТЬ(&Рейс КАК Документ.упРейс).ОграничениеМаршрутизатора = упРасстоянияМеждуТочками.Ограничение)
|";	
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("СтатусыРейсов", СтатусыРейсов());
	Результат = Запрос.ВыполнитьПакет();
	
	КоличествоТаблиц = Результат.Количество(); 
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	тзОтмененныеРейсы = Результат[КоличествоТаблиц - 4].Выгрузить();
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	Если тзОтмененныеРейсы.Количество() > 0 Тогда
		
		СтрокаТС = тзОтмененныеРейсы[0];
		РазблокироватьДанныеДляРедактирования(Рейс, УникальныйИдентификатор);
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ЗапрещеноНепосредственноеУдалениеОбъектаРейс") = Истина Тогда
			РейсОбъект = Рейс.ПолучитьОбъект();
			РейсОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРейса.Отменен);
			РейсОбъект.Записать();
		Иначе
			мсвПараметров = Новый Массив;
			мсвПараметров.Добавить(Рейс);
			ФоновыеЗадания.Выполнить("упСервисныеФункции.УдалитьРейсНепосредственно", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Непосредственное удаление отмененного рейса'"));
			//упСервисныеФункции.УдалитьРейсНепосредственно(Рейс);
		КонецЕсли;
		УдаляемаяСтрока = ДеревоРейсы.ПолучитьЭлементы().Получить(ИндексВставки);
		ЗаполнитьЗначенияСвойств(СтрокаТС, УдаляемаяСтрока,, "Активно");
		
		ЭлементыРейсы.Удалить(ИндексВставки);
		КоличествоРейсов = КоличествоРейсов - 1;
		
		СоздатьНовуюСтроку = Истина;
		Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
			Если Не ЗначениеЗаполнено(ЭлементРейс.Рейс) И ЭлементРейс.ТранспортноеСредство = СтрокаТС.ТранспортноеСредство И ЭлементРейс.Перевозчик = СтрокаТС.Перевозчик И ЭлементРейс.ТипТС = СтрокаТС.ТипТС Тогда
				ЭлементРейс.Количество = ЭлементРейс.Количество + 1;
				ПредставлениеСтроки = ПолучитьПредставлениеСтрокиДереваРейсов(ЭлементРейс);
				ЭлементРейс.Представление = НСтр(ПредставлениеСтроки, КодЯзыка);
				СоздатьНовуюСтроку = Ложь;
				
				Если ДополнительныеРеквизиты <> Неопределено И ДополнительныеРеквизиты.Свойство("Смещение") Тогда
					ДополнительныеРеквизиты.Смещение = ДополнительныеРеквизиты.Смещение - 1;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СоздатьНовуюСтроку Тогда
			НоваяСтрокаРейс = ЭлементыРейсы.Вставить(ИндексВставки);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаРейс, СтрокаТС);
			Если СписокТранспорта.НайтиСтроки(Новый Структура("ТранспортноеСредство", СтрокаТС.ТранспортноеСредство)).Количество() = 0 Тогда 
				НоваяСтрокаРейс.ТранспортноеСредство = Неопределено;
			КонецЕсли; 
			//НоваяСтрокаРейс.Активно = УдаляемаяСтрока.Активно;
			//НоваяСтрокаРейс.РазрешитьАвтопланирование = УдаляемаяСтрока.РазрешитьАвтопланирование;
			НоваяСтрокаРейс.РазрешитьАвтопланирование = НоваяСтрокаРейс.Активно;
			НоваяСтрокаРейс.Представление = УдаляемаяСтрока.Представление;
			НоваяСтрокаРейс.ПопадаетВоВременноеОкно = Истина;
			НоваяСтрокаРейс.Уровень = 0;
			НоваяСтрокаРейс.Количество = 1;
			НоваяСтрокаРейс.ИндексКартинки = 99;
			
			Если УчитыватьМассу Тогда
				НоваяСтрокаРейс.МассаПредставление = 0;
			КонецЕсли;
			Если УчитыватьОбъем Тогда
				НоваяСтрокаРейс.ОбъемПредставление = 0;
			КонецЕсли;
			Если УчитыватьКоличествоМест Тогда
				НоваяСтрокаРейс.КоличествоМестПредставление = 0;
			КонецЕсли;
			Если УчитыватьКоличествоЗагрузочныхМетров Тогда
				НоваяСтрокаРейс.КоличествоЗагрузочныхМетровПредставление = 0;
			КонецЕсли;
			НоваяСтрокаРейс.ОценочнаяСтоимостьПредставление = 0;
			ЗаполнитьПоляПредставленийСтрокиМаршрута(НоваяСтрокаРейс);
			СписокИндексов.Добавить(ИндексВставки);
			Если ЗначениеЗаполнено(НоваяСтрокаРейс.Рейс) Тогда
				КоличествоРейсов = КоличествоРейсов + 1;
			КонецЕсли; 
		КонецЕсли;
		
	Иначе
		
		СтрокаУдалена = Ложь;
		
		СнимокСохраняемыхСвойствРейсов = СнимокСохраняемыхСвойствРейсов();
		текРедактируемаяСтрока = ЭлементыРейсы.Получить(ИндексВставки);
		Если НЕ ЗначениеЗаполнено(текРедактируемаяСтрока.Рейс) И текРедактируемаяСтрока.Количество > 1 Тогда
			текРедактируемаяСтрока.Количество = текРедактируемаяСтрока.Количество - 1;
			ПредставлениеСтроки = ПолучитьПредставлениеСтрокиДереваРейсов(текРедактируемаяСтрока);
			текРедактируемаяСтрока.Представление = НСтр(ПредставлениеСтроки, КодЯзыка);
		Иначе
			Если ЗначениеЗаполнено(текРедактируемаяСтрока.Рейс) Тогда
				КоличествоРейсов = КоличествоРейсов - 1;
			КонецЕсли; 
			ЭлементыРейсы.Удалить(ИндексВставки);
			СтрокаУдалена = Истина;
		КонецЕсли;	
		
		тзРейсы = Результат[КоличествоТаблиц - 3].Выгрузить();
		тзЗадания = Результат[КоличествоТаблиц - 2].Выгрузить();
		тзМаршруты = Результат[КоличествоТаблиц - 1].Выгрузить();
		
		Если НЕ РазбиватьЗаданияПоГрузовымМестам Тогда
			тзЗадания.Свернуть("Рейс, Адрес, ПредставлениеАдреса, ЗаданиеНаПеревозку, НомерЗадания, ПорядокОбъезда, Широта, Долгота", "ПопадаетВоВременноеОкно, КоличествоМест, КоличествоЗагрузочныхМетров, Объем, Масса, ОценочнаяСтоимость");
		КонецЕсли; 
		тзЗадания.Сортировать("ПорядокОбъезда");
		Для каждого СтрокаРейс Из тзРейсы Цикл
			
			КоличествоРейсов = КоличествоРейсов + 1;
			
			НоваяСтрокаРейс = ЭлементыРейсы.Вставить(ИндексВставки);
			
			// пересчитаем индексы всех строк, которые стоят ниже вставляемой строки
			Если Не СтрокаУдалена И СписокИндексов.Количество() > 0 Тогда
				Для каждого текСтрокаИндекса Из СписокИндексов Цикл
					Если текСтрокаИндекса.Значение > ИндексВставки Тогда
						текСтрокаИндекса.Значение = текСтрокаИндекса.Значение + 1;
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаРейс.Рейс) Тогда
				НоваяСтрокаРейс.Активно = Ложь;
				НоваяСтрокаРейс.РазрешитьАвтопланирование = Ложь;
			//Иначе	
			//	НоваяСтрокаРейс.Активно = текРедактируемаяСтрока.Активно;
			//	НоваяСтрокаРейс.РазрешитьАвтопланирование = текРедактируемаяСтрока.РазрешитьАвтопланирование;
			КонецЕсли; 
			
			НоваяСтрокаРейс.Геокодирован = Истина;
			ЗаполнитьЗначенияСвойств(НоваяСтрокаРейс, СтрокаРейс);
			ПредставлениеСтроки = ПолучитьПредставлениеСтрокиДереваРейсов(СтрокаРейс);
			НоваяСтрокаРейс.Представление = НСтр(ПредставлениеСтроки, КодЯзыка);
			НоваяСтрокаРейс.Уровень = 0;
			НоваяСтрокаРейс.ПопадаетВоВременноеОкно = Истина;
			ВосстановитьСохраняемыеСвойстваРейса(НоваяСтрокаРейс, СнимокСохраняемыхСвойствРейсов);
			
			Если УчитыватьМассу Тогда
				//НоваяСтрокаРейс.Масса = тзЗадания.Итог("Масса");
				НоваяСтрокаРейс.Масса = СтрокаРейс.Масса;
				НоваяСтрокаРейс.МассаПредставление = Строка(НоваяСтрокаРейс.Масса);
				Если НоваяСтрокаРейс.МассаМакс > 0 Тогда
					НоваяСтрокаРейс.МассаПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.Масса/НоваяСтрокаРейс.МассаМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьОбъем Тогда
				//НоваяСтрокаРейс.Объем = тзЗадания.Итог("Объем");
				НоваяСтрокаРейс.Объем = СтрокаРейс.Объем;
				НоваяСтрокаРейс.ОбъемПредставление = Строка(НоваяСтрокаРейс.Объем);
				Если НоваяСтрокаРейс.ОбъемМакс > 0 Тогда
					НоваяСтрокаРейс.ОбъемПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.Объем/НоваяСтрокаРейс.ОбъемМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьКоличествоМест Тогда
				//НоваяСтрокаРейс.КоличествоМест = тзЗадания.Итог("КоличествоМест");
				НоваяСтрокаРейс.КоличествоМест = СтрокаРейс.КоличествоМест;
				НоваяСтрокаРейс.КоличествоМестПредставление = Строка(НоваяСтрокаРейс.КоличествоМест);
				Если НоваяСтрокаРейс.КоличествоМестМакс > 0 Тогда
					НоваяСтрокаРейс.КоличествоМестПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.КоличествоМест/НоваяСтрокаРейс.КоличествоМестМакс*100));
				КонецЕсли; 
			КонецЕсли;
			Если УчитыватьКоличествоЗагрузочныхМетров Тогда
				//НоваяСтрокаРейс.КоличествоЗагрузочныхМетров = тзЗадания.Итог("КоличествоЗагрузочныхМетров");
				НоваяСтрокаРейс.КоличествоЗагрузочныхМетров = СтрокаРейс.КоличествоЗагрузочныхМетров;
				НоваяСтрокаРейс.КоличествоЗагрузочныхМетровПредставление = Строка(НоваяСтрокаРейс.КоличествоЗагрузочныхМетров);
				Если НоваяСтрокаРейс.КоличествоЗагрузочныхМетровМакс > 0 Тогда
					НоваяСтрокаРейс.КоличествоЗагрузочныхМетровПроцентЗагрузки = Строка(Окр(НоваяСтрокаРейс.КоличествоЗагрузочныхМетров/НоваяСтрокаРейс.КоличествоЗагрузочныхМетровМакс*100));
				КонецЕсли; 
			КонецЕсли;
			НоваяСтрокаРейс.ОценочнаяСтоимость = тзЗадания.Итог("ОценочнаяСтоимость");
			НоваяСтрокаРейс.ОценочнаяСтоимостьПредставление = Строка(НоваяСтрокаРейс.ОценочнаяСтоимость);
			
			мсвАдресов = Новый Массив;
			Для каждого СтрокаЗадание Из тзЗадания Цикл // задания на перевозку (грузовые места)
				мсвАдресов.Добавить(СтрокаЗадание.Адрес);
				
				НоваяСтрокаЗадание = НоваяСтрокаРейс.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаЗадание, СтрокаЗадание);
				текКоординаты = ПредставлениеЧисла(СтрокаЗадание.Широта) + ";" + ПредставлениеЧисла(СтрокаЗадание.Долгота) + ";";
				НоваяСтрокаЗадание.КоординатыТочек = текКоординаты;
				НоваяСтрокаЗадание.Пометка = Истина;

				НоваяСтрокаЗадание.СтатусРейса = СтрокаРейс.СтатусРейса;
				НоваяСтрокаЗадание.ДоступноИзменениеМаршрута = СтрокаРейс.ДоступноИзменениеМаршрута;
				НоваяСтрокаЗадание.ИндексКартинки = 99; // отмена вывода картинки строки для заданий
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					НоваяСтрокаЗадание.Представление = СтрШаблон("%1 (%2)", СтрокаЗадание.ГрузовоеМестоПредставление, СтрокаЗадание.КоличествоГрузов); 
				Иначе
					НоваяСтрокаЗадание.Представление = НСтр(СтрШаблон("ru = '№ %1'", СтрокаЗадание.НомерЗадания), КодЯзыка); 
				КонецЕсли;
				
				Если УчитыватьМассу Тогда
					НоваяСтрокаЗадание.МассаПредставление = Строка(НоваяСтрокаЗадание.Масса);
				КонецЕсли;
				Если УчитыватьОбъем Тогда
					НоваяСтрокаЗадание.ОбъемПредставление = Строка(НоваяСтрокаЗадание.Объем);
				КонецЕсли;
				Если УчитыватьКоличествоМест Тогда
					НоваяСтрокаЗадание.КоличествоМестПредставление = Строка(НоваяСтрокаЗадание.КоличествоМест);
				КонецЕсли;
				Если УчитыватьКоличествоЗагрузочныхМетров Тогда
					НоваяСтрокаЗадание.КоличествоЗагрузочныхМетровПредставление = Строка(НоваяСтрокаЗадание.КоличествоЗагрузочныхМетров);
				КонецЕсли;
				НоваяСтрокаЗадание.ОценочнаяСтоимостьПредставление = Строка(НоваяСтрокаЗадание.ОценочнаяСтоимость);
				
				НоваяСтрокаЗадание.Уровень = 1;
				
				Если НЕ СтрокаЗадание.ПопадаетВоВременноеОкно Тогда
					НоваяСтрокаРейс.ПопадаетВоВременноеОкно = Ложь;
				КонецЕсли;
				
				Если РежимФормированияКонтейнеров Тогда
					НоваяСтрокаРейс.АдресПолучения = НоваяСтрокаЗадание.Адрес;
				КонецЕсли;
			КонецЦикла; 
			
			тзМаршрутРейса = тзМаршруты.Скопировать(Новый Структура("Рейс", СтрокаРейс.Рейс));
			тзМаршрутРейса.Сортировать("НомерСтроки");
			КоординатыЛиний = "";
			НоваяСтрокаРейс.КоличествоТочек = тзМаршрутРейса.Количество();
						
			НоваяСтрокаРейс.КоличествоТочек = тзМаршрутРейса.Количество();
			Если НоваяСтрокаРейс.КоличествоТочек > 0 Тогда
				НачалоМаршрута = тзМаршрутРейса[0];
				НоваяСтрокаРейс.Адрес = НачалоМаршрута.Адрес;
				НоваяСтрокаРейс.ПредставлениеАдреса = НачалоМаршрута.ПредставлениеАдреса;
			КонецЕсли;
			
			Для каждого ТочкаМаршрута Из тзМаршрутРейса Цикл
				текКоординаты = "";
				Если ТочкаМаршрута.Геокодирован Тогда
					текКоординаты = ПредставлениеЧисла(ТочкаМаршрута.Широта) + ";" + ПредставлениеЧисла(ТочкаМаршрута.Долгота) + ";";
					
					Если ЗначениеЗаполнено(ТочкаМаршрута.Путь) Тогда
						КоординатыЛиний = КоординатыЛиний + ТочкаМаршрута.Путь + "//";
					ИначеЕсли ЗначениеЗаполнено(ТочкаМаршрута.ШиротаКонечная) И ЗначениеЗаполнено(ТочкаМаршрута.ДолготаКонечная) Тогда  
						КоординатыЛиний = КоординатыЛиний + текКоординаты + ПредставлениеЧисла(ТочкаМаршрута.ШиротаКонечная) + ";" + ПредставлениеЧисла(ТочкаМаршрута.ДолготаКонечная) + ";" + "//";
					КонецЕсли;
					
				Иначе
					НоваяСтрокаРейс.Геокодирован = Ложь;
					НовыйНегеокодированныйАдрес = СписокНегеокодированныхАдресов.Добавить();
					НовыйНегеокодированныйАдрес.Объект = ТочкаМаршрута.Адрес;
					
				КонецЕсли; 

				Если мсвАдресов.Найти(ТочкаМаршрута.Адрес) = Неопределено Тогда // заполняем адреса отправления
					сткДанныеАдреса = Новый Структура("ПредставлениеАдреса, ИндексВМассиве, Координаты", ТочкаМаршрута.ПредставлениеАдреса, 0, текКоординаты);
					НоваяСтрокаРейс.АдресаОтправлений.Добавить(сткДанныеАдреса);
				КонецЕсли;				
			КонецЦикла;
			
			ЭлементыЗадания = НоваяСтрокаРейс.ПолучитьЭлементы();
			Для Каждого текЗадания Из ЭлементыЗадания Цикл
				Если Не ЗначениеЗаполнено(текЗадания.КоординатыТочек) Тогда
					НоваяСтрокаРейс.Геокодирован = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			КоординатыЛиний = Лев(КоординатыЛиний, СтрДлина(КоординатыЛиний) - 2);
			НоваяСтрокаРейс.КоординатыЛиний = КоординатыЛиний;
			
			НоваяСтрокаРейс.Пометка = Истина;
			Если ДополнительныеРеквизиты <> Неопределено Тогда
				Если ДополнительныеРеквизиты.Свойство("НомерЦветовойСхемы") И НоваяСтрокаРейс.Геокодирован Тогда
					НоваяСтрокаРейс.НомерЦветовойСхемы = ДополнительныеРеквизиты.НомерЦветовойСхемы;
				КонецЕсли;
			КонецЕсли;
			ЗаполнитьПоляПредставленийСтрокиМаршрута(НоваяСтрокаРейс);
		КонецЦикла;
		Если СписокИндексов.НайтиПоЗначению(ИндексВставки) = Неопределено Тогда
			СписокИндексов.Добавить(ИндексВставки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбновитьВетвьДереваРейсовНаСервере

#КонецОбласти 

#Область ОбработчикиОжидания

&НаКлиенте
// Обработчик действия ожидания. Вызывается при неудачной попытке отображения карты.
//
Процедура ВыполнитьОтложенноеДействие() Экспорт 

	HTMLДокументДокументСформирован(Элементы.HTMLДокумент);

КонецПроцедуры // ВыполнитьОтложенноеДействие()

&НаКлиенте
Процедура СписокЗаданийПриАктивизацииПоляОбработчикОжидания()
	#Если НЕ ВебКлиент Тогда
	текСтрока = Элементы.СписокЗаданий.ТекущиеДанные;
	текЭлемент = Элементы.СписокЗаданий.ТекущийЭлемент;
	Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
		Если текЭлемент.Имя = "СписокЗаданийВыбрано" Тогда
			Если Ложь
				Или Не ЗначениеЗаполнено(текСтрока.ПредыдущееЗадание) 
				Или текСтрока.ПредыдущееЗадание = текСтрока.ЗаданиеНаПеревозку 
			Тогда
				ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
				Выбрано = НЕ текСтрока.Выбрано;
				
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					мсвОтправлений = Новый Массив;
					Для каждого Строка Из ИскомыеСтроки Цикл
						Строка.Выбрано = Выбрано;
						Если Строка.Геокодировано Тогда 
							Режим = ?(Выбрано, 2, 1);
							ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения, Режим, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
							мсвОтправлений.Добавить(Строка.АдресОтправления);
						КонецЕсли;
						СвязанныеЗадания = СвязанныеСтрокиЗаданий(Строка, СписокЗаданий);
						Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
							СвязанноеЗадание.Выбрано = Выбрано;
							Если СвязанноеЗадание.Геокодировано Тогда 
								ИзменитьИконкуМаркераПоВидуАдреса(СвязанноеЗадание.ВидАдресаПолучения, СвязанноеЗадание.ИндексВМассивеПолучения, Режим, ОпределитьКоэффициентПересчета(СвязанноеЗадание), (ВыделятьСрочныеГрузы И СвязанноеЗадание.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
							КонецЕсли; 
						КонецЦикла;
					КонецЦикла;
					Если мсвОтправлений.Количество() Тогда
						мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
					КонецЕсли;
					Для каждого текОтправление Из мсвОтправлений Цикл
						Если Выбрано Тогда
							ИзменитьИконкуОтправления(текОтправление, 2); 
						Иначе
							ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Выбрано", текОтправление, Истина));
							Если ИскомыеСтроки.Количество() = 0 Тогда
								УбратьВыборМаркера(текОтправление);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла; 
				Иначе
					Для каждого Строка Из ИскомыеСтроки Цикл
						Строка.Выбрано = Выбрано;
					КонецЦикла;
				КонецЕсли; 
				ОбновитьПоказателиПоВыбраннымСтрокам();
			КонецЕсли; 
		ИначеЕсли текЭлемент.Имя = "СписокЗаданийПометка" Тогда
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
			Пометка = НЕ текСтрока.Пометка;
			мсвОтправлений = Новый Массив;
			Для каждого Строка Из ИскомыеСтроки Цикл
				Строка.Пометка = Пометка;
				мсвОтправлений.Добавить(Строка.АдресОтправления);
				ИзменитьВидимостьПоСтрокеСпискаЗаданий(Строка);
			КонецЦикла; 
			Если мсвОтправлений.Количество() Тогда
				мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
			КонецЕсли;
			Для каждого текОтправление Из мсвОтправлений Цикл
				Если Пометка Тогда
					ИзменитьВидимостьТочкиОтправления(текОтправление, Пометка);
				Иначе
					ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Пометка", текОтправление, Истина));
					Если ИскомыеСтроки.Количество() = 0 Тогда
						ИзменитьВидимостьТочкиОтправления(текОтправление, Пометка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли текЭлемент.Имя = "СписокЗаданийАктивно" Тогда
			Если Ложь
				Или Не ЗначениеЗаполнено(текСтрока.ПредыдущееЗадание) 
				Или текСтрока.ПредыдущееЗадание = текСтрока.ЗаданиеНаПеревозку 
			Тогда
				ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
				Активно = НЕ текСтрока.Активно;
				Для каждого Строка Из ИскомыеСтроки Цикл
					Если Строка.ТипПеревозки <> 3 Тогда
						Строка.Активно = Активно;
					КонецЕсли;
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						ИзменитьПрозрачностьМаркера(Строка.ИндексВМассивеПолучения, ?(Строка.Активно, 0, 50));
					КонецЕсли;
					СвязанныеЗадания = СвязанныеСтрокиЗаданий(Строка, СписокЗаданий);
					Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
						СвязанноеЗадание.Активно = Активно;
						Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
							ИзменитьПрозрачностьМаркера(СвязанноеЗадание.ИндексВМассивеПолучения, ?(СвязанноеЗадание.Активно, 0, 50));
						КонецЕсли; 
					КонецЦикла;
				КонецЦикла;
			КонецЕсли; 
		ИначеЕсли текЭлемент.Имя = "СписокЗаданийПозиционирование" Тогда
			Если текСтрока.Пометка Тогда
				ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текСтрока.АдресОтправления));
				Если ИскомыеСтроки.Количество() Тогда
					ЦентрироватьСлойНаКарте(ИскомыеСтроки[0].ИндексВМассиве);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли текЭлемент.Имя = "СписокЗаданийПозиционированиеНаТочкеПолучения" Тогда
			Если текСтрока.Пометка Тогда
				ЦентрироватьСлойНаКарте(текСтрока.ИндексВМассивеПолучения);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Ложь
			Или текЭлемент.Имя = "СписокЗаданийВыбрано" 
			Или текЭлемент.Имя = "СписокЗаданийПометка" 
			Или текЭлемент.Имя = "СписокЗаданийАктивно" 
			Или текЭлемент.Имя = "СписокЗаданийПозиционирование" 
			Или текЭлемент.Имя = "СписокЗаданийПозиционированиеНаТочкеПолучения" 
		Тогда
			// изменяем текущий элемент для возможности его повторной активизации
			Элементы.СписокЗаданий.ТекущийЭлемент = Элементы.СписокЗаданийПредставление;
			РазрешитьСобытиеВыборВСпискеЗаданий = Ложь;
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры // СписокЗаданийПриАктивизацииПоляОбработчикОжидания()

&НаКлиенте
Процедура СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания()
	#Если НЕ ВебКлиент Тогда

	//Если ПервыйЗапуск Тогда
	//	ОчиститьВыделенныеСтрокиСпискаЗаданий();
	//	Возврат;
	//КонецЕсли; 
	текСтрока = Элементы.СписокЗаданий.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		// При множественном выделении с Shift'ом не на каждой строке срабатывает событие "ПриАктивизации".
		мсвОтправлений = Новый Массив;
		Для каждого Строка Из ВыделенныеСтроки Цикл
			текЗадание = СписокЗаданий.НайтиПоИдентификатору(Строка);
			//Если текЗадание <> Неопределено И текЗадание.Геокодировано И НЕ текЗадание.Выделено Тогда
			Если текЗадание <> Неопределено И НЕ текЗадание.Выделено Тогда
				Если текЗадание.Геокодировано Тогда
					Если НЕ текЗадание.Выбрано Тогда
						мсвОтправлений.Добавить(текЗадание.АдресОтправления);
						ИзменитьИконкуМаркераПоВидуАдреса(текЗадание.ВидАдресаПолучения, текЗадание.ИндексВМассивеПолучения, 1, ОпределитьКоэффициентПересчета(текЗадание), (ВыделятьСрочныеГрузы И текЗадание.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
					КонецЕсли;
					ДобавитьОбъектВВыделенные(текЗадание.ИндексВМассивеПолучения);
				КонецЕсли;
				текЗадание.Выделено = Истина;
				СвязанныеЗадания = СвязанныеСтрокиЗаданий(текЗадание, СписокЗаданий);
				Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
					Если текЗадание.Геокодировано Тогда
						Если НЕ текЗадание.Выбрано Тогда
							ИзменитьИконкуМаркераПоВидуАдреса(СвязанноеЗадание.ВидАдресаПолучения, СвязанноеЗадание.ИндексВМассивеПолучения, 1, ОпределитьКоэффициентПересчета(СвязанноеЗадание), (ВыделятьСрочныеГрузы И СвязанноеЗадание.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
						КонецЕсли;
						ДобавитьОбъектВВыделенные(СвязанноеЗадание.ИндексВМассивеПолучения);
					КонецЕсли;
					СвязанноеЗадание.Выделено = Истина;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		Если мсвОтправлений.Количество() Тогда
			мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
		КонецЕсли;
		Для каждого текОтправление Из мсвОтправлений Цикл
			ИзменитьИконкуОтправления(текОтправление, 1);
		КонецЦикла; 
		мсвОтправлений.Очистить();
		
		ДобавляемыеСтроки = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			СтрокаЗадания = СписокЗаданий.НайтиПоИдентификатору(ВыделеннаяСтрока);
			СвязанныеЗадания = СвязанныеСтрокиЗаданий(СтрокаЗадания, СписокЗаданий);
			Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
				СвязаннаяСтрока = СвязанноеЗадание.ПолучитьИдентификатор();
				ДобавляемыеСтроки.Добавить(СвязаннаяСтрока);
			КонецЦикла;
		КонецЦикла;
		Для Каждого ДобавляемаяСтрока Из ДобавляемыеСтроки Цикл
			Если ВыделенныеСтроки.Найти(ДобавляемаяСтрока) = Неопределено Тогда
				ВыделенныеСтроки.Добавить(ДобавляемаяСтрока);
				//ПодключитьОбработчикОжидания("ОбновитьВыделенныеСтрокиСпискаЗаданийОтложенно", 0.1, Истина);
			КонецЕсли; 
		КонецЦикла;
		ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
		Для каждого Строка Из ИскомыеСтроки Цикл
			Если НЕ Строка.Геокодировано Тогда
				Продолжить;
			КонецЕсли;
			ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
			Если ВыделенныеСтроки.Найти(ИдентификаторСтроки) = Неопределено Тогда
				УдалитьОбъектИзВыделенных(Строка.ИндексВМассивеПолучения);
				Строка.Выделено = Ложь;
				Если НЕ Строка.Выбрано Тогда
					мсвОтправлений.Добавить(Строка.АдресОтправления);
					ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения,, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая"))); 
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если мсвОтправлений.Количество() Тогда
			мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
		КонецЕсли;
		Для каждого текОтправление Из мсвОтправлений Цикл
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Выделено", текОтправление, Истина));
			Если ИскомыеСтроки.Количество() = 0 Тогда
				ИзменитьИконкуОтправления(текОтправление);
			КонецЕсли;
		КонецЦикла; 
		
	Иначе
		Для каждого Строка Из ВыделенныеСтроки Цикл
			текЗадание = СписокЗаданий.НайтиПоИдентификатору(Строка);
			Если текЗадание <> Неопределено И Не текЗадание.Выделено Тогда
				текЗадание.Выделено = Истина;
			КонецЕсли;
		КонецЦикла;
		
		ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Выделено", Истина));
		Для каждого Строка Из ИскомыеСтроки Цикл
			ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
			Если ВыделенныеСтроки.Найти(ИдентификаторСтроки) = Неопределено Тогда
				Строка.Выделено = Ложь;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
	 #КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьСписокЗаданийПриАктивизацииСтроки()
	
	Если ПервыйЗапуск Тогда
		ОчиститьВыделенныеСтрокиСпискаЗаданий();
		Возврат;
	КонецЕсли; 
	ПодключитьОбработчикОжидания("СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВыделенныеСтрокиСпискаЗаданий()
	
	ПервыйЗапуск = Ложь;
	Элементы.СписокЗаданий.ТекущаяСтрока = Неопределено;
	Элементы.СписокЗаданий.ВыделенныеСтроки.Очистить();
	ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();

КонецПроцедуры

&НаКлиенте
Функция СвязанныеСтрокиЗаданий(СтрокаЗаданий, СтрокиЗаданий)
	
	Если ТипЗнч(СтрокиЗаданий) = Тип("ДанныеФормыКоллекция") Тогда
		Результат = СтрокиЗаданий.НайтиСтроки(Новый Структура("ПредыдущееЗадание", СтрокаЗаданий.ЗаданиеНаПеревозку));
		Результат1 = СтрокиЗаданий.НайтиСтроки(Новый Структура("ЭтоВозвратГруза, АдресОтправления", Истина, СтрокаЗаданий.АдресПолучения));
		Если АвтоматическиДобавлятьЗаданияИнкассации Тогда
			Результат2 = СтрокиЗаданий.НайтиСтроки(Новый Структура("ЭтоИнкассация, АдресОтправления", Истина, СтрокаЗаданий.АдресПолучения));
		КонецЕсли;
	Иначе
		Результат = упСервисныеФункцииКлиентСервер.НайтиЭлементыКоллекцииПоЗначениюСвойства(СтрокиЗаданий, "ПредыдущееЗадание", СтрокаЗаданий.ЗаданиеНаПеревозку);
		Результат1 = упСервисныеФункцииКлиентСервер.НайтиЭлементыКоллекцииПоЗначениюСвойства(СтрокиЗаданий, "АдресОтправления", СтрокаЗаданий.Адрес);
		Если АвтоматическиДобавлятьЗаданияИнкассации Тогда
			Результат2 = Результат1;
		КонецЕсли;
	КонецЕсли; 
	Если АвтоматическиДобавлятьЗаданияНаВозврат Тогда
		Для Каждого СтрокаКандидат Из Результат1 Цикл
			Если Результат.Найти(СтрокаКандидат) = Неопределено И СтрокаКандидат.ЭтоВозвратГруза Тогда
				Результат.Добавить(СтрокаКандидат);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
	Если АвтоматическиДобавлятьЗаданияИнкассации Тогда
		Для Каждого СтрокаКандидат Из Результат2 Цикл
			Если Результат.Найти(СтрокаКандидат) = Неопределено И СтрокаКандидат.ЭтоИнкассация Тогда
				Результат.Добавить(СтрокаКандидат);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания()
	#Если НЕ ВебКлиент Тогда

	// Бывшее ДеревоРейсыПриАктивизацииСтроки
	ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		// При множественном выделении с Shift'ом не на каждой строке срабатывает событие "ПриАктивизации".
		Для каждого Строка Из ВыделенныеСтрокиДерева Цикл
			текСтрокаДерева = ДеревоРейсы.НайтиПоИдентификатору(Строка);
			Если текСтрокаДерева <> Неопределено И ЗначениеЗаполнено(текСтрокаДерева.Рейс) И Не текСтрокаДерева.Выделен Тогда
				Если текСтрокаДерева.Уровень = 0 Тогда
					Если текСтрокаДерева.Геокодирован И текСтрокаДерева.НомерЦветовойСхемы > 0 Тогда
						текЦветоваяСхема = ЦветовыеСхемы[текСтрокаДерева.НомерЦветовойСхемы - 1];
						ИзменитьСтильРейса(текСтрокаДерева.ИндексВМассивеМаршрут, ПредставлениеЧисла(текЦветоваяСхема.ХарактеристикиЛинии.Толщина*2) + "," + "''");
					КонецЕсли;
					текСтрокаДерева.Выделен = Истина;
				ИначеЕсли текСтрокаДерева.Уровень = 1 Тогда
					текРодитель = текСтрокаДерева.ПолучитьРодителя();
					Если текРодитель.Геокодирован И текРодитель.НомерЦветовойСхемы > 0 Тогда
						текЦветоваяСхема = ЦветовыеСхемы[текРодитель.НомерЦветовойСхемы - 1];
					КонецЕсли;
					Если НЕ текРодитель.Выделен Тогда
						Если текРодитель.Геокодирован И текРодитель.ИндексВМассивеМаршрут > 0 Тогда
							ИзменитьСтильРейса(текРодитель.ИндексВМассивеМаршрут, ПредставлениеЧисла(текЦветоваяСхема.ХарактеристикиЛинии.Толщина*2) + "," + "''");								
						КонецЕсли;
						текРодитель.Выделен = Истина;
					КонецЕсли;
					Если Не текСтрокаДерева.Выбран И текРодитель.Геокодирован И текСтрокаДерева.ИндексВМассиве > 0 Тогда             
						ИзменитьИконкуМаркера(текСтрокаДерева.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
					КонецЕсли;
					ДобавитьОбъектВВыделенные(текСтрокаДерева.ИндексВМассиве);
					текСтрокаДерева.Выделен = Истина;
					СвязанныеЗадания = СвязанныеСтрокиЗаданий(текСтрокаДерева, текРодитель.ПолучитьЭлементы());
					Для Каждого СвязанноеЗадание Из СвязанныеЗадания Цикл
						Элементы.ДеревоРейсы.ВыделенныеСтроки.Добавить(СвязанноеЗадание.ПолучитьИдентификатор());
						// Иначе добавленные выделенные строки не станут визуально выделенными https://partners.v8.1c.ru/forum/t/1631040/m/1631040
						ПодключитьОбработчикОжидания("ОбновитьВыделенныеСтрокиДереваРейсовОтложенно", 0.1, Истина);
						СвязанноеЗадание.Выделен = Истина;
						Если Не СвязанноеЗадание.Выбран И текРодитель.Геокодирован И СвязанноеЗадание.ИндексВМассиве > 0 Тогда
							ИзменитьИконкуМаркера(СвязанноеЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
						КонецЕсли;
						ДобавитьОбъектВВыделенные(СвязанноеЗадание.ИндексВМассиве);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
		Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
			Если ЭлементРейс.Геокодирован И ЗначениеЗаполнено(ЭлементРейс.Рейс) Тогда
				Если ЭлементРейс.НомерЦветовойСхемы > 0 Тогда 
					текЦветоваяСхема = ЦветовыеСхемы[ЭлементРейс.НомерЦветовойСхемы - 1];
				КонецЕсли; 
				ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
				ЕстьВыделенныеЗадания = Ложь;
				Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
					Если ЭлементЗадание.Выделен Тогда
						ИдентификаторСтрокиДерева = ЭлементЗадание.ПолучитьИдентификатор();
						Если ВыделенныеСтрокиДерева.Найти(ИдентификаторСтрокиДерева) = Неопределено Тогда
							Если ЭлементРейс.НомерЦветовойСхемы > 0 Тогда 
								Если Не ЭлементЗадание.Выбран Тогда
									ИзменитьИконкуМаркера(ЭлементЗадание.ИндексВМассиве, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
								КонецЕсли;
								УдалитьОбъектИзВыделенных(ЭлементЗадание.ИндексВМассиве);
							КонецЕсли; 
							ЭлементЗадание.Выделен = Ложь;
						Иначе
							ЕстьВыделенныеЗадания = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла; 
				Если ЭлементРейс.Выделен И НЕ ЕстьВыделенныеЗадания Тогда
					ИдентификаторСтрокиДерева = ЭлементРейс.ПолучитьИдентификатор();
					Если ВыделенныеСтрокиДерева.Найти(ИдентификаторСтрокиДерева) = Неопределено Тогда
						Если ЭлементРейс.НомерЦветовойСхемы > 0 Тогда 
							ИзменитьСтильРейса(ЭлементРейс.ИндексВМассивеМаршрут, ПредставлениеЧисла(текЦветоваяСхема.ХарактеристикиЛинии.Толщина/2) + "," + "'3, 2'");
						КонецЕсли; 
						ЭлементРейс.Выделен = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для каждого Строка Из ВыделенныеСтрокиДерева Цикл
			текСтрокаДерева = ДеревоРейсы.НайтиПоИдентификатору(Строка);
			Если текСтрокаДерева <> Неопределено И ЗначениеЗаполнено(текСтрокаДерева.Рейс) И Не текСтрокаДерева.Выделен Тогда
				Если текСтрокаДерева.Уровень = 0 Тогда
					текСтрокаДерева.Выделен = Истина;
				ИначеЕсли текСтрокаДерева.Уровень = 1 Тогда
					текРодитель = текСтрокаДерева.ПолучитьРодителя();
					Если Не текРодитель.Выделен Тогда
						текРодитель.Выделен = Истина;
					КонецЕсли;
					текСтрокаДерева.Выделен = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
		Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
			Если ЗначениеЗаполнено(ЭлементРейс.Рейс) Тогда
				ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
				ЕстьВыделенныеЗадания = Ложь;
				Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
					Если ЭлементЗадание.Выделен Тогда
						ИдентификаторСтрокиДерева = ЭлементЗадание.ПолучитьИдентификатор();
						Если ВыделенныеСтрокиДерева.Найти(ИдентификаторСтрокиДерева) = Неопределено Тогда
							ЭлементЗадание.Выделен = Ложь;
						Иначе
							ЕстьВыделенныеЗадания = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла; 
				Если ЭлементРейс.Выделен И НЕ ЕстьВыделенныеЗадания Тогда
					ИдентификаторСтрокиДерева = ЭлементРейс.ПолучитьИдентификатор();
					Если ВыделенныеСтрокиДерева.Найти(ИдентификаторСтрокиДерева) = Неопределено Тогда
						ЭлементРейс.Выделен = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	ОбновитьПоказателиПоВыделеннымСтрокамДереваРейсов();
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
// https://partners.v8.1c.ru/forum/t/1631040/m/1631040
Процедура ОбновитьВыделенныеСтрокиДереваРейсовОтложенно()
	
	Элементы.ДеревоРейсы.ВыделенныеСтроки.Добавить(Элементы.ДеревоРейсы.ВыделенныеСтроки[0]);

КонецПроцедуры // ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания()

&НаКлиенте
// // https://partners.v8.1c.ru/forum/t/1631040/m/1631040
Процедура _ОбновитьВыделенныеСтрокиСпискаЗаданийОтложенно()
	
	Элементы.СписокЗаданий.ВыделенныеСтроки.Добавить(Элементы.СписокЗаданий.ВыделенныеСтроки[0]);

КонецПроцедуры // ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания()

#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после выбора значения меню.
//
// Параметры:
//  Результат  - Дата - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыборТипаПериодаПланированияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Элементы.ПериодПланирования.ВыбиратьТип = Истина;
	ИначеЕсли Результат.Значение = 0 Тогда
		сткПараметры = Новый Структура("НачалоПериода, КонецПериода, ПроверкаЗаполнения", НачалоПериодаПланирования, ОкончаниеПериодаПланирования, Истина);
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораПроизвольногоПериода", сткПараметры,,,,, Новый ОписаниеОповещения("ВыборСтандартногоПериодаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	ИначеЕсли Результат.Значение = 1 Тогда
		ОткрытьФорму("Справочник.упПериодыПланирования.ФормаВыбора",,,,,, Новый ОписаниеОповещения("ВыборПериодаПланированияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
// Параметры:
//  Результат  - Дата - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыборСтандартногоПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ПериодПланирования.ВыбиратьТип = Истина;
	Если Результат <> Неопределено Тогда
		ПериодПланирования = Неопределено;
		НачалоПериодаПланирования    = Результат.НачалоПериода;
		ОкончаниеПериодаПланирования = Результат.КонецПериода;
		ОбновитьТаблицуЗаданий(Неопределено);
		
		ОбновитьДоступность();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
// Параметры:
//  Результат  - Дата - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыборПериодаПланированияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ПериодПланирования.ВыбиратьТип = Истина;
	Если Результат <> Неопределено Тогда
		ПериодПланирования = Результат;
		сткДанныеПериода = ВернутьПериодОтбора(ПериодПланирования, ?(ЗначениеЗаполнено(ТочкаОтсчета), ТочкаОтсчета, Неопределено));
		НачалоПериодаПланирования    = сткДанныеПериода.НачалоПериода;
		ОкончаниеПериодаПланирования = сткДанныеПериода.ОкончаниеПериода;
		ОбновитьТаблицуЗаданий(Неопределено);
		
		ОбновитьДоступность();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при изменении режима использования эл. карт в настройках системы.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ИзменениеРежимаИспользованияЭлектронныхГеографическихКартЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Закрыть();
	
КонецПроцедуры // ИзменениеРежимаИспользованияЭлектронныхГеографическихКартЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы геокодирования адресов
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ДекорацияГеокодированиеАдресовНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
	ОбновитьТаблицуЗаданий(Неопределено);
    
КонецПроцедуры // ДекорацияГеокодированиеАдресовНажатиеЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы выбора доступных транспортных средств
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыборДоступныхТСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ВыборДоступныхТСЗавершениеНаСервере(Результат.ТаблицаРезультат);
		ОбновитьДеревоРейсов(Неопределено);
		ПервыйЗапуск = Истина;
	КонецЕсли;
	
КонецПроцедуры // ВыборДоступныхТСЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы редактирования рейса
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ИзменениеРейсаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		текДанные = Элементы.ДеревоРейсы.ТекущиеДанные;
		Если текДанные <> Неопределено И ЗначениеЗаполнено(текДанные.Рейс) Тогда
			Если текДанные.Уровень = 1 Тогда
				текДанные = текДанные.ПолучитьРодителя();
			КонецЕсли;
			
			ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
			ИндексСтроки = ЭлементыРейсы.Индекс(текДанные);
			
			мсвОбъектов = Новый Массив;
			ЭлементыЗадания = текДанные.ПолучитьЭлементы();
			Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
				мсвОбъектов.Добавить(ЭлементЗадание[ИмяРеквизита]);
			КонецЦикла; 

			ДополнительныеРеквизиты = Новый Структура;
			ДополнительныеРеквизиты.Вставить("НомерЦветовойСхемы", текДанные.НомерЦветовойСхемы);
			Если текДанные.ИндексВМассивеМаршрут > 0 Тогда
				УдалитьОтображениеРейса(текДанные.ИндексВМассивеМаршрут);
			КонецЕсли;
			ОчиститьМаркерыРейсов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИндексСтроки));
			
			ОбновитьВетвьДереваРейсовНаСервере(текДанные.Рейс, ИндексСтроки, ДополнительныеРеквизиты);
			мсвОтборов = Новый Массив;
			Отбор = Новый Структура;
			Отбор.Вставить("ИмяПоля"       , ИмяРеквизита);
			Отбор.Вставить("ПравоеЗначение", мсвОбъектов);
			Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);
			мсвОтборов.Добавить(Отбор);
			ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь);
			
			ИдентификаторСтрокиДерева = ЭлементыРейсы.Получить(ИндексСтроки).ПолучитьИдентификатор();
			Элементы.ДеревоРейсы.Развернуть(ИдентификаторСтрокиДерева, Ложь);
			ОтобразитьРейсыНаКарте();
			Элементы.ДеревоРейсы.ТекущаяСтрока = ИдентификаторСтрокиДерева;
			НовыеСтроки = Новый Массив;
			
			Если КоличествоДобавленныхСтрок > 0 Тогда
				КоличествоСтрок = СписокЗаданий.Количество();
				Для каждого текОбъект Из мсвОбъектов Цикл
					Пока КоличествоДобавленныхСтрок > 0 Цикл
						ИндексСтроки = КоличествоСтрок - КоличествоДобавленныхСтрок;
						ТекущаяСтрока = СписокЗаданий[ИндексСтроки];
						НовыеСтроки.Добавить(ТекущаяСтрока);
						КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли; 
			
			Если НовыеСтроки.Количество() Тогда
				ОтобразитьОбъектыНаКарте(НовыеСтроки);
			Иначе
				СписокЗаданийСформирован = Истина;
			КонецЕсли;
			ОбновитьДоступность();
			ОбновитьПоказателиПоВыбраннымСтрокам();
			
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры // ИзменениеРейсаЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы настроек списка заданий на перевозку
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НастройкаЗаданийНаПеревозкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Результат.Настройки);
		ОбновитьТаблицуЗаданий(Неопределено);
	КонецЕсли;
    
КонецПроцедуры // НастройкаЗаданийНаПеревозкуЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров планирования рейсов
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыПланированияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		Если Результат.СохранятьИзмененияВарианта Тогда
			СохранитьВариантНастройкиПланирования(Неопределено);
		КонецЕсли; 
		
		Если Ложь
			Или ПараметрыПланирования.ВыделятьКрупныеГрузы <> Результат.ВыделятьКрупныеГрузы
			Или ПараметрыПланирования.РазмерКрупногоГруза <> Результат.РазмерКрупногоГруза
			Или ПараметрыПланирования.ПоказательКрупногоГруза <> Результат.ПоказательКрупногоГруза
			Или ПараметрыПланирования.ВыделятьСрочныеГрузы <> Результат.ВыделятьСрочныеГрузы
		Тогда
			Если Результат.ПоказательКрупногоГруза = 0 Тогда 
				ПоказательКрупногоГруза = "Масса";
			ИначеЕсли Результат.ПоказательКрупногоГруза = 1 Тогда 
				ПоказательКрупногоГруза = "Объем";	
			ИначеЕсли Результат.ПоказательКрупногоГруза = 2 Тогда 
				ПоказательКрупногоГруза = "КоличествоМест";	
			Иначе 
				ПоказательКрупногоГруза = "КоличествоЗагрузочныхМетров";		
			КонецЕсли;
			ВыделятьКрупныеГрузы = Результат.ВыделятьКрупныеГрузы;
			РазмерКрупногоГруза  = Результат.РазмерКрупногоГруза;
			ВыделятьСрочныеГрузы = Результат.ВыделятьСрочныеГрузы;
			ОбновитьВсе = Истина;
		Иначе
			ОбновитьВсе = Ложь;
		КонецЕсли;
		
		МассивыИдентичны = ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(ПараметрыПланирования.МестаИнтереса,Результат.МестаИнтереса);
		
		Если ПараметрыПланирования.КоличествоВидимыхРейсов <> Результат.КоличествоВидимыхРейсов Тогда
			Если Результат.КоличествоВидимыхРейсов = 0 Тогда
				Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Видимость = Ложь;
				Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Видимость = Ложь;
			Иначе	
				Элементы.ДеревоРейсыОтобразитьПредыдущуюГруппуРейсов.Видимость = Истина;
				Элементы.ДеревоРейсыОтобразитьСледующуюГруппуРейсов.Видимость = Истина;
			КонецЕсли;
		КонецЕсли; 
		
		Если Результат.СохранятьИзмененияВарианта Тогда
			
		КонецЕсли; 
		ПараметрыПланирования = Результат;
		
		Если НЕ МассивыИдентичны Тогда
			// Исходный массив и отредактированный не равны, удалим все места на карте.
			// Получим даные для новых и отобразим если потребуется.
			ВыполнитьПроцедуруHTML("ClearSiteInterest()");
			ОбновитьСписокМестИнтересаНаСервере();
			ОбновитьМестаИнтересаНаКлиенте();
			ИзменитьВидимостьМестИнтереса(ОтображатьМестаИнтереса);
		КонецЕсли;
		
		Если ОбновитьВсе Тогда
			// Серверные вызовы.
			ОбновитьТаблицуЗаданийНаСервере();
			ОбновитьДеревоРейсовНаСервере();
			
			// Клиентские вызовы.
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				ОчиститьОбъекты();
				ОтобразитьОбъектыНаКарте();
				ВыполнитьПроцедуруHTML("clearTrips()");
				ОтобразитьРейсыНаКарте();
				
				ИзменитьВидимостьЗон(ОтображатьЗоны);
			КонецЕсли;
			ОбновитьДоступность(); 
		КонецЕсли;
	КонецЕсли;
    
КонецПроцедуры // УстановитьПараметрыПланированияЗавершение()

&НаКлиенте
// Процедура вызывается при закрытии формы установки фильтра по адресам отправления
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановкаФильтраПоАдресуОтправленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Если Результат.Количество() Тогда
			Для каждого текОтправление Из СписокОтправлений Цикл
				Если Результат.Найти(текОтправление.Адрес) <> Неопределено Тогда
					текОтправление.Пометка = Не текОтправление.Пометка;
					ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления", текОтправление.Адрес));
					Для каждого текСтрока  Из ИскомыеСтроки Цикл
						текСтрока.Пометка = текОтправление.Пометка;
						ИзменитьВидимостьПоСтрокеСпискаЗаданий(текСтрока);
					КонецЦикла;
					ИзменитьВидимостьОбъекта(текОтправление.ИндексВМассиве, текОтправление.Пометка);
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановкаФильтраПоАдресуОтправленияЗавершение ()

&НаКлиенте
// Процедура завершения после закрытия формы настройки видимости зон. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьВидимыеЗоныЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		УдаленныеЗоны = Неопределено;
		ПолучитьДанныеНовыхЗон(Результат, УдаленныеЗоны); 
		Для Каждого Зона Из СписокЗон Цикл
			Если Зона.ИндексВМассиве = 0 Тогда
				Зона.ИндексВМассиве = ОтобразитьЗону(Зона.Данные);
			КонецЕсли;	
			ИзменитьВидимостьЗоны(Зона.ИндексВМассиве, Зона.Пометка);
		КонецЦикла;	
		Если Не УдаленныеЗоны = Неопределено Тогда
			Для Каждого УдаленнаяЗона Из УдаленныеЗоны Цикл
				ИзменитьВидимостьЗоны(УдаленнаяЗона, Ложь);	
			КонецЦикла;
		КонецЕсли;
		СохранитьВариантНастроекСпискаЗон();	
	КонецЕсли;

КонецПроцедуры // УстановитьВидимыеЗоныЗавершение ()

&НаСервере
// Процедура преобразования полученных данных в таблицу значений. 
//
// Параметры:
//  Результат  - ДанныеФормыКоллекция - результат выбора.
//
Процедура ПолучитьДанныеНовыхЗон(Результат,УдаленныеЗоны)
	
		СтарыйСписок    = ДанныеФормыВЗначение(СписокЗон,Тип("ТаблицаЗначений"));
		ТаблицаНовыхЗон = ДанныеФормыВЗначение(Результат,Тип("ТаблицаЗначений"));
		МассивПустыхЗон = Новый Массив;
		Для Каждого ТекущаяЗона Из ТаблицаНовыхЗон Цикл
			ТекущаяЗона.Пометка = ТекущаяЗона.ПометкаНовое;
			Если ТекущаяЗона.ИндексВМассиве = 0 Тогда
				МассивПустыхЗон.Добавить(ТекущаяЗона.Зона);
			КонецЕсли;	
		КонецЦикла;	
		УдаленныеЗоныТЗ = ПолучитьСписокУдаленныхЗон(ТаблицаНовыхЗон);
		УдаленныеЗоны = УдаленныеЗоныТЗ.ВыгрузитьКолонку("ИндексВМассиве");
		ОбъединенныйСписок = ПолучитьСписокЗонОбъединением(ТаблицаНовыхЗон,СтарыйСписок,Ложь);
		Если Не ОбъединенныйСписок = Неопределено Тогда
			Для Индекс = -(ОбъединенныйСписок.Количество()-1) По 0 Цикл
				Строка = ОбъединенныйСписок.Получить(-Индекс);
				Если УдаленныеЗоныТЗ.Найти(Строка.Зона,"Зона") <> Неопределено  Тогда
					ОбъединенныйСписок.Удалить(Строка);
				КонецЕсли;
			КонецЦикла;
			ТаблицаНовыхЗон = ОбъединенныйСписок;
		КонецЕсли;
		
		Если МассивПустыхЗон.Количество() > 0 Тогда
			ДанныеЗон = ПолучитьДанныеЗон(МассивПустыхЗон);
			Для Каждого СтрокаТаблицы Из ТаблицаНовыхЗон Цикл
				Если СтрокаТаблицы.ИндексВМассиве = 0 Тогда
					Для Каждого ДанныеЗоны Из ДанныеЗон Цикл
						Если СтрокаТаблицы.Зона = ДанныеЗоны.Зона Тогда
							СтрокаТаблицы.Данные = ДанныеЗоны.Данные; 
						КонецЕсли;	
					КонецЦикла;
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
		
		СписокЗон.Очистить();
		СписокЗон.Загрузить(ТаблицаНовыхЗон); 
	
КонецПроцедуры // ПолучитьДанныеНовыхЗон()

&НаСервере
// Функция объединяет два списка зон, полученный из заданий
// и полученный из хранилища.
//
// Параметры:
//  СписокИзЗаданий  - Список зон из заданий.
//  СписокИзХранилища  - Список зон из хранилища.
//
Функция ПолучитьСписокЗонОбъединением(СписокИзЗаданий,СтарыйСписок,ПерваяЗагрузка)
	
	ОбъединенныйСписок = СтарыйСписок.Скопировать();
	Для Каждого НоваяЗона Из СписокИзЗаданий Цикл
		Отбор = Новый Структура;
		отбор.Вставить("Зона",НоваяЗона.Зона);
		Строки = ОбъединенныйСписок.НайтиСтроки(Отбор);
		Если Строки.Количество() = 0 Тогда
			НоваяСтрока = ОбъединенныйСписок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,НоваяЗона);
		ИначеЕсли Строки.Количество() > 0 И Не ПерваяЗагрузка Тогда
			Для Каждого Строка Из Строки Цикл
				Строка.Пометка = НоваяЗона.Пометка;	
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат ОбъединенныйСписок;
	
КонецФункции
	
&НаСервере
// Функция получает список зон которые были удалены пользователем
//
// Параметры:
//  СписокИзЗаданий  - Список зон из заданий.
//  СписокИзХранилища  - Список зон из хранилища.
//
Функция ПолучитьСписокУдаленныхЗон(ТаблицаНовыхЗон)

	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Зона");
	ТаблицаЗначений.Колонки.Добавить("Пометка");
	ТаблицаЗначений.Колонки.Добавить("ИндексВМассиве");
	ТаблицаЗначений.Колонки.Добавить("Данные");
	
	Для Каждого ЗонаИзХранилища Из СписокЗон Цикл
		Отбор = Новый Структура;
		отбор.Вставить("Зона",ЗонаИзХранилища.Зона);
		Строки = ТаблицаНовыхЗон.НайтиСтроки(Отбор);
		Если Строки.Количество() = 0 Тогда
			НоваяСтрока = ТаблицаЗначений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ЗонаИзХранилища);
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат ТаблицаЗначений;
		
КонецФункции
	
&НаСервере
// Процедура завершения после закрытия формы выбора транспортных средств. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыборДоступныхТСЗавершениеНаСервере(ТаблицаРезультат)
	
	тзТаблицаРезультат = ДанныеФормыВЗначение(ТаблицаРезультат, Тип("ТаблицаЗначений"));
	СписокТранспорта.Загрузить(тзТаблицаРезультат);
	СохранитьВариантНастроекВидаПеревозки(тзТаблицаРезультат, "Обработка.упРМПланированиеРейсов.ДоступныеТС." + ВидТранспортнойУслугиПредставление);
	
КонецПроцедуры // ВыборДоступныхТСЗавершениеНаСервере()

&НаСервере
// Процедура завершения после закрытия формы сохранения настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СохранитьВариантНастройкиПланированияНаСервере(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.ВариантПланирования = Результат.КлючНастроек;
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("ВидПеревозки", ВидПеревозки);
		сткНастройки.Вставить("Пользователь", ТекущийПользователь);
		сткНастройки.Вставить("Результат", ПараметрыПланирования);
		ХранилищаНастроек.упХранилищеНастроек.Сохранить("Обработка.упРМПланированиеРейсов.ПараметрыПланирования." + ВидТранспортнойУслугиПредставление, ВариантПланирования, сткНастройки);
		УстановитьДоступныеЗначенияВариантовПланирования();
	КонецЕсли;

КонецПроцедуры

&НаСервере
// Процедура завершения после закрытия формы сохранения настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СохранитьВариантНастройкиСпискаЗаданийНаСервере(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.ВариантСписков = Результат.КлючНастроек;
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("ВидПеревозки", ВидПеревозки);
		сткНастройки.Вставить("Пользователь", ТекущийПользователь);
		сткНастройки.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроек.Настройки);
		ХранилищаНастроек.упХранилищеНастроек.Сохранить("Обработка.упРМПланированиеРейсов.НастройкаСпискаЗаданий." + ВидТранспортнойУслугиПредставление, ВариантСписков, сткНастройки);
		УстановитьДоступныеЗначенияВариантовСписков();
		СохранитьВариантНастроекВидаПеревозки(СписокТранспорта.Выгрузить(), "Обработка.упРМПланированиеРейсов.ДоступныеТС." + ВидТранспортнойУслугиПредставление);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы загрузки настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОткрытьВариантНастройкиСпискаЗаданийНаКлиенте(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		УстановитьДоступныеЗначенияВариантовСписков();
		ЭтаФорма.ВариантСписков = Результат.КлючНастроек;
		сткНастройки = ВернутьВариантНастройкиСпискаЗаданий();
		Если сткНастройки <> Неопределено Тогда
			//КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
			//ОбновитьТаблицуЗаданий(Неопределено);
			ВариантНастроекПриИзменении();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОткрытьВариантНастройкиСпискаЗаданийНаКлиенте()

&НаКлиенте
// Процедура завершения после закрытия формы загрузки настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОткрытьВариантНастройкиПланированияНаКлиенте(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		УстановитьДоступныеЗначенияВариантовПланирования();
		ЭтаФорма.ВариантПланирования = Результат.КлючНастроек;
		сткНастройки = ВернутьВариантНастройкиПланирования();
		Если сткНастройки <> Неопределено Тогда
			ВариантПланированияПриИзменении();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОткрытьВариантНастройкиСпискаЗаданийНаКлиенте()

&НаКлиенте
// Процедура завершения после закрытия формы оптимизации маршрута. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОптимизацияМаршрутаЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = КодВозвратаДиалога.Отмена Или Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Оптимизация маршрутов не удалась'");
		ПоказатьПредупреждение(, ТекстСообщения);
		
	Иначе	
		Для каждого текИдентификаторСтроки Из Результат Цикл
			ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
			текДанные = ДеревоРейсы.НайтиПоИдентификатору(текИдентификаторСтроки);
			Развернут = Элементы.ДеревоРейсы.Развернут(текИдентификаторСтроки); 
			ИндексСтроки = ЭлементыРейсы.Индекс(текДанные);
			
			ДополнительныеРеквизиты = Новый Структура;
			ДополнительныеРеквизиты.Вставить("НомерЦветовойСхемы", текДанные.НомерЦветовойСхемы);
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				ОчиститьМаркерыРейсов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИндексСтроки));
				Если текДанные.ИндексВМассивеМаршрут > 0 Тогда
					УдалитьОтображениеРейса(текДанные.ИндексВМассивеМаршрут);
				КонецЕсли;
			КонецЕсли; 
			
			ОбновитьВетвьДереваРейсовНаСервере(текДанные.Рейс, ИндексСтроки, ДополнительныеРеквизиты);
			ИдентификаторСтрокиДерева = ЭлементыРейсы.Получить(ИндексСтроки).ПолучитьИдентификатор();
			Если Развернут Тогда
				Элементы.ДеревоРейсы.Развернуть(ИдентификаторСтрокиДерева, Ложь);
			КонецЕсли;
		КонецЦикла; 
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ОтобразитьРейсыНаКарте();
		Иначе
			СписокИндексов.Очистить();
		КонецЕсли;
		Элементы.ДеревоРейсы.ТекущаяСтрока = ИдентификаторСтрокиДерева;		
		
	КонецЕсли;
		
КонецПроцедуры // ОптимизацияМаршрутаЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы автоматического планироваия рейсов. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура АвтоматическоеПланированиеРейсовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	//Если Результат = Истина Тогда
	    Если ИспользуютсяЭлектронныеГеографическиеКарты  Тогда
			ИзменитьВидимостьЗон();
		КонецЕсли; 
		ОбновитьТаблицуЗаданийНаСервере();
		Если ИспользуютсяЭлектронныеГеографическиеКарты  Тогда
			ОчиститьОбъекты();
			ОтобразитьОбъектыНаКарте();
			ВыполнитьПроцедуруHTML("clearTrips()");
			ИзменитьВидимостьЗон(ОтображатьЗоны);
		КонецЕсли; 
		ОбновитьДеревоРейсовНаСервере();
		Если ИспользуютсяЭлектронныеГеографическиеКарты  Тогда
			ОтобразитьРейсыНаКарте();
		КонецЕсли;
		ОбновитьДоступность();
		
	//ИначеЕсли Результат = Ложь Тогда
	//	ТекстСообщения = НСтр("ru = 'Автоматическое планирование не удалось'");
	//	ПоказатьПредупреждение(, ТекстСообщения);
	//	
	//КонецЕсли;

КонецПроцедуры // АвтоматическоеПланированиеРейсовЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы "ФормаНазначенияПеревозокТранспортнойКомпании". 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НазначитьПеревозкуТранспортнойКомпанииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	НазначенныеЗадания = Результат; // структура
	Если НазначенныеЗадания.Количество() Тогда
		Для каждого текЗадание Из НазначенныеЗадания Цикл
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозку", текЗадание.Значение));
			КоличествоСтрок = ИскомыеСтроки.Количество();
			Если КоличествоСтрок > 0 Тогда
				текИндекс = 0;
				Сч = 0;
				мсвОтправлений = Новый Массив;
				Пока Сч < КоличествоСтрок Цикл
					ИскомаяСтрока = ИскомыеСтроки.Получить(Сч);
					
					Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
						мсвОтправлений.Добавить(ИскомаяСтрока.АдресОтправления);
						ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеПолучения, Ложь);
						Если РежимОтображенияКарты = 1 Тогда // линии
							ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеЛиния, Ложь);
						ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
							ИзменитьВидимостьОбъекта(ИскомаяСтрока.ИндексВМассивеМаршрут, Ложь);
						КонецЕсли;
					КонецЕсли;
					текИндекс = СписокЗаданий.Индекс(ИскомаяСтрока);
					УдалитьСтрокуСпискаЗаданий(ИскомаяСтрока);
					Сч = Сч + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		Если мсвОтправлений.Количество() Тогда
			ОбработатьСостояниеОтправлений(мсвОтправлений);
		КонецЕсли;
		
		мсвОтборов = Новый Массив;
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяПоля"       , "ЗаданиеНаПеревозку");
		Отбор.Вставить("ПравоеЗначение", НазначенныеЗадания);
		Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);
		мсвОтборов.Добавить(Отбор);
		ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь); 
		
		НовыеСтроки = Новый Массив;
		КоличествоСтрок = СписокЗаданий.Количество();
		Пока КоличествоДобавленныхСтрок > 0 Цикл
			ИндексСтроки = КоличествоСтрок - КоличествоДобавленныхСтрок;
			ТекущаяСтрока = СписокЗаданий[ИндексСтроки];
			НовыеСтроки.Добавить(ТекущаяСтрока);
			КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
			СписокЗаданий.Сдвинуть(ИндексСтроки, текИндекс - ИндексСтроки);
		КонецЦикла;
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ОтобразитьОбъектыНаКарте(НовыеСтроки);
		КонецЕсли; 
		ОбновитьДоступность();
	КонецЕсли;
	
КонецПроцедуры // НазначитьПеревозкуТранспортнойКомпанииЗавершение()

&НаКлиенте
// Процедура завершения после немодального ввода числового значения. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВводКоличестваГрузаВНовойСтрокеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		текСтрока = Элементы.СписокЗаданий.ТекущиеДанные;
		КоличествоСтарое = текСтрока.КоличествоГрузов;
		Количество = Результат;
		Отказ = Ложь;
		Если Количество = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Количество груза в новой строке не может быть равно нулю'");
			Отказ = Истина;
		ИначеЕсли Количество < 0 Тогда	
			ТекстСообщения = НСтр("ru = 'Количество груза в новой строке не может быть отрицательным'");
			Отказ = Истина;
		ИначеЕсли Количество >= КоличествоСтарое Тогда
			ТекстСообщения = НСтр("ru = 'Количество груза в новой строке должно быть меньше количества груза в текущей'");
			Отказ = Истина;
		КонецЕсли; 
		
		Если Отказ Тогда
			ПоказатьПредупреждение(,ТекстСообщения);
		Иначе
			текИндекс = СписокЗаданий.Индекс(текСтрока);
			НоваяСтрока = СписокЗаданий.Вставить(текИндекс + 1);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			
			НоваяСтрока.КоличествоГрузов = Количество;
			текСтрока.КоличествоГрузов = КоличествоСтарое - НоваяСтрока.КоличествоГрузов;
			Если УчитыватьМассу Тогда
				МассаЕдиницыГруза = текСтрока.Масса/КоличествоСтарое;		
				НоваяСтрока.Масса = НоваяСтрока.КоличествоГрузов * МассаЕдиницыГруза; 
				текСтрока.Масса   = текСтрока.Масса - НоваяСтрока.Масса;
			КонецЕсли; 
			Если УчитыватьОбъем Тогда
				ОбъемЕдиницыГруза = текСтрока.Объем/КоличествоСтарое;		
				НоваяСтрока.Объем = НоваяСтрока.КоличествоГрузов * ОбъемЕдиницыГруза; 
				текСтрока.Объем   = текСтрока.Объем - НоваяСтрока.Объем;
			КонецЕсли; 
			Если УчитыватьКоличествоМест Тогда
				КоличествоМестЕдиницыГруза = текСтрока.КоличествоМест/КоличествоСтарое;		
				НоваяСтрока.КоличествоМест = НоваяСтрока.КоличествоГрузов * КоличествоМестЕдиницыГруза; 
				текСтрока.КоличествоМест   = текСтрока.КоличествоМест - НоваяСтрока.КоличествоМест;
			КонецЕсли;
			Если УчитыватьКоличествоЗагрузочныхМетров Тогда
				КоличествоЗагрузочныхМетровЕдиницыГруза = текСтрока.КоличествоЗагрузочныхМетров/КоличествоСтарое;		
				НоваяСтрока.КоличествоЗагрузочныхМетров = НоваяСтрока.КоличествоГрузов * КоличествоЗагрузочныхМетровЕдиницыГруза; 
				текСтрока.КоличествоЗагрузочныхМетров   = текСтрока.КоличествоЗагрузочныхМетров - НоваяСтрока.КоличествоЗагрузочныхМетров;
			КонецЕсли;
			ОценочнаяСтоимостьЕдиницыГруза = текСтрока.ОценочнаяСтоимость/КоличествоСтарое;		
			НоваяСтрока.ОценочнаяСтоимость = НоваяСтрока.ОценочнаяСтоимость * ОценочнаяСтоимостьЕдиницыГруза; 
			текСтрока.ОценочнаяСтоимость   = текСтрока.ОценочнаяСтоимость - НоваяСтрока.ОценочнаяСтоимость;
			
			Представление = СтрШаблон("ru = '%1 (%2)%3'", НоваяСтрока.ПредставлениеГруза, НоваяСтрока.КоличествоГрузов, ?(НоваяСтрока.НомерВЦепиПоставок > 0, СтрШаблон(" <%1>", НоваяСтрока.НомерВЦепиПоставок), ""));	
			НоваяСтрока.Представление = НСтр(Представление);
			Представление = СтрШаблон("ru = '%1 (%2)%3'", текСтрока.ПредставлениеГруза, текСтрока.КоличествоГрузов, ?(текСтрока.НомерВЦепиПоставок > 0, СтрШаблон(" <%1>", текСтрока.НомерВЦепиПоставок), ""));	
			текСтрока.Представление = НСтр(Представление);
			
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				Если НоваяСтрока.Геокодировано Тогда
					ИскомыеСтроки = СписокМаркеровВидовАдресов.НайтиСтроки(Новый Структура("ВидАдреса", НоваяСтрока.ВидАдресаПолучения));
					КоэффициентПересчета = ОпределитьКоэффициентПересчета(НоваяСтрока);
					СтрокаТаблицы = ИскомыеСтроки[0];
					ДанныеМаркера = СтрШаблон("'%1',%2,%3,false,%4,%5", СтрокаТаблицы.ДанныеМаркера,
	                                    ПредставлениеЧисла(СтрокаТаблицы.ПоложениеПривязкиXМаркера*КоэффициентПересчета),
										ПредставлениеЧисла(СтрокаТаблицы.ПоложениеПривязкиYМаркера*КоэффициентПересчета),
										ПредставлениеЧисла(СтрокаТаблицы.ШиринаМаркера*КоэффициентПересчета),
										ПредставлениеЧисла(СтрокаТаблицы.ВысотаМаркера*КоэффициентПересчета));
					НоваяСтрока.ИндексВМассивеПолучения = ОтобразитьОбъект(НоваяСтрока.ИмяПолучения, ДанныеМаркера, НоваяСтрока.КоординатыПолучения);
					Если РежимОтображенияКарты = 1 Тогда // линии
						НоваяСтрока.ИндексВМассивеЛиния = ОтобразитьОбъект(, НоваяСтрока.ХарактеристикиЛинии, НоваяСтрока.КоординатыОтправления + НоваяСтрока.КоординатыПолучения, 1);
						НоваяСтрока.ИндексВМассивеМаршрут = 0;
					ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
						НоваяСтрока.ИндексВМассивеМаршрут = ОтобразитьОбъект(, НоваяСтрока.ХарактеристикиЛинии, ?(НоваяСтрока.Путь = "", НоваяСтрока.КоординатыОтправления + НоваяСтрока.КоординатыПолучения, НоваяСтрока.Путь), 2);
						НоваяСтрока.ИндексВМассивеЛиния = 0;
					Иначе
						НоваяСтрока.ИндексВМассивеМаршрут = 0;
						НоваяСтрока.ИндексВМассивеЛиния = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если Не НоваяСтрока.Пометка Тогда
					ИзменитьВидимостьОбъекта(НоваяСтрока.ИндексВМассивеПолучения, Ложь);
					Если НоваяСтрока.ИндексВМассивеЛиния > 0 Тогда
						ИзменитьВидимостьОбъекта(НоваяСтрока.ИндексВМассивеЛиния, Ложь);
					КонецЕсли;
					Если НоваяСтрока.ИндексВМассивеМаршрут > 0 Тогда
						ИзменитьВидимостьОбъекта(НоваяСтрока.ИндексВМассивеМаршрут, Ложь);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Элементы.СписокЗаданий.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры // ВводКоличестваГрузаВНовойСтрокеЗавершение()

&НаКлиенте
// Процедура обработки ответа пользовател по распределению заданий.  
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура РаспределитьЗаданияПоПериодическимРейсамВопросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Отказ = Ложь;
		РаспределитьЗаданияПоПериодическимРейсамНаСервере(Отказ);
		Если Отказ Тогда
			ТекстСообщения = НСтр("ru = 'Распределение заданий по периодическим рейсам не удалось'");
			ПоказатьПредупреждение(, ТекстСообщения);
		Иначе	
			Если ИспользуютсяЭлектронныеГеографическиеКарты  Тогда
				ОчиститьОбъекты();
				ОтобразитьОбъектыНаКарте();
				ВыполнитьПроцедуруHTML("clearTrips()");
				ОтобразитьРейсыНаКарте();
				
				ИзменитьВидимостьЗон(ОтображатьЗоны);
			КонецЕсли;
			ОбновитьДоступность();
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры	

&НаКлиенте
// Процедура - Обработать ввод строки для комментария по рейсу.
//
// Параметры:
//  Строка - Строка - Комментарий.
//  ДополнительныеПараметры	 - ДокументСсылка.упРейс - ссылка на выбранный рейс.
//
Процедура ОбработатьВводСтрокиДляКомментарияПоРейсу(Строка, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
			И НЕ Строка = Неопределено Тогда
		текРейс = ДополнительныеПараметры.Рейс;
		КомментарийДоИзменения = ДополнительныеПараметры.КомментарийДоИзменения;
		Отказ = Ложь;
		Если НЕ КомментарийДоИзменения = Строка Тогда
			УстановитьКомментарийРейсу(текРейс, Строка, Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось установить комментарий рейсу, побробности см. журнал регистрации'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Функция ВернутьЗаданияРейсовНаСервере(МассивРейсов);
	
	Возврат Документы.упРейс.ВернутьЗаданияРейсов(МассивРейсов);
	
КонецФункции // ВернутьЗаданияРейсовНаСервере()

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	СхемаКомпоновкиДанных = Обработки.упРМПланированиеРейсов.ПолучитьМакет("ЗаданияНаПеревозку");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры // ИнициализироватьКомпоновщикНастроек()
	
&НаКлиенте
Функция ПодобратьСвободнуюЦветовуюСхему()
	
	СвободныеЦветовыеСхемы = ЦветовыеСхемы.НайтиСтроки(Новый Структура("Использована", Ложь));
	Если СвободныеЦветовыеСхемы.Количество() Тогда
		текЦветоваяСхема = СвободныеЦветовыеСхемы[0];
		текЦветоваяСхема.Использована = Истина;
	Иначе
		Для каждого текСхема Из ЦветовыеСхемы Цикл
			текСхема.Использована = Ложь;
		КонецЦикла; 
		текЦветоваяСхема = ЦветовыеСхемы[0];
		текЦветоваяСхема.Использована = Истина;
	КонецЕсли;
	Возврат текЦветоваяСхема;
	
КонецФункции // ПодобратьСвободнуюЦветовуюСхему()					

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции // ПредставлениеЧисла()

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтроки(Знач Строка)
	
	Строка = СтрЗаменить(Строка, """", "");
	Строка = СтрЗаменить(Строка, ";", "");
	Возврат Строка;
	
КонецФункции // ПредставлениеСтроки()

&НаСервере
Процедура СохранитьВариантНастроекВидаПеревозки(Настройки, КлючОбъекта)
	
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("ВидПеревозки", ВидПеревозки);
		Если КлючОбъекта = "Обработка.упРМПланированиеРейсов.ДоступныеТС." + ВидТранспортнойУслугиПредставление Тогда
			сткНастройки.Вставить("КлючТекущихНастроек", ВариантСписков);
		КонецЕсли; 
		сткНастройки.Вставить("Результат", Настройки);
		СоздатьНовыйВариант = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упВариантыНастроек.КлючВарианта
		|ИЗ
		|	Справочник.упВариантыНастроек КАК упВариантыНастроек
		|ГДЕ
		|   упВариантыНастроек.ВидПеревозки = &ВидПеревозки
		|	И упВариантыНастроек.Автор = &Автор
		|	И упВариантыНастроек.Объект = &Объект
		|	И НЕ упВариантыНастроек.ПометкаУдаления";
		Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		Запрос.УстановитьПараметр("Автор"       , ТекущийПользователь);
		Запрос.УстановитьПараметр("Объект"      , КлючОбъекта);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
				Если СохраненныеНастройки <> Неопределено Тогда
					Если КлючОбъекта = "Обработка.упРМПланированиеРейсов.ДоступныеТС." + ВидТранспортнойУслугиПредставление Тогда
						Если СохраненныеНастройки.Свойство("КлючТекущихНастроек") И СохраненныеНастройки.КлючТекущихНастроек = ВариантСписков Тогда
							ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, Выборка.КлючВарианта, сткНастройки);
							СоздатьНовыйВариант = Ложь;
							Прервать;
						КонецЕсли;
					Иначе
						 ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, Выборка.КлючВарианта, сткНастройки);
						 СоздатьНовыйВариант = Ложь;
						 Прервать;
					КонецЕсли; 
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СоздатьНовыйВариант Тогда
			НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
			НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
			НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
			НовыйВариант.ВидПеревозки = ВидПеревозки;
			НовыйВариант.Автор = ТекущийПользователь;
			НовыйВариант.ТолькоДляАвтора = Истина;
			НовыйВариант.Объект = КлючОбъекта;
			НовыйВариант.Записать();
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	 
КонецПроцедуры // СохранитьВариантНастроекВидаПеревозки()

&НаСервере
Процедура СохранитьВариантНастроекСпискаЗон()
	
	Настройки = ДанныеФормыВЗначение(СписокЗон, Тип("ТаблицаЗначений")).Скопировать(,"Зона, Пометка");
	
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("ВидПеревозки", ВидПеревозки);
		КлючОбъекта = "Обработка.упРМПланированиеРейсов.СписокЗон." + ВидТранспортнойУслугиПредставление; 
		сткНастройки.Вставить("КлючТекущихНастроек", ВариантСписков);
		сткНастройки.Вставить("Результат", Настройки);
		СоздатьНовыйВариант = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упВариантыНастроек.КлючВарианта
		|ИЗ
		|	Справочник.упВариантыНастроек КАК упВариантыНастроек
		|ГДЕ
		|   упВариантыНастроек.ВидПеревозки = &ВидПеревозки
		|	И упВариантыНастроек.Автор = &Автор
		|	И упВариантыНастроек.Объект = &Объект
		|	И НЕ упВариантыНастроек.ПометкаУдаления";
		Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		Запрос.УстановитьПараметр("Автор"       , ТекущийПользователь);
		Запрос.УстановитьПараметр("Объект"      , КлючОбъекта);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
				Если СохраненныеНастройки <> Неопределено Тогда
					Если КлючОбъекта = "Обработка.упРМПланированиеРейсов.СписокЗон." + ВидТранспортнойУслугиПредставление Тогда
						Если СохраненныеНастройки.Свойство("КлючТекущихНастроек") И СохраненныеНастройки.КлючТекущихНастроек = ВариантСписков Тогда
							ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, Выборка.КлючВарианта, сткНастройки);
							СоздатьНовыйВариант = Ложь;
							Прервать;
						КонецЕсли;
					Иначе
						 ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, Выборка.КлючВарианта, сткНастройки);
						 СоздатьНовыйВариант = Ложь;
						 Прервать;
					КонецЕсли; 
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СоздатьНовыйВариант Тогда
			НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
			НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
			НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
			НовыйВариант.ВидПеревозки = ВидПеревозки;
			НовыйВариант.Автор = ТекущийПользователь;
			НовыйВариант.ТолькоДляАвтора = Истина;
			НовыйВариант.Объект = КлючОбъекта;
			НовыйВариант.Записать();
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	 
КонецПроцедуры // СохранитьВариантНастроекСпискаЗон()

&НаСервере
Процедура СохранитьОбщиеНастройкиФормы()
	
	сткНастройки = Новый Структура;
	сткНастройки.Вставить("ВидПеревозки"             , ВидПеревозки);
	сткНастройки.Вставить("ПериодПланирования"       , ПериодПланирования);
	сткНастройки.Вставить("ВариантНастроек"          , ВариантСписков);
	сткНастройки.Вставить("ВариантПланирования"      , ВариантПланирования);
	сткНастройки.Вставить("НачалоПериода"            , НачалоПериодаПланирования);
	сткНастройки.Вставить("ОкончаниеПериода"         , ОкончаниеПериодаПланирования);
	сткНастройки.Вставить("ОтображатьЗоны"           , ОтображатьЗоны);
	сткНастройки.Вставить("ИспользоватьКластеризацию", ИспользоватьКластеризацию);
	сткНастройки.Вставить("ОтображатьМестаИнтереса"  , ОтображатьМестаИнтереса);
	сткНастройки.Вставить("РасположитьСпискиВертикально", РасположитьСпискиВертикально);
	сткНастройки.Вставить("ТочкаОтсчета", ТочкаОтсчета);
	КлючОбъекта = "Обработка.упРМПланированиеРейсов.ОбщиеНастройки." + ВидТранспортнойУслугиПредставление;
	
	Если КлючОбщихНастроекФормы = "" Тогда
		НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
		НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
		НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
		НовыйВариант.ВидПеревозки = ВидПеревозки;
		НовыйВариант.Автор = ТекущийПользователь;
		НовыйВариант.ТолькоДляАвтора = Истина;
		НовыйВариант.Объект = КлючОбъекта;
		НовыйВариант.Записать();
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
	Иначе
		СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, КлючОбщихНастроекФормы);
		Если СохраненныеНастройки <> Неопределено Тогда
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, КлючОбщихНастроекФормы, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СохранитьОбщиеНастройкиФормы()

&НаСервере
Процедура ВосстановитьОбщиеНастройкиФормы()
	
	КлючОбъекта = "Обработка.упРМПланированиеРейсов.ОбщиеНастройки." + ВидТранспортнойУслугиПредставление;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта,
	|	упВариантыНастроек.ВидПеревозки
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор" , ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено И ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
				ЭтаФорма.КлючОбщихНастроекФормы = Выборка.КлючВарианта;
				ЭтаФорма.ВидПеревозки = Выборка.ВидПеревозки;
				ЭтаФорма.ПериодПланирования = СохраненныеНастройки.ПериодПланирования;
				ЭтаФорма.ОтображатьЗоны = СохраненныеНастройки.ОтображатьЗоны;
				ЭтаФорма.ИспользоватьКластеризацию = СохраненныеНастройки.ИспользоватьКластеризацию;
				ЭтаФорма.ОтображатьМестаИнтереса = СохраненныеНастройки.ОтображатьМестаИнтереса;
				Если СохраненныеНастройки.Свойство("РасположитьСпискиВертикально") Тогда
					ЭтаФорма.РасположитьСпискиВертикально = СохраненныеНастройки.РасположитьСпискиВертикально;
					РасположитьСпискиВертикальноНаСервере(Ложь);
				КонецЕсли; 
				Если СохраненныеНастройки.Свойство("ТочкаОтсчета") Тогда
					Если Элементы.ТочкаОтсчета.Видимость Тогда
						ЭтаФорма.ТочкаОтсчета = СохраненныеНастройки.ТочкаОтсчета;
					КонецЕсли; 
				КонецЕсли; 
				Если ЗначениеЗаполнено(ПериодПланирования) Тогда
					сткДанныеПериода = ВернутьПериодОтбора(ПериодПланирования, ?(ЗначениеЗаполнено(ТочкаОтсчета), ТочкаОтсчета, Неопределено));
					ЭтаФорма.НачалоПериодаПланирования    = сткДанныеПериода.НачалоПериода;
					ЭтаФорма.ОкончаниеПериодаПланирования = сткДанныеПериода.ОкончаниеПериода;
				Иначе
					ЭтаФорма.НачалоПериодаПланирования    = СохраненныеНастройки.НачалоПериода;
					ЭтаФорма.ОкончаниеПериодаПланирования = СохраненныеНастройки.ОкончаниеПериода;
				КонецЕсли; 
				ЭтаФорма.ВариантСписков = СохраненныеНастройки.ВариантНастроек;
				сткНастройки = ВернутьВариантНастройкиСпискаЗаданий();
				Если сткНастройки <> Неопределено Тогда
					КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
				КонецЕсли;
				Если СохраненныеНастройки.Свойство("ВариантПланирования") Тогда
					//ЭтаФорма.ТекущийВариантПланирования = СохраненныеНастройки.ВариантПланирования;
					ЭтаФорма.ВариантПланирования = СохраненныеНастройки.ВариантПланирования;
					УстановитьПараметрыПланированияНаСервере();
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВосстановитьОбщиеНастройкиФормы()

&НаСервере
Процедура ВосстановитьВариантНабораДоступныхТС()
	
	СписокТранспорта.Очистить();
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упВариантыНастроек.КлючВарианта КАК КлючВарианта
		|ИЗ
		|	Справочник.упВариантыНастроек КАК упВариантыНастроек
		|ГДЕ
		|	упВариантыНастроек.ВидПеревозки = &ВидПеревозки
		|	И упВариантыНастроек.Автор = &Автор
		|	И упВариантыНастроек.Объект = &Объект
		|	И НЕ упВариантыНастроек.ПометкаУдаления";
		Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		Запрос.УстановитьПараметр("Автор"       , ТекущийПользователь);
		Запрос.УстановитьПараметр("Объект"      , "Обработка.упРМПланированиеРейсов.ДоступныеТС." + ВидТранспортнойУслугиПредставление);
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить("Обработка.упРМПланированиеРейсов.ДоступныеТС." + ВидТранспортнойУслугиПредставление, Выборка.КлючВарианта);
				Если СохраненныеНастройки <> Неопределено Тогда
					Если СохраненныеНастройки.Свойство("КлючТекущихНастроек") Тогда
						Если СохраненныеНастройки.КлючТекущихНастроек = ВариантСписков Тогда
							СписокТранспорта.Загрузить(СохраненныеНастройки.Результат);
							Прервать;
						КонецЕсли;
					КонецЕсли; 
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ИспользоватьВиртуальныеТранспортныеСредства = ПолучитьФункциональнуюОпцию("упИспользоватьВиртуальныеТранспортныеСредства");
		Если СписокТранспорта.Количество() = 0 Тогда
			Возврат;
		КонецЕсли; 
		
		Запрос = Новый Запрос;
		Запрос.Текст = Справочники.упВидыПеревозок.ПолучитьТекстЗапросаСоответствиеТранспортаВидуПеревозки();
		Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
		Запрос.УстановитьПараметр("ИспользоватьВиртуальныеТранспортныеСредства", ИспользоватьВиртуальныеТранспортныеСредства);
		Запрос.УстановитьПараметр("ТранспортныеСредства", ДанныеФормыВЗначение(СписокТранспорта, Тип("ТаблицаЗначений")));
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			СписокТранспорта.Очистить();
		Иначе	
			СписокТранспорта.Загрузить(Результат.Выгрузить());
		КонецЕсли; 
		Для Каждого СтрокаТаблицы Из СписокТранспорта Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.ТранспортноеСредство) Тогда
				сткДанные = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.ТранспортноеСредство, "Партнер, ТипТС");
				СтрокаТаблицы.Перевозчик = сткДанные.Партнер;
				СтрокаТаблицы.ТипТС = сткДанные.ТипТС;
			КонецЕсли; 
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры // ВосстановитьВариантНабораДоступныхТС()

&НаСервере
Функция ВернутьВариантНастройкиСпискаЗаданий()
	
	Возврат ВернутьВариантНастройки("НастройкаСпискаЗаданий", ВариантСписков);
	
КонецФункции

&НаСервере
Функция ВернутьВариантНастройкиПланирования()
	
	Возврат ВернутьВариантНастройки("ПараметрыПланирования", ВариантПланирования);
	
КонецФункции

&НаСервере
Функция ВернутьВариантНастройки(ТипНастройки, КлючНастроек)
	
	Возврат ХранилищаНастроек.упХранилищеНастроек.Загрузить("Обработка.упРМПланированиеРейсов." + ТипНастройки + "." + ВидТранспортнойУслугиПредставление, КлючНастроек);
	
КонецФункции

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовНастроек(ТипНастройки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.КлючВарианта,
	|	упВариантыНастроек.Наименование,
	|	упВариантыНастроек.ПометкаУдаления
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.ВидПеревозки = &ВидПеревозки
	|	И упВариантыНастроек.Объект = &Объект
	|	И (упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь)";
	Запрос.УстановитьПараметр("ВидПеревозки"       , ВидПеревозки);
	Запрос.УстановитьПараметр("Объект"             , "Обработка.упРМПланированиеРейсов." + ТипНастройки + "." + ВидТранспортнойУслугиПредставление);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	ДоступныеВариантов = Запрос.Выполнить().Выгрузить();
	
	Если ТипНастройки = "НастройкаСпискаЗаданий" Тогда
		ПолеФормы = Элементы.ВариантСписков;
	ИначеЕсли ТипНастройки = "ПараметрыПланирования" Тогда
		ПолеФормы = Элементы.ВариантПланирования;
	Иначе
		ВызватьИсключение "Неизвестный тип настройки " + ТипНастройки;
	КонецЕсли;
	ПолеФормы.СписокВыбора.Очистить();
	Для каждого СтрокаВарианта Из ДоступныеВариантов Цикл
		Если СтрокаВарианта.ПометкаУдаления Тогда
			ПолеФормы.СписокВыбора.Добавить(СтрокаВарианта.КлючВарианта, СтрокаВарианта.Наименование,, БиблиотекаКартинок.Удалить);
		Иначе
			ПолеФормы.СписокВыбора.Добавить(СтрокаВарианта.КлючВарианта, СтрокаВарианта.Наименование);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // УстановитьДоступныеЗначенияВариантовНастроекСпискаЗаданий()

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовСписков()
	
	УстановитьДоступныеЗначенияВариантовНастроек("НастройкаСпискаЗаданий");
	
КонецПроцедуры // УстановитьДоступныеЗначенияВариантовНастроекСпискаЗаданий()

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовПланирования()
	
	УстановитьДоступныеЗначенияВариантовНастроек("ПараметрыПланирования");
	
КонецПроцедуры // УстановитьДоступныеЗначенияВариантовНастроекСпискаЗаданий()

&НаСервере
Функция ВернутьМассивОрганизаций()
	
	тзОрганизации = СписокЗаданий.Выгрузить(, "Организация");
	тзОрганизации.Свернуть("Организация");
	Возврат тзОрганизации.ВыгрузитьКолонку("Организация");
	
КонецФункции // ВернутьМассивОрганизаций()

&НаСервереБезКонтекста
Функция ВернутьПериодОтбора(ПериодПланирования, ТочкаОтсчета)
	
	Возврат Справочники.упПериодыПланирования.РассчитатьПериодПланирования(ПериодПланирования, ТочкаОтсчета);	
	
КонецФункции // ВернутьПериодОтбора()

&НаСервере
Функция ПолучитьКоординатыТочекСтрокойНаСервере(ТолькоВидимые = Ложь)
	
	Координаты = "";
	сткОтбор = Новый Структура;
	сткОтбор.Вставить("Геокодировано", Истина);
	Если ТолькоВидимые Тогда
		сткОтбор.Вставить("Пометка", Истина);
	КонецЕсли;
	
	тзЗадания = СписокЗаданий.Выгрузить(сткОтбор);
	Если тзЗадания.Количество() Тогда
		мсвАдресовОтправления = тзЗадания.ВыгрузитьКолонку("АдресОтправления");
		мсвАдресовПолучения = тзЗадания.ВыгрузитьКолонку("АдресПолучения");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвАдресовОтправления, мсвАдресовПолучения);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упАдреса.Широта КАК Широта,
		|	упАдреса.Долгота КАК Долгота
		|ПОМЕСТИТЬ втКоординаты
		|ИЗ
		|	Справочник.упАдреса КАК упАдреса
		|ГДЕ
		|	упАдреса.Ссылка В(&СписокАдресов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(втКоординаты.Широта) КАК МаксШирота,
		|	МИНИМУМ(втКоординаты.Широта) КАК МинШирота,
		|	МАКСИМУМ(втКоординаты.Долгота) КАК МаксДолгота,
		|	МИНИМУМ(втКоординаты.Долгота) КАК МинДолгота
		|ИЗ
		|	втКоординаты КАК втКоординаты";
		Запрос.УстановитьПараметр("СписокАдресов", ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвАдресовОтправления));
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			Координаты = ПредставлениеЧисла(Выборка.МаксШирота) + ";" + ПредставлениеЧисла(Выборка.МаксДолгота) + ";" + ПредставлениеЧисла(Выборка.МинШирота) + ";" + ПредставлениеЧисла(Выборка.МинДолгота) + ";";
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Координаты;
	
КонецФункции // ПолучитьКоординатыТочекСтрокойНаСервере()

&НаСервереБезКонтекста
Функция ВернутьПересекающиесяВТочкахРейсыНаСервере(МассивРейсов, ПересечениеЗаявок)
	
	Если ПересечениеЗаявок Тогда
		Возврат Документы.упРейс.ВернутьПересекающиесяЗаявкиВТочкахРейсы(МассивРейсов);
	КонецЕсли; 
	
	Возврат Документы.упРейс.ВернутьПересекающиесяВТочкахРейсы(МассивРейсов);
	
КонецФункции // ВернутьПересекающиесяВТочкахРейсыНаСервере()

&НаСервереБезКонтекста
// Функция - Получить комментарий.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - рейс.
// 
// Возвращаемое значение:
//  Строка - комментарий рейса.
//
Функция ПолучитьКомментарий(Рейс)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "Комментарий");
	
КонецФункции

&НаСервереБезКонтекста
// Процедура - Установить комментарий рейсу.
//
// Параметры:
//  Рейс		 - ДокументСсылка.упРейс - рейс.
//  Комментарий	 - Строка	 - комментарий.
//  Отказ		 - Булево	 - Истина в случае отказа.
//
Процедура УстановитьКомментарийРейсу(Рейс, Комментарий, Отказ)
	
	Если ЗначениеЗаполнено(Рейс) Тогда
		РейсОбъект = Рейс.ПолучитьОбъект();	
		//РейсОбъект.ОптимизацияМаршрута = Истина;
		РейсОбъект.Комментарий	= Комментарий;
		Попытка
			РейсОбъект.Записать(РежимЗаписиДокумента.Запись);	
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОшибкаУстановкиКомментария", УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКартой

&НаКлиенте
Функция	ИзменитьФорматВременногоФайла(Имя)
	
	Возврат "file://" + СтрЗаменить(Имя,"\","/");
	
КонецФункции // ИзменитьФорматВременногоФайла()

&НаКлиенте
Процедура ОбработатьИзменениеРежимаОтображенияКарты()
	
	Для каждого Строка Из СписокЗаданий Цикл
		Если Строка.Пометка Тогда
			Если РежимОтображенияКарты = 1 Тогда
				Если Строка.ИндексВМассивеЛиния > 0 Тогда
					ИзменитьВидимостьОбъекта(Строка.ИндексВМассивеЛиния, Истина);
				Иначе
					Строка.ИндексВМассивеЛиния = ОтобразитьОбъект(, Строка.ХарактеристикиЛинии, Строка.КоординатыОтправления + ";" + Строка.КоординатыПолучения + ";", 1);
				КонецЕсли;
			ИначеЕсли РежимОтображенияКарты = 2 Тогда
				Если Строка.ИндексВМассивеМаршрут > 0 Тогда
					ИзменитьВидимостьОбъекта(Строка.ИндексВМассивеМаршрут, Истина);
				Иначе
					Строка.ИндексВМассивеМаршрут = ОтобразитьОбъект(, Строка.ХарактеристикиЛинии, ?(Строка.Путь = "", Строка.КоординатыОтправления + ";" + Строка.КоординатыПолучения + ";", Строка.Путь), 2);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ОбработатьИзменениеРежимаОтображенияКарты()

&НаКлиенте
Процедура СкрытьОбъектыПриИзмененииРежимаОтображенияКарты(Режим)
	
	Для каждого Строка Из СписокЗаданий Цикл
		Если Строка.Пометка Тогда
			Если Режим = 1 И Строка.ИндексВМассивеЛиния > 0 Тогда
				ИзменитьВидимостьОбъекта(Строка.ИндексВМассивеЛиния, Ложь);
			ИначеЕсли Режим = 2 И Строка.ИндексВМассивеМаршрут > 0 Тогда
				ИзменитьВидимостьОбъекта(Строка.ИндексВМассивеМаршрут, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // СкрытьОбъектыПриИзмененииРежимаОтображенияКарты()

&НаКлиенте
Процедура УстановитьРежимМножественногоВыделения(Пометка)
	
	Если Пометка Тогда
		ВыполнитьПроцедуруHTML("setMultipleSelectionMode(true)");
	Иначе
		ВыполнитьПроцедуруHTML("setMultipleSelectionMode(false)");
	КонецЕсли;
	
КонецПроцедуры // УстановитьРежимМножественногоВыделения()

&НаКлиенте
Процедура УстановитьРежимМножественногоСнятияВыделения(Пометка)
	
	Если Пометка Тогда
		ВыполнитьПроцедуруHTML("setMultipleUnselectionMode(true)");
	Иначе
    	ВыполнитьПроцедуруHTML("setMultipleUnselectionMode(false)");
	КонецЕсли;
	
КонецПроцедуры // УстановитьРежимМножественногоСнятияВыделения()

&НаКлиенте
Процедура ИзменитьРежимКластеризации()
	
	Если ИспользоватьКластеризацию Тогда
		ВыполнитьПроцедуруHTML("setMarkerClustering(true)");
	Иначе
		ВыполнитьПроцедуруHTML("setMarkerClustering(false)");
	КонецЕсли;
	
КонецПроцедуры // ИзменитьРежимКластеризации();

&НаКлиенте
Процедура УстановитьРежимКластеризации()
	
	Индексы = "";
	ИндексыЛинииМаршруты = "";
	Для каждого текЗадание Из СписокЗаданий Цикл
		Если текЗадание.Пометка И текЗадание.ИндексВМассивеПолучения > 0 Тогда
			Индексы = Индексы + ПредставлениеЧисла(текЗадание.ИндексВМассивеПолучения) + ";";
			Если РежимОтображенияКарты = 1 И текЗадание.ИндексВМассивеЛиния > 0 Тогда // линии
				ИндексыЛинииМаршруты = ИндексыЛинииМаршруты + ПредставлениеЧисла(текЗадание.ИндексВМассивеЛиния) + ";"
			ИначеЕсли РежимОтображенияКарты = 2 И текЗадание.ИндексВМассивеМаршрут > 0 Тогда // маршруты
				ИндексыЛинииМаршруты = ИндексыЛинииМаршруты + ПредставлениеЧисла(текЗадание.ИндексВМассивеМаршрут) + ";"
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла; 
	Для каждого текОтправление Из СписокОтправлений Цикл
		Если текОтправление.ИндексВМассиве > 0 Тогда
			Индексы = Индексы + ПредставлениеЧисла(текОтправление.ИндексВМассиве) + ";"
		КонецЕсли;
	КонецЦикла; 
	Если ЗначениеЗаполнено(ИндексыЛинииМаршруты) Тогда
		ИзменитьВидимостьОбъектов(ИндексыЛинииМаршруты, Ложь);
	КонецЕсли;
	Если ЗначениеЗаполнено(Индексы) Тогда
		ВыполнитьПроцедуруHTML("switchOnClustering(""" + Индексы + """)");	
	КонецЕсли;
	
КонецПроцедуры // УстановитьРежимКластеризации()

&НаКлиенте
Процедура УбратьРежимКластеризации()
	
	Если РежимОтображенияКарты > 0 Тогда
		ИндексыЛинииМаршруты = "";
		ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("Пометка", Истина));
		Для каждого Строка Из ИскомыеСтроки Цикл
			Если РежимОтображенияКарты = 1 И Строка.ИндексВМассивеЛиния > 0 Тогда // линии
				ИндексыЛинииМаршруты = ИндексыЛинииМаршруты + ПредставлениеЧисла(Строка.ИндексВМассивеЛиния) + ";"
			ИначеЕсли РежимОтображенияКарты = 2 И Строка.ИндексВМассивеМаршрут > 0 Тогда // маршруты
				ИндексыЛинииМаршруты = ИндексыЛинииМаршруты + ПредставлениеЧисла(Строка.ИндексВМассивеМаршрут) + ";"
			КонецЕсли;
		КонецЦикла; 
		Если ЗначениеЗаполнено(ИндексыЛинииМаршруты) Тогда
			ИзменитьВидимостьОбъектов(ИндексыЛинииМаршруты, Истина);
		КонецЕсли;
	КонецЕсли;
	ВыполнитьПроцедуруHTML("switchOffClustering()");
	
КонецПроцедуры // УбратьРежимКластеризации()

&НаКлиенте
Процедура УстановитьРежимТеплокарты()
	
	ДанныеТеплокарты = ПодготовитьДанныеДляТеплокарты();
	
	Если ЗначениеЗаполнено(ДанныеТеплокарты) Тогда
		ВыполнитьПроцедуруHTML("switchOnHeatmap(""" + ДанныеТеплокарты + """)");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьРежимТеплокарты()
	
	ВыполнитьПроцедуруHTML("switchOffHeatmap()");
	
КонецПроцедуры // УбратьРежимКластеризации()

&НаКлиенте
Функция ПодготовитьДанныеДляТеплокарты()
	
	ДанныеТеплокарты = "";
	максЗначение = 1;
	МассивКоординат = Новый Массив;
	Если Измерение = "weight" Тогда
		Для каждого текЗадание Из СписокЗаданий Цикл
			Если текЗадание.Масса = 0 Тогда
				Продолжить;
			КонецЕсли; 
			Если текЗадание.ЭтоВозвратГруза Тогда
				МассивКоординат.Добавить(текЗадание.КоординатыОтправления);
			Иначе	
				МассивКоординат.Добавить(текЗадание.КоординатыПолучения);
			КонецЕсли; 
			текЗначение = текЗадание.Масса;
			максЗначение = Макс(текЗначение, максЗначение);
			МассивКоординат.Добавить(ПредставлениеЧисла(текЗначение));	
		КонецЦикла;
	ИначеЕсли Измерение = "volume" Тогда
		Для каждого текЗадание Из СписокЗаданий Цикл
			Если текЗадание.Объем = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если текЗадание.ЭтоВозвратГруза Тогда
				МассивКоординат.Добавить(текЗадание.КоординатыОтправления);
			Иначе	
				МассивКоординат.Добавить(текЗадание.КоординатыПолучения);
			КонецЕсли; 
			текЗначение = текЗадание.Объем;
			максЗначение = Макс(текЗначение, максЗначение);
			МассивКоординат.Добавить(ПредставлениеЧисла(текЗначение));	
		КонецЦикла;
		
	ИначеЕсли Измерение = "quantityCargo" Тогда
		Для каждого текЗадание Из СписокЗаданий Цикл
			Если текЗадание.КоличествоМест = 0 Тогда
				Продолжить;
			КонецЕсли;

			Если текЗадание.ЭтоВозвратГруза Тогда
				МассивКоординат.Добавить(текЗадание.КоординатыОтправления);
			Иначе	
				МассивКоординат.Добавить(текЗадание.КоординатыПолучения);
			КонецЕсли; 
			текЗначение = текЗадание.КоличествоМест;
			максЗначение = Макс(текЗначение, максЗначение);
			МассивКоординат.Добавить(ПредставлениеЧисла(текЗначение));	
		КонецЦикла;
		
	ИначеЕсли Измерение = "quantityMeters" Тогда
		Для каждого текЗадание Из СписокЗаданий Цикл
			Если текЗадание.КоличествоЗагрузочныхМетров = 0 Тогда
				Продолжить;
			КонецЕсли;

			Если текЗадание.ЭтоВозвратГруза Тогда
				МассивКоординат.Добавить(текЗадание.КоординатыОтправления);
			Иначе	
				МассивКоординат.Добавить(текЗадание.КоординатыПолучения);
			КонецЕсли; 
			текЗначение = текЗадание.КоличествоЗагрузочныхМетров;
			максЗначение = Макс(текЗначение, максЗначение);
			МассивКоординат.Добавить(ПредставлениеЧисла(текЗначение));	
		КонецЦикла;
		
	ИначеЕсли Измерение = "timeWindows" Тогда
		ПолныйИнтервал = (ОкончаниеПериодаПланирования - НачалоПериодаПланирования)/3600;
		
		Для каждого текЗадание Из СписокЗаданий Цикл
			Если текЗадание.ЭтоВозвратГруза Тогда
				МассивКоординат.Добавить(текЗадание.КоординатыОтправления);
				
				текЗначение = ((Макс(текЗадание.ОтправлениеГрузаС, НачалоПериодаПланирования) - НачалоПериодаПланирования)/3600
				+ 
				(?(ЗначениеЗаполнено(текЗадание.ОтправлениеГрузаПо), текЗадание.ОтправлениеГрузаПо, ОкончаниеПериодаПланирования) - НачалоПериодаПланирования)/3600)/(2*ПолныйИнтервал);
			Иначе	
				МассивКоординат.Добавить(текЗадание.КоординатыПолучения);
				
				текЗначение = ((Макс(текЗадание.ПолучениеГрузаС, НачалоПериодаПланирования) - НачалоПериодаПланирования)/3600
				+ 
				(?(ЗначениеЗаполнено(текЗадание.ПолучениеГрузаПо), текЗадание.ПолучениеГрузаПо, ОкончаниеПериодаПланирования) - НачалоПериодаПланирования)/3600)/(2*ПолныйИнтервал);
			КонецЕсли; 
			//максЗначение = Макс(текЗначение, максЗначение);
			МассивКоординат.Добавить(ПредставлениеЧисла(Окр(текЗначение, 1)));	
		КонецЦикла;
	КонецЕсли;
		
	Если МассивКоординат.Количество() Тогда
		ДанныеТеплокарты = СтрСоединить(МассивКоординат, ";") + """," + ПредставлениеЧисла(максЗначение) + ", 14, """ + Измерение; 
	КонецЕсли; 
	
	Возврат ДанныеТеплокарты;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьВидимостьЗон(Пометка = Ложь)
	
	Если Пометка Тогда
		соотДанныеЗон = Новый Соответствие;
		мсвЗон        = Новый Массив;
		мсвНепомеч    = Новый Массив;
		Для каждого текЗона Из СписокЗон Цикл
			Если Не текЗона.Пометка И текЗона.ИндексВМассиве > 0 Тогда 
				ИзменитьВидимостьЗоны(текЗона.ИндексВМассиве, Ложь);
			ИначеЕсли текЗона.Пометка И	текЗона.ИндексВМассиве > 0 Тогда
				ИзменитьВидимостьЗоны(текЗона.ИндексВМассиве, Истина);
			Иначе
				Если ЗначениеЗаполнено(текЗона.Данные) Тогда
					текЗона.ИндексВМассиве = ОтобразитьЗону(текЗона.Данные);
					Если Не текЗона.Пометка Тогда
						мсвНепомеч.Добавить(текЗона.Зона);
					КонецЕсли;	
				Иначе	
					соотДанныеЗон.Вставить(текЗона.Зона, текЗона); 
					мсвЗон.Добавить(текЗона.Зона);
					Если Не текЗона.Пометка Тогда
						мсвНепомеч.Добавить(текЗона.Зона);
					КонецЕсли;	
				КонецЕсли;  
			КонецЕсли; 
		КонецЦикла; 
		
		Если мсвЗон.Количество() Тогда
			мсвДанныеЗон = ПолучитьДанныеЗон(мсвЗон);
			Для каждого текДанныеЗоны Из мсвДанныеЗон Цикл
				текСтрока = соотДанныеЗон.Получить(текДанныеЗоны.Зона);
				текСтрока.Данные = текДанныеЗоны.Данные;
				текСтрока.ИндексВМассиве = ОтобразитьЗону(текСтрока.Данные);
			КонецЦикла; 
			Для Каждого текЗона Из мсвНепомеч Цикл  
				текСтрока = соотДанныеЗон.Получить(текЗона);
				ИзменитьВидимостьЗоны(текСтрока.ИндексВМассиве, Ложь);	
			КонецЦикла;	
		КонецЕсли; 
		
	Иначе
		Для каждого текЗона Из СписокЗон Цикл
			Если текЗона.ИндексВМассиве > 0 Тогда
				//текЗона.Пометка = Ложь;
				ИзменитьВидимостьЗоны(текЗона.ИндексВМассиве, Ложь); 
			КонецЕсли; 
		КонецЦикла;
		
	КонецЕсли; 	
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьМестИнтереса(Пометка = Ложь)
	
	Если Пометка Тогда
		
		ОбновитьМестаИнтересаНаКлиенте();
		
		Для каждого МестоИнтереса Из СписокМестаИнтереса Цикл
			Если МестоИнтереса.ИндексВМассиве = 0 Тогда
				МестоИнтереса.ИндексВМассиве = ОтобразитьМестаИнтереса(МестоИнтереса);
			Иначе
				ИзменитьВидимостьМестаИнтереса(МестоИнтереса.ИндексВМассиве, Истина);
			КонецЕсли;
		КонецЦикла;		
	Иначе
		Для каждого МестоИнтереса Из СписокМестаИнтереса Цикл
			Если МестоИнтереса.ИндексВМассиве > 0 Тогда
				ИзменитьВидимостьМестаИнтереса(МестоИнтереса.ИндексВМассиве, Ложь);
			КонецЕсли; 
		КонецЦикла;	
	КонецЕсли; 	
	
КонецПроцедуры // ИзменитьВидимостьМестИнтереса()

&НаСервере
Функция ПолучитьДанныеЗон(МассивЗон)
	
    мсвДанныеЗон = Новый Массив;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка,
	|	упГеографическиеЗоны.Наименование,
	|	ЕСТЬNULL(упСтилиПолигонов.ЦветГраницы, упСтильОсновной.ЦветГраницы) КАК ЦветГраницы,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ТолщинаГраницы, упСтильОсновной.ТолщинаГраницы) КАК ТолщинаГраницы,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ПрозрачностьГраницы, упСтильОсновной.ПрозрачностьГраницы) КАК ПрозрачностьГраницы,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ЦветЗаливки, упСтильОсновной.ЦветЗаливки) КАК ЦветЗаливки,
	|	ЕСТЬNULL(упГеографическиеЗоны.СтильОтображения.ПрозрачностьЗаливки, упСтильОсновной.ПрозрачностьЗаливки) КАК ПрозрачностьЗаливки
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтилиПолигонов КАК упСтилиПолигонов
	|		ПО упГеографическиеЗоны.СтильОтображения = упСтилиПолигонов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСтилиПолигонов КАК упСтильОсновной
	|		ПО (упСтильОсновной.Ссылка = ЗНАЧЕНИЕ(Справочник.упСтилиПолигонов.Основной))
	|ГДЕ
	|	упГеографическиеЗоны.Ссылка В(&МассивЗон)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Полигон.Ссылка КАК Ссылка,
	|	Полигон.Широта,
	|	Полигон.Долгота
	|ИЗ
	|	Справочник.упГеографическиеЗоны.Полигон КАК Полигон
	|ГДЕ
	|	Полигон.Ссылка В(&МассивЗон)
	|ИТОГИ ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("МассивЗон", МассивЗон);
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[1].Пустой() Тогда   
		ВыборкаДанных = Результат[0].Выгрузить();
		Выборка       = Результат[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Пока Выборка.Следующий() Цикл
			ВыборкаКоординат = Выборка.Выбрать();
			Координаты = "";
			Пока ВыборкаКоординат.Следующий() Цикл
				Координаты = Координаты + ПредставлениеЧисла(ВыборкаКоординат.Широта) + ";" + ПредставлениеЧисла(ВыборкаКоординат.Долгота) + ";";			
			КонецЦикла;
			Если Координаты = "" Тогда
				Продолжить;
			КонецЕсли; 
			
			ИскомаяСтрока = ВыборкаДанных.Найти(Выборка.Ссылка, "Ссылка");
			Если Не ИскомаяСтрока = Неопределено Тогда
				текПредставлениеЗоны = ИскомаяСтрока.Наименование;
				Данные  =  """" + Координаты + """, """ + ПредставлениеСтроки(текПредставлениеЗоны) + """, """ + ИскомаяСтрока.ЦветГраницы + """, " + ПредставлениеЧисла(ИскомаяСтрока.ТолщинаГраницы) 
				                             + ", " + ПредставлениеЧисла(1 - ИскомаяСтрока.ПрозрачностьГраницы / 100)
				                             + ", """ + ИскомаяСтрока.ЦветЗаливки
				                             + """, " + ПредставлениеЧисла(1 - ИскомаяСтрока.ПрозрачностьЗаливки / 100);
											 
											 
				мсвДанныеЗон.Добавить(Новый Структура("Зона, Данные", Выборка.Ссылка, Данные));											 
			 КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат мсвДанныеЗон;
	
КонецФункции

&НаКлиенте
// Процедура позиционирует слой на карте.
//
// Параметры:
//  ИндексВМассиве - Число - Индекс в массиве объектов(слоев, рейсов), помещенных на карту.
//  Режим - Число - числовое представление режима отображения (0 - точки, 1 - линии, 2 - маршруты, 3 - рейсы).
//
Процедура ЦентрироватьСлойНаКарте(ИндексВМассиве,  Режим = 0)
	
	Если Режим = 0 Тогда
		ВыполнитьПроцедуруHTML("gotoObjectMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		
	ИначеЕсли Режим = 1 Или Режим = 2 Тогда
		ВыполнитьПроцедуруHTML("gotoObjectPolygon(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		
	ИначеЕсли Режим = 3 Тогда
		ВыполнитьПроцедуруHTML("gotoTrip(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	
	КонецЕсли; 
	
КонецПроцедуры // ЦентрироватьСлойНаКарте()

&НаКлиенте
Процедура ЦентрироватьТочкиНаКарте(Координаты)
	
	ВыполнитьПроцедуруHTML("FillArrayPoint(""" + Координаты + """)");
	ВыполнитьПроцедуруHTML("fitMapToBounds()");
	
	//мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
	//Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
	//	ВыполнитьПроцедуруHTML("addPoint(" + мсвКоординат[Сч * 2] + ", " + мсвКоординат[(Сч * 2) + 1] + ")");
	//КонецЦикла;
	//ВыполнитьПроцедуруHTML("fitMapToBounds()");
	
КонецПроцедуры // ЦентрироватьНерассчитанныйРейсНаКарте()

&НаКлиенте
// Процедура очищает все объекты на карте
//
Процедура ОчиститьОбъекты()
	
	Если ИспользоватьКластеризацию Тогда
		ВыполнитьПроцедуруHTML("setMarkerClustering(false)");
	КонецЕсли;
	ВыполнитьПроцедуруHTML("clearObjects()");
	ВыполнитьПроцедуруHTML("clearInterfaceData()");
	ВыполнитьПроцедуруHTML("clearCirclesWithNumber()");
	ВыполнитьПроцедуруHTML("clearVehicleArrayPlanning()");
	Для каждого текОтправление Из СписокОтправлений Цикл
		текОтправление.ИндексВМассиве = 0;
	КонецЦикла; 
	
КонецПроцедуры // ОчиститьОбъекты()

&НаКлиенте
// Процедура очищает все рейсы на карте
//
Процедура ОчиститьРейсы()
	
	ОчиститьМаркерыРейсов();
	ВыполнитьПроцедуруHTML("clearTrips()");
	
КонецПроцедуры // ОчиститьРейсы()

&НаКлиенте
Процедура ОчиститьМаркерыРейсов(МассивИндексов = Неопределено)
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	мсвМаркеров = Новый Массив;
	Если МассивИндексов = Неопределено Тогда
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если ЗначениеЗаполнено(текРейс.Рейс) Тогда
				Для каждого текАдресОтправления Из текРейс.АдресаОтправлений Цикл
					сткДанныеАдреса = текАдресОтправления.Значение;
					Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
						мсвМаркеров.Добавить(сткДанныеАдреса.ИндексВМассиве);
					КонецЕсли;
				КонецЦикла; 
				ЭлементыЗадания = текРейс.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					Если текЗадание.Пометка И текЗадание.ИндексВМассиве > 0 Тогда
						мсвМаркеров.Добавить(текЗадание.ИндексВМассиве);			
					КонецЕсли;
				КонецЦикла; 
			КонецЕсли;
		КонецЦикла; 
	Иначе
		Для каждого текИндекс Из МассивИндексов Цикл
			текСтрокаДерева = ЭлементыРейсы.Получить(текИндекс);
			Если ЗначениеЗаполнено(текСтрокаДерева.Рейс) Тогда
				Для каждого текАдресОтправления Из текСтрокаДерева.АдресаОтправлений Цикл
					сткДанныеАдреса = текАдресОтправления.Значение;
					Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
						мсвМаркеров.Добавить(сткДанныеАдреса.ИндексВМассиве);
					КонецЕсли;
				КонецЦикла;
				ЭлементыЗадания = текСтрокаДерева.ПолучитьЭлементы();
				Для каждого текЗадание Из ЭлементыЗадания Цикл
					Если текЗадание.Пометка И текЗадание.ИндексВМассиве > 0 Тогда
						мсвМаркеров.Добавить(текЗадание.ИндексВМассиве);			
					КонецЕсли;
				КонецЦикла; 
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	мсвМаркеров = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвМаркеров);
	Для каждого текМаркер Из мсвМаркеров Цикл
		УдалитьОбъект(текМаркер, Истина);
	КонецЦикла; 
	
КонецПроцедуры // ОчиститьМаркерыРейсов()

&НаСервере
Функция ПолучитьИндексыМаркеровРейса(СтрокаРейса)
	
	мсвИндексов = Новый Массив;
	Для каждого текАдресОтправления Из СтрокаРейса.АдресаОтправлений Цикл
		сткДанныеАдреса = текАдресОтправления.Значение;
		Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
			мсвИндексов.Добавить(сткДанныеАдреса.ИндексВМассиве);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыЗадания = СтрокаРейса.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыЗадания Цикл
		Если текЗадание.Пометка И текЗадание.ИндексВМассиве > 0 Тогда
			мсвИндексов.Добавить(текЗадание.ИндексВМассиве);			
		КонецЕсли;
	КонецЦикла;
	
	Возврат мсвИндексов;

КонецФункции // ПолучитьИндексыМаркеровРейса()

&НаКлиенте
Функция ПолучитьИндексыМаркеровРейсаНаКлиенте(СтрокаРейса)
	
	мсвИндексов = Новый Массив;
	Для каждого текАдресОтправления Из СтрокаРейса.АдресаОтправлений Цикл
		сткДанныеАдреса = текАдресОтправления.Значение;
		Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
			мсвИндексов.Добавить(сткДанныеАдреса.ИндексВМассиве);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыЗадания = СтрокаРейса.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыЗадания Цикл
		Если текЗадание.Пометка И текЗадание.ИндексВМассиве > 0 Тогда
			мсвИндексов.Добавить(текЗадание.ИндексВМассиве);			
		КонецЕсли;
	КонецЦикла;
	
	Возврат мсвИндексов;

КонецФункции // ПолучитьИндексыМаркеровРейсаНаКлиенте()

&НаКлиенте
Процедура ИзменитьВидимостьОбъекта(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда 
		ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьОбъекта()

&НаКлиенте
Процедура ИзменитьВидимостьОбъектов(Знач Индексы, Знач Пометка, ИспользоватьКластеризацию = Истина)
	
	Если Пометка Тогда 
		Если ИспользоватьКластеризацию Тогда
			ВыполнитьПроцедуруHTML("showObjects(""" + Индексы + """, true)");
		Иначе 
			ВыполнитьПроцедуруHTML("showObjects(""" + Индексы + """, false)");
		КонецЕсли;
	Иначе	
		Если ИспользоватьКластеризацию Тогда
			ВыполнитьПроцедуруHTML("hideObjects(""" + Индексы + """, true)");
		Иначе 
			ВыполнитьПроцедуруHTML("hideObjects(""" + Индексы + """, false)");
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьОбъектов()

&НаКлиенте
Процедура УдалитьОбъект(ИндексВМассиве, ТочкаМаршрута = Ложь)
	
	Если ИспользоватьКластеризацию И Не ТочкаМаршрута Тогда
		ВыполнитьПроцедуруHTML("deleteObjectMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ", true)");
	Иначе	
		ВыполнитьПроцедуруHTML("deleteObjectMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ", false)");
	КонецЕсли; 
	
КонецПроцедуры // УдалитьОбъект();

&НаКлиенте
Процедура УдалитьОтображениеРейса(ИндексВМассиве)
	
	ВыполнитьПроцедуруHTML("deleteTrip(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	
КонецПроцедуры // УдалитьРейс();

&НаКлиенте
Процедура ИзменитьВидимостьРейса(ТекущийРейс, НоваяВидимость = Неопределено)
	
	//Если Не ЗначениеЗаполнено(ТекущийРейс.КоординатыТочек) Тогда
	//	Возврат;
	//КонецЕсли; 
	ИндексВМассиве = ТекущийРейс.ИндексВМассивеМаршрут;
	Если НоваяВидимость = Неопределено Тогда
		НоваяВидимость = ТекущийРейс.Пометка;
	КонецЕсли; 
	Если НоваяВидимость Тогда 
		Если ИндексВМассиве > 0 Тогда
			ВыполнитьПроцедуруHTML("showTrip(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		Иначе
			ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
			СписокИндексов.Добавить(ЭлементыДерева.Индекс(ТекущийРейс));
			ОтобразитьРейсыНаКарте();
		КонецЕсли;
	Иначе	
		ВыполнитьПроцедуруHTML("hideTrip(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьРейса()

&НаКлиенте
Процедура ИзменитьВидимостьЗоны(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда 
		ВыполнитьПроцедуруHTML("showZone(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideZone(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьЗоны()

&НаКлиенте
Процедура ИзменитьВидимостьМестаИнтереса(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда
		ВыполнитьПроцедуруHTML("showSiteInterest(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideSiteInterest(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьМестаИнтереса()

&НаКлиенте
Процедура ИзменитьСтильРейса(ИндексВМассиве, Данные)
	
	ВыполнитьПроцедуруHTML("setTripStyle(" + ПредставлениеЧисла(ИндексВМассиве) + ", " + Данные + ")");
	
КонецПроцедуры // ИзменитьСтильРейса()

// Процедура выводит объекты на карту
//
&НаКлиенте
Процедура ОтобразитьОбъектыНаКарте(МассивСтрок = Неопределено)
	
	Если МассивСтрок = Неопределено Тогда
		МассивСтрок = СписокЗаданий;
		ОбновлятьПолностью = Истина;
		
		// отправим данные для отображения представлений в html
		ВыполнитьПроцедуруHTML("addInterfaceData(""" + НСтр("ru = 'Адрес:'")+ """)");
		ВыполнитьПроцедуруHTML("addInterfaceData(""" + Элементы.ПоказателиЗагрузкиСпискаЗаданийМасса.Заголовок + ": "")");
		ВыполнитьПроцедуруHTML("addInterfaceData(""" + Элементы.ПоказателиЗагрузкиСпискаЗаданийОбъем.Заголовок + ": "")");
		ВыполнитьПроцедуруHTML("addInterfaceData(""" + Элементы.ПоказателиЗагрузкиСпискаЗаданийКоличествоМест.Заголовок + ": "")");
		ВыполнитьПроцедуруHTML("addInterfaceData(""" + Элементы.ПоказателиЗагрузкиСпискаЗаданийКоличествоЗагрузочныхМетров.Заголовок + ": "")");
		ВыполнитьПроцедуруHTML("addInterfaceData(""" + ?(РазбиватьЗаданияПоГрузовымМестам, НСтр("ru = 'Всего грузов: '"), НСтр("ru = 'Всего заданий:'")) + """)");
	Иначе
		ОбновлятьПолностью = Ложь;
	КонецЕсли;
	
	Если КартаСформирована Тогда
		Если ОбновлятьПолностью Тогда
			ИзменитьРежимКластеризации();
		КонецЕсли;
		ОбновитьСписокМаркеровНаКлиенте();
				
		МассивКоординат = Новый Массив;
		КоличествоМаркеров = ВыполнитьФункциюHTML("CheckCountOfMarkers()");
		Если МассивСтрок.Количество() Или СписокОтправлений.Количество() Тогда
			соотДанные = Новый Соответствие;
			Для каждого текСтрока Из СписокМаркеровВидовАдресов Цикл
				соотДанные.Вставить(текСтрока.ВидАдреса, Новый Структура("ДанныеМаркера, ПоложениеПривязкиXМаркера, ПоложениеПривязкиYМаркера, ШиринаМаркера, ВысотаМаркера",
				                                                         текСтрока.ДанныеМаркера,
				                                                         текСтрока.ПоложениеПривязкиXМаркера,
				                                                         текСтрока.ПоложениеПривязкиYМаркера,
				                                                         текСтрока.ШиринаМаркера,
				                                                         текСтрока.ВысотаМаркера));
			КонецЦикла;
		КонецЕсли; 
		
		Для каждого Строка Из МассивСтрок Цикл
			Если Строка.Пометка Тогда
				
				ИскомаяСтрока = соотДанные.Получить(Строка.ВидАдресаПолучения);
				КоэффициентПересчета = ОпределитьКоэффициентПересчета(Строка);
				Если ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая") Тогда
					ДанныеСостояния = ПутьКФайлу_СостояниеСрочногоГруза;
				Иначе
					ДанныеСостояния = Null;
				КонецЕсли;
				
				МассивКоординат.Добавить(Строка.КоординатыПолучения);
				МассивКоординат.Добавить(ПредставлениеСтроки(Строка.ИмяПолучения));
				МассивКоординат.Добавить(ИскомаяСтрока.ДанныеМаркера);
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ПоложениеПривязкиXМаркера*КоэффициентПересчета));
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ПоложениеПривязкиYМаркера*КоэффициентПересчета));
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ШиринаМаркера*КоэффициентПересчета));
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ВысотаМаркера*КоэффициентПересчета));
				МассивКоординат.Добавить(ДанныеСостояния);
				
				МассивКоординат.Добавить(ПредставлениеЧисла(Строка.Масса));
				МассивКоординат.Добавить(ПредставлениеЧисла(Строка.Объем));
				МассивКоординат.Добавить(ПредставлениеЧисла(Строка.КоличествоМест));
				МассивКоординат.Добавить(ПредставлениеЧисла(Строка.КоличествоЗагрузочныхМетров));
							
				КоличествоМаркеров = КоличествоМаркеров + 1;
				Строка.ИндексВМассивеПолучения = КоличествоМаркеров;
			КонецЕсли;
		КонецЦикла;
				
		Для каждого текОтправление Из СписокОтправлений Цикл
			Если текОтправление.Геокодировано И текОтправление.ИндексВМассиве = 0 Тогда
				ИскомаяСтрока = соотДанные.Получить(текОтправление.ВидАдреса);
				КоличествоМаркеров = КоличествоМаркеров + 1;
				текОтправление.ИндексВМассиве = КоличествоМаркеров;
				
				МассивКоординат.Добавить(текОтправление.Координаты);
				МассивКоординат.Добавить(ПредставлениеСтроки(текОтправление.Имя));
				МассивКоординат.Добавить(ИскомаяСтрока.ДанныеМаркера);
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ПоложениеПривязкиXМаркера));
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ПоложениеПривязкиYМаркера));
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ШиринаМаркера));
				МассивКоординат.Добавить(ПредставлениеЧисла(ИскомаяСтрока.ВысотаМаркера));
				МассивКоординат.Добавить(Null);
				
				МассивКоординат.Добавить(0);
				МассивКоординат.Добавить(0);
				МассивКоординат.Добавить(0);
				МассивКоординат.Добавить(0);

			КонецЕсли;
		КонецЦикла; 
				
		Если МассивКоординат.Количество() Тогда
			ОтобразитьМаркерыНаКарте(СтрСоединить(МассивКоординат, ";") + ";", Истина);
		КонецЕсли;
		
		Если РежимОтображенияКарты = 1 Тогда // Линии.
			Для каждого Строка Из МассивСтрок Цикл
				Строка.ИндексВМассивеЛиния = ОтобразитьОбъект(, Строка.ХарактеристикиЛинии, Строка.КоординатыОтправления + ";" + Строка.КоординатыПолучения + ";", 1);
				Если Строка.ИндексВМассивеМаршрут > 0 Тогда
					ИзменитьВидимостьОбъекта(Строка.ИндексВМассивеМаршрут, Ложь);
				КонецЕсли;
			КонецЦикла; 
		ИначеЕсли РежимОтображенияКарты = 2 Тогда // Маршруты.
			Для каждого Строка Из МассивСтрок Цикл
				Строка.ИндексВМассивеМаршрут = ОтобразитьОбъект(, Строка.ХарактеристикиЛинии, ?(Строка.Путь = "", Строка.КоординатыОтправления + ";" + Строка.КоординатыПолучения + ";", Строка.Путь), 2);
				Если Строка.ИндексВМассивеЛиния > 0 Тогда
					ИзменитьВидимостьОбъекта(Строка.ИндексВМассивеЛиния, Ложь);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
				
		СписокЗаданийСформирован = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КоординатыПозиционирования) Тогда
		ЦентрироватьТочкиНаКарте(КоординатыПозиционирования);
	КонецЕсли;
	
КонецПроцедуры // ОтобразитьОбъектыНаКарте()

&НаКлиенте
Процедура ОбновитьСписокМаркеровНаКлиенте()
	
	Для каждого текВидАдреса Из СписокМаркеровВидовАдресов Цикл
		
		Если текВидАдреса.ИндексВМассиве = 0 Тогда
			ОбработатьМаркер(текВидАдреса);
			ОбработатьМаркер(текВидАдреса, "Выделенного");
			ОбработатьМаркер(текВидАдреса, "Выбранного");
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ОбновитьСписокМаркеровНаКлиенте()

&НаКлиенте
Функция ПолучитьСтрокуДанныхИконки(СтрокаТаблицы, Префикс = "", КоэффициентПересчета)
	
	Возврат СтрШаблон("'%1',%2,%3,%4,%5", СтрокаТаблицы["ДанныеМаркера" + Префикс],
	                                    ПредставлениеЧисла(СтрокаТаблицы["ПоложениеПривязкиXМаркера" + Префикс]*КоэффициентПересчета),
										ПредставлениеЧисла(СтрокаТаблицы["ПоложениеПривязкиYМаркера" + Префикс]*КоэффициентПересчета),
										ПредставлениеЧисла(СтрокаТаблицы["ШиринаМаркера" + Префикс]*КоэффициентПересчета),
										ПредставлениеЧисла(СтрокаТаблицы["ВысотаМаркера" + Префикс]*КоэффициентПересчета));
	
КонецФункции

&НаКлиенте
Функция ОпределитьКоэффициентПересчета(Строка);
	
	Если ВыделятьКрупныеГрузы И Строка[ПоказательКрупногоГруза] > РазмерКрупногоГруза Тогда
		КоэффициентПересчета = 1.5
	Иначе
		КоэффициентПересчета = 1;
	КонецЕсли;
	
	Возврат КоэффициентПересчета;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьМаркер(ЭлементКоллекции, Префикс = "", Режим = 0)
	#Если НЕ ВебКлиент Тогда
	Стиль = "";
	текКартинка = ЭлементКоллекции["КартинкаМаркера" + Префикс];
	Если ЭлементКоллекции["ЭтоКартинка" + Префикс] Тогда
		мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Маркер",  текКартинка));
		Если мсвСохраненныйМаркер.Количество() Тогда 
			Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Файл) Тогда 
				Стиль = мсвСохраненныйМаркер[0].Файл;
			КонецЕсли;	
		Иначе	
			АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");		
			ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Маркер = текКартинка;
			НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			Стиль = НовыйСохраненныйМаркер.Файл; 
		КонецЕсли;	
	Иначе 
		мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Стиль",  ЭлементКоллекции["СтильМаркера" + Префикс]));
		Если мсвСохраненныйМаркер.Количество() Тогда
			Стиль = мсвСохраненныйМаркер[0].Файл;
		Иначе
			АдресКартинки = ПоместитьВоВременноеХранилище(текКартинка, УникальныйИдентификатор);
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");	
			ПолучитьИзВременногоХранилища(АдресКартинки).Записать(ИмяВременногоФайла);
			
			НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
			НовыйСохраненныйМаркер.Стиль = ЭлементКоллекции["СтильМаркера" + Префикс];
			НовыйСохраненныйМаркер.Файл = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
			Стиль = НовыйСохраненныйМаркер.Файл;
		КонецЕсли;
	КонецЕсли;
	Если Режим = 0 Тогда
		ЭлементКоллекции["ДанныеМаркера" + Префикс] = Стиль;
	Иначе		
		ЭлементКоллекции["ДанныеМаркера" + Префикс] = "'" + Стиль + "', " + ЭлементКоллекции["ДанныеМаркера" + Префикс];
	КонецЕсли;

	#КонецЕсли
КонецПроцедуры // ОбработатьМаркер()

&НаКлиенте
Процедура ОтобразитьРейсыНаКарте()
	
	Если ЭтаФорма.ЭтоСамовывоз Тогда
		Если СписокИндексов.Количество() Тогда
			СписокИндексов.Очистить();
		КонецЕсли;
		Возврат;
	КонецЕсли; 
	
	#Если НЕ ВебКлиент Тогда
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	
	Если РежимКорректировки Тогда
		СтрокаМаркера = СписокНезависимыхМаркеров[0];
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".png");
		СтрокаМаркера.КартинкаМаркера.Записать(ИмяВременногоФайла);
		ИмяФайлаКартинкиСостоянияТранспорта = ИзменитьФорматВременногоФайла(ИмяВременногоФайла);
	Иначе
		ИмяФайлаКартинкиСостоянияТранспорта = "";
	КонецЕсли;
	
	Если СписокИндексов.Количество() Тогда
		Для каждого текИндекс Из СписокИндексов Цикл
			Если ЭлементыРейсы.Количество() <= текИндекс.Значение Тогда
				// Такое случается при перетаскивании первой строки рейса на другую строку рейса
				Продолжить;
			КонецЕсли; 
			текРейс = ЭлементыРейсы.Получить(текИндекс.Значение);
			Если ЗначениеЗаполнено(текРейс.Рейс) И текРейс.Геокодирован Тогда
				текЦветоваяСхема = ЦветоваяСхемаРейса(текРейс);
				текРейс.НомерЦветовойСхемы = ЦветовыеСхемы.Индекс(текЦветоваяСхема) + 1;
				ВидимостьРейса = ОтобразитьРейсНаКарте(текРейс, ИмяФайлаКартинкиСостоянияТранспорта);
			КонецЕсли;
		КонецЦикла; 
		СписокИндексов.Очистить();
	Иначе
		ОбновитьЦветовыеСхемыНаКлиенте();
		КоличествоСхем = ЦветовыеСхемы.Количество();
		Сч = 0;
		КоординатыТочек = "";
		Для каждого текРейс Из ЭлементыРейсы Цикл
			Если текРейс.Пометка Тогда
				Если Сч > КоличествоСхем - 1 Тогда
					Сч = 0;
				КонецЕсли;
				текЦветоваяСхема = ЦветовыеСхемы[Сч];
				текРейс.НомерЦветовойСхемы = Сч + 1;
				текЦветоваяСхема.Использована = Истина;
				Сч = Сч + 1;
				ВидимостьРейса = ОтобразитьРейсНаКарте(текРейс, ИмяФайлаКартинкиСостоянияТранспорта);
			КонецЕсли;
		КонецЦикла; 
		Если Истина
			//И СписокЗаданий.Количество() = 0 
			И ЗначениеЗаполнено(КоординатыТочек)
		Тогда
			ЦентрироватьТочкиНаКарте(КоординатыТочек);
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Функция ЦветоваяСхемаРейса(Знач текРейс)
	
	Перем текЦветоваяСхема;
	
	Если текРейс.НомерЦветовойСхемы > 0 Тогда
		текЦветоваяСхема = ЦветовыеСхемы[текРейс.НомерЦветовойСхемы - 1];
	Иначе
		текЦветоваяСхема = ПодобратьСвободнуюЦветовуюСхему();
	КонецЕсли;
	Возврат текЦветоваяСхема;

КонецФункции

&НаКлиенте
Функция ОтобразитьРейсНаКарте(СтрокаРейса, Знач ИмяФайлаКартинкиСостоянияТранспорта, КоординатыТочек = "")
	
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	ИндексВДереве = ЭлементыРейсы.Индекс(СтрокаРейса);
	текЦветоваяСхема = ЦветоваяСхемаРейса(СтрокаРейса);
	Если ЗначениеЗаполнено(СтрокаРейса.КоординатыЛиний) Тогда
		СтрокаРейса.ИндексВМассивеМаршрут = ОтобразитьРейс(текЦветоваяСхема.ХарактеристикиЛинии, СтрокаРейса.КоординатыЛиний);
	Иначе
		ТекстСообщения = "Для рейса " + СтрокаРейса.Рейс + " маршрут не построен";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 
	ВидимостьРейса = СтрокаРейса.Пометка;

	Если РежимКорректировки Тогда
		ИндексРейсаВЛучших = Неопределено;
		ПозицииВставкиЛучшего = "";
		ДатаТекущихКоординат = СтрокаРейса.ДатаТекущихКоординат;
		СтрокиЛучших = ЛучшиеРейсы.НайтиСтроки(Новый Структура("Рейс", СтрокаРейса.Рейс));
		Если Истина
			И СтрокиЛучших.Количество() > 0
			Тогда
			ЭлементСпискаЛучших = СтрокиЛучших[0];
			ИндексРейсаВЛучших = ЛучшиеРейсы.Индекс(ЭлементСпискаЛучших);
			Если ЭлементСпискаЛучших.ПозицияПогрузки > 0 Тогда
				ПозицииВставкиЛучшего = "погрузка " + ЭлементСпискаЛучших.ПозицияПогрузки + ", разгрузка " + ЭлементСпискаЛучших.ПозицияРазгрузки;
			КонецЕсли; 
		КонецЕсли; 
		ЭтоЛучшийРейс = Истина
		И ИндексРейсаВЛучших <> Неопределено
		И ИндексРейсаВЛучших < ПараметрыПланирования.МаксимальноеКоличествоРекоммендаций;
		Если Истина
			//И РежимКорректировки
			И (Ложь
			Или Не ОтображениеПлановыхМаршрутов
			Или (Истина
			И ЛучшиеРейсы.Количество() > 0
			И Не ЭтоЛучшийРейс))
			Тогда 
			ВидимостьРейса = Ложь;
		КонецЕсли;
		
		Если ЭтоЛучшийРейс И ЗначениеЗаполнено(ЭлементСпискаЛучших.ВставкаОтправленияШирота) Тогда
			ДатаТекущихКоординат = Неопределено;
		КонецЕсли; 
		КонтактнаяИнформация = упРейсВызовСервера.ПолучитьКонтактнуюИнформациюЭкипажа(СтрокаРейса.Рейс);
		ВыполнитьПроцедуруHTML("addTripData(" + ИндексВДереве + ", """ + ПредставлениеСтроки(СтрокаРейса.Представление) + """, """ + ПредставлениеСтроки(КонтактнаяИнформация) + """, """ + ДатаТекущихКоординат + """, "
		+ """" + ПозицииВставкиЛучшего + """)");
		Если ЛучшиеРейсы.Количество() = 0 И ЗначениеЗаполнено(СтрокаРейса.ТекущаяШирота) Тогда
			ОтобразитьТранспорт(ЭлементыРейсы.Индекс(СтрокаРейса), СтрокаРейса, ИмяФайлаКартинкиСостоянияТранспорта);
			КоординатыТочек = КоординатыТочек +  ПредставлениеЧисла(СтрокаРейса.ТекущаяШирота) + ";" + ПредставлениеЧисла(СтрокаРейса.ТекущаяДолгота) + ";";
		ИначеЕсли ЭтоЛучшийРейс Тогда 
			СтрокаРейса.НомерВПорядкеЛучшихРейсов = ИндексРейсаВЛучших + 1;
			Если ЗначениеЗаполнено(ЭлементСпискаЛучших.ВставкаОтправленияШирота) Тогда
				ОтобразитьМаркерЛучшегоРейса(СтрокаРейса, ИндексВДереве, ЭлементСпискаЛучших.ВставкаОтправленияШирота, ЭлементСпискаЛучших.ВставкаОтправленияДолгота);
				//ОтобразитьМаркерЛучшегоРейса(СтрокаРейса, ИндексВДереве, ЭлементСпискаЛучших.ВставкаПолученияШирота, ЭлементСпискаЛучших.ВставкаПолученияДолгота); // Отключил, т.к. этот маркер надо отдельно запоминать, чтобы потом его удалять с карты
			Иначе
				ОтобразитьМаркерЛучшегоРейса(СтрокаРейса, ИндексВДереве);
			КонецЕсли; 
			КоординатыТочек = КоординатыТочек +  ПредставлениеЧисла(СтрокаРейса.ТекущаяШирота) + ";" + ПредставлениеЧисла(СтрокаРейса.ТекущаяДолгота) + ";";
			Если ЗначениеЗаполнено(СтрокаРейса.КоординатыЛинийКорректировка) Тогда
				ОтобразитьРейс(текЦветоваяСхема.ХарактеристикиЛинии, СтрокаРейса.КоординатыЛинийКорректировка);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
	Если Не ВидимостьРейса Тогда
		ИзменитьВидимостьРейса(СтрокаРейса, ВидимостьРейса);
		//Продолжить;
	КонецЕсли; 
	Для каждого текАдресОтправления Из СтрокаРейса.АдресаОтправлений Цикл
		сткДанныеАдреса = текАдресОтправления.Значение;
		сткДанныеАдреса.ИндексВМассиве = ОтобразитьОбъект(сткДанныеАдреса.ПредставлениеАдреса, текЦветоваяСхема.ДанныеМаркера, сткДанныеАдреса.Координаты);
		КоординатыТочек = КоординатыТочек + сткДанныеАдреса.Координаты;
		Если НЕ ВидимостьРейса Тогда
			ИзменитьВидимостьОбъекта(сткДанныеАдреса.ИндексВМассиве, ВидимостьРейса);
		КонецЕсли;
	КонецЦикла;
	ЭлементыЗадания = СтрокаРейса.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыЗадания Цикл
		текЗадание.ИндексВМассиве = ОтобразитьОбъект(текЗадание.ПредставлениеАдреса, текЦветоваяСхема.ДанныеМаркера, текЗадание.КоординатыТочек); 
		КоординатыТочек = КоординатыТочек + текЗадание.КоординатыТочек;
		Если НЕ ВидимостьРейса Тогда
			ИзменитьВидимостьОбъекта(текЗадание.ИндексВМассиве, ВидимостьРейса);
		КонецЕсли;
	КонецЦикла;
	Возврат ВидимостьРейса;

КонецФункции // ОтобразитьРейсыНаКарте()

&НаКлиенте
// Функция добавляет объект на карту.
//
// Параметры:
//  Имя        - Строка - отображаемое в виде всплывающей подсказки имя объекта.
//  Данные     - Структура, Строка - дополнительные данные 
//               (для точки - физ. адрес картинки, для линии и маршрута - характеристики отображения линии).
//  Координаты - Строка - строка с координатами, перечисленными через точку с запятой.
//  Режим      - Число  - числовое представление режима отображения (0 - точки, 1 - линии, 2 - маршруты).
//
// Возвращаемое значение:
//   Число - уникальный идентификатор объекта в виде числового индекса. 
//   При помощи данного индекса в последствии возможно изменение свойств объекта (видимость и т.д.).
//
Функция ОтобразитьОбъект(Имя = "", Данные = "", Координаты, Режим = 0) 
	
	мсвКоординат = СтрРазделить(Координаты, ";");
	//Если мсвКоординат.Количество() > 0 Тогда
		Если Режим = 0 Тогда
			Индекс = ВыполнитьФункциюHTML("addObjectMarker(" + мсвКоординат[0] + ", " + мсвКоординат[1] + ", """ + ПредставлениеСтроки(Имя) + """, " + Данные + ", false)"); // параметр со значением false указывает на запрет использования popup окон
		ИначеЕсли Режим = 1 ИЛИ Режим = 2 Тогда
			Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
				ВыполнитьПроцедуруHTML("addPoint(" + мсвКоординат[Сч * 2] + ", " + мсвКоординат[(Сч * 2) + 1] + ")");
			КонецЦикла;	
			Индекс = ВыполнитьФункциюHTML("addObjectPolylineBetweenObjects(""" + Данные.Цвет + """, " + ПредставлениеЧисла(Данные.Толщина) + ", " + ПредставлениеЧисла(Данные.Прозрачность) + ")");
		КонецЕсли;	
	//КонецЕсли; 
	
	Возврат Индекс;
	
Конецфункции // ОтобразитьОбъект()

&НаКлиенте
Функция ОтобразитьТранспорт(ИндексВДереве, СтрокаРейса, ИмяФайлаКартинки) 
	
	ВыполнитьПроцедуруHTML("showVehicleMarkerPlanning(" + ИндексВДереве + ", " + ПредставлениеЧисла(СтрокаРейса.ТекущаяШирота) + ", " 
		+ ПредставлениеЧисла(СтрокаРейса.ТекущаяДолгота) + ", """ + СтрокаРейса.Представление + """, """ + ИмяФайлаКартинки + """, "
		+ """" + СтрокаРейса.ДатаТекущихКоординат + """)"); // параметр со значением false указывает на запрет использования popup окон
	
Конецфункции // ОтобразитьОбъект()

&НаКлиенте
Процедура ОтобразитьМаркерыНаКарте(Данные, МаркерыКластеризуемые = Ложь)
	
	Если МаркерыКластеризуемые Тогда
		ВыполнитьПроцедуруHTML("addMarkers("""+ Данные + """, true)");
	Иначе
		ВыполнитьПроцедуруHTML("addMarkers("""+ Данные + """)");
	КонецЕсли;
	
КонецПроцедуры // ОтобразитьМаркерыНаКарте()

&НаКлиенте
Функция ОтобразитьРейс(ДанныеЛиний, КоординатыЛиний)
	
	Если Не ЗначениеЗаполнено(КоординатыЛиний) Тогда
		ВызватьИсключение "Невозможно отобразить пустой путь на карте";
	КонецЕсли; 
	Индекс = ВыполнитьФункциюHTML("addTrip(""" + КоординатыЛиний + """, """ + ДанныеЛиний.Цвет + """, "
	                                           + ПредставлениеЧисла(ДанныеЛиний.Толщина) + ", " + ПредставлениеЧисла(ДанныеЛиний.Прозрачность) + ")");
	Возврат Индекс;
	
КонецФункции // ОтобразитьРейс()

&НаКлиенте
Функция ОтобразитьЗону(Данные)
	
	Индекс = ВыполнитьФункциюHTML("addZone(" + Данные + ")");
	Возврат Индекс;
	
КонецФункции // ОтобразитьРейс()

&НаКлиенте
Функция ОтобразитьМестаИнтереса(МестоИнтереса)
	
	Данные = "'" + МестоИнтереса.ДанныеМаркера + "', " + МестоИнтереса.Данные;
	
	Индекс = ВыполнитьФункциюHTML("addSiteInterest(" + Данные + ")");
	
	Возврат Индекс;
	
КонецФункции // ОтобразитьМестаИнтереса()

&НаКлиенте
Процедура ИзменитьПрозрачностьМаркера(ИндексВМассиве, Прозрачность)
	
	ВыполнитьПроцедуруHTML("setMarkerTransparency(" + ПредставлениеЧисла(ИндексВМассиве) + ", " + ПредставлениеЧисла(Прозрачность) + ")");
	
КонецПроцедуры // ИзменитьПрозрачностьМаркера()

&НаКлиенте
// Процедура изменяет икону-картинку маркера на карте
//
Процедура ИзменитьИконкуМаркера(ИндексВМассиве, Данные = "", Режим = 0, ВыделятьСрочность = Ложь)
	
	ВыполнитьПроцедуруHTML("setMarkerIcon(" + ПредставлениеЧисла(ИндексВМассиве) + ", " + Данные + "," + ?(ВыделятьСрочность, ПредставлениеЧисла(Режим), """""," + ПредставлениеЧисла(Режим)) + ")");
	
КонецПроцедуры // ИзменитьИконкуМаркера() 

&НаКлиенте
// Процедура изменяет икону-картинку маркера на карте по виду адреса
//
Процедура ИзменитьИконкуМаркераПоВидуАдреса(ВидАдреса, ИндексВМассиве, Режим = 0, КоэффициентПересчета = 1, ВыделятьСрочность = Ложь)
	
	ИскомыеСтроки = СписокМаркеровВидовАдресов.НайтиСтроки(Новый Структура("ВидАдреса", ВидАдреса));
	Если ИскомыеСтроки.Количество() Тогда
		Если Режим = 0 Тогда // стандартный маркер
			ДанныеМаркера = ПолучитьСтрокуДанныхИконки(ИскомыеСтроки[0],, КоэффициентПересчета);
		ИначеЕсли Режим = 1 Тогда // выделен
			ДанныеМаркера = ПолучитьСтрокуДанныхИконки(ИскомыеСтроки[0], "Выделенного", КоэффициентПересчета);
		ИначеЕсли Режим = 2 Тогда // выбран
			ДанныеМаркера = ПолучитьСтрокуДанныхИконки(ИскомыеСтроки[0], "Выбранного", КоэффициентПересчета);
		КонецЕсли;
		Если ВыделятьСрочность Тогда
			ДанныеМаркера = СтрШаблон("%1,'%2'", ДанныеМаркера, ПутьКФайлу_СостояниеСрочногоГруза);
		КонецЕсли;
	КонецЕсли;
	ИзменитьИконкуМаркера(ИндексВМассиве, ДанныеМаркера, Режим, ВыделятьСрочность);
	
КонецПроцедуры // ИзменитьИконкуМаркераПоВидуАдреса() 

&НаКлиенте
// Процедура изменяет икону-картинку маркера на карте по адресу отправления
//
Процедура ИзменитьИконкуОтправления(Адрес, Режим = 0)
	
	ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", Адрес));
	Если ИскомыеСтроки.Количество() Тогда
		СтрокаОтправления = ИскомыеСтроки[0];
		Если Режим = 0 Тогда // стандартный маркер
			УдалитьОбъектИзВыделенных(СтрокаОтправления.ИндексВМассиве);
			СтрокаОтправления.Выделен = Ложь;
			Если НЕ СтрокаОтправления.Выбран Тогда
				ИзменитьИконкуМаркераПоВидуАдреса(СтрокаОтправления.ВидАдреса, СтрокаОтправления.ИндексВМассиве, 0); 
			КонецЕсли;
		ИначеЕсли Режим = 1 И Не СтрокаОтправления.Выделен Тогда // выделен
			СтрокаОтправления.Выделен = Истина;
			Если НЕ СтрокаОтправления.Выбран Тогда
				ИзменитьИконкуМаркераПоВидуАдреса(СтрокаОтправления.ВидАдреса, СтрокаОтправления.ИндексВМассиве, 1); 
			КонецЕсли;
		ИначеЕсли Режим = 2 Тогда // выбран
			Если НЕ СтрокаОтправления.Выбран Тогда
				СтрокаОтправления.Выбран = Истина;
				ИзменитьИконкуМаркераПоВидуАдреса(СтрокаОтправления.ВидАдреса, СтрокаОтправления.ИндексВМассиве, 2); 
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры // ИзменитьИконкуОтправления() 

&НаКлиенте
Процедура УбратьВыборМаркера(Адрес)
	
	ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", Адрес));
	Если ИскомыеСтроки.Количество() Тогда
		СтрокаОтправления = ИскомыеСтроки[0];
		СтрокаОтправления.Выделен = Истина;
		СтрокаОтправления.Выбран  = Ложь;
		ИзменитьИконкуМаркераПоВидуАдреса(СтрокаОтправления.ВидАдреса, СтрокаОтправления.ИндексВМассиве, 1); 
	КонецЕсли;
	
КонецПроцедуры // УбратьВыборМаркера() 

&НаКлиенте
Процедура ИзменитьВидимостьПоСтрокеСпискаЗаданий(ТекущаяСтрока)
	
	ИзменитьВидимостьОбъекта(ТекущаяСтрока.ИндексВМассивеПолучения, ТекущаяСтрока.Пометка);
	Если РежимОтображенияКарты = 1 Тогда
		Если ТекущаяСтрока.ИндексВМассивеЛиния > 0 Тогда
			ИзменитьВидимостьОбъекта(ТекущаяСтрока.ИндексВМассивеЛиния, ТекущаяСтрока.Пометка);
		ИначеЕсли ТекущаяСтрока.Пометка Тогда 	
			ТекущаяСтрока.ИндексВМассивеЛиния = ОтобразитьОбъект(, ТекущаяСтрока.ХарактеристикиЛинии, ТекущаяСтрока.КоординатыОтправления + ";" + ТекущаяСтрока.КоординатыПолучения + ";", 1);
		КонецЕсли;	
	ИначеЕсли РежимОтображенияКарты = 2 Тогда
		Если ТекущаяСтрока.ИндексВМассивеМаршрут > 0 Тогда
			ИзменитьВидимостьОбъекта(ТекущаяСтрока.ИндексВМассивеМаршрут, ТекущаяСтрока.Пометка);
		ИначеЕсли ТекущаяСтрока.Пометка Тогда 	
			ТекущаяСтрока.ИндексВМассивеМаршрут = ОтобразитьОбъект(, ТекущаяСтрока.ХарактеристикиЛинии, ?(ТекущаяСтрока.Путь = "", ТекущаяСтрока.КоординатыОтправления + ";" + ТекущаяСтрока.КоординатыПолучения + ";", ТекущаяСтрока.Путь), 2);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВидимостьПоСтрокеСпискаЗаданий()

&НаКлиенте
Процедура ИзменитьВидимостьТочкиОтправления(Адрес, Пометка)
	
	ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", Адрес));
	Если ИскомыеСтроки.Количество() Тогда
		СтрокаОтправления = ИскомыеСтроки[0];
		СтрокаОтправления.Пометка = Пометка;
		ИзменитьВидимостьОбъекта(СтрокаОтправления.ИндексВМассиве, Пометка);
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВидимостьТочкиОтправления

&НаКлиенте
Процедура ОбработатьСостояниеОтправлений(МассивОтправлений)
	
	мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивОтправлений);
	Для каждого текОтправление Из мсвОтправлений Цикл
		ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления", текОтправление));
		СкрытьТочкуОтправления  = Ложь;
		УдалитьТочкуОтправления = Ложь;
		Если ИскомыеСтроки.Количество() = 0 Тогда
			СкрытьТочкуОтправления  = Истина;
			УдалитьТочкуОтправления = Истина;
		Иначе
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Пометка", текОтправление, Истина));
			Если ИскомыеСтроки.Количество() = 0 Тогда
				СкрытьТочкуОтправления = Истина;
			КонецЕсли;
		КонецЕсли;
		Если СкрытьТочкуОтправления Тогда
			ИскомыеОтправления = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текОтправление));
			Если ИскомыеОтправления.Количество() Тогда
				СтрокаОтправления = ИскомыеОтправления[0];
				ИзменитьВидимостьОбъекта(СтрокаОтправления.ИндексВМассиве, Ложь);
				Если УдалитьТочкуОтправления Тогда
					СписокОтправлений.Удалить(СтрокаОтправления);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // ОбработатьСостояниеОтправлений()

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Нет.
// 
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОбъектВВыделенные(ИндексВМассиве)
	
	ВыполнитьПроцедуруHTML("addSelectedMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	
КонецПроцедуры // ДобавитьОбъектВВыделенные()

&НаКлиенте
Процедура УдалитьОбъектИзВыделенных(ИндексВМассиве)
	
	ВыполнитьПроцедуруHTML("deleteSelectedMarker(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	
КонецПроцедуры // УдалитьОбъектИзВыделенных()

&НаКлиенте
Процедура ОбновитьСостояниеМаркеров()

	КоличествоВыделенныхМаркеров = ВыполнитьФункциюHTML("CheckCountOfSelectedMarkers()");
	Если КоличествоВыделенныхМаркеров Тогда
		ОбработатьВыделенныеМаркеры();
	КонецЕсли;	
	
	КоличествоСнятыхСВыделенияМаркеров = ВыполнитьФункциюHTML("CheckCountOfUnselectedMarkers()");
	Если КоличествоСнятыхСВыделенияМаркеров Тогда
	    ОбработатьСнятыеСВыделенияМаркеры();
	КонецЕсли;
	
	Если (КоличествоВыделенныхМаркеров + КоличествоСнятыхСВыделенияМаркеров) > 0 Тогда
		ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
		ОбновитьПоказателиПоВыделеннымСтрокамДереваРейсов();
	КонецЕсли;

КонецПроцедуры // ОбновитьСостояниеМаркеров()

&НаКлиенте
Процедура ОбработатьВыделенныеМаркеры()
	
	ПредставлениеВыделенныхТочек = ВыполнитьФункциюHTML("getSelectedMarkers()");
	мсвТочек = СтрРазделить(ПредставлениеВыделенныхТочек, ";id=", Ложь);
	мсвТочек = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвТочек);
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	
	ИдентификаторСтроки = -1;
	мсвНеидентифицированныхТочек = Новый Массив;
	мсвОтправлений = Новый Массив;
	мсвИндексыВыделенныхТочекВДеревеРейсов = Новый Массив;
	Для каждого текЭлемент Из мсвТочек Цикл
		
		Если текЭлемент <> "" Тогда
			текЭлемент = Число(текЭлемент);
			мсвИндексыВыделенныхТочекВДеревеРейсов.Добавить(текЭлемент);
			Если текЭлемент = 0 Тогда
				Продолжить;
			КонецЕсли; 
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("ИндексВМассивеПолучения, ЭтоВозвратГруза", текЭлемент, Ложь));
			Если ИскомыеСтроки.Количество() Тогда
				Для каждого Строка Из ИскомыеСтроки Цикл
					Если Строка.Пометка И Не Строка.Выделено Тогда
						Строка.Выделено = Истина;
						Если Не Строка.Выбрано Тогда
							мсвОтправлений.Добавить(Строка.АдресОтправления);
							ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения, 1, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая"))); 
						КонецЕсли;
						ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
						Если ВыделенныеСтроки.Найти(ИдентификаторСтроки) = Неопределено Тогда
							ВыделенныеСтроки.Добавить(ИдентификаторСтроки);
							//ПодключитьОбработчикОжидания("ОбновитьВыделенныеСтрокиСпискаЗаданийОтложенно", 0.1, Истина);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла; 
			Иначе
				мсвНеидентифицированныхТочек.Добавить(текЭлемент);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если ИдентификаторСтроки >= 0 Тогда
		ВыделенныеСтрокиКопия = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ВыделенныеСтроки);
		РазрешитьАктивизациюСтроки = Ложь;
		Элементы.СписокЗаданий.ТекущаяСтрока = ИдентификаторСтроки;
		РазрешитьАктивизациюСтроки = Истина;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Элементы.СписокЗаданий.ВыделенныеСтроки, ВыделенныеСтрокиКопия, Истина);
	КонецЕсли;
	
	// проверка точек элементов рейсов
	Если мсвНеидентифицированныхТочек.Количество() Тогда
		ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
		
		ИдентификаторСтрокиДерева = -1;
		ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
		
		мсвУдалить = Новый Массив;
		
		Для каждого текЭлемент Из мсвНеидентифицированныхТочек Цикл
			Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
				Если ЗначениеЗаполнено(ЭлементРейс.Рейс) Тогда
					ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
					Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
						Если ЭлементЗадание.ИндексВМассиве = текЭлемент Тогда							
							ЭлементЗадание.Выделен = Истина;
							текЦветоваяСхема = ЦветовыеСхемы[ЭлементРейс.НомерЦветовойСхемы - 1];
							Если Не ЭлементЗадание.Выбран Тогда
								ИзменитьИконкуМаркера(текЭлемент, текЦветоваяСхема.ДанныеМаркераВыделенного + ",0,0", 1);
							КонецЕсли;
							Если Не ЭлементРейс.Выделен Тогда
								ИзменитьСтильРейса(ЭлементРейс.ИндексВМассивеМаршрут, ПредставлениеЧисла(текЦветоваяСхема.ХарактеристикиЛинии.Толщина*2) + "," + "''");
								ЭлементРейс.Выделен = Истина;
							КонецЕсли;
							
							ИдентификаторСтрокиДерева = ЭлементЗадание.ПолучитьИдентификатор();
							Если ВыделенныеСтрокиДерева.Найти(ИдентификаторСтрокиДерева) = Неопределено Тогда
								ВыделенныеСтрокиДерева.Добавить(ИдентификаторСтрокиДерева);
							КонецЕсли;
							
							мсвУдалить.Добавить(текЭлемент);
							Прервать;
						КонецЕсли;
					КонецЦикла; 
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СократитьМассив(мсвНеидентифицированныхТочек, мсвУдалить); 
	
		Если ИдентификаторСтрокиДерева >= 0 Тогда
			ВыделенныеСтрокиДереваКопия = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ВыделенныеСтрокиДерева);
			РазрешитьАктивизациюСтроки = Ложь;
			Элементы.ДеревоРейсы.ТекущаяСтрока = ИдентификаторСтрокиДерева;
			РазрешитьАктивизациюСтроки = Истина;
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Элементы.ДеревоРейсы.ВыделенныеСтроки, ВыделенныеСтрокиДереваКопия, Истина);
		КонецЕсли;
	КонецЕсли;
	
	// проверка точек возвратов
	Если мсвНеидентифицированныхТочек.Количество() Тогда
		Для каждого текЭлемент Из мсвНеидентифицированныхТочек Цикл
			ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("ИндексВМассиве", текЭлемент));
			Если ИскомыеСтроки.Количество() Тогда
				НайденноеОтправление = ИскомыеСтроки[0]; 
				
				// найдем все строки заданий с найденным адресом отправления, являющиеся возвратами
				ИскомыеВозвраты = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, ЭтоВозвратГруза", НайденноеОтправление.Адрес, Истина));
				Если ИскомыеВозвраты.Количество() Тогда
					Для каждого Строка Из ИскомыеВозвраты Цикл
						Если Строка.Пометка И Не Строка.Выделено Тогда
							Строка.Выделено = Истина;
							Если Не Строка.Выбрано Тогда
								мсвОтправлений.Добавить(Строка.АдресОтправления);
								ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения, 1, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая"))); 
							КонецЕсли;
							ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
							Если ВыделенныеСтроки.Найти(ИдентификаторСтроки) = Неопределено Тогда
								ВыделенныеСтроки.Добавить(ИдентификаторСтроки);
								//ПодключитьОбработчикОжидания("ОбновитьВыделенныеСтрокиСпискаЗаданийОтложенно", 0.1, Истина);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	Если мсвОтправлений.Количество() Тогда
		мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
	КонецЕсли;
	Для каждого текОтправление Из мсвОтправлений Цикл
		ИзменитьИконкуОтправления(текОтправление, 1);
	КонецЦикла;
	
	// Проверить, что каждой выделенной строке дерева соответствует выделенная точка на карте.
	// Выделенные строки в дереве рейсов без выделенных точек в маршруте бывают при открытии формы, либо после планирования рейсов.
	КоличествоВыделенныхСтрок = Элементы.ДеревоРейсы.ВыделенныеСтроки.Количество();
	Если мсвИндексыВыделенныхТочекВДеревеРейсов.Количество() <> КоличествоВыделенныхСтрок Тогда
		Для й = 1 По КоличествоВыделенныхСтрок Цикл
			ИндексЭлемента = КоличествоВыделенныхСтрок - й;
			ИдентификаторСтроки = Элементы.ДеревоРейсы.ВыделенныеСтроки[ИндексЭлемента];
			ДанныеЭлементДерева = ДеревоРейсы.НайтиПоИдентификатору(ИдентификаторСтроки);
			Если Истина
				И ДанныеЭлементДерева <> Неопределено
				И мсвИндексыВыделенныхТочекВДеревеРейсов.Найти(ДанныеЭлементДерева.ИндексВМассиве) = Неопределено Тогда
				Элементы.ДеревоРейсы.ВыделенныеСтроки.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры //ОбработатьВыделенныеМаркеры()

&НаКлиенте
Процедура ОбработатьСнятыеСВыделенияМаркеры()
	
	ПредставлениеСнятыхСВыделенияТочек = ВыполнитьФункциюHTML("getUnselectedMarkers()");
	мсвТочек = СтрРазделить(ПредставлениеСнятыхСВыделенияТочек, ";id=", Ложь);	
	ВыделенныеСтроки = Элементы.СписокЗаданий.ВыделенныеСтроки;
	
	мсвНеидентифицированныхТочек = Новый Массив;
	мсвОтправлений = Новый Массив;
	Для каждого текЭлемент Из мсвТочек Цикл
		Если текЭлемент <> "" Тогда
			текЭлемент = Число(текЭлемент);
			ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("ИндексВМассивеПолучения, ЭтоВозвратГруза", текЭлемент, Ложь));
			Если ИскомыеСтроки.Количество() Тогда
				Для каждого Строка Из ИскомыеСтроки Цикл
					Если Строка.Пометка И Строка.Выделено Тогда
						Строка.Выделено = Ложь;
						Если Не Строка.Выбрано Тогда
							мсвОтправлений.Добавить(Строка.АдресОтправления);
							ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения, 0, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
						КонецЕсли;
					КонецЕсли;
					ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
					ИндексВыделеннойСтроки = ВыделенныеСтроки.Найти(ИдентификаторСтроки);
					Если ИндексВыделеннойСтроки <> Неопределено Тогда
						ВыделенныеСтроки.Удалить(ИндексВыделеннойСтроки);
					КонецЕсли;
				КонецЦикла; 
			Иначе
				мсвНеидентифицированныхТочек.Добавить(текЭлемент);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если мсвНеидентифицированныхТочек.Количество() Тогда
		мсвРейсовДляПроверкиСнятияСВыделения = Новый Массив;
		
		ВыделенныеСтрокиДерева = Элементы.ДеревоРейсы.ВыделенныеСтроки;
		ИдентификаторСтрокиДерева = -1;
		
		ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
		Для каждого текЭлемент Из мсвНеидентифицированныхТочек Цикл
			Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
				Если ЭлементРейс.Пометка Тогда
					ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
					ПроверитьВыделениеРейса = Истина;
					Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
						Если ЭлементЗадание.Выделен И ЭлементЗадание.ИндексВМассиве = текЭлемент Тогда
							ЭлементЗадание.Выделен = Ложь;						
							
							текЦветоваяСхема = ЦветовыеСхемы[ЭлементРейс.НомерЦветовойСхемы - 1];
							Если Не ЭлементЗадание.Выбран Тогда
								ИзменитьИконкуМаркера(текЭлемент, текЦветоваяСхема.ДанныеМаркера + ",0,0", 0);
							КонецЕсли;
							ИдентификаторСтрокиДерева = ЭлементЗадание.ПолучитьИдентификатор();
							ИндексВыделеннойСтрокиДерева = ВыделенныеСтрокиДерева.Найти(ИдентификаторСтрокиДерева);
							Если ИндексВыделеннойСтрокиДерева <> Неопределено Тогда
								ВыделенныеСтрокиДерева.Удалить(ИндексВыделеннойСтрокиДерева);
							КонецЕсли;
							Если ПроверитьВыделениеРейса Тогда
								мсвРейсовДляПроверкиСнятияСВыделения.Добавить(ЭлементРейс);
								ПроверитьВыделениеРейса = Ложь;
							КонецЕсли; 
						КонецЕсли;
					КонецЦикла; 
				КонецЕсли;
			КонецЦикла;
		КонецЦикла; 
		мсвРейсовДляПроверкиСнятияСВыделения = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвРейсовДляПроверкиСнятияСВыделения);
		
		Для каждого текРейс Из мсвРейсовДляПроверкиСнятияСВыделения Цикл
			ЭлементыЗадания = текРейс.ПолучитьЭлементы();
			СнятьВыделениеРейса = Истина;
			Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
				Если ЭлементЗадание.Выделен Тогда
					СнятьВыделениеРейса = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если СнятьВыделениеРейса Тогда
				текЦветоваяСхема = ЦветовыеСхемы[текРейс.НомерЦветовойСхемы - 1];
				ИзменитьСтильРейса(текРейс.ИндексВМассивеМаршрут, ПредставлениеЧисла(текЦветоваяСхема.ХарактеристикиЛинии.Толщина/2) + "," + "'3, 2'");
				текРейс.Выделен = Ложь;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	// проверка точек возвратов
	Если мсвНеидентифицированныхТочек.Количество() Тогда
		Для каждого текЭлемент Из мсвНеидентифицированныхТочек Цикл
			ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("ИндексВМассиве", текЭлемент));
			Если ИскомыеСтроки.Количество() Тогда
				НайденноеОтправление = ИскомыеСтроки[0]; 
				
				// найдем все строки заданий с найденным адресом отправления, являющиеся возвратами
				ИскомыеВозвраты = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, ЭтоВозвратГруза", НайденноеОтправление.Адрес, Истина));
				Если ИскомыеВозвраты.Количество() Тогда
					Для каждого Строка Из ИскомыеВозвраты Цикл
						Если Строка.Пометка И Строка.Выделено Тогда
							Строка.Выделено = Ложь;
							Если Не Строка.Выбрано Тогда
								мсвОтправлений.Добавить(Строка.АдресОтправления);
								ИзменитьИконкуМаркераПоВидуАдреса(Строка.ВидАдресаПолучения, Строка.ИндексВМассивеПолучения, 0, ОпределитьКоэффициентПересчета(Строка), (ВыделятьСрочныеГрузы И Строка.Срочность = ПредопределенноеЗначение("Перечисление.упВариантыВажности.Высокая")));
							КонецЕсли;
						КонецЕсли;
						ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
						ИндексВыделеннойСтроки = ВыделенныеСтроки.Найти(ИдентификаторСтроки);
						Если ИндексВыделеннойСтроки <> Неопределено Тогда
							ВыделенныеСтроки.Удалить(ИндексВыделеннойСтроки);
						КонецЕсли;
					КонецЦикла; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	Если мсвОтправлений.Количество() Тогда
		мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
	КонецЕсли;
	Для каждого текОтправление Из мсвОтправлений Цикл
		ИскомыеСтроки = СписокЗаданий.НайтиСтроки(Новый Структура("АдресОтправления, Выделено", текОтправление, Истина));
		Если ИскомыеСтроки.Количество() = 0 Тогда
			ИзменитьИконкуОтправления(текОтправление, 0);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // ОбработатьСнятыеСВыделенияМаркеры()

#КонецОбласти

#Область РучнаяМаршрутизация

&НаКлиенте
Процедура ПеренестиСтрокиВнутриДереваРейсовНаКлиенте(МассивИДСтрок, ИДСтрокиНазначения)
	
	Если МассивИДСтрок.Количество() Тогда
		Отказ = Ложь;
		ТекстОшибки = "";
		сткИндексыУдаляемыхОбъектовКарты = Новый Структура;
		сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыРейсов", Новый Массив);
		сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыТочекМаршрута", Новый Массив);

		ЗаданияКУдалениюИзСписка = ПеренестиСтрокиВнутриДереваРейсовНаСервере(МассивИДСтрок, ИДСтрокиНазначения, сткИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ);
		
		Если Отказ Тогда
			//Если ТекстОшибки = "" Тогда
			//	ТекстОшибки = НСтр("ru = 'Произошла неисправимая ошибка. Рекомендуется обновить данные формы'");
			//КонецЕсли; 
			//ПоказатьПредупреждение(, ТекстОшибки);
			ОповеститьПользователяОбОшибке(, ТекстОшибки);
		Иначе
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				// очистка старых объектов с карты
				Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов Цикл
					УдалитьОтображениеРейса(текИндексКарты);
				КонецЦикла; 
				Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута Цикл
					УдалитьОбъект(текИндексКарты, Истина);
				КонецЦикла;
			КонецЕсли; 
			
			Для каждого текИндекс Из СписокИндексов Цикл
				ДочерниеСтроки = ДеревоРейсы.ПолучитьЭлементы();
				Если ДочерниеСтроки.Количество() > текИндекс.Значение Тогда
					Элементы.ДеревоРейсы.Развернуть(ДочерниеСтроки.Получить(текИндекс.Значение).ПолучитьИдентификатор(), Ложь);
				Иначе
					// Такое случается при перетаскивании первой строки рейса на другую строку рейса
				КонецЕсли; 
			КонецЦикла; 
			
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				ОтобразитьРейсыНаКарте();
			Иначе
				СписокИндексов.Очистить();
			КонецЕсли;
			ОбновитьПоказателиПоВыбраннымСтрокам();
		КонецЕсли; 
		ИзменитьДоступностьРейсов();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователяОбОшибке(ОтборПоТекущемуСеансу = Истина, ТекстОшибки = Неопределено)
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(НСтр(СтрШаблон("ru = 'Произошла ошибка. Рекомендуется обновить данные формы. Текст ошибки: %1'", ТекстОшибки)), Истина, ОтборПоТекущемуСеансу);
	Иначе	
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(НСтр("ru = 'Произошла ошибка. Рекомендуется обновить данные формы.'"), Истина, ОтборПоТекущемуСеансу);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиСтрокиИзСпискаЗаданийВРейсНаКлиенте(МассивСтрокЗаданий, ИДСтрокиНазначения)
	
	Если Не МассивСтрокЗаданий.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ТекстОшибки = "";
	
	мсвЗаданий = Новый Массив;
	мсвГрузовыхМест = Новый Массив;
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		Для каждого текСтрока Из МассивСтрокЗаданий Цикл
			сткДанныеОГрузе = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
			ЗаполнитьЗначенияСвойств(сткДанныеОГрузе, текСтрока);
			мсвГрузовыхМест.Добавить(сткДанныеОГрузе);
			мсвЗаданий.Добавить(текСтрока.ЗаданиеНаПеревозку);
		КонецЦикла;
		мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
	Иначе
		Для каждого текСтрока Из МассивСтрокЗаданий Цикл
			мсвЗаданий.Добавить(текСтрока.ЗаданиеНаПеревозку);
		КонецЦикла;
	КонецЕсли;
	
	Если РежимКорректировки Тогда
		//СтрокаПриемник = ДеревоРейсы.НайтиПоИдентификатору(ИДСтрокиНазначения);
		//Если СтрокаПриемник.Уровень = 1 Тогда
		//	СтрокаПриемник = СтрокаПриемник.ПолучитьРодителя();
		//КонецЕсли;
		//ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
		//ИндексСтроки = ЭлементыДерева.Индекс(СтрокаПриемник);
		//СписокИндексов.Добавить(ИндексСтроки);
		//текРейс = СтрокаПриемник.Рейс;
		//ПараметрыФормы = Новый Структура("Основание, ДобавляемыеЗадания", текРейс, мсвЗаданий);
		//ОткрытьФорму("Документ.упКорректировкаРейса.Форма.ФормаДокумента", ПараметрыФормы,, текРейс);
		ОткрытьКорректировкуСДобавлениемЗаданий(ИДСтрокиНазначения, мсвЗаданий);
		Возврат;
	КонецЕсли; 
	сткИндексыУдаляемыхОбъектовКарты = Новый Структура;
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыРейсов", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыМаркеров", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыТочекМаршрута", Новый Массив);
	ЗаданияКУдалениюИзСписка = ПеренестиСтрокиИзСпискаЗаданийВРейсНаСервере(мсвЗаданий, мсвГрузовыхМест, ИДСтрокиНазначения, сткИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ);
	Если Отказ Тогда
		//Если ТекстОшибки = "" Тогда
		//	ТекстОшибки = НСтр("ru = 'Произошла неисправимая ошибка. Рекомендуется обновить данные формы'");
		//КонецЕсли; 
		//ПоказатьПредупреждение(, ТекстОшибки);
		ОповеститьПользователяОбОшибке(, ТекстОшибки);
	Иначе
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			// очистка старых объектов с карты
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов Цикл
				УдалитьОтображениеРейса(текИндексКарты);
			КонецЦикла; 
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров Цикл
				УдалитьОбъект(текИндексКарты);
			КонецЦикла;
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута Цикл
				УдалитьОбъект(текИндексКарты, Истина);
			КонецЦикла;
		КонецЕсли; 
		
		ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
		Для каждого текИндекс Из СписокИндексов Цикл   
			ИДТекущейСтроки = ЭлементыРейсы.Получить(текИндекс.Значение).ПолучитьИдентификатор();
			Элементы.ДеревоРейсы.Развернуть(ИДТекущейСтроки, Ложь);
		КонецЦикла; 
		
		мсвОтправлений = Новый Массив;
		Для Каждого текСтрока Из ЗаданияКУдалениюИзСписка Цикл
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				сткПоиска = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов", текСтрока.Задание, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов);
			Иначе
				сткПоиска = Новый Структура("ЗаданиеНаПеревозку", текСтрока.Задание);
			КонецЕсли; 
			СтрокиЗадания = СписокЗаданий.НайтиСтроки(сткПоиска);
			Если СтрокиЗадания.Количество() > 0 Тогда
				СтрокаЗадания = СтрокиЗадания[0];
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					мсвОтправлений.Добавить(СтрокаЗадания.АдресОтправления);
					ИзменитьВидимостьОбъекта(СтрокаЗадания.ИндексВМассивеПолучения, Ложь);
					Если РежимОтображенияКарты = 1 Тогда // линии
						ИзменитьВидимостьОбъекта(СтрокаЗадания.ИндексВМассивеЛиния, Ложь);
					ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
						ИзменитьВидимостьОбъекта(СтрокаЗадания.ИндексВМассивеМаршрут, Ложь);
					КонецЕсли;
				КонецЕсли;
				УдалитьСтрокуСпискаЗаданий(СтрокаЗадания);
			КонецЕсли; 
		КонецЦикла;
		
		Если мсвОтправлений.Количество() Тогда
			ОбработатьСостояниеОтправлений(мсвОтправлений);
		КонецЕсли;
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ОтобразитьРейсыНаКарте();
		Иначе
			СписокИндексов.Очистить();
		КонецЕсли;
		ОбновитьПоказателиПоВыбраннымСтрокам();
		ПодготовитьДанныеОтображения();
		ОбновитьДоступность();
		
		Элементы.ДеревоРейсы.ТекущаяСтрока = ИДТекущейСтроки;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуСпискаЗаданий(Знач СтрокаЗадания)
	
	СписокЗаданий.Удалить(СтрокаЗадания);
	Если Элементы.СписокЗаданий.ВыделенныеСтроки.Количество() = 0 Тогда
		Элементы.СписокЗаданий.ТекущаяСтрока = Неопределено;
	КонецЕсли;

КонецПроцедуры // ПеренестиСтрокиИзСпискаЗаданийВРейсНаКлиенте() 

&НаКлиенте
Процедура ПеренестиСтрокиИзДереваРейсовВСписокЗаданийНаКлиенте(МассивСтрок)
	
	Если Не МассивСтрок.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ТекстОшибки = "";
	сткИндексыУдаляемыхОбъектовКарты = Новый Структура;
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыРейсов", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыМаркеров", Новый Массив);
	сткИндексыУдаляемыхОбъектовКарты.Вставить("ИндексыТочекМаршрута", Новый Массив);
	
	// определяем задания из перемещаемых строк
	мсвСтрокРейсов = Новый Массив;
	мсвСтрокЗаданий = Новый Массив;
	Для каждого текСтрока Из МассивСтрок Цикл
		Если текСтрока.Уровень = 1 Тогда // строка задание
			мсвСтрокЗаданий.Добавить(текСтрока);
		Иначе
			мсвСтрокРейсов.Добавить(текСтрока);
		КонецЕсли;
	КонецЦикла;
	
	мсвРейсов = Новый Массив;
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Для каждого СтрокаРейс Из мсвСтрокРейсов Цикл
		мсвЗаданий = Новый Массив;
		ЭлементыЗадания = СтрокаРейс.ПолучитьЭлементы();
		Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				сткДанныеОГрузе = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
				ЗаполнитьЗначенияСвойств(сткДанныеОГрузе, ЭлементЗадание);
				мсвЗаданий.Добавить(сткДанныеОГрузе);
			Иначе	
				мсвЗаданий.Добавить(ЭлементЗадание.ЗаданиеНаПеревозку);
			КонецЕсли; 
			Если ИспользуютсяЭлектронныеГеографическиеКарты И ЭлементЗадание.ИндексВМассиве > 0 Тогда
				сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(ЭлементЗадание.ИндексВМассиве);
			КонецЕсли;
		КонецЦикла; 
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			Если СтрокаРейс.ИндексВМассивеМаршрут > 0 Тогда
				сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаРейс.ИндексВМассивеМаршрут);
			КонецЕсли;
			Для каждого текАдресОтправления Из СтрокаРейс.АдресаОтправлений Цикл
				сткДанныеАдреса = текАдресОтправления.Значение;
				Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
					сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(сткДанныеАдреса.ИндексВМассиве);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
		сткДанныеПоРейсу = Новый Структура("Рейс, Индекс, ДополнительныеПараметры, МассивЗаданий", СтрокаРейс.Рейс, ЭлементыРейсы.Индекс(СтрокаРейс), Новый Структура("НомерЦветовойСхемы", СтрокаРейс.НомерЦветовойСхемы), мсвЗаданий);
		мсвРейсов.Добавить(сткДанныеПоРейсу);
	КонецЦикла; 
	
	соотЗаданияПоРейсам = Новый Соответствие;
	Для каждого СтрокаЗадание Из мсвСтрокЗаданий Цикл
		СтрокаРейс = СтрокаЗадание.ПолучитьРодителя();
		Если мсвСтрокРейсов.Найти(СтрокаРейс) = Неопределено Тогда
			ИскомоеЗначение = соотЗаданияПоРейсам.Получить(СтрокаРейс.Рейс);
			Если ИскомоеЗначение = Неопределено Тогда
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					сткДанныеОГрузе = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
					ЗаполнитьЗначенияСвойств(сткДанныеОГрузе, СтрокаЗадание);
					соотЗаданияПоРейсам.Вставить(СтрокаРейс.Рейс, Новый Структура("Индекс, ДополнительныеПараметры, МассивЗаданий",
					                                                               ЭлементыРейсы.Индекс(СтрокаРейс),
																				   Новый Структура("НомерЦветовойСхемы", СтрокаРейс.НомерЦветовойСхемы),
																				   ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(сткДанныеОГрузе)));
				Иначе
					соотЗаданияПоРейсам.Вставить(СтрокаРейс.Рейс, Новый Структура("Индекс, ДополнительныеПараметры, МассивЗаданий",
					                                                               ЭлементыРейсы.Индекс(СтрокаРейс),
																				   Новый Структура("НомерЦветовойСхемы", СтрокаРейс.НомерЦветовойСхемы),
																				   ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаЗадание.ЗаданиеНаПеревозку)));
				КонецЕсли;
			Иначе
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					сткДанныеОГрузе = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
					ЗаполнитьЗначенияСвойств(сткДанныеОГрузе, СтрокаЗадание);
					ИскомоеЗначение.МассивЗаданий.Добавить(сткДанныеОГрузе);
				Иначе
					ИскомоеЗначение.МассивЗаданий.Добавить(СтрокаЗадание.ЗаданиеНаПеревозку);
				КонецЕсли;
			КонецЕсли;
			
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				Если СтрокаРейс.ИндексВМассивеМаршрут > 0 Тогда
					сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаРейс.ИндексВМассивеМаршрут);
				КонецЕсли;
				мсвИндексовМаркеров = ПолучитьИндексыМаркеровРейсаНаКлиенте(СтрокаРейс);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута, мсвИндексовМаркеров);
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла; 
	сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов);
	сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута = ОбщегоНазначенияКлиентСервер.СвернутьМассив(сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута);
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты И ОтображатьЗоны Тогда
		ИзменитьВидимостьЗон();
	КонецЕсли; 
	
	Для каждого текСоответствие Из соотЗаданияПоРейсам Цикл
		сткДанныеПоРейсу = Новый Структура("Рейс, Индекс, ДополнительныеПараметры, МассивЗаданий");
		сткДанныеПоРейсу.Рейс = текСоответствие.Ключ;
		ЗаполнитьЗначенияСвойств(сткДанныеПоРейсу, текСоответствие.Значение);
		мсвРейсов.Добавить(сткДанныеПоРейсу);
	КонецЦикла; 
	ПеренестиСтрокиИзРейсаВСписокЗаданийНаСервере(мсвРейсов, сткИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ);

	Если Отказ Тогда
		//Если ТекстОшибки = "" Тогда
		//	ТекстОшибки = НСтр("ru = 'Произошла неисправимая ошибка. Рекомендуется обновить данные формы'");
		//КонецЕсли; 
		//ПоказатьПредупреждение(, ТекстОшибки);
		ОповеститьПользователяОбОшибке(, ТекстОшибки);
	Иначе
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			// очистка старых объектов с карты
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыРейсов Цикл
				УдалитьОтображениеРейса(текИндексКарты);
			КонецЦикла; 
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров Цикл
				УдалитьОбъект(текИндексКарты);
			КонецЦикла;
			Для каждого текИндексКарты Из сткИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута Цикл
				УдалитьОбъект(текИндексКарты, Истина);
			КонецЦикла;
		КонецЕсли; 
		
		Для каждого текИндекс Из СписокИндексов Цикл
			Элементы.ДеревоРейсы.Развернуть(ДеревоРейсы.ПолучитьЭлементы().Получить(текИндекс.Значение).ПолучитьИдентификатор(), Ложь);
		КонецЦикла; 
		
		мсвОтправлений = Новый Массив;
		НовыеСтроки = Новый Массив;
		КоличествоСтрок = СписокЗаданий.Количество();
		Пока КоличествоДобавленныхСтрок > 0 Цикл
			ИндексСтроки = КоличествоСтрок - КоличествоДобавленныхСтрок;
			ТекущаяСтрока = СписокЗаданий[ИндексСтроки];
			мсвОтправлений.Добавить(ТекущаяСтрока.АдресОтправления);
			НовыеСтроки.Добавить(ТекущаяСтрока);
			КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок - 1;
		КонецЦикла;
		
		Для каждого текИндекс Из СписокИндексов Цикл
			Элементы.ДеревоРейсы.Развернуть(ДеревоРейсы.ПолучитьЭлементы().Получить(текИндекс.Значение).ПолучитьИдентификатор(), Ложь);
		КонецЦикла; 
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			ИзменитьВидимостьЗон(ОтображатьЗоны);
			Если мсвОтправлений.Количество() Тогда
				мсвОтправлений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвОтправлений);
				Для каждого текОтправление Из мсвОтправлений Цикл
					ИскомыеСтроки = СписокОтправлений.НайтиСтроки(Новый Структура("Адрес", текОтправление));
					Для каждого текСтрока Из ИскомыеСтроки Цикл
						Если Не текСтрока.Пометка Тогда
							ИзменитьВидимостьОбъекта(текСтрока.ИндексВМассиве, Истина);
						КонецЕсли; 
					КонецЦикла; 
				КонецЦикла;
			КонецЕсли;
			
			Если НовыеСтроки.Количество() Тогда
				ОтобразитьОбъектыНаКарте(НовыеСтроки);
			Иначе
				ОтобразитьОбъектыНаКарте();
			КонецЕсли;
			Если СписокИндексов.Количество() Тогда
				ОтобразитьРейсыНаКарте();
			КонецЕсли;
		Иначе
			СписокИндексов.Очистить();
		КонецЕсли;
			
		ОбновитьПоказателиПоВыделеннымСтрокамСпискаЗаданий();
		ОбновитьДоступность();
	КонецЕсли;
	
КонецПроцедуры // ПеренестиСтрокиИзДереваРейсовВСписокЗаданийНаКлиенте()

&НаСервере
Функция ПеренестиСтрокиВнутриДереваРейсовНаСервере(МассивИДСтрок, ИДСтрокиНазначения, ИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ)

	соотЗаданияКИсключению = Новый Соответствие;
	
    мсвЗаданий = Новый Массив;
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗадания = Новый ТаблицаЗначений;
		тзЗадания.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
		тзЗадания.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
		тзЗадания.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
	КонецЕсли;	
	ЗаданияКУдалениюИзСписка = Новый Массив;
	
	// определяем задания из перемещаемых строк
	мсвСтрокРейсов = Новый Массив;
	мсвСтрокЗаданий = Новый Массив;
	Для каждого идСтроки Из МассивИДСтрок Цикл
		СтрокаДерева = ДеревоРейсы.НайтиПоИдентификатору(идСтроки);
		Если СтрокаДерева.Уровень = 1 Тогда // строка задание
			мсвСтрокЗаданий.Добавить(СтрокаДерева);
		Иначе
			мсвСтрокРейсов.Добавить(СтрокаДерева);
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрокаРейс Из мсвСтрокРейсов Цикл
		Если РазбиватьЗаданияПоГрузовымМестам Тогда
			тзЗаданияИскл = соотЗаданияКИсключению.Получить(СтрокаРейс.Рейс);
			Если тзЗаданияИскл = Неопределено Тогда
				тзЗаданияИскл = Новый ТаблицаЗначений;
				тзЗаданияИскл.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
				тзЗаданияИскл.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
				тзЗаданияИскл.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
				соотЗаданияКИсключению.Вставить(СтрокаРейс.Рейс, тзЗаданияИскл);
			КонецЕсли;
		Иначе
			мсвИсключений = соотЗаданияКИсключению.Получить(СтрокаРейс.Рейс);
			Если мсвИсключений = Неопределено Тогда
				мсвИсключений = Новый Массив;
				соотЗаданияКИсключению.Вставить(СтрокаРейс.Рейс, мсвИсключений);
			КонецЕсли;
		КонецЕсли;
		
		ЭлементыЗадания = СтрокаРейс.ПолучитьЭлементы();
		Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
			мсвЗаданий.Добавить(ЭлементЗадание.ЗаданиеНаПеревозку);
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				ЗаполнитьЗначенияСвойств(тзЗадания.Добавить(), ЭлементЗадание);
				ЗаполнитьЗначенияСвойств(тзЗаданияИскл.Добавить(), ЭлементЗадание);
			Иначе
				мсвИсключений.Добавить(ЭлементЗадание.ЗаданиеНаПеревозку);
			КонецЕсли; 
		КонецЦикла; 
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			Если СтрокаРейс.ИндексВМассивеМаршрут > 0 Тогда
				ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаРейс.ИндексВМассивеМаршрут);
			КонецЕсли;
			мсвИндексовМаркеров = ПолучитьИндексыМаркеровРейса(СтрокаРейс);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута, мсвИндексовМаркеров);
		КонецЕсли;
	КонецЦикла; 
	Для каждого СтрокаЗадание Из мсвСтрокЗаданий Цикл
		СтрокаРейс = СтрокаЗадание.ПолучитьРодителя();
		Если мсвСтрокРейсов.Найти(СтрокаРейс) = Неопределено Тогда
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				тзЗаданияИскл = соотЗаданияКИсключению.Получить(СтрокаЗадание.Рейс);
				Если тзЗаданияИскл = Неопределено Тогда
					тзЗаданияИскл = Новый ТаблицаЗначений;
					тзЗаданияИскл.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
					тзЗаданияИскл.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
					тзЗаданияИскл.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
					соотЗаданияКИсключению.Вставить(СтрокаЗадание.Рейс, тзЗаданияИскл);
				КонецЕсли;
			Иначе
				мсвИсключений = соотЗаданияКИсключению.Получить(СтрокаЗадание.Рейс);
				Если мсвИсключений = Неопределено Тогда
					мсвИсключений = Новый Массив;
					соотЗаданияКИсключению.Вставить(СтрокаЗадание.Рейс, мсвИсключений);
				КонецЕсли;
			КонецЕсли;
			
			мсвЗаданий.Добавить(СтрокаЗадание.ЗаданиеНаПеревозку);
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				ЗаполнитьЗначенияСвойств(тзЗадания.Добавить(), СтрокаЗадание);
				ЗаполнитьЗначенияСвойств(тзЗаданияИскл.Добавить(), СтрокаЗадание); 
			Иначе
				мсвИсключений.Добавить(СтрокаЗадание.ЗаданиеНаПеревозку);
			КонецЕсли; 
			
			Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
				Если СтрокаРейс.ИндексВМассивеМаршрут > 0 Тогда
					ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаРейс.ИндексВМассивеМаршрут);
				КонецЕсли;
				мсвИндексовМаркеров = ПолучитьИндексыМаркеровРейса(СтрокаРейс);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута, мсвИндексовМаркеров);
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла; 
	ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов);
	ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута);
	
	Если мсвЗаданий.Количество() = 0 Тогда
		Возврат ЗаданияКУдалениюИзСписка;
	КонецЕсли; 
	
	Для каждого текЗадание Из мсвЗаданий Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текЗадание,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат ЗаданияКУдалениюИзСписка;
		КонецПопытки
	КонецЦикла;	
	
	// определяем строку приемник
	СтрокаПриемник = ДеревоРейсы.НайтиПоИдентификатору(ИДСтрокиНазначения);
	Если СтрокаПриемник.Уровень = 1 Тогда
		СтрокаПриемник = СтрокаПриемник.ПолучитьРодителя();
	КонецЕсли;
	
	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	ИндексСтроки   = ЭлементыДерева.Индекс(СтрокаПриемник);
	СписокИндексов.Добавить(ИндексСтроки);
	
	Рейс = СтрокаПриемник.Рейс;
	
	Если ЗначениеЗаполнено(Рейс) Тогда
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Рейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Рейс);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат ЗаданияКУдалениюИзСписка;
		КонецПопытки;	
	КонецЕсли; 
	
	Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Если СтрокаПриемник.ИндексВМассивеМаршрут > 0 Тогда
			ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаПриемник.ИндексВМассивеМаршрут);
		КонецЕсли;
		мсвИндексовМаркеров = ПолучитьИндексыМаркеровРейса(СтрокаПриемник);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута, мсвИндексовМаркеров);
	КонецЕсли;
	Попытка
		НачатьТранзакцию();
		
		Если РазбиватьЗаданияПоГрузовымМестам Тогда
			мсвГрузовыхМест = Новый Массив;
			Для Каждого текСтрокаИсключения Из соотЗаданияКИсключению Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвГрузовыхМест, текСтрокаИсключения.Значение.ВыгрузитьКолонку("ГрузовоеМесто"), Истина);
				Документы.упРейс.УдалитьЗаданияИзРейса(Неопределено, текСтрокаИсключения.Значение, текСтрокаИсключения.Ключ, ТекстОшибки, Отказ);
			КонецЦикла;
			ОбновитьВетвиДереваРейсовНаСервере(мсвГрузовыхМест);
		Иначе	
			Для Каждого текСтрокаИсключения Из соотЗаданияКИсключению Цикл
				Документы.упРейс.УдалитьЗаданияИзРейса(текСтрокаИсключения.Значение, Неопределено, текСтрокаИсключения.Ключ, ТекстОшибки, Отказ); 
			КонецЦикла;
			ОбновитьВетвиДереваРейсовНаСервере(мсвЗаданий);
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Рейс) Тогда
			// проверим статус рейса
			ПроверитьСтатусРейса(Рейс, ТекстОшибки, Отказ);
			Если Отказ Тогда
				Возврат ЗаданияКУдалениюИзСписка;
			КонецЕсли;
			
			сткПериодПланирования = Новый Структура;
			сткПериодПланирования.Вставить("НачалоПериодаПланирования"   , НачалоПериодаПланирования);
			сткПериодПланирования.Вставить("ОкончаниеПериодаПланирования", ОкончаниеПериодаПланирования);
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				ЗаданияКУдалениюИзСписка = Документы.упРейс.ДобавитьЗаданияВРейс(Неопределено, тзЗадания, Рейс, сткПериодПланирования, ТекстОшибки, Отказ);
			Иначе
				ЗаданияКУдалениюИзСписка = Документы.упРейс.ДобавитьЗаданияВРейс(мсвЗаданий, Неопределено, Рейс, сткПериодПланирования, ТекстОшибки, Отказ);
			КонецЕсли;
			
		Иначе			
			Рейсы = ВручнуюСформироватьРейсы(мсвЗаданий, Отказ, СтрокаПриемник, ТекстОшибки, тзЗадания);
			
			Если Не Отказ Тогда
				Если Рейсы.Количество() Тогда
					Рейс = Рейсы[0];
				КонецЕсли;
				Для Каждого Рейс Из Рейсы Цикл
					Если Ложь Тогда // Для контекстной подсказки
					    Рейс = Документы.упРейс.ПустаяСсылка();
					КонецЕсли;
					тзЗаданияРейса = Рейс.Задания.Выгрузить(, "Задание, ГрузовоеМесто, КоличествоГрузов");
					Для каждого текСтрока Из тзЗаданияРейса Цикл
						ЗаданияКУдалениюИзСписка.Добавить(Новый Структура("Задание, ГрузовоеМесто, КоличествоГрузов", текСтрока.Задание, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов));
					КонецЦикла;
					//ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗаданияКУдалениюИзСписка, Рейс.Задания.ВыгрузитьКолонку("Задание"));
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		текИндекс = СписокИндексов[0].Значение;
		ОбновитьВетвьДереваРейсовНаСервере(Рейс, текИндекс, Новый Структура("НомерЦветовойСхемы", СтрокаПриемник.НомерЦветовойСхемы));
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;	
		Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
			ВызватьИсключение;
		КонецЕсли; 
		ЗаписьЖурналаРегистрации("Перетаскивание задания в рейс", УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;
	КонецПопытки;
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	Возврат ЗаданияКУдалениюИзСписка;
	
КонецФункции

&НаСервере
Функция ПеренестиСтрокиИзСпискаЗаданийВРейсНаСервере(МассивЗаданий, МассивГрузовыхМест, ИДСтрокиНазначения, ИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ)
	
	ЗаданияКУдалениюИзСписка = Новый Массив;
	СтрокаПриемник = ДеревоРейсы.НайтиПоИдентификатору(ИДСтрокиНазначения);
	Если СтрокаПриемник.Уровень = 1 Тогда
		СтрокаПриемник = СтрокаПриемник.ПолучитьРодителя();
	КонецЕсли;
	
	ЭлементыДерева = ДеревоРейсы.ПолучитьЭлементы();
	ИндексСтроки   = ЭлементыДерева.Индекс(СтрокаПриемник);
	СписокИндексов.Добавить(ИндексСтроки);
	
	текРейс	= СтрокаПриемник.Рейс;
	
	Если ЗначениеЗаполнено(текРейс) Тогда
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текРейс);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат ЗаданияКУдалениюИзСписка;
		КонецПопытки;	
	КонецЕсли; 
	
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗадания = Новый ТаблицаЗначений;
		тзЗадания.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
		тзЗадания.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
		тзЗадания.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
		Для каждого текСтруктура Из МассивГрузовыхМест Цикл
			ЗаполнитьЗначенияСвойств(тзЗадания.Добавить(), текСтруктура);
		КонецЦикла;
		тзЗадания.Свернуть("ЗаданиеНаПеревозку, ГрузовоеМесто", "КоличествоГрузов");
	КонецЕсли;
	
	Для каждого текЗадание Из МассивЗаданий Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текЗадание,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат ЗаданияКУдалениюИзСписка;
		КонецПопытки
	КонецЦикла; 
	
	Если ЗначениеЗаполнено(текРейс) И ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Если СтрокаПриемник.ИндексВМассивеМаршрут > 0 Тогда
			ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаПриемник.ИндексВМассивеМаршрут);
		КонецЕсли;
		мсвИндексовМаркеров = ПолучитьИндексыМаркеровРейса(СтрокаПриемник);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута, мсвИндексовМаркеров);
	КонецЕсли; 
	
	Попытка
		НачатьТранзакцию();
		Если ЗначениеЗаполнено(текРейс) Тогда
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Документ", текРейс); 
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				ЭлементБлокировки.ИсточникДанных	= тзЗадания;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "ЗаданиеНаПеревозку");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
			Иначе
				Для каждого текЗадание Из МассивЗаданий Цикл
					ЭлементБлокировки.УстановитьЗначение("Задание", текЗадание);
				КонецЦикла; 
			КонецЕсли;
			упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, текРейс, Отказ);
			Если Отказ Тогда
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли; 
				Возврат ЗаданияКУдалениюИзСписка;
			КонецЕсли;
			
			// проверим статус рейса
			ПроверитьСтатусРейса(текРейс, ТекстОшибки, Отказ);
			Если Отказ Тогда
				Возврат ЗаданияКУдалениюИзСписка;
			КонецЕсли;
			
			сткПериодПланирования = Новый Структура;
			сткПериодПланирования.Вставить("НачалоПериодаПланирования"   , НачалоПериодаПланирования);
			сткПериодПланирования.Вставить("ОкончаниеПериодаПланирования", ОкончаниеПериодаПланирования);
			
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				ЗаданияКУдалениюИзСписка = Документы.упРейс.ДобавитьЗаданияВРейс(Неопределено, тзЗадания, текРейс, сткПериодПланирования, ТекстОшибки, Отказ);
			Иначе
				ЗаданияКУдалениюИзСписка = Документы.упРейс.ДобавитьЗаданияВРейс(МассивЗаданий, Неопределено, текРейс, сткПериодПланирования, ТекстОшибки, Отказ);
			КонецЕсли;
		Иначе
			Рейсы = ВручнуюСформироватьРейсы(МассивЗаданий, Отказ, СтрокаПриемник, ТекстОшибки, тзЗадания);
			
			Если Не Отказ Тогда
				Если Рейсы.Количество() Тогда
					текРейс = Рейсы[0];
				КонецЕсли;
				Для Каждого Рейс Из Рейсы Цикл
					Если Ложь Тогда // Для контекстной подсказки
					    Рейс = Документы.упРейс.ПустаяСсылка();
					КонецЕсли;
					тзЗаданияРейса = Рейс.Задания.Выгрузить(, "Задание, ГрузовоеМесто, КоличествоГрузов");
					Для каждого текСтрока Из тзЗаданияРейса Цикл
						ЗаданияКУдалениюИзСписка.Добавить(Новый Структура("Задание, ГрузовоеМесто, КоличествоГрузов", текСтрока.Задание, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов));
					КонецЦикла; 
					//ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗаданияКУдалениюИзСписка, Рейс.Задания.ВыгрузитьКолонку("Задание"));
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		ОбновитьВетвьДереваРейсовНаСервере(текРейс, ИндексСтроки, Новый Структура("НомерЦветовойСхемы", СтрокаПриемник.НомерЦветовойСхемы));
		Если ТранзакцияАктивна() Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли; 
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;	
		ЗаписьЖурналаРегистрации("Перетаскивание задания в рейс", УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
			ВызватьИсключение;
		КонецЕсли; 
		Отказ = Истина;
	КонецПопытки;
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор);
	
	Возврат ЗаданияКУдалениюИзСписка;
	
КонецФункции

&НаСервере
Функция ВручнуюСформироватьРейсы(МассивЗаданий, Отказ, СтрокаПриемник, ТекстОшибки, тзЗадания)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения"   , НачалоПериодаПланирования);
	ДополнительныеПараметры.Вставить("ПлановоеОкончаниеВыполнения", ОкончаниеПериодаПланирования);
	ДополнительныеПараметры.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей", ПараметрыПланирования.УчитыватьГрафикиРаботыОсновныхВодителей);
	
	РеквизитыПоУмолчанию = Новый Структура("Перевозчик, ТипТС, ТранспортноеСредство");
	ЗаполнитьЗначенияСвойств(РеквизитыПоУмолчанию, СтрокаПриемник);
	РеквизитыПоУмолчанию.Вставить("Автор",ПользователиКлиентСервер.ТекущийПользователь());
	РеквизитыПоУмолчанию.Вставить("ВидТранспортнойУслуги", ?(РежимФормированияКонтейнеров, Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами, Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов));
	РеквизитыПоУмолчанию.Вставить("ПорядокИсполненияЗаявки", Перечисления.упПорядокИсполненияЗаявок.Рейсом);
	
	Если ЗначениеЗаполнено(РеквизитыПоУмолчанию.ТранспортноеСредство) Тогда
		НовоеПлановоеВремя = упРейс.ПолучитьПлановоеВремяНачалаРейса(РеквизитыПоУмолчанию.ТранспортноеСредство, НачалоПериодаПланирования, ОкончаниеПериодаПланирования);
		Если НовоеПлановоеВремя <> Неопределено Тогда
			ДополнительныеПараметры.ПлановоеНачалоВыполнения = НовоеПлановоеВремя;
		КонецЕсли; 
	КонецЕсли; 
	
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		Рейсы = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(Неопределено, тзЗадания, РеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ);
	Иначе
		Рейсы = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(МассивЗаданий, Неопределено, РеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ);
	КонецЕсли;
	
	Возврат Рейсы;

КонецФункции // ПеренестиСтрокиИзСпискаЗаданийВРейсНаСервере()

&НаСервере
Процедура ПеренестиСтрокиИзРейсаВСписокЗаданийНаСервере(МассивРейсов, ИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ); 
	
	мсвРейсов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивРейсов);
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗадания = Новый ТаблицаЗначений;
		тзЗадания.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
		тзЗадания.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
		тзЗадания.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
	Иначе
		мсвЗаданийИтог = Новый Массив;
	КонецЕсли;
	
	Для каждого сткДанныеРейса Из мсвРейсов Цикл
		текРейс = сткДанныеРейса.Рейс;
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текРейс);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат;
		КонецПопытки;
		
		мсвЗаданий = сткДанныеРейса.МассивЗаданий;
		Если РазбиватьЗаданияПоГрузовымМестам Тогда
			тзЗаданияПоРейсу = тзЗадания.СкопироватьКолонки();
			Для каждого сткЗадание Из мсвЗаданий Цикл
				текЗадание = сткЗадание.ЗаданиеНаПеревозку;
				Попытка
					ЗаблокироватьДанныеДляРедактирования(текЗадание,, УникальныйИдентификатор);
				Исключение
					ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание);
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
					Отказ = Истина;
					Возврат;
				КонецПопытки;
				ЗаполнитьЗначенияСвойств(тзЗаданияПоРейсу.Добавить(), сткЗадание);
			КонецЦикла; 
			сткДанныеРейса.Вставить("тзЗадания", тзЗаданияПоРейсу);
		Иначе
			Для каждого текЗадание Из мсвЗаданий Цикл
				Попытка
					ЗаблокироватьДанныеДляРедактирования(текЗадание,, УникальныйИдентификатор);
				Исключение
					ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание);
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
					Отказ = Истина;
					Возврат;
				КонецПопытки;
			КонецЦикла;
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвЗаданийИтог, мсвЗаданий);
		КонецЕсли;
	КонецЦикла;
	
	ИндексРедактируемогоРейса = -1;
	Смещение = 0; // так как виртуальные средства одного типа ТС и перевозчика храняться в свернутом виде, возможны случаи удаления строк без добавления новых
	Для каждого сткДанныеРейса Из мсвРейсов Цикл
		Попытка
			НачатьТранзакцию();
			// проверим статус рейса
			ПроверитьСтатусРейса(сткДанныеРейса.Рейс, ТекстОшибки, Отказ);
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
			Если РазбиватьЗаданияПоГрузовымМестам Тогда
				Документы.упРейс.УдалитьЗаданияИзРейса(Неопределено, сткДанныеРейса.тзЗадания, сткДанныеРейса.Рейс, ТекстОшибки, Отказ); 
			Иначе	
				Документы.упРейс.УдалитьЗаданияИзРейса(сткДанныеРейса.МассивЗаданий, Неопределено, сткДанныеРейса.Рейс, ТекстОшибки, Отказ); 
			КонецЕсли; 
			Если Не Отказ Тогда
				сткДанныеРейса.ДополнительныеПараметры.Вставить("Смещение", 0);
				ОбновитьВетвьДереваРейсовНаСервере(сткДанныеРейса.Рейс, сткДанныеРейса.Индекс + Смещение, сткДанныеРейса.ДополнительныеПараметры);
				Смещение = Смещение + сткДанныеРейса.ДополнительныеПараметры.Смещение;
				Если ИндексРедактируемогоРейса >= 0 И сткДанныеРейса.ИндексСтроки < ИндексРедактируемогоРейса Тогда
					ИндексРедактируемогоРейса = ИндексРедактируемогоРейса + Смещение;
				КонецЕсли;
			КонецЕсли;
			
			Если ТранзакцияАктивна() Тогда
				ЗафиксироватьТранзакцию();
			КонецЕсли;
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
				ВызватьИсключение;
			Иначе
				ТекстТекущейОшибки = ОписаниеОшибки();
				ТекстОшибки = ТекстОшибки + ТекстТекущейОшибки;
				Отказ = Истина;
			КонецЕсли; 
			ЗаписьЖурналаРегистрации("Удаление заданий из рейса", УровеньЖурналаРегистрации.Ошибка,,, ТекстТекущейОшибки);
		КонецПопытки;
	КонецЦикла; 
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор); 
	
	Если Не Отказ Тогда
		Если РазбиватьЗаданияПоГрузовымМестам Тогда
			Если СписокЗаданий.Количество()  Тогда
				// Удаление строк из списка заданий с аналогичным грузовым местом (узкое место, в будущем оптимизировать).
				мсвЗаданий = Новый Массив;
				мсвГрузовыхМест = Новый Массив;
				Для каждого сткДанныеРейса Из мсвРейсов Цикл
					
					тзЗаданияПоРейсу = сткДанныеРейса.тзЗадания;
					Для каждого текСтрокаПоРейсу Из тзЗаданияПоРейсу Цикл
						сткПоиска = Новый Структура("ЗаданиеНаПеревозку, ГрузовоеМесто");
						ЗаполнитьЗначенияСвойств(сткПоиска, текСтрокаПоРейсу); 
						ИскомыеСтроки = СписокЗаданий.НайтиСтроки(сткПоиска);
						Для каждого текСтрока Из ИскомыеСтроки Цикл
							Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
								Если текСтрока.ИндексВМассивеПолучения > 0 Тогда
									ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеПолучения);
								КонецЕсли;
								Если РежимОтображенияКарты = 1 Тогда // линии
									ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеЛиния);
								ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
									ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеМаршрут);
								КонецЕсли;
							КонецЕсли;
							СписокЗаданий.Удалить(текСтрока);
						КонецЦикла;
						мсвЗаданий.Добавить(текСтрокаПоРейсу.ЗаданиеНаПеревозку);
						мсвГрузовыхМест.Добавить(текСтрокаПоРейсу.ГрузовоеМесто);
					КонецЦикла;
				КонецЦикла; 
				
				мсвОтборов = Новый Массив;
				Отбор = Новый Структура;
				Отбор.Вставить("ИмяПоля"       , "ЗаданиеНаПеревозку");
				Отбор.Вставить("ПравоеЗначение", мсвЗаданий);
				Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);
				мсвОтборов.Добавить(Отбор);
				
				Отбор = Новый Структура;
				Отбор.Вставить("ИмяПоля"       , "ГрузовоеМесто");
				Отбор.Вставить("ПравоеЗначение", мсвГрузовыхМест);
				Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);				
				мсвОтборов.Добавить(Отбор);
				
				ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь);
			Иначе
				ОбновитьТаблицуЗаданийНаСервере();
			КонецЕсли; 
		Иначе
			мсвОтборов = Новый Массив;
			Отбор = Новый Структура;
			Отбор.Вставить("ИмяПоля"       , "ЗаданиеНаПеревозку");
			Отбор.Вставить("ПравоеЗначение", мсвЗаданийИтог);
			Отбор.Вставить("ВидСравнения"  , ВидСравненияКомпоновкиДанных.ВСписке);
			мсвОтборов.Добавить(Отбор);
			ОбновитьТаблицуЗаданийНаСервере(мсвОтборов, Ложь);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры // ПеренестиСтрокиИзРейсаВСписокЗаданийНаСервере()

&НаСервере
Функция ПеренестиВыбранныйГрузВРейсНаСервере(ИДСтрокиНазначения, ИндексыУдаляемыхОбъектовКарты, ТекстОшибки, Отказ)

	КоличествоДобавленныхСтрок = 0;
	
	ЗаданияКУдалениюИзСписка = Новый Массив;
	СтрокаПриемник = ДеревоРейсы.НайтиПоИдентификатору(ИДСтрокиНазначения);
	ЭлементыРейсы = ДеревоРейсы.ПолучитьЭлементы();
	Рейс = СтрокаПриемник.Рейс;
	
	мсвЗаданий = Новый Массив;
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗаданияШаблон = Новый ТаблицаЗначений;
		тзЗаданияШаблон.Колонки.Добавить("ЗаданиеНаПеревозку", Новый ОписаниеТипов("ДокументСсылка.упЗаданиеНаПеревозкуГруза"));
		тзЗаданияШаблон.Колонки.Добавить("ГрузовоеМесто", Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"));
		тзЗаданияШаблон.Колонки.Добавить("КоличествоГрузов", Новый ОписаниеТипов("Число"));
		
		тзЗадания = тзЗаданияШаблон.СкопироватьКолонки();
	КонецЕсли;	
	
	ИскомыеСтрокиСпискаЗаданий = СписокЗаданий.НайтиСтроки(Новый Структура("Выбрано", Истина));
	
	Для каждого текСтрока Из ИскомыеСтрокиСпискаЗаданий Цикл
		мсвЗаданий.Добавить(текСтрока.ЗаданиеНаПеревозку);
		Если РазбиватьЗаданияПоГрузовымМестам Тогда
			ЗаполнитьЗначенияСвойств(тзЗадания.Добавить(), текСтрока);
		КонецЕсли;
		
		Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
			Если текСтрока.ИндексВМассивеПолучения > 0 Тогда
				ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеПолучения);
			КонецЕсли;
			Если РежимОтображенияКарты = 1 Тогда // линии
				ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеЛиния);
			ИначеЕсли РежимОтображенияКарты = 2 Тогда // маршруты
				ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(текСтрока.ИндексВМассивеМаршрут);
			КонецЕсли;
			ИндексыУдаляемыхОбъектовКарты.МассивОтправлений.Добавить(текСтрока.АдресОтправления);
		КонецЕсли;
	КонецЦикла; 
	
	мсвРейсов = Новый Массив;
	Для каждого ЭлементРейс Из ЭлементыРейсы Цикл
		
		Если ЭлементРейс.Выбран Тогда
			
			Если ЭлементРейс = СтрокаПриемник Тогда
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					Если ЭлементРейс.ИндексВМассивеМаршрут > 0 Тогда
						ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(ЭлементРейс.ИндексВМассивеМаршрут);
					КонецЕсли;
					Для каждого текАдресОтправления Из ЭлементРейс.АдресаОтправлений Цикл
						сткДанныеАдреса = текАдресОтправления.Значение;
						Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
							ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(сткДанныеАдреса.ИндексВМассиве);
						КонецЕсли;
					КонецЦикла;
					
					ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
					Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
						Если ЭлементЗадание.ИндексВМассиве > 0 Тогда
							ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(ЭлементЗадание.ИндексВМассиве);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
			Иначе
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					тзЗаданияПоРейсу = тзЗаданияШаблон.СкопироватьКолонки();
				КонецЕсли;
				мсвЗаданийПоРейсу = Новый Массив;
				
				Если ИспользуютсяЭлектронныеГеографическиеКарты Тогда
					Если ЭлементРейс.ИндексВМассивеМаршрут > 0 Тогда
						ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(ЭлементРейс.ИндексВМассивеМаршрут);
					КонецЕсли;
					Для каждого текАдресОтправления Из ЭлементРейс.АдресаОтправлений Цикл
						сткДанныеАдреса = текАдресОтправления.Значение;
						Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
							ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(сткДанныеАдреса.ИндексВМассиве);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				ЭлементыЗадания = ЭлементРейс.ПолучитьЭлементы();
				Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
					Если ЭлементЗадание.Выбран Тогда
						мсвЗаданийПоРейсу.Добавить(ЭлементЗадание.ЗаданиеНаПеревозку);
						Если РазбиватьЗаданияПоГрузовымМестам Тогда
							ЗаполнитьЗначенияСвойств(тзЗаданияПоРейсу.Добавить(), ЭлементЗадание);
						КонецЕсли;	
					КонецЕсли;
					
					Если ИспользуютсяЭлектронныеГеографическиеКарты И ЭлементЗадание.ИндексВМассиве > 0 Тогда
						ИндексыУдаляемыхОбъектовКарты.ИндексыТочекМаршрута.Добавить(ЭлементЗадание.ИндексВМассиве);
					КонецЕсли;
				КонецЦикла;
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвЗаданий, мсвЗаданийПоРейсу);
				
				сткДанныеПоРейсу = Новый Структура("Рейс, Индекс, ДополнительныеПараметры", ЭлементРейс.Рейс, ЭлементыРейсы.Индекс(ЭлементРейс), Новый Структура("НомерЦветовойСхемы", ЭлементРейс.НомерЦветовойСхемы));
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					сткДанныеПоРейсу.Вставить("ТаблицаЗаданий", тзЗаданияПоРейсу);
					ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(тзЗаданияПоРейсу, тзЗадания);
				Иначе
					сткДанныеПоРейсу.Вставить("МассивЗаданий", мсвЗаданийПоРейсу);
				КонецЕсли;
				мсвРейсов.Добавить(сткДанныеПоРейсу);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если мсвЗаданий.Количество() = 0 И мсвРейсов.Количество() = 0 Тогда
		Возврат ЗаданияКУдалениюИзСписка;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Рейс) И Не СтрокаПриемник.Выбран И ИспользуютсяЭлектронныеГеографическиеКарты Тогда
		Если СтрокаПриемник.ИндексВМассивеМаршрут > 0 Тогда
			ИндексыУдаляемыхОбъектовКарты.ИндексыРейсов.Добавить(СтрокаПриемник.ИндексВМассивеМаршрут);
		КонецЕсли;
		Для каждого текАдресОтправления Из СтрокаПриемник.АдресаОтправлений Цикл
			сткДанныеАдреса = текАдресОтправления.Значение;
			Если сткДанныеАдреса.ИндексВМассиве > 0 Тогда
				ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(сткДанныеАдреса.ИндексВМассиве);
			КонецЕсли;
		КонецЦикла;
		ЭлементыЗадания = СтрокаПриемник.ПолучитьЭлементы();
		Для каждого ЭлементЗадание Из ЭлементыЗадания Цикл
			Если ЭлементЗадание.ИндексВМассиве > 0 Тогда
				ИндексыУдаляемыхОбъектовКарты.ИндексыМаркеров.Добавить(ЭлементЗадание.ИндексВМассиве);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	Для каждого сткДанныеРейса Из мсвРейсов Цикл
		текРейс = сткДанныеРейса.Рейс;
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текРейс,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текРейс);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат ЗаданияКУдалениюИзСписка;
		КонецПопытки;
	КонецЦикла;
	
	мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
	Для каждого текЗадание Из мсвЗаданий Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(текЗадание,, УникальныйИдентификатор);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
			Возврат ЗаданияКУдалениюИзСписка;
		КонецПопытки;
	КонецЦикла;
	
	Если мсвРейсов.Количество() Тогда
		// удаление заданий из рейсов
		ИндексРедактируемогоРейса = -1;
		Смещение = 0;
		Для каждого сткДанныеРейса Из мсвРейсов Цикл
			Попытка
				НачатьТранзакцию();
				
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					Документы.упРейс.УдалитьЗаданияИзРейса(Неопределено, сткДанныеРейса.ТаблицаЗаданий, сткДанныеРейса.Рейс, ТекстОшибки, Отказ); 
				Иначе	
					Документы.упРейс.УдалитьЗаданияИзРейса(сткДанныеРейса.МассивЗаданий, Неопределено, сткДанныеРейса.Рейс, ТекстОшибки, Отказ); 
				КонецЕсли; 
				сткДанныеРейса.ДополнительныеПараметры.Вставить("Смещение", 0);
				ОбновитьВетвьДереваРейсовНаСервере(сткДанныеРейса.Рейс, сткДанныеРейса.Индекс + Смещение, сткДанныеРейса.ДополнительныеПараметры);
				Смещение = Смещение + сткДанныеРейса.ДополнительныеПараметры.Смещение;
				Если ИндексРедактируемогоРейса >= 0 И сткДанныеРейса.ИндексСтроки < ИндексРедактируемогоРейса Тогда
					ИндексРедактируемогоРейса = ИндексРедактируемогоРейса + Смещение;
				КонецЕсли;
				
				Если ТранзакцияАктивна() Тогда
					ЗафиксироватьТранзакцию();
				КонецЕсли;
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
					ВызватьИсключение;
				КонецЕсли; 
				ЗаписьЖурналаРегистрации("Перетаскивание заданий из рейса", УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЦикла;
	КонецЕсли; 
		
	Если (РазбиватьЗаданияПоГрузовымМестам И тзЗадания.Количество()) Или мсвЗаданий.Количество() Тогда
		// добавление заданий в рейс
		Попытка
			НачатьТранзакцию();
						
			Если ЗначениеЗаполнено(Рейс) Тогда
				БлокировкаДанных = Новый БлокировкаДанных;
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Документ", Рейс); 
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					ЭлементБлокировки.ИсточникДанных	= тзЗадания;
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "ЗаданиеНаПеревозку");
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
				Иначе
					Для каждого текЗадание Из мсвЗаданий Цикл
						ЭлементБлокировки.УстановитьЗначение("Задание", текЗадание);
					КонецЦикла; 
				КонецЕсли;
				упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Рейс, Отказ);
				Если Отказ Тогда
					Если ТранзакцияАктивна() Тогда
						ОтменитьТранзакцию();
					КонецЕсли; 
					Возврат ЗаданияКУдалениюИзСписка;
				КонецЕсли;
				
				// проверим статус рейса
				ПроверитьСтатусРейса(Рейс, ТекстОшибки, Отказ);
				Если Отказ Тогда
					Возврат ЗаданияКУдалениюИзСписка;
				КонецЕсли; 
				
				сткПериодПланирования = Новый Структура;
				сткПериодПланирования.Вставить("НачалоПериодаПланирования"   , НачалоПериодаПланирования);
				сткПериодПланирования.Вставить("ОкончаниеПериодаПланирования", ОкончаниеПериодаПланирования);
				
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					ЗаданияКУдалениюИзСписка = Документы.упРейс.ДобавитьЗаданияВРейс(Неопределено, тзЗадания, Рейс, сткПериодПланирования, ТекстОшибки, Отказ);
				Иначе
					ЗаданияКУдалениюИзСписка = Документы.упРейс.ДобавитьЗаданияВРейс(мсвЗаданий, Неопределено, Рейс, сткПериодПланирования, ТекстОшибки, Отказ);
				КонецЕсли;

			Иначе
				БлокировкаДанных = Новый БлокировкаДанных;
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Если РазбиватьЗаданияПоГрузовымМестам Тогда
					ЭлементБлокировки.ИсточникДанных	= тзЗадания;
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "ЗаданиеНаПеревозку");
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
				Иначе
					Для каждого текЗадание Из мсвЗаданий Цикл
						ЭлементБлокировки.УстановитьЗначение("Задание", текЗадание);
					КонецЦикла; 
				КонецЕсли;
				упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных,, Отказ);
				Если Отказ Тогда
					Если ТранзакцияАктивна() Тогда
						ОтменитьТранзакцию();
					КонецЕсли; 
					Возврат ЗаданияКУдалениюИзСписка;
				КонецЕсли;
				
				//ДополнительныеПараметры = Новый Структура;
				//ДополнительныеПараметры.Вставить("ПлановоеНачалоВыполнения", НачалоПериодаПланирования);
				//ДополнительныеПараметры.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей", ПараметрыПланирования.УчитыватьГрафикиРаботыОсновныхВодителей);
				//
				//РеквизитыПоУмолчанию = Новый Структура("Перевозчик, ТипТС, ТранспортноеСредство");
				//ЗаполнитьЗначенияСвойств(РеквизитыПоУмолчанию, СтрокаПриемник);
				//РеквизитыПоУмолчанию.Вставить("Автор",ПользователиКлиентСервер.ТекущийПользователь());
				//
				//Если ЗначениеЗаполнено(РеквизитыПоУмолчанию.ТранспортноеСредство) Тогда
				//	НовоеПлановоеВремя = упРейс.ПолучитьПлановоеВремяНачалаРейса(РеквизитыПоУмолчанию.ТранспортноеСредство, НачалоПериодаПланирования, ОкончаниеПериодаПланирования);
				//	Если НовоеПлановоеВремя <> Неопределено Тогда
				//		ДополнительныеПараметры.ПлановоеНачалоВыполнения = НовоеПлановоеВремя;
				//	КонецЕсли; 
				//КонецЕсли; 
				//
				//Если РазбиватьЗаданияПоГрузовымМестам Тогда
				//	Рейсы = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(Неопределено, тзЗадания, РеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ);
				//Иначе
				//	Рейсы = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(мсвЗаданий, Неопределено, РеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ);
				//КонецЕсли;
				
				Рейсы = ВручнуюСформироватьРейсы(мсвЗаданий, Отказ, СтрокаПриемник, ТекстОшибки, тзЗадания);
				
				Если Не Отказ Тогда
					Если мсвРейсов.Количество() Тогда
						Рейс = мсвРейсов[0];
					КонецЕсли;
					Для Каждого Рейс Из Рейсы Цикл
						Если Ложь Тогда // Для контекстной подсказки
							Рейс = Документы.упРейс.ПустаяСсылка();
						КонецЕсли;
						тзЗаданияРейса = Рейс.Задания.Выгрузить(, "Задание, ГрузовоеМесто, КоличествоГрузов");
						Для каждого текСтрока Из тзЗаданияРейса Цикл
							ЗаданияКУдалениюИзСписка.Добавить(Новый Структура("Задание, ГрузовоеМесто, КоличествоГрузов", текСтрока.Задание, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов));
						КонецЦикла;
						//ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗаданияКУдалениюИзСписка, Рейс.Задания.ВыгрузитьКолонку("Задание"));
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			текИндекс = ДеревоРейсы.ПолучитьЭлементы().Индекс(СтрокаПриемник);
			Если текИндекс < 0 Тогда
				текИндекс = СписокИндексов[0].Значение;
			КонецЕсли;
			ОбновитьВетвьДереваРейсовНаСервере(Рейс, текИндекс, Новый Структура("НомерЦветовойСхемы", СтрокаПриемник.НомерЦветовойСхемы));
			Если ТранзакцияАктивна() Тогда
				ЗафиксироватьТранзакцию();
			КонецЕсли;
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;	
			Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
				ВызватьИсключение;
			КонецЕсли; 
			ЗаписьЖурналаРегистрации("Перетаскивание заданий в рейс", УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Отказ = Истина;
		КонецПопытки;
	КонецЕсли; 
	РазблокироватьДанныеДляРедактирования(, УникальныйИдентификатор); 
	
	Если Не Отказ Тогда
		//КоличествоДобавленныхСтрок = 0;
		Для каждого текСтрока Из ИскомыеСтрокиСпискаЗаданий Цикл
			СписокЗаданий.Удалить(текСтрока);
		КонецЦикла;
		ПодготовитьДанныеОтображения();
	КонецЕсли;

	Возврат ЗаданияКУдалениюИзСписка;
	
КонецФункции

&НаСервере
Процедура ПроверитьСтатусРейса(Рейс, ТекстОшибки, Отказ)
	
	ТекущийСтатус = Документы.упРейс.ПолучитьСтатус(Рейс);
	Если Не (ТекущийСтатус = Перечисления.упСтатусыРейса.Новый Или ТекущийСтатус = Перечисления.упСтатусыРейса.КПланированиюТС Или ТекущийСтатус = Перечисления.упСтатусыРейса.НазначеноТС) Тогда
		Отказ = Истина;
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено изменение документа %1. Рейс находится в статусе ""%2""'", Рейс, ТекущийСтатус));
	КонецЕсли;
	
КонецПроцедуры				

&НаСервереБезКонтекста
Процедура ПересчитатьМаршрутНаСервере(Рейс, ПараметрыПланирования, ИдентификаторЗадания, АдресВременногоХранилища, ИдентификаторФормы, ПериодПланирования)
	
	МетодРешенияЗК             = ПараметрыПланирования.МетодРешенияЗК;
	ВариантОптимизацииМаршрута = ПараметрыПланирования.ВариантОптимизацииМаршрута;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрут.Идентификатор,
	|	упМаршрут.Адрес,
	|	упМаршрут.ТипТочки,
	|	упМаршрут.Предшественники,
	|	ВЫБОР
	|		КОГДА упМаршрут.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &НачалоПериода
	|		ИНАЧЕ упМаршрут.ВременноеОкноНачало
	|	КОНЕЦ КАК ВременноеОкноНачало,
	|	ВЫБОР
	|		КОГДА упМаршрут.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &КонецПериода
	|		ИНАЧЕ упМаршрут.ВременноеОкноОкончание
	|	КОНЕЦ КАК ВременноеОкноОкончание,
	|	упМаршрут.ВремяРабот
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрут
	|ГДЕ
	|	упМаршрут.Рейс = &Рейс
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрут.СтрокаНомер";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("НачалоПериода", ПериодПланирования.НачалоПериодаПланирования);
	Запрос.УстановитьПараметр("КонецПериода" , ПериодПланирования.ОкончаниеПериодаПланирования);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Новый Массив, ИдентификаторФормы);
		
		тзТочек = Результат.Выгрузить();
		сткДопПараметры = Новый Структура;
		Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
			сткВесов = Новый Структура;
			сткВесов.Вставить("ПространственныйВес", ПараметрыПланирования.ПространственныйВес);
			сткВесов.Вставить("ВременнойВес", ПараметрыПланирования.ВременнойВес);
			сткВесов.Вставить("ВесСрочности", ПараметрыПланирования.ВесСрочности);
			
			сткДопПараметры.Вставить("СтруктураВесов", сткВесов);
		КонецЕсли; 
		
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(тзТочек);
		мсвПараметров.Добавить(ВариантОптимизацииМаршрута);
		мсвПараметров.Добавить(АдресВременногоХранилища);
		мсвПараметров.Добавить(МетодРешенияЗК);
		мсвПараметров.Добавить(сткДопПараметры);
		
		//упКомбинаторнаяОптимизация.ИзменитьПорядокТочек(тзТочек, ВариантОптимизацииМаршрута, АдресВременногоХранилища, МетодРешенияЗК, сткДопПараметры);
		ФоновоеЗадание = ФоновыеЗадания.Выполнить("упКомбинаторнаяОптимизация.ИзменитьПорядокТочек", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Решение ЗК'"));
		ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;
		
	КонецЕсли;
		
КонецПроцедуры // ПересчитатьМаршрутНаСервере()

&НаСервереБезКонтекста
Процедура ИзменитьПорядокОбъездаТочекРейса(СтруктураВозврат, Рейс, ПараметрыПланирования, ТекстОшибки)
	
	РейсОбъект = Рейс.ПолучитьОбъект();
	РейсОбъект.ОптимизацияМаршрута = Истина;
	Попытка
		РейсОбъект.Заблокировать();
	Исключение
		ТекстОшибки = НСтр("ru= 'Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Рейс);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	НачатьТранзакцию();
	
	Отказ = Ложь;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упРейс");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Рейс);
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Рейс, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РейсОбъект.ДополнительныеСвойства.Вставить("ПересчетМаршрута"      , СтруктураВозврат);
	РейсОбъект.ДополнительныеСвойства.Вставить("УменьшатьВремяОжидания", ПараметрыПланирования.УменьшатьВремяОжидания);
	Попытка
		РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Не удалось оптимизировать маршрут (%1): %2.'", Рейс, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;
	
	Если ТранзакцияАктивна() Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли; 
	
КонецПроцедуры // ИзменитьПорядокОбъездаТочекРейса()

&НаКлиенте
Функция РазрешеноКорректироватьРейсы(мсвСтатусыРейсов)
	
	Результат = Истина;
	Для каждого Статус Из мсвСтатусыРейсов Цикл
		 Результат = Результат И НЕ упРаботаСоСтатусамиКлиентСервер.РейсПереданКВыполнению(Статус);
		 Если НЕ Результат Тогда
		 	Прервать;
		 КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ТочкаОтсчетаУдалитьПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ПериодПланирования) Тогда
		сткДанныеПериода = ВернутьПериодОтбора(ПериодПланирования, ?(ЗначениеЗаполнено(ТочкаОтсчета), ТочкаОтсчета, Неопределено));
		ЭтаФорма.НачалоПериодаПланирования    = сткДанныеПериода.НачалоПериода;
		ЭтаФорма.ОкончаниеПериодаПланирования = сткДанныеПериода.ОкончаниеПериода;
		ОбновитьТаблицуЗаданий(Неопределено);
		ОбновитьДоступность();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеСтрокиСпискаЗаданий(Команда)
	
	Элементы.СписокЗаданий.ВыделенныеСтроки.Очистить();
	Для Каждого СтрокаДерева Из СписокЗаданий Цикл
		Элементы.СписокЗаданий.ВыделенныеСтроки.Добавить(СтрокаДерева.ПолучитьИдентификатор());
	КонецЦикла;
	ВыбратьСтрокиСпискаЗаданий(Элементы.СписокЗаданий.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеСтрокиДереваРейсов(Команда)
	
	ВыделенныеСтрокиДерева = Новый Массив;
	Для Каждого СтрокаДерева Из ДеревоРейсы.ПолучитьЭлементы() Цикл
		ВыделенныеСтрокиДерева.Добавить(СтрокаДерева);
	КонецЦикла;
	ВыбратьСтрокиДереваРейсов(ВыделенныеСтрокиДерева);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиЗагрузкиСпискаЗаданийНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	//СписокЗаданийПриАктивизацииСтрокиОбработчикОжидания();

КонецПроцедуры

&НаКлиенте
Процедура ДеревоРейсыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если РежимКорректировки Тогда
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
		Возврат;
	КонецЕсли; 
	ДеревоРейсыПриАктивизацииСтрокиОбработчикОжидания();

КонецПроцедуры

&НаКлиенте
Процедура РежимКорректировки(Команда)
	
	ЭтаФорма.РежимКорректировки = Не РежимКорректировки;
	Если Не РежимКорректировки Тогда
		ЛучшиеРейсы.Очистить();
	КонецЕсли; 
	ОбновитьДанныеИОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказатьБлижайшиеПоТекущемуПоложению(Команда)
	
	ВыбранныеЗадания = Новый Массив;
	Для Каждого СтрокаГруза Из СписокЗаданий Цикл
		Если СтрокаГруза.Выбрано И ВыбранныеЗадания.Найти(СтрокаГруза.ЗаданиеНаПеревозку) = Неопределено Тогда
			ВыбранныеЗадания.Добавить(СтрокаГруза.ЗаданиеНаПеревозку);
		КонецЕсли; 
	КонецЦикла;
	Если ВыбранныеЗадания.Количество() = 0 Тогда
		ТекстСообщения = "Выбрано 0 заданий. Выберите флажками нужные задания.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
	РассчитатьБлижайшиеПоПутиСледованияНаСервере(ВыбранныеЗадания, Ложь);
	ОбновитьОтображениеНаКарте();
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьМаркерЛучшегоРейса(СтрокаРейса, ИндексВДереве, Знач Широта = Неопределено, Знач Долгота = Неопределено)
	
	Если Широта = Неопределено Тогда
		Широта = СтрокаРейса.ТекущаяШирота;
	КонецЕсли; 
	Если Долгота = Неопределено Тогда
		Долгота = СтрокаРейса.ТекущаяДолгота;
	КонецЕсли; 
	МассивТочек = Новый Массив;
	МассивТочек.Добавить(ПредставлениеЧисла(Широта));
	МассивТочек.Добавить(ПредставлениеЧисла(Долгота));
	МассивТочек.Добавить(СтрокаРейса.НомерВПорядкеЛучшихРейсов);
	МассивТочек.Добавить(ПредставлениеСтроки(СтрокаРейса.Представление));
	
	МассивКоординат = Новый  Массив;
	МассивКоординат.Добавить("#FFA84D"); // Оранжевый цвет заливки круга
	МассивКоординат.Добавить(3);
	МассивКоординат.Добавить(0); // Прозрачность
	МассивКоординат.Добавить(СтрСоединить(МассивТочек, "//"));
	СтрокаКоординат = СтрСоединить(МассивКоординат, ";");
	ВыполнитьПроцедуруHTML("showCircleWithNumber(""" + СтрокаКоординат + """, " + ИндексВДереве + ")");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеПлановыхМаршрутов(Команда)
	
	ЭтаФорма.ОтображениеПлановыхМаршрутов = Не ОтображениеПлановыхМаршрутов;
	ОбновитьОтображениеНаКарте();
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказатьВсеРейсы(Команда)
	
	ЛучшиеРейсы.Очистить();
	ОбновитьОтображениеНаКарте();
	ОбновитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьБлижайшиеПоПутиСледованияНаСервере(ВыбранныеЗадания, ЭтоЛучшиеПоПутиСледования)
	
	//МассивРейсов = Новый Массив;
	//Для Каждого СтрокаРейса Из ДеревоРейсы.ПолучитьЭлементы() Цикл
	//	МассивРейсов.Добавить(СтрокаРейса.Рейс);
	//КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Т.АдресПолучения.Широта КАК ПолучениеШирота,
	               |	Т.АдресПолучения.Долгота КАК ПолучениеДолгота,
	               |	Т.Масса,
	               |	Т.Объем,
	               |	Т.АдресОтправления.Широта КАК ОтправлениеШирота,
	               |	Т.АдресОтправления.Долгота КАК ОтправлениеДолгота,
	               |	Т.АдресОтправления,
	               |	Т.АдресПолучения,
	               |	Т.Ссылка
	               |ИЗ
	               |	Документ.упЗаданиеНаПеревозкуГруза КАК Т
	               |ГДЕ
	               |	Т.Ссылка В(&Ссылка)";
	Запрос.УстановитьПараметр("Ссылка", ВыбранныеЗадания);
	//Запрос.УстановитьПараметр("Рейс", МассивРейсов);
	ТаблицаВыбранныхЗаданий = Запрос.Выполнить().Выгрузить();
	ЭтаФорма.ЦентрОтправленияВыбранныхЗаданийШирота = ТаблицаВыбранныхЗаданий.Итог("ОтправлениеШирота") / ТаблицаВыбранныхЗаданий.Количество();
	ЭтаФорма.ЦентрОтправленияВыбранныхЗаданийДолгота = ТаблицаВыбранныхЗаданий.Итог("ОтправлениеДолгота") / ТаблицаВыбранныхЗаданий.Количество();
	ЭтаФорма.ЦентрПолученияВыбранныхЗаданийШирота = ТаблицаВыбранныхЗаданий.Итог("ПолучениеШирота") / ТаблицаВыбранныхЗаданий.Количество();
	ЭтаФорма.ЦентрПолученияВыбранныхЗаданийДолгота = ТаблицаВыбранныхЗаданий.Итог("ПолучениеДолгота") / ТаблицаВыбранныхЗаданий.Количество();
	МассаЗаданий = ТаблицаВыбранныхЗаданий.Итог("Масса");
	ОбъемЗаданий = ТаблицаВыбранныхЗаданий.Итог("Объем");
	ПараметрыКарты = Неопределено;
	ЛучшиеРейсы.Очистить();
	СписокРейсов = Новый ТаблицаЗначений;
	СписокРейсов.Колонки.Добавить("Рейс");
	СписокРейсов.Колонки.Добавить("Расстояние");
	Для Каждого СтрокаРейса Из ДеревоРейсы.ПолучитьЭлементы() Цикл
		СтрокаРейса.КоординатыЛинийКорректировка = "";
		СтрокаРейса.НомерВПорядкеЛучшихРейсов = 0;
		Если Ложь
			Или (СтрокаРейса.Масса + МассаЗаданий) / СтрокаРейса.МассаМакс > ПараметрыПланирования.МаксимальныйПроцентЗагрузкиТС / 100
			Или (СтрокаРейса.Объем + ОбъемЗаданий) / СтрокаРейса.ОбъемМакс > ПараметрыПланирования.МаксимальныйПроцентЗагрузкиТС / 100
		Тогда
			Продолжить;
		КонецЕсли;
		ЛучшаяПозицияПогрузки = Неопределено;
		ЛучшаяПозицияРазгрузки = Неопределено;
		ЛучшаяПогрузкаШирота = Неопределено;
		ЛучшаяПогрузкаДолгота = Неопределено;
		ЛучшаяРазгрузкаШирота = Неопределено;
		ЛучшаяРазрузкаДолгота = Неопределено;
		ЛучшееРасстояниеПогрузки = Неопределено;
		ЛучшееРасстояниеРазгрузки = Неопределено;
		Если ЭтоЛучшиеПоПутиСледования Тогда
			МаршрутРейса = МаршрутыРейсов.Выгрузить(Новый Структура("Рейс", СтрокаРейса.Рейс));
			Для Каждого СтрокаМаршрута Из МаршрутРейса Цикл
				МассивКоординат = СтрРазделить(СтрокаМаршрута.Путь, ";");
				Для ИндексКоординаты = 0 По МассивКоординат.Количество() / 2 - 1 Цикл
					ТочкаМаршрутаШирота  = Число(МассивКоординат[ИндексКоординаты * 2 + 0]);
					ТочкаМаршрутаДолгота = Число(МассивКоординат[ИндексКоординаты * 2 + 1]);
					РасстояниеПогрузки = (ЦентрОтправленияВыбранныхЗаданийШирота - ТочкаМаршрутаШирота) * (ЦентрОтправленияВыбранныхЗаданийШирота - ТочкаМаршрутаШирота) 
						+ (ЦентрОтправленияВыбранныхЗаданийДолгота - ТочкаМаршрутаДолгота) * (ЦентрОтправленияВыбранныхЗаданийДолгота - ТочкаМаршрутаДолгота);
					РасстояниеРазгрузки = (ЦентрПолученияВыбранныхЗаданийШирота - ТочкаМаршрутаШирота) * (ЦентрПолученияВыбранныхЗаданийШирота - ТочкаМаршрутаШирота) 
						+ (ЦентрПолученияВыбранныхЗаданийДолгота - ТочкаМаршрутаДолгота) * (ЦентрПолученияВыбранныхЗаданийДолгота - ТочкаМаршрутаДолгота);
					Если Ложь
						Или ЛучшееРасстояниеПогрузки = Неопределено
						Или ЛучшееРасстояниеПогрузки > РасстояниеПогрузки
					Тогда
						ЛучшееРасстояниеПогрузки = РасстояниеПогрузки;
						ЛучшаяПозицияПогрузки = СтрокаМаршрута.НомерСтроки;
						ЛучшаяПогрузкаШирота = ТочкаМаршрутаШирота;
						ЛучшаяПогрузкаДолгота = ТочкаМаршрутаДолгота;
					КонецЕсли; 
					Если Ложь
						Или ЛучшееРасстояниеРазгрузки = Неопределено
						Или ЛучшееРасстояниеРазгрузки > РасстояниеРазгрузки
					Тогда
						ЛучшееРасстояниеРазгрузки = РасстояниеРазгрузки;
						ЛучшаяПозицияРазгрузки = СтрокаМаршрута.НомерСтроки;
						ЛучшаяРазгрузкаШирота = ТочкаМаршрутаШирота;
						ЛучшаяРазрузкаДолгота = ТочкаМаршрутаДолгота;
					КонецЕсли; 
				КонецЦикла;
			КонецЦикла;
			КорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
			КорректировкаМаршрутаОбъект.Рейс = СтрокаРейса.Рейс;
			//КорректировкаМаршрутаОбъект.Период = ;
			КорректировкаМаршрутаОбъект.Инициализация(Новый Структура);
			Если Не КорректировкаМаршрутаОбъект.ЗаполнитьКорректировкуЗаданиями(ВыбранныеЗадания, ЛучшаяПозицияПогрузки, ЛучшаяПозицияРазгрузки) Тогда 
				Продолжить;
			КонецЕсли;
			СтрокаРейса.РасстояниеКорректировка = КорректировкаМаршрутаОбъект.Маршрут.Итог("РасстояниеОтПредыдущейТочки") - КорректировкаМаршрутаОбъект.МаршрутДоКорректировки.Итог("РасстояниеОтПредыдущейТочки");
		Иначе  
			МаршрутОтТекущейТочкиДоЦентраЗаданий = Неопределено;
			Если СтрокаРейса.ТекущаяШирота = 0 И СтрокаРейса.ТекущаяДолгота = 0 Тогда
				СтрокаРейса.РасстояниеКорректировка = 999999;
				СтрокаРейса.ВремяКорректировка = 999999;
			Иначе
				Если ПараметрыПланирования.РассчитыватьРасстоянияПриОпределенииРекоммендуемыхРейсов Тогда
					БылаОшибка = Ложь;
					МаршрутОтТекущейТочкиДоЦентраЗаданий = упМаршрутизация.ПолучитьРасстояниеМеждуКоординатамиПоТипуКарты(ПараметрыКарты, СтрокаРейса.ТекущаяШирота, СтрокаРейса.ТекущаяДолгота,
						ЦентрОтправленияВыбранныхЗаданийШирота, ЦентрОтправленияВыбранныхЗаданийДолгота, НачалоПериодаПланирования, БылаОшибка);
					Если БылаОшибка Тогда
						МаршрутОтТекущейТочкиДоЦентраЗаданий = Неопределено;
					КонецЕсли; 
				КонецЕсли; 
				Если МаршрутОтТекущейТочкиДоЦентраЗаданий <> Неопределено Тогда
					СтрокаРейса.РасстояниеКорректировка = МаршрутОтТекущейТочкиДоЦентраЗаданий.Расстояние;
					Если ЗначениеЗаполнено(МаршрутОтТекущейТочкиДоЦентраЗаданий.Путь) Тогда
						СтрокаРейса.КоординатыЛинийКорректировка = МаршрутОтТекущейТочкиДоЦентраЗаданий.Путь;
					КонецЕсли;
					СтрокаРейса.ВремяКорректировка = МаршрутОтТекущейТочкиДоЦентраЗаданий.Время;
				Иначе
					СтрокаРейса.РасстояниеКорректировка = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(СтрокаРейса.ТекущаяШирота, СтрокаРейса.ТекущаяДолгота,
						ЦентрОтправленияВыбранныхЗаданийШирота, ЦентрОтправленияВыбранныхЗаданийДолгота);
					СтрокаРейса.КоординатыЛинийКорректировка = ПредставлениеЧисла(СтрокаРейса.ТекущаяШирота) + ";" + ПредставлениеЧисла(СтрокаРейса.ТекущаяДолгота) + ";" + ПредставлениеЧисла(ЦентрОтправленияВыбранныхЗаданийШирота) 
						+ ";" + ПредставлениеЧисла(ЦентрОтправленияВыбранныхЗаданийДолгота) + ";//";
					СтрокаРейса.ВремяКорректировка = 3600 * СтрокаРейса.РасстояниеКорректировка / 60; // приближенное время из расчета, что скорость движения составляет 60 км/ч
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		СтрокаЛучшихРейсов = ЛучшиеРейсы.Добавить();
		СтрокаЛучшихРейсов.Рейс = СтрокаРейса.Рейс;
		СтрокаЛучшихРейсов.ДельтаРасстояния = СтрокаРейса.РасстояниеКорректировка;
		СтрокаЛучшихРейсов.ВставкаОтправленияШирота = ЛучшаяПогрузкаШирота;
		СтрокаЛучшихРейсов.ВставкаОтправленияДолгота = ЛучшаяПогрузкаДолгота;
		СтрокаЛучшихРейсов.ВставкаПолученияШирота = ЛучшаяРазгрузкаШирота;
		СтрокаЛучшихРейсов.ВставкаПолученияДолгота = ЛучшаяРазрузкаДолгота;
		СтрокаЛучшихРейсов.ПозицияПогрузки = ЛучшаяПозицияПогрузки;
		СтрокаЛучшихРейсов.ПозицияРазгрузки = ЛучшаяПозицияРазгрузки;
	КонецЦикла;
	ЛучшиеРейсы.Сортировать("ДельтаРасстояния");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказатьБлижайшиеПоПутиСледования(Команда)
	
	ВыбранныеЗадания = Новый Массив;
	Для Каждого СтрокаГруза Из СписокЗаданий Цикл
		Если СтрокаГруза.Выбрано И ВыбранныеЗадания.Найти(СтрокаГруза.ЗаданиеНаПеревозку) = Неопределено Тогда
			ВыбранныеЗадания.Добавить(СтрокаГруза.ЗаданиеНаПеревозку);
		КонецЕсли; 
	КонецЦикла;
	Если ВыбранныеЗадания.Количество() = 0 Тогда
		ТекстСообщения = "Выбрано 0 заданий. Выберите флажками нужные задания.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
	РассчитатьБлижайшиеПоПутиСледованияНаСервере(ВыбранныеЗадания, Истина);
	ОбновитьОтображениеНаКарте();
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВариантНастройкиПланированияПоУмолчанию(ЭтаФорма)
	
	ЭлементСписка = упСервисныеФункцииКлиентСервер.НайтиЭлементКоллекцииПоЗначениюСвойства(ЭтаФорма.Элементы.ВариантПланирования.СписокВыбора, "Представление", "по умолчанию");
	Возврат ЭлементСписка;

КонецФункции

&НаКлиенте
Процедура РасположитьСпискиВертикально(Команда)
	
	РасположитьСпискиВертикальноНаСервере();
	// Приводит к ошибке "Неизвестная ошибка"
	//Если РасположитьСпискиВертикально Тогда
	//	Элементы.ГруппаКарта.Видимость = Ложь;
	//Иначе
	//	Элементы.ГруппаКарта.Видимость = Истина;
	//КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасположитьСпискиВертикальноНаСервере(Переключить = Истина)
	
	Если Переключить Тогда
		ЭтаФорма.РасположитьСпискиВертикально = Не ЭтаФорма.РасположитьСпискиВертикально;
	КонецЕсли; 
	Элементы.РасположитьСпискиВертикально.Пометка = РасположитьСпискиВертикально;
	Если РасположитьСпискиВертикально Тогда
		Элементы.Переместить(Элементы.ГруппаСписокЗаданий, Элементы.ГруппаСправа);
		Элементы.ГруппаБыстрыеОтборы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	Иначе
		Элементы.Переместить(Элементы.ГруппаСписокЗаданий, Элементы.ГруппаСлева);
		Элементы.ГруппаБыстрыеОтборы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти 

 
