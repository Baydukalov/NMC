
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	сткПараметрыОптимизации = Параметры.ПараметрыОптимизации;
	Метод = сткПараметрыОптимизации.Метод;
	РасчетИзФормыРедактированияРейса = сткПараметрыОптимизации.РасчетИзФормыРедактированияРейса;
	мсвИдентификаторов = сткПараметрыОптимизации.МассивИдентификаторов;
	Для каждого текИдентификатор Из мсвИдентификаторов Цикл
		НоваяСтрока = СписокИдентификаторов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текИдентификатор);
	КонецЦикла; 
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ОписаниеМетода = ВернутьОписаниеМетода();
	HTMLОписаниеМетода = Обработки.упРМПланированиеРейсов.ПолучитьМакет("HTML_ОписаниеМетода").ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьСостояниеЗадачи", 1);
	
	HTMLОписаниеМетода = СтрЗаменить(HTMLОписаниеМетода, "%Метод%", НСтр(СтрШаблон("ru = 'Метод оптимизации: %1'", НРег(Строка(Метод))), КодЯзыка));
	HTMLОписаниеМетода = СтрЗаменить(HTMLОписаниеМетода, "%ОписаниеМетода%", ОписаниеМетода);
			
	КартинкаДвоичныДанные = упСервисныеФункцииВызовСервера.ПолучитьДвоичныеДанныеКартинки(БиблиотекаКартинок.упАнимированноеРешениеЗК);
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(КартинкаДвоичныДанные, УникальныйИдентификатор);
	HTMLОписаниеМетода = СтрЗаменить(HTMLОписаниеМетода, "%Картинка%", ИмяВременногоФайла);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершениеИзмененияМаршрутаРейса" Тогда
		ОтключитьОбработчикОжидания("ОбновитьСостояниеЗадачи");
		// Закрываем форму асинхронно для исправления ошибки с крашем клиентского приложения 
		// из-за длинной последовательности оповещений между родителской формой 
		// и формой оптимизации маршрута (текущей)
		//	Закрыть(мсвИдентификаторовСтрок);
		//  	Сигнатура проблемы:
		// Имя события проблемы:	APPCRASH
		// Имя приложения:	1CV8C.exe
		// Версия приложения:	8.3.10.2561
		// Отметка времени приложения:	5983aaba
		// Имя модуля с ошибкой:	StackHash_f5bb
		// Версия модуля с ошибкой:	6.3.9600.18233
		// Отметка времени модуля с ошибкой:	56bb4e1d
		// Код исключения:	c0000374
		// Смещение исключения:	PCH_DF_FROM_ntdll+0x0003C7EC
		// Версия ОС:	6.3.9600.2.0.0.16.7
		ПодключитьОбработчикОжидания("ЗакрытьОтложенно", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьОтложенно()
	мсвИдентификаторовСтрок = Новый Массив;
	Для каждого текСтрока Из СписокИдентификаторов Цикл
		мсвИдентификаторовСтрок.Добавить(текСтрока.ИдентификаторСтроки);
	КонецЦикла; 
	Закрыть(мсвИдентификаторовСтрок);
КонецПроцедуры	

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Процедура запускается из обрабочика ожидания.
//
Процедура ОбновитьСостояниеЗадачи()
	
	ОписаниеОшибки = "";
	ОбновитьСостояниеЗадачиНаСервере(ОписаниеОшибки);
	Если ЗаданиеВыполнено Тогда 
		ОтключитьОбработчикОжидания("ОбновитьСостояниеЗадачи");
		Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ОписаниеОшибки, Истина, Ложь);
		Иначе
			Если РасчетИзФормыРедактированияРейса Тогда
				сткПараметров = Новый Структура;
				сткПараметров.Вставить("АдресВременногоХранилища", СписокИдентификаторов[0].АдресВременногоХранилища);
				сткПараметров.Вставить("ТолькоАктивные", СписокИдентификаторов[0].ТолькоАктивные);
				Оповестить("ОбработкаРешенияЗКРедактированиеРейса", сткПараметров);
			Иначе	
				мсвРешений = Новый Массив;
				Для каждого текСтрока Из СписокИдентификаторов Цикл
					сткПараметров = Новый Структура;
					сткПараметров.Вставить("АдресВременногоХранилища", текСтрока.АдресВременногоХранилища);
					сткПараметров.Вставить("Рейс", текСтрока.Рейс);
					мсвРешений.Добавить(сткПараметров);
				КонецЦикла; 
				Оповестить("ОбработкаРешенияЗК", мсвРешений);
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСостояниеЗадачи()

&НаСервере
Функция ВернутьОписаниеМетода()
	
	текОписание = ""; 
	Если Метод = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритм Тогда
		текОписание = "Алгоритм ближайшего соседа - вариация жадного алгоритма. Принцип работы алгоритма заключается в поиске ближайшей точки 
		|на каждом шаге прокладки маршрута. Алгоритм отличается высокой скоростью работы при сравнительно приемлемых результатах.";
	ИначеЕсли Метод = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
		текОписание = "Расширенный алгоритм ближайшего соседа с учетом временных окон - вариация жадного алгоритма. Принцип работы алгоритма заключается в поиске ближайшей точки 
		|на каждом шаге прокладки маршрута, при этом критерий ""близости"" определяется с учетом временного окна в точке, времени сервисного обслуживания и времени в пути между точками. 
		|Алгоритм отличается высокой скоростью работы при сравнительно приемлемых результатах.";
	ИначеЕсли Метод = Перечисления.упМетодыРешенияЗК.МетодВетвейИГраниц Тогда
		текОписание = НСтр("ru = 'Метод ветвей и границ — общий алгоритмический метод для нахождения оптимальных решений различных задач оптимизации.
		|Метод является вариацией полного перебора с отсевом подмножеств допустимых решений, заведомо не содержащих оптимальных решений.
		|<b>Внимание! Не рекомендуется использовать метод при количестве точек, большем десяти,</b> так как это приводит к существенному
		|удлинению времени решения задачи коммивояжера.'", КодЯзыка);
	ИначеЕсли ТипЗнч(Метод) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда //поставляемый алгоритм
		ОписаниеАлгоритма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Метод, "ОписаниеАлгоритма");
		Если ЗначениеЗаполнено(ОписаниеАлгоритма) Тогда
			текОписание = ОписаниеАлгоритма;
		Иначе	
		    текОписание = НСтр("ru = 'Используется поставляемый алгоритм.'", КодЯзыка);
		КонецЕсли; 
	КонецЕсли;
	Возврат текОписание;
	
КонецФункции // ВернутьОписаниеМетода()

&НаСервере
Функция ОбновитьСостояниеЗадачиНаСервере(выхОписаниеОшибки = "")
	
	ЗаданиеВыполнено = Ложь;
	Для каждого текСтрока Из СписокИдентификаторов Цикл
		Если Не текСтрока.ЗаданиеВыполнено Тогда
			Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(текСтрока.ИдентификаторЗадания);
			Если Задание = Неопределено Или Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда 
				текСтрока.ЗаданиеВыполнено = Истина;
				Если Задание <> Неопределено И Задание.ИнформацияОбОшибке <> Неопределено Тогда
					выхОписаниеОшибки = ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
				КонецЕсли; 
			Иначе
				мсвСообшений = Задание.ПолучитьСообщенияПользователю(Истина);
				Если мсвСообшений <> Неопределено И мсвСообшений.Количество() Тогда
					Для Каждого текСообщение Из мсвСообшений Цикл
						Текст = текСообщение.Текст;
						Если Текст = "true" Тогда
							текСтрока.ЗаданиеВыполнено = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 

	тзИдентификаторов = СписокИдентификаторов.Выгрузить(Новый Структура("ЗаданиеВыполнено", Ложь));
	Если тзИдентификаторов.Количество() = 0 Тогда
		ЗаданиеВыполнено = Истина;
	КонецЕсли;
	
	Возврат ЗаданиеВыполнено;
	
КонецФункции // ОбновитьСостояниеЗадачиНаСервере()

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Для каждого текСтрока Из СписокИдентификаторов Цикл
		ДлительныеОперации.ОтменитьВыполнениеЗадания(текСтрока.ИдентификаторЗадания);
	КонецЦикла; 
	
КонецПроцедуры // ПриЗакрытииНаСервере()

#КонецОбласти 
