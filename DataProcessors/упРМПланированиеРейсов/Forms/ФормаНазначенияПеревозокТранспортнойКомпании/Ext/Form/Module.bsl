
#Область ПеременныеМодуляФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мсвСтруктурЗаданий = Параметры.МассивСтруктурЗаданий;
	РазбиватьЗаданияПоГрузовымМестам = Параметры.РазбиватьЗаданияПоГрузовымМестам;
	Сч = 0;
	Для каждого текСтруктура Из мсвСтруктурЗаданий Цикл
		НоваяСтрока = СписокНазначений.Добавить();
		Сч = Сч + 1;
		НоваяСтрока.НомерСтроки = Сч;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтруктура);
	КонецЦикла; 
	Элементы.СписокНазначенийГрузовоеМесто.Видимость = РазбиватьЗаданияПоГрузовымМестам;
	Элементы.СписокНазначенийКоличествоГрузов.Видимость = РазбиватьЗаданияПоГрузовымМестам;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Закрыть(НазначенныеЗадания);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокНазначенийПеревозчикПриИзменении(Элемент)
	
	текСтрока = Элементы.СписокНазначений.ТекущиеДанные;
	ОбработатьИзменениеДанныхСтроки(текСтрока);
	ОбработатьВыделенныеСтроки("Перевозчик", текСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНазначенийСоглашениеПриИзменении(Элемент)
	
	текСтрока = Элементы.СписокНазначений.ТекущиеДанные;
	ОбработатьИзменениеДанныхСтроки(текСтрока);
	ОбработатьВыделенныеСтроки("Соглашение", текСтрока);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура РаспределитьЗадания(Команда)
	
	Отказ = Ложь;
	Ошибки = Неопределено;
	ПроверитьЗаполнениеВыбранныхСтрок(Ошибки);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	Если Не Отказ Тогда
		РаспределитьЗаданияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКСпискуНазначений(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Страница1;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьИзменениеДанныхСтроки(Строка)

	Если Строка <> Неопределено Тогда
		Если ЗначениеЗаполнено(Строка.Перевозчик) И ЗначениеЗаполнено(Строка.Соглашение) Тогда
			Строка.Пометка = Истина;
		Иначе
			Строка.Пометка = Ложь;
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры // ОбработатьИзменениеДанныхСтроки()	

&НаКлиенте
Процедура ОбработатьВыделенныеСтроки(ИмяЭлемента, Строка);
	
	ВыделенныеСтроки = Элементы.СписокНазначений.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			ИскомаяСтрока = СписокНазначений.НайтиПоИдентификатору(ИдентификаторСтроки);
			Если ИскомаяСтрока <> Неопределено И ИскомаяСтрока <> Строка Тогда
				ИскомаяСтрока[ИмяЭлемента] = Строка[ИмяЭлемента];
				ОбработатьИзменениеДанныхСтроки(ИскомаяСтрока);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыделенныеСтроки()

&НаКлиенте
Процедура ПроверитьЗаполнениеВыбранныхСтрок(Ошибки)
	
	ЕстьПомеченныеСтроки = Ложь;
	Для каждого текСтрока Из СписокНазначений Цикл
		Если текСтрока.Пометка Тогда
			Если Не ЗначениеЗаполнено(текСтрока.Перевозчик) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "СписокНазначений[%1].Перевозчик", НСтр("ru = 'Перевозчик в строке не выбран.'"),"СписокНазначений.Перевозчик", текСтрока.НомерСтроки - 1, НСтр("ru = 'Перевозчик в строке %1 не выбран.'"), текСтрока.НомерСтроки - 1);
			КонецЕсли;	
			Если Не ЗначениеЗаполнено(текСтрока.Соглашение) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "СписокНазначений[%1].Соглашение", НСтр("ru = 'Соглашение в строке не выбрано.'"),"СписокНазначений.Соглашение", текСтрока.НомерСтроки - 1, НСтр("ru = 'Соглашение в строке %1 не выбрано.'"), текСтрока.НомерСтроки - 1);
			КонецЕсли;	
			ЕстьПомеченныеСтроки = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьПомеченныеСтроки Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "СписокНазначений", НСтр("ru = 'Не выбрано ни одной строки!'"), "",,,);
	КонецЕсли;
		
КонецПроцедуры // ПроверитьЗаполнениеВыбранныхСтрок()

&НаСервере
Процедура РаспределитьЗаданияНаСервере()
	
	тзНазначения = СписокНазначений.Выгрузить(Новый Структура("Пометка", Истина));
	ТекстОшибки = "";
	Отказ = Ложь;
	
	Если РазбиватьЗаданияПоГрузовымМестам Тогда
		тзЗадания = тзНазначения.Скопировать(, "ЗаданиеНаПеревозку, Перевозчик, Соглашение");
		тзЗадания.Свернуть("ЗаданиеНаПеревозку, Перевозчик, Соглашение");
		сткПоиска = Новый Структура("ЗаданиеНаПеревозку, Перевозчик, Соглашение");
		Для каждого текЗадание Из тзЗадания Цикл
			Попытка
				ЗаблокироватьДанныеДляРедактирования(текЗадание.ЗаданиеНаПеревозку,, УникальныйИдентификатор);
			Исключение
				ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание.ЗаданиеНаПеревозку);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Продолжить;
			КонецПопытки;
			ЗаполнитьЗначенияСвойств(сткПоиска, текЗадание);
			тзЗадания = тзНазначения.Скопировать(сткПоиска, "ЗаданиеНаПеревозку, ГрузовоеМесто, КоличествоГрузов");
			
			СформироватьРейсы(текЗадание, тзЗадания, ТекстОшибки, Отказ);
		КонецЦикла; 
	Иначе
		Для каждого текЗадание Из тзНазначения Цикл
			Попытка
				ЗаблокироватьДанныеДляРедактирования(текЗадание.ЗаданиеНаПеревозку,, УникальныйИдентификатор);
			Исключение
				ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        текЗадание.ЗаданиеНаПеревозку);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Продолжить;
			КонецПопытки;
			
			СформироватьРейсы(текЗадание, Неопределено, ТекстОшибки, Отказ);
		КонецЦикла; 
	КонецЕсли; 
	
	Если СписокНазначений.Количество() = 0 Тогда
		Элементы.ВернутьсяКСпискуНазначений.Доступность = Ложь;
	КонецЕсли; 
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Страница2;
	
КонецПроцедуры // РаспределитьЗаданияНаСервере()

&НаСервере
Процедура СформироватьРейсы(СтрокаЗадание, ТаблицаЗаданий, ТекстОшибки, Отказ);
	
	ДополнительныеРеквизиты = Новый Структура;
	сткДанныеТС = Новый Структура("Перевозчик, Соглашение");
	сткДанныеТС.Вставить("ПеревозкаТранспортнойКомпанией", Истина);
	ЗаполнитьЗначенияСвойств(сткДанныеТС, СтрокаЗадание);
	сткДанныеТС.Вставить("Автор",ПользователиКлиентСервер.ТекущийПользователь());
	
	Если ТаблицаЗаданий = Неопределено Тогда
		мсвРейсов = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаЗадание.ЗаданиеНаПеревозку), Неопределено, сткДанныеТС, Неопределено, ТекстОшибки, Отказ);
	Иначе	
		ТаблицаЗаданий.Свернуть("ЗаданиеНаПеревозку, ГрузовоеМесто", "КоличествоГрузов");
		мсвРейсов = Документы.упЗаданиеНаПеревозкуГруза.СформироватьРейсы(Неопределено, ТаблицаЗаданий, сткДанныеТС, Неопределено, ТекстОшибки, Отказ);
	КонецЕсли; 
	РазблокироватьДанныеДляРедактирования(СтрокаЗадание.ЗаданиеНаПеревозку, УникальныйИдентификатор);
	Если Отказ Тогда
		ЗаписьЖурналаРегистрации("Создание рейса", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	Иначе	
		Для каждого текРейс Из мсвРейсов Цикл
			НоваяСтрока = СозданныеДокументы.Добавить();
			НоваяСтрока.Рейс = текРейс;
		КонецЦикла;
		НазначенныеЗадания.Добавить(СтрокаЗадание.ЗаданиеНаПеревозку);
		ИскомыеСтроки = СписокНазначений.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозку", СтрокаЗадание.ЗаданиеНаПеревозку));
		Для каждого текСтрока Из ИскомыеСтроки Цикл
			СписокНазначений.Удалить(текСтрока);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // СформироватьРейсы()
	
#КонецОбласти 