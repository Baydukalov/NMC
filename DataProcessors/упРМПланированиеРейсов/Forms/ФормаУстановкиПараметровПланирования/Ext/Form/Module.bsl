#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчитыватьМассу                             = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	УчитыватьОбъем                             = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	УчитыватьКоличествоМест                    = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	УчитыватьКоличествоЗагрузочныхМетров       = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	ДоступенУчетМестоположенийТранспортныхСредств = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств");
	РассчитыватьРасстоянияСУчетомОграниченийСитиГИД = ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД");
	КонтролироватьДоступностьРесурсовПартнеру = ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьРесурсовПартнеру");
	КонтролироватьДоступностьГеографическихЗон = ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон");
	РежимКорректировки = Параметры.РежимКорректировки;
	
	УстановитьСписокДоступныхПоказателейКонтроля();
	ЭтаФорма.СписокТранспорта = Параметры.СписокТранспорта;
	ЭтаФорма.СписокПартнеров = Параметры.СписокПартнеров;
	ЭтаФорма.КлючВарианта = Параметры.КлючВарианта;
	ПараметрыПланирования = Параметры.ПараметрыПланирования;
	Если НЕ ПараметрыПланирования = Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыПланирования,, "ДоступныеЗоны,МестаИнтереса");
		МестаИнтереса.ЗагрузитьЗначения(ПараметрыПланирования.МестаИнтереса);
	КонецЕсли;	
	Для каждого текЗона Из Параметры.ПараметрыПланирования.ДоступныеЗоны Цикл
		Если текЗона.Значение.Количество() Тогда
			Для каждого текТС Из текЗона.Значение Цикл
				НоваяСтрока = ДоступныеЗоны.Добавить();
				НоваяСтрока.Зона = текЗона.Ключ;
				НоваяСтрока.ТранспортноеСредство = текТС;
			КонецЦикла; 
		Иначе
			НоваяСтрока = ДоступныеЗоны.Добавить();
			НоваяСтрока.Зона = текЗона.Ключ;
		КонецЕсли; 
	КонецЦикла; 
	ОбновитьНадписиДоступныхОбъектовНаСервере();
	
	Если МаксимальнаяДлительностьВремениОжиданияВТочке > 0 Тогда
		ВремяОжиданияЧасов = Цел(МаксимальнаяДлительностьВремениОжиданияВТочке);
		ВремяОжиданияМинут = Окр((МаксимальнаяДлительностьВремениОжиданияВТочке - ВремяОжиданияЧасов)*60);
	КонецЕсли;
	
	Элементы.ГруппаКорректировкаРейсов.Видимость = РежимКорректировки;
	Элементы.ГруппаЗК.Видимость = Не РежимКорректировки;
	Элементы.ГруппаЗМТ.Видимость = Не РежимКорректировки;
	Если РежимКорректировки Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКорректировкаРейсов;
	Иначе
		ОбработатьИзменениеМетодаОптимизации();
	КонецЕсли; 
	
	Элементы.ГруппаУчитыватьМестоположенияТС.Видимость = ДоступенУчетМестоположенийТранспортныхСредств;
	Элементы.УчитыватьПропускиНаВъездВМосквуДляГрузовогоТранспорта.Видимость = РассчитыватьРасстоянияСУчетомОграниченийСитиГИД;
	Элементы.ГруппаДоступностьРесурсов.Видимость = КонтролироватьДоступностьРесурсовПартнеру;
	Элементы.ГруппаДоступностьГеозон.Видимость = КонтролироватьДоступностьГеографическихЗон;

	НадписьЗнакБольше = НСтр("ru = '>'");
	ОбновитьДоступность(ЭтаФорма);
	
	ЗаполнитьСписокВыбораМетодовРешенияЗМТ();
	ЗаполнитьСписокВыбораМетодовРешенияЗК();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступность(ЭтаФорма);
	ПривестиТипРазмераГруза();
	ПривестиТипРазмераКрупногоГруза();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗМТ.ЗональноеПланирование Тогда
		мсвНепроверяемых = Новый Массив;
		мсвНепроверяемых.Вставить("ВариантОптимизацииЗМТ");
		ПроверяемыеРеквизиты = ОбщегоНазначенияКлиентСервер.СократитьМассив(ПроверяемыеРеквизиты, мсвНепроверяемых);
	ИначеЕсли МетодРешенияЗК = Перечисления.упМетодыРешенияЗМТ.МеханизмСбереженийКларкаРайта Тогда	
		Если Не УчитыватьМинимальноеКоличествоГрузовПриДеленииЗаданий Тогда
			мсвНепроверяемых = Новый Массив;
			мсвНепроверяемых.Вставить("МинимальноеКоличествоГрузовПриДеленииЗаданий");
			ПроверяемыеРеквизиты = ОбщегоНазначенияКлиентСервер.СократитьМассив(ПроверяемыеРеквизиты, мсвНепроверяемых);
		КонецЕсли; 
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(МетодРешенияЗМТ) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан метод решения задачи маршрутизации транспорта",, "МетодРешенияЗадачиМаршрутизации",, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыДоступныеЗоныПланирования" Тогда
		ОбновитьНадписиДоступныхОбъектовНаСервере();
		ЭтаФорма.УчитыватьДоступностьГеозонДляТранспорта = Истина;
	ИначеЕсли ИмяСобытия = "ИзмененыДоступныеРесурсыПланирования" Тогда
		ОбновитьНадписиДоступныхОбъектовНаСервере();
		ЭтаФорма.УчитыватьДоступностьРесурсовПартнеров = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

#Область ЭлементыЗадачаКоммивояжера

&НаКлиенте
Процедура МетодРешенияЗКПриИзменении(Элемент)
	
	Если МетодРешенияЗК = "Поставляемый" Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("Отбор", Новый Структура("ВидЗадачиОптимизации", ПредопределенноеЗначение("Перечисление.упВидыЗадачОптимизации.ЗадачаКоммивояжера")));
		ОткрытьФорму("Справочник.упАлгоритмыОптимизации.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("АлгоритмыОптимизацииЗКВыборЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОбработатьИзменениеМетодаОптимизации();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмыОптимизацииЗКВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭлементУжеВСписке = Ложь;
		
		СписокВыбораМетодаЗК = Элементы.МетодРешенияЗК.СписокВыбора;
		Для каждого текЗначение Из СписокВыбораМетодаЗК Цикл
			Если Результат = текЗначение.Значение Тогда
			    ЭлементУжеВСписке = Истина;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если Не ЭлементУжеВСписке И СписокВыбораМетодаЗК.Количество() < 10 Тогда
			СписокВыбораМетодаЗК.Вставить(0, Результат, ВернутьПредставлениеПоставляемогоАлгоритма(Результат));
		КонецЕсли; 
	    МетодРешенияЗК = Результат;
	Иначе	
		МетодРешенияЗК = Неопределено;
	КонецЕсли;
	ОбработатьИзменениеМетодаОптимизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПространственныйВесПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Ложь, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВременнойВесПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Истина, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВесСрочностиПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Истина, Истина, Ложь);
	
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыЗадачаМаршрутизацииТранспорта

&НаКлиенте
Процедура МетодРешенияЗМТПриИзменении(Элемент)
	
	Если МетодРешенияЗМТ = "Поставляемый" Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("Отбор", Новый Структура("ВидЗадачиОптимизации", ПредопределенноеЗначение("Перечисление.упВидыЗадачОптимизации.ЗадачаМаршрутизацииТранспорта")));
		ОткрытьФорму("Справочник.упАлгоритмыОптимизации.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("АлгоритмыОптимизацииЗМТВыборЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОбработатьИзменениеМетодаОптимизации();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмыОптимизацииЗМТВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭлементУжеВСписке = Ложь;
		
		СписокВыбораМетодаЗМТ = Элементы.МетодРешенияЗадачиМаршрутизации.СписокВыбора;
		Для каждого текЗначение Из СписокВыбораМетодаЗМТ Цикл
			Если Результат = текЗначение.Значение Тогда
			    ЭлементУжеВСписке = Истина;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если Не ЭлементУжеВСписке И СписокВыбораМетодаЗМТ.Количество() < 10 Тогда
			СписокВыбораМетодаЗМТ.Вставить(0, Результат, ВернутьПредставлениеПоставляемогоАлгоритма(Результат));
		КонецЕсли; 
	    МетодРешенияЗМТ = Результат;
	Иначе	
		МетодРешенияЗМТ = Неопределено;
	КонецЕсли;
	ОбработатьИзменениеМетодаОптимизации();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьПредставлениеПоставляемогоАлгоритма(АлгоритмОптимизации)
	
	сткЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(АлгоритмОптимизации, "Наименование, Версия");
	Если ЗначениеЗаполнено(сткЗначенияРеквизитов.Версия) Тогда
		ПредставлениеАлгоритма = НСтр(СтрШаблон("ru = 'Поставляемый: %1, вер. %2'", сткЗначенияРеквизитов.Наименование, сткЗначенияРеквизитов.Версия));
	Иначе
		ПредставлениеАлгоритма = НСтр(СтрШаблон("ru = 'Поставляемый: %1'", сткЗначенияРеквизитов.Наименование));
	КонецЕсли;
	
	Возврат ПредставлениеАлгоритма;

КонецФункции

&НаКлиенте
Процедура ВариантОптимизацииЗМТПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешенаПогрузкаСНесколькихСкладовВОдномРейсеПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешеноЦиклическоеПланированиеПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьКоличествоСкладовВРейсеПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьВременныеОкнаПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузоваяЕдиницаПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	ПривестиТипРазмераГруза();
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьКоличествоРейсовПриИзменении(Элемент)
	
	 ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОграничитьРазмерГрузаПриИзменении(Элемент)
	
	 ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьДлинуРейсаПриИзменении(Элемент)
	
	 ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьМинимальноеСбережениеПриИзменении(Элемент)
	
	 ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры
 
&НаКлиенте
Процедура УчитыватьМинимальноеКоличествоГрузовПриДеленииЗаданийПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьКоличествоТочекВРейсеПриИзменении(Элемент)
	
	 ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничиватьСписокЗонПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыКарта

&НаКлиенте
Процедура ВыделятьКрупныеГрузыПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательКрупногоГрузаПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	ПривестиТипРазмераКрупногоГруза();
	
КонецПроцедуры
	
#КонецОбласти 

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ЗавершитьРедактированиеФормы();
	
КонецПроцедуры // ЗавершитьРедактирование()

&НаКлиенте
Процедура ЗавершитьРедактированиеСохранить(Команда)
	
	ЗавершитьРедактированиеФормы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактированиеФормы(СохранятьИзмененияВарианта = Ложь)
	
	Если ПроверитьЗаполнение() Тогда
		МаксимальнаяДлительностьВремениОжиданияВТочке = ВремяОжиданияЧасов + ВремяОжиданияМинут/60;
		
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("СохранятьИзмененияВарианта"                              , СохранятьИзмененияВарианта);
		сткПараметры.Вставить("ВариантОптимизацииМаршрута"                              , ВариантОптимизацииМаршрута);
		сткПараметры.Вставить("ВариантОптимизацииЗМТ"                                   , ВариантОптимизацииЗМТ); 
		сткПараметры.Вставить("ГрузоваяЕдиница"                                         , ГрузоваяЕдиница);
		сткПараметры.Вставить("ДлинаРейса"                                              , ДлинаРейса);
		сткПараметры.Вставить("КоличествоРейсов"                                        , КоличествоРейсов);
		сткПараметры.Вставить("КоличествоТочекВРейсе"                                   , КоличествоТочекВРейсе);
		сткПараметры.Вставить("КоэффициентСбережения"                                   , КоэффициентСбережения);
		сткПараметры.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей"                 , УчитыватьГрафикиРаботыОсновныхВодителей);
		сткПараметры.Вставить("УчитыватьРасписаниеРаботыТС"                             , УчитыватьРасписаниеРаботыТС);
		сткПараметры.Вставить("УчитыватьМестоположенияТС"                               , УчитыватьМестоположенияТС);
		сткПараметры.Вставить("УчитыватьДоступностьГеозонДляТранспорта"                 , УчитыватьДоступностьГеозонДляТранспорта);
		сткПараметры.Вставить("ЗональныйКоэффициент"                                    , ЗональныйКоэффициент);
		сткПараметры.Вставить("МинимальноеСбережение"                                   , МинимальноеСбережение);
		сткПараметры.Вставить("ОграничитьДлинуРейса"                                    , ОграничитьДлинуРейса);
		сткПараметры.Вставить("ОграничитьКоличествоРейсов"                              , ОграничитьКоличествоРейсов);
		сткПараметры.Вставить("ОграничитьКоличествоТочекВРейсе"                         , ОграничитьКоличествоТочекВРейсе);
		сткПараметры.Вставить("ОграничитьРазмерГруза"                                   , ОграничитьРазмерГруза);
		сткПараметры.Вставить("РазмерГруза"                                             , РазмерГруза);
		сткПараметры.Вставить("УчитыватьМинимальноеСбережение"                          , УчитыватьМинимальноеСбережение);
		сткПараметры.Вставить("МетодРешенияЗК"                                          , МетодРешенияЗК);
		сткПараметры.Вставить("МетодРешенияЗМТ"                                         , МетодРешенияЗМТ);
		сткПараметры.Вставить("МаксимальныйПроцентЗагрузкиТС"                           , МаксимальныйПроцентЗагрузкиТС);
		сткПараметры.Вставить("УчитыватьВременныеОкна"                                  , УчитыватьВременныеОкна);
		сткПараметры.Вставить("МаксимальнаяДлительностьВремениОжиданияВТочке"           , МаксимальнаяДлительностьВремениОжиданияВТочке); 
		сткПараметры.Вставить("ПространственныйВес"                                     , ПространственныйВес);
		сткПараметры.Вставить("ВременнойВес"                                            , ВременнойВес);
		сткПараметры.Вставить("ВесСрочности"                                            , ВесСрочности);
		сткПараметры.Вставить("УменьшатьВремяОжидания"                                  , УменьшатьВремяОжидания);
		сткПараметры.Вставить("ВыделятьКрупныеГрузы"                                    , ВыделятьКрупныеГрузы);
		сткПараметры.Вставить("ПоказательКрупногоГруза"                                 , ПоказательКрупногоГруза);
		сткПараметры.Вставить("РазмерКрупногоГруза"                                     , РазмерКрупногоГруза);
		сткПараметры.Вставить("ВыделятьСрочныеГрузы"                                    , ВыделятьСрочныеГрузы);
		сткПараметры.Вставить("КонтролироватьПревышениеЗагрузки"                        , КонтролироватьПревышениеЗагрузки);
		сткПараметры.Вставить("ОграничиватьСписокЗон"                                   , ОграничиватьСписокЗон);
		сткПараметры.Вставить("КоличествоВидимыхРейсов"                                 , КоличествоВидимыхРейсов);
		сткПараметры.Вставить("УчитыватьПропускиНаВъездВМосквуДляГрузовогоТранспорта"   , УчитыватьПропускиНаВъездВМосквуДляГрузовогоТранспорта);
		сткПараметры.Вставить("УчитыватьДоступностьРесурсовПартнеров"                   , УчитыватьДоступностьРесурсовПартнеров);
		сткПараметры.Вставить("МаксимальноеКоличествоРекоммендаций"                     , МаксимальноеКоличествоРекоммендаций);
		сткПараметры.Вставить("ПериодАктуальностиСпутниковыхДанных"                     , ПериодАктуальностиСпутниковыхДанных);
		сткПараметры.Вставить("РассчитыватьРасстоянияПриОпределенииРекоммендуемыхРейсов", РассчитыватьРасстоянияПриОпределенииРекоммендуемыхРейсов);
		сткПараметры.Вставить("УчитыватьВременныеОкнаПриРекомендацииРейсов"             , УчитыватьВременныеОкнаПриРекомендацииРейсов);
		сткПараметры.Вставить("УчитыватьГабаритыГрузовыхМест"                           , УчитыватьГабаритыГрузовыхМест);
		сткПараметры.Вставить("УчитыватьСовместимостьТовара"                            , УчитыватьСовместимостьТовара);
		сткПараметры.Вставить("ЗапретитьМногократныйОбходТаблицыСбережений"             , ЗапретитьМногократныйОбходТаблицыСбережений);
		сткПараметры.Вставить("ИзменятьНачалоРейсаСУчетомДоступностиСкладскихВорот"     , ИзменятьНачалоРейсаСУчетомДоступностиСкладскихВорот);
		сткПараметры.Вставить("РазрешенаПогрузкаСНесколькихСкладов"                     , РазрешенаПогрузкаСНесколькихСкладов);
		сткПараметры.Вставить("ОграничитьКоличествоСкладовВРейсе"                       , ОграничитьКоличествоСкладовВРейсе);
		сткПараметры.Вставить("КоличествоСкладовВРейсе"                                 , КоличествоСкладовВРейсе);
		сткПараметры.Вставить("НачинатьРейсВДепоСОсновнымВидомТочкиОтправления"         , НачинатьРейсВДепоСОсновнымВидомТочкиОтправления);
		сткПараметры.Вставить("ВключатьГрузыПоЗаявкеСЕдинымАдресомПолученияВЕдиныйРейс" , ВключатьГрузыПоЗаявкеСЕдинымАдресомПолученияВЕдиныйРейс);
		сткПараметры.Вставить("КонтролироватьКоличествоПосадочныхМест"                  , КонтролироватьКоличествоПосадочныхМест);
		сткПараметры.Вставить("УчитыватьПриоритетностьЗаданийНаПеревозку"               , УчитыватьПриоритетностьЗаданийНаПеревозку);
		сткПараметры.Вставить("РазрешеноЦиклическоеПланирование"                        , РазрешеноЦиклическоеПланирование);
		сткПараметры.Вставить("ВсегдаВключатьОбъединениеПоПромежуточнымТочкам"          , ВсегдаВключатьОбъединениеПоПромежуточнымТочкам);
		сткПараметры.Вставить("ВключатьГрузыСЕдинымАдресомПолученияВЕдиныйРейс"         , ВключатьГрузыСЕдинымАдресомПолученияВЕдиныйРейс);
		сткПараметры.Вставить("УчитыватьМинимальноеКоличествоГрузовПриДеленииЗаданий"   , УчитыватьМинимальноеКоличествоГрузовПриДеленииЗаданий);
		сткПараметры.Вставить("МинимальноеКоличествоГрузовПриДеленииЗаданий"            , МинимальноеКоличествоГрузовПриДеленииЗаданий);
		
		МассивМестИнтереса = Новый Массив;
		// проверим каждое место интереса
		Для каждого текЭлемент Из МестаИнтереса Цикл
			ТекущееМесто = текЭлемент.Значение;
			Если ЗначениеЗаполнено(ТекущееМесто) И МассивМестИнтереса.Найти(ТекущееМесто) = Неопределено Тогда
				МассивМестИнтереса.Добавить(ТекущееМесто);
			КонецЕсли;
		КонецЦикла;
		
		сткПараметры.Вставить("МестаИнтереса", МассивМестИнтереса);
		
		соотДоступныеЗоны = Новый Соответствие;
		Для каждого текСтрока Из ДоступныеЗоны Цикл
			текЗначение = соотДоступныеЗоны.Получить(текСтрока.Зона);
			Если текЗначение = Неопределено Тогда
				мсвТС = Новый Массив;
				Если ЗначениеЗаполнено(текСтрока.ТранспортноеСредство) Тогда
					мсвТС.Добавить(текСтрока.ТранспортноеСредство);
				КонецЕсли; 
				соотДоступныеЗоны.Вставить(текСтрока.Зона, мсвТС);
			Иначе
				текЗначение.Добавить(текСтрока.ТранспортноеСредство);
			КонецЕсли; 
		КонецЦикла; 
		сткПараметры.Вставить("ДоступныеЗоны", соотДоступныеЗоны);

		Закрыть(сткПараметры);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните обязательные настройки!'"));
	КонецЕсли;
	
КонецПроцедуры // ЗавершитьРедактирование()

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);

КонецПроцедуры // Отмена()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораМетодовРешенияЗМТ()
	
	СписокВыбораМетодаЗМТ = Элементы.МетодРешенияЗадачиМаршрутизации.СписокВыбора;
	СписокВыбораМетодаЗМТ.Очистить();
	Если ТипЗнч(МетодРешенияЗМТ) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		СписокВыбораМетодаЗМТ.Добавить(МетодРешенияЗМТ, ВернутьПредставлениеПоставляемогоАлгоритма(МетодРешенияЗМТ));	
	КонецЕсли; 
	СписокВыбораМетодаЗМТ.Добавить(Перечисления.упМетодыРешенияЗМТ.МеханизмСбереженийКларкаРайта, НСтр("ru = 'Механизм сбережений Кларка-Райта'"));
	СписокВыбораМетодаЗМТ.Добавить(Перечисления.упМетодыРешенияЗМТ.ЗональноеПланирование, НСтр("ru = 'Зональное планирование'"));
	СписокВыбораМетодаЗМТ.Добавить("Поставляемый", НСтр("ru = 'Выбрать поставляемый...'"));	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораМетодовРешенияЗК()
	
	СписокВыбораМетодаЗК = Элементы.МетодРешенияЗК.СписокВыбора;
	СписокВыбораМетодаЗК.Очистить();
	Если ТипЗнч(МетодРешенияЗК) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		СписокВыбораМетодаЗК.Добавить(МетодРешенияЗК, ВернутьПредставлениеПоставляемогоАлгоритма(МетодРешенияЗК));	
	КонецЕсли; 
	СписокВыбораМетодаЗК.Добавить(Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритм, НСтр("ru = 'Жадный алгоритм'"));
	СписокВыбораМетодаЗК.Добавить(Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон, НСтр("ru = 'Жадный алгоритм с учетом временных окон'"));
	СписокВыбораМетодаЗК.Добавить(Перечисления.упМетодыРешенияЗК.МетодВетвейИГраниц, НСтр("ru = 'Метод ветвей и границ'"));
	СписокВыбораМетодаЗК.Добавить("Поставляемый", НСтр("ru = 'Выбрать поставляемый...'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПривестиСуммуВесовКЕдинице(ИзменятьПространственныйВес, ИзменятьВременнойВес, ИзменятьВесСрочности)
	
	СуммаВесов = ПространственныйВес + ВременнойВес + ВесСрочности;
	Если СуммаВесов > 1 Тогда
		СуммаУменьшить = СуммаВесов - 1;
		Если Не ИзменятьПространственныйВес Тогда
			УменьшитьВес(СуммаУменьшить, ВременнойВес); 
			УменьшитьВес(СуммаУменьшить, ВесСрочности);
		КонецЕсли;
		
		Если Не ИзменятьВременнойВес Тогда
			УменьшитьВес(СуммаУменьшить, ВесСрочности);
			УменьшитьВес(СуммаУменьшить, ПространственныйВес); 
		КонецЕсли;
		
		Если Не ИзменятьВесСрочности Тогда
			УменьшитьВес(СуммаУменьшить, ПространственныйВес); 
			УменьшитьВес(СуммаУменьшить, ВременнойВес);
		КонецЕсли;
		
	ИначеЕсли СуммаВесов < 1 Тогда
		СуммаУвеличить = 1 - СуммаВесов;
		Если Не ИзменятьПространственныйВес Тогда
			УвеличитьВес(СуммаУвеличить, ВременнойВес); 
			УвеличитьВес(СуммаУвеличить, ВесСрочности);
		КонецЕсли;
		
		Если Не ИзменятьВременнойВес Тогда
			УвеличитьВес(СуммаУвеличить, ВесСрочности);
			УвеличитьВес(СуммаУвеличить, ПространственныйВес); 
		КонецЕсли;
		
		Если Не ИзменятьВесСрочности Тогда
			УвеличитьВес(СуммаУвеличить, ПространственныйВес); 
			УвеличитьВес(СуммаУвеличить, ВременнойВес);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПривестиСуммуВесовКЕдинице()

&НаКлиенте 
Процедура УвеличитьВес(Сумма, Вес)
	
	текУвеличение = Мин(Сумма, 1 - Вес);
	Вес = Вес + текУвеличение;
	Сумма = Сумма - текУвеличение;
	
КонецПроцедуры // УвеличитьВес()

&НаКлиенте 
Процедура УменьшитьВес(Сумма, Вес)
	
	текУменьшение = Мин(Сумма, Вес);
	Вес = Вес - текУменьшение;
	Сумма = Сумма - текУменьшение;
	
КонецПроцедуры // Уменьшитьвес()

&НаКлиенте
Процедура ПривестиТипРазмераГруза()
	
	Если ГрузоваяЕдиница = 0 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("Масса");
	ИначеЕсли ГрузоваяЕдиница = 1 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("Объем");
	ИначеЕсли ГрузоваяЕдиница = 2 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("КоличествоМест");
	ИначеЕсли ГрузоваяЕдиница = 3 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("КоличествоЗагрузочныхМетров");
	КонецЕсли;
	Элементы.РазмерГруза.ОграничениеТипа = ОписаниеТипа; 
	Элементы.РазмерГруза.ОграничениеТипа.ПривестиЗначение(РазмерГруза); 
	
КонецПроцедуры // ПривестиТипРазмераГруза()

&НаКлиенте
Процедура ПривестиТипРазмераКрупногоГруза()
	
	Если ПоказательКрупногоГруза = 0 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("Масса");
	ИначеЕсли ПоказательКрупногоГруза = 1 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("Объем");
	ИначеЕсли ПоказательКрупногоГруза = 2 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("КоличествоМест");
	ИначеЕсли ПоказательКрупногоГруза = 3 Тогда
		ОписаниеТипа = ВернутьОписаниеОпределяемогоТипа("КоличествоЗагрузочныхМетров");
	КонецЕсли;
	Элементы.РазмерКрупногоГруза.ОграничениеТипа = ОписаниеТипа; 
	Элементы.РазмерКрупногоГруза.ОграничениеТипа.ПривестиЗначение(РазмерКрупногоГруза); 
	
КонецПроцедуры // ПривестиТипРазмераКрупногоГруза()

&НаСервере
Процедура УстановитьСписокДоступныхПоказателейКонтроля()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьКоличествоЗагрузочныхМетров);
	
	Если УчитыватьМассу Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(0, "Масса");
		Элементы.ПоказательКрупногоГруза.СписокВыбора.Добавить(0, "масса");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 0;
		НоваяСтрока.Представление = сткПредставления.Масса;
	КонецЕсли; 
	Если УчитыватьОбъем Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(1, "Объем");
		Элементы.ПоказательКрупногоГруза.СписокВыбора.Добавить(1, "объем");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 1;
		НоваяСтрока.Представление = сткПредставления.Объем;
	КонецЕсли; 
	Если УчитыватьКоличествоМест Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(2, "Место");
		Элементы.ПоказательКрупногоГруза.СписокВыбора.Добавить(2, "кол. мест");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 2;
		НоваяСтрока.Представление = сткПредставления.КоличествоМест;
	КонецЕсли; 
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(3, "Загрузочный метр");
		Элементы.ПоказательКрупногоГруза.СписокВыбора.Добавить(3, "кол. метров");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 3;
		НоваяСтрока.Представление = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
КонецПроцедуры // УстановитьСписокДоступныхПоказателейКонтроля()

&НаСервереБезКонтекста
Функция ВернутьОписаниеОпределяемогоТипа(ИмяТипа)
	
	Возврат Метаданные.ОпределяемыеТипы[ИмяТипа].Тип;
	
КонецФункции // ВернутьОписаниеОпределяемогоТипа()

&НаКлиенте
Процедура УчитыватьРасписаниеРаботыТСПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьДоступностьГеозонДляТранспортаПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаДоступныхЗонНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(КлючВарианта) Тогда
		ТекстСообщения = "Сначала необходимо выбрать вариант планирования в главной форме!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("КлючВарианта", КлючВарианта));
	ПараметрыФормы.Вставить("СписокОбъектов", СписокТранспорта);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.НастройкаДоступныхЗон", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступность(ЭтаФорма)
	
	Элементы = ЭтаФорма.Элементы;
	Элементы.ОграничитьКоличествоСкладовВРейсе.Доступность = ЭтаФорма.РазрешенаПогрузкаСНесколькихСкладов;
	Элементы.КоличествоСкладовВРейсе.Доступность = ЭтаФорма.РазрешенаПогрузкаСНесколькихСкладов И ЭтаФорма.ОграничитьКоличествоСкладовВРейсе;
	Элементы.ВключатьГрузыПоЗаявкеСЕдинымАдресомПолученияВЕдиныйРейс.Доступность = ЭтаФорма.РазрешенаПогрузкаСНесколькихСкладов;
	Элементы.НачинатьРейсВДепоСОсновнымВидомТочкиОтправления.Доступность = ЭтаФорма.РазрешенаПогрузкаСНесколькихСкладов;
	
	Если ЭтаФорма.РазрешенаПогрузкаСНесколькихСкладов И ЭтаФорма.ИзменятьНачалоРейсаСУчетомДоступностиСкладскихВорот Тогда
		ЭтаФорма.ИзменятьНачалоРейсаСУчетомДоступностиСкладскихВорот = Ложь;
	КонецЕсли;
	Элементы.ИзменятьНачалоРейсаСУчетомДоступностиСкладскихВорот.Доступность = Не ЭтаФорма.РазрешенаПогрузкаСНесколькихСкладов И Не ЭтаФорма.РазрешеноЦиклическоеПланирование;
	Элементы.ОграничитьДлинуРейса.Доступность = Не (Истина
		И Не ЭтаФорма.УчитыватьВременныеОкна
		И ЭтаФорма.ВариантОптимизацииЗМТ = ПредопределенноеЗначение("Перечисление.упВариантыОптимизацииМаршрута.ПоВремени"));
	Элементы.ДлинаРейса.Доступность = Элементы.ОграничитьДлинуРейса.Доступность;
	Если Не Элементы.ОграничитьДлинуРейса.Доступность Тогда
		ЭтаФорма.ОграничитьДлинуРейса = Ложь;
	КонецЕсли; 
	Элементы.ЗональныйКоэффициент.Доступность = ЭтаФорма.УчитыватьДоступностьГеозонДляТранспорта;
	Элементы.ДоступныеЗоны.Доступность = ЭтаФорма.ОграничиватьСписокЗон; 
	Элементы.КоличествоРейсов.Доступность      = ЭтаФорма.ОграничитьКоличествоРейсов;
	Элементы.КоличествоТочекВРейсе.Доступность = ЭтаФорма.ОграничитьКоличествоТочекВРейсе;
	Элементы.ГруппаРазмерГруза.Доступность     = ЭтаФорма.ОграничитьРазмерГруза;
	Элементы.МинимальноеСбережение.Доступность = ЭтаФорма.УчитыватьМинимальноеСбережение;
	Элементы.МинимальноеКоличествоГрузовПриДеленииЗаданий.Доступность = ЭтаФорма.УчитыватьМинимальноеКоличествоГрузовПриДеленииЗаданий;
	Элементы.ГруппаДлинаРейса.Доступность      = ЭтаФорма.ОграничитьДлинуРейса;
	Элементы.ГруппаВремяОжидания.Доступность   = ЭтаФорма.УчитыватьВременныеОкна;
	Элементы.ГруппаВыделениеКрупногоГруза.Доступность = ЭтаФорма.ВыделятьКрупныеГрузы;
	Элементы.УчитыватьМестоположенияТС.Доступность = ЭтаФорма.УчитыватьРасписаниеРаботыТС;
	Элементы.ЗональныйКоэффициент.Доступность = ЭтаФорма.УчитыватьДоступностьГеозонДляТранспорта;
	Если ЭтаФорма.ВариантОптимизацииЗМТ = ПредопределенноеЗначение("Перечисление.упВариантыОптимизацииМаршрута.ПоВремени") Тогда 
		ЭтаФорма.РазмерностьДлиныРейса = НСтр("ru = 'ч'");
	Иначе // по расстоянию
		ЭтаФорма.РазмерностьДлиныРейса = НСтр("ru = 'км'");
	КонецЕсли;
	ИскомыеСтроки = ЭтаФорма.ПредставлениеПоказателей.НайтиСтроки(Новый Структура("Показатель", ЭтаФорма.ГрузоваяЕдиница));
	Если ИскомыеСтроки.Количество() Тогда
		ЭтаФорма.ЕИГруза = ИскомыеСтроки[0].Представление;
	КонецЕсли;
	ИскомыеСтроки = ЭтаФорма.ПредставлениеПоказателей.НайтиСтроки(Новый Структура("Показатель", ЭтаФорма.ПоказательКрупногоГруза));
	Если ИскомыеСтроки.Количество() Тогда
		ЭтаФорма.ЕИГрузаКарта = ИскомыеСтроки[0].Представление;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеМетодаОптимизации()

	Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
		ВариантОптимизацииМаршрута = Перечисления.упВариантыОптимизацииМаршрута.ПоВремени;
		Элементы.ВариантОптимизацииМаршрута.Доступность = Ложь;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗКВО;
	ИначеЕсли (МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритм Или МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.МетодВетвейИГраниц) Тогда
		Элементы.ВариантОптимизацииМаршрута.Доступность = Истина;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗК;
	ИначеЕсли ТипЗнч(МетодРешенияЗМТ) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		МетодРешенияЗКДляТиповыхНастроек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МетодРешенияЗК, "МетодРешенияЗКДляТиповыхНастроек");
		Если МетодРешенияЗКДляТиповыхНастроек = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
			ВариантОптимизацииМаршрута = Перечисления.упВариантыОптимизацииМаршрута.ПоВремени;
			Элементы.ВариантОптимизацииМаршрута.Доступность = Ложь;
			Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗКВО;
		ИначеЕсли (МетодРешенияЗКДляТиповыхНастроек = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритм Или МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.МетодВетвейИГраниц) Тогда
			Элементы.ВариантОптимизацииМаршрута.Доступность = Истина;
			Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗК; 
		КонецЕсли;
	КонецЕсли;	
	
	Если МетодРешенияЗМТ = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗМТ.МеханизмСбереженийКларкаРайта") Тогда
		Элементы.ГруппаСтраницыЗМТ.ТекущаяСтраница = Элементы.ГруппаКларкРайт;
	ИначеЕсли МетодРешенияЗМТ = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗМТ.ЗональноеПланирование") Тогда
		Элементы.ГруппаСтраницыЗМТ.ТекущаяСтраница = Элементы.ГруппаЗональноеПланирование;		 
	ИначеЕсли ТипЗнч(МетодРешенияЗМТ) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		МетодРешенияЗМТДляТиповыхНастроек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МетодРешенияЗМТ, "МетодРешенияЗМТДляТиповыхНастроек");
		Если МетодРешенияЗМТДляТиповыхНастроек = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗМТ.МеханизмСбереженийКларкаРайта") Тогда
			Элементы.ГруппаСтраницыЗМТ.ТекущаяСтраница = Элементы.ГруппаКларкРайт;
		ИначеЕсли МетодРешенияЗМТДляТиповыхНастроек = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗМТ.ЗональноеПланирование") Тогда
			Элементы.ГруппаСтраницыЗМТ.ТекущаяСтраница = Элементы.ГруппаЗональноеПланирование;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаДоступныхРесурсовНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(КлючВарианта) Тогда
		ТекстСообщения = "Сначала необходимо выбрать вариант планирования в главной форме!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("КлючВарианта", КлючВарианта));
	ПараметрыФормы.Вставить("СписокОбъектов", СписокПартнеров);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.НастройкаДоступныхРесурсов", ПараметрыФормы);

КонецПроцедуры

&НаСервере
Процедура ОбновитьНадписиДоступныхОбъектовНаСервере()
	
	НаборЗаписей = РегистрыСведений.упДоступныеГеографическиеЗоныВариантовПланирования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КлючВарианта.Установить(КлючВарианта);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		Элементы.ДекорацияНастройкаДоступныхЗон.Заголовок = "Доступные зоны: список";
	Иначе
		Элементы.ДекорацияНастройкаДоступныхЗон.Заголовок = "Доступные зоны: авто";
	КонецЕсли; 
	НаборЗаписей = РегистрыСведений.упДоступныеРесурсыВариантовПланирования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КлючВарианта.Установить(КлючВарианта);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		Элементы.ДекорацияНастройкаДоступныхРесурсов.Заголовок = "Доступные ресурсы: список";
	Иначе
		Элементы.ДекорацияНастройкаДоступныхРесурсов.Заголовок = "Доступные ресурсы: авто";
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 
