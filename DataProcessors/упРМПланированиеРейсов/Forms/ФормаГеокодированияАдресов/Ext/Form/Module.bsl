
#Область ПеременныеМодуляФормы

// Переменная ЧислоПопыток хранит количество попыток сформировать документ HTML.
&НаКлиенте
Перем ЧислоПопыток, СлужебныеЭлементыHtml;

#КонецОбласти 
			   
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьЦветаУсловногоОформления();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСтилиПолигонов.ЦветЗаливки КАК ОсновнойЦвет
	|ИЗ
	|	Справочник.упСтилиПолигонов КАК упСтилиПолигонов
	|ГДЕ
	|	НЕ упСтилиПолигонов.ЦветЗаливки В (&ОсновнойЦвет)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСтилиМаркеров.ОсновнойЦвет
	|ИЗ
	|	Справочник.упСтилиМаркеров КАК упСтилиМаркеров
	|ГДЕ
	|	НЕ упСтилиМаркеров.ОсновнойЦвет В (&ОсновнойЦвет)";
	Запрос.УстановитьПараметр("ОсновнойЦвет", спзЦветаУсловногоОформления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если Не ЗначениеЗаполнено(Выборка.ОсновнойЦвет) Тогда 
			Продолжить;
		КонецЕсли;
		
		НовыйЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		НовыйЭлементУсловногоОформления.Использование = Истина;
		
		НовыйЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", упСервисныеФункцииКлиентСервер.HexСтрВЦвет(Выборка.ОсновнойЦвет));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементУсловногоОформления.Отбор, "тбпАдресаЗаданийНаПеревозку.ОсновнойЦвет", Выборка.ОсновнойЦвет, ВидСравненияКомпоновкиДанных.Равно,, Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		
		ОформляемоеПоле = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Использование = Истина;
		ОформляемоеПоле.Поле          = Новый ПолеКомпоновкиДанных("тбпАдресаЗаданийНаПеревозкуОсновнойЦвет");
	КонецЦикла;	

	КлючAPI = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКлючЭлектронныхГеографическихКарт");
	
	HTMLДокумент = упМаршрутизация.ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса();	
	
	НачальнаяШирота  = 55.84128;
	НачальнаяДолгота = 37.6495;
	НачальныйЗум     = 13;
	
	ЭтоЯндекс = (упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипКарт") = Перечисления.упТипыКарт.ЯндексКарты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЧислоПопыток = 0;
	
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%КлючAPI%", СокрЛП(КлючAPI));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Широта%", ПредставлениеЧисла(НачальнаяШирота));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Долгота%", ПредставлениеЧисла(НачальнаяДолгота));
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Увеличение%", ПредставлениеЧисла(НачальныйЗум));	
	
	сткКартинок = Новый Структура();
	сткКартинок.Вставить("упМаркерПростойБелый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойБелый, Неопределено));
	сткКартинок.Вставить("упМаркерПростойЖелтый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЖелтый, Неопределено));
	сткКартинок.Вставить("упМаркерПростойКрасный", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойКрасный, Неопределено));
	сткКартинок.Вставить("упГеокодируемыйМаркер", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упГеокодируемыйМаркер, Неопределено));
	сткКартинок.Вставить("упТумблер",              	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упТумблер, Неопределено));
	
	упСервисныеФункцииВызовСервера.ЗаполнитьДвоичныеДанныеКартинок(сткКартинок);
	 	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упТумблер.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%toggle%", ИмяВременногоФайла);

	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойБелый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерРедактируемогоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойБелый");
	НовыйСохраненныйМаркер.Адрес   = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЖелтый.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПервогоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойЖелтый");
	НовыйСохраненныйМаркер.Адрес   = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойКрасный.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПоследнегоУгла%", ИмяВременногоФайла);
	
	НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
	НовыйСохраненныйМаркер.Маркер = ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойКрасный");
	НовыйСохраненныйМаркер.Адрес   = ИмяВременногоФайла;
	
	ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упГеокодируемыйМаркер.ДвоичныеДанные, УникальныйИдентификатор);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерГеокодирования%", ИмяВременногоФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
#Если ТонкийКлиент ИЛИ ВебКлиент Тогда
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

	Если РежимРедактирования Тогда 
		ТекстВопроса = НСтр("ru = 'При закрытии измененные данные будут потеряны. Сохранить?'");
		
		Обработчик = Новый ОписаниеОповещения("ПередЗакрытиемОтветНаВопрос", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Да);
		Отказ = Истина;
	Иначе	
		Если КартаСформирована Тогда 
			Положение = ВыполнитьФункциюHTML("getMapStatus()");
			мсвЧисел = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Положение, ";");
			Если мсвЧисел.Количество() Тогда 
				НачальнаяШирота  = мсвЧисел[0];
				НачальнаяДолгота = мсвЧисел[1];
				НачальныйЗум     = мсвЧисел[2];
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
#КонецЕсли	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ГеокодироватьАдресаЗаданийНаПеревозку" Тогда
		мсвСтруктур = Новый Массив;
		Для каждого Строка Из Параметр Цикл
			сткВозврат = ПолучитьДанныеОбъекта(Строка.Объект);
			сткВозврат.Вставить("ЗаданиеНаПеревозку", Строка.ЗаданиеНаПеревозку);
			мсвСтруктур.Добавить(сткВозврат);
		КонецЦикла;	
						
		Для каждого текСтруктура Из мсвСтруктур Цикл
			
			НоваяСтрока = тбпАдресаЗаданийНаПеревозку.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтруктура);
			
			Если НоваяСтрока.Пометка И КартаСформирована Тогда 
				ОтобразитьОбъект(НоваяСтрока);
			КонецЕсли;
		КонецЦикла; 	
		
		УстановитьВидимость();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	
	ЧислоПопыток = ЧислоПопыток + 1;
	
	// Проверяет на стороне карты сформирован ли объект Map.
	MapExists = ВыполнитьФункциюHTML("ValidateCacheCalls()");
	Если MapExists Тогда
		Если Не КартаСформирована Тогда 
			КартаСформирована = Истина;
			
			Для каждого текСтрока Из тбпАдресаЗаданийНаПеревозку Цикл
				Если текСтрока.Пометка И ЗначениеЗаполнено(текСтрока.Координаты) Тогда 
					ОтобразитьОбъект(текСтрока);
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	Иначе	
		КартаСформирована = Истина;
		Если ЧислоПопыток >= 2 ИЛИ ЭтоЯндекс Тогда
			// Число попыток больше 1 тогда подключаем отложеное действие.
			ЧислоПопыток = 0;
			ПодключитьОбработчикОжидания("ВыполнитьОтложенноеДействие",0.1,Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тбпАдресаЗаданийНаПеревозкуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текДанные = Элементы.тбпАдресаЗаданийНаПеревозку.ТекущиеДанные;
	Если Поле = Элементы.тбпАдресаЗаданийНаПеревозкуРедактировать И ЗначениеЗаполнено(текДанные.Редактировать) Тогда 
		
		Если РежимРедактирования Тогда 
			Если Элементы.тбпАдресаЗаданийНаПеревозку.ТекущаяСтрока = РедактируемаяСтрока Тогда 
				Ответ = ВыполнитьФункциюHTML("setObjectMarker(" + ПредставлениеЧисла(текДанные.ИндексВМассиве) + ", """ + текДанные.Имя + """, " + текДанные.Данные + ")");
				Если Ответ = 0 ИЛИ Ответ = "0" Тогда 
					текДанные.Редактировать  = НСтр("ru = 'Указать точку'");
					текДанные.Пометка        = Ложь;
					текДанные.ИндексКартинки = 1;
					текДанные.Координаты     = "";
					текДанные.ИндексВМассиве = 0;
					
					СохранитьКоординатыМаркера(текДанные.Объект, 0, 0);
				Иначе	
					текДанные.Редактировать  = НСтр("ru = 'Редактировать'");
					текДанные.Пометка        = Истина;
					текДанные.ИндексКартинки = 2;
					
					мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ответ, ";");
					
					текДанные.Координаты     = мсвКоординат[1] + ";" + мсвКоординат[2] + ";";
					текДанные.ИндексВМассиве = мсвКоординат[0];
					
					СохранитьКоординатыМаркера(текДанные.Объект, мсвКоординат[1], мсвКоординат[2]);
				КонецЕсли;
				
				РежимРедактирования = Ложь;
				РедактируемаяСтрока = 0;
			КонецЕсли;
		Иначе	
			РедактироватьОбъект(текДанные);
			
			РежимРедактирования = Истина;
			РедактируемаяСтрока = Элементы.тбпАдресаЗаданийНаПеревозку.ТекущаяСтрока;
			
			текДанные.Редактировать = НСтр("ru = 'Завершить редактирование'");
		КонецЕсли;	
		
	ИначеЕсли Поле = Элементы.тбпАдресаЗаданийНаПеревозкуОпределитьКоординаты Тогда
		
		СтрокаПоиска = текДанные.Имя;
		
		#Если ВебКлиент Тогда
			сткКоординаты = упГеокодированиеСервер.НайтиКоординатыАдреса(СтрокаПоиска);
		#Иначе	
			сткКоординаты = упГеокодированиеКлиентСервер.НайтиКоординатыАдреса(СтрокаПоиска);
		#КонецЕсли	
		
		Если сткКоординаты <> Неопределено Тогда
			ПредставлениеКоординат = ПредставлениеЧисла(сткКоординаты.Широта) + ";" + ПредставлениеЧисла(сткКоординаты.Долгота) + ";";
			Если текДанные.Координаты <> ПредставлениеКоординат Тогда
				УдалитьТочку = ЗначениеЗаполнено(текДанные.Координаты);
				
				текДанные.Редактировать  = НСтр("ru = 'Редактировать'");
				текДанные.Пометка        = Истина;
				текДанные.ИндексКартинки = 2;
				текДанные.Координаты     = ПредставлениеЧисла(сткКоординаты.Широта) + ";" + ПредставлениеЧисла(сткКоординаты.Долгота) + ";";
				СохранитьКоординатыМаркера(текДанные.Объект, сткКоординаты.Широта,сткКоординаты.Долгота);
				
				Если УдалитьТочку Тогда
					ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(текДанные.ИндексВМассиве) + ")");
				КонецЕсли;
				
				Индекс = ВыполнитьФункциюHTML("addObjectMarker(" +ПредставлениеЧисла(сткКоординаты.Широта) + ", " + ПредставлениеЧисла(сткКоординаты.Долгота) + ", """ + текДанные.Имя + """, " + текДанные.Данные + ")");
				текДанные.ИндексВМассиве = Индекс;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Поле = Элементы.тбпАдресаЗаданийНаПеревозкуЗаданиеНаПеревозку Тогда
		
		сткПараметры = Новый Структура("Ключ", текДанные.ЗаданиеНаПеревозку);
		ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.Форма.ФормаДокумента",сткПараметры,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тбпАдресаЗаданийНаПеревозкуПометкаПриИзменении(Элемент)
	
	текДанные = Элементы.тбпАдресаЗаданийНаПеревозку.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(текДанные.ИндексВМассиве) И ЗначениеЗаполнено(текДанные.Координаты) Тогда 
		ИзменитьВидимостьОбъекта(текДанные.ИндексВМассиве, текДанные.Пометка);
		Если текДанные.Пометка Тогда 
			текДанные.ИндексКартинки = текДанные.ИндексКартинки + 1;
		Иначе	
			текДанные.ИндексКартинки = текДанные.ИндексКартинки - 1;
		КонецЕсли;	
	Иначе
		текДанные.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
			   
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Обработчик действия ожидания. При неудачной попытке загрузки карты выполняется повторный вызов.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьОтложенноеДействие() Экспорт 

	HTMLДокументДокументСформирован(Неопределено);

КонецПроцедуры // ВыполнитьОтложенноеДействие()

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ.
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Нет.
// 
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	DOMДокумент - ПолеHTMLДокумента.Документ.
//	текстСкрипта - Строка - тектс на javascript.
//
// Возвращаемое значение:
//	Строка - значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат	упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьОбъект(СтрокаОбъекта)
	
	мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаОбъекта.Координаты, ";");
	
	Индекс = ВыполнитьФункциюHTML("addObjectMarker(" + мсвКоординат[0] + ", " + мсвКоординат[1] + ", """ + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
	СтрокаОбъекта.ИндексВМассиве = Индекс;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьОбъекта(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда 
		ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьОбъект(СтрокаОбъекта)
	
	Если ЗначениеЗаполнено(СтрокаОбъекта.Координаты) Тогда 
		ВыполнитьПроцедуруHTML("redrawObjectMarker(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """, 0, 0)");
	Иначе
		ВыполнитьПроцедуруHTML("drawObjectMarker(""" + СтрокаОбъекта.Имя + """)");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОбъект(СтрокаОбъекта)
	
	Ответ = ВыполнитьФункциюHTML("setObjectMarker(" + ПредставлениеЧисла(СтрокаОбъекта.ИндексВМассиве) + ", """ + СтрокаОбъекта.Имя + """, " + СтрокаОбъекта.Данные + ")");
	Если Ответ = 0 ИЛИ Ответ = "0" Тогда 
		СтрокаОбъекта.Редактировать  = НСтр("ru = 'Указать точку'");
		СтрокаОбъекта.Пометка        = Ложь;
		СтрокаОбъекта.ИндексКартинки = 1;
		СтрокаОбъекта.Координаты     = "";
		СтрокаОбъекта.ИндексВМассиве = 0;
		
		СохранитьКоординатыМаркера(СтрокаОбъекта.Объект, 0, 0);
	Иначе	
		СтрокаОбъекта.Редактировать  = НСтр("ru = 'Редактировать'");
		СтрокаОбъекта.Пометка        = Истина;
		СтрокаОбъекта.ИндексКартинки = 2;
		
		мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Ответ, ";");
		
		СтрокаОбъекта.Координаты     = мсвКоординат[1] + ";" + мсвКоординат[2] + ";";
		СтрокаОбъекта.ИндексВМассиве = мсвКоординат[0];
		
		СохранитьКоординатыМаркера(СтрокаОбъекта.Объект, мсвКоординат[1], мсвКоординат[2]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКоординатыМаркера(Маркер, Широта, Долгота)
	
	текОбъект = Маркер.ПолучитьОбъект();
	текОбъект.Широта  = Широта;
	текОбъект.Долгота = Долгота;
	текОбъект.Записать();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеОбъекта(ВыбранныйОбъект)
	
	сткВозврат = Новый Структура("Объект, Редактировать, ОпределитьКоординаты, Пометка, ИндексКартинки, Имя, Координаты, Данные, ОсновнойЦвет");
	сткВозврат.Объект = ВыбранныйОбъект;
	
	ТипЗначения = ТипЗнч(ВыбранныйОбъект);
	
	Если ТипЗначения = Тип("СправочникСсылка.упАдреса") Тогда
		
		АдресКартинки = "";
		
		сткДанныеИмени = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыбранныйОбъект, "Наименование, ПолноеНаименование");
		сткВозврат.Имя   = ?(ЗначениеЗаполнено(сткДанныеИмени.ПолноеНаименование), сткДанныеИмени.ПолноеНаименование, сткДанныеИмени.Наименование);
		СтильОтображения = ВыбранныйОбъект.ВидАдреса.СтильМаркера;
		Если Не ЗначениеЗаполнено(СтильОтображения) Тогда 
			СтильОтображения = Справочники.упСтилиМаркеров.Основной;
		КонецЕсли;	
		
		Если СтильОтображения.Картинка = Перечисления.упКартинкиМаркеров.МаркерПоУмолчанию Тогда 
			АдресКартинки = "";
		ИначеЕсли СтильОтображения.Картинка = Перечисления.упКартинкиМаркеров.ВыбратьИзФайла Тогда 
			мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Стиль", СтильОтображения));
			Если мсвСохраненныйМаркер.Количество() Тогда 
				Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Адрес) Тогда 
					АдресКартинки = мсвСохраненныйМаркер[0].Адрес;
				КонецЕсли;	
			Иначе
				АдресКартинки = ПоместитьВоВременноеХранилище(СтильОтображения.ХранилищеЗначения.Получить(), УникальныйИдентификатор);

				НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
				НовыйСохраненныйМаркер.Стиль = СтильОтображения;
				НовыйСохраненныйМаркер.Адрес = АдресКартинки;
			КонецЕсли;	
		Иначе	
			мсвСохраненныйМаркер = СохраненныеМаркеры.НайтиСтроки(Новый Структура("Маркер", СтильОтображения.Картинка));
			Если мсвСохраненныйМаркер.Количество() Тогда 
				Если ЗначениеЗаполнено(мсвСохраненныйМаркер[0].Адрес) Тогда 
					АдресКартинки = мсвСохраненныйМаркер[0].Адрес;
				КонецЕсли;	
			Иначе
				Картинка = упРаботаСКартамиКлиентСервер.ПолучитьКартинку(СтильОтображения.Картинка);
				
				АдресКартинки = ПоместитьВоВременноеХранилище(Картинка, УникальныйИдентификатор);

				НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
				НовыйСохраненныйМаркер.Маркер = СтильОтображения.Картинка;
				НовыйСохраненныйМаркер.Адрес  = АдресКартинки;
			КонецЕсли;	
		КонецЕсли;	
		
		сткВозврат.Данные         = "'" + АдресКартинки + "', " + ПредставлениеЧисла(СтильОтображения.ПоложениеПривязкиX) + ", " + ПредставлениеЧисла(СтильОтображения.ПоложениеПривязкиY);
		сткВозврат.ОсновнойЦвет   = СтильОтображения.ОсновнойЦвет;
		сткВозврат.ОпределитьКоординаты = НСтр("ru = 'Позиционировать'");
		Если ЗначениеЗаполнено(ВыбранныйОбъект.Широта) ИЛИ ЗначениеЗаполнено(ВыбранныйОбъект.Долгота) Тогда 
			сткВозврат.Редактировать  = НСтр("ru = 'Редактировать'");
			сткВозврат.Пометка        = Истина;
			сткВозврат.ИндексКартинки = 2;
			сткВозврат.Координаты     = ПредставлениеЧисла(ВыбранныйОбъект.Широта) + ";" + ПредставлениеЧисла(ВыбранныйОбъект.Долгота) + ";";
		Иначе
			сткВозврат.Редактировать  = НСтр("ru = 'Указать точку'");
			сткВозврат.Пометка        = Ложь;
			сткВозврат.ИндексКартинки = 1;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат сткВозврат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.тбпАдресаЗаданийНаПеревозку.Видимость   = Истина;
	Элементы.тбпАдресаЗаданийНаПеревозку.Доступность = Истина;
	
	Элементы.тбпАдресаЗаданийНаПеревозку.ТолькоПросмотр           = Ложь;
	Элементы.тбпАдресаЗаданийНаПеревозкуПометка.ТолькоПросмотр    = Ложь;
	Элементы.тбпАдресаЗаданийНаПеревозкуРедактировать.Доступность = Истина;
	АвтоЗаголовок = Ложь;
	Заголовок = "Геокодирование адресов";
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьЦветаУсловногоОформления()
    
    спзЦветаУсловногоОформления.Добавить("#FFFFFF");
    спзЦветаУсловногоОформления.Добавить("#1C7ED4");
    спзЦветаУсловногоОформления.Добавить("#1FCCCF");
    спзЦветаУсловногоОформления.Добавить("#DADE00");
    спзЦветаУсловногоОформления.Добавить("#33D41E");
    спзЦветаУсловногоОформления.Добавить("#D1221F");
    спзЦветаУсловногоОформления.Добавить("#1F1FD1");
    спзЦветаУсловногоОформления.Добавить("#DB09C3");
    спзЦветаУсловногоОформления.Добавить("#00FFFF");
    спзЦветаУсловногоОформления.Добавить("#FFFF00");
    спзЦветаУсловногоОформления.Добавить("#00FF00");
    спзЦветаУсловногоОформления.Добавить("#FF0000");
    спзЦветаУсловногоОформления.Добавить("#FF8500");
    спзЦветаУсловногоОформления.Добавить("#FFB4B4");
    спзЦветаУсловногоОформления.Добавить("#C0C0C0");
    спзЦветаУсловногоОформления.Добавить("#0000FF");
    спзЦветаУсловногоОформления.Добавить("#FF00FF");
    спзЦветаУсловногоОформления.Добавить("#000000");
    спзЦветаУсловногоОформления.Добавить("#BBFF2F");

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемОтветНаВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		текДанные = тбпАдресаЗаданийНаПеревозку.НайтиПоИдентификатору(РедактируемаяСтрока);
		
		Если текДанные <> Неопределено Тогда 
			СохранитьОбъект(текДанные);
		КонецЕсли;
	КонецЕсли;
	РедактируемаяСтрока = 0;
	РежимРедактирования = Ложь;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти 