#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.СписокОбъектов = Параметры.СписокОбъектов;
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не Параметры.Свойство("ТолькоПросмотр") Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ЗаполнитьОбъектыНаСервере();
	ОбновитьТипыОбъектов();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = РегистрыСведений.упДоступныеРесурсыВариантовПланирования.СоздатьНаборЗаписей();
	КонецЕсли;
	Для Каждого СтрокаНабора Из ТекущийОбъект Цикл
		СтрокаНабора.КлючВарианта = ТекущийОбъект.Отбор.КлючВарианта.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененыДоступныеРесурсыПланирования");
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьТипыОбъектов();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаборЗаписейПриИзменении(Элемент)
	
	ОбновитьТипыОбъектов();
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Дозаполнить(Команда)
	
	ЗаполнитьОбъектыНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Перезаполнить(Команда)
	
	ЗаполнитьОбъектыНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	НаборЗаписей.Очистить();
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ОбновитьТипыОбъектов()
	
	СтрокаОбщегоПравила = СтрокаОбщегоПравила();
	Для Каждого СтрокаНабора Из ЭтаФорма.НаборЗаписей Цикл
		//СтрокаНабора.ТипОбъекта = ТипЗнч(СтрокаНабора.Объект);
		Если ЗначениеЗаполнено(СтрокаНабора.ПравилоДоступности) Тогда
			СтрокаНабора.РезультирующееПравило = СтрокаНабора.ПравилоДоступности;
		ИначеЕсли Истина
			И СтрокаОбщегоПравила <> Неопределено
			И ЗначениеЗаполнено(СтрокаОбщегоПравила.ПравилоДоступности)
		Тогда
			СтрокаНабора.РезультирующееПравило = СтрокаОбщегоПравила.ПравилоДоступности;
		ИначеЕсли СтрокаНабора.Объект <> Неопределено Тогда 
			СтрокаНабора.РезультирующееПравило = СтрокаНабора.Объект.ДоступныеРесурсы;
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция СтрокаОбщегоПравила()
	
	СтрокаОбщегоПравила = ЭтаФорма.НаборЗаписей.НайтиСтроки(Новый Структура("Объект", Справочники.упПартнеры.ПустаяСсылка()));
	Если СтрокаОбщегоПравила.Количество() > 0 Тогда
		СтрокаОбщегоПравила = СтрокаОбщегоПравила[0];
	Иначе
		СтрокаОбщегоПравила = Неопределено;
	КонецЕсли;
	Возврат СтрокаОбщегоПравила;

КонецФункции

&НаСервере
Процедура ЗаполнитьОбъектыНаСервере(ОчиститьПередЗаполнением = Ложь)
	
	Если Ложь Тогда // Для контекстной подсказки
	    НаборЗаписей = РегистрыСведений.упДоступныеРесурсыВариантовПланирования.СоздатьНаборЗаписей();
	КонецЕсли;
	Если ОчиститьПередЗаполнением Тогда
		НачальноеКоличество = НаборЗаписей.Количество(); 
		Для СчетчикНаборЗаписей = 1 По НачальноеКоличество Цикл
			СтрокаНабора = НаборЗаписей[НачальноеКоличество - СчетчикНаборЗаписей];
			Если Истина
				И СтрокаНабора.Объект <> Неопределено
				И СписокОбъектов.НайтиПоЗначению(СтрокаНабора.Объект) = Неопределено
			Тогда
				НаборЗаписей.Удалить(СтрокаНабора);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	Для Каждого ЭлементСписка Из СписокОбъектов Цикл
		Если НаборЗаписей.НайтиСтроки(Новый Структура("Объект", ЭлементСписка.Значение)).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаНабора = НаборЗаписей.Добавить();
		СтрокаНабора.Объект = ЭлементСписка.Значение;
		//СтрокаНабора.ПравилоДоступности = СтрокаНабора.Объект.ДоступныеРесурсы;
	КонецЦикла;
	СтрокаОбщегоПравила = СтрокаОбщегоПравила();
	Если СтрокаОбщегоПравила = Неопределено Тогда
		СтрокаОбщегоПравила = НаборЗаписей.Вставить(0);
	КонецЕсли; 
	ОбновитьТипыОбъектов();
	НаборЗаписей.Сортировать("Объект");
	
КонецПроцедуры

#КонецОбласти