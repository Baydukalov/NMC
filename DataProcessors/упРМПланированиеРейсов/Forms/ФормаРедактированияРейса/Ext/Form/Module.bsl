
#Область ПеременныеМодуляФормы

&НаКлиенте
Перем ЧислоПопыток;

&НаКлиенте
Перем ПроизведенПереходПоСсылке;

&НаКлиенте
Перем ЗафиксироватьИсходныеПоказатели;

&НаКлиенте
Перем РазрешитьАктивизациюСтроки, СлужебныеЭлементыHtml;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыМаршрутизацииСитиГИД = упМаршрутизация.ПолучитьПараметрыМаршрутизацииСитиГИД();
	
	Рейс = Параметры.Рейс;
	ВерсияДанных = ПолучитьВерсиюРейса(Рейс);
		
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Рейс,, УникальныйИдентификатор);
	Исключение
		ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Рейс);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	НачалоПериодаПланирования = Параметры.НачалоПериодаПланирования;
	ОкончаниеПериодаПланирования = Параметры.ОкончаниеПериодаПланирования;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ПараметрыПланирования);
	Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
		ВариантОптимизацииМаршрута = Перечисления.упВариантыОптимизацииМаршрута.ПоВремени;
		Элементы.ВариантОптимизацииМаршрута.Доступность = Ложь;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗКВО;
	Иначе
		Элементы.ВариантОптимизацииМаршрута.Доступность = Истина;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗК;
	КонецЕсли; 
	
	РежимКорректировки = Параметры.РежимКорректировки;
	Элементы.МаршрутПометка.Видимость 	= НЕ РежимКорректировки;
	Элементы.РаботыПометка.Видимость 	= НЕ РежимКорректировки;
	Если РежимКорректировки Тогда
		ЗаголовокКоманды	= Нстр("ru = 'Создать корректировку'");
		Команды["ЗавершитьРедактирование"].Заголовок = ЗаголовокКоманды;	
		Команды["ЗавершитьРедактирование"].Подсказка = ЗаголовокКоманды;
		
		Элементы.ПересчитатьМаршрутПоАктивнымТочкам.Доступность = Ложь;
		Элементы.ПересчитатьМаршрутПоВсемТочкам.Заголовок 		= Нстр("ru = 'по не пройденным точкам.'");
		Элементы.РедактироватьПосещениеГаража.Видимость			= Ложь;
	КонецЕсли;
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	
	Если ДеньНедели(НачалоПериодаПланирования) = 1 Тогда
		ВремяДеньНедели = "ВремяПонедельник";
	ИначеЕсли  ДеньНедели(НачалоПериодаПланирования) = 2 Тогда	
		ВремяДеньНедели = "ВремяВторник";
	ИначеЕсли  ДеньНедели(НачалоПериодаПланирования) = 3 Тогда	
		ВремяДеньНедели = "ВремяСреда";
	ИначеЕсли  ДеньНедели(НачалоПериодаПланирования) = 4 Тогда	
		ВремяДеньНедели = "ВремяЧетверг";
	ИначеЕсли  ДеньНедели(НачалоПериодаПланирования) = 5 Тогда	
		ВремяДеньНедели = "ВремяПятница";
	ИначеЕсли  ДеньНедели(НачалоПериодаПланирования) = 6 Тогда	
		ВремяДеньНедели = "ВремяСуббота";
	ИначеЕсли  ДеньНедели(НачалоПериодаПланирования) = 7 Тогда	
		ВремяДеньНедели = "ВремяВоскресенье";
	КонецЕсли; 
	
	УчитыватьМассу                       = упСервисныеФункции.ПолучитьЗначениеКонстанты("упУчитыватьМассу");
	УчитыватьОбъем                       = упСервисныеФункции.ПолучитьЗначениеКонстанты("упУчитыватьОбъем");
	УчитыватьКоличествоМест              = упСервисныеФункции.ПолучитьЗначениеКонстанты("упУчитыватьКоличествоМест");
	УчитыватьКоличествоЗагрузочныхМетров = упСервисныеФункции.ПолучитьЗначениеКонстанты("упУчитыватьКоличествоЗагрузочныхМетров");
	ПроизводитьРасчетПопарныхРасстоянийПриРешенииЗадачиКоммивояжера = ПолучитьФункциональнуюОпцию("упПроизводитьРасчетПопарныхРасстоянийПриРешенииЗадачиКоммивояжера");
	
	УчитыватьРежимТрудаИОтдыхаСотрудников = ПолучитьФункциональнуюОпцию("упУчитыватьРежимТрудаИОтдыхаСотрудников") И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидПеревозки.ПравилоРежимаТрудаИОтдыхаСотрудников"));
	
	HTMLДокумент = упМаршрутизация.ПолучитьТекстОбщегоМакетаКартоГрафическогоСервиса();

	НачальнаяШирота  = 55.84128;
	НачальнаяДолгота = 37.6495;
	НачальныйЗум     = 13;
	
	ЦветУлучшениеПоказания = WebЦвета.Синий;
	ЦветУхудшениеПоказания = WebЦвета.Красный;
	ЦветИнтервалаФона      = WebЦвета.БледноЗеленый;
	
	ПримененныеПравилаВключенияКонтрольныхТочек.Очистить();
	ОбновитьМаршрутРаботыНаСервере();
	АктуализироватьРасстоянияМеждуАдресамиНаСервере();
	УстановитьПредставлениеЕдиницИзмеренияПоказателей();
	
	УстановитьСписокДоступныхПоказателейКонтроля();
	УстановитьПараметрыДиаграммыПоУмолчанию();
	ОбновитьШкалу(ЭтаФорма);   
	ОбновитьДанныеНаДиаграмме();

	ЭтоЯндекс = (упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипКарт") = Перечисления.упТипыКарт.ЯндексКарты);	
	
	Если Истина
		И НЕ УчитыватьМассу
		И НЕ УчитыватьОбъем
		И НЕ УчитыватьКоличествоМест
		И НЕ УчитыватьКоличествоЗагрузочныхМетров 
	Тогда
		Элементы.ГруппаДиаграммаЗагрузки.Видимость = Ложь;
	КонецЕсли;
	
	Если УчитыватьМассу Тогда
		ГрузоваяЕдиница = 0;
	ИначеЕсли УчитыватьОбъем Тогда	
		ГрузоваяЕдиница = 1;	
	ИначеЕсли УчитыватьКоличествоМест Тогда			
		ГрузоваяЕдиница = 2;	
	ИначеЕсли УчитыватьКоличествоМест Тогда			
		ГрузоваяЕдиница = 3;
	Иначе 
		ГрузоваяЕдиница = -1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РазрешитьАктивизациюСтроки = Истина;
	ЧислоПопыток = 0;
	ПроизведенПереходПоСсылке = Ложь;
	ЗафиксироватьИсходныеПоказатели = Истина;
	#Если ВебКлиент Тогда
		ПоказатьПредупреждение(,Нстр("ru = 'Работоспособность формы в веб-клиенте не поддерживается.'"));
		Отказ=Истина;
		Возврат;
	#Иначе
		
		// Переопределение текущих элементов таблиц формы для изменения активизации поля при первичной загрузке.
		Элементы.Маршрут.ТекущийЭлемент = Элементы.МаршрутАдрес;
		Элементы.Работы.ТекущийЭлемент = Элементы.РаботыЗаказчик;
		
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Широта%", ПредставлениеЧисла(НачальнаяШирота));
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Долгота%", ПредставлениеЧисла(НачальнаяДолгота));
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Увеличение%", ПредставлениеЧисла(НачальныйЗум));	
		
		сткКартинок = Новый Структура();
		сткКартинок.Вставить("упМаркерПростойБелый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойБелый, Неопределено));
		сткКартинок.Вставить("упМаркерПростойЖелтый", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойЖелтый, Неопределено));
		сткКартинок.Вставить("упМаркерПростойКрасный", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойКрасный, Неопределено));
		сткКартинок.Вставить("упГеокодируемыйМаркер", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упГеокодируемыйМаркер, Неопределено));
		сткКартинок.Вставить("упМаркерПростойСиний", 	Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упМаркерПростойСиний, Неопределено));
		сткКартинок.Вставить("упТумблер", 	            Новый Структура("Картинка, ДвоичныеДанные", БиблиотекаКартинок.упТумблер, Неопределено));
		
		упСервисныеФункцииВызовСервера.ЗаполнитьДвоичныеДанныеКартинок(сткКартинок);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упТумблер.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%toggle%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойБелый.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерРедактируемогоУгла%", ИмяВременногоФайла);
		
		НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
		НовыйСохраненныйМаркер.Маркер	= ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойБелый");
		НовыйСохраненныйМаркер.Адрес	= ИмяВременногоФайла;
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойЖелтый.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПервогоУгла%", ИмяВременногоФайла);
		
		НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
		НовыйСохраненныйМаркер.Маркер	= ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойЖелтый");
		НовыйСохраненныйМаркер.Адрес	= ИмяВременногоФайла;
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойКрасный.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерПоследнегоУгла%", ИмяВременногоФайла);
		
		НовыйСохраненныйМаркер = СохраненныеМаркеры.Добавить();
		НовыйСохраненныйМаркер.Маркер	= ПредопределенноеЗначение("Перечисление.упКартинкиМаркеров.МаркерПростойКрасный");
		НовыйСохраненныйМаркер.Адрес	= ИмяВременногоФайла;
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упГеокодируемыйМаркер.ДвоичныеДанные, УникальныйИдентификатор);
		HTMLДокумент = СтрЗаменить(HTMLДокумент, "%МаркерГеокодирования%", ИмяВременногоФайла);
		
		ИмяВременногоФайла = ПоместитьВоВременноеХранилище(сткКартинок.упМаркерПростойСиний.ДвоичныеДанные, УникальныйИдентификатор);
		
		// Исходные данные точки для отрисовки на карте: цвет текста подписи, цвет заполнения внутренней окружности, цвет заполнения внешней окружности.
		ДанныеСтандартнойТочки   = "'#61411E', '#6BCBE8', '#6B8DE8'";
		ДанныеДепо               = "'#61411E', '#FFE12D', '#6B8DE8'";
		ДанныеИсключеннойТочки   = "'#61411E', '#A5A696', '#7A7A6F'";
		ДанныеНарушенийПоВремени = "'#61411E', '#FF2A3C', '#6B8DE8'";
		ДанныеПройденнойТочки	 = "'#61411E', '#6BE896', '#6B8DE8'";
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	#Если НЕ ВебКлиент Тогда
		Если ИмяСобытия = "ОбработкаРешенияЗКРедактированиеРейса" Тогда	
			АдресВременногоХранилища = Параметр.АдресВременногоХранилища;
			ТолькоАктивные           = Параметр.ТолькоАктивные;
			Если АдресВременногоХранилища <> "" Тогда
				сткВозврат = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
				Если сткВозврат <> Неопределено Тогда
					ИзменитьПорядокОбъездаТочекРейса(сткВозврат, ТолькоАктивные);
				КонецЕсли; 
			КонецЕсли;	
			Оповестить("ЗавершениеИзмененияМаршрутаРейса"); 
		КонецЕсли;
	#КонецЕсли	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	#Если НЕ ВебКлиент Тогда

	Если НЕ ПроизведенПереходПоСсылке Тогда
		ЧислоПопыток = ЧислоПопыток + 1;
		
		// Проверяет на стороне карты сформирован ли объект Map.
		MapExists = ВыполнитьФункциюHTML("ValidateCacheCalls()");
		Если MapExists Тогда
			ОчиститьСообщения();
			
			Если Не КартаСформирована Тогда 
				ВыполнитьПроцедуруHTML("setUsePositioning(false)");
				КартаСформирована = Истина;
				ОтобразитьОбъектыНаКарте();
				ОтобразитьМаршрут();
				ОбновитьПредставлениеПоказателейНаФорме(Истина, Истина);
			КонецЕсли;
		Иначе	
			
			Если НЕ ЭтоЯндекс Тогда
				КартаСформирована = Истина;
			КонецЕсли;
			
			Если ЧислоПопыток >= 2 ИЛИ ЭтоЯндекс Тогда
				ЧислоПопыток = 0;
				ПодключитьОбработчикОжидания("ВыполнитьОтложенноеДействие", 0.1, Истина);				
			КонецЕсли;
			
			
			
		КонецЕсли;
	КонецЕсли;
#КонецЕсли	
КонецПроцедуры

&НаКлиенте
Процедура HTMLДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	 #Если НЕ ВебКлиент Тогда
	Если ВыполняетОбращениеКДокументу Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроизведенПереходПоСсылке Или ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда // переход по ссылке на другой адрес, стандартная работа с картой больше не обрабатывается
		ПроизведенПереходПоСсылке = Истина;
	Иначе
		ОбновитьСостояниеМаркеров();
	КонецЕсли;
#КонецЕсли	
КонецПроцедуры

&НаКлиенте
Процедура МетодРешенияЗКПриИзменении(Элемент)
	
	Если МетодРешенияЗК = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон") Тогда
		ВариантОптимизацииМаршрута = ПредопределенноеЗначение("Перечисление.упВариантыОптимизацииМаршрута.ПоВремени");
		Элементы.ВариантОптимизацииМаршрута.Доступность = Ложь;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗКВО;
	Иначе
		Элементы.ВариантОптимизацииМаршрута.Доступность = Истина;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗК;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПространственныйВесПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Ложь, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВременнойВесПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Истина, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВесСрочностиПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Истина, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузоваяЕдиницаПриИзменении(Элемент)
	
	ОбновитьДанныеНаДиаграммеЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанныеМаршрут = Элементы.Маршрут.ТекущиеДанные;
	текЭлемент = Элементы.Маршрут.ТекущийЭлемент;
	#Если НЕ ВебКлиент Тогда
	Если КартаСформирована Тогда
		Если текЭлемент.Имя = Элементы.МаршрутПометка.Имя И Не ТекущиеДанныеМаршрут.НомерСтроки = 1 И Не ЗначениеЗаполнено(ТекущиеДанныеМаршрут.Гараж) Тогда
			
			ПересчитатьВремя = Истина;
			ПерерисоватьДиаграмму = Ложь;
			Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = Элементы.ГруппаДиаграмма.Имя Тогда
				ПерерисоватьДиаграмму = Истина;
			КонецЕсли;
			ТекущиеДанныеМаршрут.Пометка = Не ТекущиеДанныеМаршрут.Пометка; 			
	        спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
			 
 			// серверные вызовы
			ИзменитьДанныеФормыНаСервере(спкДействий, ПересчитатьВремя, ПерерисоватьДиаграмму,, Истина);
	
			// клиентские вызовы
			Если спкДействий.Количество() Тогда
				ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
			КонецЕсли;
			ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
			
		ИначеЕсли текЭлемент.Имя = Элементы.МаршрутПозиционирование.Имя Тогда
			
			ЦентрироватьСлойНаКарте(ТекущиеДанныеМаршрут.ИндексВМассиве);
			
		КонецЕсли;
	КонецЕсли; 
	
	//Если текЭлемент.Имя = "МаршрутПометка" Или
	//	текЭлемент.Имя = "МаршрутПозиционирование" Тогда
	//	
	//	// изменяем текущий элемент для возможности его повторной активизации
	//	Элементы.Маршрут.ТекущийЭлемент = Элементы.МаршрутАдрес;
	//КонецЕсли;
	#КонецЕсли
	
	Если ТекущиеДанныеМаршрут <> Неопределено Тогда
		Если Поле = Элементы.МаршрутАдрес Тогда
			ОткрытьФорму("Справочник.упАдреса.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанныеМаршрут.Адрес), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле = Элементы.МаршрутВременноеОкноПредставление Тогда	
			Если ТекущиеДанныеМаршрут.Пройдена Тогда
				ТекстПредупреждения = Нстр("ru = 'Запрещено редактировать временное окно у пройденной точки'");
				ПоказатьПредупреждение(,ТекстПредупреждения);
			Иначе
				ИдентификаторДляИзмененияВременногоОкна = Элементы.Маршрут.ТекущаяСтрока;
				сткПараметры = Новый Структура("НачалоПериода, КонецПериода", ТекущиеДанныеМаршрут.ВременноеОкноНачало, ТекущиеДанныеМаршрут.ВременноеОкноОкончание);
				ОбработатьВводПроизвольногоПериода = Новый ОписаниеОповещения("ОбработатьВводПроизвольногоПериодаМаршрут", ЭтотОбъект);
				ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораПроизвольногоПериода", сткПараметры, Элемент, УникальныйИдентификатор,,,ОбработатьВводПроизвольногоПериода, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);						
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриАктивизацииСтроки(Элемент)
	#Если НЕ ВебКлиент Тогда
	Если РазрешитьАктивизациюСтроки Тогда
		ПодключитьОбработчикОжидания("МаршрутПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриАктивизацииСтрокиОбработчикОжидания()
	#Если НЕ ВебКлиент Тогда	
	текСтрока = Элементы.Маршрут.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", текСтрока.Идентификатор);
		сткТочка = Новый Структура("Пройдена, Отменена, Идентификатор");
		ЗаполнитьЗначенияСвойств(сткТочка, текСтрока);
		РазрешеноРазделятьРаботы = РазрешеноРазделятьРаботы(сткТочка);
		Элементы.РазделитьРаботы.Доступность		= РазрешеноРазделятьРаботы;
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры // МаршрутПриАктивизацииСтрокиОбработчикОжидания()

&НаКлиенте
Процедура МаршрутПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если Строка = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МаршрутПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтрокаИсточник = Маршрут.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение);
	СтрокаПриемник = Маршрут.НайтиПоИдентификатору(Строка);
	
	НомерСтрокиПриемника = СтрокаПриемник.НомерСтроки;
	НомерСтрокиИсточника = СтрокаИсточник.НомерСтроки;
	СтрокаПриемник.НомерСтроки = НомерСтрокиИсточника;
	СтрокаИсточник.НомерСтроки = НомерСтрокиПриемника;
	
	Если Не ПорядокСУчетомПредшественниковСоблюден() Тогда
		СтрокаПриемник.НомерСтроки = НомерСтрокиПриемника;
		СтрокаИсточник.НомерСтроки = НомерСтрокиИсточника;
		
		ТекстСообщения = СтрШаблон("ru = 'Ошибка изменения маршрута! Нарушен порядок следования с учетом предшественников'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстСообщения));
		Возврат;
	КонецЕсли;
	
	ИндексИсточника = Маршрут.Индекс(СтрокаИсточник);
	ИндексПриемника = Маршрут.Индекс(СтрокаПриемник);
	
	Маршрут.Сдвинуть(ИндексИсточника, ИндексПриемника - ИндексИсточника);
	Маршрут.Сдвинуть(Маршрут.Индекс(СтрокаПриемник),  ИндексИсточника - Маршрут.Индекс(СтрокаПриемник));
	
	ОтобразитьМаршрут();
	спкДействий = Новый СписокЗначений;
	ПересчитатьВременнойГрафикМаршрута(, спкДействий);
	Если спкДействий.Количество() Тогда
		ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
	КонецЕсли;
	ОбновитьТекущиеРасстояниеИВремяРейса();
	ОбновитьДиаграммуНаКлиенте();
	ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПлановоеПрибытиеПриИзменении(Элемент)
	
	ПересчитатьВременнойГрафикМаршрутаНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	#Если НЕ ВебКлиент Тогда
	Если Не РазрешитьАктивизациюСтроки Тогда
		Возврат;
	КонецЕсли;
	
	текСтрока 	= Элементы.Работы.ТекущиеДанные;
	текЭлемент 	= Элементы.Работы.ТекущийЭлемент;
	
	Если КартаСформирована Тогда
		Если текЭлемент.Имя = "РаботыПометка" Тогда
			
			ПересчитатьВремя = Истина;
			ПерерисоватьДиаграмму = Ложь;
			Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграмма" Тогда
				ПерерисоватьДиаграмму = Истина;
			КонецЕсли;
			текСтрока.Пометка = НЕ текСтрока.Пометка;
			спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
			
 			// серверные вызовы
			ИзменитьДанныеФормыНаСервере(спкДействий, ПересчитатьВремя, ПерерисоватьДиаграмму, Истина, Истина);
	
			// клиентские вызовы
			Если спкДействий.Количество() Тогда
				ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
			КонецЕсли;
			ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
			
		КонецЕсли;
	КонецЕсли;
	
	Если текЭлемент.Имя = "РаботыПометка" Тогда
		// изменяем текущий элемент для возможности его повторной активизации
		Элементы.Работы.ТекущийЭлемент = Элементы.РаботыЗаказчик;
	КонецЕсли;
	#КонецЕсли
	
	ТекущиеДанныеРабота = Элементы.Работы.ТекущиеДанные;
	Если НЕ ТекущиеДанныеРабота = Неопределено Тогда
		
		Если Поле = Элементы.РаботыЗаказчик Тогда
			ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанныеРабота.Задание), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле = Элементы.РаботыВременноеОкноПредставление Тогда	
			ТекущиеДанныеМаршрут = Элементы.Маршрут.ТекущиеДанные;
			Если НЕ ТекущиеДанныеМаршрут = Неопределено Тогда
				
				Если ТекущиеДанныеМаршрут.Пройдена Тогда
					ТекстПредупреждения = Нстр("ru = 'Запрещено редактировать временное окно у пройденной точки'");
					ПоказатьПредупреждение(,ТекстПредупреждения);
				Иначе
					ИдентификаторДляИзмененияВременногоОкна = Элементы.Работы.ТекущаяСтрока;
					сткПараметры = Новый Структура("НачалоПериода, КонецПериода", ТекущиеДанныеРабота.ВременноеОкноНачало, ТекущиеДанныеРабота.ВременноеОкноОкончание);
					ОбработатьВводПроизвольногоПериода = Новый ОписаниеОповещения("ОбработатьВводПроизвольногоПериода", ЭтотОбъект);
					ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораПроизвольногоПериода", сткПараметры, Элемент, УникальныйИдентификатор,,,ОбработатьВводПроизвольногоПериода, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);						
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГруппаВерхПравоПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграмма" Тогда
		ОбновитьДанныеНаДиаграмме();
	ИначеЕсли Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграммаЗагрузки" Тогда
		ОбновитьДанныеНаДиаграммеЗагрузки();
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПересчитатьМаршрутПоВсемТочкам(Команда)
	
	ПересчитатьМаршрутНаКлиенте();
	
КонецПроцедуры // ПересчитатьМаршрутПоВсемТочкам()

&НаКлиенте
Процедура ПересчитатьМаршрутПоАктивнымТочкам(Команда)
	
	ПересчитатьМаршрутНаКлиенте(Истина);
	
КонецПроцедуры // ПересчитатьМаршрутПоВидимымТочкам()

&НаКлиенте
Процедура МасштабироватьКартуПоТочкамРейса(Команда)
	
	МасштабироватьКартуПоТочкамНаКлиенте(Ложь);
	
КонецПроцедуры // МасштабироватьКартуПоТочкамРейса()

&НаКлиенте
Процедура МасштабироватьКартуПоАктивнымТочкамРейса(Команда)
	
	 МасштабироватьКартуПоТочкамНаКлиенте();
	
КонецПроцедуры // МасштабироватьКартуПоАктивнымТочкамРейса()

&НаКлиенте
Процедура ПереместитьТочкуВверх(Команда)
	
	текСтрока = Элементы.Маршрут.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИндексСтроки = Маршрут.Индекс(текСтрока);
	Если ИндексСтроки = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если РежимКорректировки Тогда
		Если ИндексСтроки > 0 Тогда
			ПредыдущаяСтрока = Маршрут[ИндексСтроки - 1];
			Если ПредыдущаяСтрока.Пройдена Тогда
				СообщитьОНарушенииМаршрутаПопыткаСдвинутьПройденнуюТочку(ПредыдущаяСтрока.АдресПредставление);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	мсвПредшественников = СтрРазделить(текСтрока.Предшественники, ",", Ложь);
	ЗаменяемаяСтрока = Маршрут[ИндексСтроки - 1];
	Если мсвПредшественников.Найти(Строка(ЗаменяемаяСтрока.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ЗаменяемаяСтрока.АдресПредставление, текСтрока.АдресПредставление);
		Возврат;
	КонецЕсли; 
	
	спкДействий = Новый СписокЗначений;
	ПоменятьМестамиТочкиВМаршруте(спкДействий);
	
	Если спкДействий.Количество() Тогда
		ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
		ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
	КонецЕсли; 
	
КонецПроцедуры // ПереместитьТочкуВверх()

&НаКлиенте
Процедура ПереместитьТочкуВниз(Команда)
	
	текСтрока = Элементы.Маршрут.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИндексСтроки = Маршрут.Индекс(текСтрока);
	Если ИндексСтроки = Маршрут.Количество() - 1 Тогда
		Возврат;
	КонецЕсли; 
	
	Если РежимКорректировки Тогда
		Если текСтрока.Пройдена Тогда
			СообщитьОНарушенииМаршрутаПопыткаСдвинутьПройденнуюТочку(текСтрока.АдресПредставление);
				Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаменяемаяСтрока = Маршрут[ИндексСтроки + 1];
	мсвПредшественников = СтрРазделить(ЗаменяемаяСтрока.Предшественники, ",", Ложь);
	Если мсвПредшественников.Найти(Строка(текСтрока.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(текСтрока.АдресПредставление, ЗаменяемаяСтрока.АдресПредставление);
		Возврат;
	КонецЕсли; 	
	
	спкДействий = Новый СписокЗначений;
	ПоменятьМестамиТочкиВМаршруте(спкДействий, Ложь);
	
	Если спкДействий.Количество() Тогда
		ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
		ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
	КонецЕсли; 
	
КонецПроцедуры // ПереместитьТочкуВниз()

&НаКлиенте
Процедура ИзменитьВремяНачалаРейса(Команда)
	
	ТекстПодсказки = НСтр("ru = 'Введите планируемое начало рейса'");
	ПоказатьВводДаты(Новый ОписаниеОповещения("ИзменениеВремениНачалаРейсаЗавершение", ЭтаФорма), Маршрут[0].ПлановоеПрибытие, ТекстПодсказки); 
	
КонецПроцедуры // ИзменитьВремяНачалаРейса()

&НаКлиенте
Процедура РедактироватьПосещениеГаража(Команда)

	сткПараметры = Новый Структура();
	сткПараметры.Вставить("НачалоМаршрута", Неопределено);
	сткПараметры.Вставить("ОкончаниеМаршрута", Неопределено);
	
	ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("ТипТочки", ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")));
	Если ИскомыеСтроки.Количество() Тогда
		сткПараметры.НачалоМаршрута = ИскомыеСтроки[0].Гараж;
	КонецЕсли; 
	
	ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("ТипТочки", ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения")));
	Если ИскомыеСтроки.Количество() Тогда
		сткПараметры.ОкончаниеМаршрута = ИскомыеСтроки[0].Гараж;
	КонецЕсли; 

	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаРедактированияПосещенияГаражаВМаршруте", сткПараметры,,,,, Новый ОписаниеОповещения("РедактированиеПосещенияГаражаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаПересчитатьВременнойГрафикМаршрута(Команда)
	
	Если Маршрут.Количество() Тогда
		Если РежимКорректировки Тогда
			ИдентификаторСтроки = Неопределено;
			Для каждого СтрокаМаршрут Из Маршрут Цикл
				Если НЕ СтрокаМаршрут.Пройдена Тогда
					ИдентификаторСтроки	= СтрокаМаршрут.ПолучитьИдентификатор();	
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если НЕ ИдентификаторСтроки = Неопределено Тогда
				Элементы.Маршрут.ТекущаяСтрока = ИдентификаторСтроки;	
			Иначе
				Возврат;
			КонецЕсли;
		Иначе	
			Элементы.Маршрут.ТекущаяСтрока = Маршрут[0].ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыМаршрутизацииСитиГИД = Неопределено Тогда
		ПересчитатьВременнойГрафикМаршрутаНаКлиенте();
	Иначе
		сткПараметры = Новый Структура("ПараметрыОткрытия",ПараметрыМаршрутизацииСитиГИД);	
		ОткрытьФорму("ОбщаяФорма.упУстановкаПараметровМаршрутизацииСитиГИД", сткПараметры,,,,, Новый ОписаниеОповещения("ИзменениеПараметровМаршрутизацииСитиГИДЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМаршрут(Команда)
	
	ОбновитьМаршрутРаботыНаСервере();
	АктуализироватьРасстоянияМеждуАдресамиНаСервере();
	ОчиститьОбъекты();
	ОтобразитьОбъектыНаКарте();
	ОтобразитьМаршрут();
	ОбновитьДиаграммуНаКлиенте();
	ОбновитьПредставлениеПоказателейНаФорме(Истина, Истина);
	
КонецПроцедуры // ОбновитьМаршрут()

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если ВерсияДанных <> ПолучитьВерсиюРейса(Рейс) Тогда
		ТекстСообщения = НСтр("ru = 'Изменение рейса не может быть выполнено из-за несоответствия версии или отсутствии записи базы данных (возможно, запись была изменена или удалена)!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;	
	КонецЕсли; 
	
	ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если ИскомыеСтроки.Количество() < 2 Тогда
		ТекстВопроса = НСтр("ru = 'Не выбрано ни одной активной точки доставки. Рейс будет отменен. Продолжить?'", КодЯзыка);
		Обработчик = Новый ОписаниеОповещения("ЗавершениеРедактированияРейсаВопросОбОтмене", ЭтаФорма);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		Если РежимКорректировки Тогда
			ПараметрыФормы = Новый Структура();	
			ПараметрыФормы.Вставить("Основание", Рейс);
			ПараметрыФормы.Вставить("сткНовыйМаршрутРаботы", НовыйМаршрутИРаботыПоРейсу());
			ПараметрыФормы.Вставить("ОптимизацияМаршрута", Истина);
			ПараметрыФормы.Вставить("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек);
			ОткрытьФорму("Документ.упКорректировкаРейса.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,);	
		Иначе	
			ИзменитьРейсНаСервере();	
		КонецЕсли;
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры // ЗавершитьРедактирование()

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры // Отмена()

&НаКлиенте
Процедура ОпределитьВременноеОкноДляТочки(Команда)
	
	ТекущиеДанныеМаршрут = Элементы.Маршрут.ТекущиеДанные;
	Если НЕ ТекущиеДанныеМаршрут = Неопределено 
		И НЕ ТекущиеДанныеМаршрут.Пройдена Тогда
			
		ИдентификаторТочки 	 = ТекущиеДанныеМаршрут.Идентификатор;
		мсвСтрокиРабот		 = Работы.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТочки));
		
		ТекущиеДанныеМаршрут.ВременноеОкноНачало =  '00010101';
		ТекущиеДанныеМаршрут.ВременноеОкноОкончание =  '00010101';
		
		Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
			
			Если ТекущиеДанныеМаршрут.ВременноеОкноНачало = '00010101' 
				Или ТекущиеДанныеМаршрут.ВременноеОкноНачало > СтрокаРабота.ВременноеОкноНачало Тогда
				ТекущиеДанныеМаршрут.ВременноеОкноНачало = СтрокаРабота.ВременноеОкноНачало;
			КонецЕсли;
			
			Если ТекущиеДанныеМаршрут.ВременноеОкноОкончание = '00010101' 
				Или ТекущиеДанныеМаршрут.ВременноеОкноОкончание < СтрокаРабота.ВременноеОкноОкончание Тогда
				ТекущиеДанныеМаршрут.ВременноеОкноОкончание = СтрокаРабота.ВременноеОкноОкончание;
			КонецЕсли;
			
		КонецЦикла;
		
		ТекущиеДанныеМаршрут.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(ТекущиеДанныеМаршрут.ВременноеОкноНачало, ТекущиеДанныеМаршрут.ВременноеОкноОкончание);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВернутьИсходноеЗначениеВременногоОкна(Команда)
	ТекущиеДанныеРабота = Элементы.Работы.ТекущиеДанные;
	ТекущиеДанныеМаршрут = Элементы.Маршрут.ТекущиеДанные;
	Если НЕ ТекущиеДанныеРабота = Неопределено 
		И НЕ ТекущиеДанныеМаршрут.Пройдена Тогда
		ТекущиеДанныеРабота.ВременноеОкноНачало 		= ТекущиеДанныеРабота.ИсходноеВременноеОкноНачало;
		ТекущиеДанныеРабота.ВременноеОкноОкончание 		= ТекущиеДанныеРабота.ИсходноеВременноеОкноОкончание;	
		ТекущиеДанныеРабота.ВременноеОкноПредставление 	= ТекущиеДанныеРабота.ИсходноеВременноеОкноПредставление;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВернутьИсходноеЗначениеВременногоОкнаМаршрута(Команда)
	ТекущиеДанныеМаршрут = Элементы.Маршрут.ТекущиеДанные;
	Если НЕ ТекущиеДанныеМаршрут = Неопределено И НЕ ТекущиеДанныеМаршрут.Пройдена Тогда
		ТекущиеДанныеМаршрут.ВременноеОкноНачало 			= ТекущиеДанныеМаршрут.ИсходноеВременноеОкноНачало;
		ТекущиеДанныеМаршрут.ВременноеОкноОкончание 		= ТекущиеДанныеМаршрут.ИсходноеВременноеОкноОкончание;	
		ТекущиеДанныеМаршрут.ВременноеОкноПредставление 	= ТекущиеДанныеМаршрут.ИсходноеВременноеОкноПредставление;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредшественников(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Маршрут", 			Маршрут);
	сткПараметры.Вставить("ИдентификаторТочки", Элементы.Маршрут.ТекущиеДанные.Идентификатор);
	ОткрытьФорму("Обработка.упКорректировкаРейса.Форма.Предшественники", сткПараметры);
КонецПроцедуры

&НаКлиенте
Процедура РазделитьРаботы(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Идентификатор", 		Элементы.Маршрут.ТекущиеДанные.Идентификатор);
	сткПараметры.Вставить("РедактированиеМаршрута", 	Истина);
	сткПараметры.Вставить("Работы", 	Работы);
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ОбработатьРазделениеТочки", ЭтаФорма);
	
	ОткрытьФорму("Обработка.упКорректировкаРейса.Форма.РазделениеРаботВТочке", сткПараметры, ЭтаФорма, УникальныйИдентификатор, , , ОбработчикЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьВерсиюРейса(Рейс)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВерсияДанных");
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьВременнойГрафикМаршрутаНаКлиенте(ПринудительноВызыватьМаршрутизацию = Ложь)
	
	АктуализироватьРасстояния = Ложь;
	ПересчитатьВремя = Истина;
	ПерерисоватьДиаграмму = Ложь;
	Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграмма" Тогда
		ПерерисоватьДиаграмму = Истина;
	КонецЕсли;
	спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
	
	// серверные вызовы
	ИзменитьДанныеФормыНаСервере(спкДействий, ПересчитатьВремя, ПерерисоватьДиаграмму,,, АктуализироватьРасстояния, ПринудительноВызыватьМаршрутизацию);
	
	// клиентские вызовы
	Если АктуализироватьРасстояния Тогда
		ОчиститьОбъекты();
		ОтобразитьОбъектыНаКарте();
		ОтобразитьМаршрут();
	Иначе
		Если спкДействий.Количество() Тогда
			ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
		КонецЕсли;
	КонецЕсли; 

	ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСписокДоступныхПоказателейКонтроля()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьКоличествоЗагрузочныхМетров);
	
	Если УчитыватьМассу Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(0, "Масса");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 0;
		НоваяСтрока.Представление = сткПредставления.Масса;
	КонецЕсли; 
	Если УчитыватьОбъем Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(1, "Объем");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 1;
		НоваяСтрока.Представление = сткПредставления.Объем;
	КонецЕсли; 
	Если УчитыватьКоличествоМест Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(2, "Место");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 2;
		НоваяСтрока.Представление = сткПредставления.КоличествоМест;
	КонецЕсли; 
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		Элементы.ГрузоваяЕдиница.СписокВыбора.Добавить(3, "Загрузочный метр");
		НоваяСтрока = ПредставлениеПоказателей.Добавить();
		НоваяСтрока.Показатель = 3;
		НоваяСтрока.Представление = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
КонецПроцедуры // УстановитьСписокДоступныхПоказателейКонтроля()

&НаКлиенте
Процедура ОбновитьПредставлениеПоказателейНаФорме(ОбновитьИсходные, ОбновитьТекущие)
	
	Если ОбновитьИсходные Тогда
		ЗначениеРасстояниеИсходное = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(РасстояниеИсходное);
		ЗначениеВремяРейсаИсходное = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяРейсаИсходное); 
		ЗначениеВремяВПутиИсходное = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяВПутиИсходное); 
		ЗначениеВремяОтдыхаВПутиИсходное = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяОтдыхаВПутиИсходное); 
		Если УчитыватьМассу Тогда
			ЗначениеМассаИсходная = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", МассаИсходная, ЕдМассы, ?(ГрузоподъемностьТС > 0, Окр(МассаИсходная/ГрузоподъемностьТС * 100), "???")), КодЯзыка); 
		КонецЕсли; 
		Если УчитыватьОбъем Тогда
			ЗначениеОбъемИсходный = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", ОбъемИсходный, ЕдОбъема, ?(ОбъемТС > 0, Окр(ОбъемИсходный/ОбъемТС * 100), "???")), КодЯзыка); 
		КонецЕсли; 
		Если УчитыватьКоличествоМест Тогда
			ЗначениеКоличествоМестИсходное = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", КоличествоМестИсходное, ЕдКоличестваМест,
			?(КоличествоМестТС > 0, Окр(КоличествоМестИсходное/КоличествоМестТС * 100), "???")), КодЯзыка); 
		КонецЕсли;
		Если УчитыватьКоличествоЗагрузочныхМетров Тогда
			ЗначениеКоличествоЗагрузочныхМетровИсходное = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", КоличествоЗагрузочныхМетровИсходное, ЕдКоличестваЗагрузочныхМетров,
			?(КоличествоЗагрузочныхМетровИсходное > 0, Окр(КоличествоЗагрузочныхМетровИсходное/КоличествоЗагрузочныхМетровТС * 100), "???")), КодЯзыка); 
		КонецЕсли;
		
		ЗначениеКоличествоАктивныхТочекИсходное = НСтр(СтрШаблон("ru = '%1'", КоличествоАктивныхТочекИсходное), КодЯзыка); 
	КонецЕсли;
	
	Если ОбновитьТекущие Тогда
		ЗначениеРасстояниеТекущее = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(РасстояниеТекущее);
		ИзменитьЦветНадписи("РасстояниеИсходное", "РасстояниеТекущее");
		ЗначениеВремяРейсаТекущее = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяРейсаТекущее);
		ИзменитьЦветНадписи("ВремяРейсаИсходное", "ВремяРейсаТекущее");
		ЗначениеВремяВПутиТекущее = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяВПутиТекущее); 
		ЗначениеВремяОтдыхаВПутиТекущее = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяОтдыхаВПутиТекущее); 
		ИзменитьЦветНадписи("ВремяОтдыхаВПутиИсходное", "ВремяОтдыхаВПутиТекущее");
		ИзменитьЦветНадписи("ВремяВПутиИсходное", "ВремяВПутиТекущее");
		
		Если УчитыватьМассу Тогда
			ЗначениеМассаТекущая = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", МассаТекущая, ЕдМассы, ?(ГрузоподъемностьТС > 0, Окр(МассаТекущая/ГрузоподъемностьТС * 100), "???")), КодЯзыка); 
			ИзменитьЦветНадписи("МассаИсходная", "МассаТекущая");
		КонецЕсли; 
		Если УчитыватьОбъем Тогда
			ЗначениеОбъемТекущий = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", ОбъемТекущий, ЕдОбъема, ?(ОбъемТС > 0, Окр(ОбъемТекущий/ОбъемТС * 100), "???")), КодЯзыка); 
			ИзменитьЦветНадписи("ОбъемИсходный", "ОбъемТекущий");
		КонецЕсли; 
		Если УчитыватьКоличествоМест Тогда
		    ЗначениеКоличествоМестТекущее = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", КоличествоМестТекущее, ЕдКоличестваМест,
                                                                                  ?(КоличествоМестТС > 0, Окр(КоличествоМестТекущее/КоличествоМестТС * 100), "???")), КодЯзыка); 
			ИзменитьЦветНадписи("КоличествоМестИсходное", "КоличествоМестТекущее");																					   
		КонецЕсли;
		Если УчитыватьКоличествоЗагрузочныхМетров Тогда
			ЗначениеКоличествоЗагрузочныхМетровТекущее = НСтр(СтрШаблон("ru = '%1 %2 (%3%%)'", КоличествоЗагрузочныхМетровТекущее, ЕдКоличестваЗагрузочныхМетров,
			                                                                                   ?(КоличествоЗагрузочныхМетровТекущее > 0, Окр(КоличествоЗагрузочныхМетровТекущее/КоличествоЗагрузочныхМетровТС * 100), "???")), КодЯзыка); 
			ИзменитьЦветНадписи("КоличествоЗагрузочныхМетровИсходное", "КоличествоЗагрузочныхМетровТекущее");
		КонецЕсли;
		ЗначениеКоличествоАктивныхТочекТекущее = НСтр(СтрШаблон("ru = '%1'", КоличествоАктивныхТочекТекущее), КодЯзыка);
	КонецЕсли;

КонецПроцедуры // ОбновитьПредставлениеПоказателейНаФорме();

&НаКлиенте
Процедура СообщитьОНарушенииМаршрута(Предшественник, Потомок)
	
	ТекстСообщения = СтрШаблон("ru = 'Ошибка изменения маршрута! Точка с адресом ""%1"" должна предшествовать точке с адресом ""%2""'", Предшественник, Потомок);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстСообщения));
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОНарушенииМаршрутаПопыткаСдвинутьПройденнуюТочку(Адрес)
	
	ТекстСообщения = СтрШаблон("ru = 'Ошибка изменения маршрута! Точка с адресом ""%1"" пройдена, её сдвигать нельзя'", Адрес);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстСообщения));
	
КонецПроцедуры


&НаСервере
Функция ПорядокСУчетомПредшественниковСоблюден()
	
	тзМаршрут = Маршрут.Выгрузить(Новый Структура("Пометка", Истина), "АдресПредставление, НомерСтроки, Предшественники, Идентификатор");
	тзМаршрут.Сортировать("НомерСтроки");
	тзМаршрут.Колонки.Добавить("ИдентификаторСтрока");
	тзМаршрут.Колонки.Добавить("МассивПредшественников");
	Для каждого текСтрока Из тзМаршрут Цикл
		текСтрока.ИдентификаторСтрока = Строка(текСтрока.Идентификатор);
		текСтрока.МассивПредшественников = СтрРазделить(текСтрока.Предшественники, ",", Ложь);
	КонецЦикла; 
	
	ПредыдущаяТочка = Неопределено;
	Для Сч = 0 По тзМаршрут.Количество() - 1 Цикл
		
		текТочка = тзМаршрут[Сч];
		мсвПредшественников = текТочка.МассивПредшественников;
		
		Если мсвПредшественников.Количество() = 0 Или (мсвПредшественников.Количество() = 1 И мсвПредшественников[0] = ПредыдущаяТочка) Тогда
			Для каждого текЭлемент Из тзМаршрут Цикл
				ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(текЭлемент.МассивПредшественников, текТочка.ИдентификаторСтрока);
			КонецЦикла; 
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
		ПредыдущаяТочка = текТочка.ИдентификаторСтрока;
	КонецЦикла; 
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ПересчитатьВременнойГрафикМаршрута(ТекущаяСтрока = Неопределено, СписокДействий = Неопределено, АктуализироватьРасстояния = Ложь, ПринудительноВызыватьМаршрутизацию = Ложь, ПересчетСоВторойТочки = Ложь, ДополнительныеПараметры = Неопределено)
	
	Если Маршрут.Количество() Тогда
		
		Если ПересчетСоВторойТочки Тогда
			ИндексТекущейСтроки = 1;
		Иначе
			ИндексТекущейСтроки = 0;
		КонецЕсли; 
		ПерваяСтрока = Маршрут[0];
		ПерваяСтрока.РасстояниеОтПредыдущейТочки = 0;
		ПерваяСтрока.ВремяОтПредыдущейТочки = 0;
		
		Если РежимКорректировки Тогда
			тзМаршрут = Маршрут.Выгрузить(Новый Структура("Пометка, Пройдена", Истина, Ложь));
		Иначе
			тзМаршрут = Маршрут.Выгрузить(Новый Структура("Пометка", Истина));
		КонецЕсли;
		
		тзРаботы = Работы.Выгрузить(Новый Структура("Пометка", Истина));
		
		КоличествоАктивныхТочекТекущее = тзМаршрут.Количество();
		
		Если ТекущаяСтрока <> Неопределено Тогда
			ИзменяемаяСтрока = Маршрут.НайтиПоИдентификатору(ТекущаяСтрока);   
			Если ИзменяемаяСтрока.Пометка Тогда
				ИндексТекущейСтроки = тзМаршрут.Индекс(тзМаршрут.Найти(ИзменяемаяСтрока.НомерСтроки, "НомерСтроки"));
			КонецЕсли;
		КонецЕсли;
		
		текДополнительныеПараметры = Новый Структура;
		Если ПринудительноВызыватьМаршрутизацию Тогда
			текДополнительныеПараметры.Вставить("ПараметрыМаршрутизацииСитиГИД",ПараметрыМаршрутизацииСитиГИД);		
		КонецЕсли; 
		ДатаНачалаПланирования = тзМаршрут[0].ПлановоеПрибытие;
		Если ЗначениеЗаполнено(ДатаНачалаПланирования)Тогда
			текДополнительныеПараметры.Вставить("ДатаНачалаПланирования",ДатаНачалаПланирования);		
		КонецЕсли;
		ВызыватьМаршрутизациюПараметр = (ПринудительноВызыватьМаршрутизацию Или ВызыватьМаршрутизацию); 
		ПересчитыватьПредыдущиеТочки = Истина;
		ПересчитыватьСледующиеТочки = Истина;
		Если ДополнительныеПараметры <> Неопределено Тогда
			ИндексТекущейСтроки = ?(ИндексТекущейСтроки > ДополнительныеПараметры.ИндексТочкиПересчета, ИндексТекущейСтроки, ДополнительныеПараметры.ИндексТочкиПересчета);
			ПересчитыватьПредыдущиеТочки = ДополнительныеПараметры.ПересчитыватьПредыдущиеТочки;
			ПересчитыватьСледующиеТочки = ДополнительныеПараметры.ПересчитыватьСледующиеТочки;
		КонецЕсли;
		
		упЗаданиеНаПеревозкуВызовСервера.СпланироватьЗадачиПоМаршруту(тзМаршрут,тзРаботы,ИндексТекущейСтроки,,1,ВызыватьМаршрутизациюПараметр,текДополнительныеПараметры,ПересчитыватьПредыдущиеТочки,,ПересчитыватьСледующиеТочки,,,,Рейс.ВидПеревозки,КоэффициентРасчетаВремениВПути,, Рейс);
	
		Для каждого текТочка Из тзМаршрут Цикл
			ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", текТочка.Идентификатор));
			НайденнаяСтрока = ИскомыеСтроки[0];
			ЗаполнитьЗначенияСвойств(НайденнаяСтрока, текТочка);
		КонецЦикла;
		Для каждого текРабота Из тзРаботы Цикл
			ИскомыеСтроки = Работы.НайтиСтроки(Новый Структура("ИдентификаторРаботы", текРабота.ИдентификаторРаботы));
			ЗаполнитьЗначенияСвойств(ИскомыеСтроки[0], текРабота);
		КонецЦикла;
		//Работы.Сортировать("СтрокаНомер");
		
		мсвПройденныхТочек = Новый Массив;
		КорректировкаРейса = Обработки.упКорректировкаРейса.Создать();
		Для каждого текТочка Из Маршрут Цикл	
			Если РежимКорректировки
					И текТочка.Пройдена Тогда
				мсвПройденныхТочек.Добавить(текТочка.Идентификатор);
				Продолжить;
			КонецЕсли;
			ПопадаетВоВременноеОкно = текТочка.ПопадаетВоВременноеОкно;
			Если Не ЗначениеЗаполнено(текТочка.ВременноеОкноНачало) И Не ЗначениеЗаполнено(текТочка.ВременноеОкноОкончание) Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текТочка.ВременноеОкноНачало) И текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текТочка.ВременноеОкноОкончание) И текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли текТочка.ПлановоеУбытие - 60 <= текТочка.ВременноеОкноОкончание И текТочка.ПлановоеПрибытие + (текТочка.ВремяОжиданияВТочке*3600) + 60 >= текТочка.ВременноеОкноНачало Тогда
				текТочка.ПопадаетВоВременноеОкно = Истина;
			Иначе
				текТочка.ПопадаетВоВременноеОкно = Ложь;	
			КонецЕсли;
			Если текТочка.ПопадаетВоВременноеОкно <> ПопадаетВоВременноеОкно Тогда
				Если СписокДействий <> Неопределено И текТочка <> ИзменяемаяСтрока Тогда
					сткИзменений = Новый Структура;
					сткИзменений.Вставить("ИмяПроцедуры", "ИзменитьИконкуМаркера"); 
					сткИзменений.Вставить("ИндексВМассиве", текТочка.ИндексВМассиве); 
					сткИзменений.Вставить("Данные", ("""" + Строка(текТочка.НомерСтроки) + """," + ?(текТочка.ПопадаетВоВременноеОкно, ДанныеСтандартнойТочки, ДанныеНарушенийПоВремени))); 
					сткИзменений.Вставить("Режим", 1); 
					СписокДействий.Добавить(сткИзменений);
				КонецЕсли;
			КонецЕсли;
			// Значение времени приходит в часах, его нужно перевести в секунды.
			//текТочка.ВремяОжиданияВТочке = текТочка.ВремяОжиданияВТочке*3600;
			//текТочка.ВремяОжиданияВТочкеПослеРабот = текТочка.ВремяОжиданияВТочкеПослеРабот*3600;
			//текТочка.ВремяРабот = текТочка.ВремяРабот*3600;
			//текТочка.ВремяОтПредыдущейТочки = текТочка.ВремяОтПредыдущейТочки*3600;
			//текТочка.ВремяОтдыхаВПутиОтПредыдущейТочки = текТочка.ВремяОтдыхаВПутиОтПредыдущейТочки*3600;
			ЗаполнитьПоляПредставленийСтрокиМаршрута(текТочка);
		КонецЦикла; 
		
		Для каждого текРабота Из Работы Цикл
			Если РежимКорректировки
					И НЕ мсвПройденныхТочек.Найти(текРабота.Идентификатор) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(текРабота.ВременноеОкноНачало) И Не ЗначениеЗаполнено(текРабота.ВременноеОкноОкончание) Тогда
				текРабота.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текРабота.ВременноеОкноНачало) И (текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание Тогда
				текРабота.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(текРабота.ВременноеОкноОкончание) И текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало Тогда
				текРабота.ПопадаетВоВременноеОкно = Истина;
			ИначеЕсли текРабота.ПлановоеВремяНачалаРабот >= текРабота.ВременноеОкноНачало И (текРабота.ПлановоеВремяНачалаРабот + текРабота.ВремяРабот - 60) <= текРабота.ВременноеОкноОкончание Тогда	
				текРабота.ПопадаетВоВременноеОкно = Истина;
			Иначе
				текРабота.ПопадаетВоВременноеОкно = Ложь;
			КонецЕсли;
			// Значение времени приходит в часах, его нужно перевести в секунды.
			//текРабота.ВремяОжидания=текРабота.ВремяОжидания*3600;
			//текРабота.ВремяРабот=текРабота.ВремяРабот*3600;
			ЗаполнитьПоляПредставленийСтрокиРабот(текРабота);
		КонецЦикла; 
		
	КонецЕсли;

	//Если АктуализироватьРасстояния Тогда
	//	АктуализироватьРасстоянияМеждуАдресамиНаСервере();
	//КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляПредставленийСтрокиРабот(Знач текРабота)
	
	текРабота.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(текРабота.ВременноеОкноНачало, текРабота.ВременноеОкноОкончание);
	текРабота.ВремяОжиданияПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текРабота.ВремяОжидания);
	текРабота.ВремяРаботПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текРабота.ВремяРабот);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляПредставленийСтрокиМаршрута(Знач текТочка)
		
	текТочка.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(текТочка.ВременноеОкноНачало, текТочка.ВременноеОкноОкончание);
	текТочка.ВремяОжиданияВТочкеПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяОжиданияВТочке);
	текТочка.ВремяОжиданияВТочкеПослеРаботПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяОжиданияВТочкеПослеРабот);
	текТочка.ВремяРаботПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяРабот);
	текТочка.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(текТочка.РасстояниеОтПредыдущейТочки);
	текТочка.ВремяОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяОтПредыдущейТочки);
	текТочка.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяОтдыхаВПутиОтПредыдущейТочки);

КонецПроцедуры // ПересчитатьВременнойГрафикМаршрута()

&НаКлиенте
Процедура ИзменитьЦветНадписи(ИмяИсходногоПоказателя, ИмяТекущегоПоказателя)
	
	Если ЭтаФорма[ИмяИсходногоПоказателя] > ЭтаФорма[ИмяТекущегоПоказателя] Тогда
		Элементы["Надпись" + ИмяТекущегоПоказателя].ЦветТекста  = ЦветУлучшениеПоказания;
		Элементы["Значение" + ИмяТекущегоПоказателя].ЦветТекста = ЦветУлучшениеПоказания;
	ИначеЕсли ЭтаФорма[ИмяИсходногоПоказателя] < ЭтаФорма[ИмяТекущегоПоказателя] Тогда
		Элементы["Надпись" + ИмяТекущегоПоказателя].ЦветТекста  = ЦветУхудшениеПоказания;
		Элементы["Значение" + ИмяТекущегоПоказателя].ЦветТекста = ЦветУхудшениеПоказания;
	Иначе
		Элементы["Надпись" + ИмяТекущегоПоказателя].ЦветТекста  = Новый Цвет;
		Элементы["Значение" + ИмяТекущегоПоказателя].ЦветТекста = новый Цвет;
	КонецЕсли;
	
КонецПроцедуры // ИзменитьЦветНадписи()

&НаСервере
Процедура ИзменитьТекущиеПоказателиЗагрузки(ТекущаяСтрока)
	
	Если УчитыватьМассу Тогда
		МассаТекущая = МассаТекущая + ?(ТекущаяСтрока.Пометка, ТекущаяСтрока.Масса, -ТекущаяСтрока.Масса);
	КонецЕсли; 
	Если УчитыватьОбъем Тогда
		ОбъемТекущий = ОбъемТекущий + ?(ТекущаяСтрока.Пометка, ТекущаяСтрока.Объем, -ТекущаяСтрока.Объем);
	КонецЕсли; 
	Если УчитыватьКоличествоМест Тогда
		КоличествоМестТекущее = КоличествоМестТекущее + ?(ТекущаяСтрока.Пометка, ТекущаяСтрока.КоличествоМест, -ТекущаяСтрока.КоличествоМест);
	КонецЕсли;
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетровТекущее = КоличествоЗагрузочныхМетровТекущее + ?(ТекущаяСтрока.Пометка, ТекущаяСтрока.КоличествоЗагрузочныхМетров, -ТекущаяСтрока.КоличествоЗагрузочныхМетров);
	КонецЕсли;
	
КонецПроцедуры // ИзменитьТекущиеПоказателиЗагрузки()

&НаСервере
Процедура ИзменитьТекущиеПоказателиРейса(ТекущаяСтрока, Множитель)
	
	РасстояниеТекущее = РасстояниеТекущее + Множитель*ТекущаяСтрока.Расстояние;
	ВремяРейсаТекущее = ВремяРейсаТекущее + Множитель*(ТекущаяСтрока[ВремяДеньНедели]);
	ВремяВПутиТекущее = ВремяВПутиТекущее + Множитель*(ТекущаяСтрока[ВремяДеньНедели]);
	
КонецПроцедуры // ИзменитьТекущиеПоказателиРейса()

&НаСервере
Процедура УстановитьПредставлениеЕдиницИзмеренияПоказателей()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьКоличествоЗагрузочныхМетров);
	
	Если УчитыватьМассу Тогда
		ЕдМассы = сткПредставления.Масса;
	Иначе
		Элементы.НадписьМассаИсходная.Видимость = Ложь;
		Элементы.ЗначениеМассаИсходная.Видимость = Ложь;
		Элементы.НадписьМассаТекущая.Видимость = Ложь;
		Элементы.ЗначениеМассаТекущая.Видимость = Ложь;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕдОбъема = сткПредставления.Объем;
	Иначе
		Элементы.НадписьОбъемИсходный.Видимость = Ложь;
		Элементы.ЗначениеОбъемИсходный.Видимость = Ложь;
		Элементы.НадписьОбъемТекущий.Видимость = Ложь;
		Элементы.ЗначениеОбъемТекущий.Видимость = Ложь;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда
		ЕдКоличестваМест = сткПредставления.КоличествоМест;
	Иначе
		Элементы.НадписьКоличествоМестИсходное.Видимость = Ложь;
		Элементы.ЗначениеКоличествоМестИсходное.Видимость = Ложь;
		Элементы.НадписьКоличествоМестТекущее.Видимость = Ложь;
		Элементы.ЗначениеКоличествоМестТекущее.Видимость = Ложь;
	КонецЕсли;
	
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		ЕдКоличестваЗагрузочныхМетров = сткПредставления.КоличествоЗагрузочныхМетров;
	Иначе
		Элементы.НадписьКоличествоЗагрузочныхМетровИсходное.Видимость = Ложь;
		Элементы.ЗначениеКоличествоЗагрузочныхМетровИсходное.Видимость = Ложь;
		Элементы.НадписьКоличествоЗагрузочныхМетровТекущее.Видимость = Ложь;
		Элементы.ЗначениеКоличествоЗагрузочныхМетровТекущее.Видимость = Ложь;
	КонецЕсли;
	
	Если Не УчитыватьРежимТрудаИОтдыхаСотрудников Тогда
	    Элементы.НадписьВремяОтдыхаВПутиИсходное.Видимость = Ложь;
		Элементы.НадписьВремяОтдыхаВПутиТекущее.Видимость = Ложь;
		Элементы.ЗначениеВремяОтдыхаВПутиИсходное.Видимость = Ложь;
		Элементы.ЗначениеВремяОтдыхаВПутиТекущее.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры // УстановитьПредставлениеЕдиницИзмеренияПоказателей()

&НаКлиенте
Процедура ВыполнитьДействияПоИзменениюОтображенияКарты(СписокДействий)
	
	сткДанныеЛинии = ПолучитьХарактеристикиЛинии();
	
	Для каждого текДействие Из СписокДействий Цикл
		сткПроцедуры = текДействие.Значение;
		Если сткПроцедуры.ИмяПроцедуры = "ИзменитьИконкуМаркера" Тогда
			ИзменитьИконкуМаркера(сткПроцедуры.ИндексВМассиве, сткПроцедуры.Данные, сткПроцедуры.Режим);
			
		ИначеЕсли сткПроцедуры.ИмяПроцедуры = "ИзменитьВидимостьОбъекта" Тогда
			ИзменитьВидимостьОбъекта(сткПроцедуры.ИндексВМассиве, сткПроцедуры.Пометка);
			
		ИначеЕсли сткПроцедуры.ИмяПроцедуры = "ОтобразитьЛинию" Тогда
			НайденнаяСтрока = Расстояния.НайтиПоИдентификатору(сткПроцедуры.Идентификатор);
			НайденнаяСтрока.ИндексВМассиве = ОтобразитьОбъект(,сткДанныеЛинии[сткПроцедуры.Данные], сткПроцедуры.Путь, сткПроцедуры.Режим);
			
		ИначеЕсли сткПроцедуры.ИмяПроцедуры = "ОтобразитьТочку" Тогда
			НайденнаяСтрока = Маршрут.НайтиПоИдентификатору(сткПроцедуры.Идентификатор);
			НайденнаяСтрока.ИндексВМассиве = ОтобразитьОбъект(НайденнаяСтрока.АдресПредставление, сткПроцедуры.Данные, НайденнаяСтрока.Координаты, сткПроцедуры.Режим, Строка(НайденнаяСтрока.НомерСтроки));
		
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // ВыполнитьДействияПоИзменениюОтображенияКарты()

&НаСервере
Процедура ИзменитьДанныеФормыНаСервере(СписокДействий, ПересчитатьВремя = Ложь, ПерерисоватьДиаграмму = Ложь, ИзмененаРабота = Ложь, ИзмененаВидимость = Ложь, АктуализироватьРасстояния = Ложь, ПринудительноВызыватьМаршрутизацию = Ложь)
	
	Если ИзмененаРабота Тогда
		
		ТекущаяСтрока = Элементы.Работы.ТекущаяСтрока;
		текСтрока = Работы.НайтиПоИдентификатору(ТекущаяСтрока);
		
		ОбновитьСостояниеТочкиПоРаботеНаСервере(СписокДействий, текСтрока.Пометка, текСтрока.Идентификатор);
		//
		ИзменитьТекущиеПоказатели = Ложь;
		ТекущаяОперация = Неопределено;
		Если текСтрока.Операция = Перечисления.упТипыОпераций.Разгрузка Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.Погрузка;
			ИзменитьТекущиеПоказатели = Истина;
		ИначеЕсли текСтрока.Операция = Перечисления.упТипыОпераций.Погрузка Тогда
			ИзменитьТекущиеПоказателиЗагрузки(текСтрока);
			ТекущаяОперация = Перечисления.упТипыОпераций.Разгрузка;
		ИначеЕсли текСтрока.Операция = Перечисления.упТипыОпераций.ПередачаДокументов Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.ПолучениеДокументов;
		ИначеЕсли текСтрока.Операция = Перечисления.упТипыОпераций.ПолучениеДокументов Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.ПередачаДокументов;
		ИначеЕсли текСтрока.Операция = Перечисления.упТипыОпераций.ВыемкаЦенностей Тогда
			ИзменитьТекущиеПоказателиЗагрузки(текСтрока);
			ТекущаяОперация = Перечисления.упТипыОпераций.ПередачаЦенностей;
		ИначеЕсли текСтрока.Операция = Перечисления.упТипыОпераций.ПередачаЦенностей Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.ВыемкаЦенностей;
			ИзменитьТекущиеПоказатели = Истина;
		КонецЕсли;
		
		Если ТекущаяОперация <> Неопределено Тогда
			сткПоиска = Новый Структура("Задание, ГрузовоеМесто, Операция", текСтрока.Задание, текСтрока.ГрузовоеМесто, ТекущаяОперация);
			грузИскомыеСтроки = Работы.НайтиСтроки(сткПоиска);
			Для каждого грузНайденнаяСтрока Из грузИскомыеСтроки Цикл
				грузНайденнаяСтрока.Пометка = текСтрока.Пометка;
				Если ИзменитьТекущиеПоказатели Тогда
					ИзменитьТекущиеПоказателиЗагрузки(грузНайденнаяСтрока);
				КонецЕсли; 
				ОбновитьСостояниеТочкиПоРаботеНаСервере(СписокДействий, грузНайденнаяСтрока.Пометка, грузНайденнаяСтрока.Идентификатор);
			КонецЦикла;
		КонецЕсли;
		
		Если ПересчитатьВремя Тогда
			ПересчитатьВременнойГрафикМаршрута(, СписокДействий);
		КонецЕсли;
	Иначе
		ТекущаяСтрока = Элементы.Маршрут.ТекущаяСтрока;
		Если ИзмененаВидимость Тогда
			ОбновитьСостояниеРаботПоТочкеНаСервере(ТекущаяСтрока);
			ОбработатьИзменениеВидимостиТочкиМаршрутаНаСервере(СписокДействий, ТекущаяСтрока);
		КонецЕсли;
		Если ПересчитатьВремя Тогда
			ПересчитатьВременнойГрафикМаршрута(ТекущаяСтрока, СписокДействий, АктуализироватьРасстояния, ПринудительноВызыватьМаршрутизацию);
		КонецЕсли;
	КонецЕсли;
	
	Если ПерерисоватьДиаграмму Тогда
		ОбновитьДанныеНаДиаграмме();
	КонецЕсли;	
	
	ОбновитьТекущиеРасстояниеИВремяРейса();
	
КонецПроцедуры // ИзменитьДанныеФормыНаСервере()

&НаСервере
Процедура ОбновитьТекущиеРасстояниеИВремяРейса()
	
	тзМаршрут = Маршрут.Выгрузить(Новый Структура("Пометка", Истина), "РасстояниеОтПредыдущейТочки, ВремяОтПредыдущейТочки, ВремяРабот, ВремяОжиданияВТочке, ВремяОжиданияВТочкеПослеРабот, ВремяОтдыхаВПутиОтПредыдущейТочки");	
	РасстояниеТекущее = тзМаршрут.Итог("РасстояниеОтПредыдущейТочки");
	ВремяВПутиТекущее = тзМаршрут.Итог("ВремяОтПредыдущейТочки");
	ВремяОтдыхаВПутиТекущее = тзМаршрут.Итог("ВремяОтдыхаВПутиОтПредыдущейТочки");
	ВремяРейсаТекущее = ВремяВПутиТекущее + тзМаршрут.Итог("ВремяРабот") + тзМаршрут.Итог("ВремяОжиданияВТочке") + тзМаршрут.Итог("ВремяОжиданияВТочкеПослеРабот") + тзМаршрут.Итог("ВремяОтдыхаВПутиОтПредыдущейТочки");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеВидимостиТочкиМаршрутаНаСервере(СписокДействий, ТекущаяСтрока)
	
	Если ТипЗнч(ТекущаяСтрока) = Тип("Число") Тогда
	    текСтрока = Маршрут.НайтиПоИдентификатору(ТекущаяСтрока);
	Иначе
		текСтрока = ТекущаяСтрока;
	КонецЕсли;

	Пометка = текСтрока.Пометка;
	НомерТочки = текСтрока.НомерСтроки;
	
	сткИзменений = Новый Структура;
	сткИзменений.Вставить("ИмяПроцедуры", "ИзменитьИконкуМаркера"); 
	сткИзменений.Вставить("ИндексВМассиве", текСтрока.ИндексВМассиве); 
	Если Пометка Тогда
		Если текСтрока.ПопадаетВоВременноеОкно Тогда
			текДанные = ДанныеСтандартнойТочки;
		Иначе
			текДанные = ДанныеНарушенийПоВремени;
		КонецЕсли;
	Иначе
		текДанные = ДанныеИсключеннойТочки;
	КонецЕсли;
	
	сткИзменений.Вставить("Данные", ("""" + Строка(НомерТочки) + """," + текДанные));
	сткИзменений.Вставить("Режим", 1); 
	СписокДействий.Добавить(сткИзменений);
	
	ПредыдущаяТочка = Неопределено;
	СледующаяТочка = Неопределено;
	СледующаяСтрока = Неопределено;
	Для Сч = 1 По НомерТочки - 1 Цикл
		ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки, Пометка", НомерТочки - Сч, Истина));
		Если ИскомыеСтроки.Количество() Тогда
			ПредыдущаяТочка = ИскомыеСтроки[0].Адрес;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Сч = НомерТочки + 1 По Маршрут.Количество() Цикл
		ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки, Пометка", Сч, Истина));
		Если ИскомыеСтроки.Количество() Тогда
			СледующаяСтрока = ИскомыеСтроки[0];
			СледующаяТочка = СледующаяСтрока.Адрес;
			Прервать;
		КонецЕсли;
	КонецЦикла;	
	
	Если ПредыдущаяТочка <> Неопределено Тогда
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", ПредыдущаяТочка, текСтрока.Адрес));
		НайденнаяСтрока = ИскомыеСтроки[0];
		
		текСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
		текСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
		текСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(текСтрока.РасстояниеОтПредыдущейТочки);
		текСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текСтрока.ВремяОтПредыдущейТочки);
		текСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
		
		сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Пометка, НайденнаяСтрока);
		СписокДействий.Добавить(сткИзменений);
		ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, ?(Пометка, 1, -1)); 
	КонецЕсли;
	
	Если СледующаяТочка <> Неопределено Тогда
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", текСтрока.Адрес, СледующаяТочка));
		НайденнаяСтрока = ИскомыеСтроки[0];
		Если Пометка Тогда
			СледующаяСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
			СледующаяСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
			СледующаяСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(СледующаяСтрока.РасстояниеОтПредыдущейТочки);
			СледующаяСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(СледующаяСтрока.ВремяОтПредыдущейТочки);
			СледующаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(СледующаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
		КонецЕсли; 
		
		сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Пометка, НайденнаяСтрока);
		СписокДействий.Добавить(сткИзменений);
		ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, ?(Пометка, 1, -1));
	КонецЕсли;
	
	Если ПредыдущаяТочка <> Неопределено И СледующаяТочка <> Неопределено Тогда
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", ПредыдущаяТочка, СледующаяТочка));
		НайденнаяСтрока = ИскомыеСтроки[0];
		
		Если Не Пометка Тогда
			СледующаяСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
			СледующаяСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
			СледующаяСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(СледующаяСтрока.РасстояниеОтПредыдущейТочки);
			СледующаяСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(СледующаяСтрока.ВремяОтПредыдущейТочки);
			СледующаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(СледующаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
		КонецЕсли; 
		сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Не Пометка, НайденнаяСтрока);
		СписокДействий.Добавить(сткИзменений);
		ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, ?(Пометка, -1, 1));
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеВидимостиТочкиМаршрутаНаСервере()

&НаСервере
Функция СформироватьИзмененияДляКлиентскихВызовов(Пометка, Строка)
	
	сткИзменений = Новый Структура;
	Если Строка.ИндексВМассиве > 0 Тогда
		сткИзменений.Вставить("ИмяПроцедуры", "ИзменитьВидимостьОбъекта"); 
		сткИзменений.Вставить("ИндексВМассиве", Строка.ИндексВМассиве); 
		сткИзменений.Вставить("Пометка", Пометка);
	Иначе
		сткИзменений.Вставить("ИмяПроцедуры", "ОтобразитьЛинию"); 
		сткИзменений.Вставить("Идентификатор", Строка.ПолучитьИдентификатор()); 
		сткИзменений.Вставить("Данные", ?(Строка.РасстояниеРассчитано, "Рассчитанные", "Нерассчитанные")); 
		сткИзменений.Вставить("Путь", Строка.Путь); 
		сткИзменений.Вставить("Режим", 1);
	КонецЕсли;
	
	Возврат сткИзменений;
	
КонецФункции // СформироватьИзмененияДляКлиентскихВызовов()

&НаСервере
Процедура ОбновитьСостояниеРаботПоТочкеНаСервере(ТекущаяСтрока)
	
	текСтрока = Маршрут.НайтиПоИдентификатору(ТекущаяСтрока);
	ИДТочкиМаршрута = текСтрока.Идентификатор;
	Пометка = текСтрока.Пометка;
	мсвИдентификаторыИзмененныхТочек = Новый Массив;
	
	ИскомыеСтроки = Работы.НайтиСтроки(Новый Структура("Идентификатор", ИДТочкиМаршрута));
	Для каждого НайденнаяСтрока Из ИскомыеСтроки Цикл
		НайденнаяСтрока.Пометка = Пометка;
		
		ИзменитьТекущиеПоказатели = Ложь;
		ТекущаяОперация = Неопределено;
		Если НайденнаяСтрока.Операция = Перечисления.упТипыОпераций.Разгрузка Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.Погрузка;
			ИзменитьТекущиеПоказатели = Истина;
		ИначеЕсли НайденнаяСтрока.Операция =  Перечисления.упТипыОпераций.Погрузка Тогда
			ИзменитьТекущиеПоказателиЗагрузки(НайденнаяСтрока);
			ТекущаяОперация =  Перечисления.упТипыОпераций.Разгрузка;
		ИначеЕсли НайденнаяСтрока.Операция =  Перечисления.упТипыОпераций.ПередачаДокументов Тогда
			ТекущаяОперация =  Перечисления.упТипыОпераций.ПолучениеДокументов;
		ИначеЕсли НайденнаяСтрока.Операция = Перечисления.упТипыОпераций.ПолучениеДокументов Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.ПередачаДокументов;
		ИначеЕсли НайденнаяСтрока.Операция = Перечисления.упТипыОпераций.ВыемкаЦенностей Тогда
			ИзменитьТекущиеПоказателиЗагрузки(НайденнаяСтрока);
			ТекущаяОперация = Перечисления.упТипыОпераций.ПередачаЦенностей;
		ИначеЕсли НайденнаяСтрока.Операция = Перечисления.упТипыОпераций.ПередачаЦенностей Тогда
			ТекущаяОперация = Перечисления.упТипыОпераций.ВыемкаЦенностей;
			ИзменитьТекущиеПоказатели = Истина;
		КонецЕсли;
		
		Если ТекущаяОперация <> Неопределено Тогда
			сткПоиска = Новый Структура("Задание, ГрузовоеМесто, Операция", НайденнаяСтрока.Задание, НайденнаяСтрока.ГрузовоеМесто, ТекущаяОперация);
			грузИскомыеСтроки = Работы.НайтиСтроки(сткПоиска);
			Для каждого грузНайденнаяСтрока Из грузИскомыеСтроки Цикл
				Если мсвИдентификаторыИзмененныхТочек.Найти(грузНайденнаяСтрока.Идентификатор) = Неопределено Тогда
					мсвИдентификаторыИзмененныхТочек.Добавить(грузНайденнаяСтрока.Идентификатор);
				КонецЕсли;
				грузНайденнаяСтрока.Пометка = Пометка;
				Если ИзменитьТекущиеПоказатели Тогда
					ИзменитьТекущиеПоказателиЗагрузки(грузНайденнаяСтрока);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого ИдентификаторТочки Из мсвИдентификаторыИзмененныхТочек Цикл
		мсвСтроки = Работы.НайтиСтроки(Новый Структура("Пометка, Идентификатор", НЕ Пометка, ИдентификаторТочки));
		Если мсвСтроки.Количество() = 0 Тогда
			мсвСтрокиМаршрута = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИдентификаторТочки));
			Если мсвСтрокиМаршрута.Количество() > 0 Тогда
				мсвСтрокиМаршрута[0].Пометка = Пометка;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры // ОбновитьСостояниеРаботПоТочке()

&НаСервере
Процедура ОбновитьСостояниеТочкиПоРаботеНаСервере(СписокДействий, Пометка, ИДТочкиМаршрута)
	
	ИзменитьВидимостьТочки = Ложь;

	ИскомыеСтроки = Работы.НайтиСтроки(Новый Структура("Пометка, Идентификатор", Истина, ИДТочкиМаршрута));
	Если Пометка Тогда
		Если ИскомыеСтроки.Количество() = 1 Тогда
			ИзменитьВидимостьТочки = Истина;
		КонецЕсли;
	Иначе
		Если ИскомыеСтроки.Количество() = 0 Тогда
			ИзменитьВидимостьТочки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ИзменитьВидимостьТочки Тогда
		точИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", ИДТочкиМаршрута));
		Для каждого точНайденнаяСтрока Из точИскомыеСтроки Цикл
			Если точНайденнаяСтрока.НомерСтроки > 1 Тогда
				точНайденнаяСтрока.Пометка = Не точНайденнаяСтрока.Пометка;
				ОбработатьИзменениеВидимостиТочкиМаршрутаНаСервере(СписокДействий, точНайденнаяСтрока);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ОбновитьСостояниеТочкиПоРаботеНаСервере()

&НаКлиенте
Процедура ПересчитатьМаршрутНаКлиенте(ТолькоАктивные = Ложь);
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(МетодРешенияЗК) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан алгоритм решения задачи коммивояжера.'"),, "АлгоритмРешенияЗК",, Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВариантОптимизацииМаршрута) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан вариант оптимизации маршрута.'"),, "ВариантОптимизацииМаршрута",, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ИдентификаторЗадания = Неопределено;
	АдресВременногоХранилища = ""; 
	ПересчитатьМаршрутНаСервере(ТолькоАктивные, ИдентификаторЗадания, АдресВременногоХранилища);
	мсвИдентификаторов = Новый Массив;
	Если ИдентификаторЗадания <> Неопределено Тогда 
		сткДанныеРейса = Новый Структура;
		сткДанныеРейса.Вставить("Рейс"                    , Рейс);
		сткДанныеРейса.Вставить("ИдентификаторЗадания"    , ИдентификаторЗадания);
		сткДанныеРейса.Вставить("АдресВременногоХранилища", АдресВременногоХранилища);
		сткДанныеРейса.Вставить("ТолькоАктивные"          , ТолькоАктивные);
		мсвИдентификаторов.Добавить(сткДанныеРейса);
	КонецЕсли;
	Если мсвИдентификаторов.Количество() Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("МассивИдентификаторов"           , мсвИдентификаторов);
		сткПараметры.Вставить("Метод"                           , МетодРешенияЗК);
		сткПараметры.Вставить("РасчетИзФормыРедактированияРейса", Истина);
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ПараметрыОптимизации", сткПараметры);
		ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаОптимизацииМаршрута", ПараметрыФормы,,,,, Новый ОписаниеОповещения("ОптимизацияМаршрутаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьМаршрутНаСервере(ТолькоАктивные = Ложь, ИдентификаторЗадания, АдресВременногоХранилища)
	
	ИндексПервойНеПройденнойТочки = 0;
	
	Если Маршрут.Количество() > 2 Тогда
		АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		
		сткДопПараметры = Новый Структура;
		сткВесов = Новый Структура;
		сткВесов.Вставить("ПространственныйВес", ПространственныйВес);
		сткВесов.Вставить("ВременнойВес"       , ВременнойВес);
		сткВесов.Вставить("ВесСрочности"       , ВесСрочности);
		сткДопПараметры.Вставить("СтруктураВесов", сткВесов);
		
		тзВершин = упКомбинаторнаяОптимизация.ПодготовитьТаблицуТочекДляИзмененияПорядка(Маршрут, Рейс, МетодРешенияЗК, ИндексПервойНеПройденнойТочки, РежимКорректировки, ТолькоАктивные, Истина);
		
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(тзВершин);
		Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
			мсвПараметров.Добавить(Неопределено);
			мсвПараметров.Добавить(АдресВременногоХранилища);
			мсвПараметров.Добавить(МетодРешенияЗК);
			мсвПараметров.Добавить(сткДопПараметры);
		Иначе
			мсвПараметров.Добавить(ВариантОптимизацииМаршрута);
			мсвПараметров.Добавить(АдресВременногоХранилища);
			мсвПараметров.Добавить(МетодРешенияЗК);
		КонецЕсли;
		ФоновоеЗадание = ФоновыеЗадания.Выполнить("упКомбинаторнаяОптимизация.ИзменитьПорядокТочек", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Решение ЗК'"));
		ИдентификаторЗадания = ФоновоеЗадание.УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьМаршрутНаСервере()

&НаСервере
Процедура ИзменитьПорядокОбъездаТочекРейса(СтруктураВозврата, ТолькоАктивные)
	
	РасстояниеТекущее = СтруктураВозврата.Расстояние;
	ВремяВПутиТекущее = СтруктураВозврата.Время; 
	мсвРешений = СтруктураВозврата.МассивТочек;
	
	Сч = ИндексПервойНеПройденнойТочки;
	Для каждого Точка Из мсвРешений Цикл
		ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Идентификатор", Точка));
		Если ИскомыеСтроки.Количество() Тогда
			НайденнаяСтрока = ИскомыеСтроки[0];
			ИндексСтроки = Маршрут.Индекс(НайденнаяСтрока);
			Маршрут.Сдвинуть(ИндексСтроки, Сч - ИндексСтроки);
			НайденнаяСтрока.НомерСтроки = Сч + 1;
		КонецЕсли;
		
		Сч = Сч + 1;
	КонецЦикла; 
	
	Если ТолькоАктивные Тогда
		НомерСтроки = НайденнаяСтрока.НомерСтроки + 1;
		ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Пометка", Ложь));
		Для Каждого Строка Из ИскомыеСтроки Цикл
			Строка.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
	ВремяРейсаТекущее = ВремяВПутиТекущее + Маршрут.Итог("ВремяРабот") + Маршрут.Итог("ВремяОжиданияВТочке") + Маршрут.Итог("ВремяОжиданияВТочкеПослеРабот");
	
КонецПроцедуры // ИзменитьПорядокОбъездаТочекРейса()

&НаСервере
Процедура ОбновитьМаршрутРаботыНаСервере()
	
	Маршрут.Очистить();
	Работы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство,
	|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.Грузоподъемность, 0)) КАК Грузоподъемность,
	|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.Объем, 0)) КАК Объем,
	|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.КоличествоМест, 0)) КАК КоличествоМест,
	|	СУММА(ЕСТЬNULL(упПрицепы.Прицеп.КоличествоЗагрузочныхМетров, 0)) КАК КоличествоЗагрузочныхМетров
	|ПОМЕСТИТЬ ХарактеристикиПрицепов
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(, ) КАК упПрицепы
	|		ПО упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = упПрицепы.ТранспортноеСредство
	|ГДЕ
	|	упПрицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|
	|СГРУППИРОВАТЬ ПО
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Задание.Масса КАК МассаИсходная,
	|	упРейсЗадания.Задание.Объем КАК ОбъемИсходный,
	|	упРейсЗадания.Задание.КоличествоМест КАК КоличествоМестИсходное,
	|	упРейсЗадания.Задание.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетровИсходное
	|ПОМЕСТИТЬ втПоказателиЗагрузки
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка = &Рейс
	|	И упРейсЗадания.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упПараметрыЗаданияНаПеревозку.Масса) * упРейсЗадания.КоличествоГрузов КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упПараметрыЗаданияНаПеревозку.Объем) * упРейсЗадания.КоличествоГрузов КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упПараметрыЗаданияНаПеревозку.КоличествоМест) * упРейсЗадания.КоличествоГрузов КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров) * упРейсЗадания.КоличествоГрузов КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних() КАК упПараметрыЗаявкиНаПеревозку
	|	ПО упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних() КАК упПараметрыЗаданияНаПеревозку
	|	ПО упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|ГДЕ
	|	упРейсЗадания.Ссылка = &Рейс
	|	И упРейсЗадания.ГрузовоеМесто <> ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрут.СтрокаНомер КАК НомерСтроки,
	|	упМаршрут.Адрес,
	|	упМаршрут.Гараж,
	|	упМаршрут.Адрес.Широта КАК Широта,
	|	упМаршрут.Адрес.Долгота КАК Долгота,
	|	упМаршрут.Идентификатор,
	|	упМаршрут.ПлановоеПрибытие,
	|	упМаршрут.ПлановоеУбытие,
	|	упМаршрут.ВремяРабот * 3600 КАК ВремяРабот,
	|	упМаршрут.ТипТочки,
	|	упМаршрут.ТипТочки.Порядок КАК ИндексКартинки,
	|	упМаршрут.ВременноеОкноНачало,
	|	упМаршрут.ВременноеОкноОкончание,
	|	упМаршрут.ВременноеОкноНачало КАК ИсходноеВременноеОкноНачало,
	|	упМаршрут.ВременноеОкноОкончание КАК ИсходноеВременноеОкноОкончание,
	|	упМаршрут.РасстояниеОтПредыдущейТочки,
	|	упМаршрут.ВремяОтПредыдущейТочки * 3600 КАК ВремяОтПредыдущейТочки,
	|	упМаршрут.ВремяОтдыхаВПутиОтПредыдущейТочки * 3600 КАК ВремяОтдыхаВПутиОтПредыдущейТочки,
	|	упМаршрут.ВремяОжиданияВТочке * 3600 КАК ВремяОжиданияВТочке,
	|	упМаршрут.ВремяОжиданияВТочкеПослеРабот * 3600 КАК ВремяОжиданияВТочкеПослеРабот,
	|	упМаршрут.НеИспользоватьАвторасчетУбытияИзТочки,
	|	упМаршрут.ПроездНеРассчитан,
	|	упМаршрут.Предшественники,
	|	упМаршрут.Пройдена,
	|	упМаршрут.Отменена
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсРаботы.Идентификатор,
	|	упРейсРаботы.Задание,
	|	упРейсРаботы.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозку,
	|	упРейсРаботы.Задание.Заказчик КАК Заказчик,
	|	упРейсРаботы.Операция,
	|	упРейсРаботы.Операция.Порядок КАК ТипОперации,
	|	упРейсРаботы.ДополнительнаяОперация,
	|	упРейсРаботы.ВремяРабот * 3600 КАК ВремяРабот,
	|	упРейсРаботы.ВременноеОкноНачало,
	|	упРейсРаботы.ВременноеОкноОкончание,
	|	упРейсРаботы.ВременноеОкноНачало КАК ИсходноеВременноеОкноНачало,
	|	упРейсРаботы.ВременноеОкноОкончание КАК ИсходноеВременноеОкноОкончание,
	|	упРейсРаботы.ГрузовоеМесто,
	|	упРейсРаботы.Тара,
	|	упРейсРаботы.ВремяОжидания * 3600 КАК ВремяОжидания,
	|	упРейсРаботы.ПлановоеВремяНачалаРабот,
	//|	упРейсРаботы.СтрокаНомер,
	|	упРейсРаботы.КоличествоДопОпераций,
	|	упРейсРаботы.КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА упРейсРаботы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И упРейсРаботы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 1
	|		КОГДА упРейсРаботы.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(упРейсРаботы.ПлановоеВремяНачалаРабот, СЕКУНДА, упРейсРаботы.ВремяРабот * 3600 - 60) <= упРейсРаботы.ВременноеОкноОкончание
	|			ТОГДА 1
	|		КОГДА упРейсРаботы.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				И упРейсРаботы.ПлановоеВремяНачалаРабот >= упРейсРаботы.ВременноеОкноНачало
	|			ТОГДА 1
	|		КОГДА упРейсРаботы.ПлановоеВремяНачалаРабот >= упРейсРаботы.ВременноеОкноНачало
	|				И ДОБАВИТЬКДАТЕ(упРейсРаботы.ПлановоеВремяНачалаРабот, СЕКУНДА, упРейсРаботы.ВремяРабот * 3600 - 60) <= упРейсРаботы.ВременноеОкноОкончание
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПопадаетВоВременноеОкно,
	|	упРейсРаботы.ТипРегламентногоДокумента,
	|	упРейсРаботы.КоличествоЭкземпляровДокумента,
	|	упРейсРаботы.Сумма КАК Сумма,
	|	упРейсРаботы.ИдентификаторРаботы
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРейсРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПеревозчикПоРейсу.ТранспортноеСредство,
	|	упПеревозчикПоРейсу.Перевозчик,
	|	упПеревозчикПоРейсу.ТипТС,
	|	упПеревозчикПоРейсу.Водитель1,
	|	упПеревозчикПоРейсу.Водитель2,
	|	упПеревозчикПоРейсу.Экспедитор,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность) = 0
	|			КОНЕЦ
	|			ТОГДА 999999999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность)
	|			КОНЕЦ
	|	КОНЕЦ КАК ГрузоподъемностьТС,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем) = 0
	|			КОНЕЦ
	|			ТОГДА 999999999999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем)
	|			КОНЕЦ
	|	КОНЕЦ КАК ОбъемТС,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест) = 0
	|			КОНЕЦ
	|			ТОГДА 99999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест)
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоМестТС,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров) = 0
	|			КОНЕЦ
	|			ТОГДА 99999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров)
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетровТС,
	|	СУММА(втПоказателиЗагрузки.МассаИсходная) КАК МассаИсходная,
	|	СУММА(втПоказателиЗагрузки.ОбъемИсходный) КАК ОбъемИсходный,
	|	СУММА(втПоказателиЗагрузки.КоличествоМестИсходное) КАК КоличествоМестИсходное,
	|	СУММА(втПоказателиЗагрузки.КоличествоЗагрузочныхМетровИсходное) КАК КоличествоЗагрузочныхМетровИсходное,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА упТипыТС.КоэффициентРасчетаВремениВПути = 0
	|				ТОГДА 1
	|			ИНАЧЕ упТипыТС.КоэффициентРасчетаВремениВПути
	|		КОНЕЦ) КАК КоэффициентРасчетаВремениВПути
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоказателиЗагрузки КАК втПоказателиЗагрузки
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ПО упПеревозчикПоРейсу.ТранспортноеСредство = упТранспортныеСредства.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК упТипыТС
	|		ПО упПеревозчикПоРейсу.ТипТС = упТипыТС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХарактеристикиПрицепов КАК ХарактеристикиПрицепов
	|		ПО упПеревозчикПоРейсу.ТранспортноеСредство = ХарактеристикиПрицепов.ТранспортноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	упПеревозчикПоРейсу.ТранспортноеСредство,
	|	упПеревозчикПоРейсу.Перевозчик,
	|	упПеревозчикПоРейсу.ТипТС,
	|	упПеревозчикПоРейсу.Водитель1,
	|	упПеревозчикПоРейсу.Водитель2,
	|	упПеревозчикПоРейсу.Экспедитор,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность) = 0
	|			КОНЕЦ
	|			ТОГДА 999999999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.Грузоподъемность ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, упТипыТС.Грузоподъемность)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Грузоподъемность + ЕСТЬNULL(ХарактеристикиПрицепов.Грузоподъемность, 0), упТипыТС.Грузоподъемность)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем) = 0
	|			КОНЕЦ
	|			ТОГДА 999999999999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.Объем ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.Объем, упТипыТС.Объем)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.Объем + ЕСТЬNULL(ХарактеристикиПрицепов.Объем, 0), упТипыТС.Объем)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест) = 0
	|			КОНЕЦ
	|			ТОГДА 99999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоМест ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, упТипыТС.КоличествоМест)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоМест + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоМест, 0), упТипыТС.КоличествоМест)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров) = 0
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров) = 0
	|			КОНЕЦ
	|			ТОГДА 99999999
	|		ИНАЧЕ ВЫБОР
	|				КОГДА упТранспортныеСредства.КоличествоЗагрузочныхМетров ЕСТЬ NULL
	|					ТОГДА ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, упТипыТС.КоличествоЗагрузочныхМетров)
	|				ИНАЧЕ ЕСТЬNULL(упТранспортныеСредства.КоличествоЗагрузочныхМетров + ЕСТЬNULL(ХарактеристикиПрицепов.КоличествоЗагрузочныхМетров, 0), упТипыТС.КоличествоЗагрузочныхМетров)
	|			КОНЕЦ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.НомерСтроки,
	|	втМаршрут.Адрес,
	|	втМаршрут.Гараж,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втМаршрут.Адрес) КАК АдресПредставление,
	|	втМаршрут.Широта,
	|	втМаршрут.Долгота,
	|	втМаршрут.Идентификатор,
	|	втМаршрут.ПлановоеПрибытие,
	|	втМаршрут.ПлановоеУбытие,
	|	втМаршрут.ВремяРабот,
	|	втМаршрут.ТипТочки,
	|	втМаршрут.ИндексКартинки,
	|	втМаршрут.ВременноеОкноНачало,
	|	втМаршрут.ВременноеОкноОкончание,
	|	втМаршрут.ИсходноеВременноеОкноНачало,
	|	втМаршрут.ИсходноеВременноеОкноОкончание,
	|	втМаршрут.РасстояниеОтПредыдущейТочки,
	|	втМаршрут.ВремяОтПредыдущейТочки,
	|	втМаршрут.ВремяОтдыхаВПутиОтПредыдущейТочки,
	|	втМаршрут.ВремяОжиданияВТочке,
	|	втМаршрут.ВремяОжиданияВТочкеПослеРабот,
	|	втМаршрут.НеИспользоватьАвторасчетУбытияИзТочки,
	|	ВЫБОР
	|		КОГДА втМаршрут.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И втМаршрут.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 1
	|		КОГДА втМаршрут.ВременноеОкноНачало = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеУбытие, СЕКУНДА, -60) <= втМаршрут.ВременноеОкноОкончание
	|			ТОГДА 1
	|		КОГДА втМаршрут.ВременноеОкноОкончание = ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеПрибытие, СЕКУНДА, втМаршрут.ВремяОжиданияВТочке + 60) >= втМаршрут.ВременноеОкноНачало
	|			ТОГДА 1
	|		КОГДА ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеУбытие, СЕКУНДА, -60) <= втМаршрут.ВременноеОкноОкончание
	|				И ДОБАВИТЬКДАТЕ(втМаршрут.ПлановоеПрибытие, СЕКУНДА, втМаршрут.ВремяОжиданияВТочке + 60) >= втМаршрут.ВременноеОкноНачало
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПопадаетВоВременноеОкно,
	|	втМаршрут.ПроездНеРассчитан,
	|	втМаршрут.Предшественники,
	|	втМаршрут.Пройдена,
	|	втМаршрут.Отменена
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|
	|УПОРЯДОЧИТЬ ПО
	|	втМаршрут.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Задание,
	|	втРаботы.Заказчик,
	|	втРаботы.Операция,
	|	втРаботы.ТипОперации,
	|	втРаботы.ДополнительнаяОперация,
	|	втРаботы.ВремяРабот,
	|	втРаботы.ВременноеОкноНачало,
	|	втРаботы.ВременноеОкноОкончание,
	|	втРаботы.ИсходноеВременноеОкноНачало,
	|	втРаботы.ИсходноеВременноеОкноОкончание,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Тара,
	|	втРаботы.ВремяОжидания,
	|	втРаботы.ПлановоеВремяНачалаРабот,
	//|	втРаботы.СтрокаНомер,
	|	втРаботы.КоличествоДопОпераций,
	|	втРаботы.КоличествоГрузов,
	|	ИСТИНА КАК Пометка,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, упПараметрыЗаданияНаПеревозку.Масса) * втРаботы.КоличествоГрузов КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, упПараметрыЗаданияНаПеревозку.Объем) * втРаботы.КоличествоГрузов КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, упПараметрыЗаданияНаПеревозку.КоличествоМест) * втРаботы.КоличествоГрузов КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров) * втРаботы.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
	|	втРаботы.ПопадаетВоВременноеОкно,
	|	втРаботы.ТипРегламентногоДокумента,
	|	втРаботы.КоличествоЭкземпляровДокумента,
	|	втРаботы.Сумма,
	|	втРаботы.ГрузовоеМесто.Валюта КАК Валюта,
	|	втРаботы.ИдентификаторРаботы
	|ИЗ
	|	втРаботы КАК втРаботы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(, ЗаявкаНаПеревозку В (ВЫБРАТЬ втРаботы.ЗаявкаНаПеревозку ИЗ втРаботы КАК втРаботы ГДЕ НЕ втРаботы.ЗаявкаНаПеревозку = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО втРаботы.ЗаявкаНаПеревозку = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И втРаботы.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(, ЗаданиеНаПеревозку В (ВЫБРАТЬ втРаботы.Задание ИЗ втРаботы КАК втРаботы)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО втРаботы.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И втРаботы.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|ГДЕ
	|	втРаботы.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРаботы.Идентификатор,
	|	втРаботы.Задание,
	|	втРаботы.Заказчик,
	|	втРаботы.Операция,
	|	втРаботы.ТипОперации,
	|	втРаботы.ДополнительнаяОперация,
	|	втРаботы.ВремяРабот,
	|	втРаботы.ВременноеОкноНачало,
	|	втРаботы.ВременноеОкноОкончание,
	|	втРаботы.ИсходноеВременноеОкноНачало,
	|	втРаботы.ИсходноеВременноеОкноОкончание,
	|	втРаботы.ГрузовоеМесто,
	|	втРаботы.Тара,
	|	втРаботы.ВремяОжидания,
	|	втРаботы.ПлановоеВремяНачалаРабот,
	//|	втРаботы.СтрокаНомер,
	|	втРаботы.КоличествоДопОпераций,
	|	втРаботы.КоличествоГрузов,
	|	ИСТИНА,
	|	втРаботы.Задание.Масса,
	|	втРаботы.Задание.Объем,
	|	втРаботы.Задание.КоличествоМест,
	|	втРаботы.Задание.КоличествоЗагрузочныхМетров,
	|	втРаботы.ПопадаетВоВременноеОкно,
	|	втРаботы.ТипРегламентногоДокумента,
	|	втРаботы.КоличествоЭкземпляровДокумента,
	|	втРаботы.Сумма,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка),
	|	втРаботы.ИдентификаторРаботы
	|ИЗ
	|	втРаботы КАК втРаботы
	|ГДЕ
	|	втРаботы.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ДополнительнаяОперация), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов)) 
	//|
	//|УПОРЯДОЧИТЬ ПО
	//|	втРаботы.СтрокаНомер
	|";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	ВыборкаШапка = Результат[КоличествоТаблиц - 3].Выбрать();
	Если ВыборкаШапка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаШапка);
		МассаТекущая                       = МассаИсходная;
		ОбъемТекущий                       = ОбъемИсходный;
		КоличествоМестТекущее              = КоличествоМестИсходное;
		КоличествоЗагрузочныхМетровТекущее = КоличествоЗагрузочныхМетровИсходное;
	КонецЕсли;
	
	Если КоэффициентРасчетаВремениВПути = 0 Тогда
		КоэффициентРасчетаВремениВПути = 1;	
	КонецЕсли;
	
	
	тзМаршрут = Результат[КоличествоТаблиц - 2].Выгрузить();
	Для каждого текТочка Из тзМаршрут Цикл
		НоваяСтрока = Маршрут.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текТочка);
		НоваяСтрока.Координаты = ПредставлениеЧисла(текТочка.Широта) + ";" + ПредставлениеЧисла(текТочка.Долгота) + ";";
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.Позиционирование = Истина;
		ЗаполнитьПоляПредставленийСтрокиМаршрута(НоваяСтрока);
		НоваяСтрока.ИсходноеВременноеОкноПредставление = НоваяСтрока.ВременноеОкноПредставление;
	КонецЦикла;
	
	КоличествоАктивныхТочекИсходное = тзМаршрут.Количество();
	КоличествоАктивныхТочекТекущее=КоличествоАктивныхТочекИсходное;
	
	тзРаботы = Результат[КоличествоТаблиц - 1].Выгрузить();
	Для каждого текРабота Из тзРаботы Цикл
		НоваяСтрока = Работы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текРабота);
		ЗаполнитьПоляПредставленийСтрокиРабот(НоваяСтрока);
		НоваяСтрока.ИсходноеВременноеОкноПредставление = НоваяСтрока.ВременноеОкноПредставление;
	КонецЦикла;
	
КонецПроцедуры // ОбновитьМаршрутРаботыНаСервере()

&НаСервере
Процедура АктуализироватьРасстоянияМеждуАдресамиНаСервере()
	
	Расстояния.Очистить();
	мсвТочек = Маршрут.Выгрузить(, "Адрес").ВыгрузитьКолонку("Адрес");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упАдреса.Ссылка КАК Точка
	|ПОМЕСТИТЬ втТочки
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Ссылка В(&МассивТочек)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Точка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТочкиНачальные.Точка КАК НачальныйАдрес,
	|	втТочкиКонечные.Точка КАК КонечныйАдрес
	|ПОМЕСТИТЬ втРебра
	|ИЗ
	|	втТочки КАК втТочкиНачальные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТочки КАК втТочкиКонечные
	|		ПО ИСТИНА 
	//втТочкиНачальные.Точка <> втТочкиКонечные.Точка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачальныйАдрес,
	|	КонечныйАдрес
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРебра.НачальныйАдрес,
	|	втРебра.КонечныйАдрес,
	|	упРасстоянияМеждуТочками.Время КАК Время,
	|	упРасстоянияМеждуТочками.Расстояние,
	|	упРасстоянияМеждуТочками.Путь,
	|	ВЫБОР
	|		КОГДА упРасстоянияМеждуТочками.Расстояние ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА упРасстоянияМеждуТочками.Расстояние = 0
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасстояниеРассчитано,
	|	упРасстоянияМеждуТочками.ВремяПонедельник,
	|	упРасстоянияМеждуТочками.ВремяВторник,
	|	упРасстоянияМеждуТочками.ВремяСреда,
	|	упРасстоянияМеждуТочками.ВремяЧетверг,
	|	упРасстоянияМеждуТочками.ВремяПятница,
	|	упРасстоянияМеждуТочками.ВремяСуббота,
	|	упРасстоянияМеждуТочками.ВремяВоскресенье
	|ПОМЕСТИТЬ втРасстояния
	|ИЗ
	|	втРебра КАК втРебра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
	|		ПО втРебра.НачальныйАдрес.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
	|			И втРебра.НачальныйАдрес.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
	|			И втРебра.КонечныйАдрес.Широта = упРасстоянияМеждуТочками.КонечнаяШирота
	|			И втРебра.КонечныйАдрес.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота
	|			И (упРасстоянияМеждуТочками.Ограничение = &Ограничение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасстояния.НачальныйАдрес,
	|	втРасстояния.КонечныйАдрес,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.Время КАК Время,
	|	втРасстояния.Расстояние,
	|	втРасстояния.Путь,
	|	втРасстояния.РасстояниеРассчитано,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяПонедельник КАК ВремяПонедельник,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяВторник КАК ВремяВторник,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяСреда КАК ВремяСреда,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяЧетверг КАК ВремяЧетверг,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяПятница КАК ВремяПятница,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяСуббота КАК ВремяСуббота,
	|	&КоэффициентРасчетаВремениВПути * втРасстояния.ВремяВоскресенье КАК ВремяВоскресенье 
	|ИЗ
	|	втРасстояния КАК втРасстояния
	|ГДЕ
	|	втРасстояния.РасстояниеРассчитано
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасстояния.НачальныйАдрес,
	|	втРасстояния.НачальныйАдрес.Широта КАК НачальнаяШирота,
	|	втРасстояния.НачальныйАдрес.Долгота КАК НачальнаяДолгота,
	|	втРасстояния.КонечныйАдрес,
	|	втРасстояния.КонечныйАдрес.Широта КАК КонечнаяШирота,
	|	втРасстояния.КонечныйАдрес.Долгота КАК КонечнаяДолгота
	|ИЗ
	|	втРасстояния КАК втРасстояния
	|ГДЕ
	|	НЕ втРасстояния.РасстояниеРассчитано";
	Запрос.УстановитьПараметр("МассивТочек", мсвТочек);
	Запрос.УстановитьПараметр("КоэффициентРасчетаВремениВПути", КоэффициентРасчетаВремениВПути);
	Запрос.УстановитьПараметр("Ограничение", Рейс.ОграничениеМаршрутизатора);
	Результат = Запрос.ВыполнитьПакет();
	
	// загрузка рассчитанных расстояний
	Расстояния.Загрузить(Результат[3].Выгрузить());
	
	// обработка нерассчитанных расстояний
	Если Не Результат[4].Пустой()  Тогда
		Выборка = Результат[4].Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Расстояния.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		    	НоваяСтрока.Расстояние = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(Выборка.НачальнаяШирота, Выборка.НачальнаяДолгота, Выборка.КонечнаяШирота, Выборка.КонечнаяДолгота)/1000;
			НоваяСтрока.Время 			= КоэффициентРасчетаВремениВПути * НоваяСтрока.Расстояние / 60; // приближенное время из расчета, что скорость движения составляет 60 км/ч
			НоваяСтрока.ВремяПонедельник 	= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.ВремяВторник 	= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.ВремяСреда 		= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.ВремяЧетверг 	= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.ВремяПятница 	= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.ВремяСуббота 	= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.ВремяВоскресенье 	= КоэффициентРасчетаВремениВПути * НоваяСтрока.Время;
			НоваяСтрока.Путь = ПредставлениеЧисла(Выборка.НачальнаяШирота) + ";" + ПредставлениеЧисла(Выборка.НачальнаяДолгота) + ";" + ПредставлениеЧисла(Выборка.КонечнаяШирота) + ";" + ПредставлениеЧисла(Выборка.КонечнаяДолгота) + ";";
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере 
Процедура РедактироватьПосещениеГаражаНаСервере(СтруктураДанных)
	
	НачалоМаршрута    = СтруктураДанных.НачалоМаршрута;
	ОкончаниеМаршрута = СтруктураДанных.ОкончаниеМаршрута;
	
	ПересчитатьВремя = Ложь;
	
	текНачалоМаршрута = Неопределено;
	ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("ТипТочки", Перечисления.упТипыТочекМаршрута.ТочкаОтправления));
	Если ИскомыеСтроки.Количество() Тогда
		текНачалоМаршрута = ИскомыеСтроки[0];
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(НачалоМаршрута) Тогда
		Если текНачалоМаршрута = Неопределено Тогда
			НоваяСтрока = Маршрут.Вставить(0);
			НоваяСтрока.Гараж = НачалоМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачалоМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			НоваяСтрока.Адрес = сткРеквизиты.Адрес;
			НоваяСтрока.АдресПредставление = Строка(сткРеквизиты.Адрес);
			НоваяСтрока.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления;
			НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
			НоваяСтрока.Координаты = ПредставлениеЧисла(сткРеквизиты.Широта) + ";" + ПредставлениеЧисла(сткРеквизиты.Долгота) + ";";
			НоваяСтрока.Пометка = Истина;
			НоваяСтрока.Позиционирование = Истина;
			НоваяСтрока.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(НоваяСтрока.ВременноеОкноНачало, НоваяСтрока.ВременноеОкноОкончание);
			НоваяСтрока.ИндексКартинки = Перечисления.упТипыТочекМаршрута.Индекс(НоваяСтрока.ТипТочки);
			
			Если Маршрут.Количество() > 1 Тогда
				СледующаяТочка = Маршрут.Получить(1);
				СледующаяТочка.ПроездНеРассчитан = Истина;
				Если ЗначениеЗаполнено(СледующаяТочка.Предшественники) Тогда
					СледующаяТочка.Предшественники = СтрШаблон("%1,%2", СледующаяТочка.Предшественники,  Строка(НоваяСтрока.Идентификатор)); 
				Иначе
					СледующаяТочка.Предшественники = Строка(НоваяСтрока.Идентификатор);
				КонецЕсли; 
				НоваяСтрока.ПлановоеПрибытие = СледующаяТочка.ПлановоеПрибытие;
				
				Для Сч = 2 По Маршрут.Количество() - 1 Цикл
					текТочка = Маршрут[Сч];
					Если ЗначениеЗаполнено(текТочка.Предшественники) Тогда
						текТочка.Предшественники = СтрШаблон("%1,%2", текТочка.Предшественники,  Строка(НоваяСтрока.Идентификатор)); 
					Иначе
						текТочка.Предшественники = Строка(НоваяСтрока.Идентификатор);
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
			ПересчитатьВремя = Истина;
			
		ИначеЕсли НачалоМаршрута <> текНачалоМаршрута.Гараж Тогда
			текНачалоМаршрута.Гараж = НачалоМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачалоМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			текНачалоМаршрута.Адрес = сткРеквизиты.Адрес;
			текНачалоМаршрута.АдресПредставление = Строка(сткРеквизиты.Адрес);
			текНачалоМаршрута.Координаты = ПредставлениеЧисла(сткРеквизиты.Широта) + ";" + ПредставлениеЧисла(сткРеквизиты.Долгота) + ";";
			
			Если Маршрут.Количество() > 1 Тогда
				СледующаяТочка = Маршрут.Получить(1);
				СледующаяТочка.ПроездНеРассчитан = Истина;
			КонецЕсли;
			ПересчитатьВремя = Истина;
		КонецЕсли; 

	Иначе
		Если текНачалоМаршрута <> Неопределено Тогда
			УдалитьИдентификатор = Строка(текНачалоМаршрута.Идентификатор);
			Маршрут.Удалить(текНачалоМаршрута);
			
			СледующаяТочка = Маршрут.Получить(0);
			СледующаяТочка.РасстояниеОтПредыдущейТочки = 0;
			СледующаяТочка.ВремяОтПредыдущейТочки = 0;
			мсвПредшественников = СтрРазделить(СледующаяТочка.Предшественники, ",", Ложь);
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(мсвПредшественников, УдалитьИдентификатор);
			СледующаяТочка.Предшественники = СтрСоединить(мсвПредшественников, ",");
			Для Сч = 1 По Маршрут.Количество() - 1 Цикл
				текТочка = Маршрут[Сч];
				мсвПредшественников = СтрРазделить(текТочка.Предшественники, ",", Ложь);
				ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(мсвПредшественников, УдалитьИдентификатор);
				текТочка.Предшественники = СтрСоединить(мсвПредшественников, ",");
			КонецЦикла;
			ПересчитатьВремя = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	текОкончаниеМаршрута = Неопределено;
	ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("ТипТочки", Перечисления.упТипыТочекМаршрута.ТочкаЗавершения));
	Если ИскомыеСтроки.Количество() Тогда
		текОкончаниеМаршрута = ИскомыеСтроки[0];
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ОкончаниеМаршрута) Тогда
		Если текОкончаниеМаршрута = Неопределено Тогда
			НоваяСтрока = Маршрут.Вставить(Маршрут.Количество());
			НоваяСтрока.Гараж = ОкончаниеМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОкончаниеМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			НоваяСтрока.Адрес = сткРеквизиты.Адрес;
			НоваяСтрока.АдресПредставление = Строка(сткРеквизиты.Адрес);
			НоваяСтрока.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения;
			НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
			НоваяСтрока.Координаты = ПредставлениеЧисла(сткРеквизиты.Широта) + ";" + ПредставлениеЧисла(сткРеквизиты.Долгота) + ";";
		    НоваяСтрока.Пометка = Истина;
		    НоваяСтрока.Позиционирование = Истина;
			НоваяСтрока.ПроездНеРассчитан = Истина;
			НоваяСтрока.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(НоваяСтрока.ВременноеОкноНачало, НоваяСтрока.ВременноеОкноОкончание);
			НоваяСтрока.ИндексКартинки = Перечисления.упТипыТочекМаршрута.Индекс(НоваяСтрока.ТипТочки);
			
			мсвПредшественников = Новый Массив;
			Для Сч = 0 По Маршрут.Количество() - 2 Цикл
				мсвПредшественников.Добавить(Строка(Маршрут[Сч].Идентификатор));
			КонецЦикла;
			Если мсвПредшественников.Количество() Тогда
				НоваяСтрока.Предшественники = СтрСоединить(мсвПредшественников, ",");
			КонецЕсли; 
			ПересчитатьВремя = Истина;
			
		ИначеЕсли ОкончаниеМаршрута <> текОкончаниеМаршрута.Гараж Тогда	
			текОкончаниеМаршрута.Гараж = ОкончаниеМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОкончаниеМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			текОкончаниеМаршрута.Адрес = сткРеквизиты.Адрес;
			текОкончаниеМаршрута.АдресПредставление = Строка(сткРеквизиты.Адрес);
			текОкончаниеМаршрута.Координаты = ПредставлениеЧисла(сткРеквизиты.Широта) + ";" + ПредставлениеЧисла(сткРеквизиты.Долгота) + ";";
			текОкончаниеМаршрута.ПроездНеРассчитан = Истина;
			ПересчитатьВремя = Истина;
		КонецЕсли; 
	Иначе
		Если текОкончаниеМаршрута <> Неопределено Тогда
			Маршрут.Удалить(текОкончаниеМаршрута);
			ПересчитатьВремя = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ПересчитатьВремя Тогда
		Сч = 1;
		Для каждого текСтрока Из Маршрут Цикл
			текСтрока.НомерСтроки = Сч;
			Сч = Сч + 1;
			текСтрока.ИндексВМассиве = 0;
		КонецЦикла; 
		РеквизитыАдреса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Маршрут[0].Адрес, "ИспользоватьВремяУбытияИзТочки, ВремяУбытияИзТочки, ДопустимоеОтклонениеУбытияИзТочки, ЧасовойПояс");
		// Костыль для ситуации гаража перед складом с одним адресом
		ПересчетСоВторойТочки = Истина
			И Маршрут[0].ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления
			И Маршрут.Количество() > 1
			И Не Маршрут[1].НеИспользоватьАвторасчетУбытияИзТочки
			И Маршрут[1].Адрес = Маршрут[0].Адрес
			И РеквизитыАдреса.ИспользоватьВремяУбытияИзТочки
			;
		ПересчитатьВременнойГрафикМаршрута(,,,, ПересчетСоВторойТочки);
		АктуализироватьРасстоянияМеждуАдресамиНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьХарактеристикиЛинии()
	
	сткДанныеЛинии = Новый Структура;
	сткРассчитанные = Новый Структура;
	сткРассчитанные.Вставить("Цвет"        , "#6B8DE8");
	сткРассчитанные.Вставить("Толщина"     , 3);
	сткРассчитанные.Вставить("Прозрачность", 20);
	сткДанныеЛинии.Вставить("Рассчитанные", сткРассчитанные);
	
	сткНерассчитанные = Новый Структура;
	сткНерассчитанные.Вставить("Цвет"        , "#E80C2F");
	сткНерассчитанные.Вставить("Толщина"     , 3);
	сткНерассчитанные.Вставить("Прозрачность", 20);
	сткДанныеЛинии.Вставить("Нерассчитанные", сткНерассчитанные);
	
	Возврат сткДанныеЛинии;
	
КонецФункции // ПолучитьХарактеристикиЛинии()

&НаСервереБезКонтекста
Процедура ОтменитьРейсНаСервере(Рейс)
	
	РейсОбъект = Рейс.ПолучитьОбъект();
	РейсОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРейса.Отменен);
    РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры // ОтменитьРейсНаСервере()

&НаСервере
Процедура ИзменитьРейсНаСервере()
	
	тзМаршрут 	= Маршрут.Выгрузить(Новый Структура("Пометка", Истина));
	тзМаршрут.Колонки.Добавить("СтрокаНомер", Новый ОписаниеТипов("Число"));
	Сч = 1;
	Для каждого текТочкаМаршрута Из тзМаршрут Цикл
		текТочкаМаршрута.СтрокаНомер = Сч;
		текТочкаМаршрута.ВремяРабот         		 = текТочкаМаршрута.ВремяРабот/3600; 
		текТочкаМаршрута.ВремяОжиданияВТочке         = текТочкаМаршрута.ВремяОжиданияВТочке/3600;
		текТочкаМаршрута.ВремяОжиданияВТочкеПослеРабот = текТочкаМаршрута.ВремяОжиданияВТочкеПослеРабот/3600;
		текТочкаМаршрута.ВремяОтПредыдущейТочки      = текТочкаМаршрута.ВремяОтПредыдущейТочки/3600;
		текТочкаМаршрута.ВремяОтдыхаВПутиОтПредыдущейТочки = текТочкаМаршрута.ВремяОтдыхаВПутиОтПредыдущейТочки/3600;
		Сч = Сч + 1;
	КонецЦикла; 
	
	тзРаботы 	= Работы.Выгрузить(Новый Структура("Пометка", Истина));
	Для каждого текРабота Из тзРаботы Цикл
		текРабота.ВремяРабот    = текРабота.ВремяРабот/3600; 
		текРабота.ВремяОжидания = текРабота.ВремяОжидания/3600;
	КонецЦикла; 
				
	РейсОбъект 	= Рейс.ПолучитьОбъект();
	тчЗадания 	= РейсОбъект.Задания;
	ПараметрыВводаИнформацииОГрузовыхМестах = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РейсОбъект.ВидПеревозки, "ПараметрыВводаИнформацииОГрузовыхМестах"); 
	ИсключенныйГруз = Работы.Выгрузить(Новый Структура("Пометка, Операция", Ложь, Перечисления.упТипыОпераций.Разгрузка));
	Если ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
		
		мсвПроверенныеЗадания = Новый Массив;
		
		Для каждого текГруз Из ИсключенныйГруз Цикл
			
			Если мсвПроверенныеЗадания.Найти(текГруз.Задание) = Неопределено Тогда
				мсвПроверенныеЗадания.Добавить(текГруз.Задание);
				мсвСтрокиЗадания = тзРаботы.НайтиСтроки(Новый Структура("Пометка, Задание", Истина,текГруз.Задание));
				
				Если мсвСтрокиЗадания.Количество() = 0 Тогда
					ИскомыеСтроки = тчЗадания.НайтиСтроки(Новый Структура("Задание", текГруз.Задание));
					Для каждого текСтрока Из ИскомыеСтроки Цикл
						тчЗадания.Удалить(текСтрока);
					КонецЦикла; 
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	Иначе	
		Для каждого текГруз Из ИсключенныйГруз Цикл
			ИскомыеСтроки = тчЗадания.НайтиСтроки(Новый Структура("Задание, ГрузовоеМесто", текГруз.Задание, текГруз.ГрузовоеМесто));
			КоличествоКИсключению = текГруз.КоличествоГрузов;
			Для каждого текСтрока Из ИскомыеСтроки Цикл
				Если КоличествоКИсключению >= текСтрока.КоличествоГрузов Тогда
					КоличествоКИсключению = КоличествоКИсключению - текСтрока.КоличествоГрузов;
					тчЗадания.Удалить(текСтрока);
				Иначе
					текСтрока.КоличествоГрузов = текСтрока.КоличествоГрузов - КоличествоКИсключению;
					КоличествоКИсключению = 0;
				КонецЕсли;
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли;	
	РейсОбъект.ОптимизацияМаршрута = Истина;
	
	РейсОбъект.ДополнительныеСвойства.Вставить("МаршрутРаботыПодготовлены", Истина);
	РейсОбъект.ДополнительныеСвойства.Вставить("Маршрут", тзМаршрут);
	РейсОбъект.ДополнительныеСвойства.Вставить("МодифицированМаршрут", Истина);
	РейсОбъект.ДополнительныеСвойства.Вставить("Работы", тзРаботы);
	РейсОбъект.ДополнительныеСвойства.Вставить("МодифицированыРаботы", Истина);
	РейсОбъект.ДополнительныеСвойства.Вставить("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек);
	РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры // ИзменитьРейсНаСервере()

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура вызывается при закрытии формы редактирования рейса Параметров Маршрутизации СитиГИД.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ИзменениеПараметровМаршрутизацииСитиГИДЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = ТИП("Структура") Тогда
		ПараметрыМаршрутизацииСитиГИД = Результат;
		ПересчитатьВременнойГрафикМаршрутаНаКлиенте(Истина);	
	КонецЕсли; 
	
КонецПроцедуры // ИзменениеРейсаЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы оптимизации маршрута.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОптимизацияМаршрутаЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Оптимизация маршрута не удалась.'"));
		
	Иначе	
		Если ПроизводитьРасчетПопарныхРасстоянийПриРешенииЗадачиКоммивояжера Тогда
			АктуализироватьРасстоянияМеждуАдресамиНаСервере();
			ОчиститьОбъекты();
			ОтобразитьОбъектыНаКарте();
		КонецЕсли;
		ОтобразитьМаршрут();
		РазрешитьАктивизациюСтроки = Ложь;
		ПересчитатьВременнойГрафикМаршрута();
		РазрешитьАктивизациюСтроки = Истина;
		Если МетодРешенияЗК = ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон") И УменьшатьВремяОжидания Тогда
			упРейсКлиентСервер.СократитьВремяОжиданияВРейсе(Маршрут, Работы, 1);
			Для каждого текТочка Из Маршрут Цикл
				текТочка.ВремяРаботПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяРабот);
				текТочка.ВремяОжиданияВТочкеПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текТочка.ВремяОжиданияВТочке);
			КонецЦикла;
			Для каждого текРабота Из Работы Цикл
				текРабота.ВремяРаботПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текРабота.ВремяРабот);
				текРабота.ВремяОжиданияПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текРабота.ВремяОжидания);
			КонецЦикла;
		КонецЕсли;
		ОбновитьТекущиеРасстояниеИВремяРейса();
		
		КоличествоТочек = Маршрут.Количество();
		Для Сч = 1 По КоличествоТочек - 1 Цикл
			текТочка = Маршрут[Сч];
			Если текТочка.Пометка Тогда
				Если текТочка.ПопадаетВоВременноеОкно Тогда
					текДанные = ДанныеСтандартнойТочки;
				Иначе
					текДанные = ДанныеНарушенийПоВремени;
				КонецЕсли;
			Иначе
				текДанные = ДанныеИсключеннойТочки;
			КонецЕсли;
			ИзменитьИконкуМаркера(текТочка.ИндексВМассиве, ("""" + Строка(текТочка.НомерСтроки) + """," + текДанные), 1); 
		КонецЦикла; 
		
		ОбновитьДиаграммуНаКлиенте();
		ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
		
	КонецЕсли;
		
КонецПроцедуры // ОптимизацияМаршрутаЗавершение()

&НаКлиенте
// Процедура завершения немодально ввода планируемой даты рейса.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ИзменениеВремениНачалаРейсаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Маршрут[0].ПлановоеПрибытие = Результат;
		ПересчитатьВремя = Истина;
		ПерерисоватьДиаграмму = Ложь;
		Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграмма" Тогда
			ПерерисоватьДиаграмму = Истина;
		КонецЕсли;
		спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
		
		// серверные вызовы
		ИзменитьДанныеФормыНаСервере(спкДействий, ПересчитатьВремя, ПерерисоватьДиаграмму);
		
		// клиентские вызовы
		Если спкДействий.Количество() Тогда
			ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
		КонецЕсли;
		//ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
		
	КонецЕсли;
	
КонецПроцедуры // ИзменениеВремениНачалаРейсаЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы "ФормаРедактированияПосещенияГаражаВМаршруте".
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура РедактированиеПосещенияГаражаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		
		// серверные вызовы
		РедактироватьПосещениеГаражаНаСервере(Результат);
		
		// клиентские вызовы
		ОчиститьОбъекты();
		ОтобразитьОбъектыНаКарте();
		ОтобразитьМаршрут();
		
		ОбновитьДиаграммуНаКлиенте();
		ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после вопрос отмены рейса.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ЗавершениеРедактированияРейсаВопросОбОтмене(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ОтменитьРейсНаСервере(Рейс);
		Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры // ЗавершениеРедактированияРейсаВопросОбОтмене()

&НаКлиенте
// Процедура - Обработать разделение точки
//
// Параметры:
//  РезультатЗакрытия		 - 	 - 
//  ДополнительныеПараметры	 - 	 - 
//
Процедура ОбработатьРазделениеТочки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		ИдентификаторТочки 	= РезультатЗакрытия.ИдентификаторТочки;
		СписокОпераций		= РезультатЗакрытия.СписокОпераций;
		РазделитьРаботыВТочкеНаСервере(ИдентификаторТочки, СписокОпераций);
		ПересчитатьВременнойГрафикМаршрутаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПересчетВесовКоэффициента_ЗадачаКоммивояжера

&НаКлиенте
Процедура ПривестиСуммуВесовКЕдинице(ИзменятьПространственныйВес, ИзменятьВременнойВес, ИзменятьВесСрочности)
	
	СуммаВесов = ПространственныйВес + ВременнойВес + ВесСрочности;
	Если СуммаВесов > 1 Тогда
		СуммаУменьшить = СуммаВесов - 1;
		Если Не ИзменятьПространственныйВес Тогда
			УменьшитьВес(СуммаУменьшить, ВременнойВес); 
			УменьшитьВес(СуммаУменьшить, ВесСрочности);
		КонецЕсли;
		
		Если Не ИзменятьВременнойВес Тогда
			УменьшитьВес(СуммаУменьшить, ВесСрочности);
			УменьшитьВес(СуммаУменьшить, ПространственныйВес); 
		КонецЕсли;
		
		Если Не ИзменятьВесСрочности Тогда
			УменьшитьВес(СуммаУменьшить, ПространственныйВес); 
			УменьшитьВес(СуммаУменьшить, ВременнойВес);
		КонецЕсли;
		
	ИначеЕсли СуммаВесов < 1 Тогда
		СуммаУвеличить = 1 - СуммаВесов;
		Если Не ИзменятьПространственныйВес Тогда
			УвеличитьВес(СуммаУвеличить, ВременнойВес); 
			УвеличитьВес(СуммаУвеличить, ВесСрочности);
		КонецЕсли;
		
		Если Не ИзменятьВременнойВес Тогда
			УвеличитьВес(СуммаУвеличить, ВесСрочности);
			УвеличитьВес(СуммаУвеличить, ПространственныйВес); 
		КонецЕсли;
		
		Если Не ИзменятьВесСрочности Тогда
			УвеличитьВес(СуммаУвеличить, ПространственныйВес); 
			УвеличитьВес(СуммаУвеличить, ВременнойВес);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПривестиСуммуВесовКЕдинице()

&НаКлиенте 
Процедура УвеличитьВес(Сумма, Вес)
	
	текУвеличение = Мин(Сумма, 1 - Вес);
	Вес = Вес + текУвеличение;
	Сумма = Сумма - текУвеличение;
	
КонецПроцедуры // УвеличитьВес()

&НаКлиенте 
Процедура УменьшитьВес(Сумма, Вес)
	
	текУменьшение = Мин(Сумма, Вес);
	Вес = Вес - текУменьшение;
	Сумма = Сумма - текУменьшение;
	
КонецПроцедуры // Уменьшитьвес()
	
#КонецОбласти 

#Область РаботаСКартой

&НаКлиенте
Процедура ОбновитьСостояниеМаркеров()

	КоличествоВыделенныхМаркеров 		= ВыполнитьФункциюHTML("CheckCountOfSelectedMarkers()");
	КоличествоСнятыхСВыделенияМаркеров 	= ВыполнитьФункциюHTML("CheckCountOfUnselectedMarkers()");
	
	Если НЕ РежимКорректировки Тогда
							
		Если КоличествоВыделенныхМаркеров Тогда
			ОбработатьВыбранныеМаркеры();
		КонецЕсли;	
				
		Если КоличествоСнятыхСВыделенияМаркеров Тогда
			ОбработатьСнятыеСВыделенияМаркеры();
		КонецЕсли;
		
		Если КоличествоВыделенныхМаркеров + КоличествоСнятыхСВыделенияМаркеров > 0 Тогда
			ПересчитатьВременнойГрафикМаршрута();
			ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры // ОбновитьСостояниеМаркеров()

&НаКлиенте
Процедура ОбработатьВыбранныеМаркеры()
	
	ПредставлениеВыделенныхТочек = ВыполнитьФункциюHTML("getSelectedMarkers()");
	МассивТочек = СтрРазделить(ПредставлениеВыделенныхТочек, ";id=", Ложь);
	
	Для каждого текЭлемент Из МассивТочек Цикл
		//Если текЭлемент <> "" Тогда
			текЭлемент = Число(текЭлемент);
			ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("ИндексВМассиве", текЭлемент));
			Для каждого Строка Из ИскомыеСтроки Цикл
				Если Строка.НомерСтроки > 1 И Не ЗначениеЗаполнено(Строка.Гараж) Тогда// И Строка.Пометка Тогда
					
					//РазрешитьАктивизациюСтроки = Ложь;
					Элементы.Маршрут.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
					//РазрешитьАктивизациюСтроки = Истина;
					
					ПересчитатьВремя = Истина;
					Строка.Пометка = Не Строка.Пометка; 			
					спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
					
					// серверные вызовы
					ИзменитьДанныеФормыНаСервере(спкДействий, Истина, Ложь,, Истина);
					
					// клиентские вызовы
					Если спкДействий.Количество() Тогда
						ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
					КонецЕсли;
					ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
				КонецЕсли;
			КонецЦикла; 
		//КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры //ОбработатьВыбранныеМаркеры()

&НаКлиенте
Процедура ОбработатьСнятыеСВыделенияМаркеры()
	
	ПредставлениеСнятыхСВыделенияТочек = ВыполнитьФункциюHTML("getUnselectedMarkers()");
	МассивТочек = СтрРазделить(ПредставлениеСнятыхСВыделенияТочек, ";id=", Ложь);
	
	Для каждого текЭлемент Из МассивТочек Цикл
		Если текЭлемент <> "" Тогда
			текЭлемент = Число(текЭлемент);
			ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("ИндексВМассиве", текЭлемент));
			Для каждого Строка Из ИскомыеСтроки Цикл
				Если Строка.НомерСтроки > 1  И Не ЗначениеЗаполнено(Строка.Гараж) Тогда
					Элементы.Маршрут.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
					
					ПересчитатьВремя = Истина;
					Строка.Пометка = НЕ Строка.Пометка; 			
					спкДействий = Новый СписокЗначений; // определение списка действий по изменению отображения карты на клиенте
					
					// серверные вызовы
					ИзменитьДанныеФормыНаСервере(спкДействий, Истина, Ложь,, Истина);
					
					// клиентские вызовы
					Если спкДействий.Количество() Тогда
						ВыполнитьДействияПоИзменениюОтображенияКарты(спкДействий);
					КонецЕсли;
					ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ОбработатьСнятыеСВыделенияМаркеры()

&НаКлиенте
Процедура МасштабироватьКартуПоТочкамНаКлиенте(ТолькоПоАктивным = Истина)
	
	Если НЕ КартаСформирована Тогда
		Возврат;
	КонецЕсли;
	
	Координаты = "";

	Для каждого текТочка Из Маршрут Цикл
		Если ТолькоПоАктивным Тогда
			Если текТочка.Пометка Тогда
				Координаты = Координаты + текТочка.Координаты;
			КонецЕсли;
		Иначе
			Координаты = Координаты + текТочка.Координаты;
		КонецЕсли;
	КонецЦикла; 
	
	МаксШирота  = -360;
	МаксДолгота = -180;
	МинШирота   = 360;
	МинДолгота  = 180;
	мсвКоординат = СтрРазделить(Координаты, ";");
	Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
		текШирота  = Число(мсвКоординат[Сч * 2]);
		МинШирота  = Мин(МинШирота, текШирота);
		МаксШирота = Макс(МаксШирота, текШирота);
		
		текДолгота = Число(мсвКоординат[Сч * 2 + 1]);
		МинДолгота  = Мин(МинДолгота, текДолгота);
		МаксДолгота = Макс(МаксДолгота, текДолгота);
	КонецЦикла; 
	Координаты = ПредставлениеЧисла(МаксШирота) + ";" + ПредставлениеЧисла(МаксДолгота) + ";" + ПредставлениеЧисла(МинШирота) + ";" + ПредставлениеЧисла(МинДолгота) + ";";
	ЦентрироватьТочкиНаКарте(Координаты);
	
КонецПроцедуры // МасштабироватьКартуПоТочкамНаКлиенте()

&НаКлиенте
// Процедура позиционирует слой на карте.
//
// Параметры:
//  ИндексВМассиве - Число - Индекс в массиве объектов(слоев, рейсов), помещенных на карту.
//  Режим          - Число - числовое представление режима отображения (0 - точки, 1 - линии, 2 - маршруты).
//
Процедура ЦентрироватьСлойНаКарте(ИндексВМассиве,  Режим = 0)
	
	Если Режим = 0 Тогда
		ВыполнитьПроцедуруHTML("setUsePositioning(true)");
		ВыполнитьПроцедуруHTML("gotoObjectMarkerSaveZoom(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
		ВыполнитьПроцедуруHTML("setUsePositioning(false)");
	ИначеЕсли Режим = 1 Или Режим = 2 Тогда
		ВыполнитьПроцедуруHTML("gotoObjectPolygon(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	ИначеЕсли Режим = 3 Тогда
		ВыполнитьПроцедуруHTML("gotoTrip(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли; 
	
КонецПроцедуры // ЦентрироватьСлойНаКарте()

&НаКлиенте
// Процедура очищает все объекты на карте.
//
// Параметры:
//  Нет.
//
Процедура ОчиститьОбъекты()
	
	ВыполнитьПроцедуруHTML("clearObjects()");
	
КонецПроцедуры // ОчиститьОбъекты()

&НаКлиенте
Процедура ИзменитьВидимостьОбъекта(ИндексВМассиве, Пометка)
	
	Если Пометка Тогда 
		ВыполнитьПроцедуруHTML("showObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	Иначе	
		ВыполнитьПроцедуруHTML("hideObject(" + ПредставлениеЧисла(ИндексВМассиве) + ")");
	КонецЕсли;	
	
КонецПроцедуры // ИзменитьВидимостьОбъекта()

&НаКлиенте
// Процедура выводит объекты на карту.
//
// Параметры:
//  Нет.
//
Процедура ОтобразитьОбъектыНаКарте()
	
	Если КартаСформирована Тогда
		Сч = 1;
		Для каждого текТочка Из Маршрут Цикл
			Если текТочка.Пройдена Тогда
				текДанные = ДанныеПройденнойТочки;
			ИначеЕсли ЗначениеЗаполнено(текТочка.Гараж) Тогда
				текДанные = ДанныеДепо;
			Иначе
				Если текТочка.ПопадаетВоВременноеОкно Тогда
					текДанные = ДанныеСтандартнойТочки;
				Иначе
					текДанные = ДанныеНарушенийПоВремени;
				КонецЕсли;
			КонецЕсли;
			текТочка.ИндексВМассиве = ОтобразитьОбъект(текТочка.АдресПредставление, текДанные, текТочка.Координаты, 3, Строка(Сч));
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОтобразитьОбъектыНаКарте()

&НаКлиенте
Процедура ОтобразитьМаршрут()
	
	сткДанныеЛинии = ПолучитьХарактеристикиЛинии();
	мсвТочкиМаршрута = Новый Массив;
	РасстояниеТекущее = 0;
	ВремяВПутиТекущее = 0;
	ВремяРейсаТекущее = 0;
	ВремяОтдыхаВПутиТекущее = 0;
	
	Для каждого текТочка Из Маршрут Цикл
		Если текТочка.Пометка Тогда
			мсвТочкиМаршрута.Добавить(текТочка);
		КонецЕсли;	
	КонецЦикла;
	
	Расстояния.Сортировать("ИндексВМассиве УБЫВ");
	Для каждого Строка Из Расстояния Цикл
		Если Строка.ИндексВМассиве > 0 Тогда
			ИзменитьВидимостьОбъекта(Строка.ИндексВМассиве, Ложь);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла; 
	Отрезки = Новый Массив;
	Для Сч = 1 По мсвТочкиМаршрута.Количество() - 1 Цикл
		НачальнаяСтрока = мсвТочкиМаршрута[Сч - 1];
		КонечнаяТочка   = мсвТочкиМаршрута[Сч];
		Отрезки.Добавить(Новый Структура("НачальныйАдрес, КонечныйАдрес", НачальнаяСтрока.Адрес, КонечнаяТочка.Адрес));
	КонецЦикла;
	ИндексыРасстояний = ИндексыРасстояний(Отрезки);
	
	Для Сч = 1 По мсвТочкиМаршрута.Количество() - 1 Цикл
		НачальнаяСтрока = мсвТочкиМаршрута[Сч - 1];
		КонечнаяТочка   = мсвТочкиМаршрута[Сч];
		
		ИндексРасстояния = ИндексыРасстояний[Сч - 1];
		Если ИндексРасстояния <> Неопределено Тогда
			НайденнаяСтрока = Расстояния[ИндексРасстояния];
			Если НайденнаяСтрока.ИндексВМассиве > 0 Тогда
				ИзменитьВидимостьОбъекта(НайденнаяСтрока.ИндексВМассиве, Истина);
			Иначе 
				НайденнаяСтрока.ИндексВМассиве = ОтобразитьОбъект(, ?(НайденнаяСтрока.РасстояниеРассчитано, сткДанныеЛинии.Рассчитанные, сткДанныеЛинии.Нерассчитанные), НайденнаяСтрока.Путь, 1);	
			КонецЕсли;
			КонечнаяТочка.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
			КонечнаяТочка.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
			КонечнаяТочка.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(КонечнаяТочка.РасстояниеОтПредыдущейТочки);
		    	КонечнаяТочка.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(КонечнаяТочка.ВремяОтПредыдущейТочки);
			КонечнаяТочка.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(КонечнаяТочка.ВремяОтдыхаВПутиОтПредыдущейТочки);
			
			ВремяОтдыхаВПутиТекущее = ВремяОтдыхаВПутиТекущее + КонечнаяТочка.ВремяОтдыхаВПутиОтПредыдущейТочки;
			РасстояниеТекущее = РасстояниеТекущее + НайденнаяСтрока.Расстояние;
			ВремяВПутиТекущее = ВремяВПутиТекущее + НайденнаяСтрока[ВремяДеньНедели];
		КонецЕсли;
	КонецЦикла;
	ВремяРейсаТекущее = ВремяВПутиТекущее + (Маршрут.Итог("ВремяРабот") + Маршрут.Итог("ВремяОжиданияВТочке") + Маршрут.Итог("ВремяОжиданияВТочкеПослеРабот")) + ВремяОтдыхаВПутиТекущее;
	Если ЗафиксироватьИсходныеПоказатели Тогда
		РасстояниеИсходное = РасстояниеТекущее;
		ВремяРейсаИсходное = ВремяРейсаТекущее;
		ВремяВПутиИсходное = ВремяВПутиТекущее;
		ВремяОтдыхаВПутиИсходное = ВремяОтдыхаВПутиТекущее;
		ЗафиксироватьИсходныеПоказатели = Ложь;
	КонецЕсли;
	МасштабироватьКартуПоТочкамРейса(Неопределено);
	
КонецПроцедуры // ОтобразитьМаршрут()

&НаСервере
Функция ИндексыРасстояний(Знач Отрезки)
	
	РасстоянияТаблица = Расстояния.Выгрузить();
	РасстоянияТаблица.Индексы.Добавить("НачальныйАдрес, КонечныйАдрес");
	МассивИндексовРасстояний = Новый Массив;
	Для Каждого Отрезок Из Отрезки Цикл
		ИскомыеСтроки = РасстоянияТаблица.НайтиСтроки(Отрезок);
		Если ИскомыеСтроки.Количество() Тогда
			ИндексРасстояния = РасстоянияТаблица.Индекс(ИскомыеСтроки[0]);
		Иначе
			ИндексРасстояния = Неопределено;
		КонецЕсли;
		МассивИндексовРасстояний.Добавить(ИндексРасстояния);
	КонецЦикла;
	Возврат МассивИндексовРасстояний;

КонецФункции// ОтобразитьМаршрутНаСервере()

&НаКлиенте
// Функция добавляет объект на карту.
//
// Параметры:
//  Имя        - Строка - отображаемое в виде всплывающей подсказки имя объекта.
//  Данные     - Структура, Строка - дополнительные данные 
//               (для точки - физ. адрес картинки, для линии и маршрута - характеристики отображения линии).
//  Координаты - Строка - строка с координатами, перечисленными через точку с запятой.
//  Режим      - Число  - числовое представление режима отображения (0 - точки, 1 - линии, 2 - маршруты).
//
// Возвращаемое значение:
//   Число     - уникальный идентификатор объекта в виде числового индекса. 
//               При помощи данного индекса в последствии возможно изменение свойств объекта (видимость и т.д.).
//
Функция ОтобразитьОбъект(Имя = "", Данные = "", Координаты, Режим = 0, ТекстМаркера = "") 
	
	
	Если Режим = 0 Тогда
		мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
		Индекс = ВыполнитьФункциюHTML("addObjectMarker(" + мсвКоординат[0] + ", " + мсвКоординат[1] + ", """ + ПредставлениеСтроки(Имя) + """, " + Данные + ")");
	ИначеЕсли Режим = 1 Или Режим = 2 Тогда
		//Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
		//	ВыполнитьПроцедуруHTML("addPoint(" + мсвКоординат[Сч * 2] + ", " + мсвКоординат[(Сч * 2) + 1] + ")");
		//КонецЦикла;	
		//Индекс = ВыполнитьФункциюHTML("addObjectPolylineBetweenObjects(""" + Данные.Цвет + """, " + ПредставлениеЧисла(Данные.Толщина) + ", " + ПредставлениеЧисла(Данные.Прозрачность) + ")");
		Индекс = ВыполнитьФункциюHTML("addPolylineWithCoordinates(""" + Координаты + """, """ + Данные.Цвет + """, " + ПредставлениеЧисла(Данные.Толщина) + ", " + ПредставлениеЧисла(Данные.Прозрачность) + ")");
	ИначеЕсли Режим = 3 Тогда
		мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
		Индекс = ВыполнитьФункциюHTML("addObjectMarkerWithDivIcon(" + мсвКоординат[0] + ", " + мсвКоординат[1] + ", """ + ПредставлениеСтроки(Имя) + """, """ + ТекстМаркера + """, " + Данные + ")");
	КонецЕсли;	
	
	Возврат Индекс;
	
Конецфункции // ОтобразитьОбъект()

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтроки(Строка)
	
	Возврат СтрЗаменить(Строка, """", "");
	
КонецФункции // ПредставлениеСтроки()

&НаКлиенте
// Процедура изменяет икону маркера на карте.
//
// Параметры:
//  ИндексВМассиве  - Число - индекс в массиве хранения.
//  Данные  - Строка - текст параметров отображения.
//  Режим  - Число - режим исполнения.
//
Процедура ИзменитьИконкуМаркера(ИндексВМассиве, Данные = "", Режим = 0)
	
	Если Режим = 0 Тогда
		ВыполнитьПроцедуруHTML("setMarkerIcon(" + ИндексВМассиве + ", " + Данные + ")");
	Иначе
		ВыполнитьПроцедуруHTML("setMarkerDivIcon(" + ИндексВМассиве + ", " + Данные + ")");
	КонецЕсли;
	
КонецПроцедуры // ИзменитьИконкуМаркера() 

&НаКлиенте
// Обработчик действия ожидания. Вызывается при неудачной попытке отображения карты.
//
Процедура ВыполнитьОтложенноеДействие()Экспорт 

	HTMLДокументДокументСформирован(Неопределено);

КонецПроцедуры // ВыполнитьОтложенноеДействие()

&НаКлиенте
// Заставляет HTML документ выполнить javascript.
//
// Параметры:
//	текстСкрипта - строка - тектс на javascript.
//
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)
	
	упРаботаСКартамиКлиент.ВыполнитьПроцедуруHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецПроцедуры

&НаКлиенте
// Заставляет HTML документ выполнить javascript и возращает результат.
//
// Параметры:
//	текстСкрипта - строка - тектс на javascript.
//
// Возвращаемое значение:
//	Строка - значение, которое возвращает javascript.
// 
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Возврат	упРаботаСКартамиКлиент.ВыполнитьФункциюHTML(текстСкрипта, ЭтаФорма, СлужебныеЭлементыHtml);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеЧисла(Координата)
	
	Возврат Формат(Координата, "ЧРД=.; ЧН=; ЧГ=0");
	
КонецФункции

&НаСервере
// Процедура обновляет отображение маршрута рейса на карте при изменении порядка следования двух соседних точек. 
//
// Параметры:
//  СписокДействий  - Массив - Дейтсвия.
//  ПереместитьВверх  - Булево - флаг выполнения.
//
Процедура ПоменятьМестамиТочкиВМаршруте(СписокДействий, ПереместитьВверх = Истина)
	
	ТекущаяСтрока = Элементы.Маршрут.ТекущаяСтрока;
	текСтрока = Маршрут.НайтиПоИдентификатору(ТекущаяСтрока);
	Если ПереместитьВверх Тогда
		
		ИндексСтроки = Маршрут.Индекс(текСтрока);
		ЗамещаемаяСтрока = Маршрут.Получить(ИндексСтроки - 1);
		ЗамещаемаяСтрока.НомерСтроки = ЗамещаемаяСтрока.НомерСтроки + 1;
		
		Маршрут.Сдвинуть(ИндексСтроки, -1);
		текСтрока.НомерСтроки = текСтрока.НомерСтроки - 1; 
		
		ПерваяСтрока = Неопределено;
		Если текСтрока.Пометка И ЗамещаемаяСтрока.Пометка Тогда
			Ограничение = текСтрока.НомерСтроки;
			Для Сч = 1 По Ограничение - 1 Цикл
				ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки", Ограничение - Сч));
				Если ИскомыеСтроки.Количество() И ИскомыеСтроки[0].Пометка Тогда
					ПерваяСтрока = ИскомыеСтроки[0];
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			
			ПоследняяСтрока = Неопределено;
			Для Сч = Ограничение + 2 По Маршрут.Количество() Цикл
				ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки", Сч));
				Если ИскомыеСтроки.Количество() И ИскомыеСтроки[0].Пометка Тогда
					ПоследняяСтрока = ИскомыеСтроки[0];
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			АдресВторойТочки    =  ЗамещаемаяСтрока.Адрес;
			АдресТретьейТочки   =  текСтрока.Адрес;
		Иначе
			Возврат;
		КонецЕсли;
		
	Иначе
		ИндексСтроки = Маршрут.Индекс(текСтрока);
		ЗамещаемаяСтрока = Маршрут.Получить(ИндексСтроки + 1);
		ЗамещаемаяСтрока.НомерСтроки = ЗамещаемаяСтрока.НомерСтроки - 1;
		
		Маршрут.Сдвинуть(ИндексСтроки, 1);
		текСтрока.НомерСтроки = текСтрока.НомерСтроки + 1; 
		
		Если текСтрока.Пометка И ЗамещаемаяСтрока.Пометка Тогда
			Ограничение = текСтрока.НомерСтроки - 1;
			Для Сч = 1 По Ограничение - 1 Цикл
				ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки", Ограничение - Сч));
				Если ИскомыеСтроки.Количество() И ИскомыеСтроки[0].Пометка Тогда
					ПерваяСтрока = ИскомыеСтроки[0];
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			
			ПоследняяСтрока = Неопределено;
			Для Сч = Ограничение + 2 По Маршрут.Количество() Цикл
				ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("НомерСтроки", Сч));
				Если ИскомыеСтроки.Количество() И ИскомыеСтроки[0].Пометка Тогда
					ПоследняяСтрока = ИскомыеСтроки[0];
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			АдресВторойТочки    =  текСтрока.Адрес;
			АдресТретьейТочки   =  ЗамещаемаяСтрока.Адрес;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	АдресПервойТочки    =  ?(ПерваяСтрока = Неопределено, Неопределено, ПерваяСтрока.Адрес);
	АдресЧетвертойТочки =  ?(ПоследняяСтрока = Неопределено, Неопределено, ПоследняяСтрока.Адрес);
	
	мсвТочек = Новый Массив;
	Если ПерваяСтрока <> Неопределено Тогда
		мсвТочек.Добавить(ПерваяСтрока);
	КонецЕсли;
	мсвТочек.Добавить(ЗамещаемаяСтрока);
	мсвТочек.Добавить(текСтрока);
	Если ПоследняяСтрока <> Неопределено Тогда
		мсвТочек.Добавить(ПоследняяСтрока);
	КонецЕсли;
	
	ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", АдресВторойТочки, АдресТретьейТочки));
	НайденнаяСтрока = ИскомыеСтроки[0];
	Если НайденнаяСтрока.ИндексВМассиве > 0 Тогда
		сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Ложь, НайденнаяСтрока);
		СписокДействий.Добавить(сткИзменений);
		ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, -1);
	КонецЕсли;
	
	Если АдресПервойТочки <> Неопределено Тогда
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", АдресПервойТочки, АдресВторойТочки));
		НайденнаяСтрока = ИскомыеСтроки[0];
		Если НайденнаяСтрока.ИндексВмассиве > 0 Тогда
			сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Ложь, НайденнаяСтрока);
			СписокДействий.Добавить(сткИзменений);
			ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, -1);
		КонецЕсли;
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", АдресПервойТочки, АдресТретьейТочки));
		НайденнаяСтрока = ИскомыеСтроки[0];
		Если ПереместитьВверх Тогда
			текСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
			текСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
			текСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(текСтрока.РасстояниеОтПредыдущейТочки);
			текСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текСтрока.ВремяОтПредыдущейТочки);
			текСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
		Иначе
			ЗамещаемаяСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
			ЗамещаемаяСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
			ЗамещаемаяСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(ЗамещаемаяСтрока.РасстояниеОтПредыдущейТочки);
			ЗамещаемаяСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ЗамещаемаяСтрока.ВремяОтПредыдущейТочки);
			ЗамещаемаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ЗамещаемаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
		КонецЕсли;
		
		сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Истина, НайденнаяСтрока);
		СписокДействий.Добавить(сткИзменений);
		ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, 1);
	КонецЕсли;
	
	ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", АдресТретьейТочки, АдресВторойТочки));
	НайденнаяСтрока = ИскомыеСтроки[0];
	Если ПереместитьВверх Тогда
		ЗамещаемаяСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
		ЗамещаемаяСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
		ЗамещаемаяСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(ЗамещаемаяСтрока.РасстояниеОтПредыдущейТочки);
		ЗамещаемаяСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ЗамещаемаяСтрока.ВремяОтПредыдущейТочки);
		ЗамещаемаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ЗамещаемаяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
	Иначе
		текСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
		текСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
		текСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(текСтрока.РасстояниеОтПредыдущейТочки);
		текСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текСтрока.ВремяОтПредыдущейТочки);
		текСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(текСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
	КонецЕсли;
	сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Истина, НайденнаяСтрока);
	СписокДействий.Добавить(сткИзменений);
	ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, 1);

	Если АдресЧетвертойТочки <> Неопределено Тогда
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", АдресТретьейТочки, АдресЧетвертойТочки));
		НайденнаяСтрока = ИскомыеСтроки[0];
		Если НайденнаяСтрока.ИндексВМассиве > 0 Тогда
			сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Ложь, НайденнаяСтрока);
	        СписокДействий.Добавить(сткИзменений);
			ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, -1);
		КонецЕсли;
		
		ИскомыеСтроки = Расстояния.НайтиСтроки(Новый Структура("НачальныйАдрес, КонечныйАдрес", АдресВторойТочки, АдресЧетвертойТочки));
		НайденнаяСтрока = ИскомыеСтроки[0];
		ПоследняяСтрока.РасстояниеОтПредыдущейТочки = НайденнаяСтрока.Расстояние;
		ПоследняяСтрока.ВремяОтПредыдущейТочки = НайденнаяСтрока[ВремяДеньНедели];
		ПоследняяСтрока.РасстояниеОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(ПоследняяСтрока.РасстояниеОтПредыдущейТочки);
		ПоследняяСтрока.ВремяОтПредыдущейТочкиПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ПоследняяСтрока.ВремяОтПредыдущейТочки);
		ПоследняяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ПоследняяСтрока.ВремяОтдыхаВПутиОтПредыдущейТочки);
		
		сткИзменений = СформироватьИзмененияДляКлиентскихВызовов(Истина, НайденнаяСтрока);
	    СписокДействий.Добавить(сткИзменений);
		ИзменитьТекущиеПоказателиРейса(НайденнаяСтрока, 1);
	КонецЕсли;
	
	ПересчитатьВременнойГрафикМаршрута(, СписокДействий);
	
	сткИзменений = Новый Структура;
	сткИзменений.Вставить("ИмяПроцедуры", "ИзменитьИконкуМаркера"); 
	сткИзменений.Вставить("ИндексВМассиве", текСтрока.ИндексВМассиве); 
	Если текСтрока.ПопадаетВоВременноеОкно Тогда
		текДанные = ДанныеСтандартнойТочки;
	Иначе
		текДанные = ДанныеНарушенийПоВремени;
	КонецЕсли;
	сткИзменений.Вставить("Данные", ("""" + Строка(текСтрока.НомерСтроки) + """," + текДанные));
	сткИзменений.Вставить("Режим", 1); 
	СписокДействий.Добавить(сткИзменений);
	
	сткИзменений = Новый Структура;
	сткИзменений.Вставить("ИмяПроцедуры", "ИзменитьИконкуМаркера"); 
	сткИзменений.Вставить("ИндексВМассиве", ЗамещаемаяСтрока.ИндексВМассиве); 
	Если ЗамещаемаяСтрока.ПопадаетВоВременноеОкно Тогда
		текДанные = ДанныеСтандартнойТочки;
	Иначе
		текДанные = ДанныеНарушенийПоВремени;
	КонецЕсли;
	сткИзменений.Вставить("Данные", ("""" + Строка(ЗамещаемаяСтрока.НомерСтроки) + """," + текДанные));
	сткИзменений.Вставить("Режим", 1); 
	СписокДействий.Добавить(сткИзменений);
	
	Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграмма" Тогда
		ОбновитьДанныеНаДиаграмме();
	КонецЕсли;
	
КонецПроцедуры // ПоменятьМестамиТочкиВМаршруте()

&НаКлиенте
Процедура ЦентрироватьТочкиНаКарте(Координаты)
	
	ВыполнитьПроцедуруHTML("FillArrayPoint(""" + Координаты + """)");
	ВыполнитьПроцедуруHTML("fitMapToBounds()");
	
	//мсвКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Координаты, ";");
	//Для Сч = 0 По (мсвКоординат.Количество() / 2) - 1 Цикл 
	//	ВыполнитьПроцедуруHTML("addPoint(" + мсвКоординат[Сч * 2] + ", " + мсвКоординат[(Сч * 2) + 1] + ")");
	//КонецЦикла;
	//ВыполнитьПроцедуруHTML("fitMapToBounds()");
	
КонецПроцедуры // ЦентрироватьНерассчитанныйРейсНаКарте()

&НаКлиенте
Функция НовыйМаршрутИРаботыПоРейсу()
	
	сткРезультат = Новый Структура();
	
	мсвМаршрут = Новый Массив;
	
	мсвИдентификаторыПройденныхТочек = Новый Массив;
	
	й = 1;
	Для каждого Строка Из Маршрут Цикл
		
		Если НЕ Строка.Пройдена Тогда
			сткТочка = Новый Структура("Идентификатор, 
										|СтрокаНомер, 
										|Предшественники, 
										|ПлановоеПрибытие, 
										|ПлановоеУбытие,
										|ВремяРабот,
										|ВременноеОкноНачало,
										|ВременноеОкноОкончание,
										|РасстояниеОтПредыдущейТочки,
										|ВремяОтПредыдущейТочки,
										|ВремяОжиданияВТочке,
										|ВремяОжиданияВТочкеПослеРабот,
										|НеИспользоватьАвторасчетУбытияИзТочки,
										|ВремяРаботПредставление,
										|ВремяОжиданияВТочкеПредставление,
										|ВремяОжиданияВТочкеПослеРаботПредставление,
										|ПопадаетВоВременноеОкно,
										|ВременноеОкноПредставление,
										|РасстояниеОтПредыдущейТочкиПредставление,
										|ВремяОтПредыдущейТочкиПредставление,
										|ВремяОтдыхаВПутиОтПредыдущейТочки,
										|ВремяОтдыхаВПутиОтПредыдущейТочкиПредставление,
										|Адрес,
										|ТипТочки,
										|ИндексКартинки");
			ЗаполнитьЗначенияСвойств(сткТочка, Строка);
			сткТочка.ВремяРабот = сткТочка.ВремяРабот/3600; 
			сткТочка.ВремяОжиданияВТочке = сткТочка.ВремяОжиданияВТочке/3600;
			сткТочка.ВремяОжиданияВТочкеПослеРабот = сткТочка.ВремяОжиданияВТочкеПослеРабот/3600;
			сткТочка.ВремяОтПредыдущейТочки = сткТочка.ВремяОтПредыдущейТочки/3600;
			сткТочка.ВремяОтдыхаВПутиОтПредыдущейТочки = сткТочка.ВремяОтдыхаВПутиОтПредыдущейТочки/3600;
			сткТочка.СтрокаНомер = й;
			мсвМаршрут.Добавить(сткТочка);
		Иначе
			мсвИдентификаторыПройденныхТочек.Добавить(Строка.Идентификатор);
		КонецЕсли;
		
		й = й + 1;
	КонецЦикла;
	
	сткРезультат.Вставить("Маршрут", мсвМаршрут);
	
	мсвРаботы = Новый Массив;
	
	Для каждого Строка Из Работы Цикл
		
		Если НЕ мсвИдентификаторыПройденныхТочек.Найти(Строка.Идентификатор) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		сткРабота = Новый Структура("ИдентификаторРаботы,
										|ВремяРабот,
										|ВременноеОкноНачало,
										|ВременноеОкноОкончание,
										|ВремяОжидания,
										|ПлановоеВремяНачалаРабот,
										|ВремяРаботПредставление,
										|ВремяОжиданияПредставление,
										|ПопадаетВоВременноеОкно,
										|ВременноеОкноПредставление");
		ЗаполнитьЗначенияСвойств(сткРабота, Строка);
		сткРабота.ВремяРабот    = сткРабота.ВремяРабот/3600; 
		сткРабота.ВремяОжидания = сткРабота.ВремяОжидания/3600;

		мсвРаботы.Добавить(сткРабота);

	КонецЦикла;
	
	сткРезультат.Вставить("Работы", мсвРаботы);

	Возврат сткРезультат;
	
КонецФункции

#КонецОбласти

#Область ДиаграммаГанта

&НаКлиенте
Процедура ОбновитьДиаграммуНаКлиенте()  
	
	Если Элементы.ГруппаВерхПраво.ТекущаяСтраница.Имя = "ГруппаДиаграмма" Тогда
		ОбновитьДанныеНаДиаграмме();
	КонецЕсли;
	
КонецПроцедуры // ОбновитьДиаграммуНаКлиенте()

&НаСервере
Процедура ОбновитьШкалу(Форма)

	ШкалаВремениЭлементы = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы;
	ПервыйЭлементШкалы = ШкалаВремениЭлементы[0];
	Для Сч = -ШкалаВремениЭлементы.Количество()+1 по -1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[-Сч]);
	КонецЦикла;
	
	// День, час
	ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
	ПервыйЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяцаДеньНедели;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
	ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
	
КонецПроцедуры // ОбновитьШкалу()

&НаСервере
Процедура УстановитьПараметрыДиаграммыПоУмолчанию()

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.ПоддержкаМасштаба               = ПоддержкаМасштабаДиаграммыГанта.Авто;
	Диаграмма.Анимация                        = АнимацияДиаграммы.Использовать;
	Диаграмма.ВертикальнаяПрокрутка           = Истина;
	Диаграмма.ОтображениеИнтервала            = ОтображениеИнтервалаДиаграммыГанта.Градиент;
	Диаграмма.ОтображатьПустыеЗначения        = Ложь;
	Диаграмма.ОтображатьЗаголовок             = Ложь;

КонецПроцедуры // УстановитьПараметрыДиаграммыПоУмолчанию()

&НаСервере
Процедура ОбновитьДанныеНаДиаграмме()
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ИскомыеСтроки = Маршрут.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если ИскомыеСтроки.Количество() < 2 Тогда
		Диаграмма.Обновление = Истина;
		Возврат;
	КонецЕсли;
	
	СерияДиаграммыРаботы = Диаграмма.УстановитьСерию("Работы");
	СерияДиаграммыРаботы.Цвет  = WebЦвета.Синий;
	СерияДиаграммыРаботы.Текст = НСтр("ru = 'Работы в точке'");

	НачалоПериода    = '00010101';
	ОкончаниеПериода = '00010101';
	СтарыйИнтервал = Неопределено;
	ЕстьИнтервалОжидания = Ложь;
	ЕстьИнтервалСНепопаданиемВоВременноеОкно = Ложь;
	Для каждого текТочка Из Маршрут Цикл
		Если текТочка.Пометка Тогда
			ТочкаДиаграммы = Диаграмма.УстановитьТочку(текТочка.Адрес);
			Если ЗначениеЗаполнено(текТочка.Гараж) Тогда
				ТочкаДиаграммы.Картинка = БиблиотекаКартинок.упГараж;
			Иначе	
				Если текТочка.ПопадаетВоВременноеОкно Тогда
					ТочкаДиаграммы.Картинка = БиблиотекаКартинок.упПериодПопадаетВоВременноеОкно;
				Иначе
					ТочкаДиаграммы.Картинка = БиблиотекаКартинок.упПериодНеПопадаетВоВременноеОкно;
				КонецЕсли;
			КонецЕсли;
			
			ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаДиаграммы, СерияДиаграммыРаботы);
			Если текТочка.ВремяОжиданияВТочке > 0 Тогда
				Интервал = ЗначениеПериод.Добавить();
				Интервал.Начало = текТочка.ПлановоеПрибытие;
				Интервал.Конец  = текТочка.ПлановоеПрибытие + текТочка.ВремяОжиданияВТочке;
				Интервал.Цвет = WebЦвета.Желтый;
				ЕстьИнтервалОжидания = Истина;
				текИнтервалНачало = Интервал;
			КонецЕсли;	
				
			Интервал = ЗначениеПериод.Добавить();
			Интервал.Начало      = текТочка.ПлановоеПрибытие + текТочка.ВремяОжиданияВТочке;
			Интервал.Конец       = текТочка.ПлановоеУбытие;	
			Если текТочка.ПопадаетВоВременноеОкно Тогда
				Интервал.Цвет = WebЦвета.Синий;
			Иначе
				Интервал.Цвет = WebЦвета.Красный;
				ЕстьИнтервалСНепопаданиемВоВременноеОкно = Истина;
			КонецЕсли;
			текИнтервалОкончание = Интервал;
				
			Если текТочка.ВремяОжиданияВТочке = 0 Тогда
				текИнтервалНачало = Интервал;
			КонецЕсли;
			
			Если Не СтарыйИнтервал = Неопределено Тогда
				СтарыйИнтервал.Добавить(текИнтервалНачало);
			КонецЕсли;
			СтарыйИнтервал = текИнтервалОкончание;
			
			НачалоПериода    = ?(НачалоПериода = '00010101', текТочка.ПлановоеПрибытие, Мин(НачалоПериода, текТочка.ПлановоеПрибытие));
			ОкончаниеПериода = Макс(ОкончаниеПериода, текТочка.ПлановоеУбытие);
		КонецЕсли;
	КонецЦикла; 
	
	Если ЕстьИнтервалОжидания Тогда
		СерияДиаграммыОжидание = Диаграмма.УстановитьСерию("Ожидание");
		СерияДиаграммыОжидание.Цвет  = WebЦвета.Желтый;
		СерияДиаграммыОжидание.Текст = НСтр("ru = 'Ожидание в точке'");
	КонецЕсли;
	Если ЕстьИнтервалСНепопаданиемВоВременноеОкно Тогда
		СерияДиаграммыНепопаданиеВоВременноеОкно = Диаграмма.УстановитьСерию("РаботыНепопаданиеВоВременноеОкно");
		СерияДиаграммыНепопаданиеВоВременноеОкно.Цвет  = WebЦвета.Красный;
		СерияДиаграммыНепопаданиеВоВременноеОкно.Текст = НСтр("ru = 'Работы, непопадающие во временное окно'");
	КонецЕсли;
	
	Диаграмма.ИнтервалыФона.Очистить();
	Если ТипЗнч(ТранспортноеСредство) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
		ГрафикРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "ГрафикРаботы");
		Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
			РасписаниеРаботы = ГрафикиРаботы.РасписанияРаботыНаПериод(ТранспортноеСредство.ГрафикРаботы, НачалоПериода, ОкончаниеПериода); 
			Для каждого текПериод Из РасписаниеРаботы Цикл
				текНачалоПериода = ?(ЗначениеЗаполнено(текПериод.ВремяНачала), текПериод.ДатаГрафика + Час(текПериод.ВремяНачала)*3600 + Минута(текПериод.ВремяНачала)*60, текПериод.ДатаГрафика);
				текОкончаниеПериода = ?(ЗначениеЗаполнено(текПериод.ВремяОкончания), текПериод.ДатаГрафика + Час(текПериод.ВремяОкончания)*3600 + Минута(текПериод.ВремяОкончания)*60, КонецДня(текПериод.ДатаГрафика));
				ИнтервалФона = Диаграмма.ИнтервалыФона.Добавить(текНачалоПериода, текОкончаниеПериода);
				ИнтервалФона.Цвет = ЦветИнтервалаФона;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
	
	Диаграмма.УстановитьПолныйИнтервал(НачалоПериода, ОкончаниеПериода);
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры // ОбновитьДанныеНаДиаграмме()

&НаКлиенте
Процедура ОбработатьВводПроизвольногоПериода(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		СтрокаРабота = Работы.НайтиПоИдентификатору(ИдентификаторДляИзмененияВременногоОкна);
		Если НЕ СтрокаРабота = Неопределено Тогда
			
			ВременноеОкноНачало 	= Неопределено;
			Если НЕ РезультатЗакрытия.Свойство("НачалоПериода", ВременноеОкноНачало) Тогда
				ВременноеОкноНачало = '00010101';
			КонецЕсли;
			ВременноеОкноОкончание 	= Неопределено;
			Если НЕ РезультатЗакрытия.Свойство("КонецПериода", ВременноеОкноОкончание) Тогда
				ВременноеОкноОкончание = '00010101';
			КонецЕсли;
			
			СтрокаРабота.ВременноеОкноНачало 		= ВременноеОкноНачало;
			СтрокаРабота.ВременноеОкноОкончание 	= ВременноеОкноОкончание;
			СтрокаРабота.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(ВременноеОкноНачало, ВременноеОкноОкончание);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВводПроизвольногоПериодаМаршрут(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		СтрокаМаршрут = Маршрут.НайтиПоИдентификатору(ИдентификаторДляИзмененияВременногоОкна);
		Если НЕ СтрокаМаршрут = Неопределено Тогда
			
			ВременноеОкноНачало 	= Неопределено;
			Если НЕ РезультатЗакрытия.Свойство("НачалоПериода", ВременноеОкноНачало) Тогда
				ВременноеОкноНачало = '00010101';
			КонецЕсли;
			ВременноеОкноОкончание 	= Неопределено;
			Если НЕ РезультатЗакрытия.Свойство("КонецПериода", ВременноеОкноОкончание) Тогда
				ВременноеОкноОкончание = '00010101';
			КонецЕсли;
			
			СтрокаМаршрут.ВременноеОкноНачало 		= ВременноеОкноНачало;
			СтрокаМаршрут.ВременноеОкноОкончание 	= ВременноеОкноОкончание;
			СтрокаМаршрут.ВременноеОкноПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВременногоОкна(ВременноеОкноНачало, ВременноеОкноОкончание);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьОжиданиеПослеРабот(Команда)
	
	упРейсКлиентСервер.ПереключитьОжиданиеПослеРабот(Элементы.Маршрут.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти 

#Область ДиаграммаЗагрузки

&НаСервере
Процедура ОбновитьДанныеНаДиаграммеЗагрузки()
	
	Если ГрузоваяЕдиница = 0 Тогда
		КонтролируемыйПоказатель = "Масса";
	ИначеЕсли ГрузоваяЕдиница = 1 Тогда
		КонтролируемыйПоказатель = "Объем";
	ИначеЕсли ГрузоваяЕдиница = 2 Тогда
		КонтролируемыйПоказатель = "КоличествоМест";
	ИначеЕсли ГрузоваяЕдиница = 3 Тогда
		КонтролируемыйПоказатель = "КоличествоЗагрузочныхМетров";
	КонецЕсли; 
	ИскомыеПредставления = ПредставлениеПоказателей.НайтиСтроки(Новый Структура("Показатель", ГрузоваяЕдиница));
	ЕдИзм = ИскомыеПредставления[0].Представление;
	
	соотПоказателиЗагрузки = упРасчетПоказателейЗагрузки.РассчитатьЗагрузкуВТочкахМаршрутаПодробно(Маршрут.Выгрузить(Новый Структура("Пометка", Истина)), Работы.Выгрузить(Новый Структура("Пометка", Истина)));
	
	ДиаграммаЗагрузки.Обновление = Ложь;
	ДиаграммаЗагрузки.Очистить();
	ДиаграммаЗагрузки.ОтображатьЛегенду = Истина;
	ДиаграммаЗагрузки.ОтображатьЗаголовок = Ложь;
	
	СерияРазгрузка = ДиаграммаЗагрузки.УстановитьСерию("Разгрузка");
	СерияРазгрузка.Текст = НСтр("ru = 'Разгрузка'");
	СерияРазгрузка.Цвет = WebЦвета.Красный;
	
	СерияПогрузка = ДиаграммаЗагрузки.УстановитьСерию("Погрузка");
	СерияПогрузка.Текст = НСтр("ru = 'Погрузка'");
	СерияПогрузка.Цвет = WebЦвета.Зеленый;
	
	СерияВсего = ДиаграммаЗагрузки.УстановитьСерию("Всего");
	СерияВсего.Текст = НСтр("ru = 'Всего (после выезда из точки)'");
	СерияВсего.Цвет = WebЦвета.Оранжевый; 
	
	Всего = 0;
	Для каждого текТочка Из Маршрут Цикл
		
		сткПоказатели = соотПоказателиЗагрузки.Получить(Строка(текТочка.Идентификатор));
		Если сткПоказатели = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		НоваяТочка = ДиаграммаЗагрузки.УстановитьТочку(текТочка.Идентификатор);
		НоваяТочка.Текст = текТочка.НомерСтроки;
		
		Всего = Всего + сткПоказатели[СтрШаблон("%1Погрузка", КонтролируемыйПоказатель)] - сткПоказатели[СтрШаблон("%1Разгрузка", КонтролируемыйПоказатель)];

		Подсказка = НСтр(СтрШаблон("ru = '%1 (%2 %3)'", текТочка.АдресПредставление, сткПоказатели[СтрШаблон("%1Погрузка", КонтролируемыйПоказатель)], ЕдИзм));
		ДиаграммаЗагрузки.УстановитьЗначение(НоваяТочка, СерияПогрузка, сткПоказатели[СтрШаблон("%1Погрузка", КонтролируемыйПоказатель)], текТочка.АдресПредставление, Подсказка);
		
		Подсказка = НСтр(СтрШаблон("ru = '%1 (%2 %3)'", текТочка.АдресПредставление, сткПоказатели[СтрШаблон("%1Разгрузка", КонтролируемыйПоказатель)], ЕдИзм));
		ДиаграммаЗагрузки.УстановитьЗначение(НоваяТочка, СерияРазгрузка, сткПоказатели[СтрШаблон("%1Разгрузка", КонтролируемыйПоказатель)], текТочка.АдресПредставление, Подсказка);
		
		Подсказка = НСтр(СтрШаблон("ru = '%1 (%2 %3)'", текТочка.АдресПредставление, Всего, ЕдИзм));
		ДиаграммаЗагрузки.УстановитьЗначение(НоваяТочка, СерияВсего, Всего, текТочка.АдресПредставление, Подсказка);

	КонецЦикла; 
	
	ДиаграммаЗагрузки.Обновление = Истина;
	
КонецПроцедуры // ОбновитьДанныеНаДиаграммеЗагрузки()
	
#КонецОбласти 

&НаСервере
Процедура РазделитьРаботыВТочкеНаСервере(ИдентификаторТочки, СписокОпераций)
	Отказ = Ложь;
	упПрохождениеТочки.РазделитьРаботыВТочке(, ИдентификаторТочки, СписокОпераций, Маршрут, Работы, Отказ);
КонецПроцедуры

&НаСервере
Функция РазрешеноРазделятьРаботы(СтрокаТочка)
	Результат = упПрохождениеТочки.РазрешеноРазделятьРаботы(СтрокаТочка, Работы);
	Возврат Результат;		
КонецФункции

&НаКлиенте
Процедура ПодобратьИДобавитьКонтрольныеТочки(Команда)
	Если ЕстьПодобранныеКонтрольныеТочкиНаСервере() Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросДобавитьПодобранныеКонтрольныеТочки", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Добавить подобранные контрольные точки?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ТекстСообщения = Нстр("ru = 'Не удалось подобрать правило включения контрольных точек. '");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЕстьПодобранныеКонтрольныеТочкиНаСервере()
	
	Результат = Ложь;
	
	ПодобранныеКонтрольныеТочки.Очистить();
	ПравилаПодбораТочек.Очистить();
	
	сткКонтрольныеТочки = упРейс.ПодобратьКонтрольныеТочкиМаршрута(Рейс, Маршрут);
	
	Если сткКонтрольныеТочки.ЕстьТочки Тогда
		ПодобранныеКонтрольныеТочки.Загрузить(сткКонтрольныеТочки.Точки);
		ПравилаПодбораТочек.Загрузить(сткКонтрольныеТочки.ПравилаВключенияПоЗвеньям);
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ДобавитьПодобранныеКонтрольныеТочкиНаСервере()
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("МестоВызова", "ФормаРедактированияРейса");
	ДополнительныеПараметры.Вставить("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек);
	упРейс.ДобавитьПодобранныеКонтрольныеТочкиВМаршрут(Рейс, Маршрут, ПодобранныеКонтрольныеТочки, ПравилаПодбораТочек,ДополнительныеПараметры); 
		
	Сч = 1;
	Для каждого текСтрока Из Маршрут Цикл
		текСтрока.НомерСтроки = Сч;
		Сч = Сч + 1;
		текСтрока.ИндексВМассиве = 0;
	КонецЦикла; 
	РеквизитыАдреса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Маршрут[0].Адрес, "ИспользоватьВремяУбытияИзТочки, ВремяУбытияИзТочки, ДопустимоеОтклонениеУбытияИзТочки, ЧасовойПояс");
	// Костыль для ситуации гаража перед складом с одним адресом
	ПересчетСоВторойТочки = Истина
						И Маршрут[0].ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления
						И Маршрут.Количество() > 1
						И Не Маршрут[1].НеИспользоватьАвторасчетУбытияИзТочки
						И Маршрут[1].Адрес = Маршрут[0].Адрес
						И РеквизитыАдреса.ИспользоватьВремяУбытияИзТочки
						;
	ПересчитатьВременнойГрафикМаршрута(,,,, ПересчетСоВторойТочки, ДополнительныеПараметры);
	АктуализироватьРасстоянияМеждуАдресамиНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросДобавитьПодобранныеКонтрольныеТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДобавитьПодобранныеКонтрольныеТочкиНаСервере();
		// клиентские вызовы
		ОчиститьОбъекты();
		ОтобразитьОбъектыНаКарте();
		ОтобразитьМаршрут();
		
		ОбновитьДиаграммуНаКлиенте();
		ОбновитьПредставлениеПоказателейНаФорме(Ложь, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 