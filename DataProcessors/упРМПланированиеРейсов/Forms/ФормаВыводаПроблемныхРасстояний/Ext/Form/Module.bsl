
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.АдресТаблицыАдресов) Тогда
		ПериодАктуальности = упСервисныеФункции.ПолучитьЗначениеКонстанты("упПериодАктуальностиРасчета");
		ОграниченияМаршрутизатора = упМаршрутизация.ПолучитьТаблицуОграниченийМаршрутизатора();
		ТекстЗапроса = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаАдресов.Адрес КАК Справочник.упАдреса) КАК Адрес,
		|	ТаблицаАдресов.ПриоритетноеПостроениеМассиваРасстояний КАК ПриоритетноеПостроениеМассиваРасстояний,
		|	ТаблицаАдресов.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
		|ПОМЕСТИТЬ ТаблицаАдресов
		|ИЗ
		|	&ТаблицаАдресов КАК ТаблицаАдресов
		|;ВЫБРАТЬ
		|	упГеографическиеЗоны.Ссылка КАК Зона,
		|	ТаблицаАдресов.Адрес КАК Адрес,
		|	ТаблицаАдресов.Адрес.Широта КАК Широта,
		|	ТаблицаАдресов.Адрес.Долгота КАК Долгота,
		|	МАКСИМУМ(ТаблицаАдресов.ПриоритетноеПостроениеМассиваРасстояний) КАК ПриоритетноеПостроениеМассиваРасстояний,
		|	МАКСИМУМ(ТаблицаАдресов.ДопОграниченияСитиГИД) КАК ДопОграниченияСитиГИД
		|ПОМЕСТИТЬ втТочки
		|ИЗ
		|	ТаблицаАдресов КАК ТаблицаАдресов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
		|		ПО упГеографическиеЗоны.Ссылка = упИерархияГеографическихЗон.Родитель
		|	ПО ТаблицаАдресов.Адрес.ГеографическаяЗона = упИерархияГеографическихЗон.Зона
		|ГДЕ ИСТИНА
		|	//И упГеографическиеЗоны.РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны
		|	И (ТаблицаАдресов.Адрес.Долгота <> 0
		|			ИЛИ ТаблицаАдресов.Адрес.Широта <> 0)
		|	И НЕ ТаблицаАдресов.Адрес.ПометкаУдаления
		|	И НЕ упГеографическиеЗоны.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	упГеографическиеЗоны.Ссылка,
		|	ТаблицаАдресов.Адрес
		|;
		|ВЫБРАТЬ ОграниченияМаршрутизатора.Ограничение ПОМЕСТИТЬ ОграниченияМаршрутизатора ИЗ &ОграниченияМаршрутизатора КАК ОграниченияМаршрутизатора
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТочкиНачальные.Адрес КАК АдресОтправления,
		|	втТочкиКонечные.Адрес КАК АдресПолучения,
		|	втТочкиНачальные.ДопОграниченияСитиГИД И втТочкиКонечные.ДопОграниченияСитиГИД КАК ДопОграниченияСитиГИД
		|ПОМЕСТИТЬ втРебра
		|ИЗ
		|	втТочки КАК втТочкиНачальные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТочки КАК втТочкиКонечные
		|		ПО втТочкиНачальные.Адрес <> втТочкиКонечные.Адрес
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АдресОтправления,
		|	АдресПолучения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втРебра.АдресОтправления,
		|	втРебра.АдресПолучения,
		|	ОграниченияМаршрутизатора.Ограничение,
		|	упРасстоянияМеждуТочками.ПопыткаРасчета,
		|	упРасстоянияМеждуТочками.Ошибка,
		|	ЕСТЬNULL(упРасстоянияМеждуТочками.ПытатьсяРассчитатьСнова, ИСТИНА) ПытатьсяРассчитатьСнова,
		|	ЕСТЬNULL(упРасстоянияМеждуТочками.АвтоматическийРасчет, ИСТИНА) АвтоматическийРасчет,
		|	ВЫБОР
		|		КОГДА упРасстоянияМеждуТочками.ПопыткаРасчета ЕСТЬ NULL ИЛИ упРасстоянияМеждуТочками.ПопыткаРасчета > 0
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НеРассчитано,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(упРасстоянияМеждуТочками.ДатаИзменения, &ДатаРасчета) < &ДатаРасчета
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Просрочено,
		|	ВЫБОР
		|		КОГДА упРасстоянияМеждуТочками.АвтоматическийРасчет ЕСТЬ NULL
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПоискЗаписи
		|ПОМЕСТИТЬ втРезультат
		|ИЗ
		|	втРебра КАК втРебра
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОграниченияМаршрутизатора
		|	ПО (втРебра.ДопОграниченияСитиГИД)
		|		ИЛИ ОграниченияМаршрутизатора.Ограничение = ЗНАЧЕНИЕ(Перечисление.упОграниченияМаршрутизаторов.ПустаяСсылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочками
		|	ПО втРебра.АдресОтправления.Широта = упРасстоянияМеждуТочками.НачальнаяШирота
		|			И втРебра.АдресОтправления.Долгота = упРасстоянияМеждуТочками.НачальнаяДолгота
		|			И втРебра.АдресПолучения.Широта = упРасстоянияМеждуТочками.КонечнаяШирота
		|			И втРебра.АдресПолучения.Долгота = упРасстоянияМеждуТочками.КонечнаяДолгота
		|			И ОграниченияМаршрутизатора.Ограничение = упРасстоянияМеждуТочками.Ограничение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРезультат.*
		|ИЗ
		|	втРезультат КАК втРезультат";
		
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
		ЗапросПакета = ПакетЗапросов[ПакетЗапросов.Количество() - 1];
		Если Параметры.ВыводитьУстаревшие Тогда
			ЗапросПакета.Операторы[0].Отбор.Добавить("Просрочено > 0");
		Иначе
			ЗапросПакета.Операторы[0].Отбор.Добавить("НеРассчитано > 0");;
		КонецЕсли;
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаАдресов", ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыАдресов));
		Запрос.УстановитьПараметр("ОграниченияМаршрутизатора", ОграниченияМаршрутизатора);
		Если ПериодАктуальности = 0 Тогда 
			Запрос.УстановитьПараметр("ДатаРасчета", '00010101');
		Иначе	
			Запрос.УстановитьПараметр("ДатаРасчета", ТекущаяДатаСеанса() - (ПериодАктуальности * 86400));
		КонецЕсли;
		Запрос.Текст = ТекстЗапроса;
		

		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(Список.Добавить(), Выборка);
			КонецЦикла; 
		КонецЕсли;
	Иначе
		ТаблицыПорций = ПолучитьИзВременногоХранилища(Параметры.АдресМассиваТаблицРасстояний).ТаблицыПорций;
		ТаблицаПарКоординат = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		Для Каждого ТаблицаПорции Из ТаблицыПорций Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПорции, ТаблицаПарКоординат);
		КонецЦикла;
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.КонечнаяДолгота КАК КонечнаяДолгота,
		|	Т.КонечнаяШирота КАК КонечнаяШирота,
		|	Т.НачальнаяДолгота КАК НачальнаяДолгота,
		|	Т.НачальнаяШирота КАК НачальнаяШирота,
		|	Т.Ограничение КАК Ограничение
		|ПОМЕСТИТЬ ТаблицаПарКоординат
		|ИЗ
		|	&ТаблицаПарКоординат КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПарКоординат.АдресПолучения КАК АдресПолучения,
		|	ТаблицаПарКоординат.АдресОтправления КАК АдресОтправления,
		|	ТаблицаПарКоординат.Ограничение КАК Ограничение,
		|	ЕСТЬNULL(упРасстоянияМеждуТочкамиТ.ПопыткаРасчета, 0) КАК ПопыткаРасчета,
		|	ЕСТЬNULL(упРасстоянияМеждуТочкамиТ.ПытатьсяРассчитатьСнова, ИСТИНА) КАК ПытатьсяРассчитатьСнова,
		|	ЕСТЬNULL(упРасстоянияМеждуТочкамиТ.Ошибка, """") КАК Ошибка,
		|	ЕСТЬNULL(упРасстоянияМеждуТочкамиТ.АвтоматическийРасчет, ЛОЖЬ) КАК АвтоматическийРасчет,
		|	ВЫБОР
		|		КОГДА упРасстоянияМеждуТочкамиТ.АвтоматическийРасчет ЕСТЬ NULL
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПоискЗаписи
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(КонечныйАдрес.Ссылка) КАК АдресПолучения,
		|		МАКСИМУМ(НачальныйАдрес.Ссылка) КАК АдресОтправления,
		|		ТаблицаПарКоординат.КонечнаяДолгота КАК КонечнаяДолгота,
		|		ТаблицаПарКоординат.КонечнаяШирота КАК КонечнаяШирота,
		|		ТаблицаПарКоординат.НачальнаяДолгота КАК НачальнаяДолгота,
		|		ТаблицаПарКоординат.НачальнаяШирота КАК НачальнаяШирота,
		|		ТаблицаПарКоординат.Ограничение КАК Ограничение
		|	ИЗ
		|		ТаблицаПарКоординат КАК ТаблицаПарКоординат
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК КонечныйАдрес
		|			ПО (ИСТИНА)
		|				И (КонечныйАдрес.Долгота = ТаблицаПарКоординат.КонечнаяДолгота)
		|				И (КонечныйАдрес.Широта = ТаблицаПарКоординат.КонечнаяШирота)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК НачальныйАдрес
		|			ПО (ИСТИНА)
		|				И (НачальныйАдрес.Долгота = ТаблицаПарКоординат.НачальнаяДолгота)
		|				И (НачальныйАдрес.Широта = ТаблицаПарКоординат.НачальнаяШирота)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ТаблицаПарКоординат.КонечнаяДолгота,
		|		ТаблицаПарКоординат.КонечнаяШирота,
		|		ТаблицаПарКоординат.НачальнаяДолгота,
		|		ТаблицаПарКоординат.НачальнаяШирота,
		|		ТаблицаПарКоординат.Ограничение) КАК ТаблицаПарКоординат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасстоянияМеждуТочками КАК упРасстоянияМеждуТочкамиТ
		|		ПО ТаблицаПарКоординат.КонечнаяДолгота = упРасстоянияМеждуТочкамиТ.КонечнаяДолгота
		|			И ТаблицаПарКоординат.КонечнаяШирота = упРасстоянияМеждуТочкамиТ.КонечнаяШирота
		|			И ТаблицаПарКоординат.НачальнаяДолгота = упРасстоянияМеждуТочкамиТ.НачальнаяДолгота
		|			И ТаблицаПарКоординат.НачальнаяШирота = упРасстоянияМеждуТочкамиТ.НачальнаяШирота
		|			И ТаблицаПарКоординат.Ограничение = упРасстоянияМеждуТочкамиТ.Ограничение";
		Запрос.УстановитьПараметр("ТаблицаПарКоординат", ТаблицаПарКоординат);
		Результат = Запрос.Выполнить().Выгрузить();
		Список.Загрузить(Результат);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Список.ТекущийЭлемент = Неопределено;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура СписокПриАктивизацииПоля(Элемент)
	
	текСтрока = Элементы.Список.ТекущиеДанные;
	текЭлемент = Элементы.Список.ТекущийЭлемент;
	Если текСтрока <> Неопределено И текЭлемент <> Неопределено Тогда
		Если текЭлемент.Имя = "СписокПоискЗаписи" Тогда
			Если текСтрока.ПоискЗаписи = 0 Тогда
				текСтрока = Элементы.Список.ТекущиеДанные;
				сткПараметры = Новый Структура;
				сткОтбор = Новый Структура;
				сткДанные = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(текСтрока.АдресОтправления, "Широта, Долгота");
				сткОтбор.Вставить("НачальнаяШирота", сткДанные.Широта);
				сткОтбор.Вставить("НачальнаяДолгота", сткДанные.Долгота);
				сткДанные = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(текСтрока.АдресПолучения, "Широта, Долгота");
				сткОтбор.Вставить("КонечнаяШирота", сткДанные.Широта);
				сткОтбор.Вставить("КонечнаяДолгота", сткДанные.Долгота);
				сткОтбор.Вставить("Ограничение", текСтрока.Ограничение);
				сткПараметры.Вставить("Отбор", сткОтбор);
				ОткрытьФорму("РегистрСведений.упРасстоянияМеждуТочками.ФормаСписка", сткПараметры, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);				
			КонецЕсли; 
			Элементы.Список.ТекущийЭлемент = Элементы.СписокАдресОтправления;			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Список.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если Поле = Элементы.СписокАдресПолучения Тогда
		ПоказатьЗначение(,  ДанныеСтроки.АдресПолучения);
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Поле = Элементы.СписокАдресОтправления Тогда
		ПоказатьЗначение(, ДанныеСтроки.АдресОтправления);
		СтандартнаяОбработка = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 
