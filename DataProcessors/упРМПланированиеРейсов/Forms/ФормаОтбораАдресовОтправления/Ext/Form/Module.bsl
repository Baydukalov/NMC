
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.ЗаполнитьКоллекциюСвойств(Параметры.СписокОтправлений, СписокОтправлений);
	Для каждого текОтправление Из СписокОтправлений Цикл
		текОтправление.ПометкаНовое = текОтправление.Пометка;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Переопределение текущего элемента таблицы формы для изменения активизации поля при первичной загрузке.
	Элементы.СписокОтправлений.ТекущийЭлемент =  Элементы.СписокОтправленийАдрес;

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокОтправленийПриАктивизацииПоля(Элемент)
	
	текСтрока = Элементы.СписокОтправлений.ТекущиеДанные;
	текЭлемент = Элементы.СписокОтправлений.ТекущийЭлемент;
	Если текЭлемент.Имя = "СписокОтправленийПометкаНовое" Тогда
		текСтрока.ПометкаНовое = Не текСтрока.ПометкаНовое;
		Модифицированность = Истина;
				
		// изменяем текущий элемент для возможности его повторной активизации
		Элементы.СписокОтправлений.ТекущийЭлемент = Элементы.СписокОтправленийАдрес;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОтправленийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.СписокОтправлений.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		Если Поле.Имя = "СписокОтправленийАдрес" Тогда
			ОткрытьФорму("Справочник.упАдреса.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Адрес), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "СписокОтправленийВидАдреса" Тогда	
			ОткрытьФорму("Справочник.упВидыАдресов.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ВидАдреса), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
   мсвАдресов = Новый Массив; // массив адресов с измененной видимостью
   Для каждого текОтправление Из СписокОтправлений Цикл
	   Если текОтправление.ПометкаНовое <> текОтправление.Пометка Тогда
		   мсвАдресов.Добавить(текОтправление.Адрес);
	   КонецЕсли;
   КонецЦикла; 
   Закрыть(мсвАдресов);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти 