
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ТранспортыДляРасчетаРасстояний <> Неопределено Тогда
		ЭтаФорма.ТранспортыДляРасчетаРасстояний.ЗагрузитьЗначения(Параметры.ТранспортыДляРасчетаРасстояний);
	КонецЕсли;
	Если Параметры.ЗаданияДляРасчетаРасстояний <> Неопределено Тогда
		ЭтаФорма.ЗаданияДляРасчетаРасстояний.ЗагрузитьЗначения(Параметры.ЗаданияДляРасчетаРасстояний);
	КонецЕсли;
	ОбновитьПоказателиВсехРасстояний();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Ложь
		Или НеРассчитаноКоличество > 0 
		Или ПросроченоКоличество > 0 
	Тогда
		Если АвтоЗаполнитьБлижайшиеРасстояния Тогда
			ЗаполнитьБлижайшиеРасстояния("");
		КонецЕсли; 
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Все отрезки расстояний между адресами заданий на перевозку уже рассчитаны'"));
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПоказатьУстаревшие(Команда)
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("АдресТаблицыАдресов", АдресПросроченныхАдресов);
	сткПараметры.Вставить("ВыводитьУстаревшие"        , Истина);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаВыводаПроблемныхРасстояний", сткПараметры,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура Рассчитать(Команда)
	
	РассчитатьРасстояния();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьТолькоНерассчитанные(Команда)
	
	РассчитатьРасстояния(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьРасчет(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьРасстояния(ТолькоНеРассчитанные = Ложь)
	
	Если Ложь
		Или НеРассчитаноКоличество > 0 
		Или ПросроченоКоличество > 0 
	Тогда
		
		Если ТолькоНеРассчитанные Тогда
			АдресТаблицыАдресов = АдресНерассчитанныхАдресов;
		Иначе
			АдресТаблицыАдресов = АдресВсехАдресов;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(АдресТаблицыАдресов) Тогда
			спзИдентификаторов = Новый СписокЗначений;
			упМаршрутизация.РассчитатьРасстоянияПоКарте(АдресТаблицыАдресов, спзИдентификаторов, ТолькоНеРассчитанные, Ложь);
			
			Если спзИдентификаторов.Количество() > 0 Тогда 
				сткПараметры = Новый Структура;
				сткПараметры.Вставить("ИдентификаторыЗаданий" , спзИдентификаторов);
				сткПараметры.Вставить("ТекстЗавершения"       , НСтр("ru = 'Расчет массива всех расстояний завершен.'"));
				сткПараметры.Вставить("ЗаголовокЗавершения"   , НСтр("ru = 'Расчет всех расстояний'"));
				сткПараметры.Вставить("ЗакрыватьПриЗавершении", Истина);
				сткПараметры.Вставить("ИмяСобытияОповещения"  , "РасчетРастоянийВсе");
				ПараметрыОповещения = Новый Структура;
				ПараметрыОповещения.Вставить("ТипРасчета", "Все");
				ОписаниеОповещения = Новый ОписаниеОповещения("ВыполненияМногопоточногоПроцессаЗавершение", ЭтаФорма, ПараметрыОповещения);
				ОткрытьФорму("ОбщаяФорма.упХодВыполненияМногопоточногоПроцесса", сткПараметры,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			КонецЕсли;
			
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указаны адреса для расчета попарных расстояний.'"),,);
			Возврат;
			
		КонецЕсли; 
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при закрытии формы хода выполнения многопоточного процесса
//
Процедура ВыполненияМногопоточногоПроцессаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
	Если КоличествоИтераций = 0 Тогда
		Если ДополнительныеПараметры.ТипРасчета = "Все" Тогда
			Закрыть();
		ИначеЕсли ДополнительныеПараметры.ТипРасчета = "Ближайшие" Тогда
			ЗаполнитьБлижайшиеРасстояния();
			ОбновитьПоказателиВсехРасстояний();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПоказателиВсехРасстояний()
	
	Результат = Обработки.упРМПланированиеРейсов.ПолучитьСостояниеМассиваРасстояний(ЗаданияДляРасчетаРасстояний, ТранспортыДляРасчетаРасстояний);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Результат); 
	ЭтаФорма.АдресВсехАдресов = ПоместитьВоВременноеХранилище(Результат.ВсеАдреса, ЭтаФорма.УникальныйИдентификатор);
	ЭтаФорма.АдресНерассчитанныхАдресов = ПоместитьВоВременноеХранилище(Результат.НерассчитанныеРасстояния, ЭтаФорма.УникальныйИдентификатор);
	ЭтаФорма.АдресПросроченныхАдресов = ПоместитьВоВременноеХранилище(Результат.ПросроченныеРасстояния, ЭтаФорма.УникальныйИдентификатор);
	ЭтаФорма.АвтоЗаполнитьБлижайшиеРасстояния = Результат.НерассчитанныеРасстояния.Количество() < 1000;
	
	Элементы.ПоказатьНеРассчитанныеВсе.Видимость = НеРассчитаноКоличество > 0;
	Элементы.ПоказатьУстаревшие.Видимость = ПросроченоКоличество > 0;
	Элементы.ПоказатьНеРассчитанныеБлижайшие.Видимость = Ложь;
	
КонецПроцедуры // ВыполненияМногопоточногоПроцессаЗавершение()

&НаКлиенте
Процедура ЗаполнитьБлижайшиеРасстояния(Команда = Неопределено)
	
	ЗапуститьЗаполнениеБлижайшихРасстоянийНаСервере();
	ПодключитьОбработчикОжидания("ПроверитьЗавершениеЗаполненияБлижайших", 2, Истина);
	Элементы.ЗаполнитьБлижайшиеРасстояния.Доступность = Ложь;
	Элементы.РассчитатьБлижайшие.Доступность = Ложь;
	ЭтаФорма.АвтоповторРассчетаБлижайших = Команда = Неопределено;
		
КонецПроцедуры

&НаСервере
Процедура ЗапуститьЗаполнениеБлижайшихРасстоянийНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МАКСИМУМ(ЗаданиеОсновноеГрузовыеМеста.Ссылка) КАК ОсновноеЗадание,
	|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
	|	ЛОЖЬ КАК ЭтоСвязьПоТаре
	|ПОМЕСТИТЬ ЗависимыеЗадания
	|ИЗ
	|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
	|		ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
	|		ПО ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза
	|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.НомерВЦепиПоставок)
	|			И (ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза)
	|			И (ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(, ) КАК Статусы
	|		ПО (ИСТИНА)
	|			И (ЗаданиеЗависимоеГрузовыеМеста.Ссылка = Статусы.Документ)
	|			И (Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию))
	|ГДЕ
	|	ЗаданиеОсновноеГрузовыеМеста.Ссылка В(&Задания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МАКСИМУМ(ГрузовыеМестаТара.Ссылка),
	|	ГрузовыеМестаВТаре.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМестаВТаре
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМестаТара
	|		ПО ГрузовыеМестаВТаре.Тара = ГрузовыеМестаТара.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(, ) КАК Статусы
	|		ПО (ИСТИНА)
	|			И (ГрузовыеМестаТара.Ссылка = Статусы.Документ)
	|ГДЕ
	|	ИСТИНА
	|	И ГрузовыеМестаВТаре.Ссылка В(&Задания)
	|	И ГрузовыеМестаВТаре.Тара <> ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|	И (ЛОЖЬ
	|			ИЛИ Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию)
	|			ИЛИ Статусы.Статус ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрузовыеМестаВТаре.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗависимыеЗадания.ОсновноеЗадание.АдресПолучения КАК АдресПолучения,
	|	ЗависимыеЗадания.ЗависимоеЗадание.АдресОтправления КАК АдресОтправления
	|ИЗ
	|	ЗависимыеЗадания КАК ЗависимыеЗадания
	|ГДЕ
	|	НЕ ЗависимыеЗадания.ЭтоСвязьПоТаре
	|	И ЗависимыеЗадания.ОсновноеЗадание.АдресПолучения <> ЗависимыеЗадания.ЗависимоеЗадание.АдресОтправления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗависимыеЗадания.ЗависимоеЗадание.АдресПолучения,
	|	ЗависимыеЗадания.ОсновноеЗадание.АдресПолучения
	|ИЗ
	|	ЗависимыеЗадания КАК ЗависимыеЗадания
	|ГДЕ
	|	ЗависимыеЗадания.ЭтоСвязьПоТаре
	|	И ЗависимыеЗадания.ОсновноеЗадание.АдресПолучения <> ЗависимыеЗадания.ЗависимоеЗадание.АдресПолучения";
	Запрос.УстановитьПараметр("Задания", ЗаданияДляРасчетаРасстояний);
	НеобходимыеПарыАдресов = Новый ТаблицаЗначений; // Создаем таблицу, чтобы в ней не было левых доступных типов
	НеобходимыеПарыАдресов.Колонки.Добавить("АдресОтправления", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	НеобходимыеПарыАдресов.Колонки.Добавить("АдресПолучения", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Запрос.Выполнить().Выгрузить(), НеобходимыеПарыАдресов);

	ТолькоНеРассчитанные = Ложь;
	АдресРезультатаЗаполненияБлижайших = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
	//ТаблицаАдресов = ПолучитьИзВременногоХранилища(АдресНерассчитанныхАдресов);
	//Если ТаблицаАдресов = Неопределено Тогда 
	//	ТаблицаАдресов = Новый ТаблицаЗначений;
	//КонецЕсли; 
	//
	//// Объединим нерассчитанные адреса с просроченными
	//ТаблицаПросроченныхАдресов = ПолучитьИзВременногоХранилища(АдресПросроченныхАдресов);
	//Если ТаблицаПросроченныхАдресов <> Неопределено Тогда 
	//	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПросроченныхАдресов, ТаблицаАдресов);
	//КонецЕсли; 
	
	ТаблицаАдресов = ПолучитьИзВременногоХранилища(АдресВсехАдресов);
	Если ТаблицаАдресов = Неопределено Тогда 
		ТаблицаАдресов = Новый ТаблицаЗначений;
	КонецЕсли; 
	
	мсвПараметров = Новый Массив;
	мсвПараметров.Добавить(ТаблицаАдресов);
	мсвПараметров.Добавить(ТолькоНеРассчитанные);
	мсвПараметров.Добавить(Истина);
	мсвПараметров.Добавить(АдресРезультатаЗаполненияБлижайших);
	мсвПараметров.Добавить(НеобходимыеПарыАдресов);
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("упМаршрутизация.ПолучитьНерассчитанныеРасстояния", мсвПараметров, Новый УникальныйИдентификатор, НСтр("ru = 'Расчет порции расстояний'"));
	ЭтаФорма.ИдентификаторЗаданияЗаполненияБлижайших = ФоновоеЗадание.УникальныйИдентификатор;
	ЭтаФорма.ОписаниеОшибкиФоновогоЗадания = "";
	ЭтаФорма.НеРассчитаноБлижайшиеКоличество = "...";
	ЭтаФорма.НеРассчитаноБлижайшие = "...";
	ЭтаФорма.РассчитаноБлижайшиеКоличество = "...";
	ЭтаФорма.РассчитаноБлижайшие = "...";
	Элементы.ПоказатьНеРассчитанныеБлижайшие.Видимость = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗавершениеЗаполненияБлижайших()
	
	Если ПроверитьЗавершениеЗаполненияБлижайшихНаСервере() Тогда 
		ПодключитьОбработчикОжидания("ПроверитьЗавершениеЗаполненияБлижайших", 2, Истина);
	Иначе
		ЭтаФорма.ИдентификаторЗаданияЗаполненияБлижайших = Неопределено;
		Элементы.ЗаполнитьБлижайшиеРасстояния.Доступность = Истина;
		Элементы.РассчитатьБлижайшие.Доступность = Истина;
		Если НеРассчитаноБлижайшиеКоличество <> "0" Тогда
			Элементы.ПоказатьНеРассчитанныеБлижайшие.Видимость = Истина;
		КонецЕсли;
		// Проблема: При отмене многопоточного процесса запускается повторный расчет
		//Если АвтоповторРассчетаБлижайших И НеРассчитаноБлижайшиеКоличество <> "0" Тогда
		//	РассчитатьБлижайшие();
		//КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗавершениеЗаполненияБлижайшихНаСервере()
	
	Элементы.НеРассчитаноБлижайшиеКоличество.Гиперссылка = Ложь;
	Элементы.НеРассчитаноБлижайшиеКоличество.ЦветТекста = Элементы.НеРассчитаноБлижайшие.ЦветТекста;
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗаданияЗаполненияБлижайших);
	Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
		РезультатФоновогоЗадания = ПолучитьИзВременногоХранилища(АдресРезультатаЗаполненияБлижайших);
	КонецЕсли; 
	Если РезультатФоновогоЗадания <> Неопределено Тогда
		ТаблицыПорций = РезультатФоновогоЗадания.ТаблицыПорций;
		БлижайшиеКоличество = РезультатФоновогоЗадания.РассчитаноБлижайшие + РезультатФоновогоЗадания.НеРассчитаноБлижайшие;
		ЭтаФорма.РассчитаноБлижайшиеКоличество = РезультатФоновогоЗадания.РассчитаноБлижайшие;
		Если БлижайшиеКоличество > 0 Тогда
			ПроцентРассчитанных = Окр(100 * РезультатФоновогоЗадания.РассчитаноБлижайшие / БлижайшиеКоличество, 2);
		Иначе
			ПроцентРассчитанных = 0.00;
		КонецЕсли; 
		ЭтаФорма.РассчитаноБлижайшие = ПроцентРассчитанных;
		ЭтаФорма.НеРассчитаноБлижайшиеКоличество = РезультатФоновогоЗадания.НеРассчитаноБлижайшие;
		ЭтаФорма.НеРассчитаноБлижайшие = 100 - ПроцентРассчитанных;
		ЭтаФорма.НевозможноРассчитатьБлижайшие = РезультатФоновогоЗадания.НевозможноРассчитатьБлижайшие;
		Если РезультатФоновогоЗадания.НеРассчитаноБлижайшие > 0 Тогда
			РазмерПорции = упСервисныеФункции.ПолучитьЗначениеКонстанты("упРазмерПорцииРассчитываемыхДанных");
			Если РазмерПорции * ТаблицыПорций.Количество() = РезультатФоновогоЗадания.НеРассчитаноБлижайшие Тогда
				ЭтаФорма.НеРассчитаноБлижайшие = ЭтаФорма.НеРассчитаноБлижайшие + "+";
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			ЭтаФорма.ОписаниеОшибкиФоновогоЗадания = ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке);
			Элементы.НеРассчитаноБлижайшиеКоличество.Гиперссылка = Истина;
			Элементы.НеРассчитаноБлижайшиеКоличество.ЦветТекста = ЦветаСтиля.упЦветТекстаОшибки;
			ЭтаФорма.НеРассчитаноБлижайшиеКоличество = "Ошибка";
			//Элементы.РассчитаноБлижайшиеКоличество.ЦветТекста = ЦветаСтиля.упЦветТекстаОшибки;
			ЭтаФорма.РассчитаноБлижайшиеКоличество = "Ошибка";
			ЭтаФорма.НеРассчитаноБлижайшие = "?";
			ЭтаФорма.РассчитаноБлижайшие = "?";
			ЭтаФорма.НевозможноРассчитатьБлижайшие = "?";
		ИначеЕсли ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
			ЭтаФорма.НеРассчитаноБлижайшие = "?";
			ЭтаФорма.НеРассчитаноБлижайшиеКоличество = "?";
			ЭтаФорма.РассчитаноБлижайшие = "?";
			ЭтаФорма.РассчитаноБлижайшиеКоличество = "?";
			ЭтаФорма.НевозможноРассчитатьБлижайшие = "?";
		КонецЕсли;
	КонецЕсли;
	Возврат ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно;

КонецФункции

&НаСервере
Функция РассчитатьБлижайшиеНаСервере()
	
	ИдентификаторовыЗаданийРасчета = Новый СписокЗначений;
	Если ЗначениеЗаполнено(АдресРезультатаЗаполненияБлижайших) Тогда
		ТаблицыПорций = ПолучитьИзВременногоХранилища(АдресРезультатаЗаполненияБлижайших).ТаблицыПорций;
		Если ТаблицыПорций <> Неопределено Тогда
			ПорцииБезРассчитанных = Новый Массив;
			Для Каждого ТаблицаПорцииСРассчитанными Из ТаблицыПорций Цикл
				ТаблицаПорцииСБезРассчитанных = ТаблицаПорцииСРассчитанными.Скопировать(Новый Структура("ПытатьсяРассчитатьСнова", Истина));
				Если ТаблицаПорцииСБезРассчитанных.Количество() > 0 Тогда
					ПорцииБезРассчитанных.Добавить(ТаблицаПорцииСБезРассчитанных);
				КонецЕсли; 
			КонецЦикла;
			Если ПорцииБезРассчитанных.Количество() > 0 Тогда
				упМаршрутизация.ЗапуститьФоновыеЗаданияРасчетаРасстояний(ПорцииБезРассчитанных, Истина, ИдентификаторовыЗаданийРасчета);
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		ТолькоНеРассчитанные = Ложь;
		упМаршрутизация.РассчитатьРасстоянияПоКарте(АдресНерассчитанныхАдресов, ИдентификаторовыЗаданийРасчета, ТолькоНеРассчитанные, Ложь);
	КонецЕсли; 
	Возврат ИдентификаторовыЗаданийРасчета;
	
КонецФункции

&НаКлиенте
Процедура РассчитатьБлижайшие(Команда = Неопределено)
	
	ИдентификаторыЗаданийРасчета = РассчитатьБлижайшиеНаСервере();
	Если ИдентификаторыЗаданийРасчета.Количество() > 0 Тогда 
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ИдентификаторыЗаданий" , ИдентификаторыЗаданийРасчета);
		сткПараметры.Вставить("ТекстЗавершения"       , НСтр("ru = 'Расчет массива ближайших расстояний завершен.'"));
		сткПараметры.Вставить("ЗаголовокЗавершения"   , НСтр("ru = 'Расчет ближайших расстояний'"));
		сткПараметры.Вставить("ЗакрыватьПриЗавершении", Истина);
		сткПараметры.Вставить("ИмяСобытияОповещения"  , "РасчетРастоянийБлижайшие");
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ТипРасчета", "Ближайшие");
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполненияМногопоточногоПроцессаЗавершение", ЭтаФорма, ПараметрыОповещения);
		ОткрытьФорму("ОбщаяФорма.упХодВыполненияМногопоточногоПроцесса", сткПараметры,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Если ЗначениеЗаполнено(ЭтаФорма.ИдентификаторЗаданияЗаполненияБлижайших) Тогда
		ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗаданияЗаполненияБлижайших);
		Если ФоновоеЗадание <> Неопределено Тогда
			Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
				ФоновоеЗадание.Отменить();
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьОшибкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ЭтаФорма.ОписаниеОшибкиФоновогоЗадания) Тогда
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ЭтаФорма.ОписаниеОшибкиФоновогоЗадания, Истина, Ложь);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНерассчитанныеВсе(Команда)
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("АдресТаблицыАдресов", АдресНерассчитанныхАдресов);
	сткПараметры.Вставить("ВыводитьУстаревшие", Ложь);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаВыводаПроблемныхРасстояний", сткПараметры,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНерассчитанныеБлижайшие(Команда)

	сткПараметры = Новый Структура;
	сткПараметры.Вставить("АдресМассиваТаблицРасстояний", АдресРезультатаЗаполненияБлижайших);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаВыводаПроблемныхРасстояний", сткПараметры,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

#КонецОбласти 