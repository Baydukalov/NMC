
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	СписокЗаявок.Параметры.УстановитьЗначениеПараметра("Пользователь", ТекущийПользователь);
	СписокРейсов.Параметры.УстановитьЗначениеПараметра("Пользователь", ТекущийПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеСчетаНаОплатуЗаказчику" Тогда
		Элементы.СписокЗаявок.Обновить();
		ПодключитьОбработчикОжидания("СписокЗаявокПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокРейсовПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СписокРейсовПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаявокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СписокЗаявокПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПоЗаявкамПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоходыПоЗаявкам.ТекущиеДанные;
	Если ТекущиеДанные.Оплатить = 0 Тогда
		ТекущиеДанные.Пометка = Ложь;		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПоЗаявкамОплатитьПриИзменении(Элемент)
	
	ТекущиеДанные 	= Элементы.ДоходыПоЗаявкам.ТекущиеДанные;
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущиеДанные.Оплатить, ТекущиеДанные.Оплатить, ТекущиеДанные.СтавкаНДС, ТекущиеДанные.ОплатитьНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "Сумма");
	ТекущиеДанные.ОплатитьНДС	= сткСтрока.СуммаНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПоЗаявкамСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные 	= Элементы.ДоходыПоЗаявкам.ТекущиеДанные;
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущиеДанные.Оплатить, ТекущиеДанные.Оплатить, ТекущиеДанные.СтавкаНДС, ТекущиеДанные.ОплатитьНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС");
	ТекущиеДанные.ОплатитьНДС	= сткСтрока.СуммаНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыПоЗаявкамСуммаДоходыСНДСПриИзменении(Элемент)
	ТекущиеДанные 	= Элементы.РасходыПоЗаявкам.ТекущиеДанные;
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущиеДанные.СуммаДоходыСНДС, ТекущиеДанные.СуммаДоходыСНДС, ТекущиеДанные.СтавкаНДС, ТекущиеДанные.СуммаДоходыНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "Сумма");
	ТекущиеДанные.СуммаДоходыНДС	= сткСтрока.СуммаНДС;

КонецПроцедуры

&НаКлиенте
Процедура РасходыПоЗаявкамСтавкаНДСПриИзменении(Элемент)
	ТекущиеДанные 	= Элементы.РасходыПоЗаявкам.ТекущиеДанные;
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущиеДанные.СуммаДоходыСНДС, ТекущиеДанные.СуммаДоходыСНДС, ТекущиеДанные.СтавкаНДС, ТекущиеДанные.СуммаДоходыНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС");
	ТекущиеДанные.СуммаДоходыНДС	= сткСтрока.СуммаНДС;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СоздатьДокументСчетНаОплатуЗаказчику(Команда)
			
	// Формируется структура на основе выделенных строки и заполненной колонки "К оплате"
	мсвДанныеДляФормированияСчетов = ДанныеДляСчетовНаОплату(); 
	
	// Если создается один документ
	Если мсвДанныеДляФормированияСчетов.Количество() = 1 Тогда
		
		// Открыть форму нового документа
		ПараметрыФормы = Новый Структура("Основание", мсвДанныеДляФормированияСчетов[0]);
		ОткрытьФорму("Документ.упСчетНаОплатуЗаказчику.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);
		
	// Если создается несколько документов	
	ИначеЕсли мсвДанныеДляФормированияСчетов.Количество() > 1 Тогда 
		
		// Задать вопрос "Будет создано несколько счетов
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОСозданииНесколькихСчетов",ЭтаФорма, мсвДанныеДляФормированияСчетов);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru = 'Будет создано несколько счетов?'"), РежимДиалогаВопрос.ДаНет);	
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументФактическиеДоходы(Команда)
	
	Отказ = Ложь;
	ТекстОшибки = "";
	
	мсвФактическиеДоходы = СоздатьДокументФактическиеДоходыНаСервере(ТекстОшибки, Отказ);
	Если Не Отказ Тогда
		Для каждого текФактическиеДоходы Из мсвФактическиеДоходы Цикл
			ПараметрыФормы = Новый Структура("Ключ", текФактическиеДоходы);
			ОткрытьФорму("Документ.упФактическиеДоходы.Форма.ФормаДокумента", ПараметрыФормы);
		КонецЦикла;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ не сформирован.'"),, ТекстОшибки, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументФактическиеДоходыПоРасходам(Команда)
	
	Отказ = Ложь;
	ТекстОшибки = "";
	
	мсвФактическиеДоходы = СоздатьДокументФактическиеДоходыПоРасходамНаСервере(ТекстОшибки, Отказ);
	Если Не Отказ Тогда
		Для каждого текФактическиеДоходы Из мсвФактическиеДоходы Цикл
			ПараметрыФормы = Новый Структура("Ключ", текФактическиеДоходы);
			ОткрытьФорму("Документ.упФактическиеДоходы.Форма.ФормаДокумента", ПараметрыФормы);
		КонецЦикла;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ не сформирован.'"),, ТекстОшибки, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("СписокРейсовПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для каждого текСтрока Из ДоходыПоЗаявкам Цикл
		Если текСтрока.Оплатить > 0 Тогда
			текСтрока.Пометка = Истина;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для каждого текСтрока Из ДоходыПоЗаявкам Цикл
		текСтрока.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсеРасходы(Команда)
	Для каждого текСтрока Из РасходыПоЗаявкам Цикл
		Если текСтрока.СуммаДоходыСНДС > 0 Тогда
			текСтрока.Пометка = Истина;
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеРасходы(Команда)
	Для каждого текСтрока Из РасходыПоЗаявкам Цикл
		текСтрока.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОСозданииНесколькихСчетов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Отказ	= Ложь;
		ТекстОшибки = "";
		мсвСчетовНаОплату = СформироватьСчетаНаОплату(ДополнительныеПараметры, ТекстОшибки, Отказ);
		
		Если НЕ Отказ Тогда
			Для каждого СчетНаОплату Из мсвСчетовНаОплату Цикл
				ПараметрыФормы = Новый Структура("Ключ", СчетНаОплату);
				ОткрытьФорму("Документ.упСчетНаОплатуЗаказчику.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);	
			КонецЦикла;
		Иначе
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);			
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("СписокЗаявокПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРейсовПриАктивизацииСтрокиОбработчикОжидания()
	
	мсвРейсов = Новый Массив;
	ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("ДокументСсылка.упРейс") Тогда
			мсвРейсов.Добавить(ВыделеннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьРасходыПоЗаявкамНаСервере(мсвРейсов);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФактическиеРасходы, "Ссылка.Рейсы.Рейс", мсвРейсов, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ПрочиеРасходы, "Ссылка.Рейсы.Рейс", мсвРейсов, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРасходыПоЗаявкамНаСервере(МассивРейсов)
	
	РасходыПоЗаявкам.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРасходыОбороты.Рейс КАК Рейс,
	|	упРасходыОбороты.ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упРасходыОбороты.СтатьяРасходов КАК СтатьяРасходов,
	|	упРасходыОбороты.Валюта КАК Валюта,
	|	упРасходыОбороты.СтатьяРасходов.СтавкаНДС КАК СтавкаНДС,
	|	КОЛИЧЕСТВО(упРасходыОбороты.ЗаданиеНаПеревозку) КАК КоличествоЗаданий,
	|	СУММА(упРасходыОбороты.СуммаПланСНДСОборот) КАК СуммаПланСНДС,
	|	СУММА(ВЫБОР
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС20)
	|				ТОГДА упРасходыОбороты.СуммаПланСНДСОборот / 1.2 * 0.2
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС18)
	|				ТОГДА упРасходыОбороты.СуммаПланСНДСОборот / 1.18 * 0.18
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС10)
	|				ТОГДА упРасходыОбороты.СуммаПланСНДСОборот / 1.1 * 0.1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПланНДС,
	|	СУММА(упРасходыОбороты.СуммаПланВалютаСНДСОборот) КАК СуммаПланВалютаСНДС,
	|	СУММА(ВЫБОР
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС20)
	|				ТОГДА упРасходыОбороты.СуммаПланВалютаСНДСОборот / 1.2 * 0.2
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС18)
	|				ТОГДА упРасходыОбороты.СуммаПланВалютаСНДСОборот / 1.18 * 0.18
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС10)
	|				ТОГДА упРасходыОбороты.СуммаПланВалютаСНДСОборот / 1.1 * 0.1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПланВалютаНДС,
	|	СУММА(упРасходыОбороты.СуммаФактСНДСОборот) КАК СуммаФактСНДС,
	|	СУММА(ВЫБОР
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС20)
	|				ТОГДА упРасходыОбороты.СуммаФактСНДСОборот / 1.2 * 0.2
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС18)
	|				ТОГДА упРасходыОбороты.СуммаФактСНДСОборот / 1.18 * 0.18
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС10)
	|				ТОГДА упРасходыОбороты.СуммаФактСНДСОборот / 1.1 * 0.1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФактНДС,
	|	СУММА(упРасходыОбороты.СуммаФактВалютаСНДСОборот) КАК СуммаФактВалютаСНДС,
	|	СУММА(ВЫБОР
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС20)
	|				ТОГДА упРасходыОбороты.СуммаФактВалютаСНДСОборот / 1.2 * 0.2
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС18)
	|				ТОГДА упРасходыОбороты.СуммаФактВалютаСНДСОборот / 1.18 * 0.18
	|			КОГДА упРасходыОбороты.СтатьяРасходов.СтавкаНДС В (&СтавкиНДС10)
	|				ТОГДА упРасходыОбороты.СуммаФактВалютаСНДСОборот / 1.1 * 0.1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФактВалютаНДС
	|ПОМЕСТИТЬ втРасходы
	|ИЗ
	|	РегистрНакопления.упРасходы.Обороты(, , , Рейс В (&МассивРейсов)) КАК упРасходыОбороты
	|ГДЕ
	|	НЕ ЕСТЬNULL(упРасходыОбороты.ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза, ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	упРасходыОбороты.Рейс,
	|	упРасходыОбороты.ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза,
	|	упРасходыОбороты.СтатьяРасходов,
	|	упРасходыОбороты.Валюта,
	|	упРасходыОбороты.СтатьяРасходов.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДоходыОбороты.ЗаявкаНаПеревозкуГруза,
	|	упДоходыОбороты.СтатьяДоходов,
	|	упДоходыОбороты.Валюта,
	|	упДоходыОбороты.СуммаФактСНДСОборот КАК СуммаФактДоходыСНДС,
	|	ВЫБОР
	|		КОГДА упДоходыОбороты.СтатьяДоходов.СтавкаНДС В (&СтавкиНДС20)
	|			ТОГДА упДоходыОбороты.СуммаФактСНДСОборот / 1.2 * 0.20
	|		КОГДА упДоходыОбороты.СтатьяДоходов.СтавкаНДС В (&СтавкиНДС18)
	|			ТОГДА упДоходыОбороты.СуммаФактСНДСОборот / 1.18 * 0.18
	|		КОГДА упДоходыОбороты.СтатьяДоходов.СтавкаНДС В (&СтавкиНДС10)
	|			ТОГДА упДоходыОбороты.СуммаФактСНДСОборот / 1.1 * 0.1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаФактДоходыНДС,
	|	ЕСТЬNULL(упДоходыОбороты.СуммаФактВалютаСНДСОборот, 0) КАК СуммаФактДоходыВалютаСНДС,
	|	ВЫБОР
	|		КОГДА упДоходыОбороты.СтатьяДоходов.СтавкаНДС В (&СтавкиНДС20)
	|			ТОГДА упДоходыОбороты.СуммаФактВалютаСНДСОборот / 1.2 * 0.20
	|		КОГДА упДоходыОбороты.СтатьяДоходов.СтавкаНДС В (&СтавкиНДС18)
	|			ТОГДА упДоходыОбороты.СуммаФактВалютаСНДСОборот / 1.18 * 0.18
	|		КОГДА упДоходыОбороты.СтатьяДоходов.СтавкаНДС В (&СтавкиНДС10)
	|			ТОГДА упДоходыОбороты.СуммаФактВалютаСНДСОборот / 1.1 * 0.1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаФактДоходыВалютаНДС
	|ПОМЕСТИТЬ втДоходы
	|ИЗ
	|	РегистрНакопления.упДоходы.Обороты(
	|			,
	|			,
	|			,
	|			ЗаявкаНаПеревозкуГруза В
	|				(ВЫБРАТЬ
	|					втРасходы.ЗаявкаНаПеревозкуГруза
	|				ИЗ
	|					втРасходы КАК втРасходы)) КАК упДоходыОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРасходы.Рейс,
	|	втРасходы.ЗаявкаНаПеревозкуГруза,
	|	втРасходы.СтатьяРасходов,
	|	втРасходы.Валюта,
	|	втРасходы.СтавкаНДС,
	|	втРасходы.КоличествоЗаданий,
	|	втРасходы.СуммаПланСНДС,
	|	втРасходы.СуммаПланНДС,
	|	втРасходы.СуммаПланВалютаСНДС,
	|	втРасходы.СуммаПланВалютаНДС,
	|	втРасходы.СуммаФактСНДС,
	|	втРасходы.СуммаФактНДС,
	|	втРасходы.СуммаФактВалютаСНДС,
	|	втРасходы.СуммаФактВалютаНДС,
	|	ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0) КАК СуммаФактДоходыСНДС,
	|	ЕСТЬNULL(втДоходы.СуммаФактДоходыНДС, 0) КАК СуммаФактДоходыНДС,
	|	ЕСТЬNULL(втДоходы.СуммаФактДоходыВалютаСНДС, 0) КАК СуммаФактДоходыВалютаСНДС,
	|	ЕСТЬNULL(втДоходы.СуммаФактДоходыВалютаНДС, 0) КАК СуммаФактДоходыВалютаНДС,
	|	ВЫБОР
	|		КОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0) > 0
	|			ТОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДоходыСНДС,
	|	ВЫБОР
	|		КОГДА втРасходы.СтавкаНДС В (&СтавкиНДС20)
	|			ТОГДА ВЫБОР
	|					КОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0) > 0
	|						ТОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ / 1.2 * 0.2
	|		КОГДА втРасходы.СтавкаНДС В (&СтавкиНДС18)
	|			ТОГДА ВЫБОР
	|					КОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0) > 0
	|						ТОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ / 1.18 * 0.18
	|		КОГДА втРасходы.СтавкаНДС В (&СтавкиНДС10)
	|			ТОГДА ВЫБОР
	|					КОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0) > 0
	|						ТОГДА втРасходы.СуммаФактСНДС - ЕСТЬNULL(втДоходы.СуммаФактДоходыСНДС, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ / 1.1 * 0.1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДоходыНДС
	|ИЗ
	|	втРасходы КАК втРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДоходы КАК втДоходы
	|		ПО втРасходы.ЗаявкаНаПеревозкуГруза = втДоходы.ЗаявкаНаПеревозкуГруза
	|			И втРасходы.СтатьяРасходов = втДоходы.СтатьяДоходов
	|			И втРасходы.Валюта = втДоходы.Валюта";
	Запрос.УстановитьПараметр("СтавкиНДС20" , ПолучитьСтавкиНДС20());
	Запрос.УстановитьПараметр("СтавкиНДС18" , ПолучитьСтавкиНДС18());
	Запрос.УстановитьПараметр("СтавкиНДС10" , ПолучитьСтавкиНДС10());
	Запрос.УстановитьПараметр("МассивРейсов", МассивРейсов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		РасходыПоЗаявкам.Загрузить(РезультатЗапроса.Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаявокПриАктивизацииСтрокиОбработчикОжидания()
	
	мсвЗаявок = Новый Массив;
	ВыделенныеСтроки = Элементы.СписокЗаявок.ВыделенныеСтроки;
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
			мсвЗаявок.Добавить(ВыделеннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьДоходыПоЗаявкамНаСервере(мсвЗаявок);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФактическиеДоходы, "Ссылка.ЗаявкиНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза", мсвЗаявок, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СчетаНаОплатуЗаказчику, "Ссылка.РасшифровкаПлатежа.ОбъектРасчетов", мсвЗаявок, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоходыПоЗаявкамНаСервере(МассивЗаявок)
	
	ДоходыПоЗаявкам.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтатьяДоходов,
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Валюта,
	|	МАКСИМУМ(упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтавкаНДС) КАК СтавкаНДС,
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка КАК Заявка
	|ПОМЕСТИТЬ втСтавкиНДСПоЗаявкам
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ПлановыеДоходы КАК упЗаявкаНаПеревозкуГрузаПлановыеДоходы
	|ГДЕ
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка В(&МассивЗаявок)
	|
	|СГРУППИРОВАТЬ ПО
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтатьяДоходов,
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Валюта,
	|	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРасчетыСЗаказчикамиОстатки.ОбъектРасчетов,
	|	упРасчетыСЗаказчикамиОстатки.СтатьяДоходов КАК СтатьяДоходов,
	|	упРасчетыСЗаказчикамиОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаРаботыСЗаявкойОстаток > 0
	|			ТОГДА упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаРаботыСЗаявкойОстаток
	|		КОГДА упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаВыполненияРейсаОстаток > 0
	|			ТОГДА упРасчетыСЗаказчикамиОстатки.КОплатеДоНачалаВыполненияРейсаОстаток
	|		КОГДА упРасчетыСЗаказчикамиОстатки.СуммаОстаток > 0
	|			ТОГДА упРасчетыСЗаказчикамиОстатки.СуммаОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Оплатить,
	|	втСтавкиНДСПоЗаявкам.СтавкаНДС
	|ПОМЕСТИТЬ втДолги
	|ИЗ
	|	РегистрНакопления.упРасчетыСЗаказчиками.Остатки(, ОбъектРасчетов В (&МассивЗаявок)) КАК упРасчетыСЗаказчикамиОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтавкиНДСПоЗаявкам КАК втСтавкиНДСПоЗаявкам
	|		ПО упРасчетыСЗаказчикамиОстатки.ОбъектРасчетов = втСтавкиНДСПоЗаявкам.Заявка
	|			И упРасчетыСЗаказчикамиОстатки.СтатьяДоходов = втСтавкиНДСПоЗаявкам.СтатьяДоходов
	|			И упРасчетыСЗаказчикамиОстатки.Валюта = втСтавкиНДСПоЗаявкам.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДоходы.ЗаявкаНаПеревозкуГруза,
	|	упДоходы.СтатьяДоходов,
	|	упДоходы.Валюта,
	|	упДоходы.СуммаПланСНДСОборот КАК СуммаПланСНДС,
	|	упДоходы.СуммаПланСНДСОборот - упДоходы.СуммаПланБезНДСОборот КАК СуммаПланНДС,
	|	упДоходы.СуммаПланВалютаСНДСОборот КАК СуммаПланВалютаСНДС,
	|	упДоходы.СуммаПланВалютаСНДСОборот - упДоходы.СуммаПланВалютаБезНДСОборот КАК СуммаПланВалютаНДС,
	|	упДоходы.СуммаФактСНДСОборот КАК СуммаФактСНДС,
	|	упДоходы.СуммаФактСНДСОборот - упДоходы.СуммаФактБезНДСОборот КАК СуммаФактНДС,
	|	упДоходы.СуммаФактВалютаСНДСОборот КАК СуммаФактВалютаСНДС,
	|	упДоходы.СуммаФактВалютаСНДСОборот - упДоходы.СуммаФактВалютаБезНДСОборот КАК СуммаФактВалютаНДС,
	|	втСтавкиНДСПоЗаявкам.СтавкаНДС
	|ПОМЕСТИТЬ втДоходы
	|ИЗ
	|	РегистрНакопления.упДоходы.Обороты(, , , ЗаявкаНаПеревозкуГруза В (&МассивЗаявок)) КАК упДоходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтавкиНДСПоЗаявкам КАК втСтавкиНДСПоЗаявкам
	|		ПО упДоходы.ЗаявкаНаПеревозкуГруза = втСтавкиНДСПоЗаявкам.Заявка
	|			И упДоходы.СтатьяДоходов = втСтавкиНДСПоЗаявкам.СтатьяДоходов
	|			И упДоходы.Валюта = втСтавкиНДСПоЗаявкам.Валюта
	//|ГДЕ
	//|	упДоходы.ЗаявкаНаПеревозкуГруза В(&МассивЗаявок)
	|
	//|СГРУППИРОВАТЬ ПО
	//|	упДоходы.ЗаявкаНаПеревозкуГруза,
	//|	упДоходы.СтатьяДоходов,
	//|	упДоходы.Валюта,
	//|	втСтавкиНДСПоЗаявкам.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДоходы.ЗаявкаНаПеревозкуГруза,
	|	втДоходы.СтатьяДоходов,
	|	втДоходы.Валюта,
	|	втДоходы.СуммаПланСНДС,
	|	втДоходы.СуммаПланНДС,
	|	втДоходы.СуммаПланВалютаСНДС,
	|	втДоходы.СуммаПланВалютаНДС,
	|	втДоходы.СуммаФактСНДС,
	|	втДоходы.СуммаФактНДС,
	|	втДоходы.СуммаФактВалютаСНДС,
	|	втДоходы.СуммаФактВалютаНДС,
	|	ЕСТЬNULL(втДолги.Оплатить, 0) КАК Оплатить,
	|	ЕСТЬNULL(втДолги.СтавкаНДС, втДоходы.СтавкаНДС) КАК СтавкаНДС
	|ИЗ
	|	втДоходы КАК втДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДолги КАК втДолги
	|		ПО втДоходы.ЗаявкаНаПеревозкуГруза = втДолги.ОбъектРасчетов
	|			И втДоходы.СтатьяДоходов = втДолги.СтатьяДоходов
	|			И втДоходы.Валюта = втДолги.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	втДоходы.ЗаявкаНаПеревозкуГруза,
	|	втДоходы.СтатьяДоходов,
	|	втДоходы.Валюта";
	//Запрос.УстановитьПараметр("СтавкиНДС18" , ПолучитьСтавкиНДС18());
	//Запрос.УстановитьПараметр("СтавкиНДС10" , ПолучитьСтавкиНДС10());
	Запрос.УстановитьПараметр("МассивЗаявок", МассивЗаявок);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ДоходыПоЗаявкам.Загрузить(РезультатЗапроса.Выгрузить());
		Для каждого СтрокаЗаявка Из ДоходыПоЗаявкам Цикл
			сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", СтрокаЗаявка.Оплатить, СтрокаЗаявка.Оплатить, СтрокаЗаявка.СтавкаНДС, СтрокаЗаявка.ОплатитьНДС, 1);
			упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "Сумма");
			СтрокаЗаявка.ОплатитьНДС	= сткСтрока.СуммаНДС;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументФактическиеДоходыНаСервере(ТекстОшибки, Отказ, ОткрыватьПослеСоздания = Ложь)
	
	мсвФактическиеДоходы = Новый Массив;
	
	ВыделенныеСтроки = Элементы.СписокЗаявок.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() Тогда
		мсвЗаявокНаПеревозкуГрузов = Новый Массив;
		Для каждого текСтрока Из ВыделенныеСтроки Цикл
			Если ТипЗнч(текСтрока) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
				мсвЗаявокНаПеревозкуГрузов.Добавить(текСтрока);
			КонецЕсли;
			
			Если мсвЗаявокНаПеревозкуГрузов.Количество() Тогда
				мсвФактическиеДоходы = Документы.упЗаявкаНаПеревозкуГруза.СформироватьФактическиеДоходы(мсвЗаявокНаПеревозкуГрузов, ТекстОшибки, Отказ);
			Иначе
				Отказ = Истина;
				ТекстОшибки = НСтр("ru = 'В списке заявок на перевозку грузов отсутсвуют выделенные заявки.'");
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'В списке заявок на перевозку грузов отсутсвуют выделенные заявки.'");
	КонецЕсли; 
	
	Возврат мсвФактическиеДоходы;
	
КонецФункции

&НаСервере
Функция СоздатьДокументФактическиеДоходыПоРасходамНаСервере(ТекстОшибки, Отказ, ОткрыватьПослеСоздания = Ложь)
	
	мсвФактическиеДоходы = Новый Массив;
	
	мсвОтмеченныеСтроки = РасходыПоЗаявкам.НайтиСтроки(Новый Структура("Пометка",Истина));
	
	Если мсвОтмеченныеСтроки.Количество() > 0 Тогда
		
		тбзРасходы = Новый ТаблицаЗначений;
		тбзРасходы.Колонки.Добавить("ЗаявкаНаПеревозкуГруза", 	Новый ОписаниеТипов("ДокументСсылка.упЗаявкаНаПеревозкуГруза"));
		тбзРасходы.Колонки.Добавить("Валюта", 					Новый ОписаниеТипов("СправочникСсылка.Валюты"));
		тбзРасходы.Колонки.Добавить("Количество", 				Новый ОписаниеТипов("Число",
												  				Новый КвалификаторыЧисла(16, 3, ДопустимыйЗнак.Любой)));
		тбзРасходы.Колонки.Добавить("СтавкаНДС", 				Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"));
		тбзРасходы.Колонки.Добавить("СтатьяДоходов", 			Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
		тбзРасходы.Колонки.Добавить("Сумма", 					Новый ОписаниеТипов("Число",
												  				Новый КвалификаторыЧисла(16, 3, ДопустимыйЗнак.Любой)));
		тбзРасходы.Колонки.Добавить("СуммаНДС", 				Новый ОписаниеТипов("Число",
												  				Новый КвалификаторыЧисла(16, 3, ДопустимыйЗнак.Любой)));
		тбзРасходы.Колонки.Добавить("Цена", 					Новый ОписаниеТипов("Число",
												  				Новый КвалификаторыЧисла(16, 3, ДопустимыйЗнак.Любой)));
		тбзРасходы.Колонки.Добавить("СтрокаДобавленаНаОснованииРасхода", Новый ОписаниеТипов("Булево"));
		
		мсвЗаявокНаПеревозкуГрузов = Новый Массив;
		Для каждого Строка Из мсвОтмеченныеСтроки Цикл
			НоваяСтрока = тбзРасходы.Добавить();
			НоваяСтрока.ЗаявкаНаПеревозкуГруза = Строка.ЗаявкаНаПеревозкуГруза;
			НоваяСтрока.Валюта = Строка.Валюта;
			НоваяСтрока.Количество = 1;
			НоваяСтрока.СтавкаНДС = Строка.СтавкаНДС;
			НоваяСтрока.СтатьяДоходов = Строка.СтатьяРасходов;
			НоваяСтрока.Сумма = Строка.СуммаДоходыСНДС;
			НоваяСтрока.СуммаНДС = Строка.СуммаДоходыНДС;
			НоваяСтрока.Цена = Строка.СуммаДоходыСНДС;
			НоваяСтрока.СтрокаДобавленаНаОснованииРасхода = Истина;
		КонецЦикла; 
		
		Если тбзРасходы.Количество() > 0 Тогда
			мсвФактическиеДоходы = Документы.упЗаявкаНаПеревозкуГруза.СформироватьФактическиеДоходыПоРасходам(тбзРасходы, ТекстОшибки, Отказ);
		Иначе
			Отказ = Истина;
			ТекстОшибки = НСтр("ru = 'В списке расходов отсутсвуют выделенные строки.'");
		КонецЕсли; 
	Иначе
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'В списке расходов отсутсвуют выделенные строки.'");
	КонецЕсли; 
	
	Возврат мсвФактическиеДоходы;
	
КонецФункции

&НаСервере
Функция СоздатьДокументФактическиеРасходыНаСервере(ТекстОшибки, Отказ)
	
	мсвРезультат = Новый Массив;
	
	ВыделенныеСтроки = Элементы.СписокРейсов.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() Тогда
		мсвРейсы = Новый Массив;
		Для каждого Строка Из ВыделенныеСтроки Цикл
			Если ТипЗнч(Строка) = Тип("ДокументСсылка.упРейс") Тогда
				мсвРейсы.Добавить(Строка);
			КонецЕсли;
		КонецЦикла; 
		
		Если мсвРейсы.Количество() Тогда
			мсвРезультат = Документы.упРейс.СформироватьФактическиеРасходы(мсвРейсы, ТекстОшибки, Отказ);
		Иначе
			Отказ = Истина;
			ТекстОшибки = НСтр("ru = 'В списке рейсов отсутствуют выделенные рейсы.'");
		КонецЕсли; 

	Иначе
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'В списке рейсов отсутствуют выделенные рейсы.'");
	КонецЕсли; 
	
	Возврат мсвРезультат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьВидимость()
	
	// Нет.
	
КонецПроцедуры

&НаСервере
Функция СформироватьСчетаНаОплату(сткДанныеДляСчетовНаОплату, ТекстОшибки, Отказ)
	
	Отказ = Ложь;
	мсвСчетаНаОплату	= Новый Массив;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Для каждого ДанныеДляСчетаНаОплату Из сткДанныеДляСчетовНаОплату Цикл
		
		СчетНаОплатуОбъект = Документы.упСчетНаОплатуЗаказчику.СоздатьДокумент();
		СчетНаОплатуОбъект.Заполнить(ДанныеДляСчетаНаОплату);
		СчетНаОплатуОбъект.Дата	= ТекущаяДатаСеанса();
		
		Попытка
			СчетНаОплатуОбъект.Записать(РежимЗаписиДокумента.Запись);	
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ФормированиеСчетаНаОплату", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Прервать;
		КонецПопытки;
		
		мсвСчетаНаОплату.Добавить(СчетНаОплатуОбъект.Ссылка);
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		мсвСчетаНаОплату = Новый Массив;
		ОтменитьТранзакцию();
	КонецЕсли;
	
	Возврат мсвСчетаНаОплату;
	
КонецФункции

&НаСервере
Функция ДанныеДляСчетовНаОплату()
	
	мсвРезультат  = Новый Массив;
	
	тбзВыделенныеСтроки = Новый ТаблицаЗначений;
	тбзВыделенныеСтроки.Колонки.Добавить("ЗаявкаНаПеревозкуГруза", 	Новый ОписаниеТипов("ДокументСсылка.упЗаявкаНаПеревозкуГруза"));
	тбзВыделенныеСтроки.Колонки.Добавить("СтатьяДоходов", 			Новый ОписаниеТипов("СправочникСсылка.упСтатьиДоходовРасходов"));
	тбзВыделенныеСтроки.Колонки.Добавить("Валюта", 					Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	тбзВыделенныеСтроки.Колонки.Добавить("Цена", 					Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(15,2)));
	тбзВыделенныеСтроки.Колонки.Добавить("Количество", 				Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(10,0)));
	тбзВыделенныеСтроки.Колонки.Добавить("Сумма", 					Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(15,2)));
	тбзВыделенныеСтроки.Колонки.Добавить("СуммаНДС", 				Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(15,2)));
	тбзВыделенныеСтроки.Колонки.Добавить("СтавкаНДС", 				Новый ОписаниеТипов("ПеречислениеСсылка.упСтавкиНДС"));
	
	мсвОтмеченныеСтроки = ДоходыПоЗаявкам.НайтиСтроки(Новый Структура("Пометка",Истина));
	
	Для каждого Строка Из мсвОтмеченныеСтроки Цикл
		
		Если Строка.Оплатить > 0 Тогда
			НоваяСтрока = тбзВыделенныеСтроки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Сумма		= Строка.Оплатить;
			НоваяСтрока.Количество	= 1;
			упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока,"Сумма");
		КонецЕсли;
	
	КонецЦикла;
	
	Если тбзВыделенныеСтроки.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	тбДанные.ЗаявкаНаПеревозкуГруза,
		               |	тбДанные.СтатьяДоходов,
		               |	тбДанные.Валюта,
		               |	тбДанные.Цена,
		               |	тбДанные.Количество,
		               |	тбДанные.Сумма,
		               |	тбДанные.СуммаНДС,
		               |	тбДанные.СтавкаНДС
		               |ПОМЕСТИТЬ втДанные
		               |ИЗ
		               |	&тбДанные КАК тбДанные
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втДанные.СтатьяДоходов,
		               |	втДанные.Валюта,
		               |	втДанные.Цена,
		               |	СУММА(втДанные.Количество) КАК Количество,
		               |	СУММА(втДанные.Сумма) КАК Сумма,
		               |	СУММА(втДанные.СуммаНДС) КАК СуммаНДС,
		               |	втДанные.СтавкаНДС,
		               |	упЗаявкаНаПеревозкуГруза.Организация КАК Организация,
		               |	упЗаявкаНаПеревозкуГруза.Заказчик КАК Заказчик,
		               |	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
		               |	втДанные.ЗаявкаНаПеревозкуГруза КАК ОбъектРасчетов
		               |ИЗ
		               |	втДанные КАК втДанные
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
		               |		ПО втДанные.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГруза.Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упЗаявкаНаПеревозкуГруза.Организация,
		               |	упЗаявкаНаПеревозкуГруза.Заказчик,
		               |	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика,
		               |	втДанные.СтатьяДоходов,
		               |	втДанные.Валюта,
		               |	втДанные.Цена,
		               |	втДанные.СтавкаНДС,
		               |	втДанные.ЗаявкаНаПеревозкуГруза
		               |ИТОГИ ПО
		               |	Организация,
		               |	Заказчик,
		               |	КонтрагентЗаказчика";
					   
		Запрос.УстановитьПараметр("тбДанные", тбзВыделенныеСтроки);
			
		ВыборкаОрганизация	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОрганизация.Следующий() Цикл
			
			Организация = ВыборкаОрганизация.Организация;
			ВыборкаЗаказчик = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаЗаказчик.Следующий() Цикл
				
				Заказчик = ВыборкаЗаказчик.Заказчик;
				ВыборкаКонтрагентЗаказчика = ВыборкаЗаказчик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаКонтрагентЗаказчика.Следующий() Цикл
					
					КонтрагентЗаказчика = ВыборкаКонтрагентЗаказчика.КонтрагентЗаказчика;
					
					мсвРасшифровка = Новый Массив;
					
					Выборка	= ВыборкаКонтрагентЗаказчика.Выбрать();
					Пока Выборка.Следующий() Цикл
						сткРасшифровка = Новый Структура("ОбъектРасчетов, СтатьяДоходов, Валюта, Цена, Количество, СтавкаНДС, Сумма, СуммаНДС");			
						ЗаполнитьЗначенияСвойств(сткРасшифровка, Выборка);
						мсвРасшифровка.Добавить(сткРасшифровка);
					КонецЦикла;
					
					сткСчетНаОплату = Новый Структура("Организация, Заказчик, Контрагент, РасшифровкаПлатежей", Организация, Заказчик, КонтрагентЗаказчика, мсвРасшифровка);
					мсвРезультат.Добавить(сткСчетНаОплату);
					
				КонецЦикла;
					
			КонецЦикла;
				
		КонецЦикла; 

	КонецЕсли;
	
	Возврат мсвРезультат;
	
КонецФункции

&НаКлиенте
Процедура СоздатьДокументФактическиеРасходы(Команда)
	Отказ = Ложь;
	ТекстОшибки = "";
	
	мсвФактическиеРасходы = СоздатьДокументФактическиеРасходыНаСервере(ТекстОшибки, Отказ);
	Если Не Отказ Тогда
		Для каждого ФактическиеРасходыСсылка Из мсвФактическиеРасходы Цикл
			ПараметрыФормы = Новый Структура("Ключ", ФактическиеРасходыСсылка);
			ОткрытьФорму("Документ.упФактическиеРасходы.Форма.ФормаДокумента", ПараметрыФормы);
		КонецЦикла;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ не сформирован.'"),, ТекстОшибки, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьСтавкиНДС20()
	
	СтавкиНДС20 = Новый Массив();
	СтавкиНДС20.Добавить(Перечисления.упСтавкиНДС.НДС20);
	СтавкиНДС20.Добавить(Перечисления.упСтавкиНДС.НДС20_120);
	
	Возврат СтавкиНДС20;
	
КонецФункции

Функция ПолучитьСтавкиНДС18()
	
	СтавкиНДС18 = Новый Массив();
	СтавкиНДС18.Добавить(Перечисления.упСтавкиНДС.НДС18);
	СтавкиНДС18.Добавить(Перечисления.упСтавкиНДС.НДС18_118);
	
	Возврат СтавкиНДС18;
	
КонецФункции

Функция ПолучитьСтавкиНДС10()
	
	СтавкиНДС10 = Новый Массив();
	СтавкиНДС10.Добавить(Перечисления.упСтавкиНДС.НДС10);
	СтавкиНДС10.Добавить(Перечисления.упСтавкиНДС.НДС10_110);
	
	Возврат СтавкиНДС10;
	
КонецФункции

#КонецОбласти 