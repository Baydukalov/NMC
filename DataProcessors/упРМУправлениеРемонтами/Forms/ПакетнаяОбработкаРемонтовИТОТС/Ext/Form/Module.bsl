#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Гараж                 = Параметры.Гараж;
	ТекущаяДата           = Параметры.ТекущаяДата;
	ДатаНачальныхОстатков = Параметры.ДатаНачальныхОстатков;
	СрокОтсчета           = Параметры.СрокОтсчета;
	СрокПредупрежденияОТОПробег      = Параметры.СрокПредупрежденияОТОПробег;
	СрокПредупрежденияОТОМоточасы    = Параметры.СрокПредупрежденияОТОМоточасы;
	СпособПланированияРемонтныхРабот = Параметры.СпособПланированияРемонтныхРабот;
	АдресСхемыКомпоновкиДанных       = Параметры.АдресСхемыКомпоновкиДанных;
	НастройкиСхемыКомпоновкиДанных   = Параметры.НастройкиСхемыКомпоновкиДанных;	
	СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами;
	ИнициализироватьКомпоновщикНастроек(НастройкиСхемыКомпоновкиДанных);
	
	// СтандартныеПодсистемы.Печать
	мсвСписокОбъектов = Новый Массив;
	мсвСписокОбъектов.Добавить(Метаданные.Документы.упРемонтИТОТС);
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.СписокРемонтовДляПечати.КоманднаяПанель, мсвСписокОбъектов);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокСозданныхЗаявокНаРемонтВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "СписокСозданныхЗаявокНаРемонтОбъектРемонта" Тогда
			Если ТипЗнч(ТекущиеДанные.ОбъектРемонта) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
				ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ОбъектРемонта), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Иначе	
				ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ОбъектРемонта), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли;
			
		ИначеЕсли Поле.Имя = "СписокСозданныхЗаявокНаРемонтЗаявкаНаРемонтИТОТС" Тогда
			ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ЗаявкаНаРемонтИТОТС), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокСозданныхРемонтовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "СписокСозданныхРемонтовОбъектРемонта" Тогда
			Если ТипЗнч(ТекущиеДанные.ОбъектРемонта) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
				ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ОбъектРемонта), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Иначе	
				ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ОбъектРемонта), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли;
			
		ИначеЕсли Поле.Имя = "СписокСозданныхРемонтовРемонтИТОТС" Тогда
			ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.РемонтИТОТС), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборПодобратьОбъектыРемонта(Команда)
	ПерейтиНаСтраницу(Элементы.СписокОбъектов);
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовОтметитьВсе(Команда)
	
	Для каждого Строка Из СписокОбъектовРемонта Цикл
		Строка.Пометка = Истина;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовСнятьВсе(Команда)
	Для каждого Строка Из СписокОбъектовРемонта Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовФормированиеРемонтов(Команда)
	ФормируютсяДокументыРемонт = Истина;
	СформироватьРемонтыНаСервере();
	ПерейтиНаСтраницу(Элементы.Ремонты);
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовФормированиеЗаявокНаРемонт(Команда)
	ФормируютсяДокументыРемонт = Ложь;
	СформироватьЗаявкиНаРемонтНаСервере();
	ПерейтиНаСтраницу(Элементы.Ремонты);	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовНазад(Команда)
	ПерейтиНаСтраницу("ОтборОбъектыРемонта");
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПечатьДокументов(Команда)
	ПерейтиНаСтраницу(Элементы.ПечатьДокументов);
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	ОписаниеКоманды = УправлениеПечатьюКлиентПовтИсп.ОписаниеКомандыПечати(Команда.Имя, ЭтаФорма.Команды.Найти("АдресКомандПечатиВоВременномХранилище").Действие);
	
	мсвДокументов = Новый Массив;
	мсвСтрокиСписка = СписокРемонтовДляПечати.НайтиСтроки(Новый Структура("Пометка",Истина));
	
	Для каждого Строка Из мсвСтрокиСписка Цикл
		мсвДокументов.Добавить(Строка.РемонтИТОТС);
	КонецЦикла;

	ОписаниеКоманды.СразуНаПринтер = НапечататьСразуНаПринтер;
	ОписаниеКоманды.Вставить("ОбъектыПечати", мсвДокументов);
	Если НапечататьСразуНаПринтер Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор,
		ОписаниеКоманды.ОбъектыПечати, ОписаниеКоманды.ДополнительныеПараметры);
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор,
		ОписаниеКоманды.ОбъектыПечати, ЭтаФорма, ОписаниеКоманды);
	КонецЕсли;

	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура РемонтыЗаявкиСнятьВсе(Команда)
	Для каждого Строка Из СписокСозданныхЗаявокНаРемонт Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РемонтыЗаявкиОтметитьВсе(Команда)
	Для каждого Строка Из СписокСозданныхЗаявокНаРемонт Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РемонтыРемонтыОтметитьВсе(Команда)
	Для каждого Строка Из СписокСозданныхРемонтов Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РемонтыРемонтыСнятьВсе(Команда)
	Для каждого Строка Из СписокСозданныхРемонтов Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РемонтыПечатьДокументов(Команда)
	ПерейтиНаСтраницу(Элементы.ПечатьДокументов);
КонецПроцедуры

&НаКлиенте
Процедура РемонтыНазад(Команда)
	ПерейтиНаСтраницу(ПредыдущаяСтраницаСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДокументовНазад(Команда)
	ПерейтиНаСтраницу(ПредыдущаяСтраницаСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДокументовОтметитьВсе(Команда)
	Для каждого Строка Из СписокРемонтовДляПечати Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьДокументовСнятьВсе(Команда)
	Для каждого Строка Из СписокРемонтовДляПечати Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьВидимостьКомандПанелиНавигации()
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ОтборОбъектыРемонта Тогда
		Элементы.НавигацияСтраницы.ТекущаяСтраница     = Элементы.НавигацияОтборОбъектыРемонта;
		Элементы.ОтборПодобратьОбъектыРемонта.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Настройка отбора объектов ремонта'");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СписокОбъектов Тогда
		Элементы.НавигацияСтраницы.ТекущаяСтраница = Элементы.НавигацияСписокОбъектовРемонта;
		//Элементы.СписокТСФормированиеПутевыхЛистов.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Список объектов ремонта'");
		
		Если СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ПоВыбору") Тогда
			Элементы.СписокОбъектовФормированиеРемонтов.КнопкаПоУмолчанию = Истина;
			
		ИначеЕсли СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ДокументомЗаявкаНаРемонт") Тогда	
			Элементы.СписокОбъектовФормированиеЗаявок.КнопкаПоУмолчанию = Истина;
			Элементы.СписокОбъектовФормированиеРемонтов.Видимость = Ложь;
						
		ИначеЕсли СпособПланированияРемонтныхРабот = ПредопределенноеЗначение("Перечисление.упСпособыПланированияРемонтныхРабот.ДокументомРемонт") Тогда		
			Элементы.СписокОбъектовФормированиеРемонтов.КнопкаПоУмолчанию = Истина;
			Элементы.СписокОбъектовФормированиеЗаявок.Видимость = Ложь;
			
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Ремонты Тогда
		Элементы.НавигацияСтраницы.ТекущаяСтраница = Элементы.НавигацияРемонты;
		//Элементы.ПутевыеЛистыПечатьДокументов.КнопкаПоУмолчанию = Истина;
				
		Если ФормируютсяДокументыРемонт Тогда
			ЗаголовокСтраницы	= Нстр("ru = 'Формирование ремонтов'");
		Иначе
			Элементы.РемонтыПечатьДокументов.Видимость = Ложь;
			ЗаголовокСтраницы	= Нстр("ru = 'Формирование заявок'");
		КонецЕсли;
	
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ПечатьДокументов Тогда	
		Элементы.НавигацияСтраницы.ТекущаяСтраница = Элементы.НавигацияПечатьДокументов;
		Элементы.ПечатьДокументовЗакрыть.КнопкаПоУмолчанию = Истина;
		ЗаголовокСтраницы	= Нстр("ru = 'Печать ремонтов'");
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницу(ЭлементСтраница)
	
	ПредыдущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	
	Если ТипЗнч(ЭлементСтраница) = Тип("Строка") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы[ЭлементСтраница];
	Иначе	
		Элементы.Страницы.ТекущаяСтраница = ЭлементСтраница;	
	КонецЕсли;
	
	ПредыдущаяСтраницаСтрока = ПредыдущаяСтраница.Имя;
	УстановитьВидимостьКомандПанелиНавигации();
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ОтборОбъектыРемонта Тогда
		
		Гараж        = ПредопределенноеЗначение("Справочник.упГаражи.ПустаяСсылка");
		МестоРемонта = ПредопределенноеЗначение("Справочник.упМестаРемонта.ПустаяСсылка");
		СервиснаяОрганизация = ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка");
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СписокОбъектов Тогда	
		
		УстановитьДоступностьЭлементовСпособаРемонта();
		
		ЗаполнитьСписокРемонтов();
				
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Ремонты Тогда		
		
		Если ФормируютсяДокументыРемонт Тогда
			Элементы.ЗаявкиРемонты.ТекущаяСтраница = Элементы.ГруппаРемонты;
		Иначе	
			Элементы.ЗаявкиРемонты.ТекущаяСтраница = Элементы.ГруппаЗаявки;
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ПечатьДокументов Тогда			
		СписокРемонтовДляПечати.Очистить();
		Если ПредыдущаяСтраница = Элементы.Ремонты Тогда
			мсвСтроки = СписокСозданныхРемонтов.НайтиСтроки(Новый Структура("Пометка",Истина));
			
			Для каждого Строка Из мсвСтроки Цикл
				НоваяСтрока = СписокРемонтовДляПечати.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.Пометка = Истина;
			КонецЦикла;
			
		ИначеЕсли ПредыдущаяСтраница = Элементы.СписокОбъектов Тогда	
			мсвСтроки = СписокОбъектовРемонта.НайтиСтроки(Новый Структура("Пометка",Истина));
			
			Для каждого Строка Из мсвСтроки Цикл
				Если ЗначениеЗаполнено(Строка.ПоследнийРемонт) Тогда
					НоваяСтрока = СписокРемонтовДляПечати.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					НоваяСтрока.РемонтИТОТС = Строка.ПоследнийРемонт;
					НоваяСтрока.Пометка = Истина;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек(Настройки)
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	Если НЕ ЗначениеЗаполнено(Настройки) Тогда
		СхемаКомпоновкиДанных = Обработки.упРМУправлениеРемонтами.ПолучитьМакет("ОбъектыРемонта");
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;
		
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРемонтов()
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	упСервисныеФункции.УстановитьПараметрСКД(Настройки, "Гараж", Гараж, ЗначениеЗаполнено(Гараж));	
	упСервисныеФункции.УстановитьПараметрСКД(Настройки, "ТекущаяДата" , ТекущаяДата, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(Настройки, "ДатаНачальныхОстатков" , ДатаНачальныхОстатков, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(Настройки, "СрокОтсчета" , СрокОтсчета, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(Настройки, "СрокПредупрежденияОТОПробег" , СрокПредупрежденияОТОПробег, Истина);
	упСервисныеФункции.УстановитьПараметрСКД(Настройки, "СрокПредупрежденияОТОМоточасы" , СрокПредупрежденияОТОМоточасы, Истина);
	
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	СписокОбъектовРемонта.Очистить();
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Для каждого Строка Из Результат Цикл
		НоваяСтрока = СписокОбъектовРемонта.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.НачалоРемонтаПлан = ?(ЗначениеЗаполнено(Строка.ДатаСледующегоТО),Строка.ДатаСледующегоТО,Строка.ПрогнозируемаяДатаСледующегоТО);
		Если Истина
			И ЗначениеЗаполнено(НоваяСтрока.НачалоРемонтаПлан)
			И ЗначениеЗаполнено(НоваяСтрока.ВидРемонта)
			И Строка.ВремяРаботПлан > 0
		Тогда
			НоваяСтрока.ОкончаниеРемонтаПлан = НоваяСтрока.НачалоРемонтаПлан + Строка.ВремяРаботПлан * 60 * 60;	
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СформироватьЗаявкиНаРемонтНаСервере()
	
	СписокСозданныхЗаявокНаРемонт.Очистить();
	
	мсвВыделенныеСтроки = СписокОбъектовРемонта.НайтиСтроки(Новый Структура("Пометка",Истина));
	мсвОбъектыРемонта   =  Новый Массив;
	
	Для каждого Строка Из мсвВыделенныеСтроки Цикл
		
		сткОбъектРемонта = Новый Структура();
		сткОбъектРемонта.Вставить("ОбъектРемонта",        Строка.ОбъектРемонта);
		сткОбъектРемонта.Вставить("ВидРемонта",           Строка.ВидРемонта);
		сткОбъектРемонта.Вставить("НачалоРемонтаПлан",    Строка.НачалоРемонтаПлан);
		сткОбъектРемонта.Вставить("ОкончаниеРемонтаПлан", Строка.ОкончаниеРемонтаПлан);
		мсвОбъектыРемонта.Добавить(сткОбъектРемонта); 
		
	КонецЦикла;
	
	Реквизиты = Новый Структура();
	Реквизиты.Вставить("МестоРемонта",  МестоРемонта);
	Реквизиты.Вставить("Гараж",         Гараж);
	Реквизиты.Вставить("СервиснаяОрганизация", СервиснаяОрганизация);
	Реквизиты.Вставить("МестоРемонта",  МестоРемонта);
	Реквизиты.Вставить("СпособРемонта", СпособРемонта);	
	Реквизиты.Вставить("Документ",      Документы.упЗаявкаНаРемонтИТОТС.ПустаяСсылка());
	Отказ = Ложь;
	мсвЗаявки = упРемонтИТОТС.СформироватьДокументыНаРемонтИТОТС(мсвОбъектыРемонта, Реквизиты, Отказ);
	
	Если НЕ Отказ Тогда
		Для каждого ЗаявкаНаРемонтИТОТС Из мсвЗаявки Цикл

			НоваяСтрока = СписокСозданныхЗаявокНаРемонт.Добавить();
			НоваяСтрока.ОбъектРемонта             = ЗаявкаНаРемонтИТОТС.ОбъектРемонта;
			НоваяСтрока.ЗаявкаНаРемонтИТОТС       = ЗаявкаНаРемонтИТОТС.ДокументСсылка;
			СтатусНовая = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая;
			НоваяСтрока.СтатусРемонтаИТОТС        = СтатусНовая;
			НоваяСтрока.ПорядокСтатусРемонтаИТОТС = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Индекс(СтатусНовая);
			НоваяСтрока.Пометка = Ложь;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьРемонтыНаСервере()
	
	СписокСозданныхРемонтов.Очистить();
	
	мсвВыделенныеСтроки = СписокОбъектовРемонта.НайтиСтроки(Новый Структура("Пометка",Истина));
	мсвОбъектыРемонта   =  Новый Массив;
	
	Для каждого Строка Из мсвВыделенныеСтроки Цикл
		
		сткОбъектРемонта = Новый Структура();
		сткОбъектРемонта.Вставить("ОбъектРемонта",        Строка.ОбъектРемонта);
		сткОбъектРемонта.Вставить("ВидРемонта",           Строка.ВидРемонта);
		сткОбъектРемонта.Вставить("НачалоРемонтаПлан",    Строка.НачалоРемонтаПлан);
		сткОбъектРемонта.Вставить("ОкончаниеРемонтаПлан", Строка.ОкончаниеРемонтаПлан);
		
		мсвОбъектыРемонта.Добавить(сткОбъектРемонта); 
		
	КонецЦикла;
	
	Реквизиты = Новый Структура();
	Реквизиты.Вставить("МестоРемонта",  МестоРемонта);
	Реквизиты.Вставить("Гараж",         Гараж);
	Реквизиты.Вставить("СервиснаяОрганизация", СервиснаяОрганизация);
	Реквизиты.Вставить("СпособРемонта", СпособРемонта);
	Реквизиты.Вставить("Документ",      Документы.упРемонтИТОТС.ПустаяСсылка());
	Отказ = Ложь;
	мсвЗаявки = упРемонтИТОТС.СформироватьДокументыНаРемонтИТОТС(мсвОбъектыРемонта, Реквизиты, Отказ);
	
	Если НЕ Отказ Тогда
		Для каждого РемонтИТОТС Из мсвЗаявки Цикл

			НоваяСтрока = СписокСозданныхРемонтов.Добавить();
			НоваяСтрока.ОбъектРемонта = РемонтИТОТС.ОбъектРемонта;
			НоваяСтрока.РемонтИТОТС   = РемонтИТОТС.ДокументСсылка;
			СтатусНовый = Перечисления.упСтатусыРемонтаИТОТС.Новый;
			НоваяСтрока.СтатусРемонтаИТОТС        = СтатусНовый;
			НоваяСтрока.ПорядокСтатусРемонтаИТОТС = Перечисления.упСтатусыРемонтаИТОТС.Индекс(СтатусНовый);
			НоваяСтрока.Пометка = Ложь;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиЗаявкиКВыполнению(Команда)
	ПеревестиЗаявкиКВыполнениюНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПеревестиЗаявкиКВыполнениюНаСервере()
	
	мсвСтрокиСписка = СписокСозданныхЗаявокНаРемонт.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого Строка Из мсвСтрокиСписка Цикл
		ОписаниеОшибки = "";
		Отказ = Ложь;
		
		Если Строка.СтатусРемонтаИТОТС = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось перевести к выполнению %1. Ошибка: %2'");
			ОписаниеОшибки = Нстр("ru = 'Нельзя переводить к выполнению отмененную заявку.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.ЗаявкаНаРемонтИТОТС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Продолжить;
		КонецЕсли;
		
		СтатусКВыполнению = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению;
		упРемонтИТОТС.УстановитьСтатусДокументаРемонта(Строка.ЗаявкаНаРемонтИТОТС, СтатусКВыполнению, ОписаниеОшибки, Отказ);
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось перевести к выполнению %1. Ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.ЗаявкаНаРемонтИТОТС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе	
			Строка.СтатусРемонтаИТОТС        = СтатусКВыполнению;
			Строка.ПорядокСтатусРемонтаИТОТС = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Индекс(СтатусКВыполнению);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗапланироватьРемонтыНаСервере()
	
	мсвСтрокиСписка = СписокСозданныхРемонтов.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого Строка Из мсвСтрокиСписка Цикл
		ОписаниеОшибки = "";
		Отказ = Ложь;
		
		Если Строка.СтатусРемонтаИТОТС = Перечисления.упСтатусыРемонтаИТОТС.Отменен Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось перевести в запланирован %1. Ошибка: %2'");
			ОписаниеОшибки = Нстр("ru = 'Нельзя планировать отмененный ремонт.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.РемонтИТОТС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Продолжить;
		КонецЕсли;
		
		СтатусЗапланирован = Перечисления.упСтатусыРемонтаИТОТС.Запланирован;
		упРемонтИТОТС.УстановитьСтатусДокументаРемонта(Строка.РемонтИТОТС, СтатусЗапланирован, ОписаниеОшибки, Отказ);
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось запланировать %1. Ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.РемонтИТОТС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе	
			Строка.СтатусРемонтаИТОТС        = СтатусЗапланирован;
			Строка.ПорядокСтатусРемонтаИТОТС = Перечисления.упСтатусыРемонтаИТОТС.Индекс(СтатусЗапланирован);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗапланироватьРемонты(Команда)
	ЗапланироватьРемонтыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтменитьЗаявкиНаСервере()
	
	мсвСтрокиСписка = СписокСозданныхЗаявокНаРемонт.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого Строка Из мсвСтрокиСписка Цикл
		ОписаниеОшибки = "";
		Отказ = Ложь;
		СтатусОтменен = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена;
		упРемонтИТОТС.УстановитьСтатусДокументаРемонта(Строка.ЗаявкаНаРемонтИТОТС, СтатусОтменен, ОписаниеОшибки, Отказ);
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось отменить %1. Ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.ЗаявкаНаРемонтИТОТС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе	
			Строка.СтатусРемонтаИТОТС        = СтатусОтменен;
			Строка.ПорядокСтатусРемонтаИТОТС = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Индекс(СтатусОтменен);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОтменитьРемонтыНаСервере()
	
	мсвСтрокиСписка = СписокСозданныхРемонтов.НайтиСтроки(Новый Структура("Пометка",Истина));
	Для каждого Строка Из мсвСтрокиСписка Цикл
		ОписаниеОшибки = "";
		Отказ = Ложь;
		СтатусОтменен = Перечисления.упСтатусыРемонтаИТОТС.Отменен;
		упРемонтИТОТС.УстановитьСтатусДокументаРемонта(Строка.РемонтИТОТС, СтатусОтменен, ОписаниеОшибки, Отказ);
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Не удалось отменить %1. Ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.РемонтИТОТС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Иначе	
			Строка.СтатусРемонтаИТОТС        = СтатусОтменен;
			Строка.ПорядокСтатусРемонтаИТОТС = Перечисления.упСтатусыРемонтаИТОТС.Индекс(СтатусОтменен);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаявки(Команда)
	ОтменитьЗаявкиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРемонты(Команда)
	ОтменитьРемонтыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура МестоРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Гараж) Тогда
		СтандартнаяОбработка = Ложь;
		сткОтбор = Новый Структура("Владелец", Гараж);
		сткПараметры = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("Справочник.упМестаРемонта.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРемонтаПриИзменении(Элемент)
	УстановитьДоступностьЭлементовСпособаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовСпособаРемонта()
	Если СпособРемонта = ПредопределенноеЗначение("Перечисление.упСпособыРемонтов.СобственнымиСилами") Тогда
		СервиснаяОрганизация = ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка");
		Элементы.СтраницыСпособыРемонта.ТекущаяСтраница = Элементы.ГруппаСобственнымиСилами;
	Иначе
		Гараж                = ПредопределенноеЗначение("Справочник.упГаражи.ПустаяСсылка");
		МестоРемонта         = ПредопределенноеЗначение("Справочник.упМестаРемонта.ПустаяСсылка");
		Элементы.СтраницыСпособыРемонта.ТекущаяСтраница = Элементы.ГруппаСервиснаяОрганизация;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти