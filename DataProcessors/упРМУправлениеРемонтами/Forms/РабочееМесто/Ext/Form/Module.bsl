
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	УстановитьПараметрыУправленияРемонтамиНаСервере();
	ОбновитьВидимость();
	
	ИнициализироватьКомпоновщикНастроек();
	
	УстановитьДоступныеЗначенияВариантовСписков();
	ВосстановитьОбщиеНастройкиФормы();
	ОбновитьСписокОбъектовРемонтаНаСервере();
	ОбновитьДинамическиеСписки();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГаражПриИзменении(Элемент)
	
	ОбновитьСписокОбъектовРемонтаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовРемонтаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("УстановитьОтборНаСписок", 0.1, Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаявокНаРемонтПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	текСтрока = Элементы.СписокОбъектовРемонта.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		Отказ = Истина;
		ОткрытьФорму("Документ.упЗаявкаНаРемонтИТОТС.ФормаОбъекта", Новый Структура("ТранспортноеСредство, ДополнительноеОборудование", текСтрока.ТранспортноеСредство, текСтрока.ДополнительноеОборудование));	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРемонтовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	текСтрока = Элементы.СписокОбъектовРемонта.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		Отказ = Истина;
		ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", Новый Структура("ТранспортноеСредство, ДополнительноеОборудование", текСтрока.ТранспортноеСредство, текСтрока.ДополнительноеОборудование));	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНастроекПриИзменении(Элемент = Неопределено)

	Если ЗначениеЗаполнено(ВариантСписков) Тогда
		сткНастройки = ВернутьВариантНастройкиСпискаОбъектовРемонта();
		Если сткНастройки <> Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
		КонецЕсли;
	Иначе
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	// серверные вызовы
	ОбновитьСписокОбъектовРемонтаНаСервере();
	ОбновитьДинамическиеСписки();
	СохранитьОбщиеНастройкиФормы();
	
КонецПроцедуры

&НаСервере
Функция ВернутьВариантНастройкиСпискаОбъектовРемонта()
	
	Возврат ХранилищаНастроек.упХранилищеНастроек.Загрузить("Обработка.упРМУправлениеРемонтами.НастройкаСпискаОбъектовРемонта", ВариантСписков);
	
КонецФункции

&НаКлиенте
Процедура ГруппаРемонтныеДокументыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта Тогда
		ОчиститьОтборНаСписок();
		ПодключитьОбработчикОжидания("УстановитьОтборНаСписок", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта(Команда)	
	ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта = Не ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта;
	Если ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта Тогда		
		Элементы.ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта.Пометка = Истина;
		Элементы.СписокЗаявокНаРемонтОтображатьДокументыТолькоПоТекущемуОбъектуРемонта.Пометка = Истина;
		ПодключитьОбработчикОжидания("УстановитьОтборНаСписок", 0.1, Истина);
	Иначе
		Элементы.ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта.Пометка = Ложь;
		Элементы.СписокЗаявокНаРемонтОтображатьДокументыТолькоПоТекущемуОбъектуРемонта.Пометка = Ложь;
		Если ОтборУстановлен Тогда
			ОчиститьОтборНаСписок();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокОбъектовРемонта(Команда)
	
	ОбновитьСписокОбъектовРемонтаНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьПараметрыУправленияРемонтами(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПараметрыУправленияРемонтами", ПараметрыУправленияРемонтами);
	ОткрытьФорму("Обработка.упРМУправлениеРемонтами.Форма.ФормаУстановкиПараметров", ПараметрыФормы,,,,,Новый ОписаниеОповещения("УстановитьПараметрыУправленияРемонтамиЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПройденныеТО(Команда)
	
	текСтрока = Элементы.СписокОбъектовРемонта.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбрано транспортное средство!'"));
	ИначеЕсли Не ЗначениеЗаполнено(текСтрока.ПоследнееТО) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(СтрШаблон("ru = 'По транспортному средству ""%1"" нет данных о пройденных ТО'", текСтрока.ТранспортноеСредство)));
	Иначе	
		ОткрытьФорму("РегистрСведений.упПройденныеТО.ФормаСписка", Новый Структура("Отбор", Новый Структура("ТранспортноеСредство", текСтрока.ОбъектРемонта)), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОбъектовРемонта(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Настройки", КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Настройка отбора списка объектов ремонта'"));
	
	ОткрытьФорму("ОбщаяФорма.упФормаНастройкиОтбора", ПараметрыФормы,,,,, Новый ОписаниеОповещения("НастройкаОбъектовРемонтаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область СохранениеВосстановлениеНастроекСписков

&НаКлиенте
Процедура СохранитьВариантНастройкиСпискаОбъектовРемонта(Команда)
	
	СохранитьВариантНастройки("НастройкаСпискаОбъектовРемонта", "СохранитьВариантНастройкиСпискаОбъектовРемонтаНаСервере", ВариантСписков);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройкиСпискаОбъектовРемонта(Команда)
	
	ОткрытьВариантНастройки("НастройкаСпискаОбъектовРемонта", "ОткрытьВариантНастройкиСпискаОбъектовРемонтаНаКлиенте", ВариантСписков);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВариантНастройки(Знач ТипНастройки, ИмяОбработчикаОповещения, ТекушийВариантНастройки)
	
	сткПараметры = Новый Структура("КлючОбъекта, КлючТекущихНастроек", "Обработка.упРМУправлениеРемонтами." + ТипНастройки, ТекушийВариантНастройки);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаСохранения", сткПараметры,,,,,Новый ОписаниеОповещения(ИмяОбработчикаОповещения, ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантНастройки(Знач ТипНастройки, ИмяОбработчикаОповещения, ВариантНастройки)
	
	сткПараметры = Новый Структура("КлючОбъекта, КлючТекущихНастроек", "Обработка.упРМУправлениеРемонтами." + ТипНастройки, ВариантНастройки);
	ОткрытьФорму("ХранилищеНастроек.упХранилищеНастроек.ФормаЗагрузки", сткПараметры,,,,,Новый ОписаниеОповещения(ИмяОбработчикаОповещения, ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаСервере
// Процедура завершения после закрытия формы сохранения настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СохранитьВариантНастройкиСпискаОбъектовРемонтаНаСервере(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.ВариантСписков = Результат.КлючНастроек;
		сткНастройки = Новый Структура;
		сткНастройки.Вставить("Пользователь", ТекущийПользователь);
		сткНастройки.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроек.Настройки);
		ХранилищаНастроек.упХранилищеНастроек.Сохранить("Обработка.упРМУправлениеРемонтами.НастройкаСпискаОбъектовРемонта", ВариантСписков, сткНастройки);
		УстановитьДоступныеЗначенияВариантовСписков();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы загрузки настроек. 
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОткрытьВариантНастройкиСпискаОбъектовРемонтаНаКлиенте(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		УстановитьДоступныеЗначенияВариантовСписков();
		ЭтаФорма.ВариантСписков = Результат.КлючНастроек;
		сткНастройки = ВернутьВариантНастройкиСпискаОбъектовРемонта();
		Если сткНастройки <> Неопределено Тогда
			ВариантНастроекПриИзменении();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

&НаКлиенте
Процедура ПакетнаяФормаОбработкаРемонтовИТОТС(Команда)
	
	ПараметрыФормы = СформироватьПараметры();
	
	НастройкиСхемыКомпоновкиДанных = Неопределено;
	
	Если ЗначениеЗаполнено(ВариантСписков) Тогда
		сткНастройки = ВернутьВариантНастройкиСпискаОбъектовРемонта();
		Если сткНастройки <> Неопределено Тогда
			НастройкиСхемыКомпоновкиДанных = сткНастройки.НастройкиКомпоновкиДанных;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("НастройкиСхемыКомпоновкиДанных", НастройкиСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("СпособПланированияРемонтныхРабот", ПараметрыУправленияРемонтами.СпособПланированияРемонтныхРабот);
	
	ОткрытьФорму("Обработка.упРМУправлениеРемонтами.Форма.ПакетнаяОбработкаРемонтовИТОТС", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Функция СформироватьПараметры()
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("Гараж", Гараж);
	
	текДата      = ТекущаяДатаСеанса();
	сткПараметры = ВернутьПараметрыОтсчета(текДата);
	
	ПараметрыФормы.Вставить("ТекущаяДата", текДата);
	ПараметрыФормы.Вставить("ДатаНачальныхОстатков", сткПараметры.ДатаНачальныхОстатков);
	ПараметрыФормы.Вставить("СрокОтсчета", сткПараметры.СрокОтсчета);
	ПараметрыФормы.Вставить("СрокПредупрежденияОТОПробег",   ПараметрыУправленияРемонтами.СрокПредупрежденияОТОПробег);
	ПараметрыФормы.Вставить("СрокПредупрежденияОТОМоточасы", ПараметрыУправленияРемонтами.СрокПредупрежденияОТОМоточасы);
	
	Возврат ПараметрыФормы;
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	СхемаКомпоновкиДанных = Обработки.упРМУправлениеРемонтами.ПолучитьМакет("ОбъектыРемонта");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры // ИнициализироватьКомпоновщикНастроек()

&НаСервере
Процедура ОбновитьСписокОбъектовРемонтаНаСервере()
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	СписокОбъектовРемонта.Очистить();
	
	// Установка параметров
	текДата = ТекущаяДатаСеанса();
	сткПараметры = ВернутьПараметрыОтсчета(текДата);
	
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "Гараж"                        , Гараж                                                     , ЗначениеЗаполнено(Гараж));	
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "ТекущаяДата"                  , текДата                                                   , Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "ДатаНачальныхОстатков"        , сткПараметры.ДатаНачальныхОстатков                        , Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "СрокОтсчета"                  , сткПараметры.СрокОтсчета                                  , Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "СрокПредупрежденияОТОДней"    , ПараметрыУправленияРемонтами.СрокПредупрежденияОТОДней    , Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "СрокПредупрежденияОТОПробег"  , ПараметрыУправленияРемонтами.СрокПредупрежденияОТОПробег  , Истина);
	упСервисныеФункции.УстановитьПараметрСКД(КомпоновщикНастроек.Настройки, "СрокПредупрежденияОТОМоточасы", ПараметрыУправленияРемонтами.СрокПредупрежденияОТОМоточасы, Истина);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));

	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	СписокОбъектовРемонта.Загрузить(Результат);
	Если СписокОбъектовРемонта.Количество() Тогда
		ВклЦвет = Истина;
		Для каждого текОбъектРемонта Из СписокОбъектовРемонта Цикл
			Если Не (текОбъектРемонта.ВидОбъектаРемонта = 1 И ЗначениеЗаполнено(текОбъектРемонта.ТранспортноеСредство)) Тогда
				ВклЦвет = Не ВклЦвет;
			КонецЕсли; 
			текОбъектРемонта.ИндексЦветаФонаСтроки = ?(ВклЦвет, 1, 0);
		КонецЦикла; 		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокЗаявокНаРемонт, "Гараж", Гараж, ЗначениеЗаполнено(Гараж));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРемонтов, "Гараж", Гараж, ЗначениеЗаполнено(Гараж));
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборНаСписок()
	
	текСтрока = Элементы.СписокОбъектовРемонта.ТекущиеДанные;
	Если ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта И НЕ текСтрока = Неопределено Тогда
		Если Элементы.ГруппаРемонтныеДокументы.ТекущаяСтраница = Элементы.ГруппаЗаявкиНаРемонт Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокЗаявокНаРемонт, "ОбъектРемонта", текСтрока.ОбъектРемонта, ВидСравненияКомпоновкиДанных.Равно);
			ОтборУстановлен = Истина;
		ИначеЕсли Элементы.ГруппаРемонтныеДокументы.ТекущаяСтраница = Элементы.ГруппаРемонты Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРемонтов, "ОбъектРемонта", текСтрока.ОбъектРемонта, ВидСравненияКомпоновкиДанных.Равно);
			ОтборУстановлен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОтображатьДокументыТолькоПоТекущемуОбъектуРемонта И ОтборУстановлен Тогда
		ОчиститьОтборНаСписок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборНаСписок()
	
	Если Элементы.ГруппаРемонтныеДокументы.ТекущаяСтраница = Элементы.ГруппаЗаявкиНаРемонт Тогда
		ОтборДинамическогоСписка = СписокЗаявокНаРемонт.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
		ОтборДинамическогоСписка.Элементы.Очистить();
		ОтборУстановлен = Ложь;
	ИначеЕсли Элементы.ГруппаРемонтныеДокументы.ТекущаяСтраница = Элементы.ГруппаРемонты Тогда
		ОтборДинамическогоСписка = СписокРемонтов.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
		ОтборДинамическогоСписка.Элементы.Очистить();
		ОтборУстановлен = Ложь;
	КонецЕсли;	
	
КонецПроцедуры // ОчиститьОтборНаСписок()

&НаСервере
Процедура ОбновитьДинамическиеСписки()
	
	текДата = ТекущаяДатаСеанса();
	СписокРемонтов.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", текДата);
	
	Элементы.СписокРемонтов.Обновить();
	Элементы.СписокЗаявокНаРемонт.Обновить();
	
КонецПроцедуры

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура вызывается при закрытии формы установки параметров управления ремонтами.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьПараметрыУправленияРемонтамиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ПараметрыУправленияРемонтами = Результат;
		УстановитьПараметрыУправленияРемонтамиЗавершениеНаСервере();
		
	КонецЕсли;
    
КонецПроцедуры // УстановитьПараметрыУправленияРемонтамиЗавершение()

&НаСервере
Процедура УстановитьПараметрыУправленияРемонтамиЗавершениеНаСервере()
	
	упВариантыНастроекВызовСервера.СохранитьВариантНастроек(ПараметрыУправленияРемонтами, "Обработка.упРМУправлениеРемонтами.ПараметрыУправленияРемонтами", ТекущийПользователь);
	ОбновитьДинамическиеСписки();
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
// Процедура вызывается при закрытии формы настроек списка объектов ремонта
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НастройкаОбъектовРемонтаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Результат.Настройки);
		ОбновитьСписокОбъектовРемонта(Неопределено);
	КонецЕсли;
    
КонецПроцедуры // НастройкаЗаданийНаПеревозкуЗавершение()

#КонецОбласти 

&НаСервере
Процедура УстановитьПараметрыУправленияРемонтамиНаСервере()
	
	ЗаполнитьПараметрыПоУмолчанию = Истина;
	
	КлючОбъекта = "Обработка.упРМУправлениеРемонтами.ПараметрыУправленияРемонтами";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено Тогда
				ПараметрыУправленияРемонтами = СохраненныеНастройки;
				ЗаполнитьПараметрыПоУмолчанию = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗаполнитьПараметрыПоУмолчанию Тогда
		ПараметрыУправленияРемонтами = упВариантыНастроек.ПараметрыУправленияРемонтамиПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметрыУправленияРемонтамиНаСервере()

&НаСервере
// Возвращает дату и срок, от которых необходимо отсчитывать среднесуточный пробег
//
// Параметры:
//  ТекущаяДата - Дата - текущая дата сеанса
//
// Возвращаемое значение:
//   Структура
//      * ДатаНачальныхОстатков
//      * СрокОтсчета (в днях)
//
Функция ВернутьПараметрыОтсчета(ТекущаяДата);
	
	сткПараметры = Новый Структура;
	Если ПараметрыУправленияРемонтами.СрокОтсчетаСреднесуточногоПробега = Перечисления.упСрокиОтсчета.ЗаПоследнююНеделю Тогда
		сткПараметры.Вставить("ДатаНачальныхОстатков", ТекущаяДата - 604800);
		сткПараметры.Вставить("СрокОтсчета"          , 7);
	ИначеЕсли ПараметрыУправленияРемонтами.СрокОтсчетаСреднесуточногоПробега = Перечисления.упСрокиОтсчета.ЗаПоследниеДвеНедели Тогда
		сткПараметры.Вставить("ДатаНачальныхОстатков", ТекущаяДата - 1209600);
		сткПараметры.Вставить("СрокОтсчета"          , 14);
	ИначеЕсли ПараметрыУправленияРемонтами.СрокОтсчетаСреднесуточногоПробега = Перечисления.упСрокиОтсчета.ЗаПоследнийМесяц Тогда	
		сткПараметры.Вставить("ДатаНачальныхОстатков", ДобавитьМесяц(ТекущаяДата, -1));
		сткПараметры.Вставить("СрокОтсчета"          , (ТекущаяДата - сткПараметры.ДатаНачальныхОстатков)/86400);
	ИначеЕсли ПараметрыУправленияРемонтами.СрокОтсчетаСреднесуточногоПробега = Перечисления.упСрокиОтсчета.ЗаПоследниеТриМесяца Тогда
		сткПараметры.Вставить("ДатаНачальныхОстатков", ДобавитьМесяц(ТекущаяДата, -3));
		сткПараметры.Вставить("СрокОтсчета"          , (ТекущаяДата - сткПараметры.ДатаНачальныхОстатков)/86400);
	ИначеЕсли ПараметрыУправленияРемонтами.СрокОтсчетаСреднесуточногоПробега = Перечисления.упСрокиОтсчета.ЗаПоследниеПолГода Тогда	
		сткПараметры.Вставить("ДатаНачальныхОстатков", ДобавитьМесяц(ТекущаяДата, -6));
		сткПараметры.Вставить("СрокОтсчета"          , (ТекущаяДата - сткПараметры.ДатаНачальныхОстатков)/86400);
	КонецЕсли;
	
	Возврат сткПараметры;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовСписков()
	
	УстановитьДоступныеЗначенияВариантовНастроек("НастройкаСпискаОбъектовРемонта");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступныеЗначенияВариантовНастроек(ТипНастройки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.КлючВарианта,
	|	упВариантыНастроек.Наименование,
	|	упВариантыНастроек.ПометкаУдаления
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Объект = &Объект
	|	И (упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь)";
	Запрос.УстановитьПараметр("Объект"             , "Обработка.упРМУправлениеРемонтами." + ТипНастройки);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	ДоступныеВариантов = Запрос.Выполнить().Выгрузить();
	
	Если ТипНастройки = "НастройкаСпискаОбъектовРемонта" Тогда
		ПолеФормы = Элементы.ВариантСписков;
	Иначе
		ВызватьИсключение "Неизвестный тип настройки " + ТипНастройки;
	КонецЕсли;
	ПолеФормы.СписокВыбора.Очистить();
	Для каждого СтрокаВарианта Из ДоступныеВариантов Цикл
		Если СтрокаВарианта.ПометкаУдаления Тогда
			ПолеФормы.СписокВыбора.Добавить(СтрокаВарианта.КлючВарианта, СтрокаВарианта.Наименование,, БиблиотекаКартинок.Удалить);
		Иначе
			ПолеФормы.СписокВыбора.Добавить(СтрокаВарианта.КлючВарианта, СтрокаВарианта.Наименование);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимость()
	
	Если ПараметрыУправленияРемонтами.СпособПланированияРемонтныхРабот = Перечисления.упСпособыПланированияРемонтныхРабот.ПоВыбору Тогда
		Элементы.ГруппаЗаявкиНаРемонт.Видимость = Истина;
		Элементы.ГруппаРемонты.Видимость = Истина;
		
	ИначеЕсли ПараметрыУправленияРемонтами.СпособПланированияРемонтныхРабот = Перечисления.упСпособыПланированияРемонтныхРабот.ДокументомЗаявкаНаРемонт Тогда
		Элементы.ГруппаЗаявкиНаРемонт.Видимость = Истина;
		Элементы.ГруппаРемонты.Видимость        = Ложь;
		
	ИначеЕсли ПараметрыУправленияРемонтами.СпособПланированияРемонтныхРабот = Перечисления.упСпособыПланированияРемонтныхРабот.ДокументомРемонт Тогда	
		Элементы.ГруппаЗаявкиНаРемонт.Видимость = Ложь;
		Элементы.ГруппаРемонты.Видимость        = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СохранитьОбщиеНастройкиФормы()
	
	сткНастройки = Новый Структура;
	сткНастройки.Вставить("ВариантНастроек", ВариантСписков);
	КлючОбъекта = "Обработка.упРМУправлениеРемонтами.ОбщиеНастройки";
	
	Если ЭтаФорма.КлючОбщихНастроекФормы = "" Тогда
		НовыйВариант = Справочники.упВариантыНастроек.СоздатьЭлемент();
		НовыйВариант.КлючВарианта = Новый УникальныйИдентификатор;
		НовыйВариант.Наименование = НовыйВариант.КлючВарианта;
		НовыйВариант.Автор = ТекущийПользователь;
		НовыйВариант.ТолькоДляАвтора = Истина;
		НовыйВариант.Объект = КлючОбъекта;
		НовыйВариант.Записать();
		ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, НовыйВариант.КлючВарианта, сткНастройки);
	Иначе
		СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, КлючОбщихНастроекФормы);
		Если СохраненныеНастройки <> Неопределено Тогда
			ХранилищаНастроек.упХранилищеНастроек.Сохранить(КлючОбъекта, КлючОбщихНастроекФормы, сткНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СохранитьОбщиеНастройкиФормы()

&НаСервере
Процедура ВосстановитьОбщиеНастройкиФормы()
	
	КлючОбъекта =  "Обработка.упРМУправлениеРемонтами.ОбщиеНастройки";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВариантыНастроек.КлючВарианта,
	|	упВариантыНастроек.ВидПеревозки
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Автор = &Автор
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления";
	Запрос.УстановитьПараметр("Автор", ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", КлючОбъекта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			СохраненныеНастройки = ХранилищаНастроек.упХранилищеНастроек.Загрузить(КлючОбъекта, Выборка.КлючВарианта);
			Если СохраненныеНастройки <> Неопределено И ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
				ЭтаФорма.КлючОбщихНастроекФормы = Выборка.КлючВарианта;
				ЭтаФорма.ВариантСписков = СохраненныеНастройки.ВариантНастроек;
				сткНастройки = ВернутьВариантНастройкиСпискаОбъектовРемонта();
				Если сткНастройки <> Неопределено Тогда
					КомпоновщикНастроек.ЗагрузитьНастройки(сткНастройки.НастройкиКомпоновкиДанных);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВосстановитьОбщиеНастройкиФормы()

&НаКлиенте
Процедура СписокОбъектовРемонтаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокОбъектовРемонта.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТекущаяСсылка = ТекущиеДанные.ОбъектРемонта;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ",ТекущаяСсылка);
		Если ТипЗнч(ТекущаяСсылка) = ТИП("СправочникСсылка.упТранспортныеСредства") Тогда
			ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта",ПараметрыОткрытия, ЭтотОбъект);
		ИначеЕсли ТипЗнч(ТекущаяСсылка) = ТИП("СправочникСсылка.упДополнительноеОборудование") Тогда
			ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта",ПараметрыОткрытия, ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
