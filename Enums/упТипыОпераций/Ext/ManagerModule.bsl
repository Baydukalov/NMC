#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ИсключитьДополнительныеОперации 		= НЕ ПолучитьФункциональнуюОпцию("упИспользуютсяДополнительныеОперации");
	ИсключитьОперацииПоЗаданиюНаПеревозку 	= Ложь;
	текЗадание = Неопределено;
	Если Параметры.Свойство("Задание", текЗадание) Тогда
		текАдрес 	= Параметры.Адрес;
		Если ТипЗнч(текЗадание) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") И ЗначениеЗаполнено(текАдрес) Тогда
			ИсключитьОперацииПоЗаданиюНаПеревозку = Истина;	
		КонецЕсли;
	КонецЕсли;
						
	Если ИсключитьДополнительныеОперации ИЛИ ИсключитьОперацииПоЗаданиюНаПеревозку ИЛИ Не ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		текЗапрос = Новый Запрос;
		
		// Формирование исключаемых операций
		ЭтоПерваяТаблица = Истина;
		
		Если ИсключитьДополнительныеОперации Тогда
			текЗапрос.Текст = текЗапрос.Текст + ПолучитьТекстПоДополнительнымОперациям(ЭтоПерваяТаблица);
			ЭтоПерваяТаблица = Ложь;
		КонецЕсли;
		
		Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Тогда
			текЗапрос.Текст = текЗапрос.Текст + ПолучитьТекстПоДокументам(ЭтоПерваяТаблица);
			ЭтоПерваяТаблица = Ложь;
		КонецЕсли;
		
		Если ИсключитьОперацииПоЗаданиюНаПеревозку Тогда
			
			текЗапрос.Текст = текЗапрос.Текст + ПолучитьТекстПоЗаданиюНаПеревозку(ЭтоПерваяТаблица, текАдрес, текЗадание);
			текЗапрос.УстановитьПараметр("Адрес", 	текАдрес);
			текЗапрос.УстановитьПараметр("Ссылка", 	текЗадание);
			ЭтоПерваяТаблица = Ложь;
		КонецЕсли;
		
		текЗапрос.Текст = текЗапрос.Текст + ";" + Символы.ПС;
		
		текЗапрос.Текст = текЗапрос.Текст + "ВЫБРАТЬ
		|	упТипыОпераций.Ссылка
		|ИЗ
		|	Перечисление.упТипыОпераций КАК упТипыОпераций
		|		ЛЕВОЕ СОЕДИНЕНИЕ втИсключаемыеОперации КАК тбИсключаемые
		|		ПО упТипыОпераций.Ссылка = тбИсключаемые.Операция
		|ГДЕ
		|	тбИсключаемые.Операция ЕСТЬ NULL ";
		
		текВыборка = текЗапрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока текВыборка.Следующий() Цикл
			ДанныеВыбора.Добавить(текВыборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;								
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстПоДополнительнымОперациям(ЭтоПерваяТаблица)
	
	текРезультат = 
	?(ЭтоПерваяТаблица, "", "ОБЪЕДИНИТЬ" + Символы.ПС)
	 + "ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ДополнительнаяОперация) КАК Операция" 
	 + Символы.ПС
	 + ?(ЭтоПерваяТаблица, "ПОМЕСТИТЬ втИсключаемыеОперации" + Символы.ПС, "");
	
	Возврат текРезультат;
	
КонецФункции

Функция ПолучитьТекстПоДокументам(ЭтоПерваяТаблица)
	
	текРезультат = 
	?(ЭтоПерваяТаблица, "", "ОБЪЕДИНИТЬ" + Символы.ПС)
	 + "ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов) КАК Операция" 
	 + Символы.ПС
	 + ?(ЭтоПерваяТаблица, "ПОМЕСТИТЬ втИсключаемыеОперации" + Символы.ПС, "")
	 + "ОБЪЕДИНИТЬ" + Символы.ПС
	 + "ВЫБРАТЬ ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов) КАК Операция" 
	 + Символы.ПС;
	
	Возврат текРезультат;
	
КонецФункции

Функция ПолучитьТекстПоЗаданиюНаПеревозку(ЭтоПерваяТаблица, Адрес, Задание)
	
	текРезультат = 
	?(ЭтоПерваяТаблица, "", "ОБЪЕДИНИТЬ" + Символы.ПС)
	+"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА тбЗадание.АдресОтправления = &Адрес
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	КОНЕЦ КАК Операция" 
	+ Символы.ПС  
	+ ?(ЭтоПерваяТаблица, "ПОМЕСТИТЬ втИсключаемыеОперации" + Символы.ПС, "")
	+ "ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК тбЗадание
	|ГДЕ
	|	тбЗадание.Ссылка = &Ссылка";
		
	Возврат текРезультат;
		
КонецФункции

#КонецОбласти 

#КонецЕсли




