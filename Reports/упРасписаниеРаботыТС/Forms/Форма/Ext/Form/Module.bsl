
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма);

	Если Параметры.Свойство("ТранспортноеСредство") Тогда
		
		ТранспортноеСредство = Параметры.ТранспортноеСредство;
		ОбновитьДанные();
		
	КонецЕсли;
	
	ОбновитьШкалу(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидШкалыПриИзменении(Элемент)
	
	 ОбновитьШкалу(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОбновитьДанные(Команда)
	
	ОбновитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанные()

	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ТаблицаРасписание = ПолучитьРасписаниеРаботыТС();
	
	НачалоПолногоПериода    = '000101010000';
	ОкончаниеПолногоПериода = '000101010000';
	Для каждого СтрокаПериод Из ТаблицаРасписание Цикл
		
		НачалоПолногоПериода = ?(НачалоПолногоПериода = '000101010000', 
									СтрокаПериод.ДатаНачала,
									Мин(СтрокаПериод.ДатаНачала, НачалоПолногоПериода));
		
		ОкончаниеПолногоПериода = ?(ОкончаниеПолногоПериода = '000101010000', 
									СтрокаПериод.ДатаОкончания,
									Макс(СтрокаПериод.ДатаОкончания, ОкончаниеПолногоПериода));					
									
	КонецЦикла; 
	
	ТаблицаТС = ТаблицаРасписание.Скопировать(, "ТранспортноеСредство");
	ТаблицаТС.Свернуть("ТранспортноеСредство");
	ТаблицаТС.Сортировать("ТранспортноеСредство");
	Для каждого СтрокаТС Из ТаблицаТС Цикл
		ТочкаТС = Диаграмма.УстановитьТочку(СтрокаТС.ТранспортноеСредство);
		ТочкаТС.Расшифровка = СтрокаТС.ТранспортноеСредство;
	КонецЦикла;
	
	// Сформируем точки и периоды по типам расписания
	ТаблицаТипыРасписаний = ТаблицаРасписание.Скопировать(, "ТипРасписания,ТранспортноеСредство");
	ТаблицаТипыРасписаний.Свернуть("ТипРасписания,ТранспортноеСредство");
	ТаблицаТипыРасписаний.Сортировать("ТранспортноеСредство, ТипРасписания");
	Сч = 0;
	Для каждого СтрокаТипРасписания Из ТаблицаТипыРасписаний Цикл
		
		Сч = Сч + 1;
        КлючТипаРасписания = Строка(СтрокаТипРасписания.ТипРасписания) + Сч;
		ТочкаДиаграммы = Диаграмма.УстановитьТочку(КлючТипаРасписания, СтрокаТипРасписания.ТранспортноеСредство);
		ТочкаДиаграммы.Текст = СтрокаТипРасписания.ТипРасписания;
		ТочкаДиаграммы.Расшифровка = СтрокаТипРасписания.ТипРасписания;
		
		СтруктураПоиска = Новый Структура("ТранспортноеСредство, ТипРасписания", СтрокаТипРасписания.ТранспортноеСредство, СтрокаТипРасписания.ТипРасписания);
		СписокСтрок = ТаблицаРасписание.НайтиСтроки(СтруктураПоиска);
		ЗаполнитьПериодыГруппы(СписокСтрок, ТочкаДиаграммы, Диаграмма);
		
	КонецЦикла;

	// Развернем транспортные средства
	Для каждого СтрокаТС Из ТаблицаТС Цикл
		ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаТС.ТранспортноеСредство);
		Диаграмма.РазвернутьТочку(ТочкаДиаграммы, Истина);
	КонецЦикла;
	
	// Установим полный интервал диаграммы
	Если НачалоПолногоПериода <> '000101010000' И ОкончаниеПолногоПериода <> '000101010000' Тогда
		Диаграмма.УстановитьПолныйИнтервал(НачалоДня(НачалоПолногоПериода), КонецДня(ОкончаниеПолногоПериода));
	КонецЕсли;
	ВыделитьТекущуюДату(ЭтаФорма);
	
	Диаграмма.Обновление         = Истина;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПериодыГруппы(СписокСтрок, ТочкаДиаграммы, Диаграмма)

	СписокДобавленныхПериодов = Новый Массив;
	
	Для каждого СтрокаПериод Из СписокСтрок Цикл
		
		сткВозврат = ОпределитьХарактеристикиСостоянияТС(СтрокаПериод.ТипСостояния);
		
		СерияДиаграммы = Диаграмма.УстановитьСерию(СтрокаПериод.ТипСостояния);
		СерияДиаграммы.Цвет = сткВозврат.Цвет;
		
		ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаДиаграммы, СерияДиаграммы);
		
		Интервал = ЗначениеПериод.Добавить();
		Интервал.Начало = СтрокаПериод.ДатаНачала;
		Интервал.Конец  = СтрокаПериод.ДатаОкончания;
		
		Если НачалоДня(СтрокаПериод.ДатаНачала) = НачалоДня(СтрокаПериод.ДатаОкончания) Тогда
			ПериодСтрока = "(" + Формат(СтрокаПериод.ДатаНачала, "ДФ=ЧЧ:мм") + " - " 
								+ Формат(СтрокаПериод.ДатаОкончания, "ДФ=ЧЧ:мм") + ")";
		Иначе	
			
			ПериодСтрока = Формат(СтрокаПериод.ДатаНачала, "ДФ='дд.ММ ЧЧ:мм'") + " - "
							+ Формат(СтрокаПериод.ДатаОкончания, "ДФ='дд.ММ ЧЧ:мм'");
		КонецЕсли; 
		
		Интервал.Цвет = сткВозврат.Цвет;
		ТекстИнтервала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(сткВозврат.Текст, ПериодСтрока);
		Интервал.Текст = НСтр(ТекстИнтервала, КодЯзыка);
		
		СписокДобавленныхПериодов.Добавить(Интервал);
		
	КонецЦикла; 

	Возврат СписокДобавленныхПериодов;
	
КонецФункции

&НаСервере
Функция ОпределитьХарактеристикиСостоянияТС(ТипСостояния)
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Цвет");
	сткВозврат.Вставить("Текст");
	
	Если ТипСостояния = Перечисления.упТипыСостоянийТС.Недоступно Тогда
		сткВозврат.Цвет  = WebЦвета.Красный;
		сткВозврат.Текст = "ru = 'Недоступно %1'";
	ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.Ремонт Тогда
		сткВозврат.Цвет  = WebЦвета.Серый;
		сткВозврат.Текст = "ru = 'Ремонт %1'";
	ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.ВРейсе Тогда
		сткВозврат.Цвет  = WebЦвета.Синий;
		сткВозврат.Текст = "ru = 'В рейсе %1'";
	ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.Свободно Тогда
		сткВозврат.Цвет  = WebЦвета.Белоснежный;
		сткВозврат.Текст = "ru = 'Свободно %1'";
	ИначеЕсли ТипСостояния = Null Тогда // перерыв между рейсами
		сткВозврат.Цвет  = WebЦвета.СинийСПороховымОттенком;
		сткВозврат.Текст = "ru = 'Страховой запас %1'";
	Иначе
		сткВозврат.Цвет  = WebЦвета.Циан;
		сткВозврат.Текст = "ru = 'Прочее %1'";
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции // ОпределитьХарактеристикиСостоянияТС()

#КонецОбласти

#Область Прочее

&НаСервере
Функция ПолучитьРасписаниеРаботыТС()

	Запрос = Новый Запрос;
	Запрос.Текст = 
"
|//{Запрос: 0 ////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	упРасписаниеРаботыТС.ТранспортноеСредство КАК ТранспортноеСредство,
|	упРасписаниеРаботыТС.ТипРасписания КАК ТипРасписания,
|	упРасписаниеРаботыТС.ДатаНачала КАК ДатаНачала,
|	упРасписаниеРаботыТС.ДатаОкончания КАК ДатаОкончания,
|	упРасписаниеРаботыТС.СостояниеТС.ТипСостоянияТС КАК ТипСостояния
|ПОМЕСТИТЬ втРасписание
|ИЗ
|	(ВЫБРАТЬ
|		упРасписаниеРаботыТС.Транспорт КАК ТранспортноеСредство,
|		ЗНАЧЕНИЕ(Перечисление.упТипыРасписанийРаботыТС.План) КАК ТипРасписания,
|		упРасписаниеРаботыТС.ДатаНачала КАК ДатаНачала,
|		упРасписаниеРаботыТС.ДатаОкончания КАК ДатаОкончания,
|		упРасписаниеРаботыТС.СостояниеТранспорта КАК СостояниеТС
|	ИЗ
|		РегистрСведений.упЗанятостьТранспорта.СрезПоследних КАК упРасписаниеРаботыТС
|	ОБЪЕДИНИТЬ ВСЕ
|	ВЫБРАТЬ
|		упРасписаниеРаботыТС.ТранспортноеСредство КАК ТранспортноеСредство,
|		ЗНАЧЕНИЕ(Перечисление.упТипыРасписанийРаботыТС.Факт) КАК ТипРасписания,
|		упРасписаниеРаботыТС.ДатаНачала КАК ДатаНачала,
|		упРасписаниеРаботыТС.ДатаОкончания КАК ДатаОкончания,
|		упРасписаниеРаботыТС.СостояниеТС КАК СостояниеТС
|	ИЗ
|		РегистрСведений.упРасписаниеРаботыТС КАК упРасписаниеРаботыТС) КАК упРасписаниеРаботыТС
|ГДЕ
|	(НЕ (&ИспользоватьОтборПоТранспортномуСредству)
|		ИЛИ упРасписаниеРаботыТС.ТранспортноеСредство = &ТранспортноеСредство)
|	И (&ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
|		ИЛИ упРасписаниеРаботыТС.ДатаНачала <= &ДатаОкончания)
|	И (&ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
|		ИЛИ упРасписаниеРаботыТС.ДатаОкончания >= &ДатаНачала)
|ИНДЕКСИРОВАТЬ ПО
|	упРасписаниеРаботыТС.ТранспортноеСредство,
|	упРасписаниеРаботыТС.СостояниеТС.ТипСостоянияТС
|;
|//{Запрос: 2 ////////////////////////////////////////
|ВЫБРАТЬ
|	втРасписание.ТранспортноеСредство КАК ТранспортноеСредство,
|	втРасписание.ТипРасписания КАК ТипРасписания,
|	втРасписание.ДатаНачала КАК ДатаНачала,
|	втРасписание.ДатаОкончания КАК ДатаОкончания,
|	втРасписание.ТипСостояния КАК ТипСостояния
|ИЗ
|	втРасписание КАК втРасписание
|ОБЪЕДИНИТЬ ВСЕ
|ВЫБРАТЬ
|	втРасписание.ТранспортноеСредство КАК ТранспортноеСредство,
|	втРасписание.ТипРасписания КАК ТипРасписания,
|	ДОБАВИТЬКДАТЕ(втРасписание.ДатаОкончания, СЕКУНДА, 1) КАК ДатаНачала,
|	ДОБАВИТЬКДАТЕ(втРасписание.ДатаОкончания, СЕКУНДА, 1 + втРасписание.ТранспортноеСредство.ПерерывМеждуРейсами * 3600) КАК ДатаОкончания,
|	NULL КАК ТипСостояния
|ИЗ
|	втРасписание КАК втРасписание
|ГДЕ
|	втРасписание.ТипРасписания = ЗНАЧЕНИЕ(Перечисление.упТипыРасписанийРаботыТС.План)
|	И втРасписание.ТранспортноеСредство.ПерерывМеждуРейсами > 0
|	И втРасписание.ТипСостояния = ЗНАЧЕНИЕ(Перечисление.упТипыСостоянийТС.ВРейсе)";
	Запрос.УстановитьПараметр("ДатаНачала",     Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",  Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ТранспортноеСредство",  ТранспортноеСредство);
	Запрос.УстановитьПараметр("ИспользоватьОтборПоТранспортномуСредству", НЕ ТранспортноеСредство.Пустая());
	
	ТаблицаРасписание = Запрос.Выполнить().Выгрузить();

	Возврат ТаблицаРасписание;
	
КонецФункции
 
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьШкалу(Форма, ВидШкалы)

	Диаграмма = Форма.Диаграмма;
	
	ШкалаВремениЭлементы = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы;
	
	ПервыйЭлементШкалы = ШкалаВремениЭлементы[0];
	Для Сч = -ШкалаВремениЭлементы.Количество()+1 по -1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[-Сч]);
	КонецЦикла;
	
	Форма.ВидШкалы = ВидШкалы;
	
	Если ВидШкалы = 0 Тогда
		// День, час
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяцаДеньНедели;
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	ИначеЕсли ВидШкалы = 1 Тогда
		// Месяц, неделя, день
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Неделя;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
		ЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
		
	Иначе
		
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Год;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	КонецЕсли; 
	
	ВыделитьТекущуюДату(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВыделитьТекущуюДату(Форма)

	Форма.Диаграмма.ИнтервалыФона.Очистить();
	
	ЦветТекущегоДня = WebЦвета.БледноЗеленый;
	
	Если Форма.ВидШкалы = 0 Тогда
		// День, час
		ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоЧаса(ТекущаяДата()), КонецЧаса(ТекущаяДата()));
		ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
	Иначе
		Если НачалоДня(Форма.Диаграмма.НачалоПолногоИнтервала) <> НачалоДня(Форма.Диаграмма.КонецПолногоИнтервала) Тогда
			ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()));
			ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма)

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.ПоддержкаМасштаба               = ПоддержкаМасштабаДиаграммыГанта.Авто;
	Диаграмма.Окантовка                       = Истина;
	Диаграмма.Анимация                        = АнимацияДиаграммы.Использовать;
	Диаграмма.ВертикальнаяПрокрутка           = Истина;
	Диаграмма.ОтображатьПустыеЗначения        = Ложь;
	Диаграмма.ОтображатьЗаголовок             = Ложь;

КонецПроцедуры

#КонецОбласти
