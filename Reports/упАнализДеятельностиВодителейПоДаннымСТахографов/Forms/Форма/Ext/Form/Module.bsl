
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма);

	Если Параметры.Свойство("Водитель") Тогда
		
		Водитель = Параметры.Водитель;
		ОбновитьДанные();
		
	КонецЕсли;
	
	ОбновитьШкалу(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидШкалыПриИзменении(Элемент)
	
	 ОбновитьШкалу(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОбновитьДанные(Команда)
	
	ОбновитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанные()

	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	дрвДанныеТахогрфа = ПолучитьДанныеТахографа();
	
	Для каждого СтрокаОбщиеИтоги Из дрвДанныеТахогрфа.Строки Цикл
		НачалоПериода	 		= СтрокаОбщиеИтоги.Период;
		ОкончаниеПериода	 	= СтрокаОбщиеИтоги.ПериодОкончание;
		Диаграмма.УстановитьПолныйИнтервал(НачалоДня(НачалоПериода), КонецДня(ОкончаниеПериода));
		Для каждого СтрокаВодитель Из СтрокаОбщиеИтоги.Строки Цикл
			ТекущийВодитель = СтрокаВодитель.Водитель;
			ТочкаВодитель = Диаграмма.УстановитьТочку(ТекущийВодитель);
			ТочкаВодитель.Расшифровка = ТекущийВодитель;
			
			РейсСтроки 		= СтрокаВодитель.Строки;
			КоличествоСтрок = РейсСтроки.Количество();
			Рейс			= Неопределено;
			
			Для каждого СтрокаРейс Из РейсСтроки Цикл
	
				Если НЕ Рейс = СтрокаРейс.Рейс Тогда
					Рейс = СтрокаРейс.Рейс;
					ТочкаРейс = УстановитьТочкуРейса(Диаграмма, Рейс, ТекущийВодитель);
				КонецЕсли;
				
				// Серия.
				сткПараметрыСерии = ХарактеристикиРежимаДеятельностиВодителя(СтрокаРейс.РежимДеятельности);
				СерияДиаграммы = Диаграмма.УстановитьСерию(СтрокаРейс.РежимДеятельности);
				СерияДиаграммы.Цвет = сткПараметрыСерии.Цвет;
				
				// На интервале может быть один рейс:
				//	1. (Рейс1) 		- (НачалоИнтервала, ОкончаниеИнтервала)
				// или два рейса
				// 	2. (Рейс1, Рейс2)  - (НачалоИнтервала, ГраницаРейсов] (ГраницаРейсов + 1, ОкончаниеИнтервала)
				// Где Рейс1, Рейс2 могут быть пустыми
							
				// Начало интервала.
				НачалоИнтервала = СтрокаРейс.Период;
				Рейс1			= СтрокаРейс.Рейс;
				ОкончаниеРейса1	= СтрокаРейс.ОкончаниеРейса;
							
				// Окончание интервала.
				ИндексТекущейСтроки = РейсСтроки.Индекс(СтрокаРейс);
				СтрокаРейс2			= Неопределено;
				Рейс2				= Рейс1;
				Если ИндексТекущейСтроки = КоличествоСтрок - 1 Тогда
					ОкончаниеИнтервала = КонецДня(НачалоИнтервала);
				Иначе	
					СтрокаРейс2			= РейсСтроки.Получить(ИндексТекущейСтроки + 1);
					Рейс2				= СтрокаРейс2.Рейс;
					НачалоРейса2		= СтрокаРейс2.НачалоРейса;
					ОкончаниеИнтервала 	= СтрокаРейс2.Период;
				КонецЕсли;
				 			
				//  Период
				ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаРейс, СерияДиаграммы);
				
				// Если интервал принадлежит одному рейсу
				Если Рейс1 = Рейс2 Тогда
					ДобавитьИнтервал(ЗначениеПериод, НачалоИнтервала, ОкончаниеИнтервала, сткПараметрыСерии.Цвет, сткПараметрыСерии.Текст);
				Иначе	
								
					Если ЗначениеЗаполнено(Рейс1) Тогда
						ГраницаРейсов = ОкончаниеРейса1;			
					Иначе	
						ГраницаРейсов = НачалоРейса2;
					КонецЕсли;
					
					ДобавитьИнтервал(ЗначениеПериод, НачалоИнтервала, ГраницаРейсов, сткПараметрыСерии.Цвет, сткПараметрыСерии.Текст);
					
					ТочкаРейс2 = УстановитьТочкуРейса(Диаграмма, Рейс2, ТекущийВодитель);
											
					ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаРейс2, СерияДиаграммы);
					НачалоИнтервала = ГраницаРейсов + 1;
					ДобавитьИнтервал(ЗначениеПериод, НачалоИнтервала, ОкончаниеИнтервала, сткПараметрыСерии.Цвет, сткПараметрыСерии.Текст);
				КонецЕсли;
			КонецЦикла;
			
			Диаграмма.РазвернутьТочку(ТочкаВодитель, Истина);
		КонецЦикла;
	КонецЦикла;
	
	ВыделитьТекущуюДату(ЭтаФорма);
	
	Диаграмма.Обновление         = Истина;
	
КонецПроцедуры

Функция УстановитьТочкуРейса(Диаграмма, Рейс, Водитель)
	Если ЗначениеЗаполнено(Рейс) Тогда
		ТочкаРейс = Диаграмма.УстановитьТочку(Рейс, Водитель);
		ТочкаРейс.Текст			= Рейс;
		ТочкаРейс.Расшифровка 	= Рейс;
	Иначе	
		ИдентификаторРейса = Нстр("ru = 'БезРейса%1'");
		ИдентификаторРейса = СтрШаблон(ИдентификаторРейса, Водитель);
		ТочкаРейс = Диаграмма.УстановитьТочку(ИдентификаторРейса, Водитель);
		ТочкаРейс.Текст			= Нстр("ru = 'Без рейса'");;
	КонецЕсли;
	
	Возврат ТочкаРейс;
	
КонецФункции

Процедура ДобавитьИнтервал(ЗначениеПериод, НачалоИнтервала, ОкончаниеИнтервала, Цвет, Текст)
	Интервал = ЗначениеПериод.Добавить();
	Интервал.Начало = НачалоИнтервала;
	Интервал.Конец  = ОкончаниеИнтервала;

	Если НачалоДня(НачалоИнтервала) = НачалоДня(ОкончаниеИнтервала) Тогда
		ПериодСтрока = "(" + Формат(НачалоИнтервала, "ДФ=ЧЧ:мм") + " - " 
		+ Формат(ОкончаниеИнтервала, "ДФ=ЧЧ:мм") + ")";
	Иначе	
		
		ПериодСтрока = Формат(НачалоИнтервала, "ДФ='дд.ММ ЧЧ:мм'") + " - "
		+ Формат(ОкончаниеИнтервала, "ДФ='дд.ММ ЧЧ:мм'");
	КонецЕсли; 
	
	Интервал.Цвет = Цвет;
	ТекстИнтервала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ПериодСтрока);
	Интервал.Текст = НСтр(ТекстИнтервала, КодЯзыка);

		
КонецПроцедуры

&НаСервере
Функция ХарактеристикиРежимаДеятельностиВодителя(РежимДеятельности)
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Цвет");
	сткВозврат.Вставить("Текст");
	
	Если РежимДеятельности = Перечисления.упТахографРежимыДеятельностиВодителей.Вождение Тогда
		сткВозврат.Цвет  = WebЦвета.Зеленый;
		сткВозврат.Текст = "ru = 'Вождение %1'";
	ИначеЕсли РежимДеятельности = Перечисления.упТахографРежимыДеятельностиВодителей.Доступность Тогда
		сткВозврат.Цвет  = WebЦвета.Черный;
		сткВозврат.Текст = "ru = 'Доступность %1'";
	ИначеЕсли РежимДеятельности = Перечисления.упТахографРежимыДеятельностиВодителей.Отдых Тогда
		сткВозврат.Цвет  = WebЦвета.Красный;
		сткВозврат.Текст = "ru = 'Отдых %1'";
	ИначеЕсли РежимДеятельности = Перечисления.упТахографРежимыДеятельностиВодителей.ПрочееРабочееВремя Тогда
		сткВозврат.Цвет  = WebЦвета.Желтый;
		сткВозврат.Текст = "ru = 'Прочее рабочее время %1'";
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции // ОпределитьХарактеристикиСостоянияТС()

#КонецОбласти

#Область Прочее

&НаСервере
Функция ПолучитьДанныеТахографа()

	дрвРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТахографДанныеОДеятельностиВодителей.Водитель КАК Водитель,
	               |	упТахографДанныеОДеятельностиВодителей.РежимДеятельности,
	               |	упТахографДанныеОДеятельностиВодителей.Период КАК Период,
	               |	упТахографДанныеОДеятельностиВодителей.Период КАК ПериодОкончание,
	               |	ЕСТЬNULL(упФактическиеПараметрыРейса.НачалоВыполнения, упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения) КАК НачалоРейса,
	               |	ВЫБОР
	               |		КОГДА упФактическиеПараметрыРейса.ОкончаниеВыполнения ЕСТЬ NULL
	               |			ТОГДА упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения
	               |		КОГДА упФактическиеПараметрыРейса.ОкончаниеВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА ДОБАВИТЬКДАТЕ(упФактическиеПараметрыРейса.НачалоВыполнения, СЕКУНДА, РАЗНОСТЬДАТ(ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)), ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)), СЕКУНДА))
	               |		ИНАЧЕ упФактическиеПараметрыРейса.ОкончаниеВыполнения
	               |	КОНЕЦ КАК ОкончаниеРейса,
	               |	упТахографДанныеОДеятельностиВодителей.Рейс
	               |ИЗ
	               |	РегистрСведений.упТахографДанныеОДеятельностиВодителей КАК упТахографДанныеОДеятельностиВодителей
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	               |			ПО упРейс.Ссылка = упФактическиеПараметрыРейса.Рейс
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |			ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	               |		ПО упТахографДанныеОДеятельностиВодителей.Рейс = упРейс.Ссылка
	               |ГДЕ
	               |	упТахографДанныеОДеятельностиВодителей.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |	И (НЕ &ИспользоватьОтборПоВодителю
	               |			ИЛИ упТахографДанныеОДеятельностиВодителей.Водитель = &Водитель)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период
	               |ИТОГИ
	               |	МИНИМУМ(Период),
	               |	МАКСИМУМ(ПериодОкончание)
	               |ПО
	               |	ОБЩИЕ,
	               |	Водитель";
	Запрос.УстановитьПараметр("ДатаНачала", 	Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	Период.ДатаОкончания);
	
	ИспользоватьОтборПоВодителю = ЗначениеЗаполнено(Водитель);
	Запрос.УстановитьПараметр("ИспользоватьОтборПоВодителю", 	ИспользоватьОтборПоВодителю);
	Запрос.УстановитьПараметр("Водитель", 						Водитель);
		
	дрвРезультат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Возврат дрвРезультат;
	
КонецФункции
 
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьШкалу(Форма, ВидШкалы)

	Диаграмма = Форма.Диаграмма;
	
	ШкалаВремениЭлементы = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы;
	
	ПервыйЭлементШкалы = ШкалаВремениЭлементы[0];
	Для Сч = -ШкалаВремениЭлементы.Количество()+1 по -1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[-Сч]);
	КонецЦикла;
	
	Форма.ВидШкалы = ВидШкалы;
	
	Если ВидШкалы = 0 Тогда
		// День, час
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяцаДеньНедели;
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	ИначеЕсли ВидШкалы = 1 Тогда
		// Месяц, неделя, день
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Неделя;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
		ЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
		
	Иначе
		
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Год;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	КонецЕсли; 
	
	ВыделитьТекущуюДату(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВыделитьТекущуюДату(Форма)

	Форма.Диаграмма.ИнтервалыФона.Очистить();
	
	ЦветТекущегоДня = WebЦвета.БледноЗеленый;
	
	Если Форма.ВидШкалы = 0 Тогда
		// День, час
		ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоЧаса(ТекущаяДата()), КонецЧаса(ТекущаяДата()));
		ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
	Иначе
		Если НачалоДня(Форма.Диаграмма.НачалоПолногоИнтервала) <> НачалоДня(Форма.Диаграмма.КонецПолногоИнтервала) Тогда
			ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()));
			ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма)

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.ПоддержкаМасштаба               = ПоддержкаМасштабаДиаграммыГанта.Авто;
	Диаграмма.Окантовка                       = Истина;
	Диаграмма.Анимация                        = АнимацияДиаграммы.Использовать;
	Диаграмма.ВертикальнаяПрокрутка           = Истина;
	Диаграмма.ОтображатьПустыеЗначения        = Ложь;
	Диаграмма.ОтображатьЗаголовок             = Ложь;

КонецПроцедуры

#КонецОбласти
