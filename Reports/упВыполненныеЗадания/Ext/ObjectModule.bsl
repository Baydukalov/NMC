
#Область ОбработчикиСобытий	

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СформироватьОтчет(ДокументРезультат, ДанныеРасшифровки);
	
КонецПроцедуры

// В данной процедуре можно программно устанавливать настройки компоновщика настроек отчета,
// процедура вызывается каждый раз при построении отчета.
//
// Параметры:
//  НастройкаПериода - СтандартныйПериод - Содержит стандартный период, используемый в компоновке данных.
//
Процедура УстановитьПараметрыОтчета(НастройкаПериода)
	// тут можно программно устанавливать параметры отчета.
	
	// например передать текущую дату в запрос СКД.
	
	НайденныйПараметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("мПериод"));
	Если НайденныйПараметр = Неопределено Тогда
		НастройкаПериода = Новый СтандартныйПериод;
		НастройкаПериода.Вариант = ВариантСтандартногоПериода.ЭтотГод;
	Иначе
		НастройкаПериода = НайденныйПараметр.Значение;		
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметрыОтчета()

// Процедура выполняет формирование табличного документа по настройкам СКД
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент - ТабличныйДокумент.
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - Объект, содержащий информацию о расшифровке.
//
Процедура СформироватьОтчет(ТабличныйДокумент, ДанныеРасшифровки) 
		
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	НастройкаПериода = Неопределено;
	
	// установим обязательные настройки отчета.
	УстановитьПараметрыОтчета(НастройкаПериода);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));	
	
	// Компоновка макета.
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки,);
	
	// при необходимости вставим внешние наборы данных в СКД.
	ВнешниеНаборыДанных = Новый Структура;
	
	ТаблицаВнешнегоНабораДанных = ПолучитьТаблицуВнешнегоНабораДанных(НастройкаПериода);
	ВнешниеНаборыДанных.Вставить("ТаблицаВнешнегоНабораДанных",ТаблицаВнешнегоНабораДанных);
	
	// Инициализация процессора компоновки.
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,ВнешниеНаборыДанных,ДанныеРасшифровки);	
	
	ТабличныйДокумент.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных); 

КонецПроцедуры // СформироватьОтчет()

// В данной функции можно сформировать таблицу внешнего набора данных для СКД,
// Функция должна возвращать таблицу данных (например таблицу значений).
//
// Параметры:
//  НастройкаПериода - СтандартныйПериод - Содержит стандартный период, используемый в компоновке данных.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Результат выпонления.
//
Функция ПолучитьТаблицуВнешнегоНабораДанных(НастройкаПериода = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Документ
	|ПОМЕСТИТЬ втДокументы
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза КАК упСтатусыЗаданийНаПеревозкуГруза
	|		ПО упЗаданиеНаПеревозкуГруза.Ссылка = упСтатусыЗаданийНаПеревозкуГруза.Документ
	|ГДЕ
	|	ВЫБОР
	|			КОГДА упСтатусыЗаданийНаПеревозкуГруза.Период ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			КОГДА упСтатусыЗаданийНаПеревозкуГруза.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто КАК Груз
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	|ГДЕ
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка В
	|			(ВЫБРАТЬ
	|				втДокументы.Документ КАК Ссылка
	|			ИЗ
	|				втДокументы КАК втДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Груз КАК Груз,
	|	упГрузовыеМестаТоварныйСоставГруза.НомерСтроки КАК НомерСтроки,
	|	упГрузовыеМестаТоварныйСоставГруза.Номенклатура КАК Номенклатура
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК упГрузовыеМестаТоварныйСоставГруза
	|		ПО втГрузовыеМеста.Груз = упГрузовыеМестаТоварныйСоставГруза.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Груз,
	|	НомерСтроки,
	|	Номенклатура
	|ИТОГИ ПО
	|	Номенклатура ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("НачалоПериода", НастройкаПериода.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",НастройкаПериода.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	ТаблицаЗначений.Колонки.Добавить("Груз",Новый ОписаниеТипов("СправочникСсылка.упГрузовыеМеста"),"Груз");
	ТаблицаЗначений.Колонки.Добавить("ПерваяГруппа",Новый ОписаниеТипов("СправочникСсылка.упНоменклатура"),"ПерваяГруппа");	
	ТаблицаЗначений.Колонки.Добавить("НомерСтроки",,"НомерСтроки");
	
	ПерваяГруппа = Справочники.упНоменклатура.ПустаяСсылка();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.Уровень() = 0 Тогда 
			ПерваяГруппа = ВыборкаДетальныеЗаписи.Номенклатура;
		ИначеЕсли ВыборкаДетальныеЗаписи.ТипЗаписи() = ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
			
			тОтбор = Новый Структура;
			тОтбор.Вставить("Груз",ВыборкаДетальныеЗаписи.Груз);
			тОтбор.Вставить("ПерваяГруппа",ПерваяГруппа);
			НайденыеСтроки = ТаблицаЗначений.НайтиСтроки(тОтбор);
			
			Если НайденыеСтроки.Количество() = 0 Тогда
				НоваяСтрока = ТаблицаЗначений.Добавить();
				НоваяСтрока.Груз = ВыборкаДетальныеЗаписи.Груз;
				НоваяСтрока.ПерваяГруппа = ПерваяГруппа;
				НоваяСтрока.НомерСтроки = ВыборкаДетальныеЗаписи.НомерСтроки;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ТаблицаЗначений;
	
КонецФункции // ПолучитьТаблицуВнешнегоНабораДанных()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 