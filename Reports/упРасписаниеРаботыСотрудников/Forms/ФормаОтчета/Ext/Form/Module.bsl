
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТолькоРаботающиеВУказанныйПериод = Ложь;
	Элементы.ТолькоРаботающиеВУказанныйПериод.Пометка  = Ложь;
		
	УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма);
	
	Если Параметры.Свойство("СписокОбъектов") Тогда
		Период = Новый СтандартныйПериод;
		Период.ДатаНачала = Параметры.ДатаНачала;
		Период.ДатаОкончания = Параметры.ДатаОкончания;
		ВидШкалы = Параметры.ВидШкалы;
		СписокОбъектов.ЗагрузитьЗначения(Параметры.СписокОбъектов.ВыгрузитьЗначения());
		ОбновитьДанныеНаСервере();
	КонецЕсли;	
	
	ОбновитьШкалуНаСервере(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьВидШкалы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьИнтервалДень(Команда)
	ВидШкалы = 0;
	ОбновитьВидШкалы();
	ОбновитьШкалуНаСервере(ЭтаФорма, ВидШкалы);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалМесяц(Команда)
	ВидШкалы = 2;
	ОбновитьВидШкалы();
	ОбновитьШкалуНаСервере(ЭтаФорма, ВидШкалы);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалНеделя(Команда)
	ВидШкалы = 1;
	ОбновитьВидШкалы();
	ОбновитьШкалуНаСервере(ЭтаФорма, ВидШкалы);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ОбновитьДанныеНаСервере()

	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
		
	РезультатЗапроса = РасписаниеРаботы();
	
	ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	НачалоПолногоПериода = '000101010000';
	ОкончаниеПолногоПериода = '000101010000';
	
	Пока ВыборкаИтоги.Следующий() Цикл
				
		НачалоПолногоПериода = ?(НачалоПолногоПериода = '000101010000', 
							ВыборкаИтоги.ДатаНачала,
							Мин(ВыборкаИтоги.ДатаНачала, НачалоПолногоПериода));
		
		ОкончаниеПолногоПериода = ?(ОкончаниеПолногоПериода = '000101010000', 
								ВыборкаИтоги.ДатаОкончания,
								Макс(ВыборкаИтоги.ДатаОкончания, ОкончаниеПолногоПериода));					
		
		ВыборкаОбъекты = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ИндексТипаРасписания = 0;
		Пока ВыборкаОбъекты.Следующий() Цикл
			
			Если Истина
				И ТолькоРаботающиеВУказанныйПериод
				И ВыборкаОбъекты.КоличествоИнтерваловРаботы = 0
			Тогда
				Продолжить;	
			КонецЕсли;
			
			Точка = Диаграмма.УстановитьТочку(ВыборкаОбъекты.Ссылка);
			Точка.Расшифровка = ВыборкаОбъекты.Ссылка;
			ВыборкаТипРасписания = ВыборкаОбъекты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаТипРасписания.Следующий() Цикл
				
				ИндексТипаРасписания = ИндексТипаРасписания + 1;
				ТипРасписания = ВыборкаТипРасписания.ТипРасписания;
				КлючТипаРасписания = СтрШаблон("%1%2", ТипРасписания, ИндексТипаРасписания);
				
				ВыборкаИнтервалы = ВыборкаТипРасписания.Выбрать();
				ЗаполнитьПериодыГруппы(ВыборкаИнтервалы, КлючТипаРасписания, ТипРасписания, ВыборкаОбъекты.Ссылка, Диаграмма);
				
			КонецЦикла;
			
			Диаграмма.РазвернутьТочку(Точка, Истина);

		КонецЦикла;
				
	КонецЦикла; 	
	
	// Установим полный интервал диаграммы
	Если НачалоПолногоПериода <> '000101010000' И ОкончаниеПолногоПериода <> '000101010000' Тогда
		Диаграмма.УстановитьПолныйИнтервал(НачалоДня(НачалоПолногоПериода), КонецДня(ОкончаниеПолногоПериода));
	КонецЕсли;
	
	ВыделитьТекущуюДату(ЭтаФорма);
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПериодыГруппы(ВыборкаИнтервалы, КлючТипаРасписания, ТипРасписания, ЗначениеОбъектаРодителя, Диаграмма)

	ТочкаТипРасписания = Неопределено;	
	
	Пока ВыборкаИнтервалы.Следующий() Цикл
		ДатаНачала = ВыборкаИнтервалы.ДатаНачала;
		ДатаОкончания = ВыборкаИнтервалы.ДатаОкончания;
		
		Если ВыборкаИнтервалы.КоличествоИнтерваловРаботы > 0 Тогда
			Если ТочкаТипРасписания = Неопределено Тогда
				ТочкаТипРасписания = Диаграмма.УстановитьТочку(КлючТипаРасписания, ЗначениеОбъектаРодителя);
				ТочкаТипРасписания.Текст = ТипРасписания;
				ТочкаТипРасписания.Расшифровка = ТипРасписания;
			КонецЕсли;
			
			Характеристики = ХарактеристикиТипаРасписания(ТипРасписания);
			
			СерияДиаграммы = Диаграмма.УстановитьСерию(Характеристики.Серия);
			СерияДиаграммы.Цвет = Характеристики.Цвет;
			
			ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаТипРасписания, СерияДиаграммы);
			
			Интервал = ЗначениеПериод.Добавить();
			Интервал.Начало = ДатаНачала;
			Интервал.Конец  = ДатаОкончания;
			
			Если НачалоДня(ДатаНачала) = НачалоДня(ДатаОкончания) Тогда
				ПериодСтрока = "(" + Формат(ДатаНачала, "ДФ=ЧЧ:мм") + " - " 
				+ Формат(ДатаОкончания, "ДФ=ЧЧ:мм") + ")";
			Иначе	
				
				ПериодСтрока = Формат(ДатаНачала, "ДФ='дд.ММ ЧЧ:мм'") + " - "
				+ Формат(ДатаОкончания, "ДФ='дд.ММ ЧЧ:мм'");
			КонецЕсли; 
		
			Интервал.Цвет = Характеристики.Цвет;
			ТекстИнтервала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ru = '%1'", ПериодСтрока);
			Интервал.Текст = НСтр(ТекстИнтервала, ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			
		Иначе
			Продолжить;
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидШкалы()
		
	Если ВидШкалы = 0 Тогда
		Элементы.УстановитьИнтервалДень.Пометка= Истина;
		Элементы.УстановитьИнтервалНеделя.Пометка = Ложь;
		Элементы.УстановитьИнтервалМесяц.Пометка = Ложь;
		
	ИначеЕсли ВидШкалы = 1 Тогда
		Элементы.УстановитьИнтервалДень.Пометка= Ложь;
		Элементы.УстановитьИнтервалНеделя.Пометка = Истина;
		Элементы.УстановитьИнтервалМесяц.Пометка = Ложь;
			
	ИначеЕсли ВидШкалы = 2 Тогда		
		Элементы.УстановитьИнтервалДень.Пометка= Ложь;
		Элементы.УстановитьИнтервалНеделя.Пометка = Ложь;
		Элементы.УстановитьИнтервалМесяц.Пометка = Истина;
	
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьШкалуНаСервере(Форма, ВидШкалы)

	Диаграмма = Форма.Диаграмма;
	
	ШкалаВремениЭлементы = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы;
	
	ПервыйЭлементШкалы = ШкалаВремениЭлементы[0];
	Для Сч = -ШкалаВремениЭлементы.Количество()+1 по -1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[-Сч]);
	КонецЦикла;
	
	Форма.ВидШкалы = ВидШкалы;
	
	Если ВидШкалы = 0 Тогда
		// День, час
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяцаДеньНедели;
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	ИначеЕсли ВидШкалы = 1 Тогда
		// Месяц, неделя, день
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Неделя;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		ЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
				
	Иначе
		
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
	
	КонецЕсли; 
	
	ВыделитьТекущуюДату(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВыделитьТекущуюДату(Форма)

	Форма.Диаграмма.ИнтервалыФона.Очистить();
	
	ЦветТекущегоДня = WebЦвета.БледноЗеленый;
	
	Если Форма.ВидШкалы = 0 Тогда
		// День, час
		ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоЧаса(ТекущаяДата()), КонецЧаса(ТекущаяДата()));
		ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
	Иначе
		Если НачалоДня(Форма.Диаграмма.НачалоПолногоИнтервала) <> НачалоДня(Форма.Диаграмма.КонецПолногоИнтервала) Тогда
			ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()));
			ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма)

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.ПоддержкаМасштаба               = ПоддержкаМасштабаДиаграммыГанта.Авто;
	Диаграмма.Окантовка                       = Истина;
	Диаграмма.Анимация                        = АнимацияДиаграммы.Использовать;
	Диаграмма.ВертикальнаяПрокрутка           = Истина;
	Диаграмма.ОтображатьПустыеЗначения        = Ложь;
	Диаграмма.ОтображатьЗаголовок             = Ложь;

КонецПроцедуры

&НаСервере
Функция РасписаниеРаботы()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|	//{Запрос: 0 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		упСотрудникиТ.Ссылка КАК Ссылка,
	|		упСотрудникиТ.Наименование КАК Наименование,
	|		упСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы КАК ИспользуетсяИндивидуальныйГрафикРаботы,
	|		упСотрудникиТ.ГрафикРаботы КАК ГрафикРаботы
	|	ПОМЕСТИТЬ втСотрудники
	|	ИЗ
	|		Справочник.упСотрудники КАК упСотрудникиТ
	|	ГДЕ ИСТИНА
	|		И (ЛОЖЬ
	|			ИЛИ НЕ (&ИспользуетсяОтборПоСотрудникам)
	|			ИЛИ упСотрудникиТ.Ссылка В  (&СписокОбъектов))
	|		И НЕ (упСотрудникиТ.ПометкаУдаления)
	|	;
	|	//{Запрос: 1 ////////////////////////////////////////
	|	// вап3456вс3р Определение расписания работ при помощи механизма БСП.Начало
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		втСотрудникиТ.ГрафикРаботы КАК ГрафикРаботы
	|	ПОМЕСТИТЬ втГрафикиРаботы
	|	ИЗ
	|		втСотрудники КАК втСотрудникиТ
	|	ГДЕ НЕ (втСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы)
	|	;
	|	//{Запрос: 2 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		ШаблонЗаполнения.Ссылка КАК ГрафикРаботы,
	|		МАКСИМУМ(ШаблонЗаполнения.НомерСтроки) КАК ДлинаЦикла
	|	ПОМЕСТИТЬ ВТДлинаЦиклаГрафиков
	|	ИЗ
	|		Справочник.Календари.ШаблонЗаполнения КАК ШаблонЗаполнения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|		ПО ШаблонЗаполнения.Ссылка = втГрафикиРаботы.ГрафикРаботы
	|	СГРУППИРОВАТЬ ПО
	|		ШаблонЗаполнения.Ссылка
	|	;
	|	//{Запрос: 3 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		втГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
	|		ДанныеПроизводственногоКалендаря.Дата КАК ДатаГрафика,
	|		ВЫБОР
	|			КОГДА ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ПредпраздничныйДень
	|	ПОМЕСТИТЬ ВТПредпраздничныеДни
	|	ИЗ
	|		РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|		ПО ИСТИНА
	|			И ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = втГрафикиРаботы.ГрафикРаботы
	|			И ДанныеПроизводственногоКалендаря.Дата >= &НачалоПериода
	|			И ДанныеПроизводственногоКалендаря.Дата <= &ОкончаниеПериода
	|			И ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|	;
	|	//{Запрос: 4 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		втГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
	|		ДанныеПроизводственногоКалендаря.Дата КАК ДатаГрафика,
	|		ДанныеПроизводственногоКалендаря.ДатаПереноса КАК ДатаПереноса
	|	ПОМЕСТИТЬ ВТДатыПереноса
	|	ИЗ
	|		РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|		ПО ИСТИНА
	|			И ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = втГрафикиРаботы.ГрафикРаботы
	|			И ДанныеПроизводственногоКалендаря.Дата >= &НачалоПериода
	|			И ДанныеПроизводственногоКалендаря.Дата <= &ОкончаниеПериода
	|			И ДанныеПроизводственногоКалендаря.ДатаПереноса <> ДАТАВРЕМЯ(1, 1, 1)
	|	;
	|	//{Запрос: 5 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		КалендарныеГрафики.Календарь КАК ГрафикРаботы,
	|		КалендарныеГрафики.ДатаГрафика КАК ДатаГрафика,
	|		РАЗНОСТЬДАТ(Календари.ДатаОтсчета, КалендарныеГрафики.ДатаГрафика, ДЕНЬ) + 1 КАК ДнейОтДатыОтсчета,
	|		ПредпраздничныеДни.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|		ДатыПереноса.ДатаПереноса КАК ДатаПереноса
	|	ПОМЕСТИТЬ ВТДниВключенныеВГрафик
	|	ИЗ
	|		РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафикиРаботы КАК втГрафикиРаботы
	|		ПО ИСТИНА
	|			И КалендарныеГрафики.Календарь = втГрафикиРаботы.ГрафикРаботы
	|			И КалендарныеГрафики.ДатаГрафика >= &НачалоПериода
	|			И КалендарныеГрафики.ДатаГрафика <= &ОкончаниеПериода
	|			И КалендарныеГрафики.ДеньВключенВГрафик
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|		ПО Календари.Ссылка = втГрафикиРаботы.ГрафикРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредпраздничныеДни КАК ПредпраздничныеДни
	|		ПО ИСТИНА
	|			И ПредпраздничныеДни.ГрафикРаботы = КалендарныеГрафики.Календарь
	|			И ПредпраздничныеДни.ДатаГрафика = КалендарныеГрафики.ДатаГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыПереноса КАК ДатыПереноса
	|		ПО ИСТИНА
	|			И ДатыПереноса.ГрафикРаботы = КалендарныеГрафики.Календарь
	|			И ДатыПереноса.ДатаГрафика = КалендарныеГрафики.ДатаГрафика
	|	;
	|	//{Запрос: 6 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|		ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|		ВЫБОР
	|			КОГДА ДниВключенныеВГрафик.РезультатДеленияПоМодулю = 0
	|				ТОГДА ДниВключенныеВГрафик.ДлинаЦикла
	|			ИНАЧЕ ДниВключенныеВГрафик.РезультатДеленияПоМодулю
	|		КОНЕЦ КАК НомерДня,
	|		ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень
	|	ПОМЕСТИТЬ ВТДатыНомераДней
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|			ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|			ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|			ДниВключенныеВГрафик.ДлинаЦикла КАК ДлинаЦикла,
	|			ДниВключенныеВГрафик.ДнейОтДатыОтсчета - ДниВключенныеВГрафик.ЦелаяЧастьРезультатаДеления * ДниВключенныеВГрафик.ДлинаЦикла КАК РезультатДеленияПоМодулю
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|				ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|				ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
	|				ДниВключенныеВГрафик.ДнейОтДатыОтсчета КАК ДнейОтДатыОтсчета,
	|				ДлинаЦиклов.ДлинаЦикла КАК ДлинаЦикла,
	|				(ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) - ВЫБОР
	|						КОГДА (ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) > ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла
	|							ТОГДА 1
	|						ИНАЧЕ 0
	|					КОНЕЦ КАК ЦелаяЧастьРезультатаДеления
	|			ИЗ
	|				ВТДниВключенныеВГрафик КАК ДниВключенныеВГрафик
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|				ПО ИСТИНА
	|					И ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
	|					И Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоЦикламПроизвольнойДлины)
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДлинаЦиклаГрафиков КАК ДлинаЦиклов
	|				ПО ДниВключенныеВГрафик.ГрафикРаботы = ДлинаЦиклов.ГрафикРаботы) КАК ДниВключенныеВГрафик) КАК ДниВключенныеВГрафик
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|		ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|		ВЫБОР
	|			КОГДА ДниВключенныеВГрафик.ДатаПереноса ЕСТЬ NULL
	|				ТОГДА ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаГрафика)
	|			ИНАЧЕ ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаПереноса)
	|		КОНЕЦ КАК НомерДня,
	|		ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень
	|	ИЗ
	|		ВТДниВключенныеВГрафик КАК ДниВключенныеВГрафик
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|		ПО ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
	|	ГДЕ Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоНеделям)
	|	;
	|	//{Запрос: 7 ////////////////////////////////////////
	|	// Определение расписания работ при помощи механизма БСП.Окончание
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
	|		ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
	|		ДниВключенныеВГрафик.НомерДня КАК НомерДня,
	|		ДОБАВИТЬКДАТЕ(ДниВключенныеВГрафик.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяНачала, РасписанияРаботы.ВремяНачала), СЕКУНДА)) КАК ДатаНачала,
	|		ДОБАВИТЬКДАТЕ(ДниВключенныеВГрафик.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяОкончания, РасписанияРаботы.ВремяОкончания), СЕКУНДА)) КАК ДатаОкончания
	|	ПОМЕСТИТЬ ВТРасписанияРаботы
	|	ИЗ
	|		ВТДатыНомераДней КАК ДниВключенныеВГрафик
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботы
	|		ПО ИСТИНА
	|			И РасписанияРаботы.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы
	|			И РасписанияРаботы.НомерДня = ДниВключенныеВГрафик.НомерДня
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботыПредпраздничногоДня
	|		ПО ИСТИНА
	|			И РасписанияРаботыПредпраздничногоДня.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы
	|			И РасписанияРаботыПредпраздничногоДня.НомерДня = 0
	|			И ДниВключенныеВГрафик.ПредпраздничныйДень
	|	;
	|	//{Запрос: 8 ////////////////////////////////////////
	|	ВЫБРАТЬ
	|		ГрафикиРаботы.Сотрудник КАК Сотрудник,
	|		ГрафикиРаботы.Период КАК ДатаГрафика,
	|		ГрафикиРаботы.ДатаНачала КАК ДатаНачала,
	|		ГрафикиРаботы.ДатаОкончания КАК ДатаОкончания
	|	ПОМЕСТИТЬ ВТРасписанияРаботыИндивидуальныеГрафики
	|	ИЗ
	|		РегистрСведений.упГрафикиРаботыСотрудников КАК ГрафикиРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСотрудники КАК втСотрудники
	|		ПО ИСТИНА
	|			И ГрафикиРаботы.Сотрудник = втСотрудники.Ссылка
	|			И втСотрудники.ИспользуетсяИндивидуальныйГрафикРаботы
	|			И ГрафикиРаботы.Период <= &ОкончаниеПериода
	|			И ГрафикиРаботы.Период >= &НачалоПериода
	|	;
	|	//{Запрос: 9 ////////////////////////////////////////
	|	// вап3456вс32 Склеивание периодов по дням в непрерывные периоды.Начало
	|	ВЫБРАТЬ
	|		втСотрудникиТ.Ссылка КАК Сотрудник,
	|		ВТРасписанияРаботыТ.ДатаГрафика КАК Период,
	|		ВТРасписанияРаботыТ.ДатаНачала КАК ДатаНачала,
	|		ВТРасписанияРаботыТ.ДатаОкончания КАК ДатаОкончания
	|	ПОМЕСТИТЬ втПериодыРаботы
	|	ИЗ
	|		втСотрудники КАК втСотрудникиТ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК ВТРасписанияРаботыТ
	|		ПО ИСТИНА
	|			И ВТРасписанияРаботыТ.ГрафикРаботы = втСотрудникиТ.ГрафикРаботы
	|			И НЕ (втСотрудникиТ.ИспользуетсяИндивидуальныйГрафикРаботы)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ВТРасписанияРаботыИндивидуальныеГрафикиТ.Сотрудник КАК Сотрудник,
	|		ВТРасписанияРаботыИндивидуальныеГрафикиТ.ДатаГрафика КАК Период,
	|		ВТРасписанияРаботыИндивидуальныеГрафикиТ.ДатаНачала КАК ДатаНачала,
	|		ВТРасписанияРаботыИндивидуальныеГрафикиТ.ДатаОкончания КАК ДатаОкончания
	|	ИЗ
	|		ВТРасписанияРаботыИндивидуальныеГрафики КАК ВТРасписанияРаботыИндивидуальныеГрафикиТ
	|	;
	|	//{Запрос: 10 ////////////////////////////////////////
	|	// Для каждого периода определяется есть ли для него
	|	// следующий(начало которого совпадает с окончанием
	|	// текущего) и предыдущий(окончания которого
	|	// совпадает с началом текущего)
	|	ВЫБРАТЬ
	|		втПериоды.Сотрудник КАК Сотрудник,
	|		втПериоды.Период КАК Период,
	|		втПериоды.ДатаНачала КАК ДатаНачала,
	|		втПериоды.ДатаОкончания КАК ДатаОкончания,
	|		ВЫБОР
	|			КОГДА втПериоды2.Период ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ КАК ПредыдущийПериодКонецРавен,
	|		// Имеется предыдущий период
	|		// Имеется следующий период
	|		ВЫБОР
	|			КОГДА втПериоды3.Период ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ КАК СледующийПериодНачалоРавен
	|	ПОМЕСТИТЬ втПериодыПодготовка
	|	ИЗ
	|		втПериодыРаботы КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПериодыРаботы КАК втПериоды2
	|		ПО ИСТИНА
	|			И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|			И втПериоды.Период = втПериоды2.Период
	|			И втПериоды.ДатаНачала = втПериоды2.ДатаОкончания
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПериодыРаботы КАК втПериоды3
	|		ПО ИСТИНА
	|			И втПериоды.Сотрудник = втПериоды3.Сотрудник
	|			И втПериоды.Период = втПериоды3.Период
	|			И втПериоды.ДатаОкончания = втПериоды3.ДатаНачала
	|	;
	|	//{Запрос: 11 ////////////////////////////////////////
	|	// Периоды по дням с объедененными интервалами по времени.
	|	// Здесь нет периодов по которым объединять нечего.
	|	ВЫБРАТЬ
	|		втПериоды.Сотрудник КАК Сотрудник,
	|		втПериоды.Период КАК Период,
	|		втПериоды.ДатаНачала КАК ДатаНачала,
	|		МИНИМУМ(втПериоды2.ДатаОкончания) КАК ДатаОкончания
	|	ПОМЕСТИТЬ втОбъединенныеВремена
	|	ИЗ
	|		втПериодыПодготовка КАК втПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериодыПодготовка КАК втПериоды2
	|		ПО ИСТИНА
	|			И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|			И втПериоды.Период = втПериоды2.Период
	|			И втПериоды2.ДатаНачала > втПериоды.ДатаНачала
	|			И втПериоды.СледующийПериодНачалоРавен
	|			И втПериоды2.ПредыдущийПериодКонецРавен
	|			И НЕ (втПериоды2.СледующийПериодНачалоРавен)
	|			И НЕ (ИСТИНА
	|				И втПериоды.ПредыдущийПериодКонецРавен = ИСТИНА
	|				И втПериоды.СледующийПериодНачалоРавен = ИСТИНА)
	|	СГРУППИРОВАТЬ ПО
	|		втПериоды.Сотрудник,
	|		втПериоды.Период,
	|		втПериоды.ДатаНачала
	|	;
	|	//{Запрос: 12 ////////////////////////////////////////
	|	// Объединение периодов-дней с объединенными временными
	|	// интервалами и периодов-дней по которым объединенять нечего
	|	ВЫБРАТЬ
	|		втПериоды.Сотрудник КАК Сотрудник,
	|		втПериоды.Период КАК Период,
	|		втПериоды.ДатаНачала КАК ДатаНачала,
	|		втПериоды.ДатаОкончания КАК ДатаОкончания
	|	ПОМЕСТИТЬ втПериодыВремена
	|	ИЗ
	|		втПериодыПодготовка КАК втПериоды
	|	ГДЕ ИСТИНА
	|		И втПериоды.ПредыдущийПериодКонецРавен = ЛОЖЬ
	|		И втПериоды.СледующийПериодНачалоРавен = ЛОЖЬ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втОбъединенныеВремена.Сотрудник КАК Сотрудник,
	|		втОбъединенныеВремена.Период КАК Период,
	|		втОбъединенныеВремена.ДатаНачала КАК ДатаНачала,
	|		втОбъединенныеВремена.ДатаОкончания КАК ДатаОкончания
	|	ИЗ
	|		втОбъединенныеВремена КАК втОбъединенныеВремена
	|	;
	|	//{Запрос: 13 ////////////////////////////////////////
	|	// Для каждого периода определяется:
	|	// Оканчивается ли он в 23:59:59
	|	// Длится ли он целые сутки
	|	// Имеется ли следующий период, который начинается в 00:00:00 следующего дня
	|	// Имеется ли предыдущий период, который оканичивается в 23:59:29 предыдущего дня
	|	ВЫБРАТЬ
	|		втПериоды.Сотрудник КАК Сотрудник,
	|		втПериоды.Период КАК Период,
	|		втПериоды.ДатаНачала КАК ДатаНачала,
	|		втПериоды.ДатаОкончания КАК ДатаОкончания,
	|		ВЫБОР
	|			КОГДА КОНЕЦПЕРИОДА(втПериоды.Период, ДЕНЬ) = втПериоды.ДатаОкончания
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ДоКонцаСуток,
	|		// Дата окончания текущего периода - 23:59:59
	|		ВЫБОР
	|			КОГДА РАЗНОСТЬДАТ(втПериоды.ДатаНачала, втПериоды.ДатаОкончания, СЕКУНДА) = 86399
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ПолныеСутки,
	|		// Текущий период длится сутки с 00:00:00 по 23:59:59
	|		ВЫБОР
	|			КОГДА втПериоды2.Период ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ КАК ПредыдущийПериодДоКонцаСуток,
	|		// У текущего опериода есть предыущий, оканчивающийся в 23:59 предыдущих суток
	|		// У текущего опериода есть следующий, начинающийся в 00:00 следующих суток
	|		ВЫБОР
	|			КОГДА втПериоды3.Период ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ КАК СледующийПериодСНачалаСуток
	|	ПОМЕСТИТЬ втПериоды
	|	ИЗ
	|		втПериодыВремена КАК втПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ // Периоды оканчивающиеся в 23:59 предыдущих суток
	|		втПериодыВремена КАК втПериоды2
	|		ПО ИСТИНА
	|			И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|			И втПериоды.ДатаНачала = втПериоды.Период
	|			И втПериоды.ДатаНачала = ДОБАВИТЬКДАТЕ(втПериоды2.ДатаОкончания, СЕКУНДА, 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПериодыВремена КАК втПериоды3
	|		ПО ИСТИНА
	|			И втПериоды.Сотрудник = втПериоды3.Сотрудник
	|			И ДОБАВИТЬКДАТЕ(втПериоды.ДатаОкончания, СЕКУНДА, 1) = втПериоды3.ДатаНачала
	|			// Периоды начинающиеся в 00:00 следующих суток
	|		
	|	;
	|	//{Запрос: 14 ////////////////////////////////////////
	|	// Периоды длительностью более суток
	|	ВЫБРАТЬ
	|		втПериоды.Сотрудник КАК Сотрудник,
	|		втПериоды.ДатаНачала КАК ДатаНачала,
	|		МИНИМУМ(втПериоды2.ДатаОкончания) КАК ДатаОкончания
	|	ПОМЕСТИТЬ втОбъединенныеИнтервалы
	|	ИЗ
	|		втПериоды КАК втПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериоды КАК втПериоды2
	|		ПО ИСТИНА
	|			И втПериоды.Сотрудник = втПериоды2.Сотрудник
	|			И втПериоды2.Период > втПериоды.Период
	|			И втПериоды.ДоКонцаСуток = ИСТИНА
	|			И втПериоды.СледующийПериодСНачалаСуток = ИСТИНА
	|			И НЕ (ИСТИНА
	|				И втПериоды.ПолныеСутки = ИСТИНА
	|				И втПериоды.ПредыдущийПериодДоКонцаСуток = ИСТИНА)
	|			И НЕ (ИСТИНА
	|				И втПериоды2.ПолныеСутки = ИСТИНА
	|				И втПериоды2.СледующийПериодСНачалаСуток = ИСТИНА)
	|	СГРУППИРОВАТЬ ПО
	|		втПериоды.Сотрудник,
	|		втПериоды.ДатаНачала
	|	;
	|	//{Запрос: 15 ////////////////////////////////////////
	|	//////////////////////
	|	// Склеивание периодов по дням в непрерывные периоды.Окончание
	|	// Объединяются периоды длительностью дент и периоды
	|	// длительностью более суток
	|	ВЫБРАТЬ
	|		втПериоды.Сотрудник КАК Сотрудник,
	|		втПериоды.ДатаНачала КАК ДатаНачала,
	|		втПериоды.ДатаОкончания КАК ДатаОкончания
	|	ПОМЕСТИТЬ втПериодыСотрудникаПоРасписанию
	|	ИЗ
	|		втПериоды КАК втПериоды
	|	ГДЕ ИСТИНА
	|		И НЕ (ИСТИНА
	|			И втПериоды.Период = втПериоды.ДатаНачала
	|			И втПериоды.ПредыдущийПериодДоКонцаСуток = ИСТИНА)
	|		И НЕ (ИСТИНА
	|			И втПериоды.ДоКонцаСуток
	|			И втПериоды.СледующийПериодСНачалаСуток = ИСТИНА)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втОбъединенныеИнтервалы.Сотрудник КАК Сотрудник,
	|		втОбъединенныеИнтервалы.ДатаНачала КАК ДатаНачала,
	|		втОбъединенныеИнтервалы.ДатаОкончания КАК ДатаОкончания
	|	ИЗ
	|		втОбъединенныеИнтервалы КАК втОбъединенныеИнтервалы
	|	;
	|	//{Запрос: 16 ////////////////////////////////////////
	|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|		""График работы"" КАК ТипРасписания,
	|		ГрафикиРаботы.ДатаНачала КАК ДатаНачала,
	|		ГрафикиРаботы.ДатаОкончания КАК ДатаОкончания,
	|		упСотрудникиТ.Ссылка КАК Ссылка,
	|		упСотрудникиТ.Наименование КАК Наименование,
	|		ВЫБОР
	|			КОГДА ГрафикиРаботы.Сотрудник ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоличествоИнтерваловРаботы
	|	ИЗ
	|		втСотрудники КАК упСотрудникиТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПериодыСотрудникаПоРасписанию КАК ГрафикиРаботы
	|		ПО ГрафикиРаботы.Сотрудник = упСотрудникиТ.Ссылка
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		""План"" КАК ТипРасписания,
	|		ЕСТЬNULL(РасписаниеРаботыЭкипажей.ДатаНачала, &НачалоПериода) КАК ДатаНачала,
	|		ЕСТЬNULL(РасписаниеРаботыЭкипажей.ДатаОкончания, &ОкончаниеПериода) КАК ДатаОкончания,
	|		упСотрудникиТ.Ссылка КАК Ссылка,
	|		упСотрудникиТ.Наименование КАК Наименование,
	|		ВЫБОР
	|			КОГДА РасписаниеРаботыЭкипажей.Водитель ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоличествоИнтерваловРаботы
	|	ИЗ
	|		втСотрудники КАК упСотрудникиТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасписаниеРаботыЭкипажей КАК РасписаниеРаботыЭкипажей
	|		ПО ИСТИНА
	|			И РасписаниеРаботыЭкипажей.Водитель = упСотрудникиТ.Ссылка
	|			И РасписаниеРаботыЭкипажей.ДатаОкончания >= &НачалоПериода
	|			И РасписаниеРаботыЭкипажей.ДатаНачала <= &ОкончаниеПериода
	|			И РасписаниеРаботыЭкипажей.ТипРасписания = ЗНАЧЕНИЕ(Перечисление.упТипыРасписанийРаботыТС.План)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		""Факт"" КАК ТипРасписания,
	|		ЕСТЬNULL(РасписаниеРаботыЭкипажей.ДатаНачала, &НачалоПериода) КАК ДатаНачала,
	|		ЕСТЬNULL(РасписаниеРаботыЭкипажей.ДатаОкончания, &ОкончаниеПериода) КАК ДатаОкончания,
	|		упСотрудникиТ.Ссылка КАК Ссылка,
	|		упСотрудникиТ.Наименование КАК Наименование,
	|		ВЫБОР
	|			КОГДА РасписаниеРаботыЭкипажей.Водитель ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоличествоИнтерваловРаботы
	|	ИЗ
	|		втСотрудники КАК упСотрудникиТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРасписаниеРаботыЭкипажей КАК РасписаниеРаботыЭкипажей
	|		ПО ИСТИНА
	|			И РасписаниеРаботыЭкипажей.Водитель = упСотрудникиТ.Ссылка
	|			И РасписаниеРаботыЭкипажей.ДатаОкончания >= &НачалоПериода
	|			И РасписаниеРаботыЭкипажей.ДатаНачала <= &ОкончаниеПериода
	|			И РасписаниеРаботыЭкипажей.ТипРасписания = ЗНАЧЕНИЕ(Перечисление.упТипыРасписанийРаботыТС.Факт)
	|	УПОРЯДОЧИТЬ ПО
	|		Наименование
	|	ИТОГИ
	|		МИНИМУМ(ДатаНачала) КАК ДатаНачала,
	|		МАКСИМУМ(ДатаОкончания) КАК ДатаОкончания,
	|		СУММА(КоличествоИнтерваловРаботы) КАК КоличествоИнтерваловРаботы
	|	ПО
	|		ОБЩИЕ,
	|		Ссылка,
	|		ТипРасписания
	|";
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокОбъектов", СписокОбъектов.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ИспользуетсяОтборПоСотрудникам", СписокОбъектов.Количество() > 0);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПриИзменении(Элемент)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

Функция ХарактеристикиТипаРасписания(ТипРасписания)
	
	Результат = Новый Структура();
	
	Если ТипРасписания = "График работы" Тогда
		Результат.Вставить("Цвет", WebЦвета.Синий);
		Результат.Вставить("Серия", "График работы");
	ИначеЕсли ТипРасписания = "План" Тогда
		Результат.Вставить("Цвет", WebЦвета.Зеленый);
		Результат.Вставить("Серия", "План");
	ИначеЕсли ТипРасписания = "Факт" Тогда
		Результат.Вставить("Цвет", WebЦвета.Желтый);
		Результат.Вставить("Серия", "Факт");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ТолькоРаботающиеВУказанныйПериод(Команда)
	ТолькоРаботающиеВУказанныйПериод = НЕ ТолькоРаботающиеВУказанныйПериод;
	Элементы.ТолькоРаботающиеВУказанныйПериод.Пометка = ТолькоРаботающиеВУказанныйПериод;
	ОбновитьДанныеНаСервере();
КонецПроцедуры


#КонецОбласти
