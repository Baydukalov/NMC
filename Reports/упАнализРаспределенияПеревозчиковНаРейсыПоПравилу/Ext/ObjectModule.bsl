
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
		
	СформироватьОтчет(ДокументРезультат, ДанныеРасшифровки);
		
КонецПроцедуры
	
// Процедура выполняет формирование табличного документа по настройкам СКД.
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент - ТабличныйДокумент.
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - Объект, содержащий информацию о расшифровке.
//
Процедура СформироватьОтчет(ТабличныйДокумент, ДанныеРасшифровки) 
		
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	ПолеСтандартныйПериод = Новый ПараметрКомпоновкиДанных("мПериод");
	ПолеПравилоПодбораПеревозчиковНаРейсы = Новый ПараметрКомпоновкиДанных("ПравилоПодбораПеревозчиковНаРейсы");
	
	ДатаДляПериода = ТекущаяДатаСеанса();
	НастройкаПериода = Неопределено;
	УстановитьПериодИзПравилоПодбораПеревозчиковНаРейсы = Ложь;
	Для каждого текЭлемент Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(текЭлемент) = ТИП("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Если текЭлемент.Параметр = ПолеСтандартныйПериод И НЕ текЭлемент.Использование Тогда			
				УстановитьПериодИзПравилоПодбораПеревозчиковНаРейсы = Истина;
				Если НЕ НастройкаПериода = Неопределено Тогда
					текЭлемент.Значение = НастройкаПериода;
					текЭлемент.Использование = Истина;
					УстановитьПериодИзПравилоПодбораПеревозчиковНаРейсы = Ложь;
				КонецЕсли;
			ИначеЕсли текЭлемент.Параметр = ПолеПравилоПодбораПеревозчиковНаРейсы Тогда
				ПериодАнализаДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текЭлемент.Значение, "ПериодАнализаДанных");
				Если ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.ДеньНачалаРейса Тогда
					НастройкаПериода = Новый СтандартныйПериод;
					НастройкаПериода.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
					НастройкаПериода.ДатаНачала = НачалоДня(ДатаДляПериода);
					НастройкаПериода.ДатаОкончания = ДатаДляПериода;
				ИначеЕсли ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.НеделяНачалаРейса Тогда
					НастройкаПериода = Новый СтандартныйПериод;
					НастройкаПериода.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
					НастройкаПериода.ДатаНачала = НачалоНедели(ДатаДляПериода);
					НастройкаПериода.ДатаОкончания = ДатаДляПериода;
				ИначеЕсли ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.МесяцНачалаРейса Тогда
					НастройкаПериода = Новый СтандартныйПериод;
					НастройкаПериода.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
					НастройкаПериода.ДатаНачала = НачалоМесяца(ДатаДляПериода);
					НастройкаПериода.ДатаОкончания = ДатаДляПериода;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если УстановитьПериодИзПравилоПодбораПеревозчиковНаРейсы И НЕ НастройкаПериода = Неопределено Тогда
		Для каждого текЭлемент Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(текЭлемент) = ТИП("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				Если текЭлемент.Параметр = ПолеСтандартныйПериод И НЕ текЭлемент.Использование Тогда			
					текЭлемент.Значение = НастройкаПериода;
					текЭлемент.Использование = Истина;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));	
		
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки,);
				
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,ДанныеРасшифровки);
		
	ТабличныйДокумент.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных); 
			
КонецПроцедуры // СформироватьОтчет()
	
	
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ФормироватьСразу = Истина;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли