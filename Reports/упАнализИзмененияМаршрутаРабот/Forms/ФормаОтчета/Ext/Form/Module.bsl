
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.КлючВарианта = "АнализИзмененияМаршрутаРаботКонтекст" Тогда
		Рейс = Параметры.Рейс;
		РейсПриИзмененииСервер();
		Элементы.Рейс.Видимость = Ложь;
		Элементы.ПолосаПрокрутки.Доступность = Истина;
		Элементы.МоментВремениДокумент.Доступность = Истина;
	Иначе
		Элементы.ПолосаПрокрутки.Доступность = Ложь;
		Элементы.МоментВремениДокумент.Доступность = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Рейс) Тогда
		РейсПриИзмененииСервер();
		Элементы.ПолосаПрокрутки.Доступность = Истина;
		Элементы.МоментВремениДокумент.Доступность = Истина;
	Иначе
		Элементы.ПолосаПрокрутки.Доступность = Ложь;
		Элементы.МоментВремениДокумент.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РейсПриИзмененииСервер()
	
	ДокументыМассив = ДанныеДляЗаполненияПолосыПрокрутки(); 
	
	// Список данных для полосы прокрутки
	СписокЗначенийПолосыПрокрутки.Очистить();
	Элементы.МоментВремениДокумент.СписокВыбора.Очистить();
	
	Для Каждого Стр Из ДокументыМассив Цикл
		Представление = ?(ТипЗнч(Стр.Регистратор)=Тип("ДокументСсылка.упПрохождениеТочки"),Строка(Стр.АдресТочки)+" ("+Строка(Стр.Регистратор)+")",Строка(Стр.Регистратор));
		СписокЗначенийПолосыПрокрутки.Добавить(Стр.Регистратор,Представление);	
		Элементы.МоментВремениДокумент.СписокВыбора.Добавить(Стр.Регистратор,Представление);
	КонецЦикла;
	
	// Изменение полосы прокрутки
	Элементы.ПолосаПрокрутки.МаксимальноеЗначение = ДокументыМассив.Количество();
	Отчет.МоментВремениДокумент = СписокЗначенийПолосыПрокрутки[СписокЗначенийПолосыПрокрутки.Количество()-1].Значение;
	ПолосаПрокрутки = СписокЗначенийПолосыПрокрутки.Индекс(СписокЗначенийПолосыПрокрутки.НайтиПоЗначению(Отчет.МоментВремениДокумент))+1;

КонецПроцедуры

&НаКлиенте
Процедура ПолосаПрокруткиПриИзменении(Элемент)
	
	Отчет.МоментВремениДокумент = СписокЗначенийПолосыПрокрутки[ПолосаПрокрутки-1].Значение;
	
КонецПроцедуры

&НаКлиенте
Процедура МоментВремениДокументПриИзменении(Элемент)
	
	ПолосаПрокрутки = СписокЗначенийПолосыПрокрутки.Индекс(СписокЗначенийПолосыПрокрутки.НайтиПоЗначению(Отчет.МоментВремениДокумент))+1;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы


#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеДляЗаполненияПолосыПрокрутки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсу.Регистратор КАК Регистратор,
	|	упМаршрутПоРейсу.Регистратор.МоментВремени КАК РегистраторМоментВремени
	|ПОМЕСТИТЬ Документы
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ
	|	упМаршрутПоРейсу.Рейс = &Рейс
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.Регистратор,
	|	упРаботыПоРейсу.Регистратор.МоментВремени
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Рейс = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документы.Регистратор КАК Регистратор,
	|	Документы.РегистраторМоментВремени КАК РегистраторМоментВремени,
	|	упПрохождениеТочки.АдресТочки
	|ИЗ
	|	Документы КАК Документы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПрохождениеТочки КАК упПрохождениеТочки
	|		ПО Документы.Регистратор = упПрохождениеТочки.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	РегистраторМоментВремени";
	
	Запрос.УстановитьПараметр("Рейс",Рейс);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	

#КонецОбласти




