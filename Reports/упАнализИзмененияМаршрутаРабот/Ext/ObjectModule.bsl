
#Область ОбработчикиСобытий	

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СформироватьОтчет(ДокументРезультат, ДанныеРасшифровки);
	
КонецПроцедуры

// В данной процедуре можно программно устанавливать настройки компоновщика настроек отчета,
// процедура вызывается каждый раз при построении отчета.
//
// Параметры:
//  НастройкаПериода - СтандартныйПериод - СтандартныйПериод.
//
Процедура УстановитьПараметрыОтчета(НастройкаПериода)
	
	// тут можно программно устанавливать параметры отчета.
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаСреза"),ЭтотОбъект.МоментВремениДокумент.МоментВремени());
	Если ТипЗнч(ЭтотОбъект.МоментВремениДокумент) = Тип("ДокументСсылка.упРейс") Тогда
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Рейс"),ЭтотОбъект.МоментВремениДокумент);
	Иначе
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Рейс"),ЭтотОбъект.МоментВремениДокумент.Рейс);
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметрыОтчета()

// Процедура выполняет формирование табличного документа по настройкам СКД.
//
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент - ТабличныйДокумент.
//  ДанныеРасшифровки - ДанныеРасшифровки - ДанныеРасшифровки.
//
Процедура СформироватьОтчет(ТабличныйДокумент, ДанныеРасшифровки) 
		
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	НастройкаПериода = Неопределено;
	
	// установим обязательные настройки отчета.
	УстановитьПараметрыОтчета(НастройкаПериода);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));	
	
	// Компоновка макета.
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки,);
	
	// при необходимости вставим внешние наборы данных в СКД.
	ВнешниеНаборыДанных = Новый Структура;
		
	// Инициализация процессора компоновки.
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,ВнешниеНаборыДанных,ДанныеРасшифровки,Истина);	
	
	ТабличныйДокумент.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных); 

	КоличествоСтрок = ТабличныйДокумент.ВысотаТаблицы;
	КоличествоКолонок = ТабличныйДокумент.ШиринаТаблицы;
	
	Для СтрокаДокумента = 1 По КоличествоСтрок Цикл
		Для КолонкаДокумента = 1 По КоличествоКолонок Цикл
			Область = ТабличныйДокумент.Область(СтрокаДокумента,КолонкаДокумента);
			Если Область.Текст = "ЗаголовокНомерТочки" Тогда
				НоваяКартинка = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
				НоваяКартинка.Картинка = Новый Картинка(БиблиотекаКартинок.упНомерСтроки.ПолучитьДвоичныеДанные());
				НоваяКартинка.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
				НоваяКартинка.Расположить(Область);
			ИначеЕсли Область.Текст = "ЗаголовокПопадаетВоВременноеОкно" Тогда
				НоваяКартинка = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
				НоваяКартинка.Картинка = Новый Картинка(БиблиотекаКартинок.упПредставлениеВремени.ПолучитьДвоичныеДанные());
				НоваяКартинка.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
				НоваяКартинка.Расположить(Область);
			ИначеЕсли Область.Текст = "ЗаголовокТипТочки" Тогда
				ОбластьОбъединения = ТабличныйДокумент.Область(СтрокаДокумента, КолонкаДокумента, СтрокаДокумента + 1, КолонкаДокумента + 1);
				ОбластьОбъединения.Объединить();
				ОбластьОбъединения.Текст = НСтр("ru = 'Адрес'");
	
			Иначе
				Расшифровка = Область.Расшифровка;
				Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
					ПоляРасшифровки = ДанныеРасшифровки.Элементы.Получить(Расшифровка).ПолучитьПоля();
					НайденноеПоле = ПоляРасшифровки.Найти("ПопадаетВоВременноеОкно");
					Если Не НайденноеПоле = Неопределено Тогда
						Если НайденноеПоле.Значение = 1 Тогда
							Картинка = Новый Картинка(БиблиотекаКартинок.упПериодПопадаетВоВременноеОкно.ПолучитьДвоичныеДанные());
						Иначе
							Картинка = Новый Картинка(БиблиотекаКартинок.упПериодНеПопадаетВоВременноеОкно.ПолучитьДвоичныеДанные());
						КонецЕсли;	
						КартинкаВДокументе = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
						КартинкаВДокументе.Картинка = Картинка;
						КартинкаВДокументе.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
						КартинкаВДокументе.Расположить(Область);
						
					КонецЕсли;
					
					НайденноеПолеТипТочки = ПоляРасшифровки.Найти("ТипТочки");
					Если Не НайденноеПолеТипТочки = Неопределено Тогда
						Если НайденноеПолеТипТочки.Значение = Перечисления.упТипыТочекМаршрута.ТочкаОтправления Тогда
							КартинкаТипТочки = Новый Картинка(БиблиотекаКартинок.упГараж.ПолучитьДвоичныеДанные());
						ИначеЕсли  НайденноеПолеТипТочки.Значение = Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию Тогда
							КартинкаТипТочки = Новый Картинка(БиблиотекаКартинок.упТочкаПоЗаданию.ПолучитьДвоичныеДанные());
						ИначеЕсли  НайденноеПолеТипТочки.Значение = Перечисления.упТипыТочекМаршрута.КонтрольнаяТочка Тогда
							КартинкаТипТочки = Новый Картинка(БиблиотекаКартинок.упКонтрольнаяТочка.ПолучитьДвоичныеДанные());
						ИначеЕсли  НайденноеПолеТипТочки.Значение = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения Тогда
							КартинкаТипТочки = Новый Картинка(БиблиотекаКартинок.упТочкаОкончанияМаршрута.ПолучитьДвоичныеДанные());
						ИначеЕсли  НайденноеПолеТипТочки.Значение = Перечисления.упТипыТочекМаршрута.ПроисшествиеВПути Тогда
							КартинкаТипТочки = Новый Картинка(БиблиотекаКартинок.упПроисшествиеВПути.ПолучитьДвоичныеДанные());
						КонецЕсли;	
						КартинкаВДокументеТипТочки = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
						КартинкаВДокументеТипТочки.Картинка = КартинкаТипТочки;
						КартинкаВДокументеТипТочки.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
						КартинкаВДокументеТипТочки.Расположить(Область);
					КонецЕсли;	
					
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры // СформироватьОтчет()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 