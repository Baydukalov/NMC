
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
		
		СтандартнаяОбработка = Ложь;
		
		ДокументРезультат.Вывести(СформироватьОтчетТМФ8());
		
КонецПроцедуры // ПриКомпоновкеРезультата()
	
#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции
	
Функция СформироватьОтчетТМФ8()
	
	НачалоПериода = ТекущаяДата();
	КонецПериода = НачалоПериода;
	
	ПолеСтандартныйПериод = Новый ПараметрКомпоновкиДанных("СтандартныйПериод");
	
	Для каждого текЭлемент Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(текЭлемент) = ТИП("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Если текЭлемент.Параметр = ПолеСтандартныйПериод  И текЭлемент.Использование Тогда			
				ТекущееЗначение = текЭлемент.Значение;
				НачалоПериода = ТекущееЗначение.ДатаНачала;
				КонецПериода = ТекущееЗначение.ДатаОкончания;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет("Макет");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ТитульныйЛист|Потолок");
	ПредставлениеОрганизация = "";
	
	ОбластьМакета.Параметры.Организация = ПредставлениеОрганизация;
	ОбластьМакета.Параметры.ПериодОтчета = "за " + Формат(НачалоПериода,"ДЛФ=D; ДП=__________") + " - " + Формат(КонецПериода,"ДЛФ=D; ДП=__________") + " г.";
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(ТаблицаЗначений);	
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы|Потолок");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы|Потолок");
	
	Для каждого тбзСтрока Из ТаблицаЗначений Цикл
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,тбзСтрока);
		ОбластьМакета.Параметры.ПутевойЛист = тбзСтрока.Документ;
		ОбластьМакета.Параметры.ПутевойЛист_Д = тбзСтрока.Документ;
		ОбластьМакета.Параметры.ТС = тбзСтрока.ТС;	
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;	
	
	Возврат ТабДокумент;
	
КонецФункции // СформироватьОтчетТМФ8()
	
#КонецОбласти
	
#КонецЕсли