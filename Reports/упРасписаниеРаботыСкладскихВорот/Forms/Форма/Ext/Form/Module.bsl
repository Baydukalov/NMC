
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма);

	Если Параметры.Свойство("Склад") Тогда
		
		Склад = Параметры.Склад;
		ОбновитьДанные();
		
	КонецЕсли;
	ОбновитьШкалу(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидШкалыПриИзменении(Элемент)
	
	 ОбновитьШкалу(ЭтаФорма, ВидШкалы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОбновитьДанные(Команда)
	
	ОбновитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанные()

	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ТаблицаРасписание = ПолучитьРасписаниеРаботыСкладскихВорот();
	
	НачалоПолногоПериода    = '000101010000';
	ОкончаниеПолногоПериода = '000101010000';
	Для каждого СтрокаПериод Из ТаблицаРасписание Цикл
		НачалоПолногоПериода = ?(НачалоПолногоПериода = '000101010000', 
									СтрокаПериод.ДатаНачала,
									Мин(СтрокаПериод.ДатаНачала, НачалоПолногоПериода));
		ОкончаниеПолногоПериода = ?(ОкончаниеПолногоПериода = '000101010000', 
									СтрокаПериод.ДатаОкончания,
									Макс(СтрокаПериод.ДатаОкончания, ОкончаниеПолногоПериода));					
	КонецЦикла; 
	
	ТаблицаВорот = ТаблицаРасписание.Скопировать(, "Ворота");
	ТаблицаВорот.Свернуть("Ворота");
	ТаблицаВорот.Сортировать("Ворота");
	Для каждого СтрокаВорота Из ТаблицаВорот Цикл
		ТочкаТС = Диаграмма.УстановитьТочку(СтрокаВорота.Ворота);
		ТочкаТС.Расшифровка = СтрокаВорота.Ворота;
	КонецЦикла;
	
	// Сформируем точки и периоды по типам расписания
	ТаблицаТипыРасписаний = ТаблицаРасписание.Скопировать(, "Рейс,Ворота,ПредставлениеТипаТС");
	ТаблицаТипыРасписаний.Свернуть("Рейс,Ворота,ПредставлениеТипаТС");
	ТаблицаТипыРасписаний.Сортировать("Ворота, Рейс");
	Сч = 0;
	Для каждого СтрокаТипРасписания Из ТаблицаТипыРасписаний Цикл
		
		Сч = Сч + 1;
        КлючТипаРасписания = Строка(СтрокаТипРасписания.Рейс) + Сч;
		ТочкаДиаграммы = Диаграмма.УстановитьТочку(КлючТипаРасписания, СтрокаТипРасписания.Ворота);
		Если ЗначениеЗаполнено(СтрокаТипРасписания.ПредставлениеТипаТС) Тогда
			ТочкаДиаграммы.Текст = НСтр(СтрШаблон("ru = '(%1) %2'", СтрокаТипРасписания.ПредставлениеТипаТС, СтрокаТипРасписания.Рейс));
		Иначе	
			ТочкаДиаграммы.Текст = СтрокаТипРасписания.Рейс;
		КонецЕсли;
		ТочкаДиаграммы.Расшифровка = СтрокаТипРасписания.Рейс;
		
		СтруктураПоиска = Новый Структура("Ворота, Рейс", СтрокаТипРасписания.Ворота, СтрокаТипРасписания.Рейс);
		СписокСтрок = ТаблицаРасписание.НайтиСтроки(СтруктураПоиска);
		ЗаполнитьПериодыГруппы(СписокСтрок, ТочкаДиаграммы, Диаграмма);
		
	КонецЦикла;

	// Развернем транспортные средства
	Для каждого СтрокаВорот Из ТаблицаВорот Цикл
		ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаВорот.Ворота);
		Диаграмма.РазвернутьТочку(ТочкаДиаграммы, Истина);
	КонецЦикла;
	
	// Установим полный интервал диаграммы
	Если НачалоПолногоПериода <> '000101010000' И ОкончаниеПолногоПериода <> '000101010000' Тогда
		Диаграмма.УстановитьПолныйИнтервал(НачалоДня(НачалоПолногоПериода), КонецДня(ОкончаниеПолногоПериода));
	КонецЕсли;
	ВыделитьТекущуюДату(ЭтаФорма);
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПериодыГруппы(СписокСтрок, ТочкаДиаграммы, Диаграмма)

	СписокДобавленныхПериодов = Новый Массив;
	
	Для каждого СтрокаПериод Из СписокСтрок Цикл
		
		//сткВозврат = ОпределитьХарактеристикиСостоянияТС(СтрокаПериод.ТипСостояния);
		
		СерияДиаграммы = Диаграмма.УстановитьСерию(СтрокаПериод.Рейс);
		Если ЗначениеЗаполнено(СтрокаПериод.ПредставлениеТипаТС) Тогда
			СерияДиаграммы.Текст = НСтр(СтрШаблон("ru = '(%1) %2'", СтрокаПериод.ПредставлениеТипаТС, СтрокаПериод.Рейс));
		КонецЕсли; 
		//СерияДиаграммы.Цвет = сткВозврат.Цвет;
		
		ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаДиаграммы, СерияДиаграммы);
		
		Интервал = ЗначениеПериод.Добавить();
		Интервал.Начало = СтрокаПериод.ДатаНачала;
		Интервал.Конец  = СтрокаПериод.ДатаОкончания;
		
		Если НачалоДня(СтрокаПериод.ДатаНачала) = НачалоДня(СтрокаПериод.ДатаОкончания) Тогда
			ПериодСтрока = "(" + Формат(СтрокаПериод.ДатаНачала, "ДФ=ЧЧ:мм") + " - " 
								+ Формат(СтрокаПериод.ДатаОкончания, "ДФ=ЧЧ:мм") + ")";
		Иначе	
			
			ПериодСтрока = Формат(СтрокаПериод.ДатаНачала, "ДФ='дд.ММ ЧЧ:мм'") + " - "
							+ Формат(СтрокаПериод.ДатаОкончания, "ДФ='дд.ММ ЧЧ:мм'");
		КонецЕсли; 
		
		//Интервал.Цвет = сткВозврат.Цвет;
		ТекстИнтервала = Строка(СтрокаПериод.Рейс);//СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(сткВозврат.Текст, ПериодСтрока);
		Интервал.Текст = НСтр(ТекстИнтервала, КодЯзыка);
		
		СписокДобавленныхПериодов.Добавить(Интервал);
		
	КонецЦикла; 

	Возврат СписокДобавленныхПериодов;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаСервере
Функция ПолучитьРасписаниеРаботыСкладскихВорот()
	
	мсвСтатусов = Новый Массив;
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.Новый);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.КПланированиюТС);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.НазначеноТС);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
	мсвСтатусов.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСкладскиеВорота.Ссылка КАК Ворота,
	|	упСкладскиеВорота.ПриоритетИспользования КАК ПриоритетИспользования
	|ПОМЕСТИТЬ втВорота
	|ИЗ
	|	Справочник.упСкладскиеВорота КАК упСкладскиеВорота
	|ГДЕ
	|	упСкладскиеВорота.Владелец = &Склад
	|	И НЕ упСкладскиеВорота.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтатусыРейсовСрезПоследних.Документ КАК Рейс,
	|	втВорота.Ворота КАК Ворота
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВорота КАК втВорота
	|		ПО упСтатусыРейсовСрезПоследних.Документ.СкладскиеВорота = втВорота.Ворота
	|ГДЕ
	|	упСтатусыРейсовСрезПоследних.Статус В(&МассивСтатусов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Ворота КАК Ворота,
	|	втРейсы.Рейс КАК Рейс,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упПеревозчикПоРейсу.ТипТС) КАК ПредставлениеТипаТС,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ДатаНачала,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК ДатаОкончания
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					втРейсы.Рейс
	|				ИЗ
	|					втРейсы)) КАК упМаршрутПоРейсуСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	|		ПО упМаршрутПоРейсуСрезПоследних.Рейс = втРейсы.Рейс
	|			И (упМаршрутПоРейсуСрезПоследних.Адрес.Склад = &Склад)
	|			И (НЕ упМаршрутПоРейсуСрезПоследних.Пройдена)
	|			И (НЕ упМаршрутПоРейсуСрезПоследних.Отменена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВорота КАК втВорота
	|		ПО (втРейсы.Ворота = втВорота.Ворота)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|				,
	|				Рейс В
	|					(ВЫБРАТЬ
	|						втРейсы.Рейс
	|					ИЗ
	|						втРейсы)) КАК упПеревозчикПоРейсу
	|		ПО (втРейсы.Рейс = упПеревозчикПоРейсу.Рейс)
	|ГДЕ
	|	(упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие < &ДатаОкончания
	|			ИЛИ упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие > &ДатаНачала)
	|
	|УПОРЯДОЧИТЬ ПО
	|	втВорота.ПриоритетИспользования,
	|	втВорота.Ворота,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие";
	Запрос.УстановитьПараметр("ДатаНачала"    , Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания" , Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Склад"         , Склад);
	Запрос.УстановитьПараметр("МассивСтатусов", мсвСтатусов);
	
	ТаблицаРасписание = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРасписание;
	
КонецФункции
 
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьШкалу(Форма, ВидШкалы)

	Диаграмма = Форма.Диаграмма;
	
	ШкалаВремениЭлементы = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы;
	
	ПервыйЭлементШкалы = ШкалаВремениЭлементы[0];
	Для Сч = -ШкалаВремениЭлементы.Количество()+1 по -1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[-Сч]);
	КонецЦикла;
	
	Форма.ВидШкалы = ВидШкалы;
	
	Если ВидШкалы = 0 Тогда
		// День, час
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяцаДеньНедели;
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	ИначеЕсли ВидШкалы = 1 Тогда
		// Месяц, неделя, день
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Неделя;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
		ЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
		
	Иначе
		
		ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Год;
		ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		
		ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
		ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	КонецЕсли; 
	ВыделитьТекущуюДату(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВыделитьТекущуюДату(Форма)

	Форма.Диаграмма.ИнтервалыФона.Очистить();
	
	ЦветТекущегоДня = WebЦвета.БледноЗеленый;
	
	Если Форма.ВидШкалы = 0 Тогда
		// День, час
		ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоЧаса(ТекущаяДата()), КонецЧаса(ТекущаяДата()));
		ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
	Иначе
		Если НачалоДня(Форма.Диаграмма.НачалоПолногоИнтервала) <> НачалоДня(Форма.Диаграмма.КонецПолногоИнтервала) Тогда
			ИнтервалТекущегоДня = Форма.Диаграмма.ИнтервалыФона.Добавить(НачалоДня(ТекущаяДата()), КонецДня(ТекущаяДата()));
			ИнтервалТекущегоДня.Цвет = ЦветТекущегоДня;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДиаграммыПоУмолчанию(Диаграмма)

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.ПоддержкаМасштаба               = ПоддержкаМасштабаДиаграммыГанта.Авто;
	Диаграмма.Окантовка                       = Истина;
	Диаграмма.Анимация                        = АнимацияДиаграммы.Использовать;
	Диаграмма.ВертикальнаяПрокрутка           = Истина;
	Диаграмма.ОтображатьПустыеЗначения        = Ложь;
	Диаграмма.ОтображатьЗаголовок             = Ложь;

КонецПроцедуры

#КонецОбласти
