
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ЗаполнитьРегистрыФильтруемыеПоИзмерениям();
	Параметры.Свойство("Документ", Отчет.Документ);
	Элементы.Документ.ТолькоПросмотр = ЗначениеЗаполнено(Отчет.Документ);
	
	//ОФормлениеМакетаПоУмолчанию = "упМакетОформленияОтчетов";
	//Если ПустаяСтрока(МакетОформления) Тогда
	//	МакетОформления = ОФормлениеМакетаПоУмолчанию;
	//КонецЕсли;
	МакетОформления = "упМакетОформленияОтчетов";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если Не ОтчетСформирован И ЗначениеЗаполнено(Отчет.Документ) Тогда
		СформироватьДвиженияДокумента();
		ОтчетСформирован = Истина;
	КонецЕсли;
	
	ЗаполнитьВариантыОформленияМакета();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МакетОформленияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Отчет.Документ) Тогда
		СформироватьДвиженияДокумента();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)

	Если ЗначениеЗаполнено(Отчет.Документ) Тогда
		СформироватьДвиженияДокумента();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРегистрыФильтруемыеПоИзмерениям()

	РегистрыФильтруемыеПоИзмерениям.Очистить();
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упФактическиеПараметрыРейса.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упПлановыеПараметрыРейса.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыЗапросовУсловийПеревозок.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыЗаявокНаПеревозкуГруза.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыЗаданийНаПеревозкуГруза.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыРейсов.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыЗаявокНаТС.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыПутевыхЛистов.Имя);
	РегистрыФильтруемыеПоИзмерениям.Добавить(Метаданные.РегистрыСведений.упСтатусыРемонтовИТОТС.Имя);

КонецПроцедуры // ЗаполнитьРегистрыФильтруемыеПоИзмерению()

&НаСервере
Процедура СформироватьДвиженияДокумента()
	
	ДеревоОтчета = РеквизитФормыВЗначение("ОбъектыОтчета");
	Результат.Очистить();
	АдресаСКД.Очистить();
	ДеревоОтчета.Строки.Очистить();
	ЗаполнитьОбъектыПостроенияОтчета(ДеревоОтчета);
	
	Для Каждого СтрокаТипаОбъектов Из ДеревоОтчета.Строки Цикл
		Результат.НачатьАвтогруппировкуСтрок();
		//ВставитьТипДанныхВМакет(СтрокаТипаОбъектов);
		Для Каждого СтрокаОбъекта Из СтрокаТипаОбъектов.Строки Цикл
			ВставитьДанныеОбъектаВМакет(СтрокаОбъекта);
		КонецЦикла;
		Результат.ЗакончитьАвтогруппировкуСтрок();
	КонецЦикла;
	
КонецПроцедуры

//&НаСервере
//Процедура ВставитьТипДанныхВМакет(СтрокаТипаОбъектов)

//	МакетТипаДанных = НовыйМакетТипаДанных(СтрокаТипаОбъектов.ТипДанных);
//	ОбластьВывода = Результат.Вывести(МакетТипаДанных, 1, СтрокаТипаОбъектов.ТипДанных , Истина);

//КонецПроцедуры // ВставитьТипДанныхВМакет(СтрокаТипаОбъектов)

&НаСервере
Процедура ВставитьДанныеОбъектаВМакет(СтрокаДанныхОбъекта)

	ВозможныеКолонкиДляОтчета = КолонкиИзРегистра(СтрокаДанныхОбъекта);
	СхемаКомпоновкиДанных = СхемаКомпоновкиПоРегистру(СтрокаДанныхОбъекта.ИмяРегистра, СтрокаДанныхОбъекта.ИмяТипаРегистра, ВозможныеКолонкиДляОтчета);
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	РасшифровкаДанных = Новый ДанныеРасшифровкиКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, СхемаКомпоновкиДанных.НастройкиПоУмолчанию, РасшифровкаДанных);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет, , РасшифровкаДанных);
	ПроцессорКомпоновки.Сбросить();
	
	РезТабл = Новый ТабличныйДокумент;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(РезТабл);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	ЗадатьФорматСтрок(РезТабл);
	
	Если РезТабл.ВысотаТаблицы > 1 Тогда
		Секция = НовыйМакетСекцииДанных(СтрокаДанныхОбъекта.ИмяРегистра, СтрокаДанныхОбъекта.Представление);
		ОбластьВывода = Результат.Вывести(Секция,2,"Группа",Истина);
		ОбластьВывода.СоздатьФорматСтрок();
		ОбластьВывода = Результат.Вывести(РезТабл, 3, "Проверка 1", Ложь);
		ОбластьДействияРасшифровки = ОбластьВывода.Имя;
		ОбластьВывода.СоздатьФорматСтрок();
		
		ПоместитьДанныеСКДВоВременноеХранилище(РасшифровкаДанных, СхемаКомпоновкиДанных, ОбластьДействияРасшифровки);
	КонецЕсли;

КонецПроцедуры // ВставитьДанныеВМакет()

&НаСервере
Процедура ДобавитьСтрокуКолонокРегистра(ТаблицаКолонок, ИмяКолонки, ПредставлениеКолонки, ИскатьОтбор, РеквизитМетаДанных)

	КолонкаОтбора = Ложь;
	НоваяСтрока = ТаблицаКолонок.Добавить();
	НоваяСтрока.Поле = ИмяКолонки;
	НоваяСтрока.Заголовок = ПредставлениеКолонки;

КонецПроцедуры // ДобавитьСтрокуКолонокРегистра()

&НаСервере
Процедура ЗадатьФорматСтрок(НастраиваемфйТабличныйДокумент)
	
	КоличествоСтрок = НастраиваемфйТабличныйДокумент.ВысотаТаблицы;
	КоличествоКолонок = НастраиваемфйТабличныйДокумент.ШиринаТаблицы;
	Ширина1 = "";
	Ширина2 = "";
	Строка1 = 0;
	Строка2 = 0;
	Для индекс = 1 По КоличествоСтрок Цикл
		
		Ширина2 = "";
		Для КоличествоКолонок = 1 По КоличествоКолонок Цикл
			Ширина2 = Ширина2 + "," + НастраиваемфйТабличныйДокумент.Область(индекс, КоличествоКолонок).ШиринаКолонки;
		КонецЦикла; 
		Если Ширина1 = Ширина2 Тогда
			Строка2 = Строка2 + 1;
		Иначе
			Если Строка1 <> 0 Тогда
				НастраиваемфйТабличныйДокумент.Область(Строка1, , Строка2).СоздатьФорматСтрок();
			КонецЕсли;
			Ширина1 = Ширина2;
			Строка1 = индекс;
			Строка2 = индекс;
		КонецЕсли;
		
	КонецЦикла; 
	
	Если Строка1 <> 0 Тогда
		НастраиваемфйТабличныйДокумент.Область(Строка1, , Строка2).СоздатьФорматСтрок();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеСКДВоВременноеХранилище(Расшифровка, СКД, ОбластьДействияРасшифровки)

	АдресРасшифровки = ПоместитьВоВременноеХранилище(Расшифровка, ЭтаФорма.УникальныйИдентификатор);
	АдресСКД = ПоместитьВоВременноеХранилище(СКД, ЭтаФорма.УникальныйИдентификатор);
	
	ДиапазонРасшифровки = ОбластьДействияРасшифровки(ОбластьДействияРасшифровки);
	
	СтрокаАресСКД = АдресаСКД.Добавить();
	СтрокаАресСКД.РасшифровкаСКД = АдресРасшифровки;
	СтрокаАресСКД.СКД = АдресСКД;
	СтрокаАресСКД.НачалоДиапазона = ДиапазонРасшифровки.НижняяГраница;
	СтрокаАресСКД.ОкончаниеДиапазона = ДиапазонРасшифровки.ВерхняяГраница;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбъектыПостроенияОтчета(ДеревоОтчета)

	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	ОбъектОтчет.ЗаполнитьРегистрыДвижений(ДеревоОтчета);
	ЗначениеВРеквизитФормы(ДеревоОтчета, "ОбъектыОтчета");

КонецПроцедуры // ЗаполнитьОбъектыПостроенияОтчета()

&НаСервере
Процедура ЗаполнитьВариантыОформленияМакета()

	Элементы.МакетОформления.СписокВыбора.Очистить();
	Для Каждого ЭлементОформления Из БиблиотекаМакетовОформленияКомпоновкиДанных Цикл
		Элементы.МакетОформления.СписокВыбора.Добавить(ЭлементОформления.Имя, ЭлементОформления.Представление);
	КонецЦикла;

КонецПроцедуры // ЗаполнитьВариантыОформленияМакета()

//&НаСервере
//Функция НовыйМакетТипаДанных(ТипДанных)

//	Если ТипДанных = "ОбъектыРегистрации" Тогда
//		ТекстТипаДанных = НСтр("ru = 'Движения сформированные текущим документом'");
//	Иначе
//		ТекстТипаДанных = НСтр("ru = 'Движения по текущему документу'");
//	КонецЕсли;
//	СекцияТипаДанных = Новый ТабличныйДокумент;
//	ОбластьРедактирования = СекцияТипаДанных.Область("R1C1");
//	ОбластьРедактирования.Текст = ТекстТипаДанных;
//	ОбластьРедактирования.ВысотаСтроки = 30;
//	ШрифтСекции = ОбластьРедактирования.Шрифт;
//	НовыйШрифтСекции = Новый Шрифт(ШрифтСекции, , 16);
//	ОбластьРедактирования.Шрифт = НовыйШрифтСекции;
//	
//	Возврат СекцияТипаДанных;
//	
//КонецФункции // НовыйМакетТипаДанных()

&НаСервере
Функция НовыйМакетСекцииДанных(ИмяРегистра, ПредставлениеРегистра)

	ТекстПредставления = НСтр("ru = 'Данные по %1 ""%2""'");
	ГотовоеПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредставления, ПредставлениеТипаРегистра(ИмяРегистра), ПредставлениеРегистра);
	СекцияДанных = Новый ТабличныйДокумент;
	ОбластьРедактирования = СекцияДанных.Область("R1C1");
	ОбластьРедактирования.Текст = ГотовоеПредставление;
	ОбластьРедактирования.ВысотаСтроки = 30;
	ОбластьРедактирования.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
	ШрифтСекции = ОбластьРедактирования.Шрифт;
	НовыйШрифтСекции = Новый Шрифт(ШрифтСекции, , 12, Истина);
	ОбластьРедактирования.Шрифт = НовыйШрифтСекции;
	
	Возврат СекцияДанных;
	
КонецФункции // НовыйМакетТипаДанных(ТипДанных)

&НаСервере
Функция КолонкиИзРегистра(СтрокаДанныхОбъекта)
	
	КолонкиОтбораРегистра.Очистить();
	ДанныеПоКолонкам = НоваяТаблицаКолонокРегистра();
	ОбъектПолей = Метаданные.РегистрыСведений.Найти(СтрокаДанныхОбъекта.ИмяРегистра);
	ИскатьКолонкуОтбора = НадоИскатьКолонкуОтбора(СтрокаДанныхОбъекта);
	Если ОбъектПолей = Неопределено Тогда
		ОбъектПолей = Метаданные.РегистрыНакопления.Найти(СтрокаДанныхОбъекта.ИмяРегистра);
	КонецЕсли;
	Если Не ИскатьКолонкуОтбора Тогда
		ИмяКолонки = "Регистратор";
		ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, ИмяКолонки, НСтр("ru = 'Регистратор'"), Истина,
		ОбъектПолей.СтандартныеРеквизиты[ИмяКолонки]);
		ДобавитьКолонкуОтбора(ИмяКолонки, Ложь, Неопределено);
		Если Метаданные.РегистрыНакопления.Найти(ОбъектПолей.Имя) <> Неопределено Тогда 
			Если ОбъектПолей.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
				ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, "ВидДвижения", НСтр("ru = 'Вид движения'"), Истина,
					ОбъектПолей.СтандартныеРеквизиты["ВидДвижения"]);
			КонецЕсли;
		КонецЕсли;
		ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, "НомерСтроки", НСтр("ru = 'Номер строки'"), Истина,
			ОбъектПолей.СтандартныеРеквизиты["НомерСтроки"]);
	Иначе
		ИспользоватьФильтрПоРеквизиту = Истина;
	КонецЕсли;
	ИспользоватьФильтрПоРеквизиту = ФильтроватьРегистрПоРеквизитам(СтрокаДанныхОбъекта.ИмяРегистра);
	Если ОбъектПолей <> Неопределено Тогда
		Для Каждого Реквизит Из ОбъектПолей.СтандартныеРеквизиты Цикл
			Если Реквизит.Имя = "Период" Тогда
				ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, Реквизит.Имя, Реквизит.Синоним, Ложь, Реквизит);
			КонецЕсли; 
		КонецЦикла;

		Для Каждого Реквизит Из ОбъектПолей.Измерения Цикл
			ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, Реквизит.Имя, Реквизит.Синоним, ИскатьКолонкуОтбора, Реквизит);
			Если ИспользоватьФильтрПоРеквизиту Тогда
				ДобавитьКолонкуОтбора(Реквизит.Имя, Истина, Реквизит);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Реквизит Из ОбъектПолей.Ресурсы Цикл
			ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, Реквизит.Имя, Реквизит.Синоним, ИскатьКолонкуОтбора, Реквизит);
			Если ИспользоватьФильтрПоРеквизиту Тогда
				ДобавитьКолонкуОтбора(Реквизит.Имя, Истина, Реквизит);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Реквизит Из ОбъектПолей.Реквизиты Цикл
			ДобавитьСтрокуКолонокРегистра(ДанныеПоКолонкам, Реквизит.Имя, Реквизит.Синоним, ИскатьКолонкуОтбора, Реквизит);
			Если ИспользоватьФильтрПоРеквизиту Тогда
				ДобавитьКолонкуОтбора(Реквизит.Имя, Истина, Реквизит);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеПоКолонкам;

КонецФункции // КолонкиИзРегистра(СтрокаДанныхОбъекта)

&НаСервере
Процедура ДобавитьКолонкуОтбора(ИмяКолонки, ПроверитьТипКолонки, РеквизитРегистра)

	Если Не ПроверитьТипКолонки
		Или ДокументВходитВСоставТипов(РеквизитРегистра, Отчет.Документ) Тогда
		КолонкиОтбораРегистра.Добавить(ИмяКолонки);
	КонецЕсли;

КонецПроцедуры // ДобавитьКолонкуОтбора(ИмяКолонки)()

&НаСервере
Функция ДокументВходитВСоставТипов(РеквизитРегистра, Документ)

	Возврат РеквизитРегистра.Тип.СодержитТип(ТипЗнч(Документ));

КонецФункции // ДокументВходитВСоставТипов(РеквизитРегистра, Отчет.Документ)()

&НаСервере
Функция НадоИскатьКолонкуОтбора(СтрокаДанныхОбъекта)

	Возврат СтрокаДанныхОбъекта.ИскатьКолонкуРегистратора;

КонецФункции // НадоИскатьКолонкуОтбора(СтрокаДанныхОбъекта)

&НаСервере
Функция НоваяТаблицаКолонокРегистра()

	ДанныеПоКолонкам = Новый ТаблицаЗначений;
	ДанныеПоКолонкам.Колонки.Добавить("Поле");
	ДанныеПоКолонкам.Колонки.Добавить("Заголовок");
	ДанныеПоКолонкам.Колонки.Добавить("КолонкаОтбора");
	
	Возврат ДанныеПоКолонкам;

КонецФункции // НоваяТаблицаКолонокРегистра()

&НаСервере
Функция ПредставлениеТипаРегистра(ИмяРегистра)

	ИмяТипаРегистра = "";
	Если Метаданные.РегистрыСведений.Найти(ИмяРегистра) <> Неопределено Тогда
		ИмяТипаРегистра = "регистру сведений";
	ИначеЕсли Метаданные.РегистрыНакопления.Найти(ИмяРегистра) <> Неопределено Тогда
		ИмяТипаРегистра = " регистру накопления";
	КонецЕсли;
	
	Возврат ИмяТипаРегистра;

КонецФункции // ПредставлениеТипаРегистра()

&НаСервере
Функция СхемаКомпоновкиПоРегистру(ИмяРегистра, ИмяТипаРегистра, КолонкиОтчета) Экспорт
	
	СКД = Новый СхемаКомпоновкиДанных;
	
	Источник = СКД.ИсточникиДанных.Добавить();
	Источник.Имя = "ЛокальнаяБаза";
	Источник.СтрокаСоединения = "";
	Источник.ТипИсточникаДанных = "Local";
	
	НастройкиСКД = СКД.НастройкиПоУмолчанию;
	
	ИмяПоляОтбора = "";
	Для Каждого СтрокаКолонки Из КолонкиОтчета Цикл
		
		ВыбранноеПолеНоменклатура = НастройкиСКД.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПолеНоменклатура.Поле = Новый ПолеКомпоновкиДанных(СтрокаКолонки.Поле);
		ВыбранноеПолеНоменклатура.Заголовок = СтрокаКолонки.Заголовок;
		
	КонецЦикла;
	
	СтрокаОтбора = СтрокаОтбораПоРегистру();
	
	Если ПустаяСтрока(СтрокаОтбора) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ * ИЗ %1.%2 КАК РегистрВыборки ГДЕ%3";
	СтрокаВыборки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ИмяТипаРегистра, ИмяРегистра, СтрокаОтбора);
	
	НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя = ИмяРегистра;
	НаборДанных.ИсточникДанных = "ЛокальнаяБаза";
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	НаборДанных.Запрос = СтрокаВыборки;
	
	ПараметрДанных = СКД.Параметры.Добавить();
	ПараметрДанных.Имя = "Документ";
	ПараметрДанных.Значение = Отчет.Документ;
	ПараметрДанных.Заголовок = ИмяПоляОтбора;
	ПараметрДанных.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	ПараметрДанных.ВключатьВДоступныеПоля = Истина;
	
	ДетальныеЗаписиГруппировкиСклад = НастройкиСКД.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	АвтоПолеГруппировки = ДетальныеЗаписиГруппировкиСклад.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	АвтоПолеГруппировки.Использование = Истина;
	
	ОформлениеМакета = Новый ПараметрКомпоновкиДанных("МакетОформления");
	ЭлементОформления = СКД.НастройкиПоУмолчанию.ПараметрыВывода.НайтиЗначениеПараметра(ОформлениеМакета);
	ЭлементОформления.Использование = Истина;
	ЭлементОформления.Значение = МакетОформления;
	
	Возврат СКД;
	
КонецФункции

&НаСервере
Функция СтрокаОтбораПоРегистру()

	ТекстУсловия = "";
	Для Каждого КолонкаОтбора Из КолонкиОтбораРегистра Цикл
		Если Не ПустаяСтрока(ТекстУсловия) Тогда
			ТекстУсловия = ТекстУсловия + " ИЛИ ";
		КонецЕсли;
		ТекстУсловия = ТекстУсловия + " РегистрВыборки." + КолонкаОтбора + " = &Документ";
	КонецЦикла;

	Возврат ТекстУсловия;
КонецФункции // СтрокаОтбораПоРегистру()

&НаСервере
Функция ОбластьДействияРасшифровки(ИмяОбласти)
	
	МассивДиапазон = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяОбласти, ":");
	СтруктураДиапазон = Новый Структура;
	Если МассивДиапазон.Количество() = 2 Тогда
		СтруктураДиапазон.Вставить("НижняяГраница", ЗначениеИмениОбластиВЧисло(МассивДиапазон[0]));
		СтруктураДиапазон.Вставить("ВерхняяГраница", ЗначениеИмениОбластиВЧисло(МассивДиапазон[1]));
	Иначе
		СтруктураДиапазон.Вставить("НижняяГраница", 0);
		СтруктураДиапазон.Вставить("ВерхняяГраница", 0);
	КонецЕсли;
	
	Возврат СтруктураДиапазон;
	
КонецФункции // ОбластьДействияРасшифровки(ИмяОбласти)

&НаСервере
Функция ЗначениеИмениОбластиВЧисло(ЗначениеИмени)

	ДиапазонБезБуков = СтрЗаменить(ЗначениеИмени, "R", "");
	ЗначениеДиапазона = Число(ДиапазонБезБуков);

	Возврат ЗначениеДиапазона;

КонецФункции // ЗначениеИмениОбластиВЧисло(ЗначениеИмени)

&НаСервере
Функция ФильтроватьРегистрПоРеквизитам(ИмяРегистра)

	Возврат РегистрыФильтруемыеПоИзмерениям.НайтиПоЗначению(ИмяРегистра) <> Неопределено;

КонецФункции // РегистрыФильтруемыеПоИзмерению()

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Перем ВыбранноеДействие;
	Перем ПараметрыВыбранногоДействия;
	Перем ОбработкаРасшифровки;
	Перем СКДРасшифровки;
	
	СтандартнаяОбработка = Ложь;
	Для Каждого ЭлементСКД Из АдресаСКД Цикл
		Если ЭлементСКД.НачалоДиапазона > Элемент.ТекущаяОбласть.Верх
			Или ЭлементСКД.ОкончаниеДиапазона < Элемент.ТекущаяОбласть.Низ Тогда
			Продолжить;
		КонецЕсли;
		
		ДоступныеДействия = Новый Массив;
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
		ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ЭлементСКД.РасшифровкаСКД, Новый ИсточникДоступныхНастроекКомпоновкиДанных(ЭлементСКД.СКД));
		ПараметрыОповещения = Новый Структура("ВыбранноеДействие, ПараметрыВыбранногоДействия", ВыбранноеДействие, ПараметрыВыбранногоДействия);
		ОписаниеВыбораДействия = Новый ОписаниеОповещения("РезультатОбработкаРасшифровкиЗавершение", ЭтаФорма, ПараметрыОповещения);
		ОбработкаРасшифровки.ПоказатьВыборДействия(ОписаниеВыбораДействия, Расшифровка, ДоступныеДействия);
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровкиЗавершение(ВыбранноеДействие, ПараметрВыполненногоДействия, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		ПоказатьЗначение( , ПараметрВыполненногоДействия);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
