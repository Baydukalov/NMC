
#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	Элементы.ГруппаТаблица.Видимость = Ложь;
	
	УстановитьПараметрыОтображения();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	Если НЕ Настройки = Неопределено Тогда
		сткНастройкиОтображения = Настройки.ДополнительныеСвойства;
		Для каждого текПараметр Из сткНастройкиОтображения Цикл
			ПараметрыОтображения.Вставить(текПараметр.Ключ, текПараметр.Значение);
		КонецЦикла; 
		
		ОбновитьВидимость();
		
		ГеоСхема.Очистить();	
		ГеоСхема.Обновление = Ложь;
		ГеоСхема.Вывести(упМаршрутизация.ПолучитьГеографическуюСхемуПоУмолчанию());
		ГеоСхема.Проекция = ПараметрыОтображения.Проекция;
		ГеоСхема.ОбластьПостроения.ЦветФона = ПараметрыОтображения.ЦветФонаОбласти;
		ГеоСхема.Обновление = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииВариантаНаСервере(Настройки)
	
	сткНастройкиОтображения = Настройки.ДополнительныеСвойства;
	сткНастройкиОтображения.Очистить();	
	Для каждого текПараметр Из ПараметрыОтображения Цикл
		сткНастройкиОтображения.Вставить(текПараметр.Ключ, текПараметр.Значение);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события "ОбработкаРасшифровки" элемента "ГеоСхема"
//
Процедура ГеоСхемаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.упГеографическиеЗоны") Тогда
		ОткрытьФорму("Справочник.упГеографическиеЗоны.ФормаОбъекта", Новый Структура("Ключ", Расшифровка), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды "кмдСформировать".
//
// Параметры:
//  Команда - Команда формы - источник события.
//
Процедура кмдСформировать(Команда)
	
	ВыполнитьОбработкуДанных();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура кмдНастройки(Команда)
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ПараметрыОтображения", ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтображения));
	
	ОткрытьФорму("Отчет.упВходящийГрузопоток.Форма.ФормаНастроек", сткПараметры,,,,, Новый ОписаниеОповещения("НастройкаГеоГрафическойСхемыЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыОтображения()
	
	ПараметрыОтображения = Новый Структура();
	ПараметрыОтображения.Вставить("ОтображатьРегионыГеоСхемы"             , Ложь);
	ПараметрыОтображения.Вставить("ОтображатьНазваниеРегионовВСлоеРегионы", Ложь);
	ПараметрыОтображения.Вставить("ОтображатьНаименованиеЗон"             , Истина);
	ПараметрыОтображения.Вставить("ОтображатьТаблицуДанных"               , Ложь);
	ПараметрыОтображения.Вставить("ТипОтображенияСерии"                   , ТипОтображенияСерииСлояГеографическойСхемы.Гистограмма);
	ПараметрыОтображения.Вставить("РежимОтображения"                      , РежимОтображенияЗначенийСерии.ОтображатьКакЗначение);
	ПараметрыОтображения.Вставить("Проекция"                              , ТипПроекцииГеографическойСхемы.АзимутальнаяПроекцияРавныхПлощадейЛамберта);
	ПараметрыОтображения.Вставить("ЦветФонаОбласти"                       , Новый Цвет(153, 204, 255));

КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимость()
	
	Элементы.ГруппаТаблица.Видимость = ПараметрыОтображения.ОтображатьТаблицуДанных;
	
КонецПроцедуры

&НаСервере
// Выполним обработку формирования результата отчета.
//
Процедура ВыполнитьОбработкуДанных()
	
	мКоллекцияЭлементовСтруктурыНастроекКомпановкиДанных = Отчет.КомпоновщикНастроек.Настройки.Структура;
	мсвПоляГруппировки = Новый Массив;
	мсвПоляГруппировки.Добавить(Новый ПолеКомпоновкиДанных("ЗонаПолучения"));           
	
	текОшибка = Ложь;
	ПолеДляСерии = Неопределено;
	СерииВВыбранныхПолях = Истина;
	
	Если мКоллекцияЭлементовСтруктурыНастроекКомпановкиДанных.Количество()	Тогда
		
		Если мКоллекцияЭлементовСтруктурыНастроекКомпановкиДанных.Количество() = 1 Тогда
			мПоляГруппировки = мКоллекцияЭлементовСтруктурыНастроекКомпановкиДанных[0].ПоляГруппировки.Элементы;
			Для каждого текЭлемент Из мсвПоляГруппировки Цикл
				ДоступноеПоле = ПолучитьВыбранноеПолеКомпановкиДанных(мПоляГруппировки, текЭлемент);
				Если ДоступноеПоле = Неопределено Тогда
					текОшибка = Истина;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для формирования отчета обязательно использование группировки: " + Строка(текЭлемент));
					Прервать;
				Иначе
					Если ДоступноеПоле.Использование = Ложь Тогда
						текОшибка = Истина;
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Укажите использование группировки: " + Строка(текЭлемент));
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если мКоллекцияЭлементовСтруктурыНастроекКомпановкиДанных[0].Структура.Количество() Тогда 
				
				мВложенныеПоляГруппировки = мКоллекцияЭлементовСтруктурыНастроекКомпановкиДанных[0].Структура[0].ПоляГруппировки.Элементы;
				Если мВложенныеПоляГруппировки.Количество() = 1 Тогда
					СерииВВыбранныхПолях = Ложь;
					ПолеДляСерии = СтрЗаменить(Строка(мВложенныеПоляГруппировки[0].Поле), ".", "");;
				ИначеЕсли мВложенныеПоляГруппировки.Количество() > 1 Тогда
					текОшибка = Истина;
					ТекстСообщения = НСтр("ru = 'Для формирования отчета в группировке должно быть не более одного группировочного поля'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецЕсли; 
			КонецЕсли; 
			
		Иначе
			текОшибка = Истина;
			ТекстСообщения = НСтр("ru = 'Для формирования отчета в группировке должно быть не более одного группировочного поля'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли; 
		
	Иначе	
		текОшибка = Истина;
		ТекстСообщения = НСтр("ru = 'Для формирования отчета обязательно использование группировок'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
	Если текОшибка Тогда
		Возврат;
	КонецЕсли;
	
	// ВЫВОД ДАННЫХ НА ГЕОГРАФИЧЕСКУЮ СХЕМУ.
	ГеоСхема.Очистить();	
	ГеоСхема.Обновление = Ложь;
	ГеоСхема.Вывести(упМаршрутизация.ПолучитьГеографическуюСхемуПоУмолчанию());
	
	ГеоСхема.Проекция = ПараметрыОтображения.Проекция;
	ГеоСхема.ОбластьПостроения.ЦветФона = ПараметрыОтображения.ЦветФонаОбласти;
	
	ГеоСхема.ОтображатьЛегенду = Истина;
	ГеоСхема.ОтображатьЗаголовок = Истина;
	
	// Установим параметры заголовка.
	ГеоСхема.ОбластьЗаголовка.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки,0);
	
	НовыйСлой = ГеоСхема.Слои.Добавить("Дополнительный", Тип("ПолигональныйОбъектГеографическойСхемы"));
	НовыйСлой.Видимость = Истина;
	
	СерияНазвание = НовыйСлой.Серии.Добавить("НАЗВАНИЕ");
	СерияНазвание.Текст = "Наименование объекта";	
	Если ПараметрыОтображения.ОтображатьНаименованиеЗон Тогда
	   	СерияНазвание.ТипОтображения = ТипОтображенияСерииСлояГеографическойСхемы.Текст;
    Иначе
		СерияНазвание.ТипОтображения = ТипОтображенияСерииСлояГеографическойСхемы.НеОтображать;
	КонецЕсли; 
	СерияНазвание.РежимОтображенияЗначений = ПараметрыОтображения.РежимОтображения;
	СерияНазвание.Значение = "Имя";
	
	Если ПараметрыОтображения.ОтображатьРегионыГеоСхемы Тогда
		
		ТекущийСлой_Регионы = ГеоСхема.Слои[0]; // будем считать что первый взятый слой это Регионы.
		
		// Установим видимость наименования.
		СерияНаименованиеРегионов = ТекущийСлой_Регионы.Серии.Найти("НАЗВАНИЕ");
		Если СерияНаименованиеРегионов <> Неопределено Тогда
			Если ПараметрыОтображения.ОтображатьНазваниеРегионовВСлоеРегионы Тогда
				СерияНаименованиеРегионов.ТипОтображения = ТипОтображенияСерииСлояГеографическойСхемы.Текст;
			Иначе
				СерияНаименованиеРегионов.ТипОтображения = ТипОтображенияСерииСлояГеографическойСхемы.НеОтображать;
			КонецЕсли;
		КонецЕсли;
		
	Иначе	
		// Скроем видимость всех данных для слоев.
		Для каждого ТекущийСлой Из ГеоСхема.Слои Цикл
			ГеоСхема.УстановитьСвойствоОбъектов(ТекущийСлой.Объекты, "ОтображатьДанные", Ложь);
			ГеоСхема.УстановитьСвойствоОбъектов(ТекущийСлой.Объекты, "Видимость", Ложь);
		КонецЦикла;
	КонецЕсли; 

	СхемаКомпоновкиДанных = Отчеты.упВходящийГрузопоток.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных, ));
	
	// Компоновка макета.
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	НачалоПериода = МакетКомпоновкиДанных.ЗначенияПараметров.НачалоПериода.Значение;
	КонецПериода = МакетКомпоновкиДанных.ЗначенияПараметров.КонецПериода.Значение;
	Если ЗначениеЗаполнено(НачалоПериода) Или ЗначениеЗаполнено(КонецПериода) Тогда
		Представление = СтрШаблон("с %1 по %2", Формат(НачалоПериода, "Л=ru_RU; ДФ='dd.MM.yy'"), Формат(КонецПериода, "Л=ru_RU; ДФ='dd.MM.yy'"));
		ТекстЗаголовка = НСтр(СтрШаблон("ru = 'Входящий грузопоток (%1)'", Представление)); 
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Входящий грузопоток за все время'"); 
	КонецЕсли;
	ГеоСхема.ОбластьЗаголовка.Текст = ТекстЗаголовка;
	
	// Инициализация процессора компоновки.
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	// Таблица значений, в которую будет получен результат.
	ДеревоЗначений = Новый ДеревоЗначений;
	
	// Получение результата.
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(ДеревоЗначений);	
	ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
	
	Если ПараметрыОтображения.ОтображатьТаблицуДанных Тогда
		РезультатОтчета.Очистить();
		
		// Компоновка макета.
		КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки());
		
		// Инициализация процессора компоновки.
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);	
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(РезультатОтчета);
		
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	КонецЕсли;
	
	мсвВыбранныеПоля = Новый Массив;
	мКоллекцияДоступныхПолейКомпоновкиДанных = Отчет.КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.Элементы;
	КоллекцияЭлементовПользовательскихНастроек = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	
	Для каждого текЭлементКоллекции Из КоллекцияЭлементовПользовательскихНастроек Цикл
		Если ТипЗнч(текЭлементКоллекции) = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
			Для каждого текПоле Из текЭлементКоллекции.Элементы Цикл
				мсвВыбранныеПоля.Добавить(Новый Структура("Заголовок,Поле", текПоле.Заголовок, СтрЗаменить(Строка(текПоле.Поле), ".", "")));
				Если Не СерииВВыбранныхПолях Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла; 
	
	Если мсвВыбранныеПоля.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрано ни одного поля'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	соотСерии = Новый Соответствие; 
	Если СерииВВыбранныхПолях Тогда
		Для каждого текЭлемент Из мсвВыбранныеПоля Цикл
			НоваяСерия = НовыйСлой.Серии.Добавить(текЭлемент.Поле);
			НоваяСерия.Текст = текЭлемент.Заголовок;
			НоваяСерия.ТипОтображения = ПараметрыОтображения.ТипОтображенияСерии;		
			НоваяСерия.РежимОтображенияЗначений = ПараметрыОтображения.РежимОтображения;
			НоваяСерия.Значение = текЭлемент.Поле;
			НоваяСерия.Формат = "ЧДЦ=2; ЧН=Отсутствует";
			
			// Добавим в легенду.
			ЭлементЛегенды = ГеоСхема.ОбластьЛегенды.Элементы.Добавить();
			ЭлементЛегенды.Серия = НоваяСерия;
			ЭлементЛегенды.ТекстПодписи = текЭлемент.Заголовок;
			ЭлементЛегенды.ОтображатьШкалу = ТипОтображенияШкалыЭлементаЛегендыГеографическойСхемы.НеОтображать;
			
			соотСерии.Вставить(текЭлемент.Поле, НоваяСерия);
		КонецЦикла; 
	КонецЕсли; 
	
	мсвОбъектов = Новый Массив;
	соотОбъекты = Новый Соответствие;

	Для каждого текЗона Из ДеревоЗначений.Строки Цикл
		Если Не ЗначениеЗаполнено(текЗона.ЗонаПолучения) Тогда
			Продолжить;
		КонецЕсли; 
		
		мсвОбъектов.Добавить(текЗона.ЗонаПолучения);
		НовыйОбъект = НовыйСлой.Объекты.Добавить();	
		НовыйОбъект.Значение = текЗона.ЗонаПолучения;
		НовыйОбъект.Расшифровка = текЗона.ЗонаПолучения;
		соотОбъекты.Вставить(текЗона.ЗонаПолучения, НовыйОбъект);

		Если СерииВВыбранныхПолях Тогда
			Для каждого текСтрокаЗначения Из текЗона.Строки Цикл
				Для каждого текПоле Из мсвВыбранныеПоля Цикл
					НовыйСлой.УстановитьЗначение(НовыйОбъект, соотСерии.Получить(текПоле.Поле), текСтрокаЗначения[текПоле.Поле]);
				КонецЦикла; 
			КонецЦикла; 
			
		Иначе
				
			Для каждого текСерия Из текЗона.Строки Цикл
				текЗначениеСерии = текСерия[ПолеДляСерии];
				
				ДействующаяСерия = соотСерии.Получить(текЗначениеСерии);
				Если ДействующаяСерия = Неопределено Тогда
					
					ПредставлениеСерии = Строка(текЗначениеСерии);
					НоваяСерия = НовыйСлой.Серии.Добавить(ЗаменитьНедопустимыеСимволы(ПредставлениеСерии));
					Если ЗначениеЗаполнено(текЗначениеСерии) Тогда
						НоваяСерия.Текст = ПредставлениеСерии;
					Иначе
						НоваяСерия.Текст = НСтр("ru = 'Не указано'");
					КонецЕсли;  
					НоваяСерия.ТипОтображения = ПараметрыОтображения.ТипОтображенияСерии;		
					НоваяСерия.РежимОтображенияЗначений = ПараметрыОтображения.РежимОтображения;
					НоваяСерия.Значение = текЗначениеСерии;
					НоваяСерия.Формат = "ЧДЦ=2; ЧН=Отсутствует";
					
					// Добавим в легенду.
					ЭлементЛегенды = ГеоСхема.ОбластьЛегенды.Элементы.Добавить();
					ЭлементЛегенды.Серия = НоваяСерия;
					ЭлементЛегенды.ТекстПодписи = текЗначениеСерии;
					ЭлементЛегенды.ОтображатьШкалу = ТипОтображенияШкалыЭлементаЛегендыГеографическойСхемы.НеОтображать;
					
					соотСерии.Вставить(текЗначениеСерии, НоваяСерия);
					ДействующаяСерия = НоваяСерия;
				КонецЕсли;
				
				Для каждого текСтрокаЗначения Из текСерия.Строки Цикл
					НовыйСлой.УстановитьЗначение(НовыйОбъект, ДействующаяСерия, текСтрокаЗначения[мсвВыбранныеПоля[0].Поле]);
				КонецЦикла; 
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла;

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", мсвОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка КАК Зона,
	|	упГеографическиеЗоны.СтильОтображения.ЦветГраницы КАК ЦветГраницы,
	|	упГеографическиеЗоны.СтильОтображения.ЦветЗаливки КАК ЦветЗаливки
	|ПОМЕСТИТЬ втЗоны
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|ГДЕ
	|	упГеографическиеЗоны.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗоны.Зона,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЗоны.Зона) КАК ЗонаПредставление,
	|	втЗоны.ЦветГраницы,
	|	втЗоны.ЦветЗаливки
	|ИЗ
	|	втЗоны КАК втЗоны
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗоны.Зона КАК Зона,
	|	ЕСТЬNULL(упГеографическиеЗоныПолигон.Широта, 0) КАК Широта,
	|	ЕСТЬNULL(упГеографическиеЗоныПолигон.Долгота, 0) КАК Долгота,
	|	упГеографическиеЗоныПолигон.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Справочник.упГеографическиеЗоны.Полигон КАК упГеографическиеЗоныПолигон
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗоны КАК втЗоны
	|		ПО упГеографическиеЗоныПолигон.Ссылка = втЗоны.Зона
	|
	|УПОРЯДОЧИТЬ ПО
	|	втЗоны.Зона,
	|	НомерСтроки,
	|	Широта,
	|	Долгота
	|ИТОГИ ПО
	|	Зона";
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[1].Пустой() Тогда
		ТаблицаЗон = Результат[1].Выгрузить();
		ДеревоТочек = Результат[2].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Для каждого текЗона Из ДеревоТочек.Строки Цикл
			текОбъект = соотОбъекты.Получить(текЗона.Зона);
			
			текДанные = ТаблицаЗон.Найти(текЗона.Зона, "Зона");
			текОбъект.Цвет = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(текДанные.ЦветЗаливки);
			текОбъект.ЦветГраницы = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(текДанные.ЦветГраницы);
			текОбъект.Видимость = Истина;
			
			НовыйСлой.УстановитьЗначение(текОбъект, СерияНазвание, текДанные.ЗонаПредставление, текДанные.ЗонаПредставление, текДанные.Зона);
			
			Контуры = текОбъект.Контуры;
			ТекущийКонтур = Контуры.Добавить();
			
			Для каждого текКоординаты Из текЗона.Строки Цикл
				НоваяКоордината = Новый ГеографическиеКоординаты(текКоординаты.Широта, текКоординаты.Долгота, 0);
				ТекущийКонтур.Добавить(НоваяКоордината);
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли; 
	
	ГеоСхема.Обновление = Истина;
	
КонецПроцедуры // ВыполнитьОбработкуДанных()

&НаСервереБезКонтекста
// Рекурсивная функция, выполняет поиск в коллекции элемента.
//
// Параметры:
//  КоллекцияВыбранныхПолей  - КоллекцияЭллементовГруппаВыбранныхПолейКомпоновкиДанных,
//								КоллекцияЭлементовСтруктурыНастроекКомпановкиДанных, КоллекцияВыбранныхПолейКомпановкиДанных  - Коллекция элементов.
//  ДоступноеПоле  - Поле поиска - Поле которое требуется найти в коллекции.
//
// Возвращаемое значение:
//   ПолеКомпоновкиДанных - Поле найденное в коллекции.
//
Функция ПолучитьВыбранноеПолеКомпановкиДанных(КоллекцияВыбранныхПолей, ДоступноеПоле)
	
	текРезультат  = Неопределено;
	
	Для каждого текЭлемент Из КоллекцияВыбранныхПолей Цикл
	
		Если ТипЗнч(текЭлемент) = ТИП("ВыбранноеПолеКомпоновкиДанных") Тогда
			Если текЭлемент.Поле = ДоступноеПоле.Поле Тогда
				текРезультат  = текЭлемент;
				Прервать;
			КонецЕсли;
		ИначеЕсли ТипЗнч(текЭлемент) = ТИП("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			текРезультат  = ПолучитьВыбранноеПолеКомпановкиДанных(текЭлемент.Элементы,ДоступноеПоле);
			Если текРезультат <> Неопределено Тогда
				Прервать;
			КонецЕсли;
		ИначеЕсли ТипЗнч(текЭлемент) = ТИП("ПолеГруппировкиКомпоновкиДанных") Тогда
			Если текЭлемент.Поле = ДоступноеПоле Тогда
				текРезультат  = текЭлемент;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;

	Возврат текРезультат;
	
КонецФункции // ПолучитьВыбранноеПолеКомпановкиДанных()

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия форы настроек.
//
Процедура НастройкаГеоГрафическойСхемыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		ПараметрыОтображения = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Результат);
		ОбновитьВидимость();
		ЭтаФорма.ВариантМодифицирован = Истина;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // НастройкаГеоГрафическойСхемыЗавершение()

#КонецОбласти 

&НаСервере
// Заменяет недопустимые символы.
//
// Параметры:
//  Имя  - Строка - Наименование.
//
// Возвращаемое значение:
//   Массив   - Результат выполнения.
//
Функция ЗаменитьНедопустимыеСимволы(Имя)

	НовоеИмя = Имя;
	
	МассивНедопустимыхСимволов = Новый Массив;
	МассивНедопустимыхСимволов.Добавить(" ");
	МассивНедопустимыхСимволов.Добавить(".");
	МассивНедопустимыхСимволов.Добавить("/");
	МассивНедопустимыхСимволов.Добавить("(");
	МассивНедопустимыхСимволов.Добавить(")");
	МассивНедопустимыхСимволов.Добавить("*");
	МассивНедопустимыхСимволов.Добавить("#");
	МассивНедопустимыхСимволов.Добавить("|");
	Для Каждого НедопустимыйСимвол Из МассивНедопустимыхСимволов Цикл
		НовоеИмя = СтрЗаменить(НовоеИмя, НедопустимыйСимвол, "");
	КонецЦикла;
	
	Возврат НовоеИмя;

КонецФункции

#КонецОбласти