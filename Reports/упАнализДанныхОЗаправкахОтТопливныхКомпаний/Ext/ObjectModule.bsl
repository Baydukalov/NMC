#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиКомпоновки = КомпоновщикНастроек.Настройки;
	
	// Получить значение периода для формирования данных
	ПериодЗначениеПараметраКомпоновкиДанных = НастройкиКомпоновки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Период 				= КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПериодЗначениеПараметраКомпоновкиДанных.ИдентификаторПользовательскойНастройки);
	
	НачалоПериода 		= '00010101';
	ОкончаниеПериода 	= '00010101';
			
	Если НЕ Период = Неопределено 
			И Период.Использование Тогда
		НачалоПериода 		= Период.Значение.ДатаНачала;
		ОкончаниеПериода 	= Период.Значение.ДатаОкончания;
	КонецЕсли;
	
	// Формируются данные отчета
	сткТаблицы = упЗакрытиеПериода.СформироватьТаблицыДляСопоставления(НачалоПериода, ОкончаниеПериода);
	
	// Установка периода, для корректного отбражения в отчете
	ПериодПараметрДанныхНастройки = НастройкиКомпоновки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	
	Если НЕ ПериодПараметрДанныхНастройки = Неопределено Тогда
		ПериодПараметрДанныхНастройки.Значение = Новый СтандартныйПериод(НачалоПериода, ОкончаниеПериода);
	КонецЕсли;


	КомпоновщикМакета 	= Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки		= КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки, ДанныеРасшифровки);
		
	ВнешниеНаборы = Новый Структура();
	ВнешниеНаборы.Вставить("ОстаткиГСМ", 				сткТаблицы.ОстаткиГСМ);
	ВнешниеНаборы.Вставить("ДанныеТопливныхКомпаний", 	сткТаблицы.ДанныеТопливныхКомпаний);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли