
// ПодключаемоеОборудование
Перем глПодключаемоеОборудование Экспорт; // для кэширования на клиенте
// Конец ПодключаемоеОборудование

// СтандартныеПодсистемы

// Хранилище глобальных переменных.
//
// ПараметрыПриложения - Соответствие - хранилище переменных, где:
//   * Ключ - Строка - имя переменной в формате "ИмяБиблиотеки.ИмяПеременной";
//   * Значение - Произвольный - значение переменной.
//
// Инициализация (на примере СообщенияДляЖурналаРегистрации):
//   ИмяПараметра = "СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации";
//   Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
//     ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
//   КонецЕсли;
//  
// Использование (на примере СообщенияДляЖурналаРегистрации):
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"].Добавить(...);
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"] = ...;
Перем ПараметрыПриложения Экспорт; // Хранилище глобальных переменных

// Конец СтандартныеПодсистемы


#Область ОбработчикиСобытий

Процедура ПередНачаломРаботыСистемы(Отказ)
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы();
	// Конец СтандартныеПодсистемы
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.ПередНачаломРаботыСистемы();
	// Конец ПодключаемоеОборудование

	// ИнтернетПоддержкаПользователей
	ИнтернетПоддержкаПользователейКлиент.ПередНачаломРаботыСистемы();
	// Конец ИнтернетПоддержкаПользователей
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы();
	// Конец СтандартныеПодсистемы
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.ПриНачалеРаботыСистемы();
	// Конец ПодключаемоеОборудование
	
	// отработка параметров запуска системы
	Если ОбработатьПараметрыЗапуска(ПараметрЗапуска) Тогда
		Возврат;
	КонецЕсли;
	
	Возврат;
	
	упЗащищенныеФункции.ПроверитьЛицензиюСеанса();
	
	ИспользуетсяУправлениеПеревозками = ПолучитьФункциональнуюОпциюИнтерфейса("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	ИспользуетсяУправлениеТранспортом = ПолучитьФункциональнуюОпциюИнтерфейса("упИспользуетсяГлобальнаяПодсистемаУправлениеТранспортом");
	ИспользуетсяСпутниковыйМониторинг = ПолучитьФункциональнуюОпциюИнтерфейса("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг");
	сткВозврат = упЗащищенныеФункции.ДоступныеМодули();
	ИзмененСостав = Ложь;
	ТекстИзмененСостав = "";
	Если ИспользуетсяУправлениеПеревозками И Не сткВозврат.УправлениеПеревозками Тогда
		ИзмененСостав = Истина;
		ТекстИзмененСостав = НСтр(СтрШаблон("ru = '%1, %2'", ТекстИзмененСостав, "компонента ""Управление перевозками"""));
	КонецЕсли; 
	Если ИспользуетсяУправлениеТранспортом И Не сткВозврат.УправлениеТранспортом Тогда
		ИзмененСостав = Истина;
		ТекстИзмененСостав = НСтр(СтрШаблон("ru = '%1, %2'", ТекстИзмененСостав, "компонента ""Управление транспортом"""));
	КонецЕсли; 
	Если ИспользуетсяСпутниковыйМониторинг И Не сткВозврат.СпутниковыйМониторинг Тогда
		ИзмененСостав = Истина;
		ТекстИзмененСостав = НСтр(СтрШаблон("ru = '%1, %2'", ТекстИзмененСостав, "компонента ""Спутниковый мониторинг"""));
	КонецЕсли; 	
	
	ЭтоАдминистратор = упСервисныеФункцииВызовСервера.ЭтоАдминистратор();
	Если ИзмененСостав Тогда
		Если ЭтоАдминистратор Тогда
			ТекстВопроса = НСтр("ru = 'При продолжении работы будут отключены недоступные компоненты. Продолжить?'");
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить("КодВозвратаДиалога.OK"    ,  НСтр("ru = 'OK'"));
			Кнопки.Добавить("КодВозвратаДиалога.Отмена", НСтр("ru = 'Отмена'"));
			
			ОбработкаОтвета = Новый ОписаниеОповещения("ПроверкаДоступностиКомпонентАдминистраторомЗавершение", упСервисныеФункцииКлиент);
		Иначе
			ТекстВопроса = НСтр("ru = 'Работа с программой будет завершена.'");
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить("КодВозвратаДиалога.OK",  НСтр("ru = 'OK'"));
			
			ОбработкаОтвета = Новый ОписаниеОповещения("ПроверкаДоступностиКомпонентЗавершение", упСервисныеФункцииКлиент);
		КонецЕсли; 
		
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
		ПараметрыВопроса.КнопкаПоУмолчанию = "КодВозвратаДиалога.OK";
		ПараметрыВопроса.Заголовок = НСтр("ru = 'Проверка доступности глобальных компонент'");
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыВопроса.БольшеНеЗадаватьЭтотВопрос = Ложь;
		
		ПараметрыВопроса.Вставить("Тест", Истина);
		
		ТекстИзмененСостав = Прав(ТекстИзмененСостав, СтрДлина(ТекстИзмененСостав) - 2);
		ТекстСообщения = НСтр(СтрШаблон("ru = 'Произошло изменение доступности используемых глобальных компонент: %1. %2'", ТекстИзмененСостав, ТекстВопроса));
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(ОбработкаОтвета, ТекстСообщения, Кнопки, ПараметрыВопроса);
	КонецЕсли;
	
	Если ЭтоАдминистратор И СтрНайти(ПараметрЗапуска, "ОтключитьИзменениеГлобальныхКомпонент") = 0 Тогда
		ВключенаКомпонентаУправлениеПеревозками = Не ИспользуетсяУправлениеПеревозками И сткВозврат.УправлениеПеревозками;
		ВключенаКомпонентаУправлениеТранспортом = Не ИспользуетсяУправлениеТранспортом И сткВозврат.УправлениеТранспортом;
		ВключенаКомпонентаСпутниковыйМониторинг = Не ИспользуетсяСпутниковыйМониторинг И сткВозврат.СпутниковыйМониторинг;
		
		упСервисныеФункцииВызовСервера.УстановитьИспользованиеГлобальныхКомпонент(ВключенаКомпонентаУправлениеПеревозками,
		                                                                          ВключенаКомпонентаУправлениеТранспортом,
		                                                                          ВключенаКомпонентаСпутниковыйМониторинг);
																				  
		Если ВключенаКомпонентаУправлениеПеревозками Или ВключенаКомпонентаУправлениеТранспортом Или ВключенаКомпонентаСпутниковыйМониторинг Тогда
			ОбновитьИнтерфейс();
		КонецЕсли; 																				  
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.ПередЗавершениемРаботыСистемы();
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы
	СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения);
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура _ЗакрытьВсеФормы(Контекст = Неопределено) Экспорт
	
	Окна = ПолучитьОкна();
	Для Каждого Окно Из Окна Цикл
		Для Каждого Форма Из Окно.Содержимое Цикл
			Если Форма.Открыта() Тогда
				Форма.Закрыть();
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	ЗавершитьРаботуСистемы(Ложь, Ложь);
	
КонецПроцедуры

Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные)

	// ПодключаемоеОборудование
	// Подготовить данные
	
	ОписаниеСобытия = Новый Структура();
	ОписаниеОшибки  = "";
	
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	
	// Передать на обработку данные.
	Результат = МенеджерОборудованияКлиент.ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	Если Не Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При обработке внешнего события от устройства произошла ошибка.'")
		                                                 + Символы.ПС + ОписаниеОшибки);
	КонецЕсли;
	// Конец ПодключаемоеОборудование

КонецПроцедуры

#КонецОбласти

#Область ОбработкаПараметровЗапуска
	
Функция ОбработатьПараметрыЗапуска(Знач ПараметрЗапуска)

	Перем Результат;
	Результат = СтандартныеПодсистемыКлиент.ОтключенаЛогикаНачалаРаботыСистемы();
	
	// СтандартныеПодсистемы
	
	// Есть ли параметры запуска
	Если ПустаяСтрока(ПараметрЗапуска) Тогда
		ПодключитьКонтрольРегламентныхЗаданий(Новый Массив);
		Возврат Результат;
	КонецЕсли;
	
	// Параметр может состоять из частей, разделенных символом ";".
	// Первая часть - главное значение параметра запуска. 
	// Наличие дополнительных частей определяется логикой обработки главного параметра.
	ПараметрыЗапуска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрЗапуска, ";");
	ЗначениеПараметраЗапуска = Врег(ПараметрыЗапуска[0]);
		
	// Конец СтандартныеПодсистемы
	
	// Код конфигурации
	ПодключитьКонтрольРегламентныхЗаданий(ПараметрыЗапуска);
	// Конец кода конфигурации

	// СтандартныеПодсистемы
	Возврат Результат;
	// Конец СтандартныеПодсистемы

КонецФункции

#КонецОбласти

#Область КонтрольРегламентныхЗаданий	

//////////////////////////////////////////////////////////////////////////////////
// КОНТРОЛЬ РЕГЛАМЕНТНЫХ ЗАДАНИЙ

// Провести контроль исполнения фоновых заданий
//
Процедура ПровестиКонтрольИсполненияФоновыхЗаданий() Экспорт
	
	Если упСервисныеФункцииВызовСервера.ВремяПоследнегоИсполненияКонтрольногоЗадания() < (ТекущаяДата() - 600) Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Регламентные задания неактивны'"),, НСтр("ru = 'Регламентные задания неактивны, либо не исполнялись долгое время'"), БиблиотекаКартинок.Предупреждение32);
	КонецЕсли;
	
КонецПроцедуры	

// Подключить контроль регламентных заданий
//
Процедура ПодключитьКонтрольРегламентныхЗаданий(ПараметрыЗапуска)
	
	Если упСервисныеФункцииВызовСервера.ЭтоПолноправныйПользователь(Неопределено) Тогда 
		Для Каждого текПараметр Из ПараметрыЗапуска Цикл 
			Если ВРег(текПараметр) = "NOJOBCONTROL" Тогда 
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		Если упСервисныеФункцииВызовСервера.КонтролироватьИсполненияФоновыхЗаданий() Тогда
			ПровестиКонтрольИсполненияФоновыхЗаданий();
			ПодключитьОбработчикОжидания("ПровестиКонтрольИсполненияФоновыхЗаданий", 300);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти
