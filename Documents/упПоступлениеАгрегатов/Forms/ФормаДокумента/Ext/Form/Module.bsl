#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();	
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
					И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
	КонецЕсли;
		
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаУпрУчета 	= упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
		Элементы.АгрегатыАгрегатПервоначальнаяСтоимость.Заголовок = НСтр(СтрШаблон("ru = '%1 (%2)'", Элементы.АгрегатыАгрегатПервоначальнаяСтоимость.Заголовок, ВалютаУпрУчета));
	КонецЕсли; 
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АгрегатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.Агрегаты.ТекущиеДанные;
		текСтрока.Валюта = ВалютаУпрУчета;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьИтоговуюСуммуРасходы();
	
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыПослеУдаления(Элемент)
	
	РассчитатьИтоговуюСуммуРасходы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыСтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если НЕ РазрешеноВыбиратьСтатью(Элементы.Агрегаты.ТекущиеДанные.Агрегат) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,НСтр("ru = 'Статью расходов можно выбирать для агрегата в типе которого
									|зафиксирован срок полезного использования.'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыАгрегатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упШиныУзлыИАгрегаты.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПодобратьАгрегаты(Команда)
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("ТипОборудования", ПредопределенноеЗначение("Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка"));
	сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упПоступлениеАгрегатов"));
	Состояния = Новый СписокЗначений;
	Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	сткПараметры.Вставить("Состояние", Состояния);
	ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборАгрегатовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьДопОборудование(Команда)
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("ТипОборудования", ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка"));
	сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упПоступлениеАгрегатов"));
	Состояния = Новый СписокЗначений;
	Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	сткПараметры.Вставить("Состояние", Состояния);
	ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборДопОборудованияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры


#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия формы подбора агрегатов.
//
Процедура ПодборАгрегатовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			НоваяСтрока = Объект.Агрегаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			НоваяСтрока.Валюта = ВалютаУпрУчета;
		КонецЦикла; 
	КонецЕсли; 
		
КонецПроцедуры // ПодборАгрегатовЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы подбора доп. оборудования.
//
Процедура ПодборДопОборудованияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			НоваяСтрока = Объект.ДополнительноеОборудование.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			НоваяСтрока.Валюта = ВалютаУпрУчета;
			НоваяСтрока.ДополнительноеОборудование = текСтрока.Агрегат;
		КонецЦикла; 
	КонецЕсли; 
		
КонецПроцедуры // ПодборАгрегатовЗавершение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	
	ИтоговаяСумма 		= 0;
	ИтоговаяСуммаНДС 	= 0;
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы", 	"Сумма");
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Агрегаты", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы", 	"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС", 	"СуммаНДС");
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ДополнительноеОборудование", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;

	
	Объект.СуммаРасходы = ИтоговаяСумма;
		
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаКлиенте
Процедура АгрегатыАгрегатПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.Агрегаты.ТекущиеДанные;
	Агрегат	= ТекущаяСтрока.Агрегат;
	Если ЗначениеЗаполнено(Агрегат) Тогда
		сткРеквизиты = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(Агрегат, "Тип.СтатьяРасходов, Тип.ФиксироватьСрокПолезногоИспользования");
		Если сткРеквизиты.ТипФиксироватьСрокПолезногоИспользования Тогда
			ТекущаяСтрока.СтатьяРасходов = сткРеквизиты.ТипСтатьяРасходов;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазрешеноВыбиратьСтатью(Агрегат)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Агрегат, "Тип.ФиксироватьСрокПолезногоИспользования");
КонецФункции

&НаКлиенте
Процедура ДополнительноеОборудованиеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.ДополнительноеОборудование.ТекущиеДанные;
		текСтрока.Валюта = ВалютаУпрУчета;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент, ИмяТаблЧасти)
	
	ТекущаяСтрока		= Элементы[ИмяТаблЧасти].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока, ПолучитьИмяПоля(Элемент.Имя, ИмяТаблЧасти), СтруктураДанные);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля, ИмяТаблЧасти)
	
	Возврат СтрЗаменить(ИмяПоля, ИмяТаблЧасти,"");
	
КонецФункции

&НаКлиенте
Процедура ДополнительноеОборудованиеСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ДополнительноеОборудование");
КонецПроцедуры

#КонецОбласти 