#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Заполнение дополнительных свойств документа.
//
Процедура ИнициализироватьДанныеДокумента(Дата, Агрегаты, ДополнительноеОборудование, ДокументСсылка, Склад, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Склад КАК Склад,
	|	ТЧДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	ТЧДопОборудование.ПробегКм КАК ПробегКм,
	|	ТЧДопОборудование.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	ТЧДопОборудование.Валюта КАК Валюта,
	|	ТЧДопОборудование.Сумма КАК Сумма,
	|	1 КАК Количество,
	|	ТЧДопОборудование.Пробег КАК ТекущийПробег,
	|	ТЧДопОборудование.Моточасы КАК ТекущиеМоточасы
	|ПОМЕСТИТЬ втДопОборудование
	|ИЗ
	|	&ДополнительноеОборудование КАК ТЧДопОборудование
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Склад КАК Склад,
	|	ТЧАгрегаты.Агрегат КАК Агрегат,
	|	ТЧАгрегаты.ПробегКм КАК ПробегКм,
	|	ТЧАгрегаты.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	ТЧАгрегаты.Валюта КАК Валюта,
	|	ТЧАгрегаты.Сумма КАК Сумма,
	|	1 КАК Количество,
	|	ТЧАгрегаты.Пробег КАК ТекущийПробег,
	|	ТЧАгрегаты.Моточасы КАК ТекущиеМоточасы
	|ПОМЕСТИТЬ втАгрегаты
	|ИЗ
	|	&Агрегаты КАК ТЧАгрегаты
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТЧАгрегаты.Склад КАК Склад,
	|	ТЧАгрегаты.Агрегат КАК Агрегат,
	|	ТЧАгрегаты.ПробегКм КАК ПробегКм,
	|	ТЧАгрегаты.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	ТЧАгрегаты.Сумма * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1)) КАК Сумма,
	|	1 КАК Количество
	|ИЗ
	|	втАгрегаты КАК ТЧАгрегаты
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		Валюта = &Валюта) КАК КурсыВалютДокумента
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		) КАК КурсыВалют
	|	ПО ТЧАгрегаты.Валюта = КурсыВалют.Валюта
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТЧДопОборудование.Склад КАК Склад,
	|	ТЧДопОборудование.ДополнительноеОборудование КАК Агрегат,
	|	ТЧДопОборудование.ПробегКм КАК ПробегКм,
	|	ТЧДопОборудование.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	ТЧДопОборудование.Сумма * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1)) КАК Сумма,
	|	1 КАК Количество
	|ИЗ
	|	втДопОборудование КАК ТЧДопОборудование
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		Валюта = &Валюта) КАК КурсыВалютДокумента
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		) КАК КурсыВалют
	|	ПО ТЧДопОборудование.Валюта = КурсыВалют.Валюта
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДопОборудование.ДополнительноеОборудование КАК Оборудование,
	|	втДопОборудование.ТекущийПробег КАК Пробег,
	|	втДопОборудование.ТекущиеМоточасы КАК Моточасы
	|ИЗ
	|	втДопОборудование КАК втДопОборудование
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втАгрегаты.Агрегат КАК Оборудование,
	|	втАгрегаты.ТекущийПробег КАК Пробег,
	|	втАгрегаты.ТекущиеМоточасы КАК Моточасы
	|ИЗ
	|	втАгрегаты КАК втАгрегаты
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|// Текущие склады и агрегаты, и те которые были в документе
	|// до его изменения
	|ВЫБРАТЬ
	|	втАгрегатыТ.Агрегат КАК Агрегат,
	|	втАгрегатыТ.Склад КАК Склад,
	|	1 КАК СтрокаДокумента
	|ПОМЕСТИТЬ втКонтрольАгрегатов
	|ИЗ
	|	втАгрегаты КАК втАгрегатыТ
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втДопОборудованиеТ.ДополнительноеОборудование КАК Агрегат,
	|	втДопОборудованиеТ.Склад КАК Склад,
	|	1 КАК СтрокаДокумента
	|ИЗ
	|	втДопОборудование КАК втДопОборудованиеТ
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упАгрегатыНаСкладах.Агрегат КАК Агрегат,
	|	упАгрегатыНаСкладах.Склад КАК Склад,
	|	0 КАК СтрокаДокумента
	|ИЗ
	|	РегистрНакопления.упАгрегатыНаСкладах КАК упАгрегатыНаСкладах
	|ГДЕ упАгрегатыНаСкладах.Регистратор = &Ссылка
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКонтрольАгрегатовТ.Агрегат КАК Агрегат,
	|	втКонтрольАгрегатовТ.Склад КАК Склад,
	|	СУММА(втКонтрольАгрегатовТ.СтрокаДокумента) КАК СтрокаДокумента
	|ИЗ
	|	втКонтрольАгрегатов КАК втКонтрольАгрегатовТ
	|СГРУППИРОВАТЬ ПО
	|	втКонтрольАгрегатовТ.Агрегат,
	|	втКонтрольАгрегатовТ.Склад
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАгрегатыТ.Агрегат КАК Агрегат,
	|	втАгрегатыТ.Склад КАК Склад
	|ИЗ
	|	втАгрегаты КАК втАгрегатыТ
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втДопОборудованиеТ.ДополнительноеОборудование КАК Агрегат,
	|	втДопОборудованиеТ.Склад КАК Склад
	|ИЗ
	|	втДопОборудование КАК втДопОборудованиеТ
	|";


	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Валюта", упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Агрегаты", Агрегаты.Выгрузить(,"Агрегат, ПробегКм, ПробегМотоЧасы, Валюта, Сумма, Пробег, Моточасы"));
	Запрос.УстановитьПараметр("ДополнительноеОборудование", ДополнительноеОборудование.Выгрузить(,"ДополнительноеОборудование, ПробегКм, ПробегМотоЧасы, Валюта, Сумма, Пробег, Моточасы"));
	
	Результат = Запрос.ВыполнитьПакет();			   
	
	ИндексВыработкаАгрегатов = 2;
	ИндексПробег = 3;
	ИндексКонтрольАгрегатов = 5; 
	ИндексАгрегаты = 6; 
	
	ДополнительныеСвойства.Вставить("ОстаткиАгрегатов", Результат[ИндексВыработкаАгрегатов].Выгрузить());
	ДополнительныеСвойства.Вставить("ВыработкаАгрегатов", Результат[ИндексВыработкаАгрегатов].Выгрузить());
	ДополнительныеСвойства.Вставить("КонтрольАгрегатов", Результат[ИндексКонтрольАгрегатов].Выгрузить());// Контролируются отрицательные остатки на складах.
	ДополнительныеСвойства.Вставить("Пробег", Результат[ИндексПробег].Выгрузить());
	ДополнительныеСвойства.Вставить("Агрегаты", Результат[ИндексАгрегаты].Выгрузить());// Контролируется что на складе в момент принятия агрегата еще не было
																	   // и он не был установлен на ТС.
	
КонецПроцедуры

// Процедура - выполнить контроль агрегатов.
//
Процедура ВыполнитьКонтрольАгрегатовИДопОборудования(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Агрегаты и доп. оборудование из документа
	|ВЫБРАТЬ
	|	тбАгрегаты.Склад КАК Склад,
	|	тбАгрегаты.Агрегат КАК Агрегат
	|ПОМЕСТИТЬ втАгрегаты
	|ИЗ
	|	&Агрегаты КАК тбАгрегаты
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Остатки агрегатов и доп. обор. на складах
	|ВЫБРАТЬ
	|	втАгрегаты.Агрегат КАК Агрегат,
	|	втАгрегаты.Склад КАК СкладДокумента,
	|	упАгрегатыНаСкладахОстатки.Склад КАК СкладАгрегата,
	|	СУММА(упАгрегатыНаСкладахОстатки.КоличествоОстаток) КАК Количество
	|ПОМЕСТИТЬ втСклады
	|ИЗ
	|	втАгрегаты КАК втАгрегаты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|		,
	|		Агрегат В (
	|			ВЫБРАТЬ
	|				втАгрегаты.Агрегат КАК Агрегат
	|			ИЗ
	|				втАгрегаты КАК втАгрегаты)) КАК упАгрегатыНаСкладахОстатки
	|	ПО втАгрегаты.Агрегат = упАгрегатыНаСкладахОстатки.Агрегат
	|СГРУППИРОВАТЬ ПО
	|	втАгрегаты.Агрегат,
	|	втАгрегаты.Склад,
	|	упАгрегатыНаСкладахОстатки.Склад
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Установленные на ТС агрегаты и доп. оборудование
	|ВЫБРАТЬ
	|	втАгрегаты.Агрегат КАК Агрегат,
	|	АгрегатыТССП.ТранспортноеСредство КАК ТранспортноеСредство
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втАгрегаты КАК втАгрегаты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		Агрегат В (
	|			ВЫБРАТЬ
	|				втАгрегаты.Агрегат КАК Агрегат
	|			ИЗ
	|				втАгрегаты КАК втАгрегаты)) КАК АгрегатыТССП
	|	ПО втАгрегаты.Агрегат = АгрегатыТССП.Агрегат
	|ГДЕ АгрегатыТССП.Операция <> ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСкладыТ.Агрегат КАК Агрегат,
	|	втСкладыТ.СкладАгрегата КАК Склад
	|ИЗ
	|	втСклады КАК втСкладыТ
	|ГДЕ ЛОЖЬ
	|	ИЛИ втСкладыТ.Количество > 1
	|	ИЛИ втСкладыТ.СкладАгрегата <> втСкладыТ.СкладДокумента
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втТСТ.Агрегат КАК Агрегат,
	|	втТСТ.ТранспортноеСредство КАК Склад
	|ИЗ
	|	втТС КАК втТСТ
	|";
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Граница);
	Запрос.УстановитьПараметр("Агрегаты", ДополнительныеСвойства.Агрегаты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.Склад) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
			     ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", установлен на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.Склад));	
			Иначе	
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Доп.оборудование: ""%1"", установлено на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.Склад));		
			КонецЕсли;
		Иначе
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", находится на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			Иначе
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Доп.оборудование: ""%1"", находится на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			КонецЕсли;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
