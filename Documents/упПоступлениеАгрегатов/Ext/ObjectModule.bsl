#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(Агрегаты, "Агрегаты", Ссылка, Отказ);
	упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ДополнительноеОборудование, "ДополнительноеОборудование", Ссылка, Отказ);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Если Не Отказ Тогда
			
			мсвОборудование = Агрегаты.ВыгрузитьКолонку("Агрегат");
			Для каждого Строка Из ДополнительноеОборудование Цикл
				мсвОборудование.Добавить(Строка.ДополнительноеОборудование);
			КонецЦикла;
			
			ДополнительныеСвойства.Вставить("Оборудование", мсвОборудование);
			
			Если РежимПроведения = РежимПроведенияДокумента.Неоперативный Тогда
				упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, ДополнительныеСвойства.Оборудование, Дата, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		// Формирование таблицы текущих движений РН.упАгрегатыНаСкладах, используемой при анализе отрицательных остатков. 
		// Нужна в основном для проверки остатков по агрегатам/доп. оборудованию, которые были удалены из документа.
		Если Не Отказ Тогда
			Документы.упПоступлениеАгрегатов.ИнициализироватьДанныеДокумента(Дата, Агрегаты, ДополнительноеОборудование, Ссылка, Склад, ДополнительныеСвойства);		
		КонецЕсли;	
		
	КонецЕсли;

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если Не Отказ Тогда
			ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
			ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
			упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проверка существования более поздних документов, изменяющих состояние агрегатов и доп. оборудования.
	// Дублирована здесь из-за оперативного проведения документа, перед записью дата еще не известна.
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, ДополнительныеСвойства.Оборудование, Дата, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упАгрегатыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.КонтрольАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упАгрегатыТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.КонтрольАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияОборудования");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.КонтрольАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаАгрегатов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.КонтрольАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНаработкаОборудования");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.КонтрольАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "Агрегат");
	 		
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Реквизиты = Новый Структура;
	Граница = Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая);
	Реквизиты.Вставить("Граница", Граница);
	Реквизиты.Вставить("Дата", Дата);
	Реквизиты.Вставить("Организация", Организация);
	Реквизиты.Вставить("Подразделение", Подразделение);
	Реквизиты.Вставить("НаправлениеДеятельности", НаправлениеДеятельности);
	
	// Формируются остатки агрегатов на складах.
	упУправлениеЗапасами.СформироватьДвиженияАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
			
	// Фиксация остаточной стоимости агрегатов.
	упВыработкаРесурсов.СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	// Прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	// Пробег.
	упУправлениеЗапасами.СформироватьДвиженияНаработкаОборудования(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	// СостоянияОборудования
	упУчетТранспортныхСредств.СформироватьДвиженияСостоянияОборудованияПоступлениеАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	Движения.Записать();
	
	упКонтрольОстатков.КонтрольОтрицательныхОстатковАгрегатовНаСкладах(ДополнительныеСвойства, Ссылка, Отказ);
	
	Документы.упПоступлениеАгрегатов.ВыполнитьКонтрольАгрегатовИДопОборудования(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		упКонтрольОстатков.КонтрольОтрицательныхОстатковАгрегатовНаСкладах(ДополнительныеСвойства, Ссылка, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
