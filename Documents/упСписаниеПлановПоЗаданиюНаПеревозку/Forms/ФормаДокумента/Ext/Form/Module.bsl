#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
			
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	Если Истина
		И Не ТолькоПросмотр
		И ТекущийОбъект.Проведен 
	Тогда
		ТолькоПросмотр = упЗаданиеНаПеревозку.ЗапрещеноРедактироватьПланПоЗаданиямНаПеревозкуГруза(ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере(Отказ = Ложь)
		
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			Объект.ЧасовойПояс = упСервисныеФункции.ЧасовойПоясВидаПеревозки();	
		КонецЕсли;
	КонецЕсли;	
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	ЗаблокироватьЭлементы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	ДоступныеСтатусыЗаданийДляОснования = Документы.упСписаниеПлановПоЗаданиюНаПеревозку.ДоступныеСтатусыЗаданийДляОснования();
	ПриСозданииПриЧтенииНаСервере(Отказ);
КонецПроцедуры	

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи <> ПредопределенноеЗначение("РежимЗаписиДокумента.Запись") Тогда
		Если ИзменилсяСтатусЗадания Тогда
			Оповестить("СменаСтатуса", , Объект.ЗаданиеНаПеревозку);
		КонецЕсли;
		
		Если ИзменилсяСтатусЗаявки Тогда
			Оповестить("СменаСтатуса", , ЗаявкаНаПеревозкуГруза);
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ИзменилсяСтатусЗаявки = Ложь;
	ИзменилсяСтатусЗадания = Ложь;
	
	мсвЗаявкиНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаявкиНаПеревозкуГруза) Тогда
		Если мсвЗаявкиНаПеревозкуГруза.Количество() > 0 Тогда
			ИзменилсяСтатусЗадания =  Истина;
			ЗаявкаНаПеревозкуГруза = мсвЗаявкиНаПеревозкуГруза[0];
		КонецЕсли;
	КонецЕсли;
	мсвЗаданияНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаданияНаПеревозкуГруза) Тогда
		ИзменилсяСтатусЗадания = мсвЗаданияНаПеревозкуГруза.Количество() > 0
	КонецЕсли;
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеНаПеревозкуГрузаПриИзменении(Элемент)
	ЗаданиеНаПеревозкуГрузаПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаблокироватьЭлементы()
		
	Если Истина
		И НЕ ТолькоПросмотр
		И ВедетсяРаботаВРазныхЧасовыхПоясах
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаданиеНаПеревозкуГрузаПриИзмененииНаСервере()
	Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеНаПеревозку, "ЧасовойПояс");
		Если НЕ ЧасовойПоясОснования = Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясОснования;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция СтруктураОтбораЗадания()
	
	Результат = Новый Структура();
	Результат.Вставить("Статус", ДоступныеСтатусыЗаданийДляОснования);
	СписокПараметровВводаИнформации = Новый СписокЗначений;
	СписокПараметровВводаИнформации.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку"));
	СписокПараметровВводаИнформации.Добавить(ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса"));
	Результат.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах", СписокПараметровВводаИнформации);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаданиеНаПеревозкуГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("Отбор", СтруктураОтбораЗадания());
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элемент);	
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


#КонецОбласти
