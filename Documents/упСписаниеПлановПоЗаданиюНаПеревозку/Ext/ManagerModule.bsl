#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формируется или открывается существующий документ "Списание планов по заданию на перевозку" на основании задания на перевозку 
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текстовое представлениеошибки.
//  Отказ  - Булево - Флаг что произошел отказ создания движения.
//
// Возвращаемое значение:
//   ДокументСсылка   - Ссылка на документ.
//
Функция СформироватьСписаниеПлановПоЗаданиюНаПеревозку(ЗаданиеНаПеревозку, ТекстОшибки = Неопределено, Отказ) Экспорт
	
	Результат = Неопределено;
	
	СтатусЗадания = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ЗаданиеНаПеревозку);
	
	Если Ложь
		Или СтатусЗадания = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое
		Или СтатусЗадания = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполнено
		Или СтатусЗадания = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
	Тогда
		Отказ = Истина;
		ТекстОшибки = СтрШаблон(Нстр("ru = 'Запрещено формировать списание планов по заданию в статусе ""%1""'"), СтатусЗадания);
		ТекстОшибкиДляЖР = ТекстОшибки;
		ЗаписьЖурналаРегистрации(упЗаданиеНаПеревозку.СобытиеОшибкаСписаниеПлановПоЗаданиюНаПеревозку(НСтр("ru = 'Добавление'")), УровеньЖурналаРегистрации.Предупреждение, ,ЗаданиеНаПеревозку, ТекстОшибкиДляЖР, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецЕсли;
	
	Если Не Отказ Тогда
		НовыйДокумент = Документы.упСписаниеПлановПоЗаданиюНаПеревозку.СоздатьДокумент();
		НовыйДокумент.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		НовыйДокумент.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		НовыйДокумент.Дата = ТекущаяДатаСеанса();
		НовыйДокумент.ЗаданиеНаПеревозку = ЗаданиеНаПеревозку;
		НовыйДокумент.Заполнить(ЗаданиеНаПеревозку);
		НовыйДокумент.ДополнительныеСвойства.Вставить("ВыводитьСообщенияПользователю", Ложь);
		Попытка
			НовыйДокумент.Записать();
			Результат = НовыйДокумент.Ссылка;
		Исключение
			ТекстОшибкиДляЖР = "";
			Отказ = Истина;
			упСервисныеФункции.ПредставленияОписанияОшибки(НовыйДокумент, ТекстОшибки, ТекстОшибкиДляЖР);
			ЗаписьЖурналаРегистрации(упЗаданиеНаПеревозку.СобытиеОшибкаСписаниеПлановПоЗаданиюНаПеревозку(НСтр("ru = 'Добавление'")), УровеньЖурналаРегистрации.Ошибка, ,ЗаданиеНаПеревозку, ТекстОшибкиДляЖР, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // СформироватьСписаниеПлановПоЗаданиюНаПеревозку()

#КонецОбласти

Функция ДоступныеСтатусыЗаданийДляОснования() Экспорт
	
	Результат = Новый СписокЗначений;
	Результат.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
	Результат.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс);
	Результат.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению);
	Результат.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполняется);
	Результат.Добавить(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВПути);
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли