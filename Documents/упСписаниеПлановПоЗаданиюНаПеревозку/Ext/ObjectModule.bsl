#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВыводитьСообщенияПользователю = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ВыводитьСообщенияПользователю", Неопределено);
	Если ВыводитьСообщенияПользователю = Неопределено Тогда
		ВыводитьСообщенияПользователю = Истина;
		ДополнительныеСвойства.Вставить("ВыводитьСообщенияПользователю", ВыводитьСообщенияПользователю);
	КонецЕсли;
	
	ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаданиеНаПеревозку, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах");
	ФормироватьПоГрузовымМестам = упУправленияЗаявками.ФормироватьПоГрузовымМестамПоПараметруВводаИнформации(ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию);
	ДополнительныеСвойства.Вставить("ФормироватьПоГрузовымМестам", ФормироватьПоГрузовымМестам);
	
	Если Не ФормироватьПоГрузовымМестам Тогда
		
		ТекстОшибки = НСтр("ru = 'Параметр ввода информации о грузовых местах, вида перевозки задания, должен быть ""Вводится в заявке на перевозку"" или ""Вводится в задании до начала планирования рейса"".'");
		ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстОшибки);
		Если ВыводитьСообщенияПользователю Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
		Иначе	
			Отказ = Истина
		КонецЕсли;
		ЗаписьЖурналаРегистрации(упЗаданиеНаПеревозку.СобытиеОшибкаСписаниеПлановПоЗаданиюНаПеревозку("ПередЗаписью"), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		Отказ = упЗаданиеНаПеревозку.ЗапрещеноРедактироватьПланПоЗаданиямНаПеревозкуГруза(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Задание", ЗаданиеНаПеревозку);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упСостояниеПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Задание", ЗаданиеНаПеревозку);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПланПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Задание", ЗаданиеНаПеревозку);
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ЗаданиеНаПеревозку);

	ТекстОшибки = "";
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата", Дата);
	РеквизитыОбъекта.Вставить("ЗаданиеНаПеревозку", ЗаданиеНаПеревозку);
	РеквизитыОбъекта.Вставить("ФормироватьПоГрузовымМестам", ДополнительныеСвойства.ФормироватьПоГрузовымМестам);
	
	ДополнительныеСвойства.Вставить("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	ДополнительныеСвойства.Вставить("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус" , Новый Массив); 
	
	упЗаданиеНаПеревозку.СформироватьТаблицыДвиженийДляСписанияПлановПоЗаданиям(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
	
	упПрохождениеТочки.СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются движения по состояниям задания.
	упПрохождениеТочки.СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется план по заданиям.
	упКорректировкаМаршрута.СформироватьДвижениеПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
		
	// Устанавливаются статусы заданий.
	ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗадания", Истина);
	упЗаданиеНаПеревозку.СформироватьДвиженияСтатусыЗаданий(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// Устанавливаются статусы заявок.
	ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗаявки", Истина);
	упУправленияЗаявками.СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.ПроконтролироватьСостояниеЗадания(Ссылка, Отказ);
	
	ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Истина);
	упЗаданиеНаПеревозку.ПроконтролироватьЗаданияКПланированию(Ссылка, РеквизитыОбъекта.ФормироватьПоГрузовымМестам, Отказ, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		ЗаданиеНаПеревозку = ДанныеЗаполнения;
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			ЧасовойПояс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "ЧасовойПояс");;	
		КонецЕсли;  
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
