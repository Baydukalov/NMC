
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;	
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
			
	КонецЕсли;	
			
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
			
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		УстановитьВалютуРеглУчета();	
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Документ.упЗаявкаНаПеревозкуГруза.Форма.ФормаВыбора" Тогда  
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
			Для каждого Заявка Из ВыбранноеЗначение Цикл
				Если Объект.ЗаявкиНаПеревозкуГруза.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", Заявка)).Количество() = 0 Тогда
					НоваяСтрока = Объект.ЗаявкиНаПеревозкуГруза.Добавить();
					НоваяСтрока.ЗаявкаНаПеревозкуГруза = Заявка;
				КонецЕсли;
			КонецЦикла;		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
		// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если ИмяСобытия = "СформироватьСчетНаОплатуЗаказчику" Тогда
		ПараметрыФормы = Новый Структура("Основание", Параметр);
		ОткрытьФорму("Документ.упСчетНаОплатуЗаказчику.Форма.ФормаДокумента", ПараметрыФормы);	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ЗаявкаНаПеревозкуГрузаПриИзменении(Элемент)
	Если ВедетсяВалютныйУчет Тогда
		УстановитьВалютуРеглУчета();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СпособВводаФактическихДоходовПриИзменении(Элемент)
	Если Объект.СпособВводаФактическихДоходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихДоходов.ДоходыПоОднойЗаявке") Тогда
		Если Объект.Доходы.Количество() > 0 
			ИЛИ Объект.ЗаявкиНаПеревозкуГруза.Количество() > 0 Тогда
				ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ВопросОбОчисткеТабличныхЧастей", ЭтаФорма);
				ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблицы ""Доходы"" и ""Заявки""  будут очищены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	Иначе	
		Объект.ЗаявкаНаПеревозкуГруза = ПредопределенноеЗначение("Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка");
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.Доходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;

		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуДоходы();
	РассчитатьПроцентСкидкиИтого();
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуДоходы();
	РассчитатьПроцентСкидкиИтого();
КонецПроцедуры

&НаКлиенте
Процедура ДоходыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДоходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСкидкаПроцентПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСкидкаСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДоходыСтатьяДоходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.Доходы.ТекущиеДанные;
	СтатьяДоходов	= ТекущаяСтрока.СтатьяДоходов;
	Если ЗначениеЗаполнено(СтатьяДоходов) Тогда
		ТекущаяСтрока.СтавкаНДС = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяДоходов, "СтавкаНДС");
		ПересчитатьСтроку(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикПриИзменении(Элемент)
	
	Структура = ПолучитьОсновногоКонтрагентаИСоглашение(Объект.Заказчик,Объект.Организация);
	Объект.КонтрагентЗаказчика = Структура.КонтрагентЗаказчика;
	Объект.Соглашение = Структура.Соглашение;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСтатьямиДоходовПоЗаявке(Команда)
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьСтатьямиДоходовКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ПеревыставитьРасходыПоРейсу(Команда)
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьРасходамиКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДоходы(Команда)
	РассчитатьДоходыНаСервере();
	РассчитатьИтоговуюСуммуДоходы();
	РассчитатьПроцентСкидкиИтого();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДоходыСРасшифровкой(Команда)
	мсвПриемников = РассчитатьДоходыСРасшифровкойНаСервере();
	РассчитатьИтоговуюСуммуДоходы();
	РассчитатьПроцентСкидкиИтого();
	Для каждого Приемник Из мсвПриемников Цикл
		Приемник.Приемник.Показать("Расшифровка расчетов");	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПодборЗаявки(Команда)
	ПараметрыПодбора = Новый Структура("Отбор, МножественныйВыбор",Новый Структура("Организация, Заказчик, Соглашение", Объект.Организация, Объект.Заказчик, Объект.Соглашение), Истина);
	ОткрытьФорму("Документ.упЗаявкаНаПеревозкуГруза.ФормаВыбора", ПараметрыПодбора, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
// Процедура - заполнить статьи доходов.
//
Процедура ЗаполнитьСтатьямиДоходовКлиент(Результат, ДополнительныеПараметры) Экспорт
	Если Объект.Доходы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ВопросОПерезаполненииПлановыхДоходов", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Доходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		// Просто заполнение
		ЗаполнитьСтатьямиДоходовПоЗаявкеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура - заполнить расходами.
//
Процедура ЗаполнитьРасходамиКлиент(Результат, ДополнительныеПараметры) Экспорт
	Если Объект.Доходы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ВопросОПерезаполненииПлановыхДоходовРасходами", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Доходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		// Просто заполнение
		ЗаполнитьРасходамиПоРейсамНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ВопросОПерезаполненииПлановыхДоходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьСтатьямиДоходовПоЗаявкеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ВопросОПерезаполненииПлановыхДоходовРасходами(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьРасходамиПоРейсамНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ВопросОбОчисткеТабличныхЧастей(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Доходы.Очистить();
		Объект.ЗаявкиНаПеревозкуГруза.Очистить();
	Иначе
		Объект.СпособВводаФактическихДоходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам");
	КонецЕсли;
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "Доходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуРеглУчета()
	Если ЗначениеЗаполнено(Объект.ЗаявкаНаПеревозкуГруза) Тогда
		ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект.ЗаявкаНаПеревозкуГруза);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.Доходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка, Соглашение", Объект.Ссылка, ПредопределенноеЗначение("Справочник.упСоглашенияСКлиентами.ПустаяСсылка"));
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоля(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуДоходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаДоходы",			"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыСкидка",		"СкидкаСумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыНДС",		"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Доходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаКлиенте
Процедура РассчитатьПроцентСкидкиИтого()
	ИтогПроцентСкидки  =  0;
	Если НЕ Объект.СуммаДоходыНДС = 0 Тогда
		ИтогПроцентСкидки = Объект.СуммаДоходыСкидка / Объект.СуммаДоходыНДС *  100;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатьямиДоходовПоЗаявкеНаСервере()
	
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ЗаполнитьСтатьямиДоходовПоЗаявкам();
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасходамиПоРейсамНаСервере()
	
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ЗаполнитьРасходамиПоРейсам(ВедетсяВалютныйУчет, ВалютаУпрУчета);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьДоходыНаСервере()
	ВходныеПараметры = Новый Структура();
	упТарификацияВызовСервера.Рассчитать(Объект, ВходныеПараметры);
КонецПроцедуры

&НаСервере
Функция РассчитатьДоходыСРасшифровкойНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;

КонецФункции

&НаСервере
Процедура УстановитьВидимость()
	Элементы.ЗаявкаНаПеревозкуГруза.Видимость = НЕ Объект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам;
	Элементы.ДоходыЗаявкаНаПеревозкуГруза.Видимость = Объект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам;
	Элементы.ЗаявкиНаПеревозкуГруза.Видимость = Объект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагентаИСоглашение(Партнер,Организация)
	
	Результат = Новый Структура;
	Результат.Вставить("КонтрагентЗаказчика",Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер));
	
	Соглашение = Справочники.упСоглашенияСКлиентами.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упСоглашенияСКлиентами.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.упСоглашенияСКлиентами КАК упСоглашенияСКлиентами
	|ГДЕ
	|	НЕ упСоглашенияСКлиентами.ПометкаУдаления
	|	И упСоглашенияСКлиентами.Владелец = &Владелец
	|	И ВЫБОР
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ упСоглашенияСКлиентами.Организация = &Организация
	|		КОНЕЦ";
	Запрос.УстановитьПараметр("Владелец", Партнер);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Соглашение = Выборка.Соглашение;
	Иначе
		Соглашение = Справочники.упСоглашенияСКлиентами.СоглашениеПоУмолчанию(Партнер);
	КонецЕсли;
	
	Результат.Вставить("Соглашение",Соглашение);
	
	Возврат Результат;
	
КонецФункции // ПолучитьОсновногоКонтрагентаИСоглашение()

#КонецОбласти