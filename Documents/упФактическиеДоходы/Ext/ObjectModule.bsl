#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура обработчик события ОбработкаПроведения объекта.
//
// Параметры:
//  Отказ  - Булево - Выполнить отказ.
//  РежимПроведения  - РежимПроведенияДокумента - Определяет набор режимов проведения документа.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упДоходы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура("Дата, 
								|Доходы, 
								|ЗаявкаНаПеревозкуГруза, 
								|Подразделение,
								|НаправлениеДеятельности,
								|СпособВводаФактическихДоходов"); 
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);
		
	упРаспределениеРасходов.СформироватьДоходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьДвиженияФактическиеДоходыРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда
		текИндекс = ПроверяемыеРеквизиты.Найти("ЗаявкаНаПеревозкуГруза");	
		Если НЕ текИндекс = Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(текИндекс);
		КонецЕсли;
	Иначе
		текИндекс = ПроверяемыеРеквизиты.Найти("Доходы.ЗаявкаНаПеревозкуГруза");	
		Если НЕ текИндекс = Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(текИндекс);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
		текИндекс = ПроверяемыеРеквизиты.Найти("Доходы.Валюта");	
		Если НЕ текИндекс = Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(текИндекс);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоОднойЗаявке Тогда
		ЗаявкиНаПеревозкуГруза.Очистить();
		НоваяСтрока = ЗаявкиНаПеревозкуГруза.Добавить();
		НоваяСтрока.ЗаявкаНаПеревозкуГруза = ЗаявкаНаПеревозкуГруза;
	КонецЕсли; 
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
		
		// Расчеты с заказчиками
		ВестиУчетРасчетовСЗаказчиками		= ПолучитьФункциональнуюОпцию("упВестиУчетРасчетовСЗаказчиками");
		ДополнительныеСвойства.Вставить("ВестиУчетРасчетовСЗаказчиками", 	 ВестиУчетРасчетовСЗаказчиками);
		АналитическийРазрезПоПартнерам		= Неопределено;
		Если ВестиУчетРасчетовСЗаказчиками Тогда
			АналитическийРазрезПоПартнерам = упУправленияЗаявками.ПодобратьАналитическийРазрезПоПартнерам(Организация, Заказчик, КонтрагентЗаказчика,Соглашение, Отказ);
			Если НЕ Отказ Тогда
				ДополнительныеСвойства.Вставить("АналитическийРазрезПоПартнерам", 	 АналитическийРазрезПоПартнерам)
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - дозаполнить ТЧ "Доходы".
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьСтатьямиДоходовПоЗаявкам() Экспорт
	
	Запрос = Новый Запрос;
	Если СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда
		мсвЗаявкиНаПеревозкуГруза = ЗаявкиНаПеревозкуГруза.ВыгрузитьКолонку("ЗаявкаНаПеревозкуГруза");	
		Запрос.Текст = "ВЫБРАТЬ
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтатьяДоходов,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Валюта,
		               |	СУММА(упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Количество) КАК Количество,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтавкаНДС,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка КАК ЗаявкаНаПеревозкуГруза,
		               |	МАКСИМУМ(ЛОЖЬ) КАК СтрокаДобавленаНаОснованииРасхода
		               |ИЗ
		               |	Документ.упЗаявкаНаПеревозкуГруза.ПлановыеДоходы КАК упЗаявкаНаПеревозкуГрузаПлановыеДоходы
		               |ГДЕ
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка В(&мсвЗаявок)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтатьяДоходов,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Валюта,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтавкаНДС,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка";
		Запрос.УстановитьПараметр("мсвЗаявок", мсвЗаявкиНаПеревозкуГруза);			   
	Иначе	
		Запрос.Текст = "ВЫБРАТЬ
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтатьяДоходов,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Валюта,
		               |	СУММА(упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Количество) КАК Количество,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтавкаНДС,
		               |	МАКСИМУМ(ЛОЖЬ) КАК СтрокаДобавленаНаОснованииРасхода					   
		               |ИЗ
		               |	Документ.упЗаявкаНаПеревозкуГруза.ПлановыеДоходы КАК упЗаявкаНаПеревозкуГрузаПлановыеДоходы
		               |ГДЕ
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Ссылка = &ЗаявкаНаПеревозкуГруза
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтатьяДоходов,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.Валюта,
		               |	упЗаявкаНаПеревозкуГрузаПлановыеДоходы.СтавкаНДС";
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);		   			   
	КонецЕсли;
	
	мсвСтрокиДобавленныеНаОснованииРасхода = Доходы.НайтиСтроки(Новый Структура("СтрокаДобавленаНаОснованииРасхода", Ложь));
	
	Для каждого Строка Из мсвСтрокиДобавленныеНаОснованииРасхода Цикл
		Доходы.Удалить(Строка);
	КонецЦикла;
	
	текВыборка = Запрос.Выполнить().Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		НоваяСтрока = Доходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текВыборка);
	КонецЦикла;

КонецПроцедуры

// Процедура - дозаполнить ТЧ "Доходы".
//
// Параметры:
//  ВедетсяВалютныйУчет  - Булево - учет.
//  ВалютаУпрУчета  - СправочникСсылка - Валюта.
//
Процедура ЗаполнитьРасходамиПоРейсам(ВедетсяВалютныйУчет, ВалютаУпрУчета) Экспорт
	
	Запрос = Новый Запрос;
	Если СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам Тогда
		мсвЗаявкиНаПеревозкуГруза = ЗаявкиНаПеревозкуГруза.ВыгрузитьКолонку("ЗаявкаНаПеревозкуГруза");	
		Запрос.Текст = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза,
		|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|ГДЕ упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза В (&мсвЗаявкиНаПеревозкуГруза)
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	Рейс_ЗаданияТ.Задание КАК ЗаданиеНаПеревозку,
		|	Рейс_ЗаданияТ.Ссылка КАК Рейс
		|ПОМЕСТИТЬ втРейсы
		|ИЗ
		|	Документ.упРейс.Задания КАК Рейс_ЗаданияТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗаданияТ
		|	ПО Рейс_ЗаданияТ.Задание = втЗаданияТ.ЗаданиеНаПеревозкуГруза
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	КорректировкаРейса_ЗаданияТ.Задание КАК ЗаданиеНаПеревозку,
		|	КорректировкаРейса_ЗаданияТ.Ссылка.Рейс КАК Рейс
		|ИЗ
		|	Документ.упКорректировкаРейса.Задания КАК КорректировкаРейса_ЗаданияТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗаданияТ
		|	ПО КорректировкаРейса_ЗаданияТ.Задание = втЗаданияТ.ЗаданиеНаПеревозкуГруза
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРасходыОбороты.СтатьяРасходов КАК СтатьяДоходов,
		|	СУММА(ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18_118)
		|				ИЛИ упСтатьиДоходовРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС20_120)
		|				ИЛИ упСтатьиДоходовРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС10_110)
		|			ТОГДА упРасходыОбороты.СуммаФактСНДСОборот
		|		ИНАЧЕ упРасходыОбороты.СуммаФактБезНДСОборот
		|	КОНЕЦ) КАК Цена,
		|	упСтатьиДоходовРасходов.СтавкаНДС КАК СтавкаНДС,
		|	втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ИЗ
		|	РегистрНакопления.упРасходы.Обороты(
		|		,
		|		,
		|		Регистратор
		|		,
		|		(Рейс, ЗаданиеНаПеревозку) В (
		|			ВЫБРАТЬ
		|				втРейсы.Рейс КАК Рейс,
		|				втРейсы.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
		|			ИЗ
		|				втРейсы КАК втРейсы)) КАК упРасходыОбороты
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО упРасходыОбороты.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
		|	ПО упРасходыОбороты.ЗаданиеНаПеревозку = втЗадания.ЗаданиеНаПеревозкуГруза
		|ГДЕ упРасходыОбороты.Регистратор ССЫЛКА Документ.упФактическиеРасходы
		|СГРУППИРОВАТЬ ПО
		|	упРасходыОбороты.СтатьяРасходов,
		|	упСтатьиДоходовРасходов.СтавкаНДС,
		|	втЗадания.ЗаявкаНаПеревозкуГруза
		|";

		Запрос.УстановитьПараметр("мсвЗаявкиНаПеревозкуГруза", мсвЗаявкиНаПеревозкуГруза);			   
		
	Иначе	
				
		Запрос.Текст = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза,
		|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|ГДЕ упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	Рейс_ЗаданияТ.Задание КАК ЗаданиеНаПеревозку,
		|	Рейс_ЗаданияТ.Ссылка КАК Рейс
		|ПОМЕСТИТЬ втРейсы
		|ИЗ
		|	Документ.упРейс.Задания КАК Рейс_ЗаданияТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗаданияТ
		|	ПО Рейс_ЗаданияТ.Задание = втЗаданияТ.ЗаданиеНаПеревозкуГруза
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	КорректировкаРейса_ЗаданияТ.Задание КАК ЗаданиеНаПеревозку,
		|	КорректировкаРейса_ЗаданияТ.Ссылка.Рейс КАК Рейс
		|ИЗ
		|	Документ.упКорректировкаРейса.Задания КАК КорректировкаРейса_ЗаданияТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗаданияТ
		|	ПО КорректировкаРейса_ЗаданияТ.Задание = втЗаданияТ.ЗаданиеНаПеревозкуГруза
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРасходыОбороты.СтатьяРасходов КАК СтатьяДоходов,
		|	СУММА(ВЫБОР
		|		КОГДА упСтатьиДоходовРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18_118)
		|				ИЛИ упСтатьиДоходовРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС20_120)
		|				ИЛИ упСтатьиДоходовРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС10_110)
		|			ТОГДА упРасходыОбороты.СуммаФактСНДСОборот
		|		ИНАЧЕ упРасходыОбороты.СуммаФактБезНДСОборот
		|	КОНЕЦ) КАК Цена,
		|	упСтатьиДоходовРасходов.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрНакопления.упРасходы.Обороты(
		|		,
		|		,
		|		Регистратор,
		|		(Рейс, ЗаданиеНаПеревозку) В (
		|				ВЫБРАТЬ
		|				втРейсы.Рейс КАК Рейс,
		|				втРейсы.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
		|			ИЗ
		|				втРейсы КАК втРейсы)) КАК упРасходыОбороты
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
		|	ПО упРасходыОбороты.СтатьяРасходов = упСтатьиДоходовРасходов.Ссылка
		|ГДЕ упРасходыОбороты.Регистратор ССЫЛКА Документ.упФактическиеРасходы
		|СГРУППИРОВАТЬ ПО
		|	упРасходыОбороты.СтатьяРасходов,
		|	упСтатьиДоходовРасходов.СтавкаНДС
		|";

		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);		   
	КонецЕсли;
	
	мсвСтрокиДобавленныеНаОснованииРасхода = Доходы.НайтиСтроки(Новый Структура("СтрокаДобавленаНаОснованииРасхода", Истина));
	
	Для каждого Строка Из мсвСтрокиДобавленныеНаОснованииРасхода Цикл
		Доходы.Удалить(Строка);
	КонецЦикла;
	
	текВыборка = Запрос.Выполнить().Выбрать();
	СтруктураДанные = Новый Структура("Ссылка, Соглашение", Ссылка, ПредопределенноеЗначение("Справочник.упСоглашенияСКлиентами.ПустаяСсылка"));
	Пока текВыборка.Следующий() Цикл
		НоваяСтрока = Доходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текВыборка);
		НоваяСтрока.Количество = 1;
		НоваяСтрока.СтрокаДобавленаНаОснованииРасхода = Истина;
		Если ВедетсяВалютныйУчет Тогда
			НоваяСтрока.Валюта = ВалютаУпрУчета;	
		Иначе
			НоваяСтрока.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуПриИзмененииЗначения(НоваяСтрока,"Цена",СтруктураДанные);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли


