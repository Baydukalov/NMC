#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти         

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	// Отбор свободных ТС, которые не являются прицепами.
	СписокСвободныхТС = ПолучитьСписокСвободныхТСНаСервереБезКонтекста(Объект.ДатаОперации);
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", СписокСвободныхТС));
	ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ТранспортноеСредствоНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ПользовательскоеСообщение = "";
	ТС = Объект.ТранспортноеСредство;
	Объект.ТранспортноеСредство = ?(ЗначениеЗаполнено(ТС),ПроверитьТСПриИзмененииНаСервере(ТС, Объект.ДатаОперации, ПользовательскоеСообщение), ТС);
	Если ЗначениеЗаполнено(ПользовательскоеСообщение) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ПользовательскоеСообщение;
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
	
// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать	

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
// Процедура завершения после закрытия формы выбора тарнспортного средства.
//
Процедура ТранспортноеСредствоНачалоВыбораЗавершение(Результат, ДополнительныеПараметры)  Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.ТранспортноеСредство = Результат;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте 
// Процедура завершения после закрытия формы выбора прицепа.
//
Процедура ПрицепНачалоВыбораЗавершение(Результат, ДополнительныеПараметры)  Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.Прицеп = Результат;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокСвободныхТСНаСервереБезКонтекста(ДатаОперации)
	мсвСвободныеТС = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТранспортныеСредства.Ссылка КАК ТС,
	               |	упСостоянияТС.СостояниеТС КАК СостояниеТС
	               |ПОМЕСТИТЬ СписокТС
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСостоянияТранспортныхСредств.СрезПоследних(
	               |				&Дата,
	               |				СостояниеТС = ЗНАЧЕНИЕ(Справочник.упСостоянияТС.Свободно)
	               |					И НЕ ТранспортноеСредство.Прицеп) КАК упСостоянияТС
	               |		ПО упТранспортныеСредства.Ссылка = упСостоянияТС.ТранспортноеСредство
	               |ГДЕ
	               |	упТранспортныеСредства.ПометкаУдаления = ЛОЖЬ
	               |	И НЕ упТранспортныеСредства.Прицеп
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокТС.ТС
	               |ИЗ
	               |	СписокТС КАК СписокТС
	               |ГДЕ
	               |	(СписокТС.СостояниеТС = ЗНАЧЕНИЕ(Справочник.упСостоянияТС.Свободно)
	               |			ИЛИ СписокТС.СостояниеТС ЕСТЬ NULL )";
	Запрос.УстановитьПараметр("Дата",Новый Граница(ДатаОперации, ВидГраницы.Включая));				   
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			мсвСвободныеТС.Добавить(Выборка.ТС);
		КонецЦикла;
	КонецЕсли;
	Возврат мсвСвободныеТС;
КонецФункции // ПолучитьСписокСвободныхТСНаСервереБезКонтекста()

&НаСервереБезКонтекста
Функция ПроверитьТСПриИзмененииНаСервере(ТС,ДатаОперации, ПользовательскоеСообщение)
	Если ТС.Прицеп Тогда
		ПользовательскоеСообщение = "ТС " + ТС.Наименование + " является прицепом."; 
		Возврат Справочники.упТранспортныеСредства.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСостоянияТС.СостояниеТС КАК СостояниеТС
	               |ИЗ
	               |	РегистрСведений.упСостоянияТранспортныхСредств.СрезПоследних(&Дата, ТранспортноеСредство = &ТС) КАК упСостоянияТС";
	Запрос.УстановитьПараметр("Дата",Новый Граница(ДатаОперации, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ТС", ТС);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ТС;
	Иначе
		выборка = Результат.Выбрать();
		выборка.Следующий();
		Если выборка.СостояниеТС = Справочники.упСостоянияТС.Свободно Тогда
			Возврат ТС;
		Иначе
			ПользовательскоеСообщение = "ТС " + ТС.Наименование + " не свободно. Операция не выполнена.";  
			Возврат Справочники.упТранспортныеСредства.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли; 

КонецФункции

#КонецОбласти
