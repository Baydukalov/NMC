#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();

		ЭтоКопирование  = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
		
		Если НЕ ЭтоКопирование Тогда
			Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();	
			КонецЕсли;
		КонецЕсли;
		
		Если Истина
			И ЭтаФорма.ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) 
		Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
		
		Если Истина
			И ЭтаФорма.ВедетсяВалютныйУчет 
			И НЕ ЗначениеЗаполнено(Объект.Валюта) 
		Тогда
			Объект.Валюта = ЭтаФорма.ВалютаУпрУчета;	
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаблокироватьЭлементы();
				
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяВалютныйУчет	          = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
    
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.Свойства
    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	ЗаблокироватьЭлементы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтатьяРасходовПриИзменении(Элемент)
	ОчиститьТабличныеЧасти();
	сткРезультат = Новый Структура("АналитическийРазрез", ПредопределенноеЗначение("Справочник.упАналитическиеРазрезы.ПустаяСсылка"));
	ЗаполнитьПоОстаткамПрочихРасходовНаСервере(сткРезультат);
	ЗаполнитьПрочиеРасходыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ПрочиеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПересчитатьРаспределениеРасходов", ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Распределение расходов будет пересчитано?'"), РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	ОбработкаОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамПрочихРасходовНаКлиенте", ЭтаФорма);
	сткОтбор	= Новый Структура();
	сткОтбор.Вставить("ВариантРасходов",     ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Косвенные"));
	сткПараметры = Новый Структура("Отбор",  сткОтбор);
	ОткрытьФорму("РегистрНакопления.упПрочиеРасходы.Форма.ФормаОстатки", сткОтбор, ЭтаФорма, УникальныйИдентификатор,,,ОбработкаОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамБезАналитическогоРазреза(Команда)
	ОбработкаОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаКлиенте", ЭтаФорма);
	сткОтбор	= Новый Структура();
	сткОтбор.Вставить("ВариантРасходов",     ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Косвенные"));
	сткПараметры = Новый Структура("Отбор",  сткОтбор);
	ОткрытьФорму("РегистрНакопления.упПрочиеРасходы.Форма.ФормаОстаткиБезАналитическогоРазреза", сткОтбор, ЭтаФорма, УникальныйИдентификатор,,,ОбработкаОповещения);
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьРасходы(Команда)
	Отказ = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.ПериодРаспределенияЗатратНачало) Тогда
		ТекстСообщения = Нстр("ru = 'Поле ""Начало периода"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							"ПериодРаспределенияЗатратНачало",
							"Объект",
							Отказ);

	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ПериодРаспределенияЗатратНачало) Тогда
		ТекстСообщения = Нстр("ru = 'Поле ""Окончание периода"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							"ПериодРаспределенияЗатратОкончание",
							"Объект",
							Отказ);

	КонецЕсли;
	Если НЕ Отказ Тогда
		РаспределитьРасходыНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьРасходыПоРейсам(Команда)
	ЗаполнитьРасходыПоРейсам            = Истина;
	ЗаполнитьРасходыПоТС                = Ложь;
	ЗаполнитьРасходыФинансовыйРезультат = Ложь;
	РаспределитьРасходыНаСервере(ЗаполнитьРасходыПоРейсам, ЗаполнитьРасходыПоТС, ЗаполнитьРасходыФинансовыйРезультат);
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьРасходыПоТС(Команда)
	ЗаполнитьРасходыПоРейсам            = Ложь;
	ЗаполнитьРасходыПоТС                = Истина;
	ЗаполнитьРасходыФинансовыйРезультат = Ложь;
	РаспределитьРасходыНаСервере(ЗаполнитьРасходыПоРейсам, ЗаполнитьРасходыПоТС, ЗаполнитьРасходыФинансовыйРезультат);
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьРасходыНаФинансовыйРезультат(Команда)
	ЗаполнитьРасходыПоРейсам            = Ложь;
	ЗаполнитьРасходыПоТС                = Ложь;
	ЗаполнитьРасходыФинансовыйРезультат = Истина;
	РаспределитьРасходыНаСервере(ЗаполнитьРасходыПоРейсам, ЗаполнитьРасходыПоТС, ЗаполнитьРасходыФинансовыйРезультат);
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
    УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
    ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПериодРаспределенияЗатратНачало", "ПериодРаспределенияЗатратОкончание"));
		
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ЗаполнитьПоОстаткамПрочихРасходовНаКлиенте(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Истина
		И ТипЗнч(РезультатЗакрытия) = Тип("Массив") 
		И РезультатЗакрытия.Количество() > 0
	Тогда
		ОчиститьТабличныеЧасти();
		ЗаполнитьЗначенияСвойств(Объект, РезультатЗакрытия[0]);
		ЗаполнитьПоОстаткамПрочихРасходовНаСервере(РезультатЗакрытия[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаКлиенте(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Истина
		И ТипЗнч(РезультатЗакрытия) = Тип("Массив") 
		И РезультатЗакрытия.Количество() > 0
	Тогда
		ОчиститьТабличныеЧасти();
		ЗаполнитьЗначенияСвойств(Объект, РезультатЗакрытия[0]);
		ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаСервере(РезультатЗакрытия[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаСервере(РезультатЗакрытия)
	Отказ = Ложь;
	упРаспределениеРасходов.ЗаполнитьПрочиеРасходы(Объект, Отказ);		
КонецПроцедуры

&НаСервере
Процедура РаспределитьРасходыНаСервере(ЗаполнитьРасходыПоРейсам = Истина, ЗаполнитьРасходыПоТС = Истина, ЗаполнитьРасходыФинансовыйРезультат = Истина)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");	
	ДокументОбъект.ЗаполнитьРасходы(ЗаполнитьРасходыПоРейсам, ЗаполнитьРасходыПоТС, ЗаполнитьРасходыФинансовыйРезультат);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамПрочихРасходовНаСервере(РезультатЗакрытия)
	Если ЗначениеЗаполнено(Объект.СтатьяРасходов) Тогда
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|
		|ВЫБРАТЬ
		|	упСтатьиДоходовРасходовТ.ПравилоРаспределенияКосвенныхРасходовНаФинансовыйРезультат КАК ПравилоРаспределенияНаФинансовыйРезультат,
		|	упСтатьиДоходовРасходовТ.ПравилоРаспределенияКосвенныхРасходовПоРейсам КАК ПравилоРаспределенияПоРейсам,
		|	упСтатьиДоходовРасходовТ.ПравилоРаспределенияКосвенныхРасходовПоТС КАК ПравилоРаспределенияПоТС,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходовТ.ОпределятьНаправлениеДеятельностиПриРаспределении
		|			ТОГДА ЕСТЬNULL(УпНаправленияДеятельностиТранспортныхСредств_СрезП.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка))
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ КАК НаправлениеДеятельностиОтбор,
		|	ВЫБОР
		|		КОГДА упСтатьиДоходовРасходовТ.ОпределятьНаправлениеДеятельностиПриРаспределении
		|			ТОГДА ЕСТЬNULL(УпПодразделенияТранспортныхСредств_СрезПоследнихТ.Подразделение, ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка))
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)
		|	КОНЕЦ КАК ПодразделениеОтбор
		|ИЗ
		|	Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходовТ
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНаправленияДеятельностиТранспортныхСредств.СрезПоследних(
		|			&Период) КАК УпНаправленияДеятельностиТранспортныхСредств_СрезП
		|		ПО упАналитическиеРазрезы.ТранспортноеСредство = УпНаправленияДеятельностиТранспортныхСредств_СрезП.ТранспортноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПодразделенияТранспортныхСредств.СрезПоследних(
		|			&Период) КАК УпПодразделенияТранспортныхСредств_СрезПоследнихТ
		|		ПО упАналитическиеРазрезы.ТранспортноеСредство = УпПодразделенияТранспортныхСредств_СрезПоследнихТ.ТранспортноеСредство
		|	ПО &ИспользуетсяДопАналитическийРазрез
		|		И &АналитическийРазрез = упАналитическиеРазрезы.Ссылка
		|ГДЕ упСтатьиДоходовРасходовТ.Ссылка = &СтатьяРасходов
		|";

		Запрос.УстановитьПараметр("СтатьяРасходов",                     Объект.СтатьяРасходов);
		Запрос.УстановитьПараметр("ИспользуетсяДопАналитическийРазрез", ПолучитьФункциональнуюОпцию("упИспользоватьДопАналитическийРазрезПриРаспределенииРасходов"));
		Запрос.УстановитьПараметр("АналитическийРазрез",                РезультатЗакрытия.АналитическийРазрез);
		Запрос.УстановитьПараметр("Период",                             КонецДня(Объект.ПериодРаспределенияЗатратОкончание));
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Объект, Выборка);
			НоваяСтрока = Объект.ПрочиеРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, РезультатЗакрытия)
		КонецЕсли; 
	
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
    УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    УправлениеПечатьюКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ЗаблокироватьЭлементы() 

	ДоступноЗаполнениеРаспределение = НЕ Объект.Проведен;
	
	Элементы.ФормаЗаполнитьПоОстаткам.Доступность                         = ДоступноЗаполнениеРаспределение;
	Элементы.ФормаЗаполнитьПоОстаткамБезАналитическогоРазреза.Доступность = ДоступноЗаполнениеРаспределение;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТабличныеЧасти()
	Объект.ПрочиеРасходы.Очистить();
	Объект.РасходыПоРейсам.Очистить();
	Объект.РасходыПоТС.Очистить();
	Объект.РезультатыФинансовыйРезультат.Очистить();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПрочиеРасходыНаСервере()
	Отказ = Ложь;
	упРаспределениеРасходов.ЗаполнитьПрочиеРасходы(Объект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПересчитатьРаспределениеРасходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.РасходыПоРейсам.Очистить();
		Объект.РасходыПоТС.Очистить();
		Объект.РезультатыФинансовыйРезультат.Очистить();	
		РаспределитьРасходыНаСервере();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти
