#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формируется или открывается существующий документ "Регистрация отложенных работ по рейсу" на основании рейса 
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текстовое представлениеошибки.
//  Отказ  - Булево - Флаг что произошел отказ создания движения.
//
// Возвращаемое значение:
//   ДокументСсылка   - Ссылка на документ.
//
Функция СформироватьРегистрацияОтложенныхРаботПоРейсу(Рейс, ТекстОшибки = Неопределено, Отказ) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упРегистрацияОтложенныхРаботПоРейсу.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.упРегистрацияОтложенныхРаботПоРейсу КАК упРегистрацияОтложенныхРаботПоРейсу
	|ГДЕ
	|	упРегистрацияОтложенныхРаботПоРейсу.Рейс = &Рейс
	|	И НЕ упРегистрацияОтложенныхРаботПоРейсу.ПометкаУдаления";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	Иначе	
		НовыйДокумент = Документы.упРегистрацияОтложенныхРаботПоРейсу.СоздатьДокумент();
		НовыйДокумент.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		НовыйДокумент.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		НовыйДокумент.Дата = ТекущаяДатаСеанса();
		НовыйДокумент.Рейс = Рейс;
		НовыйДокумент.Заполнить(Рейс);
		НовыйДокумент.ДополнительныеСвойства.Вставить("ВыводитьСообщенияПользователю", Ложь);
		Попытка
			НовыйДокумент.Записать();
			Результат = НовыйДокумент.Ссылка;
		Исключение
			ТекстОшибкиДляЖР = "";
			Отказ = Истина;
			упСервисныеФункции.ПредставленияОписанияОшибки(НовыйДокумент, ТекстОшибки, ТекстОшибкиДляЖР);
			ЗаписьЖурналаРегистрации(упКорректировкаМаршрута.СобытиеОшибкаРегистрацияОтложенныхРаботПоРейсу(НСтр("ru = 'Добавление'")), УровеньЖурналаРегистрации.Ошибка, ,Рейс, ТекстОшибкиДляЖР, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
	КонецЕсли; 
	
	Возврат Результат;	
	
КонецФункции // СформироватьРегистрацияЗадолженностиПоДокументамРейса()

#КонецОбласти

#КонецЕсли