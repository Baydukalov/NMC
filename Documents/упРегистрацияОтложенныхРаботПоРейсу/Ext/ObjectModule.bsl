#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВыводитьСообщенияПользователю = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ВыводитьСообщенияПользователю", Неопределено);
	Если ВыводитьСообщенияПользователю = Неопределено Тогда
		ВыводитьСообщенияПользователю = Истина;
		ДополнительныеСвойства.Вставить("ВыводитьСообщенияПользователю", ВыводитьСообщенияПользователю);
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		Отказ = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ЭтотОбъект);
		Если Не Отказ Тогда
			СтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Рейс);
			ТекстОшибки = "";
			Если НЕ упРаботаСоСтатусамиКлиентСервер.РазрешеноФормироватьКорректировкуРейса(СтатусРейса, РежимЗаписи)  Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено проведение и отмена проведения документа, так как рейс находится в статусе ""%1""'", СтатусРейса));
				ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстОшибки);
				Если ВыводитьСообщенияПользователю Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
				Иначе	
					Отказ = Истина
				КонецЕсли;
				ЗаписьЖурналаРегистрации(упКорректировкаМаршрута.СобытиеОшибкаРегистрацияОтложенныхРаботПоРейсу("ПередЗаписью"), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если Истина
		И Не Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения
	Тогда
		
		Если ЗначениеЗаполнено(Рейс) Тогда
			РеквизитыОбъекта = Новый Структура();
			РеквизитыОбъекта.Вставить("Дата", Дата);
			РеквизитыОбъекта.Вставить("Рейс", Рейс);
			РеквизитыОбъекта.Вставить("ФактическиеПараметры", Новый Структура);    
			ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет"));
			
			упКорректировкаРейсаОбъект = Неопределено;
			Если НЕ ДополнительныеСвойства.Свойство("КорректировкаРейса", упКорректировкаРейсаОбъект) Тогда
				упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();	
				ИнициализацияКорректировкиМаршрута(упКорректировкаРейсаОбъект);
				ДополнительныеСвойства.Вставить("СтатусРейсаДоКорректировки", упКорректировкаРейсаОбъект.СтатусРейсаДоКорректировки);
			КонецЕсли;	
			РеквизитыОбъекта.Вставить("ТранспортноеСредство", упКорректировкаРейсаОбъект.ТранспортноеСредство);
			РеквизитыОбъекта.Вставить("ВариантФормированияМаршрута", упКорректировкаРейсаОбъект.ВариантФормированияМаршрута);
			РеквизитыОбъекта.Вставить("ВидЗоныДляФормированияПредставленияРейса", упКорректировкаРейсаОбъект.ВидЗоныДляФормированияПредставленияРейса);
			ДополнительныеСвойства.Вставить("РеквизитыОбъекта", РеквизитыОбъекта);	
			РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу = упКорректировкаРейсаОбъект.РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу;
			ВидПеревозкиТекущий = упКорректировкаРейсаОбъект.ВидПеревозки;
			
			Если ЗначениеЗаполнено(упКорректировкаРейсаОбъект.ПрохождениеТочки) Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Точка проходится на мобильном клиенте(%1)'", упКорректировкаРейсаОбъект.ПрохождениеТочки));
				ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстОшибки);
				Если ВыводитьСообщенияПользователю Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,упКорректировкаРейсаОбъект.ПрохождениеТочки,,,Отказ);
				Иначе	
					Отказ = Истина
				КонецЕсли;
				ЗаписьЖурналаРегистрации(упКорректировкаМаршрута.СобытиеОшибкаРегистрацияОтложенныхРаботПоРейсу("ПередЗаписью"), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;
			
			Если Не Отказ Тогда
				Если РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу Тогда
					упКорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляРегистрацияОтложенныхРаботПоРейсу(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
				Иначе	
					ТекстОшибки = НСтр(СтрШаблон("ru = 'Для вида перевозки ""%1"" запрещено отложенное выполнение части работ'", ВидПеревозкиТекущий));
					ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстОшибки);
					Если ВыводитьСообщенияПользователю Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,ВидПеревозкиТекущий,,,Отказ);
					Иначе	
						Отказ = Истина
					КонецЕсли;
					ЗаписьЖурналаРегистрации(упКорректировкаМаршрута.СобытиеОшибкаРегистрацияОтложенныхРаботПоРейсу("ПередЗаписью"), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	РеквизитыОбъекта = ДополнительныеСвойства.РеквизитыОбъекта;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеПараметрыРейса");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
	
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Транспорт", ТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
		
	КонецЕсли; 
		
	ТекстОшибки = "";
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
		
	ДатаВыполняется = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Рейс, Перечисления.упСтатусыРейса.Выполняется);
	
	Если Истина
		И ЗначениеЗаполнено(ДатаВыполняется) 
		И РеквизитыОбъекта.Дата < ДатаВыполняется 
	Тогда
		ТекстОшибки = НСтр("ru = 'Дата документа должна быть больше даты перевода рейса в статус ""Выполняется"": " + ДатаВыполняется + "!'");
	Иначе
		
		ДатаКВыполнению = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Рейс, Перечисления.упСтатусыРейса.КВыполнению);
		Если Не ЗначениеЗаполнено(ДатаКВыполнению) Или РеквизитыОбъекта.Дата <= ДатаКВыполнению Тогда
			ТекстОшибки = НСтр("ru = 'Дата документа должна быть больше даты перевода рейса к выполнению: " + ДатаКВыполнению + "!'");
		ИначеЕсли Не ДополнительныеСвойства.Свойство("МобильныйКлиент") И РеквизитыОбъекта.Дата > ТекущаяДатаСеанса() Тогда // при получении данных с мобильного клиента возможна рассинхронизация дат на android-устройстве и в информационной базе
			ТекстОшибки = НСтр("ru = 'Дата документа не может быть больше текущей даты!'");
		КонецЕсли; 
	КонецЕсли;
	
	Если ТекстОшибки <> "" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации(упКорректировкаМаршрута.СобытиеОшибкаРегистрацияОтложенныхРаботПоРейсу("ОбработкаПроведения"), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецЕсли; 
	
	// Формируется маршрут.
	упКорректировкаМаршрута.СформироватьДвижениеМаршрутПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются работы.
	упКорректировкаМаршрута.СформироватьДвижениеРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	// Устанавливаются статусы рейса.
	ДополнительныеСвойства.Вставить("ДобавитьСтатус", Ложь);
	ДополнительныеСвойства.Вставить("РассчитыватьСтатус", Истина);
	ДополнительныеСвойства.Вставить("ПровестиПроверкуНаВозможныеСтатусы", Истина);
	упРейс.СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		Движения.Записать();
	КонецЕсли;

	// При прохождении последней точки формируются фактические параметры рейса.
	ДополнительныеСвойства.Вставить("ФактическиеПараметрыРассчитыватьРасстояние", Ложь);
	ДополнительныеСвойства.Вставить("ФактическиеПараметрыРассчитыватьВремя", Ложь);
	ДополнительныеСвойства.Вставить("ВыводитьОшибкиПриОпределенииЗонТолькоВЖР", Истина);
	упРейс.СформироватьФактическиеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При прохождении последней точки фиксируется фактическое время нахождения ТС в рейсе.
	упРейс.СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется расписание работы ТС.
	упРейс.СформироватьДвиженияРасписаниеРаботыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При выполнении рейса на мобильном клиенте фиксируем необоходимость обновить данные по рейсу
	упКорректировкаМаршрута.СформироватьДвиженияИзмененияРейсов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Процедура - Инициализация
//
// Параметры:
//  КорректировкаМаршрутаОбъект	 
//
Процедура ИнициализацияКорректировкиМаршрута(КорректировкаМаршрутаОбъект) Экспорт
	
	ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	
	КорректировкаМаршрутаОбъект.Рейс = Рейс;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		КорректировкаМаршрутаОбъект.Ссылка = Ссылка;
		КорректировкаМаршрутаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
	Иначе
		КорректировкаМаршрутаОбъект.Ссылка = Документы.упРегистрацияОтложенныхРаботПоРейсу.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КорректировкаМаршрутаОбъект.Период) Тогда
		КорректировкаМаршрутаОбъект.Период = Дата;
	КонецЕсли;
	
	сткПараметрыЗаполнения = Новый Структура();
	сткПараметрыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками);
	КорректировкаМаршрутаОбъект.Инициализация(сткПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
