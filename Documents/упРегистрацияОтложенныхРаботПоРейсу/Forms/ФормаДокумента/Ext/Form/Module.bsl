#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ТекущийОбъект.Проведен Тогда
		ТолькоПросмотр = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ТекущийОбъект, ТекущийОбъект.Дата);
	КонецЕсли;
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере(Отказ = Ложь)
		
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;	
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере(Отказ);
КонецПроцедуры	

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи <> ПредопределенноеЗначение("РежимЗаписиДокумента.Запись") Тогда
		мсвСобытийРейса = Новый Массив;
		мсвСобытийРейса.Добавить("ОбновитьРаботы");
		мсвСобытийРейса.Добавить("ИзменилсяМаршрут");
		мсвСобытийРейса.Добавить("СменаСтатуса");
		
		Если мсвСобытийРейса.Количество() > 0 Тогда
			События = СтрСоединить(мсвСобытийРейса, ";");
			Оповестить(События, ,Объект.Рейс);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьДанныеФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ОбновитьДанныеФормы()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		КорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
		КорректировкаМаршрутаОбъект.Рейс = Объект.Рейс;
		КорректировкаМаршрутаОбъект.Ссылка = Объект.Ссылка;
		КорректировкаМаршрутаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
		
		Если НЕ ЗначениеЗаполнено(КорректировкаМаршрутаОбъект.Период) Тогда
			КорректировкаМаршрутаОбъект.Период = Объект.Дата;
		КонецЕсли;
		
		сткПараметрыЗаполнения = Новый Структура();
		сткПараметрыЗаполнения.Вставить("ЧастичнаяИнициализация", Истина);
		КорректировкаМаршрутаОбъект.Инициализация(сткПараметрыЗаполнения);
		
		сткСтатистикаПоМаршруту = КорректировкаМаршрутаОбъект.СтатистикаПоМаршруту();
		КоличествоОтмененныхТочек = сткСтатистикаПоМаршруту.КоличествоОтмененныхТочек;
		КоличествоОтложенныхТочек = сткСтатистикаПоМаршруту.КоличествоОтложенныхТочек;
		КоличествоОтложенныхРабот = сткСтатистикаПоМаршруту.КоличествоОтложенныхРабот;
	Иначе
		КоличествоОтмененныхТочек = 0;
		КоличествоОтложенныхТочек = 0;
		КоличествоОтложенныхРабот = 0;
	КонецЕсли;
	
	КоличествоОтмененныхТочекПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоОтмененныхТочек));
	КоличествоОтложенныхТочекПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоОтложенныхТочек));
	КоличествоОтложенныхРаботПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоОтложенныхРабот));
КонецПроцедуры

#КонецОбласти
