#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьРасходы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|		упПрочиеРасходыОстаткиИОбороты.СуммаСНДСКонечныйОстаток - упПрочиеРасходыОстаткиИОбороты.СуммаБезНДСКонечныйОстаток - упПрочиеРасходыОстаткиИОбороты.СуммаСНДСНачальныйОстаток + упПрочиеРасходыОстаткиИОбороты.СуммаБезНДСНачальныйОстаток КАК СуммаНДС,
	|		упПрочиеРасходыОстаткиИОбороты.СуммаСНДСКонечныйОстаток- упПрочиеРасходыОстаткиИОбороты.СуммаСНДСНачальныйОстаток  КАК Сумма,
	|		упПрочиеРасходыОстаткиИОбороты.СуммаВалютаСНДСКонечныйОстаток - упПрочиеРасходыОстаткиИОбороты.СуммаВалютаБезНДСКонечныйОстаток - упПрочиеРасходыОстаткиИОбороты.СуммаВалютаСНДСНачальныйОстаток + упПрочиеРасходыОстаткиИОбороты.СуммаВалютаБезНДСНачальныйОстаток КАК СуммаВалютаНДС,
	|		упПрочиеРасходыОстаткиИОбороты.СуммаВалютаСНДСКонечныйОстаток - упПрочиеРасходыОстаткиИОбороты.СуммаВалютаСНДСНачальныйОстаток  КАК СуммаВалюта,
	|		упПрочиеРасходыОстаткиИОбороты.Период КАК Дата
	|	ИЗ
	|		РегистрНакопления.упПрочиеРасходы.ОстаткиИОбороты(
	|			,
	|			,
	|			МЕСЯЦ,
	|			,
	|			Организация = &Организация
	|			И Подразделение = &Подразделение
	|			И НаправлениеДеятельности = &НаправлениеДеятельности
	|			И АналитическийРазрез = &АналитическийРазрез
	|			И СтатьяРасходов = &СтатьяРасходов
	|			И Валюта = &Валюта) КАК упПрочиеРасходыОстаткиИОбороты
	|";

	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("Подразделение",           Подразделение);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", НаправлениеДеятельности);
	Запрос.УстановитьПараметр("АналитическийРазрез",     АналитическийРазрез);
	Запрос.УстановитьПараметр("СтатьяРасходов",          СтатьяРасходов);
	Запрос.УстановитьПараметр("Валюта",                  Валюта);
	
	Расходы.Очистить();
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Истина 
			И Выборка.СуммаНДС = 0
			И Выборка.Сумма = 0
			И Выборка.СуммаВалютаНДС = 0
			И Выборка.СуммаВалюта = 0
		Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Расходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	РеквизитыОбъекта = Новый Структура("Дата,
								|Организация,
								|Подразделение,
								|Валюта,
								|СтатьяРасходов,
								|НаправлениеДеятельности,
								|АналитическийРазрез,
								|Валюта");
	
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);

	//// Формируются прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();

	// Контроль остатков прочих расходов.
	упРаспределениеРасходов.ПроконтролироватьПрочиеРасходы(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРаспределениеРасходовБудущихПериодов") Тогда
		Организация   = ДанныеЗаполнения.Организация;
		Подразделение = ДанныеЗаполнения.Подразделение;
		НаправлениеДеятельности = ДанныеЗаполнения.НаправлениеДеятельности;
		СтатьяРасходов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.СтатьяРасходов, "СтатьяСписанияРасходов");
		АналитическийРазрез = ДанныеЗаполнения.АналитическийРазрез;
		Валюта = ДанныеЗаполнения.Валюта;
		ЗаполнитьРасходы();		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли 