#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	РежимВыбора = Параметры.РежимВыбора;
	
	Если РежимВыбора Тогда
		Элементы.ГруппаПодобрать.Видимость = Ложь;
	Иначе
		Элементы.ФормаВыбрать.Заголовок = "Перенести в документ";
	КонецЕсли;
	
	Сотрудник = Справочники.упСотрудники.ПустаяСсылка();
	Если ЗначениеЗаполнено(Параметры.ПодотчетноеЛицо) Тогда
		Сотрудник = Параметры.ПодотчетноеЛицо;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Водитель1", Сотрудник,Истина);	
	НачалоПериода = Дата("00010101000000");
	Если ЗначениеЗаполнено(Параметры.НачалоПериода) Тогда
		НачалоПериода = Параметры.НачалоПериода;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НачалоПериода", НачалоПериода,Истина);	
	ОкончаниеПериода = Дата("00010101000000");
	Если ЗначениеЗаполнено(Параметры.ОкончаниеПериода) Тогда
		ОкончаниеПериода = Параметры.ОкончаниеПериода;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОкончаниеПериода", ОкончаниеПериода,Истина);
	
	Если Параметры.Свойство("СписокРейсов") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", Параметры.СписокРейсов, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСеанса.ТекущийВнешнийПользователь) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Пользователи.ТекущийПользователь());	
		// СтандартныеПодсистемы.Печать
		УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.Печать
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Справочники.Пользователи.ПустаяСсылка());		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "СформированыЗаявкиНаТС" Тогда
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Для каждого текЗаявкаНаТС Из Параметр Цикл
				ОткрытьФорму("Документ.упЗаявкаНаТС.ФормаОбъекта", Новый Структура("Ключ", текЗаявкаНаТС), Источник);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если РежимВыбора Тогда
		ВыбранРейс();
	Иначе
		ТекущаяСтрокаСписка = Элементы.Список.ТекущиеДанные;
		Если НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			Попытка
				Рейс = ТекущаяСтрокаСписка.Ссылка;
			Исключение
				Рейс = Неопределено;
			КонецПопытки;
			Если НЕ Рейс = Неопределено Тогда
				ПодобратьДокумент(Рейс);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	Если РежимВыбора Тогда
		ВыбранРейс();
	Иначе
		СписокЗначений = Неопределено;
		Если ТаблицаПодбора.Количество() Тогда
			СписокЗначений = Новый СписокЗначений;
			Для каждого ТекущаяСтрокаРейсов Из ТаблицаПодбора Цикл
				СписокЗначений.Добавить(ТекущаяСтрокаРейсов.Рейс);
			КонецЦикла;
		КонецЕсли;	
		Закрыть(СписокЗначений);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКоманда(Команда)
	Закрыть();	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодобратьДокумент(ВыбранныйРейс)
	
	НоваяСтрока = ТаблицаПодбора.Добавить();
	НоваяСтрока.Рейс = ВыбранныйРейс;
	ОбновитьОтборДляСписка();
	
КонецПроцедуры // ПодобратьДокумент()

&НаСервере
Процедура ОбновитьОтборДляСписка()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Ссылка");
	СписокЗначений = Новый СписокЗначений;
	Для каждого ТекущаяСтрокаРейсов Из ТаблицаПодбора Цикл
		СписокЗначений.Добавить(ТекущаяСтрокаРейсов.Рейс);
	КонецЦикла;
	Если СписокЗначений.Количество() Тогда		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", СписокЗначений, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;	
	
КонецПроцедуры // ОбновитьОтборДляСписка()

&НаКлиенте
Процедура ВыбранРейс()
	
	ТекущаяСтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущаяСтрокаСписка = Неопределено Тогда
		Закрыть(ТекущаяСтрокаСписка.Ссылка);
	КонецЕсли;
	
КонецПроцедуры // ВыбранРейс()

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ТаблицаПодбораПослеУдаления(Элемент)
	ОбновитьОтборДляСписка();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодбораПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	МассивЗначений = ПараметрыПеретаскивания.Значение;
	Для каждого ИдентификаторСтроки Из МассивЗначений Цикл
		ТекущаяСтрокаСписка = Элементы.Список.ДанныеСтроки(ИдентификаторСтроки);
		Если НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			Попытка
				Рейс = ТекущаяСтрокаСписка.Ссылка;
			Исключение
				Рейс = Неопределено;
			КонецПопытки;
			Если НЕ Рейс = Неопределено Тогда
				Структура = Новый Структура;
				Структура.Вставить("Рейс",Рейс);
				НайденныеСтроки = ТаблицаПодбора.НайтиСтроки(Структура);
				Если НайденныеСтроки.Количество() = 0 Тогда			
					ПодобратьДокумент(Рейс);			
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти