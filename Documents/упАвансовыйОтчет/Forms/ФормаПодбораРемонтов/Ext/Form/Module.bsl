#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВедетсяУчетШинУзловИАгрегатов = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетШинУзловИАгрегатов");	
	ВедетсяУчетДополнительногоОборудования = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования");
	
	Если НЕ ВедетсяУчетШинУзловИАгрегатов И НЕ ВедетсяУчетДополнительногоОборудования Тогда 
		Элементы.ОбъектРемонта.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаВыбрать.Заголовок = "Перенести в документ";
	
	НачалоПериода = Дата("00010101000000");
	Если ЗначениеЗаполнено(Параметры.НачалоПериода) Тогда
		НачалоПериода = Параметры.НачалоПериода;
	КонецЕсли;
	ОкончаниеПериода = Дата("00010101000000");
	Если ЗначениеЗаполнено(Параметры.ОкончаниеПериода) Тогда
		ОкончаниеПериода = Параметры.ОкончаниеПериода;
	КонецЕсли;
	// получим ТС и агрегаты для них чтобы установить параметры.
	СтруктураТранспортныхСредств  = ПолучитьТС_Агрегаты(Параметры.СписокРейсов, НачалоПериода, ОкончаниеПериода);
	МассивТС = Справочники.упТранспортныеСредства.ПустаяСсылка();
	МассивАгрегаты = Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка();
	Если НЕ СтруктураТранспортныхСредств = Неопределено Тогда
		МассивТС = СтруктураТранспортныхСредств.ТранспортныеСредства;
		МассивАгрегаты = СтруктураТранспортныхСредств.Агрегаты;
	КонецЕсли;	
	// установим парамеры.
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "МассивТС", МассивТС,Истина);	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "МассивАгрегаты", МассивАгрегаты,Истина);	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НачалоПериода", НачалоПериода,Истина);	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОкончаниеПериода", ОкончаниеПериода,Истина);
	
	Если Параметры.Свойство("СписокРемонтов") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", Параметры.СписокРемонтов, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСеанса.ТекущийВнешнийПользователь) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Пользователи.ТекущийПользователь());	
		// СтандартныеПодсистемы.Печать
		УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.Печать
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Справочники.Пользователи.ПустаяСсылка());		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "СформированыЗаявкиНаТС" Тогда
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Для каждого текЗаявкаНаТС Из Параметр Цикл
				ОткрытьФорму("Документ.упЗаявкаНаТС.ФормаОбъекта", Новый Структура("Ключ", текЗаявкаНаТС), Источник);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаПодбораПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	МассивЗначений = ПараметрыПеретаскивания.Значение;
	Для каждого ТекущийРемонт Из МассивЗначений Цикл
		Если ТипЗнч(ТекущийРемонт) = ТИП("ДокументСсылка.упРемонтИТОТС") Тогда
			Структура = Новый Структура;
			Структура.Вставить("Ремонт",ТекущийРемонт);
			НайденныеСтроки = ТаблицаПодбора.НайтиСтроки(Структура);
			Если НайденныеСтроки.Количество() = 0 Тогда			
				ПодобратьДокумент(ТекущийРемонт);			
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры                        

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если РежимВыбора Тогда
		ВыбранРемонта();
	Иначе
		ТекущаяСтрокаСписка = Элементы.Список.ТекущиеДанные;
		Если НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			Попытка
				Ремонт = ТекущаяСтрокаСписка.Ссылка;
			Исключение
				Ремонт = Неопределено;
			КонецПопытки;
			Если НЕ Ремонт = Неопределено Тогда
				ПодобратьДокумент(Ремонт);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодбораПослеУдаления(Элемент)
	ОбновитьОтборДляСписка();
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	Если РежимВыбора Тогда
		ВыбранРемонта();
	Иначе
		СписокЗначений = Неопределено;
		Если ТаблицаПодбора.Количество() Тогда
			СписокЗначений = Новый СписокЗначений;
			Для каждого ТекущаяСтрокаПодбора Из ТаблицаПодбора Цикл
				СписокЗначений.Добавить(ТекущаяСтрокаПодбора.Ремонт);
			КонецЦикла;
		КонецЕсли;	
		Закрыть(СписокЗначений);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКоманда(Команда)
	Закрыть();	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбранРемонта()
	
	ТекущаяСтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущаяСтрокаСписка = Неопределено Тогда
		Закрыть(ТекущаяСтрокаСписка.Ссылка);
	КонецЕсли;
	
КонецПроцедуры // ВыбранРемонта()

&НаСервере
Процедура ОбновитьОтборДляСписка()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Ссылка");
	СписокЗначений = Новый СписокЗначений;
	Для каждого ТекущаяСтрокаРемонт Из ТаблицаПодбора Цикл
		СписокЗначений.Добавить(ТекущаяСтрокаРемонт.Ремонт);
	КонецЦикла;
	Если СписокЗначений.Количество() Тогда		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", СписокЗначений, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;	
	
КонецПроцедуры // ОбновитьОтборДляСписка()

&НаСервере
Процедура ПодобратьДокумент(ВыбранныйРемонт)
	
	НоваяСтрока = ТаблицаПодбора.Добавить();
	НоваяСтрока.Ремонт = ВыбранныйРемонт;
	ОбновитьОтборДляСписка();
	
КонецПроцедуры // ПодобратьДокумент()

&НаСервереБезКонтекста
Функция ПолучитьТС_Агрегаты(СписокРейсов, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ вт_Документы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|ГДЕ
	|	упРейс.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство КАК ТС
	|ПОМЕСТИТЬ вт_ТС
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					вт_Документы.Ссылка КАК Ссылка
	|				ИЗ
	|					вт_Документы КАК вт_Документы)) КАК упПеревозчикПоРейсуСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АгрегатыТС.Агрегат КАК Агрегат
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств КАК АгрегатыТС
	|ГДЕ
	|	АгрегатыТС.ТранспортноеСредство В
	|			(ВЫБРАТЬ
	|				вт_ТС.ТС КАК ТС
	|			ИЗ
	|				вт_ТС КАК вт_ТС)
	|	И ВЫБОР
	|			КОГДА &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ АгрегатыТС.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		КОНЕЦ
	|	И ТИПЗНАЧЕНИЯ(АгрегатыТС.Агрегат) = ТИП(Справочник.упШиныУзлыИАгрегаты)
	|
	|СГРУППИРОВАТЬ ПО
	|	АгрегатыТС.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ТС.ТС КАК ТС
	|ИЗ
	|	вт_ТС КАК вт_ТС
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ТС.ТС";
	
	Запрос.УстановитьПараметр("КонецПериода", ?(ЗначениеЗаполнено(КонецПериода), КонецПериода, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("МассивОбъектов", СписокРейсов);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаТС = ПакетЗапросов[3].Выбрать();
	ВыборкаАгрегаты = ПакетЗапросов[2].Выбрать();
	
	Результат = Неопределено;
	
	Если ВыборкаТС.Количество() Тогда
		СписокоТС = Новый СписокЗначений;
		Пока ВыборкаТС.Следующий() Цикл
			СписокоТС.Добавить(ВыборкаТС.ТС);
		КонецЦикла;
		
		СписокоАгрегаты = Новый СписокЗначений;
		Пока ВыборкаАгрегаты.Следующий() Цикл
			СписокоАгрегаты.Добавить(ВыборкаАгрегаты.Агрегат);
		КонецЦикла;
		
		Результат = Новый Структура;
		Результат.Вставить("ТранспортныеСредства",СписокоТС);
		Если СписокоАгрегаты.Количество() Тогда
			Результат.Вставить("Агрегаты", СписокоАгрегаты);
		Иначе
			Результат.Вставить("Агрегаты", Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка());
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПолучитьТС_Агрегаты()

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти