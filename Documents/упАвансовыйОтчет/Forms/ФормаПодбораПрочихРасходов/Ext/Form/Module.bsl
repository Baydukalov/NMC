#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПрочиеРасходыРасходы.Ссылка КАК Ссылка,
	               |	упПрочиеРасходыРасходы.НомерСтроки КАК НомерСтроки,
	               |	упПрочиеРасходыРасходы.АналитическийРазрез КАК АналитическийРазрез,
	               |	упПрочиеРасходыРасходы.Валюта КАК Валюта,
	               |	упПрочиеРасходыРасходы.Количество КАК Количество,
	               |	упПрочиеРасходыРасходы.ПравилоРаспределения КАК ПравилоРаспределения,
	               |	упПрочиеРасходыРасходы.СтавкаНДС КАК СтавкаНДС,
	               |	упПрочиеРасходыРасходы.СтатьяРасходов КАК СтатьяРасходов,
	               |	упПрочиеРасходыРасходы.Сумма КАК Сумма,
	               |	упПрочиеРасходыРасходы.СуммаНДС КАК СуммаНДС,
	               |	упПрочиеРасходыРасходы.ФормаОплаты КАК ФормаОплаты,
	               |	упПрочиеРасходыРасходы.Цена КАК Цена,
	               |	упПрочиеРасходыРасходы.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности
	               |ИЗ
	               |	Документ.упПрочиеРасходы.Расходы КАК упПрочиеРасходыРасходы
	               |ГДЕ
	               |	упПрочиеРасходыРасходы.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.упДоступныеФормыОплаты.Наличная)
	               |	И упПрочиеРасходыРасходы.Ссылка.Дата >= &НачалоПериода
	               |	И упПрочиеРасходыРасходы.Ссылка.Дата <= &ОкончаниеПериода
	               |	И упПрочиеРасходыРасходы.Ссылка.Проведен
	               |	И упПрочиеРасходыРасходы.Ссылка.ПодотчетноеЛицо = &ПодотчетноеЛицо";
	Запрос.УстановитьПараметр("НачалоПериода", Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", Параметры.ОкончаниеПериода);
	Запрос.УстановитьПараметр("ПодотчетноеЛицо", Параметры.ПодотчетноеЛицо);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокЗаявок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокЗаявокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработатьВыбранныеЗначения(Значение);
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаявокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработатьВыбранныеЗначения(ВыбраннаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Выбрать(Команда)
	
	мсвПодобранныеСтроки = Новый Массив;
	
	Для каждого Строка Из ПодобранныеРасходы Цикл
		
		сткСтрока = Новый Структура("Ссылка, НомерСтроки, Валюта, Сумма, СуммаНДС, СтавкаНДС, СтатьяРасходов, НаправлениеДеятельности");
		ЗаполнитьЗначенияСвойств(сткСтрока, Строка);
		мсвПодобранныеСтроки.Добавить(сткСтрока);
				
	КонецЦикла;
	
	Закрыть(мсвПодобранныеСтроки);
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОбработатьВыбранныеЗначения(ВыбранныеЗначения)
	мсвКлючейСтрок = Новый Массив;
	
	Если ТипЗнч(ВыбранныеЗначения) = Тип("Массив") Тогда
		мсвКлючейСтрок = ВыбранныеЗначения;	
	ИначеЕсли ВыбранныеЗначения <> Неопределено Тогда	
		мсвКлючейСтрок.Добавить(ВыбранныеЗначения);
	КонецЕсли;
	
	Для каждого КлючСтроки Из мсвКлючейСтрок Цикл
		ДанныеСтроки = Элементы.СписокЗаявок.ДанныеСтроки(КлючСтроки);
		НоваяСтрока = ПодобранныеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
		СписокЗаявок.Удалить(СписокЗаявок.Индекс(ДанныеСтроки));
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
