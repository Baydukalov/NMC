#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// Новый документ.
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда 
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если ИспользуетсяУчетПодразделений
					И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
				Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
		
		Объект.НачалоПериода = ТекущаяДатаСеанса();
		Объект.ОкончаниеПериода = ТекущаяДатаСеанса();
			
	КонецЕсли;
	
	УстановитьВидимость();
			
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		УстановитьВалютуУчета();
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;	
	//РасходыПоПутевымЛистам
	Элементы.РасходыПоПутевымЛистамНомерСтроки.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамСтатьяРасходов.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамНаправлениеДеятельности.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамДокументОснование.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамВалюта.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамСумма.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамСтавкаНДС.ТолькоПросмотр = Истина;
	Элементы.РасходыПоПутевымЛистамСуммаНДС.ТолькоПросмотр = Истина;
	
	//РасходыПоРемонтам
	Элементы.РасходыПоРемонтамНомерСтроки.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамСтатьяРасходов.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамНаправлениеДеятельности.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамДокументОснование.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамВалюта.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамСумма.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамСтавкаНДС.ТолькоПросмотр = Истина;
	Элементы.РасходыПоРемонтамСуммаНДС.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки	
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	ПриСозданииПриЧтенииНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура РейсыРейсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	сткПараметры = Новый Структура("РежимВыбора", Истина);
	сткПараметры.Вставить("ПодотчетноеЛицо", Объект.ПодотчетноеЛицо);
	сткПараметры.Вставить("НачалоПериода", Объект.НачалоПериода);
	сткПараметры.Вставить("ОкончаниеПериода", Объект.ОкончаниеПериода);
	СписокРейсов = Неопределено;
	Если Объект.Рейсы.Количество() Тогда
		СписокЗначений = Новый СписокЗначений;
		Для каждого ТекущаяСтрокаРейсов Из Объект.Рейсы Цикл
			СписокЗначений.Добавить(ТекущаяСтрокаРейсов.Рейс);
		КонецЦикла;
		сткПараметры.Вставить("СписокРейсов", СписокЗначений);
	КонецЕсли;	
	ОткрытьФорму("Документ.упАвансовыйОтчет.Форма.ФормаПодбораРейсов", сткПараметры,Элементы.РейсыРейс,,,,Новый ОписаниеОповещения("РезультатПодбораРейсов", ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПодбораРейсов(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = ТИП("ДокументСсылка.упРейс") Тогда
		ТекущиеДанныеРейсов = Элементы.Рейсы.ТекущиеДанные;
		ТекущиеДанныеРейсов.Рейс = Результат;
	ИначеЕсли ТипЗнч(Результат) = ТИП("СписокЗначений")Тогда
		Для каждого ТекущаяСтрокаРейсов Из Результат Цикл
			НоваяСтрока = Объект.Рейсы.Добавить();
			НоваяСтрока.Рейс = ТекущаяСтрокаРейсов.Значение;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры // РезультатПодбораРейсов()

&НаКлиенте
Процедура РасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		//ТекущаяСтрока.Количество 	= 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		ТекущаяСтрока.СтавкаНДС = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "СтавкаНДС");
		ПересчитатьСтроку(Элемент);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьРейсы(Команда)
	 
	сткПараметры = Новый Структура("РежимВыбора", Ложь);
	сткПараметры.Вставить("ПодотчетноеЛицо", Объект.ПодотчетноеЛицо);
	сткПараметры.Вставить("НачалоПериода", Объект.НачалоПериода);
	сткПараметры.Вставить("ОкончаниеПериода", Объект.ОкончаниеПериода);
	СписокРейсов = Неопределено;
	Если Объект.Рейсы.Количество() Тогда
		СписокЗначений = Новый СписокЗначений;
		Для каждого ТекущаяСтрокаРейсов Из Объект.Рейсы Цикл
			СписокЗначений.Добавить(ТекущаяСтрокаРейсов.Рейс);
		КонецЦикла;
		сткПараметры.Вставить("СписокРейсов", СписокЗначений);
	КонецЕсли;	
	ОткрытьФорму("Документ.упАвансовыйОтчет.Форма.ФормаПодбораРейсов", сткПараметры,Элементы.РейсыРейс,,,,Новый ОписаниеОповещения("РезультатПодбораРейсов", ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьРемонты(Команда)
		
	Если Объект.Рейсы.Количество() Тогда
		сткПараметры = Новый Структура("РежимВыбора", Ложь);
		сткПараметры.Вставить("НачалоПериода", Объект.НачалоПериода);
		сткПараметры.Вставить("ОкончаниеПериода", Объект.ОкончаниеПериода);
		СписокРейсов = Неопределено;
		СписокЗначений = Новый СписокЗначений;
		Для каждого ТекущаяСтрокаРейсов Из Объект.Рейсы Цикл
			СписокЗначений.Добавить(ТекущаяСтрокаРейсов.Рейс);
		КонецЦикла;
		сткПараметры.Вставить("СписокРейсов", СписокЗначений);
		
		СписокРемонтов = Неопределено;
		Если Объект.Рейсы.Количество() Тогда
			СписокРемонтов = Новый СписокЗначений;
			Для каждого ТекущаяСтрокаРемонта Из Объект.РасходыПоРемонтам Цикл
				СписокРемонтов.Добавить(ТекущаяСтрокаРемонта.ДокументОснование);
			КонецЦикла;
			сткПараметры.Вставить("СписокРемонтов", СписокРемонтов);
		КонецЕсли;
		
		ОткрытьФорму("Документ.упАвансовыйОтчет.Форма.ФормаПодбораРемонтов", сткПараметры,Элементы.РасходыПоРемонтамДокументОснование,,,,Новый ОписаниеОповещения("РезультатПодбораРемонтов", ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	УстановитьПометкаБУ(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	УстановитьПометкаБУ();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПЛ(Команда)
	Если Объект.Рейсы.Количество() Тогда
		Если Объект.РасходыПоПутевымЛистам.Количество() Тогда
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаРасходовПоПутевымЛистам", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'В таблице расходы по путевым листам добавлены строки, заполнение будет выполнено с очищением табличной части. Продолжить?';"
			+ " en = 'Do you want to continue?'"), Режим, 0);
		Иначе
			ВыполнитЗаполнениеРасходовПоПутевымЛистам();
		КонецЕсли;	
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заполнить расход по путевым листам'"),,
		НСтр("ru = 'Укажите рейсы, на основании которых выполнить поиск путевых листов.'"),БиблиотекаКартинок.Информация32);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВременноеОкноПериода(Команда)
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "НачалоПериода", "ОкончаниеПериода"));
	Модифицированность = Истина
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПрочиеРасходы(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("НачалоПериода", Объект.НачалоПериода);
	сткПараметры.Вставить("ОкончаниеПериода", Объект.ОкончаниеПериода);
	сткПараметры.Вставить("ПодотчетноеЛицо", Объект.ПодотчетноеЛицо);
	ОткрытьФорму("Документ.упАвансовыйОтчет.Форма.ФормаПодбораПрочихРасходов", сткПараметры, Элементы.Расходы,,,,Новый ОписаниеОповещения("РезультатПодбораПрочихРасходов", ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗакрытияВопросаРасходовПоПутевымЛистам(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитЗаполнениеРасходовПоПутевымЛистам();
	КонецЕсли;
КонецПроцедуры // ПослеЗакрытияВопросаРасходовПоПутевымЛистам()

&НаСервере
Процедура ВыполнитЗаполнениеРасходовПоПутевымЛистам()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ вт_Рейсы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|ГДЕ
	|	упРейс.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛист.Ссылка КАК Ссылка,
	|	упПутевойЛист.Рейс КАК Рейс
	|ПОМЕСТИТЬ вт_ПутевыеЛисты
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|ГДЕ
	|	упПутевойЛист.Рейс В
	|			(ВЫБРАТЬ
	|				вт_Рейсы.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_Рейсы КАК вт_Рейсы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упПутевойЛистРейсы.Ссылка,
	|	упПутевойЛистРейсы.Рейс
	|ИЗ
	|	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	|ГДЕ
	|	упПутевойЛистРейсы.Рейс В
	|			(ВЫБРАТЬ
	|				вт_Рейсы.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_Рейсы КАК вт_Рейсы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ПутевыеЛисты.Ссылка КАК Ссылка,
	|	вт_ПутевыеЛисты.Рейс КАК Рейс
	|ПОМЕСТИТЬ вт_ПЛ
	|ИЗ
	|	вт_ПутевыеЛисты КАК вт_ПутевыеЛисты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиГСМ КАК упОстаткиГСМ
	|		ПО вт_ПутевыеЛисты.Ссылка = упОстаткиГСМ.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_ПутевыеЛисты.Ссылка,
	|	вт_ПутевыеЛисты.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛистЗаправкиСливыГСМ.Ссылка КАК Ссылка,
	|	упПутевойЛистЗаправкиСливыГСМ.Количество КАК Количество,
	|	упПутевойЛистЗаправкиСливыГСМ.Стоимость КАК Стоимость,
	|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упВидыГСМ.СтатьяРасходов КАК СтатьяРасходов,
	|	упПутевойЛистЗаправкиСливыГСМ.ВидГСМ КАК ВидГСМ,
	|	упПутевойЛистЗаправкиСливыГСМ.СтавкаНДС КАК СтавкаНДС,
	|	упПутевойЛистЗаправкиСливыГСМ.СтоимостьНДС КАК СтоимостьНДС,
	|	упПутевойЛистЗаправкиСливыГСМ.ДатаВходногоДокумента КАК ДатаВходногоДокумента,
	|	упПутевойЛистЗаправкиСливыГСМ.НаименованиеВходногоДокумента КАК НаименованиеВходногоДокумента,
	|	упПутевойЛистЗаправкиСливыГСМ.НомерВходногоДокумента КАК НомерВходногоДокумента
	|ИЗ
	|	Документ.упПутевойЛист.ЗаправкиСливыГСМ КАК упПутевойЛистЗаправкиСливыГСМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыГСМ КАК упВидыГСМ
	|		ПО упПутевойЛистЗаправкиСливыГСМ.ВидГСМ = упВидыГСМ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПЛ КАК вт_ПЛ
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|			ПО вт_ПЛ.Рейс = упРейс.Ссылка
	|		ПО упПутевойЛистЗаправкиСливыГСМ.Ссылка = вт_ПЛ.Ссылка
	|ГДЕ
	|	упПутевойЛистЗаправкиСливыГСМ.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_ПутевыеЛисты.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_ПутевыеЛисты КАК вт_ПутевыеЛисты)
	|	И упПутевойЛистЗаправкиСливыГСМ.ТопливнаяКарта = ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка)
	|	И упПутевойЛистЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПутевойЛистЗаправкиСливыГСМДоп.Ссылка,
	|	ПутевойЛистЗаправкиСливыГСМДоп.Количество,
	|	ПутевойЛистЗаправкиСливыГСМДоп.Стоимость,
	|	упРейс.НаправлениеДеятельности,
	|	упВидыГСМ.СтатьяРасходов,
	|	ПутевойЛистЗаправкиСливыГСМДоп.ВидГСМ,
	|	ПутевойЛистЗаправкиСливыГСМДоп.СтавкаНДС,
	|	ПутевойЛистЗаправкиСливыГСМДоп.СтоимостьНДС,
	|	ПутевойЛистЗаправкиСливыГСМДоп.ДатаВходногоДокумента,
	|	ПутевойЛистЗаправкиСливыГСМДоп.НаименованиеВходногоДокумента,
	|	ПутевойЛистЗаправкиСливыГСМДоп.НомерВходногоДокумента
	|ИЗ
	|	Документ.упПутевойЛист.ЗаправкиСливыГСМДополнительноеОборудование КАК ПутевойЛистЗаправкиСливыГСМДоп
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыГСМ КАК упВидыГСМ
	|		ПО ПутевойЛистЗаправкиСливыГСМДоп.ВидГСМ = упВидыГСМ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПЛ КАК вт_ПЛ
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|			ПО вт_ПЛ.Рейс = упРейс.Ссылка
	|		ПО ПутевойЛистЗаправкиСливыГСМДоп.Ссылка = вт_ПЛ.Ссылка
	|ГДЕ
	|	ПутевойЛистЗаправкиСливыГСМДоп.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_ПутевыеЛисты.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_ПутевыеЛисты КАК вт_ПутевыеЛисты)
	|	И ПутевойЛистЗаправкиСливыГСМДоп.ТопливнаяКарта = ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка)
	|	И ПутевойЛистЗаправкиСливыГСМДоп.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.упВидыДвиженияТоплива.Заправка)";
	
	МассивОбъектов = Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс");
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	Выборка = ПакетЗапросов[3].Выбрать();
	Модифицированность = Истина;
	Объект.РасходыПоПутевымЛистам.Очистить();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.РасходыПоПутевымЛистам.Добавить();
		НоваяСтрока.Валюта = ВалютаУпрУчета;
		НоваяСтрока.ДокументОснование = Выборка.Ссылка;
		НоваяСтрока.НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
		НоваяСтрока.Сумма = Выборка.Стоимость;
		НоваяСтрока.СуммаНДС = Выборка.СтоимостьНДС;
		НоваяСтрока.СтатьяРасходов = Выборка.СтатьяРасходов;
		НоваяСтрока.СтавкаНДС = Выборка.СтавкаНДС;
		НоваяСтрока.ДатаВходногоДокумента = Выборка.ДатаВходногоДокумента;
		НоваяСтрока.НомерВходногоДокумента = Выборка.НомерВходногоДокумента;
		НоваяСтрока.НаименованиеВходногоДокумента = Выборка.НаименованиеВходногоДокумента;
	КонецЦикла;

КонецПроцедуры // ВыполнитЗаполнениеРасходовПоПутевымЛистам()

&НаКлиенте
Процедура РезультатПодбораРемонтов(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = ТИП("ДокументСсылка.упРемонтИТОТС") Тогда
		// Нет.
	ИначеЕсли ТипЗнч(Результат) = ТИП("СписокЗначений")Тогда
		ЗаполнитьРасходыПоРемонтам(Результат);
	КонецЕсли;	
КонецПроцедуры // РезультатПодбораРемонтов()

&НаСервере
Процедура ЗаполнитьРасходыПоРемонтам(СписокРемонтов)
	 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	упРемонтИТОТС.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ вт_Документы
		|ИЗ
		|	Документ.упРемонтИТОТС КАК упРемонтИТОТС
		|ГДЕ
		|	упРемонтИТОТС.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРемонтИТОТСРаботы.Ссылка КАК ДокументОснование,
		|	упРемонтИТОТСРаботы.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	упРемонтИТОТСРаботы.СтатьяРасходов КАК СтатьяРасходов,
		|	упРемонтИТОТСРаботы.Сумма КАК Сумма,
		|	упРемонтИТОТСРаботы.СуммаНДС КАК СуммаНДС,
		|	упРемонтИТОТСРаботы.Валюта КАК Валюта,
		|	упРемонтИТОТСРаботы.СтавкаНДС КАК СтавкаНДС,
		|	упРемонтИТОТСРаботы.Ссылка.ДатаВходногоДокумента КАК ДатаВходногоДокумента,
		|	упРемонтИТОТСРаботы.Ссылка.НомерВходногоДокумента КАК НомерВходногоДокумента,
		|	упРемонтИТОТСРаботы.Ссылка.НаименованиеВходногоДокумента КАК НаименованиеВходногоДокумента
		|ИЗ
		|	Документ.упРемонтИТОТС.Работы КАК упРемонтИТОТСРаботы
		|ГДЕ
		|	упРемонтИТОТСРаботы.Ссылка В
		|			(ВЫБРАТЬ
		|				вт_Документы.Ссылка КАК Ссылка
		|			ИЗ
		|				вт_Документы КАК вт_Документы)
		|	И упРемонтИТОТСРаботы.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.упДоступныеФормыОплаты.Наличная)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упРемонтИТОТСГСМ.Ссылка,
		|	упРемонтИТОТСГСМ.Ссылка.НаправлениеДеятельности,
		|	упРемонтИТОТСГСМ.ВидГСМ.СтатьяРасходов,
		|	упРемонтИТОТСГСМ.Сумма,
		|	упРемонтИТОТСГСМ.СуммаНДС,
		|	упРемонтИТОТСГСМ.Валюта,
		|	упРемонтИТОТСГСМ.СтавкаНДС,
		|	упРемонтИТОТСГСМ.Ссылка.ДатаВходногоДокумента,
		|	упРемонтИТОТСГСМ.Ссылка.НомерВходногоДокумента,
		|	упРемонтИТОТСГСМ.Ссылка.НаименованиеВходногоДокумента
		|ИЗ
		|	Документ.упРемонтИТОТС.ГСМ КАК упРемонтИТОТСГСМ
		|ГДЕ
		|	упРемонтИТОТСГСМ.Ссылка В
		|			(ВЫБРАТЬ
		|				вт_Документы.Ссылка КАК Ссылка
		|			ИЗ
		|				вт_Документы КАК вт_Документы)
		|	И упРемонтИТОТСГСМ.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.упДоступныеФормыОплаты.Наличная)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упРемонтИТОТСЗапчасти.Ссылка,
		|	упРемонтИТОТСЗапчасти.Ссылка.НаправлениеДеятельности,
		|	упРемонтИТОТСЗапчасти.СтатьяРасходов,
		|	упРемонтИТОТСЗапчасти.Сумма,
		|	упРемонтИТОТСЗапчасти.СуммаНДС,
		|	упРемонтИТОТСЗапчасти.Валюта,
		|	упРемонтИТОТСЗапчасти.СтавкаНДС,
		|	упРемонтИТОТСЗапчасти.Ссылка.ДатаВходногоДокумента,
		|	упРемонтИТОТСЗапчасти.Ссылка.НомерВходногоДокумента,
		|	упРемонтИТОТСЗапчасти.Ссылка.НаименованиеВходногоДокумента
		|ИЗ
		|	Документ.упРемонтИТОТС.Запчасти КАК упРемонтИТОТСЗапчасти
		|ГДЕ
		|	упРемонтИТОТСЗапчасти.Ссылка В
		|			(ВЫБРАТЬ
		|				вт_Документы.Ссылка КАК Ссылка
		|			ИЗ
		|				вт_Документы КАК вт_Документы)
		|	И упРемонтИТОТСЗапчасти.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.упДоступныеФормыОплаты.Наличная)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка.НаправлениеДеятельности,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка.ВидРемонта.СтатьяРасходов,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Сумма,
		|	упРемонтИТОТСШиныУзлыАгрегаты.СуммаНДС,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Валюта,
		|	упРемонтИТОТСШиныУзлыАгрегаты.СтавкаНДС,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка.ДатаВходногоДокумента,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка.НомерВходногоДокумента,
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка.НаименованиеВходногоДокумента
		|ИЗ
		|	Документ.упРемонтИТОТС.ШиныУзлыАгрегаты КАК упРемонтИТОТСШиныУзлыАгрегаты
		|ГДЕ
		|	упРемонтИТОТСШиныУзлыАгрегаты.Ссылка В
		|			(ВЫБРАТЬ
		|				вт_Документы.Ссылка КАК Ссылка
		|			ИЗ
		|				вт_Документы КАК вт_Документы)
		|	И упРемонтИТОТСШиныУзлыАгрегаты.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.упДоступныеФормыОплаты.Наличная)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", СписокРемонтов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.РасходыПоРемонтам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
		           
КонецПроцедуры // ЗапонлитьРасходыПоРемонтам()

&НаКлиенте
Процедура УстановитьПометкаБУ(ТекущаяПометка = Ложь)
	
	ТекущяТабличнаяЧасть = Неопределено;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаРасходыПоПутевымЛистам Тогда
		ТекущяТабличнаяЧасть = Объект.РасходыПоПутевымЛистам;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаРасходыПоРемонтам Тогда
		ТекущяТабличнаяЧасть = Объект.РасходыПоРемонтам;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаРасходы Тогда
		ТекущяТабличнаяЧасть = Объект.Расходы;		
	КонецЕсли;	
	Если НЕ ТекущяТабличнаяЧасть = Неопределено Тогда
		Для каждого СтрокаТабличнойЧасти Из ТекущяТабличнаяЧасть Цикл
			СтрокаТабличнойЧасти.БУ = ТекущаяПометка;
		КонецЦикла;
		Модифицированность = Истина;
	КонецЕсли;	
	
КонецПроцедуры // УстановитьПометкаБУ()

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	// Нет.

КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "Расходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	//ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Расходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаКлиенте
Процедура РасходыПоРемонтамПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РезультатПодбораПрочихРасходов(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого Строка Из Результат Цикл
			НоваяСтрока = Объект.Расходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ДатаВходногоДокумента = Строка.апДатаВходящегоДокумента;
			НоваяСтрока.НомерВходногоДокумента = Строка.апНомерВходящегоДокумента;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры // РезультатПодбораПрочихРасходов()
#КонецОбласти
