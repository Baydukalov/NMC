
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ТолькоПросмотр = НЕ упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийПробег(ТекущийОбъект.Ссылка, ТекущийОбъект.Дата, ТекущийОбъект.ТранспортноеСредство);
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.ТранспортноеСредство = Параметры.ТранспортноеСредство;
		Объект.Автор  = ПользователиКлиентСервер.ТекущийПользователь();
		Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
			сткПробегМоточасы = упПутевойЛистУчетГСМ.ПробегМоточасыПоРегиструОстатков(Объект.ТранспортноеСредство);
			ЗаполнитьЗначенияСвойств(Объект, сткПробегМоточасы);
		КонецЕсли;
		
	КонецЕсли;
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Объект.Дата);
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);
	НастроитьДоступностьВводаМоточасовПоТС();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство,ТекущийОбъект.Дата);
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Оповестить("КорректировкаПробега", Объект.ТранспортноеСредство);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписанТранспортноеСредство" 
		И Источник = Объект.ТранспортноеСредство Тогда
		НастроитьДоступностьВводаМоточасовПоТС();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Объект.Дата);
	НастроитьДоступностьВводаМоточасовПоТС();
КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьВводаМоточасовПоТС()

	СчетчикМоточасов = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		СчетчикМоточасов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТранспортноеСредство, "СчетчикМоточасов");
	КонецЕсли;
	
	СчетчикМоточасов = Объект.Моточасы > 0 ИЛИ СчетчикМоточасов;
	
	сткНастроек = Новый Структура;
	сткНастроек.Вставить("ИспользоватьСчетчикМоточасовТранспортныхСредств", СчетчикМоточасов);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);

КонецПроцедуры

#КонецОбласти
