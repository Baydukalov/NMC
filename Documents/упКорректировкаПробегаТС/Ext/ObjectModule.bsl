#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ЕстьБолееПоздниеДокументы(Отказ);
	
	Если НЕ Отказ Тогда
		РеквизитыОбъекта = Новый Структура("Дата", Дата);
		упПутевойЛистУчетГСМ.СформироватьДвижениеПробег(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ЕстьБолееПоздниеДокументы(Отказ);
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЕстьБолееПоздниеДокументы(Отказ)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упПробегТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", 	ТранспортноеСредство);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Отказ = НЕ упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийПробег(Ссылка, Дата, ТранспортноеСредство);
	
	Если Отказ Тогда
		ТекстСообщения = Нстр("ru = 'Существуют более поздние документы изменяющие пробег ТС.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 	

#КонецЕсли