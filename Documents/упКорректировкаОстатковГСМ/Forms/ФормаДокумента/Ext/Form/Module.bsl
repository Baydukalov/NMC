#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТолькоПросмотр = НЕ упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийОстаткиГСМ(ТекущийОбъект.Ссылка, ТекущийОбъект.Дата, ТекущийОбъект.ТранспортноеСредство, ТекущийОбъект.ВидГСМ);
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.ТранспортноеСредство = Параметры.ТранспортноеСредство;
		Объект.ВидГСМ = Параметры.ВидГСМ;
		Объект.Автор  = ПользователиКлиентСервер.ТекущийПользователь();
		
		Если Истина
			И ЗначениеЗаполнено(Объект.ТранспортноеСредство) 
			И ЗначениеЗаполнено(Объект.ВидГСМ)
		Тогда
			сткУровень = упПутевойЛистУчетГСМ.УровеньТопливаПоРегиструОстатков(Объект.ТранспортноеСредство, Объект.ВидГСМ);
			Объект.Количество = сткУровень.Количество;
			Объект.Стоимость  = сткУровень.Стоимость;
		КонецЕсли;
		
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.упВидыГСМТранспортныхСредств"));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти	

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	УстановитьВидГСМ();
КонецПроцедуры	

&НаКлиенте
Процедура ВидГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", СписокДоступныхВидовГСМ));
	ОткрытьФорму("Справочник.упВидыГСМ.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВидГСМАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", СписокДоступныхВидовГСМ);
	ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ВидГСМОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", СписокДоступныхВидовГСМ);
	ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Функция УстановитьВидГСМ()
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	Если СписокДоступныхВидовГСМ.Количество() = 1 Тогда
		Объект.ВидГСМ = СписокДоступныхВидовГСМ[0];
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьДоступныеВидыГСМНаСервере(ТранспортноеСредство)
	
	мсвДоступныхВидовГСМ = упПутевойЛистУчетГСМ.ВернутьДоступныеВидыГСМ(ТранспортноеСредство);
	Возврат мсвДоступныхВидовГСМ;
	
КонецФункции // ВернутьДоступныеВидыГСМНаСервере()

&НаКлиенте
// Процедура завершения после закрытия формы выбора вида ГСМ.
//
Процедура ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Объект.ВидГСМ = Результат;
	КонецЕсли;	
КонецПроцедуры // ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение()

#КонецОбласти


