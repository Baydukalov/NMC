#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
		
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор       = ПользователиКлиентСервер.ТекущийПользователь();
		// Если копирование
		Если НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.Рейс, Объект.Автор);
		КонецЕсли;
		
	КонецЕсли;
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.ДатаНарушения);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
		
	ПриСозданииПриЧтенииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.ДатаНарушения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ТранспортноеСредство)
			И НЕ ЗначениеЗаполнено(Объект.Водитель) 
			И ЗначениеЗаполнено(Объект.Рейс) Тогда
		ЗаполнитьДаныеЭкипажемПоРейсу();	
	КонецЕсли;
	
	
	ЗаполнитьПодразделение();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьДаныеЭкипажемПоРейсу()
	сткЭкипаж = упРейс.ПолучитьЭкипажРейса(Объект.Рейс);
	Объект.ТранспортноеСредство	= сткЭкипаж.ТранспортноеСредство;
	Объект.Водитель				= сткЭкипаж.Водитель1
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодразделение()
	
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.Рейс);
		Если ЗначениеЗаполнено(Подразделение)
				И НЕ Подразделение = Объект.Подразделение Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

