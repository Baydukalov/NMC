#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Документы.упСнятиеСУчетаТранспортногоСредства.ПолучениеПривязаныхПрицеповТСНаСервере(ДатаСнятияСУчета, ТранспортноеСредство, ДополнительныеСвойства);
	Документы.упСнятиеСУчетаТранспортногоСредства.ПолучитьАгрегатыТС(ДатаСнятияСУчета, ТранспортноеСредство, ДополнительныеСвойства);	
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упУчетныеПараметрыТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	Если ДополнительныеСвойства.Свойство("Прицепы") Тогда		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрицепыТранспортныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.Прицепы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Прицеп", "Прицеп");
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Агрегаты") Тогда		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упАгрегатыТранспортныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.Агрегаты;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	КонецЕсли;
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	ГраницаИсключая = Новый Граница(ДатаСнятияСУчета, ВидГраницы.Исключая);
	сткСостояниеТС	= упУчетТранспортныхСредств.УчетноеСостояниеТранспортногоСредства(ТранспортноеСредство, ГраницаИсключая);
	
	Если НЕ сткСостояниеТС.ПринятоКУчету Тогда
		ТекстСообщения = Нстр("ru = 'Транспортное средство %1 на %2 еще не принято к учету'");
		ТекстСообщения = СтрШаблон(ТекстСообщения,ТранспортноеСредство,ДатаСнятияСУчета);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,,,Отказ);
		Возврат;
	КонецЕсли;
	
	докСнятияСУчета = упУчетТранспортныхСредств.ДокументСнятияСУчетаТранспортногоСредства(ТранспортноеСредство);
	
	Если ЗначениеЗаполнено(докСнятияСУчета) И НЕ докСнятияСУчета = Ссылка Тогда
		ТекстСообщения = Нстр("ru = 'Транспортное средство %1 уже снято с учета документом %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения,ТранспортноеСредство,докСнятияСУчета);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,докСнятияСУчета,,,Отказ);
		Возврат;
	КонецЕсли;
	
	Граница = Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая);
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", 	ТранспортноеСредство);
	РеквизитыОбъекта.Вставить("Дата", 				    ДатаСнятияСУчета);	
	РеквизитыОбъекта.Вставить("Граница", 				Граница);

	ДополнительныеСвойства.Вставить("Состояние", Справочники.упСостоянияТС.Недоступно);
	ДополнительныеСвойства.Вставить("ДатаИзмененияСостояния", ДатаСнятияСУчета);
	
	// регистр упСостоянияТранспортныхСредств.
	упУчетТранспортныхСредств.СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// регистр упУчетныеПараметрыТранспортныхСредств.
	упУчетТранспортныхСредств.СформироватьДвижениеУчетныеПараметрыТССнятиеСУчета(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// Отвязка прицепа.
	упУчетТранспортныхСредств.СформироватьДвиженияПрицепыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Отвязка агрегатов.
	упУчетТранспортныхСредств.СформироватьДвижениеАгрегатовТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 

#КонецЕсли
