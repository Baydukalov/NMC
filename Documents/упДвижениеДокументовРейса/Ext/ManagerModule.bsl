#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполним формирование движений документа.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текстовое представлениеошибки.
//  Отказ  - Булево - Флаг что произошел отказ создания движения.
//
// Возвращаемое значение:
//   ДокументСсылка   - Ссылка на документ.
//
Функция СформироватьДвижениеДокументовРейса(Рейс, ТекстОшибки, Отказ) Экспорт
	
	текДокумент = Неопределено;
	
	Статус = Документы.упРейс.ПолучитьСтатус(Рейс);
	Если Не (Статус = Перечисления.упСтатусыРейса.ВыполненЧастично 
				Или	Статус = Перечисления.упСтатусыРейса.Выполнен 
				Или Статус = Перечисления.упСтатусыРейса.Завершен) Тогда
		ТекстОшибки = НСтр("ru = 'Запрещено отражение непереданных/недополученных документов по рейсу пока он не выполнен или не завершен'");
		Отказ = Истина;
	КонецЕсли; 
	
	Если Не Отказ Тогда
		
		ТаблицаОстатков = ПолучитьОстаткиПоДвижениямДокументовРейса(Рейс);
		Если ТаблицаОстатков = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'По рейсу отсутствуют непереданные/недополученные документы. Создание документа невозможно'");
			Отказ = Истина;
			
		Иначе
			НовыйДокумент = Документы.упДвижениеДокументовРейса.СоздатьДокумент();
			НовыйДокумент.Дата = ТекущаяДатаСеанса();
			НовыйДокумент.Рейс = Рейс;
			Для каждого текСтрока Из ТаблицаОстатков Цикл
				ЗаполнитьЗначенияСвойств(НовыйДокумент.Работы.Добавить(), текСтрока);
			КонецЦикла;
			
			НовыйДокумент.Записать();
			текДокумент = НовыйДокумент.Ссылка;
			
		КонецЕсли;	
	КонецЕсли;
	
	Возврат текДокумент;
	
КонецФункции // СформироватьДвижениеДокументовРейса()

// Выполним формирование движений документа.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текстовое представлениеошибки.
//  Отказ  - Булево - Флаг что произошел отказ создания движения.
//
// Возвращаемое значение:
//   ДокументСсылка   - Ссылка на документ.
//
Функция СформироватьВозвратДокументовПослеИсполненияРейса(Рейс, ТекстОшибки, Отказ) Экспорт
	
	текДокумент = Неопределено;
	
	Статус = Документы.упРейс.ПолучитьСтатус(Рейс);
	Если Не (Статус = Перечисления.упСтатусыРейса.ВыполненЧастично  
				Или Статус = Перечисления.упСтатусыРейса.Выполнен 
				Или Статус = Перечисления.упСтатусыРейса.Завершен) Тогда
		ТекстОшибки = НСтр("ru = 'Запрещено отражение непереданных/недополученных документов по рейсу пока он не выполнен или не завершен'");
		Отказ = Истина;
	КонецЕсли; 
	
	Если Не Отказ Тогда
		
		ТаблицаОстатков = ПолучитьОстаткиПоНевозвращеннымДокументам(Рейс);
		Если ТаблицаОстатков = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'По рейсу отсутствуют невозвращенные документы. Создание документа невозможно'");
			Отказ = Истина;
			
		Иначе
			НовыйДокумент = Документы.упДвижениеДокументовРейса.СоздатьДокумент();
			НовыйДокумент.Дата = ТекущаяДатаСеанса();
			НовыйДокумент.Рейс = Рейс;
			НовыйДокумент.ВозвратДокументовПослеИсполненияРейса = Истина;
			Для каждого текСтрока Из ТаблицаОстатков Цикл
				ЗаполнитьЗначенияСвойств(НовыйДокумент.ДокументыПоРейсу.Добавить(), текСтрока);
			КонецЦикла;
			
			НовыйДокумент.Записать();
			текДокумент = НовыйДокумент.Ссылка;
			
		КонецЕсли;	
	КонецЕсли;
	
	Возврат текДокумент;	
	
КонецФункции // СформироватьВозвратДокументовПослеИсполненияРейса()

// Получим остаток по документу рейс.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  Регистратор  - ДокументСсылка - Ссылка на документ.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Ответ проверки.
//
Функция ПолучитьОстаткиПоДвижениямДокументовРейса(Рейс, Регистратор = Неопределено) Экспорт
	
	ТаблицаОстатков = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРаботыПоРейсу.Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы,
	|	упРаботыПоРейсу.Задание,
	|	упРаботыПоРейсу.Операция,
	|	упРаботыПоРейсу.ТипРегламентногоДокумента,
	|	СУММА(упРаботыПоРейсу.КоличествоЭкземпляровДокумента) КАК КоличествоЭкземпляровДокумента
	|ПОМЕСТИТЬ втПлановыеРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Регистратор = &Рейс
	|	И упРаботыПоРейсу.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов))
	|
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсу.Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы,
	|	упРаботыПоРейсу.Задание,
	|	упРаботыПоРейсу.Операция,
	|	упРаботыПоРейсу.ТипРегламентногоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы,
	|	упРаботыПоРейсу.Задание,
	|	упРаботыПоРейсу.Операция,
	|	упРаботыПоРейсу.ТипРегламентногоДокумента,
	|	упРаботыПоРейсу.КоличествоЭкземпляровДокумента
	|ПОМЕСТИТЬ втФактическиеРаботыПодготовка
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПолучениеДокументов), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаДокументов))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упРаботыПоРейсу.Идентификатор,
	|	упРаботыПоРейсу.ИдентификаторРаботы,
	|	упРаботыПоРейсу.Задание,
	|	упРаботыПоРейсу.Операция,
	|	упРаботыПоРейсу.ТипРегламентногоДокумента,
	|	-упРаботыПоРейсу.КоличествоЭкземпляровДокумента
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ
	|	упРаботыПоРейсу.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втФактическиеРаботы.Идентификатор,
	|	втФактическиеРаботы.ИдентификаторРаботы,
	|	втФактическиеРаботы.Задание,
	|	втФактическиеРаботы.Операция,
	|	втФактическиеРаботы.ТипРегламентногоДокумента,
	|	СУММА(втФактическиеРаботы.КоличествоЭкземпляровДокумента) КАК КоличествоЭкземпляровДокумента
	|ПОМЕСТИТЬ втФактическиеРаботы
	|ИЗ
	|	втФактическиеРаботыПодготовка КАК втФактическиеРаботы
	|
	|СГРУППИРОВАТЬ ПО
	|	втФактическиеРаботы.Идентификатор,
	|	втФактическиеРаботы.ИдентификаторРаботы,
	|	втФактическиеРаботы.Задание,
	|	втФактическиеРаботы.Операция,
	|	втФактическиеРаботы.ТипРегламентногоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПлановыеРаботы.Идентификатор,
	|	втПлановыеРаботы.ИдентификаторРаботы,
	|	втПлановыеРаботы.Задание,
	|	втПлановыеРаботы.Операция,
	|	втПлановыеРаботы.ТипРегламентногоДокумента,
	|	втПлановыеРаботы.КоличествоЭкземпляровДокумента - ЕСТЬNULL(втФактическиеРаботы.КоличествоЭкземпляровДокумента, 0) КАК КоличествоЭкземпляровДокумента
	|ИЗ
	|	втПлановыеРаботы КАК втПлановыеРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втФактическиеРаботы КАК втФактическиеРаботы
	|		ПО втПлановыеРаботы.Идентификатор = втФактическиеРаботы.Идентификатор
	|			И втПлановыеРаботы.ИдентификаторРаботы = втФактическиеРаботы.ИдентификаторРаботы
	|			И втПлановыеРаботы.Задание = втФактическиеРаботы.Задание
	|			И втПлановыеРаботы.Операция = втФактическиеРаботы.Операция
	|			И втПлановыеРаботы.ТипРегламентногоДокумента = втФактическиеРаботы.ТипРегламентногоДокумента
	|ГДЕ
	|	втПлановыеРаботы.КоличествоЭкземпляровДокумента - ЕСТЬNULL(втФактическиеРаботы.КоличествоЭкземпляровДокумента, 0) > 0";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТаблицаОстатков = Результат.Выгрузить();
	КонецЕсли; 
	
	Возврат ТаблицаОстатков;
	
КонецФункции

// Получим остаток по документу рейс.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  Регистратор  - ДокументСсылка - Ссылка на документ.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Ответ проверки.
//
Функция ПолучитьОстаткиПоНевозвращеннымДокументам(Рейс, Регистратор = Неопределено) Экспорт
	
	ТаблицаОстатков = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упНепереданныеДокументыОстатки.ЗаданиеНаПеревозку,
	|	упНепереданныеДокументыОстатки.ТипРегламентногоДокумента,
	|	упНепереданныеДокументыОстатки.Операция,
	|	упНепереданныеДокументыОстатки.ЭкземплярДокумента,
	|	упНепереданныеДокументыОстатки.КоличествоОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.упНепереданныеДокументы.Остатки(, Рейс = &Рейс) КАК упНепереданныеДокументыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упНепереданныеДокументы.ЗаданиеНаПеревозку,
	|	упНепереданныеДокументы.ТипРегламентногоДокумента,
	|	упНепереданныеДокументы.Операция,
	|	упНепереданныеДокументы.ЭкземплярДокумента,
	|	-упНепереданныеДокументы.Количество
	|ИЗ
	|	РегистрНакопления.упНепереданныеДокументы КАК упНепереданныеДокументы
	|ГДЕ
	|	упНепереданныеДокументы.Рейс = &Рейс
	|	И упНепереданныеДокументы.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.ЗаданиеНаПеревозку,
	|	втОстатки.ТипРегламентногоДокумента,
	|	втОстатки.Операция,
	|	втОстатки.ЭкземплярДокумента,
	|	СУММА(втОстатки.КоличествоОстаток) КАК Количество
	|ИЗ
	|	втОстатки КАК втОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.ЗаданиеНаПеревозку,
	|	втОстатки.ТипРегламентногоДокумента,
	|	втОстатки.Операция,
	|	втОстатки.ЭкземплярДокумента
	|
	|ИМЕЮЩИЕ
	|	СУММА(втОстатки.КоличествоОстаток) > 0";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТаблицаОстатков = Результат.Выгрузить();
	КонецЕсли; 
	
	Возврат ТаблицаОстатков;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
