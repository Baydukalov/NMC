#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	
	Если ВозвратДокументовПослеИсполненияРейса Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНепереданныеДокументы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс)
	Иначе
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		ЭлементБлокировки.ИсточникДанных = Работы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Идентификатор"      , "Идентификатор");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторРаботы", "ИдентификаторРаботы");
	КонецЕсли; 
		
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	ДвиженияНепереданныеДокументы				= Движения.упНепереданныеДокументы;
	ДвиженияНепереданныеДокументы.Записывать	= Истина;
	
	ДвиженияРаботыПоРейсу				= Движения.упРаботыПоРейсу;
	ДвиженияРаботыПоРейсу.Записывать	= Истина;
	
	Если ВозвратДокументовПослеИсполненияРейса Тогда
		Если ДокументыПоРейсу.Количество() Тогда
			Для каждого текСтрока Из ДокументыПоРейсу Цикл
				Если НЕ текСтрока.Пометка Тогда
					Продолжить;
				КонецЕсли;
				Движение = ДвиженияНепереданныеДокументы.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				ЗаполнитьЗначенияСвойств(Движение, текСтрока);
				Движение.Период = Дата;
				Движение.Рейс = Рейс;
			КонецЦикла;
		КонецЕсли; 
		
	Иначе	
		Если Работы.Количество() Тогда
			соотИдентификаторов = ПолучитьПоследниеНомераРабот(Ссылка, Рейс);
			текИдентификатор = Неопределено;
			Для каждого текРабота Из Работы Цикл
				Если текИдентификатор <> текРабота.Идентификатор Тогда
					текИдентификатор = текРабота.Идентификатор;
					Сч = соотИдентификаторов.Получить(текИдентификатор);
				КонецЕсли; 
				Сч = Сч + 1;
				ДвижениеРабота = ДвиженияРаботыПоРейсу.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеРабота, текРабота);
				ДвижениеРабота.Период = Дата;
				ДвижениеРабота.Рейс = Рейс;
				ДвижениеРабота.СтрокаНомер = Сч;
			КонецЦикла; 
		КонецЕсли;
		
	КонецЕсли; 
	
	Движения.Записать();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Отказ = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПоследниеНомераРабот(Ссылка, Рейс)
	
	соотИдентификаторов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упДвижениеДокументовРейсаРаботы.Идентификатор
	|ПОМЕСТИТЬ втИдентификаторы
	|ИЗ
	|	Документ.упДвижениеДокументовРейса.Работы КАК упДвижениеДокументовРейсаРаботы
	|ГДЕ
	|	упДвижениеДокументовРейсаРаботы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Идентификатор,
	|	МАКСИМУМ(упРаботыПоРейсуСрезПоследних.СтрокаНомер) КАК СтрокаНомер
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И Идентификатор В
	|					(ВЫБРАТЬ
	|						втИдентификаторы.Идентификатор
	|					ИЗ
	|						втИдентификаторы)) КАК упРаботыПоРейсуСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсуСрезПоследних.Идентификатор";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			соотИдентификаторов.Вставить(Выборка.Идентификатор, Выборка.СтрокаНомер);
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат соотИдентификаторов;
	
КонецФункции
	
#КонецОбласти

#КонецЕсли
