
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если ТекущийОбъект.Проведен Тогда
		ТолькоПросмотр = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ТекущийОбъект);
	КонецЕсли;
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
			
	упПрохождениеТочки.УстановитьЗаголовокФормыДвиженияДокументовРейса(ЭтотОбъект);
	
	ОбновитьДеревоРабот();
	
	ЭтаФорма.РежимДокумента = Объект.ВозвратДокументовПослеИсполненияРейса;
	
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Тогда
		ЭтаФорма.РежимДокумента = 1;
		Элементы.РежимДокумента.Видимость = Ложь;
	КонецЕсли; 
	
	Если Не ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
		ЭтаФорма.РежимДокумента = 0;
		Элементы.РежимДокумента.Видимость = Ложь;
	КонецЕсли; 
	
	Если ЭтаФорма.РежимДокумента = 0 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДеревоРабот;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДокументыПоРейсу;
	КонецЕсли; 
	
	УстановитьВидимостьКолонокДокументовПоРейсу();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПриСозданииПриЧтенииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьДеревоНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Работы = ТекущийОбъект.Работы;
	
	тзРаботы = Работы.ВыгрузитьКолонки();
	ЭлементыТочки = ДеревоРабот.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыТочки Цикл
		ЭлементыРаботы = текТочка.ПолучитьЭлементы();
		Для каждого текРабота Из ЭлементыРаботы Цикл
			ЗаполнитьЗначенияСвойств(тзРаботы.Добавить(), текРабота);
		КонецЦикла; 
	КонецЦикла; 
	
	Работы.Загрузить(тзРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упПрохождениеТочки.УстановитьЗаголовокФормыДвиженияДокументовРейса(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ОбновитьРаботы",, Объект.Рейс);	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Рейс) Тогда
		Если РежимДокумента = 0 Тогда
			ДеревоРабот.ПолучитьЭлементы().Очистить();
		Иначе
			Объект.ДокументыПоРейсу.Очистить();
		КонецЕсли; 
	Иначе
		УстановитьВидимостьКолонокДокументовПоРейсу();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РейсОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.Рейс Тогда
		Если РежимДокумента = 0 Тогда
			ДеревоРабот.ПолучитьЭлементы().Очистить();
		Иначе
			Объект.ДокументыПоРейсу.Очистить();
		КонецЕсли; 
		УстановитьВидимостьКолонокДокументовПоРейсу();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РежимДокументаПриИзменении(Элемент)
	
	Если РежимДокумента = 0 Тогда
		Объект.ВозвратДокументовПослеИсполненияРейса = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДеревоРабот;
	Иначе
		Объект.ВозвратДокументовПослеИсполненияРейса = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДокументыПоРейсу;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СвернутьДерево(Команда)
	
	ЭлементыТочки = ДеревоРабот.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыТочки Цикл
		Элементы.ДеревоРабот.Свернуть(текТочка.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте

Процедура РазвернутьДерево(Команда)
	
	РазвернутьДеревоНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	
	Если ДеревоРабот.ПолучитьЭлементы().Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Дерево работ будет перезаполнено. Продолжить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОстаткамВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПерезаполнитьРаботыПоОстаткам();
		РазвернутьДеревоНаКлиенте();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамДокументовПоРейсу(Команда)
	
	Если Объект.ДокументыПоРейсу.Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Указанные документы по рейсу будут перезаполнены. Продолжить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОстаткамДокументовПоРейсуВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПерезаполнитьДокументыПоРейсуПоОстаткам();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для каждого Строка Из Объект.ДокументыПоРейсу Цикл
		Строка.Пометка = Истина;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для каждого Строка Из Объект.ДокументыПоРейсу Цикл
		Строка.Пометка = Ложь;
	КонецЦикла; 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РазвернутьДеревоНаКлиенте()
	
	ЭлементыТочки = ДеревоРабот.ПолучитьЭлементы();
	Для каждого текТочка Из ЭлементыТочки Цикл
		Элементы.ДеревоРабот.Развернуть(текТочка.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Заполнение таблицы работ после утвердительного ответа.
//
Процедура ЗаполнитьПоОстаткамВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПерезаполнитьРаботыПоОстаткам();
		РазвернутьДеревоНаКлиенте();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Заполнение таблицы документов рейса после утвердительного ответа.
//
Процедура ЗаполнитьПоОстаткамДокументовПоРейсуВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПерезаполнитьДокументыПоРейсуПоОстаткам();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоРабот()
	
	ЭтаФорма.ДеревоРабот.ПолучитьЭлементы().Очистить();
	
	Если Объект.Работы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Работы = Объект.Работы;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Рейс", Объект.Рейс);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.Адрес,
	|	упМаршрутПоРейсуСрезПоследних.ТипТочки.Порядок КАК ИндексКартинки,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ВыборкаТочек = Результат.Выбрать();
		ЭлементыТочки = ЭтаФорма.ДеревоРабот.ПолучитьЭлементы();
		Пока ВыборкаТочек.Следующий() Цикл
			ИскомыеСтроки = Работы.НайтиСтроки(Новый Структура("Идентификатор", ВыборкаТочек.Идентификатор));
			Если ИскомыеСтроки.Количество() Тогда
				НоваяТочка = ЭлементыТочки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяТочка, ВыборкаТочек);
				НоваяТочка.НазначениеСтроки = ВыборкаТочек.Адрес;
				ЭлементыРаботы = НоваяТочка.ПолучитьЭлементы();
				Для каждого текРабота Из ИскомыеСтроки Цикл
					НоваяРабота = ЭлементыРаботы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяРабота, текРабота);
					НоваяРабота.НазначениеСтроки = текРабота.Операция;
					НоваяРабота.ИндексКартинки = Общегоназначения.ЗначениеРеквизитаОбъекта(текРабота.Операция, "Порядок") + 4;
				КонецЦикла; 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьРаботыПоОстаткам()
	
	Объект.Работы.Очистить();
	
	ТаблицаОстатков = Документы.упДвижениеДокументовРейса.ПолучитьОстаткиПоДвижениямДокументовРейса(Объект.Рейс, Объект.Ссылка);
	Если Не ТаблицаОстатков = Неопределено Тогда
		Для каждого текСтрока Из ТаблицаОстатков Цикл
			ЗаполнитьЗначенияСвойств(Объект.Работы.Добавить(), текСтрока);
		КонецЦикла; 
		ОбновитьДеревоРабот();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'По рейсу отсутствуют непереданные/недополученные документы в точках'"));
	КонецЕсли; 
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДокументыПоРейсуПоОстаткам()
	
	Объект.ДокументыПоРейсу.Очистить();
	
	ТаблицаОстатков = Документы.упДвижениеДокументовРейса.ПолучитьОстаткиПоНевозвращеннымДокументам(Объект.Рейс, Объект.Ссылка);
	Если Не ТаблицаОстатков = Неопределено Тогда
		Для каждого текСтрока Из ТаблицаОстатков Цикл
			ЗаполнитьЗначенияСвойств(Объект.ДокументыПоРейсу.Добавить(), текСтрока);
		КонецЦикла; 
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'По рейсу отсутствуют невозвращенные документы'"));
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонокДокументовПоРейсу()
	
	Если ЗначениеЗаполнено(Объект.Рейс) Тогда
		
		ЭтаФорма.СпособУчетаВозвращенныхДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Рейс, "ВидПеревозки.СпособУчетаВозвращенныхДокументов");
		
		Если ЭтаФорма.СпособУчетаВозвращенныхДокументов = Перечисления.упСпособыУчетаВозвращенныхДокументов.СТочностьюДоТиповДокументов Тогда
			 Элементы.ДокументыПоРейсуТипОперации.Видимость = Истина;
			 Элементы.ДокументыПоРейсуЭкземплярДокумента.Видимость = Ложь;
		Иначе	
			Элементы.ДокументыПоРейсуТипОперации.Видимость = Ложь;	
			Элементы.ДокументыПоРейсуЭкземплярДокумента.Видимость = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 