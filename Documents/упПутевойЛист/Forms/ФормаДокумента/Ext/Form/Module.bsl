#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ИспользуетсяУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	ИспользуетсяСпутниковыйМониторинг = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг");
	ИспользуетсяУчетПодразделений = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	РазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов = ПолучитьФункциональнуюОпцию("упРазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов");	
	
	ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств = ПолучитьФункциональнуюОпцию("упВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств");
	
	УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, Ложь, Ложь, Ложь);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
	КонецЕсли;
	
	ЭтаФорма.ТипПечатнойФормыПутевогоЛиста = Неопределено;
	РейсДляВидаПеревозки = Неопределено;
	Если Объект.Рейсы.Количество() > 1 Тогда
		РейсДляВидаПеревозки = Объект.Рейсы[0].Рейс;
	Иначе
		РейсДляВидаПеревозки = Документы.упРейс.ПустаяСсылка();	
	КонецЕсли;
			
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТипПечатнойФормыПутевогоЛиста = упПутевойЛистУчетГСМ.ПолучитьТипПечатнойФормы(Объект.ТранспортноеСредство);
		ЭтаФорма.Статус         = Документы.упПутевойЛист.ПолучитьСтатус(Объект.Ссылка);	
		ЭтаФорма.ТолькоПросмотр = Ложь;
		Для каждого Строка Из Объект.ЗаправкиСливыГСМ Цикл
			Строка.ЭтоСклад = ЗначениеЗаполнено(Строка.ПриемникСливаГСМ) И ТипЗнч(Строка.ПриемникСливаГСМ) = Тип("СправочникСсылка.упСклады");
		КонецЦикла;		
	Иначе	
		ЭтаФорма.Статус = Перечисления.упСтатусыПутевогоЛиста.Новый;
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		
		Если Истина
			И ИспользуетсяУчетПодразделений 
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) 
		Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(РейсДляВидаПеревозки, Объект.Автор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РейсДляВидаПеревозки) Тогда
			Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
				Объект.ЧасовойПояс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РейсДляВидаПеревозки, "ЧасовойПояс");	
			КонецЕсли;
			Объект.ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РейсДляВидаПеревозки, "ВидТранспортнойУслуги");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ПорядокИсполненияЗаявки) Тогда
			Объект.ПорядокИсполненияЗаявки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ПорядокИсполненияЗаявки", Перечисления.упПорядокИсполненияЗаявок.ПустаяСсылка());
		КонецЕсли;
				
	КонецЕсли;
	
	ДанныеПоМаршрутуВводятсяВПутевомЛисте = Ложь
	                                        Или НЕ ИспользуетсяУправлениеПеревозками
	                                        Или Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаПассажиров
									Или Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.УслугиСпецтехники;
	
	ЗаполнитьДополнительныеПоляТабличныхЧастей();
		
	// Доступ к кнопке рассчитать конечный остаток.
	РольДоступнаРазработчик = РольДоступна("упРазработчик");
	РольДоступнаПолныеПрава = РольДоступна("ПолныеПрава");
	РольДоступнаРасчетОстатковТопливаПоНормативномуРасходу = РольДоступна("упРасчетОстатковТопливаПоНормативномуРасходу");
	
	ДоступнаКнопкаРассчитатьКонечныйОстаток =  РольДоступнаРазработчик ИЛИ РольДоступнаПолныеПрава ИЛИ РольДоступнаРасчетОстатковТопливаПоНормативномуРасходу;
	
	ЗаблокироватьЭлементы();
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаПутевогоЛиста(Объект.Ссылка, Статус, Элементы);
	
	ОтобразитьПрицепы();
	
	ЗаполнитьКоличествоДопОборудования();
	
	ВычислитьПройденноеРасстояние();
		
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ВедетсяВалютныйУчет Тогда
		ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета(); 
		ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	Элементы.ГруппаДополнительноеОборудование.Видимость = ИспользуетсяУчетДополнительногоОборудования;
	
	Если НЕ ДанныеПоМаршрутуВводятсяВПутевомЛисте Тогда
		Если РазрешитьВыпискуОдногоПутевогоЛистаНаНесколькоРейсов И Объект.Рейсы.Количество() > 1 Тогда
			Элементы.ГруппаДобавитьРейс.Видимость = Ложь;
			Элементы.ГруппаРейсы.Видимость = Истина;
		Иначе
			Элементы.ГруппаДобавитьРейс.Видимость = Истина;
			Элементы.ГруппаРейсы.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// При открытии документа из РМ диспетчера гаража для заправки.
	Если Параметры.Свойство("ЗаправкиСливыГСМ")Тогда
		Элементы.ГруппаГСМ.ТекущаяСтраница = Элементы.ГруппаЗаправкиСливыГСМ;
	КонецЕсли;
	
	НастроитьДоступностьВводаМоточасовПоТС();
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	УстановитьВидФормыВЗависимостиОтДоступныхКомпонент();
	
	Если ДанныеПоМаршрутуВводятсяВПутевомЛисте Тогда
		ОбновитьДанныеФормыПриОткрытии();	
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ВедетсяРаботаВРазныхЧасовыхПоясах 			= ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	ИспользоватьДополнительныеОтчетыИОбработки 	= ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки");
	ИспользуетсяУчетДополнительногоОборудования 	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования");	
	ИспользуетсяУчетМатериалов 				= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМатериалов");
	ВедетсяУчетДокументов               		= ПолучитьФункциональнуюОпцию("упВедетсяУчетДокументовТранспортныхСредствВодителей");
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИзменитьОтобратьНормыПоТекущемуВидуГСМ(Ложь);
	ИзменитьОтобратьСтрокиТСПоТекущемуДопОборудованию(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	// Проверка отсутствия пустых адресов в ТЧ маршрут.
	Если упКорректировкаРейса.Маршрут.Количество() Тогда
		ТекстПредупреждения = "ru= 'Не заполнен адрес табличной части вкладки ""Данные рейса"" :";
		Для Каждого СтрокаМаршрута Из упКорректировкаРейса.Маршрут Цикл
			Если Не ЗначениеЗаполнено(СтрокаМаршрута.Адрес) Тогда
				Отказ = Истина;
				ТекстПредупреждения = ТекстПредупреждения + " Строка " + СтрокаМаршрута.НомерСтроки + " ;";
			КонецЕсли;	
		КонецЦикла;
		ТекстПредупреждения = ТекстПредупреждения + " ' ";
		Если Отказ Тогда
			ПоказатьПредупреждение(,НСтр(ТекстПредупреждения));
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ДанныеПоМаршрутуВводятсяВПутевомЛисте Тогда
		КорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		ТекущийОбъект.ДополнительныеСвойства.Вставить("КорректировкаРейса", КорректировкаМаршрутаОбъект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;	
	НовыйСтатус = Неопределено;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ДанныеПоМаршрутуВводятсяВПутевомЛисте Тогда
		КорректировкаМаршрутаОбъект = ТекущийОбъект.ДополнительныеСвойства.КорректировкаРейса;
		ЗначениеВРеквизитФормы(КорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	КонецЕсли;
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда	
		ИзменилсяСтатус();
	КонецЕсли;	
	
	ОтобразитьПрицепы();
	
	ВычислитьПройденноеРасстояние();

	Элементы.ГСМЗаполнитьГСМПоУмолчанию.Доступность = НЕ ТекущийОбъект.Проведен;
	ЗаполнитьДополнительныеПоляТабличныхЧастей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.РежимЗаписи  = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Для каждого Строка Из Объект.Заявки Цикл
			Оповестить("СменаСтатуса",,Строка.ЗаявкаНаПеревозкуГруза);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// При статусе "Отменен" ничего проверять не надо.
	Если Ложь
		Или Статус = Перечисления.упСтатусыПутевогоЛиста.Отменен 
		Или НовыйСтатус = Перечисления.упСтатусыПутевогоЛиста.Отменен 
	Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
		
	КонецЕсли; 
	  	
	// Проверить заполнение заправок и сливов.
	Для каждого СтрокаЗаправки Из Объект.ЗаправкиСливыГСМ Цикл
				
		// Проверить, что дата заправки/слива не меньше даты документа.
		Если Истина
			И ЗначениеЗаполнено(СтрокаЗаправки.ДатаЗаправкиСлива)
			И СтрокаЗаправки.ДатаЗаправкиСлива < Объект.Дата 
		Тогда
			
			ТекстОшибки = НСтр("ru = 'Дата %1 должна быть позже даты документа'");
			текВидДвижения = ?(СтрокаЗаправки.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка"), НСтр("ru = 'заправки'"), НСтр("ru = 'слива'"));
			ТекстОшибки    = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, текВидДвижения);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМ",СтрокаЗаправки.НомерСтроки,"ДатаЗаправкиСлива"),,Отказ);
			Прервать;
			
		КонецЕсли;
						
		// Проверить, что для всех сливов на склад заполнен ГСМ.
		Если Истина
			И ИспользуетсяУчетМатериалов 
			И ЗначениеЗаполнено(СтрокаЗаправки.ПриемникСливаГСМ) 
			И ТипЗнч(СтрокаЗаправки.ПриемникСливаГСМ) = Тип("СправочникСсылка.упСклады") 
			И НЕ ЗначениеЗаполнено(СтрокаЗаправки.ГСМ) 
		Тогда
			
			ТекстОшибки = НСтр("ru = 'Для слива ГСМ на склад необходимо заполнить поле ГСМ'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМ",СтрокаЗаправки.НомерСтроки,"ГСМ"),,Отказ);
			Прервать;
			
		КонецЕсли;

	КонецЦикла;
		
	
	// Проверить при статусе "Возвращен".
	Если Истина
		И НЕ Отказ 
		И (Ложь
		   Или Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен 
		   Или НовыйСтатус = Перечисления.упСтатусыПутевогоЛиста.Возвращен) 
	Тогда 
		
		// Проверить, что конечный пробег больше начального.
		Если Ложь
			Или (Истина
				И НЕ ИспользуетсяСчетчикМоточасов
				И Объект.ПробегНачало >= Объект.ПробегОкончание)
			Или (Истина
				И ИспользуетсяСчетчикМоточасов
				И Объект.ПробегНачало > Объект.ПробегОкончание) 
		Тогда
			ТекстОшибки = НСтр("ru = 'Конечный пробег должен быть больше начального'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ПробегОкончание","Объект",Отказ);
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			
			Для каждого Строка Из Объект.ДополнительноеОборудование Цикл
				Если Строка.МоточасыНачало > Строка.МоточасыОкончание Тогда
					ТекстОшибки = НСтр("ru = 'Конечные моточасы должены быть не меньше начальных'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Объект.Ссылка,"Объект.ДополнительноеОборудование[" + Формат(Строка.НомерСтроки - 1,"ЧН=0; ЧГ=0") + "].МоточасыОкончание",,Отказ);							
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		// Проверить, что пробег с грузом не превышает пробег.
		Если НЕ Отказ Тогда
			
			Если Объект.ПробегСГрузом > Объект.Пробег Тогда
				ТекстОшибки = НСтр("ru = 'Пробег с грузом не должен быть больше общего пробега'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ПробегСГрузом","Объект",Отказ);
			КонецЕсли;

		КонецЕсли;
		
		// Проверить, что по ТС есть строка с основным видом ГСМ и заполнен конечный остаток топлива.
		//Если НЕ Отказ Тогда
		//	
		//	текОсновнойВидГСМПоТС = упПутевойЛистУчетГСМ.ОсновнойВидГСМПоТранспортномуСредству(Объект.ТранспортноеСредство);
		//	
		//	Если ЗначениеЗаполнено(текОсновнойВидГСМПоТС) Тогда
		//		мсвСтроки = Объект.ГСМ.НайтиСтроки(Новый Структура("ВидГСМ, ДопОборудование", текОсновнойВидГСМПоТС, Справочники.упДополнительноеОборудование.ПустаяСсылка()));
		//		Если мсвСтроки.Количество() > 0 Тогда
		//			СтрокаСГСМ = мсвСтроки[0];
		//			Если СтрокаСГСМ.КонОстаток = 0 Тогда
		//				ТекстОшибки = НСтр("ru = 'Не заполнен конечный остаток для %1'");
		//				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, текОсновнойВидГСМПоТС);
		//				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ",СтрокаСГСМ.НомерСтроки,"КонОстаток"),,Отказ);
		//			КонецЕсли;
		//		Иначе
		//			ТекстОшибки = НСтр("ru = 'В табличной части расхода ГСМ отсутствует строка с основным видом ГСМ(%1) для %2'");
		//			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, текОсновнойВидГСМПоТС, Объект.ТранспортноеСредство);
		//			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ГСМ","Объект",Отказ);					
		//		КонецЕсли;
		//	Иначе	
		//		ТекстОшибки = НСтр("ru = 'Для %1 не заполнен основной вид ГСМ'");
		//		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Объект.ТранспортноеСредство);
		//		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ТранспортноеСредство","Объект",Отказ);				
		//	КонецЕсли;
		//	
		//КонецЕсли;
				
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		// Проверить при статусе "Закрыт".
		Если Ложь
			Или Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт 
			Или НовыйСтатус = Перечисления.упСтатусыПутевогоЛиста.Закрыт
		Тогда
			
			// Проверить, что заполнена дата начала.
			Если НЕ ЗначениеЗаполнено(Объект.НачалоРейса) Тогда
				ТекстОшибки = НСтр("ru = 'Не заполнена дата начала'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"НачалоРейса","Объект",Отказ);			
			КонецЕсли;
			
			// Проверить, что заполнена дата окончания.
			Если Истина 
				И НЕ Отказ 
				И НЕ ЗначениеЗаполнено(Объект.ОкончаниеРейса) 
			Тогда
				ТекстОшибки = НСтр("ru = 'Не заполнена дата окончания'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ОкончаниеРейса","Объект",Отказ);			
			КонецЕсли;
			
			Если Истина
				И НЕ Отказ 
				И НЕ(Объект.ОкончаниеРейса > Объект.НачалоРейса) 
			Тогда
				ТекстОшибки = НСтр("ru = 'Дата окончания должна быть больше даты начала'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ОкончаниеРейса","Объект",Отказ);			
			КонецЕсли;
			
		ИначеЕсли Истина 
				И ЗначениеЗаполнено(Объект.НачалоРейса) 
				И ЗначениеЗаполнено(Объект.ОкончаниеРейса) 
				И НЕ(Объект.ОкончаниеРейса > Объект.НачалоРейса) Тогда
					
			ТекстОшибки = НСтр("ru = 'Дата окончания должна быть больше даты начала'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ОкончаниеРейса","Объект",Отказ);			
			
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И НЕ Отказ
		И ДанныеПоМаршрутуВводятсяВПутевомЛисте 
		И НЕ ЗначениеЗаполнено(упКорректировкаРейса.Водитель1) 
	Тогда
		ТекстОшибки = НСтр("ru = 'Поле ""Водитель"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"упКорректировкаРейса.Водитель1",,Отказ);
	
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Истина
		И ИмяСобытия = "ЗаписанТранспортноеСредство" 
		И Источник = Объект.ТранспортноеСредство 
	Тогда
		НастроитьДоступностьВводаМоточасовПоТС();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ВыбранноеЗначение <> Неопределено Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
			ЗаполнитьМаршрутПоЗаявкеНаКлиенте(ВыбранноеЗначение);					
		ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.упТиповыеМаршруты") Тогда			
			ЗаполнитьМаршрутАдресами(АдресаПоМаршруту(ВыбранноеЗначение));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПлановоеНачалоВыполненияПриИзменении(Элемент)
	
	Модифицированность = Истина;	
	ПлановоеНачалоВыполненияПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПлановоеНачалоВыполненияПриИзмененииНаСервере()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеПлановогоНачалаВыполнения();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаКлиенте
Процедура ПлановоеОкончаниеВыполненияПриИзменении(Элемент)

	Модифицированность = Истина;	

КонецПроцедуры

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	ЗаполнитьГСМКлиент(Объект.Рейс);
	ЗаполнитьВидТранспортнойУслугиПоРейсуНаСервере(Объект.Рейс);
КонецПроцедуры

&НаКлиенте
Процедура РейсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткПараметры =Новый Структура("Статус", СписокСтатусовДоступныхРейсов());
	ТекущийОтбор = Новый Структура("Отбор", сткПараметры);
	ОткрытьФорму("Документ.упРейс.ФормаВыбора", ТекущийОтбор,Элементы.Рейс);
КонецПроцедуры

&НаКлиенте
Процедура ПробегОкончаниеПриИзменении(Элемент)
	ПересчитатьПробег();
КонецПроцедуры

&НаКлиенте
Процедура ПробегНачалоПриИзменении(Элемент)
	ПересчитатьПробег();
КонецПроцедуры

&НаКлиенте
Процедура МоточасыНачалоПриИзменении(Элемент)
	ПересчитатьМоточасы(Объект);
КонецПроцедуры

&НаКлиенте
Процедура МоточасыОкончаниеПриИзменении(Элемент)
	ПересчитатьМоточасы(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ОдометрНачалоПриИзменении(Элемент)
	ПересчитатьОдометр();
КонецПроцедуры

&НаКлиенте
Процедура ОдометрОкончаниеПриИзменении(Элемент)
	ПересчитатьОдометр();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ЗаполнитьНормыРасходыГСМКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗаполнитьНормыРасходыГСМКлиент();
КонецПроцедуры

&НаКлиенте
Процедура НачалоРейсаПриИзменении(Элемент)
	ЗаполнитьНормыРасходыГСМКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ЗаполнитьГСМКлиент(Объект.ТранспортноеСредство);
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если Истина
		И ДанныеПоМаршрутуВводятсяВПутевомЛисте
		И ЗначениеЗаполнено(ВыбранноеЗначение) 
		И ВыбранноеЗначение <> Объект.ТранспортноеСредство 
	Тогда
		
		мсвПараметрыОбработки = ПараметрыОбработкиПриВыбореТранспортногоСредства(ВыбранноеЗначение);
	
		Для каждого сткПараметрыОбработки Из мсвПараметрыОбработки Цикл
			Если НЕ сткПараметрыОбработки.ИмяПроцедуры = Неопределено Тогда
				ГаражТекущий = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(сткПараметрыОбработки.ДополнительныеПараметры, "Гараж", ПредопределенноеЗначение("Справочник.упГаражи.ПустаяСсылка"));
				Если Объект.Гараж <> ГаражТекущий Тогда
					Объект.Гараж = ГаражТекущий;
				КонецЕсли;
				ОписаниеОповещения = Новый ОписаниеОповещения(сткПараметрыОбработки.ИмяПроцедуры, ЭтаФорма, сткПараметрыОбработки.ДополнительныеПараметры);
				ПоказатьВопрос(ОписаниеОповещения, сткПараметрыОбработки.ТекстВопроса, РежимДиалогаВопрос.ДаНет);	 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ВодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	сткПараметры = Новый Структура;
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		сткПараметры.Вставить("ТС", Объект.ТранспортноеСредство);
	КонецЕсли;
	ОткрытьФорму("Справочник.упСотрудники.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("Водитель1НачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

#Область ТЧ_ГСМ

&НаКлиенте
Процедура ГСМПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = НЕ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый");
КонецПроцедуры

&НаКлиенте
Процедура ГСМПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ОтменаРедактирования Тогда
		ТекущиеДанные.ВидГСМ  		= ВидГСМПередИзменением;
		ТекущиеДанные.НормаРасхода  	= НормаРасходаПередИзменением;
	КонецЕсли;
	упОбработкаТабличнойЧастиКлиент.ЕстьДублиСтрокВТабличнойЧасти(ТекущиеДанные, ЭтаФорма, Элемент.Имя ,Отказ);		
	
	Если Истина
		И НЕ Отказ
		И (Ложь
			Или ТекущиеДанные.НормаРасхода <> НормаРасходаПередИзменением
			Или ТекущиеДанные.ВидГСМ <> ВидГСМПередИзменением) Тогда
		
		Если Объект.НормыРасходаГСМ.Количество() > 0 Тогда
			ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветПерезаполнениеНормПриИзмененииВидаГСМ", ЭтаФорма);
			ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Табличная часть ""Нормы расхода ГСМ"" будет перезаполнена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);		
		Иначе	
			ОбработатьИзменениеВидаГСМ();		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМПриАктивизацииСтроки(Элемент)
	ОтобратьНормыРасходаГСМПоТекущемуВидуГСМ();
КонецПроцедуры

&НаКлиенте
Процедура ГСМПередУдалением(Элемент, Отказ)
	
	Отказ = Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый");
	
	Если Отказ Тогда
		ЗаполнитьНормыРасходыГСМКлиент();	
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ГСМПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.ГСМ.ТекущиеДанные;
     ВидГСМПередИзменением 		= Неопределено;
	НормаРасходаПередИзменением	= Неопределено;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ВидГСМПередИзменением 		= ТекущиеДанные.ВидГСМ;	
		НормаРасходаПередИзменением	= ТекущиеДанные.НормаРасхода;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГСМВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Строка = Элементы.ГСМ.ТекущиеДанные;
	Если Истина
		И Строка <> Неопределено 
		И (Ложь
			Или Элемент.ТолькоПросмотр 
			Или Поле.ТолькоПросмотр
		  )
	Тогда
		Если Поле.Имя = "ГСМДопОборудование" И ЗначениеЗаполнено(Строка.ДополнительноеОборудование) Тогда
			ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", Строка.ДополнительноеОборудование), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ГСМВидГСМ"  И ЗначениеЗаполнено(Строка.ВидГСМ) Тогда
			ОткрытьФорму("Справочник.упВидыГСМ.ФормаОбъекта", Новый Структура("Ключ", Строка.ВидГСМ), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ГСМНормаРасхода"  И ЗначениеЗаполнено(Строка.НормаРасхода) Тогда
			ОткрытьФорму("Справочник.упНормыРасходаГСМ.ФормаОбъекта", Новый Структура("Ключ", Строка.НормаРасхода), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);			
		ИначеЕсли Поле.Имя = "ГСМФормулаРасчета"  И ЗначениеЗаполнено(Строка.ФормулаРасчета) Тогда
			ОткрытьФорму("Справочник.упПравилаРасчета.ФормаОбъекта", Новый Структура("Ключ", Строка.ФормулаРасчета), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);						
		ИначеЕсли Поле.Имя = "ГСМШкалаТемпературныхКоэффициентов"  И ЗначениеЗаполнено(Строка.ШкалаТемпературныхКоэффициентов) Тогда
			ОткрытьФорму("Справочник.упОценочнаяШкала.ФормаОбъекта", Новый Структура("Ключ", Строка.ШкалаТемпературныхКоэффициентов), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);									
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГСМДопОборудованиеПриИзменении(Элемент)
	ТекущиеДанные 			 = Элементы.ГСМ.ТекущиеДанные;
	ТранспортноеСредство 	 =	?(ЗначениеЗаполнено(ТекущиеДанные.ДопОборудование), ТекущиеДанные.ДопОборудование, Объект.ТранспортноеСредство);
	ТекущиеДанные.НачОстаток  = ПолучитьУровеньТопливаНаСервере(ТранспортноеСредство, ТекущиеДанные.ВидГСМ);
КонецПроцедуры

#КонецОбласти


#Область ТЧ_НормыРасхода

&НаКлиенте
Процедура НормыРасходаГСМПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаГСМПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаГСМВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Строка = Элементы.НормыРасходаГСМ.ТекущиеДанные;
	Если Истина
		И Строка <> Неопределено 
		И (Ложь
			Или Элемент.ТолькоПросмотр 
			Или Поле.ТолькоПросмотр
		  )
	Тогда
		Если Поле.Имя = "НормыРасходаГСМВидГСМ" И ЗначениеЗаполнено(Строка.ВидГСМ) Тогда
			ОткрытьФорму("Справочник.упВидыГСМ.ФормаОбъекта", Новый Структура("Ключ", Строка.ВидГСМ), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ТЧ_ЗаправкиСливы

&НаКлиенте
Процедура ЗаправкиСливыГСМПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Истина
		И ВедетсяВалютныйУчет 
		И НоваяСтрока 
		И Не Копирование 
	Тогда
		ТекущаяСтрока	= Элементы.ЗаправкиСливыГСМ.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта = ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		ТекущаяСтрока.Количество = 1;
		ТекущаяСтрока.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли; 
	ТекущиеДанные = Элементы.ЗаправкиСливыГСМ.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	мсвСтроки = Объект.ГСМ.НайтиСтроки(Новый Структура("ВидГСМ, ДопОборудование",ТекущиеДанные.ВидГСМ, ТекущиеДанные.ДопОборудование));
	
	Если мсвСтроки.Количество() = 0 И ЗначениеЗаполнено(ТекущиеДанные.ВидГСМ) Тогда
		ДобавитьСтрокуГСМ(ТекущиеДанные.ДопОборудование, ТекущиеДанные.ВидГСМ);
	КонецЕсли;
		
	ДатаЗаправкиСлива = ТекущиеДанные.ДатаЗаправкиСлива;
	Если ЗначениеЗаполнено(ДатаЗаправкиСлива) 
		И ДатаЗаправкиСлива < Объект.Дата Тогда
			
			ТекстОшибки 	= НСтр("ru = 'Дата %1 должна быть позже даты документа'");
			текВидДвижения 	= ?(ТекущиеДанные.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка"), НСтр("ru = 'заправки'"), НСтр("ru = 'слива'"));
			ТекстОшибки 	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, текВидДвижения);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМ",ТекущиеДанные.НомерСтроки,"ДатаЗаправкиСлива"));
	КонецЕсли;

	РассчитатьИтоговуюСуммуРасходы();

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМАЗСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры =Новый Структура("ЭтоАЗС", Истина);
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.упПартнеры.ФормаВыбора", сткПараметры,Элементы.ЗаправкиСливыГСМАЗС);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМПриемникСливаГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	текТекущиеДанныеЗаправки = Элементы.ЗаправкиСливыГСМ.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(текТекущиеДанныеЗаправки.ВидГСМ) Тогда
		СтандартнаяОбработка = Ложь;
		Если текТекущиеДанныеЗаправки.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Слив") Тогда
			спзВыборТипа = Новый СписокЗначений;
			спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"));
			
			Если ИспользуетсяУчетДополнительногоОборудования Тогда
				спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упДополнительноеОборудование"));	
			КонецЕсли;
			
			Если ИспользуетсяУчетМатериалов Тогда
				спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упСклады"));	
			КонецЕсли;
			
			текОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораТипаДляПриемникСливаГСМ", ЭтаФорма);
			
			ПоказатьВыборИзМеню(текОписаниеОповещения, спзВыборТипа, Элемент);
		КонецЕсли;
	Иначе	
		ТекстСообщения = Нстр("ru = 'Не заполнено поле ""Вид ГСМ"".'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
		,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМ",текТекущиеДанныеЗаправки.НомерСтроки,"ВидГСМ"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМСтоимостьПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМВидГСМПриИзменении(Элемент)
	ВидГСМПриИзмененииНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМПриемникСливаГСМПриИзменении(Элемент)
	Если ТипЗнч(Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ПриемникСливаГСМ) = Тип("СправочникСсылка.упСклады") Тогда
		Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ЭтоСклад = Истина;
	Иначе	
		Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ЭтоСклад = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМПриемникСливаГСМОчистка(Элемент, СтандартнаяОбработка)
	Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ПриемникСливаГСМ = Неопределено;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМВидДвиженияПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ЗаправкиСливыГСМ.ТекущиеДанные;
	ВидДвиженияТоплива	= ТекущаяСтрока.ВидДвижения;
	Если НЕ ВидДвиженияТоплива = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка") Тогда
		ТекущаяСтрока.Цена = 0;
		ТекущаяСтрока.Стоимость = 0;
		ТекущаяСтрока.СтоимостьНДС = 0;
		ТекущаяСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.ПустаяСсылка");
	Иначе
		Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ПриемникСливаГСМ 	= Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМВидГСМАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", СписокДоступныхВидовГСМ);
	ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМВидГСМОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", СписокДоступныхВидовГСМ);
	ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

#КонецОбласти


#Область ТЧ_ДополнительноеОборудование

&НаКлиенте
Процедура ДополнительноеОборудованиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Строка = Элементы.ДополнительноеОборудование.ТекущиеДанные;
	Если Истина
		И Строка <> Неопределено 
		И (Ложь
			Или Элемент.ТолькоПросмотр 
			Или Поле.ТолькоПросмотр
		  )
	Тогда
		Если Поле.Имя = "ДополнительноеОборудованиеДополнительноеОборудование" И ЗначениеЗаполнено(Строка.ДополнительноеОборудование)  Тогда
			ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", Строка.ДополнительноеОборудование), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ДополнительноеОборудованиеТранспортноеСредство" И ЗначениеЗаполнено(Строка.ТранспортноеСредство)  Тогда
			ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаОбъекта", Новый Структура("Ключ", Строка.ТранспортноеСредство), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеМоточасыНачалоПриИзменении(Элемент)
	ПересчитатьМоточасы(Элементы.ДополнительноеОборудование.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеМоточасыОкончаниеПриИзменении(Элемент)
	ПересчитатьМоточасы(Элементы.ДополнительноеОборудование.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеМоточасыПриИзменении(Элемент)
	ПересчитатьМоточасыОкончание(Элементы.ДополнительноеОборудование.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПриАктивизацииСтроки(Элемент)
	ОтобратьСтрокиТЧПоТекущемуДопОборудованию();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НЕ ОтменаРедактирования Тогда
		упОбработкаТабличнойЧастиКлиент.ЕстьДублиСтрокВТабличнойЧасти(Элемент.ТекущиеДанные, ЭтаФорма, Элемент.Имя ,Отказ);		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ЗаполнитьКоличествоДопОборудования();
КонецПроцедуры

#КонецОбласти


#Область ТЧ_РежимыРаботы_ДополнительноеОборудование

&НаКлиенте
Процедура НормыРасходаРежимовРаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Строка = Элементы.НормыРасходаРежимовРаботы.ТекущиеДанные;
	Если Истина
		И Строка <> Неопределено 
		И (Ложь
			Или Элемент.ТолькоПросмотр 
			Или Поле.ТолькоПросмотр
		  )
	Тогда
		Если Поле.Имя = "НормыРасходаРежимовРаботыДополнительноеОборудование" И ЗначениеЗаполнено(Строка.ДополнительноеОборудование) Тогда
			ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", Строка.ДополнительноеОборудование), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "НормыРасходаРежимовРаботыРежимРаботы" И ЗначениеЗаполнено(Строка.РежимРаботы) Тогда
			ОткрытьФорму("Справочник.упРежимыРаботыОборудования.ФормаОбъекта", Новый Структура("Ключ", Строка.РежимРаботы), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаРежимовРаботыКоличествоПриИзменении(Элемент)
	ПересчитатьМоточасыПоРежимам(Элементы.НормыРасходаРежимовРаботы.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаРежимовРаботыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти


#Область ТЧ_ГСМ_ДополнительноеОборудование

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Строка = Элементы.ГСМДополнительноеОборудование.ТекущиеДанные;
	Если Истина
		И Строка <> Неопределено 
		И (Ложь
			Или Элемент.ТолькоПросмотр 
			Или Поле.ТолькоПросмотр
		  )
	Тогда
		Если Поле.Имя = "ГСМДополнительноеОборудованиеДополнительноеОборудование"  И ЗначениеЗаполнено(Строка.ДополнительноеОборудование) Тогда
			ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", Строка.ДополнительноеОборудование), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ГСМДополнительноеОборудованиеВидГСМ" И ЗначениеЗаполнено(Строка.ВидГСМ) Тогда
			ОткрытьФорму("Справочник.упВидыГСМ.ФормаОбъекта", Новый Структура("Ключ", Строка.ВидГСМ), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ГСМДополнительноеОборудованиеНормаРасхода" И ЗначениеЗаполнено(Строка.НормаРасхода) Тогда
			ОткрытьФорму("Справочник.упНормыРасходаГСМ.ФормаОбъекта", Новый Структура("Ключ", Строка.НормаРасхода), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);			
		ИначеЕсли Поле.Имя = "ГСМДополнительноеОборудованиеФормулаРасчета" И ЗначениеЗаполнено(Строка.ФормулаРасчета) Тогда
			ОткрытьФорму("Справочник.упПравилаРасчета.ФормаОбъекта", Новый Структура("Ключ", Строка.ФормулаРасчета), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);						
		ИначеЕсли Поле.Имя = "ГСМДополнительноеОборудованиеШкалаТемпературныхКоэффициентов" И ЗначениеЗаполнено(Строка.ШкалаТемпературныхКоэффициентов) Тогда
			ОткрытьФорму("Справочник.упОценочнаяШкала.ФормаОбъекта", Новый Структура("Ключ", Строка.ШкалаТемпературныхКоэффициентов), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);						
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеПередУдалением(Элемент, Отказ)
	ЗаполнитьНормыРасходаГСМДопОборудованияКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанныеГСМДопОборудование    = Элементы.ГСМДополнительноеОборудование.ТекущиеДанные;
	
     ВидГСМПередИзменением 		= Неопределено;
	НормаРасходаПередИзменением	= Неопределено;
	ДопОборудованиеПередИзменением = Неопределено;
	Если НЕ ТекущиеДанныеГСМДопОборудование = Неопределено Тогда
		ВидГСМПередИзменением 			= ТекущиеДанныеГСМДопОборудование.ВидГСМ;	
		НормаРасходаПередИзменением		= ТекущиеДанныеГСМДопОборудование.НормаРасхода;
		ДопОборудованиеПередИзменением 	= ТекущиеДанныеГСМДопОборудование.ДополнительноеОборудование;
	КонецЕсли;

	
	Если НоваяСтрока Тогда
		ТекушиеДанныеДопОборудование 		= Элементы.ДополнительноеОборудование.ТекущиеДанные;
		Если Истина
			И ТекушиеДанныеДопОборудование <> Неопределено
			И ТекущиеДанныеГСМДопОборудование <> Неопределено 
		Тогда
			ТекущиеДанныеГСМДопОборудование.ДополнительноеОборудование = ТекушиеДанныеДопОборудование.ДополнительноеОборудование;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ОтменаРедактирования Тогда
		ТекущиеДанные.ВидГСМ  		= ВидГСМПередИзменением;
		ТекущиеДанные.НормаРасхода  	= НормаРасходаПередИзменением;
		ТекущиеДанные.ДополнительноеОборудование  	= ДопОборудованиеПередИзменением;
	КонецЕсли;
	упОбработкаТабличнойЧастиКлиент.ЕстьДублиСтрокВТабличнойЧасти(Элемент.ТекущиеДанные, ЭтаФорма, Элемент.Имя ,Отказ);		
	
	Если НЕ Отказ Тогда
		
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.РасходТопливаСТС > 0 Тогда
				мсвСтрокиГСМ = Объект.ГСМ.НайтиСтроки(Новый Структура("ВидГСМ",ТекущиеДанные.ВидГСМ));
				Если мсвСтрокиГСМ.Количество() = 0 Тогда
					
					Если ОтменаРедактирования Тогда
						ТекущиеДанные.РасходТопливаСТС = 0
					Иначе	
						ТекстСообщения = Нстр("ru = 'Вид ГСМ ""%1"" отсутствует на закладке ""Расход ГСМ"" по транспортному средству. Расход топлива с ТС на доп. оборудование запрещен.'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущиеДанные.ВидГСМ);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И НЕ Отказ
		И (Ложь
			Или ТекущиеДанные.НормаРасхода <> НормаРасходаПередИзменением
			Или ТекущиеДанные.ВидГСМ <> ВидГСМПередИзменением
			Или ТекущиеДанные.ДополнительноеОборудование <> ДопОборудованиеПередИзменением	
		  ) 
	Тогда
		Если Объект.НормыРасходаГСМДополнительноеОборудование.Количество() > 0 Тогда
			ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветПерезаполнениеНормПриИзмененииВидаГСМДополнительноеОборудование", ЭтаФорма);
			ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Табличная часть ""Нормы расхода ГСМ по дополнительному оборудованию"" будет перезаполнена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);		
		Иначе	
			ОбработатьИзменениеВидаГСМДополнительноеОборудование();		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеНормаРасходаПриИзменении(Элемент)
	РассчитатьНормативныйРасходТопливаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеРасходИзСобственногоБакаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ГСМДополнительноеОборудование.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если НЕ ТекущиеДанные.РасходИзСобственногоБака Тогда
			ТекущиеДанные.РасходТопливаПлан	= 0;
			ТекущиеДанные.РасходТопливаПоНорме	= 0;
			ТекущиеДанные.РасходТопливаФакт	= 0;
			ТекущиеДанные.СуммаРасходов		= 0;
			ТекущиеДанные.СуммаРасходовНДС	= 0;                    
			ТекущиеДанные.СуммаРасходовПлан	= 0;
			ТекущиеДанные.СуммаРасходовПланНДС	= 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительноеОборудованиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = НЕ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый");
КонецПроцедуры

#КонецОбласти


#Область ТЧ_НормыРасхода_ДополнительноеОборудование 

&НаКлиенте
Процедура НормыРасходаГСМДополнительноеОборудованиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Строка = Элементы.НормыРасходаГСМДополнительноеОборудование.ТекущиеДанные;
	Если Истина
		И Строка <> Неопределено 
		И (Ложь
			Или Элемент.ТолькоПросмотр 
			Или Поле.ТолькоПросмотр
		  )
	Тогда
		Если Поле.Имя = "НормыРасходаГСМДополнительноеОборудованиеВидГСМ" И ЗначениеЗаполнено(Строка.ВидГСМ) Тогда
			ОткрытьФорму("Справочник.упВидыГСМ.ФормаОбъекта", Новый Структура("Ключ", Строка.ВидГСМ), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "НормыРасходаГСМДополнительноеОборудованиеДополнительноеОборудование" И ЗначениеЗаполнено(Строка.ДополнительноеОборудование) Тогда
			ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаОбъекта", Новый Структура("Ключ", Строка.ДополнительноеОборудование), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НормыРасходаГСМДополнительноеОборудованиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти


#Область ТЧ_ЗаправкиСливы_ДополнительноеОборудование

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ТекушиеДанныеДопОборудование 			= Элементы.ДополнительноеОборудование.ТекущиеДанные;
		ТекущиеДанныеЗаправкиДопОборудование    = Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные;
		Если Истина
			И ТекушиеДанныеДопОборудование <> Неопределено
			И ТекущиеДанныеЗаправкиДопОборудование <> Неопределено 
		Тогда
			ТекущиеДанныеЗаправкиДопОборудование.ДополнительноеОборудование = ТекушиеДанныеДопОборудование.ДополнительноеОборудование;
		КонецЕсли;
		
		Если ВедетсяВалютныйУчет И Не Копирование Тогда
			Если ВедетсяВалютныйУчет Тогда
				ТекущиеДанныеЗаправкиДопОборудование.Валюта 		= ВалютаРеглУчета;	
			Иначе
				ТекущиеДанныеЗаправкиДопОборудование.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
			КонецЕсли;
			ТекущиеДанныеЗаправкиДопОборудование.Количество 	= 1;
			ТекущиеДанныеЗаправкиДопОборудование.ВидДвижения	= ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка");
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеВидГСМПриИзменении(Элемент)
	ТекущаяСтрока		= Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные;
	ВидГСМ			= ТекущаяСтрока.ВидГСМ;
	ВидДвиженияТоплива	= ТекущаяСтрока.ВидДвижения;
	ТекущаяСтрока.ПриемникСливаГСМ = Неопределено;
	Если Истина
		И ЗначениеЗаполнено(ВидГСМ) 
		И (Ложь
			Или НЕ ЗначениеЗаполнено(ВидДвиженияТоплива) 
			Или ВидДвиженияТоплива = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка")
		) 
	Тогда
		ТекущаяСтрока.СтавкаНДС = СтавкаНДСПоГСМ(ВидГСМ);
		Если ЗначениеЗаполнено(ТекущаяСтрока.СтавкаНДС) 
			И НЕ ТекущаяСтрока.Стоимость = 0 Тогда
			ПересчитатьСтроку(Элемент, "ЗаправкиСливыГСМДополнительноеОборудование");	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли; 
	ТекущиеДанные = Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	мсвСтроки = Объект.ГСМДополнительноеОборудование.НайтиСтроки(Новый Структура("ВидГСМ, ДополнительноеОборудование",ТекущиеДанные.ВидГСМ, ТекущиеДанные.ДополнительноеОборудование));
	
	Если Истина
		И мсвСтроки.Количество() = 0 
		И ЗначениеЗаполнено(ТекущиеДанные.ВидГСМ) 
	Тогда
		ДобавитьСтрокуГСМДопОборудование(ТекущиеДанные.ДополнительноеОборудование, ТекущиеДанные.ВидГСМ);
	КонецЕсли;
		
	ДатаЗаправкиСлива = ТекущиеДанные.ДатаЗаправкиСлива;
	Если Истина
		И ЗначениеЗаполнено(ДатаЗаправкиСлива) 
		И ДатаЗаправкиСлива < Объект.Дата 
	Тогда
		ТекстОшибки 	= НСтр("ru = 'Дата %1 должна быть позже даты документа'");
		текВидДвижения = ?(ТекущиеДанные.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка"), НСтр("ru = 'заправки'"), НСтр("ru = 'слива'"));
		ТекстОшибки 	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, текВидДвижения);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,
		,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМДополнительноеОборудование",ТекущиеДанные.НомерСтроки,"ДатаЗаправкиСлива"));
	КонецЕсли;

	РассчитатьИтоговуюСуммуРасходы();

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеВидГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМДополнительноеОборудованиеНаСервере(Элементы.ЗаправкиСливыГСМДопОборудование.ДополнительноеОборудование);
	
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", СписокДоступныхВидовГСМ));
	ОткрытьФорму("Справочник.упВидыГСМ.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ЗаправкиСливыГСМВидГСМДополнительноеОборудованиеНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеВидДвиженияПриИзменении(Элемент)
	ТекущиеДанные	= Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные;
	ВидДвиженияТоплива	= ТекущиеДанные.ВидДвижения;
	Если НЕ ВидДвиженияТоплива = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка") Тогда
		ТекущиеДанные.Цена = 0;
		ТекущиеДанные.Стоимость = 0;
		ТекущиеДанные.СтоимостьНДС = 0;
		ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.ПустаяСсылка");
	Иначе
		ТекущиеДанные.ПриемникСливаГСМ 	= Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент,"ЗаправкиСливыГСМДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент,"ЗаправкиСливыГСМДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент,"ЗаправкиСливыГСМДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеСтоимостьПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент,"ЗаправкиСливыГСМДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеАЗСПриИзменении(Элемент)
	// Выполним подбор топливной карты.
	ТекущийЭлемент.ТекущиеДанные.ТопливнаяКарта = ПодборТопливнойКартыПоАЗСНаСервере(ТекущийЭлемент.ТекущиеДанные.АЗС);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеАЗСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры =Новый Структура("ЭтоАЗС", Истина);
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.упПартнеры.ФормаВыбора", сткПараметры,Элементы.ЗаправкиСливыГСМДополнительноеОборудованиеАЗС);

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеТопливнаяКартаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь; 	
	сткПараметры = Новый Структура();
	Если ЗначениеЗаполнено(ТекущийЭлемент.ТекущиеДанные.АЗС) Тогда
		СписокДоступныхТК = ВернутьДоступныеТКНаСервере(ТекущийЭлемент.ТекущиеДанные.АЗС); 		
		сткПараметры.Вставить(Новый Структура("Отбор", Новый Структура("Ссылка", СписокДоступныхТК)));
	КонецЕсли;
	ОткрытьФорму("Справочник.упТопливныеКарты.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ЗаправкиСливыГСМТопливнаяКартаНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеПриемникСливаГСМПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЗаправкиСливыГСМДопОборудование.ТекущиеДанные;
	ТекущиеДанные.ЭтоСклад = ТипЗнч(ТекущиеДанные.ПриемникСливаГСМ) = Тип("СправочникСсылка.упСклады");
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеПриемникСливаГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	текТекущиеДанныеЗаправки = Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(текТекущиеДанныеЗаправки.ВидГСМ) Тогда
		СтандартнаяОбработка = Ложь;
		Если текТекущиеДанныеЗаправки.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Слив") Тогда
			спзВыборТипа = Новый СписокЗначений;
			спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"));
			
			Если ИспользуетсяУчетДополнительногоОборудования Тогда
				спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упДополнительноеОборудование"));	
			КонецЕсли;
			
			Если ИспользуетсяУчетМатериалов Тогда
				спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упСклады"));	
			КонецЕсли;
			
			текОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораТипаДляПриемникСливаГСМДополнительноеОборудование", ЭтаФорма);
			
			ПоказатьВыборИзМеню(текОписаниеОповещения, спзВыборТипа, Элемент);
		КонецЕсли;
	Иначе	
		ТекстСообщения = Нстр("ru = 'Не заполнено поле ""Вид ГСМ"".'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
		,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМДополнительноеОборудование",текТекущиеДанныеЗаправки.НомерСтроки,"ВидГСМ"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМДополнительноеОборудованиеПриемникСливаГСМОчистка(Элемент, СтандартнаяОбработка)
	Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные.ПриемникСливаГСМ = Неопределено;
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

#КонецОбласти


#Область КорректировкаМаршрута

&НаКлиенте
Процедура упКорректировкаРейсаМаршрутПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	текСтрока = Элемент.ТекущиеДанные;
	Если НоваяСтрока ИЛИ Копирование Тогда
		текСтрока.СтрокаДокумента	= Истина;
		текСтрока.Идентификатор     = Новый УникальныйИдентификатор;
		текСтрока.СтрокаНомер		= текСтрока.НомерСтроки;
	КонецЕсли;
	сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;
	Модифицированность = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаМаршрутПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НоваяСтрока Тогда
		Отказ = Ложь;
		ДобавитьТочкуВМаршрутНаСервере(Элемент.ТекущиеДанные.НомерСтроки, Отказ);
		Если НЕ Отказ Тогда
			Модифицированность = Истина;	
		КонецЕсли;
	КонецЕсли;
	
	текСтрока = Элемент.ТекущиеДанные;
	ОбработатьСтрокуТочкаМаршрутаНаСервере(текСтрока.НомерСтроки,НоваяСтрока,,ОтменаРедактирования, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаМаршрутПриАктивизацииСтроки(Элемент)
	упКорректировкаМаршрутаКлиент.УстановитьДоступностьКнопокПеремещенияТочекМаршрута(Элемент, 
																	  упКорректировкаРейса.Маршрут,
																	  Элементы.упКорректировкаРейсаМаршрутПереместитьТочкуВверх,
																	  Элементы.упКорректировкаРейсаМаршрутПереместитьТочкуВниз);
КонецПроцедуры

#КонецОбласти

#Область ТЧ_Рейсы

&НаКлиенте
Процедура РейсыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Для каждого Рейс Из ВыбранноеЗначение Цикл
		Элементы.Рейсы.ДобавитьСтроку();	
		ТекущиеДанные 		= Элементы.Рейсы.ТекущиеДанные;
		ТекущиеДанные.Рейс 	= Рейс;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РейсыРейсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткОтбор = ОтборДляРейсов();
	сткПараметры = Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Документ.упРейс.ФормаВыбора", сткПараметры, Элементы.РейсыРейс);
КонецПроцедуры

&НаКлиенте
Процедура РейсыПослеУдаления(Элемент)
	Если Объект.Рейсы.Количество() = 0 Тогда
		Объект.Рейс = ПредопределенноеЗначение("Документ.упРейс.ПустаяСсылка");
		Объект.ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ПустаяСсылка");
	Иначе
		ЗаполнитьВидТранспортнойУслугиПоРейсуНаСервере(Объект.Рейсы[0].Рейс);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РейсыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Объект.Рейсы.Количество() > 0 Тогда
		ЗаполнитьВидТранспортнойУслугиПоРейсуНаСервере(Объект.Рейсы[0].Рейс);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура СтатусВыдан(Команда)
	Если УстановитьСтатусВыданНаСервере() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Для каждого Строка Из Объект.Заявки Цикл
			Оповестить("СменаСтатуса",,Строка.ЗаявкаНаПеревозкуГруза);
		КонецЦикла;
	Иначе
		ОповеститьОбОшибке();				
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтатусВозвращен(Команда)
	Если УстановитьСтатусВозвращенНаСервере() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	Иначе
		ОповеститьОбОшибке();				
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтатусЗакрыт(Команда)
	Если УстановитьСтатусЗакрытНаСервере() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	Иначе
		ОповеститьОбОшибке();				
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтменен(Команда)
	Если УстановитьСтатусОтмененоНаСервере() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Для каждого Строка Из Объект.Заявки Цикл
			Оповестить("СменаСтатуса",,Строка.ЗаявкаНаПеревозкуГруза);
		КонецЦикла;
	Иначе
		ОповеститьОбОшибке();				
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтрольнуюТочку(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Рейс) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Для заполнения табличной части необходимо записать документ.'"));
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;	
	
	ДобавитьКонтрольнуюТочкуНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТочкуПоЗаданию(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Рейс) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Для заполнения табличной части необходимо записать документ.'"));
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;	
	
	ДобавитьТочкуПоЗаданиюНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьПосещениеГаража(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Рейс) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Для заполнения табличной части необходимо записать документ.'"));
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;	
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("НачалоМаршрута", Неопределено);
	сткПараметры.Вставить("ОкончаниеМаршрута", Неопределено);
	
	ИскомыеСтроки = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("ТипТочки", ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")));
	Если ИскомыеСтроки.Количество() Тогда
		сткПараметры.НачалоМаршрута = ИскомыеСтроки[0].Гараж;
	КонецЕсли; 
	
	ИскомыеСтроки = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("ТипТочки", ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения")));
	Если ИскомыеСтроки.Количество() Тогда
		сткПараметры.ОкончаниеМаршрута = ИскомыеСтроки[0].Гараж;
	КонецЕсли; 

	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаРедактированияПосещенияГаражаВМаршруте", сткПараметры,,,,, Новый ОписаниеОповещения("РедактированиеПосещенияГаражаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыйРасходТоплива(Команда)
	РассчитатьПлановыйРасходТопливаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНормативныйРасходТоплива(Команда)
	РассчитатьНормативныйРасходТопливаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьКонечныйОстаток(Команда)
	РассчитатьКонечныйОстатокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьГСМПоУмолчанию(Команда)
	ЗаполнитьГСМКлиент(Объект.ТранспортноеСредство);
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	ИмяПечатнойФормы = ИмяПечатнойФормы(); 
	Если НЕ ИмяПечатнойФормы = Неопределено Тогда
		//КомандаПечатьПоУмолчанию = ЭтаФорма.Команды["ПодменюПечатьОбычное_"+ИмяПечатнойФормы];
		КомандаПечатьПоУмолчанию = ЭтаФорма.Команды[ИмяПечатнойФормы];
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(КомандаПечатьПоУмолчанию, ЭтотОбъект, Объект);
	ИначеЕсли ТипЗнч(ТипПечатнойФормыПутевогоЛиста) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") 
			И ИспользоватьДополнительныеОтчетыИОбработки Тогда
		//ПараметрыПечати = Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Истина);
		ИмяОбъектаМетаданных = "Документ.упПутевойЛист";
		ИмяМакета 		= упСервисныеФункцииВызовСервера.ПолучитьИмяМакетаВнешнейПечатнойФормы(ТипПечатнойФормыПутевогоЛиста, ИмяОбъектаМетаданных);
		мсвОбъектов = Новый Массив;
		мсвОбъектов.Добавить(Объект.Ссылка);
		//УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяОбъектаМетаданных, ИмяМакета, мсвОбъектов, ЭтаФорма, ПараметрыПечати);
		ВыполняемаяКоманда = Новый Структура();
		ВыполняемаяКоманда.Вставить("Идентификатор",СтрЗаменить(ИмяМакета,"ВнешняяПечатнаяФорма.",""));
		ВыполняемаяКоманда.Вставить("Ссылка",ТипПечатнойФормыПутевогоЛиста);
		ДополнительныеПараметры = Новый Структура("ВыполняемаяКоманда",ВыполняемаяКоманда);
		ДополнительныеПараметры.Вставить("Форма",ЭтаФорма);
		ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьОткрытиеПечатнойФормыЗавершение(мсвОбъектов,ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМВидГСМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СписокДоступныхВидовГСМ = ВернутьДоступныеВидыГСМНаСервере(Объект.ТранспортноеСредство);
	
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", СписокДоступныхВидовГСМ));
	ОткрытьФорму("Справочник.упВидыГСМ.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМТопливнаяКартаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь; 	
	сткПараметры = Новый Структура();
	Если ЗначениеЗаполнено(ТекущийЭлемент.ТекущиеДанные.АЗС) Тогда
		СписокДоступныхТК = ВернутьДоступныеТКНаСервере(ТекущийЭлемент.ТекущиеДанные.АЗС); 		
		сткПараметры.Вставить(Новый Структура("Отбор", Новый Структура("Ссылка", СписокДоступныхТК)));
	КонецЕсли;
	ОткрытьФорму("Справочник.упТопливныеКарты.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ЗаправкиСливыГСМТопливнаяКартаНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ЗаправкиСливыГСМАЗСПриИзменении(Элемент)
	// Выполним подбор топливной карты.
	ТекущийЭлемент.ТекущиеДанные.ТопливнаяКарта = ПодборТопливнойКартыПоАЗСНаСервере(ТекущийЭлемент.ТекущиеДанные.АЗС);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРейс(Команда)
	Элементы.ГруппаРейсы.Видимость 			= Истина;
	Элементы.ГруппаДобавитьРейс.Видимость 	= Ложь;
	Элементы.ГруппаГСМ.ТекущаяСтраница 		= Элементы.ГруппаРейсы;
	ТекущийЭлемент = Элементы.Рейсы;
	сткОтбор = ОтборДляРейсов();
	ПараметрыФормы = Новый Структура("МножественныйВыбор, Отбор", Истина, сткОтбор);
	ОткрытьФорму("Документ.упРейс.ФормаВыбора", ПараметрыФормы, Элементы.Рейсы);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуМаршрута(Команда)
	текИдентификатор = Элементы.упКорректировкаРейсаМаршрут.ТекущаяСтрока;
	текТочка = упКорректировкаРейса.Маршрут.НайтиПоИдентификатору(текИдентификатор); 
	Отказ = ложь;
	Если НЕ текТочка.СтрокаДокумента Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не текТочка = Неопределено И НЕ Отказ Тогда
		сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
		
		Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, текТочка[текПоле.Ключ]);
		КонецЦикла;
		ОбработатьСтрокуТочкаМаршрутаНаСервере(текТочка.НомерСтроки,, Истина,, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Модифицированность = Истина;	
	КонецЕсли;
	
	упКорректировкаРейса.Маршрут.Удалить(текТочка);
	
	ПересчитатьВременнойГрафикМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьТочкуВверх(Команда)
	ТекущиеДанные = Элементы.упКорректировкаРейсаМаршрут.ТекущиеДанные;
	Отказ = Ложь;
	
	ИндексСтроки = упКорректировкаРейса.Маршрут.Индекс(ТекущиеДанные);
	мсвПредшественников = СтрРазделить(ТекущиеДанные.Предшественники, ",", Ложь);
	ЗаменяемаяСтрока = упКорректировкаРейса.Маршрут[ИндексСтроки - 1];
	Если мсвПредшественников.Найти(Строка(ЗаменяемаяСтрока.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ЗаменяемаяСтрока.Адрес, ТекущиеДанные.Адрес);
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтроки = СдвинутьТочкуВМаршрутеНаСервере(ТекущиеДанные.НомерСтроки,,Ложь);
	Если НЕ Отказ Тогда
		Элементы.упКорректировкаРейсаМаршрут.ТекущаяСтрока = ИдентификаторСтроки;
		Модифицированность = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьТочкуВниз(Команда)
	ТекущиеДанные = Элементы.упКорректировкаРейсаМаршрут.ТекущиеДанные;
	Отказ = Ложь;
	
	ИндексСтроки = упКорректировкаРейса.Маршрут.Индекс(ТекущиеДанные);
	ЗаменяемаяСтрока = упКорректировкаРейса.Маршрут[ИндексСтроки + 1];
	мсвПредшественников = СтрРазделить(ЗаменяемаяСтрока.Предшественники, ",", Ложь);
	Если мсвПредшественников.Найти(Строка(ТекущиеДанные.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ТекущиеДанные.Адрес, ЗаменяемаяСтрока.Адрес);
		Возврат;
	КонецЕсли; 	
	
	ИдентификаторСтроки = СдвинутьТочкуВМаршрутеНаСервере(ТекущиеДанные.НомерСтроки,Ложь,Ложь);
	Если НЕ Отказ Тогда
		Элементы.упКорректировкаРейсаМаршрут.ТекущаяСтрока = ИдентификаторСтроки;
		Модифицированность = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДополнительноеОборудование(Команда)
	Если Объект.ДополнительноеОборудование.Количество() > 0 Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветНаВопросЗаполнениеДопОборудования", ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'При заполнении табличная часть ""Доп. оборудование"" будет очищена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);		
	иначе
		ЗаполнитьДополнительноеОборудованиеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьГСМДополнительноеОборудование(Команда)
	Если Объект.ГСМДополнительноеОборудование.Количество() > 0 Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветНаВопросЗаполнениеГСМДопОборудования", ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'При заполнении табличная часть ""Расход ГСМ"" будет очищена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);		
	иначе
		ЗаполнитьГСМДополнительноеОборудованиеНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИспользуемыеНормы(Команда)
	ЗаполнитьНормыРасходыГСМКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоТекущемуВидуГСМ(Команда)
	ИзменитьОтобратьНормыПоТекущемуВидуГСМ(Истина);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьКонечныйОстатокТопливаПоДаннымДатчиков(Команда)
	РассчитатьКонечныйОстатокТопливаПоДаннымДатчиковНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьКонечныйПробегПоДаннымДатчиков(Команда)
	РассчитатьКонечныйПробегПоДаннымДатчиковНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРежимыРаботыДопОборудования(Команда)
	ЗаполнитьРежимыРаботыДопОборудованияКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНадбавкиДопОборудование(Команда)
	ЗаполнитьНормыРасходаГСМДопОборудованияКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьГСМПоТранспортномуСредству(Команда)
	ЗаполнитьГСМКлиент(Объект.ТранспортноеСредство);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНормативныйРасходТопливаСРасшифровкой(Команда)
	мсвПриемников = РассчитатьНормативныйРасходТопливаНаСервереСРасшифровкой();
	Для каждого Приемник Из мсвПриемников Цикл
		Приемник.Приемник.Показать("Расшифровка расчетов");	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНормативныйРасходТопливаДопОборудованияСРасшифровкой(Команда)
	мсвПриемников = РассчитатьНормативныйРасходТопливаДопОборудованияНаСервереСРасшифровкой();
	Для каждого Приемник Из мсвПриемников Цикл
		Приемник.Приемник.Показать("Расшифровка расчетов");	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНормативныйРасходТопливаДопОборудования(Команда)
	РассчитатьНормативныйРасходТопливаДопОборудованияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыйРасходТопливаСРасшифровкой(Команда)
	мсвПриемников = РассчитатьПлановыйРасходТопливаНаСервереСРасшифровкой();
	Для каждого Приемник Из мсвПриемников Цикл
		Приемник.Приемник.Показать("Расшифровка расчетов");	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоТекущемуДопОборудованию(Команда)
	ИзменитьОтобратьСтрокиТСПоТекущемуДопОборудованию(Истина);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьМассуПрицепногоСостава(Команда)
	ЗаполнитьМассуПрицепногоСостава();	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМаршрутВыбратьМаршрут(Команда)
	
	ОткрытьФорму("Справочник.упТиповыеМаршруты.ФормаВыбора", ,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМаршрутПоЗаявке(Команда)
	
	ВыбратьЗаявкуДляЗаполненияМаршрута();
	     	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Функция ПодборТопливнойКартыПоАЗСНаСервере(ссПартнерАЗС)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПеревозчикПоРейсуСрезПоследних.Водитель1
	               |ПОМЕСТИТЬ ВодительПоРейсу
	               |ИЗ
	               |	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&мсвРейсы)) КАК упПеревозчикПоРейсуСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упКонтрагенты.Ссылка КАК Контрагент
	               |ПОМЕСТИТЬ СписокКонтрагентов
	               |ИЗ
	               |	Справочник.упКонтрагенты КАК упКонтрагенты
	               |ГДЕ
	               |	упКонтрагенты.Партнер = &Партнер
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упКонтрагенты.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упТопливныеКарты.Ссылка КАК ТопливнаяКарта
	               |ИЗ
	               |	РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				ИЛИ Водитель В
	               |					(ВЫБРАТЬ
	               |						ВодительПоРейсу.Водитель1
	               |					ИЗ
	               |						ВодительПоРейсу КАК ВодительПоРейсу)) КАК упВыдачаТопливныхКартСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты КАК упТопливныеКарты
	               |		ПО упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта = упТопливныеКарты.Ссылка
	               |ГДЕ
	               |	упТопливныеКарты.Контрагент В
	               |			(ВЫБРАТЬ
	               |				СписокКонтрагентов.Контрагент
	               |			ИЗ
	               |				СписокКонтрагентов КАК СписокКонтрагентов)";
	
	Запрос.УстановитьПараметр("Партнер", ссПартнерАЗС);
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Запрос.УстановитьПараметр("мсвРейсы", Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс"));
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если Выборка.Количество() > 1 Тогда
			ссТопливнаяКарта = Справочники.упТопливныеКарты.ПустаяСсылка();
		Иначе 
			Выборка.Следующий();
			ссТопливнаяКарта = Выборка.ТопливнаяКарта;
		КонецЕсли;
		
	Иначе
		ссТопливнаяКарта = Справочники.упТопливныеКарты.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ссТопливнаяКарта;
	
КонецФункции // ПодборТопливнойКартыПоАЗСНаСервере()

&НаСервере
Функция ВернутьДоступныеТКНаСервере(ссПартнерАЗС)
	мсвТК = Новый Массив;	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПеревозчикПоРейсуСрезПоследних.Водитель1
	               |ПОМЕСТИТЬ ВодительПоРейсу
	               |ИЗ
	               |	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&мсвРейсы)) КАК упПеревозчикПоРейсуСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упКонтрагенты.Ссылка КАК Контрагент
	               |ПОМЕСТИТЬ СписокКонтрагентов
	               |ИЗ
	               |	Справочник.упКонтрагенты КАК упКонтрагенты
	               |{ГДЕ
	               |	(упКонтрагенты.Партнер = &Партнер)}
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упКонтрагенты.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упТопливныеКарты.Ссылка КАК ТопливнаяКарта
	               |ИЗ
	               |	РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних(
	               |			,
	               |			ТранспортноеСредство = &ТранспортноеСредство
	               |				ИЛИ Водитель В
	               |					(ВЫБРАТЬ
	               |						ВодительПоРейсу.Водитель1
	               |					ИЗ
	               |						ВодительПоРейсу КАК ВодительПоРейсу)) КАК упВыдачаТопливныхКартСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты КАК упТопливныеКарты
	               |		ПО упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта = упТопливныеКарты.Ссылка
	               |ГДЕ
	               |	упТопливныеКарты.Контрагент В
	               |			(ВЫБРАТЬ
	               |				СписокКонтрагентов.Контрагент
	               |			ИЗ
	               |				СписокКонтрагентов КАК СписокКонтрагентов)";
	
	Если ЗначениеЗаполнено(ссПартнерАЗС) Тогда
		Запрос.УстановитьПараметр("Партнер", ссПартнерАЗС);
	КонецЕсли;
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Запрос.УстановитьПараметр("мсвРейсы", Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс"));
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			мсвТК.Добавить(Выборка.ТопливнаяКарта);
		КонецЦикла; 
	КонецЕсли;
	
	Возврат мсвТК;
	
КонецФункции // ВернутьДоступныеАЗСНаСервере()

&НаСервереБезКонтекста
Функция ВернутьДоступныеВидыГСМНаСервере(ТранспортноеСредство)
	
	мсвДоступныхВидовГСМ = упПутевойЛистУчетГСМ.ВернутьДоступныеВидыГСМ(ТранспортноеСредство);
	Возврат мсвДоступныхВидовГСМ;
	
КонецФункции // ВернутьДоступныеВидыГСМНаСервере()

&НаСервереБезКонтекста
Функция ВернутьДоступныеВидыГСМДополнительноеОборудованиеНаСервере(ДополнительноеОборудование)
	мсвДоступныхВидовГСМ = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упДополнительноеОборудованиеВидыГСМ.ВидГСМ КАК ВидГСМ
	               |ИЗ
	               |	Справочник.упДополнительноеОборудование.ВидыГСМ КАК упДополнительноеОборудованиеВидыГСМ
	               |ГДЕ
	               |	упДополнительноеОборудованиеВидыГСМ.Ссылка = &ДополнительноеОборудование";
	
	Запрос.УстановитьПараметр("ДополнительноеОборудование", ДополнительноеОборудование);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			мсвДоступныхВидовГСМ.Добавить(Выборка.ВидГСМ);
		КонецЦикла; 
	КонецЕсли;
	
	Возврат мсвДоступныхВидовГСМ;
	
КонецФункции // ВернутьДоступныеВидыГСМНаСервере()

&НаКлиенте
// Процедура завершения после закрытия формы выбора вида ГСМ.
//
Процедура ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ТекущийЭлемент.ТекущиеДанные.ВидГСМ = Результат;
		ВидГСМПриИзмененииНаКлиенте();
	КонецЕсли;	
КонецПроцедуры // ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы выбора топливной карты.
//
Процедура ЗаправкиСливыГСМТопливнаяКартаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
		ТекущиеДанные.ТопливнаяКарта = Результат;
		Если Истина
			И НЕ ЗначениеЗаполнено(ТекущиеДанные.АЗС)
			И ЗначениеЗаполнено(ТекущиеДанные.ТопливнаяКарта)
		Тогда
			ТекущиеДанные.АЗС = ПартнерТопливнаяКарта(ТекущиеДанные.ТопливнаяКарта);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры // ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение()

&НаСервере
Функция ИмяПечатнойФормы()
	
	ИмяКоманды = Неопределено;
	Если ТипЗнч(ТипПечатнойФормыПутевогоЛиста) = Тип("ПеречислениеСсылка.упТипыПечатныхФормПутевогоЛиста") Тогда
		//Возврат ОбщегоНазначения.ИмяЗначенияПеречисления(ТипПечатнойФормыПутевогоЛиста);
		ИмяПФ = Строка(ТипПечатнойФормыПутевогоЛиста);
		Для каждого ТекущаяКоманда Из Команды Цикл
			Если СтрЧислоВхождений(ТекущаяКоманда.Заголовок,ИмяПФ) Тогда
				ИмяКоманды = ТекущаяКоманда.Имя;
			КонецЕсли;
		КонецЦикла;
		Возврат ИмяКоманды;	
	//Иначе	
	//	Возврат Неопределено;	
	КонецЕсли;

	Возврат ИмяКоманды;

КонецФункции // ИмяПечатнойФормы()

&НаСервере
Процедура УстановитьВидФормыВЗависимостиОтДоступныхКомпонент()
	
	Элементы.ДобавитьРейс.Видимость = НЕ ДанныеПоМаршрутуВводятсяВПутевомЛисте;
	Элементы.РассчитатьКонечныйПробегПоДаннымДатчиков.Видимость = Истина
														И ИспользуетсяУправлениеПеревозками
														И ИспользуетсяСпутниковыйМониторинг;
	Элементы.ГСМРассчитатьКонечныйОстатокТопливаПоДаннымДатчиков.Видимость = Истина
																И ИспользуетсяУправлениеПеревозками
																И ИспользуетсяСпутниковыйМониторинг;
	Элементы.ГруппаДанныеРейса.Видимость = ДанныеПоМаршрутуВводятсяВПутевомЛисте;
	Элементы.упКорректировкаРейсаВодитель1.Видимость = ДанныеПоМаршрутуВводятсяВПутевомЛисте;
	Элементы.ГруппаПеревозчик.Видимость = ДанныеПоМаршрутуВводятсяВПутевомЛисте;
	Элементы.ГруппаМассаГруза.Видимость = ДанныеПоМаршрутуВводятсяВПутевомЛисте;
	
	Если НЕ ДанныеПоМаршрутуВводятсяВПутевомЛисте Тогда
		Элементы.Рейс.Вид = ВидПоляФормы.ПолеВвода;
	Иначе
		Элементы.Рейс.Вид = ВидПоляФормы.ПолеНадписи;
		Элементы.Рейс.Гиперссылка = Истина;
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
		
	Если Статус = Перечисления.упСтатусыПутевогоЛиста.Отменен Тогда 
		ТолькоПросмотр = Истина;
	ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда
		
		УстановитьДоступностьНачальныхОстатков(Ложь);
	     УстановитьДоступностьКонечныхОстатков(Ложь);
		
		// Проверить, является ли путевой лист последним в цепочке изменявших остатки ГСМ и пробег документов.
		ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ 	= упПутевойЛистУчетГСМ.ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ(Объект.Ссылка, Объект.Дата);
		
		мсвТранспортныхСредств = Новый Массив;
		мсвТранспортныхСредств.Добавить(Объект.ТранспортноеСредство);
			
		ЭтоПоследнийДокументИзменявшийПробег = упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийПробег(Объект.Ссылка, Объект.Дата, мсвТранспортныхСредств);
		
		мсвДополнительноеОборудование = Новый Массив;
		Для каждого Строка Из Объект.ДополнительноеОборудование Цикл
			мсвДополнительноеОборудование.Добавить(Строка.ДополнительноеОборудование);
		КонецЦикла;

		мсвАгрегатыПоТС = упУправлениеЗапасами.АгрегатыПоТранспортномуСредству(Объект.Дата, Объект.ТранспортноеСредство);
		Для каждого Агрегат Из мсвАгрегатыПоТС Цикл
			мсвДополнительноеОборудование.Добавить(Агрегат);	
		КонецЦикла;
		ЭтоПоследнийДокументИзменявшийНаработкуОборудования = упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийНаработку(Объект.Ссылка, Объект.Дата, мсвДополнительноеОборудование);
		
		// Если документ последний, то открыт не только на просмотр.
		ТолькоПросмотр = НЕ (Истина
						 И ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ 
						 И ЭтоПоследнийДокументИзменявшийПробег
						 И ЭтоПоследнийДокументИзменявшийНаработкуОборудования);	
		
		// Запрещено изменять значения табличных форм.
		Для Каждого текЭлемент Из Элементы Цикл 
			Если ТипЗнч(текЭлемент) = Тип("ПолеФормы")
				ИЛИ ТипЗнч(текЭлемент) = Тип("ТаблицаФормы") Тогда 
				текЭлемент.ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Элементы.ГСМРассчитатьКонечныйОстаток.Доступность          = Ложь;
		Элементы.ГСМРассчитатьНормативныйРасходТоплива.Доступность = Ложь;
		Элементы.ГСМРассчитатьПлановыйРасходТоплива.Доступность    = Ложь;
		Элементы.ГСМРассчитатьПлановыйРасходТопливаСРасшифровкой.Доступность    = Ложь;
		Элементы.ГСМРассчитатьНормативныйРасходТопливаСРасшифровкой.Доступность = Ложь;
		
		Элементы.НормыРасходаГСМОбновитьИспользуемыеНормы.Доступность = Ложь;
		Элементы.ГСМДополнительноеОборудованиеРассчитатьНормативныйРасходТопливаДопОборудования.Доступность	= Ложь;
		Элементы.ГСМДополнительноеОборудованиеРассчитатьНормативныйРасходТопливаДопОборудованияСРасшифровкой.Доступность = Ложь;
		Элементы.НормыРасходаГСМДополнительноеОборудованиеЗаполнитьНадбавкиДопОборудование.Доступность = Ложь;
		Элементы.НормыРасходаРежимовРаботыЗаполнитьРежимыРаботыДопОборудования.Доступность = Ложь;
		Элементы.ДополнительноеОборудованиеЗаполнитьДополнительноеОборудование.Доступность = Ложь;
	                             
		Элементы.ДобавитьРейс.Доступность = Ложь;
		Элементы.Рейсы.ТолькоПросмотр     = Истина;
		Элементы.Заявки.ТолькоПросмотр    = Истина;
			
	ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда
		
		УстановитьДоступностьНачальныхОстатков(Ложь);
	     УстановитьДоступностьКонечныхОстатков(Истина);
		
		// ГСМ.
		Элементы.ГСМ.ТолькоПросмотр = Ложь;
		
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМВидГСМ, 		 Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДопОборудование, Ложь);
				
		Элементы.ГСМРассчитатьКонечныйОстаток.Доступность		 	 = Истина И ДоступнаКнопкаРассчитатьКонечныйОстаток;
		Элементы.ГСМРассчитатьНормативныйРасходТоплива.Доступность	 = Истина;
		Элементы.ГСМРассчитатьПлановыйРасходТоплива.Доступность	 = Истина;
		
		// Редактирование таблицы ГСМ 
		Элементы.ГСМДобавить.Доступность 						 = Ложь;
		Элементы.ГСМКонтекстноеМенюКнопкаДобавить.Доступность 	 	 = Ложь;
		Элементы.ГСМСкопировать.Доступность 					 = Ложь;
		Элементы.ГСМКонтекстноеМенюКнопкаСкопировать.Доступность 	 = Ложь;
		Элементы.ГСМУдалить.Доступность 						 = Ложь;
		Элементы.ГСМКонтекстноеМенюКнопкаУдалить.Доступность 	 	 = Ложь;
		Элементы.НормыРасходаГСМОбновитьИспользуемыеНормы.Доступность = Ложь;
		
		// Нормы расхода.
		Элементы.НормыРасходаГСМ.ТолькоПросмотр = Ложь;
		
		// Доп. обрудование
		Элементы.ДополнительноеОборудование.ТолькоПросмотр		= Ложь;
		Элементы.ДополнительноеОборудованиеДобавить.Доступность 	= Ложь;
		Элементы.ДополнительноеОборудованиеЗаполнитьДополнительноеОборудование.Доступность 	= Ложь;
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеДополнительноеОборудование,	Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеТранспортноеСредство,	Ложь);
		
		Элементы.ДополнительноеОборудованиеЗаполнитьДополнительноеОборудование.Доступность 				 = Ложь;
		Элементы.ДополнительноеОборудованиеДобавить.Доступность 				 = Ложь;
		Элементы.ДополнительноеОборудованиеКонтекстноеМенюДобавить.Доступность 	 = Ложь;
		Элементы.ДополнительноеОборудованиеСкопировать.Доступность 				 = Ложь;
		Элементы.ДополнительноеОборудованиеКонтекстноеМенюСкопировать.Доступность 	 = Ложь;
		Элементы.ДополнительноеОборудованиеУдалить.Доступность 				 = Ложь;
		Элементы.ДополнительноеОборудованиеКонтекстноеМенюУдалить.Доступность 	 = Ложь;
		
		// Режимы работы.
		Элементы.НормыРасходаРежимовРаботыЗаполнитьРежимыРаботыДопОборудования.Доступность = Ложь;
		Элементы.НормыРасходаРежимовРаботы.ТолькоПросмотр = Ложь;
		
		// ГСМ доп. оборудование.
		Элементы.ГСМДополнительноеОборудование.ТолькоПросмотр		= Ложь;
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеВидГСМ, 		 	Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеДополнительноеОборудование, 		 	Ложь);
		Элементы.ГСМДополнительноеОборудованиеДобавить.Доступность 						 = Ложь;
		Элементы.ГСМДополнительноеОборудованиеКонтекстноеМенюКнопкаДобавить.Доступность 	 = Ложь;
		Элементы.ГСМДополнительноеОборудованиеСкопировать.Доступность 					 = Ложь;
		Элементы.ГСМДополнительноеОборудованиеКонтекстноеМенюКнопкаСкопировать.Доступность 	 = Ложь;
		Элементы.ГСМДополнительноеОборудованиеУдалить.Доступность 						 = Ложь;
		Элементы.ГСМДополнительноеОборудованиеКонтекстноеМенюКнопкаУдалить.Доступность 	 	 = Ложь;
		
		
		Элементы.НормыРасходаГСМДополнительноеОборудованиеЗаполнитьНадбавкиДопОборудование.Доступность = Ложь;
		
		// Рейсы.
		Элементы.ДобавитьРейс.Доступность = Истина;
		Элементы.Рейсы.ТолькоПросмотр	    = Ложь;
		Элементы.Заявки.ТолькоПросмотр    = Истина;
		
	ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда	
		
		УстановитьДоступностьНачальныхОстатков(Ложь);
	     УстановитьДоступностьКонечныхОстатков(Ложь);
		
		// Проверить, является ли путевой лист последним в цепочке изменявших остатки ГСМ и пробег документов.
		ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ 	= упПутевойЛистУчетГСМ.ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ(Объект.Ссылка, Объект.Дата);
		ЭтоПоследнийПутевойЛистПоТС = упПутевойЛистУчетГСМ.ЭтоПоследнийПутевойЛистПоТС(Объект.Ссылка, Объект.Дата, Объект.ТранспортноеСредство);

		// ГСМ.
		Элементы.ГСМ.ТолькоПросмотр							 = Истина;
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМВидГСМ, 		 	Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДопОборудование,	Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМНормаРасхода,		Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМСуммаРасходовПлан,	Ложь);
		
		Элементы.ГСМРассчитатьКонечныйОстаток.Доступность		 	 = Истина
														   И ЭтоПоследнийПутевойЛистПоТС
														   И ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ;
		Элементы.ГСМРассчитатьНормативныйРасходТоплива.Доступность	 = Истина;
		Элементы.ГСМРассчитатьПлановыйРасходТоплива.Доступность	 = Истина;
		
		// Нормы расхода.
		Элементы.НормыРасходаГСМОбновитьИспользуемыеНормы.Доступность = Ложь;
		Элементы.НормыРасходаГСМ.ТолькоПросмотр = Истина;
		
		// Заправки и сливы.
		Элементы.ЗаправкиСливыГСМ.ТолькоПросмотр				 = Ложь;
		
		// Доп. обрудование
		Элементы.ДополнительноеОборудованиеДобавить.Доступность 	= Ложь;
		Элементы.ДополнительноеОборудованиеЗаполнитьДополнительноеОборудование.Доступность 	= Ложь;
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеДополнительноеОборудование, Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеТранспортноеСредство,       Ложь);
		
		// Режимы работы.
		Элементы.НормыРасходаРежимовРаботыЗаполнитьРежимыРаботыДопОборудования.Доступность = Ложь;
		Элементы.НормыРасходаРежимовРаботы.ТолькоПросмотр = Истина;
		
		// ГСМ доп. оборудование.
		Элементы.ГСМДополнительноеОборудование.ТолькоПросмотр = Истина;
		
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеРасходИзСобственногоБака,   Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеРасходТопливаСТС,           Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеВидГСМ,                     Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеДополнительноеОборудование, Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеНормаРасхода,               Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеСуммаРасходовПлан,          Ложь);
		
		Элементы.НормыРасходаГСМДополнительноеОборудованиеЗаполнитьНадбавкиДопОборудование.Доступность = Ложь;
		
		// Заправки и сливы доп. оборудование.
		Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТолькоПросмотр		= Ложь;
		Элементы.ЗаправкиСливыГСМДополнительноеОборудованиеДобавить.Доступность 	= Истина;
		
          // Рейсы.
		Элементы.ДобавитьРейс.Доступность = Ложь;
		Элементы.Рейсы.ТолькоПросмотр     = Истина;
		Элементы.Заявки.ТолькоПросмотр    = Истина;
		
		// Шапка.
		Элементы.ОкончаниеРейса.ТолькоПросмотр    = Ложь;
		Элементы.ОкончаниеРейса.Доступность       = Истина;
		Элементы.ТранспортноеСредство.Доступность = Ложь;
		Элементы.Гараж.Доступность                = Ложь;
		
		
	ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Новый Тогда 	
		
		УстановитьДоступностьНачальныхОстатков(Истина);
	     УстановитьДоступностьКонечныхОстатков(Ложь);
		
		// ГСМ.
		Элементы.ГСМ.ТолькоПросмотр		= Ложь;
		Элементы.ГСМДобавить.Доступность 	= Истина;
		Элементы.НормыРасходаГСМОбновитьИспользуемыеНормы.Доступность = Истина;
		
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМВидГСМ, 		 	Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДопОборудование,	Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМНормаРасхода,		Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМСуммаРасходовПлан,	Истина);
		
		Элементы.ГСМРассчитатьКонечныйОстаток.Доступность		 	 = Истина И ДоступнаКнопкаРассчитатьКонечныйОстаток;
		Элементы.ГСМРассчитатьНормативныйРасходТоплива.Доступность	 = Истина;
		Элементы.ГСМРассчитатьПлановыйРасходТоплива.Доступность	 = Истина;
		
		// Нормы расхода.
		Элементы.НормыРасходаГСМОбновитьИспользуемыеНормы.Доступность = Истина;
		Элементы.НормыРасходаГСМ.ТолькоПросмотр = Ложь;
		
		// Заправки и сливы.
		Элементы.ЗаправкиСливыГСМ.ТолькоПросмотр 	 = Ложь;
		Элементы.ЗаправкиСливыГСМДобавить.Доступность = Истина;
		
		// Доп. обрудование
		Элементы.ДополнительноеОборудованиеДобавить.Доступность 	= Истина;
		Элементы.ДополнительноеОборудованиеЗаполнитьДополнительноеОборудование.Доступность 	= Истина;
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеДополнительноеОборудование,	Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеТранспортноеСредство,	Истина);
		
		// Режимы работы.
		Элементы.НормыРасходаРежимовРаботыЗаполнитьРежимыРаботыДопОборудования.Доступность = Истина;
		Элементы.НормыРасходаРежимовРаботы.ТолькоПросмотр = Ложь;
		
		// ГСМ доп. оборудование.
		Элементы.ГСМДополнительноеОборудование.ТолькоПросмотр		= Ложь;
		Элементы.ГСМДополнительноеОборудованиеДобавить.Доступность 	= Истина;
				
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеРасходИзСобственногоБака, 	Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеРасходТопливаСТС,	Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеВидГСМ, 		 	Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеДополнительноеОборудование, 		 	Ложь);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеНормаРасхода,		Истина);
		ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеСуммаРасходовПлан,	Истина);
		
		Элементы.НормыРасходаГСМДополнительноеОборудованиеЗаполнитьНадбавкиДопОборудование.Доступность = Истина;
		
		// Заправки и сливы доп. оборудование.
		Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТолькоПросмотр		= Ложь;
		Элементы.ЗаправкиСливыГСМДополнительноеОборудованиеДобавить.Доступность 	= Истина;
		
		// Рейсы.
		Элементы.Рейсы.ТолькоПросмотр     = Ложь;
		Элементы.ДобавитьРейс.Доступность = Истина;
		Элементы.Заявки.ТолькоПросмотр    = Ложь;

	КонецЕсли; 	  
	
	ДоступностьКнопкиЗаполненияГСМ = Истина 
								И НЕ ТолькоПросмотр 
								И (	Ложь
									Или НЕ Объект.Проведен 
									Или Статус = Перечисления.упСтатусыПутевогоЛиста.Новый
									);						
	
	Элементы.ГСМЗаполнитьГСМПоУмолчанию.Доступность 			= ДоступностьКнопкиЗаполненияГСМ;
     Элементы.ЗаполнитьГСМДополнительноеОборудование.Доступность = ДоступностьКнопкиЗаполненияГСМ;
												
	// Установить доступность кнопки "Статус отменить".
	// Доступна для нового документа или если рейс отмене.
	Если НЕ ТолькоПросмотр Тогда
		
		Если ЗначениеЗаполнено(Объект.Рейсы.Количество()) Тогда
			РейсОтменен = Истина;
			тбзСтатусыРейса = упРаботаСоСтатусами.ПолучитьТекущиеСтатусыРейсов(Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс"));
			Для каждого СтрокаСтатус Из тбзСтатусыРейса Цикл
				РейсОтменен = РейсОтменен И СтрокаСтатус.Статус = Перечисления.упСтатусыРейса.Отменен;	
				Если НЕ РейсОтменен Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе	
			РейсОтменен = Ложь;
		КонецЕсли;
		
		Элементы.СтатусОтменить.Доступность = 	Ложь
										Или Статус = Перечисления.упСтатусыПутевогоЛиста.Новый
										Или РейсОтменен;
	КонецЕсли;
	
	Элементы.ФормаПечатьПутевогоЛиста.Доступность = ЗначениеЗаполнено(ТипПечатнойФормыПутевогоЛиста);
	
	// Установить заголовок кнопки печати по умолчанию.
	Если Истина
		И ЗначениеЗаполнено(ТипПечатнойФормыПутевогоЛиста) 
		И (	Ложь
			Или Истина
				И ТипЗнч(ТипПечатнойФормыПутевогоЛиста) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") 
		 		И ИспользоватьДополнительныеОтчетыИОбработки
			Или ТипЗнч(ТипПечатнойФормыПутевогоЛиста) = Тип("ПеречислениеСсылка.упТипыПечатныхФормПутевогоЛиста")
		  ) 
	Тогда
		Элементы.ФормаПечатьПутевогоЛиста.Заголовок = "Печать """ + ТипПечатнойФормыПутевогоЛиста + """";
	Иначе
		Элементы.ФормаПечатьПутевогоЛиста.Заголовок = "Печать (по умолч)";	
	КонецЕсли;
	
	// Контроль совпадения часовых поясов.
	Если Истина
		И НЕ ТолькоПросмотр
		И ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах 
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьНачальныхОстатков(Доступность)
	Элементы.ПробегНачало.Доступность	 = Доступность И НЕ ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств;
	Элементы.ОдометрНачало.Доступность  = Доступность;
	Элементы.МоточасыНачало.Доступность = Доступность;
	ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМНачОстаток, Доступность);
	ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ДополнительноеОборудованиеМоточасыНачало, Доступность);
	ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеНачОстаток, Доступность);
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКонечныхОстатков(Доступность)
	Элементы.ПробегОкончание.Доступность	 = Доступность И НЕ ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств;
	Элементы.ОдометрОкончание.Доступность	 = Доступность;
	Элементы.МоточасыОкончание.Доступность 	 = Доступность;
	ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМКонОстаток, Доступность);
	ИзменитьДоступностьЭлементаТабличнойЧасти(Элементы.ГСМДополнительноеОборудованиеКонОстаток, Доступность);	
КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьЭлементаТабличнойЧасти(Элемент, Доступность)
	//Элемент.Доступность 	= Доступность;
	Элемент.ТолькоПросмотр	= НЕ Доступность;
КонецПроцедуры

#Область Статусы

&НаСервере
Функция УстановитьСтатусВыданНаСервере()
	
	Если (Статус = Перечисления.упСтатусыПутевогоЛиста.Новый) Тогда 
		Возврат ИзменитьСтатусПутевогоЛиста(Перечисления.упСтатусыПутевогоЛиста.Выдан);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус путевого листа должен быть ""Новый""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

&НаСервере
Функция УстановитьСтатусВозвращенНаСервере()
	
	Возврат ИзменитьСтатусПутевогоЛиста(Перечисления.упСтатусыПутевогоЛиста.Возвращен);

КонецФункции

&НаСервере
Функция УстановитьСтатусЗакрытНаСервере()
	
	Возврат ИзменитьСтатусПутевогоЛиста(Перечисления.упСтатусыПутевогоЛиста.Закрыт);

КонецФункции

&НаСервере
Функция УстановитьСтатусОтмененоНаСервере()
	
	Возврат ИзменитьСтатусПутевогоЛиста(Перечисления.упСтатусыПутевогоЛиста.Отменен);

КонецФункции

&НаСервере
Функция ИзменитьСтатусПутевогоЛиста(СтатусПутевогоЛиста)
	
	НовыйСтатус = СтатусПутевогоЛиста;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение
			
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда 
				ТекстСообщения = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ИзменениеСтатусаПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПутевойЛист,Объект.Ссылка, ТекстСообщения);
			Возврат Ложь;    
			
		КонецПопытки;
		
		ИзменилсяСтатус();
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус()
	Статус = Документы.упПутевойЛист.ПолучитьСтатус(Объект.Ссылка);
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаПутевогоЛиста(Объект.Ссылка, Статус, Элементы);	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ИзменитьРейсНаСервере()
	
	мсвРейсы = Новый Массив;
	мсвРейсы.Добавить(Объект.Рейс);
	ВыполнитьМетодОбъекта("Заполнить(Параметр1)", мсвРейсы);
	Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясОснования	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Рейс, "ЧасовойПояс");
		Если НЕ ЧасовойПоясОснования = Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясОснования;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ИзменитьТранспортноеСредствоНаСервере()
	
	ВыполнитьМетодОбъекта("Заполнить(Объект.ТранспортноеСредство)");
	ТипПечатнойФормыПутевогоЛиста = упПутевойЛистУчетГСМ.ПолучитьТипПечатнойФормы(Объект.ТранспортноеСредство);
	ОтобразитьПрицепы();
	ВычислитьПройденноеРасстояние(); 
	ЗаполнитьМассуПрицепногоСостава();
	ЗаблокироватьЭлементы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьОдометр()
	Объект.Одометр = Объект.ОдометрОкончание - Объект.ОдометрНачало;
	Объект.Пробег 	= Объект.Одометр;
	Объект.ПробегОкончание = Объект.ПробегНачало + Объект.Одометр;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьПробег()
	Объект.Пробег = Объект.ПробегОкончание - Объект.ПробегНачало;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьМоточасы(ОбъектПересчета)
	ОбъектПересчета.Моточасы = ОбъектПересчета.МоточасыОкончание - ОбъектПересчета.МоточасыНачало;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьМоточасыПоРежимам(ОбъектПересчета)
	
	ДопОборудование = ОбъектПересчета.ДополнительноеОборудование;
	мсвСтрокРежимыПоДопОборудованию = Объект.НормыРасходаРежимовРаботы.НайтиСтроки(Новый Структура("ДополнительноеОборудование", ДопОборудование));
	
	Моточасы = 0;
	
	Для каждого Строка Из мсвСтрокРежимыПоДопОборудованию Цикл
		Моточасы = Моточасы + Строка.Количество;
	КонецЦикла;
	
	мсвСтрокаДопОборудование =  Объект.ДополнительноеОборудование.НайтиСтроки(Новый Структура("ДополнительноеОборудование", ДопОборудование));
	Если мсвСтрокаДопОборудование.Количество() > 0 Тогда
		СтрокаДопОборудование = мсвСтрокаДопОборудование[0];
		СтрокаДопОборудование.Моточасы = Моточасы;
		ПересчитатьМоточасыОкончание(СтрокаДопОборудование);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПересчитатьМоточасыОкончание(ОбъектПересчета)
	ОбъектПересчета.МоточасыОкончание =  ОбъектПересчета.МоточасыНачало + ОбъектПересчета.Моточасы;
КонецПроцедуры

&НаСервере
Функция ДобавитьСтрокуГСМ(ДопОборудование, ВидГСМ)
	
	ТранспортноеСредство = ?(ЗначениеЗаполнено(ДопОборудование), ДопОборудование, Объект.ТранспортноеСредство);
	
	Строка = Объект.ГСМ.Добавить();
	Строка.ВидГСМ 			= ВидГСМ;
	Строка.ДопОборудование 	= ДопОборудование;
	Строка.НачОстаток		= упПутевойЛистУчетГСМ.УровеньТоплива(ТранспортноеСредство, Строка.ВидГСМ);

КонецФункции

&НаСервере
Функция ДобавитьСтрокуГСМДопОборудование(ДопОборудование, ВидГСМ)
	
	Строка = Объект.ГСМДополнительноеОборудование.Добавить();
	Строка.ВидГСМ 					= ВидГСМ;
	Строка.ДополнительноеОборудование 	= ДопОборудование;
	Строка.НачОстаток				= упПутевойЛистУчетГСМ.УровеньТоплива(ДопОборудование, ВидГСМ);

КонецФункции

&НаСервере
Процедура РассчитатьПлановыйРасходТопливаНаСервере()
	ВыполнитьМетодОбъекта("РассчитатьПлановыйРасходТоплива()");
КонецПроцедуры

&НаСервере
Процедура РассчитатьНормативныйРасходТопливаНаСервере()
	ВыполнитьМетодОбъекта("РассчитатьНормативныйРасходТоплива()");
КонецПроцедуры

&НаСервере
Процедура РассчитатьНормативныйРасходТопливаДопОборудованияНаСервере()
	ВыполнитьМетодОбъекта("РассчитатьНормативныйРасходТопливаДопОборудования()");
КонецПроцедуры

&НаСервере
Функция РассчитатьНормативныйРасходТопливаНаСервереСРасшифровкой()
	
	ДокументПутевойЛист = РеквизитФормыВЗначение("Объект");
	Результат = ДокументПутевойЛист.РассчитатьНормативныйРасходТопливаСРасшифровкой();
	ЗначениеВРеквизитФормы(ДокументПутевойЛист, "Объект");	
	ЗаполнитьДополнительныеПоляТабличныхЧастей();
	Модифицированность = Истина;	

	Возврат Результат;
КонецФункции

Функция РассчитатьПлановыйРасходТопливаНаСервереСРасшифровкой()
	
	ДокументПутевойЛист = РеквизитФормыВЗначение("Объект");
	Результат = ДокументПутевойЛист.РассчитатьПлановыйРасходТопливаСРасшифровкой();
	ЗначениеВРеквизитФормы(ДокументПутевойЛист, "Объект");	
	ЗаполнитьДополнительныеПоляТабличныхЧастей();
	Модифицированность = Истина;	

	Возврат Результат;
КонецФункции

&НаСервере
Функция РассчитатьНормативныйРасходТопливаДопОборудованияНаСервереСРасшифровкой()
	
	ДокументПутевойЛист = РеквизитФормыВЗначение("Объект");
	Результат = ДокументПутевойЛист.РассчитатьНормативныйРасходТопливаДопОборудованияСРасшифровкой();
	ЗначениеВРеквизитФормы(ДокументПутевойЛист, "Объект");	
	ЗаполнитьДополнительныеПоляТабличныхЧастей();
	Модифицированность = Истина;	

	Возврат Результат;
КонецФункции

&НаСервере
Процедура РассчитатьКонечныйОстатокНаСервере()
	ВыполнитьМетодОбъекта("РассчитатьКонечныйОстаток()");
КонецПроцедуры

&НаСервере
Процедура РассчитатьКонечныйОстатокТопливаПоДаннымДатчиковНаСервере()
	
	тбзГСМ = Объект.ГСМ.Выгрузить();
	тбзГСМ.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства, СправочникСсылка.упДополнительноеОборудование"));
	
	Для каждого СтрокаТЧ Из тбзГСМ Цикл
		СтрокаТЧ.ТранспортноеСредство 	= ?(ЗначениеЗаполнено(СтрокаТЧ.ДопОборудование), СтрокаТЧ.ДопОборудование, Объект.
		ТранспортноеСредство);
	КонецЦикла;
		
	ТекстОшибки = "";
	Документы.упПутевойЛист.ЗаполнитьДаннымиДатчиковУровняТоплива(Объект, ТекстОшибки, тбзГСМ);
	
	Если НЕ ТекстОшибки = "" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект.Ссылка);	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьКонечныйПробегПоДаннымДатчиковНаСервере()
	ТекстОшибки = "";
	Документы.упПутевойЛист.ЗаполнитьДаннымиДатчиковПробега(Объект,ТекстОшибки);
	Если НЕ ТекстОшибки = "" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект.Ссылка, "Пробег" );	
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьУровеньТопливаНаСервере(ТранспортноеСредство, ВидГСМ)
	НачОстаток		= упПутевойЛистУчетГСМ.УровеньТоплива(ТранспортноеСредство, ВидГСМ);
	Возврат НачОстаток;
КонецФункции

&НаКлиенте
Процедура ОповеститьОбОшибке()
	ТекстОшибки = НСтр("ru = 'При изменении статуса произошла ошибка.'");
	упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	
	ИтоговаяСумма 		= 0;
	ИтоговаяСуммаНДС 	= 0;

	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",	"Стоимость");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СтоимостьНДС");
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ЗаправкиСливыГСМ", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 	= ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС 	= ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ЗаправкиСливыГСМДополнительноеОборудование", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	Объект.СуммаРасходы 	= ИтоговаяСумма;
	Объект.СуммаРасходыНДС	= ИтоговаяСуммаНДС; 

КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент, ИмяТабличнойЧасти = "ЗаправкиСливыГСМ")
	ТекущаяСтрока		= Элементы[ИмяТабличнойЧасти].ТекущиеДанные;
	сткТекущаяСтрока	= Новый Структура("Сумма, СуммаНДС, СтавкаНДС, Цена, Количество", ТекущаяСтрока.Стоимость, ТекущаяСтрока.СтоимостьНДС, ТекущаяСтрока.СтавкаНДС, ТекущаяСтрока.Цена, ТекущаяСтрока.Количество);
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя, ИмяТабличнойЧасти),СтруктураДанные);
	ТекущаяСтрока.Стоимость 		= сткТекущаяСтрока.Сумма;
	ТекущаяСтрока.СтоимостьНДС 	= сткТекущаяСтрока.СуммаНДС;
	ТекущаяСтрока.Цена 			= сткТекущаяСтрока.Цена;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля, ИмяТабличнойЧасти)
	Возврат СтрЗаменить(ИмяПоля, ИмяТабличнойЧасти, "");
КонецФункции

&НаСервереБезКонтекста
Функция СтавкаНДСПоГСМ(ВидГСМ)
	сткРеквизитыВидГСМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидГСМ, "СтатьяРасходов, СтатьяРасходов.СтавкаНДС");
	СтатьяРасходов = сткРеквизитыВидГСМ.СтатьяРасходов;
	СтавкаНДС = сткРеквизитыВидГСМ.СтатьяРасходовСтавкаНДС;
	Возврат СтавкаНДС;
КонецФункции

&НаКлиенте
// Процедура завершения после выбора типа для приемки слива ГСМ.
//
Процедура ЗавершениеВыбораТипаДляПриемникСливаГСМ(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		
		сткПараметры = Новый Структура("ВидГСМ", Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ВидГСМ);
		Если ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.упТранспортныеСредства")) Тогда
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
			текИмяФормы			= "Справочник.упТранспортныеСредства.ФормаВыбора";
		ИначеЕсли ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.упДополнительноеОборудование")) Тогда
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка");
			текИмяФормы			= "Справочник.упДополнительноеОборудование.ФормаВыбора";
		Иначе
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упСклады.ПустаяСсылка");
			текИмяФормы			= "Справочник.упСклады.ФормаВыбора";
		КонецЕсли;
		
		Элементы.ЗаправкиСливыГСМ.ТекущиеДанные.ПриемникСливаГСМ = текПустоеЗначение;
		ОткрытьФорму(текИмяФормы,сткПараметры,Элементы.ЗаправкиСливыГСМПриемникСливаГСМ,УникальныйИдентификатор);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора типа для приемки слива ГСМ.
//
Процедура ЗавершениеВыбораТипаДляПриемникСливаГСМДополнительноеОборудование(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		
		ТекущиеДанные = Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ТекущиеДанные;
		
		сткПараметры = Новый Структура("ВидГСМ", ТекущиеДанные.ВидГСМ);
		Если ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.упТранспортныеСредства")) Тогда
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
			текИмяФормы			= "Справочник.упТранспортныеСредства.ФормаВыбора";
		ИначеЕсли ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.упДополнительноеОборудование")) Тогда
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка");
			текИмяФормы			= "Справочник.упДополнительноеОборудование.ФормаВыбора";
		Иначе
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упСклады.ПустаяСсылка");
			текИмяФормы			= "Справочник.упСклады.ФормаВыбора";
		КонецЕсли;
		
		ТекущиеДанные.ПриемникСливаГСМ = текПустоеЗначение;
		ОткрытьФорму(текИмяФормы,сткПараметры,Элементы.ЗаправкиСливыГСМДополнительноеОборудованиеПриемникСливаГСМ,УникальныйИдентификатор);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Функция СписокСтатусовДоступныхРейсов()
	
	Массив = Новый Массив();
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.НазначеноТС"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению"));
	
	Возврат Массив;
	
КонецФункции

&НаКлиенте
Функция ОтборДляРейсов()
	
	сткОтбор = Новый Структура("Статус", СписокСтатусовДоступныхРейсов());
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		сткОтбор.Вставить("Организация" , 			Объект.Организация);	
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		сткОтбор.Вставить("ТранспортноеСредство" , 	Объект.ТранспортноеСредство);	
	КонецЕсли;

	Возврат сткОтбор;
	
КонецФункции

#Область упРМДиспетчераГаража

&НаКлиенте
// Выполнить получение данных проверки.
//
// Параметры:
//  СтатусДокументаПроверки - Перечисление.упСтатусыПутевогоЛиста - Статус проверяемого документа.
//
// Возвращаемое значение:
//   Структура   - Ключи результаты проверки.
//
Функция ПолучитьРезультатПроверкиФормы(СтатусДокументаПроверки) Экспорт
	
	НовыйСтатус = СтатусДокументаПроверки;
	ТекущийРезультатПроверки = ВыполнитьПроверкуОбъектаНаСервере();

	Возврат ТекущийРезультатПроверки;

КонецФункции // ПолучитьРезультатПроверкиФормы()

&НаСервере
// Выполнить проверку объекта на сервере.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Структура   - Ключи результаты проверки.
//
Функция ВыполнитьПроверкуОбъектаНаСервере()

	СтруктураОтвета = Новый Структура();
	СтруктураОтвета.Вставить("Отказ",НЕ ПроверитьЗаполнение());
	
	ТекущийМассивСообщений = ПолучитьСообщенияПользователю();
	
	ОписаниеОшибки = "";
	
	Для каждого ТекущееСообщение Из ТекущийМассивСообщений Цикл
	
		ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки),"",Символы.ПС) + ТекущееСообщение.Текст;
	
	КонецЦикла;
	
	СтруктураОтвета.Вставить("ОписаниеОшибки", ОписаниеОшибки);
	
	Возврат СтруктураОтвета;

КонецФункции // ВыполнитьПроверкуОбъектаНаСервере()

#КонецОбласти

&НаСервере
Процедура НастроитьДоступностьВводаМоточасовПоТС()

	ИспользуетсяСчетчикМоточасов = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		ИспользуетсяСчетчикМоточасов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТранспортноеСредство, "СчетчикМоточасов");
	КонецЕсли;
	
	ИспользуетсяСчетчикМоточасов = Ложь
				    			 Или Объект.Моточасы > 0 
				    			 Или ИспользуетсяСчетчикМоточасов;

	упУправленияЗаявками.НастроитьФормуПутевогоЛиста(ЭтаФорма, Объект.ПорядокИсполненияЗаявки, ИспользуетсяСчетчикМоточасов);

КонецПроцедуры

&НаСервере
Функция ПараметрыОбработкиПриВыбореТранспортногоСредства(ТранспортноеСредство)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	ПодбиратьГаражи = Объект.ВидТранспортнойУслуги <> ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ПеревозкаГрузов");
	сткПараметрыОбработки = текКорректировкаМаршрутаОбъект.ОбработатьИзменениеТранспортногоСредстваРейса(ТранспортноеСредство, , , ПодбиратьГаражи);		
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	Возврат сткПараметрыОбработки;
КонецФункции

&НаКлиенте
// Процедура завершения после закрытия формы выбора сотрудника.
//
Процедура Водитель1НачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		упКорректировкаРейса.Водитель1 = Результат;
		Если ВедетсяУчетДокументов Тогда
			Отказ = Ложь;
			ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(Объект.ТранспортноеСредство, упКорректировкаРейса.Водитель1, Отказ);	
		КонецЕсли;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель, Отказ)
	
	упРейс.ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель, Отказ);	
		
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеАдресаМаршрута(НомерСтроки)
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеАдресаМаршрута(НомерСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТочкаМаршрутаНаСервере(НомерСтроки, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено)
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиМаршрута(НомерСтроки, НоваяСтрока, Удалить, ОтменаРедактирования, Отказ, мсвВыделенныеСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТочкуПоЗаданиюНаСервере()
	
	Элементы.упКорректировкаРейсаМаршрут.ТекущаяСтрока = ДобавитьТочкуВМаршрут(Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКонтрольнуюТочкуНаСервере()
	
	Элементы.упКорректировкаРейсаМаршрут.ТекущаяСтрока = ДобавитьТочкуВМаршрут(Перечисления.упТипыТочекМаршрута.КонтрольнаяТочка);
	
КонецПроцедуры

&НаСервере
Функция ДобавитьТочкуВМаршрут(ТипТочки = Неопределено, Адрес = Неопределено)
		
	НоваяСтрока = упКорректировкаРейса.Маршрут.Добавить();
	НоваяСтрока.Идентификатор   = Новый УникальныйИдентификатор;
	НоваяСтрока.СтрокаНомер     = НоваяСтрока.НомерСтроки;
	НоваяСтрока.СтрокаДокумента = Истина;
	Если ЗначениеЗаполнено(Адрес) Тогда
		НоваяСтрока.Адрес = Адрес;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипТочки) Тогда
		НоваяСтрока.ТипТочки       = ТипТочки;	
		НоваяСтрока.ИндексКартинки = Перечисления.упТипыТочекМаршрута.Индекс(ТипТочки);
	КонецЕсли;
	
	// Проверка что перед добавляемой точкой не было гаража.
	ИндексПредыдущейСтроки = НоваяСтрока.СтрокаНомер - 2;
	Если ИндексПредыдущейСтроки >= 0 Тогда
		ПредыдущаяСтрока = упКорректировкаРейса.Маршрут[ИндексПредыдущейСтроки];
		Если ПредыдущаяСтрока.ТипТочки  = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения Тогда
			 упКорректировкаРейса.Маршрут.Сдвинуть(НоваяСтрока.СтрокаНомер - 1,-1);
			 НоваяСтрока.СтрокаНомер      = НоваяСтрока.НомерСтроки;
			 ПредыдущаяСтрока.СтрокаНомер = ПредыдущаяСтрока.НомерСтроки;
		КонецЕсли;	
	КонецЕсли;
	 
	Возврат НоваяСтрока.ПолучитьИдентификатор();
	
КонецФункции

&НаКлиенте
// Процедура завершения после закрытия формы "ФормаРедактированияПосещенияГаражаВМаршруте".
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура РедактированиеПосещенияГаражаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		
		РедактироватьПосещениеГаражаНаСервере(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура РедактироватьПосещениеГаражаНаСервере(СтруктураДанных)
	
	НачалоМаршрута    = СтруктураДанных.НачалоМаршрута;
	ОкончаниеМаршрута = СтруктураДанных.ОкончаниеМаршрута;
	
	ПересчитатьВремя = Ложь;
	
	текНачалоМаршрута = Неопределено;
	ИскомыеСтроки = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("ТипТочки", Перечисления.упТипыТочекМаршрута.ТочкаОтправления));
	Если ИскомыеСтроки.Количество() Тогда
		текНачалоМаршрута = ИскомыеСтроки[0];
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(НачалоМаршрута) Тогда
		Если текНачалоМаршрута = Неопределено Тогда
			НоваяСтрока = упКорректировкаРейса.Маршрут.Вставить(0);
			НоваяСтрока.Гараж = НачалоМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачалоМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			НоваяСтрока.Адрес = сткРеквизиты.Адрес;
			НоваяСтрока.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаОтправления;
			НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
			НоваяСтрока.ИндексКартинки = Перечисления.упТипыТочекМаршрута.Индекс(НоваяСтрока.ТипТочки);
			НоваяСтрока.СтрокаДокумента = Истина;
			Если упКорректировкаРейса.Маршрут.Количество() > 1 Тогда
				СледующаяТочка = упКорректировкаРейса.Маршрут.Получить(1);
				СледующаяТочка.ПроездНеРассчитан = Истина;
				Если ЗначениеЗаполнено(СледующаяТочка.Предшественники) Тогда
					СледующаяТочка.Предшественники = СтрШаблон("%1,%2", СледующаяТочка.Предшественники,  Строка(НоваяСтрока.Идентификатор)); 
				Иначе
					СледующаяТочка.Предшественники = Строка(НоваяСтрока.Идентификатор);
				КонецЕсли; 
				НоваяСтрока.ПлановоеПрибытие = СледующаяТочка.ПлановоеПрибытие;
				
				Для Сч = 2 По упКорректировкаРейса.Маршрут.Количество() - 1 Цикл
					текТочка = упКорректировкаРейса.Маршрут[Сч];
					Если ЗначениеЗаполнено(текТочка.Предшественники) Тогда
						текТочка.Предшественники = СтрШаблон("%1,%2", текТочка.Предшественники,  Строка(НоваяСтрока.Идентификатор)); 
					Иначе
						текТочка.Предшественники = Строка(НоваяСтрока.Идентификатор);
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
			ПересчитатьВремя = Истина;
			
		ИначеЕсли НачалоМаршрута <> текНачалоМаршрута.Гараж Тогда
			текНачалоМаршрута.Гараж = НачалоМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НачалоМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			текНачалоМаршрута.Адрес = сткРеквизиты.Адрес;
			
			Если упКорректировкаРейса.Маршрут.Количество() > 1 Тогда
				СледующаяТочка = упКорректировкаРейса.Маршрут.Получить(1);
				СледующаяТочка.ПроездНеРассчитан = Истина;
			КонецЕсли;
			ПересчитатьВремя = Истина;
		КонецЕсли; 

	Иначе
		Если текНачалоМаршрута <> Неопределено Тогда
			УдалитьИдентификатор = Строка(текНачалоМаршрута.Идентификатор);
			упКорректировкаРейса.Маршрут.Удалить(текНачалоМаршрута);
			
			СледующаяТочка = упКорректировкаРейса.Маршрут.Получить(0);
			СледующаяТочка.РасстояниеОтПредыдущейТочки = 0;
			СледующаяТочка.ВремяОтПредыдущейТочки = 0;
			мсвПредшественников = СтрРазделить(СледующаяТочка.Предшественники, ",", Ложь);
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(мсвПредшественников, УдалитьИдентификатор);
			СледующаяТочка.Предшественники = СтрСоединить(мсвПредшественников, ",");
			Для Сч = 1 По упКорректировкаРейса.Маршрут.Количество() - 1 Цикл
				текТочка = упКорректировкаРейса.Маршрут[Сч];
				мсвПредшественников = СтрРазделить(текТочка.Предшественники, ",", Ложь);
				ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(мсвПредшественников, УдалитьИдентификатор);
				текТочка.Предшественники = СтрСоединить(мсвПредшественников, ",");
			КонецЦикла;
			ПересчитатьВремя = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	текОкончаниеМаршрута = Неопределено;
	ИскомыеСтроки = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("ТипТочки", Перечисления.упТипыТочекМаршрута.ТочкаЗавершения));
	Если ИскомыеСтроки.Количество() Тогда
		текОкончаниеМаршрута = ИскомыеСтроки[0];
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ОкончаниеМаршрута) Тогда
		Если текОкончаниеМаршрута = Неопределено Тогда
			НоваяСтрока = упКорректировкаРейса.Маршрут.Вставить(упКорректировкаРейса.Маршрут.Количество());
			НоваяСтрока.Гараж = ОкончаниеМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОкончаниеМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			НоваяСтрока.Адрес = сткРеквизиты.Адрес;
			НоваяСтрока.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаЗавершения;
			НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
			НоваяСтрока.ИндексКартинки = Перечисления.упТипыТочекМаршрута.Индекс(НоваяСтрока.ТипТочки);
			НоваяСтрока.СтрокаДокумента = Истина;
			мсвПредшественников = Новый Массив;
			Для Сч = 0 По упКорректировкаРейса.Маршрут.Количество() - 2 Цикл
				мсвПредшественников.Добавить(Строка(упКорректировкаРейса.Маршрут[Сч].Идентификатор));
			КонецЦикла;
			Если мсвПредшественников.Количество() Тогда
				НоваяСтрока.Предшественники = СтрСоединить(мсвПредшественников, ",");
			КонецЕсли; 
			ПересчитатьВремя = Истина;
			
		ИначеЕсли ОкончаниеМаршрута <> текОкончаниеМаршрута.Гараж Тогда	
			текОкончаниеМаршрута.Гараж = ОкончаниеМаршрута;
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОкончаниеМаршрута, Новый Структура("Адрес, Широта, Долгота", "Адрес", "Адрес.Широта", "Адрес.Долгота"));
			текОкончаниеМаршрута.Адрес = сткРеквизиты.Адрес;
			ПересчитатьВремя = Истина;
		КонецЕсли; 
	Иначе
		Если текОкончаниеМаршрута <> Неопределено Тогда
			упКорректировкаРейса.Маршрут.Удалить(текОкончаниеМаршрута);
			ПересчитатьВремя = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ПересчитатьВремя Тогда
		Сч = 1;
		Для каждого текСтрока Из упКорректировкаРейса.Маршрут Цикл
			текСтрока.СтрокаНомер = Сч;
			Сч = Сч + 1;
		КонецЦикла; 
		ПересчитатьВременнойГрафикМаршрута();
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьВременнойГрафикМаршрута()
	
	Если упКорректировкаРейса.Маршрут.Количество() Тогда
		Для Каждого СтрокаМаршрута ИЗ упКорректировкаРейса.Маршрут Цикл
			ОбработатьИзменениеАдресаМаршрута(СтрокаМаршрута.НомерСтроки);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры // ПересчитатьВременнойГрафикМаршрута()

&НаСервере
Процедура ОбновитьДанныеФормыПриОткрытии()
	
	Если ЗначениеЗаполнено(Объект.Рейс) Тогда
		текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		сткРеквизитыЗаполнения = Новый Структура();
		сткРеквизитыЗаполнения.Вставить("ИспользуютсяДополнительныеОперации", Ложь);
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		
		сткРеквизитыЗаполнения.Вставить("Организация", Объект.Организация);
		сткРеквизитыЗаполнения.Вставить("УчитыватьКоличествоЗагрузочныхМетров", ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров"));
		сткРеквизитыЗаполнения.Вставить("УчитыватьКоличествоМест", ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест"));
		сткРеквизитыЗаполнения.Вставить("УчитыватьМассу", ПолучитьФункциональнуюОпцию("упУчитыватьМассу"));
		сткРеквизитыЗаполнения.Вставить("УчитыватьОбъем", ПолучитьФункциональнуюОпцию("упУчитыватьОбъем"));
		
		сткРеквизитыРейса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Рейс, "Дата, ЧасовойПояс");
		
		текКорректировкаМаршрутаОбъект.Рейс = Объект.Рейс;
		текКорректировкаМаршрутаОбъект.Ссылка = Объект.Рейс;
		текКорректировкаМаршрутаОбъект.Период = сткРеквизитыРейса.Дата;
		текКорректировкаМаршрутаОбъект.ЧасовойПояс = сткРеквизитыРейса.ЧасовойПояс;
		текКорректировкаМаршрутаОбъект.Инициализация(сткРеквизитыЗаполнения);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упПеревозчикПоРейсуСрезПоследних.Водитель1,
		               |	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
		               |	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика,
		               |	упПеревозчикПоРейсуСрезПоследних.Соглашение
		               |ИЗ
		               |	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
				
		Запрос.УстановитьПараметр("Рейс", Объект.Рейс);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(текКорректировкаМаршрутаОбъект, Выборка);
			КонецЕсли;
		КонецЕсли;
		ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТочкуВМаршрутНаСервере(СтрокаНомер, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьДобавлениеТочкиВМаршрут(СтрокаНомер,,Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаКлиенте
// Процедура заполнения тч.доп.оборудование
//
Процедура ОбработатьОтветНаВопросЗаполнениеДопОборудования(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Объект.ДополнительноеОборудование.Очистить();
		ЗаполнитьДополнительноеОборудованиеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительноеОборудованиеНаСервере()
	ВыполнитьМетодОбъекта("ЗаполнитьДополнительноеОборудование()");
	ЗаполнитьКоличествоДопОборудования();
КонецПроцедуры

&НаКлиенте
// Процедура заполнения тч.доп.оборудование
//
Процедура ОбработатьОтветНаВопросЗаполнениеГСМДопОборудования(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Объект.ГСМДополнительноеОборудование.Очистить();
		ЗаполнитьГСМДополнительноеОборудованиеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГСМДополнительноеОборудованиеНаСервере()
	ВыполнитьМетодОбъекта("ЗаполнитьГСМДополнительноеОборудование()");
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора вида ГСМ.
//
Процедура ЗаправкиСливыГСМВидГСМДополнительноеОборудованиеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ТекущийЭлемент.ТекущиеДанные.ВидГСМ = Результат;
	КонецЕсли;	
КонецПроцедуры // ЗаправкиСливыГСМВидГСМНачалоВыбораЗавершение()

&НаКлиенте
Процедура ОбработатьОтветПерезаполнениеНормПриИзмененииВидаГСМ(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОбработатьИзменениеВидаГСМ();
	Иначе
		ТекущиеДанные = Элементы.ГСМ.ТекущиеДанные;
		Если НЕ ТекущиеДанные = Неопределено Тогда
			ТекущиеДанные.ВидГСМ = ВидГСМПередИзменением;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#Область ГСМ

&НаКлиенте
Процедура ОбработатьИзменениеВидаГСМ() Экспорт
	ТекущиеДанные = Элементы.ГСМ.ТекущиеДанные;	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТранспортноеСредство = ?(ЗначениеЗаполнено(ТекущиеДанные.ДопОборудование), ТекущиеДанные.ДопОборудование, Объект.ТранспортноеСредство);
		ТекущиеДанные.НачОстаток = ПолучитьУровеньТопливаНаСервере(ТранспортноеСредство, ТекущиеДанные.ВидГСМ);
		Если Истина
			И ЗначениеЗаполнено(ТекущиеДанные.ВидГСМ) 
			И ЗначениеЗаполнено(Объект.Организация) 
			И ЗначениеЗаполнено(Объект.ТранспортноеСредство) 
			Тогда
			сткПараметрыНормыРасхода = ПараметрыНормыРасходаГСМ(Объект.НачалоРейса, Объект.Организация, Объект.Подразделение, Объект.ТранспортноеСредство, ТекущиеДанные.ВидГСМ, ТекущиеДанные.НормаРасхода);
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, сткПараметрыНормыРасхода);
		КонецЕсли;
		ЗаполнитьНормыРасходаГСМНаСервере();
		ОтобратьНормыРасходаГСМПоТекущемуВидуГСМ();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьГСМКлиент(ОбъектОснование)
	Если Объект.ГСМ.Количество() > 0 Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветЗаполнитьГСМ", ЭтаФорма, ОбъектОснование);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'При заполнении табличные части будут очищены. Продолжить?'"),РежимДиалогаВопрос.ДаНет);	
	Иначе	
		ЗаполнитьГСМНаСервере(ОбъектОснование)
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГСМНаСервере(ОбъектОснование)
	
	Объект.ГСМ.Очистить();
	Если ТипЗнч(ОбъектОснование) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
		ИзменитьТранспортноеСредствоНаСервере(); 
		НастроитьДоступностьВводаМоточасовПоТС();
	ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.упРейс") Тогда
		ИзменитьРейсНаСервере();		
	Иначе
		
	КонецЕсли;
	ЗаполнитьНормыРасходаГСМНаСервере();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНадписиНормы(Строка)
	
	Строка.НадписьРасход 		  = СтрШаблон("(%1)", Строка.ТипНормыРасходаГСМ);
	
	Если Строка.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаСтоКилометров") Тогда
		Строка.НадписьИзменениеРасхода = Нстр("ru = '(на 100 тонн км)'");
	Иначе
		Строка.НадписьИзменениеРасхода = "";	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеПоляГСМ()
	
	Для каждого Строка Из Объект.ГСМ Цикл
		ЗаполнитьНадписиНормы(Строка);	
		Строка.ЭкономияПережог = Строка.РасходТопливаПоНорме - Строка.РасходТопливаФакт;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Процедура измененения рейса или транспортного средства.
//
Процедура ОбработатьОтветЗаполнитьГСМ(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьГСМНаСервере(ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыНормыРасходаГСМ(Период, Организация, Подразделение, ТранспортноеСредство, ВидГСМ, НормаРасхода)
	Возврат упПутевойЛистУчетГСМ.ПараметрыНормыРасходаГСМ(Период, Организация, Подразделение, ТранспортноеСредство, ВидГСМ, НормаРасхода);
КонецФункции

&НаКлиенте
Процедура ОбработатьОтветПерезаполнениеНормПослеУдаленияСтрокиГСМ(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		Объект.ГСМ.Удалить(ДополнительныеПараметры);
		ЗаполнитьНормыРасходаГСМНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидГСМПриИзмененииНаКлиенте() 
	ТекущаяСтрока = Элементы.ЗаправкиСливыГСМ.ТекущиеДанные;
	ВидГСМ = ТекущаяСтрока.ВидГСМ;
	ВидДвиженияТоплива = ТекущаяСтрока.ВидДвижения;
	ТекущаяСтрока.ПриемникСливаГСМ = Неопределено;
	Если Истина
		И ЗначениеЗаполнено(ВидГСМ) 
		И (Ложь
			Или НЕ ЗначениеЗаполнено(ВидДвиженияТоплива) 
			Или ВидДвиженияТоплива = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка")
		) 
	Тогда
		ТекущаяСтрока.СтавкаНДС = СтавкаНДСПоГСМ(ВидГСМ);
		
		Если Истина
			И ЗначениеЗаполнено(ТекущаяСтрока.СтавкаНДС) 
			И ТекущаяСтрока.Стоимость <> 0
		Тогда
			ПересчитатьСтроку(Элементы.ЗаправкиСливыГСМВидГСМ);	
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область НормыРасходаГСМ

&НаКлиенте
Процедура ЗаполнитьНормыРасходыГСМКлиент(РассчитатьНормативныйРасходТоплива = Ложь)
	Если Объект.НормыРасходаГСМ.Количество() > 0 Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветПерезаполнениеНормРасходаГСМ", ЭтаФорма, РассчитатьНормативныйРасходТоплива);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Табличная часть ""Нормы расхода ГСМ"" будет перезаполнена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);	
	Иначе	
		ЗаполнитьНормыРасходаГСМНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНормыРасходаГСМНаСервере()
	ВыполнитьМетодОбъекта("ЗаполнитьНормыРасходаГСМ()");
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНадписиНадбавки(Строка)
	
	Строка.НадписьКоличество = упСервисныеФункцииКлиентСервер.ЕдиницыИзмеренияКоличестваНадбавкиНормыГСМ(Строка.ТипРасхода );	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеПоляНормРасходаГСМ()
	
	Для каждого Строка Из Объект.НормыРасходаГСМ Цикл
		Строка.ИндексКартинки = Перечисления.упВидыЭксплуатационныхНормРасходаГСМ.Индекс(Строка.ВидЭксплуатационнойНормы);	
		ЗаполнитьНадписиНадбавки(Строка);
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтобратьНормыПоТекущемуВидуГСМ(Переключить = Ложь)
	Если Переключить Тогда
		ОтобратьПоТекущемуВидуГСМ = НЕ ОтобратьПоТекущемуВидуГСМ;	
		Элементы.НормыРасходаГСМВидГСМ.ПропускатьПриВводе = ОтобратьПоТекущемуВидуГСМ;
	КонецЕсли;
	Элементы.НормыРасходаГСМОтображатьПоТекущемуВидуГСМ.Пометка = ОтобратьПоТекущемуВидуГСМ;
	ОтобратьНормыРасходаГСМПоТекущемуВидуГСМ();
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьНормыРасходаГСМПоТекущемуВидуГСМ()
	Если ОтобратьПоТекущемуВидуГСМ Тогда
		ТекушиеДанные = Элементы.ГСМ.ТекущиеДанные;
		Если ТекушиеДанные <> Неопределено Тогда
			сткОтбор = Новый ФиксированнаяСтруктура( Новый Структура("ВидГСМ",ТекушиеДанные.ВидГСМ));
			Элементы.НормыРасходаГСМ.ОтборСтрок = сткОтбор;
		КонецЕсли; 
	Иначе
		Элементы.НормыРасходаГСМ.ОтборСтрок = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветПерезаполнениеНормРасходаГСМ(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ЗаполнитьНормыРасходаГСМНаСервере();
		Если ДополнительныеПараметры Тогда
			РассчитатьНормативныйРасходТопливаНаСервере();
		КонецЕсли;
	Иначе
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ДополнительноеОборудование

&НаКлиенте
Процедура ОбработатьИзменениеВидаГСМДополнительноеОборудование() Экспорт
	ТекущиеДанные = Элементы.ГСМДополнительноеОборудование.ТекущиеДанные;	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ДополнительноеОборудование = ТекущиеДанные.ДополнительноеОборудование;
		ТекущиеДанные.НачОстаток = ПолучитьУровеньТопливаНаСервере(ДополнительноеОборудование, ТекущиеДанные.ВидГСМ);
		Если Истина
			И ЗначениеЗаполнено(ТекущиеДанные.ВидГСМ) 
			И ЗначениеЗаполнено(Объект.Организация) 
			И ЗначениеЗаполнено(ДополнительноеОборудование) 
			Тогда
			сткПараметрыНормыРасхода = ПараметрыНормыРасходаГСМ(Объект.НачалоРейса, Объект.Организация, Объект.Подразделение, ДополнительноеОборудование, ТекущиеДанные.ВидГСМ, ТекущиеДанные.НормаРасхода);
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, сткПараметрыНормыРасхода);
		КонецЕсли;
		ЗаполнитьНормыРасходаГСМДопОборудованияНаСервере();
		ИзменитьОтобратьСтрокиТСПоТекущемуДопОборудованию(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветПерезаполнениеНормПриИзмененииВидаГСМДополнительноеОборудование(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОбработатьИзменениеВидаГСМДополнительноеОборудование();
	Иначе
		ТекущиеДанные = Элементы.ГСМДополнительноеОборудование.ТекущиеДанные;
		Если НЕ ТекущиеДанные = Неопределено Тогда
			ТекущиеДанные.ВидГСМ = ВидГСМПередИзменением;
			ТекущиеДанные.ДополнительноеОборудование = ДопОборудованиеПередИзменением;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРежимыРаботыДопОборудованияКлиент()
	Если Объект.НормыРасходаРежимовРаботы.Количество() > 0 Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветЗаполнитьРежимыРаботыДопОборудования", ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Табличная часть ""Режимы работы"" будет перезаполнена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);	
	Иначе	
		ЗаполнитьРежимыРаботыДопОборудованияНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРежимыРаботыДопОборудованияНаСервере()
	ВыполнитьМетодОбъекта("ЗаполнитьРежимыРаботыДополнительноеОборудование()");	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветЗаполнитьРежимыРаботыДопОборудования(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ЗаполнитьРежимыРаботыДопОборудованияНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеПоляГСМДопоборудование()
	
	Для каждого Строка Из Объект.ГСМДополнительноеОборудование Цикл
		ЗаполнитьНадписиНормыДопОборудования(Строка);	
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНадписиНормыДопОборудования(Строка)
	
	Строка.НадписьРасход 		  = СтрШаблон("(%1)", Строка.ТипНормыРасходаГСМ);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеПоляНормРасходаГСМДополнительноеОборудование()
	
	Для каждого Строка Из Объект.НормыРасходаГСМДополнительноеОборудование Цикл
		Строка.ИндексКартинки = Перечисления.упВидыЭксплуатационныхНормРасходаГСМ.Индекс(Строка.ВидЭксплуатационнойНормы);	
		ЗаполнитьНадписиНадбавки(Строка);
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНормыРасходаГСМДопОборудованияКлиент()
	Если Объект.НормыРасходаГСМДополнительноеОборудование.Количество() > 0 Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветЗаполнитьНормыРасходаГСМДопОборудования", ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Табличная часть ""Нормы расхода ГСМ"" будет перезаполнена. Продолжить?'"),РежимДиалогаВопрос.ДаНет);	
	Иначе	
		ЗаполнитьНормыРасходаГСМДопОборудованияНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНормыРасходаГСМДопОборудованияНаСервере()
	ВыполнитьМетодОбъекта("ЗаполнитьНормыРасходаГСМДополнительноеоборудование()");	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветЗаполнитьНормыРасходаГСМДопОборудования(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ЗаполнитьНормыРасходаГСМДопОборудованияНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьСтрокиТЧПоТекущемуДопОборудованию()
	
	Если ОтобратьПоТекущемуДопОборудованию Тогда
		ТекушиеДанные = Элементы.ДополнительноеОборудование.ТекущиеДанные;
		Если ТекушиеДанные <> Неопределено Тогда
			сткОтбор = Новый ФиксированнаяСтруктура( Новый Структура("ДополнительноеОборудование",ТекушиеДанные.ДополнительноеОборудование));
			Элементы.НормыРасходаРежимовРаботы.ОтборСтрок 				= сткОтбор;
			Элементы.ГСМДополнительноеОборудование.ОтборСтрок 			= сткОтбор;
			Элементы.НормыРасходаГСМДополнительноеОборудование.ОтборСтрок 	= сткОтбор;
			Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ОтборСтрок 	= сткОтбор;
		КонецЕсли; 
	Иначе
		Элементы.НормыРасходаРежимовРаботы.ОтборСтрок 				= Неопределено;
		Элементы.ГСМДополнительноеОборудование.ОтборСтрок 			= Неопределено;
		Элементы.НормыРасходаГСМДополнительноеОборудование.ОтборСтрок 	= Неопределено;
		Элементы.ЗаправкиСливыГСМДополнительноеОборудование.ОтборСтрок 	= Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтобратьСтрокиТСПоТекущемуДопОборудованию(Переключить = Ложь)
	Если Переключить Тогда
		ОтобратьПоТекущемуДопОборудованию = НЕ ОтобратьПоТекущемуДопОборудованию;	
	КонецЕсли;
	Элементы.ДополнительноеОборудованиеОтобратьПоТекущемуДопОборудованию.Пометка 			= ОтобратьПоТекущемуДопОборудованию;
	Элементы.НормыРасходаРежимовРаботыДополнительноеОборудование.Видимость 				= НЕ ОтобратьПоТекущемуДопОборудованию;
	Элементы.ГСМДополнительноеОборудованиеДополнительноеОборудование.Видимость 			= НЕ ОтобратьПоТекущемуДопОборудованию;
	Элементы.НормыРасходаГСМДополнительноеОборудованиеДополнительноеОборудование.Видимость 	= НЕ ОтобратьПоТекущемуДопОборудованию;
	Элементы.ЗаправкиСливыГСМДополнительноеОборудованиеДополнительноеОборудование.Видимость 	= НЕ ОтобратьПоТекущемуДопОборудованию;
	ОтобратьСтрокиТЧПоТекущемуДопОборудованию();
КонецПроцедуры

#КонецОбласти

#Область СервисныеФункции

&НаКлиенте
// Продолжение добавления точки после утвердительного ответа.
//
Процедура ОбработатьВопросОДобавлениеТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	ДобавитьТочку = РезультатВопроса = КодВозвратаДиалога.Да;
	ДобавитьТочкуПриИзмененииТранспортногоСредстваНаСервере(ДобавитьТочку, ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
// Продолжение удаления точки после утвердительного ответа.
//
Процедура ОбработатьВопросОбУдаленииТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	УдалитьТочку = РезультатВопроса = КодВозвратаДиалога.Да;
	УдалитьТочкуПриИзмененииТранспортногоСредстваНаСервере(УдалитьТочку, ДополнительныеПараметры);
КонецПроцедуры

&НаСервере
Процедура ДобавитьТочкуПриИзмененииТранспортногоСредстваНаСервере(ДобавитьТочку, ДополнительныеПараметры)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ДобавитьТочкуПриИзмененииТранспортногоСредства(ДобавитьТочку, ДополнительныеПараметры);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура УдалитьТочкуПриИзмененииТранспортногоСредстваНаСервере(УдалитьТочку, ДополнительныеПараметры)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.УдалитьТочкуПриИзмененииТранспортногоСредства(УдалитьТочку, ДополнительныеПараметры);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеПоляТабличныхЧастей()
	ЗаполнитьДополнительныеПоляГСМ();
	ЗаполнитьДополнительныеПоляНормРасходаГСМ();	
	ЗаполнитьДополнительныеПоляГСМДопоборудование();
	ЗаполнитьДополнительныеПоляНормРасходаГСМДополнительноеОборудование();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьМетодОбъекта(ИмяМетода, Параметр1 = Неопределено)
		
	ДокументПутевойЛист = РеквизитФормыВЗначение("Объект");
	Выполнить(СтрШаблон("ДокументПутевойЛист.%1", ИмяМетода));
	ЗначениеВРеквизитФормы(ДокументПутевойЛист, "Объект");	
	ЗаполнитьДополнительныеПоляТабличныхЧастей();
	Модифицированность = Истина;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМассуПрицепногоСостава()
	Объект.МассаПрицепногоСостава = МассаПрицепногоСоставаТранспортногоСредстваНаСервере();
КонецПроцедуры

&НаСервере
Функция МассаПрицепногоСоставаТранспортногоСредстваНаСервере(Период = Неопределено)
	Если Период = Неопределено Тогда
		Период = НачалоРейсов();
	КонецЕсли;
	Возврат упПутевойЛистУчетГСМ.МассаПрицепногоСоставаТранспортногоСредства(Период, Объект.ТранспортноеСредство);
КонецФункции

&НаСервере
Функция НачалоРейсов()
	Возврат ?(ЗначениеЗаполнено(Объект.НачалоРейса), Объект.НачалоРейса, Документы.упРейс.ПолучитьВремяНачалаПоРейсам(Объект.Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс")));
КонецФункции

&НаСервере
Процедура ОтобразитьПрицепы()
	Прицепы.Очистить();
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, НачалоРейсов(), Прицепы);
	КоличествоПрицепов = Прицепы.Количество();
КонецПроцедуры

&НаСервере
Процедура ВычислитьПройденноеРасстояние()
	
	ПробегПоДаннымТрекеров = 0;
	
	ИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг");
	Если ИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг Тогда
		
		НачалоПериода = Дата("00010101000000");
		ОкончаниеПериода = Дата("00010101000000");
		ТранспортноеСредство = Объект.ТранспортноеСредство;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упФактическиеПараметрыРейса.НачалоВыполнения КАК НачалоВыполнения,
		|	упФактическиеПараметрыРейса.ОкончаниеВыполнения КАК ОкончаниеВыполнения
		|ИЗ
		|	РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
		|ГДЕ
		|	упФактическиеПараметрыРейса.Рейс = &Рейс";
		
		Запрос.УстановитьПараметр("Рейс", Объект.Рейс);
		
		Выборка = Запрос.Выполнить().Выбрать();					
		Если Выборка.Следующий() Тогда
			НачалоПериода = Выборка.НачалоВыполнения;
			ОкончаниеПериода = Выборка.ОкончаниеВыполнения;				
		КонецЕсли;		
		Если ЗначениеЗаполнено(НачалоПериода) и ЗначениеЗаполнено(ОкончаниеПериода) И ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			ТекущееРасстояние = упМониторинг.упВычислитьРасстояниеПоДаннымТрекеров(НачалоПериода,ОкончаниеПериода,ТранспортноеСредство);
			// Значение расстояния получаем в метрах делим на 1000 для получения км.
			ПробегПоДаннымТрекеров = ?(ТекущееРасстояние=0,0,ТекущееРасстояние/1000);
		КонецЕсли;
	КонецЕсли;	
	
	ПробегПоДаннымТрекеровПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(ПробегПоДаннымТрекеров);
	
КонецПроцедуры // ВычислитьПройденноеРасстояние()

&НаСервере
Процедура ЗаполнитьКоличествоДопОборудования()
	КоличествоОборудованияНаПрицепномСоставе = 0;
	КоличествоОборудованияНаТС = 0;
	Для каждого Строка Из Объект.ДополнительноеОборудование Цикл
		ТранспортноеСредствоСтрока = Строка.ТранспортноеСредство;
		Если ЗначениеЗаполнено(ТранспортноеСредствоСтрока) Тогда
			
			Если ТранспортноеСредствоСтрока = Объект.ТранспортноеСредство Тогда
				КоличествоОборудованияНаТС = КоличествоОборудованияНаТС + 1;
			Иначе
				КоличествоОборудованияНаПрицепномСоставе = КоличествоОборудованияНаПрицепномСоставе + 1;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидТранспортнойУслугиПоРейсуНаСервере(Рейс)
	Если ЗначениеЗаполнено(Рейс) Тогда
		Объект.ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидПеревозки.ВидТранспортнойУслуги");		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ДанныеПоМаршрутуВводятсяВПутевомЛисте Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
			текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
			текКорректировкаМаршрутаОбъект.УдалитьТочкиОтправленияЗавершения();
			ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
		Иначе
			упКорректировкаРейса.Водитель1 = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТранспортноеСредство, "ОсновнойВодитель");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СдвинутьТочкуВМаршрутеНаСервере(СтрокаНомер, СдвигВверх = Истина, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	ИдентификаторСтроки = текКорректировкаМаршрутаОбъект.ОбработатьСдвигТочкиМаршрута(СтрокаНомер, СдвигВверх, Ложь, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	Возврат ИдентификаторСтроки;
КонецФункции

&НаКлиенте
Процедура СообщитьОНарушенииМаршрута(Предшественник, Потомок)
	
	ТекстСообщения = СтрШаблон("ru = 'Ошибка изменения маршрута! Точка с адресом ""%1"" должна предшествовать точке с адресом ""%2""'", Предшественник, Потомок);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстСообщения));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМаршрутПоЗаявкеНаКлиенте(ЗаявкаНаПеревозкуГруза)
	мсвАдресаПоЗаявке   = АдресаПоЗаявкеНаПеревозкуГруза(ЗаявкаНаПеревозкуГруза, Объект.ВидТранспортнойУслуги);
	ЗаполнитьМаршрутАдресами(мсвАдресаПоЗаявке);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМаршрутАдресами(мсвАдреса)
	
	Если мсвАдреса.Количество() > 0 Тогда
		НомерДобавленнойПервойСтроки = Неопределено;
		Для каждого Адрес Из мсвАдреса Цикл
			мсвСтроки = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("Адрес", Адрес));		
			Если мсвСтроки.Количество() = 0 Тогда
				ИдентификаторСтроки = ДобавитьТочкуВМаршрут(ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию"), Адрес);	
				СтрокаМаршрут = упКорректировкаРейса.Маршрут.НайтиПоИдентификатору(ИдентификаторСтроки);
				Если Истина
					И НомерДобавленнойПервойСтроки = Неопределено
					И СтрокаМаршрут <> Неопределено 
				Тогда
					НомерДобавленнойПервойСтроки = СтрокаМаршрут.НомерСтроки;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаМаршрут <> Неопределено Тогда
			Отказ = Ложь;
			ОбработатьСтрокуТочкаМаршрутаНаСервере(НомерДобавленнойПервойСтроки,,,Ложь, Отказ);	
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресаПоЗаявкеНаПеревозкуГруза(ЗаявкаНаПеревозкуГруза, ВидТранспортнойУслуги)
	Результат = Новый Массив;
	Если ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза) Тогда
		Если ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаПассажиров Тогда
			мсвЗаявкиНаПеревозкуГруза = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗаявкаНаПеревозкуГруза);
			Результат = упУправленияЗаявками.МаршрутСледованияПоЗаявкамНаПеревозкуГруза(мсвЗаявкиНаПеревозкуГруза);
		Иначе
			АдресОтправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаНаПеревозкуГруза, "АдресОтправления");
			Если ЗначениеЗаполнено(АдресОтправления) Тогда
				Результат.Добавить(АдресОтправления);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция АдресаПоМаршруту(ТиповойМаршрут)
	Возврат Справочники.упТиповыеМаршруты.Маршрут(ТиповойМаршрут);
КонецФункции

&НаКлиенте
Процедура ВыбратьЗаявкуДляЗаполненияМаршрута()
	
		// Если в списке одна заявка.
	Если Объект.Заявки.Количество() = 1 Тогда
		
		// Заполнить маршрут по заявке.
		ЗаполнитьМаршрутПоЗаявкеНаКлиенте(Объект.Заявки[0].ЗаявкаНаПеревозкуГруза);
		
	// Если заявок несколько.
	Иначе	
		
		// Выбрать заявку из списка.
		
		СписокЗаявок = Новый Массив;
		Для каждого Строка Из Объект.Заявки Цикл
			Если СписокЗаявок.Найти(Строка.ЗаявкаНаПеревозкуГруза) = Неопределено Тогда
				СписокЗаявок.Добавить(Строка.ЗаявкаНаПеревозкуГруза);
			КонецЕсли;
			
		КонецЦикла;
		
		сткОтбор = Новый Структура();
		сткОтбор.Вставить("ВидТранспортнойУслуги", Объект.ВидТранспортнойУслуги);
		сткОтбор.Вставить("Ссылка",                СписокЗаявок);
		сткПараметры = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("Документ.упЗаявкаНаПеревозкуГруза.ФормаВыбора", сткПараметры, ЭтотОбъект, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПартнерТопливнаяКарта(ТопливнаяКарта)
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТопливнаяКарта, Новый Структура("Партнер", "Контрагент.Партнер"));
	Возврат сткРеквизиты.Партнер;		
КонецФункции

&НаКлиенте
Процедура ОбработатьОтветНаВопросОчиститьМаршрут(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		упКорректировкаРейса.Маршрут.Очистить();
	КонецЕсли;
	ВыбратьЗаявкуДляЗаполненияМаршрута();
КонецПроцедуры

#КонецОбласти

#КонецОбласти