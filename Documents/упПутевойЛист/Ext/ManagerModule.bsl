#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа.
//
// Параметры:
//   Ссылка - ДокументСсылка - Ссылка на документ.
//
// Возвращаемое значение:
//     ПеречислениеСсылка - упСтатусыПутевогоЛиста - Ссылка на значение.
//
Функция ПолучитьСтатус(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыПутевогоЛиста.Новый;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыПутевыхЛистовСрезПоследних.Статус,
	|	упПутевойЛист.ПометкаУдаления
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних(, Документ = &Документ) КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка)
	|ГДЕ
	|	упПутевойЛист.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыПутевогоЛиста.Отменен;
	Иначе	
		Возврат Перечисления.упСтатусыПутевогоЛиста.Новый;
	КонецЕсли;
	
КонецФункции		

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Рейс");
	БлокируемыеРеквизиты.Добавить("Гараж");
	БлокируемыеРеквизиты.Добавить("ТранспортноеСредство");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Процедура заполнения значения уровня топлива по данным датчиков.
//
// Параметры:
//  Объект	 - ДокументОбъект.упПутевойЛист	 - путевой лист.
//  ТекстОшибки - Строка	 - описание ошибки.
//  тбзГСМ	 - ТаблицаЗначений	 - таблица ГСМ.
//
Процедура ЗаполнитьДаннымиДатчиковУровняТоплива(Объект, ТекстОшибки = "", тбзГСМ = Неопределено) Экспорт
	
	
	Если тбзГСМ = Неопределено Тогда
		тбзГСМ = Объект.ДополнительныеСвойства.тбзГСМ;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзГСМ.ТранспортноеСредство,
	               |	тбзГСМ.ВидГСМ
	               |ПОМЕСТИТЬ втГСМ
	               |ИЗ
	               |	&тбзГСМ КАК тбзГСМ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втГСМ.ТранспортноеСредство,
	               |	втГСМ.ВидГСМ,
	               |	упДатчики.Ссылка КАК Датчик
	               |ПОМЕСТИТЬ втДатчики
	               |ИЗ
	               |	Справочник.упДатчики КАК упДатчики
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГСМ КАК втГСМ
	               |		ПО (упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо))
	               |			И упДатчики.ВидГСМ = втГСМ.ВидГСМ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упДанныеДатчиковСрезПоследних.Значение,
	               |	упДанныеДатчиковСрезПоследних.ТранспортноеСредство,
	               |	упДанныеДатчиковСрезПоследних.Датчик.ВидГСМ КАК ВидГСМ
	               |ИЗ
	               |	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	               |			&Период,
	               |			(ТранспортноеСредство, Датчик) В
	               |				(ВЫБРАТЬ
	               |					втДатчики.ТранспортноеСредство,
	               |					втДатчики.Датчик
	               |				ИЗ
	               |					втДатчики КАК втДатчики)) КАК упДанныеДатчиковСрезПоследних";
	Запрос.УстановитьПараметр("тбзГСМ", тбзГСМ);
	Запрос.УстановитьПараметр("Период", Объект.ОкончаниеРейса);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ТекстОшибки = "Отсутствуют данные датчиков уровня топлива";
	КонецЕсли; 
	
	текВыборка	= Результат.Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		
		ДопОборудование = ?(ТипЗнч(текВыборка.ТранспортноеСредство) = Тип("СправочникСсылка.упДополнительноеОборудование"), текВыборка.ТранспортноеСредство, Справочники.упДополнительноеОборудование.ПустаяСсылка());
		
		мсвСтроки = Объект.ГСМ.НайтиСтроки(Новый Структура("ДопОборудование, ВидГСМ", ДопОборудование, текВыборка.ВидГСМ));
	
		Если мсвСтроки.Количество() > 0 Тогда
			текСтрокаГСМ = мсвСтроки[0];
			текСтрокаГСМ.КонОстаток	= текВыборка.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполнения значения пробега по данным датчиков.
//
// Параметры:
//  Объект	 - ДокументОбъект.упПутевойЛист 	 - путевой лист.
//  ТекстОшибки - Строка	 - описание ошибки.
//
Процедура ЗаполнитьДаннымиДатчиковПробега(Объект, ТекстОшибки = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упДанныеДатчиковСрезПоследних.Значение
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|			,
	|			ТранспортноеСредство = &ТранспортноеСредство
	|				И Датчик.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Одометр)) КАК упДанныеДатчиковСрезПоследних";
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ТекстОшибки = "Отсутствуют данные датчиков пробега";
	КонецЕсли;
	
	текВыборка = Результат.Выбрать();
	Если текВыборка.Следующий() Тогда
		Объект.ПробегОкончание = текВыборка.Значение;
		Объект.Пробег 			= Объект.ПробегОкончание - Объект.ПробегНачало;		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	мсвТипов = Новый Массив;
	мсвТипов.Добавить(Тип("ДокументСсылка.упПутевойЛист"));
	мсвТипов.Добавить(Тип("ДокументСсылка.упРейс"));
	
	// Путевой лист легкового автомобиля
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "Форма3";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист легкового автомобиля (Форма 3)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 1;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	
	// Путевой лист грузового автомобиля 4М
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "Форма4М";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист грузового автомобиля (Форма 4М)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 2;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	// Путевой лист грузового автомобиля 4П
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "Форма4П";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист грузового автомобиля (Форма 4П)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 3;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	// Путевой лист грузового автомобиля 4С
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "Форма4С";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист грузового автомобиля (Форма 4С)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 4;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	// Путевой лист легкового автомобиля индивидуального предпринимателя
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "ФормаПЛ1";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист легкового автомобиля индивидуального предпринимателя (Форма ПЛ1)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 5;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	// Путевой лист грузового автомобиля индивидуального предпринимателя
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "ФормаПГ1";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист грузового автомобиля индивидуального предпринимателя (Форма ПГ1)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 6;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	// Настраиваемый комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Порядок = 75;
	КомандаПечати.Идентификатор = "Форма3,Форма4М,Форма4П,Форма4С,ФормаПЛ1,ФормаПГ1";
	КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
	// Путевой лист специального автомобиля Спец3
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "ФормаСпец3";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист специального автомобиля (форма № 3 спец)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 7;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;

	// Путевой лист автобуса необщего пользования Спец6
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упПутевойЛист";
	КомандаПечати.Идентификатор = "ФормаСпец6";
	КомандаПечати.Представление = НСтр("ru = 'Путевой лист автобуса необщего пользования (форма № 6 спец)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок			= 8;
	КомандаПечати.ТипыОбъектовПечати = мсвТипов;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект.
//     представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	МассивРейсов = Новый Массив;
	
	Для каждого Элемент Из МассивОбъектов Цикл
		Если ТипЗнч(Элемент) = Тип("ДокументСсылка.упРейс") Тогда
			МассивРейсов.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
	МассивОбъектовДляПечати = Новый Массив;
	
	Если МассивРейсов.Количество() > 0 Тогда
		МассивОбъектовДляПечати = упПутевойЛистУчетГСМ.ПутевыеЛистыПоРейсам(МассивРейсов);
	Иначе
		МассивОбъектовДляПечати = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивОбъектов);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Форма3") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Форма3",
		НСтр("ru = 'Путевой лист легкового автомобиля (Форма 3)'"),
		ПечатьФорма3(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упПутевойЛист.ПФ_MXL_Форма3");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Форма4М") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Форма4М",
		НСтр("ru = 'Путевой лист грузового автомобиля (Форма 4М)'"),
		ПечатьФорма4М(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Форма4М");
	КонецЕсли;	

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Форма4П") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Форма4П",
		НСтр("ru = 'Путевой лист грузового автомобиля (Форма 4П)'"),
		ПечатьФорма4П(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Форма4П");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Форма4С") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Форма4С",
		НСтр("ru = 'Путевой лист грузового автомобиля (Форма 4С)'"),
		ПечатьФорма4С(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Форма4С");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаПЛ1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ФормаПЛ1",
		НСтр("ru = 'Путевой лист легкового автомобиля индивидуального предпринимателя (Форма ПЛ1)'"),
		ПечатьФормаПЛ1(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ФормаПЛ1");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаПГ1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ФормаПГ1",
		НСтр("ru = 'Путевой лист грузового автомобиля индивидуального предпринимателя (Форма ПГ1)'"),
		ПечатьФормаПГ1(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ФормаПГ1");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСпец3") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ФормаСпец3",
		НСтр("ru = 'Путевой лист специального автомобиля (форма № 3 спец)'"),
		ПечатьФормаСпец3(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упПутевойЛист.ПФ_MXL_ФормаСпец3");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаСпец6") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ФормаСпец6",
		НСтр("ru = 'Путевой лист автобуса необщего пользования (форма № 6 спец)'"),
		ПечатьФормаСпец6(МассивОбъектовДляПечати, ОбъектыПечати),
		,
		"Документ.упПутевойЛист.ПФ_MXL_ФормаСпец6");
	КонецЕсли;	
	
КонецПроцедуры

// Формирование печатной формы Форма3
Функция ПечатьФорма3(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_Форма3";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФорма3(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 5;
	ТабличныйДокумент.ПолеСлева				= 5;
	ТабличныйДокумент.ПолеСнизу				= 5;
	ТабличныйДокумент.ПолеСправа			= 5;

  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументФорма3(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
	тбДанныеКатегории      = ДанныеДляПечати.Категории;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_Форма3");
	
	ОбластьЛицевая		= Макет.ПолучитьОбласть("Лицевая");
	ОбластьОборотная	= Макет.ПолучитьОбласть("Оборотная");
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьЛицевая.Параметры.Заполнить(текСтрокаШапка);
		РейсВодитель1 = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Строка(текСтрокаШапка.РейсВодитель1));
		ОбластьЛицевая.Параметры.РейсВодитель1 = Строка(текСтрокаШапка.РейсВодитель1);

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ОбластьЛицевая.Параметры.НаименованиеОрганизации = Строка(текСтрокаШапка.РейсОрганизация);
		ОбластьЛицевая.Параметры.СсылкаНаОрганизацию = текСтрокаШапка.РейсОрганизация;
		ОбластьЛицевая.Параметры.ДатаПлановогоНачала = текСтрокаШапка.ПлановоеНачалоВыполнения;
		ОбластьЛицевая.Параметры.ВыдалДиспетчер = текСтрокаШапка.Автор;
		
		// Штрихкод
		Рисунок = ОбластьЛицевая.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		// Водительское удостоверение
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицевая.Параметры.РейсВодитель1УдостоверениеНомер = СтрШаблон("%1 %2", СтрокаДанные.РегистраторСерия, СтрокаДанные.РегистраторНомер);
		КонецЕсли;
		
		// ЛицензионнаяКарточка
		ОбластьЛицевая.Параметры.РейсВодитель1ЛицензионнаяКарточка = "стандартная, ограниченная";
		
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ЛицензионнаяКарточка));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицевая.Параметры.РейсВодитель1СерияЛицензионнойКарточки = СтрокаДанные.РегистраторСерия;
			ОбластьЛицевая.Параметры.РейсВодитель1НомерЛицензионнойКарточки = СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		// Водитель1
		ОбластьЛицевая.Параметры.РейсВодитель1Принял	= РейсВодитель1;
		ОбластьЛицевая.Параметры.РейсВодитель1Сдал		= РейсВодитель1;
		
		// Категория водителя.
		мсвСтрок = тбДанныеКатегории.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество() > 0 Тогда
			КлассВодителя = "";
			Для каждого строкаНомер Из мсвСтрок Цикл
				КлассВодителя = КлассВодителя + строкаНомер.Категория + ", ";
			КонецЦикла;
			КлассВодителя = Лев(КлассВодителя, СтрДлина(КлассВодителя)-2);
			ОбластьЛицевая.Параметры.КлассВодителя = КлассВодителя;
		КонецЕсли;
		
		// ГСМ.
		// мсвСтрок = тбДанныеГСМ.НайтиСтроки(Новый Структура("Ссылка, ВидГСМ, ДопОборудование", текСтрокаШапка.Ссылка, текСтрокаШапка.ВидГСМОсновной, Справочники.упДополнительноеОборудование.ПустаяСсылка()));
		мсвСтрок = тбДанныеГСМ.НайтиСтроки(Новый Структура("Ссылка, ДопОборудование", текСтрокаШапка.Ссылка, Справочники.упДополнительноеОборудование.ПустаяСсылка()));
		НачОстаток = "";
		РасходТопливаПоНорме = "";
		МассивВидовГСМ = Новый Массив;
		Для каждого СтрокаДанные Из мсвСтрок Цикл
			МассивВидовГСМ.Добавить(СтрокаДанные.ВидГСМ);
			Если СтрокаДанные.ВидГСМ = текСтрокаШапка.ВидГСМОсновной Тогда
				НачОстаток = СтрокаДанные.РасходТопливаПоНорме;
				РасходТопливаПоНорме = СтрокаДанные.РасходТопливаПоНорме;
			КонецЕсли;
		КонецЦикла;
		
		ОбластьЛицевая.Параметры.ВидГСМОсновной = СтрСоединить(МассивВидовГСМ, ", ");
		ОбластьЛицевая.Параметры.ОстатокПриВыезде1 = НачОстаток;
		ОбластьЛицевая.Параметры.РасходТопливаПоНорме = РасходТопливаПоНорме;

		// Механик
		Механик = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Строка(текСтрокаШапка.Механик));
		
		ОбластьЛицевая.Параметры.ВыпустилМеханик 	= Механик;
		ОбластьЛицевая.Параметры.ПринялМеханик 		= Механик;
		
		ТабличныйДокумент.Вывести(ОбластьЛицевая);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ОбластьОборотная);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры

// Формирование печатной формы Форма4М
Функция ПечатьФорма4М(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_Форма4М";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФорма4М(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументФорма4М(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	= ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_Форма4М");
	
	ОбластьЛицевая		= Макет.ПолучитьОбласть("Лицевая");
	ОбластьОборотная	= Макет.ПолучитьОбласть("Оборотная");
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьЛицевая.Параметры.Заполнить(текСтрокаШапка);

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Штрихкод
		Рисунок = ОбластьЛицевая.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		РейсВодитель1Представление = "" + текСтрокаШапка.РейсВодитель1;
		РейсВодитель2Представление = "" + текСтрокаШапка.РейсВодитель2;
		
		// Водительское удостоверение Водитель1
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			РейсВодитель1Представление  = РейсВодитель1Представление + ?(ПустаяСтрока(СтрокаДанные.РегистраторНомер),"",", " + СтрокаДанные.РегистраторНомер);
		КонецЕсли;
		
		ОбластьЛицевая.Параметры.РейсВодитель1Представление  = РейсВодитель1Представление;
		
		// Водительское удостоверение Водитель2
		мсвСтрок = тбДанныеВодитель2.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			РейсВодитель2Представление  = РейсВодитель2Представление + ?(ПустаяСтрока(СтрокаДанные.РегистраторНомер),"",", " + СтрокаДанные.РегистраторНомер);
		КонецЕсли;
		
		ОбластьЛицевая.Параметры.РейсВодитель2Представление  = РейсВодитель2Представление;
		
		// Водитель1
		ОбластьЛицевая.Параметры.РейсВодитель1Принял	= текСтрокаШапка.РейсВодитель1;
		ОбластьЛицевая.Параметры.РейсВодитель1Сдал		= текСтрокаШапка.РейсВодитель1;
		
		// ГСМ
		мсвСтрок = тбДанныеГСМ.НайтиСтроки(Новый Структура("ССылка, ВидГСМ, ДопОборудование", текСтрокаШапка.Ссылка, текСтрокаШапка.ВидГСМОсновной, Справочники.упДополнительноеОборудование.ПустаяСсылка()));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицевая.Параметры.ОстатокПриВыезде1 = СтрокаДанные.НачОстаток;
		КонецЕсли;

		// Механик
		Механик = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Строка(текСтрокаШапка.Механик));
			
		ОбластьЛицевая.Параметры.ВыпустилМеханик 	= Механик;
		ОбластьЛицевая.Параметры.ПринялМеханик 		= Механик;
		
		ТабличныйДокумент.Вывести(ОбластьЛицевая);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ОбластьОборотная);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры

// Формирование печатной формы Форма4П
Функция ПечатьФорма4П(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_Форма4П";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФорма4П(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументФорма4П(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		   = ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	   = ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	   = ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			   = ДанныеДляПечати.ГСМ;
	тбДанныеПрицеп         = ДанныеДляПечати.Прицеп;
	тбДанныеКатегории      = ДанныеДляПечати.Категории;
	тбДанныеВремяПоГрафику = ДанныеДляПечати.ВремяПоГрафику;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_Форма4П");
	
	ОбластьЛицевая		= Макет.ПолучитьОбласть("Лицевая");
	ОбластьОборотная	= Макет.ПолучитьОбласть("Оборотная");
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьЛицевая.Параметры.Заполнить(текСтрокаШапка);
		
		ОбластьЛицевая.Параметры.ДатаВыписки = Формат(текСтрокаШапка.ДатаВыписки, "ДЛФ=D") + " г.";
		Если ЗначениеЗаполнено(текСтрокаШапка.ДатаДействияПутевогоЛиста) Тогда
			ДДПЛ_Д = Формат(текСтрокаШапка.ДатаДействияПутевогоЛиста, "ДФ=dd");
			ДДПЛ_д1 = Формат(текСтрокаШапка.ДатаДействияПутевогоЛиста, "ДФ='MMMM yyyy'");
			Если ЗначениеЗаполнено(текСтрокаШапка.ПлановоеНачалоВыполнения) Тогда
				ПНВ_Д = Формат(текСтрокаШапка.ПлановоеНачалоВыполнения, "ДФ=dd");
				ПНВ_д1 = Формат(текСтрокаШапка.ПлановоеНачалоВыполнения, "ДФ='MMMM yyyy'");
				ТестПрдеставления = СтрШаблон("с ""%1"" %2 г. по ""%3"" %4 г.",ПНВ_Д, ПНВ_д1, ДДПЛ_Д, ДДПЛ_д1);
			Иначе
				ТестПрдеставления = СтрШаблон("Действителен по ""%1"" %2 г.", ДДПЛ_Д, ДДПЛ_д1);
			КонецЕсли;
			ОбластьЛицевая.Параметры.ДатаВыписки = ТестПрдеставления;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;		
		// Штрихкод
		Рисунок = ОбластьЛицевая.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		// Заполняем время по графику.		
		мсвСтрок = тбДанныеВремяПоГрафику.НайтиСтроки(Новый Структура("Рейс", текСтрокаШапка.Рейс));
		КоличествоЭлементов = мсвСтрок.Количество();
		Если КоличествоЭлементов > 0 Тогда
			СтрокаДанные = мсвСтрок[0];
		    ОбластьЛицевая.Параметры.НачалоРейса = СтрокаДанные.НачалоРейса;
			
			Если КоличествоЭлементов > 1 Тогда
				СтрокаДанные = мсвСтрок[КоличествоЭлементов-1];
		    	ОбластьЛицевая.Параметры.ОкончаниеРейса = СтрокаДанные.ОкончаниеРейса;
			КонецЕсли;
		КонецЕсли;
		
		// Водительское удостоверение
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицевая.Параметры.РейсВодитель1УдостоверениеНомер = СтрокаДанные.РегистраторСерия+" "+СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		// Категория водителя.
		мсвСтрок = тбДанныеКатегории.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество() > 0 Тогда
			КлассВодителя = "";
			Для каждого строкаНомер Из мсвСтрок Цикл
				КлассВодителя = КлассВодителя + строкаНомер.Категория + ", ";
			КонецЦикла;
			КлассВодителя = Лев(КлассВодителя, СтрДлина(КлассВодителя)-2);
			ОбластьЛицевая.Параметры.КлассВодителя = КлассВодителя;
		КонецЕсли;
		
		// ЛицензионнаяКарточка
		ОбластьЛицевая.Параметры.РейсВодитель1ЛицензионнаяКарточка = "стандартная, ограниченная";
				
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ЛицензионнаяКарточка));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицевая.Параметры.РейсВодитель1СерияЛицензионнойКарточки = СтрокаДанные.РегистраторСерия;
			ОбластьЛицевая.Параметры.РейсВодитель1НомерЛицензионнойКарточки = СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		// Водитель1
		РейсВодитель1 = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрокаШапка.РейсВодитель1, "Наименование"));
		РейсВодитель1 = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(РейсВодитель1);
		
		ОбластьЛицевая.Параметры.РейсВодитель1Принял	= РейсВодитель1;
		ОбластьЛицевая.Параметры.РейсВодитель1Сдал		= РейсВодитель1;
		
		// Прицеп.
		мсвСтрок = тбДанныеПрицеп.НайтиСтроки(Новый Структура("ТранспортноеСредство", текСтрокаШапка.ТранспортноеСредство));
		Если мсвСтрок.Количество() > 0 Тогда
			мсвСтрокПоДатам = Новый Массив;
			
			Для каждого Строка Из мсвСтрок Цикл
				Если Строка.ДатаРегистратора <= текСтрокаШапка.Дата Тогда
					мсвСтрокПоДатам.Добавить(Строка);
			    КонецЕсли;
			КонецЦикла;

			количествоСтрок = мсвСтрокПоДатам.Количество();  
			выводСтрок = 0;
			
			// В макете предусмотрен вывод только для 2-х прицепов.
			Пока выводСтрок <= 2 И выводСтрок < количествоСтрок Цикл
					текСтрока = мсвСтрокПоДатам[выводСтрок];
					ОбластьЛицевая.Параметры["ПрицепМодель" + (выводСтрок+1)] =  текСтрока.ПрицепМодель;
					ОбластьЛицевая.Параметры["ПрицепГосНомер" + (выводСтрок+1)] =  текСтрока.ПрицепРегистрационныйНомер;		
					
					выводСтрок = выводСтрок + 1;
			КонецЦикла;			
		КонецЕсли;
		
		// ГСМ
		мсвСтрок = тбДанныеГСМ.НайтиСтроки(Новый Структура("ССылка, ВидГСМ, ДопОборудование", текСтрокаШапка.Ссылка, текСтрокаШапка.ВидГСМОсновной, Справочники.упДополнительноеОборудование.ПустаяСсылка()));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицевая.Параметры.ОстатокПриВыезде1 = СтрокаДанные.НачОстаток;
		КонецЕсли;

		// Механик
		Механик = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрокаШапка.Механик, "Наименование"));
		Механик = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(Механик);
		
		ОбластьЛицевая.Параметры.ВыпустилМеханик 	= Механик;
		ОбластьЛицевая.Параметры.ПринялМеханик 		= Механик;
		ОбластьЛицевая.Параметры.ВыдалДиспетчер = Строка(ПользователиКлиентСервер.ТекущийПользователь());

		
		// Врач
		Врач = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрокаШапка.Врач, "Наименование"));
		Врач = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(Врач);

		ОбластьЛицевая.Параметры.ОсмотрелВрач 		= Врач;
		
		ТабличныйДокумент.Вывести(ОбластьЛицевая);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ОбластьОборотная);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры

// Формирование печатной формы Форма4С
Функция ПечатьФорма4С(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_Форма4С";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФорма4С(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

// Формирование печатной формы ФормаСпец3
Функция ПечатьФормаСпец3(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСверху = 10;
	ТабличныйДокумент.ПолеСнизу = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_ФормаСпец3";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФормаСпец3(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции // ПечатьФормаСпец3()

// Формирование печатной формы ФормаСпец6
Функция ПечатьФормаСпец6(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСверху = 10;
	ТабличныйДокумент.ПолеСнизу = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_ФормаСпец6";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФормаСпец6(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции // ПечатьФормаСпец6()

Процедура ЗаполнитьТабличныйДокументФорма4С(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	= ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
	тбДанныеПрицеп      = ДанныеДляПечати.Прицеп;
	тбДанныеКатегории   = ДанныеДляПечати.Категории;
	тбДанныеВремяПоГрафику = ДанныеДляПечати.ВремяПоГрафику;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_Форма4С");
	
	ОбластьЛицеваяШапка				= Макет.ПолучитьОбласть("ЛицеваяШапка");
	ОбластьЛицеваяДанныеПоТСЛевая	= Макет.ПолучитьОбласть("ЛицеваяДанныеПоТС|Левая");
	ГСМШапка						= Макет.ПолучитьОбласть("ГСМШапка|Правая");
	ГСМСтрока						= Макет.ПолучитьОбласть("ГСМСтрока|Правая");
	ГСМПустаяСтрока					= Макет.ПолучитьОбласть("ГСМПустаяСтрока|Правая");
	ГСМПодвал						= Макет.ПолучитьОбласть("ГСМПодвал|Правая");
	ОбластьЛицеваяПодвал			= Макет.ПолучитьОбласть("ЛицеваяПодвал");
	ОбластьОборотная	= Макет.ПолучитьОбласть("Оборотная");
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ТабличныйДокументТС				= Новый ТабличныйДокумент; 
		ТабличныйДокументГСМ			= Новый ТабличныйДокумент;
		
		ОбластьЛицеваяШапка.Параметры.Заполнить(текСтрокаШапка);
		ОбластьЛицеваяПодвал.Параметры.Заполнить(текСтрокаШапка);
		ОбластьЛицеваяДанныеПоТСЛевая.Параметры.Заполнить(текСтрокаШапка);

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Штрихкод
		Рисунок = ОбластьЛицеваяШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		// Заполняем время по графику.		
		мсвСтрок = тбДанныеВремяПоГрафику.НайтиСтроки(Новый Структура("Рейс", текСтрокаШапка.Рейс));
		КоличествоЭлементов = мсвСтрок.Количество();
		Если КоличествоЭлементов > 0 Тогда
			СтрокаДанные = мсвСтрок[0];
		    ОбластьЛицеваяШапка.Параметры.НачалоРейса = СтрокаДанные.НачалоРейса;
			
			Если КоличествоЭлементов > 1 Тогда
				СтрокаДанные = мсвСтрок[КоличествоЭлементов-1];
		    	ОбластьЛицеваяШапка.Параметры.ОкончаниеРейса = СтрокаДанные.ОкончаниеРейса;
			КонецЕсли;
		КонецЕсли;
		
		// Водительское удостоверение Водитель1
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицеваяДанныеПоТСЛевая.Параметры.РейсВодитель1УдостоверениеНомер = 	СтрокаДанные.РегистраторСерия + " " + СтрокаДанные.РегистраторНомер;			          
		КонецЕсли;
		
		// Категория водителя.
		мсвСтрок = тбДанныеКатегории.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество() > 0 Тогда
			КлассВодителя = "";
			Для каждого строкаНомер Из мсвСтрок Цикл
				КлассВодителя = КлассВодителя + строкаНомер.Категория + ", ";
			КонецЦикла;
			КлассВодителя = Лев(КлассВодителя, СтрДлина(КлассВодителя)-2);
			ОбластьЛицеваяДанныеПоТСЛевая.Параметры.КлассВодителя = КлассВодителя;
		КонецЕсли;
		
		// ЛицензионнаяКарточка
		ОбластьЛицеваяДанныеПоТСЛевая.Параметры.РейсВодитель1ЛицензионнаяКарточка = "стандартная, ограниченная";
		
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ЛицензионнаяКарточка));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛицеваяДанныеПоТСЛевая.Параметры.РейсВодитель1НомерЛицензионнойКарточки = СтрокаДанные.РегистраторСерия + " " + СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		// Водитель1
		ОбластьЛицеваяПодвал.Параметры.РейсВодитель1Принял	= текСтрокаШапка.РейсВодитель1;
		ОбластьЛицеваяПодвал.Параметры.РейсВодитель1Сдал		= текСтрокаШапка.РейсВодитель1;
		
		// Прицеп.
		мсвСтрок = тбДанныеПрицеп.НайтиСтроки(Новый Структура("ТранспортноеСредство", текСтрокаШапка.ТранспортноеСредство));
		Если мсвСтрок.Количество() > 0 Тогда
			количествоСтрок = мсвСтрок.Количество();  
			выводСтрок = 0;
			
			// В макете предусмотрен вывод только для 4-х прицепов.
			Пока выводСтрок <= 4 И выводСтрок < количествоСтрок Цикл
				текСтрока = мсвСтрок[выводСтрок];
				ОбластьЛицеваяДанныеПоТСЛевая.Параметры["ПрицепМодель" + (выводСтрок+1)] =  текСтрока.ПрицепМодель;
				ОбластьЛицеваяДанныеПоТСЛевая.Параметры["ПрицепГосНомер" + (выводСтрок+1)] =  текСтрока.ПрицепРегистрационныйНомер;		
				
				выводСтрок = выводСтрок + 1;
			КонецЦикла;			
		КонецЕсли;
		
		ТабличныйДокументГСМ.Вывести(ГСМШапка);
				
		// ГСМ
		мсвСтрокиГСМ = тбДанныеГСМ.НайтиСтроки(Новый Структура("Ссылка", текСтрокаШапка.Ссылка));
		Для каждого СтрокаДанные Из мсвСтрокиГСМ Цикл
			ГСМСтрока						= Макет.ПолучитьОбласть("ГСМСтрока|Правая");
			ЗаполнитьЗначенияСвойств(ГСМСтрока.Параметры, СтрокаДанные);
			ТабличныйДокументГСМ.Вывести(ГСМСтрока);
		КонецЦикла;
		
		КоличествоСтрокГСМ = мсвСтрокиГСМ.Количество();
		
		Пока КоличествоСтрокГСМ < 3 Цикл
			ТабличныйДокументГСМ.Вывести(ГСМПустаяСтрока);
		    КоличествоСтрокГСМ = КоличествоСтрокГСМ + 1;
		КонецЦикла;
		
		ТабличныйДокументГСМ.Вывести(ГСМПодвал);
				
		// Гараж
		ОбластьЛицеваяПодвал.Параметры.Гараж 	= текСтрокаШапка.АдресГаража.ПолноеНаименование;
		ОбластьЛицеваяПодвал.Параметры.ВыдалДиспетчер = Строка(ПользователиКлиентСервер.ТекущийПользователь());

		// Механик
		ОбластьЛицеваяПодвал.Параметры.ВыпустилМеханик 	= текСтрокаШапка.Механик;
		ОбластьЛицеваяПодвал.Параметры.ПринялМеханик 		= текСтрокаШапка.Механик;
		
		// Врач
		ОбластьЛицеваяПодвал.Параметры.ОсмотрелВрач 		= текСтрокаШапка.Врач;
		
		ТабличныйДокумент.Вывести(ОбластьЛицеваяШапка);
		
		ТабличныйДокументТС.Присоединить(ОбластьЛицеваяДанныеПоТСЛевая); 
		ТабличныйДокументТС.Присоединить(ТабличныйДокументГСМ); 
		
		ТабличныйДокумент.Вывести(ТабличныйДокументТС);
		
		ТабличныйДокумент.Вывести(ОбластьЛицеваяПодвал);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ОбластьОборотная);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры

Процедура ЗаполнитьТабличныйДокументФормаСпец3(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	= ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
	тбДанныеПрицеп      = ДанныеДляПечати.Прицеп;
	тбДанныеКатегории   = ДанныеДляПечати.Категории;
	тбДанныеВремяПоГрафику = ДанныеДляПечати.ВремяПоГрафику;
	тбДанныеГаражи = ДанныеДляПечати.Гаражи;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_ФормаСпец3");
	
	ОбластьСтраница1				= Макет.ПолучитьОбласть("Страница1|Потолок");
	ОбластьСтраница2				= Макет.ПолучитьОбласть("Страница2|Потолок");
	
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьСтраница1.Параметры.НомерПЛ = текСтрокаШапка.Номер;
		ОбластьСтраница1.Параметры.ДПЛ_День = Формат(текСтрокаШапка.Дата, "ДФ=dd; ДЛФ=D");
		ОбластьСтраница1.Параметры.ДПЛ_Месяц = Формат(текСтрокаШапка.Дата, "ДФ=MMMM");
		ОбластьСтраница1.Параметры.ДПЛ_Год = Формат(текСтрокаШапка.Дата, "ДФ=yyyy; ДЛФ=D");
		ОбластьСтраница1.Параметры.КодОКПО = текСтрокаШапка.ОКПО;
		ОбластьСтраница1.Параметры.МодельТС = текСтрокаШапка.ТСМодель;
		ОбластьСтраница1.Параметры.НомерТС = текСтрокаШапка.ТСРегистрационныйНомер;
		ОбластьСтраница1.Параметры.НомерГараж = текСтрокаШапка.ТСГаражныйНомер;
		ОбластьСтраница1.Параметры.ОрганизацияНаименование = текСтрокаШапка.РейсОрганизацияНаименованиеПолное;
		ОбластьСтраница1.Параметры.ПредставлениеВодитель = текСтрокаШапка.РейсВодитель1;
		ОбластьСтраница1.Параметры.ТабНомерВодитель = текСтрокаШапка.РейсВодитель1ТабельныйНомер;
		ОбластьСтраница1.Параметры.КлассВодитель = текСтрокаШапка.РейсВодитель1Класс;
		ОбластьСтраница1.Параметры.Дата = текСтрокаШапка.Дата;

		Если СтрДлина(текСтрокаШапка.РейсОрганизацияПредставление) > 100 Тогда
			МассивСтрок = СтрРазделить(текСтрокаШапка.РейсОрганизацияПредставление, " ");
			РейсОрганизацияПредставление = "";
			СчетчикСтрок = 1;
			Для Каждого ТекущаяСтрокаМассива Из МассивСтрок Цикл
				Если СтрДлина(РейсОрганизацияПредставление + " " + ТекущаяСтрокаМассива) < 100 Тогда
					РейсОрганизацияПредставление = РейсОрганизацияПредставление + " " + ТекущаяСтрокаМассива;
				Иначе
					Если СчетчикСтрок = 1 Тогда
						ОбластьСтраница1.Параметры.Организация = РейсОрганизацияПредставление;
						РейсОрганизацияПредставление = ТекущаяСтрокаМассива;
					КонецЕсли;
					СчетчикСтрок = СчетчикСтрок + 1;
				КонецЕсли;
			КонецЦикла;
			ОбластьСтраница1.Параметры.Организация2 = РейсОрганизацияПредставление;
		Иначе
			ОбластьСтраница1.Параметры.Организация = текСтрокаШапка.РейсОрганизацияПредставление;
		КонецЕсли;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		// Водительское удостоверение Водитель1
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьСтраница1.Параметры.УдостоверениеВодитель = СтрокаДанные.РегистраторСерия + " " + СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		СтрукутраПоиска = Новый Структура("Ссылка, Рейс");
		ЗаполнитьЗначенияСвойств(СтрукутраПоиска, текСтрокаШапка);
		НайденныеСтроки = тбДанныеГаражи.НайтиСтроки(СтрукутраПоиска);
		ГаражныйНомер = "";
		Если НайденныеСтроки.Количество() Тогда
			ГаражныйНомер = НайденныеСтроки[0].ГаражныйНомер;
		КонецЕсли;
		ОбластьСтраница1.Параметры.НомерГараж = ГаражныйНомер;
		
		ОбластьСтраница1.Параметры.НачальныйПробег = текСтрокаШапка.ПробегНачало;
		ОбластьСтраница1.Параметры.НачалоПериодаС = текСтрокаШапка.ПлановоеНачалоВыполнения;
		ОбластьСтраница1.Параметры.НачалоПериодаПо = текСтрокаШапка.ПлановоеОкончаниеВыполнения;
		ОбластьСтраница1.Параметры.ВидГСМ = текСтрокаШапка.ВидГСМОсновной;
		
		// ГСМ
		НачальныйОстаток = 0;
		КонечныйРасход = 0;
		РасходГСМ_Норма = 0;
		РасходГСМ_Факт = 0;
		ВидГСМ = текСтрокаШапка.ВидГСМОсновной;
		Если ЗначениеЗаполнено(ВидГСМ) Тогда
			Структура = Новый Структура;
			Структура.Вставить("ВидГСМ", текСтрокаШапка.ВидГСМОсновной);
			мсвСтрокиГСМ = тбДанныеГСМ.НайтиСтроки(Структура);
			Для каждого СтрокаДанные Из мсвСтрокиГСМ Цикл
				НачальныйОстаток = СтрокаДанные.НачОстаток;
				КонечныйРасход = СтрокаДанные.КонОстаток;
				РасходГСМ_Норма = СтрокаДанные.РасходТопливаПоНорме;
				РасходГСМ_Факт = СтрокаДанные.РасходТопливаФакт;
			КонецЦикла;
		КонецЕсли;
		ОбластьСтраница1.Параметры.НачальныйОстаток = НачальныйОстаток;
		
		
		// Водитель1
		ОбластьСтраница1.Параметры.РейсВодитель1Принял	= текСтрокаШапка.РейсВодитель1;
		ОбластьСтраница1.Параметры.РейсВодитель1Сдал	= текСтрокаШапка.РейсВодитель1;
		
		// Прицеп.
		мсвСтрок = тбДанныеПрицеп.НайтиСтроки(Новый Структура("ТранспортноеСредство", текСтрокаШапка.ТранспортноеСредство));
		Если мсвСтрок.Количество() > 0 Тогда
			количествоСтрок = мсвСтрок.Количество();  
			выводСтрок = 0;
			
			// В макете предусмотрен вывод только для 4-х прицепов.
			Пока выводСтрок <= 4 И выводСтрок < количествоСтрок Цикл
				текСтрока = мсвСтрок[выводСтрок];
				ОбластьСтраница1.Параметры["ПрицепМодель" + (выводСтрок+1)] =  текСтрока.ПрицепМодель;
				ОбластьСтраница1.Параметры["ПрицепГосНомер" + (выводСтрок+1)] =  текСтрока.ПрицепРегистрационныйНомер;		
				ОбластьСтраница1.Параметры["ГаражНомерПрицепа" + (выводСтрок+1)] =  текСтрока.ПрицепРегистрационныйНомер;		
				Прервать;				
				выводСтрок = выводСтрок + 1;
			КонецЦикла;			
		КонецЕсли;
		
		// Гараж
		ОбластьСтраница1.Параметры.ВыдалДиспетчер = Строка(ПользователиКлиентСервер.ТекущийПользователь());

		// Механик
		ОбластьСтраница1.Параметры.ВыпустилМеханик 	= текСтрокаШапка.Механик;
		ОбластьСтраница1.Параметры.ПринялМеханик 		= текСтрокаШапка.Механик;
		
		// Врач
		ОбластьСтраница1.Параметры.ОсмотрелВрач 		= текСтрокаШапка.Врач;
		
		ТабличныйДокумент.Вывести(ОбластьСтраница1);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ОбластьСтраница2);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры // ЗаполнитьТабличныйДокументФормаСпец3()

Процедура ЗаполнитьТабличныйДокументФормаСпец6(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	= ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
	тбДанныеПрицеп      = ДанныеДляПечати.Прицеп;
	тбДанныеКатегории   = ДанныеДляПечати.Категории;
	тбДанныеВремяПоГрафику = ДанныеДляПечати.ВремяПоГрафику;
	тбДанныеГаражи = ДанныеДляПечати.Гаражи;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_ФормаСпец6");
	
	ОбластьСтраница1				= Макет.ПолучитьОбласть("Страница1|Потолок");
	ОбластьСтраница2				= Макет.ПолучитьОбласть("Страница2|Потолок");
	
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьСтраница1.Параметры.НомерПЛ = текСтрокаШапка.Номер;
		ОбластьСтраница1.Параметры.ДПЛ_День = Формат(текСтрокаШапка.Дата, "ДФ=dd; ДЛФ=D");
		ОбластьСтраница1.Параметры.ДПЛ_Месяц = Формат(текСтрокаШапка.Дата, "ДФ=MMMM");
		ОбластьСтраница1.Параметры.ДПЛ_Год = Формат(текСтрокаШапка.Дата, "ДФ=yyyy; ДЛФ=D");		
		ОбластьСтраница1.Параметры.МодельТС = текСтрокаШапка.ТСМодель;
		ОбластьСтраница1.Параметры.КодОКПО = текСтрокаШапка.ОКПО;
		ОбластьСтраница1.Параметры.НомерТС = текСтрокаШапка.ТСРегистрационныйНомер;		
		ОбластьСтраница1.Параметры.ОрганизацияНаименование = текСтрокаШапка.РейсОрганизацияНаименованиеПолное;
		ОбластьСтраница1.Параметры.ОрганизацияАдрес = текСтрокаШапка.РейсОрганизацияАдрес;		
		ОбластьСтраница1.Параметры.ПредставлениеВодитель = текСтрокаШапка.РейсВодитель1;
		ОбластьСтраница1.Параметры.ТабНомерВодитель = текСтрокаШапка.РейсВодитель1ТабельныйНомер;
		ОбластьСтраница1.Параметры.КлассВодитель = текСтрокаШапка.РейсВодитель1Класс;
		ОбластьСтраница1.Параметры.Дата = текСтрокаШапка.Дата;

		
		СтрукутраПоиска = Новый Структура("Ссылка, Рейс");
		ЗаполнитьЗначенияСвойств(СтрукутраПоиска, текСтрокаШапка);
		НайденныеСтроки = тбДанныеГаражи.НайтиСтроки(СтрукутраПоиска);
		ГаражныйНомер = "";
		Если НайденныеСтроки.Количество() Тогда
			ГаражныйНомер = НайденныеСтроки[0].ГаражныйНомер;
		КонецЕсли;
		ОбластьСтраница1.Параметры.НомерГараж = ГаражныйНомер;

		Если СтрДлина(текСтрокаШапка.РейсОрганизацияПредставление) > 100 Тогда
			МассивСтрок = СтрРазделить(текСтрокаШапка.РейсОрганизацияПредставление, " ");
			РейсОрганизацияПредставление = "";
			СчетчикСтрок = 1;
			Для Каждого ТекущаяСтрокаМассива Из МассивСтрок Цикл
				Если СтрДлина(РейсОрганизацияПредставление + " " + ТекущаяСтрокаМассива) < 100 Тогда
					РейсОрганизацияПредставление = РейсОрганизацияПредставление + " " + ТекущаяСтрокаМассива;
				Иначе
					Если СчетчикСтрок = 1 Тогда
						ОбластьСтраница1.Параметры.Организация = РейсОрганизацияПредставление;
						РейсОрганизацияПредставление = ТекущаяСтрокаМассива;
					КонецЕсли;
					СчетчикСтрок = СчетчикСтрок + 1;
				КонецЕсли;
			КонецЦикла;
			ОбластьСтраница1.Параметры.Организация2 = РейсОрганизацияПредставление;
		Иначе
			ОбластьСтраница1.Параметры.Организация = текСтрокаШапка.РейсОрганизацияПредставление;
		КонецЕсли;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		// Водительское удостоверение Водитель1
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьСтраница1.Параметры.УдостоверениеВодитель = СтрокаДанные.РегистраторСерия + " " + СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		ОбластьСтраница1.Параметры.НачальныйПробег = текСтрокаШапка.ПробегНачало;
		ОбластьСтраница1.Параметры.НачалоПериодаС = текСтрокаШапка.ПлановоеНачалоВыполнения;
		ОбластьСтраница1.Параметры.НачалоПериодаПо = текСтрокаШапка.ПлановоеОкончаниеВыполнения;
		
		// ГСМ
		выводСтрок = 1;
		Для каждого СтрокаДанные Из тбДанныеГСМ Цикл
			ОбластьСтраница1.Параметры["ВидГСМ" + (выводСтрок)] = СтрокаДанные.ВидГСМ;
			ОбластьСтраница1.Параметры["НачальныйОстаток" + (выводСтрок)] = СтрокаДанные.НачОстаток;
			выводСтрок = выводСтрок + 1;
			Если выводСтрок > 2 Тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;
				
		// Водитель1
		ОбластьСтраница1.Параметры.РейсВодитель1Принял	= текСтрокаШапка.РейсВодитель1;
		ОбластьСтраница1.Параметры.РейсВодитель1Сдал	= текСтрокаШапка.РейсВодитель1;
		
		ОбластьСтраница1.Параметры.ВыдалДиспетчер = Строка(ПользователиКлиентСервер.ТекущийПользователь());

		// Механик
		ОбластьСтраница1.Параметры.ВыпустилМеханик 	= текСтрокаШапка.Механик;
		ОбластьСтраница1.Параметры.ПринялМеханик 		= текСтрокаШапка.Механик;
		
		// Врач
		ОбластьСтраница1.Параметры.ОсмотрелВрач 		= текСтрокаШапка.Врач;
		
		ТабличныйДокумент.Вывести(ОбластьСтраница1);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьСтраница2.Параметры.НомерПЛ = текСтрокаШапка.Номер;
		ОбластьСтраница2.Параметры.ДПЛ_День = Формат(текСтрокаШапка.Дата, "ДФ=dd; ДЛФ=D");
		ОбластьСтраница2.Параметры.ДПЛ_Месяц = Формат(текСтрокаШапка.Дата, "ДФ=MMMM");
		ОбластьСтраница2.Параметры.ДПЛ_Год = Формат(текСтрокаШапка.Дата, "ДФ=yyyy; ДЛФ=D");
		ОбластьСтраница2.Параметры.МодельТС = текСтрокаШапка.ТСМодель;
		ОбластьСтраница2.Параметры.НомерТС = текСтрокаШапка.ТСРегистрационныйНомер;
		ОбластьСтраница2.Параметры.ОрганизацияНаименование = текСтрокаШапка.РейсОрганизацияНаименованиеПолное;
		ОбластьСтраница2.Параметры.ОрганизацияАдрес = текСтрокаШапка.РейсОрганизацияАдрес;
		
		ТабличныйДокумент.Вывести(ОбластьСтраница2);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры // ЗаполнитьТабличныйДокументФормаСпец6()

// Формирование печатной формы ФормаПЛ1
Функция ПечатьФормаПЛ1(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_ФормаПЛ1";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФормаПЛ1(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументФормаПЛ1(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	= ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_ФормаПЛ1");
	
	ОбластьШапка		= Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьПодвал		= Макет.ПолучитьОбласть("ОбластьПодвал");
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Штрихкод
		Рисунок = ОбластьШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
				
		// Водительское удостоверение Водитель1
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьШапка.Параметры.РейсВодитель1УдостоверениеНомер = 	СтрокаДанные.РегистраторНомер;			          
		КонецЕсли;
		
		// Водительское удостоверение Водитель2
		мсвСтрок = тбДанныеВодитель2.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьШапка.Параметры.РейсВодитель2УдостоверениеНомер = 	СтрокаДанные.РегистраторНомер;			          
		КонецЕсли;
		
		// ЛицензионнаяКарточка
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ЛицензионнаяКарточка));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьШапка.Параметры.РейсВодитель1НомерЛицензионнойКарточки = СтрокаДанные.РегистраторНомер;
		КонецЕсли;

		
		ОбластьШапка.Параметры.ДатаНач	= Формат(текСтрокаШапка.НачалоРейсаФакт, "ДФ=d.MM.yyyy");
		ОбластьШапка.Параметры.ДатаКон	= Формат(текСтрокаШапка.НачалоРейсаФакт, "ДФ=d.MM.yyyy");
		
	
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры

// Формирование печатной формы ФормаПГ1
Функция ПечатьФормаПГ1(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПутевойЛист_ФормаПГ1";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументФормаПГ1(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументФормаПГ1(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  		= ДанныеДляПечати.Шапка;
	тбДанныеВодитель1 	= ДанныеДляПечати.Водитель1;
	тбДанныеВодитель2 	= ДанныеДляПечати.Водитель2;
	тбДанныеГСМ			= ДанныеДляПечати.ГСМ;
	тбДанныеПрицеп      = ДанныеДляПечати.Прицеп;
			
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упПутевойЛист.ПФ_MXL_ФормаПГ1");
	
	ОбластьШапка		= Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьПодвал		= Макет.ПолучитьОбласть("ОбластьПодвал");
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Штрихкод
		Рисунок = ОбластьШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		// Водительское удостоверение Водитель1
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьШапка.Параметры.РейсВодитель1УдостоверениеНомер = 	СтрокаДанные.РегистраторНомер;			          
		КонецЕсли;
		
		// Водительское удостоверение Водитель2
		мсвСтрок = тбДанныеВодитель2.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьШапка.Параметры.РейсВодитель2УдостоверениеНомер = 	СтрокаДанные.РегистраторНомер;			          
		КонецЕсли;
		
		// ЛицензионнаяКарточка
		мсвСтрок = тбДанныеВодитель1.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ЛицензионнаяКарточка));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьШапка.Параметры.РейсВодитель1НомерЛицензионнойКарточки = СтрокаДанные.РегистраторНомер;
		КонецЕсли;
		
		// Прицеп.
		мсвСтрок = тбДанныеПрицеп.НайтиСтроки(Новый Структура("ТранспортноеСредство", текСтрокаШапка.ТранспортноеСредство));
		Если мсвСтрок.Количество() > 0 Тогда
			количествоСтрок = мсвСтрок.Количество();  
			выводСтрок = 0;
			
			// В макете предусмотрен вывод только для 2-х прицепов.
			Пока выводСтрок <= 2 И выводСтрок < количествоСтрок Цикл
				текСтрока = мсвСтрок[выводСтрок];
				ОбластьШапка.Параметры["ПрицепМодель" + (выводСтрок+1)] =  текСтрока.ПрицепМодель;
				ОбластьШапка.Параметры["ПрицепГосНомер" + (выводСтрок+1)] =  текСтрока.ПрицепРегистрационныйНомер;		
				
				выводСтрок = выводСтрок + 1;
			КонецЦикла;			
		КонецЕсли;
		
		ОбластьШапка.Параметры.ДатаНач	= Формат(текСтрокаШапка.НачалоРейсаФакт, "ДФ=d.MM.yyyy");
		ОбластьШапка.Параметры.ДатаКон	= Формат(текСтрокаШапка.НачалоРейсаФакт, "ДФ=d.MM.yyyy");
		
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);
				
	КонецЦикла;	

КонецПроцедуры

// Возвращает структуру данных для заполнения печатных форм.
//
Функция ПолучитьДанныеДляПечатныхФорм(МассивОбъектов) Экспорт
		
	Запрос = Новый Запрос;	
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛист.Ссылка КАК Ссылка,
	|	упПутевойЛист.Автор КАК Автор,
	|	упПутевойЛист.Номер КАК Номер,
	|	упПутевойЛист.Дата КАК Дата,
	|	упПутевойЛист.Рейс КАК Рейс,
	|	упПутевойЛист.НачалоРейса КАК НачалоРейсаФакт,
	|	упПутевойЛист.ОкончаниеРейса КАК ОкончаниеРейсаФакт,
	|	упПутевойЛист.ПробегНачало КАК ПробегНачало,
	|	упПутевойЛист.ПробегОкончание КАК ПробегОкончание,
	|	упПутевойЛист.Пробег КАК Пробег,
	|	упПутевойЛист.Гараж.Адрес КАК АдресГаража,
	|	упПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упПутевойЛист.ТемператураВоздуха КАК ТемператураВоздуха,
	|	ЕСТЬNULL(упТранспортныеСредстваВидыГСМ.ВидГСМ, ЗНАЧЕНИЕ(Справочник.упВидыГСМ.ПустаяСсылка)) КАК ВидГСМОсновной,
	|	упПутевойЛист.ТранспортноеСредство.ДатчикУровняТоплива КАК ДатчикУровняТоплива,
	|	упПутевойЛист.Организация КАК РейсОрганизация,
	|	упПутевойЛист.ТранспортноеСредство.РегистрационныйНомер КАК ТСРегистрационныйНомер,
	|	упПутевойЛист.ТранспортноеСредство.РегистрационныйНомер КАК ТСГаражныйНомер,
	|	упПутевойЛист.ТранспортноеСредство.Модель КАК ТСМодель,
	|	упПутевойЛист.ТранспортноеСредство.ВидТранспорта КАК ТСВидТранспорта,
	|	упПутевойЛист.Организация.НаименованиеПолное КАК РейсОрганизацияНаименованиеПолное,
	|	упПутевойЛист.Организация.КодПоОКПО КАК ОКПО,
	|	упПутевойЛист.Организация.ОГРН КАК ОГРН,
	|	упПутевойЛист.Организация.ИНН КАК ОрганизацияИНН,
	|	упПутевойЛист.Организация.КПП КАК ОрганизацияКПП,
	|	упПутевойЛист.Механик КАК Механик,
	|	упПутевойЛист.Врач КАК Врач,
	|	упПутевойЛист.ДатаДействияПутевогоЛиста КАК ДатаДействияПутевогоЛиста
	|ПОМЕСТИТЬ втПутевыеЛистыПодготовка
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних КАК упТранспортныеСредстваВидыГСМ
	|	ПО ИСТИНА
	|		И упПутевойЛист.ТранспортноеСредство = упТранспортныеСредстваВидыГСМ.ТранспортноеСредство
	|		И упТранспортныеСредстваВидыГСМ.Основной
	|		И упТранспортныеСредстваВидыГСМ.Используется
	|ГДЕ упПутевойЛист.Ссылка В  (&Ссылка)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПутевыеЛисты.*,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель1 КАК РейсВодитель1,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель1.Класс КАК РейсВодитель1Класс,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель1.ТабельныйНомер КАК РейсВодитель1ТабельныйНомер,
	|	упПеревозчикПоРейсуСрезПоследних.Экспедитор КАК Экспедитор,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель2 КАК РейсВодитель2,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель2.Класс КАК РейсВодитель2Класс,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель2.ТабельныйНомер КАК РейсВодитель2ТабельныйНомер,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеНачалоВыполнения,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеОкончаниеВыполнения
	|ПОМЕСТИТЬ втПутевыеЛисты
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|		ВЫБРАТЬ
	|		втПутевыеЛистыПодготовка.Рейс КАК Рейс
	|		ИЗ
	|		втПутевыеЛистыПодготовка КАК втПутевыеЛистыПодготовка)) КАК упПеревозчикПоРейсуСрезПоследних
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПутевыеЛистыПодготовка КАК втПутевыеЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО втПутевыеЛисты.Рейс = упПлановыеПараметрыРейса.Рейс
	|	ПО упПеревозчикПоРейсуСрезПоследних.Рейс = втПутевыеЛисты.Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втПутевыеЛисты КАК втПутевыеЛисты
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоследних.ТипДокумента КАК ТипДокумента,
	|	СрезПоследних.Водитель КАК Водитель,
	|	СрезПоследних.ДействуетПо КАК ДействуетПо,
	|	СрезПоследних.Регистратор.Серия КАК РегистраторСерия,
	|	СрезПоследних.Регистратор.Номер КАК РегистраторНомер,
	|	СрезПоследних.Регистратор.ДействуетС КАК РегистраторДействуетС,
	|	СрезПоследних.Регистратор.ДействуетПо КАК РегистраторДействуетПо,
	|	СрезПоследних.Регистратор.Выдан КАК РегистраторВыдан,
	|	СрезПоследних.Регистратор.Дата КАК РегистраторДата
	|ИЗ
	|	РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(
	|		,
	|		ВЫБОР
	|		КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ВодительскоеУдостоверение)
	|		ТОГДА Водитель В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.РейсВодитель1 КАК РейсВодитель1
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)
	|		КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ЛицензионнаяКарточка)
	|		ТОГДА ТранспортноеСредство В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.ТранспортноеСредство КАК ТранспортноеСредство
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)
	|		ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СрезПоследних
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоследних.ТипДокумента КАК ТипДокумента,
	|	СрезПоследних.Водитель КАК Водитель,
	|	СрезПоследних.ДействуетПо КАК ДействуетПо,
	|	СрезПоследних.Регистратор.Серия КАК РегистраторСерия,
	|	СрезПоследних.Регистратор.Номер КАК РегистраторНомер,
	|	СрезПоследних.Регистратор.ДействуетС КАК РегистраторДействуетС,
	|	СрезПоследних.Регистратор.ДействуетПо КАК РегистраторДействуетПо,
	|	СрезПоследних.Регистратор.Выдан КАК РегистраторВыдан,
	|	СрезПоследних.Регистратор.Дата КАК РегистраторДата
	|ИЗ
	|	РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(
	|		,
	|		ВЫБОР
	|		КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ВодительскоеУдостоверение)
	|		ТОГДА Водитель В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.РейсВодитель2 КАК РейсВодитель2
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)
	|		КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ЛицензионнаяКарточка)
	|		ТОГДА ТранспортноеСредство В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.ТранспортноеСредство КАК ТранспортноеСредство
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)
	|		ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СрезПоследних
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛистГСМ.Ссылка КАК Ссылка,
	|	упПутевойЛистГСМ.НомерСтроки КАК НомерСтроки,
	|	упПутевойЛистГСМ.ВидГСМ КАК ВидГСМ,
	|	упПутевойЛистГСМ.ДопОборудование КАК ДопОборудование,
	|	упПутевойЛистГСМ.НачОстаток КАК НачОстаток,
	|	упПутевойЛистГСМ.КонОстаток КАК КонОстаток,
	|	упПутевойЛистГСМ.РасходТопливаПлан КАК РасходТопливаПлан,
	|	упПутевойЛистГСМ.РасходТопливаФакт КАК РасходТопливаФакт,
	|	упПутевойЛистГСМ.СуммаРасходов КАК СуммаРасходов,
	|	упПутевойЛистГСМ.РасходТопливаПоНорме КАК РасходТопливаПоНорме
	|ИЗ
	|	Документ.упПутевойЛист.ГСМ КАК упПутевойЛистГСМ
	|ГДЕ упПутевойЛистГСМ.Ссылка В (
	|		ВЫБРАТЬ
	|			втПутевыеЛисты.Ссылка КАК Ссылка
	|		ИЗ
	|			втПутевыеЛисты КАК втПутевыеЛисты)
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбМаршрут.Адрес КАК Адрес,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.Операция, ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПустаяСсылка)) КАК Операция,
	|	тбМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	тбМаршрут.ТипТочки КАК ТипТочки,
	|	тбМаршрут.Гараж КАК Гараж,
	|	тбМаршрут.Адрес.ПолноеНаименование КАК АдресПолноеНаименование
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.Рейс КАК Рейс
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)) КАК тбМаршрут
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.Рейс КАК Рейс
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)) КАК упРаботыПоРейсуСрезПоследних
	|	ПО тбМаршрут.Идентификатор = упРаботыПоРейсуСрезПоследних.Идентификатор
	|ГДЕ ЛОЖЬ
	|	ИЛИ НЕ (упРаботыПоРейсуСрезПоследних.Отменена)
	|	ИЛИ упРаботыПоРейсуСрезПоследних.СтрокаНомер ЕСТЬ NULL
	|УПОРЯДОЧИТЬ ПО
	|	тбМаршрут.СтрокаНомер
	|ИТОГИ
	|	МАКСИМУМ(АдресПолноеНаименование),
	|	МАКСИМУМ(Адрес),
	|	МАКСИМУМ(ТипТочки),
	|	МАКСИМУМ(Гараж)
	|ПО
	|	тбМаршрут.Идентификатор
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(упПрицепыТранспортныхСредствСрезПоследних.Прицеп.Модель) КАК ПрицепМодель,
	|	упПрицепыТранспортныхСредствСрезПоследних.Прицеп.РегистрационныйНомер КАК ПрицепРегистрационныйНомер,
	|	упПрицепыТранспортныхСредствСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упПрицепыТранспортныхСредствСрезПоследних.Период КАК ДатаРегистратора
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
	|		,
	|		) КАК упПрицепыТранспортныхСредствСрезПоследних
	|ГДЕ ИСТИНА
	|	И упПрицепыТранспортныхСредствСрезПоследних.ТранспортноеСредство В (
	|			ВЫБРАТЬ
	|				втПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство
	|			ИЗ
	|				втПутевыеЛистыПодготовка КАК втПутевойЛист)
	|	И упПрицепыТранспортныхСредствСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоследних.ТипДокумента КАК ТипДокумента,
	|	ЕСТЬNULL(КатегорииТЧ.Категория, """") КАК Категория
	|ИЗ
	|	РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(
	|		,
	|		ВЫБОР
	|		КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ВодительскоеУдостоверение)
	|		ТОГДА Водитель В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.РейсВодитель1 КАК РейсВодитель1
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты
	|		ОБЪЕДИНИТЬ ВСЕ
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.РейсВодитель2 КАК РейсВодитель1
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)
	|		КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ЛицензионнаяКарточка)
	|		ТОГДА ТранспортноеСредство В (
	|		ВЫБРАТЬ
	|		втПутевыеЛисты.ТранспортноеСредство КАК ТранспортноеСредство
	|		ИЗ
	|		втПутевыеЛисты КАК втПутевыеЛисты)
	|		ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК СрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упДокументТранспортногоСредстваВодителя.Категории КАК КатегорииТЧ
	|	ПО СрезПоследних.Регистратор = КатегорииТЧ.Ссылка
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсу.Регистратор КАК Рейс,
	|	упМаршрутПоРейсу.ПлановоеПрибытие КАК ОкончаниеРейса,
	|	упМаршрутПоРейсу.ПлановоеУбытие КАК НачалоРейса
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу КАК упМаршрутПоРейсу
	|ГДЕ ИСТИНА
	|	И упМаршрутПоРейсу.Регистратор В (
	|			ВЫБРАТЬ
	|				втПутевыеЛистыПодготовка.Рейс КАК Рейс
	|			ИЗ
	|				втПутевыеЛистыПодготовка КАК втПутевыеЛистыПодготовка)
	|	И НЕ (упМаршрутПоРейсу.Гараж = ЗНАЧЕНИЕ(Справочник.упГаражи.ПустаяСсылка))
	|	И упМаршрутПоРейсу.Отменена = ЛОЖЬ
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсу.ПлановоеУбытие
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПутевыеЛистыПодготовка.Рейс КАК Рейс,
	|	втПутевыеЛистыПодготовка.Ссылка КАК Ссылка,
	|	втПутевыеЛистыПодготовка.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упТранспортныеСредстваГаражи.Гараж КАК Гараж,
	|	упТранспортныеСредстваГаражи.ГаражныйНомер КАК ГаражныйНомер
	|ИЗ
	|	Справочник.упТранспортныеСредства.Гаражи КАК упТранспортныеСредстваГаражи
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПутевыеЛистыПодготовка КАК втПутевыеЛистыПодготовка
	|	ПО упТранспортныеСредстваГаражи.Ссылка = втПутевыеЛистыПодготовка.ТранспортноеСредство
	|";
	 
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов);
	
	ПакетРезультатовЗапроса	= Запрос.ВыполнитьПакет();
	
	ИндексШапка = 2;
	
	тбДанныеШапка  = ПакетРезультатовЗапроса[ИндексШапка].Выгрузить();
	тбДанныеШапка.Колонки.Добавить("ДатаВыписки");
	тбДанныеШапка.Колонки.Добавить("РейсОрганизацияПредставление");
	тбДанныеШапка.Колонки.Добавить("РейсОрганизацияАдрес");
	тбДанныеШапка.Колонки.Добавить("ВРаспоряжение");
	тбДанныеШапка.Колонки.Добавить("АдресПодачи");
	
	дрвМаршрут = ПакетРезультатовЗапроса[6].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	АдресПодачи = "";
	ИспользоватьГараж = Ложь;
	
	// Адрес подачи
	Для каждого Точка Из дрвМаршрут.Строки Цикл
		
		Если ИспользоватьГараж Тогда
			АдресПодачи = Точка.Адрес;
			Прервать;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Точка.Гараж) Тогда
			ИспользоватьГараж = Истина;
		КонецЕсли;		
		Для каждого Операция Из Точка.Строки Цикл
			Если НЕ ЗначениеЗаполнено(АдресПодачи) И Операция.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию Тогда
				АдресПодачи = Операция.Адрес;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого Строка Из тбДанныеШапка Цикл
		
		Строка.ДатаВыписки = ТекущаяДатаСеанса();
		
		// Организация
		Если ЗначениеЗаполнено(Строка.РейсОрганизация) Тогда
			ОрганизацияПолноеНаименование	= Строка.РейсОрганизацияНаименованиеПолное; 
			АдресОрганизация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Строка.РейсОрганизация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
			Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Строка.РейсОрганизация, Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
			ОГРН = Строка.ОГРН;
			
			Строка.РейсОрганизацияПредставление = ""
										   + ?(ПустаяСтрока(ОрганизацияПолноеНаименование),Строка.РейсОрганизация, ОрганизацияПолноеНаименование)
										   + ?(ПустаяСтрока(АдресОрганизация), "", ", " + АдресОрганизация)
										   + ?(ПустаяСтрока(Телефон), "", ", " + Телефон)
										   + ?(ПустаяСтрока(ОГРН), "", ", " + ОГРН);
			Строка.РейсОрганизацияАдрес = ""
			                              + ?(ПустаяСтрока(АдресОрганизация), "", ", " + АдресОрганизация)
									+ ?(ПустаяСтрока(Телефон), "", ", "+Телефон);			
			Строка.ВРаспоряжение = ОрганизацияПолноеНаименование;
			Строка.АдресПодачи = АдресПодачи;
		КонецЕсли;	
	
	КонецЦикла;	
	
	тбДанныеВодитель1 = ПакетРезультатовЗапроса[3].Выгрузить();
	тбДанныеВодитель2 = ПакетРезультатовЗапроса[4].Выгрузить();
	тбДанныеГСМ = ПакетРезультатовЗапроса[5].Выгрузить();
	тбДанныеПрицепа = ПакетРезультатовЗапроса[7].Выгрузить();
	тбДанныеКатегории = ПакетРезультатовЗапроса[8].Выгрузить();
	тбДанныеВремяПоГрафику = ПакетРезультатовЗапроса[9].Выгрузить();
	тбДанныеГаражи = ПакетРезультатовЗапроса[10].Выгрузить();
		
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Шапка", тбДанныеШапка);
	СтруктураДанныхДляПечати.Вставить("Водитель1", тбДанныеВодитель1);
	СтруктураДанныхДляПечати.Вставить("Водитель2", тбДанныеВодитель2);
	СтруктураДанныхДляПечати.Вставить("ГСМ", тбДанныеГСМ);
	СтруктураДанныхДляПечати.Вставить("Маршрут", дрвМаршрут);
	СтруктураДанныхДляПечати.Вставить("Прицеп", тбДанныеПрицепа);
	СтруктураДанныхДляПечати.Вставить("Категории", тбДанныеКатегории);
	СтруктураДанныхДляПечати.Вставить("ВремяПоГрафику", тбДанныеВремяПоГрафику);
	СтруктураДанныхДляПечати.Вставить("Гаражи", тбДанныеГаражи);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

#КонецОбласти

#КонецЕсли