
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		Если ДанныеЗаполнения.Количество() > 0 Тогда
			
			ПервыйЭлементМассива = ДанныеЗаполнения[0];
			
			Если ТипЗнч(ПервыйЭлементМассива) = Тип("ДокументСсылка.упРейс") Тогда
			
				Для каждого ЭлементРейс Из ДанныеЗаполнения Цикл
					НоваяСтрока = Рейсы.Добавить();
					НоваяСтрока.Рейс = ЭлементРейс;
				КонецЦикла;
				
				Рейс = ПервыйЭлементМассива;
				
				Если НЕ ЗначениеЗаполнено(ТранспортноеСредство) Тогда
					сткЭкипаж = упРейс.ПолучитьЭкипажРейса(Рейс);
					ТранспортноеСредство = сткЭкипаж.ТранспортноеСредство;  	
				КонецЕсли;
				
				сткРеквизиты	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, "Организация, Подразделение, ВидТранспортнойУслуги, ПорядокИсполненияЗаявки");
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткРеквизиты);
				Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
					ЧасовойПояс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ЧасовойПояс");;	
				КонецЕсли;  
				
				Если Не ЗначениеЗаполнено(Гараж) Тогда
					ПервыйРейс = ПервыйРейс(ДанныеЗаполнения);
					Гараж = Документы.упРейс.ПолучитьГаражВыезда(ПервыйРейс);
					Если Не ЗначениеЗаполнено(Гараж) Тогда
						Гараж = упРейс.ГаражПоТочкеОтправленияРейса(ПервыйРейс, ТранспортноеСредство);
					КонецЕсли; 
				КонецЕсли; 
				
			ИначеЕсли ТипЗнч(ПервыйЭлементМассива) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
								
				ЗаявкаНаПеревозкуГруза = ПервыйЭлементМассива;
				сткРеквизиты	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаявкаНаПеревозкуГруза, "Организация, Подразделение, ВидТранспортнойУслуги, ВидПеревозки, ПорядокИсполненияЗаявки");
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткРеквизиты);
				ДополнительныеСвойства.Вставить("ВидПеревозки", сткРеквизиты.ВидПеревозки);
				
				мсвМаршрут = Новый Массив;
				мсвМаршрут = упУправленияЗаявками.МаршрутСледованияПоЗаявкамНаПеревозкуГруза(ДанныеЗаполнения);
				ДополнительныеСвойства.Вставить("Маршрут", мсвМаршрут);

				Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
					ЧасовойПояс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаНаПеревозкуГруза, "ЧасовойПояс");;	
				КонецЕсли;  
											
				Для каждого ЭлементЗаявка Из ДанныеЗаполнения Цикл
					
					// Заявки.
					НоваяСтрока = Заявки.Добавить();
					НоваяСтрока.ЗаявкаНаПеревозкуГруза = ЭлементЗаявка;
					НоваяСтрока.Количество = 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда	
		ТранспортноеСредство = ДанныеЗаполнения;
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТранспортноеСредство, "Партнер.Организация, ОсновнойВодитель");
		Организация  = сткРеквизиты.ПартнерОрганизация;
		ДополнительныеСвойства.Вставить("Водитель1", сткРеквизиты.ОсновнойВодитель);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ЗаполнитьГСМ();
	
	ЗаполнитьНормыРасходаГСМ();
	
	// Заполняются сотрудники
	Если ЗначениеЗаполнено(Гараж) Тогда
		сткСотрудники = упПутевойЛистУчетГСМ.СотрудникиПоПутевомуЛисту(Гараж);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткСотрудники);
	КонецЕсли;
	
	ЗаполнитьПробег();
	
	ЗаполнитьДополнительноеОборудование();
	
	ЗаполнитьГСМДополнительноеОборудование();
	
	ЗаполнитьРежимыРаботыДополнительноеОборудование();
	
	ЗаполнитьНормыРасходаГСМДополнительноеоборудование();
	
	ЗаполнитьМассуПрицепногоСостава();	
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Статус = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("Статус", Статус) Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ИспользуетсяУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	
	// Если используется только компанента "Управление транспортом",
	// тогда данные по рейсу вводятся в путевом листе.
	Если Истина
		И Не Отказ
		И (Ложь
		   Или Не ИспользуетсяУправлениеПеревозками 
		   Или ПорядокИсполненияЗаявки = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом)
	Тогда
		ВидПеревозки = Неопределено;
		Маршрут = Неопределено;
		ВидПеревозки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ВидПеревозки");
		Маршрут = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Маршрут");
		Водитель1 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Водитель1");
		ПередатьДанныеВРейс(ВидПеревозки, Маршрут, Водитель1, Отказ);
	КонецЕсли;
	
	// Если заполнено поле "Рейс" и не заполнена табличная часть "Рейсы".
	Если Истина
		И ЗначениеЗаполнено(Рейс) 
		И Рейсы.Количество() = 0 
	Тогда
		НоваяСтрока = Рейсы.Добавить();
		НоваяСтрока.Рейс = Рейс;
	КонецЕсли;
	
	Если Истина
		И (Ложь
		   Или НЕ ЗначениеЗаполнено(ВидТранспортнойУслуги)
		   Или НЕ ЗначениеЗаполнено(ПорядокИсполненияЗаявки))
		И (Ложь
		   Или Рейсы.Количество() > 0 
		   Или Заявки.Количество() > 0)
	Тогда
		Если Заявки.Количество() Тогда
			СсылкаНаДокументОснование = Заявки[0].ЗаявкаНаПеревозкуГруза;
		Иначе
			СсылкаНаДокументОснование = Рейсы[0].Рейс;
		КонецЕсли;
		сткРеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокументОснование, "ВидПеревозки.ВидТранспортнойУслуги, ВидПеревозки.ПорядокИсполненияЗаявки");	
		ВидТранспортнойУслуги = сткРеквизитыОбъекта.ВидПеревозкиВидТранспортнойУслуги;
		ПорядокИсполненияЗаявки = сткРеквизитыОбъекта.ВидПеревозкиПорядокИсполненияЗаявки;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПорядокИсполненияЗаявки) Тогда
		ПорядокИсполненияЗаявки = Перечисления.упПорядокИсполненияЗаявок.Рейсом;
	КонецЕсли;
	          	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверитьНаличиеПутевыхЛистовПоРейсу(Отказ);
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования") Тогда
			ПроверитьУстановкуДопОборудованияНаТС(ЭтотОбъект, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ПериодДвиженияГСМ", ПериодДвиженияГСМ());
	
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.Запись  
	Тогда
		
		// Формируется таблица ГСМ по расходу транспортного средства и доп. оборудования.
		тбзГСМ = ГСМ.Выгрузить();
		тбзГСМ.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства, СправочникСсылка.упДополнительноеОборудование"));
				
		Для каждого СтрокаТЧ Из тбзГСМ Цикл
			СтрокаТЧ.ТранспортноеСредство = ?(ЗначениеЗаполнено(СтрокаТЧ.ДопОборудование), СтрокаТЧ.ДопОборудование, ТранспортноеСредство);
		КонецЦикла;
		
		Для каждого Строка Из ДополнительноеОборудование Цикл
			НоваяСтрока = тбзГСМ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ТранспортноеСредство = Строка.ДополнительноеОборудование;
		КонецЦикла;
		
		ДополнительныеСвойства.Вставить("тбзГСМ", тбзГСМ);
		ДатаПоследнегоДвиженияГСМ = Неопределено;
		
		// Проверка на наличие более поздних документов изменяющих остатки по ТС(доп. оборудованию) и виду ГСМ.
		Если Истина
			И Статус <> Перечисления.упСтатусыПутевогоЛиста.Закрыт 
			И НЕ РазрешеноПроведениеИлиОтменаПроведения(тбзГСМ, ДополнительныеСвойства.ПериодДвиженияГСМ, ДатаПоследнегоДвиженияГСМ) 
		Тогда
			Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
				ДатаПоследнегоДвиженияГСМ = ДатаПоследнегоДвиженияГСМ + 1;
				Дата = ДатаПоследнегоДвиженияГСМ ;
				ДополнительныеСвойства.Вставить("ПериодДвиженияГСМ", ПериодДвиженияГСМ());
			Иначе	
				ТекстОшибки = НСтр("ru = 'Существуют документы изменяющие остатки или пробег по ""%1"", проведенные позднее %2'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ТранспортноеСредство, Дата);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Ссылка,,,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// При отмене проведения очищаются все движения.
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения 
	Тогда
		Для каждого НаборЗаписей Из Движения Цикл
			НаборЗаписей.Очистить();	
			НаборЗаписей.Записать();	
		КонецЦикла;
	КонецЕсли;

	// Проведение документа
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи = РежимЗаписиДокумента.Проведение 
	Тогда
			
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		
		ИспользуетсяУчетДополнительногоОборудования = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования");
		ДополнительныеСвойства.Вставить("ИспользуетсяУчетДополнительногоОборудования", ИспользуетсяУчетДополнительногоОборудования);
				
		// Блокировки устанавливаются чуть раньше чем обычно, 
		//  потому что при некоторых статусах приходится дозаполнять табл. часть ГСМ.
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаявкиКВыполнению");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Заявки;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Заявка", "ЗаявкаНаПеревозкуГруза");
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Заявки;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "ЗаявкаНаПеревозкуГруза");
				
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Рейсы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Рейс");
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыПутевыхЛистов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНаработкаОборудования");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительноеОборудование;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "ДополнительноеОборудование");
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упПробегТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
			
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиГСМ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = тбзГСМ;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТранспортноеСредство", "ТранспортноеСредство");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВидГСМ", 				"ВидГСМ");
		
		// Если статус "Возвращен", то заполняются данные документа по данным датчиков.
		Если Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДанныеДатчиков");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = тбзГСМ;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТранспортноеСредство", "ТранспортноеСредство");
		КонецЕсли;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Рейсы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Рейс", "Рейс");
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеПараметрыРейса");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Рейсы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Рейс", "Рейс");
	
		Попытка
			БлокировкаДанных.Заблокировать();
		Исключение
			Отказ = Истина;
			Возврат;
		КонецПопытки;
		
		Если Статус <> Перечисления.упСтатусыПутевогоЛиста.Отменен Тогда
					
			// Очищаются записи регистров остатков.
			Движения.упОстаткиГСМ.Прочитать();
			Если Движения.упОстаткиГСМ.Количество() > 0 Тогда
				Движения.упОстаткиГСМ.Очистить();
				Движения.упОстаткиГСМ.Записать(Истина);
			КонецЕсли;
			
			Движения.упПробегТС.Прочитать();
			Если Движения.упПробегТС.Количество() > 0 Тогда
				Движения.упПробегТС.Очистить();
				Движения.упПробегТС.Записать(Истина);
			КонецЕсли;
			
			Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования") Тогда
				Движения.упНаработкаОборудования.Прочитать();
				Если Движения.упНаработкаОборудования.Количество() > 0 Тогда
					Движения.упНаработкаОборудования.Очистить();
					Движения.упНаработкаОборудования.Записать(Истина);
				КонецЕсли;
			КонецЕсли;
			
			Движения.упВыработкаТС.Прочитать();
			Если Движения.упВыработкаТС.Количество() > 0 Тогда
				Движения.упВыработкаТС.Очистить();
				Движения.упВыработкаТС.Записать(Истина);
			КонецЕсли;
						
			ПроверитьФактическийРасход = Ложь;
			
			Если Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда
				cткРеквизитыТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТранспортноеСредство, "АвтоматическиОпределятьКонечныйПробегПоДаннымДатчиков, АвтоматическиОпределятьКонечныйОстатокТопливаПоДаннымДатчиков");
				Если cткРеквизитыТС.АвтоматическиОпределятьКонечныйПробегПоДаннымДатчиков Тогда 
					Документы.упПутевойЛист.ЗаполнитьДаннымиДатчиковПробега(ЭтотОбъект);
				КонецЕсли;	
				Если cткРеквизитыТС.АвтоматическиОпределятьКонечныйОстатокТопливаПоДаннымДатчиков Тогда 
					Документы.упПутевойЛист.ЗаполнитьДаннымиДатчиковУровняТоплива(ЭтотОбъект);
				КонецЕсли;
			ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Закрыт Тогда
				ПроверитьФактическийРасход = Истина;
			КонецЕсли;	
						
			ПересчитатьФактическийРасходТоплива(Отказ, ПроверитьФактическийРасход);				
			
			Если НЕ Отказ Тогда	
				
				ЗаполнитьТаблицуДвиженийГСМ();
				
				Если Истина
					И Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан 
					И НЕ ЗначениеЗаполнено(НачалоРейса) 
				Тогда
					НачалоРейса = ТекущаяДатаСеанса();
				КонецЕсли;
				
				Если Статус = Перечисления.упСтатусыПутевогоЛиста.Выдан Тогда
					РассчитатьПлановыйРасходТоплива();
				ИначеЕсли Статус = Перечисления.упСтатусыПутевогоЛиста.Возвращен Тогда
					РассчитатьНормативныйРасходТоплива();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИспользуютсяЛимитыРасходаГСМ = ПолучитьФункциональнуюОпцию("упИспользуютсяЛимитыРасходаГСМ");
	
	Если ИспользуютсяЛимитыРасходаГСМ И ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЛимитыРасходаГСМ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;  
	КонецЕсли;
		
	ГраницаИсключая = Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая);
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата", Дата);
	РеквизитыОбъекта.Вставить("ГраницаИсключая", ГраницаИсключая);
	РеквизитыОбъекта.Вставить("Ссылка", Ссылка);
	РеквизитыОбъекта.Вставить("Пробег", Пробег);
	РеквизитыОбъекта.Вставить("Моточасы", Моточасы);
	РеквизитыОбъекта.Вставить("ВидДвижения", ВидДвиженияНакопления.Приход);
	РеквизитыОбъекта.Вставить("Сумма", 0);
	
	РеквизитыОбъекта.Вставить("ПробегВыработка", Пробег);
	РеквизитыОбъекта.Вставить("МоточасыВыработка", Моточасы);
	РеквизитыОбъекта.Вставить("ВидДвиженияВыработка", ВидДвиженияНакопления.Расход);
	
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", ТранспортноеСредство);
	РеквизитыОбъекта.Вставить("Рейсы", Рейсы);
	
	РеквизитыОбъекта.Вставить("ИспользуютсяЛимитыРасходаГСМ", ИспользуютсяЛимитыРасходаГСМ);
	
	РеквизитыОбъекта.Вставить("Организация", Организация);
	РеквизитыОбъекта.Вставить("Подразделение", Подразделение);
	РеквизитыОбъекта.Вставить("НаправлениеДеятельности", Справочники.упНаправленияДеятельности.ПустаяСсылка());
	РеквизитыОбъекта.Вставить("ДополнительноеОборудование", ДополнительноеОборудование);
	РеквизитыОбъекта.Вставить("Заявки", Заявки);
	ДополнительныеСвойства.Вставить("ПорядокИсполненияЗаявки", ПорядокИсполненияЗаявки);
	
	ДополнительныеСвойства.Вставить("АнализироватьОстатокВыработки", Истина);
	ДополнительныеСвойства.Вставить("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	мсвЗаявки = Заявки.ВыгрузитьКолонку("ЗаявкаНаПеревозкуГруза");
	ДополнительныеСвойства.Вставить("мсвЗаявки", мсвЗаявки);
	ДополнительныеСвойства.Вставить("КонтролироватьЗаявки", Ложь);										
	//ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗаявки", Истина);
	
	// Установить статус
	упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
	ПроверитьЗаявкиВыполняютсяТСОтличнымиОтТекущего(ТранспортноеСредство, Отказ);
	ПроверитьНаличиеПутевыхЛистовПоТСРейса(ДополнительныеСвойства, Отказ);
	
	упПутевойЛистУчетГСМ.СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Установить пробег
	упПутевойЛистУчетГСМ.СформироватьДвижениеПробег(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);	
	
	// Установить остатки ГСМ
	упПутевойЛистУчетГСМ.СформироватьДвижениеОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Установить статус
	упПутевойЛистУчетГСМ.СформироватьДвиженияСтатусПутевогоЛиста(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	РеквизитыОбъекта.Вставить("ГСМ", ГСМ); 
	РеквизитыОбъекта.Вставить("ГСМДополнительноеОборудование", ГСМДополнительноеОборудование); 
	РеквизитыОбъекта.Вставить("ЗаправкиСливыГСМ", ЗаправкиСливыГСМ);
	РеквизитыОбъекта.Вставить("ЗаправкиСливыГСМДополнительноеОборудование", ЗаправкиСливыГСМДополнительноеОборудование);
	
	// Формируется выработка по агрегатам
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетШинУзловИАгрегатов") Тогда
		упВыработкаРесурсов.РассчитатьВыработкуАгрегатов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		упВыработкаРесурсов.СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
	// Списывается пробег и моточасы в выработке.
	упУчетТранспортныхСредств.СформироватьДвижениеВыработкаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются движения по сливам ГСМ
	упПутевойЛистУчетГСМ.СформироватьДвиженияСливыГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Заполняются таблицы расходов.
	упРаспределениеРасходов.ЗаполнитьДоходыРасходы_ПутевойЛист(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются прямые фактические и плановые расходы.
	РеквизитыОбъекта.Вставить("Дата", ДополнительныеСвойства.ПериодДвиженияГСМ);
	упПутевойЛистУчетГСМ.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются фактический параметр "Пробег" по рейсу
	РеквизитыОбъекта.Вставить("Дата", Дата);
	упПутевойЛистУчетГСМ.СформироватьПробегПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	Если ДополнительныеСвойства.КонтролироватьЗаявки Тогда
		упУправленияЗаявками.ПроконтролироватьЗаявкиКВыполнению(мсвЗаявки, Отказ, Ссылка);
	КонецЕсли;
	
	Если Заявки.Количество() > 0 Тогда                                                                                              		
		
		// Устанавливаются статусы заявок.
		упУправленияЗаявками.СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	КонецЕсли;
	
	упПутевойЛистУчетГСМ.ПроконтролироватьПереливБака(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Движения.упОстаткиГСМ.Очистить();
	Движения.упОстаткиГСМ.Записать();
	Движения.упПробегТС.Очистить();
	Движения.упПробегТС.Записать();
	Движения.упРасходы.Очистить();
	Движения.упРасходы.Записать();
	Движения.упОстаткиМатериалов.Очистить();
	Движения.упОстаткиМатериалов.Записать();
	Если Рейсы.Количество() > 0 Тогда
		Для каждого СтрокаРейс Из Рейсы Цикл
			упРейс.УстановитьФактическиеПараметрыРейса(Новый Структура("Пробег", 0), СтрокаРейс.Рейс);			
		КонецЦикла;
	КонецЕсли;
	
	Движения.упВыработкаАгрегатов.Очистить();
	Движения.упВыработкаАгрегатов.Записать();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// При использовании только компоненты "Управление транспортом" рейс создается перед записью программно
	Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда	
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Рейс"));
	КонецЕсли;
	
	Если ПорядокИсполненияЗаявки <> Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Заявки"));
	КонецЕсли;
	
	мсвРейсы = Новый Массив;
	Для каждого СтрокаРейс Из Рейсы Цикл
		
		Если мсвРейсы.Найти(СтрокаРейс.Рейс) = Неопределено Тогда
			мсвРейсы.Добавить(СтрокаРейс.Рейс);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Рейс уже есть в списке.'"), ЭтотОбъект, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Рейсы", СтрокаРейс.НомерСтроки, "Рейс"),, Отказ);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	НомерСтроки = 1;
	Для каждого СтрокаГСМ Из ЗаправкиСливыГСМ Цикл
		
		Если НЕ Отказ И СтрокаГСМ.Количество < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Количество"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЗаправкиСливыГСМ", НомерСтроки, "Количество"),,Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;

 КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьГСМ() Экспорт
	
	// Заполняются строки по транспортному средству
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		 Период = ?(ЗначениеЗаполнено(НачалоРейса), НачалоРейса, Дата);
		 тбзГСМ = упПутевойЛистУчетГСМ.УровеньТопливаТСПоВсемВидамГСМ(ТранспортноеСредство, Период, Организация, Подразделение, НачалоРейса, ОкончаниеРейса);
		 мсвВидыГСМ =  Новый Массив;
		 Для каждого СтрокаГСМ Из тбзГСМ Цикл
			 
			 Если мсвВидыГСМ.Найти(СтрокаГСМ.ВидГСМ) = Неопределено Тогда
			 	мсвВидыГСМ.Добавить(СтрокаГСМ.ВидГСМ);
			 Иначе	
				Продолжить;
			 КонецЕсли;
			 
			 мсвСтрокПоГСМ = ГСМ.НайтиСтроки(Новый Структура("ВидГСМ, ДопОборудование", СтрокаГСМ.ВидГСМ, Справочники.упДополнительноеОборудование.ПустаяСсылка()));
			 
			 Если мсвСтрокПоГСМ.Количество() > 0 Тогда
				НоваяСтрока = мсвСтрокПоГСМ[0]; 
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаГСМ); 
				НоваяСтрока.НачОстаток 		= СтрокаГСМ.УровеньТоплива;
				НоваяСтрока.КонОстаток 		= 0; 
			 Иначе	 
				НоваяСтрока = ГСМ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаГСМ);
				НоваяСтрока.НачОстаток 		= СтрокаГСМ.УровеньТоплива;
				НоваяСтрока.КонОстаток 		= 0; 
			 КонецЕсли;
		 КонецЦикла;
	КонецЕсли;
	
	// Заполняются строки по дополнительному оборудованию
	Для каждого СтрокаГСМ Из ГСМ Цикл
		Если ЗначениеЗаполнено(СтрокаГСМ.ДопОборудование) Тогда
			текДопОборудование  = СтрокаГСМ.ДопОборудование;
			текВидГСМ			= СтрокаГСМ.ВидГСМ;
			текУровеньТоплива	= упПутевойЛистУчетГСМ.УровеньТоплива(текДопОборудование, текВидГСМ);
			СтрокаГСМ.НачОстаток= текУровеньТоплива;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - Заполнить дополнительное оборудование
//
Процедура ЗаполнитьДополнительноеОборудование() Экспорт
	
	ДополнительноеОборудование.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	ДопОборудование.Агрегат КАК ДополнительноеОборудование,
	|	ПробегТС_ОстаткиТ.МоточасыОстаток КАК МоточасыНачало,
	|	&ТранспортноеСредство КАК ТранспортноеСредство,
	|	ДопОборудование.Агрегат.ИспользуютсяРазличныеРежимыРаботы КАК ИспользуютсяРазличныеРежимыРаботы
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		) КАК ДопОборудование
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упНаработкаОборудования.Остатки(
	|		,
	|		) КАК ПробегТС_ОстаткиТ
	|	ПО ДопОборудование.Агрегат = ПробегТС_ОстаткиТ.Оборудование
	|ГДЕ ИСТИНА
	|	И ДопОборудование.ТранспортноеСредство = &ТранспортноеСредство
	|	И ДопОборудование.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И ТИПЗНАЧЕНИЯ(ДопОборудование.Агрегат) = ТИП(Справочник.упДополнительноеОборудование)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ДопОборудование.Агрегат КАК ДополнительноеОборудование,
	|	ПробегТС_ОстаткиТ.МоточасыОстаток КАК МоточасыНачало,
	|	Прицепы.Прицеп КАК ТранспортноеСредство,
	|	ДопОборудование.Агрегат.ИспользуютсяРазличныеРежимыРаботы КАК ИспользуютсяРазличныеРежимыРаботы
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		ТранспортноеСредство = &ТранспортноеСредство) КАК Прицепы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		) КАК ДопОборудование
	|	ПО Прицепы.Прицеп = ДопОборудование.ТранспортноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упПробегТС.Остатки(
	|		,
	|		) КАК ПробегТС_ОстаткиТ
	|	ПО ДопОборудование.Агрегат = ПробегТС_ОстаткиТ.ТранспортноеСредство
	|ГДЕ ИСТИНА
	|	И ДопОборудование.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И Прицепы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И ТИПЗНАЧЕНИЯ(ДопОборудование.Агрегат) = ТИП(Справочник.упДополнительноеОборудование)
	|";


	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(НачалоРейса),НачалоРейса, Дата));
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ДополнительноеОборудование.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
		
КонецПроцедуры

// Процедура - Заполнить ГСМДополнительное оборудование.
//
Процедура ЗаполнитьГСМДополнительноеОборудование() Экспорт
	
	ГСМДополнительноеОборудование.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{: Текст собран конструктором из подсистемы ""Инструменты разработчика"" (http://devtool1c.ucoz.ru), который сохраняет звездочки и комментарии.
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДополнительноеОборудованиеВидыГСМ.Ссылка КАК ДополнительноеОборудование,
	|	упДополнительноеОборудованиеВидыГСМ.ВидГСМ КАК ВидГСМ
	|ПОМЕСТИТЬ втВидыГСМПоДопОборудованию
	|ИЗ
	|	Справочник.упДополнительноеОборудование.ВидыГСМ КАК упДополнительноеОборудованиеВидыГСМ
	|ГДЕ упДополнительноеОборудованиеВидыГСМ.Ссылка В  (&ДополнительноеОборудование)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДатчики.Ссылка КАК Датчик,
	|	втВидыГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втВидыГСМ.ВидГСМ КАК ВидГСМ
	|ПОМЕСТИТЬ втДатчики
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыГСМПоДопОборудованию КАК втВидыГСМ
	|	ПО ИСТИНА
	|		И упДатчики.ВидГСМ = втВидыГСМ.ВидГСМ
	|		И упДатчики.Назначение = ЗНАЧЕНИЕ(Справочник.упНазначенияДатчиков.Топливо)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВидыГСМТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втВидыГСМТ.ВидГСМ КАК ВидГСМ,
	|	ЕСТЬNULL(УпДанныеДатчиков_СрезПоследнихТ.Значение, упОстаткиГСМОстатки.КоличествоОстаток) КАК НачОстаток
	|ПОМЕСТИТЬ втВидыГСМ
	|ИЗ
	|	втВидыГСМПоДопОборудованию КАК втВидыГСМТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втДатчики КАК втДатчики
	|	ПО ИСТИНА
	|		И втВидыГСМТ.ВидГСМ = втДатчики.ВидГСМ
	|		И втВидыГСМТ.ДополнительноеОборудование = втДатчики.ДополнительноеОборудование
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|		,
	|		(ТранспортноеСредство, Датчик) В (
	|				ВЫБРАТЬ
	|				втДатчики.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|				втДатчики.Датчик КАК Датчик
	|			ИЗ
	|				втДатчики КАК втДатчики)) КАК УпДанныеДатчиков_СрезПоследнихТ
	|	ПО ИСТИНА
	|		И втДатчики.ДополнительноеОборудование = УпДанныеДатчиков_СрезПоследнихТ.ТранспортноеСредство
	|		И втДатчики.Датчик = УпДанныеДатчиков_СрезПоследнихТ.Датчик
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упОстаткиГСМ.Остатки(
	|		,
	|		(ТранспортноеСредство, ВидГСМ) В (
	|				ВЫБРАТЬ
	|				втВидыГСМ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|				втВидыГСМ.ВидГСМ КАК ВидГСМ
	|			ИЗ
	|				втВидыГСМПоДопОборудованию КАК втВидыГСМ)) КАК упОстаткиГСМОстатки
	|	ПО ИСТИНА
	|		И втВидыГСМТ.ДополнительноеОборудование = упОстаткиГСМОстатки.ТранспортноеСредство
	|		И втВидыГСМТ.ВидГСМ = упОстаткиГСМОстатки.ВидГСМ
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВидыГСМТ.ВидГСМ КАК ВидГСМ,
	|	втВидыГСМТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втВидыГСМТ.НачОстаток КАК НачОстаток,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.Расход КАК Расход,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ФормулаРасчета КАК ФормулаРасчета,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.РасходИзСобственногоБака КАК РасходИзСобственногоБака
	|ПОМЕСТИТЬ втНормыРасхода
	|ИЗ
	|	втВидыГСМ КАК втВидыГСМТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|			И Подразделение = &Подразделение) КАК УпНормыРасходаГСМ_СрезПоследнихТ
	|	ПО ИСТИНА
	|		И УпНормыРасходаГСМ_СрезПоследнихТ.ВидГСМ = втВидыГСМТ.ВидГСМ
	|		И УпНормыРасходаГСМ_СрезПоследнихТ.ОбъектНормРасхода = втВидыГСМТ.ДополнительноеОборудование
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНормыРасхода.ВидГСМ КАК ВидГСМ,
	|	втНормыРасхода.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втНормыРасхода.НачОстаток КАК НачОстаток,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.Расход КАК Расход,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ФормулаРасчета КАК ФормулаРасчета,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	УпНормыРасходаГСМ_СрезПоследнихТ.РасходИзСобственногоБака КАК РасходИзСобственногоБака
	|ИЗ
	|	втНормыРасхода КАК втНормыРасхода
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|			И Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)) КАК УпНормыРасходаГСМ_СрезПоследнихТ
	|	ПО ИСТИНА
	|		И УпНормыРасходаГСМ_СрезПоследнихТ.ВидГСМ = втНормыРасхода.ВидГСМ
	|		И УпНормыРасходаГСМ_СрезПоследнихТ.ОбъектНормРасхода = втНормыРасхода.ДополнительноеОборудование
	|ГДЕ втНормыРасхода.ТипНормыРасходаГСМ ЕСТЬ NULL
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втНормыРасхода.ВидГСМ КАК ВидГСМ,
	|	втНормыРасхода.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втНормыРасхода.НачОстаток КАК НачОстаток,
	|	втНормыРасхода.Расход КАК Расход,
	|	втНормыРасхода.ТипНормыРасходаГСМ КАК ТипНормыРасходаГСМ,
	|	втНормыРасхода.ФормулаРасчета КАК ФормулаРасчета,
	|	втНормыРасхода.ШкалаТемпературныхКоэффициентов КАК ШкалаТемпературныхКоэффициентов,
	|	втНормыРасхода.РасходИзСобственногоБака КАК РасходИзСобственногоБака
	|ИЗ
	|	втНормыРасхода КАК втНормыРасхода
	|ГДЕ НЕ (втНормыРасхода.ТипНормыРасходаГСМ ЕСТЬ NULL)
	|";


	Запрос.УстановитьПараметр("ДополнительноеОборудование", ДополнительноеОборудование.ВыгрузитьКолонку("ДополнительноеОборудование"));
	Запрос.УстановитьПараметр("Период", 		НачалоРейсов());
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Подразделение", 	Подразделение);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ГСМДополнительноеОборудование.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
		
КонецПроцедуры

// Процедура - Заполнить режимы работы дополнительное оборудование
//
Процедура ЗаполнитьРежимыРаботыДополнительноеОборудование() Экспорт
	
	НормыРасходаРежимовРаботы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{: Текст собран конструктором из подсистемы ""Инструменты разработчика"" (http://devtool1c.ucoz.ru), который сохраняет звездочки и комментарии.
	|//{Запрос: 0 ////////////////////////////////////////
	|// Все режимы работы по доп. оборудованию
	|ВЫБРАТЬ
	|	РежимыРаботы.Владелец КАК ДопОборудование,
	|	РежимыРаботы.Ссылка КАК РежимРаботы
	|ПОМЕСТИТЬ втДопОборудование
	|ИЗ
	|	Справочник.упРежимыРаботыОборудования КАК РежимыРаботы
	|ГДЕ РежимыРаботы.Владелец В  (&ДополнительноеОборудование)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Расход по режимам работы доп. оборудования с точностью до подразделения из путевого листа.
	|ВЫБРАТЬ
	|	втДопОборудованиеТ.ДопОборудование КАК ДополнительноеОборудование,
	|	втДопОборудованиеТ.РежимРаботы КАК РежимРаботы,
	|	ЕСТЬNULL(РежимыРаботы.Расход, 0) КАК Расход,
	|	РежимыРаботы.РежимРаботыОборудования КАК РежимРаботыПоПодразделению
	|ПОМЕСТИТЬ втРежимыРаботыПоПодразделению
	|ИЗ
	|	втДопОборудование КАК втДопОборудованиеТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаРежимовРаботы.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|			И Подразделение = &Подразделение) КАК РежимыРаботы
	|	ПО ИСТИНА
	|		И РежимыРаботы.ОбъектНормРасхода = втДопОборудованиеТ.ДопОборудование
	|		И РежимыРаботы.РежимРаботыОборудования = втДопОборудованиеТ.РежимРаботы
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// 1. Расход по режимам работы доп. оборудования с точностью до подразделения из путевого листа.
	|ВЫБРАТЬ
	|	втРежимыРаботыПоПодразделению.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втРежимыРаботыПоПодразделению.РежимРаботы КАК РежимРаботы,
	|	втРежимыРаботыПоПодразделению.Расход КАК Расход
	|ПОМЕСТИТЬ втВсеРежимыПоДопОборудованию
	|ИЗ
	|	втРежимыРаботыПоПодразделению КАК втРежимыРаботыПоПодразделению
	|ГДЕ НЕ (втРежимыРаботыПоПодразделению.РежимРаботыПоПодразделению ЕСТЬ NULL)
	|ОБЪЕДИНИТЬ ВСЕ
	|// 2. Расход по режимам работы доп. оборудования для которых не нашлось нормы по подразеделению,
	|//    если для них существует норма по пустому подразделению, то берется она.
	|ВЫБРАТЬ
	|	втДопОборудование.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	втДопОборудование.РежимРаботы КАК РежимРаботы,
	|	ЕСТЬNULL(РежимыРаботы.Расход, 0) КАК Расход
	|ИЗ
	|	втРежимыРаботыПоПодразделению КАК втДопОборудование
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаРежимовРаботы.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|			И Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)) КАК РежимыРаботы
	|	ПО ИСТИНА
	|		И втДопОборудование.ДополнительноеОборудование = РежимыРаботы.ОбъектНормРасхода
	|		И втДопОборудование.РежимРаботы = РежимыРаботы.РежимРаботыОборудования
	|ГДЕ втДопОборудование.РежимРаботыПоПодразделению ЕСТЬ NULL
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|// Так как для разных видов ГСМ по доп. оборудованию есть возможность задать один и тот же режим(хотя это и не корректно),
	|// то расход по этому режиму суммируется.
	|ВЫБРАТЬ
	|	втВсеРежимыПоДопОборудованиюТ.ДополнительноеОборудование КАК ДополнительноеОборудование,
	|	СУММА(втВсеРежимыПоДопОборудованиюТ.Расход) КАК Расход,
	|	втВсеРежимыПоДопОборудованиюТ.РежимРаботы КАК РежимРаботы
	|ИЗ
	|	втВсеРежимыПоДопОборудованию КАК втВсеРежимыПоДопОборудованиюТ
	|СГРУППИРОВАТЬ ПО
	|	втВсеРежимыПоДопОборудованиюТ.ДополнительноеОборудование,
	|	втВсеРежимыПоДопОборудованиюТ.РежимРаботы
	|";

	Запрос.УстановитьПараметр("ДополнительноеОборудование", 	ДополнительноеОборудование.ВыгрузитьКолонку("ДополнительноеОборудование"));
	Запрос.УстановитьПараметр("Период", 					НачалоРейсов());
	Запрос.УстановитьПараметр("Организация", 				Организация);
	Запрос.УстановитьПараметр("Подразделение",				Подразделение);
	
	НормыРасходаРежимовРаботы.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

// Процедура - Заполнить надбавки ГСМДополнительноеоборудование
//
Процедура ЗаполнитьНормыРасходаГСМДополнительноеоборудование() Экспорт
	
	Если Истина
		И ЗначениеЗаполнено(Организация)
	Тогда
		Период 	= НачалоРейсов();
		тбзГСМ	= ГСМДополнительноеОборудование.Выгрузить(, "ВидГСМ, ДополнительноеОборудование");
		тбзНадбавкиПоНормам = упПутевойЛистУчетГСМ.НадбавкиНормыРасходаГСМПоДопОборудованию(Период, Организация, Подразделение, тбзГСМ);
		
		мсвНеАктуальныеСтроки	= Новый Массив();
		
		// Проводится поиск строк, которые перестали быть актуальными.
		Для каждого Строка Из НормыРасходаГСМДополнительноеОборудование Цикл
			сткПоиска = Новый Структура("ДополнительноеОборудование, ВидГСМ, ВидЭксплуатационнойНормы, ТипРасхода");
			ЗаполнитьЗначенияСвойств(сткПоиска, Строка);
			мсвСтрокиНормыРасхода = тбзНадбавкиПоНормам.НайтиСтроки(сткПоиска);
			Если мсвСтрокиНормыРасхода.Количество() = 0 Тогда
				мсвНеАктуальныеСтроки.Добавить(Строка);
			КонецЕсли;
		КонецЦикла;
		
		// Удаляются не актуальные строки.
		Для каждого Строка Из мсвНеАктуальныеСтроки Цикл
			НормыРасходаГСМДополнительноеОборудование.Удалить(Строка);
		КонецЦикла;
		
		// Добавляются новые нормы или обновляются расходы у существующих.
		// Порядок соответствует порядку надбавок в запросе.
		ДобавлятьНовыеСтроки = Ложь;
		Для каждого СтрокаНадбавкаПоНормам Из тбзНадбавкиПоНормам Цикл
			
			Если НЕ ДобавлятьНовыеСтроки Тогда
				ИндексСтроки 		 = тбзНадбавкиПоНормам.Индекс(СтрокаНадбавкаПоНормам);
				КоличествоСтрокНормы = НормыРасходаГСМДополнительноеОборудование.Количество();
				
				Если ИндексСтроки > КоличествоСтрокНормы - 1 Тогда
					ДобавлятьНовыеСтроки = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ДобавлятьНовыеСтроки Тогда
				НоваяСтрока = НормыРасходаГСМДополнительноеОборудование.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНадбавкаПоНормам);
			Иначе
				 			
				СтрокаНорма 	= НормыРасходаГСМДополнительноеОборудование.Получить(ИндексСтроки);
				
				Если Истина
					И СтрокаНорма.ВидГСМ = СтрокаНадбавкаПоНормам.ВидГСМ
					И СтрокаНорма.ВидЭксплуатационнойНормы = СтрокаНадбавкаПоНормам.ВидЭксплуатационнойНормы
					И СтрокаНорма.ТипРасхода = СтрокаНадбавкаПоНормам.ТипРасхода
					И СтрокаНорма.ДополнительноеОборудование = СтрокаНадбавкаПоНормам.ДополнительноеОборудование
				Тогда
					Если СтрокаНорма.Расход = 0 Тогда
						СтрокаНорма.Расход = СтрокаНадбавкаПоНормам.Расход;
					КонецЕсли;
				Иначе
					НоваяСтрока = НормыРасходаГСМДополнительноеОборудование.Вставить(ИндексСтроки);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНадбавкаПоНормам);
				КонецЕсли;

			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

// Процедура - Заполнить нормы расхода ГСМ
//
Процедура ЗаполнитьНормыРасходаГСМ() Экспорт
	
	Если Истина
		И ЗначениеЗаполнено(ТранспортноеСредство)
		И ЗначениеЗаполнено(Организация)
	Тогда
		Период = НачалоРейсов();
		тбзГСМ	= ГСМ.Выгрузить(, "ВидГСМ, НормаРасхода");
		тбзНадбавкиПоНормам = упПутевойЛистУчетГСМ.НадбавкиНормыРасходаГСМ(Период, Организация, Подразделение, ТранспортноеСредство, тбзГСМ);
		
		мсвНеАктуальныеСтроки	= Новый Массив();
		
		// Проводится поиск строк, которые перестали быть актуальными.
		Для каждого Строка Из НормыРасходаГСМ Цикл
			сткПоиска = Новый Структура("ВидГСМ, ВидЭксплуатационнойНормы, ТипРасхода");
			ЗаполнитьЗначенияСвойств(сткПоиска, Строка);
			мсвСтрокиНормыРасхода = тбзНадбавкиПоНормам.НайтиСтроки(сткПоиска);
			Если мсвСтрокиНормыРасхода.Количество() = 0 Тогда
				мсвНеАктуальныеСтроки.Добавить(Строка);
			КонецЕсли;
		КонецЦикла;
		
		// Удаляются не актуальные строки.
		Для каждого Строка Из мсвНеАктуальныеСтроки Цикл
			НормыРасходаГСМ.Удалить(Строка);
		КонецЦикла;
		
		// Добавляются новые нормы или обновляются расходы у существующих.
		// Порядок соответствует порядку надбавок в запросе.
		ДобавлятьНовыеСтроки = Ложь;
		Для каждого СтрокаНадбавкаПоНормам Из тбзНадбавкиПоНормам Цикл
			
			Если НЕ ДобавлятьНовыеСтроки Тогда
				ИндексСтроки 		 = тбзНадбавкиПоНормам.Индекс(СтрокаНадбавкаПоНормам);
				КоличествоСтрокНормы = НормыРасходаГСМ.Количество();
				
				Если ИндексСтроки > КоличествоСтрокНормы - 1 Тогда
					ДобавлятьНовыеСтроки = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ДобавлятьНовыеСтроки Тогда
				НоваяСтрока = НормыРасходаГСМ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНадбавкаПоНормам);
			Иначе
				 			
				СтрокаНорма 	= НормыРасходаГСМ.Получить(ИндексСтроки);
				
				Если Истина
					И СтрокаНорма.ВидГСМ = СтрокаНадбавкаПоНормам.ВидГСМ
					И СтрокаНорма.ВидЭксплуатационнойНормы = СтрокаНадбавкаПоНормам.ВидЭксплуатационнойНормы
					И СтрокаНорма.ТипРасхода = СтрокаНадбавкаПоНормам.ТипРасхода
					Тогда
					Если СтрокаНорма.Расход = 0 Тогда
						СтрокаНорма.Расход = СтрокаНадбавкаПоНормам.Расход;
					КонецЕсли;
				Иначе
					НоваяСтрока = НормыРасходаГСМ.Вставить(ИндексСтроки);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНадбавкаПоНормам);
				КонецЕсли;

			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;	
		
КонецПроцедуры

// Процедура - Заполнить пробег
//
Процедура ЗаполнитьПробег() Экспорт
		
	ПробегНачало 		= 0;
	ПробегОкончание	= 0;
	Пробег 			= 0;
	ОдометрНачало 		= 0;
	Одометр			= 0;
	ОдометрОкончание	= 0;
	МоточасыНачало		= 0;
	МоточасыОкончание	= 0;
	Моточасы			= 0;

	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		сткДанные 	= упПутевойЛистУчетГСМ.ПробегМоточасы(ТранспортноеСредство);
		ОдометрНачало	= сткДанные.Одометр;
		ПробегНачало 	= сткДанные.Пробег;
		МоточасыНачало	= сткДанные.Моточасы;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьМассуПрицепногоСостава()
	МассаПрицепногоСостава = упПутевойЛистУчетГСМ.МассаПрицепногоСоставаТранспортногоСредства(НачалоРейсов(), ТранспортноеСредство);		
КонецПроцедуры

// Производит расчет правил по объекту.
//
Процедура РассчитатьПлановыйРасходТоплива() Экспорт
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект);	
КонецПроцедуры

// Производит расчет правил по объекту.
//
Процедура РассчитатьНормативныйРасходТоплива() Экспорт
	сткВходныеПараметры = Новый Структура("РассчитатьНормативныйРасходТоплива",Истина);
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект, сткВходныеПараметры);	
КонецПроцедуры

Процедура РассчитатьНормативныйРасходТопливаДопОборудования() Экспорт
	сткВходныеПараметры = Новый Структура("РассчитатьНормативныйРасходТоплива, РассчитатьДополнительноеОборудование",Истина, Истина);
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект, сткВходныеПараметры);	
КонецПроцедуры

Функция РассчитатьПлановыйРасходТопливаСРасшифровкой() Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПутевомуЛисту");
	сткВходныеПараметры = Новый Структура();
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект, сткВходныеПараметры,сткТрассировка);	
	
	Возврат мсвПриемников;
	
КонецФункции

Функция РассчитатьНормативныйРасходТопливаСРасшифровкой() Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПутевомуЛисту");
	сткВходныеПараметры = Новый Структура("РассчитатьНормативныйРасходТоплива",Истина);
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект, сткВходныеПараметры,сткТрассировка);	
	
	Возврат мсвПриемников;
	
КонецФункции

Функция РассчитатьНормативныйРасходТопливаДопОборудованияСРасшифровкой() Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПутевомуЛисту");
	сткВходныеПараметры = Новый Структура("РассчитатьНормативныйРасходТоплива, РассчитатьДополнительноеОборудование",Истина, Истина);
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект, сткВходныеПараметры,сткТрассировка);	
	
	Возврат мсвПриемников;
	
КонецФункции

// Производит расчет конечного остатка по данным нормативного расхода.
//
Процедура РассчитатьКонечныйОстаток() Экспорт
	ПересчитатьНормативныйРасходТоплива = Ложь;
	Для каждого СтрокаГСМ  Из ГСМ Цикл
		Если СтрокаГСМ.РасходТопливаПоНорме = 0 Тогда
			ПересчитатьНормативныйРасходТоплива = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ПересчитатьНормативныйРасходТоплива Тогда
		РассчитатьНормативныйРасходТоплива();		
	КонецЕсли;
	ПересчитатьКонечныйОстатокТопливаПоДаннымНормативногоРасходаТоплива();
КонецПроцедуры

// Пересчет значений расхода топлива.
//
Процедура ПересчитатьФактическийРасходТоплива(Отказ, ПроверитьФактическийРасход)
	
	ПересчитатьФактическийРасходТопливаПоТЧ(Отказ, "ГСМ", ПроверитьФактическийРасход); 
	ПересчитатьФактическийРасходТопливаПоТЧ(Отказ, "ГСМДополнительноеОборудование", ПроверитьФактическийРасход);

КонецПроцедуры

// Процедура - Пересчитать фактический расход топлива по ТЧ
//
// Параметры:
//  ИмяТЧ	 - Строка	 - имя ТЧ гсм.
//
Процедура ПересчитатьФактическийРасходТопливаПоТЧ(Отказ, ИмяТЧ, ПроверитьФактическийРасход)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИспользуетсяУчетДополнительногоОборудования = Ложь;
	
	Если ИмяТЧ = "ГСМ" Тогда
		ТабличнаяЧасть 			= ГСМ;
		ТабличнаяЧастьЗаправки 		= ЗаправкиСливыГСМ;
		ИмяРеквизитаДопОборудование 	= "ДопОборудование";
		ИспользуетсяУчетДополнительногоОборудования = ДополнительныеСвойства.ИспользуетсяУчетДополнительногоОборудования;
	Иначе
		ТабличнаяЧасть = ГСМДополнительноеОборудование;	
		ТабличнаяЧастьЗаправки 	= ЗаправкиСливыГСМДополнительноеОборудование;
		ИмяРеквизитаДопОборудование 	= "ДополнительноеОборудование";
	КонецЕсли; 
	
	Для каждого СтрокаГСМ Из ТабличнаяЧасть Цикл
		
		Если СтрокаГСМ.КонОстаток < 0 Тогда
			ОписаниеОшибки = Нстр("ru = 'Конечный остаток не должен быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, ЭтотОбъект, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, СтрокаГСМ.НомерСтроки, "КонОстаток"),, Отказ);
			ЗаписьЖурналаРегистрации(СобытиеОшибкаПроведения(), УровеньЖурналаРегистрации.Предупреждение,, , ОписаниеОшибки);    
			Прервать;
		КонецЕсли;
		
		ДопОборудование 	 = СтрокаГСМ[ИмяРеквизитаДопОборудование];
		ВидГСМ			 = СтрокаГСМ.ВидГСМ;
		мсвСтрокЗаправкаСлив = ТабличнаяЧастьЗаправки.НайтиСтроки(Новый Структура(ИмяРеквизитаДопОборудование+", ВидГСМ",ДопОборудование, ВидГСМ));
		
		Заправка = 0;
		Слив	 = 0;
		
		Для каждого СтрокаЗаправкаСлив Из мсвСтрокЗаправкаСлив Цикл
			Если СтрокаЗаправкаСлив.ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка Тогда
				Заправка = Заправка + СтрокаЗаправкаСлив.Количество;
			ИначеЕсли СтрокаЗаправкаСлив.ВидДвижения = Перечисления.упВидыДвиженияТоплива.Слив Тогда
				Слив	 = Слив + СтрокаЗаправкаСлив.Количество;
			КонецЕсли;
		КонецЦикла;
		
		// Учитвается топливо, израсходованное на доп. оборудование.
		РасходНаДопОборудование = 0;
		Если Истина
			И ИспользуетсяУчетДополнительногоОборудования
			И ИмяТЧ = "ГСМ" Тогда
			
			мсвСтрокиДопОборудование = ГСМДополнительноеОборудование.НайтиСтроки(Новый Структура("ВидГСМ", ВидГСМ));	
			
			Для каждого СтрокаДопОборудование Из мсвСтрокиДопОборудование Цикл
				РасходНаДопОборудование = РасходНаДопОборудование + СтрокаДопОборудование.РасходТопливаСТС;
			КонецЦикла;
			
		КонецЕсли;
			
		СтрокаГСМ.РасходТопливаФакт = СтрокаГСМ.НачОстаток - СтрокаГСМ.КонОстаток + Заправка - Слив - РасходНаДопОборудование;
		
		Если СтрокаГСМ.РасходТопливаФакт < 0 И ПроверитьФактическийРасход Тогда
			
			Если ИспользуетсяУчетДополнительногоОборудования Тогда
				Формула = Нстр("ru = '(ФактРасход = НачОстаток - КонОстаток + Заправка - Слив - Доп. оборудование)'");
			Иначе
				Формула = Нстр("ru = '(ФактРасход = НачОстаток - КонОстаток + Заправка - Слив)'");
			КонецЕсли;
					
			ОписаниеОшибки = Нстр("ru = 'Фактический расход топлива отрицательный, проведение невозможно. 
										|Фактический расход = %5 %6
										|Начальный остаток = %1; Конечный остаток = %2; Заправка = %3; Слив = %4'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, СтрокаГСМ.НачОстаток, СтрокаГСМ.КонОстаток, Заправка, Слив, СтрокаГСМ.РасходТопливаФакт, Формула);
			Если ИспользуетсяУчетДополнительногоОборудования Тогда
				ОписаниеОшибки = ОписаниеОшибки + Нстр("ru = '; Расход на доп.оборудование = %1'");
				ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, РасходНаДопОборудование);
			КонецЕсли; 
			
			текИндексСтроки = СтрокаГСМ.НомерСтроки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, ЭтотОбъект,,ИмяТЧ+"["+текИндексСтроки+"]",Отказ);
			
			ЗаписьЖурналаРегистрации(СобытиеОшибкаПроведения(), УровеньЖурналаРегистрации.Предупреждение,, , ОписаниеОшибки);    
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

// Пересчет конечный остаток топлива по по данным нормативного расхода.
//
Процедура ПересчитатьКонечныйОстатокТопливаПоДаннымНормативногоРасходаТоплива()
	
	Для каждого СтрокаГСМ Из ГСМ Цикл
		
		ДопОборудование 		= СтрокаГСМ.ДопОборудование;
		ВидГСМ				= СтрокаГСМ.ВидГСМ;
		мсвСтрокЗаправкаСлив 	= ЗаправкиСливыГСМ.НайтиСтроки(Новый Структура("ДопОборудование, ВидГСМ",ДопОборудование, ВидГСМ));
		
		Заправка 	= 0;
		Слив	 	= 0;
		
		Для каждого СтрокаЗаправкаСлив Из мсвСтрокЗаправкаСлив Цикл
			Если СтрокаЗаправкаСлив.ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка Тогда
				Заправка = Заправка + СтрокаЗаправкаСлив.Количество;
			ИначеЕсли СтрокаЗаправкаСлив.ВидДвижения = Перечисления.упВидыДвиженияТоплива.Слив Тогда
				Слив	 = Слив + СтрокаЗаправкаСлив.Количество;
			КонецЕсли;
		КонецЦикла;
		
		СтрокаГСМ.РасходТопливаФакт = СтрокаГСМ.РасходТопливаПоНорме;
		
		СтрокаГСМ.КонОстаток = СтрокаГСМ.НачОстаток - СтрокаГСМ.РасходТопливаФакт + Заправка - Слив;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуДвиженийГСМ()
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ПериодДвиженияГСМ = ДополнительныеСвойства.ПериодДвиженияГСМ;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"
		|
		|//{Запрос: 0 ////////////////////////////////////////
		|// Расход ГСМ по транспортному средству.
		|ВЫБРАТЬ
		|	втГСМ.ВидГСМ КАК ВидГСМ,
		|	ВЫБОР
		|		КОГДА втГСМ.ДопОборудование = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
		|			ТОГДА &ТранспортноеСредство
		|		ИНАЧЕ втГСМ.ДопОборудование
		|	КОНЕЦ КАК ТранспортноеСредство,
		|	втГСМ.НачОстаток КАК НачОстаток,
		|	втГСМ.РасходТопливаФакт КАК РасходТопливаФакт,
		|	втГСМ.СуммаРасходов КАК СуммаРасходов,
		|	втГСМ.СуммаРасходовНДС КАК СуммаРасходовНДС,
		|	втГСМ.РасходТопливаПлан КАК РасходТопливаПлан,
		|	&ПериодДвиженияГСМ КАК Период
		|ПОМЕСТИТЬ втГСМ
		|ИЗ
		|	&тбГСМ КАК втГСМ
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Заправка/слив по транспортному средству.
		|ВЫБРАТЬ
		|	втЗаправкиСливыГСМ.ВидГСМ КАК ВидГСМ,
		|	ВЫБОР
		|		КОГДА втЗаправкиСливыГСМ.ДопОборудование = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
		|			ТОГДА &ТранспортноеСредство
		|		ИНАЧЕ втЗаправкиСливыГСМ.ДопОборудование
		|	КОНЕЦ КАК ТранспортноеСредство,
		|	втЗаправкиСливыГСМ.Количество КАК Количество,
		|	втЗаправкиСливыГСМ.ВидДвижения КАК ВидДвижения,
		|	втЗаправкиСливыГСМ.ТопливнаяКарта КАК ТопливнаяКарта,
		|	втЗаправкиСливыГСМ.Стоимость КАК Стоимость,
		|	втЗаправкиСливыГСМ.СтоимостьНДС КАК СтоимостьНДС,
		|	ВЫБОР
		|		КОГДА втЗаправкиСливыГСМ.ДатаЗаправкиСлива = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА &ПериодДвиженияГСМ
		|		ИНАЧЕ втЗаправкиСливыГСМ.ДатаЗаправкиСлива
		|	КОНЕЦ КАК Период
		|ПОМЕСТИТЬ втЗаправкиСливыГСМ
		|ИЗ
		|	&тбЗаправкиСливыГСМ КАК втЗаправкиСливыГСМ
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|// Расходы ГСМ по дополнительному оборудованию.
		|ВЫБРАТЬ
		|	втГСМ.ВидГСМ КАК ВидГСМ,
		|	втГСМ.ДополнительноеОборудование КАК ТранспортноеСредство,
		|	втГСМ.НачОстаток КАК НачОстаток,
		|	втГСМ.РасходТопливаФакт КАК РасходТопливаФакт,
		|	втГСМ.СуммаРасходов КАК СуммаРасходов,
		|	втГСМ.СуммаРасходовНДС КАК СуммаРасходовНДС,
		|	втГСМ.РасходТопливаПлан КАК РасходТопливаПлан,
		|	&ПериодДвиженияГСМ КАК Период,
		|	втГСМ.РасходТопливаСТС КАК РасходТопливаСТС,
		|	втГСМ.РасходИзСобственногоБака КАК РасходИзСобственногоБака
		|ПОМЕСТИТЬ втГСМДопОборудование
		|ИЗ
		|	&тбГСМДопОборудование КАК втГСМ
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|// Заправка/слив по дополнительному оборудованию.
		|ВЫБРАТЬ
		|	втЗаправкиСливыГСМ.ВидГСМ КАК ВидГСМ,
		|	втЗаправкиСливыГСМ.ДополнительноеОборудование КАК ТранспортноеСредство,
		|	втЗаправкиСливыГСМ.Количество КАК Количество,
		|	втЗаправкиСливыГСМ.ВидДвижения КАК ВидДвижения,
		|	втЗаправкиСливыГСМ.ТопливнаяКарта КАК ТопливнаяКарта,
		|	втЗаправкиСливыГСМ.Стоимость КАК Стоимость,
		|	втЗаправкиСливыГСМ.СтоимостьНДС КАК СтоимостьНДС,
		|	ВЫБОР
		|		КОГДА втЗаправкиСливыГСМ.ДатаЗаправкиСлива = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА &ПериодДвиженияГСМ
		|		ИНАЧЕ втЗаправкиСливыГСМ.ДатаЗаправкиСлива
		|	КОНЕЦ КАК Период
		|ПОМЕСТИТЬ втЗаправкиСливыГСМДопОборудование
		|ИЗ
		|	&тбЗаправкиСливыГСМДопОборудование КАК втЗаправкиСливыГСМ
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|// Различные сочетания ТС/Доп оборудования и видов ГСМ, используемых
		|// в документе.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втТСВидГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втТСВидГСМ.ВидГСМ КАК ВидГСМ
		|ПОМЕСТИТЬ втТранспортныеСредстваВидГСМ
		|ИЗ
		|	втГСМ КАК втТСВидГСМ
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	втЗаправкиСливыГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втЗаправкиСливыГСМ.ВидГСМ КАК ВидГСМ
		|ИЗ
		|	втЗаправкиСливыГСМ КАК втЗаправкиСливыГСМ
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втГСМДопОборудованиеТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ
		|ИЗ
		|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
		|ГДЕ втГСМДопОборудованиеТ.РасходИзСобственногоБака
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втЗаправкиСливыГСМДопОборудованиеТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втЗаправкиСливыГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ
		|ИЗ
		|	втЗаправкиСливыГСМДопОборудование КАК втЗаправкиСливыГСМДопОборудованиеТ
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|// Заправки и сливы по ТС и доп. оборудованию
		|ВЫБРАТЬ
		|	втЗаправкиСливыГСМТ.ВидГСМ КАК ВидГСМ,
		|	втЗаправкиСливыГСМТ.ВидДвижения КАК ВидДвижения,
		|	втЗаправкиСливыГСМТ.Количество КАК Количество,
		|	втЗаправкиСливыГСМТ.Период КАК Период,
		|	втЗаправкиСливыГСМТ.Стоимость КАК Стоимость,
		|	втЗаправкиСливыГСМТ.СтоимостьНДС КАК СтоимостьНДС,
		|	втЗаправкиСливыГСМТ.ТопливнаяКарта КАК ТопливнаяКарта,
		|	втЗаправкиСливыГСМТ.ТранспортноеСредство КАК ТранспортноеСредство
		|ПОМЕСТИТЬ втЗаправкиСливыГСМПоДокументу
		|ИЗ
		|	втЗаправкиСливыГСМ КАК втЗаправкиСливыГСМТ
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втЗаправкиСливыГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
		|	втЗаправкиСливыГСМДопОборудованиеТ.ВидДвижения КАК ВидДвижения,
		|	втЗаправкиСливыГСМДопОборудованиеТ.Количество КАК Количество,
		|	втЗаправкиСливыГСМДопОборудованиеТ.Период КАК Период,
		|	втЗаправкиСливыГСМДопОборудованиеТ.Стоимость КАК Стоимость,
		|	втЗаправкиСливыГСМДопОборудованиеТ.СтоимостьНДС КАК СтоимостьНДС,
		|	втЗаправкиСливыГСМДопОборудованиеТ.ТопливнаяКарта КАК ТопливнаяКарта,
		|	втЗаправкиСливыГСМДопОборудованиеТ.ТранспортноеСредство КАК ТранспортноеСредство
		|ИЗ
		|	втЗаправкиСливыГСМДопОборудование КАК втЗаправкиСливыГСМДопОборудованиеТ
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|// Расход ГСМ по ТС и доп. оборудованию
		|ВЫБРАТЬ
		|	тбГСМТранспортноеСредство.ТранспортноеСредство КАК ТранспортноеСредство,
		|	тбГСМТранспортноеСредство.ВидГСМ КАК ВидГСМ,
		|	тбГСМТранспортноеСредство.Период КАК Период,
		|	СУММА(тбГСМТранспортноеСредство.НачОстаток) КАК НачОстаток,
		|	СУММА(тбГСМТранспортноеСредство.РасходТопливаПлан) КАК РасходТопливаПлан,
		|	СУММА(тбГСМТранспортноеСредство.РасходТопливаФакт) КАК РасходТопливаФакт
		|ПОМЕСТИТЬ втГСМПоДокументу
		|ИЗ
		|	(ВЫБРАТЬ
		|		втГСМТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|		втГСМТ.ВидГСМ КАК ВидГСМ,
		|		втГСМТ.РасходТопливаФакт КАК РасходТопливаФакт,
		|		втГСМТ.РасходТопливаПлан КАК РасходТопливаПлан,
		|		втГСМТ.НачОстаток КАК НачОстаток,
		|		втГСМТ.Период КАК Период
		|	ИЗ
		|		втГСМ КАК втГСМТ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		&ТранспортноеСредство КАК ТранспортноеСредство,
		|		втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
		|		втГСМДопОборудованиеТ.РасходТопливаСТС КАК РасходТопливаФакт,
		|		0 КАК РасходТопливаПлан,
		|		0 КАК НачОстаток,
		|		втГСМДопОборудованиеТ.Период КАК Период
		|	ИЗ
		|		втГСМДопОборудование КАК втГСМДопОборудованиеТ
		|ГДЕ втГСМДопОборудованиеТ.РасходТопливаСТС > 0		
		|) КАК тбГСМТранспортноеСредство
		|СГРУППИРОВАТЬ ПО
		|	тбГСМТранспортноеСредство.ТранспортноеСредство,
		|	тбГСМТранспортноеСредство.ВидГСМ,
		|	тбГСМТранспортноеСредство.Период
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втГСМДопОборудованиеТ.ТранспортноеСредство КАК ТранспортноеСредство,
		|	втГСМДопОборудованиеТ.ВидГСМ КАК ВидГСМ,
		|	втГСМДопОборудованиеТ.Период КАК Период,
		|	втГСМДопОборудованиеТ.НачОстаток КАК НачОстаток,
		|	втГСМДопОборудованиеТ.РасходТопливаПлан КАК РасходТопливаПлан,
		|	втГСМДопОборудованиеТ.РасходТопливаФакт КАК РасходТопливаФакт
		|ИЗ
		|	втГСМДопОборудование КАК втГСМДопОборудованиеТ
		|ГДЕ втГСМДопОборудованиеТ.РасходИзСобственногоБака
		|;
		|//{Запрос: 7 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбДвиженияТоплива.ТранспортноеСредство КАК ТранспортноеСредство,
		|	тбДвиженияТоплива.ВидГСМ КАК ВидГСМ,
		|	тбДвиженияТоплива.ТопливнаяКарта КАК ТопливнаяКарта,
		|	СУММА(тбДвиженияТоплива.КоличествоЗаправка) КАК КоличествоЗаправка,
		|	СУММА(тбДвиженияТоплива.КоличествоСлив) КАК КоличествоСлив,
		|	СУММА(тбДвиженияТоплива.СтоимостьЗаправка) КАК СтоимостьЗаправка,
		|	СУММА(тбДвиженияТоплива.СтоимостьНДСЗаправка) КАК СтоимостьНДСЗаправка,
		|	СУММА(тбДвиженияТоплива.КоличествоРасходДокумент) КАК КоличествоРасходДокумент,
		|	СУММА(тбДвиженияТоплива.КоличествоРасходПланДокумент) КАК КоличествоРасходПланДокумент,
		|	СУММА(тбДвиженияТоплива.СтоимостьНачальныйОстатокРегистр) КАК СтоимостьНачальныйОстатокРегистр,
		|	СУММА(тбДвиженияТоплива.СтоимостьНДСНачальныйОстатокРегистр) КАК СтоимостьНДСНачальныйОстатокРегистр,
		|	СУММА(тбДвиженияТоплива.КоличествоНачальныйОстатокРегистр) КАК КоличествоНачальныйОстатокРегистр,
		|	СУММА(тбДвиженияТоплива.КоличествоНачальныйОстатокДокумент) КАК КоличествоНачальныйОстатокДокумент,
		|	тбДвиженияТоплива.Период КАК Период
		|ИЗ
		|	(ВЫБРАТЬ
		|		тбЗаправкиСливыГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
		|		тбЗаправкиСливыГСМ.ВидГСМ КАК ВидГСМ,
		|		тбЗаправкиСливыГСМ.ТопливнаяКарта КАК ТопливнаяКарта,
		|		ВЫБОР
		|			КОГДА тбЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(перечисление.упВидыДвиженияТоплива.Заправка)
		|				ТОГДА тбЗаправкиСливыГСМ.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КоличествоЗаправка,
		|		ВЫБОР
		|			КОГДА тбЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(перечисление.упВидыДвиженияТоплива.Слив)
		|				ТОГДА тбЗаправкиСливыГСМ.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КоличествоСлив,
		|		ВЫБОР
		|			КОГДА тбЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(перечисление.упВидыДвиженияТоплива.Заправка)
		|				ТОГДА тбЗаправкиСливыГСМ.Стоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СтоимостьЗаправка,
		|		ВЫБОР
		|			КОГДА тбЗаправкиСливыГСМ.ВидДвижения = ЗНАЧЕНИЕ(перечисление.упВидыДвиженияТоплива.Заправка)
		|				ТОГДА тбЗаправкиСливыГСМ.СтоимостьНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СтоимостьНДСЗаправка,
		|		0 КАК КоличествоРасходДокумент,
		|		0 КАК КоличествоРасходПланДокумент,
		|		0 КАК КоличествоНачальныйОстатокРегистр,
		|		0 КАК СтоимостьНачальныйОстатокРегистр,
		|		0 КАК СтоимостьНДСНачальныйОстатокРегистр,
		|		0 КАК КоличествоНачальныйОстатокДокумент,
		|		тбЗаправкиСливыГСМ.Период КАК Период
		|	ИЗ
		|		втЗаправкиСливыГСМПоДокументу КАК тбЗаправкиСливыГСМ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		тбГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
		|		тбГСМ.ВидГСМ КАК ВидГСМ,
		|		ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка) КАК ТопливнаяКарта,
		|		0 КАК КоличествоЗаправка,
		|		0 КАК КоличествоСлив,
		|		0 КАК СтоимостьЗаправка,
		|		0 КАК СтоимостьНДСЗаправка,
		|		тбГСМ.РасходТопливаФакт КАК КоличествоРасходДокумент,
		|		тбГСМ.РасходТопливаПлан КАК КоличествоРасходПланДокумент,
		|		0 КАК КоличествоНачальныйОстатокРегистр,
		|		0 КАК СтоимостьНачальныйОстатокРегистр,
		|		0 КАК СтоимостьНДСНачальныйОстатокРегистр,
		|		тбГСМ.НачОстаток КАК КоличествоНачальныйОстатокДокумент,
		|		тбГСМ.Период КАК Период
		|	ИЗ
		|		втГСМПоДокументу КАК тбГСМ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		упОстаткиГСМОстатки.ТранспортноеСредство КАК ТранспортноеСредство,
		|		упОстаткиГСМОстатки.ВидГСМ КАК ВидГСМ,
		|		ЗНАЧЕНИЕ(Справочник.упТопливныеКарты.ПустаяСсылка) КАК ТопливнаяКарта,
		|		0 КАК КоличествоЗаправка,
		|		0 КАК КоличествоСлив,
		|		0 КАК СтоимостьЗаправка,
		|		0 КАК СтоимостьНДСЗаправка,
		|		0 КАК КоличествоРасходДокумент,
		|		0 КАК КоличествоРасходПланДокумент,
		|		упОстаткиГСМОстатки.КоличествоОстаток КАК КоличествоНачальныйОстатокРегистр,
		|		упОстаткиГСМОстатки.СтоимостьОстаток КАК СтоимостьНачальныйОстатокРегистр,
		|		упОстаткиГСМОстатки.СтоимостьНДСОстаток КАК СтоимостьНДСНачальныйОстатокРегистр,
		|		0 КАК КоличествоНачальныйОстатокДокумент,
		|		&ПериодДвиженияГСМ КАК Период
		|	ИЗ
		|		РегистрНакопления.упОстаткиГСМ.Остатки(
		|			,
		|			(ТранспортноеСредство, ВидГСМ) В (
		|					ВЫБРАТЬ
		|					втТранспортныеСредстваВидГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
		|					втТранспортныеСредстваВидГСМ.ВидГСМ КАК ВидГСМ
		|				ИЗ
		|					втТранспортныеСредстваВидГСМ КАК втТранспортныеСредстваВидГСМ)) КАК упОстаткиГСМОстатки) КАК тбДвиженияТоплива
		|СГРУППИРОВАТЬ ПО
		|	тбДвиженияТоплива.ТранспортноеСредство,
		|	тбДвиженияТоплива.ВидГСМ,
		|	тбДвиженияТоплива.ТопливнаяКарта,
		|	тбДвиженияТоплива.Период
		|ИТОГИ
		|	СУММА(КоличествоЗаправка),
		|	СУММА(КоличествоСлив),
		|	СУММА(СтоимостьЗаправка),
		|	СУММА(СтоимостьНДСЗаправка),
		|	СУММА(КоличествоРасходДокумент),
		|	СУММА(КоличествоРасходПланДокумент),
		|	СУММА(СтоимостьНачальныйОстатокРегистр),
		|	СУММА(СтоимостьНДСНачальныйОстатокРегистр),
		|	СУММА(КоличествоНачальныйОстатокРегистр),
		|	СУММА(КоличествоНачальныйОстатокДокумент),
		|	МАКСИМУМ(Период)
		|ПО
		|	ТранспортноеСредство,
		|	ВидГСМ
		|";

				   
	тбзЗаправки = ЗаправкиСливыГСМ.Выгрузить(,"ДатаЗаправкиСлива, 
										|ВидГСМ, 
										|ВидДвижения, 
										|ДопОборудование, 
										|Валюта, 
										|Количество, 
										|Стоимость, 
										|СтоимостьНДС, 
										|ТопливнаяКарта");			   
	СуммаРасходыТекущие = 0;
	СуммаРасходыНДСТекущие = 0;

	Если ВедетсяВалютныйУчет Тогда
		Для каждого Строка Из тбзЗаправки Цикл
			Если ЗначениеЗаполнено(Строка.Валюта) Тогда
				Строка.Стоимость = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.Стоимость, Дата); 	
				Строка.СтоимостьНДС = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СтоимостьНДС, Дата); 	
				СуммаРасходыТекущие = СуммаРасходыТекущие + Строка.Стоимость;
				СуммаРасходыНДСТекущие = СуммаРасходыНДСТекущие + Строка.СтоимостьНДС;
			КонецЕсли;
		КонецЦикла;
		
		ЭтотОбъект.СуммаРасходы = СуммаРасходыТекущие;
		ЭтотОбъект.СуммаРасходыНДС = СуммаРасходыНДСТекущие;
	КонецЕсли;
	
	тбзЗаправкиДопОборудование = ЗаправкиСливыГСМДополнительноеОборудование.Выгрузить(,"ДатаЗаправкиСлива, 
																		|ВидГСМ, 
																		|ВидДвижения, 
																		|ДополнительноеОборудование, 
																		|Валюта, 
																		|Количество, 
																		|Стоимость, 
																		|СтоимостьНДС, 
																		|ТопливнаяКарта");			   
	Если ВедетсяВалютныйУчет Тогда

		Для каждого Строка Из тбзЗаправкиДопОборудование Цикл
			Если ЗначениеЗаполнено(Строка.Валюта) Тогда
				Строка.Стоимость = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.Стоимость, Дата); 	
				Строка.СтоимостьНДС = упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СтоимостьНДС, Дата); 	
				СуммаРасходыТекущие = СуммаРасходыТекущие + Строка.Стоимость;
				СуммаРасходыНДСТекущие = СуммаРасходыНДСТекущие + Строка.СтоимостьНДС;
			КонецЕсли;
		КонецЦикла;
		
		ЭтотОбъект.СуммаРасходы = СуммаРасходыТекущие;
		ЭтотОбъект.СуммаРасходыНДС = СуммаРасходыНДСТекущие;
	КонецЕсли;
	 				   
	Запрос.УстановитьПараметр("тбГСМ",	ГСМ.Выгрузить(,"ВидГСМ, 
											|ДопОборудование, 
											|НачОстаток, 
											|РасходТопливаФакт, 
											|СуммаРасходов, 
											|СуммаРасходовНДС, 
											|РасходТопливаПлан"));
	Запрос.УстановитьПараметр("тбЗаправкиСливыГСМ", 	тбзЗаправки);
	Запрос.УстановитьПараметр("тбГСМДопОборудование", ГСМДополнительноеОборудование.Выгрузить(,"ВидГСМ, 
														|ДополнительноеОборудование, 
														|НачОстаток, 
														|РасходТопливаФакт, 
														|СуммаРасходов, 
														|СуммаРасходовНДС, 
														|РасходТопливаПлан, 
														|РасходИзСобственногоБака, 
														|РасходТопливаСТС"));
	Запрос.УстановитьПараметр("тбЗаправкиСливыГСМДопОборудование", тбзЗаправкиДопОборудование);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("ПериодДвиженияГСМ", ПериодДвиженияГСМ);
		
	ВыборкаТранспортноеСредство = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	тбзОстаткиГСМ = упПутевойЛистУчетГСМ.СтруктураТаблицыОстаткиГСМ();
	
	Пока ВыборкаТранспортноеСредство.Следующий() Цикл
		
		ВыборкаВидГСМ	= ВыборкаТранспортноеСредство.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаВидГСМ.Следующий() Цикл
			
			// Корректировка остатков
			ДельтаНачОстаток	= ВыборкаВидГСМ.КоличествоНачальныйОстатокДокумент - ВыборкаВидГСМ.КоличествоНачальныйОстатокРегистр;
			СтоимостьОстаток	= ВыборкаВидГСМ.СтоимостьНачальныйОстатокРегистр;
			СтоимостьНДСОстаток	= ВыборкаВидГСМ.СтоимостьНДСНачальныйОстатокРегистр;
			КоличествоОстаток	= ВыборкаВидГСМ.КоличествоНачальныйОстатокРегистр;
			
			Если НЕ ДельтаНачОстаток = 0 Тогда
				
				СтрокаГСМ	= тбзОстаткиГСМ.Добавить();
				СтрокаГСМ.Период				= Дата;
				СтрокаГСМ.ВидДвижения 			= ?(ДельтаНачОстаток >=0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
				СтрокаГСМ.ВидДвиженияТоплива 		= Перечисления.упВидыДвиженияТоплива.Корректировка;
				СтрокаГСМ.ВидГСМ 				= ВыборкаВидГСМ.ВидГСМ;
				СтрокаГСМ.ТопливнаяКарта 		= Справочники.упТопливныеКарты.ПустаяСсылка();
				СтрокаГСМ.ТранспортноеСредство 	= ВыборкаВидГСМ.ТранспортноеСредство;
				СтрокаГСМ.Количество			= ?(ДельтаНачОстаток >=0, ДельтаНачОстаток, -ДельтаНачОстаток);
				
				Если  НЕ  -ДельтаНачОстаток = КоличествоОстаток Тогда
					СтоимостьЕдиницы	= 0;
					СтоимостьНДСЕдиницы	= 0;
					Если НЕ КоличествоОстаток = 0 Тогда
						СтоимостьЕдиницы = СтоимостьОстаток / КоличествоОстаток;
						СтоимостьНДСЕдиницы	= СтоимостьНДСОстаток / КоличествоОстаток;
					КонецЕсли;
					СтрокаГСМ.Стоимость		= СтоимостьЕдиницы * СтрокаГСМ.Количество;
					СтрокаГСМ.СтоимостьНДС	= СтоимостьНДСЕдиницы * СтрокаГСМ.Количество;
				Иначе
					СтрокаГСМ.Стоимость		= СтоимостьОстаток;
					СтрокаГСМ.СтоимостьНДС	= СтоимостьНДСОстаток;				
				КонецЕсли;
				
				СтоимостьОстаток	= СтоимостьОстаток + ?(ДельтаНачОстаток >=0, СтрокаГСМ.Стоимость, -СтрокаГСМ.Стоимость);
				СтоимостьНДСОстаток	= СтоимостьНДСОстаток + ?(ДельтаНачОстаток >=0, СтрокаГСМ.СтоимостьНДС, -СтрокаГСМ.СтоимостьНДС);
				КоличествоОстаток	= КоличествоОстаток + ДельтаНачОстаток;
				
			КонецЕсли;
			
			ВыборкаПериод = ВыборкаВидГСМ.Выбрать();
			
			Пока ВыборкаПериод.Следующий() Цикл
				
				// Заправка
				Заправка = ВыборкаПериод.КоличествоЗаправка;
				
				Если Заправка > 0 Тогда
					СтрокаГСМ	= тбзОстаткиГСМ.Добавить();
					СтрокаГСМ.Период				= ВыборкаПериод.Период;
					СтрокаГСМ.ВидДвижения 			= ВидДвиженияНакопления.Приход;
					СтрокаГСМ.ВидДвиженияТоплива 		= Перечисления.упВидыДвиженияТоплива.Заправка;
					СтрокаГСМ.ВидГСМ 				= ВыборкаПериод.ВидГСМ;
					СтрокаГСМ.ТопливнаяКарта 		= ВыборкаПериод.ТопливнаяКарта;
					СтрокаГСМ.ТранспортноеСредство 	= ВыборкаПериод.ТранспортноеСредство;
					СтрокаГСМ.Количество			= Заправка;
					СтрокаГСМ.Стоимость				= ВыборкаПериод.СтоимостьЗаправка;	
					СтрокаГСМ.СтоимостьНДС			= ВыборкаПериод.СтоимостьНДСЗаправка;	
					
					СтоимостьОстаток	= СтоимостьОстаток + СтрокаГСМ.Стоимость;
					СтоимостьНДСОстаток	= СтоимостьНДСОстаток + СтрокаГСМ.СтоимостьНДС;
					КоличествоОстаток	= КоличествоОстаток + СтрокаГСМ.Количество;
					
				КонецЕсли;
				
				// Слив
				Слив = ВыборкаПериод.КоличествоСлив;
				
				Если Слив > 0 Тогда
					СтрокаГСМ	= тбзОстаткиГСМ.Добавить();
					СтрокаГСМ.Период				= ВыборкаПериод.Период;
					СтрокаГСМ.ВидДвижения 			= ВидДвиженияНакопления.Расход;
					СтрокаГСМ.ТопливнаяКарта 		= ВыборкаПериод.ТопливнаяКарта;
					СтрокаГСМ.ВидДвиженияТоплива 		= Перечисления.упВидыДвиженияТоплива.Слив;
					СтрокаГСМ.ВидГСМ 				= ВыборкаПериод.ВидГСМ;
					СтрокаГСМ.ТранспортноеСредство 	= ВыборкаПериод.ТранспортноеСредство;
					СтрокаГСМ.Количество			= Слив;
					
					Если НЕ СтрокаГСМ.Количество = КоличествоОстаток Тогда
						СтоимостьЕдиницы	= 0;
						СтоимостьНДСЕдиницы	= 0;
						Если НЕ КоличествоОстаток = 0 Тогда
							СтоимостьЕдиницы	= СтоимостьОстаток / КоличествоОстаток;
							СтоимостьНДСЕдиницы	= СтоимостьНДСОстаток / КоличествоОстаток;
						КонецЕсли;
						СтрокаГСМ.Стоимость		= СтоимостьЕдиницы * Слив;		
						СтрокаГСМ.СтоимостьНДС	= СтоимостьНДСЕдиницы * Слив;				
					Иначе
						СтрокаГСМ.Стоимость		= СтоимостьОстаток;		
						СтрокаГСМ.СтоимостьНДС	= СтоимостьНДСОстаток;								
					КонецЕсли;
					
					
					СтоимостьОстаток	= СтоимостьОстаток - СтрокаГСМ.Стоимость;
					СтоимостьНДСОстаток	= СтоимостьНДСОстаток - СтрокаГСМ.СтоимостьНДС;
					КоличествоОстаток	= КоличествоОстаток - СтрокаГСМ.Количество;
					
				КонецЕсли;
			КонецЦикла;
						
			// Заполняется плановый расход топлива по ТС и доп. оборудованию.
			ПланРасход = ВыборкаВидГСМ.КоличествоРасходПланДокумент;
			Если ПланРасход > 0 Тогда
				
				СтоимостьЕдиницы	= 0;
				СтоимостьНДСЕдиницы	= 0;
				Если НЕ КоличествоОстаток = 0 Тогда
					СтоимостьЕдиницы 	= СтоимостьОстаток / КоличествоОстаток;
					СтоимостьНДСЕдиницы	= СтоимостьНДСОстаток / КоличествоОстаток;
				КонецЕсли;
							
				СуммаРасходовПлан = СтоимостьЕдиницы * ПланРасход;
				СуммаРасходовПланНДС = СтоимостьНДСЕдиницы * ПланРасход;					
			Иначе
				СуммаРасходовПлан = 0;
				СуммаРасходовПланНДС = 0;
			КонецЕсли;
			
			ДопОборудование = ?(ТипЗнч(ВыборкаВидГСМ.ТранспортноеСредство) = Тип("СправочникСсылка.упДополнительноеОборудование"), 
									ВыборкаВидГСМ.ТранспортноеСредство, 
									Справочники.упДополнительноеОборудование.ПустаяСсылка());
			
			мсвСтрокиТЧГСМ  = ГСМ.НайтиСтроки(Новый Структура("ДопОборудование, ВидГСМ", ДопОборудование, ВыборкаВидГСМ.ВидГСМ));
			
			Если мсвСтрокиТЧГСМ.Количество() > 0 Тогда
				мсвСтрокиТЧГСМ[0].СуммаРасходовПлан 	= СуммаРасходовПлан;
				мсвСтрокиТЧГСМ[0].СуммаРасходовПланНДС 	= СуммаРасходовПланНДС;
			Иначе
				мсвСтрокиТЧГСМ  = ГСМДополнительноеОборудование.НайтиСтроки(Новый Структура("ДополнительноеОборудование, ВидГСМ", ДопОборудование, ВыборкаВидГСМ.ВидГСМ));
				Если мсвСтрокиТЧГСМ.Количество() > 0 Тогда
					мсвСтрокиТЧГСМ[0].СуммаРасходовПлан 	= СуммаРасходовПлан;
					мсвСтрокиТЧГСМ[0].СуммаРасходовПланНДС 	= СуммаРасходовПланНДС;
				КонецЕсли;	
			КонецЕсли;
	
						
			// Заполняется фактический расход топлива по ТС и доп. оборудованию.
			ФактРасход = ВыборкаВидГСМ.КоличествоРасходДокумент;
			
			Если НЕ ФактРасход = 0 Тогда
				
				ФактРасходПоМодулю = ?(ФактРасход < 0, -ФактРасход, ФактРасход);
				
				СтрокаГСМ	= тбзОстаткиГСМ.Добавить();
				СтрокаГСМ.Период				= ВыборкаВидГСМ.Период;
				СтрокаГСМ.ВидДвижения 			= ?(ФактРасход < 0, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
				СтрокаГСМ.ВидДвиженияТоплива 		= Перечисления.упВидыДвиженияТоплива.Расход;
				СтрокаГСМ.ВидГСМ 				= ВыборкаВидГСМ.ВидГСМ;
				СтрокаГСМ.ТопливнаяКарта 		= Справочники.упТопливныеКарты.ПустаяСсылка();
				СтрокаГСМ.ТранспортноеСредство 	= ВыборкаВидГСМ.ТранспортноеСредство;
				СтрокаГСМ.Количество			= ФактРасходПоМодулю;
				
				Если НЕ СтрокаГСМ.Количество = КоличествоОстаток Тогда
					СтоимостьЕдиницы	= 0;
					СтоимостьНДСЕдиницы	= 0;
					Если НЕ КоличествоОстаток = 0 Тогда
						СтоимостьЕдиницы	= СтоимостьОстаток / КоличествоОстаток;
						СтоимостьНДСЕдиницы	= СтоимостьНДСОстаток / КоличествоОстаток;					
					КонецЕсли;
					
					СтрокаГСМ.Стоимость = СтоимостьЕдиницы * ФактРасходПоМодулю;		
					СтрокаГСМ.СтоимостьНДС = СтоимостьНДСЕдиницы * ФактРасходПоМодулю;		
				Иначе
					СтрокаГСМ.Стоимость = СтоимостьОстаток;		
					СтрокаГСМ.СтоимостьНДС = СтоимостьНДСОстаток;		
				КонецЕсли;
																	
				Стоимость = СтрокаГСМ.Стоимость;
				СтоимостьНДС = СтрокаГСМ.СтоимостьНДС;
			Иначе	
				Стоимость = 0;
				СтоимостьНДС = 0;
			КонецЕсли;
			
			ДопОборудование = ?(ТипЗнч(ВыборкаВидГСМ.ТранспортноеСредство) = Тип("СправочникСсылка.упДополнительноеОборудование"), 
									ВыборкаВидГСМ.ТранспортноеСредство, 
									Справочники.упДополнительноеОборудование.ПустаяСсылка());
			
			мсвСтрокиТЧГСМ = ГСМ.НайтиСтроки(Новый Структура("ДопОборудование, ВидГСМ", ДопОборудование, ВыборкаВидГСМ.ВидГСМ ));
			
			Если мсвСтрокиТЧГСМ.Количество() > 0 Тогда
				мсвСтрокиТЧГСМ[0].СуммаРасходов = Стоимость;
				мсвСтрокиТЧГСМ[0].СуммаРасходовНДС = СтоимостьНДС;
			Иначе
				мсвСтрокиТЧГСМ  = ГСМДополнительноеОборудование.НайтиСтроки(Новый Структура("ДополнительноеОборудование, ВидГСМ", ДопОборудование, ВыборкаВидГСМ.ВидГСМ));
				Если мсвСтрокиТЧГСМ.Количество() > 0 Тогда
					мсвСтрокиТЧГСМ[0].СуммаРасходов = Стоимость;
					мсвСтрокиТЧГСМ[0].СуммаРасходовНДС = СтоимостьНДС;
				КонецЕсли;	
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("тбзОстаткиГСМ", тбзОстаткиГСМ);
		
КонецПроцедуры

Функция РазрешеноПроведениеИлиОтменаПроведения(тбзГСМ, ПериодПроверки, ДатаПоследнегоДвиженияГСМ)
	
	Результат = Ложь;
	
	ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ = упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийОстаткиГСМПоТаблицеГСМ(Ссылка, ПериодПроверки, тбзГСМ, ДатаПоследнегоДвиженияГСМ);
	ЭтоПоследнийДокументИзменявшийПробег        = упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийПробег(Ссылка, ПериодПроверки, ТранспортноеСредство);
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования") Тогда
		мсвДополнительноеОборудование = ДополнительноеОборудование.ВыгрузитьКолонку("ДополнительноеОборудование");
		мсвАгрегатыПоТС = упУправлениеЗапасами.АгрегатыПоТранспортномуСредству(ПериодПроверки, ТранспортноеСредство);
		Для каждого Агрегат Из мсвАгрегатыПоТС Цикл
			мсвДополнительноеОборудование.Добавить(Агрегат);	
		КонецЦикла;
		ЭтоПоследнийДокументИзменявшийНаработкуОборудования = упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийНаработку(Ссылка, ПериодПроверки, мсвДополнительноеОборудование);
	Иначе
		ЭтоПоследнийДокументИзменявшийНаработкуОборудования = Истина;
	КонецЕсли;	
	
	Результат = Истина
			  И ЭтоПоследнийДокументИзменявшийПробег 
			  И ЭтоПоследнийДокументИзменявшийНаработкуОборудования
			  И ЭтоПоследнийПутевойЛистИзменявшийОстаткиГСМ;
	
	Возврат Результат;
		
КонецФункции

Процедура ПроверитьНаличиеПутевыхЛистовПоРейсу(Отказ)
	упПутевойЛистУчетГСМ.ПроверитьНаличиеПутевыхЛистовПоРейсу(Рейсы.ВыгрузитьКолонку("Рейс") ,Ссылка, Отказ);
КонецПроцедуры

Процедура ПроверитьУстановкуДопОборудованияНаТС(Объект, Отказ)
	упПутевойЛистУчетГСМ.ПроверитьУстановкуДопОборудованияНаТС(Объект, ДополнительноеОборудование.Выгрузить(,"НомерСтроки, ТранспортноеСредство, ДополнительноеОборудование"), ТранспортноеСредство, НачалоРейсов(), Отказ);	
КонецПроцедуры

Процедура ПроверитьНаличиеПутевыхЛистовПоТСРейса(ДополнительныеСвойства, Отказ)
	
	ТекущийСтатус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Статус", Неопределено);
	Если НЕ ЗначениеЗаполнено(ТекущийСтатус) Тогда
		ТекущийСтатус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ТекущийСтатус", Неопределено);	
	КонецЕсли;
	
	Если Ложь
		Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Выдан
		Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Возвращен
		Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Закрыт 
	Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	упПутевойЛист.Ссылка КАК Ссылка,
		               |	упСтатусыПутевыхЛистовСрезПоследних.Статус КАК Статус
		               |ИЗ
		               |	Документ.упПутевойЛист КАК упПутевойЛист
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
		               |		ПО упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка
		               |ГДЕ
		               |	упПутевойЛист.МоментВремени < &МоментВремени
		               |	И (упСтатусыПутевыхЛистовСрезПоследних.Статус ЕСТЬ NULL 
		               |			ИЛИ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Новый)
		               |			ИЛИ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Выдан))
		               |	И НЕ упПутевойЛист.ПометкаУдаления
		               |	И упПутевойЛист.ТранспортноеСредство = &ТранспортноеСредство";
		Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
		Запрос.УстановитьПараметр("МоментВремени",        Новый МоментВремени(Дата, Ссылка));
		текВыборка	= Запрос.Выполнить().Выбрать();
		Если текВыборка.Следующий() Тогда
			ОписаниеОшибки = Нстр("ru = 'По %1 существует %2 в статусе %3'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, ТранспортноеСредство, текВыборка.Ссылка, текВыборка.Статус);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,текВыборка.Ссылка,,,Отказ);
			
			ЗаписьЖурналаРегистрации(СобытиеОшибкаПроведения(), УровеньЖурналаРегистрации.Предупреждение, Ссылка.Метаданные(), Ссылка, ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);    
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьЗаявкиВыполняютсяТСОтличнымиОтТекущего(ТранспортноеСредство, Отказ)
	
	Если Истина
		И ЗначениеЗаполнено(ТранспортноеСредство)
		И Заявки.Количество() > 0
	Тогда
	
		ТекущийСтатус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Статус", Неопределено);
	Если НЕ ЗначениеЗаполнено(ТекущийСтатус) Тогда
		ТекущийСтатус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ТекущийСтатус", Неопределено);	
	КонецЕсли;
	
		Если Ложь
			Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Новый
			Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Выдан
			Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Возвращен
			Или ТекущийСтатус = Перечисления.упСтатусыПутевогоЛиста.Закрыт 
		Тогда
		
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"
			|ВЫБРАТЬ
			|	ПутевыеЛисты.Ссылка КАК ПутевойЛист,
			|	ПутевыеЛисты.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
			|	ПутевыеЛисты.Ссылка.ТранспортноеСредство КАК ТранспортноеСредство
			|ИЗ
			|	Документ.упПутевойЛист.Заявки КАК ЗаявкиТекущие
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПутевойЛист.Заявки КАК ПутевыеЛисты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних(
			|			,
			|			) КАК СтатусыПутевыхЛистов
			|		ПО СтатусыПутевыхЛистов.Документ = ПутевыеЛисты.Ссылка
			|	ПО ИСТИНА
			|		И ЗаявкиТекущие.ЗаявкаНаПеревозкуГруза = ПутевыеЛисты.ЗаявкаНаПеревозкуГруза
			|		И ЗаявкиТекущие.Ссылка.ТранспортноеСредство <> ПутевыеЛисты.Ссылка.ТранспортноеСредство
			|		И ПутевыеЛисты.Ссылка.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
			|		И ЗаявкиТекущие.Ссылка <> ПутевыеЛисты.Ссылка
			|		И НЕ ПутевыеЛисты.Ссылка.ПометкаУдаления
			|ГДЕ ИСТИНА
			|И ЗаявкиТекущие.Ссылка = &ПутевойЛист
			|И СтатусыПутевыхЛистов.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)
			| 
			|";

			
			Запрос.УстановитьПараметр("ПутевойЛист", Ссылка);
			Выборка	= Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ТекстСообщения = Нстр("ru = '%1 уже выполняется путевым листом:%2 на ТС:%3. Заявка может выполнятся только одним транспортным средством.'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ЗаявкаНаПеревозкуГруза, Выборка.ПутевойЛист, Выборка.ТранспортноеСредство);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.ПутевойЛист,,,Отказ);
				
				ЗаписьЖурналаРегистрации(СобытиеОшибкаПроведения(), УровеньЖурналаРегистрации.Ошибка,,ЭтотОбъект,ТекстСообщения,РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				 
			КонецЦикла; 	
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Функция ПервыйРейс(мсвРейсы)
	
	Результат = Документы.упРейс.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРейс.Ссылка
	               |ИЗ
	               |	Документ.упРейс КАК упРейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |		ПО (упПлановыеПараметрыРейса.Рейс = упРейс.Ссылка)
	               |ГДЕ
	               |	упРейс.Ссылка В(&мсвРейсы)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1))";
	Запрос.УстановитьПараметр("мсвРейсы", мсвРейсы);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Результат = текВыборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

Функция НачалоРейсов()
	НачалоРейсов 	= ?(ЗначениеЗаполнено(НачалоРейса), НачалоРейса, Документы.упРейс.ПолучитьВремяНачалаПоРейсам(Рейсы.Выгрузить().ВыгрузитьКолонку("Рейс")));
	Результат 	= ?(ЗначениеЗаполнено(НачалоРейса), НачалоРейса, Дата);
	Возврат Результат;
КонецФункции

Процедура ПередатьДанныеВРейс(ВидПеревозки = Неопределено, мсвМаршрут = Неопределено, Водитель1 = Неопределено, Отказ)
	
	КорректировкаРейсаОбъект = Неопределено;
		
	Если НЕ ДополнительныеСвойства.Свойство("КорректировкаРейса", КорректировкаРейсаОбъект) Тогда
		КорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();		
	КонецЕсли;
	                      
	ПровестиИнициализацию = Неопределено;
	Если Не ЗначениеЗаполнено(Рейс) Тогда
		РейсОбъект              = Документы.упРейс.СоздатьДокумент();
		РейсОбъект.Дата         = Дата;
		РейсОбъект.ИсточникСозданияОбъекта	= упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		Если ВидПеревозки <> Неопределено Тогда
			РейсОбъект.ВидПеревозки = ВидПеревозки;
		Иначе	
			РейсОбъект.ВидПеревозки = Справочники.упВидыПеревозок.Основной;
		КонецЕсли;
		
		ПровестиИнициализацию   = Истина;
	Иначе
		РейсОбъект = Рейс.ПолучитьОбъект();
		ПровестиИнициализацию = Ложь;
	КонецЕсли;
	
	сткРеквизитыЗаполнения = Новый Структура();
	сткРеквизитыЗаполнения.Вставить("ИспользуютсяДополнительныеОперации", Ложь);
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	РейсОбъект.Организация = Организация;
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
		РейсОбъект.ЧасовойПояс = упСервисныеФункции.ЧасовойПоясВидаПеревозки(РейсОбъект.ВидПеревозки);	
	КонецЕсли; 
	
	сткРеквизитыЗаполнения.Вставить("Организация",                          Организация);
	сткРеквизитыЗаполнения.Вставить("УчитыватьКоличествоЗагрузочныхМетров", ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров"));
	сткРеквизитыЗаполнения.Вставить("УчитыватьКоличествоМест",              ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест"));
	сткРеквизитыЗаполнения.Вставить("УчитыватьМассу",                       ПолучитьФункциональнуюОпцию("упУчитыватьМассу"));
	сткРеквизитыЗаполнения.Вставить("УчитыватьОбъем",                       ПолучитьФункциональнуюОпцию("упУчитыватьОбъем"));

	КорректировкаРейсаОбъект.Рейс        = РейсОбъект.Ссылка;
	КорректировкаРейсаОбъект.Ссылка      = РейсОбъект.Ссылка;
	КорректировкаРейсаОбъект.Период      = РейсОбъект.Дата;
	КорректировкаРейсаОбъект.ЧасовойПояс = РейсОбъект.ЧасовойПояс;
	
	Если ПровестиИнициализацию Тогда
		КорректировкаРейсаОбъект.Инициализация(сткРеквизитыЗаполнения);
	КонецЕсли;
	
	Если Водитель1 <> Неопределено Тогда
		КорректировкаРейсаОбъект.Водитель1 = Водитель1;
	КонецЕсли;

	Если мсвМаршрут <> Неопределено Тогда
		Для каждого АдресПодачи Из мсвМаршрут Цикл
			сткПараметры = Новый Структура("Адрес", АдресПодачи);
			КорректировкаРейсаОбъект.ДобавитьТочкуВМаршрут(Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию, сткПараметры);
		КонецЦикла;
		// Пересчитывается время на всем маршруте
		КорректировкаРейсаОбъект.СпланироватьРаботыПоМаршруту(0,Ложь,Ложь,,Истина);
		КорректировкаРейсаОбъект.УстановитьПредставлениеМаршурта();
	КонецЕсли;
	
	сткРеквизиты = Новый Структура();
	РейсОбъект.ДополнительныеСвойства.Вставить("ПлановоеНачалоВыполнения",	КорректировкаРейсаОбъект.ПлановоеНачалоВыполнения);
	РейсОбъект.ДополнительныеСвойства.Вставить("ПлановоеОкончаниеВыполнения",	КорректировкаРейсаОбъект.ПлановоеОкончаниеВыполнения);
	КорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляРейса(РейсОбъект.ДополнительныеСвойства, РейсОбъект.Ссылка, сткРеквизиты, Отказ);
	РейсОбъект.ДополнительныеСвойства.Вставить("КорректировкаРейса", КорректировкаРейсаОбъект);
	
	сткЭкипаж = Новый Структура();
	сткЭкипаж.Вставить("Перевозчик"           , КорректировкаРейсаОбъект.Перевозчик);
	сткЭкипаж.Вставить("КонтрагентПеревозчика", КорректировкаРейсаОбъект.КонтрагентПеревозчика);
	сткЭкипаж.Вставить("Соглашение"           , КорректировкаРейсаОбъект.Соглашение);
	сткЭкипаж.Вставить("ТипТС"                , ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство,"ТипТС"));
	сткЭкипаж.Вставить("ТранспортноеСредство" , ТранспортноеСредство);
	сткЭкипаж.Вставить("Водитель1"            , КорректировкаРейсаОбъект.Водитель1);
	сткЭкипаж.Вставить("Водитель2"            , );
	сткЭкипаж.Вставить("Экспедитор"           , );
	
	РейсОбъект.ДополнительныеСвойства.Вставить("ИзмененЭкипаж", сткЭкипаж);
	РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Рейс = РейсОбъект.Ссылка;
	
КонецПроцедуры	

Функция СобытиеОшибкаПроведения()
	Возврат Нстр("ru = 'Ошибка при проведении путевого листа'");		
КонецФункции

Функция ПериодДвиженияГСМ()
	Возврат ?(ЗначениеЗаполнено(ОкончаниеРейса), ОкончаниеРейса, Дата);	
КонецФункции


#КонецОбласти

#КонецЕсли
