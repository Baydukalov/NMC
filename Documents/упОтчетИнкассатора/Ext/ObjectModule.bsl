#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Если Сумма = 0 Тогда
	//	ТекстСообщения = Нстр("ru = 'Проведение документа с нулевой суммой запрещено'");
	//	ВызватьИсключение ТекстСообщения;
	//КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упИнкассацияДенежныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", 	ЗаявкаНаПеревозкуГруза);
	ЭлементБлокировки.УстановитьЗначение("Валюта", 					Валюта);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
		
	РеквизитыОбъекта = Новый Структура(); 
	РеквизитыОбъекта.Вставить("Дата");
	РеквизитыОбъекта.Вставить("ЗаявкаНаПеревозкуГруза");
	РеквизитыОбъекта.Вставить("Валюта");
	РеквизитыОбъекта.Вставить("Сумма");
	
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);
	
	упИнкассация.СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	сткРеквизитыИмена = Новый Структура();
	сткРеквизитыИмена.Вставить("ИспользуютсяУслугиИнкассации", "ВидПеревозки.ИспользуютсяУслугиИнкассации");
	сткРеквизитыИмена.Вставить("ВидПеревозки", "ВидПеревозки");
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаявкаНаПеревозкуГруза, сткРеквизитыИмена);
	
	ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = Ложь;
	
	ВидПеревозки = сткРеквизиты.ВидПеревозки;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	
	ИспользуютсяУслугиИнкассации = сткРеквизиты.ИспользуютсяУслугиИнкассации;
	
	Если ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест Тогда
		ТекстОшибки = Нстр("ru = 'Для заявки с видом перевозки ""%2"", суммы по услугам инкассации вводятся с точностью до грузовых мест. Для этого используется документ ""Прохождение точки"".'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ЗаявкаНаПеревозкуГруза, ВидПеревозки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ЗаявкаНаПеревозкуГруза","Объект",Отказ);
		ЗаписьЖурналаРегистрации("ПроверкаЗаполненияОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
	ИначеЕсли Не ИспользуютсяУслугиИнкассации Тогда
		ТекстОшибки = Нстр("ru = 'Для заявки с видом перевозки ""%2"" не используются услуги инкассации'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ЗаявкаНаПеревозкуГруза","Объект",Отказ);
		Отказ = Истина;
		ЗаписьЖурналаРегистрации("ПроверкаЗаполненияОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли


