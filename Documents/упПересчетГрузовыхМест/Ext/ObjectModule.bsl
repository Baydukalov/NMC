#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	РеквизитыОбъекта = Новый Структура("Дата, Склад");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);

	ОстаткиГрузовыхМест = ГрузовыеМеста.ВыгрузитьКолонки("ГрузовоеМесто, Тара, СкладскаяЯчейка");
	ОстаткиГрузовыхМест.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",
													   Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Любой)));
	
	
	Для каждого Строка Из ГрузовыеМеста Цикл
		
		Если НЕ Строка.Количество = 0 Тогда
			НоваяСтрока = ОстаткиГрузовыхМест.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Количество = -Строка.Количество;	
		КонецЕсли;
		
		Если НЕ Строка.КоличествоФакт = 0 Тогда
			НоваяСтрока = ОстаткиГрузовыхМест.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Количество = Строка.КоличествоФакт;	
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("ОстаткиГрузовыхМест", ОстаткиГрузовыхМест);
	
	// Формируются остатки грузовых мест на хабах, в случае мультимодальной перевозки.
	упКорректировкаМаршрута.СформироватьДвижениеОстаткиГрузовыхМест(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	упКорректировкаМаршрута.ПроконтролироватьОтрицательныеОстаткиГрузовыхМестПоСкладу(ДополнительныеСвойства, Ссылка, Отказ);	

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// Процедура - дозаполнить ТЧ "Грузовые места".
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьОстаткамиПоСкладу() Экспорт
	
	Запрос = Новый Запрос;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упОстаткиГрузовыхМестОстатки.ГрузовоеМесто,
	|	упОстаткиГрузовыхМестОстатки.Тара,
	|	упОстаткиГрузовыхМестОстатки.СкладскаяЯчейка,
	|	упОстаткиГрузовыхМестОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.упОстаткиГрузовыхМест.Остатки(&Дата, СкладскаяЯчейка.Владелец = &Склад) КАК упОстаткиГрузовыхМестОстатки";
	Запрос.УстановитьПараметр("Дата", 	Дата);
	Запрос.УстановитьПараметр("Склад", 	Склад);
	
	ГрузовыеМеста.Очистить();
	ГрузовыеМеста.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

#КонецОбласти

#КонецЕсли