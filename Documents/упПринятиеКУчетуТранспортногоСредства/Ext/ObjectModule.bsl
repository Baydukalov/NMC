#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(Агрегаты, "Агрегаты", Ссылка, Отказ);
	упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ДополнительноеОборудование, "ДополнительноеОборудование", Ссылка, Отказ);
	
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.Запись  
	Тогда
		// Проверка на наличие более поздних документов изменяющих состояние агрегатов и доп. оборудования.
		мсвОборудование = Агрегаты.ВыгрузитьКолонку("Агрегат");
		Для каждого Строка Из ДополнительноеОборудование Цикл
			мсвОборудование.Добавить(Строка.ДополнительноеОборудование);
		КонецЦикла;
		упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, мсвОборудование, Дата, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Сч = 1;
		Для каждого Текущаястрока Из Агрегаты Цикл
			Если Текущаястрока.Сумма > 0 
				И НЕ ЗначениеЗаполнено(Текущаястрока.Валюта) Тогда
				ТекстОшибки = НСтр("ru = 'Не указана Валюта на закладке ""Агрегаты""'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Агрегаты", Сч, "Валюта"),, Отказ);
				Прервать;
			КонецЕсли;
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	Если НЕ Отказ Тогда
		Сч = 1;
		Для каждого Текущаястрока Из ДополнительноеОборудование Цикл
			Если Текущаястрока.Сумма > 0 
				И НЕ ЗначениеЗаполнено(Текущаястрока.Валюта) Тогда
				ТекстОшибки = НСтр("ru = 'Не указана Валюта на закладке ""Дополнительное оборудование""'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ДополнительноеОборудование", Сч, "Валюта"),, Отказ);
				Прервать;
			КонецЕсли;
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
	
КонецПроцедуры	
	
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Документы.упПринятиеКУчетуТранспортногоСредства.ИнициализироватьДанныеДокумента(Дата, Ссылка, ДополнительныеСвойства);
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упНаправленияДеятельностиТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упАгрегатыТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Агрегаты;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаАгрегатов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Агрегаты;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упУчетныеПараметрыТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упПробегТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	Если ДополнительныеСвойства.Свойство("ДополнительноеОборудованиеПробег") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНаработкаОборудования");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ДополнительноеОборудованиеПробег;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "Оборудование");
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("АгрегатыНаТС") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияОборудования");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.АгрегатыНаТС;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "Агрегат");	
	КонецЕсли;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиГСМ");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ОстаткиГСМ;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВидГСМ", "ВидГСМ");
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);

	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрицепыТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Прицепы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Прицеп", "Прицеп");
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПодразделенияТранспортныхСредств");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение                                                      
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	докПринятияКУчету = упУчетТранспортныхСредств.ДокументПринятияКУчетуТранспортногоСредства(ТранспортноеСредство);
	
	Если ЗначениеЗаполнено(докПринятияКУчету) И НЕ докПринятияКУчету = Ссылка Тогда
		ТекстСообщения = Нстр("ru = 'Транспортное средство %1 уже принято к учету документом %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения,ТранспортноеСредство,докПринятияКУчету);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,докПринятияКУчету,,,Отказ);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Прицепы") Тогда
		мсвУстановленныеПрицепы = ДополнительныеСвойства.Прицепы.НайтиСтроки(Новый Структура("ТекОперация",Перечисления.упТипыОперацийМонтажа.Установлено));
		Если мсвУстановленныеПрицепы.Количество() > 0 Тогда
			ТекстСообщения = Нстр("ru = 'Прицеп ""%1"" уже установлен на транспортное средство ""%2""'");   
			Для Каждого стр из мсвУстановленныеПрицепы Цикл
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, стр.Прицеп, стр.ТранспортноеСредствоСПрицепом);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ); 
			КонецЦикла; 		
			Возврат;
		КонецЕсли;			
	КонецЕсли;	
	
	СчетчикМоточасов	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "СчетчикМоточасов");
	
	Граница = Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая);
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", ТранспортноеСредство);
	РеквизитыОбъекта.Вставить("Дата"                , ДатаПринятияКУчету);	
	РеквизитыОбъекта.Вставить("ДатаОтсчетаТО"       , ДатаОтсчетаТО);	
	РеквизитыОбъекта.Вставить("ПробегОтсчетаТО"     , ПробегОтсчетаТО);
	РеквизитыОбъекта.Вставить("МоточасыОтсчетаТО"   , МоточасыОтсчетаТО);
	РеквизитыОбъекта.Вставить("Сумма"               , ОстаточнаяСтоимость);
	РеквизитыОбъекта.Вставить("Пробег"              , Пробег);
	РеквизитыОбъекта.Вставить("Одометр"             , Одометр);
	РеквизитыОбъекта.Вставить("Моточасы"            , Моточасы);
	РеквизитыОбъекта.Вставить("ВидДвижения"         , ВидДвиженияНакопления.Приход);
	РеквизитыОбъекта.Вставить("ПробегВыработка"     , ?(СчетчикМоточасов,0,ПробегДоКапитальногоРемонта));
	РеквизитыОбъекта.Вставить("МоточасыВыработка"   , ?(СчетчикМоточасов,МоточасыДоКапитальногоРемонта,0));
	РеквизитыОбъекта.Вставить("ВидДвиженияВыработка", ВидДвиженияНакопления.Приход);
	РеквизитыОбъекта.Вставить("ВидДвиженияПробег"   , Перечисления.упВидДвиженияПробег.ВводНачальныхОстатков);
	РеквизитыОбъекта.Вставить("Граница"             , Граница);
	РеквизитыОбъекта.Вставить("Подразделение"       , Подразделение);
	РеквизитыОбъекта.Вставить("Организация"         , Организация);
	РеквизитыОбъекта.Вставить("НаправлениеДеятельности", НаправлениеДеятельности);	
	РеквизитыОбъекта.Вставить("ПервичнаяСтоимость",      ПервичнаяСтоимость);	
	РеквизитыОбъекта.Вставить("ЛиквидационнаяСтоимость", ЛиквидационнаяСтоимость);	
	РеквизитыОбъекта.Вставить("СрокИспользования",       СрокИспользования);	

	ДополнительныеСвойства.Вставить("Состояние", Справочники.упСостоянияТС.Свободно);
	ДополнительныеСвойства.Вставить("ДатаИзмененияСостояния", ДатаПринятияКУчету);
	
	Документы.упПринятиеКУчетуТранспортногоСредства.ВыполнитьКонтрольАгрегатов(Ссылка, РеквизитыОбъекта, Отказ);
	Документы.упПринятиеКУчетуТранспортногоСредства.ВыполнитьКонтрольДополнительноеОборудование(Ссылка, РеквизитыОбъекта, Отказ);
	
	упУправлениеЗапасами.СформироватьДвиженияАгрегатовНаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Установка состояния транспортного средства.
	упУчетТранспортныхСредств.СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Установка Направления Деятельности транспортного средства.
	упУчетТранспортныхСредств.СформироватьДвижениеНаправленияДеятельностиТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Фиксация остаточной стоимости агрегатов.
	упВыработкаРесурсов.СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);	
	
	// Увеличение суммы по транспортному средству в регистре "Выработка". 
	упУчетТранспортныхСредств.СформироватьДвижениеВыработкаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУчетТранспортныхСредств.СформироватьДвижениеУчетныеПараметрыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// Указание начальных показаний одометра. 
	упУчетТранспортныхСредств.СформироватьДвиженияПробегТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Пробег/моточасы дополнительного оборудования.
	упУправлениеЗапасами.СформироватьДвиженияНаработкаОборудования(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Указание начальных остатков ГСМ.
	упУчетТранспортныхСредств.СформироватьДвиженияОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Привязка прицепа
	упУчетТранспортныхСредств.СформироватьДвиженияПрицепыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// Привязка подразделения
	упУчетТранспортныхСредств.СформироватьДвижениеПодразделенияТранспортныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	// СостоянияОборудования
	упУчетТранспортныхСредств.СформироватьДвиженияСостоянияОборудования(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	упПутевойЛистУчетГСМ.ПроконтролироватьПереливБака(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	Если ЛиквидационнаяСтоимость > ПервичнаяСтоимость Тогда
		ТекстСообщения = Нстр("ru = 'Ликвидационная стоимость не может быть больше первичной.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Одометр","Объект",Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти 

#КонецЕсли