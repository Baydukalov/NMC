#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Организация");
	БлокируемыеРеквизиты.Добавить("Подразделение");
	БлокируемыеРеквизиты.Добавить("ТранспортноеСредство");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение
// Заполнение дополнительных свойств документа.
//
Процедура ИнициализироватьДанныеДокумента(Дата, ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбчАгрегаты.Ссылка.ТранспортноеСредство КАК ТранспортноеСредство,
	|	тбчАгрегаты.Агрегат КАК Агрегат,
	|	тбчАгрегаты.ХодоваяШина КАК ХодоваяШина,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено) КАК Операция,
	|	тбчАгрегаты.ПробегКм КАК ПробегКм,
	|	тбчАгрегаты.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	//	тбчАгрегаты.Сумма КАК Сумма,
	|	тбчАгрегаты.Сумма * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1)) КАК Сумма,
	|	1 КАК Количество
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.Агрегаты КАК тбчАгрегаты
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		Валюта = &Валюта) КАК КурсыВалютДокумента
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		) КАК КурсыВалют
	|	ПО тбчАгрегаты.Валюта = КурсыВалют.Валюта
	|ГДЕ тбчАгрегаты.Ссылка = &Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбчАгрегаты.Ссылка.ТранспортноеСредство КАК ТранспортноеСредство,
	|	тбчАгрегаты.ДополнительноеОборудование КАК Агрегат,
	|	ЛОЖЬ КАК ХодоваяШина,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено) КАК Операция,
	|	тбчАгрегаты.ПробегКм КАК ПробегКм,
	|	тбчАгрегаты.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	//	тбчАгрегаты.Сумма,
	|	тбчАгрегаты.Сумма * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1)) КАК Сумма,
	|	1 КАК Количество
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.ДополнительноеОборудование КАК тбчАгрегаты
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		Валюта = &Валюта) КАК КурсыВалютДокумента
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		) КАК КурсыВалют
	|	ПО тбчАгрегаты.Валюта = КурсыВалют.Валюта
	|ГДЕ тбчАгрегаты.Ссылка = &Ссылка
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиГСМ.Ссылка.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ОстаткиГСМ.Количество КАК Количество,
	|	ОстаткиГСМ.Стоимость КАК Стоимость,
	|	ОстаткиГСМ.СтоимостьНДС КАК СтоимостьНДС,
	|	ОстаткиГСМ.ВидГСМ КАК ВидГСМ
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.ОстаткиГСМ КАК ОстаткиГСМ
	|ГДЕ ОстаткиГСМ.Ссылка = &Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПринятиеКУчетуТранспортногоСредстваПрицепы.Прицеп КАК Прицеп,
	|	упПринятиеКУчетуТранспортногоСредстваПрицепы.Ссылка.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ЕСТЬNULL(упПрицепы.Операция, ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.ПустаяСсылка)) КАК ТекОперация,
	|	ЕСТЬNULL(упПрицепы.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредствоСПрицепом,
	|	упПрицепы.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено) КАК Операция
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.Прицепы КАК упПринятиеКУчетуТранспортногоСредстваПрицепы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
	|		,
	|		) КАК упПрицепы
	|	ПО упПринятиеКУчетуТранспортногоСредстваПрицепы.Прицеп = упПрицепы.Прицеп
	|ГДЕ упПринятиеКУчетуТранспортногоСредстваПрицепы.Ссылка = &Ссылка
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбчДопОборудование.ДополнительноеОборудование КАК Оборудование,
	|	тбчДопОборудование.Пробег КАК Пробег,
	|	тбчДопОборудование.Моточасы КАК Моточасы
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.ДополнительноеОборудование КАК тбчДопОборудование
	|ГДЕ тбчДопОборудование.Ссылка = &Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	тбчАгрегаты.Агрегат КАК Оборудование,
	|	тбчАгрегаты.Пробег КАК Пробег,
	|	тбчАгрегаты.Моточасы КАК Моточасы
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.Агрегаты КАК тбчАгрегаты
	|ГДЕ тбчАгрегаты.Ссылка = &Ссылка
	|";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Валюта", упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета"));  
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	НаборРезультатов = Запрос.ВыполнитьПакет();
				   
	// Агрегаты
	Если Не НаборРезультатов[0].Пустой() Тогда
		ДополнительныеСвойства.Вставить("АгрегатыНаТС", НаборРезультатов[0].Выгрузить());
	КонецЕсли;
	
	// ОстаткиГСМ
	Если Не НаборРезультатов[1].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ОстаткиГСМ", НаборРезультатов[1].Выгрузить());
	КонецЕсли;

	// Прицепы
	Если Не НаборРезультатов[2].Пустой() Тогда
		ДополнительныеСвойства.Вставить("Прицепы", НаборРезультатов[2].Выгрузить());
	КонецЕсли;
	
	// Пробег дополнительного оборудования
	Если Не НаборРезультатов[3].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ДополнительноеОборудованиеПробег", НаборРезультатов[3].Выгрузить());
	КонецЕсли;

	
КонецПроцедуры

// Процедура - выполнить контроль агрегатов.
//
Процедура ВыполнитьКонтрольАгрегатов(Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	тбчАгрегаты.Ссылка КАК Ссылка,
	|	тбчАгрегаты.Агрегат КАК Агрегат
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.Агрегаты КАК тбчАгрегаты
	|ГДЕ
	|	тбчАгрегаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.Склад, ЗНАЧЕНИЕ(Справочник.упСклады.ПустаяСсылка)) КАК Склад,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.КоличествоОстаток, 0) КАК Количество
	|ПОМЕСТИТЬ втСклады
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК упАгрегатыНаСкладахОстатки
	|		ПО втДокумент.Агрегат = упАгрегатыНаСкладахОстатки.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат,
	|	ЕСТЬNULL(АгрегатыТССП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС,
	|	втСклады.Склад,
	|	ВЫБОР
	|		КОГДА АгрегатыТССП.ТранспортноеСредство ЕСТЬ NULL 
	|				ИЛИ АгрегатыТССП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК АгрегатыТССП
	|		ПО втДокумент.Агрегат = АгрегатыТССП.Агрегат
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСклады КАК втСклады
	|		ПО втДокумент.Агрегат = втСклады.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСклады.Агрегат,
	|	втСклады.Склад,
	|	ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка) КАК ТС,
	|	втСклады.Количество
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втСклады КАК втСклады
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТС.Агрегат,
	|	втТС.Склад,
	|	втТС.ТС,
	|	втТС.Количество
	|ИЗ
	|	втТС КАК втТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Агрегат,
	|	втДанные.Склад,
	|	втДанные.ТС,
	|	СУММА(втДанные.Количество) КАК Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	втДанные.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанные.Склад,
	|	втДанные.ТС,
	|	втДанные.Агрегат";
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Граница);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ТС) Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", установлен на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.ТС));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Склад) Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", находится на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - выполнить контроль ДополнительноеОборудование.
//
Процедура ВыполнитьКонтрольДополнительноеОборудование(Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	тбчАгрегаты.Ссылка КАК Ссылка,
	|	тбчАгрегаты.ДополнительноеОборудование КАК Агрегат
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.упПринятиеКУчетуТранспортногоСредства.ДополнительноеОборудование КАК тбчАгрегаты
	|ГДЕ
	|	тбчАгрегаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.Склад, ЗНАЧЕНИЕ(Справочник.упСклады.ПустаяСсылка)) КАК Склад,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.КоличествоОстаток, 0) КАК Количество
	|ПОМЕСТИТЬ втСклады
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК упАгрегатыНаСкладахОстатки
	|		ПО втДокумент.Агрегат = упАгрегатыНаСкладахОстатки.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат,
	|	ЕСТЬNULL(АгрегатыТССП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС,
	|	втСклады.Склад,
	|	ВЫБОР
	|		КОГДА АгрегатыТССП.ТранспортноеСредство ЕСТЬ NULL 
	|				ИЛИ АгрегатыТССП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК АгрегатыТССП
	|		ПО втДокумент.Агрегат = АгрегатыТССП.Агрегат
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСклады КАК втСклады
	|		ПО втДокумент.Агрегат = втСклады.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСклады.Агрегат,
	|	втСклады.Склад,
	|	ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка) КАК ТС,
	|	втСклады.Количество
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втСклады КАК втСклады
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТС.Агрегат,
	|	втТС.Склад,
	|	втТС.ТС,
	|	втТС.Количество
	|ИЗ
	|	втТС КАК втТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Агрегат,
	|	втДанные.Склад,
	|	втДанные.ТС,
	|	СУММА(втДанные.Количество) КАК Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	НЕ втДанные.Количество = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанные.Склад,
	|	втДанные.ТС,
	|	втДанные.Агрегат";
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Граница);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ТС) Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Дополнительное оборудование: ""%1"", установлено на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.ТС));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Склад) Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Дополнительное оборудование: ""%1"", находится на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьКонтрольДополнительноеОборудование()

#КонецОбласти

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Нет.
   	
КонецПроцедуры // ДобавитьКомандыПечати()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Нет.
	
КонецПроцедуры // Печать()

#КонецОбласти

#КонецЕсли
