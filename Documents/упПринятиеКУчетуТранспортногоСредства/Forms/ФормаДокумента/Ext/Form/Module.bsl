#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств = ПолучитьФункциональнуюОпцию("упВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств");
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор       = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	Элементы.ГруппаОдометр.Видимость = ВестиОтдельныйУчетПоказанийОдометровТранспортныхСредств;
	УстановитьДоступность();
	РассчитатьИтоговуюСумму();
	ВыполнитьЗаполнениеШины();
	НастроитьДоступностьВводаМоточасовПоТС();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	
	ПриСозданииПриЧтенииНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Объект.ДатаПринятияКУчету < Объект.ДатаОтсчетаТО Тогда
		ТекстСообщения = НСтр("ru = 'Дата крайнего пройденного технического обслуживания не может превышать дату принятия к учету транспортного средства!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.ДатаОтсчетаТО",, Отказ);
	КонецЕсли; 
	
	Если Объект.Пробег < Объект.ПробегОтсчетаТО Тогда
		ТекстСообщения = НСтр("ru = 'Пробег на момент крайнего пройденного технического обслуживания не может превышать остаточный пробег на дату принятия к учету транспортного средства!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.ПробегОтсчетаТО",, Отказ);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если ИмяСобытия = "ЗаписанТранспортноеСредство" 
		И Источник = Объект.ТранспортноеСредство Тогда
		НастроитьДоступностьВводаМоточасовПоТС();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(ВалютаУпрУчета) Тогда
		Для каждого ТекущаяСтрокаАгрегата Из Объект.Агрегаты Цикл
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаАгрегата.Агрегат) Тогда
				ТекущаяСтрокаАгрегата.Валюта = ВалютаУпрУчета;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура АгрегатыАгрегатПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Агрегаты.ТекущиеДанные;
	СтрокаОбъекта = Объект.Агрегаты.НайтиПоИдентификатору(Элементы.Агрегаты.ТекущаяСтрока);
	СтрокаОбъекта.ЭтоШина = ПолучитьВидАгрегата(ТекущиеДанные.Агрегат);	
	Если НЕ СтрокаОбъекта.ЭтоШина Тогда
		СтрокаОбъекта.ХодоваяШина = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.Агрегаты.ТекущиеДанные;
		текСтрока.Валюта = ВалютаУпрУчета;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.ДополнительноеОборудование.ТекущиеДанные;
		текСтрока.Валюта = ВалютаУпрУчета;		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура ОстаточнаяСтоимостьПриИзменении(Элемент)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Оповестить("ПринятКУчету", Объект.ТранспортноеСредство);	
	КонецЕсли;
	
	ВыполнитьЗаполнениеШины();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПрицепНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СписокДоступныхПрицепов = ПолучитьСписокСвободныхПрицеповНаСервере(Объект.ДатаПринятияКУчету);
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", СписокДоступныхПрицепов));
	ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ПрицепыПрицепНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПрицепПриИзменении(Элемент)
	ПользовательскоеСообщение = "";
	Прицеп = ТекущийЭлемент.ТекущиеДанные.Прицеп;
	ТекущийЭлемент.ТекущиеДанные.Прицеп = ?(ЗначениеЗаполнено(Прицеп),ПроверитьПрицепПриИзмененииНаСервере(Прицеп, Объект.ДатаПринятияКУчету, ПользовательскоеСообщение), прицеп);
	Если ЗначениеЗаполнено(ПользовательскоеСообщение) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ПользовательскоеСообщение;
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ТранспортноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ПробегПриИзменении(Элемент)
	Если Истина
		И Объект.Пробег > 0 
		И Объект.Одометр = 0 
	Тогда
		Объект.Одометр = Объект.Пробег;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыАгрегатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упШиныУзлыИАгрегаты.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
	
// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать	

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьДоступность()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		ЭтоПрицеп = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТранспортноеСредство, "Прицеп");
		Элементы.ГруппаПрицепы.Видимость = Не ЭтоПрицеп;
	Иначе
		Элементы.ГруппаПрицепы.Видимость = Истина;	
	КонецЕсли;
	
	НастроитьДоступностьВводаМоточасовПоТС();
	
КонецПроцедуры

&НаСервереБезКонтекста
// Проверка является ли элемент шиной.
//
// Параметры:
//  Агрегат  - СправочникСсылка.упШиныУзлыИАгрегаты - элемент справочника.
//
// Возвращаемое значение:
//   Булево   - значение это шина.
//
Функция ПолучитьВидАгрегата(Агрегат)

	Возврат Справочники.упШиныУзлыИАгрегаты.ЭтоШина(Агрегат);

КонецФункции // ПолучитьВидАгрегата()

&НаСервере
// Выполним заполнение таблицы на форме
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьЗаполнениеШины()
	
	// - устанвоим признак что это шина
	Для каждого тбчСтрока Из Объект.Агрегаты Цикл		
		тбчСтрока.ЭтоШина = ПолучитьВидАгрегата(тбчСтрока.Агрегат);		
	КонецЦикла;
	// - устанвоим признак что это шина
	
КонецПроцедуры // ВыполнитьЗаполнениеШины()

&НаСервере
Процедура РассчитатьИтоговуюСумму()
	Всего = Объект.Агрегаты.Итог("Сумма") + Объект.ОстаточнаяСтоимость + Объект.ДополнительноеОборудование.Итог("Сумма");
КонецПроцедуры

&НаКлиенте 
// Процедура завершения после закрытия формы выбора.
//
Процедура ПрицепыПрицепНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если Результат <> Неопределено Тогда 
		ТекущийЭлемент.ТекущиеДанные.Прицеп = Результат;	
	КонецЕсли;    
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокСвободныхПрицеповНаСервере(ДатаПринятияКУчету)
	мсвДоступныеПрицепы = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТранспортныеСредства.Ссылка КАК Прицеп
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(&ДатаПринятияКУчету, ) КАК упПрицепы
	               |		ПО упТранспортныеСредства.Ссылка = упПрицепы.Прицеп
	               |ГДЕ
	               |	упТранспортныеСредства.Прицеп = ИСТИНА
	               |	И упТранспортныеСредства.ПометкаУдаления = ЛОЖЬ
	               |	И (упПрицепы.Операция <> ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	               |			ИЛИ упПрицепы.Операция ЕСТЬ NULL )";
	Запрос.УстановитьПараметр("ДатаПринятияКУчету",Новый Граница(ДатаПринятияКУчету, ВидГраницы.Включая));				   
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл  		
			мсвДоступныеПрицепы.Добавить(выборка.Прицеп); 		
		КонецЦикла;
	КонецЕсли;
	Возврат мсвДоступныеПрицепы;	
КонецФункции // ПолучитьСписокСвободныхприцеповНаСервере()

&НаСервереБезКонтекста
Функция ПроверитьПрицепПриИзмененииНаСервере(Прицеп, ДатаПринятияКУчету, ПользовательскоеСообщение)
	Если НЕ Прицеп.Прицеп Тогда
		ПользовательскоеСообщение = "ТС " + Прицеп.Наименование + " не является прицепом."; 
		Возврат Справочники.упТранспортныеСредства.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПрицепы.Операция КАК Операция,
	               |	упПрицепы.ТранспортноеСредство.Наименование КАК ТранспортноеСредство
	               |ИЗ
	               |	РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(&ДатаПринятияКУчету, Прицеп = &Прицеп) КАК упПрицепы";
	Запрос.УстановитьПараметр("ДатаПринятияКУчету", Новый Граница(ДатаПринятияКУчету, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Прицеп", Прицеп);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Прицеп;
	Иначе
		выборка = Результат.Выбрать();
		выборка.Следующий();
		Если выборка.Операция = Перечисления.упТипыОперацийМонтажа.Снято Тогда
			Возврат Прицеп;
		Иначе
			ПользовательскоеСообщение = "Прицеп " + Прицеп.Наименование + " уже привязан к ТС " + выборка.ТранспортноеСредство + ".";  
			Возврат Справочники.упТранспортныеСредства.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ОчиститьТЧ()
	Если Объект.Прицепы.Количество() Тогда
		Объект.Прицепы.Очистить();
	КонецЕсли;
	Если Объект.ОстаткиГСМ.Количество() Тогда 
		Объект.ОстаткиГСМ.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьВводаМоточасовПоТС()

	СчетчикМоточасов = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		СчетчикМоточасов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТранспортноеСредство, "СчетчикМоточасов");
	КонецЕсли;
	
	СчетчикМоточасов = Объект.Моточасы > 0 ИЛИ СчетчикМоточасов;
	
	сткНастроек = Новый Структура;
	сткНастроек.Вставить("ИспользоватьСчетчикМоточасовТранспортныхСредств", СчетчикМоточасов);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);

КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(,Объект.ТранспортноеСредство);
		КонецЕсли;		
		
		Объект.НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,Объект.ТранспортноеСредство);
		
	КонецЕсли;
	
	Если Ложь
		Или Объект.Прицепы.Количество() 
		Или Объект.ОстаткиГСМ.Количество()
	Тогда 
		ОчиститьТЧ();
	КонецЕсли;	

	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

