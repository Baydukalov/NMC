#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
	Документы.упСписаниеМатериалов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиМатериалов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных	= ДополнительныеСвойства.ОстаткиМатериалов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Материал", "Материал");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад"   , "Склад");
	КонецЕсли; 
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата"       , Дата);
	Реквизиты.Вставить("Граница"    , Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
	
    упУправлениеЗапасами.РассчитатьСуммыСписанияМатериалов(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ);	
	
	// Формирование движений по регистру накопления "упОстаткиМатериалов"
	упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	Движения.Записать();
	
	// Контроль возникновения отрицательных остатков по складу
	Если Не ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО Тогда
		Реквизиты.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));
		Реквизиты.Вставить("ГраницаИсключая", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		упУправлениеЗапасами.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
