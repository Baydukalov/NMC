

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Период");
	БлокируемыеРеквизиты.Добавить("ПредставлениеПериода");
	БлокируемыеРеквизиты.Добавить("УменьшитьПериод");
	БлокируемыеРеквизиты.Добавить("УвеличитьПериод");
	
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// печать стандартной формы
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упУстановкаЛимитовРасходаГСМ";
  КомандаПечати.Идентификатор = "ЛимитРасходовГСМ";
  КомандаПечати.Представление = НСтр("ru = 'Лимит расходов видов горюче-смазочных материалов'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЛимитРасходовГСМ") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЛимитРасходовГСМ",
		НСтр("ru = 'Лимит расходов видов горюче-смазочных материалов'"),
		ПечатьЛимитРасходовГСМ(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упУстановкаЛимитовРасходаГСМ.ПФ_MXL_Форма");
	КонецЕсли;
	
КонецПроцедуры // Печать()

// Формирование печатной формы ТТН
Функция ПечатьЛимитРасходовГСМ(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ЛимитРасходовГСМ";
	
	ЗаполнитьТабличныйДокументЛимитРасходовГСМ(ТабличныйДокумент, МассивОбъектов, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьЛимитРасходовГСМ()

Процедура ЗаполнитьТабличныйДокументЛимитРасходовГСМ(ТабличныйДокумент, МассивОбъектов, ОбъектыПечати)
	
	ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений");
	 
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упУстановкаЛимитовРасходаГСМ.ПФ_MXL_Форма");
	
	ОбластьШапкаТаблицы	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапка1 		= Макет.ПолучитьОбласть("Шапка_1");
	ОбластьШапка2 		= Макет.ПолучитьОбласть("Шапка_2");
	ОбластьШапка3 		= Макет.ПолучитьОбласть("Шапка_3");
	ОбластьСтроки    	= Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал		= Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упУстановкаЛимитовРасходаГСМ.Ссылка КАК Ссылка,
	|	упУстановкаЛимитовРасходаГСМ.Организация КАК Организация,
	|	упУстановкаЛимитовРасходаГСМ.Подразделение КАК Подразделение,
	|	упУстановкаЛимитовРасходаГСМ.Период КАК Период
	|ПОМЕСТИТЬ вт_Документы
	|ИЗ
	|	Документ.упУстановкаЛимитовРасходаГСМ КАК упУстановкаЛимитовРасходаГСМ
	|ГДЕ
	|	упУстановкаЛимитовРасходаГСМ.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЛимитыРасходаГСМ.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упЛимитыРасходаГСМ.ВидГСМ КАК ВидГСМ,
	|	упЛимитыРасходаГСМ.Лимит КАК Лимит,
	|	упЛимитыРасходаГСМ.Регистратор КАК Документ
	|ИЗ
	|	РегистрНакопления.упЛимитыРасходаГСМ КАК упЛимитыРасходаГСМ
	|ГДЕ
	|	упЛимитыРасходаГСМ.Регистратор В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Документы.Ссылка КАК Документ,
	|	вт_Документы.Организация КАК Организация,
	|	вт_Документы.Подразделение КАК Подразделение,
	|	вт_Документы.Период КАК Период
	|ИЗ
	|	вт_Документы КАК вт_Документы";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаТаблицы = ПакетЗапросов[1].Выбрать();
	Выборка = ПакетЗапросов[2].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьШапка1.Параметры.Заполнить(Выборка);
		ОбластьШапка3.Параметры.Заполнить(Выборка);
		Если ИспользуетсяУчетПодразделений Тогда
			ОбластьШапка2.Параметры.Заполнить(Выборка);
		КонецЕсли;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ТабличныйДокумент.Вывести(ОбластьШапка1);
		Если ИспользуетсяУчетПодразделений Тогда
			ТабличныйДокумент.Вывести(ОбластьШапка2);
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьШапка3);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ТекущийОтбор = Новый Структура();
		ТекущийОтбор.Вставить("Документ",Выборка.Документ);
		ВыборкаТаблицы.Сбросить();
		
		Пока ВыборкаТаблицы.НайтиСледующий(ТекущийОтбор) Цикл
			ОбластьСтроки.Параметры.Заполнить(ВыборкаТаблицы);
			ТабличныйДокумент.Вывести(ОбластьСтроки);
		КонецЦикла;
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Документ);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьТабличныйДокументЛимитРасходовГСМ()


#КонецОбласти

#КонецЕсли

#Область ИнтерфейсДляРаботыСПодсистемойВзаимодействия

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Взаимодействия.

// Возвращает партнера и контактных лиц сделки.
// 
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаявкаНаПеревозкуГруза - Ссылка на документ.
//
// Возвращаемое значение:
//   Массив   - Контакты - Ссылки на контакты.
//
Функция ПолучитьКонтакты(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоКонтактам();
	Запрос.УстановитьПараметр("Предмет",Ссылка);
	
	НачатьТранзакцию();
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Результат = Неопределено;
		Иначе
			Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контакт");
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Вовращает текст запроса по контактам.
//
// Параметры:
//  ТекстВременнаяТаблица  - Строка - Данные.
//  Объединить - Булево - Выпонлить.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ТекстЗапросаПоКонтактам(ТекстВременнаяТаблица = "", Объединить = Ложь) Экспорт
	
	ШаблонВыбрать = ?(Объединить,"ВЫБРАТЬ РАЗЛИЧНЫЕ","ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ");
	
	ТекстЗапроса = "
	|%ШаблонВыбрать%
	|	упЗаявкаНаПеревозкуГруза.Заказчик КАК Контакт " + ТекстВременнаяТаблица + "
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.Заказчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтактноеЛицоЗаказчика
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтактноеЛицоЗаказчика = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтактноеЛицоОтправителя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтактноеЛицоОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтактноеЛицоПолучателя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтактноеЛицоПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтрагентОтправителя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтрагентПолучателя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.Отправитель
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.Получатель
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ШаблонВыбрать%",ШаблонВыбрать);
	
	Если Объединить Тогда
		
		ТекстЗапроса = "
		| ОБЪЕДИНИТЬ ВСЕ
		|" + ТекстЗапроса;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти