
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура обработчик события ПередЗаписью объекта
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	ТекстСообщения	= НСтр("ru = 'В табличной части ""%1"" присутствуют строки с продублированными позициями транспортных средств. %2.'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, "Лимиты", Ссылка);
	Для каждого ТекущийЛимит Из Лимиты Цикл		
		ТекущийИндекс = Лимиты.Индекс(ТекущийЛимит);
		ТекущийОтбор = Новый Структура();
		ТекущийОтбор.Вставить("ТранспортноеСредство", ТекущийЛимит.ТранспортноеСредство);
		ТекущийОтбор.Вставить("ВидГСМ", ТекущийЛимит.ВидГСМ);
		НайденныеСтроки = Лимиты.НайтиСтроки(ТекущийОтбор);
		Если НайденныеСтроки.Количество() > 1 Тогда
			Для каждого текСтрока Из НайденныеСтроки Цикл
				ИндексСтроки = Лимиты.Индекс(текСтрока);
				Если НЕ ТекущийИндекс = ИндексСтроки Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,"Объект.Лимиты[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].ТранспортноеСредство");
				КонецЕсли;
			КонецЦикла;		
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;	
		
КонецПроцедуры

// Процедура обработчик события ОбработкаПроведения объекта
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЛимитыРасходаГСМ");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Лимиты;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТранспортноеСредство", "ТранспортноеСредство");	
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ДвиженияЛимитыРасходаГСМ	= Движения.упЛимитыРасходаГСМ;
	ДвиженияЛимитыРасходаГСМ.Записывать = Истина;
	
	Для каждого ТекущийЛимит Из Лимиты Цикл
		ДвижениеЛимита = ДвиженияЛимитыРасходаГСМ.Добавить();
		ДвижениеЛимита.Период = Период;
		ДвижениеЛимита.Регистратор = Ссылка;
		ДвижениеЛимита.Организация = Организация;
		ДвижениеЛимита.Подразделение = Подразделение;
		
		ДвижениеЛимита.ТранспортноеСредство = ТекущийЛимит.ТранспортноеСредство;
		ДвижениеЛимита.ВидГСМ = ТекущийЛимит.ВидГСМ;
		ДвижениеЛимита.Лимит = ТекущийЛимит.Лимит;
		ДвижениеЛимита.Расход = 0;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 