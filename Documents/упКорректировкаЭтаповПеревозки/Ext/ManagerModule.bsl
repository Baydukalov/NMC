#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Заполнить дополнительные свойства документа.
//
Процедура ИнициализироватьДанныеДокумента(Ссылка, РеквизитыОбъекта, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза);
	Запрос.УстановитьПараметр("ИзмененияПлановПеревозки", РеквизитыОбъекта.ИзмененияПлановПеревозки.Выгрузить(, "ГрузовоеМесто, НомерВЦепиПоставок, НомерВЦепиПоставокКонец, Тара, КоличествоГрузов, Отменен, ОбработкаПрерывания"));
	Запрос.Текст = 
	"
	|
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененияПлановПеревозки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ИзмененияПлановПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ИзмененияПлановПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ИзмененияПлановПеревозки.КоличествоГрузов КАК Количество,
	|	ИзмененияПлановПеревозки.Отменен КАК Отменен,
	|	ИзмененияПлановПеревозки.ОбработкаПрерывания КАК ОбработкаПрерывания,
	|	ИзмененияПлановПеревозки.Тара КАК Тара
	|ПОМЕСТИТЬ втИзмененияПлановПодготовка
	|ИЗ
	|	&ИзмененияПлановПеревозки КАК ИзмененияПлановПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|	втИзмененияПланов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втИзмененияПланов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	1 КАК Количество,
	|	МАКСИМУМ(втИзмененияПланов.Отменен) КАК Отменен,
	|	СУММА(ВЫБОР КОГДА втИзмененияПланов.Отменен ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоОтменен,
	|    СУММА(1) КАК КоличествоРазличныхГрузовыхМест,
	|	МАКСИМУМ(втИзмененияПланов.ОбработкаПрерывания) КАК ОбработкаПрерывания,
	|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК Тара
	|ПОМЕСТИТЬ втИзмененияПланов
	|ИЗ
	|	втИзмененияПлановПодготовка КАК втИзмененияПланов
	|ГДЕ НЕ (ВЫРАЗИТЬ(&ЗаявкаНаПеревозкуГруза КАК Документ.упЗаявкаНаПеревозкуГруза).ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку))
	|СГРУППИРОВАТЬ ПО
	|	втИзмененияПланов.НомерВЦепиПоставок,
	|	втИзмененияПланов.НомерВЦепиПоставокКонец
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втИзмененияПланов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втИзмененияПланов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втИзмененияПланов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втИзмененияПланов.Количество КАК Количество,
	|	втИзмененияПланов.Отменен КАК Отменен,
	|	ВЫБОР КОГДА втИзмененияПланов.Отменен ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК КоличествоОтменен,
	|	1 КАК КоличествоРазличныхГрузовыхМест,
	|	втИзмененияПланов.ОбработкаПрерывания КАК ОбработкаПрерывания,
	|	втИзмененияПланов.Тара КАК Тара
	|ИЗ
	|	втИзмененияПлановПодготовка КАК втИзмененияПланов
	|ГДЕ ВЫРАЗИТЬ(&ЗаявкаНаПеревозкуГруза КАК Документ.упЗаявкаНаПеревозкуГруза).ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	втИзмененияПланов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втИзмененияПланов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втИзмененияПланов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втИзмененияПланов.Количество КАК Количество,
	|	втИзмененияПланов.Тара КАК Тара
	|ИЗ
	|	втИзмененияПланов КАК втИзмененияПланов
	|ГДЕ ИСТИНА
	|	И НЕ (втИзмененияПланов.Отменен)
	|//	И НЕ (втИзмененияПланов.ОбработкаПрерывания)
	|	И втИзмененияПланов.КоличествоОтменен = 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	упЗаявкиКВыполнениюОстатки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	упЗаявкиКВыполнениюОстатки.КоличествоОстаток КАК Количество,
	|	упЗаявкиКВыполнениюОстатки.Тара КАК Тара
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|		,
	|		Заявка = &ЗаявкаНаПеревозкуГруза) КАК упЗаявкиКВыполнениюОстатки
	|ГДЕ (упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок, упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец,
	|		упЗаявкиКВыполнениюОстатки.ГрузовоеМесто) В (
	|			ВЫБРАТЬ
	|			втИзмененияПланов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|			втИзмененияПланов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|			втИзмененияПланов.ГрузовоеМесто КАК ГрузовоеМесто
	|		ИЗ
	|			втИзмененияПланов КАК втИзмененияПланов
	|			ГДЕ Истина
	|				И втИзмененияПланов.КоличествоОтменен > 0
	|				И втИзмененияПланов.КоличествоОтменен = втИзмененияПланов.КоличествоРазличныхГрузовыхМест
	|)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИзмененияПлановПодготовкаТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	СУММА(втИзмененияПлановПодготовкаТ.Количество) КАК КоличествоГрузов,
	|	МИНИМУМ(втИзмененияПлановПодготовкаТ.НомерВЦепиПоставок) КАК НомерВЦепиПоставокКонец,
	|	МИНИМУМ(ИСТИНА) КАК Обработано,
	|	&ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозку
	|ИЗ
	|	втИзмененияПлановПодготовка КАК втИзмененияПлановПодготовкаТ
	|ГДЕ втИзмененияПлановПодготовкаТ.ОбработкаПрерывания
	|	И втИзмененияПлановПодготовкаТ.Отменен
	|СГРУППИРОВАТЬ ПО
	|	втИзмененияПлановПодготовкаТ.ГрузовоеМесто,
	|	&ЗаявкаНаПеревозкуГруза
	|";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексЗапросаЗаявкиКВыполнению = 2;
	ИндексЗапросаПрерываниеГП = 3;
	Если Не РезультатЗапроса[ИндексЗапросаЗаявкиКВыполнению].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ДвиженияЗаявкиКВыполнению", РезультатЗапроса[ИндексЗапросаЗаявкиКВыполнению].Выгрузить());
	Иначе
		ДополнительныеСвойства.Вставить("ДвиженияЗаявкиКВыполнению", Неопределено);
	КонецЕсли; 
	
	Если Не РезультатЗапроса[ИндексЗапросаПрерываниеГП].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ПрерыванияГрузовыхПотоков", РезультатЗапроса[ИндексЗапросаПрерываниеГП].Выгрузить());
	КонецЕсли;	
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

// Формируются документы корректировки этапов перевозки на основании заявки на перевозку груза
//
// Параметры:
//  ЗаявкиНаПеревозку  - Массив(ДокументСсылка.упЗаявкиНаПеревозкуГрузов) - Ссылки на документ.
//  ТекстОшибки  - Строка - Текстовое представлениеошибки.
//  Отказ  - Булево - Флаг что произошел отказ создания движения.
//
// Возвращаемое значение:
//   ДокументСсылка   - Ссылка на документ.
//
Функция ОбработатьПрерыванияГрузовыхПотоков(ИсточникЗаявок, ТекстОшибки = Неопределено, Отказ) Экспорт
	
	Результат = Новый Массив;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если ТипЗнч(ИсточникЗаявок) = Тип("ДокументСсылка.упРейс") 	Тогда
		Запрос.Текст = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаданияРейса.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втЗадание.ЗаявкаНаПеревозкуГруза КАК Заявка
		|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявкам
		|ИЗ
		|	Документ.упРейс.Задания КАК ЗаданияРейса
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК втЗадание
		|	ПО втЗадание.Ссылка = ЗаданияРейса.Задание
		|ГДЕ ЗаданияРейса.Ссылка = &ИсточникЗаявок
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втГрузовыеМестаПоЗаявкамТ.*
		|ИЗ
		|	втГрузовыеМестаПоЗаявкам КАК втГрузовыеМестаПоЗаявкамТ
		|";
	Иначе	
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ГрузовыеМеста.Ссылка КАК Заявка
		|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявкам
		|ИЗ
		|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
		|ГДЕ ГрузовыеМеста.Ссылка В (&ИсточникЗаявок)
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втГрузовыеМестаПоЗаявкамТ.*
		|ИЗ
		|	втГрузовыеМестаПоЗаявкам КАК втГрузовыеМестаПоЗаявкамТ
		|";

	КонецЕсли;

	Запрос.УстановитьПараметр("ИсточникЗаявок", ИсточникЗаявок);
	РезультатЗапроса = Запрос.Выполнить();

	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаявкиКВыполнению");
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Заявка", "Заявка");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Заявка");

	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеЭтапыПеревозки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаявкаНаПеревозкуГруза", "Заявка");
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрерыванияГрузовыхПотоков");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаявкаНаПеревозку", "Заявка");
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = СтрШаблон(Нстр("ru = 'Ошибка при установке блокировки на ресурсы для прерывания грузовых потоков по заявкам:%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;
	КонецПопытки;
	
	Если Не Отказ Тогда
		
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втГрузовыеМестаПоЗаявкам.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втГрузовыеМестаПоЗаявкам.Заявка КАК Заявка
		|ПОМЕСТИТЬ втГрузовыеМеста
		|ИЗ
		|	втГрузовыеМестаПоЗаявкам КАК втГрузовыеМестаПоЗаявкам
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК втЗаявки
		|	ПО втГрузовыеМестаПоЗаявкам.Заявка = втЗаявки.Ссылка
		|ГДЕ втЗаявки.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
		|	втГрузовыеМестаПоЗаявкам.Заявка КАК Заявка
		|ИЗ
		|	втГрузовыеМестаПоЗаявкам КАК втГрузовыеМестаПоЗаявкам
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК втЗаявки
		|	ПО втГрузовыеМестаПоЗаявкам.Заявка = втЗаявки.Ссылка
		|ГДЕ втЗаявки.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упЗаявкиКВыполнениюОстатки.Заявка КАК Заявка,
		|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	упЗаявкиКВыполнениюОстатки.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упЗаявкиКВыполнениюОстатки.КоличествоОстаток КАК Количество
		|ПОМЕСТИТЬ втЦепьПоставок
		|ИЗ
		|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(
		|		,
		|		(Заявка, ГрузовоеМесто) В (
		|				ВЫБРАТЬ
		|				втЗаявки.Заявка КАК Заявка,
		|				втЗаявки.ГрузовоеМесто КАК ГрузовоеМесто
		|			ИЗ
		|				втГрузовыеМеста КАК втЗаявки)) КАК упЗаявкиКВыполнениюОстатки
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	упЗаявкиКВыполнению.Заявка КАК Заявка,
		|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	втЗаявки.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упЗаявкиКВыполнению.Количество КАК Количество
		|ИЗ
		|	РегистрНакопления.упЗаявкиКВыполнению КАК упЗаявкиКВыполнению
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втЗаявки
		|	ПО ИСТИНА
		|		И упЗаявкиКВыполнению.Заявка = втЗаявки.Заявка
		|		И упЗаявкиКВыполнению.ГрузовоеМесто = втЗаявки.ГрузовоеМесто
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
		|	ПО ЗаданиеНаПеревозкуГруза.Ссылка = упЗаявкиКВыполнению.Регистратор
		|ГДЕ упЗаявкиКВыполнению.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрерыванияГрузовыхПотоков.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ПрерыванияГрузовыхПотоков.ЗаявкаНаПеревозку КАК ЗаявкаНаПеревозку,
		|	ПРЕДСТАВЛЕНИЕ(ПрерыванияГрузовыхПотоков.ЗаявкаНаПеревозку) КАК ЗаявкаНаПеревозкуПредставление,
		|	ПрерыванияГрузовыхПотоков.КоличествоГрузов КАК КоличествоГрузов,
		|	ПрерыванияГрузовыхПотоков.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	ПрерыванияГрузовыхПотоков.ГрузРазделен КАК ГрузРазделен
		|ПОМЕСТИТЬ втПрерываниеГрузовыхПотоков
		|ИЗ
		|	РегистрСведений.упПрерыванияГрузовыхПотоков.СрезПоследних(
		|		,
		|		ЗаявкаНаПеревозку В (
		|			ВЫБРАТЬ
		|				втЗаявки.Заявка КАК Заявка
		|			ИЗ
		|				втГрузовыеМеста КАК втЗаявки)) КАК ПрерыванияГрузовыхПотоков
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГрузаТ
		|	ПО упЗаявкаНаПеревозкуГрузаТ.Ссылка = ПрерыванияГрузовыхПотоков.ЗаявкаНаПеревозку
		|ГДЕ НЕ (ПрерыванияГрузовыхПотоков.Обработано)
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ПлановыеЭтапы.ГрузовоеМесто, ЗаявкиКВыполнению.ГрузовоеМесто) КАК ГрузовоеМестоЗаявка,
		|	ЗаявкиКВыполнению.Заявка КАК Заявка,
		|	ЗаявкиКВыполнению.Количество КАК КоличествоЗаявка,
		|	ЗаявкиКВыполнению.НомерВЦепиПоставок КАК НомерВЦепиПоставокЗаявка,
		|	ЗаявкиКВыполнению.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонецЗаявка,
		|	ПлановыеЭтапы.*
		|ПОМЕСТИТЬ втИзменениеПлановПеревозки
		|ИЗ
		|	втЦепьПоставок КАК ЗаявкиКВыполнению
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(
		|		,
		|		ЗаявкаНаПеревозкуГруза В (
		|			ВЫБРАТЬ
		|				втЗаявки.Заявка КАК Заявка
		|			ИЗ
		|				втГрузовыеМеста КАК втЗаявки)) КАК ПлановыеЭтапы
		|	ПО ИСТИНА
		|		И (ЛОЖЬ
		|			ИЛИ ЗаявкиКВыполнению.ГрузовоеМесто = ПлановыеЭтапы.ГрузовоеМесто
		|			ИЛИ ЗаявкиКВыполнению.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
		|		И ЗаявкиКВыполнению.Заявка = ПлановыеЭтапы.ЗаявкаНаПеревозкуГруза
		|		И ЗаявкиКВыполнению.НомерВЦепиПоставок = ПлановыеЭтапы.НомерВЦепиПоставок
		|		И ЗаявкиКВыполнению.НомерВЦепиПоставокКонец = ПлановыеЭтапы.НомерВЦепиПоставокКонец
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПрерываниеГрузовыхПотоковТ.ГрузовоеМесто КАК ГрузовоеМестоРодитель,
		|	втПрерываниеГрузовыхПотоковТ.ЗаявкаНаПеревозку КАК ЗаявкаНаПеревозку,
		|	втПрерываниеГрузовыхПотоковТ.ЗаявкаНаПеревозкуПредставление КАК ЗаявкаНаПеревозкуПредставление,
		|	втПрерываниеГрузовыхПотоковТ.КоличествоГрузов КАК КоличествоГрузов,
		|	втПрерываниеГрузовыхПотоковТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	РазделениеГрузовыхМест.ГрузовоеМесто КАК ГрузовоеМесто
		|ПОМЕСТИТЬ втНовыеГрузовыеМеста
		|ИЗ
		|	втПрерываниеГрузовыхПотоков КАК втПрерываниеГрузовыхПотоковТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРазделениеГрузовыхМест.СрезПоследних(
		|		,
		|		) КАК РазделениеГрузовыхМест
		|	ПО РазделениеГрузовыхМест.Родитель = втПрерываниеГрузовыхПотоковТ.ГрузовоеМесто
		|ГДЕ РазделениеГрузовыхМест.Проблема В (ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка))
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПрерываниеГрузовыхПотоковТ.*
		|ИЗ
		|	втПрерываниеГрузовыхПотоков КАК втПрерываниеГрузовыхПотоковТ
		|ИТОГИ
		|	МАКСИМУМ(ЗаявкаНаПеревозкуПредставление)
		|ПО
		|	ЗаявкаНаПеревозку,
		|	НомерВЦепиПоставокКонец
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втИзменениеПлановПеревозкиТ.АдресОтправления КАК АдресОтправления,
		|	втИзменениеПлановПеревозкиТ.АдресПолучения КАК АдресПолучения,
		|	втИзменениеПлановПеревозкиТ.ВидПеревозки КАК ВидПеревозки,
		|	втИзменениеПлановПеревозкиТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
		|	втИзменениеПлановПеревозкиТ.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
		|	втИзменениеПлановПеревозкиТ.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втИзменениеПлановПеревозкиТ.ГрузовоеМестоЗаявка КАК ГрузовоеМестоЗаявка,
		|	втИзменениеПлановПеревозкиТ.Заказчик КАК Заказчик,
		|	втИзменениеПлановПеревозкиТ.Заявка КАК Заявка,
		|	втИзменениеПлановПеревозкиТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	втИзменениеПлановПеревозкиТ.ЗонаОтправления КАК ЗонаОтправления,
		|	втИзменениеПлановПеревозкиТ.ЗонаПолучения КАК ЗонаПолучения,
		|	втИзменениеПлановПеревозкиТ.Изменен КАК Изменен,
		|	втИзменениеПлановПеревозкиТ.КоличествоГрузов КАК КоличествоГрузов,
		|	втИзменениеПлановПеревозкиТ.КоличествоЗаявка КАК КоличествоЗаявка,
		|	втИзменениеПлановПеревозкиТ.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
		|	втИзменениеПлановПеревозкиТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
		|	втИзменениеПлановПеревозкиТ.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
		|	втИзменениеПлановПеревозкиТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
		|	втИзменениеПлановПеревозкиТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
		|	втИзменениеПлановПеревозкиТ.КонтрагентПолучателя КАК КонтрагентПолучателя,
		|	втИзменениеПлановПеревозкиТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставокЗаявка КАК НомерВЦепиПоставокЗаявка,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставокКонецЗаявка КАК НомерВЦепиПоставокКонецЗаявка,
		|	втИзменениеПлановПеревозкиТ.Организация КАК Организация,
		|	втИзменениеПлановПеревозкиТ.Отменен КАК Отменен,
		|	втИзменениеПлановПеревозкиТ.Отправитель КАК Отправитель,
		|	втИзменениеПлановПеревозкиТ.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
		|	втИзменениеПлановПеревозкиТ.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
		|	втИзменениеПлановПеревозкиТ.Период КАК Период,
		|	втИзменениеПлановПеревозкиТ.Подразделение КАК Подразделение,
		|	втИзменениеПлановПеревозкиТ.Получатель КАК Получатель,
		|	втИзменениеПлановПеревозкиТ.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
		|	втИзменениеПлановПеревозкиТ.ПолучениеГрузаС КАК ПолучениеГрузаС,
		|	втИзменениеПлановПеревозкиТ.Приоритет КАК Приоритет,
		|	втИзменениеПлановПеревозкиТ.Регистратор КАК Регистратор,
		|	втИзменениеПлановПеревозкиТ.Соглашение КАК Соглашение,
		|	втИзменениеПлановПеревозкиТ.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
		|	втИзменениеПлановПеревозкиТ.Тара КАК Тара,
		|	втИзменениеПлановПеревозкиТ.ТипПеревозки КАК ТипПеревозки,
		|	втИзменениеПлановПеревозкиТ.ЧасовойПояс КАК ЧасовойПояс,
		|	Ложь КАК ГрузРазделен
		|ИЗ
		|	втИзменениеПлановПеревозки КАК втИзменениеПлановПеревозкиТ
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втИзменениеПлановПеревозкиТ.АдресОтправления КАК АдресОтправления,
		|	втИзменениеПлановПеревозкиТ.АдресПолучения КАК АдресПолучения,
		|	втИзменениеПлановПеревозкиТ.ВидПеревозки КАК ВидПеревозки,
		|	втИзменениеПлановПеревозкиТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
		|	втИзменениеПлановПеревозкиТ.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
		|	втНовыеГрузовыеМестаТ.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втНовыеГрузовыеМестаТ.ГрузовоеМестоРодитель КАК ГрузовоеМестоЗаявка,
		|	втИзменениеПлановПеревозкиТ.Заказчик КАК Заказчик,
		|	втИзменениеПлановПеревозкиТ.Заявка КАК Заявка,
		|	втИзменениеПлановПеревозкиТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	втИзменениеПлановПеревозкиТ.ЗонаОтправления КАК ЗонаОтправления,
		|	втИзменениеПлановПеревозкиТ.ЗонаПолучения КАК ЗонаПолучения,
		|	втИзменениеПлановПеревозкиТ.Изменен КАК Изменен,
		|	втИзменениеПлановПеревозкиТ.КоличествоГрузов КАК КоличествоГрузов,
		|	втИзменениеПлановПеревозкиТ.КоличествоЗаявка КАК КоличествоЗаявка,
		|	втИзменениеПлановПеревозкиТ.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
		|	втИзменениеПлановПеревозкиТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
		|	втИзменениеПлановПеревозкиТ.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
		|	втИзменениеПлановПеревозкиТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
		|	втИзменениеПлановПеревозкиТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
		|	втИзменениеПлановПеревозкиТ.КонтрагентПолучателя КАК КонтрагентПолучателя,
		|	втИзменениеПлановПеревозкиТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставокЗаявка КАК НомерВЦепиПоставокЗаявка,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	втИзменениеПлановПеревозкиТ.НомерВЦепиПоставокКонецЗаявка КАК НомерВЦепиПоставокКонецЗаявка,
		|	втИзменениеПлановПеревозкиТ.Организация КАК Организация,
		|	втИзменениеПлановПеревозкиТ.Отменен КАК Отменен,
		|	втИзменениеПлановПеревозкиТ.Отправитель КАК Отправитель,
		|	втИзменениеПлановПеревозкиТ.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
		|	втИзменениеПлановПеревозкиТ.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
		|	втИзменениеПлановПеревозкиТ.Период КАК Период,
		|	втИзменениеПлановПеревозкиТ.Подразделение КАК Подразделение,
		|	втИзменениеПлановПеревозкиТ.Получатель КАК Получатель,
		|	втИзменениеПлановПеревозкиТ.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
		|	втИзменениеПлановПеревозкиТ.ПолучениеГрузаС КАК ПолучениеГрузаС,
		|	втИзменениеПлановПеревозкиТ.Приоритет КАК Приоритет,
		|	втИзменениеПлановПеревозкиТ.Регистратор КАК Регистратор,
		|	втИзменениеПлановПеревозкиТ.Соглашение КАК Соглашение,
		|	втИзменениеПлановПеревозкиТ.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
		|	втИзменениеПлановПеревозкиТ.Тара КАК Тара,
		|	втИзменениеПлановПеревозкиТ.ТипПеревозки КАК ТипПеревозки,
		|	втИзменениеПлановПеревозкиТ.ЧасовойПояс КАК ЧасовойПояс,
		|	Истина КАК ГрузРазделен
		|ИЗ
		|	втИзменениеПлановПеревозки КАК втИзменениеПлановПеревозкиТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНовыеГрузовыеМеста КАК втНовыеГрузовыеМестаТ
		|	ПО втНовыеГрузовыеМестаТ.ГрузовоеМестоРодитель = втИзменениеПлановПеревозкиТ.ГрузовоеМестоЗаявка
		|";

				
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		ИндексЗапросаПрерыванияГП = 5;
		ИндексЗапросаЦепьПоставок = 6;
		
		Если Не РезультатЗапроса[ИндексЗапросаПрерыванияГП].Пустой() Тогда
			
		
			ВыборкаПрерыванияГП = РезультатЗапроса[ИндексЗапросаПрерыванияГП].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			тбзЦепьПоставок = РезультатЗапроса[ИндексЗапросаЦепьПоставок].Выгрузить();
			
			Пока ВыборкаПрерыванияГП.Следующий() Цикл
				
				Заявка = ВыборкаПрерыванияГП.ЗаявкаНаПеревозку;
				ЗаявкаПредставление = ВыборкаПрерыванияГП.ЗаявкаНаПеревозкуПредставление;
				
				КорректировкаЭтаповПеревозки = Документы.упКорректировкаЭтаповПеревозки.СоздатьДокумент();
				КорректировкаЭтаповПеревозки.Автор = ПользователиКлиентСервер.ТекущийПользователь();
				КорректировкаЭтаповПеревозки.Дата = ТекущаяДатаСеанса();
				КорректировкаЭтаповПеревозки.ЗаявкаНаПеревозкуГруза = Заявка;
				ИзмененияПлановПеревозки = КорректировкаЭтаповПеревозки.ИзмененияПлановПеревозки;
				
				ВыборкаНомерЦепи = ВыборкаПрерыванияГП.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								
				Пока ВыборкаНомерЦепи.Следующий() Цикл
									
					НомерВЦепиПоставок = ВыборкаНомерЦепи.НомерВЦепиПоставокКонец;
																				
					мсвНомераВЦепиПоставок = Новый Массив; // Для проверки на зацикливание
					мсвСтекДляПоискаЗвеньев = Новый Массив;
					мсвСтекДляПоискаЗвеньев.Добавить(НомерВЦепиПоставок);
					
					Пока мсвСтекДляПоискаЗвеньев.Количество() > 0 Цикл
						
						ИндексПоследнего = мсвСтекДляПоискаЗвеньев.ВГраница();
						НомерВЦепиПоставокТекущий = мсвСтекДляПоискаЗвеньев[ИндексПоследнего];
						мсвСтекДляПоискаЗвеньев.Удалить(ИндексПоследнего);
						Если мсвНомераВЦепиПоставок.Найти(НомерВЦепиПоставокТекущий) = Неопределено Тогда
							мсвНомераВЦепиПоставок.Добавить(НомерВЦепиПоставокТекущий);
						Иначе
							ТекстОшибки = СтрШаблон(Нстр("ru = 'Произошло зацикливание этапов перевозки на номере цепи:%1; %2'"),НомерВЦепиПоставокТекущий, Заявка);
							Отказ = Истина;
							Прервать;
						КонецЕсли;
						
						ВыборкаГрузовоеМесто = ВыборкаНомерЦепи.Выбрать();
						
						ОтборТолькоПоЗаявке = Ложь;
						
						Пока ВыборкаГрузовоеМесто.Следующий() Цикл
													
							ГрузовоеМесто= ВыборкаГрузовоеМесто.ГрузовоеМесто;
							
							мсвСтрокиЦепиПоставокПоЗаявке = тбзЦепьПоставок.НайтиСтроки(Новый Структура("НомерВЦепиПоставокЗаявка, Заявка, ГрузовоеМестоЗаявка",
							НомерВЦепиПоставокТекущий,
							Заявка, 
							ГрузовоеМесто));
							
							// Если по какой либо причине плановые этапы не заполнены
							Если Истина
								И ЗначениеЗаполнено(ГрузовоеМесто)
								И мсвСтрокиЦепиПоставокПоЗаявке.Количество() = 0 
							Тогда
								// Анализируются данные целиком по заявке.
								мсвСтрокиЦепиПоставокПоЗаявке = тбзЦепьПоставок.НайтиСтроки(Новый Структура("НомерВЦепиПоставокЗаявка, Заявка, ГрузовоеМестоЗаявка",
								НомерВЦепиПоставокТекущий,
								Заявка, 
								Справочники.упГрузовыеМеста.ПустаяСсылка()));
								ОтборТолькоПоЗаявке = Истина;
							КонецЕсли;
							
							Если мсвСтрокиЦепиПоставокПоЗаявке.Количество() > 0 Тогда
								Для каждого ЗвеноЦепи Из мсвСтрокиЦепиПоставокПоЗаявке Цикл
									
									Если мсвСтекДляПоискаЗвеньев.Найти(ЗвеноЦепи.НомерВЦепиПоставокКонец) = Неопределено Тогда
										мсвСтекДляПоискаЗвеньев.Добавить(ЗвеноЦепи.НомерВЦепиПоставокКонец);	
									КонецЕсли;
									
									НоваяСтрока = ИзмененияПлановПеревозки.Добавить();
									Если ЗначениеЗаполнено(ЗвеноЦепи.ЗаявкаНаПеревозкуГруза) Тогда
										ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗвеноЦепи);
									Иначе	
										Если ЗвеноЦепи.ГрузРазделен Тогда
											НоваяСтрока.ГрузовоеМесто = ЗвеноЦепи.ГрузовоеМесто;
										Иначе
											НоваяСтрока.ГрузовоеМесто = ЗвеноЦепи.ГрузовоеМестоЗаявка;
										КонецЕсли;
										НоваяСтрока.КоличествоГрузов = ЗвеноЦепи.КоличествоЗаявка;
										НоваяСтрока.НомерВЦепиПоставок = ЗвеноЦепи.НомерВЦепиПоставокЗаявка;
										НоваяСтрока.НомерВЦепиПоставокКонец = ЗвеноЦепи.НомерВЦепиПоставокКонецЗаявка;
									КонецЕсли;
									Если ЗвеноЦепи.ГрузРазделен Тогда
										НоваяСтрока.Отменен = Ложь;
									Иначе	
										НоваяСтрока.Отменен = Истина;
									КонецЕсли;	
									НоваяСтрока.ОбработкаПрерывания = Истина;
									
								КонецЦикла;
							КонецЕсли;
							
							Если ОтборТолькоПоЗаявке Тогда
								Прервать;
							КонецЕсли;
							
						КонецЦикла;
					КонецЦикла;
					
					Если Отказ Тогда
						Прервать
					КонецЕсли;
									
				КонецЦикла;
				
				Если Не Отказ Тогда
					Попытка
						КорректировкаЭтаповПеревозки.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
						Результат.Добавить(КорректировкаЭтаповПеревозки.Ссылка);
					Исключение
						ПредставлениеОшибки = Неопределено;
						ТекстОшибки = Нстр("ru = 'Не удалось Обработка прерывание грузового потока по %1. Произошла ошибка: %2'");
						Если КорректировкаЭтаповПеревозки.ДополнительныеСвойства.Свойство("Ошибка", ПредставлениеОшибки) Тогда
							ТекстОшибки = СтрШаблон(ТекстОшибки, ЗаявкаПредставление, ПредставлениеОшибки);
						Иначе
							ТекстОшибки = СтрШаблон(ТекстОшибки, ЗаявкаПредставление, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						КонецЕсли;
						ЗаписьЖурналаРегистрации(НСтр("ru = 'Обработка прерывание грузового потока'"), УровеньЖурналаРегистрации.Ошибка, ,ЗаявкаПредставление, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
						Отказ = Истина;
					КонецПопытки;
				КонецЕсли;
				
				Если Отказ Тогда
					Прервать
				КонецЕсли;
					
			КонецЦикла; 
		Иначе
			ТекстСообщения = Нстр("ru = 'Не найдено прерывания грузового потока.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	Если Не Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе	
		ОтменитьТранзакцию();
	КонецЕсли;

	Возврат Результат;	
	
КонецФункции // ОбработатьПрерыванияГрузовыхПотоков()

#КонецОбласти

#КонецЕсли

