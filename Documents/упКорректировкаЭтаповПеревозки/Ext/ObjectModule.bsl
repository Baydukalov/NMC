
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		
		Отказ = Истина
			   И РежимЗаписи = РежимЗаписиДокумента.Проведение
			   И РежимПроведения = РежимПроведенияДокумента.Неоперативный;
		
		Если Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Запрещено неоперативное проведение документа.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
			Событие = Нстр("ru = 'Ошибка при корректировке этапов перевозки'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
		КонецЕсли;
		
		Если Не Отказ Тогда
			Отказ = упКорректировкаЭтаповПеревозки.ЗапрещеноРедактироватьЭтапыМультимодальнойПеревозки(ЭтотОбъект);
		КонецЕсли;
		
		Если Истина
			И Не Отказ
			И РежимЗаписи = РежимЗаписиДокумента.Проведение 
		Тогда
		
			ТекстОшибки = "";
			упКорректировкаЭтаповПеревозки.ОтменитьЗаданияНаПеревозкуПоЭтапам(ЭтотОбъект, ТекстОшибки, Отказ);	
			
		КонецЕсли;
		
	КонецЕсли;
		
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	сткРеквизиты = Новый Структура();
	сткРеквизиты.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", "ВидПеревозки.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки");
	сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаявкаНаПеревозкуГруза, сткРеквизиты);
	ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки = сткДанные.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки;
	ДополнительныеСвойства.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки);
		
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаявкиКВыполнению");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Заявка", ЗаявкаНаПеревозкуГруза);
	
	Если ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеЭтапыПеревозки");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	КонецЕсли;
		
	тбзПрерыванияГрузовыхПотоков = Неопределено;
	Если ДополнительныеСвойства.Свойство("ПрерыванияГрузовыхПотоков", тбзПрерыванияГрузовыхПотоков) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрерыванияГрузовыхПотоков");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	КонецЕсли;
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	РеквизитыОбъекта = Новый Структура(); 
	РеквизитыОбъекта.Вставить("Дата", Дата);
	РеквизитыОбъекта.Вставить("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	РеквизитыОбъекта.Вставить("ИзмененияПлановПеревозки", ИзмененияПлановПеревозки);
	
	ДвиженияЗаданияНаПеревозку = Движения.упЗаявкиКВыполнению;
	ДвиженияЗаданияНаПеревозку.Очистить();	
	ДвиженияЗаданияНаПеревозку.Записывать = Истина;
	
	ДвиженияСтатусыЗаявок = Движения.упСтатусыЗаявокНаПеревозкуГруза;
	ДвиженияСтатусыЗаявок.Очистить();	
	ДвиженияСтатусыЗаявок.Записывать = Истина;
	Движения.Записать();
		
	Документы.упКорректировкаЭтаповПеревозки.ИнициализироватьДанныеДокумента(Ссылка, РеквизитыОбъекта, ДополнительныеСвойства);
	
	упКорректировкаЭтаповПеревозки.СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упКорректировкаЭтаповПеревозки.СформироватьДвиженияПлановыеЭтапыМультимодальнойПеревозки(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	упКорректировкаМаршрута.СформироватьДвижениеПрерыванияГрузовыхПотоков(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	ДополнительныеСвойства.Вставить("РассчитатьСтатус", Истина);
	ДополнительныеСвойства.Вставить("ЗаписыватьСтатус", Ложь);
	ДополнительныеСвойства.Вставить("Статус", упРаботаСоСтатусами.ПолучитьТекущийСтатус(РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза));
	ДополнительныеСвойства.Вставить("ДатаСтатуса", ТекущаяДатаСеанса());
	упУправленияЗаявками.СформироватьДвиженияСтатусЗаявки(ДополнительныеСвойства, РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза, Движения, РеквизитыОбъекта, Отказ);	
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли 

