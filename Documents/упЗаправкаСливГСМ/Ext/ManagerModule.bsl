#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Документы.упЗаправкаСливГСМ.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Заполнение временного параметра "ОстаткиМатериалов".
//
Процедура ИнициализироватьДанныеДокумента(Дата, ВидДвиженияТоплива, ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	тбзОстаткиМатериалов = Неопределено;
	
	Если ВидДвиженияТоплива = Перечисления.упВидыДвиженияТоплива.Слив Тогда
		 Запрос = Новый Запрос;
		 Запрос.Текст = "ВЫБРАТЬ
		                |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		                |	упЗаправкаСливГСМ.ГСМ КАК Материал,
						|	&Партия КАК Партия,
		                |	упЗаправкаСливГСМ.Склад,
		                |	упЗаправкаСливГСМ.Количество,
		                |	упЗаправкаСливГСМ.Стоимость,
		                |	упЗаправкаСливГСМ.Стоимость + упЗаправкаСливГСМ.СтоимостьНДС КАК СтоимостьСНДС,
		                |	упЗаправкаСливГСМ.Валюта,
						|	упЗаправкаСливГСМ.Стоимость КАК СуммаБезНДС,
						|	упЗаправкаСливГСМ.Стоимость + упЗаправкаСливГСМ.СтоимостьНДС КАК СуммаСНДС
		                |ИЗ
		                |	Документ.упЗаправкаСливГСМ КАК упЗаправкаСливГСМ
		                |ГДЕ
		                |	упЗаправкаСливГСМ.Ссылка = &Ссылка";
		 Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		 Запрос.УстановитьПараметр("Партия", ?(ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО, ДокументСсылка, документы.упПоступлениеМатериалов.ПустаяСсылка()));
		 тбзОстаткиМатериалов = Запрос.Выполнить().Выгрузить();
		 ВедетсяВалютныйУчет = ДополнительныеСвойства.ВедетсяВалютныйУчет;
		 Если ВедетсяВалютныйУчет Тогда
			 Для каждого Строка Из тбзОстаткиМатериалов Цикл
				 Строка.СуммаБезНДС 	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СуммаБезНДС, Дата);
				 Строка.СуммаСНДС 	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Строка.Валюта, Строка.СуммаСНДС, Дата);	
			 КонецЦикла;
		 КонецЕсли; 
	
	Иначе	
		 Запрос = Новый Запрос;
		 Запрос.Текст = "ВЫБРАТЬ
		 		        |	упЗаправкаСливГСМ.Склад,
		                |	упЗаправкаСливГСМ.ГСМ КАК Материал,
		                |	упЗаправкаСливГСМ.Количество
						|ИЗ
		                |	Документ.упЗаправкаСливГСМ КАК упЗаправкаСливГСМ
		                |ГДЕ
		                |	упЗаправкаСливГСМ.Ссылка = &Ссылка";
		 Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		 тбзОстаткиМатериалов = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Если НЕ тбзОстаткиМатериалов = Неопределено 
			И тбзОстаткиМатериалов.Количество()>0 Тогда
		ДополнительныеСвойства.Вставить("ОстаткиМатериалов", тбзОстаткиМатериалов);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#КонецЕсли