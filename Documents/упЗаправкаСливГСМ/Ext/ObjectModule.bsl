#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий	
	
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
	
	Если СпособЗаправки = Перечисления.упСпособыЗаправки.СкладГСМ Тогда
		ДополнительныеСвойства.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
		Документы.упЗаправкаСливГСМ.ИнициализироватьДанныеДокумента(Дата, ВидДвижения, Ссылка, ДополнительныеСвойства);
	КонецЕсли;	
	
	ЕстьБолееПоздниеДокументы(Отказ);
	Если НЕ Отказ Тогда
		РеквизитыОбъекта = Новый Структура();
		РеквизитыОбъекта.Вставить("Дата", 					Дата);										
		РеквизитыОбъекта.Вставить("ТранспортноеСредство", 	ТранспортноеСредство);										
		РеквизитыОбъекта.Вставить("ВидДвижения", 			ВидДвижения);										
		РеквизитыОбъекта.Вставить("СпособЗаправки", 		СпособЗаправки);										
		РеквизитыОбъекта.Вставить("Склад", 					Склад);
		
		Если СпособЗаправки = Перечисления.упСпособыЗаправки.СкладГСМ 
				И ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка Тогда
			РеквизитыОбъекта.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		КонецЕсли;
			
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиГСМ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", 	ТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("ВидГСМ", 					ВидГСМ);
		
		Если СпособЗаправки = Перечисления.упСпособыЗаправки.СкладГСМ Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиМатериалов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Материал", 	ГСМ);
			Если ВидДвижения = Перечисления.упВидыДвиженияТоплива.Слив Тогда
				ЭлементБлокировки.УстановитьЗначение("Партия", 	Ссылка);	
			КонецЕсли;
			ЭлементБлокировки.УстановитьЗначение("Склад", 		Склад);
		КонецЕсли;
		
		Попытка
			БлокировкаДанных.Заблокировать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
			ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Возврат;
		КонецПопытки;
		
		тбзОстаткиГСМ = упПутевойЛистУчетГСМ.СтруктураТаблицыОстаткиГСМ();
		
		СтрокаОстаткиГСМ = тбзОстаткиГСМ.Добавить();
		СтрокаОстаткиГСМ.Период					= Дата;
		СтрокаОстаткиГСМ.ТранспортноеСредство 	= ТранспортноеСредство;
		СтрокаОстаткиГСМ.ВидГСМ 				= ВидГСМ;
		СтрокаОстаткиГСМ.ТопливнаяКарта			= ТопливнаяКарта;
		СтрокаОстаткиГСМ.Количество 			= Количество;
		Если ВедетсяВалютныйУчет Тогда
			СтрокаОстаткиГСМ.Стоимость 				= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Валюта, Стоимость, Дата);
			СтрокаОстаткиГСМ.СтоимостьНДС 			= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Валюта, СтоимостьНДС, Дата);	
		Иначе	
			СтрокаОстаткиГСМ.Стоимость 				= Стоимость;
			СтрокаОстаткиГСМ.СтоимостьНДС 			= СтоимостьНДС;	
		КонецЕсли;
		СтрокаОстаткиГСМ.ВидДвиженияТоплива 	= ВидДвижения;
		СтрокаОстаткиГСМ.ВидДвижения 			= ?(ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
		
		// Если это слив в ТС или доп.оборудование 
		Если ВидДвижения = Перечисления.упВидыДвиженияТоплива.Слив
			И СпособЗаправки = Перечисления.упСпособыЗаправки.ТранспортноеСредство Тогда
			
			// Увеличиваются остатки по соответствующему ТС или доп.оборудованию
			СтрокаОстаткиГСМ = тбзОстаткиГСМ.Добавить();
			СтрокаОстаткиГСМ.Период					= Дата;
			СтрокаОстаткиГСМ.ТранспортноеСредство 	= Склад;
			СтрокаОстаткиГСМ.ВидГСМ 				= ВидГСМ;
			СтрокаОстаткиГСМ.ТопливнаяКарта			= ТопливнаяКарта;
			СтрокаОстаткиГСМ.Количество 			= Количество;
			Если ВедетсяВалютныйУчет Тогда
				СтрокаОстаткиГСМ.Стоимость 				= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Валюта, Стоимость, Дата);
				СтрокаОстаткиГСМ.СтоимостьНДС 			= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Валюта, СтоимостьНДС, Дата);	
			Иначе	
				СтрокаОстаткиГСМ.Стоимость 				= Стоимость;
				СтрокаОстаткиГСМ.СтоимостьНДС 			= СтоимостьНДС;	
			КонецЕсли;
			СтрокаОстаткиГСМ.ВидДвиженияТоплива 	= ВидДвижения;
			СтрокаОстаткиГСМ.ВидДвижения 			= ВидДвиженияНакопления.Приход;
			
		КонецЕсли;	
		
		ДополнительныеСвойства.Вставить("тбзОстаткиГСМ", 		тбзОстаткиГСМ);										
		
		упПутевойЛистУчетГСМ.СформироватьДвижениеОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формирование движений по регистру накопления "упОстаткиМатериалов"
		Если СпособЗаправки = Перечисления.упСпособыЗаправки.СкладГСМ Тогда
			Если ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка Тогда
				упУправлениеЗапасами.РассчитатьСуммыСписанияМатериалов(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);	
			КонецЕсли;
			упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		КонецЕсли;	
								
		Движения.Записать();
				
		упПутевойЛистУчетГСМ.ПроконтролироватьОтрицательныеОстатки(ВидГСМ, ТранспортноеСредство, Отказ);
		упПутевойЛистУчетГСМ.ПроконтролироватьПереливБака(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
				
		Если СпособЗаправки = Перечисления.упСпособыЗаправки.СкладГСМ 
				И ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка 
				И Не ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО Тогда
			РеквизитыОбъекта.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));	
			РеквизитыОбъекта.Вставить("ГраницаИсключая", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
			// Контроль возникновения отрицательных остатков по складу
	        упУправлениеЗапасами.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

Процедура ЕстьБолееПоздниеДокументы(Отказ)
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиГСМ");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", 	ТранспортноеСредство);
	ЭлементБлокировки.УстановитьЗначение("ВидГСМ", 					ВидГСМ);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Отказ = НЕ упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийОстаткиГСМ(Ссылка, Дата, ТранспортноеСредство, ВидГСМ);
	
	Если Отказ Тогда
		ТекстСообщения = Нстр("ru = 'Существуют более поздние документы изменяющие остатки ГСМ.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ЕстьБолееПоздниеДокументы(Отказ);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособЗаправки = Перечисления.упСпособыЗаправки.СкладГСМ Тогда
		ПроверяемыеРеквизиты.Добавить("Склад");
		ПроверяемыеРеквизиты.Добавить("ГСМ");
	ИначеЕсли СпособЗаправки = Перечисления.упСпособыЗаправки.ТранспортноеСредство Тогда
		ПроверяемыеРеквизиты.Добавить("ТранспортноеСредство");
	ИначеЕсли СпособЗаправки = Перечисления.упСпособыЗаправки.СторонняяОрганизация Тогда	
		ПроверяемыеРеквизиты.Добавить("Контрагент");	
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
