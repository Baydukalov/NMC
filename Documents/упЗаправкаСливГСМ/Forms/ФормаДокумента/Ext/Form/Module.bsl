
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если ТекущийОбъект.Проведен Тогда
		ТолькоПросмотр = НЕ упПутевойЛистУчетГСМ.ЭтоПоследнийДокументИзменявшийОстаткиГСМ(ТекущийОбъект.Ссылка, ТекущийОбъект.Дата, ТекущийОбъект.ТранспортноеСредство, ТекущийОбъект.ВидГСМ);	
	КонецЕсли;
	УстановитьПараметрыВыбораТопливнойКартыНаСервере();
	Если Не Параметры.Свойство("ТолькоПросмотр") Тогда
		УстановитьПараметрыВыбораТопливнойКартыНаСервере();
	КонецЕсли; 
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если ЭтаФорма.ВедетсяВалютныйУчет  Тогда
			Объект.Валюта = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();		
		КонецЕсли;
		
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Объект.СпособЗаправки = Перечисления.упСпособыЗаправки.СторонняяОрганизация;
		Если ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
	КонецЕсли;
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.Дата);

	ОбновитьПредставлениеГрафичекихЭлементов();
		
	УстановитьПараметрыВыбораТопливнойКартыНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ИспользуетсяУчетПодразделений		= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяВалютныйУчет				= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ЭтаФорма.ИспользуютсяДополнительныеОперации = ПолучитьФункциональнуюОпцию("упИспользуютсяДополнительныеОперации");
	
	ПриСозданииПриЧтенииНаСервере();		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПереключитьСтраницуВидаДвиженияТоплива();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство, ТекущийОбъект.Дата);

	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПартнерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь; 	
	сткПараметры = Новый Структура("Отбор", Новый Структура("АЗС", Истина));
	ОткрытьФорму("Справочник.упПартнеры.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ПартнерНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	ЗаполнитьПоПартнеру();
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь; 
	сткОтбор = Новый Структура;
	сткОтбор.Вставить("ТипСоглашения", ПредопределенноеЗначение("Перечисление.упТипыПрочихСоглашений.СоглашениеСАЗС"));
	Если ЗначениеЗаполнено(Объект.Партнер) Тогда
		сткОтбор.Вставить("Владелец",Объект.Партнер);
	КонецЕсли;
	сткПараметры = Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упПрочиеСоглашения.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("СоглашениеНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", Объект.Стоимость, Объект.Стоимость, Объект.СтавкаНДС, Объект.СтоимостьНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	Объект.СтоимостьНДС	= сткСтрока.СуммаНДС;
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПриИзменении(Элемент)
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", Объект.Стоимость, Объект.Стоимость, Объект.СтавкаНДС, Объект.СтоимостьНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	Объект.СтоимостьНДС		= сткСтрока.СуммаНДС;
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", Объект.Стоимость, Объект.Стоимость, Объект.СтавкаНДС, Объект.СтоимостьНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	Объект.СтоимостьНДС		= сткСтрока.СуммаНДС;
КонецПроцедуры

&НаКлиенте
Процедура ВидГСМПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидГСМ) Тогда
		УстановитьСтавкуНДС();
	КонецЕсли;
	УстановитьПараметрыВыбораТопливнойКартыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДвиженияПриИзменении(Элемент)
	ОбновитьПредставлениеГрафичекихЭлементов();
	Объект.СпособЗаправки = ПредопределенноеЗначение("Перечисление.упСпособыЗаправки.СкладГСМ");
	Объект.Склад 			= Неопределено;
	Объект.Партнер 			= ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка");
	Объект.ТопливнаяКарта	= ПредопределенноеЗначение("Справочник.упТопливныеКарты.ПустаяСсылка");
	Объект.Контрагент		= ПредопределенноеЗначение("Справочник.упКонтрагенты.ПустаяСсылка");
	ПереключитьСтраницуВидаДвиженияТоплива();
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаправкиПриИзменении(Элемент)
	Объект.Склад 			= Неопределено;
	Объект.Партнер 			= ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка");
	Объект.ТопливнаяКарта	= ПредопределенноеЗначение("Справочник.упТопливныеКарты.ПустаяСсылка");
	Объект.Контрагент		= ПредопределенноеЗначение("Справочник.упКонтрагенты.ПустаяСсылка");
	ПереключитьСтраницуСпособаЗаправки();
КонецПроцедуры

&НаКлиенте
Процедура СпособСливаПриИзменении(Элемент)
	Объект.Склад = Неопределено;
	ПереключитьСтраницуСпособаСлива();
КонецПроцедуры

&НаКлиенте
Процедура СкладНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Объект.Склад = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.упСклады.Форма.ФормаВыбора",,Элементы.Склад,УникальныйИдентификатор);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкладГСМДляСливаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Объект.Склад = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.упСклады.Форма.ФормаВыбора",,Элементы.СкладГСМДляСлива,УникальныйИдентификатор);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоДляСливаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Если ЗначениеЗаполнено(Объект.ВидГСМ) Тогда
		Если Объект.Склад = Неопределено Тогда
			
			СтандартнаяОбработка = Ложь;
			
			спзВыборТипа = Новый СписокЗначений;
			спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"));
			
			Если ИспользуютсяДополнительныеОперации Тогда
				спзВыборТипа.Добавить(Новый ОписаниеТипов("СправочникСсылка.упДополнительноеОборудование"));	
			КонецЕсли;
			
			текОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораТипаДляВыбораТранспортногоСредства", ЭтаФорма);
			
			ПоказатьВыборИзМеню(текОписаниеОповещения, спзВыборТипа, Элемент);
		Иначе
			сткПараметры 	= Новый Структура("ВидГСМ", Объект.ВидГСМ);
			текЭлемент 		= Неопределено;
			Если ТипЗнч(Объект.Склад) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда
				текИмяФормы			= "Справочник.упТранспортныеСредства.ФормаВыбора";
				текЭлемент			= Элементы.ТранспортноеСредствоДляСлива;
			ИначеЕсли ТипЗнч(Объект.Склад) = Тип("СправочникСсылка.упДополнительноеОборудование") Тогда
				текИмяФормы			= "Справочник.упДополнительноеОборудование.ФормаВыбора";
				текЭлемент			= Элементы.ТранспортноеСредствоДляСлива;
			Иначе
				текИмяФормы			= "Справочник.упСклады.ФормаВыбора";
				текЭлемент			= Элементы.СкладГСМДляСлива;
			КонецЕсли;
			ОткрытьФорму(текИмяФормы,сткПараметры,текЭлемент,УникальныйИдентификатор);
		КонецЕсли;
	Иначе
		ТекстСообщения = Нстр("ru = 'Не заполнено поле ""Вид ГСМ"".'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.ВидГСМ");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПредставлениеГрафичекихЭлементов()
	
	Если Объект.ВидДвижения = Перечисления.упВидыДвиженияТоплива.Заправка Тогда
		Элементы.ГруппаГСМ.Картинка = БиблиотекаКартинок.упЗаправка;
	Иначе
		Элементы.ГруппаГСМ.Картинка = БиблиотекаКартинок.упСлив;
	КонецЕсли; 
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаСервере
Процедура УстановитьСтавкуНДС()
	СтатьяРасходов 		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидГСМ, "СтатьяРасходов");
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		Объект.СтавкаНДС 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "СтавкаНДС");	
		Если НЕ Объект.Стоимость = 0 Тогда
			СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
			сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", Объект.Стоимость, Объект.Стоимость, Объект.СтавкаНДС, Объект.СтоимостьНДС, 1);
			упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
			Объект.СтоимостьНДС	= сткСтрока.СуммаНДС;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницуВидаДвиженияТоплива()
	Если Объект.ВидДвижения = ПредопределенноеЗначение("Перечисление.упВидыДвиженияТоплива.Заправка") Тогда
		Элементы.ГруппаСпособыЗаправкиСлива.ТекущаяСтраница = Элементы.ГруппаЗаправка;
		ПереключитьСтраницуСпособаЗаправки();
	Иначе
		Элементы.ГруппаСпособыЗаправкиСлива.ТекущаяСтраница = Элементы.ГруппаСливТоплива;
		ПереключитьСтраницуСпособаСлива()
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницуСпособаЗаправки()
	Элементы.Контрагент.АвтоОтметкаНезаполненного = Ложь;
	Если Объект.СпособЗаправки = ПредопределенноеЗначение("Перечисление.упСпособыЗаправки.СкладГСМ") Тогда
		Элементы.ГруппаСпособыЗаправки.ТекущаяСтраница	= Элементы.ГруппаСкладГСМ;
	Иначе
		Элементы.Контрагент.АвтоОтметкаНезаполненного = Истина;
		Элементы.ГруппаСпособыЗаправки.ТекущаяСтраница	= Элементы.ГруппаСторонняяОрганизация;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницуСпособаСлива()
	Если Объект.СпособЗаправки = ПредопределенноеЗначение("Перечисление.упСпособыЗаправки.СкладГСМ") Тогда
		Элементы.ГруппаСпособыСлива.ТекущаяСтраница	= Элементы.ГруппаСливСкладГСМ;
	Иначе	
		Элементы.ГруппаСпособыСлива.ТекущаяСтраница	= Элементы.ГруппаСливТранспортноеСредство;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процдура завершения после выбора типа.
//
Процедура ЗавершениеВыбораТипаДляВыбораТранспортногоСредства(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		
		сткПараметры = Новый Структура("ВидГСМ", Объект.ВидГСМ);
		Если ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.упТранспортныеСредства")) Тогда
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
			текИмяФормы			= "Справочник.упТранспортныеСредства.ФормаВыбора";
		ИначеЕсли ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.упДополнительноеОборудование")) Тогда
			текПустоеЗначение 	= ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка");
			текИмяФормы			= "Справочник.упДополнительноеОборудование.ФормаВыбора";
		КонецЕсли;
		
		Объект.Склад = текПустоеЗначение;
		ОткрытьФорму(текИмяФормы,сткПараметры,Элементы.ТранспортноеСредствоДляСлива,УникальныйИдентификатор);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора партнера.
//
Процедура ПартнерНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если НЕ Результат = Неопределено Тогда	
		Объект.Партнер = Результат;	
		ЗаполнитьПоПартнеру();
	КонецЕсли;  	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПартнеру()
	Объект.Соглашение = Неопределено;
	Объект.Контрагент = Неопределено;
	Если ЗначениеЗаполнено(Объект.Партнер) Тогда
		Объект.Соглашение = ПолучитьОсновноеСоглашение(Объект.Партнер,Объект.Организация);
		Объект.Контрагент = ПолучитьОсновногоКонтрагента(Объект.Партнер);
	КонецЕсли;
КонецПроцедуры // ЗаполнитьПоПартнеру()

&НаКлиенте
// Процедура завершения после выбора соглашения.
//
Процедура СоглашениеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если Результат<>Неопределено Тогда	
		Объект.Соглашение = Результат;	
	КонецЕсли;  	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.Дата);
	УстановитьПараметрыВыбораТопливнойКартыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораТопливнойКартыНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТопливнаяКарта) Тогда
		// Когда вид топлива не указан в карте заполним пустым значением.
		тбчВидыТоплива = Объект.ТопливнаяКарта.ВидыТоплива;
		текОтбор = Новый Структура("ВидТоплива",Объект.ВидГСМ);
		НайденныеСтроки = тбчВидыТоплива.НайтиСтроки(текОтбор);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Объект.ТопливнаяКарта = ПредопределенноеЗначение("Справочник.упТопливныеКарты.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
"
|//{Запрос: НазначенныеТопливныеКарты     ////////////////////////////////////////
|ВЫБРАТЬ
|	упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта КАК ТопливнаяКарта
|ПОМЕСТИТЬ НазначенныеТопливныеКарты
|ИЗ
|	РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних(
|		&Дата,
|		) КАК упВыдачаТопливныхКартСрезПоследних
|ГДЕ ЛОЖЬ
|	ИЛИ (ИСТИНА
|		И &ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
|		И упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство = &ТранспортноеСредство
|		И упВыдачаТопливныхКартСрезПоследних.Водитель = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
|	ИЛИ (ИСТИНА
|		И &Водитель <> ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
|		И упВыдачаТопливныхКартСрезПоследних.Водитель = &Водитель
|		И упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
|	ИЛИ (ИСТИНА
|		И &ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
|		И &Водитель <> ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
|		И упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство = &ТранспортноеСредство
|		И упВыдачаТопливныхКартСрезПоследних.Водитель = &Водитель)
|
|;
|//{Запрос: НеназначенныеТопливныеКарты ////////////////////////////////////////
|ВЫБРАТЬ
|	упТопливныеКарты.Ссылка КАК ТопливнаяКарта
|ПОМЕСТИТЬ НеназначенныеТопливныеКарты
|ИЗ
|	Справочник.упТопливныеКарты КАК упТопливныеКарты
|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упВыдачаТопливныхКарт.СрезПоследних(
|		&Дата,
|		) КАК упВыдачаТопливныхКартСрезПоследних
|	ПО упТопливныеКарты.Ссылка = упВыдачаТопливныхКартСрезПоследних.ТопливнаяКарта
|ГДЕ
|	упВыдачаТопливныхКартСрезПоследних.ТранспортноеСредство ЕСТЬ NULL
|;
|//{Запрос: ПодходящиеНеназначенныеТопливныеКарты ////////////////////////////////////////
|ВЫБРАТЬ
|	ТопливныеКартыТ.ТопливнаяКарта КАК ТопливнаяКарта
|ИЗ
|	НеназначенныеТопливныеКарты КАК ТопливныеКартыТ
|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты.ВидыТоплива КАК УпТопливныеКартыПоВидуТоплива
|	ПО УпТопливныеКартыПоВидуТоплива.Ссылка = ТопливныеКартыТ.ТопливнаяКарта
|		И УпТопливныеКартыПоВидуТоплива.ВидТоплива = &ВидТоплива
|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты.ВидыТоплива КАК УпТопливныеКартыИтоги
|	ПО УпТопливныеКартыИтоги.Ссылка = ТопливныеКартыТ.ТопливнаяКарта
|СГРУППИРОВАТЬ ПО
|	ТопливныеКартыТ.ТопливнаяКарта
|ИМЕЮЩИЕ
|	КОЛИЧЕСТВО(УпТопливныеКартыИтоги.ВидТоплива) = 0
|		ИЛИ СУММА(ВЫБОР
|				КОГДА НЕ УпТопливныеКартыПоВидуТоплива.ВидТоплива ЕСТЬ NULL
|					ТОГДА 1
|				ИНАЧЕ 0
|			КОНЕЦ) > 0
|;
|//{Запрос: ПодходящиеНазначенныеТопливныеКарты ////////////////////////////////////////
|ВЫБРАТЬ
|	ТопливныеКартыТ.ТопливнаяКарта КАК ТопливнаяКарта
|ИЗ
|	НазначенныеТопливныеКарты КАК ТопливныеКартыТ
|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты.ВидыТоплива КАК УпТопливныеКартыПоВидуТоплива
|	ПО УпТопливныеКартыПоВидуТоплива.Ссылка = ТопливныеКартыТ.ТопливнаяКарта
|		И УпТопливныеКартыПоВидуТоплива.ВидТоплива = &ВидТоплива
|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТопливныеКарты.ВидыТоплива КАК УпТопливныеКартыИтоги
|	ПО УпТопливныеКартыИтоги.Ссылка = ТопливныеКартыТ.ТопливнаяКарта
|СГРУППИРОВАТЬ ПО
|	ТопливныеКартыТ.ТопливнаяКарта
|ИМЕЮЩИЕ
|	КОЛИЧЕСТВО(УпТопливныеКартыИтоги.ВидТоплива) = 0
|		ИЛИ СУММА(ВЫБОР
|				КОГДА НЕ УпТопливныеКартыПоВидуТоплива.ВидТоплива ЕСТЬ NULL
|					ТОГДА 1
|				ИНАЧЕ 0
|			КОНЕЦ) > 0";
	Индекс_НазначенныеТопливныеКарты = 0;
	Индекс_НеназначенныеТопливныеКарты = 1;
	Индекс_ПодходящиеНеназначенныеТопливныеКарты = 2;
	Индекс_ПодходящиеНазначенныеТопливныеКарты = 3;
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Запрос.УстановитьПараметр("Водитель", Объект.Водитель);
	Запрос.УстановитьПараметр("ВидТоплива", Объект.ВидГСМ);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	РезультатПакета = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатПакета.Количество();
	// Назначенные
	ТопливныеКарты = РезультатПакета[Индекс_ПодходящиеНазначенныеТопливныеКарты].Выгрузить().ВыгрузитьКолонку(0);
	Если ТопливныеКарты.Количество() = 0 Тогда
		// Неназначенные
		ТопливныеКарты = РезультатПакета[Индекс_ПодходящиеНеназначенныеТопливныеКарты].Выгрузить().ВыгрузитьКолонку(0);
	КонецЕсли; 
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(Элементы.ТопливнаяКарта, "Ссылка", ТопливныеКарты);

КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	
	УстановитьПараметрыВыбораТопливнойКартыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	УстановитьПараметрыВыбораТопливнойКартыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновноеСоглашение(Партнер,Организация)
	
	Результат = Справочники.упПрочиеСоглашения.ПустаяСсылка();
	 
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Соглашения.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.упПрочиеСоглашения КАК Соглашения
	|ГДЕ
	|	НЕ Соглашения.ПометкаУдаления И Соглашения.ТипСоглашения = ЗНАЧЕНИЕ(Перечисление.упТипыПрочихСоглашений.СоглашениеСАЗС)
	|	И Соглашения.Владелец = &Владелец
	|	И ВЫБОР КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ТОГДА ИСТИНА
	|	ИНАЧЕ Соглашения.Организация = &Организация КОНЕЦ";
	Запрос.УстановитьПараметр("Владелец", Партнер);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Соглашение;
	Иначе
		Результат = Справочники.упПрочиеСоглашения.СоглашениеПоУмолчанию(Партнер);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьОсновноеСоглашение()

#КонецОбласти
