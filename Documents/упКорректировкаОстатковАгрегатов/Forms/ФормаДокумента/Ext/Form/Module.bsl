
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();		
	КонецЕсли;
		
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ВедетсяВалютныйУчет 	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаУпрУчета 	= упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура АгрегатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьВыборТипаИзСписка", ЭтаФорма);
		ПоказатьВыборИзМеню(ОписаниеОповещенияОЗавершении, СписокВыбораТипа(),Элемент);
	Иначе	
		ТекстСообщения = Нстр("ru = 'Поле ""Склад"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,"Склад","Объект");
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
Функция СписокВыбораТипа()
	
	СписокТипов = Новый СписокЗначений;

	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования") Тогда
		СписокТипов.Добавить(Справочники.упДополнительноеОборудование.ПустаяСсылка(), Справочники.упДополнительноеОборудование.ПустаяСсылка().Метаданные().Синоним);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетШинУзловИАгрегатов") Тогда
		СписокТипов.Добавить(Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка(), Справочники.упШиныУзлыИАгрегаты.ПустаяСсылка().Метаданные().Синоним);
	КонецЕсли;
	
	Возврат СписокТипов;
	
КонецФункции

&НаКлиенте
// Процедура завершения после закрытия формы подбора агрегатов.
//
Процедура ПодборАгрегатовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Объект.Агрегат = Результат;

КонецПроцедуры // ПодборАгрегатовЗавершение()

&НаКлиенте
Процедура ОбработатьВыборТипаИзСписка(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		Состояния = Новый СписокЗначений;
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
		сткПараметры.Вставить("ТипОборудования", ВыбранныйЭлемент.Значение);
		сткПараметры.Вставить("Склад", Объект.Склад);
		сткПараметры.Вставить("Состояние", Состояния);
		сткПараметры.Вставить("ЗакрыватьПриВыбореАгрегата", Истина);
		сткПараметры.Вставить("ТипДокумента", Тип("ДокументСсылка.упКорректировкаОстатковАгрегатов"));
		ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,Элементы.Агрегат,,,, Новый ОписаниеОповещения("ПодборАгрегатовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

