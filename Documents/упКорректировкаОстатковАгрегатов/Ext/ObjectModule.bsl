#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Если Не Отказ Тогда
			// Проверка на наличие более поздних документов изменяющих состояние агрегатов и доп. оборудования.
			мсвОборудование = Новый Массив;
			мсвОборудование.Добавить(Агрегат);
			ДополнительныеСвойства.Вставить("Оборудование", мсвОборудование);
			
			Если РежимПроведения = РежимПроведенияДокумента.Неоперативный Тогда
				упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка,  ДополнительныеСвойства.Оборудование, Дата, Отказ);
			КонецЕсли;

		КонецЕсли;
		
		Если Истина
			И Не Отказ
			И РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		Тогда
			Документы.упКорректировкаОстатковАгрегатов.ИнициализироватьДанныеДокумента(Дата, Ссылка, ДополнительныеСвойства);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Документы.упКорректировкаОстатковАгрегатов.ИнициализироватьДанныеДокумента(Дата, Ссылка, ДополнительныеСвойства);
	
	// Проверка существования более поздних документов, изменяющих состояние агрегатов и доп. оборудования.
	// Дублирована здесь из-за оперативного проведения документа, перед записью дата еще не известна.
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, ДополнительныеСвойства.Оборудование, Дата, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упАгрегатыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Агрегат", Агрегат);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаАгрегатов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Агрегат", Агрегат);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияОборудования");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Оборудование", Агрегат);
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата", Дата);
	Реквизиты.Вставить("Склад", Склад);
	Реквизиты.Вставить("Агрегат", Агрегат);
	
	Граница = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	Реквизиты.Вставить("Граница", Граница);
			
	упУправлениеЗапасами.СформироватьДвиженияАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	упВыработкаРесурсов.СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	упУчетТранспортныхСредств.СформироватьДвиженияСостоянияОборудованияКорректировки(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	Движения.Записать();
	
	упКонтрольОстатков.КонтрольОтрицательныхОстатковАгрегатовНаСкладах(ДополнительныеСвойства, Ссылка, Отказ);
	
	Документы.упКорректировкаОстатковАгрегатов.ВыполнитьКонтрольАгрегатов(Ссылка, Реквизиты, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		упКонтрольОстатков.КонтрольОтрицательныхОстатковАгрегатовНаСкладах(ДополнительныеСвойства, Ссылка, Отказ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли