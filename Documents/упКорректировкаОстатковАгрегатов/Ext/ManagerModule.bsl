#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура заполнения дополнительных свойств.
//
Процедура ИнициализироватьДанныеДокумента(Дата, ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упКорректировкаОстатковАгрегатов.Склад КАК Склад,
	|	упКорректировкаОстатковАгрегатов.Агрегат КАК Агрегат,
	|	упКорректировкаОстатковАгрегатов.ПробегКм КАК ПробегКм,
	|	упКорректировкаОстатковАгрегатов.ПробегМоточасы КАК ПробегМоточасы,
	|	упКорректировкаОстатковАгрегатов.ОстаточнаяСтоимость КАК ОстаточнаяСтоимость
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.упКорректировкаОстатковАгрегатов КАК упКорректировкаОстатковАгрегатов
	|ГДЕ упКорректировкаОстатковАгрегатов.Ссылка = &Ссылка
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	АгрегатыНаСкладах.Агрегат КАК Агрегат,
	|	АгрегатыНаСкладах.Склад КАК Склад,
	|	АгрегатыНаСкладах.КоличествоОстаток КАК Количество,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|ИЗ
	|	РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|		&Период,
	|		Агрегат В (
	|			ВЫБРАТЬ
	|				втДокумент.Агрегат КАК Агрегат
	|			ИЗ
	|				втДокумент КАК втДокумент)) КАК АгрегатыНаСкладах
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	втДокумент.Склад КАК Склад,
	|	1 КАК Количество,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
	|ИЗ
	|	втДокумент КАК втДокумент
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	упВыработкаАгрегатов.Агрегат КАК Агрегат,
	|	упВыработкаАгрегатов.ПробегКмОстаток КАК ПробегКм,
	|	упВыработкаАгрегатов.ПробегМотоЧасыОстаток КАК ПробегМоточасы,
	|	упВыработкаАгрегатов.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.упВыработкаАгрегатов.Остатки(
	|		&Период,
	|		Агрегат В (
	|			ВЫБРАТЬ
	|				втДокумент.Агрегат КАК Агрегат
	|			ИЗ
	|				втДокумент КАК втДокумент)) КАК упВыработкаАгрегатов
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	втДокумент.Агрегат КАК Агрегат,
	|	втДокумент.ПробегКм КАК ПробегКм,
	|	втДокумент.ПробегМоточасы КАК ПробегМоточасы,
	|	втДокумент.ОстаточнаяСтоимость КАК Сумма
	|ИЗ
	|	втДокумент КАК втДокумент
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокументТ.Агрегат КАК Агрегат,
	|	втДокументТ.Склад КАК Склад,
	|	1 КАК СтрокаДокумента
	|ИЗ
	|	втДокумент КАК втДокументТ
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	упАгрегатыНаСкладах.Агрегат КАК Агрегат,
	|	упАгрегатыНаСкладах.Склад КАК Склад,
	|	0 КАК СтрокаДокумента
	|ИЗ
	|	РегистрНакопления.упАгрегатыНаСкладах КАК упАгрегатыНаСкладах
	|ГДЕ упАгрегатыНаСкладах.Регистратор = &Ссылка
	|";

	Граница = Новый Граница(ДокументСсылка.МоментВремени(), ВидГраницы.Включая);
	
	Запрос.УстановитьПараметр("Период",Граница);
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
		
	Результат = Запрос.ВыполнитьПакет();			   
	
	ИндексОстаткиАгрегатов = 1;
	ИндексВыработкаАгрегатов = 2;
	ИндексКонтрольАгрегатов = 3; 
		
	ДополнительныеСвойства.Вставить("ОстаткиАгрегатов", Результат[ИндексОстаткиАгрегатов].Выгрузить());
	ДополнительныеСвойства.Вставить("ВыработкаАгрегатов", Результат[ИндексВыработкаАгрегатов].Выгрузить());
	ДополнительныеСвойства.Вставить("КонтрольАгрегатов", Результат[ИндексКонтрольАгрегатов].Выгрузить());// Контролируются отрицательные остатки на складах.
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Процедура - выполнить контроль агрегатов.
//
Процедура ВыполнитьКонтрольАгрегатов(Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ДокументАгрегаты.Ссылка КАК Ссылка,
	|	ДокументАгрегаты.Склад КАК Склад,
	|	ДокументАгрегаты.Агрегат КАК Агрегат
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.упКорректировкаОстатковАгрегатов КАК ДокументАгрегаты
	|ГДЕ
	|	ДокументАгрегаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат,
	|	ЕСТЬNULL(АгрегатыТССП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС,
	|	ВЫБОР
	|		КОГДА АгрегатыТССП.ТранспортноеСредство ЕСТЬ NULL 
	|				ИЛИ АгрегатыТССП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК АгрегатыТССП
	|		ПО втДокумент.Агрегат = АгрегатыТССП.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТС.Агрегат,
	|	втТС.ТС КАК ТС,
	|	втТС.Количество
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втТС КАК втТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Агрегат,
	|	втДанные.ТС,
	|	СУММА(втДанные.Количество) КАК Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	втДанные.Количество <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанные.ТС,
	|	втДанные.Агрегат";
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Граница);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ТС) Тогда
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", установлен на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.ТС));
			Иначе	
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Доп. оборудование: ""%1"", установлено на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.ТС));
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
