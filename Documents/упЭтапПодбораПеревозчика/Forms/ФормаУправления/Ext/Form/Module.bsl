
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Рейс = Параметры.Рейс;
	ОбновитьДеревоЭтаповНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьДеревоНаКлиенте();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
		
	Если ИмяСобытия = "СменаСтатусаЗаявкиНаТС" Тогда
		
		ЭлементыДерева = ДеревоЭтапов.ПолучитьЭлементы();
		Для каждого текЭтап Из ЭлементыДерева Цикл
			ЭлементыИтерации = текЭтап.ПолучитьЭлементы();
			Для каждого текИтерация Из ЭлементыИтерации Цикл
				Если текИтерация.ЗаявкаНаТС = Источник Тогда
					ОбновитьДеревоЭтаповНаСервере();
					РазвернутьДеревоНаКлиенте();
					Возврат;
				КонецЕсли; 
			КонецЦикла; 
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоЭтаповВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ДеревоЭтапов.ТекущиеДанные;
	Если ЗначениеЗаполнено(текСтрока.ЗаявкаНаТС) Тогда
		ОткрытьФорму("Документ.упЗаявкаНаТС.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ЗаявкаНаТС), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СвернутьДерево(Команда)
	
	ЭлементыДерева = ДеревоЭтапов.ПолучитьЭлементы();
	Для каждого текЭлемент Из ЭлементыДерева Цикл
		Элементы.ДеревоЭтапов.Свернуть(текЭлемент.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево(Команда)
	
	РазвернутьДеревоНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСледующейИтерации(Команда)
	
	ТекущийЭтап = ПолучитьТекущийЭтапНаСервере();
	Если ЗначениеЗаполнено(ТекущийЭтап) Тогда
		ЭтоТендер = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ТекущийЭтап, "Тендер");
		Если ЭтоТендер Тогда
			ТекстВопроса = НСтр("ru = 'Текущий этап является тендером, переход к следующей итерации по данному этапу невозможен. Перейти к следующему этапу (все заявки на ТС по нему будут отменены)?'");
			ПоказатьВопрос(Новый ОписаниеОповещения("ВопросПерейтиКСледующемуЭтапу", ЭтаФорма, Новый Структура("ТекущийЭтап", ТекущийЭтап)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ТекущаяИтерация = ПолучитьТекущуюИтерациюНаСервере();
			Если ЗначениеЗаполнено(ТекущаяИтерация) Тогда
				ТекстВопроса = НСтр("ru = 'Текущая итерация будет отклонена. Продолжить?'");
				ПоказатьВопрос(Новый ОписаниеОповещения("ВопросПерейтиКСледующейИтерации", ЭтаФорма, Новый Структура("ТекущаяИтерация", ТекущаяИтерация)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Иначе	
				ПерейтиКСледующейИтерацииНаСервере();
				РазвернутьДеревоНаКлиенте();
			КонецЕсли; 
		КонецЕсли; 
	Иначе
		ПроверитьСледующийЭтапНаТендер();	
		
	КонецЕсли; 
				
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСледующемуЭтапу(Команда)
	
	ТекущийЭтап = ПолучитьТекущийЭтапНаСервере();
	
	Если ЗначениеЗаполнено(ТекущийЭтап) Тогда
		сткРеквизиты = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(ТекущийЭтап, "Завершен, ЗавершенУспешно, Отклонен");
		Если Не сткРеквизиты.Отклонен И Не сткРеквизиты.Завершен И Не сткРеквизиты.ЗавершенУспешно Тогда
			ТекстВопроса = НСтр("ru = 'Текущий этап будет закрыт, и все заявки на ТС по нему будут отменены. Продолжить?'");
			ПоказатьВопрос(Новый ОписаниеОповещения("ВопросПерейтиКСледующемуЭтапу", ЭтаФорма, Новый Структура("ТекущийЭтап", ТекущийЭтап)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли; 
	Иначе
		ПроверитьСледующийЭтапНаТендер();	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДеревоЭтапов(Команда)
	
	ОбновитьДеревоЭтаповНаСервере();
	РазвернутьДеревоНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РазвернутьДеревоНаКлиенте()
	
	ЭлементыДерева = ДеревоЭтапов.ПолучитьЭлементы();
	Для каждого текЭлемент Из ЭлементыДерева Цикл
		Элементы.ДеревоЭтапов.Развернуть(текЭлемент.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ОпределитьСледующийЭтап()
	
	СледующийЭтап = Неопределено;
	
	ЭлементыЭтапы = ДеревоЭтапов.ПолучитьЭлементы();
	Для каждого текЭтап Из ЭлементыЭтапы Цикл
		Если Не ЗначениеЗаполнено(текЭтап.Этап) Тогда
			СледующийЭтап = текЭтап;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат СледующийЭтап;

КонецФункции

&НаКлиенте
Процедура ПроверитьСледующийЭтапНаТендер()
	
	СледующийЭтап = ОпределитьСледующийЭтап();
	Если СледующийЭтап = Неопределено Тогда
		Возврат;
	Иначе	
		Если СледующийЭтап.Тендер Тогда
			МассивПеревозчиков = ПолучитьМассивПеревозчиков(Рейс, СледующийЭтап.ЭтапСхемыВыбораПеревозчика, Истина, СледующийЭтап.ВидТранспорта);
			Если МассивПеревозчиков.Количество() Тогда
				
				СписокПеревозчиков = Новый СписокЗначений;
				СписокПеревозчиков.ЗагрузитьЗначения(МассивПеревозчиков);
				
				сткПараметры = Новый Структура;
				сткПараметры.Вставить("КлючУникальности", УникальныйИдентификатор);
				сткПараметры.Вставить("Представление", НСтр("ru = 'участников тендера'"));
				сткПараметры.Вставить("ОграничиватьВыборУказаннымиЗначениями", Истина);
				сткПараметры.Вставить("ЗначенияДляВыбора", СписокПеревозчиков);
				
				сткПараметры.Вставить("Отмеченные"   , СписокПеревозчиков);
				сткПараметры.Вставить("ОписаниеТипов", Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
				ОткрытьФорму("ОбщаяФорма.ВводЗначенийСпискомСФлажками", сткПараметры, ЭтотОбъект,,,, Новый ОписаниеОповещения("ВыборПеревозчиковДляТендераЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Иначе	
				ПерейтиКСледующемуЭтапуНаСервере();
				РазвернутьДеревоНаКлиенте();
			КонецЕсли; 
			
		Иначе
			ПерейтиКСледующемуЭтапуНаСервере();
			РазвернутьДеревоНаКлиенте();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры		

&НаСервере
Функция ПолучитьМассивПеревозчиков(Рейс, ЭтапСхемы, ЭтоТендер, ВидТранспорта)
	
	Возврат Документы.упЭтапПодбораПеревозчика.ПодобратьПеревозчиков(Рейс, ЭтапСхемы, ЭтоТендер,, ВидТранспорта).ВыгрузитьКолонку("Перевозчик");
	
КонецФункции

&НаСервере
Функция ПолучитьТекущийЭтапНаСервере()
	
	Возврат Документы.упЭтапПодбораПеревозчика.ПолучитьТекущийЭтап(Рейс);
	
КонецФункции

&НаСервере
Функция ПолучитьТекущуюИтерациюНаСервере()
	
	ТекущаяИтерация = Документы.упЭтапПодбораПеревозчика.ПолучитьТекущуюИтерацию(Рейс);
	Если ЗначениеЗаполнено(ТекущаяИтерация) Тогда
		Статус = Документы.упЗаявкаНаТС.ПолучитьСтатус(ТекущаяИтерация);
		Если Статус = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Или Статус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
			ТекущаяИтерация = Неопределено;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат ТекущаяИтерация;
	
КонецФункции

&НаСервере
Процедура ОбновитьДеревоЭтаповНаСервере()
	
	ДеревоЭтапов.ПолучитьЭлементы().Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.ВидПеревозки.СхемаВыбораПеревозчика КАК СхемаВыбораПеревозчика
	|ПОМЕСТИТЬ втРейс
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|ГДЕ
	|	упРейс.Ссылка = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЭтапы.НомерСтроки КАК НомерЭтапа,
	|	упЭтапы.Этап КАК ЭтапСхемыВыбораПеревозчика,
	|	упЭтапы.РазыгрыватьТендер КАК Тендер,
	|	упЭтапы.ВидТранспорта
	|ПОМЕСТИТЬ втВозможныеЭтапы
	|ИЗ
	|	Справочник.упСхемыВыбораПеревозчика.Этапы КАК упЭтапы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейс КАК втРейс
	|		ПО упЭтапы.Ссылка = втРейс.СхемаВыбораПеревозчика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЭтапПодбораПеревозчика.НомерЭтапа,
	|	упЭтапПодбораПеревозчика.Ссылка КАК Этап,
	|	упЭтапПодбораПеревозчика.Завершен,
	|	упЭтапПодбораПеревозчика.ЗавершенУспешно,
	|	упЭтапПодбораПеревозчика.Отклонен,
	|	упЭтапПодбораПеревозчика.Тендер,
	|	упЭтапПодбораПеревозчика.ВидТранспорта,
	|	упЭтапПодбораПеревозчика.ЭтапСхемыВыбораПеревозчика
	|ПОМЕСТИТЬ втТекущиеЭтапы
	|ИЗ
	|	Документ.упЭтапПодбораПеревозчика КАК упЭтапПодбораПеревозчика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейс КАК втРейс
	|		ПО упЭтапПодбораПеревозчика.Рейс = втРейс.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеЭтапы.Этап,
	|	упЗаявки.НомерСтроки КАК Итерация,
	|	упЗаявки.ЗаявкаНаТС КАК ЗаявкаНаТС
	|ПОМЕСТИТЬ втИтерацииПоЭтапам
	|ИЗ
	|	втТекущиеЭтапы КАК втТекущиеЭтапы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЭтапПодбораПеревозчика.Заявки КАК упЗаявки
	|		ПО втТекущиеЭтапы.Этап = упЗаявки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтатусыЗаявокНаТССрезПоследних.Документ КАК ЗаявкаНаТС,
	|	упСтатусыЗаявокНаТССрезПоследних.Статус
	|ПОМЕСТИТЬ втСтатусыЗаявок
	|ИЗ
	|	РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних(
	|			,
	|			Документ В
	|				(ВЫБРАТЬ
	|					втИтерацииПоЭтапам.ЗаявкаНаТС
	|				ИЗ
	|					втИтерацииПоЭтапам)) КАК упСтатусыЗаявокНаТССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВозможныеЭтапы.НомерЭтапа,
	|	ЕСТЬNULL(втТекущиеЭтапы.Этап, ЗНАЧЕНИЕ(Документ.упЭтапПодбораПеревозчика.ПустаяСсылка)) КАК Этап,
	|	ЕСТЬNULL(втТекущиеЭтапы.Завершен, ЛОЖЬ) КАК Завершен,
	|	ЕСТЬNULL(втТекущиеЭтапы.ЗавершенУспешно, ЛОЖЬ) КАК ЗавершенУспешно,
	|	ЕСТЬNULL(втТекущиеЭтапы.Отклонен, ЛОЖЬ) КАК Отклонен,
	|	ЕСТЬNULL(втТекущиеЭтапы.Тендер, втВозможныеЭтапы.Тендер) КАК Тендер,
	|	ЕСТЬNULL(втТекущиеЭтапы.ВидТранспорта, втВозможныеЭтапы.ВидТранспорта) КАК ВидТранспорта,
	|	ВЫБОР
	|		КОГДА втТекущиеЭтапы.Завершен ЕСТЬ NULL 
	|			ТОГДА 4
	|		КОГДА ЕСТЬNULL(втТекущиеЭтапы.Отклонен, ЛОЖЬ)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(втТекущиеЭтапы.Завершен, ЛОЖЬ)
	|			ТОГДА 2
	|		КОГДА ЕСТЬNULL(втТекущиеЭтапы.ЗавершенУспешно, ЛОЖЬ)
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИндексКартинки,
	|	ЕСТЬNULL(втТекущиеЭтапы.ЭтапСхемыВыбораПеревозчика, втВозможныеЭтапы.ЭтапСхемыВыбораПеревозчика) КАК ЭтапСхемыВыбораПеревозчика,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЕСТЬNULL(втТекущиеЭтапы.ЭтапСхемыВыбораПеревозчика, втВозможныеЭтапы.ЭтапСхемыВыбораПеревозчика)) КАК ПредставлениеЭтапа
	|ИЗ
	|	втВозможныеЭтапы КАК втВозможныеЭтапы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТекущиеЭтапы КАК втТекущиеЭтапы
	|		ПО втВозможныеЭтапы.НомерЭтапа = втТекущиеЭтапы.НомерЭтапа
	|
	|УПОРЯДОЧИТЬ ПО
	|	втВозможныеЭтапы.НомерЭтапа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИтерацииПоЭтапам.Этап,
	|	втИтерацииПоЭтапам.Итерация,
	|	втИтерацииПоЭтапам.ЗаявкаНаТС,
	|	втИтерацииПоЭтапам.ЗаявкаНаТС.Перевозчик КАК Перевозчик,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втИтерацииПоЭтапам.ЗаявкаНаТС.Перевозчик) КАК ПредставлениеПеревозчика,
	|	ЕСТЬNULL(втСтатусыЗаявок.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Создана)) КАК Статус,
	|	ВЫБОР
	|		КОГДА втСтатусыЗаявок.Статус ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА втСтатусыЗаявок.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Создана)
	|			ТОГДА 0
	|		КОГДА втСтатусыЗаявок.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Отклонена)
	|			ТОГДА 1
	|		ИНАЧЕ втСтатусыЗаявок.Статус.Порядок + 4
	|	КОНЕЦ КАК ИндексКартинки,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЕСТЬNULL(втСтатусыЗаявок.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Создана))) КАК ПредставлениеСтатуса
	|ИЗ
	|	втИтерацииПоЭтапам КАК втИтерацииПоЭтапам
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтатусыЗаявок КАК втСтатусыЗаявок
	|		ПО втИтерацииПоЭтапам.ЗаявкаНаТС = втСтатусыЗаявок.ЗаявкаНаТС";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	Если Не Результат[КоличествоТаблиц - 2].Пустой()  Тогда
		тзЭтапы    = Результат[КоличествоТаблиц - 2].Выгрузить();
		тзИтерации = Результат[КоличествоТаблиц - 1].Выгрузить();
		
		ЭлементыДерева = ДеревоЭтапов.ПолучитьЭлементы();
		Для каждого текЭтап Из тзЭтапы Цикл
			НовыйЭтап = ЭлементыДерева.Добавить();
			НовыйЭтап.Представление = НСтр(СтрШаблон("ru = 'Этап %1. %2'", Строка(текЭтап.НомерЭтапа), текЭтап.ПредставлениеЭтапа));
			ЗаполнитьЗначенияСвойств(НовыйЭтап, текЭтап);
			
			тзЗаявкиПоЭтапу = тзИтерации.Скопировать(Новый Структура("Этап", текЭтап.Этап));
			Если тзЗаявкиПоЭтапу.Количество() Тогда
				тзЗаявкиПоЭтапу.Сортировать("Итерация");
				ЭлементыИтерации = НовыйЭтап.ПолучитьЭлементы();
				Для каждого текИтерация Из тзЗаявкиПоЭтапу Цикл
					НоваяИтерация = ЭлементыИтерации.Добавить();
					Если НовыйЭтап.Тендер Тогда
						НоваяИтерация.Представление = НСтр(СтрШаблон("ru = 'Заявка перевозчику ""%1"" (%2)'", текИтерация.ПредставлениеПеревозчика, текИтерация.ПредставлениеСтатуса));
					Иначе
						НоваяИтерация.Представление = НСтр(СтрШаблон("ru = 'Итерация %1. Заявка перевозчику ""%2"" (%3)'", Строка(текИтерация.Итерация),  текИтерация.ПредставлениеПеревозчика, текИтерация.ПредставлениеСтатуса));
					КонецЕсли; 
					ЗаполнитьЗначенияСвойств(НоваяИтерация, текИтерация);
				КонецЦикла; 
			КонецЕсли; 

		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиКСледующейИтерацииНаСервере(ТекущаяИтерация = Неопределено)
	
	ТекстОшибки = "";
	Отказ = Ложь;
	Документы.упЭтапПодбораПеревозчика.ПерейтиКСледующейИтерации(Рейс, ТекущаяИтерация,, ТекстОшибки, Отказ);
	Если Отказ Тогда
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли; 
	Иначе	
		ОбновитьДеревоЭтаповНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиКСледующемуЭтапуНаСервере(ТекущийЭтап = Неопределено, СписокПеревозчиков = Неопределено)
	
	ТекстОшибки = "";
	Отказ = Ложь;
	Документы.упЭтапПодбораПеревозчика.ПерейтиКСледующемуЭтапу(Рейс, ТекущийЭтап, СписокПеревозчиков, ТекстОшибки, Отказ);
	Если Отказ Тогда
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			ОбновитьДеревоЭтаповНаСервере();
		КонецЕсли; 
	Иначе	
		ОбновитьДеревоЭтаповНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

#Область ОповещенияПоСобытиям

&НаКлиенте
// Продолжение процедуры (см. выше).
Процедура ВопросПерейтиКСледующемуЭтапу(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПерейтиКСледующемуЭтапуНаСервере(ДополнительныеПараметры.ТекущийЭтап);
		РазвернутьДеревоНаКлиенте();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
Процедура ВопросПерейтиКСледующейИтерации(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПерейтиКСледующейИтерацииНаСервере(ДополнительныеПараметры.ТекущаяИтерация);
		РазвернутьДеревоНаКлиенте();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора перевозчика из списка.
//
Процедура ВыборПеревозчиковДляТендераЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Ответ) = Тип("СписокЗначений") Тогда
		СписокПеревозчиков = Новый СписокЗначений;
		Для каждого текЗначение Из Ответ Цикл
			Если текЗначение.Пометка Тогда
				СписокПеревозчиков.Добавить(текЗначение.Значение);
			КонецЕсли; 
		КонецЦикла; 
		
		Если СписокПеревозчиков.Количество() Тогда
			ПерейтиКСледующемуЭтапуНаСервере(, СписокПеревозчиков);
			РазвернутьДеревоНаКлиенте();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 