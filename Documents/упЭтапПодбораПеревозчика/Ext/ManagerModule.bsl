
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.Свойство("Ссылка") Тогда
		СтандартнаяОбработка = Ложь;
		
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные.Ссылка, "НомерЭтапа, ЭтапСхемыВыбораПеревозчика");
		Представление = НСтр(СтрШаблон("ru = 'Этап %1. %2'", Строка(сткРеквизиты.НомерЭтапа), сткРеквизиты.ЭтапСхемыВыбораПеревозчика));
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 	
	
#Область ПрограммныйИнтерфейс  

// Процедура изменения статуса документа.
//
// Параметры:
//  Рейс  - ДокументСсылка - ссылка на документ.
//  ТекущийЭтап  - СправочникСсылка - Ссылка на объект.
//  СписокПеревозчиков  - СписоЗначений - Список ссылок.
//  ТекстОшибки  - Строка - Пердставление ошибки.
//  Отказ  - Булево - Выполнить отказ.
//
Процедура ПерейтиКСледующемуЭтапу(Рейс, ТекущийЭтап, СписокПеревозчиков = Неопределено, ТекстОшибки, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ТекущийЭтап) Тогда
		
		НачатьТранзакцию();
		
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упЭтапПодбораПеревозчика");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		
		Попытка
			БлокировкаДанных.Заблокировать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ТекущийЭтап,,, Отказ);
			ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,, ТекущийЭтап, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
		
		ЭтапОбъект = ТекущийЭтап.ПолучитьОбъект();
		Если ЭтапОбъект.Завершен Или ЭтапОбъект.ЗавершенУспешно Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = '""%1"" завершен. Дальнейшие действия над этапом запрещены'", ТекущийЭтап));
			Отказ = Истина;
		КонецЕсли; 
		
		Если Не Отказ Тогда
			Для каждого текСтрока Из ЭтапОбъект.Заявки Цикл
				ЗаявкаОбъект = текСтрока.ЗаявкаНаТС.ПолучитьОбъект();
				ЗаявкаОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаТС.Отклонена);
				
				Попытка
					ЗаявкаОбъект.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					Отказ = Истина;                                                                                                                                                                                                                                                                                                                                                                                                                                                 
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаТС,, ТекстОшибки);
					Прервать;
				КонецПопытки;
			КонецЦикла;
		КонецЕсли; 
				
		ЭтапОбъект.Отклонен = Истина;
		ЭтапОбъект.Записать();

		Если Отказ Тогда
			ОтменитьТранзакцию();
			Возврат;
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли; 
		
		Результат = Документы.упРейс.СформироватьЗаявкуНаТСПоРейсу(Рейс,, СписокПеревозчиков, ТекстОшибки, Отказ);
		
	Иначе
		Результат = Документы.упРейс.СформироватьЗаявкуНаТСПоРейсу(Рейс,, СписокПеревозчиков, ТекстОшибки, Отказ);
		
	КонецЕсли; 
	
КонецПроцедуры

// Возвращает результат формирования заявки.
//
// Параметры:
//  Рейс  - ДокументСсылка - ссылка на документ.
//  ТекущаяИтерация  - СправочникСсылка - Ссылка на объект.
//  СписокПеревозчиков  - СписоЗначений - Список ссылок.
//  ТекстОшибки  - Строка - Пердставление ошибки.
//  Отказ  - Булево - Выполнить отказ.
//
// Возвращаемое значение:
//     ДокументСсылка - Ссылка на документ.
//
Функция ПерейтиКСледующейИтерации(Рейс, ТекущаяИтерация, СписокПеревозчиков = Неопределено, ТекстОшибки, Отказ) Экспорт
	
	Результат = Неопределено;
	
	Если ЗначениеЗаполнено(ТекущаяИтерация) Тогда
		ЗаявкаОбъект = ТекущаяИтерация.ПолучитьОбъект();
		ЗаявкаОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаТС.Отклонена);
		
		Попытка
			ЗаявкаОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Отказ = Истина;                                                                                                                                                                                                                                                                                                                                                                                                                                                 
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаТС,, ТекстОшибки);
		КонецПопытки;
		
		Если Не Отказ Тогда
			Результат = Документы.упРейс.СформироватьЗаявкуНаТСПоРейсу(Рейс,, СписокПеревозчиков, ТекстОшибки, Отказ);
		КонецЕсли; 
		
	Иначе
		Результат = Документы.упРейс.СформироватьЗаявкуНаТСПоРейсу(Рейс,, СписокПеревозчиков, ТекстОшибки, Отказ);
		
	КонецЕсли; 
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает таблицу доступных перевозчиков.
//
// Параметры:
//  Рейс  - ДокументСсылка - ссылка на документ.
//  ЭтапСхемы  - СправочникСсылка - Ссылка на объект.
//  ЭтоТендер  - Булево - флаг.
//  СписокПеревозчиков  - СписоЗначений - Список ссылок.
//  ВидТранспорта  - ПеречислениеСсылка.упВидыТранспорта - вид.
//
// Возвращаемое значение:
//     ТаблицаЗначений - Данные обработки.
//
Функция ПодобратьПеревозчиков(Рейс, ЭтапСхемы, ЭтоТендер, СписокПеревозчиков = Неопределено, ВидТранспорта = Неопределено) Экспорт
	
	тзПеревозчики = Новый ТаблицаЗначений;
	тзПеревозчики.Колонки.Добавить("Перевозчик", Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
	тзПеревозчики.Колонки.Добавить("Соглашение", Новый ОписаниеТипов("СправочникСсылка.упСоглашенияСПеревозчиками"));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упЭтапПодбораПеревозчикаЗаявки.ЗаявкаНаТС
	|ПОМЕСТИТЬ втЗаявки
	|ИЗ
	|	Документ.упЭтапПодбораПеревозчика.Заявки КАК упЭтапПодбораПеревозчикаЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЭтапПодбораПеревозчика КАК упЭтапПодбораПеревозчика
	|		ПО упЭтапПодбораПеревозчикаЗаявки.Ссылка = упЭтапПодбораПеревозчика.Ссылка
	|			И (упЭтапПодбораПеревозчика.Рейс = &Рейс)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаТС.Перевозчик
	|ПОМЕСТИТЬ втПеревозчикиИсключить
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявки КАК втЗаявки
	|		ПО упЗаявкаНаТС.Ссылка = втЗаявки.ЗаявкаНаТС
	|;";
	
	Если ЭтапСхемы = Перечисления.упЭтапыСхемыВыбораПеревозчика.ЗаявкаТекущемуПеревозчику Тогда
		ДополнитьЗапросЗаявкаТекущемуПеревозчику(ТекстЗапроса);
	ИначеЕсли ЭтапСхемы = Перечисления.упЭтапыСхемыВыбораПеревозчика.ОпределениеПоДействующимСоглашениям Тогда	
		ДополнитьЗапросОпределениеПоДействующимСоглашениям(ТекстЗапроса);
	ИначеЕсли ЭтапСхемы = Перечисления.упЭтапыСхемыВыбораПеревозчика.ВыборПеревозчикаБезСоглашения Тогда	
		ДополнитьЗапросВыборПеревозчикаБезСоглашения(ТекстЗапроса);
	КонецЕсли;
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Если СписокПеревозчиков = Неопределено И Не ЗначениеЗаполнено(ВидТранспорта) Тогда
		Запрос.Текст = ТекстЗапроса;
		
	Иначе
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		
		Если СписокПеревозчиков <> Неопределено Тогда
			Запрос.УстановитьПараметр("СписокПеревозчиков", СписокПеревозчиков);
			СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1].Операторы[0].Отбор.Добавить("Перевозчик В (&СписокПеревозчиков)");
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(ВидТранспорта) Тогда
			Запрос.УстановитьПараметр("ВидТранспорта", ВидТранспорта);
			СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1].Операторы[0].Отбор.Добавить("Перевозчик.ВидТранспорта В (&ВидТранспорта, ЗНАЧЕНИЕ(Перечисление.упВидыТранспорта.ПустаяСсылка))");
		КонецЕсли; 
			
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
	КонецЕсли; 
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Если ЭтоТендер Тогда
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(тзПеревозчики.Добавить(), Выборка);
			КонецЦикла; 
		Иначе
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(тзПеревозчики.Добавить(), Выборка);
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат тзПеревозчики;

КонецФункции // ПодобратьПеревозчиков()

// Возвращает ссылку на текущий этап.
//
// Параметры:
//  Рейс  - ДокументСсылка - ссылка на документ.
//
// Возвращаемое значение:
//     ДокументСсылка - Этап.
//
Функция ПолучитьТекущийЭтап(Рейс) Экспорт
	
	ТекущийЭтап = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	упЭтапПодбораПеревозчика.Ссылка
	|ИЗ
	|	Документ.упЭтапПодбораПеревозчика КАК упЭтапПодбораПеревозчика
	|ГДЕ
	|	упЭтапПодбораПеревозчика.Рейс = &Рейс
	|	И НЕ упЭтапПодбораПеревозчика.Отклонен
	|	И НЕ упЭтапПодбораПеревозчика.ЗавершенУспешно
	|	И НЕ упЭтапПодбораПеревозчика.Завершен
	|
	|УПОРЯДОЧИТЬ ПО
	|	упЭтапПодбораПеревозчика.НомерЭтапа УБЫВ";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ТекущийЭтап = Выборка.Ссылка;
	КонецЕсли; 
	
	Возврат ТекущийЭтап;
	
КонецФункции // ПолучитьТекущийЭтап()

// Возвращает ссылку на текущую заявку на ТС.
//
// Параметры:
//  Рейс  - ДокументСсылка - ссылка на документ.
//
// Возвращаемое значение:
//     ДокументСсылка - Итерация.
//
Функция ПолучитьТекущуюИтерацию(Рейс) Экспорт
		
	ТекущаяИтерация = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЭтапПодбораПеревозчика.Ссылка КАК Этап,
	|	упЭтапПодбораПеревозчика.НомерЭтапа
	|ПОМЕСТИТЬ втЭтапы
	|ИЗ
	|	Документ.упЭтапПодбораПеревозчика КАК упЭтапПодбораПеревозчика
	|ГДЕ
	|	упЭтапПодбораПеревозчика.Рейс = &Рейс
	|	И НЕ упЭтапПодбораПеревозчика.Отклонен
	|	И НЕ упЭтапПодбораПеревозчика.ЗавершенУспешно
	|	И НЕ упЭтапПодбораПеревозчика.Завершен
	|	И НЕ упЭтапПодбораПеревозчика.Тендер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапы.Этап
	|ПОМЕСТИТЬ втТекущийЭтап
	|ИЗ
	|	втЭтапы КАК втЭтапы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(втЭтапы.НомерЭтапа) КАК НомерЭтапа
	|		ИЗ
	|			втЭтапы КАК втЭтапы) КАК ВложенныйЗапрос
	|		ПО втЭтапы.НомерЭтапа = ВложенныйЗапрос.НомерЭтапа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявки.НомерСтроки,
	|	упЗаявки.ЗаявкаНаТС
	|ПОМЕСТИТЬ втЗаявкиПоЭтапу
	|ИЗ
	|	втТекущийЭтап КАК втТекущийЭтап
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЭтапПодбораПеревозчика.Заявки КАК упЗаявки
	|		ПО втТекущийЭтап.Этап = упЗаявки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявкиПоЭтапу.ЗаявкаНаТС
	|ИЗ
	|	втЗаявкиПоЭтапу КАК втЗаявкиПоЭтапу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(втЗаявкиПоЭтапу.НомерСтроки) КАК НомерСтроки
	|		ИЗ
	|			втЗаявкиПоЭтапу КАК втЗаявкиПоЭтапу) КАК ВложенныйЗапрос
	|		ПО втЗаявкиПоЭтапу.НомерСтроки = ВложенныйЗапрос.НомерСтроки";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ТекущаяИтерация = Выборка.ЗаявкаНаТС;
	КонецЕсли; 
	
	Возврат ТекущаяИтерация;
	 
КонецФункции
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьЗапросЗаявкаТекущемуПеревозчику(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
	|	упПеревозчикПоРейсуСрезПоследних.Соглашение
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	|ГДЕ
	|	НЕ упПеревозчикПоРейсуСрезПоследних.Перевозчик В
	|				(ВЫБРАТЬ
	|					втПеревозчикиИсключить.Перевозчик
	|				ИЗ
	|					втПеревозчикиИсключить)
	|	И упПеревозчикПоРейсуСрезПоследних.Перевозчик <> ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|	И упПеревозчикПоРейсуСрезПоследних.Соглашение <> ЗНАЧЕНИЕ(Справочник.упСоглашениясПеревозчиками.ПустаяСсылка)";
	
КонецПроцедуры

Процедура ДополнитьЗапросОпределениеПоДействующимСоглашениям(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчикиПодготовка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|ГДЕ
	|	НЕ упПартнеры.ПометкаУдаления
	|	И упПартнеры.Перевозчик
	|	И НЕ упПартнеры.Ссылка В
	|				(ВЫБРАТЬ
	|					втПеревозчикиИсключить.Перевозчик
	|				ИЗ
	|					втПеревозчикиИсключить)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения
	|ПОМЕСТИТЬ втПараметрыРейса
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО (упРейс.Ссылка = &Рейс)
	|			И упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеревозчики.Перевозчик,
	|	упСоглашенияСПеревозчиками.Ссылка КАК Соглашение,
	|	упСоглашенияСПеревозчиками.ДатаНачалаДействия,
	|	ВЫБОР
	|		КОГДА упСоглашенияСПеревозчиками.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
	|		ИНАЧЕ упСоглашенияСПеревозчиками.ДатаОкончанияДействия
	|	КОНЕЦ КАК ДатаОкончанияДействия
	|ПОМЕСТИТЬ втСоглашения
	|ИЗ
	|	втПеревозчикиПодготовка КАК втПеревозчики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
	|		ПО втПеревозчики.Перевозчик = упСоглашенияСПеревозчиками.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСоглашения.Перевозчик,
	|	МАКСИМУМ(втСоглашения.Соглашение) КАК Соглашение
	|ИЗ
	|	втСоглашения КАК втСоглашения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыРейса КАК втПараметрыРейса
	|		ПО (втПараметрыРейса.ПлановоеНачалоВыполнения МЕЖДУ втСоглашения.ДатаНачалаДействия И втСоглашения.ДатаОкончанияДействия)
	|
	|СГРУППИРОВАТЬ ПО
	|	втСоглашения.Перевозчик
	|
	|УПОРЯДОЧИТЬ ПО
	|	втСоглашения.Перевозчик";
	
	// дополнить сортировку по тарифам
	
КонецПроцедуры

Процедура ДополнитьЗапросВыборПеревозчикаБезСоглашения(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчикиПодготовка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|ГДЕ
	|	НЕ упПартнеры.ПометкаУдаления
	|	И упПартнеры.Перевозчик
	|	И НЕ упПартнеры.Ссылка В
	|				(ВЫБРАТЬ
	|					втПеревозчикиИсключить.Перевозчик
	|				ИЗ
	|					втПеревозчикиИсключить)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения
	|ПОМЕСТИТЬ втПараметрыРейса
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО (упРейс.Ссылка = &Рейс)
	|			И упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеревозчики.Перевозчик,
	|	упСоглашенияСПеревозчиками.Ссылка КАК Соглашение,
	|	упСоглашенияСПеревозчиками.ДатаНачалаДействия,
	|	ВЫБОР
	|		КОГДА упСоглашенияСПеревозчиками.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
	|		ИНАЧЕ упСоглашенияСПеревозчиками.ДатаОкончанияДействия
	|	КОНЕЦ КАК ДатаОкончанияДействия
	|ПОМЕСТИТЬ втСоглашения
	|ИЗ
	|	втПеревозчикиПодготовка КАК втПеревозчики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
	|		ПО втПеревозчики.Перевозчик = упСоглашенияСПеревозчиками.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСоглашения.Перевозчик
	|ПОМЕСТИТЬ втИсключитьДополнительно
	|ИЗ
	|	втСоглашения КАК втСоглашения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыРейса КАК втПараметрыРейса
	|		ПО (втПараметрыРейса.ПлановоеНачалоВыполнения МЕЖДУ втСоглашения.ДатаНачалаДействия И втСоглашения.ДатаОкончанияДействия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеревозчики.Перевозчик
	|ИЗ
	|	втПеревозчикиПодготовка КАК втПеревозчики
	|ГДЕ
	|	НЕ втПеревозчики.Перевозчик В
	|				(ВЫБРАТЬ
	|					втИсключитьДополнительно.Перевозчик
	|				ИЗ
	|					втИсключитьДополнительно)
	|
	|УПОРЯДОЧИТЬ ПО
	|	втПеревозчики.Перевозчик";
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
