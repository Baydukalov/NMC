&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДокументОснование", Объект.Ссылка);
	ПериодПредставление = упЗакрытиеПериода.ПолучитьПредставлениеПериода(Объект.Дата);
	ОбновитьФормуНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПоказатьСписокВыбораПериода(Объект.Дата);
КонецПроцедуры	

&НаКлиенте
Процедура ПериодПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	ПровестиОперациюЗакрытияНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ОтменаПроведения(Команда)
	ОтменитьПроведениеНаСервере();
	ОповеститьОбИзменении(Тип("ДокументСсылка.упРегламентнаяОперация"));
КонецПроцедуры

&НаКлиенте
Процедура Исключить(Команда)
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыОперацииЗакрытия.Исключена") Тогда
		Возврат;
	КонецЕсли;
	
	ИсключитьНаСервере();
	ОповеститьОбИзменении(Тип("ДокументСсылка.упРегламентнаяОперация"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ПровестиОперациюЗакрытияНаКлиенте()
	
	ФоновоеЗадание = ПровестиОперациюЗакрытияНаСервере();
	
	Если ЗначениеЗаполнено(ФоновоеЗадание) Тогда
				
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		НастройкиОжидания.ПолучатьРезультат = Истина;
				
		Обработчик = Новый ОписаниеОповещения("ПослеЗакрытияПериода", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПровестиОперациюЗакрытияНаСервере()
	
	ЗаданиеВыполнено = Ложь;
	ВыполненоУспешно = Ложь;
	ФоновоеЗадание   = Неопределено;

	Если упЗакрытиеПериода.ЕстьДокументыЗакрытия(Объект.Ссылка, Объект.ВидОперации, Объект.Дата) Тогда
		Возврат ФоновоеЗадание;
	КонецЕсли;
	
	докРегламентнаяОперацияОбъект = РеквизитФормыВЗначение("Объект");
	
	Если Ложь
		Или докРегламентнаяОперацияОбъект.ЭтоНовый()
		Или докРегламентнаяОперацияОбъект.Модифицированность()
		Или докРегламентнаяОперацияОбъект.ПометкаУдаления() 
	Тогда
		докРегламентнаяОперацияОбъект.ПометкаУдаления = Ложь;
		докРегламентнаяОперацияОбъект.Записать();
	КонецЕсли;
	
	
	ЗначениеВРеквизитФормы(докРегламентнаяОперацияОбъект, "Объект");
	
	
	
	Если НЕ упСервисныеФункции.ЗаданиеЕщеВыполняется(ИдентификаторЗадания) Тогда	
				
		НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Проведение закрытия периода операции'"), Объект.ВидОперации);
		ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
					
	
		ПараметрыПроцедуры = Новый Структура("Ссылка, ФоновоеЗадание", докРегламентнаяОперацияОбъект.Ссылка, Истина);
		
		ФоновоеЗадание = ДлительныеОперации.ВыполнитьВФоне("упЗакрытиеПериода.ПровестиОперациюЗакрытия", 
		ПараметрыПроцедуры, ПараметрыВыполненияВФоне);
		
		ИдентификаторЗадания = ФоновоеЗадание.ИдентификаторЗадания;
		
	КонецЕсли;
	
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаСервере
Процедура ОтменитьПроведениеНаСервере()
	
	Если упЗакрытиеПериода.ЕстьДокументыЗакрытия(Объект.Ссылка, Объект.ВидОперации, Объект.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	докРегламентнаяОперацияОбъект = РеквизитФормыВЗначение("Объект");
	докРегламентнаяОперацияОбъект.ОтменитьПроведениеОперацииЗакрытия();
	ОбновитьФормуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИсключитьНаСервере()
	
	Если упЗакрытиеПериода.ЕстьДокументыЗакрытия(Объект.Ссылка, Объект.ВидОперации, Объект.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	докРегламентнаяОперацияОбъект = РеквизитФормыВЗначение("Объект");
	докРегламентнаяОперацияОбъект.ИсключитьОперациюЗакрытия();
	ОбновитьФормуНаСервере();
КонецПроцедуры // ПропуститьНаСервере()

&НаСервере
Процедура ПолучитьИзДлительнойОперацииСообщенияПользователю()
	упСервисныеФункции.ПолучитьИзДлительнойОперацииСообщенияПользователю(ИдентификаторЗадания);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуНаКлиенте()
	
	ОбновитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуНаСервере()
	
	ЕстьОшибки = Объект.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыОперацииЗакрытия.ЕстьОшибки");
	
	Элементы.ГруппаОшибки.Видимость = ЕстьОшибки;
	
	Если ЕстьОшибки И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Ошибки = ПолучитьТабличныйДокументОшибок(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТабличныйДокументОшибок(Ссылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Ошибки").Получить();
		
КонецФункции

#Область ВыборПериода

&НаКлиенте
// Процедура завершения после закрытия формы выбора периода.
//
Процедура ПредставлениеПериодаНачалоВыбораЗавершение(СтруктураПериода, ДополнительныеПараметры) Экспорт
	
	// Установим полученный период
	Если СтруктураПериода <> Неопределено Тогда
		Если СтруктураПериода.Представление = "Ранее..." 
			ИЛИ СтруктураПериода.Представление = "Позже..." Тогда
			ПоказатьСписокВыбораПериода(СтруктураПериода.Значение);
		Иначе	
			ПериодПредставление = СтруктураПериода.Представление;
			Объект.Дата = КонецМесяца(СтруктураПериода.Значение);	
		КонецЕсли;
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСписокВыбораПериода(ДатаПериода)
	
	СписокПериодов = упСервисныеФункцииКлиентСервер.СписокФиксированныхПериодов(НачалоМесяца(ДатаПериода), ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредставлениеПериодаНачалоВыбораЗавершение", ЭтотОбъект);
	
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокПериодов, Элементы.ПериодПредставление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьРегламентныеОперации" Тогда
		ОбновитьФормуНаКлиенте();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПослеЗакрытияПериода(ФоновоеЗадание, ДополнительныеПараметры) Экспорт

	Если ФоновоеЗадание.Статус = "Выполнено" Тогда
		
		сткРезультатВыполнения = ПолучитьИзВременногоХранилища(ФоновоеЗадание.АдресРезультата);
		Если ТипЗнч(сткРезультатВыполнения) =  Тип("Структура") Тогда
			ВыполненоУспешно = сткРезультатВыполнения.ВыполненоУспешно;
			Если ВыполненоУспешно Тогда
				Если ЭтаФорма.Открыта() Тогда
					Модифицированность = Ложь;
					Закрыть();
					ПолучитьИзДлительнойОперацииСообщенияПользователю();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗаданиеВыполнено = Истина;
		ОбновитьФормуНаКлиенте();
		
		ОповеститьОбИзменении(Тип("ДокументСсылка.упРегламентнаяОперация"));
		
	ИначеЕсли ФоновоеЗадание.Статус = "Ошибка" Тогда
		ТекстСообщения = Нстр("ru = 'Не удалось закрыть период. %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФоновоеЗадание.КраткоеПредставлениеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

	
КонецПроцедуры


#КонецОбласти
