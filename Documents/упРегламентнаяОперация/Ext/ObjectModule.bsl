#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Дата        = упСервисныеФункцииВызовСервера.ДатаСеансаНачалоДняКонецМесяца();
	Автор       = ПользователиКлиентСервер.ТекущийПользователь();
	Статус      = Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена;
	ВидОперации = Перечисления.упВидыРегламентныхОпераций.АмортизацияТранспортныхСредств;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Истина
		И ПометкаУдаления
		И Статус <> Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена
	Тогда
		ОтменитьПроведениеОперацииЗакрытия(Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограмныйИнтерфейс

// Процедура завершения документа. Формирование движений по регистрам. Изменение статуса.
//
Процедура ПровестиОперациюЗакрытия(сткПрогресс, Отказ = Ложь) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если упЗакрытиеПериода.ЕстьДокументыЗакрытия(Ссылка, ВидОперации, Дата) Тогда
		Отказ = Истина;	
		Возврат;
	КонецЕсли; 
		
	Если Ложь
		Или ЭтоНовый() 
		Или Модифицированность() 
	Тогда
		Записать();
	КонецЕсли;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата",        Дата);
	РеквизитыОбъекта.Вставить("ВидОперации", ВидОперации);
	РеквизитыОбъекта.Вставить("Объект",      ЭтотОбъект);
	
	мсвПриемникиИнформацииОбОшибках = Новый Массив;
	ПодготовитьСтруктурыДанныхДляДобавленияИнформацииОбОшибках(мсвПриемникиИнформацииОбОшибках);
	
	ДополнительныеСвойства.Вставить("Прогресс", сткПрогресс);
	
	упЗакрытиеПериода.СформироватьДвижения(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	ДополнитьИнформациюОбОшибкахСообщениямиПользователю(мсвПриемникиИнформацииОбОшибках);
			
	Если Отказ Тогда
		упСервисныеФункции.УдалитьДвижения(Движения);
		НовыйСтатус = Перечисления.упСтатусыОперацииЗакрытия.ЕстьОшибки;
	Иначе
		НовыйСтатус = Перечисления.упСтатусыОперацииЗакрытия.Выполнена;
	КонецЕсли;
	
	ИзменитьСтатусОперацииЗакрытия(НовыйСтатус);
	
КонецПроцедуры

// Процедура ответны завершения документа. Удаление движений. Отмена статуса.
//
Процедура ОтменитьПроведениеОперацииЗакрытия(Отказ = Ложь) Экспорт
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);                
		
	упСервисныеФункции.УдалитьДвижения(Движения);
	
	упЗакрытиеПериода.ОтменитьПроведениеСвязанныхДокументов(Ссылка, Отказ);

	Ошибки = Неопределено;
	
	ИзменитьСтатусОперацииЗакрытия(Перечисления.упСтатусыОперацииЗакрытия.НеВыполнена);
	
КонецПроцедуры // ОтменитьОперацию()

// Пропустить операцию закрытия.
//
Процедура ИсключитьОперациюЗакрытия(Отказ = Ложь) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);

	Если упЗакрытиеПериода.ЕстьДокументыЗакрытия(Ссылка, ВидОперации, Дата) Тогда
		Отказ = Истина;	
		Возврат;
	КонецЕсли; 
		
	Если Статус = Перечисления.упСтатусыОперацииЗакрытия.Выполнена Тогда
		упСервисныеФункции.УдалитьДвижения(Движения);
	КонецЕсли;
	
	Ошибки = Неопределено;
	Статус = Перечисления.упСтатусыОперацииЗакрытия.Исключена;
	Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры // ПропуститьОперацию()

#КонецОбласти

#Область СлужебныеФункции

Процедура ИзменитьСтатусОперацииЗакрытия(НовыйСтатус)
	
	Если НЕ Статус = НовыйСтатус Тогда
		Статус = НовыйСтатус; 
	КонецЕсли;
	
	Если Модифицированность() Тогда
		Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьСтруктурыДанныхДляДобавленияИнформацииОбОшибках(мсвПриемникиИнформацииОбОшибках)
	
	ТабличныйДокумент 				= Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент 	= Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемникиИнформацииОбОшибках.Добавить(сткПриемникТабличныйДокумент);
	сткИнформацияОбОшибках = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемникиИнформацииОбОшибках, "РегламентнаяОперация");
	упСервисныеФункцииКлиентСервер.ДобавитьСообщениеРасшифровки(сткИнформацияОбОшибках,,"ВидОперации",,ВидОперации,2);		
	
	ДополнительныеСвойства.Вставить("сткИнформацияОбОшибках", сткИнформацияОбОшибках);
	
КонецПроцедуры

Процедура ДополнитьИнформациюОбОшибкахСообщениямиПользователю(мсвПриемникиИнформацииОбОшибках)
	
	сткИнформацияОбОшибках = ДополнительныеСвойства.сткИнформацияОбОшибках;
	
	СообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
	
	упСервисныеФункцииКлиентСервер.ДополнитьРасшифровкуСообщениямиПользователю(сткИнформацияОбОшибках, СообщенияПользователю);
	
	упСервисныеФункцииКлиентСервер.СформироватьРасшифровку(сткИнформацияОбОшибках);
		
	ТабличныйДокумент 	= мсвПриемникиИнформацииОбОшибках[0].Приемник;
	Ошибки 				= Новый ХранилищеЗначения(ТабличныйДокумент, Новый СжатиеДанных(9));
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли	