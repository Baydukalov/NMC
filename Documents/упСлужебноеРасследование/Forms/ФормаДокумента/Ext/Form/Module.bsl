#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор       = ПользователиКлиентСервер.ТекущийПользователь();
		// Если копирование
		Если НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) 
			И НЕ ЗначениеЗаполнено(Объект.Организация)Тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.СпособРаспределенияРасходов) Тогда
			Объект.СпособРаспределенияРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыЗаПериод;	
		КонецЕсли;
		
		Если ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
		
	КонецЕсли;		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
		ЭтаФорма.ВалютаУпрУчета 	= упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли; 
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
		
	ПриСозданииПриЧтенииНаСервере();		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьВидимостьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УщербПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура УщербПослеУдаления(Элемент)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура УщербЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УщербКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УщербСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УщербСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныеЛицаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ОтветственныеЛица.ТекущиеДанные;
		ТекущаяСтрока.Валюта = ВалютаРеглУчета;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныеЛицаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныеЛицаПослеУдаления(Элемент)
	РассчитатьИтоговуюСумму();
КонецПроцедуры

&НаКлиенте
Процедура СпособРаспределенияРасходовПриИзменении(Элемент)
	ОбновитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ВодительскоеУдостоверениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("Водитель, ТекущаяДата", Объект.Сотрудник, Объект.Дата);
	текОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыВыбораВодительскогоУдостоверения",ЭтаФорма);
	ОткрытьФорму("РегистрСведений.упДействующиеДокументыТСВодителей.Форма.ФормаВыбораВодительскогоУдостоверения", сткПараметры, Элемент,,,,текОповещениеОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	ЗаполнитьВодительскоеУдостоверениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.упШтраф") Тогда
		ЗаполнитьПоДокументуОснованию(ВыбранноеЗначение);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
		упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПериодРаспределенияЗатратНачало", "ПериодРаспределенияЗатратОкончание"));
КонецПроцедуры	

&НаКлиенте
Процедура УщербПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.Ущерб.ТекущиеДанные;
		ТекущаяСтрока.Количество 	= 1;
		ТекущаяСтрока.Валюта = ВалютаРеглУчета;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОбновитьВидимостьЭлементов()
	
	Если Объект.СпособРаспределенияРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу") Тогда
		 Элементы.ГруппаПараметрыРаспределенияРасходов.ТекущаяСтраница 	= Элементы.ГруппаРейс;
		 Объект.ПериодРаспределенияЗатратНачало 	= '00010101';
		 Объект.ПериодРаспределенияЗатратОкончание 	= '00010101';
	Иначе	
		Элементы.ГруппаПараметрыРаспределенияРасходов.ТекущаяСтраница 	= Элементы.ГруппаПериодРаспределенияЗатрат;	
		Объект.Рейс = ПредопределенноеЗначение("Документ.упРейс.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСумму()
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"РазмерШтрафа");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ОтветственныеЛица", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	
	текСуммаШтрафаПоОтветственнымЛицам = Объект.СуммаРасходы;
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	ПараметрыОтбора	= Новый Структура("План", Ложь);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Ущерб", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет, ПараметрыОтбора);
	
	Объект.СуммаРасходы = Объект.СуммаРасходы - текСуммаШтрафаПоОтветственнымЛицам;
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыПлан",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыПланНДС",	"СуммаНДС");
	ПараметрыОтбора	= Новый Структура("План", Истина);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Ущерб", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет, ПараметрыОтбора);

КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.Ущерб.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока, ПолучитьИмяПоляРасходы(Элемент.Имя), СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "Ущерб","");
КонецФункции

&НаСервере
Процедура ЗаполнитьПоДокументуОснованию(ВыбранноеЗначение)
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.Заполнить(ВыбранноеЗначение);
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора водительского удостоверения.
//
Процедура ОбработатьЗакрытиеФормыВыбораВодительскогоУдостоверения(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ДокументСсылка.упДокументТранспортногоСредстваВодителя") Тогда
		ЗаполнитьВодительскоеУдостоверениеНаСервере(Результат);		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВодительскоеУдостоверениеНаСервере(ДокументТранспортногоСредстваВодителя = Неопределено)
	
	Объект.ВодительскоеУдостоверение = упПодборТС.ПолучитьСериюНомерВодительскогоУдостоверения(ДокументТранспортногоСредстваВодителя, Объект.Дата, Объект.Сотрудник);
	
КонецПроцедуры

#КонецОбласти
