#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упПроисшествиеВПути") Тогда
		ДокументОснование 	= ДанныеЗаполнения;
		Организация 		= ДанныеЗаполнения.Организация;
		Сотрудник			= ДанныеЗаполнения.Водитель1;
		ВодительскоеУдостоверение = упПодборТС.ПолучитьСериюНомерВодительскогоУдостоверения(,Дата, Сотрудник);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упШтраф") Тогда
		ДокументОснование 	= ДанныеЗаполнения;
		Организация 		= ДанныеЗаполнения.Организация;
		Рейс 				= ДанныеЗаполнения.Рейс;
		Сотрудник			= ДанныеЗаполнения.Водитель;
		ВодительскоеУдостоверение = упПодборТС.ПолучитьСериюНомерВодительскогоУдостоверения(,Дата, Сотрудник);
	
		НоваяСтрокаОтветственныеЛица = ОтветственныеЛица.Добавить();
		НоваяСтрокаОтветственныеЛица.Сотрудник = Сотрудник;
		
		ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		Если ВедетсяВалютныйУчет Тогда
			ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
			НоваяСтрокаОтветственныеЛица.Валюта = ВалютаРеглУчета;
		КонецЕсли;
		
		СпособРаспределенияРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Если основанием является штраф
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.упШтраф") Тогда
		
		// Проверить существуют ли проведенные служ. расследования по текущему штрафу
		упПроисшествиеВПути.ЕстьСлужебныеРасследованияПоШтрафу(Ссылка, ДокументОснование, Отказ);
		
	КонецЕсли;
	
	ТранспортноеСредство = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ТранспортноеСредство");
	
	РеквизитыОбъекта = Новый Структура("Дата,
								|ПериодРаспределенияЗатратНачало,
								|ПериодРаспределенияЗатратОкончание,
								|ТранспортноеСредство, 
								|Рейс, 
								|СпособРаспределенияРасходов, 
								|Организация,
								|Подразделение");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект); 
	РеквизитыОбъекта.Вставить("НаправлениеДеятельности", Справочники.упНаправленияДеятельности.ПустаяСсылка());
	
	// Формируются прямые фактические расходы.
	упСлужебноеРасследование.СформироватьДвиженияРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособРаспределенияРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		Если НЕ ЗначениеЗаполнено(Рейс) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru = 'Не заполнен рейс для распределения расходов'"),,"Рейс","Объект",Отказ);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 

#КонецЕсли	

