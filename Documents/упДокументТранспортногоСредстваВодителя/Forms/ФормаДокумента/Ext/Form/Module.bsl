
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя() Тогда
			Объект.Партнер = ПользователиКлиентСервер.ТекущийВнешнийПользователь().ОбъектАвторизации;
		КонецЕсли; 
	КонецЕсли; 
	
	УстановитьДоступность();
	Элементы.Водители.Доступность 				= Не Объект.НеограниченноеЧислоВодителей;
	Элементы.ТранспортныеСредства.Доступность 	= Не Объект.НеограниченноеЧислоТС;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Проведен И ЗначениеЗаполнено(ТекущийОбъект.ДокументОснование) Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗадачаИсполнителя.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	НЕ ЗадачаИсполнителя.Выполнена
		|	И ЗадачаИсполнителя.Предмет = &Предмет";
		Запрос.УстановитьПараметр("Предмет", ТекущийОбъект.ДокументОснование);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			УстановитьПривилегированныйРежим(Истина);
			
			текОбъект = Выборка.Ссылка.ПолучитьОбъект();
			текОбъект.ВыполнитьЗадачу();
			
			ЗаданиеОбъект = текОбъект.БизнесПроцесс.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(текОбъект.Ссылка);
			ЗаданиеОбъект.ДокументРезультат = ТекущийОбъект.Ссылка;
			ЗаданиеОбъект.Выполнено         = Истина;
			ЗаданиеОбъект.Записать();
		КонецЦикла;	
	КонецЕсли;	
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СгруппироватьКатегории();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипДокументаПриИзменении(Элемент)
	
	УстановитьДоступность(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействуетСПриИзменении(Элемент)
	
	Если Объект.ДействуетС > Объект.ДействуетПо Тогда 
		Объект.ДействуетПо = Объект.ДействуетС;
	КонецЕсли;
	
	Если СрокДействия = ПредопределенноеЗначение("Перечисление.упСрокДействияДокумента.НеограниченныйСрок") Тогда 
		Объект.ДействуетПо = '29991231';
	ИначеЕсли СрокДействия = ПредопределенноеЗначение("Перечисление.упСрокДействияДокумента.ФиксированныйСрок") Тогда 
		Объект.ДействуетПо = ДобавитьМесяц(Объект.ДействуетС, ПериодДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействуетПоПриИзменении(Элемент)
	
	Если Объект.ДействуетС > Объект.ДействуетПо Тогда 
		Объект.ДействуетС = Объект.ДействуетПо;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ДействуетС) Тогда 
		Объект.ДействуетС = Объект.Дата;
		ДействуетСПриИзменении(Неопределено);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда 
		ДокументОснованиеПриИзмененииНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВодителиВодитель1ПриИзменении(Элемент)
	
	Если Объект.Водители.Количество() = 0 Тогда 
		НоваяСтрока = Объект.Водители.Добавить();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НеограниченноеЧислоВодителейПриИзменении(Элемент)
	
	Если Объект.НеограниченноеЧислоВодителей Тогда 
		Объект.Водители.Очистить();
	КонецЕсли;	
	Элементы.Водители.Доступность = Не Объект.НеограниченноеЧислоВодителей;
	
КонецПроцедуры

&НаКлиенте
Процедура НеограниченноеЧислоТСПриИзменении(Элемент)
	
	Если Объект.НеограниченноеЧислоТС Тогда 
		Объект.ТранспортныеСредства.Очистить();
	КонецЕсли;	
	Элементы.ТранспортныеСредства.Доступность = Не Объект.НеограниченноеЧислоТС;
	
КонецПроцедуры

&НаСервере
Процедура СгруппироватьКатегории()
	
	ОбъектЗначение = РеквизитФормыВЗначение("Объект");

	ОбъектЗначение.Категории.Свернуть("Категория");

	ЗначениеВРеквизитФормы (ОбъектЗначение,"Объект");

Конецпроцедуры	

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере()

	Объект.ТипДокумента                 = Объект.ДокументОснование.ТипДокумента;
	Объект.ДействуетС                   = Объект.ДокументОснование.ДействуетПо + 86400;
	Объект.ДействуетПо                  = Объект.ДействуетС + (Объект.ДокументОснование.ДействуетПо - Объект.ДокументОснование.ДействуетС);
	Объект.НеограниченноеЧислоТС        = Объект.ДокументОснование.НеограниченноеЧислоТС;
	Объект.НеограниченноеЧислоВодителей = Объект.ДокументОснование.НеограниченноеЧислоВодителей;
	Объект.Партнер                      = Объект.ДокументОснование.Партнер;
	Объект.ТранспортныеСредства.Загрузить(Объект.ДокументОснование.ТранспортныеСредства.Выгрузить());
	Объект.Водители.Загрузить(Объект.ДокументОснование.Водители.Выгрузить());
	
	УстановитьДоступность(Истина);
	
КонецПроцедуры // ДокументОснованиеПриИзмененииНаСервере()

&НаСервере
Процедура УстановитьДоступность(ИзмененТипДокумента = Ложь)
	
	сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ТипДокумента, "КоличествоТС, КоличествоВодителей, ДокументВыдается, ПериодДействия, СрокДействия");
	СрокДействия   = сткДанные.СрокДействия;
	ПериодДействия = сткДанные.ПериодДействия;
	
	Если сткДанные.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель Тогда 
		Элементы.ГруппаВодители.Доступность             = Истина;
		Элементы.ГруппаТранспортныеСредства.Доступность = Ложь;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоТС        = Ложь;
			Объект.ТранспортныеСредства.Очистить();
		КонецЕсли;	
	ИначеЕсли сткДанные.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство Тогда 
		Элементы.ГруппаВодители.Доступность             = Ложь;
		Элементы.ГруппаТранспортныеСредства.Доступность = Истина;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоВодителей = Ложь;
			Объект.Водители.Очистить();
		КонецЕсли;	
	ИначеЕсли сткДанные.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТСВодитель Тогда 
		Элементы.ГруппаВодители.Доступность             = Истина;
		Элементы.ГруппаТранспортныеСредства.Доступность = Истина;
	Иначе 
		Элементы.ГруппаВодители.Доступность             = Ложь;
		Элементы.ГруппаТранспортныеСредства.Доступность = Ложь;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоТС        = Ложь;
			Объект.НеограниченноеЧислоВодителей = Ложь;
			Объект.ТранспортныеСредства.Очистить();
			Объект.Водители.Очистить();
		КонецЕсли;	
	КонецЕсли;	

	Если сткДанные.КоличествоТС = Перечисления.упКоличествоОбъектовДокументовТСВодителей.ВозможноНеограниченноеЧисло Тогда 
		Элементы.СтраницыТранспортныеСредства.ТекущаяСтраница = Элементы.ГруппаМногоТС;
		Элементы.НеограниченноеЧислоТС.Доступность            = Истина;
	ИначеЕсли сткДанные.КоличествоТС = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Несколько Тогда 
		Элементы.СтраницыТранспортныеСредства.ТекущаяСтраница = Элементы.ГруппаМногоТС;
		Элементы.НеограниченноеЧислоТС.Доступность            = Ложь;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоТС        = Ложь;
		КонецЕсли;	
	Иначе	
		Элементы.СтраницыТранспортныеСредства.ТекущаяСтраница = Элементы.ГруппаОдноТС;
		Элементы.НеограниченноеЧислоТС.Доступность            = Ложь;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоТС        = Ложь;
			Если Объект.ТранспортныеСредства.Количество() = 0 Тогда 
				Объект.ТранспортныеСредства.Добавить();
			Иначе 
				ТС = Объект.ТранспортныеСредства[0].ТранспортноеСредство;
				Объект.ТранспортныеСредства.Очистить();
				НоваяСтрока = Объект.ТранспортныеСредства.Добавить();
				НоваяСтрока.ТранспортноеСредство = ТС;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;

	Если сткДанные.КоличествоВодителей = Перечисления.упКоличествоОбъектовДокументовТСВодителей.ВозможноНеограниченноеЧисло Тогда 
		Элементы.СтраницыВодители.ТекущаяСтраница             = Элементы.ГруппаМногоВодителей;
		Элементы.НеограниченноеЧислоВодителей.Доступность     = Истина;
	ИначеЕсли сткДанные.КоличествоВодителей = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Несколько Тогда 
		Элементы.СтраницыВодители.ТекущаяСтраница             = Элементы.ГруппаМногоВодителей;
		Элементы.НеограниченноеЧислоВодителей.Доступность     = Ложь;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоВодителей = Ложь;
		КонецЕсли;	
	Иначе	
		Элементы.СтраницыВодители.ТекущаяСтраница             = Элементы.ГруппаОдинВодитель;
		Элементы.НеограниченноеЧислоВодителей.Доступность     = Ложь;
		Если ИзмененТипДокумента Тогда 
			Объект.НеограниченноеЧислоВодителей = Ложь;
			Если Объект.Водители.Количество() = 0 Тогда 
				Объект.Водители.Добавить();
			Иначе 
				Водитель = Объект.Водители[0].Водитель;
				Объект.Водители.Очистить();
				НоваяСтрока = Объект.Водители.Добавить();
				НоваяСтрока.Водитель = Водитель;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
	Элементы.ДействуетПо.Доступность = СрокДействия = Перечисления.упСрокДействияДокумента.СрокУказываетсяВДокументе;
	Если ИзмененТипДокумента Тогда 
		Если СрокДействия = Перечисления.упСрокДействияДокумента.НеограниченныйСрок Тогда 
			Объект.ДействуетПо = '29991231';
		ИначеЕсли СрокДействия = Перечисления.упСрокДействияДокумента.ФиксированныйСрок Тогда 
			Объект.ДействуетПо = ДобавитьМесяц(Объект.ДействуетС, ПериодДействия);
		КонецЕсли;
	КонецЕсли;	
	
	Если Объект.ТипДокумента = Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение Тогда
		Элементы.ГруппаКатегории.Доступность  = Истина;	
	Иначе	
		Элементы.ГруппаКатегории.Доступность  = Ложь;
	КонецЕсли;	
	
	Элементы.ВидПропускаНаВъездВыездВЗону.Видимость = Ложь;
	Если Объект.ТипДокумента = ПредопределенноеЗначение("Справочник.упТипыДокументовТСВодителей.ПропускНаПроездТранспортаЧерезЗону") Тогда
		Элементы.ВидПропускаНаВъездВыездВЗону.Видимость = Истина;
	КонецЕсли;
		
КонецПроцедуры // УстановитьДоступность()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти