
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ТипДокумента.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель ИЛИ НеограниченноеЧислоТС Тогда 
		ТранспортныеСредства.Очистить();
	КонецЕсли;	
	Если ТипДокумента.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство ИЛИ НеограниченноеЧислоВодителей Тогда 
		Водители.Очистить();
	КонецЕсли;	
	
	Если ТипДокумента.КоличествоВодителей = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Один Тогда 
		КоличествоСтрок = Водители.Количество();
		Для Сч = 1 По КоличествоСтрок - 1 Цикл 
			Водители.Удалить(КоличествоСтрок - Сч);
		КонецЦикла;
	КонецЕсли;	
	Если ТипДокумента.КоличествоТС        = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Один Тогда 
		КоличествоСтрок = ТранспортныеСредства.Количество();
		Для Сч = 1 По КоличествоСтрок - 1 Цикл 
			ТранспортныеСредства.Удалить(КоличествоСтрок - Сч);
		КонецЦикла;
	КонецЕсли;	
	
	Если ТипДокумента = ПредопределенноеЗначение("Справочник.упТипыДокументовТСВодителей.ПропускНаПроездТранспортаЧерезЗону") Тогда
		Если НЕ ЗначениеЗаполнено(ВидПропускаНаВъездВыездВЗону) Тогда
			ТекстСообщения = Нстр("ru = 'Укажите Вид пропуска !!!'");		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,"ВидПропускаНаВъездВыездВЗону", , Отказ);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ДокументОснование) И ДокументОснование = Ссылка Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Неверно заполнен предыдущий документ (цикличная ссылка)'"), Ссылка, "Объект.ДокументОснование",, Отказ);
	КонецЕсли;
	
	Если ТипДокумента.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель ИЛИ НеограниченноеЧислоТС Тогда 
		ТранспортныеСредства.Очистить();
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ТранспортныеСредства");
	КонецЕсли;	
	Если ТипДокумента.ДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство ИЛИ НеограниченноеЧислоВодителей Тогда 
		Водители.Очистить();
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Водители");
	КонецЕсли;	
	
	Если ТипДокумента.КоличествоВодителей = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Один Тогда 
		КоличествоСтрок = Водители.Количество();
		Для Сч = 1 По КоличествоСтрок - 1 Цикл 
			Водители.Удалить(КоличествоСтрок - Сч);
		КонецЦикла;
	КонецЕсли;	
	Если ТипДокумента.КоличествоТС        = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Один Тогда 
		КоличествоСтрок = ТранспортныеСредства.Количество();
		Для Сч = 1 По КоличествоСтрок - 1 Цикл 
			ТранспортныеСредства.Удалить(КоличествоСтрок - Сч);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	мсвТС = ТранспортныеСредства.ВыгрузитьКолонку("ТранспортноеСредство");
	Если Не мсвТС.Количество() Тогда 
		мсвТС.Добавить(Неопределено);
	КонецЕсли;
	
	мсвВодителей = Водители.ВыгрузитьКолонку("Водитель");
	Если Не мсвВодителей.Количество() Тогда 
		мсвВодителей.Добавить(Неопределено);
	КонецЕсли;
	
	Для Каждого текТС Из мсвТС Цикл 
		Для Каждого текВодитель Из мсвВодителей Цикл 
			НоваяЗапись = Движения.упДействующиеДокументыТСВодителей.Добавить();
			НоваяЗапись.Период               = ДействуетС;
			НоваяЗапись.Регистратор          = Ссылка;
			НоваяЗапись.ТипДокумента         = ТипДокумента;
			НоваяЗапись.ТранспортноеСредство = текТС;
			НоваяЗапись.Водитель             = текВодитель;
			НоваяЗапись.ДействуетПо           = ДействуетПо;
		КонецЦикла;
	КонецЦикла;
	
	Движения.упДействующиеДокументыТСВодителей.Записывать = Истина;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = ОбъектКопирования.Ссылка;
	ДействуетС        = ОбъектКопирования.ДействуетПо + 86400;
	ДействуетПо       = ДействуетС + (ОбъектКопирования.ДействуетПо - ОбъектКопирования.ДействуетС);
	Номер             = "";
	Серия             = "";
	Выдан             = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
