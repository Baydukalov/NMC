
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Документы.упДокументТранспортногоСредстваВодителя.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Функция - Сформировать прочие расходы
//
// Параметры:
//  ДокументТранспортногоСредстваВодителя	 - ДокументСсылка.упДокументТранспортногоСредстваВодителя	 - ссылка на документ основание.
//  ТекстОшибки						 - Строка	 - текст ошибки.
//  Отказ								 - Булево - Истина, в случае ошибки.
// 
// Возвращаемое значение:
//  ДокументСсылка.упПрочиеРасходы  - прочие расходы.
//
Функция СформироватьПрочиеРасходы(ДокументТранспортногоСредстваВодителя, ТекстОшибки, Отказ) Экспорт
	
	Результат = Неопределено;
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументТранспортногоСредстваВодителя, "ТипДокумента, ТипДокумента.ДокументВыдается, ТипДокумента.КоличествоТС, ТипДокумента.КоличествоВодителей, Партнер");
	
	ПрочиеРасходыОбъект = Неопределено;
	
	Если сткРеквизиты.ТипДокументаДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.Водитель
			ИЛИ сткРеквизиты.ТипДокументаДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТСВодитель
				И сткРеквизиты.ТипДокументаКоличествоВодителей = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Один Тогда
		ПрочиеРасходыОбъект = Документы.упПрочиеРасходы.СоздатьДокумент();
		ПрочиеРасходыОбъект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВодителюЭкспедитору;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упДокументТранспортногоСредстваВодителяВодители.Водитель
		|ИЗ
		|	Документ.упДокументТранспортногоСредстваВодителя.Водители КАК упДокументТранспортногоСредстваВодителяВодители
		|ГДЕ
		|	упДокументТранспортногоСредстваВодителяВодители.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДокументТранспортногоСредстваВодителя);
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПрочиеРасходыОбъект.Водитель = Выборка.Водитель;					
		КонецЕсли; 
		
	ИначеЕсли сткРеквизиты.ТипДокументаДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТранспортноеСредство 
				ИЛИ сткРеквизиты.ТипДокументаДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТСВодитель
					И сткРеквизиты.ТипДокументаКоличествоТС = Перечисления.упКоличествоОбъектовДокументовТСВодителей.Один Тогда	
		ПрочиеРасходыОбъект = Документы.упПрочиеРасходы.СоздатьДокумент();	
		ПрочиеРасходыОбъект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоТранспортномуСредству;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упДокументТранспортногоСредстваВодителяТранспортныеСредства.ТранспортноеСредство
		               |ИЗ
		               |	Документ.упДокументТранспортногоСредстваВодителя.ТранспортныеСредства КАК упДокументТранспортногоСредстваВодителяТранспортныеСредства
		               |ГДЕ
		               |	упДокументТранспортногоСредстваВодителяТранспортныеСредства.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДокументТранспортногоСредстваВодителя);
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПрочиеРасходыОбъект.ТранспортноеСредство = Выборка.ТранспортноеСредство;					
		КонецЕсли; 

	ИначеЕсли сткРеквизиты.ТипДокументаДокументВыдается = Перечисления.упОбъектДокументовТСВодителей.ТСВодитель Тогда	
		ТекстОшибки = Нстр("ru = 'Для типа документа ""%1"" не предусмотрено формирование прочих расходов'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, сткРеквизиты.ТипДокумента);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ПрочиеРасходыОбъект = Неопределено Тогда
		ПрочиеРасходыОбъект.Дата		= ТекущаяДатаСеанса();
		ПрочиеРасходыОбъект.Партнер = сткРеквизиты.Партнер;
		ПрочиеРасходыОбъект.ДокументОснование = ДокументТранспортногоСредстваВодителя;
		ПрочиеРасходыОбъект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		ПрочиеРасходыОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
		Если ИспользуетсяУчетПодразделений Тогда
			ПрочиеРасходыОбъект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), ПрочиеРасходыОбъект.Автор);
		КонецЕсли;
			
		Попытка
			ПрочиеРасходыОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Результат = ПрочиеРасходыОбъект.Ссылка;
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПрочиеРасходы,, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("ТипДокумента");
	Поля.Добавить("Ссылка");
	Поля.Добавить("Серия");
	Поля.Добавить("Номер");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = Строка(Данные.ТипДокумента) + ?(ЗначениеЗаполнено(Данные.Серия), НСтр("ru = ' серия '") + Данные.Серия, "") + " № " + Данные.Номер;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


