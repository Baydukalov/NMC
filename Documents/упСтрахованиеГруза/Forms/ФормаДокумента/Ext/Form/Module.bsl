#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор       = ПользователиКлиентСервер.ТекущийПользователь();
		Если НЕ ЗначениеЗаполнено(Объект.Организация) тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.Рейс, Объект.Автор);
		КонецЕсли;
		
	КонецЕсли;
		
	Элементы.ЗаданияКоличествоГрузов.Видимость 	= ЭтаФорма.ИспользуетсяКоличественныйУчетГрузов;
	Элементы.СтраховаяПремияВалюта.Видимость 	= ЭтаФорма.ВедетсяВалютныйУчет;
	
	ЗаблокироватьЭлементы();
		
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект,Параметры,Ложь);

	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Взаиморасчеты
	Элементы.ГруппаСостояниеВзаиморасчетов.Видимость = РазрешеноЗагружатьВзаиморасчеты;		
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства	
	
	ЭтаФорма.РазрешеноЗагружатьВзаиморасчеты		= ПолучитьФункциональнуюОпцию("упЗагружатьВзаиморасчетыСПартнерами");
	ЭтаФорма.ИспользуетсяУчетПодразделений			= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ИспользуетсяКоличественныйУчетГрузов   = ПолучитьФункциональнуюОпцию("упИспользуетсяКоличественныйУчетГрузов");
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет тогда
		ЭтаФорма.ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
	КонецЕсли;
	
	ЭтаФорма.ВзаиморасчетыЗагружаются 		= Ложь;
	ЭтаФорма.ВзаиморасчетыЗагрузить			= Ложь;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗагрузитьВзаиморасчетыСПартнерами();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

	// Проверка ТЧ.Задания на дубли
	тбзЗадания	= Объект.Задания.Выгрузить(,"Задание, ГрузовоеМесто");
	тбзЗадания.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число"));
	
	тбзЗадания.ЗаполнитьЗначения(1,"Количество");
	тбзЗадания.Свернуть("Задание, ГрузовоеМесто", "Количество");
	
	Для каждого текСтрока Из тбзЗадания Цикл
		Если текСтрока.Количество > 1 Тогда
			ТекстСообщения = Нстр("ru = 'Содержит дублирующие строки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							Объект.Ссылка,
							"Задание",
							,
							Отказ);
			
			Прервать; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РазблокироватьДанныеДляРедактирования(ТекущийОбъект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#Область ЭлементыФормы_СтраницаЗадания

&НаКлиенте
Процедура ЗаданияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		текСтрока.КоличествоГрузов = 1;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьЗадания(Команда = Неопределено)
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьЗаданияКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	ЗаполнитьЗадания();
	РейсПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыФормы_СтраницаСтраховаяПремия

&НаКлиенте
Процедура СтраховаяПремияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.СтраховаяПремия.ТекущиеДанные;
		ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		ТекущаяСтрока.Количество 	= 1;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СтраховщикПриИзменении(Элемент)
	ЗагрузитьВзаиморасчетыСПартнерами();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьСтраховуюПремию(Команда) 
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		РассчитатьСтраховуюПремиюНаСервере();
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Объект.Соглашение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтраховуюПремию(Команда)

	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьСтраховуюПремиюКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСтраховуюПремиюСРасшифровкой(Команда)
		
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		мсвПриемников = РассчитатьСтраховуюПремиюСРасшифровкойНаСервере();
		Для каждого Приемник Из мсвПриемников Цикл
			Приемник.Приемник.Показать("Расшифровка расчетов");	
		КонецЦикла;
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Соглашение");
	КонецЕсли;

КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура РассчитатьСтраховуюПремиюНаСервере()
	
	ВходныеПараметры = Новый Структура();
	
	упТарификацияВызовСервера.Рассчитать(Объект, ВходныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		//ТолькоПросмотр = Истина;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
// Процедура - перезаполнить ТЧ "Задания".
//
Процедура ЗаполнитьЗаданияКлиент(Результат, ДополнительныеПараметры) Экспорт
	Если Объект.Задания.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполненииЗаданий", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Задания"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьЗаданияНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаданияНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Рейс) Тогда
		текОбъект = РеквизитФормыВЗначение("Объект");
		текОбъект.ЗаполнитьЗаданияПоРейсу(Объект.Рейс);
		ЗначениеВРеквизитФормы(текОбъект,"Объект");
	КонецЕсли;
				
КонецПроцедуры

&НаКлиенте
Процедура СтраховаяПремияСтатьяРасходовПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СтраховаяПремия.ТекущиеДанные;
	
	СтатьяРасходов	= ТекущиеДанные.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		сткДанные = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
		ТекущиеДанные.СтавкаНДС = сткДанные.СтавкаНДС;
		ТекущиеДанные.ПравилоРаспределения = сткДанные.ПравилаРаспределения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
//  Процедура - перезаполнить ТЧ "СтраховаяПремия".
//
Процедура ЗаполнитьСтраховуюПремиюКлиент(Результат, ДополнительныеПараметры) Экспорт
	Если Объект.СтраховаяПремия.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполненииСтраховойПремии", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Страховая премия"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьСтраховуюПремиюНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраховуюПремиюНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		текОбъект = РеквизитФормыВЗначение("Объект");
		текОбъект.ЗаполнитьСтраховуюПремию();
		ЗначениеВРеквизитФормы(текОбъект,"Объект");
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Функция РассчитатьСтраховуюПремиюСРасшифровкойНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;
КонецФункции

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура СтраховщикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеОтбора  = Новый Структура("Страховщик", Истина);
	ПараметрыВыбораСтраховщика = Новый Структура("Отбор", ЗначениеОтбора);
	ОткрытьФорму("Справочник.упПартнеры.ФормаВыбора", ПараметрыВыбораСтраховщика, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховщикОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Страховщик = ВыбранноеЗначение;
	
КонецПроцедуры

#Область ОповещенияПоСобытиям

&НаКлиенте
//  Процедура - перезаполнить ТЧ "СтраховаяПремия".
//
Процедура ОбработатьВопросОПерезаполненииСтраховойПремии(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 ЗаполнитьСтраховуюПремиюНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура - перезаполнить ТЧ "Задания".
//
Процедура ОбработатьВопросОПерезаполненииЗаданий(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 ЗаполнитьЗаданияНаСервере();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Взаиморасчеты

&НаКлиенте
Процедура ЗагрузитьВзаиморасчетыСПартнерами()
	ВзаиморасчетыЗагрузить = Истина;
	Если РазрешеноЗагружатьВзаиморасчеты 
		И НЕ ВзаиморасчетыЗагружаются Тогда
		ВзаиморасчетыЗагружаются 	= Истина;
		ВзаиморасчетыЗагрузить 		= Ложь;
		ВзаиморасчетыАдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов", 1);
		ЗагрузитьВзаиморасчетыСПартнерамиНаСервере();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВзаиморасчетыСПартнерамиНаСервере()
	Если РазрешеноЗагружатьВзаиморасчеты 
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Страховщик) Тогда
		
		Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Обновить;
		мсвПараметры = Новый Массив;
		мсвПараметры.Добавить(Объект.Страховщик);
		мсвПараметры.Добавить(Объект.Организация);
		мсвПараметры.Добавить(ВзаиморасчетыАдресХранилища);
		ФоновыеЗадания.Выполнить("упВзаиморасчеты.СостояниеВзаиморасчетовСПоставщиками", мсвПараметры);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗагрузкуВзаиморасчетов()
	текРезультат = ПолучитьИзВременногоХранилища(ВзаиморасчетыАдресХранилища);
	Если НЕ текРезультат = Неопределено Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов");		
		ВзаиморасчетыЗагружаются = Ложь;
		Если НЕ ВзаиморасчетыЗагрузить Тогда
			Если НЕ ТипЗнч(текРезультат) = Тип("Булево") Тогда
				СформироватьТаблицуВзаиморасчетов(текРезультат);
				Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Успешно;
				ПоказатьОповещениеПользователя(НСтр("ru = 'Взаиморасчеты загружены.'"),,, БиблиотекаКартинок.Успешно32);
			Иначе
				Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Остановить;	
				ПоказатьОповещениеПользователя(НСтр("ru = 'Не удалось загрузить взаиморасчеты.'"),, НСтр("ru = 'Подробности см. в Журнале регистрации.'"), БиблиотекаКартинок.Ошибка32);
			КонецЕсли;		
		Иначе
			ЗагрузитьВзаиморасчетыСПартнерами();		
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьТаблицуВзаиморасчетов(СтрокаXML)
	
	СостояниеВзаиморасчетов.Очистить();
	
	тбзВзаиморасчеты = упВзаиморасчеты.СформироватьТаблицуВзаиморасчетовСПоставщиками(СтрокаXML);
	
	текСхемаКомпоновкиДанных	= ПолучитьОбщийМакет("упВзаиморасчетыСПоставщиками");
	
	текКомпоновщикМакетаКомпоновкиДанных	= Новый КомпоновщикМакетаКомпоновкиДанных;
	
	текМакетКомпоновкиДанных	= текКомпоновщикМакетаКомпоновкиДанных.Выполнить(текСхемаКомпоновкиДанных, текСхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	текПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	
	текПроцессорКомпоновкиДанных.Инициализировать(текМакетКомпоновкиДанных, Новый Структура("тбзВзаиморасчеты", тбзВзаиморасчеты), );
	
	текПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	текПроцессорВыводаРезультата.УстановитьДокумент(СостояниеВзаиморасчетов);
	
	текПроцессорВыводаРезультата.Вывести(текПроцессорКомпоновкиДанных);

		
КонецПроцедуры

&НаКлиенте
Процедура СтраховщикАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗначениеОтбора  = Новый Структура("Страховщик", Истина);
	ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховщикОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗначениеОтбора  = Новый Структура("Страховщик", Истина);
	ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
	
КонецПроцедуры

&НаСервере
Процедура РейсПриИзмененииНаСервере()
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.Рейс);
		Если ЗначениеЗаполнено(Подразделение)
				И НЕ Подразделение = Объект.Подразделение Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

