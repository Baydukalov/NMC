
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипОснования = ТипЗнч(ДанныеЗаполнения);
	Если ТипОснования = Тип("ДокументСсылка.упРейс") Тогда
		
		Организация		= ДанныеЗаполнения.Организация;
		Рейс			= ДанныеЗаполнения.Ссылка;
		
		ЗаполнитьЗаданияПоРейсу(ДанныеЗаполнения.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения ТЧ "Задания".
//
// Параметры:
//  РейсСсылка  - ДокументСсылка - Ссылка на документ.
//
Процедура ЗаполнитьЗаданияПоРейсу(РейсСсылка) Экспорт
	
	Задания.Очистить();
	Задания.Загрузить(РейсСсылка.Задания.Выгрузить());
	
КонецПроцедуры

// Процедура заполнения ТЧ "СтраховаяПремия".
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьСтраховуюПремию() Экспорт
	
	ИспользуетсяКоличественныйУчетГрузов       = ПолучитьФункциональнуюОпцию("упИспользуетсяКоличественныйУчетГрузов");
	
	СтраховаяПремия.Очистить();
	Для каждого СтрокаСтатьяРасходов Из Соглашение.СтатьиРасходов цикл
		Для каждого СтрокаЗадание Из Задания цикл
			СтрокаСтраховаяПремия = СтраховаяПремия.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСтраховаяПремия, СтрокаЗадание);
			СтрокаСтраховаяПремия.Количество = ?(ИспользуетсяКоличественныйУчетГрузов, СтрокаЗадание.КоличествоГрузов, 1);
			СтрокаСтраховаяПремия.СтатьяРасходов = СтрокаСтатьяРасходов.СтатьяРасходов;
			
			сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаСтатьяРасходов.СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
			СтрокаСтраховаяПремия.СтавкаНДС = сткДанные.СтавкаНДС;
			СтрокаСтраховаяПремия.ПравилоРаспределения = сткДанные.ПравилаРаспределения;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти  

#КонецЕсли
