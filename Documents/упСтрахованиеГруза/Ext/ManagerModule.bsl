#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Рейс");
	БлокируемыеРеквизиты.Добавить("Соглашение");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
 
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упСтрахованиеГруза";
  КомандаПечати.Идентификатор = "СтраховойПолис";
  КомандаПечати.Представление = НСтр("ru = 'Страховой полис'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
   // Настраиваемый комплект документов
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упСтрахованиеГруза";
  КомандаПечати.Порядок = 75;
  КомандаПечати.Идентификатор = "СтраховойПолис";
  КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений  - значение, ссылка на объект, 
//                       представление, имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СтраховойПолис") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"СтраховойПолис", 
		"Страховой полис",
		СформироватьПечатнуюФорму_СтраховойПолис(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упСтрахованиеГруза.СтраховойПолис");
	КонецЕсли;
	
КонецПроцедуры // Печать()

// Функция формирует печатную форму документа.
//
// Параметры:
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат формирования.
//
Функция СформироватьПечатнуюФорму_СтраховойПолис(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упСтрахованиеГруза.СтраховойПолис";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упСтрахованиеГруза.ПФ_MXL_СтраховойПолис");
	
	ОбластьЛоготип = Макет.ПолучитьОбласть("Логотип");
	ОбластьПояснительныйТекстКЛоготипу = Макет.ПолучитьОбласть("ПояснительныйТекстКЛоготипу");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьСтрокаУпаковка = Макет.ПолучитьОбласть("СтрокаУпаковка");
	ОбластьСтрокаПериодПеревозки = Макет.ПолучитьОбласть("СтрокаПериодПеревозки");
	ОбластьСтрокаВидТранспорта = Макет.ПолучитьОбласть("СтрокаВидТранспорта");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтрахованиеГрузаЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК Ссылка
	|ПОМЕСТИТЬ втЗаявки
	|ИЗ
	|	Документ.упСтрахованиеГруза.Задания КАК упСтрахованиеГрузаЗадания
	|ГДЕ
	|	упСтрахованиеГрузаЗадания.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтрахованиеГруза.Соглашение КАК Ссылка
	|ПОМЕСТИТЬ втПрочиеСоглашения
	|ИЗ
	|	Документ.упСтрахованиеГруза КАК упСтрахованиеГруза
	|ГДЕ
	|	упСтрахованиеГруза.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПрочиеСоглашения.Ссылка,
	|	упПрочиеСоглашенияДополнительныеРеквизиты.Свойство.Заголовок КАК Наименование,
	|	упПрочиеСоглашенияДополнительныеРеквизиты.Значение
	|ИЗ
	|	втПрочиеСоглашения КАК втПрочиеСоглашения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПрочиеСоглашения.ДополнительныеРеквизиты КАК упПрочиеСоглашенияДополнительныеРеквизиты
	|		ПО втПрочиеСоглашения.Ссылка = упПрочиеСоглашенияДополнительныеРеквизиты.Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втЗаявки.Ссылка,
	|	упЗаявкаНаПеревозкуГрузаДополнительныеРеквизиты.Свойство.Заголовок,
	|	упЗаявкаНаПеревозкуГрузаДополнительныеРеквизиты.Значение
	|ИЗ
	|	втЗаявки КАК втЗаявки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза.ДополнительныеРеквизиты КАК упЗаявкаНаПеревозкуГрузаДополнительныеРеквизиты
	|		ПО втЗаявки.Ссылка = упЗаявкаНаПеревозкуГрузаДополнительныеРеквизиты.Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	тзДопРеквизиты = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упСтрахованиеГруза.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упСтрахованиеГруза КАК упСтрахованиеГруза
	|ГДЕ упСтрахованиеГруза.Ссылка В  (&МассивОбъектов)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Внутренний.Рейс КАК Рейс,
	|	Внутренний.Задание КАК Задание,
	|	МИНИМУМ(Внутренний.НачалоСтраховки) КАК НачалоСтраховки,
	|	МАКСИМУМ(Внутренний.ОкончаниеСтраховки) КАК ОкончаниеСтраховки
	|ПОМЕСТИТЬ втПериодСтраховки
	|ИЗ
	|	(ВЫБРАТЬ
	|		упРаботыПоРейсу.Рейс КАК Рейс,
	|		упРаботыПоРейсу.Задание КАК Задание,
	|		упРаботыПоРейсу.ПлановоеВремяНачалаРабот КАК НачалоСтраховки,
	|		NULL КАК ОкончаниеСтраховки
	|	ИЗ
	|		втРейсы КАК втРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|		ПО втРейсы.Рейс = упРаботыПоРейсу.Рейс
	|	ГДЕ ЛОЖЬ
	|		ИЛИ упРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|		ИЛИ упРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|	ОБЪЕДИНИТЬ
	|	ВЫБРАТЬ
	|		упРаботыПоРейсу.Рейс КАК Рейс,
	|		упРаботыПоРейсу.Задание КАК Задание,
	|		NULL КАК НачалоСтраховки,
	|		ДОБАВИТЬКДАТЕ(упРаботыПоРейсу.ПлановоеВремяНачалаРабот, ЧАС, упРаботыПоРейсу.ВремяРабот) КАК ОкончаниеСтраховки
	|	ИЗ
	|		втРейсы КАК втРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|		ПО втРейсы.Рейс = упРаботыПоРейсу.Рейс
	|	ГДЕ ЛОЖЬ
	|		ИЛИ упРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|		ИЛИ упРаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПередачаЦенностей)) КАК Внутренний
	|СГРУППИРОВАТЬ ПО
	|	Внутренний.Рейс,
	|	Внутренний.Задание
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтрахованиеГруза.Номер КАК Номер,
	|	упСтрахованиеГруза.Дата КАК Дата,
	|	упСтрахованиеГруза.Страховщик.НаименованиеПолное КАК Страховщик,
	|	упСтрахованиеГруза.Организация.НаименованиеПолное КАК Организация,
	|	упСтрахованиеГруза.КонтактноеЛицо.Должность КАК КонтактноеЛицоДолжность,
	|	упСтрахованиеГруза.КонтактноеЛицо.Наименование КАК КонтактноеЛицоНаименование,
	|	упСтрахованиеГруза.Рейс КАК Рейс,
	|	упСтрахованиеГруза.Соглашение КАК Соглашение,
	|	упПодтверждениеЗаявкиНаТС.ТранспортноеСредство КАК ТС,
	|	упПодтверждениеЗаявкиНаТС.ТранспортноеСредство.ТипТС КАК ТипТС,
	|	упПодтверждениеЗаявкиНаТС.ТранспортноеСредство.Наименование КАК ТранспортноеСредство,
	|	упПодтверждениеЗаявкиНаТС.Водитель1.Наименование КАК Водитель,
	|	упПодтверждениеЗаявкиНаТС.Перевозчик.НаименованиеПолное КАК Перевозчик,
	|	упДействующиеДокументыТСВодителейСрезПоследних.ТипДокумента.Наименование КАК ТипДокумента,
	|	упДействующиеДокументыТСВодителейСрезПоследних.Регистратор.Серия КАК Серия,
	|	упДействующиеДокументыТСВодителейСрезПоследних.Регистратор.Номер КАК НомерДок,
	|	упДействующиеДокументыТСВодителейСрезПоследних.Регистратор.Выдан КАК Выдан,
	|	упДействующиеДокументыТСВодителейСрезПоследних.Регистратор.Дата КАК ДатаВыдачи,
	|	упСтрахованиеГруза.Организация.РуководительПредприятия.Наименование КАК РуководительПредприятияНаименование,
	|	упСтрахованиеГруза.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.упСтрахованиеГруза КАК упСтрахованиеГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упПодтверждениеЗаявкиНаТС КАК упПодтверждениеЗаявкиНаТС
	|	ПО ИСТИНА
	|		И упСтрахованиеГруза.Рейс = упПодтверждениеЗаявкиНаТС.Рейс
	|		И упПодтверждениеЗаявкиНаТС.Проведен
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних КАК упДействующиеДокументыТСВодителейСрезПоследних
	|	ПО ИСТИНА
	|		И упПодтверждениеЗаявкиНаТС.Водитель1 = упДействующиеДокументыТСВодителейСрезПоследних.Водитель
	|		И упДействующиеДокументыТСВодителейСрезПоследних.ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.Паспорт)
	|ГДЕ упСтрахованиеГруза.Ссылка В  (&МассивОбъектов)
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСтрахованиеГрузаСтраховаяПремия.Задание КАК Задание,
	|	упСтрахованиеГрузаСтраховаяПремия.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упСтрахованиеГрузаСтраховаяПремия.Сумма КАК Сумма,
	|	упСтрахованиеГрузаСтраховаяПремия.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упСтрахованиеГрузаСтраховаяПремия.Ссылка.Рейс КАК Рейс,
	|	упЗаданиеНаПеревозкуГруза.Дата КАК ЗаданиеДата,
	|	упЗаданиеНаПеревозкуГруза.НомерКИС КАК НомерКИС,
	|	упЗаданиеНаПеревозкуГруза.ДатаКИС КАК ДатаКИС,
	|	упЗаданиеНаПеревозкуГруза.Получатель.НаименованиеПолное КАК Получатель,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения.Наименование КАК АдресПолучения,
	|	упЗаданиеНаПеревозкуГруза.АдресОтправления.Наименование КАК АдресОтправления,
	|	упЗаданиеНаПеревозкуГруза.Отправитель.НаименованиеПолное КАК Отправитель
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упСтрахованиеГруза.СтраховаяПремия КАК упСтрахованиеГрузаСтраховаяПремия
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО упСтрахованиеГрузаСтраховаяПремия.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|ГДЕ упСтрахованиеГрузаСтраховаяПремия.Ссылка В  (&МассивОбъектов)
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗадания.Задание КАК Задание,
	|	втЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЗадания.Сумма КАК Сумма,
	|	втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЗадания.ЗаданиеДата КАК ЗаданиеДата,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Масса)) КАК ГрузовоеМестоМасса,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Сумма КАК ГрузовоеМестоСумма,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.ТипГруза.Наименование КАК Упаковка,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.ЗонаОтправления.Наименование КАК ЗонаОтправления,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.ЗонаПолучения.Наименование КАК ЗонаПолучения,
	|	втЗадания.Рейс КАК Рейс,
	|	втЗадания.НомерКИС КАК НомерКИС,
	|	втЗадания.ДатаКИС КАК ДатаКИС,
	|	втЗадания.Получатель КАК Получатель,
	|	втЗадания.АдресПолучения КАК АдресПолучения,
	|	втЗадания.АдресОтправления КАК АдресОтправления,
	|	втЗадания.Отправитель КАК Отправитель
	|ПОМЕСТИТЬ втСтроки
	|ИЗ
	|	втЗадания КАК втЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	|	ПО ИСТИНА
	|		И втЗадания.Задание = упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка
	|		И втЗадания.ГрузовоеМесто В (упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто,
	|				ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка))
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗадания КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСтроки.Задание КАК Задание,
	|	СУММА(втСтроки.Сумма) КАК Сумма,
	|	втСтроки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втСтроки.ЗаданиеДата КАК ЗаданиеДата,
	|	СУММА(втСтроки.ГрузовоеМестоМасса) КАК ГрузовоеМестоМасса,
	|	СУММА(втСтроки.ГрузовоеМестоСумма) КАК ГрузовоеМестоСумма,
	|	втСтроки.ЗонаОтправления КАК ЗонаОтправления,
	|	втСтроки.ЗонаПолучения КАК ЗонаПолучения,
	|	втСтроки.Рейс КАК Рейс,
	|	втПериодСтраховки.НачалоСтраховки КАК НачалоСтраховки,
	|	втПериодСтраховки.ОкончаниеСтраховки КАК ОкончаниеСтраховки,
	|	втСтроки.НомерКИС КАК НомерКИС,
	|	втСтроки.ДатаКИС КАК ДатаКИС,
	|	втСтроки.Получатель КАК Получатель,
	|	втСтроки.АдресПолучения КАК АдресПолучения,
	|	втСтроки.АдресОтправления КАК АдресОтправления,
	|	втСтроки.Отправитель КАК Отправитель
	|ИЗ
	|	втСтроки КАК втСтроки
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПериодСтраховки КАК втПериодСтраховки
	|	ПО ИСТИНА
	|		И втСтроки.Рейс = втПериодСтраховки.Рейс
	|		И втСтроки.Задание = втПериодСтраховки.Задание
	|СГРУППИРОВАТЬ ПО
	|	втСтроки.Задание,
	|	втСтроки.ЗаявкаНаПеревозкуГруза,
	|	втСтроки.ЗаданиеДата,
	|	втСтроки.ЗонаОтправления,
	|	втСтроки.ЗонаПолучения,
	|	втСтроки.Рейс,
	|	втПериодСтраховки.НачалоСтраховки,
	|	втПериодСтраховки.ОкончаниеСтраховки,
	|	втСтроки.НомерКИС,
	|	втСтроки.ДатаКИС,
	|	втСтроки.Получатель,
	|	втСтроки.АдресПолучения,
	|	втСтроки.АдресОтправления,
	|	втСтроки.Отправитель
	|УПОРЯДОЧИТЬ ПО
	|	втСтроки.ЗаданиеДата
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСтроки.Рейс КАК Рейс,
	|	втСтроки.Задание КАК Задание,
	|	втСтроки.Упаковка КАК Упаковка
	|ИЗ
	|	втСтроки КАК втСтроки
	|";

	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка = ПакетЗапросов[2].Выбрать();
	тзВыборкаЗадания = ПакетЗапросов[5].Выгрузить();
	тзУпаковки = ПакетЗапросов[6].Выгрузить();
	
	Пока ВыборкаШапка.Следующий() Цикл
		
		Статус = Документы.упРейс.ПолучитьСтатус(ВыборкаШапка.Рейс);
		Если Статус = Перечисления.упСтатусыРейса.Отменен ИЛИ Перечисления.упСтатусыРейса.Индекс(Статус) < 3 тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ ""Страхование груза"" не может быть сформирован, так как на %1 не назначено ТС'"), ВыборкаШапка.Рейс);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			// Продолжить;
		КонецЕсли;
		
		сткПоиск = Новый Структура("Рейс", ВыборкаШапка.Рейс);
		СтрокиПоРейсу = тзВыборкаЗадания.НайтиСтроки(сткПоиск);
		
		Для каждого СтрокаПоРейсу Из СтрокиПоРейсу цикл
		
			Статус = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьСтатус(СтрокаПоРейсу.Задание);
			Если Ложь
				Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
				Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отказ
				Или Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Индекс(Статус) < 2 
			тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ ""Страхование груза"" не может быть сформирован, так как %1 не включено в рейс'"), СтрокаПоРейсу.Задание);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				// Продолжить;
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьЛоготип);
			ОбластьПояснительныйТекстКЛоготипу.Параметры.ПояснительныйТекстКЛоготипу = ПолучитьДопРеквизит(тзДопРеквизиты, ВыборкаШапка.Соглашение, "Текст под логотип");
			ТабличныйДокумент.Вывести(ОбластьПояснительныйТекстКЛоготипу);
		
			ОбластьЗаголовок.Параметры.Заполнить(ВыборкаШапка);
			ОбластьЗаголовок.Параметры.ПояснительныйТекстКЗаголовку = ПолучитьДопРеквизит(тзДопРеквизиты, ВыборкаШапка.Соглашение, "Текст к шапке бланка");
			
			// Штрихкод
			Рисунок = ОбластьЗаголовок.Рисунки.BarCode;
			СтруктураШтрихкода = Новый Структура;
			СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
			СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
			СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(ВыборкаШапка.Ссылка));
			СтруктураШтрихкода.Вставить("ТипКода", 4);
			СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
			СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
			Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		     			
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Наименование груза"; ОбластьСтрока.Параметры.ЗначениеПоля = ПолучитьДопРеквизит(тзДопРеквизиты, СтрокаПоРейсу.ЗаявкаНаПеревозкуГруза, "Описание груза");
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			сткПоискУпаковки = Новый Структура("Рейс, Задание", ВыборкаШапка.Рейс, СтрокаПоРейсу.Задание);
			СтрокиПоУпаковкам = тзУпаковки.НайтиСтроки(сткПоискУпаковки);
			
			стрУпаковка = "";
			Для каждого СтрокаУпаковки Из СтрокиПоУпаковкам цикл
				стрУпаковка = стрУпаковка + ", " + СтрокаУпаковки.Упаковка;
			КонецЦикла;
			
			ОбластьСтрокаУпаковка.Параметры.НаименованиеУпаковки = Сред(стрУпаковка, 3);
			ОбластьСтрокаУпаковка.Параметры.Количество = "" + СтрокаПоРейсу.ГрузовоеМестоМасса + " " + Константы.упЕдиницаИзмеренияМассы.Получить();
			ТабличныйДокумент.Вывести(ОбластьСтрокаУпаковка);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Страховая стоимость груза/ Страховая сумма"; ОбластьСтрока.Параметры.ЗначениеПоля = 
			"" + СтрокаПоРейсу.ГрузовоеМестоСумма
			+ " " + Константы.упВалютаУправленческогоУчета.Получить()
			+ " (" + ЧислоПрописью(СтрокаПоРейсу.ГрузовоеМестоСумма, "Л = ru_RU", НСтр("ru = 'рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2'")) + ")";
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Маршрут"; ОбластьСтрока.Параметры.ЗначениеПоля = "" + СтрокаПоРейсу.ЗонаОтправления + " - " + СтрокаПоРейсу.ЗонаПолучения;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрокаПериодПеревозки.Параметры.НачалоСтраховки = СтрокаПоРейсу.НачалоСтраховки;
			ОбластьСтрокаПериодПеревозки.Параметры.ОкончаниеСтраховки = СтрокаПоРейсу.ОкончаниеСтраховки;
			ТабличныйДокумент.Вывести(ОбластьСтрокаПериодПеревозки);
			
			ОбластьСтрокаВидТранспорта.Параметры.НаименованиеВидаТранспорта	 = ВыборкаШапка.ТипТС;
			ОбластьСтрокаВидТранспорта.Параметры.Перевозчик	 = ВыборкаШапка.Перевозчик;
			ТабличныйДокумент.Вывести(ОбластьСтрокаВидТранспорта);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Транспортное средство"; ОбластьСтрока.Параметры.ЗначениеПоля = ВыборкаШапка.ТранспортноеСредство;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "ФИО и паспортные данные водителя (для автомобильной перевозки)"; ОбластьСтрока.Параметры.ЗначениеПоля = 
			"" + ВыборкаШапка.Водитель + ", " + ВыборкаШапка.ТипДокумента + " серия " + ВыборкаШапка.Серия + " № " + ВыборкаШапка.НомерДок + ", выдан " + ВыборкаШапка.ДатаВыдачи + " " + ВыборкаШапка.Выдан;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Номер и дата ТТН или иного перевозочного документа"; ОбластьСтрока.Параметры.ЗначениеПоля = СтрокаПоРейсу.НомерКИС + " от " + Формат(СтрокаПоРейсу.ДатаКИС, "ДФ=dd.MM.yyyy");
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Грузополучатель (наименование и адрес)"; ОбластьСтрока.Параметры.ЗначениеПоля = "" + СтрокаПоРейсу.Получатель + ", " + СтрокаПоРейсу.АдресПолучения;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Грузоотправитель (наименование и адрес)"; ОбластьСтрока.Параметры.ЗначениеПоля = "" + СтрокаПоРейсу.Отправитель + ", " + СтрокаПоРейсу.АдресОтправления;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Собственник груза"; ОбластьСтрока.Параметры.ЗначениеПоля = СтрокаПоРейсу.Получатель;
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Условия страхования"; ОбластьСтрока.Параметры.ЗначениеПоля = ПолучитьДопРеквизит(тзДопРеквизиты, ВыборкаШапка.Соглашение, "Условия страхования");
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Период ответственности поставщика"; ОбластьСтрока.Параметры.ЗначениеПоля = ПолучитьДопРеквизит(тзДопРеквизиты, ВыборкаШапка.Соглашение, "Период ответственности поставщика");
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Франшиза"; ОбластьСтрока.Параметры.ЗначениеПоля = ПолучитьДопРеквизит(тзДопРеквизиты, ВыборкаШапка.Соглашение, "Франшиза");
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьСтрока.Параметры.НаименованиеПоля = "Страховая премия"; ОбластьСтрока.Параметры.ЗначениеПоля = 
			"" + СтрокаПоРейсу.Сумма
			+ " " + Константы.упВалютаУправленческогоУчета.Получить()
			+ " (" + ЧислоПрописью(СтрокаПоРейсу.Сумма, "Л = ru_RU", НСтр("ru = 'рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2'")) + ")";
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			ОбластьПодвал.Параметры.Заполнить(ВыборкаШапка);
			Если ЗначениеЗаполнено(ВыборкаШапка.РуководительПредприятияНаименование) тогда
				ОбластьПодвал.Параметры.ФИО_Организация = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(ВыборкаШапка.РуководительПредприятияНаименование);
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаШапка.КонтактноеЛицоНаименование) тогда
				ОбластьПодвал.Параметры.ФИО_Страховщик = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(ВыборкаШапка.КонтактноеЛицоНаименование);
			КонецЕсли;
			ТабличныйДокумент.Вывести(ОбластьПодвал);
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_СтраховойПолис

Функция ПолучитьДопРеквизит(тзДопРеквизиты, Ссылка, Наименование)
	
	сткПоиска = Новый Структура("Ссылка, Наименование", Ссылка, Наименование);
	НайденныеСтроки = тзДопРеквизиты.НайтиСтроки(сткПоиска);
	Если НайденныеСтроки.Количество() > 0 тогда
		Возврат "" + НайденныеСтроки[0].Значение;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции
#КонецОбласти

#КонецЕсли
