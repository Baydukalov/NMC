
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ИспользуемыеРегистры.ЗагрузитьЗначения(Параметры.СписокРегистров.ВыгрузитьЗначения());
	
	ЗаполнитьПоРегистрам("РегистрыНакопления", РегистрыНакопления);
	ЗаполнитьПоРегистрам("РегистрыСведений", РегистрыСведений);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЗаголовокРегистрыНакопления = ЗаголовокСтраницы(РегистрыНакопления);
	ЗаголовокРегистрыСведений = ЗаголовокСтраницы(РегистрыСведений);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокРегистровНакопленияПриИзменении(Элемент)

	ЗаголовокРегистрыНакопления = ЗаголовокСтраницы(РегистрыНакопления);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРегистровСведенийПриИзменении(Элемент)

	ЗаголовокРегистрыСведений = ЗаголовокСтраницы(РегистрыСведений);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	Результат = Новый СписокЗначений;

	ЗаполнитьСписокРегистровДляКорректировки(Результат, РегистрыНакопления);
	ЗаполнитьСписокРегистровДляКорректировки(Результат, РегистрыСведений);

	Закрыть(Результат);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Функция ЗаголовокСтраницы(СписокРегистровСтраницы)

	Возврат СтрШаблон("%1/%2", Строка(КоличествоОтмеченныхВСписке(СписокРегистровСтраницы)), Строка(СписокРегистровСтраницы.Количество()));

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КоличествоОтмеченныхВСписке(Список)

	Результат = 0;
	
	Для Каждого ЭлементСписка Из Список Цикл
		
		Если ЭлементСписка.Пометка Тогда
			Результат = Результат + 1;
		КонецЕсли;
		
	КонецЦикла;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ЗаполнитьСписокРегистровДляКорректировки(Результат, СписокРегистров)

	Для Каждого Элемент Из СписокРегистров Цикл

		РегистрИспользуетсяДокументом = ИспользуемыеРегистры.НайтиПоЗначению(Элемент.Значение) <> Неопределено;
		
		Если Истина
			И Элемент.Пометка 
			И Не РегистрИспользуетсяДокументом Тогда
			
			// Регистр добавляется в список с пометкой использования.

			Результат.Добавить(Элемент.Значение, Элемент.Представление, Истина);

		
		ИначеЕсли Истина
				И Не Элемент.Пометка 
				И РегистрИспользуетсяДокументом Тогда
				
			// Регистр добавляется с отключенной пометкой использования.	

			Результат.Добавить(Элемент.Значение, Элемент.Представление, Ложь);

		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоРегистрам(ВидРегистров, СписокРегистров)
	
	Для Каждого Регистр Из Метаданные.Документы.упКорректировкаРегистров.Движения Цикл

		Если Метаданные[ВидРегистров].Содержит(Регистр) Тогда

			Пометка = ИспользуемыеРегистры.НайтиПоЗначению(Регистр.Имя) <> Неопределено;
			СписокРегистров.Добавить(Регистр.Имя, Регистр.Синоним, Пометка);

		КонецЕсли;

	КонецЦикла;

	СписокРегистров.СортироватьПоЗначению(НаправлениеСортировки.Возр);

	
КонецПроцедуры

#КонецОбласти

