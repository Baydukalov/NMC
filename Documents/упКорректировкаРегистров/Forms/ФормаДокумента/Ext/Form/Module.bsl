
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПриСозданииПриЧтенииНаСервере();
	
	ПоказатьРегистры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьСоставРегистров(Команда)

	Регистры = Новый СписокЗначений;

	Для Каждого Строка Из Объект.Регистры Цикл
		Регистры.Добавить(Строка.Имя);
	КонецЦикла;

	ОписаниеОповещенияОЗавершенииВыбора = Новый ОписаниеОповещения("РедактироватьСоставРегистровЗавершение", ЭтаФорма);
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("СписокРегистров", Регистры);
	
	ОткрытьФорму("Документ.упКорректировкаРегистров.Форма.ФормаВыборРегистра", сткПараметры,,,,, ОписаниеОповещенияОЗавершенииВыбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДобавитьСтраницу(ИмяСтраницы, Заголовок, Родитель)

	Страница = Элементы.Добавить(ИмяСтраницы, Тип("ГруппаФормы"), Родитель);
	Страница.Заголовок = Заголовок;
	Страница.Вид = ВидГруппыФормы.Страница;
	
	Страница.РастягиватьПоГоризонтали = Истина;
	Страница.РастягиватьПоВертикали   = Истина;
	

	Возврат Страница;

КонецФункции

&НаСервере
Процедура ПоказатьТаблицуРегистра(Знач СтрокаТЧ)

	Если Метаданные.РегистрыНакопления.Найти(СтрокаТЧ.Имя) <> Неопределено Тогда

		СтраницаРегистра      = Элементы.РегистрыНакопления;
		МенеджерРегистра      = РегистрыНакопления[СтрокаТЧ.Имя];
		МетаданныеРегистра    = Метаданные.РегистрыНакопления[СтрокаТЧ.Имя];
		ЕстьПолеПериод= Истина;

	ИначеЕсли Метаданные.РегистрыСведений.Найти(СтрокаТЧ.Имя) <> Неопределено Тогда

		СтраницаРегистра      = Элементы.РегистрыСведений;
		МенеджерРегистра      = РегистрыСведений[СтрокаТЧ.Имя];
		МетаданныеРегистра    = Метаданные.РегистрыСведений[СтрокаТЧ.Имя];

		ЕстьПолеПериод = МетаданныеРегистра.ПериодичностьРегистраСведений 
									<> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический;

	Иначе

		Возврат;

	КонецЕсли;

	МассивКолонокРегистра = МассивПолейРегистра(МенеджерРегистра, МетаданныеРегистра);

	СтраницаДляРегистра = ДобавитьСтраницу(ИмяСтраницыРегистра(СтрокаТЧ.Имя), МетаданныеРегистра.Синоним, СтраницаРегистра);

	ТаблицаФормы = ДобавитьТаблицуФормыРегистра(СтрокаТЧ.Имя, МассивКолонокРегистра, СтраницаДляРегистра);

	Если ЕстьПолеПериод Тогда
		ТаблицаФормы.УстановитьДействие("ПриНачалеРедактирования", "Подключаемый_ТаблицаРегистровПриНачалеРедактирования");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ИмяСтраницыРегистра(ИмяРегистра)

	Результат = "Страница" + ИмяРегистра;
	
	Возврат Результат;

КонецФункции

&НаСервере
Процедура УдалитьСтраницуРегистра(ИмяРегистра)

	Элементы.Удалить(Элементы.Найти(ИмяСтраницыРегистра(ИмяРегистра)));

КонецПроцедуры

Функция СоздатьСвязиПараметровВыбораДляПоляФормы(ИсходныйМассив, ПутьКДанным)

	Результат = Новый Массив;
	
	Для Каждого Элемент Из ИсходныйМассив Цикл

		Результат.Добавить(Новый СвязьПараметраВыбора(Элемент.Имя, ПутьКДанным + "." + Элемент.ПутьКДанным, Элемент.ИзменениеЗначения));

	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(Результат);

КонецФункции

&НаСервере
Функция МассивПолейРегистра(МенеджерРегистра, МетаданныеРегистра)

	ТаблицаРегистра = МенеджерРегистра.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаРегистра.Колонки.Удалить("Регистратор");
	Если ТаблицаРегистра.Колонки.Найти("МоментВремени") <> Неопределено Тогда
		ТаблицаРегистра.Колонки.Удалить("МоментВремени");
	КонецЕсли;

	МассивКолонок = Новый Массив;
	Для Каждого Колонка Из ТаблицаРегистра.Колонки Цикл

		сткКолонка = Новый Структура("Имя, Заголовок, СвязиПараметровВыбора", Колонка.Имя);

		МассивКолонок.Добавить(сткКолонка);

	КонецЦикла;

	// Обновление заголовков колонок таблицы по синонимам полей регистра.
	МассивПолейРегистра = Новый Массив;
	МассивПолейРегистра.Добавить("Измерения");
	МассивПолейРегистра.Добавить("Ресурсы");
	МассивПолейРегистра.Добавить("Реквизиты");

	Для Каждого ВидПоля Из МассивПолейРегистра Цикл
		Для Каждого Поле Из МетаданныеРегистра[ВидПоля] Цикл
			Для Каждого ЭлементМассива Из МассивКолонок Цикл

				Если ЭлементМассива.Имя = Поле.Имя Тогда

					ЭлементМассива.Заголовок             = Поле.Синоним;
					ЭлементМассива.СвязиПараметровВыбора = Поле.СвязиПараметровВыбора;

				КонецЕсли;

			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	Возврат МассивКолонок;

КонецФункции

&НаСервере
Функция ДобавитьТаблицуФормыРегистра(ИмяРегистра, КолонкиТаблицы, Родитель)

	ТаблицаФормы = Элементы.Добавить("ТаблицаДвижений" + ИмяРегистра, Тип("ТаблицаФормы"), Родитель);
	ТаблицаФормы.ПутьКДанным      = "Объект.Движения." + ИмяРегистра;
	Родитель.ПутьКДаннымЗаголовка = ТаблицаФормы.ПутьКДанным + ".КоличествоСтрок";

	МассивДобавленныхПолей = Новый Массив;
	
	Для Каждого Колонка Из КолонкиТаблицы Цикл

		ПолеФормы = Элементы.Добавить(ТаблицаФормы.Имя + Колонка.Имя, Тип("ПолеФормы"), ТаблицаФормы);
		ПолеФормы.ПутьКДанным = ТаблицаФормы.ПутьКДанным + "." + Колонка.Имя;
		ПолеФормы.Заголовок   = Колонка.Заголовок;
		ПолеФормы.Вид         = ВидПоляФормы.ПолеВвода;

		МассивДобавленныхПолей.Добавить(ПолеФормы);

	КонецЦикла;

	Счетчик = 0;
	Для Каждого ПолеФормы Из МассивДобавленныхПолей Цикл

		Если Истина
			И КолонкиТаблицы[Счетчик].СвязиПараметровВыбора <> Неопределено 
			И КолонкиТаблицы[Счетчик].СвязиПараметровВыбора.Количество() > 0 
		Тогда

			ПолеФормы.СвязиПараметровВыбора = СоздатьСвязиПараметровВыбораДляПоляФормы(КолонкиТаблицы[Счетчик].СвязиПараметровВыбора,
									 "Элементы." + ТаблицаФормы.Имя + ".ТекущиеДанные");
		КонецЕсли;

		Счетчик = Счетчик + 1;
	
	КонецЦикла;

	Возврат ТаблицаФормы;

КонецФункции

&НаСервере
Процедура ОбработатьИзменениеСоставаРегистров(СписокРегистров)

	Модифицированность = Истина;
	
	Для Каждого Элемент Из СписокРегистров Цикл

		// Нужно добавить новый регистр.
		Если Элемент.Пометка Тогда

			СтрокаТЧ = Объект.Регистры.Добавить();
			СтрокаТЧ.Имя = Элемент.Значение;

			ПоказатьТаблицуРегистра(СтрокаТЧ);

		Иначе
			
			мсвСтрокиДляУдаления = Объект.Регистры.НайтиСтроки(Новый Структура("Имя", Элемент.Значение));

			Для Каждого Строка Из мсвСтрокиДляУдаления Цикл
				Объект.Регистры.Удалить(Строка);
			КонецЦикла;

			Объект.Движения[Элемент.Значение].Очистить();
			
			УдалитьСтраницуРегистра(Элемент.Значение);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПоказатьРегистры()

	Для Каждого СтрокаТаб Из Объект.Регистры Цикл

		ПоказатьТаблицуРегистра(СтрокаТаб);

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаРегистровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Период = Объект.Дата;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСоставРегистровЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если ТипЗнч(Результат) = Тип("СписокЗначений") Тогда
	    
	    ОбработатьИзменениеСоставаРегистров(Результат);
        
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

