#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
		
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;

	КонецЕсли;	
	
	УстановитьВидимость();
		
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
			
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		УстановитьВалютуУчета();
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	сткСпособыЗаполнения = Новый Структура;
	сткСпособыЗаполнения.Вставить("ЗаполнитьПоЗаданиям", 	1);
	сткСпособыЗаполнения.Вставить("ЗаполнитьПоРейсам", 		2);
	
	ЭтаФорма.СпособыЗаполнения = Новый ФиксированнаяСтруктура(сткСпособыЗаполнения);
	
	ЭтаФорма.ТекущийСпособЗаполнения = СпособыЗаполнения.ЗаполнитьПоЗаданиям;	
	
	ПриСозданииПриЧтенииНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
		
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ЭтоЗаполнениеРасходов Тогда
		// Определяются нерассчитанные строки.
		Для каждого Строка Из Объект.Расходы Цикл
			Если Строка.Сумма = 0 Тогда
				ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОСтрокахСНулевымиСуммами",ЭтаФорма);
				ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Удалить строки расходов с нулевыми суммами?'"), РежимДиалогаВопрос.ДаНет);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Документ.упРейс.Форма.ФормаВыбора" Тогда  
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
			Если Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам") Тогда
				Для каждого Рейс Из ВыбранноеЗначение Цикл
					Если Объект.Рейсы.НайтиСтроки(Новый Структура("Рейс", Рейс)).Количество() = 0 Тогда
						НоваяСтрока = Объект.Рейсы.Добавить();
						НоваяСтрока.Рейс = Рейс;
					КонецЕсли;
				КонецЦикла;		
			ИначеЕсли Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам") Тогда
				
				Если ЗаполнитьПлановымиРасходамиПослеПодбора Тогда
					
					Если ТекущийСпособЗаполнения = СпособыЗаполнения.ЗаполнитьПоЗаданиям Тогда
						ЗаполнитьПлановымиРаходамиПоЗаданиям(ВыбранноеЗначение);
					Иначе	
						ЗаполнитьПлановымиРаходамиПоРейсам(ВыбранноеЗначение);								
					КонецЕсли;
				Иначе	
					Для каждого Рейс Из ВыбранноеЗначение Цикл
						Если Объект.Расходы.НайтиСтроки(Новый Структура("Рейс", Рейс)).Количество() = 0 Тогда
							НоваяСтрока = Объект.Расходы.Добавить();
							НоваяСтрока.Рейс = Рейс;
						КонецЕсли;
					КонецЦикла;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОСтрокахСНулевымиСуммами(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Расходы	= Объект.Расходы;
		КоличествоСтрок = Расходы.Количество();
		Для й = 1 По КоличествоСтрок Цикл
			Если Расходы[КоличествоСтрок - й].Сумма = 0 Тогда
				Расходы.Удалить(КоличествоСтрок - й);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура РасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		ТекущаяСтрока.Количество 	= 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура СпособВводаРасходовПриИзменении(Элемент)
	
	УстановитьВидимость();
		
КонецПроцедуры

&НаКлиенте
Процедура РасходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		сткРеквизитыСтатьиРасходов = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
		ТекущаяСтрока.СтавкаНДС 			= сткРеквизитыСтатьиРасходов.СтавкаНДС;
		ТекущаяСтрока.ПравилоРаспределения 	= сткРеквизитыСтатьиРасходов.ПравилаРаспределения;
		ПересчитатьСтроку(Элемент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	Структура = ПолучитьОсновногоКонтрагентаИСоглашение(Объект.Партнер,Объект.Организация);
	Объект.Контрагент = Структура.Контрагент;
	Объект.Соглашение = Структура.Соглашение;
	
	ВыполнитьПроверкуВалютыСоглашения();
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	
	ВыполнитьПроверкуВалютыСоглашения();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	мсвЗадания 	= ЗаданияПоРейсам();
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
	сткПараметры 	= Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.РасходыЗадание);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура РасходыЗаданиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	мсвЗадания 	= ЗаданияПоРейсам();
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
    ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура РасходыЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	мсвЗадания 	= ЗаданияПоРейсам();
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
    ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьРасходы(Команда)
	РассчитатьРасходыНаСервере();
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьРасходыСРасшифровкой(Команда)
	мсвПриемников = РассчитатьРасходыСРасшифровкойНаСервере();
	РассчитатьИтоговуюСуммуРасходы();
	Для каждого Приемник Из мсвПриемников Цикл
		Приемник.Приемник.Показать("Расшифровка расчетов");	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлановымиРасходами(Команда)
	ЭтоЗаполнениеРасходов	= Истина;
	ТекущийСпособЗаполнения = СпособыЗаполнения.ЗаполнитьПоЗаданиям;	
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьПлановымиРасходамиКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлановымиРасходамиПоРейсам(Команда)
	ЭтоЗаполнениеРасходов	= Истина;
	ТекущийСпособЗаполнения = СпособыЗаполнения.ЗаполнитьПоРейсам;	
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьПлановымиРасходамиКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ПодборРейса(Команда)
	ЗаполнитьПлановымиРасходамиПослеПодбора = Ложь;
	ПараметрыПодбора = Новый Структура("Отбор, МножественныйВыбор, ЗакрыватьПриВыборе",Новый Структура("Организация, Перевозчик, Соглашение, КонтрагентПеревозчика", Объект.Организация, Объект.Партнер, Объект.Соглашение, Объект.Контрагент), Истина, Ложь);
	ОткрытьФорму("Документ.упРейс.ФормаВыбора", ПараметрыПодбора, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаКлиенте
// Процедура заполнения плановых расходов.
// 
Процедура ЗаполнитьПлановымиРасходамиКлиент(Результат, ДополнительныеПараметры) Экспорт
	ЭтоЗаполнениеРасходов	= Ложь;
	Если ТекущийСпособЗаполнения = СпособыЗаполнения.ЗаполнитьПоЗаданиям Тогда
		Если Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам") Тогда
			ЗаполнитьПлановымиРасходамиПослеПодбора = Истина;
			ПараметрыПодбора = Новый Структура("Отбор, МножественныйВыбор, ЗакрыватьПриВыборе",Новый Структура("Организация, Перевозчик, Соглашение, КонтрагентПеревозчика", Объект.Организация, Объект.Партнер, Объект.Соглашение, Объект.Контрагент), Истина, Ложь);
			ОткрытьФорму("Документ.упРейс.ФормаВыбора", ПараметрыПодбора, ЭтаФорма, УникальныйИдентификатор);
		Иначе	
			 ЗаполнитьПлановымиРаходамиПоЗаданиям();
		КонецЕсли;
	Иначе
		Если Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам") Тогда
			ЗаполнитьПлановымиРасходамиПослеПодбора = Истина;
			ПараметрыПодбора = Новый Структура("Отбор, МножественныйВыбор, ЗакрыватьПриВыборе",Новый Структура("Организация, Перевозчик, Соглашение, КонтрагентПеревозчика", Объект.Организация, Объект.Партнер, Объект.Соглашение, Объект.Контрагент), Истина, Ложь);
			ОткрытьФорму("Документ.упРейс.ФормаВыбора", ПараметрыПодбора, ЭтаФорма, УникальныйИдентификатор);
		Иначе
			ЗаполнитьПлановымиРаходамиПоРейсам();	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Элементы.Рейс.Видимость        = Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу;
	Элементы.РасходыРейс.Видимость = Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам;
	Элементы.ГруппаРейсы.Видимость = Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам;
	Если Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам") Тогда
		 Элементы.РасходыПодборРейса.Видимость 					= Истина;
		 Элементы.РейсыПодборРейса.Видимость 					= Ложь;
	ИначеЕсли Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам") Тогда
		 Элементы.РасходыПодборРейса.Видимость 					= Ложь;
		 Элементы.РейсыПодборРейса.Видимость 					= Истина;
	ИначеЕсли Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу") Тогда	 
		 Элементы.РасходыПодборРейса.Видимость 					= Ложь;
		 Элементы.РейсыПодборРейса.Видимость 					= Ложь;
	КонецЕсли;
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "Расходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Расходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаСервереБезКонтекста
Функция СписокЗаданийПоРейсу(Рейс)
	Возврат упРейс.СписокЗаданийПоРейсу(Рейс);
КонецФункции

&НаКлиенте
Функция ЗаданияПоРейсам()
	Если Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу") Тогда
		мсвРейсы	= Новый Массив;
		мсвРейсы.Добавить(Объект.Рейс);
	ИначеЕсли	Объект.СпособВводаРасходов = ПредопределенноеЗначение("Перечисление.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам") Тогда
		текРейс		= Элементы.Расходы.ТекущиеДанные.Рейс;
		мсвРейсы	= Новый Массив;
		мсвРейсы.Добавить(текРейс);
	Иначе
		мсвРейсы	= Новый Массив;
		Для каждого СтрокаРейс Из Объект.Рейсы Цикл
			Если мсвРейсы.Найти(СтрокаРейс.Рейс) = Неопределено Тогда
				мсвРейсы.Добавить(СтрокаРейс.Рейс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	мсвЗадания 	= СписокЗаданийПоРейсу(мсвРейсы);
	
	Возврат мсвЗадания;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПлановымиРаходамиПоЗаданиям(мсвРейсы = Неопределено)
	Если мсвРейсы = Неопределено Тогда
		мсвРейсы = Новый Массив;	
	КонецЕсли;
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		мсвРейсы.Добавить(Объект.Рейс);
		тбзПлановыеРасходы = упРейс.ПлановыеРасходыПоЗаданиямРейсов(мсвРейсы, ВедетсяВалютныйУчет);
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		мсвРейсы = Объект.Рейсы.Выгрузить(,"Рейс").ВыгрузитьКолонку("Рейс");
		тбзПлановыеРасходы = упРейс.ПлановыеРасходыПоЗаданиямРейсов(мсвРейсы, ВедетсяВалютныйУчет);
		
	ИначеЕсли Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда
		
		тбзПлановыеРасходы = упРаспределениеРасходов.ПлановыеРасходыПоВыполненнымЗаданиямРейсов(мсвРейсы, ВедетсяВалютныйУчет);
		
	КонецЕсли;
	
	Объект.Расходы.Очистить();
	
	Для каждого СтрокаПлановыйРасход Из тбзПлановыеРасходы Цикл
		НоваяСтрока = Объект.Расходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПлановыйРасход);
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Цена = СтрокаПлановыйРасход.Сумма;
		СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
		упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока,"Сумма",СтруктураДанные);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановымиРаходамиПоРейсам(мсвРейсы = Неопределено)
	
	Если мсвРейсы = Неопределено Тогда
		мсвРейсы = Новый Массив;	
	КонецЕсли;
	
	Если Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		мсвРейсы.Добавить(Объект.Рейс);
		тбзПлановыеРасходы = упРейс.ПлановыеРасходыПоРейсам(мсвРейсы, ВедетсяВалютныйУчет);
	ИначеЕсли 	Объект.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда
		мсвРейсы = Объект.Рейсы.Выгрузить(,"Рейс").ВыгрузитьКолонку("Рейс");
		тбзПлановыеРасходы = упРейс.ПлановыеРасходыПоРейсам(мсвРейсы, ВедетсяВалютныйУчет);
	Иначе
		тбзПлановыеРасходы = упРейс.ПлановыеРасходыПоРейсам(мсвРейсы, ВедетсяВалютныйУчет, Истина);
	КонецЕсли;

	Для каждого СтрокаПлановыйРасход Из тбзПлановыеРасходы Цикл
		НоваяСтрока = Объект.Расходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПлановыйРасход);
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Цена = СтрокаПлановыйРасход.Сумма;
		СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
		упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока,"Сумма",СтруктураДанные);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ВопросОПерезаполненииРасходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Если ТекущийСпособЗаполнения = СпособыЗаполнения.ЗаполнитьПоЗаданиям Тогда
			ЗаполнитьПлановымиРаходамиПоЗаданиям();
		Иначе
			ЗаполнитьПлановымиРаходамиПоРейсам();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РассчитатьРасходыНаСервере()
	ВходныеПараметры = Новый Структура();
	упТарификацияВызовСервера.Рассчитать(Объект, ВходныеПараметры);
КонецПроцедуры

&НаСервере
Функция РассчитатьРасходыСРасшифровкойНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагентаИСоглашение(Партнер,Организация)
	
	Результат = Новый Структура;
	Результат.Вставить("Контрагент",Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер));
	
	Соглашение = Справочники.упСоглашенияСПеревозчиками.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Соглашения.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками КАК Соглашения
	|ГДЕ
	|	НЕ Соглашения.ПометкаУдаления
	|	И Соглашения.Владелец = &Владелец
	|	И ВЫБОР
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Соглашения.Организация = &Организация
	|		КОНЕЦ";
	Запрос.УстановитьПараметр("Владелец", Партнер);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Соглашение = Выборка.Соглашение;
	Иначе
		Соглашение = Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(Партнер);
	КонецЕсли;
	
	Результат.Вставить("Соглашение",Соглашение);
	
	Возврат Результат;
	
КонецФункции // ПолучитьОсновногоКонтрагентаИСоглашение()

&НаКлиенте
// Выполним действия для установки валютного учета.
//
Процедура ВыполнитьПроверкуВалютыСоглашения()

	Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		УстановитьВалютуУчета();	
	КонецЕсли;

КонецПроцедуры // ВыполнитьПроверкуВалютыСоглашения()


// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти
