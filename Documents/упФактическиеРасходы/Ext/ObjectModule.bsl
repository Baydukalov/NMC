
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		Рейсы.Очистить();
		НоваяСтрока = Рейсы.Добавить();
		НоваяСтрока.Рейс = Рейс;
		
	ИначеЕсли СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда	
		Рейсы.Очистить();
		мсвРейсы = Расходы.ВыгрузитьКолонку("Рейс");
		мсвРейсы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвРейсы);
		Для каждого РейсТекущий Из мсвРейсы Цикл
			Если ЗначениеЗаполнено(РейсТекущий) Тогда
				НоваяСтрока = Рейсы.Добавить();
				НоваяСтрока.Рейс = РейсТекущий;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли; 
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		// Вынесено сюда для корректного подсчета итоговых сумм в валюте управленческого учета.
		// Заполняются таблицы расходов, формируются аналитические разрезы,
		// подсчитываются итоговые суммы в валюте управленческого учета.
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходы");
	
	Если СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);	
	ИначеЕсли СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда
		ЭлементБлокировки.ИсточникДанных = Расходы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Рейс", "Рейс");
	ИначеЕсли СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда		
		ЭлементБлокировки.ИсточникДанных = Рейсы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Рейс", "Рейс");
	КонецЕсли;
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;		
			
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Рейс",                Рейс);
	РеквизитыОбъекта.Вставить("Дата",                Дата);
	РеквизитыОбъекта.Вставить("Расходы",             Расходы);
	РеквизитыОбъекта.Вставить("Рейсы",               Рейсы);
	РеквизитыОбъекта.Вставить("Организация",         Организация);
	РеквизитыОбъекта.Вставить("Подразделение",       Подразделение);
	РеквизитыОбъекта.Вставить("СпособВводаРасходов", СпособВводаРасходов);
	РеквизитыОбъекта.Вставить("НаправлениеДеятельности", Справочники.упНаправленияДеятельности.ПустаяСсылка());
	
	// Формируются фактические расходы
	упРаспределениеРасходов.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда
		Организация = ДанныеЗаполнения.Организация;
		Рейс 		= ДанныеЗаполнения.Ссылка;
		
		сткПараметрыРейса = упРейс.ПараметрыПеревозкиРейса(Рейс);
		
		Партнер 	= сткПараметрыРейса.Перевозчик;
		Контрагент	= сткПараметрыРейса.КонтрагентПеревозчика;
		Соглашение	= сткПараметрыРейса.Соглашение;
		СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы.Рейс");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Расходы.Рейс");
	ИначеЕсли СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы.Рейс");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейс");
	ИначеЕсли СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейс");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Расходы.Рейс");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти

#КонецЕсли