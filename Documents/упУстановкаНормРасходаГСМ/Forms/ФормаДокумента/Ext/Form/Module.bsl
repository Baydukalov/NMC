#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ИспользуетсяУчетПодразделений		= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
	КонецЕсли;
	
	Для каждого Строка Из Объект.НормыТранспортныхСредств Цикл
		ЗаполнитьРасходВсего(Строка, Объект.ОбъектНормРасходаГСМ);
		ЗаполнитьНадписиНормы(Строка)	
	КонецЦикла;
	
	Для каждого Строка Из Объект.НормыДополнительногоОборудования Цикл
		ЗаполнитьРасходВсего(Строка, Объект.ОбъектНормРасходаГСМ);
		ЗаполнитьНадписиНормы(Строка)	
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ЗаблокироватьЭлементы();
КонецПроцедуры	

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли;    
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЗаполнитьДопСведенияТабличнойЧасти();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
    Если ТекущаяСтраница.Имя = "ГруппаДополнительно"
        И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
        СвойстваВыполнитьОтложеннуюИнициализацию();
        УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
    КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	Если Истина
		И НЕ Копирование
		И НоваяСтрока Тогда
		ЗаполнитьТекущиеДанныеПоУмолчанию(ТекущиеДанные);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	Если Истина
		И НЕ Копирование
		И НоваяСтрока Тогда
		ЗаполнитьТекущиеДанныеПоУмолчанию(ТекущиеДанные);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипНормыРасходаГСМПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьНадписиНормы(ТекущиеДанные);
	Если ТекущиеДанные.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаОдинЧасРаботы") Тогда
		ТекущиеДанные.ИзменениеРасхода = 0;
	КонецЕсли;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияТипНормыРасходаГСМПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьНадписиНормы(ТекущиеДанные);
	Если ТекущиеДанные.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаОдинЧасРаботы") Тогда
		ТекущиеДанные.ИзменениеРасхода = 0;
	КонецЕсли;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НЕ ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		мсвЗначений = Новый Массив;
		мсвЗначений.Добавить(ВыбранноеЗначение);
	Иначе	
		мсвЗначений = ВыбранноеЗначение;
	КонецЕсли;
	
	Если мсвЗначений.Количество() > 0 Тогда
		ПервыйЭлемент = мсвЗначений[0];
		Если Ложь
			Или ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.упДополнительноеОборудование")
			Или ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.упТранспортныеСредства") 
		Тогда
			СоответствиеВидыГСМ		= ОсновныеВидыГСМПоТранспортномСредствамНаСервере(мсвЗначений);
			ЭтоУстановкаНормДляТС 	= ЭтоУстановкаНормДляТранспортногоСредства(Объект.ОбъектНормРасходаГСМ);

			Если ЭтоУстановкаНормДляТС Тогда
				ТабличнаяЧасть = Объект.НормыТранспортныхСредств;
			Иначе
				ТабличнаяЧасть = Объект.НормыДополнительногоОборудования;
			КонецЕсли;

			Для каждого Элемент Из мсвЗначений Цикл
				НоваяСтрока = ТабличнаяЧасть.Добавить();
				Если ЭтоУстановкаНормДляТС Тогда
					НоваяСтрока.ТранспортноеСредство 		= Элемент;
				Иначе
					НоваяСтрока.ДополнительноеОборудование 	= Элемент;
				КонецЕсли;
				НоваяСтрока.ВидГСМ	 = СоответствиеВидыГСМ.Получить(Элемент);
				ЗаполнитьТекущиеДанныеПоУмолчанию(НоваяСтрока);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаОтопительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходКондиционерПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаКондиционерПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходКлиматКонтрольПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаКлиматКонтрольПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходГрузовойПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаГрузовойПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходСпециальноеОборудованиеПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаСпециальноеОборудованиеПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходПростойПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаЗимнийПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходОтопительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаПростойПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходДлительнаяЭксплуатацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаДлительнаяЭксплуатацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходСложностьДорогПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаСложностьДорогПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходГорнаяМестностьПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаГорнаяМестностьПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходНаселенныйПунктПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаНаселенныйПунктПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходРабочаяОперацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствТипРасходаРабочаяОперацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры


&НаКлиенте
Процедура НормыТранспортныхСредствРасходЗимнийПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияРасходПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствРасходПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияРасходЗимнийПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияТипРасходаЗимнийПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияРасходДлительнаяЭксплуатацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияТипРасходаДлительнаяЭксплуатацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияРасходКондиционерПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияТипРасходаКондиционерПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияРасходОтопительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияТипРасходаОтопительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияРасходРабочаяОперацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияТипРасходаРабочаяОперацияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ЗаполнитьРасходВсего(ТекущиеДанные, Объект.ОбъектНормРасходаГСМ);
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	СтрокаДопОбор = Элемент.ТекущиеДанные;
	Если СтрокаДопОбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительноеОборудование = СтрокаДопОбор.ДополнительноеОборудование;
	КлючСвязи = СтрокаДопОбор.КлючСвязи;
	Если ПустаяСтрока(КлючСвязи)Тогда
		КлючСвязи = Новый УникальныйИдентификатор;
		СтрокаДопОбор.КлючСвязи = КлючСвязи;
	КонецЕсли;	
	ИспользуютсяРазличныеРежимыРаботы = Ложь;
	ИзменилиДополнительноеОборудование = Истина;
	
	Если НоваяСтрока Тогда
		Элементы.НормыРежимовРаботыДополнительногоОборудования.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", КлючСвязи);
	Иначе
		ИзменилиДополнительноеОборудование = ПроверитьНормыРежимовРаботыДополнительногоОборудования(ДополнительноеОборудование, КлючСвязи);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительноеОборудование) Тогда
		ИспользуютсяРазличныеРежимыРаботы = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ДополнительноеОборудование, "ИспользуютсяРазличныеРежимыРаботы");
		Если ИспользуютсяРазличныеРежимыРаботы И ИзменилиДополнительноеОборудование Тогда
			ЗаполнитьРежимыРаботыПоДополнительномуОборудованиюНаСервере(ДополнительноеОборудование, КлючСвязи);
		КонецЕсли;
	КонецЕсли;	
	
	УстановитьДоступностьНормыРежимовРаботыДополнительногоОборудования(ИспользуютсяРазличныеРежимыРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияПриАктивизацииСтроки(Элемент)
	
	СтрокаДопОбор = Элемент.ТекущиеДанные;
	Если НЕ СтрокаДопОбор = Неопределено Тогда
		КлючСвязи = Элемент.ТекущиеДанные.КлючСвязи;
		Если ПустаяСтрока(КлючСвязи)Тогда
			КлючСвязи = Новый УникальныйИдентификатор;
		КонецЕсли;	
		Элементы.НормыРежимовРаботыДополнительногоОборудования.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", КлючСвязи);
		ДополнительноеОборудование = Элемент.ТекущиеДанные.ДополнительноеОборудование;
		ИспользуютсяРазличныеРежимыРаботы = Ложь;
		Если ЗначениеЗаполнено(ДополнительноеОборудование) Тогда
			ИспользуютсяРазличныеРежимыРаботы = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ДополнительноеОборудование, "ИспользуютсяРазличныеРежимыРаботы");
		КонецЕсли;
		УстановитьДоступностьНормыРежимовРаботыДополнительногоОборудования(ИспользуютсяРазличныеРежимыРаботы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НормыРежимовРаботыДополнительногоОборудованияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока Тогда
		СтрокаНормыРежимовРаботы = Элемент.ТекущиеДанные;
		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.КлючСвязи) Тогда
			СтрокаНормыРежимовРаботы.КлючСвязи = Элементы.НормыДополнительногоОборудования.ТекущиеДанные.КлючСвязи;
		КонецЕсли;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияПередУдалением(Элемент, Отказ)
	
	СтрокаДопОбор = Элемент.ТекущиеДанные;
	Если НЕ СтрокаДопОбор = Неопределено Тогда
		
		ТекущийОтбор = Новый Структура("КлючСвязи", СтрокаДопОбор.КлючСвязи);
		НайденныеСтроки = Объект.НормыРежимовРаботыДополнительногоОборудования.НайтиСтроки(ТекущийОтбор);
		
		Если НайденныеСтроки.Количество() Тогда
			Для каждого ЭлементУдаления Из НайденныеСтроки Цикл
				Объект.НормыРежимовРаботыДополнительногоОборудования.Удалить(ЭлементУдаления);
			КонецЦикла;			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НормыРежимовРаботыДополнительногоОборудованияРежимРаботыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаДопОбор = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаДопОбор.ДополнительноеОборудование) Тогда
		Если НЕ СтрокаДопОбор = Неопределено Тогда
			сткОтбор     = Новый Структура("Владелец", СтрокаДопОбор.ДополнительноеОборудование);
			сткОтбор.Вставить("ПометкаУдаления", Ложь);
			СписокЗначений = Новый СписокЗначений;
			ТекущийОтбор = Новый Структура("КлючСвязи", СтрокаДопОбор.КлючСвязи);
			НайденныеСтроки = Объект.НормыРежимовРаботыДополнительногоОборудования.НайтиСтроки(ТекущийОтбор);
			Если НайденныеСтроки.Количество() Тогда
				Для каждого ЭлементУдаления Из НайденныеСтроки Цикл
					СписокЗначений.Добавить(ЭлементУдаления.РежимРаботы);
				КонецЦикла;			
				Если СписокЗначений.Количество() Тогда
					сткОтбор.Вставить("РежимыРаботы", СписокЗначений);
				КонецЕсли;
			КонецЕсли;
			сткПараметры = Новый Структура("Отбор", сткОтбор);
			ОткрытьФорму("Справочник.упРежимыРаботыОборудования.ФормаВыбора", сткПараметры, Элементы.НормыРежимовРаботыДополнительногоОборудованияРежимРаботы);
		КонецЕсли;
	Иначе
		СтрокаКоллекции = Объект.НормыДополнительногоОборудования.НайтиПоИдентификатору(Элементы.НормыДополнительногоОборудования.ТекущаяСтрока);
		ИндексСтрокиКоллекции = Объект.НормыДополнительногоОборудования.Индекс(СтрокаКоллекции);
			ТекстОшибки = НСтр("ru='Не задано Дополнительное оборудование'");	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки, 
			Объект.Ссылка,
			"НормыДополнительногоОборудования[" + Формат(ИндексСтрокиКоллекции,"ЧГ=0") + "].ДополнительноеОборудование",
			"Объект");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НормыДополнительногоОборудованияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.НормыДополнительногоОборудования.ВыделенныеСтроки;
	Если НЕ ТекущиеДанные=Неопределено И ВыделенныеСтроки.Количество() > 1 Тогда
		ТекущаяСтрока = Элементы.НормыДополнительногоОборудования.ТекущаяСтрока;
		СтрокаИсточник = Объект.НормыДополнительногоОборудования.НайтиПоИдентификатору(ТекущаяСтрока);
		Имя = СтрЗаменить(Элементы.НормыДополнительногоОборудования.ТекущийЭлемент.Имя, "НормыДополнительногоОборудования", "");
		Для каждого ТекущийИдентификатор Из ВыделенныеСтроки Цикл
			Если ТекущийИдентификатор=ТекущаяСтрока Тогда
				Продолжить;
			Иначе
				СтрокаПриемник = Объект.НормыДополнительногоОборудования.НайтиПоИдентификатору(ТекущийИдентификатор);
				СтрокаПриемник[Имя] = СтрокаИсточник[Имя];
				ИспользуютсяРазличныеРежимыРаботы = ПолучитьРазличныеРежимыРаботы(СтрокаПриемник.ДополнительноеОборудование);
				Если НЕ ИспользуютсяРазличныеРежимыРаботы Тогда
					ЗаполнитьРасходВсего(СтрокаПриемник, Объект.ОбъектНормРасходаГСМ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура НормыТранспортныхСредствПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.НормыТранспортныхСредств.ВыделенныеСтроки;
	Если НЕ ТекущиеДанные=Неопределено И ВыделенныеСтроки.Количество() > 1 Тогда
		ТекущаяСтрока = Элементы.НормыТранспортныхСредств.ТекущаяСтрока;
		СтрокаИсточник = Объект.НормыТранспортныхСредств.НайтиПоИдентификатору(ТекущаяСтрока);
		Имя = СтрЗаменить(Элементы.НормыТранспортныхСредств.ТекущийЭлемент.Имя, "НормыТранспортныхСредств", "");
		Для каждого ТекущийИдентификатор Из ВыделенныеСтроки Цикл
			Если ТекущийИдентификатор=ТекущаяСтрока Тогда
				Продолжить;
			Иначе
				СтрокаПриемник = Объект.НормыТранспортныхСредств.НайтиПоИдентификатору(ТекущийИдентификатор);
				СтрокаПриемник[Имя] = СтрокаИсточник[Имя];
				ЗаполнитьРасходВсего(СтрокаПриемник, Объект.ОбъектНормРасходаГСМ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	сткПараметры = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе, МножественныйВыбор", Истина, Ложь, Истина);
	Если ЭтоУстановкаНормДляТранспортногоСредства(Объект.ОбъектНормРасходаГСМ) Тогда
		ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры, ЭтаФорма, УникальныйИдентификатор);
	Иначе
		сткОтбор = Новый Структура("ИспользуетсяУчетГСМ",Истина);
		сткПараметры.Вставить("Отбор", сткОтбор);
		ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаВыбора", сткПараметры, ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоНорме(Команда)
	
	ИмяТабличнойЧасти = ИмяТекущейТабличнойЧасти(Объект.ОбъектНормРасходаГСМ);
	
	Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("СпособЗаполнения", "ПоНорме");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПерезаполнитьНормыТабличнойЧасти", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Нормы расхода будут перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ПодобратьНормуРасходаГСМ();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоИстории(Команда)
	
	ИмяТабличнойЧасти = ИмяТекущейТабличнойЧасти(Объект.ОбъектНормРасходаГСМ);
	
	Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("СпособЗаполнения", "ПоИстории");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПерезаполнитьНормыТабличнойЧасти", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Нормы расхода будут перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе	
		ЗаполнитьНормыРасходаГСМ();
	КонецЕсли;
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
    УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
    УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    УправлениеПечатьюКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если Не ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ЗаполнитьРежимыРаботыПоДополнительномуОборудованию(Команда)
	
	СтрокаДопОбор = Элементы.НормыДополнительногоОборудования.ТекущиеДанные;
	Если СтрокаДопОбор = Неопределено Тогда
			ТекстОшибки = НСтр("ru='Укажате строку дополнительного оборудования'");	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки, 
			Объект.Ссылка,
			"НормыДополнительногоОборудования",
			"Объект");
		Возврат;	
	КонецЕсли;
	ДополнительноеОборудование = СтрокаДопОбор.ДополнительноеОборудование;
	КлючСвязи = СтрокаДопОбор.КлючСвязи;
	
	Если НЕ ЗначениеЗаполнено(СтрокаДопОбор.ДополнительноеОборудование) Тогда		
		СтрокаКоллекции = Объект.НормыДополнительногоОборудования.НайтиПоИдентификатору(Элементы.НормыДополнительногоОборудования.ТекущаяСтрока);
		ИндексСтрокиКоллекции = Объект.НормыДополнительногоОборудования.Индекс(СтрокаКоллекции);
			ТекстОшибки = НСтр("ru='Не задано Дополнительное оборудование'");	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки, 
			Объект.Ссылка,
			"НормыДополнительногоОборудования[" + Формат(ИндексСтрокиКоллекции,"ЧГ=0") + "].ДополнительноеОборудование",
			"Объект");
		Возврат;	
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ДополнительноеОборудование", ДополнительноеОборудование);
	ДополнительныеПараметры.Вставить("КлючСвязи", КлючСвязи);
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросЗаполнитьРежимыРаботыПоДополнительномуОборудованию", ЭтаФорма, ДополнительныеПараметры);
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Заполнить нормы режимов работы дополнительного оборудования?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
Функция ПолучитьРазличныеРежимыРаботы(ДополнительноеОборудование)
	Возврат упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ДополнительноеОборудование, "ИспользуютсяРазличныеРежимыРаботы");
КонецФункции // ПолучитьРазличныеРежимыРаботы()

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыПодбораНормГСМ(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(РезультатЗакрытия) = Тип("СправочникСсылка.упНормыРасходаГСМ") Тогда
		ЗаполнитьНормыРасходаГСМ(РезультатЗакрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПерезаполнитьНормыТабличнойЧасти(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Если ДополнительныеПараметры.СпособЗаполнения = "ПоНорме" Тогда
			ПодобратьНормуРасходаГСМ();
		Иначе	
			ЗаполнитьНормыРасходаГСМ();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
	
	Если ЭтоУстановкаНормДляТранспортногоСредства(Объект.ОбъектНормРасходаГСМ) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаНормыТранспортныхСредств;
		Элементы.ГруппаНормыДополнительногоОборудования.Видимость = Ложь;
		Элементы.ГруппаНормыТранспортныхСредств.Видимость = Истина;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаНормыДополнительногоОборудования;
		Элементы.ГруппаНормыДополнительногоОборудования.Видимость = Истина;
		Элементы.ГруппаНормыТранспортныхСредств.Видимость = Ложь;		
		Элементы.НормыРежимовРаботыДополнительногоОборудования.ТолькоПросмотр = Истина;
		Элементы.НормыРежимовРаботыДополнительногоОборудованияЗаполнитьРежимыРаботыПоДополнительномуОборудованию.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНормРасходаГСМПриИзменении(Элемент)

	Если Объект.НормыДополнительногоОборудования.Количество() > 0 Тогда
		ДополнительныеПараметры = Новый Структура("ИмяТЧ", "НормыДополнительногоОборудования");
		ДополнительныеПараметры.Вставить("ИмяТЧ_Доп", "НормыРежимовРаботыДополнительногоОборудования");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОчисткиТЧ", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Данные норм дополнительного оборудования будут очищены?'"), РежимДиалогаВопрос.ДаНет);
	ИначеЕсли Объект.НормыТранспортныхСредств.Количество() > 0 Тогда
		ДополнительныеПараметры = Новый Структура("ИмяТЧ", "НормыТранспортныхСредств");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОчисткиТЧ", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Данные норм транспортных средств будут очищены?'"), РежимДиалогаВопрос.ДаНет);
	Иначе	
		ЗаблокироватьЭлементы();	
	КонецЕсли;

КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ОбработатьОтветНаВопросОчисткиТЧ(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект[ДополнительныеПараметры.ИмяТЧ].Очистить();
		Если ДополнительныеПараметры.Свойство("ИмяТЧ_Доп") Тогда
			Объект[ДополнительныеПараметры.ИмяТЧ_Доп].Очистить();
		КонецЕсли;
	КонецЕсли;
	ЗаблокироватьЭлементы();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТекущиеДанныеПоУмолчанию(Строка)
	
	СпособыРасчетаНадбавокПроцент = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода");
	СпособыРасчетаЛитрыЗаЧас      = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗаЧас");
	СпособыРасчетаЛитрыЗаОперацию = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗаОперацию");
	ТипНормыРасходаГСМНа100Км     = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаСтоКилометров");
	ТипНормыРасходаГСМНа1час	     = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаОдинЧасРаботы");
	
	Если ЭтоУстановкаНормДляТранспортногоСредства(Объект.ОбъектНормРасходаГСМ) Тогда
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаГорнаяМестность) Тогда
			Строка.ТипРасходаГорнаяМестность 	= СпособыРасчетаНадбавокПроцент;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаСложностьДорог) Тогда
			Строка.ТипРасходаСложностьДорог 	= СпособыРасчетаНадбавокПроцент;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаНаселенныйПункт) Тогда
			Строка.ТипРасходаНаселенныйПункт 	= СпособыРасчетаНадбавокПроцент;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаГрузовой) Тогда
			Строка.ТипРасходаГрузовой 	= СпособыРасчетаНадбавокПроцент;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаКлиматКонтроль) Тогда
			Строка.ТипРасходаКлиматКонтроль 	= СпособыРасчетаНадбавокПроцент;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаСпециальноеОборудование) Тогда
			Строка.ТипРасходаСпециальноеОборудование 	= СпособыРасчетаЛитрыЗаЧас;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаПростой) Тогда
			Строка.ТипРасходаПростой 	= СпособыРасчетаЛитрыЗаЧас;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.ТипНормыРасходаГСМ) Тогда
			Строка.ТипНормыРасходаГСМ 	= ТипНормыРасходаГСМНа100Км;	
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(Строка.ТипНормыРасходаГСМ) Тогда
			Строка.ТипНормыРасходаГСМ 	= ТипНормыРасходаГСМНа1час;	
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.КлючСвязи) Тогда
			Строка.КлючСвязи = Новый УникальныйИдентификатор;
		КонецЕсли;		
		
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаЗимний) Тогда
		Строка.ТипРасходаЗимний 	= СпособыРасчетаНадбавокПроцент;	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаКондиционер) Тогда
		Строка.ТипРасходаКондиционер 	= СпособыРасчетаНадбавокПроцент;	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаОтопитель) Тогда
		Строка.ТипРасходаОтопитель 	= СпособыРасчетаЛитрыЗаЧас;	
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаДлительнаяЭксплуатация) Тогда
		Строка.ТипРасходаДлительнаяЭксплуатация = СпособыРасчетаНадбавокПроцент;	
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Строка.ТипРасходаРабочаяОперация) Тогда
		Строка.ТипРасходаРабочаяОперация = СпособыРасчетаЛитрыЗаОперацию;
	КонецЕсли;
	
	ЗаполнитьНадписиНормы(Строка);

	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНормыРасходаГСМ(НормаРасходаГСМ = Неопределено)
	ЗаполнитьНормыРасходаГСМНаСервере(НормаРасходаГСМ);
	ЗаполнитьДопСведенияТабличнойЧасти();			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНормыРасходаГСМНаСервере(НормаРасходаГСМ)
	мсвНомераВыделенныхСтрок = Новый Массив;
	ИмяТабличнойЧасти = ИмяТекущейТабличнойЧасти(Объект.ОбъектНормРасходаГСМ);
	Для каждого ИдентификаторСтроки Из Элементы[ИмяТабличнойЧасти].ВыделенныеСтроки Цикл
		Строка = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(ИдентификаторСтроки);
		мсвНомераВыделенныхСтрок.Добавить(Строка.НомерСтроки);
	КонецЦикла;
	УстановкаНормРасходаГСМ = РеквизитФормыВЗначение("Объект");	
	УстановкаНормРасходаГСМ.ЗаполнитьНормыРасходаГСМ(мсвНомераВыделенныхСтрок,НормаРасходаГСМ);
	ЗначениеВРеквизитФормы(УстановкаНормРасходаГСМ, "Объект");	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновныеВидыГСМПоТранспортномСредствамНаСервере(мсвТранспортныеСредства)
	Возврат упПутевойЛистУчетГСМ.ОсновныеВидыГСМПоТранспортномСредствам(мсвТранспортныеСредства)	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДопСведенияТабличнойЧасти()
	ИмяТабличнойЧасти = ИмяТекущейТабличнойЧасти(Объект.ОбъектНормРасходаГСМ);
	Для каждого Строка Из Объект[ИмяТабличнойЧасти] Цикл
		ЗаполнитьТекущиеДанныеПоУмолчанию(Строка);	
		ЗаполнитьРасходВсего(Строка, Объект.ОбъектНормРасходаГСМ);
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоУстановкаНормДляТранспортногоСредства(ОбъектНормРасходаГСМ)
	Возврат ОбъектНормРасходаГСМ = ПредопределенноеЗначение("Перечисление.упОбъектыНормРасходаГСМ.ТранспортноеСредство");	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяТекущейТабличнойЧасти(ОбъектНормРасходаГСМ)
	Возврат ?(ЭтоУстановкаНормДляТранспортногоСредства(ОбъектНормРасходаГСМ), "НормыТранспортныхСредств", "НормыДополнительногоОборудования");	
КонецФункции

&НаКлиенте
Процедура ПодобратьНормуРасходаГСМ()
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("МножественныйВыбор",Ложь);
	сткПараметры.Вставить("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.упНормыРасходаГСМ.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыПодбораНормГСМ", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНадписиНормы(Строка)
	
	Строка.НадписьРасход 		  = СтрШаблон("(%1)", Строка.ТипНормыРасходаГСМ);
	
	Если Строка.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаСтоКилометров") Тогда
		Строка.НадписьИзменениеРасхода = Нстр("ru = '(на 100 тонн км)'");
	Иначе
		Строка.НадписьИзменениеРасхода = "";	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРасходВсего(Строка, ОбъектНормРасходаГСМ)
	
	СпособыРасчетаНадбавокПроцент = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода");
	СпособыРасчетаЛитрыЗаЧас 	= ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.РасходВЛитрахЗаЧас");
	ТипНормыРасходаГСМНа100Км	= ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаСтоКилометров");
	ТипНормыРасходаГСМНа1час		= ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаОдинЧасРаботы");

	Расход = Строка.Расход;
	РасходСНадбавками = Расход;
	
	Если ЭтоУстановкаНормДляТранспортногоСредства(ОбъектНормРасходаГСМ) Тогда
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаГорнаяМестность, Строка.РасходГорнаяМестность); 
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаСложностьДорог, Строка.РасходСложностьДорог); 
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаНаселенныйПункт, Строка.РасходНаселенныйПункт); 
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаГрузовой, Строка.РасходГрузовой); 
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаКлиматКонтроль, Строка.РасходКлиматКонтроль); 
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаСпециальноеОборудование, Строка.РасходСпециальноеОборудование); 
		ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаПростой, Строка.РасходПростой); 
	КонецЕсли;
	
	ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаЗимний, Строка.РасходЗимний); 
	ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаКондиционер, Строка.РасходКондиционер); 
	ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаОтопитель, Строка.РасходОтопитель); 
	ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаДлительнаяЭксплуатация, Строка.РасходДлительнаяЭксплуатация); 
	ДобавитьНадбавкуКРасходу(РасходСНадбавками, Расход, Строка.ТипРасходаРабочаяОперация, Строка.РасходРабочаяОперация); 
	
	Строка.РасходСНадбавками	= РасходСНадбавками;
	Строка.НадписьРасходСНадбавками = СтрШаблон("(%1)", Строка.ТипНормыРасходаГСМ);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьНадбавкуКРасходу(РасходИтог, РасходБазовый, ТипНадбавкиРасход, НадбавкаРасход)
	
	Если Истина
		И ТипНадбавкиРасход = ПредопределенноеЗначение("Перечисление.упСпособыРасчетаНадбавокГСМ.ПроцентОтОбщегоРасхода")
		И ЗначениеЗаполнено(НадбавкаРасход) 
	Тогда
		РасходИтог = РасходИтог + РасходБазовый  / 100 * НадбавкаРасход;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьНормыРежимовРаботыДополнительногоОборудования(ИспользуютсяРазличныеРежимыРаботы)
	
	Элементы.НормыРежимовРаботыДополнительногоОборудования.ТолькоПросмотр = НЕ ИспользуютсяРазличныеРежимыРаботы;
	Элементы.НормыРежимовРаботыДополнительногоОборудованияЗаполнитьРежимыРаботыПоДополнительномуОборудованию.Доступность = ИспользуютсяРазличныеРежимыРаботы;
	
	Элементы.НормыДополнительногоОборудованияРасход.ТолькоПросмотр = ИспользуютсяРазличныеРежимыРаботы;
	Элементы.НормыДополнительногоОборудованияИзменениеРасхода.ТолькоПросмотр = ИспользуютсяРазличныеРежимыРаботы;
	
КонецПроцедуры // УстановитьДоступностьНормыРежимовРаботыДополнительногоОборудования()

&НаСервере
// Проверим доп оборудывание.
//
// Параметры:
//  ДополнительноеОборудование  - СправочникСсылка.упДополнительноеОборудование - Входящее значене
//  КлючСвязи  - Строка - Идентификатор.
//
// Возвращаемое значение:
//   Булево   - Доп оборудывание изменено.
//
Функция ПроверитьНормыРежимовРаботыДополнительногоОборудования(ДополнительноеОборудование, КлючСвязи)
	
	ИзменилиДополнительноеОборудование = ?(Объект.НормыРежимовРаботыДополнительногоОборудования.Количество() = 0, Истина, Ложь);
	Если ЗначениеЗаполнено(ДополнительноеОборудование) Тогда
		ТекущийОтбор = Новый Структура("КлючСвязи", КлючСвязи);
		НайденныеСтроки = Объект.НормыРежимовРаботыДополнительногоОборудования.НайтиСтроки(ТекущийОтбор);
		Для каждого ЭлементУдаления Из НайденныеСтроки Цикл
			Владелец = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ЭлементУдаления.РежимРаботы, "Владелец");
			Если НЕ Владелец = ДополнительноеОборудование Тогда
				ИзменилиДополнительноеОборудование = Истина;
				Объект.НормыРежимовРаботыДополнительногоОборудования.Удалить(ЭлементУдаления);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ИзменилиДополнительноеОборудование;
	
КонецФункции // ПроверитьНормыРежимовРаботыДополнительногоОборудования()

&НаСервере
Процедура ЗаполнитьРежимыРаботыПоДополнительномуОборудованиюНаСервере(ДополнительноеОборудование, КлючСвязи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	упРежимыРаботыОборудования.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.упРежимыРаботыОборудования КАК упРежимыРаботыОборудования
		|ГДЕ
		|	упРежимыРаботыОборудования.Владелец = &Владелец
		|	И НЕ упРежимыРаботыОборудования.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Владелец", ДополнительноеОборудование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекущийОтбор = Новый Структура("РежимРаботы",  Выборка.Ссылка);
		НайденныеСтроки = Объект.НормыРежимовРаботыДополнительногоОборудования.НайтиСтроки(ТекущийОтбор);		
		Если НайденныеСтроки.Количество() Тогда
			Продолжить;			
		КонецЕсли;
		
		НоваяСтрока = Объект.НормыРежимовРаботыДополнительногоОборудования.Добавить();
		НоваяСтрока.КлючСвязи = КлючСвязи;
		НоваяСтрока.РежимРаботы = Выборка.Ссылка;
		НоваяСтрока.Расход = 0;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросЗаполнитьРежимыРаботыПоДополнительномуОборудованию(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьРежимыРаботыПоДополнительномуОборудованиюНаСервере(ДополнительныеПараметры.ДополнительноеОборудование, ДополнительныеПараметры.КлючСвязи);
	КонецЕсли;
	
КонецПроцедуры // ОбработатьОтветНаВопросЗаполнитьРежимыРаботыПоДополнительномуОборудованию()

&НаКлиенте
Процедура НормыТранспортныхСредствТранспортноеСредствоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.НормыТранспортныхСредств.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ТранспортноеСредство) Тогда
		ТекущиеДанные.ВидГСМ = ОсновнойВидГСМПоТранспортномуСредству(ТекущиеДанные.ТранспортноеСредство);	
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновнойВидГСМПоТранспортномуСредству(ТранспортноеСредство)
	Возврат упПутевойЛистУчетГСМ.ОсновнойВидГСМПоТранспортномуСредству(ТранспортноеСредство);	
КонецФункции


#КонецОбласти