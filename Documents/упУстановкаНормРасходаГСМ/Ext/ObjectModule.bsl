#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Документы.упУстановкаНормРасходаГСМ.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	Если ДополнительныеСвойства.Свойство("НормыРежимовРаботы") Тогда
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упНормыРасходаРежимовРаботы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.НормыРежимовРаботы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ОбъектНормРасхода", "ОбъектНормРасхода");
		упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;  
	КонецЕсли;
		
	РеквизитыОбъекта = Новый Структура(); 
	РеквизитыОбъекта.Вставить("ДатаНачалаДействия");
	РеквизитыОбъекта.Вставить("Организация");
	РеквизитыОбъекта.Вставить("Подразделение");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);
	РеквизитыОбъекта.Вставить("НормыТранспортныхСредств", 			НормыТранспортныхСредств.Выгрузить());
	РеквизитыОбъекта.Вставить("НормыДополнительногоОборудования", 	НормыДополнительногоОборудования.Выгрузить());
	
	упПутевойЛистУчетГСМ.СформироватьДвижениеНормыРасходаГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упПутевойЛистУчетГСМ.СформироватьДвижениеНормыРасходаРежимовРаботы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ОбъектНормРасходаГСМ = Перечисления.упОбъектыНормРасходаГСМ.ТранспортноеСредство Тогда
		ИмяРеквизита = "НормыДополнительногоОборудования";
	ИначеЕсли ОбъектНормРасходаГСМ = Перечисления.упОбъектыНормРасходаГСМ.ДополнительноеОборудование Тогда
		ИмяРеквизита = "НормыТранспортныхСредств";			
	КонецЕсли;
	текИндекс = ПроверяемыеРеквизиты.Найти(ИмяРеквизита);
	Если НЕ текИндекс = Неопределено Тогда
		ПроверяемыеРеквизиты.Удалить(текИндекс);		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыФункции

Процедура ЗаполнитьНормыРасходаГСМ(мсвНомераСтрокТабличнойЧасти, НормаРасходаГСМ = Неопределено) Экспорт
	
	Если мсвНомераСтрокТабличнойЧасти.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЭтоОбъектРасчетаНормыТС = ОбъектНормРасходаГСМ = Перечисления.упОбъектыНормРасходаГСМ.ТранспортноеСредство;
	Если ЭтоОбъектРасчетаНормыТС Тогда
		ТабличнаяЧасть = НормыТранспортныхСредств;
	Иначе	
		ТабличнаяЧасть = НормыДополнительногоОборудования;
	КонецЕсли;	
	
	// Если не задана норма расхода.
	Если НормаРасходаГСМ = Неопределено Тогда
		
		// Строки табличной части "Нормы" заполняются по РС.упНормыРасходаГСМ.
		
		Запрос.Текст = "ВЫБРАТЬ
		|	втТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
		|	втТабличнаяЧасть.Организация КАК Организация,
		|	втТабличнаяЧасть.Подразделение КАК Подразделение,
		|	втТабличнаяЧасть.ВидГСМ КАК ВидГСМ,
		|	втТабличнаяЧасть.ОбъектНормРасхода КАК ОбъектНормРасхода
		|ПОМЕСТИТЬ втНормы
		|ИЗ
		|	&ТабличнаяЧасть КАК втТабличнаяЧасть
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбНормыРасхода.*,
		|	втНормы.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	втНормы КАК втНормы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упНормыРасходаГСМ.СрезПоследних(
		|				,
		|				(Организация, Подразделение, ОбъектНормРасхода, ВидГСМ) В
		|					(ВЫБРАТЬ
		|						втНормы.Организация КАК Организация,
		|						втНормы.Подразделение КАК Подразделение,
		|						втНормы.ОбъектНормРасхода КАК ОбъектНормРасхода,
		|						втНормы.ВидГСМ КАК ВидГСМ
		|					ИЗ
		|						втНормы КАК втНормы)) КАК тбНормыРасхода
		|		ПО втНормы.Организация = тбНормыРасхода.Организация
		|			И втНормы.Подразделение = тбНормыРасхода.Подразделение
		|			И втНормы.ВидГСМ = тбНормыРасхода.ВидГСМ
		|			И втНормы.ОбъектНормРасхода = тбНормыРасхода.ОбъектНормРасхода";
		
		
		ИмяПоляОбъектНормРасхода = "";
				
		Если ЭтоОбъектРасчетаНормыТС Тогда
			тбзНормы = ТабличнаяЧасть.ВыгрузитьКолонки("НомерСтроки, ТранспортноеСредство, ВидГСМ");
			тбзНормы.Колонки.Добавить("ОбъектНормРасхода", 	Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства")); 	
			ИмяПоляОбъектНормРасхода = "ТранспортноеСредство";
		Иначе	
			тбзНормы = ТабличнаяЧасть.ВыгрузитьКолонки("НомерСтроки, ДополнительноеОборудование, ВидГСМ");
			тбзНормы.Колонки.Добавить("ОбъектНормРасхода", 	Новый ОписаниеТипов("СправочникСсылка.упДополнительноеОборудование"));
			ИмяПоляОбъектНормРасхода = "ДополнительноеОборудование";
		КонецЕсли;	
		
		тбзНормы.Колонки.Добавить("Организация",	Новый ОписаниеТипов("СправочникСсылка.Организации")); 
		тбзНормы.Колонки.Добавить("Подразделение", 	Новый ОписаниеТипов("СправочникСсылка.упСтруктураПредприятия")); 		
		
		Для каждого ЭлементНомерСтроки Из мсвНомераСтрокТабличнойЧасти Цикл
			Строка = ТабличнаяЧасть.Получить(ЭлементНомерСтроки - 1);
			НоваяСтрока = тбзНормы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ОбъектНормРасхода = Строка[ИмяПоляОбъектНормРасхода];	
			НоваяСтрока.Организация		= Организация;
			НоваяСтрока.Подразделение	= Подразделение;
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ТабличнаяЧасть", тбзНормы);
		
		Выборка	= Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Строка = ТабличнаяЧасть.Получить(Выборка.НомерСтроки - 1);
			ЗаполнитьЗначенияСвойств(Строка, Выборка);
			Если Строка.ТипНормыРасходаГСМ = Перечисления.упТипыНормРасходаГСМ.НаОдинЧасРаботы Тогда
					Строка.ИзменениеРасхода = 0;
			КонецЕсли;
		КонецЦикла; 
		
	Иначе	
		
		// Строки табличной части "Нормы" заполняются данными заданной нормы.
		
		Запрос.Текст = "ВЫБРАТЬ
		|	НормыРасходаГСМ.*
		|ИЗ
		|	Справочник.упНормыРасходаГСМ КАК НормыРасходаГСМ
		|ГДЕ
		|	НормыРасходаГСМ.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", НормаРасходаГСМ);
		
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			Для каждого ЭлементНомерСтроки Из мсвНомераСтрокТабличнойЧасти Цикл
				Строка = ТабличнаяЧасть.Получить(ЭлементНомерСтроки - 1);
				ЗаполнитьЗначенияСвойств(Строка, Выборка);
				Если Строка.ТипНормыРасходаГСМ = Перечисления.упТипыНормРасходаГСМ.НаОдинЧасРаботы Тогда
					Строка.ИзменениеРасхода = 0;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли