#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс		
	
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
 // Товарно-транспортная накладная
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упУстановкаНормРасходаГСМ";
  КомандаПечати.Идентификатор = "УстановкаНормРасходаГСМ";
  КомандаПечати.Представление = НСтр("ru = 'Установка норм расхода ГСМ'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УстановкаНормРасходаГСМ") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"УстановкаНормРасходаГСМ",
		НСтр("ru = 'Установка норм расхода ГСМ'"),
		ПечатьУстановкаНормГСМ(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упУстановкаНормРасходаГСМ.ПФ_MXL_УстановкаНормРасходаГСМ");
	КонецЕсли;
	
КонецПроцедуры

// Формирование печатной формы УстановкаНормГСМ
Функция ПечатьУстановкаНормГСМ(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева			= 0;
	ТабличныйДокумент.ПолеСнизу			= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_УстановкаНормГСМ";
	
	ЗаполнитьТабличныйДокументУстановкаНормГСМ(ТабличныйДокумент, ОбъектыПечати, МассивОбъектов);
	
	Возврат ТабличныйДокумент;
КонецФункции

// Процедура - Заполнить табличный документ УстановкаНормГСМ
//
// Параметры:
//  ТабличныйДокумент	 - 	 - 
//  ДанныеДляПечати		 - 	 - 
//  ОбъектыПечати		 - 	 - 
//
Процедура ЗаполнитьТабличныйДокументУстановкаНормГСМ(ТабличныйДокумент, ОбъектыПечати, МассивОбъектов) Экспорт
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упУстановкаНормРасходаГСМ.ПФ_MXL_УстановкаНормРасходаГСМ");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упУстановкаНормРасходаГСМ.Автор,
	|	упУстановкаНормРасходаГСМ.Дата,
	|	упУстановкаНормРасходаГСМ.ДатаНачалаДействия,
	|	упУстановкаНормРасходаГСМ.Номер,
	|	упУстановкаНормРасходаГСМ.Организация,
	|	упУстановкаНормРасходаГСМ.НормыТранспортныхСредств.*,
	|	упУстановкаНормРасходаГСМ.НормыДополнительногоОборудования.*,
	|	упУстановкаНормРасходаГСМ.НормыРежимовРаботыДополнительногоОборудования.*
	|ИЗ
	|	Документ.упУстановкаНормРасходаГСМ КАК упУстановкаНормРасходаГСМ
	|ГДЕ
	|	упУстановкаНормРасходаГСМ.Ссылка В (&МассивОбъектов)";
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();

	Шапка 	= Макет.ПолучитьОбласть("Шапка");
	Подвал	= Макет.ПолучитьОбласть("Подвал");
	ОбластьНормыТранспортныхСредствШапка = Макет.ПолучитьОбласть("НормыТранспортныхСредствШапка");
	ОбластьНормыТранспортныхСредств = Макет.ПолучитьОбласть("НормыТранспортныхСредств");
	ОбластьНормыДополнительногоОборудованияШапка = Макет.ПолучитьОбласть("НормыДополнительногоОборудованияШапка");
	ОбластьНормыДополнительногоОборудования = Макет.ПолучитьОбласть("НормыДополнительногоОборудования");
	ТабличныйДокумент.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Шапка.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(Шапка, Выборка.Уровень());

		// Транспортные средства
		ВыборкаНормыТранспортныхСредств = Выборка.НормыТранспортныхСредств.Выбрать();
		Если ВыборкаНормыТранспортныхСредств.Количество() > 0 Тогда
			
			ТабличныйДокумент.Вывести(ОбластьНормыТранспортныхСредствШапка);
			РеквизитыТабличнойЧасти = Метаданные.Документы.упУстановкаНормРасходаГСМ.ТабличныеЧасти.НормыТранспортныхСредств.Реквизиты;
			мсвИменаНадбавок	= Новый Массив;
			Для каждого РеквизитТабличнойЧасти Из РеквизитыТабличнойЧасти Цикл
				Если Лев(РеквизитТабличнойЧасти.Имя, 10) = "ТипРасхода" Тогда
					мсвИменаНадбавок.Добавить(Прав(РеквизитТабличнойЧасти.Имя, СтрДлина(РеквизитТабличнойЧасти.Имя)-10));
				КонецЕсли;
			КонецЦикла;
				
			Пока ВыборкаНормыТранспортныхСредств.Следующий() Цикл
				ОбластьНормыТранспортныхСредств.Параметры.Заполнить(ВыборкаНормыТранспортныхСредств);
				Для каждого ИмяРеквизита Из мсвИменаНадбавок Цикл
					ИмяПараметра 	= "СтрокаРасход" + ИмяРеквизита;
					ИмяРасхода	= "Расход" + ИмяРеквизита;
					ИмяТипаРасхода	= "ТипРасхода" + ИмяРеквизита;
					Если ВыборкаНормыТранспортныхСредств[ИмяРасхода] <> 0 Тогда
						ОбластьНормыТранспортныхСредств.Параметры[ИмяПараметра] = СтрШаблон("%1 %2", ВыборкаНормыТранспортныхСредств[ИмяРасхода], ВыборкаНормыТранспортныхСредств[ИмяТипаРасхода]);
					Иначе
						ОбластьНормыТранспортныхСредств.Параметры[ИмяПараметра] = "";
					КонецЕсли;

				КонецЦикла;
				ТабличныйДокумент.Вывести(ОбластьНормыТранспортныхСредств, ВыборкаНормыТранспортныхСредств.Уровень());
			КонецЦикла;
		КонецЕсли;
		
		// Дополнительное оборудование
		ВыборкаНормыДополнительногоОборудования = Выборка.НормыДополнительногоОборудования.Выбрать();
		ВыборкаРежимыРаботы = Выборка.НормыРежимовРаботыДополнительногоОборудования.Выбрать();
		Если ВыборкаНормыДополнительногоОборудования.Количество() > 0 Тогда
			
			ТабличныйДокумент.Вывести(ОбластьНормыДополнительногоОборудованияШапка);
			РеквизитыТабличнойЧасти = Метаданные.Документы.упУстановкаНормРасходаГСМ.ТабличныеЧасти.НормыДополнительногоОборудования.Реквизиты;
			мсвИменаНадбавок	= Новый Массив;
			Для каждого РеквизитТабличнойЧасти Из РеквизитыТабличнойЧасти Цикл
				Если Лев(РеквизитТабличнойЧасти.Имя, 10) = "ТипРасхода" Тогда
					мсвИменаНадбавок.Добавить(Прав(РеквизитТабличнойЧасти.Имя, СтрДлина(РеквизитТабличнойЧасти.Имя)-10));
				КонецЕсли;
			КонецЦикла;
				
			Пока ВыборкаНормыДополнительногоОборудования.Следующий() Цикл
				ОбластьНормыДополнительногоОборудования.Параметры.Заполнить(ВыборкаНормыДополнительногоОборудования);				
				ДополнительноеОборудование = ВыборкаНормыДополнительногоОборудования.ДополнительноеОборудование;
				ИспользуютсяРазличныеРежимыРаботы = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ДополнительноеОборудование, "ИспользуютсяРазличныеРежимыРаботы");
				РежимыРаботы = "";
				Если ИспользуютсяРазличныеРежимыРаботы Тогда
					ВыборкаРежимыРаботы.Сбросить();
					КлючСвязи = ВыборкаНормыДополнительногоОборудования.КлючСвязи;
					ТекущийОтбор = Новый Структура();
					ТекущийОтбор.Вставить("КлючСвязи", КлючСвязи);
					Пока ВыборкаРежимыРаботы.НайтиСледующий(ТекущийОтбор) Цикл
						РежимыРаботы = РежимыРаботы + ?(ПустаяСтрока(РежимыРаботы),"", Символы.ПС) + СтрШаблон("режим: %1 - расход: %2",Строка(ВыборкаРежимыРаботы.РежимРаботы), Формат(ВыборкаРежимыРаботы.Расход, "ЧГ=0"));
					КонецЦикла;
					Если НЕ ПустаяСтрока(РежимыРаботы) Тогда
						РежимыРаботы = СтрШаблон(Символы.ПС + "Режимы работы(%1)",РежимыРаботы);
					КонецЕсли;
					КонецЕсли;
				ОбластьНормыДополнительногоОборудования.Параметры.РежимыРаботы = РежимыРаботы;
				Для каждого ИмяРеквизита Из мсвИменаНадбавок Цикл
					ИмяПараметра 	= "СтрокаРасход" + ИмяРеквизита;
					ИмяРасхода	= "Расход" + ИмяРеквизита;
					ИмяТипаРасхода	= "ТипРасхода" + ИмяРеквизита;
					Если ВыборкаНормыДополнительногоОборудования[ИмяРасхода] <> 0 Тогда
						ОбластьНормыДополнительногоОборудования.Параметры[ИмяПараметра] = СтрШаблон("%1 %2", ВыборкаНормыДополнительногоОборудования[ИмяРасхода], ВыборкаНормыДополнительногоОборудования[ИмяТипаРасхода]);
					Иначе
						ОбластьНормыДополнительногоОборудования.Параметры[ИмяПараметра] = "";		
					КонецЕсли;

				КонецЦикла;
				ТабличныйДокумент.Вывести(ОбластьНормыДополнительногоОборудования, ВыборкаНормыДополнительногоОборудования.Уровень());
			КонецЦикла;
		КонецЕсли;

		Подвал.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(Подвал);

		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
				
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение
// Заполнить дополнительные свойства документа.
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Оборудование.Ссылка КАК Ссылка,
	               |	Оборудование.ДополнительноеОборудование КАК ОбъектНормРасхода,
	               |	РежимыРаботы.РежимРаботы КАК РежимРаботыОборудования,
	               |	СУММА(РежимыРаботы.Расход) КАК Расход,
	               |	Оборудование.ВидГСМ КАК ВидГСМ
	               |ИЗ
	               |	Документ.упУстановкаНормРасходаГСМ.НормыРежимовРаботыДополнительногоОборудования КАК РежимыРаботы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упУстановкаНормРасходаГСМ.НормыДополнительногоОборудования КАК Оборудование
	               |		ПО РежимыРаботы.Ссылка = Оборудование.Ссылка
	               |			И РежимыРаботы.КлючСвязи = Оборудование.КлючСвязи
	               |ГДЕ
	               |	РежимыРаботы.Ссылка = &Ссылка
	               |	И РежимыРаботы.Расход > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Оборудование.Ссылка,
	               |	Оборудование.ДополнительноеОборудование,
	               |	РежимыРаботы.РежимРаботы,
	               |	Оборудование.ВидГСМ";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ДополнительныеСвойства.Вставить("НормыРежимовРаботы", Результат.Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли	
