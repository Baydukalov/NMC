#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
		
	Статус = Неопределено;
			
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		Статус      = ДополнительныеСвойства.Статус;
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	
	Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
						
		Если НЕ Отказ Тогда
			// Предварительная установка статуса										
			ДополнительныеСвойства.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
			ДополнительныеСвойства.Вставить("ДоступнаРольСогласованиеЗаявкиНаРемонтИТО", РольДоступна("упСогласованиеЗаявкиНаРемонтИТО"));
			ДополнительныеСвойства.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению", ВидРемонта.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению);
			ДополнительныеСвойства.Вставить("ТребуетсяСогласованиеЗаявкиНаРемонт", ВидРемонта.ТребуетсяСогласованиеЗаявкиНаРемонт);
			ДополнительныеСвойства.Вставить("Ссылка", Ссылка);
			
			Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ПустаяСсылка();	
				ДополнительныеСвойства.Вставить("Статус", Статус);
			КонецЕсли;
			
			упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
			
			Если НЕ Отказ И ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.ПустаяСсылка() Тогда
				упРаботаСоСтатусами.ЕстьДругиеРегистраторыИзменявшиеСтатус(Ссылка, Отказ);
				Если НЕ Отказ Тогда
					упРемонтИТОТС.ЕстьРемонтПоЗаявке(Ссылка, Отказ);	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена Тогда
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
			ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
			упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ЭтоРемонтТС = Ложь;
	Если ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство
		ИЛИ НЕ ЗначениеЗаполнено(ОбъектРемонта) Тогда
		ЭтоРемонтТС = Истина;
	КонецЕсли;
	
	Документы.упЗаявкаНаРемонтИТОТС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ЭтоРемонтТС Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Транспорт", ТранспортноеСредство);	
		ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
		
		Если ДополнительныеСвойства.Свойство("ПлановыеДатыТО") Тогда 
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеДатыТО");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ПлановыеДатыТО;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТранспортноеСредство", "ТранспортноеСредство");
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаРемонтИТОТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановоеРасписаниеРаботыГаража.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Ссылка);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура("ВидРемонта, 
										|Дата,
										|ТранспортноеСредство,
										|СуммаРасходыПлан, 
										|СуммаРасходыПланНДС, 										
										|СпособРемонта, 
										|Гараж, 
										|МестоРемонта, 
										|НачалоРемонтаПлан, 
										|ОкончаниеРемонтаПлан, 
										|ПериодРаспределенияЗатратНачало,
										|ПериодРаспределенияЗатратОкончание");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект); 
	РеквизитыОбъекта.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));	
	РеквизитыОбъекта.Вставить("Работы", Работы);
	РеквизитыОбъекта.Вставить("ОбъектРемонта", ОбъектРемонта);
	РеквизитыОбъекта.Вставить("ЭтоРемонтТС", ЭтоРемонтТС);
	РеквизитыОбъекта.Вставить("Агрегат", Агрегат);
	РеквизитыОбъекта.Вставить("ДополнительноеОборудованиеРемонта", ДополнительноеОборудованиеРемонта);
	РеквизитыОбъекта.Вставить("СпособРемонта", СпособРемонта);
	Период = ОпределитьПериодФормированияДвижений(ДополнительныеСвойства.Статус, ЧасовойПояс);
	РеквизитыОбъекта.Вставить("Дата", Период);
	
	РеквизитыОбъекта.Вставить("ДатаСтатуса", ТекущаяДатаСеанса());
	
	НеКонтролироватьРасписаниеРаботыГаража = Ложь;
	Если ЗначениеЗаполнено(Гараж) Тогда
		НеКонтролироватьРасписаниеРаботыГаража = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Гараж, "НеКонтролироватьРасписаниеРаботыГаража");		
	КонецЕсли;
	ДополнительныеСвойства.Вставить("НеКонтролироватьРасписаниеРаботыГаража",    НеКонтролироватьРасписаниеРаботыГаража);
	ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыГаража"   , Ложь);
	ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС"       , Ложь);
	ДополнительныеСвойства.Вставить("КонтрольПлановыеДатыТО", Ложь);
	ДополнительныеСвойства.Вставить("СпособРемонта", СпособРемонта);
	
	ДополнительныеСвойства.Вставить("РассчитатьСтатус", Ложь); 
	
	упРемонтИТОТС.ПровестиПроверкиПриПереводеЗаявкиНаРемонтВСтатусКВыполнению(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
	
	//упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
	Если ЭтоРемонтТС Тогда		
		// Формируются движения по занятости транспорта
		упРемонтИТОТС.СформироватьДвиженияЗанятостьТранспортаЗаявкаНаРемонт(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формируются движения по Плановые даты ТО
		упРемонтИТОТС.СформироватьДвиженияЗаявкиНаРемонтПлановыеДатыТО(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;

	// Формируются движения по расписанию работы гаража
	упРемонтИТОТС.СформироватьДвиженияПлановоеРасписаниеРаботыГаражаЗаявкаНаРемонт(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	// Распределение затрат
	упРемонтИТОТС.СформироватьДвиженияРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется и устанавливается статус документа
	упРемонтИТОТС.СформироватьДвиженияСтатусЗаявкаНаРемонтИТОТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();

	// Выполняется контроль результатов проведения
	Если ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыГаража Тогда
		РегистрыСведений.упПлановоеРасписаниеРаботыГаража.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	КонецЕсли;
	Если ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыТС Тогда
		РегистрыСведений.упЗанятостьТранспорта.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, ЭтотОбъект.ТранспортноеСредство, Отказ);
	КонецЕсли;
	Если ДополнительныеСвойства.КонтрольПлановыеДатыТО Тогда
		РегистрыСведений.упПлановыеДатыТО.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ИндексАгрегат = Неопределено;
	ИндексДополнительноеОборудованиеРемонта = Неопределено;
	ИндексТранспортноеСредство = Неопределено;
	Если НЕ ЗначениеЗаполнено(ОбъектРемонта) 
		ИЛИ ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство Тогда 
		ИндексАгрегат = ПроверяемыеРеквизиты.Найти("Агрегат");		
		Если НЕ ИндексАгрегат = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексАгрегат);
		КонецЕсли;
		ИндексДополнительноеОборудованиеРемонта = ПроверяемыеРеквизиты.Найти("ДополнительноеОборудованиеРемонта");		
		Если НЕ ИндексДополнительноеОборудованиеРемонта = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексДополнительноеОборудованиеРемонта);
		КонецЕсли;
	ИначеЕсли ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда 
		ИндексАгрегат = ПроверяемыеРеквизиты.Найти("Агрегат");
		Если НЕ ИндексАгрегат = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексАгрегат);
		КонецЕсли;
		ИндексТранспортноеСредство = ПроверяемыеРеквизиты.Найти("ТранспортноеСредство");		
		Если НЕ ИндексТранспортноеСредство = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексТранспортноеСредство);
		КонецЕсли;
	ИначеЕсли ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда 
		ИндексДополнительноеОборудованиеРемонта = ПроверяемыеРеквизиты.Найти("ДополнительноеОборудованиеРемонта");
		Если НЕ ИндексДополнительноеОборудованиеРемонта = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексДополнительноеОборудованиеРемонта);
		КонецЕсли;
		ИндексТранспортноеСредство = ПроверяемыеРеквизиты.Найти("ТранспортноеСредство");
		Если НЕ ИндексТранспортноеСредство = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексТранспортноеСредство);
		КонецЕсли;
	КонецЕсли;
	
	Если СпособРемонта = Перечисления.упСпособыРемонтов.СторонняяОрганизация Тогда 
		Если Не ЗначениеЗаполнено(СервиснаяОрганизация) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Сервисная организация"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.СервиснаяОрганизация",, Отказ);
		КонецЕсли; 
		Если Не ЗначениеЗаполнено(АдресРемонта) И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Адрес ремонта"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.АдресРемонта",, Отказ);
		КонецЕсли; 
	ИначеЕсли СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами Тогда

		Если Не ЗначениеЗаполнено(Гараж) Тогда
		    ТекстОшибки = НСтр("ru = 'Поле ""Гараж"" не заполнено'");
		    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.Гараж",, Отказ);
		КонецЕсли; 
		
	КонецЕсли;
	
	Если НачалоРемонтаПлан > ОкончаниеРемонтаПлан Тогда
		ТекстОшибки = НСтр("ru = 'Плановое время начала работ не может быть больше планового времени окончания работ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НачалоРемонтаПлан",, Отказ);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ПериодРаспределенияЗатратНачало) И ЗначениеЗаполнено(ПериодРаспределенияЗатратОкончание) И ПериодРаспределенияЗатратНачало > ПериодРаспределенияЗатратОкончание Тогда
		ТекстОшибки = НСтр("ru = 'Начало периода распределения затрат не может быть больше окончания периода распределения затрат'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ПериодРаспределенияЗатратНачало",, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ТаблицаЗначений.Колонки.Добавить("ДополнительноеОборудование",,"ДополнительноеОборудование");
		ТаблицаЗначений.Колонки.Добавить("ТипРемонта",,"ТипРемонта");
		ТаблицаЗначений.Колонки.Добавить("Количество",,"Количество");
		Для каждого СтрокаДополнительноеОборудование Из РемонтируемоеДополнительноеОборудование Цикл
			ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаДополнительноеОборудование.ВидРемонта, "ТипРемонта");
			Если Перечисления.упТипыРемонтов.ПлановоеТО = ТипРемонта Тогда
				НоваяСтрока = ТаблицаЗначений.Добавить();
				НоваяСтрока.ДополнительноеОборудование = СтрокаДополнительноеОборудование.ДополнительноеОборудование;				
				НоваяСтрока.ТипРемонта = ТипРемонта;
				НоваяСтрока.Количество = 1;
			КонецЕсли;
		КонецЦикла;
		Если ТаблицаЗначений.Количество() Тогда
			ТаблицаЗначений.Свернуть("ДополнительноеОборудование, ТипРемонта", "Количество");
			ТаблицаЗначений.Сортировать("Количество УБЫВ");
			Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
				Если СтрокаТаблицы.Количество >1 Тогда
					ТекстОшибки = НСтр("ru = 'Выбраны виды ремонта с типом реомнта Плановое ТО больше одного это излишне.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.РемонтируемоеДополнительноеОборудование",, Отказ);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

// Функция определяет дату формирования движений
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений(Статус, ЧасовойПоясДокумента)
	
	Период = упСервисныеФункции.ПриведеннаяКЧасовомуПоясуДокументаДатаСеанса(ЧасовойПоясДокумента);
	
	Если Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена Тогда
		Возврат Период;
	КонецЕсли; 
	
	ПереданоКВыполнению = Документы.упЗаявкаНаРемонтИТОТС.ПолучитьДатуПереводаВСтатус(Ссылка, Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению);
	Если ЗначениеЗаполнено(ПереданоКВыполнению) Тогда
		Период = Мин(ПереданоКВыполнению, Период);
	КонецЕсли; 
	
	Возврат Период;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
