

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа
//
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаявкаНаРемонтИТОТС - документ, которого необходимо получить.
//
// Возвращаемое значение:
//   ПеречислениеСсылка   - Ссылка на перечисление.
//
Функция ПолучитьСтатус(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыЗаявокНаРемонтИТОТС.Статус,
	|	ЗаявкаНаРемонтИТОТС.ПометкаУдаления
	|ИЗ
	|	Документ.упЗаявкаНаРемонтИТОТС КАК ЗаявкаНаРемонтИТОТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаРемонтИТОТС.СрезПоследних(, Документ = &Документ) КАК упСтатусыЗаявокНаРемонтИТОТС
	|		ПО упСтатусыЗаявокНаРемонтИТОТС.Документ = ЗаявкаНаРемонтИТОТС.Ссылка
	|ГДЕ
	|	ЗаявкаНаРемонтИТОТС.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена;
	Иначе	
		Возврат Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая;
	КонецЕсли;
	
КонецФункции		

// Функция возвращает дату установки указанного статуса
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаявкаНаПеревозкуГруза - Ссылка на документ
//  Статус  - ПеречислениеСсылка.упСтатусыЗаявокНаПеревозкуГруза - Статус задания
//
// Возвращаемое значение:
//  Дата - дата перевода документа в заданный статус. 
//
Функция ПолучитьДатуПереводаВСтатус(Ссылка, Статус) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат '00010101';
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтатусыЗаявокНаРемонтИТОТС.Период
	|ИЗ
	|	РегистрСведений.упСтатусыЗаявокНаРемонтИТОТС.СрезПоследних(
	|			,
	|			Документ = &Документ
	|				И Статус = &Статус) КАК упСтатусыЗаявокНаРемонтИТОТС";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Запрос.УстановитьПараметр("Статус"  , Статус);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат '00010101';
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Период;
	КонецЕсли; 

КонецФункции // ПолучитьДатуПереводаВСтатус()


////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидРемонта");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Функция создает ремонт на основании заявки на ремонт.
//
//   Параметры:
//     ЗаявкаНаРемонтИТО   - ДокументСсылка.упЗаявкаНаРемонтИТОТС - документ основание.
//     ОбъектРемонтИТОТС - ДокументОбъект.упРемонтИТОТС, Неопределено - параметр передается 
//                         при вызове функции из формы документа ремонта.
//     ТекстОшибки              - Строка - описание ошибки.
//     Отказ                    - Булево - флаг отказа.
//
//   Возвращаемое значение:
//     Массив - Массив созданных заданий на перевозку.
//  
Функция СформироватьРемонтИТОТС(ЗаявкаНаРемонтИТО, ОбъектРемонтИТОТС = Неопределено, ТекстОшибки = "", Отказ) Экспорт
	
	МассивРезультатов = Новый Массив();
	
	Если Отказ Тогда
		Возврат МассивРезультатов;
	КонецЕсли;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();	
	текДокументОбъект = Документы.упРемонтИТОТС.СоздатьДокумент();
	текДокументОбъект.Заполнить(ЗаявкаНаРемонтИТО);
	
	Если текДокументОбъект.ДополнительныеСвойства.Свойство("ОшибкаПриЗаполнении") Тогда
		Если текДокументОбъект.ДополнительныеСвойства.ОшибкаПриЗаполнении Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		текДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		МассивРезультатов.Добавить(текДокументОбъект.Ссылка);
	КонецЕсли;
	
	Возврат МассивРезультатов;
	
КонецФункции // СформироватьРемонтИТОТС()

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Нет.
   	
КонецПроцедуры // ДобавитьКомандыПечати()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Нет.
	
КонецПроцедуры // Печать()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Заполнить дополнительные свойства документа.
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаРемонтИТОТС.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ вт_Документы
	|ИЗ
	|	Документ.упЗаявкаНаРемонтИТОТС КАК упЗаявкаНаРемонтИТОТС
	|ГДЕ
	|	упЗаявкаНаРемонтИТОТС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаРемонтИТОТС.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упЗаявкаНаРемонтИТОТС.ВидРемонта КАК ВидРемонта
	|ИЗ
	|	Документ.упЗаявкаНаРемонтИТОТС КАК упЗаявкаНаРемонтИТОТС
	|ГДЕ
	|	упЗаявкаНаРемонтИТОТС.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|	И упЗаявкаНаРемонтИТОТС.ВидРемонта.ТипРемонта = ЗНАЧЕНИЕ(Перечисление.упТипыРемонтов.ПлановоеТО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	тч_Ремонтируемое.ДополнительноеОборудование,
	|	тч_Ремонтируемое.ВидРемонта
	|ИЗ
	|	Документ.упЗаявкаНаРемонтИТОТС.РемонтируемоеДополнительноеОборудование КАК тч_Ремонтируемое
	|ГДЕ
	|	тч_Ремонтируемое.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка КАК Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|	И тч_Ремонтируемое.ВидРемонта.ТипРемонта = ЗНАЧЕНИЕ(Перечисление.упТипыРемонтов.ПлановоеТО)";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДополнительныеСвойства.Вставить("ПлановыеДатыТО", Результат.Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#КонецОбласти

#КонецЕсли


