
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	СпискиМассивов = ПолучитьСпискиМассивов(ПараметрКоманды);
	
	упРаботаСоСтатусамиКлиент.УстановитьСтатусДокументам(СпискиМассивов.МассивНаСогласование, ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Согласована"));
	упРаботаСоСтатусамиКлиент.УстановитьСтатусДокументам(СпискиМассивов.МассивКВыполнению, ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению"));
	
	Для каждого СсылкаНаЗаявку Из ПараметрКоманды Цикл
		
		ТекущийСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Согласована");
		Если НЕ СпискиМассивов.МассивКВыполнению.Найти(СсылкаНаЗаявку) = Неопределено Тогда
			ТекущийСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению");
		КонецЕсли;
		Структура = Новый Структура("Статус, Ссылка", ТекущийСтатус, СсылкаНаЗаявку);
		Оповестить("СменаСтатуса",Структура, ПараметрыВыполненияКоманды.Источник);
	КонецЦикла;
	
КонецПроцедуры
	
&НаСервере
Функция ПолучитьСпискиМассивов(МассивДокументов)
	
	МассивНаСогласование = Новый Массив;
	МассивКВыполнению     = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаявкаНаРемонтИТОТС.Ссылка КАК СылкаНаДокумент
	               |ИЗ
	               |	Документ.упЗаявкаНаРемонтИТОТС КАК упЗаявкаНаРемонтИТОТС
	               |ГДЕ
	               |	упЗаявкаНаРемонтИТОТС.Ссылка В(&МассивДокументов)
	               |	И упЗаявкаНаРемонтИТОТС.ВидРемонта.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упЗаявкаНаРемонтИТОТС.Ссылка КАК СылкаНаДокумент
	               |ИЗ
	               |	Документ.упЗаявкаНаРемонтИТОТС КАК упЗаявкаНаРемонтИТОТС
	               |ГДЕ
	               |	упЗаявкаНаРемонтИТОТС.Ссылка В(&МассивДокументов)
	               |	И НЕ упЗаявкаНаРемонтИТОТС.ВидРемонта.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Результат = Запрос.ВыполнитьПакет();
	МассивКВыполнению = Результат[0].Выгрузить().ВыгрузитьКолонку("СылкаНаДокумент");
	МассивНаСогласование = Результат[1].Выгрузить().ВыгрузитьКолонку("СылкаНаДокумент");
	
	Структура = Новый Структура;
	Структура.Вставить("МассивНаСогласование", МассивНаСогласование);
	Структура.Вставить("МассивКВыполнению", МассивКВыполнению);
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 

