
#Область ПеременныеФормы

&НаКлиенте
Перем ТекущиеВидыРемонта;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере(Отказ = Ложь)
	
	ЭтоРемонтТС = Ложь;
	
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(Элементы.ТранспортноеСредство, "СкрытьНеучетные", Истина);
	
	ВедетсяУчетШинУзловИАгрегатов = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетШинУзловИАгрегатов");	
	ВедетсяУчетДополнительногоОборудования = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования");
	
	Элементы.ОбъектРемонта.СписокВыбора.Очистить();
	Элементы.ГруппаВыборОбъектыРемонта.ОтображатьЗаголовок = Ложь;
	Если НЕ ВедетсяУчетШинУзловИАгрегатов И НЕ ВедетсяУчетДополнительногоОборудования Тогда 
		ЭтоРемонтТС = Истина;
		Элементы.ТранспортноеСредство.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		Элементы.ОбъектРемонта.Видимость = Ложь;
		Элементы.ГруппаВыборОбъектыРемонта.ОтображатьЗаголовок = Истина;
	Иначе
		
		СписокОбъектРемонта = Новый Массив;
		СписокОбъектРемонта.Добавить(Перечисления.упОбъектыРемонта.ТранспортноеСредство);
		Если ВедетсяУчетДополнительногоОборудования Тогда 
			СписокОбъектРемонта.Добавить(Перечисления.упОбъектыРемонта.ДополнительноеОборудование);
		КонецЕсли;
		Если ВедетсяУчетШинУзловИАгрегатов Тогда 
			СписокОбъектРемонта.Добавить(Перечисления.упОбъектыРемонта.Агрегат);
		КонецЕсли;
		Элементы.ОбъектРемонта.СписокВыбора.ЗагрузитьЗначения(СписокОбъектРемонта);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		Если ВедетсяВалютныйУчет Тогда
			Объект.Валюта = ВалютаРеглУчета;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.ТранспортноеСредство) И Параметры.Свойство("ТранспортноеСредство") Тогда
			Если Параметры.Свойство("ДополнительноеОборудование") Тогда
				Если ЗначениеЗаполнено(Параметры.ТранспортноеСредство) Тогда
					Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство;
					Объект.ТранспортноеСредство = Параметры.ТранспортноеСредство;
					Если ЗначениеЗаполнено(Параметры.ДополнительноеОборудование) Тогда
						НоваяСтрока = Объект.РемонтируемоеДополнительноеОборудование.Добавить();
						НоваяСтрока.ДополнительноеОборудование = Параметры.ДополнительноеОборудование;
					КонецЕсли; 
					
				ИначеЕсли ЗначениеЗаполнено(Параметры.ДополнительноеОборудование) Тогда
					Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование;
					Объект.ДополнительноеОборудованиеРемонта = Параметры.ДополнительноеОборудование;
					
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		
		Если Параметры.Свойство("МестоРемонта") И ЗначениеЗаполнено(Параметры.МестоРемонта) Тогда
			Объект.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами;
			Объект.Гараж = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МестоРемонта, "Владелец");
			Объект.МестоРемонта = Параметры.МестоРемонта;
			Объект.ВидРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МестоРемонта, "ВидРемонтаПоУмолчанию");
		КонецЕсли;
		Если Параметры.Свойство("НачалоРемонтаПлан") Тогда
			Объект.НачалоРемонтаПлан = Параметры.НачалоРемонтаПлан;
		КонецЕсли;
		Если Параметры.Свойство("ОкончаниеРемонтаПлан") Тогда
			Объект.ОкончаниеРемонтаПлан = Параметры.ОкончаниеРемонтаПлан;
		КонецЕсли;
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			Объект.ЧасовойПояс	= упСервисныеФункции.ЧасовойПоясВидаПеревозки();	
		КонецЕсли;
		
	КонецЕсли;
	
	Статус = Документы.упЗаявкаНаРемонтИТОТС.ПолучитьСтатус(Объект.Ссылка);
	
	// установка доступностьи элементов формы
	ЗаблокироватьЭлементы();
	ТребуетсяСогласованиеЗаявкиНаРемонт = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		ТребуетсяСогласованиеЗаявкиНаРемонт = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТребуетсяСогласованиеЗаявкиНаРемонт");
	КонецЕсли;
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиРемонтаИТОТС(Объект.Ссылка, Статус, Элементы, ТребуетсяСогласованиеЗаявкиНаРемонт);
	
	УстановитьОтметкуНезаполненного();
	ОбновитьВидФормыПоВидуРемонта();
	ОбновитьВидФормыПоСпособуРемонта();
		
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.СформированРемонт"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.ВРемонте"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Отменена"));
	
	Если НЕ МассивСтатусов.Найти(Статус) = Неопределено Тогда
		Элементы.ГруппаОбщаяШапка.ТолькоПросмотр = Истина;
		Элементы.ГруппаШапка.ТолькоПросмотр = Истина;
		Элементы.ГруппаРемонтОборудования.ТолькоПросмотр = Истина;
		Элементы.ГруппаРаботы.ТолькоПросмотр = Истина;
		Элементы.Подразделение.ТолькоПросмотр = Истина;
		Элементы.ЧасовойПояс.ТолькоПросмотр = Истина;
		Элементы.ПричинаОбращения.ТолькоПросмотр = Истина;
		Элементы.РаботыЗаполнитьРаботыПоУмолчанию.Доступность = Ложь;
		Элементы.РемонтируемоеДополнительноеОборудованиеЗаполнитьОстаткамиНаТС.Доступность = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаРеглУчета	= упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
		ЭтаФорма.ВалютаУпрУчета 	= упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли; 
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	
	ПриСозданииПриЧтенииНаСервере(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если ИмяСобытия = "СменаСтатуса" И Источник = Объект.Ссылка Тогда
		ИзменилсяСтатус();
	Иначе
		Если ТипЗнч(Параметр) = ТИП("Структура") Тогда
			Если Параметр.Свойство("Ссылка") Тогда
				Если ТипЗнч(Параметр.Ссылка) = ТИП("ДокументСсылка.упРемонтИТОТС") Тогда
					ЗаявкаНаРемонтИТО = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Параметр.Ссылка, "ЗаявкаНаРемонтИТО");
					Если ЗаявкаНаРемонтИТО = Объект.Ссылка Тогда
						ИзменилсяСтатус();
					КонецЕсли;
				ИначеЕсли ТипЗнч(Параметр.Ссылка) = ТИП("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда
					Если Параметр.Ссылка = Объект.Ссылка Тогда
						ИзменилсяСтатус();
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Не ЗначениеЗаполнено(НовыйСтатус) Тогда 
		Возврат;
	Иначе
		ПроверяемыйСтатус = НовыйСтатус;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.МестоРемонта) Тогда
		Если ПроверяемыйСтатус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована Тогда 
			Если Не ЗначениеЗаполнено(Объект.НачалоРемонтаПлан) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано плановое время начала ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НачалоРемонтаПлан",, Отказ);
			КонецЕсли; 
			
			Если Не ЗначениеЗаполнено(Объект.ОкончаниеРемонтаПлан) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано плановое время окончания ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ОкончаниеРемонтаПлан",, Отказ);
			КонецЕсли; 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;
	НовыйСтатус = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Статус = Документы.упЗаявкаНаРемонтИТОТС.ПолучитьСтатус(Объект.Ссылка);
	
	ТребуетсяСогласованиеЗаявкиНаРемонт = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		ТребуетсяСогласованиеЗаявкиНаРемонт = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТребуетсяСогласованиеЗаявкиНаРемонт");
	КонецЕсли;
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиРемонтаИТОТС(Объект.Ссылка, Статус, Элементы, ТребуетсяСогласованиеЗаявкиНаРемонт);
	УстановитьОтметкуНезаполненного();
	ЗаблокироватьЭлементы();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОповеститьОбИзменении(Объект.Ссылка);	
	
	Структура = Новый Структура("Статус, Ссылка",Статус,Объект.Ссылка);
	Структура.Вставить("ВидРемонта",Объект.ВидРемонта);
	Оповестить("ЗаписьДокументаЗаявкаРемонтИТОТС",Структура,ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#Область СобытияШапки

&НаКлиенте
Процедура ТранспортноеСредствоИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОжь;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОжь;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	сткПараметры = Новый Структура("Организация, СкрытьНеучетные,ЭтоСобственноеТС", Объект.Организация, Истина,Истина);
	ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеВидыРемонта = Новый Структура("Вид",Объект.ВидРемонта);
	МассивТиповРемонта = Новый Массив;
	Если НЕ ПроверитьЭтоРемонтТС(Объект.ОбъектРемонта, МассивТиповРемонта) Тогда				
		сткОтбора = Новый Структура("ТипРемонта", МассивТиповРемонта);
		сткПараметры = Новый Структура("Отбор", сткОтбора);
		ОткрытьФорму("Справочник.упВидыРемонтов.Форма.ФормаВыбора", сткПараметры, Элемент);
		СтандартнаяОбработка = Ложь;		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьЭтоРемонтТС(ОбъектРемонта, СписокТипов = Неопределено)

	РемонтТС = Истина;
	
	Если ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ДополнительноеОборудование")Тогда
		РемонтТС = Ложь;
		Если НЕ СписокТипов = Неопределено Тогда
			СписокТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.ПлановоеТО"));
			СписокТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.Ремонт"));
		КонецЕсли;
	ИначеЕсли ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат")Тогда		
		РемонтТС = Ложь;
		Если НЕ СписокТипов = Неопределено Тогда
			СписокТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.ПлановоеТО"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат РемонтТС;

КонецФункции // ПроверитьЭтоРемонтТС()

&НаКлиенте
Процедура ВидРемонтаПриИзменении(Элемент)	
	
	Если Объект.Работы.Количество() Тогда		
		ПараметрыОтвета = Новый Структура;
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаВидРемонта", ЭтаФорма,ПараметрыОтвета);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'При изменении «Вида ремонта» табличная часть «Работы» будет перезаполнена. Продолжить?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
	Иначе	
		ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта();
		ОбновитьВидФормыПоВидуРемонта();
		ЗаполнитьРаботыПоУмолчаниюНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРемонтаПриИзменении(Элемент)
	
   ОбновитьВидФормыПоСпособуРемонта();
	
КонецПроцедуры

&НаКлиенте
Процедура ГаражПриИзменении(Элемент)
	
	УстановитьОтметкуНезаполненного();
	
КонецПроцедуры

&НаКлиенте
Процедура МестоРемонтаПриИзменении(Элемент)
	
	УстановитьОтметкуНезаполненного();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеРемонтаПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура АгрегатПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура НачалоРемонтаПланПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

#КонецОбласти

#Область СобытияСтраниц

#Область СтраницаОбщее

&НаКлиенте
Процедура ОбъектРемонтаПриИзменении(Элемент)
	
	ОбъектРемонтаУстановитьСтраницу();
	
КонецПроцедуры

#КонецОбласти 

#Область СтраницаРаботы

&НаКлиенте
Процедура РаботыИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Механик"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Прочее"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Оператор"));
	ТекущийОтбор = Новый Структура("ТипСотрудника", СписокЗначений);
	сткПараметры = Новый Структура("Отбор", ТекущийОтбор);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИспольнительНачалоВыбораЗавершение", ЭтаФорма);
	ОткрытьФорму("Справочник.упСотрудники.ФормаВыбора", сткПараметры,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	Если НЕ ПроверитьТипСотрудника(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		ТекстОшибки = "Укажите исполнителя с типом: Механик, Прочее, Оператор";
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.упСотрудники.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.Работы.ТекущиеДанные;
		текСтрока.Валюта = ВалютаРеглУчета;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РаботыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РаботыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

#КонецОбласти  

#Область РемонтируемоеДополнительноеОборудование

&НаКлиенте
Процедура РемонтируемоеДополнительноеОборудованиеВидРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	МассивТиповРемонта = Новый Массив;
	МассивТиповРемонта.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.Ремонт"));
	МассивТиповРемонта.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.ПлановоеТО"));
	
	сткОтбора = Новый Структура("ТипРемонта", МассивТиповРемонта);
	сткПараметры = Новый Структура("Отбор", сткОтбора);
	ОткрытьФорму("Справочник.упВидыРемонтов.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#КонецОбласти 

#Область ОбработчикиСобытийКоманд


// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура ЗаполнитьОстаткамиНаТС(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите транспортное средство'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ТранспортноеСредство");
		Возврат;
	КонецЕсли; 
	
	Если Объект.РемонтируемоеДополнительноеОборудование.Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Ремонтируемое дополнительное оборудование будут перезаполнены оборудование установленном транспортном средстве. Очистить список ремонтируемого дополнительного оборудования?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРемонтируемоеДополнительноеОборудованиеОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКВыполнению(Команда)
	
	Если ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		ТекстОшибки = "";
		Если УстановитьСтатусКВыполнениюСервер(ТекстОшибки) Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
		Иначе
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				//упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
			КонецЕсли;
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не заполнен вид ремонта'"), БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусКВыполнениюСервер(ТекстОшибки)
	
	сткВозврата = упРемонтИТОТС.РазрешеноПеревестиКВыполнению(Статус, упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТребуетсяСогласованиеЗаявкиНаРемонт"));
	Если сткВозврата.Результат Тогда
	    Возврат ИзменитьСтатусЗаявкиРемонта(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению, ТекстОшибки);
	Иначе
		ТекстОшибки = сткВозврата.ТекстОшибки;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(сткВозврата.ТекстОшибки, Объект.Ссылка, "Статус");
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСтатусСогласована(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Новая") 
		ИЛИ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.НеСогласована") Тогда 
		ТекстОшибки = "";
		Если УстановитьСтатусСогласованаСервер(ТекстОшибки) Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""Новая"" или ""Не согласована""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусСогласованаСервер(ТекстОшибки)
	
	АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению");
	
	Если АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению Тогда 
		Возврат ИзменитьСтатусЗаявкиРемонта(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению, ТекстОшибки);
	Иначе	
		Возврат ИзменитьСтатусЗаявкиРемонта(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Согласована, ТекстОшибки);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСтатусОтменена(Команда)
	
	Если НЕ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена") Тогда 
		ТекстОшибки = "";
		Если УстановитьСтатусОтмененаСервер(ТекстОшибки) Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заявка выполнена, отмена невозможна'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусОтмененаСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусЗаявкиРемонта(Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена, ТекстОшибки);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьРаботыПоУмолчанию(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите вид ремонта'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ВидРемонта");
		Возврат;
	КонецЕсли; 
	
	Если Объект.Работы.Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Работы будут перезаполнены работами по умолчанию для вида ремонта. Продолжить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРаботыПоУмолчаниюОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ЗаполнитьРаботыПоУмолчаниюНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПериодРаспределенияЗатратНачало", "ПериодРаспределенияЗатратОкончание"));
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаКлиенте
// Согласно указанному виду ремонта выполним очистку табличных частей.
//
Процедура ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта()
	
	ЗаполненыеОбъекты = ПолучитьТабличныеЧастиДляОчистки();
	
	Если ЗаполненыеОбъекты.Количество() Тогда
		
		ПараметрыОтвета = Новый Структура;
		ПараметрыОтвета.Вставить("Таблицы",ЗаполненыеОбъекты);
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаТаблицыОчистки", ЭтаФорма,ПараметрыОтвета);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'При изменении «Вида ремонта» найденны заполненые таблицы, данные таблиц будут очищены. Продолжить?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта()

&НаКлиенте
Процедура ПослеЗакрытияВопросаТаблицыОчистки(Результат, ПараметрыОтвета) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда		
		Возврат;
	КонецЕсли;
	
	Массив = ПараметрыОтвета.Таблицы;
	
	Для каждого ТекущаяТабличнаяЧасть Из Массив Цикл
		ТабличнаяЧасть = Объект[ТекущаяТабличнаяЧасть];
		ТабличнаяЧасть.Очистить();
	КонецЦикла;
	
КонецПроцедуры // ПослеЗакрытияВопросаТаблицыОчистки()

&НаСервере	
// Выполним проверку и получим массив табличных частей для очистки.
//
// Возвращаемое значение:
//   Массив - Значение строка, список табличных частей.
//
Функция ПолучитьТабличныеЧастиДляОчистки()

	Массив = Новый Массив;
	
	ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТипРемонта");
	Если ТипРемонта = Перечисления.упТипыРемонтов.МонтажОборудования Тогда
		Массив.Добавить("Работы");
	КонецЕсли;

	Возврат Массив;
	
КонецФункции // ПолучитьТабличныеЧастиДляОчистки()

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#Область УправлениеСтатусом

&НаСервере
Функция ИзменитьСтатусЗаявкиРемонта(СтатусЗаявкиРемонта, ТекстОшибки)
	
	НовыйСтатус = СтатусЗаявкиРемонта;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
        	Возврат Ложь;    
		КонецПопытки;
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус()
	Статус = Документы.упЗаявкаНаРемонтИТОТС.ПолучитьСтатус(Объект.Ссылка);
	ЗаблокироватьЭлементы();
	ТребуетсяСогласованиеЗаявкиНаРемонт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТребуетсяСогласованиеЗаявкиНаРемонт");
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиРемонтаИТОТС(Объект.Ссылка, Статус, Элементы, ?(ТребуетсяСогласованиеЗаявкиНаРемонт = Неопределено, Ложь, ТребуетсяСогласованиеЗаявкиНаРемонт));	
КонецПроцедуры
	
#КонецОбласти 

#Область УправлениеВидимостью

&НаСервере
Процедура ОбновитьВидФормыПоВидуРемонта()
	
	Если Не ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		Элементы.ГруппаРаботы.Доступность = Ложь;
		Возврат;
	КонецЕсли;	
	
	ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТипРемонта");
	Если ТипРемонта = Перечисления.упТипыРемонтов.Ремонт Или ТипРемонта = Перечисления.упТипыРемонтов.ПлановоеТО Тогда
		Элементы.ГруппаРаботы.Доступность					  = Истина;
	ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.МонтажОборудования Тогда
		Элементы.ГруппаРаботы.Доступность                     = Ложь;
	ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.ЗаменаШин Тогда
		Элементы.ГруппаРаботы.Доступность					  = Истина;
	ИначеЕсли ТипРемонта = Перечисления.упТипыРемонтов.МонтажПрицепов Тогда
		Элементы.ГруппаРаботы.Доступность					  = ?(ЭтоПрицеп(Объект.ТранспортноеСредство), Ложь, Истина);
	КонецЕсли; 
	
КонецПроцедуры // ОбновитьВидФормыПоВидуРемонта()

&НаСервере
Процедура ОбновитьВидФормыПоСпособуРемонта()
	
	ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидРемонта, "ТипРемонта");
	Если Объект.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами Тогда
		Элементы.СтраницыСпособыРемонта.ТекущаяСтраница = Элементы.ГруппаСобственнымиСилами;
	Иначе
		Элементы.СтраницыСпособыРемонта.ТекущаяСтраница = Элементы.ГруппаСервиснаяОрганизация;		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкуНезаполненного()
	
	Элементы.НачалоРемонтаПлан.АвтоОтметкаНезаполненного    = Ложь;
	Элементы.ОкончаниеРемонтаПлан.АвтоОтметкаНезаполненного = Ложь;
	
	Если Объект.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами И ЗначениеЗаполнено(Объект.МестоРемонта) Тогда
		Если Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая Тогда
			Элементы.НачалоРемонтаПлан.АвтоОтметкаНезаполненного    = Истина;
			Элементы.ОкончаниеРемонтаПлан.АвтоОтметкаНезаполненного = Истина;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Заполнить ТЧ "Работы" типовыми данными.
//
Процедура ЗаполнитьРаботыПоУмолчаниюОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЗаполнитьРаботыПоУмолчаниюНаСервере();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьРаботыПоУмолчаниюНаСервере()
	
	Объект.Работы.Очистить();
	
	тбзРаботы = упРемонтИТОТС.РаботыПоВидуРемонта(Объект.ВидРемонта);
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	Для каждого Строка Из тбзРаботы Цикл
		НоваяСтрока = Объект.Работы.Добавить();
		НоваяСтрока.Валюта = Объект.Валюта;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока, "Цена", СтруктураДанные);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Если Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Выполнена Или Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Отменена Тогда
			ТолькоПросмотр = Истина;
			Возврат;
		КонецЕсли;
		
		Если Не Статус = Перечисления.упСтатусыЗаявкиНаРемонтИТОТС.Новая Тогда
			Элементы.ГруппаУстановкаСпособаРемонта.ТолькоПросмотр = Истина;
			Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = Истина;
			Элементы.ГруппаВремяРемонтаПлан.ТолькоПросмотр          = Истина;
			Элементы.ГруппаПлановаяСтоимость.ТолькоПросмотр       = Истина;
		КонецЕсли;		
	КонецЕсли;	
	
	ОбъектРемонтаУстановитьСтраницу();

КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	
	ИтоговаяСумма 		= 0;
	ИтоговаяСуммаНДС 	= 0;
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыПлан",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыПланНДС",	"СуммаНДС");
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Работы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходыПлан;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыПланНДС;
	
	Объект.СуммаРасходыПлан 	= ИтоговаяСумма;
	Объект.СуммаРасходыПланНДС	= ИтоговаяСуммаНДС; 
		
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент, ИмяТаблЧасти)
	ТекущаяСтрока	= Элементы[ИмяТаблЧасти].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоля(Элемент.Имя, ИмяТаблЧасти),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля, ИмяТаблЧасти)
	Возврат СтрЗаменить(ИмяПоля, ИмяТаблЧасти,"");
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоПрицеп(ТС)
	Возврат ТС.Прицеп;
КонецФункции

&НаКлиенте
// Процедура завершения после закрытия формы выбора прицепа.
//
Процедура ИспольнительНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если НЕ Результат = Неопределено Тогда 
		ТекущийЭлемент.ТекущиеДанные.Исполнитель = Результат;	
	КонецЕсли;    
КонецПроцедуры // ИспольнительНачалоВыбораЗавершение()

&НаКлиенте
Процедура ПослеЗакрытияВопросаВидРемонта(Результат, ПараметрыОтвета) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Объект.ВидРемонта = ТекущиеВидыРемонта.Вид;		
		Возврат;
	КонецЕсли;
	
	ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта();
	ОбновитьВидФормыПоВидуРемонта();
	ЗаполнитьРаботыПоУмолчаниюНаСервере();
	
КонецПроцедуры // ПослеЗакрытияВопросаВидРемонта()

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервереБезКонтекста
// Выполним проверку сотрудника исполнителя.
//
// Параметры:
//  ТекущийСотрудник  - СправочникСсылка.упСотрудники - Ссылка на исполнителя.
//
// Возвращаемое значение:
//   Булево   - результат проверки.
//
Функция ПроверитьТипСотрудника(ТекущийСотрудник)

	ТипСотрудника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийСотрудник, "ТипСотрудника");
	Массив = Новый Массив;
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Механик"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Прочее"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Оператор"));
	Результат = Ложь;
	Если НЕ Массив.Найти(ТипСотрудника) = Неопределено Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьТипСотрудника()

&НаКлиенте
// Заполнить ТЧ "Работы" типовыми данными.
//
Процедура ЗаполнитьРемонтируемоеДополнительноеОборудованиеОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
		
	Если НЕ Результат = КодВозвратаДиалога.Отмена Тогда
		Если Результат = КодВозвратаДиалога.Да Тогда
			Объект.РемонтируемоеДополнительноеОборудование.Очистить();
		КонецЕсли;
		ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере()
	
	Период = Объект.Дата;
	Если ЗначениеЗаполнено(Объект.НачалоРемонтаПлан) Тогда
		Период = Объект.НачалоРемонтаПлан;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АгрегатыТС.Агрегат КАК ДополнительноеОборудование
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|			&Период,
	|			Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|				И Агрегат.Ссылка ССЫЛКА Справочник.упДополнительноеОборудование) КАК АгрегатыТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упДополнительноеОборудование КАК ДопОборудование
	|		ПО АгрегатыТС.Агрегат = ДопОборудование.Ссылка
	|ГДЕ
	|	АгрегатыТС.ТранспортноеСредство = &ТранспортноеСредство";
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Запрос.УстановитьПараметр("Период", Период);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Объект.РемонтируемоеДополнительноеОборудование.Добавить(), Выборка);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере()

&НаСервере
Процедура ОбъектРемонтаУстановитьСтраницу()
	
	Элементы.ГруппаСтраницыОбъектыРемонта.ТекущаяСтраница = Элементы.ГруппаСтраницаТранспортноеСредствоОбъектРемонта;
		
	Если Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ДополнительноеОборудование")Тогда
		Элементы.ГруппаСтраницыОбъектыРемонта.ТекущаяСтраница = Элементы.ГруппаСтраницаДополнительноеОборудованиеРемонтаОбъектРемонта;
		Если ЭтоРемонтТС Тогда
			Элементы.ДополнительноеОборудованиеРемонта.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		КонецЕсли;
	ИначеЕсли Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат")Тогда
		Элементы.ГруппаСтраницыОбъектыРемонта.ТекущаяСтраница = Элементы.ГруппаСтраницаАгрегатОбъектРемонта;
		Если ЭтоРемонтТС Тогда
			Элементы.Агрегат.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаРемонтОборудования.Видимость = Ложь
										 Или Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ДополнительноеОборудование")
										 Или Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат");
	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(,Объект.ТранспортноеСредство);
		КонецЕсли;		
		
		Объект.НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,Объект.ТранспортноеСредство);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТранспортноеСредствоОбъектаРемонта()
	Если Объект.ОбъектРемонта <> ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ТранспортноеСредство") Тогда
		Агрегат = ?(Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат"),
					Объект.Агрегат,
					Объект.ДополнительноеОборудованиеРемонта);
		Если ЗначениеЗаполнено(Агрегат) Тогда
			Период = ?(ЗначениеЗаполнено(Объект.НачалоРемонтаПлан), Объект.НачалоРемонтаПлан, Объект.Дата);
			Объект.ТранспортноеСредствоОбъектаРемонта = ТранспортноеСредствоПоАгрегату(Период, Агрегат);	
		Иначе
			Объект.ТранспортноеСредствоОбъектаРемонта = ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТранспортноеСредствоПоАгрегату(Период, Агрегат)
	Возврат упУправлениеЗапасами.ТранспортноеСредстваПоАгрегату(Период, Агрегат);
КонецФункции

#КонецОбласти 