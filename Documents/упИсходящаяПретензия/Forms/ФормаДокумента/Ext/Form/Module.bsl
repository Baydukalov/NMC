
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Установка статуса.
	Статус = Документы.упИсходящаяПретензия.ПолучитьСтатус(Объект.Ссылка);
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаИсходящейПретензии(Объект.Ссылка, Статус, Элементы);

	// Валютный учет.
	Если ЭтаФорма.ВедетсяВалютныйУчет 
		И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		УстановитьВалютуУчета();
		ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	// Взаимодействия.
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект,Параметры,Ложь);
	
	// Если новый документ
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
			И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ДокументОснование, Объект.Автор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
    УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
    УправлениеПечатьюКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	НовыйСтатус = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Статус = Документы.упИсходящаяПретензия.ПолучитьСтатус(Объект.Ссылка);
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаИсходящейПретензии(Объект.Ссылка, Статус, Элементы);
	ЗаблокироватьЭлементы();

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	 //СтандартныеПодсистемы.Свойства 
    	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
    		ОбновитьЭлементыДополнительныхРеквизитов();
    	КонецЕсли;
     //Конец СтандартныеПодсистемы.Свойства	

КонецПроцедуры

//// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура РейсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ЗаявкаНаПеревозкуГруза) Тогда
		
		СтандартнаяОбработка = Ложь;
		Массив = РейсыПоЗаявке();
		ЗначениеОтбора  = Новый Структура("Ссылка", Массив);
		ПараметрыВыбораСтраховщика = Новый Структура("Отбор", ЗначениеОтбора);
		ОткрытьФорму("Документ.упРейс.ФормаВыбора", ПараметрыВыбораСтраховщика, Элемент);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РейсОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Объект.Рейс = ВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура РейсАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ЗаявкаНаПеревозкуГруза) И НЕ Текст = "" Тогда
		
		Массив = РейсыПоЗаявке();
		ЗначениеОтбора  = Новый Структура("Ссылка", Массив);
		ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
		
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура РейсОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ЗаявкаНаПеревозкуГруза) Тогда
		
		Массив = РейсыПоЗаявке();
		ЗначениеОтбора  = Новый Структура("Ссылка", Массив);
		ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
		
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаНаПеревозкуГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Рейс) Тогда
		
		СтандартнаяОбработка = Ложь;
		Массив = ЗаявкиПоРейсу();
		ЗначениеОтбора  = Новый Структура("Ссылка", Массив);
		ПараметрыВыбораСтраховщика = Новый Структура("Отбор", ЗначениеОтбора);
		ОткрытьФорму("Документ.упЗаявкаНаПеревозкуГруза.ФормаВыбора", ПараметрыВыбораСтраховщика, Элемент);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаНаПеревозкуГрузаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Объект.ЗаявкаНаПеревозкуГруза = ВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаНаПеревозкуГрузаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Рейс) И НЕ Текст = "" Тогда
		
		Массив = ЗаявкиПоРейсу();
		ЗначениеОтбора  = Новый Структура("Ссылка", Массив);
		ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаНаПеревозкуГрузаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Рейс) Тогда
		
		Массив = ЗаявкиПоРейсу();
		ЗначениеОтбора  = Новый Структура("Ссылка", Массив);
		ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	ДокументОснованиеПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыПлановыеДоходы

&НаКлиенте
Процедура ПлановыеДоходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;

		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСуммаПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыЦенаПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыКоличествоПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСтавкаНДСПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСтатьяДоходовПриИзменении(Элемент)
	
	ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
	СтатьяДоходов	= ТекущаяСтрока.СтатьяДоходов;
	Если ЗначениеЗаполнено(СтатьяДоходов) Тогда
		СтавкаНДС 				= упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяДоходов, "СтавкаНДС");
		ТекущаяСтрока.СтавкаНДС = СтавкаНДС;
		ПересчитатьСтроку(Элементы.ПлановыеДоходыКоличество);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	
	ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоля(Элемент.Имя),СтруктураДанные);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля)
	
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеДоходы","");
	
КонецФункции

&НаСервере 
Функция РейсыПоЗаявке()
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упСтатусыЗаявокНаПеревозкуГруза.Регистратор КАК Рейс
		|ИЗ
		|	РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза КАК упСтатусыЗаявокНаПеревозкуГруза
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(упСтатусыЗаявокНаПеревозкуГруза.Регистратор) = ТИП(Документ.упРейс)
		|	И упСтатусыЗаявокНаПеревозкуГруза.Документ = &Документ";
		
		Запрос.УстановитьПараметр("Документ", Объект.ЗаявкаНаПеревозкуГруза);
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Рейс");
				
		Возврат РезультатЗапроса;
		
КонецФункции
	
&НаСервере 
Функция ЗаявкиПоРейсу();
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза КАК Задание
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза КАК упСтатусыЗаданийНаПеревозкуГруза
		|		ПО упРаботыПоРейсу.Задание = упСтатусыЗаданийНаПеревозкуГруза.Документ
		|ГДЕ
		|	НЕ упСтатусыЗаданийНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
		|	И упРаботыПоРейсу.Рейс = &Рейс";
		
		Запрос.УстановитьПараметр("Рейс", Объект.Рейс);
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Задание");
				
		Возврат РезультатЗапроса;
		
КонецФункции		

#КонецОбласти

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СтатусОтменен(Команда)
	
	Если Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыИсходящейПретензии.Удовлетворена") 
		ИЛИ Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыИсходящейПретензии.УдовлетворенаЧастично") 
		ИЛИ Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыИсходящейПретензии.Отклонена") Тогда 
		
		ТекстОшибки = "";
		Если УстановитьСтатусОтмененаСервер(ТекстОшибки) Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
		Иначе
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Претензия отработана, отменить невозможно'"), Объект.Ссылка, "Статус");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтправлена(Команда)
	
	ТекстОшибки = "";
	Если УстановитьСтатусОтправленаСервер(ТекстОшибки) Тогда 
		ОповеститьОбИзменении(Объект.Ссылка); 
	Иначе
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтатусЗавершен(Команда)
	
	Список = Новый СписокЗначений;
	Список.Добавить(1,"Удовлетворена");
	Список.Добавить(2, "Удовлетворена частично");
	Список.Добавить(0	,"Отклонена");
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМеню", ЭтотОбъект, Параметры);
	ПоказатьВыборИзМеню(Оповещение, Список, Элементы.СтатусЗавершен);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт

	ТекстОшибки = "";
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если (ВыбранныйЭлемент.Значение = 0 И УстановитьСтатусОтклоненаСервер(ТекстОшибки)) 
		ИЛИ (ВыбранныйЭлемент.Значение = 1 И УстановитьСтатусУдовлетворенаСервер(ТекстОшибки))
		ИЛИ (ВыбранныйЭлемент.Значение = 2 И УстановитьСтатусУдовлетворенаЧастичноСервер(ТекстОшибки)) Тогда 
			ОповеститьОбИзменении(Объект.Ссылка); 
	Иначе
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
			КонецЕсли; 
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область УправлениеСтатусом

&НаСервере
Функция УстановитьСтатусОтправленаСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусПретензии(Перечисления.упСтатусыИсходящейПретензии.Отправлена, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОтклоненаСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусПретензии(Перечисления.упСтатусыИсходящейПретензии.Отклонена, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусУдовлетворенаСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусПретензии(Перечисления.упСтатусыИсходящейПретензии.Удовлетворена, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусУдовлетворенаЧастичноСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусПретензии(Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОтмененаСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусПретензии(Перечисления.упСтатусыИсходящейПретензии.Отменена, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция ИзменитьСтатусПретензии(СтатусПретензии, ТекстОшибки)
	
	НовыйСтатус = СтатусПретензии;
	
	Если ПроверитьЗаполнение() Тогда 
		
		ФлагВозврата = Ложь;
		
		Если (СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Удовлетворена
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Отклонена
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Отправлена)
			И НЕ ЗначениеЗаполнено(Объект.ОписаниеПретензии) Тогда
			ТекстОшибки = ТекстОшибки + "Не заполнено поле ""Описание претензии""! ";
			Элементы.ОписаниеПретензии.АвтоОтметкаНезаполненного = Истина;
			ФлагВозврата = Истина;
		КонецЕсли;
		
		Если (СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Удовлетворена
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Отклонена)
			И НЕ ЗначениеЗаполнено(Объект.ПричинаПретензии) Тогда
			ТекстОшибки = ТекстОшибки + "Не заполнено поле ""Причина претензии""! ";	
			Элементы.ПричинаПретензии.АвтоОтметкаНезаполненного = Истина;
		    ФлагВозврата = Истина;		
		КонецЕсли;
		
		Если (СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Удовлетворена
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.УдовлетворенаЧастично
			ИЛИ СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Отклонена)
			И НЕ ЗначениеЗаполнено(Объект.РезультатОбработки) Тогда
			ТекстОшибки = ТекстОшибки + "Не заполнено поле ""Результат обработки""! ";
			Элементы.РезультатОбработки.АвтоОтметкаНезаполненного = Истина;
		    ФлагВозврата = Истина;		
		КонецЕсли;
		
		Если СтатусПретензии = Перечисления.упСтатусыИсходящейПретензии.Отклонена
			И ЗначениеЗаполнено(Объект.ПлановыеДоходы) Тогда
			ТекстОшибки = ТекстОшибки + "Табличная часть ""Доходы"" не может быть заполнена при отклонении претензии! ";	
		    ФлагВозврата = Истина;		
		КонецЕсли;
		
		Если ФлагВозврата Тогда
			Возврат Ложь;	
		КонецЕсли; 
		
		Попытка
			Записать();		
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
        	Возврат Ложь;    
		КонецПопытки;
		
		Возврат Истина;
		
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаблокироватьЭлементы()

	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Если Статус = Перечисления.упСтатусыИсходящейПретензии.Удовлетворена Или Статус = Перечисления.упСтатусыИсходящейПретензии.Отменена Тогда
			ТолькоПросмотр = Истина;
			Возврат;
		КонецЕсли;		
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере()
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ДокументОснование);
		Если ЗначениеЗаполнено(Подразделение)
				И НЕ Подразделение = Объект.Подразделение Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 
