#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
	упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Записать();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПретензииПоЗаявкамНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Претензия", Ссылка);
	 
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПретензииПоРейсам");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Претензия", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыИсходящихПретензий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
		
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
		
	РеквизитыОбъекта = Новый Структура("ЗаявкаНаПеревозкуГруза,
										|Дата,
										|Рейс,
										|Организация,
										|ПричинаПретензии,
										|Партнер, 
										|Подразделение,
										|НаправлениеДеятельности,
										|ПлановыеДоходы");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект); 
	упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
	Если Истина
		И ПлановыеДоходы.Количество() > 0 
		И (Ложь
		   Или ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза)
		   Или ЗначениеЗаполнено(Рейс)) 
	Тогда
		
		// Распределение доходов
		упИсходящаяПретензия.СформироватьДвиженияДоходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);		

	КонецЕсли; 
	
	// Движения по заявке на перевозку
	упИсходящаяПретензия.СформироватьДвиженияПоЗаявкеИсходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Движения по документу рейс
	упИсходящаяПретензия.СформироватьДвиженияПоРейсуИсходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется и устанавливается статус документа
	упИсходящаяПретензия.СформироватьДвиженияСтатусИсходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		// Заполнение шапки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упЗаявкаНаПеревозкуГруза.Заказчик КАК Партнер,
		|	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК Контрагент,
		|	упЗаявкаНаПеревозкуГруза.Организация,
		|	упЗаявкаНаПеревозкуГруза.Соглашение,
		|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ДокументОснование,
		|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозкуГруза
		|ИЗ
		|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
		|ГДЕ
		|	упЗаявкаНаПеревозкуГруза.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,Запрос.Выполнить().Выгрузить()[0]);

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда
		// Заполнение шапки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упРейс.Ссылка КАК ДокументОснование,
		|	упРейс.Ссылка КАК Рейс,
		|	упПеревозчикПоРейсуСрезПоследних.Перевозчик КАК Партнер,
		|	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика КАК Контрагент,
		|	упПеревозчикПоРейсуСрезПоследних.Соглашение,
		|	упРейс.Организация
		|ИЗ
		|	Документ.упРейс КАК упРейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
		|		ПО упПеревозчикПоРейсуСрезПоследних.Регистратор = упРейс.Ссылка
		|			И упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка
		|ГДЕ
		|	упРейс.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,Запрос.Выполнить().Выгрузить()[0]);
				
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
