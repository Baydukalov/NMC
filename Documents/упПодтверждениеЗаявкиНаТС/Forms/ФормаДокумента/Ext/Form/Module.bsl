
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта();
		
		Если Параметры.Свойство("ДокументОснование") И ЗначениеЗаполнено(Параметры.ДокументОснование) Тогда
			Объект.Основание = Параметры.ДокументОснование;
			Документы.упПодтверждениеЗаявкиНаТС.ОбработкаЗаполненияПодтвержденияЗаявкиНаТС(Объект);
		КонецЕсли; 
	КонецЕсли;
	
	Если ВедетсяРаботаВРазныхЧасовыхПоясах И ЗначениеЗаполнено(Объект.ЧасовойПояс) Тогда
		ТолькоПросмотр = Не упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.Рейс));
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);

	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	
	
	ЗаблокироватьЭлементы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
	//	Если НЕ ЗначениеЗаполнено(Объект.Основание) Тогда
	//		ТекстСообщения = Нстр("ru = 'Подтверждение заявки на транспортное средство возможно создать только на основании документа ""Заявка на транспортное средство"" или ""Предложение перевозчика"".'");
	//		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	//		Возврат;
	//	КонецЕсли;
	//КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЭтаФорма.УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест  = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
		
	УстановитьЗаголовки();
	
	ПриСозданииПриЧтенииНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяСтатусЗаявкиНаТС") Тогда
		ОповеститьОбИзмененииСтатусаЗаявкиНаТС = Истина;
		ЗаявкаНаТС = ТекущийОбъект.ДополнительныеСвойства.ЗаявкаНаТС;
	КонецЕсли;
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		ОповеститьОбИзмененииСтатусаРейса = Истина;
	КонецЕсли;
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяПлановыйРасход") Тогда
		ОповеститьОбИзмененииПлановыхРасходовПоРейсу = Истина;
	КонецЕсли;
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство, Документы.упРейс.ПолучитьВремяНачалаРейса(ТекущийОбъект.Рейс));
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ЗаблокироватьЭлементы();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ОповеститьОбИзмененииСтатусаЗаявкиНаТС Тогда
		Оповестить("СменаСтатуса",ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаТС.Подтверждена"),ЗаявкаНаТС);	
		ОповеститьОбИзмененииСтатусаЗаявкиНаТС = Ложь;
	КонецЕсли;
	
	мсвСобытийРейса = Новый Массив;
	мсвСобытийРейса.Добавить("ОбновитьРаботы");
	мсвСобытийРейса.Добавить("ИзменилсяМаршрут");
		
	Если ОповеститьОбИзмененииСтатусаРейса Тогда
		мсвСобытийРейса.Добавить("СменаСтатуса");
		ОповеститьОбИзмененииСтатусаРейса = Ложь;
	КонецЕсли;
	
	Если ОповеститьОбИзмененииПлановыхРасходовПоРейсу Тогда
		мсвСобытийРейса.Добавить("ИзменилсяПлановыйРасход");
		ОповеститьОбИзмененииПлановыхРасходовПоРейсу = Ложь;
	КонецЕсли;
	
	мсвСобытийРейса.Добавить("ОбновитьРаботы");
	
	Если мсвСобытийРейса.Количество() > 0 Тогда
		События = СтрСоединить(мсвСобытийРейса, ";");
		Оповестить(События, ,Объект.Рейс);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФорма
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикПриИзменении(Элемент)
	Объект.КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Объект.Перевозчик);		
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	РейсПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	сткПараметры = Новый Структура();
	Если ЗначениеЗаполнено(Объект.ТипТС) Тогда
		сткОтбор = Новый Структура();
		сткОтбор.Вставить("ТипТС", Объект.ТипТС); 
		сткПараметры.Вставить("Отбор", сткОтбор);
	КонецЕсли; 
	ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ТипТС) Тогда
		СтандартнаяОбработка = Ложь;
		ЗначениеОтбора  = Новый Структура("ТипТС", Объект.ТипТС);
		ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда         
		ЕИ_Грузов = сткПредставления.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
		СимволРазделения = "х";
	КонецЕсли; 
	
	Если ТипЗнч(Объект.Основание) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда
		Элементы.Основание.Заголовок = НСтр("ru = 'Предложение'");
	ИначеЕсли ТипЗнч(Объект.Основание) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		Элементы.Основание.Заголовок = НСтр("ru = 'Заявка на ТС'");	
	КонецЕсли; 
	
КонецПроцедуры // УстановитьЗаголовки()

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.МаксимальнаяШирина * Объект.МаксимальнаяВысота * Объект.МаксимальнаяДлина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.Рейс));
	Если Не ЗначениеЗаполнено(Объект.Водитель1) Или Не ЗначениеЗаполнено(Объект.ТипТС) Тогда
		сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ТранспортноеСредство, "ТипТС, ОсновнойВодитель");
		Объект.ТипТС = сткДанные.ТипТС;
		Объект.Водитель1 = сткДанные.ОсновнойВодитель;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РейсПриИзмененииНаСервере()
	Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясОснования	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Основание, "ЧасовойПояс");
		Если НЕ ЧасовойПоясОснования = Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясОснования;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТипТСЗаявки(ЗаявкаТС)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаТС, "ТипТС");
КонецФункции

&НаСервере
Процедура ЗаблокироватьЭлементы()
 
	ТипТС = Неопределено;
	Если ТипЗнч(Объект.Основание) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		ТипТС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Основание, "ТипТС");
	ИначеЕсли ТипЗнч(Объект.Основание) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда	
		ТипТС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Основание, "ТипТС");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ТипТС) И ТипТС = Объект.ТипТС Тогда
		Элементы.ТипТС.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ТипТС.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

#КонецОбласти