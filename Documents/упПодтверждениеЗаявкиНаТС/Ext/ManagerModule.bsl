
#Область ПрограммныйИнтерфейс


// Заполнение реквизитов подтверждения заявки на ТС.
//
// Параметры:
//  ПодтверждениеОбъект - ДокументОбъект.упПодтверждениеЗаявкиНаТС. 
//
Процедура ОбработкаЗаполненияПодтвержденияЗаявкиНаТС(ПодтверждениеОбъект) Экспорт
	
	Основание = ПодтверждениеОбъект.Основание;
	Если Не ЗначениеЗаполнено(Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Основание);
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упЗаявкаНаТС.БеречьОтВлаги КАК БеречьОтВлаги,
		|	упЗаявкаНаТС.БеречьОтИзлучения КАК БеречьОтИзлучения,
		|	упЗаявкаНаТС.БеречьОтНагрева КАК БеречьОтНагрева,
		|	упЗаявкаНаТС.КлассОпасности КАК КлассОпасности,
		|	упЗаявкаНаТС.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	упЗаявкаНаТС.КоличествоМест КАК КоличествоМест,
		|	упЗаявкаНаТС.КонтрагентПеревозчика КАК КонтрагентПеревозчика,
		|	упЗаявкаНаТС.МаксимальнаяВысота КАК МаксимальнаяВысота,
		|	упЗаявкаНаТС.МаксимальнаяДлина КАК МаксимальнаяДлина,
		|	упЗаявкаНаТС.МаксимальнаяШирина КАК МаксимальнаяШирина,
		|	упЗаявкаНаТС.Масса КАК Масса,
		|	упЗаявкаНаТС.НачалоРейса КАК НачалоРейса,
		|	упЗаявкаНаТС.Объем КАК Объем,
		|	упЗаявкаНаТС.ОкончаниеРейса КАК ОкончаниеРейса,
		|	упЗаявкаНаТС.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	упЗаявкаНаТС.Перевозчик КАК Перевозчик,
		|	упЗаявкаНаТС.Рейс КАК Рейс,
		|	упЗаявкаНаТС.Соглашение КАК Соглашение,
		|	упЗаявкаНаТС.ТемпературныйРежим КАК ТемпературныйРежим,
		|	упЗаявкаНаТС.ТранспортноеСредство КАК ТранспортноеСредство,
		|	упЗаявкаНаТС.Хрупкий КАК Хрупкий,
		|	упЗаявкаНаТС.ЧасовойПояс КАК ЧасовойПояс,
		|	упЗаявкаНаТС.ЗапрещеноСкладироватьДругНаДруга КАК ЗапрещеноСкладироватьДругНаДруга
		|ИЗ
		|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
		|ГДЕ
		|	упЗаявкаНаТС.Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПодтверждениеОбъект.Основание) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда	
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упПредложениеПеревозчика.Водитель1 КАК Водитель1,
		|	упПредложениеПеревозчика.Водитель2 КАК Водитель2,
		|	упПредложениеПеревозчика.ВремяЗавершения КАК ОкончаниеРейса,
		|	упПредложениеПеревозчика.ВремяПодачи КАК НачалоРейса,
		|	упПредложениеПеревозчика.КонтрагентПеревозчика КАК КонтрагентПеревозчика,
		|	упПредложениеПеревозчика.Перевозчик КАК Перевозчик,
		|	упПредложениеПеревозчика.Соглашение КАК Соглашение,
		|	упПредложениеПеревозчика.ТранспортноеСредство КАК ТранспортноеСредство,
		|	упПредложениеПеревозчика.Экспедитор КАК Экспедитор,
		|	упПредложениеПеревозчика.ЧасовойПояс КАК ЧасовойПояс,
		|	упПредложениеПеревозчика.ЗаявкаНаТС.Рейс КАК Рейс
		|ИЗ
		|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
		|ГДЕ
		|	упПредложениеПеревозчика.Ссылка = &Ссылка";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ПодтверждениеОбъект, Выборка);
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Рейс");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти