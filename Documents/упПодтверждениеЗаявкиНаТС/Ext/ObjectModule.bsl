
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	СтатусРейса = Документы.упРейс.ПолучитьСтатус(Рейс);
	Если Не (СтатусРейса = Перечисления.упСтатусыРейса.Новый Или СтатусРейса = Перечисления.упСтатусыРейса.КПланированиюТС Или СтатусРейса = Перечисления.упСтатусыРейса.НазначеноТС) Тогда
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Документ ""%1"" находится в статусе ""%2"". Изменение подтверждения заявки на ТС запрещено.'"), Рейс, СтатусРейса);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,,,, Отказ);
		Возврат;
	КонецЕсли; 
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		// Заполняются таблицы расходов
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ДополнительныеСвойства.Вставить("ИзменилсяСтатусЗаявкиНаТС");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("РассчитыватьПлановыеРасходыВсего") Тогда
		// Рассчитать и записать итоговые расходы по рейсу
		упРейс.СформироватьДвижениеВсегоРасходыПоРейсу(Рейс, Отказ);
		Если НЕ Отказ Тогда
			ДополнительныеСвойства.Вставить("ИзменилсяПлановыйРасход");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	текЗаявкаНаТС = Неопределено;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭтоПодтверждениеПредложения = Ложь;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.упЗаявкаНаТС") Тогда
		текЗаявкаНаТС 	= Основание;
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.упПредложениеПеревозчика") Тогда
		текЗаявкаНаТС	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ЗаявкаНаТС");	
		ЭтоПодтверждениеПредложения = Истина;
	КонецЕсли;
	
	Если ЭтоПодтверждениеПредложения Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упПредложениеПеревозчика");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Основание);
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПеревозчикПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаТС");
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", текЗаявкаНаТС);
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	
	Если ЭтоПодтверждениеПредложения Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетРасходовНаТС") Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходыНаТС");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		КонецЕсли;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упВсегоРасходыПоРейсу");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	КонецЕсли;	
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта 	= Новый Структура("ТранспортноеСредство, Дата, Рейс, ДатаДокумента", ТранспортноеСредство, Дата, Рейс, Дата);	
	ПлановыеРасходы 	= Неопределено;
	
	Если ЭтоПодтверждениеПредложения Тогда
		ПлановыеРасходы =  упРаспределениеРасходов.ПлановыеРасходыПоПредложениюПеревозчика(Основание);	
		РеквизитыОбъекта.Вставить("ПлановыеРасходы", ПлановыеРасходы);
	КонецЕсли;
		
	текСтатусРейса = Документы.упРейс.ПолучитьСтатус(Рейс);										
	ДополнительныеСвойства.Вставить("Статус", 	Перечисления.упСтатусыРейса.НазначеноТС);
	
	ДополнительныеСвойства.Вставить("РейсПодтвержден", Ложь);
	Подтверждение = упРейс.ДокументПодтверждающийРейс(Рейс);
	Если ЗначениеЗаполнено(Подтверждение) И Подтверждение <> Ссылка Тогда
		ТекстСообщения = Нстр(СтрШаблон("ru = 'Рейс ""%1"" уже подтвержден документом ""%2""'", Рейс, Подтверждение));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли; 
	
	Если Не Отказ Тогда
		сткЭкипаж = Новый Структура();
		сткЭкипаж.Вставить("Перевозчик"           , Перевозчик);
		сткЭкипаж.Вставить("КонтрагентПеревозчика", КонтрагентПеревозчика);
		сткЭкипаж.Вставить("Соглашение"           , Соглашение);
		сткЭкипаж.Вставить("ТипТС"                , ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "ТипТС"));
		сткЭкипаж.Вставить("ТранспортноеСредство" , ТранспортноеСредство);
		сткЭкипаж.Вставить("Водитель1"            , Водитель1);
		сткЭкипаж.Вставить("Водитель2"            , Водитель2);
		сткЭкипаж.Вставить("Экспедитор"           , Экспедитор);
		ДополнительныеСвойства.Вставить("ИзмененЭкипаж", сткЭкипаж);
	КонецЕсли; 
	
	// Формируются данные перевозчика рейса
	упРейс.СформироватьДвижениеПеревозчикПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются плановые расходы по рейсу
	Если ЭтоПодтверждениеПредложения И НЕ ПлановыеРасходы = Неопределено Тогда
		упРейс.СформироватьПлановыеРасходыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);	
		Если НЕ Отказ Тогда
			ДополнительныеСвойства.Вставить("ИзменилсяПлановыйРасход");
		КонецЕсли;
	КонецЕсли;
			
	Если текСтатусРейса = Перечисления.упСтатусыРейса.КПланированиюТС Тогда
		ДополнительныеСвойства.Вставить("ЗаписыватьСтатус", 	Истина);
		ДополнительныеСвойства.Вставить("РассчитыватьСтатус", 	Ложь);
		ДополнительныеСвойства.Вставить("ДобавитьСтатус", 		Истина);
		РеквизитыОбъекта.Вставить("Рейс", Рейс);
		упРейс.СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Рейс, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
	Если НЕ текЗаявкаНаТС = Неопределено Тогда
		ДополнительныеСвойства.Вставить("ЗаявкаНаТС", 	текЗаявкаНаТС);
		ДополнительныеСвойства.Вставить("Рейс",			Рейс);
		упРейс.СформироватьДвиженияПодтвердитьЗаявкуНаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		Если НЕ Отказ Тогда
			ДополнительныеСвойства.Вставить("ИзменилсяСтатусЗаявкиНаТС");
		КонецЕсли;
	КонецЕсли;
	
	упРейс.СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	// Рассчитать и записать итоговые расходы по рейсу
	Если ЭтоПодтверждениеПредложения И НЕ ПлановыеРасходы = Неопределено Тогда
		упРейс.СформироватьДвижениеВсегоРасходыПоРейсу(Рейс, Отказ);	
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ЭтоПодтверждениеПредложения = ТипЗнч(Основание) = Тип("ДокументСсылка.упПредложениеПеревозчика");

	Если ЭтоПодтверждениеПредложения Тогда
		
		Движения.упРасходы.Прочитать();
		
		ДвиженияРасходы = Движения.упРасходы;
		
		Если ДвиженияРасходы.Количество()>0 Тогда
			ДополнительныеСвойства.Вставить("РассчитыватьПлановыеРасходыВсего");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 
	
#Область СлужебныеПроцедурыИФункции

#КонецОбласти  

#КонецЕсли
