#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		РеквизитыОбъекта = Новый Структура;	
		Если ЗначениеЗаполнено(Ссылка) Тогда
			РеквизитыОбъекта.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));	
		Иначе
			Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
				РеквизитыОбъекта.Вставить("Граница", '00010101');	
			Иначе
				РеквизитыОбъекта.Вставить("Граница", Дата);	
			КонецЕсли;
		КонецЕсли;                                  
		ДополнительныеСвойства.Вставить("РеквизитыОбъекта", РеквизитыОбъекта);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Реквизиты = ДополнительныеСвойства.РеквизитыОбъекта;
	
	ДополнительныеСвойства.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
	Документы.упПеремещениеМатериалов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиМатериалов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных	= ДополнительныеСвойства.ОстаткиМатериалов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Материал", "Материал");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад"   , "Склад");
	КонецЕсли; 
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	
	Реквизиты.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
	упУправлениеЗапасами.РассчитатьСуммыСписанияМатериалов(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ);	
	
	Реквизиты.Вставить("Дата", Дата);
	Реквизиты.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));
		
	// Формирование движений по регистру накопления "упОстаткиМатериалов"
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный ИЛИ Отказ Тогда
		упСервисныеФункции.УдалитьДвижения(Движения);
		упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
		Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
			Для каждого текСтрока Из ДополнительныеСвойства.ОстаткиМатериалов Цикл
				текСтрока.ВидДвижения = ВидДвиженияНакопления.Приход;
				текСтрока.Склад       = СкладПолучатель;
			КонецЦикла; 
		КонецЕсли;
		упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
		Движения.Записать();
	Иначе
		упКонтрольОстатков.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	КонецЕсли;
	
	
	// Контроль возникновения отрицательных остатков по складу
	Если Не ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО Тогда
		Реквизиты.Вставить("ГраницаИсключая", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		упУправлениеЗапасами.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата"       , Дата);
	Реквизиты.Вставить("Граница"    , Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));
	Реквизиты.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
	
	упКонтрольОстатков.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
