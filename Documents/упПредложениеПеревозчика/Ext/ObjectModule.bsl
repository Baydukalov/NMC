#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		ДополнительныеСвойства.Вставить("ИзменилсяСтатусЗаявкиНаТС");
		
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			ПроверкаСтатусаЗаявкиНаТС(Отказ);
		КонецЕсли;
				
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ЗаявкаНаТС);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упПредложениеПеревозчика");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаТС", ЗаявкаНаТС);
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПредложенияПеревозчика");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаТС", ЗаявкаНаТС);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	ПроверкаСтатусаЗаявкиНаТС(Отказ);
	
	Если Не Отказ Тогда
		ДвиженияСтатуса = Движения.упСтатусыЗаявокНаТС;
		ДвиженияСтатуса.Записывать = Истина;
		
		ДвижениеСтатуса = ДвиженияСтатуса.Добавить();
		ДвижениеСтатуса.Период = ТекущаяДатаСеанса();
		ДвижениеСтатуса.Регистратор = Ссылка;
		ДвижениеСтатуса.Документ = ЗаявкаНаТС;
		ДвижениеСтатуса.Статус = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение;
		
		ДвиженияПредложения = Движения.упПредложенияПеревозчика;
		ДвиженияПредложения.Записывать = Истина;
		
		ДвижениеПредложения = ДвиженияПредложения.Добавить();
		ДвижениеПредложения.Период = ТекущаяДатаСеанса();
		ДвижениеПредложения.Регистратор = Ссылка;
		ДвижениеПредложения.ЗаявкаНаТС  = ЗаявкаНаТС;
		ДвижениеПредложения.ПредложениеПеревозчика  = Ссылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Процедура заполнения табличной части "ПлановыеРасходы".
//
Процедура ЗаполнитьПлановыеРасходы(тбзСтатьиРасходов) Экспорт
	
	ПлановыеРасходы.Очистить();
			
	Для каждого текСтатьяРасходов Из тбзСтатьиРасходов Цикл
		
		текСтрока = ПлановыеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(текСтрока, текСтатьяРасходов);
		текСтрока.Валюта = Валюта;
		текСтрока.Сумма = 0;
		текСтрока.СуммаНДС = 0;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаСтатусаЗаявкиНаТС(Отказ)
	
	СтатусЗаявки = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ЗаявкаНаТС);
	
	Если СтатусЗаявки = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
		ТекстСообщения = Нстр("ru = 'Заявка (%1) уже подтверждена, запись невозможна'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ЗаявкаНаТС);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЗаявкаНаТС,,,Отказ);
	ИначеЕсли СтатусЗаявки = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение Тогда	
		ПредложениеПоЗаявкеНаТС = упРейс.ПредложениеПоЗаявкеНаТС(ЗаявкаНаТС);
		Если ПредложениеПоЗаявкеНаТС = Неопределено И НЕ ПредложениеПоЗаявкеНаТС = Ссылка Тогда
			ТекстСообщения = Нстр("ru = 'По заявке (%1) уже получено предложение, запись невозможна'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, ЗаявкаНаТС);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЗаявкаНаТС,,,Отказ);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти 

#КонецЕсли
