#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта();
		Если ВедетсяВалютныйУчет Тогда
			Объект.Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
			ЭтаФорма.ВалютаУпрУчета = Объект.Валюта;
		КонецЕсли;
		Если Параметры.Свойство("ЗаявкаНаТС") И ЗначениеЗаполнено(Параметры.ЗаявкаНаТС) Тогда
			Объект.ЗаявкаНаТС = Параметры.ЗаявкаНаТС;
			Документы.упЗаявкаНаТС.ОбработкаЗаполненияПредложенияПеревозчикаПоЗаявкеНаТС(Объект);
		КонецЕсли; 
	КонецЕсли; 
	
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект,Параметры,Ложь);
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.ЗаявкаНаТС.Рейс));
		
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
				
	Рейс = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ЗаявкаНаТС, "Рейс");
		
	Если ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяСтатусЗаявкиНаТС") Тогда
		ОповеститьОбИзмененииСтатусаЗаявкиНаТС = Истина;
	КонецЕсли;	
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство, Документы.упРейс.ПолучитьВремяНачалаРейса(ТекущийОбъект.ЗаявкаНаТС.Рейс));

	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ОповеститьОбИзмененииСтатусаЗаявкиНаТС Тогда
		Оповестить("СменаСтатуса",ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаТС.ПолученоПредложение"), Объект.ЗаявкаНаТС);	
		ОповеститьОбИзменении(Объект.ЗаявкаНаТС);
		ОповеститьОбИзмененииСтатусаЗаявкиНаТС = Ложь;
	КонецЕсли;
	
	ВзаимодействияКлиент.КонтактПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи,"упПредложениеПеревозчика");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ПеревозчикПриИзменении(Элемент)
	Объект.КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Объект.Перевозчик);		
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	сткПараметры = Новый Структура("Партнер", Объект.Перевозчик);
	Если ЗначениеЗаполнено(Объект.ТипТС) Тогда
		сткПараметры.Вставить("ТипТС", Объект.ТипТС);
	КонецЕсли; 
	ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбора", Новый Структура("Отбор", сткПараметры), Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= Объект.Валюта;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;

		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	мсвРейсы	= Новый Массив;
	мсвРейсы.Добавить(Рейс);
	мсвЗадания 	= СписокЗаданийПоРейсу(мсвРейсы);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
	сткПараметры 	= Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.ПлановыеРасходыЗадание);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	мсвРейсы	= Новый Массив;
	мсвРейсы.Добавить(Рейс);
	мсвЗадания 	= СписокЗаданийПоРейсу(мсвРейсы);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
    ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		ТекущаяСтрока.СтавкаНДС = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "СтавкаНДС");
		ПересчитатьСтроку(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаНаТСПриИзменении(Элемент)
	ЗаявкаНаТСПриИзмененииНаСервере();
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПлановыеРасходы(Команда)
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьПлановыеРасходыКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлановыеРасходыПоЗаявкеНаТС(Команда)
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьПлановыеРасходыПоЗаявкеНаТСКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыеРасходы(Команда) 
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		РассчитатьПлановыеРасходыНаСервере();
		РассчитатьИтоговуюСуммуРасходы();
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Объект.Соглашение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыеРасходыСРасшифровкой(Команда)
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		мсвПриемников = РассчитатьПлановыеРасходыСРасшифровкойНаСервере();
		РассчитатьИтоговуюСуммуРасходы();
		Для каждого Приемник Из мсвПриемников Цикл
			Приемник.Приемник.Показать("Расшифровка расчетов");	
		КонецЦикла;
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Объект.Соглашение");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаКлиенте
// Окрываем форму вопроса пользователю и перезаполняем плановые расходы.
//
Процедура ЗаполнитьПлановыеРасходыКлиент(Результат, ДополнительныеПараметры) Экспорт
	Если Объект.ПлановыеРасходы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполненииПлановыхРасходов", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Плановые расходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПлановыеРасходыНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Окрываем форму вопроса пользователю и перезаполняем плановые расходы.
//
Процедура ЗаполнитьПлановыеРасходыПоЗаявкеНаТСКлиент(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Объект.ЗаявкаНаТС) Тогда
		Если Объект.ПлановыеРасходы.Количество() > 0 Тогда
			ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполненииПлановыхРасходовПоЗаявкеНаТС", ЭтаФорма);
			ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Плановые расходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		Иначе
			ЗаполнитьПлановыеРасходыПоЗаявкеНаТСНаСервере();
		КонецЕсли;
	Иначе	
		ТекстСообщения = Нстр("ru = 'Поле ""Заявка на ТС"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ЗаявкаНаТС","Объект");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыеРасходыНаСервере()
	
	тбзРасходы = упРаспределениеРасходов.ПолучитьСписокСтатейРасходов(Объект.Ссылка, Объект.ТипТС, Объект.Соглашение);
	
	Если НЕ тбзРасходы = Неопределено Тогда
		текОбъект = РеквизитФормыВЗначение("Объект");
		текОбъект.ЗаполнитьПлановыеРасходы(тбзРасходы);
		ЗначениеВРеквизитФормы(текОбъект,"Объект");
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыеРасходыПоЗаявкеНаТСНаСервере()
	
	тбзРасходы = упРаспределениеРасходов.ПолучитьСписокСтатейРасходовПоЗаявкеНаТС(Объект.ЗаявкаНаТС);
	
	Если НЕ тбзРасходы = Неопределено Тогда
		текОбъект = РеквизитФормыВЗначение("Объект");
		текОбъект.ЗаполнитьПлановыеРасходы(тбзРасходы);
		ЗначениеВРеквизитФормы(текОбъект,"Объект");
	КонецЕсли;
				
КонецПроцедуры

&НаКлиенте
// Процедура завершения после ответа пользователя.
//
Процедура ОбработатьВопросОПерезаполненииПлановыхРасходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПлановыеРасходыНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура завершения после ответа пользователя.
//
Процедура ОбработатьВопросОПерезаполненииПлановыхРасходовПоЗаявкеНаТС(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПлановыеРасходыПоЗаявкеНаТСНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РассчитатьПлановыеРасходыНаСервере()

	ВходныеПараметры = Новый Структура();  
	упТарификацияВызовСервера.Рассчитать(Объект, ВходныеПараметры);
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеРасходы","");
КонецФункции

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ПлановыеРасходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаСервереБезКонтекста
Функция СписокЗаданийПоРейсу(Рейс)
	Возврат упРейс.СписокЗаданийПоРейсу(Рейс);
КонецФункции

&НаКлиенте
Процедура ПлановыеРасходыЗаданиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	мсвРейсы	= Новый Массив;
	мсвРейсы.Добавить(Рейс);
	мсвЗадания 	= СписокЗаданийПоРейсу(мсвРейсы);
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
    ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ТранспортноеСредство, "ОсновнойВодитель, ТипТС");
		Объект.Водитель1 = сткРеквизиты.ОсновнойВодитель; 
		Объект.ТипТС     = сткРеквизиты.ТипТС;
	КонецЕсли; 
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.ЗаявкаНаТС.Рейс));

КонецПроцедуры

&НаСервере
Функция РассчитатьПлановыеРасходыСРасшифровкойНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;
КонецФункции

&НаСервере
Процедура ЗаявкаНаТСПриИзмененииНаСервере()
	
	Документы.упЗаявкаНаТС.ОбработкаЗаполненияПредложенияПеревозчикаПоЗаявкеНаТС(Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

#КонецОбласти
