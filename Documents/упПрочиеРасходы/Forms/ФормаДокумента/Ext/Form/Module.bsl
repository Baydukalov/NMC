#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли;
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// Новый документ.
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда 
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;		
		Объект.ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
		Объект.Водитель				= Справочники.упСотрудники.ПустаяСсылка();
		Объект.ВидПеревозки			= Справочники.упВидыПеревозок.ПустаяСсылка();
		Объект.Рейсы.Очистить();
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если ИспользуетсяУчетПодразделений
					И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
				Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимость();
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.НачалоПериодаРаспределения);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	Если ЭтаФорма.ВедетсяВалютныйУчет И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		УстановитьВалютуУчета();
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки	
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство, ТекущийОбъект.НачалоПериодаРаспределения);
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)	
	
	Если ЗначениеЗаполнено(Объект.Партнер) Тогда
		
		Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Объект.Контрагент = ПолучитьОсновногоКонтрагента(Объект.Партнер);		 
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.Соглашение) Тогда
			Объект.Соглашение = ПолучитьСоглашениеПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РейсыРейсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//текОтбор = Новый Структура("Организация,Перевозчик", Объект.Организация, Объект.Партнер);
	текОтбор = Новый Структура("Организация", Объект.Организация);
	
	ОткрытьФорму("Документ.упРейс.ФормаВыбора", текОтбор, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПрочихРасходовПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		ТекущаяСтрока.Количество 	= 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РасходыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		сткРеквизитыСтатьиРасходов = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
		ТекущаяСтрока.СтавкаНДС 	          = сткРеквизитыСтатьиРасходов.СтавкаНДС;
		ТекущаяСтрока.ПравилоРаспределения = сткРеквизитыСтатьиРасходов.ПравилаРаспределения;
		ПересчитатьСтроку(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		УстановитьВалютуУчета();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РасходыСтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Объект.ВидПрочихРасходов = ПредопределенноеЗначение("Перечисление.упВидыПрочихРасходов.ОбщиеЗаПериод")
		ИЛИ Объект.ВидПрочихРасходов = ПредопределенноеЗначение("Перечисление.упВидыПрочихРасходов.ПоРейсам")
		ИЛИ Объект.ВидПрочихРасходов = ПредопределенноеЗначение("Перечисление.упВидыПрочихРасходов.ПустаяСсылка") Тогда
		сткОтбор = Новый Структура("ВариантРасходов", ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Прямые"));
		сткПараметры = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("Справочник.упСтатьиДоходовРасходов.Форма.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Водитель) Тогда
		Объект.НаправлениеДеятельности = НаправлениеДеятельностиПоСотруднику(Объект.Водитель);
	Иначе
	     Объект.НаправлениеДеятельности = ПредопределенноеЗначение("Справочник.упНаправленияДеятельности.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьБезналичнаяОплата(Команда)
	ФормаОплаты = ПредопределенноеЗначение("Перечисление.упДоступныеФормыОплаты.Безналичная");
	ЗаполнитьФормуОплаты(ФормаОплаты);	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаличнаяОплата(Команда)
	ФормаОплаты = ПредопределенноеЗначение("Перечисление.упДоступныеФормыОплаты.Наличная");
	ЗаполнитьФормуОплаты(ФормаОплаты);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаСервере
Функция ПолучитьСоглашениеПоУмолчанию()

	Возврат Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(Объект.Партнер, Объект.Организация);

КонецФункции // ПолучитьСоглашениеПоУмолчанию()

&НаСервере
Процедура УстановитьВидимость()
	
	Элементы.ТранспортноеСредство.Видимость = Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоТранспортномуСредству;
	Элементы.Водитель.Видимость             = Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВодителюЭкспедитору;
	Элементы.ВидПеревозки.Видимость         = Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВидуПеревозки;
	Элементы.ГруппаРейсы.Видимость          = Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоРейсам;
	Элементы.ГруппаПериодРаспределения.Видимость = НЕ Объект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоРейсам;

КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.Расходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "Расходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Расходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()    	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.НачалоПериодаРаспределения); 
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаправлениеДеятельностиПоСотруднику(Сотрудник)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "НаправлениеДеятельности");
КонецФункции

&НаКлиенте
Процедура ЗаполнитьФормуОплаты(ФормаОплаты)
	Для каждого СтрокаТаблицы Из Объект.Расходы Цикл
		СтрокаТаблицы["ФормаОплаты"] = ФормаОплаты;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьФормуОплаты()

#КонецОбласти
