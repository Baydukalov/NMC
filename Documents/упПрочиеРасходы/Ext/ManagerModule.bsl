
#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...];
//       где ИмяРеквизита - имя реквизита объекта, 
//		ИмяЭлементаФормы - имя элемента формы, связанного с реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Партнер");
	БлокируемыеРеквизиты.Добавить("Организация");
	БлокируемыеРеквизиты.Добавить("ВидПеревозки");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки данных из файла
//
// Параметры:
//     Параметры - Структура - Список параметров. Поля:
//         * Заголовок - Строка - Заголовок окна.
//         * ОбязательныеКолонки - Массив - Список имен колонок обязательных для заполнения.
//         * ТипДанныхКолонки - Соответствие, Ключ - Имя колонки, Значение - Описание типа данных.
//
Процедура ОпределитьПараметрыЗагрузкиДанныхИзФайла(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упУчетнаяПолитикаСрезПоследних.ОплатаПлатонСтатьяРасходов
	|ИЗ
	|	РегистрСведений.упУчетнаяПолитика.СрезПоследних КАК упУчетнаяПолитикаСрезПоследних";
	
	Выборка	= Запрос.Выполнить().Выбрать();
	
	СтатьяРасходов = Неопределено;
	
	Если Выборка.Следующий() Тогда
		СтатьяРасходов = Выборка.ОплатаПлатонСтатьяРасходов;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтатьяРасходов) Тогда
		ТекстСообщения = НСтр("ru = 'Для списания расходов, полученных из системы ""Платон"", необходимо в учетной политике задать статью расходов.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
		
КонецПроцедуры

// Производит сопоставление загружаемых данных с данными в ИБ.
//
// Параметры:
//   ЗагружаемыеДанные - ТаблицаЗначений - таблица значений с загружаемыми данными:
//     * СопоставленныйОбъект - СправочникСсылка - Ссылка на сопоставленный объект. Заполняется внутри процедуры.
//     * <другие колонки>     - Произвольный - Состав колонок соответствует макету "ЗагрузкаИзФайла".
//
Процедура СопоставитьЗагружаемыеДанныеИзФайла(ЗагружаемыеДанные) Экспорт

	тбзДанныеДляСопоставления = Новый ТаблицаЗначений;
	ТипУникальногоНомера = Метаданные.Документы.упОперацияСистемыПлатон.Реквизиты.УникальныйНомер.Тип;
	тбзДанныеДляСопоставления.Колонки.Добавить("УникальныйНомер", ТипУникальногоНомера);

	Для каждого Строка Из ЗагружаемыеДанные Цикл
		НоваяСтрока = тбзДанныеДляСопоставления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;																	 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзДанныеДляСопоставления.УникальныйНомер
	               |ПОМЕСТИТЬ втДанныеДляСопоставления
	               |ИЗ
	               |	&тбзДанныеДляСопоставления КАК тбзДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упРасходы.Регистратор КАК Ссылка,
	               |	втДанныеДляСопоставления.УникальныйНомер
	               |ИЗ
	               |	втДанныеДляСопоставления КАК втДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упРасходы КАК упРасходы
	               |		ПО втДанныеДляСопоставления.УникальныйНомер = упРасходы.АналитическийРазрез.Наименование";
	Запрос.УстановитьПараметр("тбзДанныеДляСопоставления", тбзДанныеДляСопоставления);
	Выборка	= Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Строки = ЗагружаемыеДанные.НайтиСтроки(Новый Структура("УникальныйНомер",Выборка.УникальныйНомер));
		Для каждого Строка Из Строки Цикл
			Строка.ОбъектСопоставления = Выборка.Ссылка;
		КонецЦикла;
	КонецЦикла;
				
КонецПроцедуры

// Загрузка данных из файла.
//
// Параметры:
//   ЗагружаемыеДанные - ТаблицаЗначений с колонками:
//     * ОбъектСопоставления         - СправочникСсылка - Ссылка на сопоставленный объект.
//     * РезультатСопоставленияСтроки - Строка       - Cтатусом загрузки, возможны варианты: Создан, Обновлен, Пропущен.
//     * ОписаниеОшибки               - Строка       - расшифровка ошибки загрузки данных.
//     * Идентификатор                - Число        - Уникальный номер строки.
//     * <другие колонки>             - Произвольный - Строки за загружаемого файла в соответствие с макетом.
// ПараметрыЗагрузки                  - Структура    - Параметры загрузки.
//     * СоздаватьНовые               - Булево       - Требуется ли создавать новые элементы справочника.
//     * ОбновлятьСуществующие        - Булево       - Требуется ли обновлять элементы справочника.
// Отказ                              - Булево       - Отмена загрузки.
//
Процедура ЗагрузитьИзФайла(ЗагружаемыеДанные, ПараметрыЗагрузки, Отказ) Экспорт
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	тбзЗагружаемыеДанные 	= Новый ТаблицаЗначений();
	тбзЗагружаемыеДанные.Колонки.Добавить("Идентификатор", 			Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный)));  
	тбзЗагружаемыеДанные.Колонки.Добавить("ТранспортноеСредство", 	Новый ОписаниеТипов("Строка", ,Новый КвалификаторыСтроки(30, ДопустимаяДлина.Переменная)));
	тбзЗагружаемыеДанные.Колонки.Добавить("УникальныйНомер",		Новый ОписаниеТипов("Строка", ,Новый КвалификаторыСтроки(15, ДопустимаяДлина.Переменная)));
	тбзЗагружаемыеДанные.Колонки.Добавить("ДатаНачалаДвижения", 	Новый ОписаниеТипов("Дата", , ,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));															
	тбзЗагружаемыеДанные.Колонки.Добавить("ДатаОкончанияДвижения",	Новый ОписаниеТипов("Дата", , ,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));																															
	
	Для каждого Строка Из ЗагружаемыеДанные Цикл
		НоваяСтрока = тбзЗагружаемыеДанные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ДатаНачалаДвижения 		= ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(Строка.ДатаНачалаДвижения));
		НоваяСтрока.ДатаОкончанияДвижения 	= ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(Строка.ДатаОкончанияДвижения));
	КонецЦикла;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбЗагружаемыеДанные.Идентификатор,
	               |	тбЗагружаемыеДанные.ТранспортноеСредство,
	               |	тбЗагружаемыеДанные.УникальныйНомер,
	               |	тбЗагружаемыеДанные.ДатаНачалаДвижения,
	               |	тбЗагружаемыеДанные.ДатаОкончанияДвижения
	               |ПОМЕСТИТЬ втЗагружаемыеДанные
	               |ИЗ
	               |	&тбЗагружаемыеДанные КАК тбЗагружаемыеДанные
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
	               |	упТранспортныеСредства.РегистрационныйНомер,
	               |	втЗагружаемыеДанные.ДатаНачалаДвижения,
	               |	втЗагружаемыеДанные.ДатаОкончанияДвижения,
	               |	втЗагружаемыеДанные.УникальныйНомер,
	               |	втЗагружаемыеДанные.Идентификатор
	               |ПОМЕСТИТЬ втТранспортныеСредства
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗагружаемыеДанные КАК втЗагружаемыеДанные
	               |		ПО упТранспортныеСредства.РегистрационныйНомер = втЗагружаемыеДанные.ТранспортноеСредство
	               |			И (НЕ упТранспортныеСредства.РегистрационныйНомер = """")
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТранспортныеСредства.ТранспортноеСредство,
	               |	втТранспортныеСредства.УникальныйНомер,
	               |	втТранспортныеСредства.Идентификатор,
	               |	упРейс.Ссылка КАК Рейс,
	               |	упРейс.Организация,
	               |	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
	               |	упПеревозчикПоРейсуСрезПоследних.Соглашение,
	               |	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика,
	               |	ЕСТЬNULL(упФактическиеПараметрыРейса.НачалоВыполнения, упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения) КАК НачалоВыполнения,
	               |	ЕСТЬNULL(упФактическиеПараметрыРейса.ОкончаниеВыполнения, упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения) КАК ОкончаниеВыполнения
	               |ПОМЕСТИТЬ втРейсы
	               |ИЗ
	               |	втТранспортныеСредства КАК втТранспортныеСредства
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	               |		ПО втТранспортныеСредства.ТранспортноеСредство = упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	               |		ПО (упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |		ПО (упСтатусыРейсовСрезПоследних.Документ = упРейс.Ссылка)
	               |			И (упСтатусыРейсовСрезПоследних.Статус В (&СтатусыРейсов))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	               |		ПО (упФактическиеПараметрыРейса.Рейс = упРейс.Ссылка)
	               |			И втТранспортныеСредства.ДатаНачалаДвижения >= упФактическиеПараметрыРейса.НачалоВыполнения
	               |			И втТранспортныеСредства.ДатаНачалаДвижения <= упФактическиеПараметрыРейса.ОкончаниеВыполнения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	               |		ПО (упПлановыеПараметрыРейса.Рейс = упРейс.Ссылка)
	               |			И втТранспортныеСредства.ДатаНачалаДвижения >= упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения
	               |			И втТранспортныеСредства.ДатаНачалаДвижения <= упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения
	               |ГДЕ
	               |	НЕ упФактическиеПараметрыРейса.НачалоВыполнения ЕСТЬ NULL
				|		ИЛИ НЕ упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения ЕСТЬ NULL
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(упОперацияСистемыПлатон.Ссылка, ЗНАЧЕНИЕ(Документ.упОперацияСистемыПлатон.ПустаяСсылка)) КАК ОперацияСистемыПлатон,
	               |	ЕСТЬNULL(упАналитическиеРазрезы.Ссылка, ЗНАЧЕНИЕ(Справочник.упАналитическиеРазрезы.ПустаяСсылка)) КАК АналитическийРазрез,
	               |	ЕСТЬNULL(втТранспортныеСредства.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	               |	втЗагружаемыеДанные.УникальныйНомер КАК УникальныйНомер,
	               |	втЗагружаемыеДанные.Идентификатор,
	               |	ЕСТЬNULL(втРейсы.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	               |	ЕСТЬNULL(втРейсы.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)) КАК Перевозчик,
	               |	ЕСТЬNULL(втРейсы.Соглашение, ЗНАЧЕНИЕ(Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка)) КАК Соглашение,
	               |	ЕСТЬNULL(втРейсы.КонтрагентПеревозчика, ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)) КАК КонтрагентПеревозчика,
	               |	ЕСТЬNULL(втРейсы.НачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоВыполнения,
	               |	ЕСТЬNULL(втРейсы.ОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеВыполнения,
	               |	ЕСТЬNULL(втРейсы.Рейс, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)) КАК Рейс
	               |ИЗ
	               |	втЗагружаемыеДанные КАК втЗагружаемыеДанные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАналитическиеРазрезы КАК упАналитическиеРазрезы
	               |		ПО втЗагружаемыеДанные.УникальныйНомер = упАналитическиеРазрезы.Наименование
	               |			И (упАналитическиеРазрезы.ОперацияСистемыПлатон)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упОперацияСистемыПлатон КАК упОперацияСистемыПлатон
	               |		ПО втЗагружаемыеДанные.УникальныйНомер = упОперацияСистемыПлатон.УникальныйНомер
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втТранспортныеСредства КАК втТранспортныеСредства
	               |			ЛЕВОЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	               |			ПО втТранспортныеСредства.Идентификатор = втРейсы.Идентификатор
	               |				И втТранспортныеСредства.ТранспортноеСредство = втРейсы.ТранспортноеСредство
	               |		ПО втЗагружаемыеДанные.Идентификатор = втТранспортныеСредства.Идентификатор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упУчетнаяПолитикаСрезПоследних.ОплатаПлатонСтатьяРасходов
	               |ИЗ
	               |	РегистрСведений.упУчетнаяПолитика.СрезПоследних КАК упУчетнаяПолитикаСрезПоследних";
	Запрос.УстановитьПараметр("тбЗагружаемыеДанные", тбзЗагружаемыеДанные);
	
	СтатусыРейсов = Новый Массив;
	СтатусыРейсов.Добавить(Перечисления.упСтатусыРейса.КВыполнению);
	СтатусыРейсов.Добавить(Перечисления.упСтатусыРейса.Выполняется);
	СтатусыРейсов.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);
	СтатусыРейсов.Добавить(Перечисления.упСтатусыРейса.Выполнен);
	СтатусыРейсов.Добавить(Перечисления.упСтатусыРейса.Завершен);
	Запрос.УстановитьПараметр("СтатусыРейсов", 		 СтатусыРейсов);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексПараметрыРейсаРезультатЗапроса 	= 3;
	ИндексУчетнаяПолитикаРезультатЗапроса 	= 4;
	
	Выборка = РезультатЗапроса[ИндексУчетнаяПолитикаРезультатЗапроса].Выбрать();
	Если Выборка.Следующий() Тогда
		СтатьяРасходов = Выборка.ОплатаПлатонСтатьяРасходов;
	КонецЕсли;

	тбзРезультатыЗапроса = РезультатЗапроса[ИндексПараметрыРейсаРезультатЗапроса].Выгрузить();
		
	Для каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл
		
		Попытка
				
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОбъектСопоставления) Тогда 
				Если ПараметрыЗагрузки.СоздаватьНовые Тогда 
					НачатьТранзакцию();
					ДокументОбъект 					 = Документы.упПрочиеРасходы.СоздатьДокумент();
					ДокументОбъект.Дата 			 = ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(СтрокаТаблицы.ДатаСписания));
					ДокументОбъект.ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоРейсам;
					
					СтрокаТаблицы.ОбъектСопоставления = ДокументОбъект;
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Создан";
				Иначе
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
					Продолжить;
				КонецЕсли;
			Иначе
				Если НЕ ПараметрыЗагрузки.ОбновлятьСуществующие Тогда 
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
					Продолжить;
				КонецЕсли;
				
				НачатьТранзакцию();
							
				ДокументОбъект = СтрокаТаблицы.ОбъектСопоставления.ПолучитьОбъект();
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Обновлен";
				Если ДокументОбъект = Неопределено Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Документа с уникальным номером %1 не существует.'"), СтрокаТаблицы.УникальныйНомер);
					СтрокаТаблицы.ОписаниеОшибки = ТекстСообщения;
					ВызватьИсключение ТекстСообщения;
				КонецЕсли;
				ДокументОбъект.Расходы.Очистить();		
			КонецЕсли;
						
			СтрокиДанныеПоРейсам	= тбзРезультатыЗапроса.НайтиСтроки(Новый Структура("Идентификатор",СтрокаТаблицы.Идентификатор));
			
			СтрокаДанныеПоРейсу = Неопределено;
			Если СтрокиДанныеПоРейсам.Количество() > 0 Тогда
				СтрокаДанныеПоРейсу = СтрокиДанныеПоРейсам[0];	
			КонецЕсли;
				
			// Заполнение реквизитов документа.
			Если НЕ ЗначениеЗаполнено(СтрокаДанныеПоРейсу) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не найдена строка с идентификатором %1.'"), СтрокаТаблицы.Идентификатор);
				СтрокаТаблицы.ОписаниеОшибки = ТекстСообщения;
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
			ТранспортноеСредство = СтрокаДанныеПоРейсу.ТранспортноеСредство;
			Если НЕ ЗначениеЗаполнено(ТранспортноеСредство) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не найдено транспортное средство с рег. номером %1. Уникальный номер операции %2.'"), СтрокаТаблицы.ТранспортноеСредство, СтрокаТаблицы.УникальныйНомер);
				СтрокаТаблицы.ОписаниеОшибки = ТекстСообщения;
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;	
		
			Рейс = СтрокаДанныеПоРейсу.Рейс;
			Если ЗначениеЗаполнено(Рейс) Тогда
				ДокументОбъект.Организация			= СтрокаДанныеПоРейсу.Организация;
				ДокументОбъект.Партнер				= СтрокаДанныеПоРейсу.Перевозчик;
				ДокументОбъект.Контрагент			= СтрокаДанныеПоРейсу.КонтрагентПеревозчика;
				ДокументОбъект.Соглашение			= СтрокаДанныеПоРейсу.Соглашение;
				НоваяСтрока 						= ДокументОбъект.Рейсы.Добавить();
				НоваяСтрока.Рейс					= Рейс;
			КонецЕсли;
			
			// Документ "упОперацияСистемыПлатон"
			
			ОперацияСистемыПлатонСсылка = СтрокаДанныеПоРейсу.ОперацияСистемыПлатон;
			
			ОперацияСистемыПлатонДокументОбъект = Неопределено;
			
			Если НЕ ЗначениеЗаполнено(ОперацияСистемыПлатонСсылка) Тогда
				
				Если ПараметрыЗагрузки.СоздаватьНовые Тогда 
					ОперацияСистемыПлатонДокументОбъект = Документы.упОперацияСистемыПлатон.СоздатьДокумент();
					ОперацияСистемыПлатонДокументОбъект.Дата 			= упСервисныеФункцииКлиентСервер.СтрокаВДату(СтрокаТаблицы.ДатаНачалаДвижения);
					ОперацияСистемыПлатонДокументОбъект.УникальныйНомер	= Сокрлп(СтрокаТаблицы.УникальныйНомер);
				Иначе
					Продолжить;
				КонецЕсли;
			Иначе
				Если ПараметрыЗагрузки.ОбновлятьСуществующие Тогда 
					ОперацияСистемыПлатонДокументОбъект = ОперацияСистемыПлатонСсылка.ПолучитьОбъект();
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ОперацияСистемыПлатонДокументОбъект = Неопределено Тогда
				ОперацияСистемыПлатонДокументОбъект.ТранспортноеСредство	= ТранспортноеСредство;
				
				// Даты начала и окончания движения.
				ДатаНачалаДвижения 		= ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(СтрокаТаблицы.ДатаНачалаДвижения));
				ДатаОкончанияДвижения	= ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(СтрокаТаблицы.ДатаОкончанияДвижения));
				ОперацияСистемыПлатонДокументОбъект.ДатаНачалаДвижения 		= ДатаНачалаДвижения;
				ОперацияСистемыПлатонДокументОбъект.ДатаОкончанияДвижения 	= ДатаОкончанияДвижения;
				
				// Номер бортового устройства.
				ОперацияСистемыПлатонДокументОбъект.НомерБортовогоУстройства = Сокрлп(СтрокаТаблицы.НомерБортовогоУстройства);
				
				// Путь, пройденный ТС по автомобильным дорогам общего пользования федерального значения (км).
				ОписаниеТипаРасстояние = Метаданные.ОпределяемыеТипы.Расстояние.Тип;
				ОперацияСистемыПлатонДокументОбъект.Расстояние = ОписаниеТипаРасстояние.ПривестиЗначение(СтрокаТаблицы.Расстояние);
				
				// Наименование операции.
				ОперацияСистемыПлатонДокументОбъект.НаименованиеОперации = Сокрлп(СтрокаТаблицы.Операция);
				
				Если ОперацияСистемыПлатонДокументОбъект.ПроверитьЗаполнение() Тогда
					Попытка
						ОперацияСистемыПлатонДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);	
					Исключение
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось записать документ ""Операция системы ""Платон"""" с уникальным номером %1.'"), СтрокаТаблицы.УникальныйНомер);
						СтрокаТаблицы.ОписаниеОшибки = ТекстСообщения;
						ВызватьИсключение ТекстСообщения;
					КонецПопытки;
				Иначе
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Документ ""Операция системы ""Платон"""" с уникальным номером %1 не прошел проверку.'"), СтрокаТаблицы.УникальныйНомер);
					СтрокаТаблицы.ОписаниеОшибки = ТекстСообщения;
					ВызватьИсключение ТекстСообщения;
				КонецЕсли;
			КонецЕсли;
			
			АналитическийРазрез	= СтрокаДанныеПоРейсу.АналитическийРазрез;
			Если НЕ ЗначениеЗаполнено(АналитическийРазрез) Тогда
				
				АналитическийРазрезСправочникОбъект = Справочники.упАналитическиеРазрезы.СоздатьЭлемент();
				АналитическийРазрезСправочникОбъект.Наименование = Сокрлп(СтрокаТаблицы.УникальныйНомер);
				АналитическийРазрезСправочникОбъект.ОперацияСистемыПлатон = Истина;
				
				Попытка
					АналитическийРазрезСправочникОбъект.Записать();	
					АналитическийРазрез = АналитическийРазрезСправочникОбъект.Ссылка;
				Исключение
   					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось записать аналитический разрез с уникальным номером %1.'"), СтрокаТаблицы.УникальныйНомер);
					СтрокаТаблицы.ОписаниеОшибки = ТекстСообщения;
					ВызватьИсключение ТекстСообщения;
				КонецПопытки;
				
            КонецЕсли;
							
			// Расходы
			НоваяСтрокаРасходы  = ДокументОбъект.Расходы.Добавить();
			НоваяСтрокаРасходы.СтатьяРасходов = СтатьяРасходов;
			
			сткРеквизитыСтатьиРасходов = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
			НоваяСтрокаРасходы.АналитическийРазрез		= АналитическийРазрез;
			НоваяСтрокаРасходы.СтавкаНДС 				= сткРеквизитыСтатьиРасходов.СтавкаНДС;
			НоваяСтрокаРасходы.ПравилоРаспределения 	= сткРеквизитыСтатьиРасходов.ПравилаРаспределения;
			ОписаниеТипаСумма = Метаданные.ОпределяемыеТипы.Расстояние.Тип;
			Если ВедетсяВалютныйУчет Тогда
				НоваяСтрокаРасходы.Валюта					= упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(ДокументОбъект, СтрокаДанныеПоРейсу.Соглашение); 	
			КонецЕсли;	
			НоваяСтрокаРасходы.Цена						= ОписаниеТипаСумма.ПривестиЗначение(СтрокаТаблицы.НачисленоПоТС);
			НоваяСтрокаРасходы.Количество				= 1;
			упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрокаРасходы,"Цена");
					
			Если ЗначениеЗаполнено(Рейс) Тогда
				Если ДокументОбъект.ПроверитьЗаполнение() Тогда
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				Иначе
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
				КонецЕсли;	
			Иначе
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;	
				
			СтрокаТаблицы.ОбъектСопоставления = ДокументОбъект.Ссылка;
			ЗафиксироватьТранзакцию();
						
		Исключение
			
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Загрузка документов ОперацияСистемыПлатон", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОперацияСистемыПлатон,, ТекстОшибки);
			
			ОтменитьТранзакцию();
			
			СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОписаниеОшибки) Тогда
				СтрокаТаблицы.ОписаниеОшибки = НСтр("ru = 'Невозможна запись из-за некорректности данных'");	
			КонецЕсли;
			
		КонецПопытки;
	КонецЦикла;
		
КонецПроцедуры

Функция ПреобразоватьМосковскоеВремяВМестное(МСКДатаВремя)
	Универсальная = УниверсальноеВремя(МСКДатаВремя, "GMT+03:00");
	Возврат МестноеВремя(Универсальная, ЧасовойПоясСеанса());
КонецФункции

#КонецОбласти 