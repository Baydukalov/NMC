#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		
		// Заполняются таблицы расходов, формируются аналитические разрезы,
		// подсчитываются итоговые суммы в валюте управленческого учета.
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);	

	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	тбзРейсы = РейсыДляРаспределения(); 
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходы");
	ЭлементБлокировки.ИсточникДанных = тбзРейсы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Рейс", "Рейс");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата", ?(ЗначениеЗаполнено(НачалоПериодаРаспределения), НачалоПериодаРаспределения, Дата));
	РеквизитыОбъекта.Вставить("СпособВводаРасходов", Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам);
	РеквизитыОбъекта.Вставить("Рейсы", тбзРейсы);
	РеквизитыОбъекта.Вставить("Организация", Организация);
	РеквизитыОбъекта.Вставить("Подразделение", Подразделение);
	РеквизитыОбъекта.Вставить("ВидПрочихРасходов", ВидПрочихРасходов);
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", ТранспортноеСредство);
	РеквизитыОбъекта.Вставить("НаправлениеДеятельности", НаправлениеДеятельности);
		
	// Формируются фактические прямые расходы.
	упРаспределениеРасходов.СформироватьРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	
КонецПроцедуры
	
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ОбщиеЗаПериод Тогда 
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ТранспортноеСредство");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Водитель");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидПеревозки");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы.Рейс");
	ИначеЕсли ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВидуПеревозки Тогда 
		ПроверяемыеРеквизиты.Добавить("ВидПеревозки");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ТранспортноеСредство");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Водитель");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы.Рейс");
	ИначеЕсли ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВодителюЭкспедитору Тогда 
		ПроверяемыеРеквизиты.Добавить("Водитель");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ТранспортноеСредство");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидПеревозки");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы.Рейс");
	ИначеЕсли ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоРейсам Тогда 
		ПроверяемыеРеквизиты.Добавить("Рейсы");
		ПроверяемыеРеквизиты.Добавить("Рейсы.Рейс");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "НачалоПериодаРаспределения");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ОкончаниеПериодаРаспределения");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Водитель");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидПеревозки");
	ИначеЕсли ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоТранспортномуСредству Тогда 
		ПроверяемыеРеквизиты.Добавить("ТранспортноеСредство");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Водитель");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидПеревозки");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы");
		//ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Рейсы.Рейс");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Функция - Возвращает таблицу рейсов для распределения по ним затрат.
//			 Отбор рейсов ведется:
//					* по фактической дате выполнения рейса.
//					* по статусу рейса.
//					* по виду прочих расходов:
//						- "ПоРейсам" 				- возвращает список рейсов на закладке "Список рейсов для распределения расходов".
//						- "ОбщиеЗаПериод" 			- все рейсы за период.
//						- "ПоВидуПеревозки" 		- рейсы с заданным видом перевозки.
//						- "ПоТранспортномуСредству" - рейсы с заданным транспортным средством.
//						- "ПоВодителюЭкспедитору"	- рейсы с на которых заданный сотрудник был водителем или экспедитором.
// 
// Возвращаемое значение:
//  тбзРейсы - ТаблицаЗначений - Результирующие данные.
//		* Рейс - ДкоументСсылка.упРейс.
//
Функция РейсыДляРаспределения() Экспорт
	
	тбзРейсы = Новый ТаблицаЗначений;
	тбзРейсы.Колонки.Добавить("Рейс", Новый ОписаниеТипов("ДокументСсылка.упРейс"));
		
	Если ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоРейсам Тогда
		тбзРейсы = Рейсы.Выгрузить();
	Иначе
		
		мсвСтатусы = Новый Массив;

		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.ВыполненЧастично);
		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Выполнен);
		мсвСтатусы.Добавить(Перечисления.упСтатусыРейса.Завершен);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упРейс.Ссылка КАК Рейс
					|ИЗ
		               |	Документ.упРейс КАК упРейс
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
		               |		ПО упРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
		               |		ПО упРейс.Ссылка = упФактическиеПараметрыРейса.Рейс
		               |ГДЕ
		               |	упФактическиеПараметрыРейса.ОкончаниеВыполнения >= &НачалоПериода
		               |	И упФактическиеПараметрыРейса.ОкончаниеВыполнения <= &ОкончаниеПериода
		               |	И упСтатусыРейсовСрезПоследних.Статус В(&Статусы)";
		Запрос.УстановитьПараметр("НачалоПериода", 		НачалоДня(НачалоПериодаРаспределения));
		Запрос.УстановитьПараметр("ОкончаниеПериода", 	КонецДня(ОкончаниеПериодаРаспределения));
		Запрос.УстановитьПараметр("Статусы", 	мсвСтатусы);
		
		Если НЕ ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ОбщиеЗаПериод Тогда
			
			СхемаЗапроса = Новый СхемаЗапроса;
			СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
			
			ПакетЗапросовСхемы 	= СхемаЗапроса.ПакетЗапросов;
			ЗапросПоРейсам 		= ПакетЗапросовСхемы[0];
			ОператорВыбратьСхемыЗапросов 	= ЗапросПоРейсам.Операторы[0];
			Отбор				= ОператорВыбратьСхемыЗапросов.Отбор;
			
			Если ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВидуПеревозки Тогда
				
				Отбор.Добавить("упРейс.ВидПеревозки = &ВидПеревозки");
				Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки); 
			Иначе
				Источники = ОператорВыбратьСхемыЗапросов.Источники; 	
				
				// Добавить таблицу по "Перевозчику".
				Источник = Источники.Добавить("РегистрСведений.упПеревозчикПоРейсу.СрезПоследних", "ПеревозчикПоРейсу");
				Источник.Соединения.Очистить();
				
				// Добавить соединение "Перевозчика" с "Рейсом".
				ИсточникРейс = Источники.НайтиПоПсевдониму("упРейс");
				ИсточникРейс.Соединения.Добавить(Источник, "упРейс.Ссылка = ПеревозчикПоРейсу.Рейс");
				СоединениеПеревозчик_Рейс = ИсточникРейс.Соединения.НайтиПоПсевдониму("ПеревозчикПоРейсу");
				СоединениеПеревозчик_Рейс.ТипСоединения=ТипСоединенияСхемыЗапроса.Внутреннее;			
				
				Если ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоТранспортномуСредству Тогда
			    	Отбор.Добавить("ПеревозчикПоРейсу.ТранспортноеСредство = &ТранспортноеСредство");
					Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство); 
				ИначеЕсли ВидПрочихРасходов = Перечисления.упВидыПрочихРасходов.ПоВодителюЭкспедитору Тогда	
					Отбор.Добавить("ПеревозчикПоРейсу.Водитель1 = &Водитель ИЛИ ПеревозчикПоРейсу.Водитель2 = &Водитель ИЛИ ПеревозчикПоРейсу.Экспедитор = &Водитель");
					Запрос.УстановитьПараметр("Водитель", Водитель); 
				КонецЕсли;
			КонецЕсли;
			Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		КонецЕсли;	
		
		тбзРейсы = Запрос.Выполнить().Выгрузить();
	
	КонецЕсли;
	
	Возврат тбзРейсы;
	
КонецФункции

#КонецОбласти 

#КонецЕсли