#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	мсвНепроверяемыхРеквизитов = Новый Массив;
	Если Не (Оплачен И Не Аннулирован) Тогда
		мсвНепроверяемыхРеквизитов.Добавить("ДатаОплаты");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, мсвНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")  Тогда
		ФактическиеДоходы = Неопределено;

		Если ДанныеЗаполнения.Свойство("ФактическиеДоходы", ФактическиеДоходы) Тогда
			сткЗначенияРеквизитов 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФактическиеДоходы, "Организация, Заказчик, КонтрагентЗаказчика");
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткЗначенияРеквизитов);
			Сумма					= ДанныеЗаполнения.Сумма;
			СуммаНДС				= ДанныеЗаполнения.СуммаНДС;
			Контрагент 				= сткЗначенияРеквизитов.КонтрагентЗаказчика;
		Иначе	
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;

		БанковскийСчет 			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ОсновнойБанковскийСчет");
		ДокументОснование 		= ФактическиеДоходы;

		мсвРасшифровкаПлатежей = Неопределено;
		Если ДанныеЗаполнения.Свойство("РасшифровкаПлатежей", мсвРасшифровкаПлатежей) Тогда
			Для каждого СтрокаПлатеж Из мсвРасшифровкаПлатежей Цикл
				НоваяСтрока = РасшифровкаПлатежа.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПлатеж);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
		
		// Расчеты с заказчиками
		ВестиУчетРасчетовСЗаказчиками		= ПолучитьФункциональнуюОпцию("упВестиУчетРасчетовСЗаказчиками");
		ДополнительныеСвойства.Вставить("ВестиУчетРасчетовСЗаказчиками", 	 ВестиУчетРасчетовСЗаказчиками);
		АналитическийРазрезПоПартнерам		= Неопределено;
		Если ВестиУчетРасчетовСЗаказчиками Тогда
			мсвЗаявки = РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов");
			мсвЗаявки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаявки);
			тбзАналитическиеРазрезПоЗаявкам = упУправленияЗаявками.ПодобратьАналитическийРазрезПоПартнерамДляКаждойЗаявки(Организация, Заказчик, Контрагент, мсвЗаявки, Отказ);
			Если НЕ Отказ Тогда
				ДополнительныеСвойства.Вставить("АналитическиеРазрезыПоПартнерам", 	 тбзАналитическиеРазрезПоЗаявкам)
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	тбзАналитическиеРазрезыПоПартнерам = ДополнительныеСвойства.АналитическиеРазрезыПоПартнерам;
		
	тбзРасшифровкиПлатежей = ДополнительныеСвойства.РасшифровкаПлатежа;
	тбзРасшифровкиПлатежей.Колонки.Добавить("АналитическийРазрезПоПартнерам", Новый ОписаниеТипов("СправочникСсылка.упАналитическиеРазрезыПоПартнерам"));
	
	Если Не тбзАналитическиеРазрезыПоПартнерам = Неопределено Тогда
		
		Для каждого СтрокаРасшифровки Из тбзРасшифровкиПлатежей Цикл
			мсвСтрокиАналитическиеРазрезы = тбзАналитическиеРазрезыПоПартнерам.НайтиСтроки(Новый Структура("ЗаявкаНаПеревозкуГруза", СтрокаРасшифровки.ОбъектРасчетов));
			Если мсвСтрокиАналитическиеРазрезы.Количество() > 0 Тогда
				СтрокаРасшифровки.АналитическийРазрезПоПартнерам = мсвСтрокиАналитическиеРазрезы[0].АналитическийРазрезПоПартнерам;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасчетыСЗаказчиками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тбзРасшифровкиПлатежей;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитическийРазрезПоПартнерам", 	"АналитическийРазрезПоПартнерам");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ОбъектРасчетов", 					"ОбъектРасчетов");
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	РеквизитыОбъекта = Новый Структура("Дата, РасшифровкаПлатежа, Аннулирован", ?(ЗначениеЗаполнено(ДатаОплаты), ДатаОплаты, Дата), тбзРасшифровкиПлатежей, Аннулирован); 
	
	// Очищаются записи регистров остатков.
	Движения.упРасчетыСЗаказчиками.Прочитать();
	Если Движения.упРасчетыСЗаказчиками.Количество() > 0 Тогда
		Движения.упРасчетыСЗаказчиками.Очистить();
		Движения.упРасчетыСЗаказчиками.Записать(Истина);
	КонецЕсли;
	
	Если Оплачен Тогда
		упРасчетыСЗаказчиками.СформироватьДвиженияРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#КонецЕсли
