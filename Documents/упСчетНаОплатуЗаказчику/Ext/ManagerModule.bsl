#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Счет на оплату
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упСчетНаОплатуЗаказчику";
  КомандаПечати.Идентификатор = "СчетНаОплату";
  КомандаПечати.Представление = НСтр("ru = 'Счет на оплату'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
 
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОплату") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СчетНаОплату",
		НСтр("ru = 'Счет на оплату'"),
		ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упСчетНаОплатуЗаказчику.ПФ_MXL_СчетНаОплату");
	КонецЕсли;	

КонецПроцедуры

// Формирование печатной формы Счет на оплату
Функция ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_СчетНаОплату";
	
	ДанныеДляПечати = ПолучитьДанныеДляПФ(МассивОбъектов);
	ЗаполнитьТабличныйДокументСчетНаОплату(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	
	Возврат ТабличныйДокумент;
КонецФункции

// Процедура - Заполнить табличный документ Счет на оплату
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент - табличный документ.
//  ДанныеДляПечати		 - Структура	 - данные печати.
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//								  * значение - ссылка на объект.
//                                        * представление - имя области в которой был выведен объект (выходной параметр).
//
Процедура ЗаполнитьТабличныйДокументСчетНаОплату(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати) Экспорт

	тбШапка  			= ДанныеДляПечати.Шапка;
	тбТаблица			= ДанныеДляПечати.Таблица;

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упСчетНаОплатуЗаказчику.ПФ_MXL_СчетНаОплату");
	
	ОбластьРеквизитыБанка	= Макет.ПолучитьОбласть("РеквизитыБанка");
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы 	= Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	          
	Для каждого СтрокаСчет Из тбШапка Цикл
		
		СчетОрганизации = СтрокаСчет.ОсновнойБанковскийСчет;
		Если ЗначениеЗаполнено(СчетОрганизации.БанкДляРасчетов)Тогда
			ОбластьРеквизитыБанка.Параметры.БанкНаименование = СчетОрганизации.БанкДляРасчетов.Наименование;
			ОбластьРеквизитыБанка.Параметры.БИКБанка = СчетОрганизации.БанкДляРасчетов.Код;
			ОбластьРеквизитыБанка.Параметры.КорСчетБанка = СчетОрганизации.БанкДляРасчетов.КоррСчет;
			ОбластьРеквизитыБанка.Параметры.НомерСчета = СчетОрганизации.НомерСчета;
		Иначе
			ОбластьРеквизитыБанка.Параметры.БанкНаименование = СчетОрганизации.Банк.Наименование;
			ОбластьРеквизитыБанка.Параметры.БИКБанка = СчетОрганизации.Банк.Код;
			ОбластьРеквизитыБанка.Параметры.КорСчетБанка = СчетОрганизации.Банк.КоррСчет;
			ОбластьРеквизитыБанка.Параметры.НомерСчета = СчетОрганизации.НомерСчета;
		КонецЕсли;
		
		ОбластьРеквизитыБанка.Параметры.ИННБанка = СтрокаСчет.Организация.ИНН;
		ОбластьРеквизитыБанка.Параметры.КППБанка = СтрокаСчет.Организация.КПП;
		
		Если ПустаяСтрока(СтрокаСчет.Организация.НаименованиеПолное) Тогда
			ОбластьРеквизитыБанка.Параметры.ПредставлениеОрганизации = СтрокаСчет.Организация.Наименование;
		Иначе
			ОбластьРеквизитыБанка.Параметры.ПредставлениеОрганизации = СтрокаСчет.Организация.НаименованиеПолное;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьРеквизитыБанка);
		
		ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры, СтрокаСчет);
		
		ОбластьШапка.Параметры.Номер	= ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СтрокаСчет.Номер);
		ОбластьШапка.Параметры.Дата		= Формат(СтрокаСчет.Дата, "ДЛФ=DD");
		мсвБанковскийСчет = Новый Массив;
		Если ЗначениеЗаполнено(СтрокаСчет.НомерСчета) Тогда
			мсвБанковскийСчет.Добавить("р/с " + СтрокаСчет.НомерСчета);
			Если ЗначениеЗаполнено(СтрокаСчет.Банк) Тогда
				мсвБанковскийСчет.Добавить(СтрокаСчет.Банк);	
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаСчет.КоррСчет) Тогда
				мсвБанковскийСчет.Добавить("к/с " + СтрокаСчет.КоррСчет);	
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаСчет.БИК) Тогда
				мсвБанковскийСчет.Добавить("БИК " + СтрокаСчет.БИК);	
			КонецЕсли;
		КонецЕсли;
		
		БанковскийСчет = СтрСоединить(мсвБанковскийСчет, ", ");
		
		ОбластьШапка.Параметры.ПредставлениеПоставщик	= КонтактныеДанные(СтрокаСчет.Организация, БанковскийСчет,СтрокаСчет.ОрганизацияИНН,СтрокаСчет.ОрганизацияКПП);
		Если ЗначениеЗаполнено(СтрокаСчет.Контрагент) Тогда
			ОбластьШапка.Параметры.ПредставлениеПокупатель	= КонтактныеДанные(СтрокаСчет.Контрагент,,СтрокаСчет.КонтрагентИНН,СтрокаСчет.КонтрагентКПП);	
		Иначе	
			ОбластьШапка.Параметры.ПредставлениеПокупатель	= КонтактныеДанные(СтрокаСчет.Заказчик);	
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		мсвСтрокиСчета = тбТаблица.НайтиСтроки(Новый Структура("Ссылка",СтрокаСчет.Ссылка));
		
		ИтогоБезНДС	= 0;
		ИтогоНДС	= 0;
		ИтогоСНДС 	= 0;
		Номер		= 1;
		
		Для каждого СтрокаРасшифровка Из мсвСтрокиСчета Цикл
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаТаблицы.Параметры, СтрокаРасшифровка);
			ОбластьСтрокаТаблицы.Параметры.Номер = Номер;
			упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуБезНДС(СтрокаРасшифровка);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			Номер		= Номер + 1;
			ИтогоБезНДС	= ИтогоБезНДС + СтрокаРасшифровка.СуммаБезНДС;
			ИтогоНДС	= ИтогоНДС + СтрокаРасшифровка.СуммаНДС;
			ИтогоСНДС 	= ИтогоСНДС + СтрокаРасшифровка.СуммаСНДС;
		КонецЦикла;
	    ОбластьПодвал.Параметры.ИтогоБезНДС 		= ИтогоБезНДС;
		ОбластьПодвал.Параметры.ИтогоНДС	= ИтогоНДС;
		ОбластьПодвал.Параметры.ИтогоСНДС 	= ИтогоСНДС;
		ОбластьПодвал.Параметры.СуммаПрописью	= ЧислоПрописью(ИтогоСНДС, "Л = ru_RU; ДП = Ложь","рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
		ОбластьПодвал.Параметры.ФИОРуководителя		= Строка(СтрокаСчет.РуководительПредприятия);
		ОбластьПодвал.Параметры.ФИОГлавБухгалтера	= Строка(СтрокаСчет.ГлавныйБухгалтер);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();

	КонецЦикла;
				
КонецПроцедуры

// Функция - Получить данные для печати
//
// Параметры:
//  мсвОбъекты	 - Массив	 - ссылки на документы.
// 
// Возвращаемое значение:
//  Структура - данные для печатной формы.
//
Функция ПолучитьДанныеДляПФ(мсвОбъекты) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упСчетНаОплатуЗаказчику.Номер,
	               |	упСчетНаОплатуЗаказчику.Дата,
	               |	упСчетНаОплатуЗаказчику.Организация,
	               |	упСчетНаОплатуЗаказчику.Заказчик,
	               |	упСчетНаОплатуЗаказчику.Контрагент,
	               |	упСчетНаОплатуЗаказчику.БанковскийСчет,
	               |	упСчетНаОплатуЗаказчику.Ссылка,
	               |	ЕСТЬNULL(упБанковскиеСчетаОрганизаций.НомерСчета, """") КАК НомерСчета,
	               |	ЕСТЬNULL(КлассификаторБанковРФ.Код, """") КАК БИК,
	               |	ЕСТЬNULL(КлассификаторБанковРФ.Наименование, """") КАК Банк,
	               |	ЕСТЬNULL(КлассификаторБанковРФ.КоррСчет, """") КАК КоррСчет,
	               |	упСчетНаОплатуЗаказчику.Организация.ОсновнойБанковскийСчет КАК ОсновнойБанковскийСчет,
	               |	упСчетНаОплатуЗаказчику.Организация.РуководительПредприятия КАК РуководительПредприятия,
	               |	упСчетНаОплатуЗаказчику.Организация.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	               |	упСчетНаОплатуЗаказчику.Организация.ИНН КАК ОрганизацияИНН,
	               |	упСчетНаОплатуЗаказчику.Организация.КПП КАК ОрганизацияКПП,
	               |	упСчетНаОплатуЗаказчику.Контрагент.ИНН КАК КонтрагентИНН,
	               |	упСчетНаОплатуЗаказчику.Контрагент.КПП КАК КонтрагентКПП
	               |ИЗ
	               |	Документ.упСчетНаОплатуЗаказчику КАК упСчетНаОплатуЗаказчику
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упБанковскиеСчетаОрганизаций КАК упБанковскиеСчетаОрганизаций
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанковРФ КАК КлассификаторБанковРФ
	               |			ПО упБанковскиеСчетаОрганизаций.Банк = КлассификаторБанковРФ.Ссылка
	               |		ПО упСчетНаОплатуЗаказчику.БанковскийСчет = упБанковскиеСчетаОрганизаций.Ссылка
	               |ГДЕ
	               |	упСчетНаОплатуЗаказчику.Ссылка В(&мсвОбъекты)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Содержание,
	               |	СУММА(упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Количество) КАК Количество,
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.СтавкаНДС,
	               |	СУММА(0) КАК СуммаБезНДС,
	               |	СУММА(упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Сумма) КАК СуммаСНДС,
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Цена,
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Ссылка
	               |ИЗ
	               |	Документ.упСчетНаОплатуЗаказчику.РасшифровкаПлатежа КАК упСчетНаОплатуЗаказчикуРасшифровкаПлатежа
	               |ГДЕ
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Ссылка В(&мсвОбъекты)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Содержание,
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Цена,
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.СтавкаНДС,
	               |	упСчетНаОплатуЗаказчикуРасшифровкаПлатежа.Ссылка";
	Запрос.УстановитьПараметр("мсвОбъекты", мсвОбъекты);
	РезультатПакет  = Запрос.ВыполнитьПакет();
	ЗапросИндексШапка 	= 0;
	ЗапросИндексТаблица = 1;
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Шапка",  			РезультатПакет[ЗапросИндексШапка].Выгрузить());
	СтруктураДанныхДляПечати.Вставить("Таблица",  			РезультатПакет[ЗапросИндексТаблица].Выгрузить());

	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция КонтактныеДанные(ЮрЛицо, БанковскийСчет = "", ИНН = "", КПП = "")
	
	мсвКонтактныеДанные = Новый Массив;
	
	Если ЗначениеЗаполнено(ЮрЛицо) Тогда
		
		мсвКонтактныеДанные.Добавить(Строка(ЮрЛицо));
		
		Если ЗначениеЗаполнено(ИНН) Тогда
			мсвКонтактныеДанные.Добавить(Нстр("ru = 'ИНН '") + ИНН);				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КПП) Тогда
			мсвКонтактныеДанные.Добавить(Нстр("ru = 'КПП '") + КПП);				
		КонецЕсли;
		
		Если ТипЗнч(ЮрЛицо) = Тип("СправочникСсылка.Организации") Тогда
			Адрес	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ЮрЛицо, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации); 
			Телефон	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ЮрЛицо, Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);				
		Иначе
			Адрес	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ЮрЛицо, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
			Телефон	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ЮрЛицо, ?(ТипЗнч(ЮрЛицо)=ТИП("СправочникСсылка.упКонтрагенты"),Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,Справочники.ВидыКонтактнойИнформации.ТелефонПартнера));	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Адрес) Тогда
			мсвКонтактныеДанные.Добавить(Адрес);				
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(Телефон) Тогда
			мсвКонтактныеДанные.Добавить(Нстр("ru = 'тел.: '") + Телефон);				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(БанковскийСчет) Тогда
			мсвКонтактныеДанные.Добавить(БанковскийСчет);
		КонецЕсли;
	КонецЕсли;

	Возврат СтрСоединить(мсвКонтактныеДанные, ", ");
		
КонецФункции

#КонецОбласти
#КонецОбласти
#КонецЕсли
