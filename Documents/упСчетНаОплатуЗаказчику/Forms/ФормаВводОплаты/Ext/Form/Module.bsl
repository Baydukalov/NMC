#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ФактическиеДоходы) Тогда
		ФактическиеДоходы = Параметры.ФактическиеДоходы;
		ЗаполнитьСписокОплат();
	Иначе	
		Возврат;
	КонецЕсли;
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ВедетсяВалютныйУчет Тогда
		УстановитьВалютуРеглУчета();
		ВалютаУпрУчета 	= упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	РассчитатьИтоговуюСуммуДоходы();

КонецПроцедуры	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.СписокОплат.ТекущиеДанные;
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоля(Элемент.Имя));
КонецПроцедуры

&НаКлиенте
Процедура СписокОплатКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписокОплатЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписокОплатСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписокОплатСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписокОплатПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуДоходы();
КонецПроцедуры

&НаКлиенте
Процедура СписокОплатПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуДоходы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьСчетНаОплатуЗаказчику(Команда)
	Оповестить("СформироватьСчетНаОплатуЗаказчику", СформироватьСчетНаОплатуЗаказчикуНаСервере());
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьСписокОплат()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА упФактическиеДоходыДоходы.Ссылка.СпособВводаФактическихДоходов = ЗНАЧЕНИЕ(Перечисление.упСпособыВводаФактическихДоходов.ДоходыПоОднойЗаявке)
	               |			ТОГДА упФактическиеДоходыДоходы.Ссылка.ЗаявкаНаПеревозкуГруза
	               |		ИНАЧЕ упФактическиеДоходыДоходы.ЗаявкаНаПеревозкуГруза
	               |	КОНЕЦ КАК ОбъектРасчетов,
	               |	упФактическиеДоходыДоходы.Валюта,
	               |	упФактическиеДоходыДоходы.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза1,
	               |	упФактическиеДоходыДоходы.Количество,
	               |	упФактическиеДоходыДоходы.СкидкаПроцент,
	               |	упФактическиеДоходыДоходы.СкидкаСумма,
	               |	упФактическиеДоходыДоходы.СтавкаНДС,
	               |	упФактическиеДоходыДоходы.СтатьяДоходов,
	               |	упФактическиеДоходыДоходы.Сумма,
	               |	упФактическиеДоходыДоходы.СуммаНДС,
	               |	упФактическиеДоходыДоходы.Цена,
	               |	упФактическиеДоходыДоходы.СтрокаДобавленаНаОснованииРасхода
	               |ИЗ
	               |	Документ.упФактическиеДоходы.Доходы КАК упФактическиеДоходыДоходы
	               |ГДЕ
	               |	упФактическиеДоходыДоходы.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ФактическиеДоходы);
	СписокОплат.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "СписокОплат","");
КонецФункции

&НаСервере
Функция СформироватьСчетНаОплатуЗаказчикуНаСервере()
	
	мсвВыделенныеСтрокиОплаты = Элементы.СписокОплат.ВыделенныеСтроки;
	мсвСтрок = Новый Массив;
	тбзСписокОплат = СписокОплат.Выгрузить(мсвСтрок);
	Для каждого ИдентификаторСтроки Из мсвВыделенныеСтрокиОплаты Цикл
		
		Строка = СписокОплат.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если НЕ Строка = Неопределено Тогда
			НоваяСтрока =  тбзСписокОплат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("ФактическиеДоходы", 		ФактическиеДоходы);
	сткПараметры.Вставить("РасшифровкаПлатежей", 	ОбщегоНазначения.ТаблицаЗначенийВМассив(тбзСписокОплат));
	сткПараметры.Вставить("Сумма", 					Сумма);
	сткПараметры.Вставить("СуммаНДС", 				СуммаНДС);
	Возврат сткПараметры;
КонецФункции

&НаСервере
Процедура РассчитатьИтоговуюСуммуДоходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("Сумма",			"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаНДС",		"СуммаНДС");
	сткОбъект = Новый Структура("Сумма, СуммаНДС, СписокОплат, Дата",0,0,РеквизитФормыВЗначение("СписокОплат"),ТекущаяДатаСеанса());
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(сткОбъект, "СписокОплат", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	Сумма 		= сткОбъект.Сумма;
	СуммаНДС 	= сткОбъект.СуммаНДС;
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаСервере
Процедура УстановитьВалютуРеглУчета()
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
КонецПроцедуры

#КонецОбласти
