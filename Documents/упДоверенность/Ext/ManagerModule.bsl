#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Доверенность
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати 					= "Документ.упДоверенность";
  КомандаПечати.Идентификатор 					= "ПФ_MXL_Доверенность";
  КомандаПечати.Представление 					= НСтр("ru = 'Доверенность'");
  КомандаПечати.ПроверкаПроведенияПередПечатью 	= Истина;
  КомандаПечати.Порядок							= 1;
  
  // Доверенность
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати 					= "Документ.упДоверенность";
  КомандаПечати.Идентификатор 					= "ПФ_MXL_РеестрДоверенностей";
  КомандаПечати.Представление 					= НСтр("ru = 'Реестр'");
  КомандаПечати.ПроверкаПроведенияПередПечатью 	= Истина;
  КомандаПечати.Порядок							= 1;

  // Настраиваемый комплект документов
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упДоверенность";
  КомандаПечати.Порядок = 75;
  КомандаПечати.Идентификатор = "ПФ_MXL_Доверенность,ПФ_MXL_РеестрДоверенностей";
  КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений  - значение - ссылка на объект;
//     представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// печать доверенности
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Доверенность") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ПФ_MXL_Доверенность", 
		"Доверенность",
		ПечатьДоверенность(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упДоверенность.ПФ_MXL_Доверенность");
	КонецЕсли;
  
	// печать реестра
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_РеестрДоверенностей") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ПФ_MXL_РеестрДоверенностей", 
		"Реестр доверенностей",
		ПечатьРеестраДоверенностей(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упДоверенность.ПФ_MXL_РеестрДоверенностей");
	КонецЕсли;
	
	
КонецПроцедуры // Печать()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений.
//
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция ПечатьДоверенность(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено) Экспорт
	
	ЭтоРейс = ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент						= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_Доверенность";
	ДанныеДляПечати = ПолучитьДанныеДляДоверенности(МассивОбъектов, ЭтоРейс);
	ЗаполнитьДоверенность(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьДоверенность()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений.
//
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция ПечатьРеестраДоверенностей(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент						= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСлева				= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_Реестр_Доверенностей";
	ДанныеДляПечати = ПолучитьДанныеДляРеестраДоверенностей(МассивОбъектов, ПараметрыПечати);
	ЗаполнитьРеестрДоверенностей(ТабличныйДокумент, ДанныеДляПечати, объектыПечати, ПараметрыПечати);
    
  	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьРеестраДоверенностей()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьДанныеПаспорта(Водитель, ДанныеПаспорта, ЕстьПаспорт)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	тбДокумент.Регистратор.Серия КАК Серия,
	               |	тбДокумент.Регистратор.Номер КАК Номер,
	               |	тбДокумент.Регистратор.Выдан КАК Выдан,
	               |	тбДокумент.Регистратор.Дата КАК ДатаВыдачи
	               |ИЗ
	               |	РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних КАК тбДокумент
	               |ГДЕ
	               |	тбДокумент.Водитель = &Водитель
	               |	И тбДокумент.ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.Паспорт)";
	Запрос.УстановитьПараметр("Водитель", Водитель);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ЕстьПаспорт = Истина;
		ДанныеПаспорта = Результат.Выгрузить()[0];
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.
//
// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Рейс");
	БлокируемыеРеквизиты.Добавить("Задание");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

Функция ПолучитьДанныеДляДоверенности(МассивОбъектов, ЭтоРейс)
	
	Если ЭтоРейс Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упДоверенность.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упДоверенность КАК упДоверенность
		|		ПО упРейсЗадания.Задание = упДоверенность.Задание
		|			И (НЕ упДоверенность.ПометкаУдаления)
		|ГДЕ
		|	упРейсЗадания.Ссылка В(&МассивОбъектов)";
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		МассивОбъектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПеревозчикПоРейсу.Рейс КАК Рейс,
	|	ПеревозчикПоРейсу.Водитель1 КАК Водитель1,
	|	ПеревозчикПоРейсу.Водитель2 КАК Водитель2,
	|	ПеревозчикПоРейсу.Экспедитор КАК Экспедитор
	|ПОМЕСТИТЬ втВодители
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					упДоверенность.Рейс
	|				ИЗ
	|					Документ.упДоверенность КАК упДоверенность
	|				ГДЕ
	|					упДоверенность.Ссылка В (&МассивОбъектов))) КАК ПеревозчикПоРейсу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упДоверенность.Ссылка,
	|	упДоверенность.Рейс КАК Рейс,
	|	упДоверенность.Рейс.Организация КАК ОрганизацияСсылка,
	|	упДоверенность.Рейс.Организация.Наименование КАК ОрганизацияНаименование,
	|	упДоверенность.Рейс.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	упДоверенность.Номер КАК НомерДоверенности,
	|	упДоверенность.Дата КАК ДатаВыдачи,
	|	ДОБАВИТЬКДАТЕ(упДоверенность.Дата, ДЕНЬ, 10) КАК ДатаСрокДействия,
	|	упДоверенность.ДолжностьДоверенноеЛицо КАК Должность,
	|	ПРЕДСТАВЛЕНИЕ(упДоверенность.ДоверенноеЛицо) КАК ФИО_временно,
	|	упДоверенность.Задание.Заказчик КАК Заказчик,
	|	упДоверенность.Задание.Заказчик.Наименование КАК ЗаказчикНаименование,
	|	упДоверенность.Задание.КонтрагентЗаказчика КАК ЗаказчикПлательщик,
	|	упДоверенность.Задание.КонтрагентЗаказчика.ИНН КАК ЗаказчикИНН,
	|	упДоверенность.Задание.КонтрагентЗаказчика.КПП КАК ЗаказчикКПП,
	|	упДоверенность.Задание.КонтрагентЗаказчика.ДополнительнаяИнформация КАК БанковскиеРеквизитыПлательщика,
	|	упДоверенность.Рейс.Организация.РуководительПредприятия КАК РуководительПредприятия,
	|	упДоверенность.Рейс.Организация.ГлавныйБухгалтер КАК БухгалтерПредприятия,
	|	ЕСТЬNULL(втВодители.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель1,
	|	ЕСТЬNULL(втВодители.Водитель2, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель2,
	|	ЕСТЬNULL(втВодители.Экспедитор, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Экспедитор
	|ИЗ
	|	Документ.упДоверенность КАК упДоверенность
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВодители КАК втВодители
	|		ПО упДоверенность.Рейс = втВодители.Рейс
	|ГДЕ
	|	упДоверенность.Ссылка В(&МассивОбъектов)
	|	И НЕ упДоверенность.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ТЗДоверенность = Запрос.Выполнить().Выгрузить();
	
	ТЗДанные = ТЗДоверенность.СкопироватьКолонки();
	ТЗДанные.Колонки.Добавить("ДолжностьФИО");
	ТЗДанные.Колонки.Добавить("ДатаДоверенности");
	ТЗДанные.Колонки.Добавить("ДатаДоверенностиПолная");
	ТЗДанные.Колонки.Добавить("СрокДействия");
	ТЗДанные.Колонки.Добавить("СрокДействияПолная");
	ТЗДанные.Колонки.Добавить("ФИО");
	ТЗДанные.Колонки.Добавить("НаименованиеПотребителяИЕгоАдрес");
	ТЗДанные.Колонки.Добавить("НаименованиеПлательщикаИЕгоАдрес");
	ТЗДанные.Колонки.Добавить("Организация");
	ТЗДанные.Колонки.Добавить("Поставщик");
	ТЗДанные.Колонки.Добавить("ПаспортСерияНомер");
	ТЗДанные.Колонки.Добавить("ПаспортВыдан");
	ТЗДанные.Колонки.Добавить("ПаспортДатаВыдачи");
	ТЗДанные.Колонки.Добавить("Руководитель");
	ТЗДанные.Колонки.Добавить("Бухгалтер");
	
	Для Каждого текДоверенность Из ТЗДоверенность Цикл
		
		НоваяДоверенность = ТЗДанные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяДоверенность, текДоверенность);
		
		НоваяДоверенность.НомерДоверенности			= текДоверенность.НомерДоверенности;
		НоваяДоверенность.ДатаДоверенности 			= Формат(текДоверенность.ДатаВыдачи, "ДФ=dd.MM.yyyy");
		НоваяДоверенность.ДатаДоверенностиПолная 	= Формат(текДоверенность.ДатаВыдачи, "ДФ='dd ММММ yyyy'") + " г.";
		НоваяДоверенность.Поставщик					= текДоверенность.ЗаказчикНаименование;
		НоваяДоверенность.Организация				= ?(ЗначениеЗаполнено(текДоверенность.ОрганизацияНаименованиеПолное), текДоверенность.ОрганизацияНаименованиеПолное, текДоверенность.ОрганизацияНаименование);
		НоваяДоверенность.СрокДействия 				= Формат(текДоверенность.ДатаСрокДействия, "ДФ=dd.MM.yyyy");
		НоваяДоверенность.СрокДействияПолная 		= Формат(текДоверенность.ДатаСрокДействия, "ДФ='dd ММММ yyyy'" + " г.");
		НоваяДоверенность.ДолжностьФИО				= Строка(текДоверенность.Должность) + " " + Строка(текДоверенность.ФИО_временно);
		КомуЧему = "";
		Если ЗначениеЗаполнено(текДоверенность.ФИО_временно) Тогда
			ОбщегоНазначения.Просклонять(текДоверенность.ФИО_временно, 3, КомуЧему);
		КонецЕсли;
		НоваяДоверенность.ФИО						= КомуЧему;
		НоваяДоверенность.НаименованиеПотребителяИЕгоАдрес = текДоверенность.ОрганизацияНаименованиеПолное + ", ИНН/КПП " + текДоверенность.ОрганизацияСсылка.ИНН + "/" + текДоверенность.ОрганизацияСсылка.КПП +", " + УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текДоверенность.ОрганизацияСсылка, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
		Если ЗначениеЗаполнено(текДоверенность.Заказчик) Тогда
			НоваяДоверенность.НаименованиеПлательщикаИЕгоАдрес = текДоверенность.ЗаказчикНаименование + ", ИНН/КПП " + текДоверенность.ЗаказчикИНН + "/" + текДоверенность.ЗаказчикКПП + ", " + УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текДоверенность.ЗаказчикПлательщик, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
			НоваяДоверенность.БанковскиеРеквизитыПлательщика = текДоверенность.БанковскиеРеквизитыПлательщика;
		КонецЕсли;
		НоваяДоверенность.Руководитель = текДоверенность.РуководительПредприятия;
		НоваяДоверенность.Бухгалтер = текДоверенность.БухгалтерПредприятия;
		
		ЕстьПаспорт = Ложь;
		ДанныеПаспорта = Неопределено;
		УстановитьДанныеПаспорта(?(ЗначениеЗаполнено(текДоверенность.Экспедитор), текДоверенность.Экспедитор, текДоверенность.Водитель1), ДанныеПаспорта, ЕстьПаспорт);
		Если ЕстьПаспорт Тогда
			НоваяДоверенность.ПаспортСерияНомер = Строка(ДанныеПаспорта.Серия) + Строка(ДанныеПаспорта.Номер);
			НоваяДоверенность.ПаспортВыдан      = ДанныеПаспорта.Выдан;
			НоваяДоверенность.ПаспортДатаВыдачи = Формат(ДанныеПаспорта.ДатаВыдачи, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Общие",  ТЗДанные);
	
	Возврат СтруктураДанныхДляПечати;
				   
КонецФункции // ПолучитьДанныеДляДоверенности()

Процедура ЗаполнитьДоверенность(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеОбщие = ДанныеДляПечати.Общие;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упДоверенность.ПФ_MXL_Доверенность");
	
	Эталон 				= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	ОбластьОбщая = Макет.ПолучитьОбласть("Вертикаль|Горизонталь");
	
	Для Каждого текСтрокаДоверенности Из тбДанныеОбщие Цикл
	
		ОбластьОбщая.Параметры.Заполнить(текСтрокаДоверенности);
		
		БанковскиеРеквизитыПлательщика = "";
		Если ЗначениеЗаполнено(текСтрокаДоверенности.ЗаказчикПлательщик.ОсновнойБанковскийСчет) Тогда
			ОсновнойБанковскийСчет = текСтрокаДоверенности.ЗаказчикПлательщик.ОсновнойБанковскийСчет;
			БанковскиеРеквизитыПлательщика = СтрШаблон("р/с %1, в банке %2 к/с %3",ОсновнойБанковскийСчет.НомерСчета,Строка(ОсновнойБанковскийСчет.Банк),ОсновнойБанковскийСчет.Банк.КоррСчет);
			Если ЗначениеЗаполнено(ОсновнойБанковскийСчет.БанкДляРасчетов) Тогда
				БанковскиеРеквизитыПлательщика = СтрШаблон("р/с %1, в банке %2 к/с %3",ОсновнойБанковскийСчет.НомерСчета,Строка(ОсновнойБанковскийСчет.БанкДляРасчетов),ОсновнойБанковскийСчет.БанкДляРасчетов.КоррСчет);
			КонецЕсли;
		КонецЕсли;
		ОбластьОбщая.Параметры.БанковскиеРеквизитыПлательщика=БанковскиеРеквизитыПлательщика;
		
		// Штрихкод
		Рисунок = ОбластьОбщая.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина",                      Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота",                      Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод",        				упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаДоверенности.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода",                     4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст",             Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон",               Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);

		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ТабличныйДокумент.Вывести(ОбластьОбщая);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаДоверенности.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДоверенность()

Функция ПолучитьДанныеДляРеестраДоверенностей(МассивОбъектов, ПараметрыПечати)
	
	РеестрФормируетсяПоРейсам = Ложь;
	
	Если ТипЗнч(МассивОбъектов) = Тип("Массив") Тогда
		Если МассивОбъектов.Количество() > 0 Тогда
			РеестрФормируетсяПоРейсам = ТипЗнч(МассивОбъектов[0]) = Тип("ДокументСсылка.упРейс");
		КонецЕсли;	
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	СхемаЗапроса = Новый СхемаЗапроса;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	упДоверенность.Ссылка,
	               |	упДоверенность.Номер,
	               |	упДоверенность.Дата,
	               |	упДоверенность.Задание.КонтрагентЗаказчика КАК Контрагент,
	               |	упДоверенность.ДоверенноеЛицо КАК Сотрудник
	               |ИЗ
	               |	Документ.упДоверенность КАК упДоверенность";
	
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ПакетЗапросовСхемы	= СхемаЗапроса.ПакетЗапросов;
	ЗапросСхемы			= ПакетЗапросовСхемы[0];
	ОператорВыбратьСхемыЗапросов 	= ЗапросСхемы.Операторы[0];
	Отбор				= ОператорВыбратьСхемыЗапросов.Отбор;
	Если РеестрФормируетсяПоРейсам Тогда
		Отбор.Добавить("упДоверенность.Рейс В (&МассивОбъектов)");
	Иначе
		Отбор.Добавить("упДоверенность.Ссылка В(&МассивОбъектов)");
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ТЗДанные = Запрос.Выполнить().Выгрузить();
		
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Общие",  ТЗДанные);
	
	Возврат СтруктураДанныхДляПечати;
				   
КонецФункции // ПолучитьДанныеДляРеестраДоверенностей()

Процедура ЗаполнитьРеестрДоверенностей(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати)
	
	тбДанныеОбщие = ДанныеДляПечати.Общие;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упДоверенность.ПФ_MXL_РеестрДоверенностей");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");

	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	НомерСтроки = 1;
	
	Если тбДанныеОбщие.Количество() = 0 Тогда
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, ТабличныйДокумент.ВысотаТаблицы + 1, ОбъектыПечати, );
	Иначе
		Для Каждого текСтрокаДоверенности Из тбДанныеОбщие Цикл
			
			ОбластьСтрока.Параметры.Заполнить(текСтрокаДоверенности);
			ОбластьСтрока.Параметры.НомерПункта = НомерСтроки;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			НомерСтроки = НомерСтроки + 1;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаДоверенности.Ссылка);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРеестрДоверенностей()

#КонецОбласти

#КонецЕсли