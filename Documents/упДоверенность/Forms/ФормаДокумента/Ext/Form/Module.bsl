
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ПриСозданииПриЧтенииНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДоверенноеЛицоПриИзменении(Элемент)
	
	Объект.ДолжностьДоверенноеЛицо = ПолучитьДолжностьДовереногоЛица(Объект.Рейс,Объект.ДоверенноеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	
	Объект.Задание = ПредопределенноеЗначение("Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка");
	
	Если НЕ ПроверитьДоверенноеЛицо(Объект.Рейс,Объект.ДоверенноеЛицо) Тогда
		Объект.ДоверенноеЛицо = ПредопределенноеЗначение("Справочник.упСотрудники.ПустаяСсылка");
		Объект.ДолжностьДоверенноеЛицо = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	текОтбор = Новый Структура();
	ЗаполнитьСтруктуруОтбораДоверенныхЛиц(текОтбор,Объект.Рейс);
	сткПараметры = Новый Структура("Отбор", текОтбор);
	ОткрытьФорму("Справочник.упСотрудники.ФормаВыбора", сткПараметры, Элементы.ДоверенноеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	текОтбор 	= Новый Структура();
	ЗаполнитьСтруктуруОтбора(текОтбор,Объект.Рейс);
	сткПараметры 	= Новый Структура("Отбор", текОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.Задание);
	
КонецПроцедуры // ЗаданиеНачалоВыбора()

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура ПечатьДоверенности(Команда)
	
	КомандаПечатьПоУмолчанию = ЭтаФорма.Команды["ПечатьДоверенности"];
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(КомандаПечатьПоУмолчанию, ЭтотОбъект, Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполнить проверку.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на рейс.
//  Сотрудник  - СправочникСсылка.упСотрудники - Ссылка на должностное лицо.
//
// Возвращаемое значение:
//   Строка - Представление должности.
//
Функция ПроверитьДоверенноеЛицо(Рейс,Сотрудник)
	
	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.упТипыСотрудников.Водитель) КАК Должность
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И (Водитель1 = &Сотрудник
	|					ИЛИ Водитель2 = &Сотрудник)) КАК ПеревозчикПоРейсуСП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.упТипыСотрудников.Экспедитор)
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И Экспедитор = &Сотрудник) КАК ПеревозчикПоРейсуСП_Э";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить();
	
	Если Выборка.Пустой() Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПроверитьДоверенноеЛицо()

&НаСервереБезКонтекста
// Получить должность доверенного лица.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на рейс.
//  Сотрудник  - СправочникСсылка.упСотрудники - Ссылка на должностное лицо.
//
// Возвращаемое значение:
//   Строка - Представление должности.
//
Функция ПолучитьДолжностьДовереногоЛица(Рейс,Сотрудник)
	
	Результат = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.упТипыСотрудников.Водитель) КАК Должность
	|ПОМЕСТИТЬ вт_Должности
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И (Водитель1 = &Сотрудник
	|					ИЛИ Водитель2 = &Сотрудник)) КАК ПеревозчикПоРейсуСП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.упТипыСотрудников.Экспедитор)
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И Экспедитор = &Сотрудник) КАК ПеревозчикПоРейсуСП_Э
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упСотрудники.ТипСотрудника
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудники
	|ГДЕ
	|	упСотрудники.Ссылка = &Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Должности.Должность КАК Должность
	|ИЗ
	|	вт_Должности КАК вт_Должности
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Должности.Должность";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Должность;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьДолжностьДовереногоЛица()

&НаСервереБезКонтекста
Процедура ЗаполнитьСтруктуруОтбора(Отбор, Рейс)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задания.Задание КАК Ссылка
	|ИЗ
	|	Документ.упРейс.Задания КАК Задания
	|ГДЕ
	|	Задания.Ссылка = &Рейс";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Массив = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Массив.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Отбор.Вставить("Ссылка", Массив);
	
КонецПроцедуры // ЗаполнитьСтруктуруОтбора()

&НаСервереБезКонтекста
Процедура ЗаполнитьСтруктуруОтбораДоверенныхЛиц(Отбор, Рейс)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП_В1.Водитель1 КАК Ссылка
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И НЕ Водитель1 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК ПеревозчикПоРейсуСП_В1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП_В2.Водитель2
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И НЕ Водитель2 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК ПеревозчикПоРейсуСП_В2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП_Э.Экспедитор
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс = &Рейс
	|				И НЕ Экспедитор = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК ПеревозчикПоРейсуСП_Э";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Массив = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Массив.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Отбор.Вставить("Ссылка", Массив);
	
КонецПроцедуры // ЗаполнитьСтруктуруОтбораДоверенныхЛиц()

#КонецОбласти 