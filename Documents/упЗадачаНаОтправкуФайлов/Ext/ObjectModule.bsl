#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Файл) И (Статус = Перечисления.упСтатусыЗадачНаОтправкуФайлов.ФайлПодготовлен Или Статус = Перечисления.упСтатусыЗадачНаОтправкуФайлов.ФайлОтправлен) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Для перевода задачи в статус ""%1"" необходимо указать файл.'"), Статус);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ВидЗапросаНаОтправкуФайлов) Тогда
		ВидЗапросаНаОтправкуФайлов = Перечисления.упВидыЗапросовНаОтправкуФайлов.ОтчетПоЗадолженностямВодителя;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Файл) И Статус = Перечисления.упСтатусыЗадачНаОтправкуФайлов.Новая Тогда
		Статус = Перечисления.упСтатусыЗадачНаОтправкуФайлов.ФайлПодготовлен;
	КонецЕсли;

	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Идентификатор = Строка(Новый УникальныйИдентификатор());
	КонецЕсли; 
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта();	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ФайлПодготовлен = (Статус = Перечисления.упСтатусыЗадачНаОтправкуФайлов.ФайлПодготовлен);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗадачиОтправкиФайловМобильныйКлиент");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Трекер"       , Трекер);
	ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
	
	Если ФайлПодготовлен Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упУведомленияМобильныйКлиент");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Трекер", Трекер);
		ЭлементБлокировки.УстановитьЗначение("Рейс", Документы.упРейс.ПустаяСсылка());
		ЭлементБлокировки.УстановитьЗначение("ИдентификаторЗадачи", ИдентификаторЗадачи);
	КонецЕсли; 
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	
	// регистр упЗадачиОтправкиФайловМобильныйКлиент
	Движения.упЗадачиОтправкиФайловМобильныйКлиент.Записывать = Истина;
	Движения.упЗадачиОтправкиФайловМобильныйКлиент.Очистить();
	
	Движение = Движения.упЗадачиОтправкиФайловМобильныйКлиент.Добавить();
	Движение.Регистратор                = Ссылка;
	Движение.Трекер                     = Трекер;
	Движение.Идентификатор              = Идентификатор;
	Движение.Статус                     = Статус;
	Движение.ИдентификаторЗадачи        = ИдентификаторЗадачи;
	Движение.Файл                       = Файл;
	Движение.ВидЗапросаНаОтправкуФайлов = ВидЗапросаНаОтправкуФайлов;
	
	Если ФайлПодготовлен Тогда
		ЗаголовокУведомления = НСтр("ru = 'Новые файлы'");
		ТипСобытия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СобытиеМониторинга, "ТипСобытия");
		
		упУведомленияМобильныйКлиент = РегистрыСведений.упУведомленияМобильныйКлиент.СоздатьМенеджерЗаписи();
		упУведомленияМобильныйКлиент.Трекер              = Трекер;
		упУведомленияМобильныйКлиент.Рейс                = Документы.упРейс.ПустаяСсылка();
		упУведомленияМобильныйКлиент.ИдентификаторЗадачи = ИдентификаторЗадачи;
		упУведомленияМобильныйКлиент.Прочитать();
		
		Если ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросОтчетаПоЗадолженностямВодителя Тогда
			ТекстУведомления = НСтр("ru = 'Отчет по задолженностям доступен для скачивания.'");	
		Иначе	
			ТекстУведомления = НСтр("ru = 'На сервере подготовлен новый файл для скачивания.'");	
		КонецЕсли; 
		
		упУведомленияМобильныйКлиент.Трекер              = Трекер;
		упУведомленияМобильныйКлиент.ИдентификаторЗадачи = ИдентификаторЗадачи;
		упУведомленияМобильныйКлиент.Отправлено = Ложь;
		упУведомленияМобильныйКлиент.Заголовок = ЗаголовокУведомления; 
		упУведомленияМобильныйКлиент.ТекстУведомления = ТекстУведомления;
		упУведомленияМобильныйКлиент.Записать();	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли



