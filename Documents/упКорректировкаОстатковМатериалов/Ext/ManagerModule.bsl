#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура заполнения дополнительных свойст документа.
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упГСМ.ГСМ КАК Материал,
	|	СУММА(упГСМ.Количество) КАК Количество,
	|	СУММА(упГСМ.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(упГСМ.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ втМатериалы
	|ИЗ
	|	Документ.упКорректировкаОстатковМатериалов.ГСМ КАК упГСМ
	|ГДЕ
	|	упГСМ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	упГСМ.ГСМ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упЗапчасти.Запчасть,
	|	СУММА(упЗапчасти.Количество),
	|	СУММА(упЗапчасти.СуммаБезНДС),
	|	СУММА(упЗапчасти.СуммаСНДС)
	|ИЗ
	|	Документ.упКорректировкаОстатковМатериалов.Запчасти КАК упЗапчасти
	|ГДЕ
	|	упЗапчасти.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	упЗапчасти.Запчасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	упКорректировкаОстатковМатериалов.Склад КАК Склад,
	|	&Партия КАК Партия,
	|	втМатериалы.Материал,
	|	втМатериалы.Количество,
	|	втМатериалы.СуммаБезНДС,
	|	втМатериалы.СуммаСНДС
	|ИЗ
	|	втМатериалы КАК втМатериалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упКорректировкаОстатковМатериалов КАК упКорректировкаОстатковМатериалов
	|		ПО (упКорректировкаОстатковМатериалов.Ссылка = &Ссылка)";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Партия", ?(ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО, ДокументСсылка, документы.упКорректировкаОстатковМатериалов.ПустаяСсылка()));
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ОстаткиМатериалов = Новый ТаблицаЗначений;
		ОстаткиМатериалов.Колонки.Добавить("ВидДвижения");
		ОстаткиМатериалов.Колонки.Добавить("Склад"      , Новый ОписаниеТипов("СправочникСсылка.упСклады"));
		ОстаткиМатериалов.Колонки.Добавить("Материал"   , Новый ОписаниеТипов("СправочникСсылка.упГСМ, СправочникСсылка.упЗапчасти"));
		ОстаткиМатериалов.Колонки.Добавить("Партия"     , Новый ОписаниеТипов("ДокументСсылка.упПоступлениеМатериалов, ДокументСсылка.упКорректировкаОстатковМатериалов"));
		ОстаткиМатериалов.Колонки.Добавить("Количество" , Новый ОписаниеТипов("Число"));
		ОстаткиМатериалов.Колонки.Добавить("СуммаБезНДС", Новый ОписаниеТипов("Число"));
		ОстаткиМатериалов.Колонки.Добавить("СуммаСНДС"  , Новый ОписаниеТипов("Число"));
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОстаткиМатериалов.Добавить(), Выборка);
		КонецЦикла;
		ДополнительныеСвойства.Вставить("ОстаткиМатериалов", ОстаткиМатериалов);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
