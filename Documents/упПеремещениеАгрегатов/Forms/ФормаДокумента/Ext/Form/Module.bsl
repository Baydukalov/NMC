
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();		
	КонецЕсли;

	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упСервисныеФункции.УстановитьЗаголовокФормыИнформацияПроведения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура АгрегатыАгрегатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упШиныУзлыИАгрегаты.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПодобратьАгрегаты(Команда)
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("Склад", Объект.СкладОтправитель);
	сткПараметры.Вставить("ТипОборудования", ПредопределенноеЗначение("Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка"));
	сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упПеремещениеАгрегатов"));
	ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборАгрегатовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьДопОборудование(Команда)
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("Склад", Объект.СкладОтправитель);
	сткПараметры.Вставить("ТипОборудования", ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка"));
	сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упПеремещениеАгрегатов"));
	ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборДопОборудованияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия формы подбора агрегатов.
//
Процедура ПодборАгрегатовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			ЗаполнитьЗначенияСвойств(Объект.Агрегаты.Добавить(), текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // ПодборАгрегатовЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы подбора агрегатов.
//
Процедура ПодборДопОборудованияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			НоваяСтрока = Объект.ДополнительноеОборудование.Добавить();
			НоваяСтрока.ДополнительноеОборудование = текСтрока.Агрегат;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // ПодборАгрегатовЗавершение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти