
#Область ПеременныеФормы

&НаКлиенте
Перем ТекущиеВидыРемонта;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере(Отказ = Ложь)
	
	ЭтоРемонтТС = Ложь;
	
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(Элементы.ТранспортноеСредство, "СкрытьНеучетные", Истина);
			
	ВедетсяУчетШинУзловИАгрегатов = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетШинУзловИАгрегатов");	
	ВедетсяУчетДополнительногоОборудования = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетДополнительногоОборудования");
	Элементы.Страницы.ПодчиненныеЭлементы.ГруппаШины.Видимость = ВедетсяУчетШинУзловИАгрегатов;
	
	Элементы.ОбъектРемонта.СписокВыбора.Очистить();
	Элементы.ГруппаВыборОбъектыРемонта.ОтображатьЗаголовок = Ложь;
	
	Если НЕ ВедетсяУчетШинУзловИАгрегатов И НЕ ВедетсяУчетДополнительногоОборудования Тогда 
		ЭтоРемонтТС = Истина;
		Элементы.ТранспортноеСредство.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		Элементы.ОбъектРемонта.Видимость = Ложь;
		Элементы.ГруппаВыборОбъектыРемонта.ОтображатьЗаголовок = Истина;
	Иначе
		СписокОбъектРемонта = Новый Массив;
		СписокОбъектРемонта.Добавить(Перечисления.упОбъектыРемонта.ТранспортноеСредство);
		Если ВедетсяУчетДополнительногоОборудования Тогда 
			СписокОбъектРемонта.Добавить(Перечисления.упОбъектыРемонта.ДополнительноеОборудование);
		КонецЕсли;
		Если ВедетсяУчетШинУзловИАгрегатов Тогда 
			СписокОбъектРемонта.Добавить(Перечисления.упОбъектыРемонта.Агрегат);
		КонецЕсли;
		Элементы.ОбъектРемонта.СписокВыбора.ЗагрузитьЗначения(СписокОбъектРемонта);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Если ЗначениеЗаполнено(Параметры.Основание) 
			И ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.упПроисшествиеВПути") Тогда
			
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Основание, "ТранспортноеСредство, Организация, ПроблемаВПути.ВидПроисшествия");
			Если сткРеквизиты.ПроблемаВПутиВидПроисшествия = Перечисления.упВидыПроисшествийВПути.ОжиданиеДозагрузки Тогда
				СтандартнаяОбработка = Ложь;
				ТекстСообщения = Нстр("ru = 'Запрещено формировать ""Ремонт и ТО"" на основаниии проишествия с видом проишествия ""%1""'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Перечисления.упВидыПроисшествийВПути.ОжиданиеДозагрузки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Иначе
				ПроисшествиеВПути 		= Параметры.Основание;
				ТранспортноеСредство	= сткРеквизиты.ТранспортноеСредство;
				Организация				= сткРеквизиты.Организация;
			КонецЕсли;
		
		КонецЕсли;
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
		Если ВедетсяВалютныйУчет Тогда
			Объект.Валюта = ВалютаРеглУчета;
		КонецЕсли; 
		
		Если Не ЗначениеЗаполнено(Объект.ТранспортноеСредство) И Параметры.Свойство("ТранспортноеСредство") Тогда
			Если Параметры.Свойство("ДополнительноеОборудование") Тогда
				Если ЗначениеЗаполнено(Параметры.ТранспортноеСредство) Тогда
					Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство;
				    Объект.ТранспортноеСредство = Параметры.ТранспортноеСредство;
					Если ЗначениеЗаполнено(Параметры.ДополнительноеОборудование) Тогда
						НоваяСтрока = Объект.РемонтируемоеДополнительноеОборудование.Добавить();
						НоваяСтрока.ДополнительноеОборудование = Параметры.ДополнительноеОборудование;
					КонецЕсли; 
					
				ИначеЕсли ЗначениеЗаполнено(Параметры.ДополнительноеОборудование) Тогда
					Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование;
				    Объект.ДополнительноеОборудованиеРемонта = Параметры.ДополнительноеОборудование;
					
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		
		Если Параметры.Свойство("МестоРемонта") И ЗначениеЗаполнено(Параметры.МестоРемонта) Тогда
			Объект.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами;
			Объект.Гараж = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МестоРемонта, "Владелец");
			Объект.МестоРемонта = Параметры.МестоРемонта;
			Объект.ВидРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МестоРемонта, "ВидРемонтаПоУмолчанию");
		КонецЕсли;
		Если Параметры.Свойство("НачалоРемонтаПлан") Тогда
			Объект.НачалоРемонтаПлан = Параметры.НачалоРемонтаПлан;
		КонецЕсли;
		Если Параметры.Свойство("ОкончаниеРемонтаПлан") Тогда
			Объект.ОкончаниеРемонтаПлан = Параметры.ОкончаниеРемонтаПлан;
		КонецЕсли;
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			Объект.ЧасовойПояс	= упСервисныеФункции.ЧасовойПоясВидаПеревозки();	
		КонецЕсли;
	КонецЕсли;

	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Элементы.СпособРемонта.ТолькоПросмотр = Ложь;
	Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = Ложь;
	
	Элементы.ЗаявкаНаРемонт.ТолькоПросмотр = Истина;
	Элементы.ЗаявкаНаРемонт.Видимость = ЗначениеЗаполнено(Объект.ЗаявкаНаРемонтИТО);
	
	ВыполнитьЗаполнениеШины();
	
	Статус = Документы.упРемонтИТОТС.ПолучитьСтатус(Объект.Ссылка);
	
	// установка доступностьи элементов формы
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаРемонтаИТОТС(Объект.Ссылка, Статус, Элементы);
	УстановитьОтметкуНезаполненного();
	ОбновитьВидФормыПоВидуРемонта();
	ОбновитьВидФормыПоСпособуРемонта();
		
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);	
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Начат") ИЛИ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		Элементы.СпособРемонта.ТолькоПросмотр = Истина;
		Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = Истина;
		ТипИспользования = Ложь;		
		Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
			ТипИспользования = Истина;
		КонецЕсли;		
		Элементы.ГСМСобственные.ТолькоПросмотр = ТипИспользования;
		Элементы.ГСМ.ТолькоПросмотр = ТипИспользования;
	КонецЕсли;
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	УстановитьПредставлениеПараметровРемонта();
	
	ВычислитьСуммыВВалютеУправленческогоУчета();
		
КонецПроцедуры 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаРеглУчета	= упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
		ЭтаФорма.ВалютаУпрУчета 	= упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли; 
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 

	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	
	ПриСозданииПриЧтенииНаСервере(Отказ);	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Не ЗначениеЗаполнено(НовыйСтатус) Тогда 
		Возврат;
	Иначе
		ПроверяемыйСтатус = НовыйСтатус;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.МестоРемонта) Тогда
		Если ПроверяемыйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован Тогда 
			Если НЕ ЗначениеЗаполнено(Объект.ЗаявкаНаРемонтИТО) И Не ЗначениеЗаполнено(Объект.НачалоРемонтаПлан) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано плановое время начала ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НачалоРемонтаПлан",, Отказ);
			КонецЕсли; 
			
			Если НЕ ЗначениеЗаполнено(Объект.ЗаявкаНаРемонтИТО) И Не ЗначениеЗаполнено(Объект.ОкончаниеРемонтаПлан) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано плановое время окончания ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ОкончаниеРемонтаПлан",, Отказ);
			КонецЕсли; 
			
		ИначеЕсли ПроверяемыйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Начат Тогда 
			Если Не ЗначениеЗаполнено(Объект.НачалоРемонтаФакт) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано время начала ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НачалоРемонтаФакт",, Отказ);
			КонецЕсли;
			
		ИначеЕсли ПроверяемыйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда	
			Если Не ЗначениеЗаполнено(Объект.ОкончаниеРемонтаФакт) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано время окончания ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ОкончаниеРемонтаФакт",, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		Если ПроверяемыйСтатус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Тогда	
			Если ЗначениеЗаполнено(Объект.НачалоРемонтаФакт) И Не ЗначениеЗаполнено(Объект.ОкончаниеРемонтаФакт) Тогда
				ТекстОшибки = НСтр("ru = 'Не указано время окончания ремонта'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ОкончаниеРемонтаФакт",, Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;
	НовыйСтатус = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ВычислитьСуммыВВалютеУправленческогоУчета();
	
	Статус = Документы.упРемонтИТОТС.ПолучитьСтатус(Объект.Ссылка);
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаРемонтаИТОТС(Объект.Ссылка, Статус, Элементы);
	УстановитьОтметкуНезаполненного();
	ЗаблокироватьЭлементы();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ВыполнитьЗаполнениеШины();
	
	УстановитьПредставлениеПараметровРемонта();
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОповеститьОбИзменении(Объект.Ссылка);	
	
	Структура = Новый Структура("Статус, Ссылка",Статус,Объект.Ссылка);
	Структура.Вставить("ВидРемонта",Объект.ВидРемонта);
	Оповестить("ЗаписьДокументаРемонтИТОТС",Структура,ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеПараметровРемонта()
	
	ТекущееУстройство = Справочники.упТранспортныеСредства.ПустаяСсылка();
	Если Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство Тогда
		ТекущееУстройство = Объект.ТранспортноеСредство;
	ИначеЕсли Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда
		ТекущееУстройство = Объект.ДополнительноеОборудованиеРемонта;
	ИначеЕсли Объект.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
		ТекущееУстройство = Объект.Агрегат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПробегТСОстатки.ПробегОстаток КАК ПробегОстаток,
	|	упПробегТСОстатки.МоточасыОстаток КАК МоточасыОстаток
	|ИЗ
	|	РегистрНакопления.упПробегТС.Остатки(
	|			&Период,
	|			ВЫБОР
	|				КОГДА &ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|						ИЛИ &ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ТранспортноеСредство = &ТранспортноеСредство
	|			КОНЕЦ) КАК упПробегТСОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаТС.МоточасыОстаток КАК Моточасы,
	|	ВыработкаТС.ПробегОстаток КАК Пробег,
	|	ВыработкаТС.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.упВыработкаТС.Остатки(
	|			&Период,
	|			ВЫБОР
	|				КОГДА &ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ТранспортноеСредство = &ТранспортноеСредство
	|			КОНЕЦ) КАК ВыработкаТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыработкаАгрегатов.ПробегМотоЧасыОстаток,
	|	ВыработкаАгрегатов.ПробегКмОстаток,
	|	ВыработкаАгрегатов.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.упВыработкаАгрегатов.Остатки(
	|			&Период,
	|			ВЫБОР
	|				КОГДА &ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упШиныУзлыИАгрегаты.ПустаяСсылка)
	|						ИЛИ &ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упДополнительноеОборудование.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ Агрегат = &ТранспортноеСредство
	|			КОНЕЦ) КАК ВыработкаАгрегатов";
	
	Период = Объект.Дата;
	Если ЗначениеЗаполнено(Объект.НачалоРемонтаФакт) Тогда
		Период = Объект.НачалоРемонтаФакт;
	ИначеЕсли ЗначениеЗаполнено(Объект.НачалоРемонтаПлан) Тогда
		Период = Объект.НачалоРемонтаПлан;
	КонецЕсли;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТекущееУстройство);
	
	ПробегПредставление = "";
	МоточасыПредставление = "";
	МежремонтныйПробегПредставление = "данных нет";
	
	ПробегОстаточныйРесурсПредставление = "";
	МоточасыОстаточныйРесурсПредставление = "";
	СуммаОстаточныйРесурсПредставление = "";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	Выборка = ПакетЗапросов[1].Выбрать();
	Если Выборка.Следующий() Тогда
		Пробег = Выборка.Пробег;
		Моточасы = Выборка.Моточасы;
		Сумма = Выборка.Сумма;
		ПробегОстаточныйРесурсПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(Пробег);
		МоточасыОстаточныйРесурсПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Моточасы*3600);
		СуммаОстаточныйРесурсПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Сумма, ВалютаУпрУчета);
	КонецЕсли;
	
	Выборка = ПакетЗапросов[0].Выбрать();
	
	Если Выборка.Следующий() Тогда
		Пробег = Выборка.ПробегОстаток;
		Моточасы = Выборка.МоточасыОстаток;
		ПробегПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(Пробег);
		МоточасыПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Моточасы*3600);
		УстановитьПривилегированныйРежим(Истина);
		ЗапросДокРемонт = Новый Запрос;
		ЗапросДокРемонт.Текст = 
		"ВЫБРАТЬ
		|	упРемонтИТОТС.Ссылка КАК Ссылка,
		|	упРемонтИТОТС.Дата КАК Дата,
		|	упРемонтИТОТС.НачалоРемонтаПлан КАК НачалоРемонтаПлан,
		|	упРемонтИТОТС.НачалоРемонтаФакт КАК НачалоРемонтаФакт
		|ИЗ
		|	Документ.упРемонтИТОТС КАК упРемонтИТОТС
		|ГДЕ
		|	упРемонтИТОТС.Дата < &Дата
		|	И упРемонтИТОТС.ТранспортноеСредство = &ТранспортноеСредство";
		ЗапросДокРемонт.УстановитьПараметр("Дата", Объект.Дата);
		ЗапросДокРемонт.УстановитьПараметр("ТранспортноеСредство", ТекущееУстройство);
		ВыборкаДок = ЗапросДокРемонт.Выполнить().Выбрать();
		Если ВыборкаДок.Следующий() Тогда
			Период = ВыборкаДок.Дата;
			Если ЗначениеЗаполнено(ВыборкаДок.НачалоРемонтаФакт) Тогда
				Период = ВыборкаДок.НачалоРемонтаФакт;
			ИначеЕсли ЗначениеЗаполнено(ВыборкаДок.НачалоРемонтаПлан) Тогда
				Период = ВыборкаДок.НачалоРемонтаПлан;
			КонецЕсли;
			Запрос.УстановитьПараметр("Период", Период);
			ПакетЗапросов = Запрос.ВыполнитьПакет();
			Выборка = ПакетЗапросов[0].Выбрать();
			Если Выборка.Следующий() Тогда
				МежремонтныйПробег = Пробег - Выборка.ПробегОстаток;
				МежремонтныйПробегПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(МежремонтныйПробег);
			КонецЕсли;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

КонецПроцедуры // УстановитьПредставлениеПараметровРемонта()

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#Область СобытияШапки

&НаКлиенте
Процедура ТранспортноеСредствоИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОжь;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОжь;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	сткПараметры = Новый Структура("Организация, СкрытьНеучетные,ЭтоСобственноеТС", Объект.Организация, Истина,Истина);
	ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьЭтоРемонтТС(ОбъектРемонта, СписокТипов = Неопределено)

	РемонтТС = Истина;
	
	Если ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ДополнительноеОборудование")Тогда
		РемонтТС = Ложь;
		Если НЕ СписокТипов = Неопределено Тогда
			СписокТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.ПлановоеТО"));
			СписокТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.Ремонт"));
		КонецЕсли;
	ИначеЕсли ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат")Тогда		
		РемонтТС = Ложь;
		Если НЕ СписокТипов = Неопределено Тогда
			СписокТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.ПлановоеТО"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат РемонтТС;

КонецФункции // ПроверитьЭтоРемонтТС()

&НаКлиенте
Процедура ВидРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеВидыРемонта = Новый Структура("Вид",Объект.ВидРемонта);
	МассивТиповРемонта = Новый Массив;
	Если НЕ ПроверитьЭтоРемонтТС(Объект.ОбъектРемонта, МассивТиповРемонта) Тогда				
		сткОтбора = Новый Структура("ТипРемонта", МассивТиповРемонта);
		сткПараметры = Новый Структура("Отбор", сткОтбора);
		ОткрытьФорму("Справочник.упВидыРемонтов.Форма.ФормаВыбора", сткПараметры, Элемент);
		СтандартнаяОбработка = Ложь;		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРемонтаПриИзменении(Элемент)	
	
	Если Объект.Работы.Количество() Тогда		
		ПараметрыОтвета = Новый Структура;
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаВидРемонта", ЭтаФорма,ПараметрыОтвета);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'При изменении «Вида ремонта» табличная часть «Работы» будет перезаполнена. Продолжить?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
	Иначе	
		ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта();
		ОбновитьВидФормыПоВидуРемонта();
		ЗаполнитьРаботыПоУмолчаниюНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРемонтаПриИзменении(Элемент)
	
   ОбновитьВидФормыПоСпособуРемонта();
	
КонецПроцедуры

&НаКлиенте
Процедура ГаражПриИзменении(Элемент)
	
	УстановитьОтметкуНезаполненного();
	
КонецПроцедуры

&НаКлиенте
Процедура МестоРемонтаПриИзменении(Элемент)
	
	УстановитьОтметкуНезаполненного();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПланПриИзменении(Элемент)
	
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, СтавкаНДС, СуммаНДС, Количество, Цена", Объект.СуммаРасходыПланВалюта, Объект.СтавкаРасходыПланНДС, Объект.СуммаРасходыПланНДСВалюта, 1, Объект.СуммаРасходыПланВалюта);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "Сумма", СтруктураДанные);
	Объект.СуммаРасходыПланВалюта		= сткСтрока.Сумма;
	Объект.СуммаРасходыПланНДСВалюта	= сткСтрока.СуммаНДС;
	
	Если ВедетсяВалютныйУчет Тогда
		Объект.СуммаРасходыПлан 	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Объект.Валюта, Объект.СуммаРасходыПланВалюта, Объект.Дата);
		Объект.СуммаРасходыПланНДС	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Объект.Валюта, Объект.СуммаРасходыПланНДСВалюта, Объект.Дата);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаРасходыПланНДСПриИзменении(Элемент)
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, СтавкаНДС, СуммаНДС, Количество, Цена", Объект.СуммаРасходыПланВалюта, Объект.СтавкаРасходыПланНДС, Объект.СуммаРасходыПланНДСВалюта, 1, Объект.СуммаРасходыПланВалюта);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "Сумма", СтруктураДанные);
	Объект.СуммаРасходыПланНДСВалюта	= сткСтрока.СуммаНДС;
	Если ВедетсяВалютныйУчет Тогда
		Объект.СуммаРасходыПланНДС	= упСервисныеФункцииВызовСервера.РассчитатьСуммуУпр(Объект.Валюта, Объект.СуммаРасходыПланНДСВалюта, Объект.Дата);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	Элементы.ГруппаПрицепы.Доступность = Не ЭтоПрицеп(Объект.ТранспортноеСредство);
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеРемонтаПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура АгрегатПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура НачалоРемонтаПланПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

&НаКлиенте
Процедура НачалоРемонтаФактПриИзменении(Элемент)
	УстановитьТранспортноеСредствоОбъектаРемонта();
КонецПроцедуры

#КонецОбласти

#Область СобытияСтраниц

#Область СобытияОбщее

&НаКлиенте
Процедура ОбъектРемонтаПриИзменении(Элемент)
	
	ОбъектРемонтаУстановитьСтраницу();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзмененаВыработкаАгрегатаПриИзменении(Элемент)
	
	Элементы.ГруппаПробегИзменить.ТолькоПросмотр = НЕ Объект.ИзмененаВыработка;
	
КонецПроцедуры

#КонецОбласти

#Область СтраницаЗапчасти

&НаКлиенте
Процедура ЗапчастиПриИзменении(Элемент)
	ВычислитьЗапчастиСуммыВВалютеУправленческогоУчета();
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиСобственныеДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораДополнительноеОборудование(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораДополнительноеОборудование(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.Запчасти.ТекущиеДанные;
		текСтрока.Валюта = ВалютаРеглУчета;
		текСтрока.Количество = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Запчасти");	
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Запчасти");	
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Запчасти");	
КонецПроцедуры

&НаКлиенте
Процедура ЗапчастиСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Запчасти");
КонецПроцедуры

#КонецОбласти 

#Область СтраницаРаботы

&НаКлиенте
Процедура РаботыПриИзменении(Элемент)
	ВычислитьРаботыСуммыВВалютеУправленческогоУчета();
КонецПроцедуры

&НаКлиенте
Процедура РаботыДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораДополнительноеОборудование(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Механик"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Прочее"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Оператор"));
	ТекущийОтбор = Новый Структура("ТипСотрудника", СписокЗначений);
	сткПараметры = Новый Структура("Отбор", ТекущийОтбор);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИспольнительНачалоВыбораЗавершение", ЭтаФорма);
	ОткрытьФорму("Справочник.упСотрудники.ФормаВыбора", сткПараметры,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	Если НЕ ПроверитьТипСотрудника(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		ТекстОшибки = "Укажите исполнителя с типом: Механик, Прочее, Оператор";
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.упСотрудники.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.Работы.ТекущиеДанные;
		текСтрока.Валюта = ВалютаРеглУчета;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РаботыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура РаботыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

&НаКлиенте
Процедура РаботыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

&НаКлиенте
Процедура РаботыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "Работы");
КонецПроцедуры

#КонецОбласти 

#Область СтраницаГСМ

&НаКлиенте
Процедура ГСМПриИзменении(Элемент)
	ВычислитьГСМСуммыВВалютеУправленческогоУчета();
КонецПроцедуры

&НаКлиенте
Процедура ГСМПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.ГСМ.ТекущиеДанные;
		текСтрока.Валюта = ВалютаРеглУчета;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ГСМПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ГСМСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока	= Элементы["ГСМ"].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущаяСтрока.Сумма, ТекущаяСтрока.Сумма, ТекущаяСтрока.СтавкаНДС, ТекущаяСтрока.СуммаНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	ТекущаяСтрока.СуммаНДС = сткСтрока.СуммаНДС;

КонецПроцедуры

&НаКлиенте
Процедура ГСМСтавкаНДСПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы["ГСМ"].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущаяСтрока.Сумма, ТекущаяСтрока.Сумма, ТекущаяСтрока.СтавкаНДС, ТекущаяСтрока.СуммаНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	ТекущаяСтрока.СуммаНДС = сткСтрока.СуммаНДС;
КонецПроцедуры

&НаКлиенте
Процедура ГСМКонечныйОстатокПриИзменении(Элемент)
	
	текСтрока = Элементы.ГСМ.ТекущиеДанные;
	Если текСтрока.КонечныйОстаток > (текСтрока.НачальныйОстаток + текСтрока.Расход  + текСтрока.РасходСоСклада)Тогда
		текСтрока.КонечныйОстаток = текСтрока.НачальныйОстаток + текСтрока.Расход + текСтрока.РасходСоСклада;
		ТекстСообщения = НСтр("ru = 'Конечный остаток ГСМ не может быть больше начального остатка с учетом заправленного объема. Значение скорректировано.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ГСМКонечныйОстаток");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМОстатокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ГСМ.ТекущиеДанные;
	ТекущаяСтрока.КонечныйОстаток = ТекущаяСтрока.НачальныйОстаток + ТекущаяСтрока.Расход + ТекущаяСтрока.РасходСоСклада;
	
КонецПроцедуры
	
#КонецОбласти 

#Область СтраницаДопОборудование

&НаКлиенте
Процедура ДополнительноеОборудованиеПриИзменении(Элемент)
	ВычислитьДопОбрСуммыВВалютеУправленческогоУчета();
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораДополнительноеОборудование(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокДопОборудывания = ПолучитьСписокВозможногоДопОборудывания();
	
	сткОтбора = Новый Структура("Ссылка", СписокДопОборудывания);
	сткПараметры = Новый Структура("Отбор", сткОтбора);
	ОткрытьФорму("Справочник.упДополнительноеОборудование.Форма.ФормаВыбора", сткПараметры, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ДополнительноеОборудование");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеСобственноеСкладПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ДополнительноеОборудованиеСобственное.ТекущиеДанные;
	Если Истина
		И НЕ ТекущиеДанные = Неопределено
		И НЕ ЗначениеЗаполнено(ТекущиеДанные.Склад) 
	Тогда
		ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.ДополнительноеОборудование.ТекущиеДанные;
	сткПараметры = Новый Структура();
	мсвСостояния = Новый Массив;
	Если ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Снято") Тогда
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		
	ИначеЕсли ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено") Тогда	
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));	
		
	Иначе	
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));		
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		
	КонецЕсли; 
	
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительноеОборудованиеСобственноеДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упДополнительноеОборудование.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


#КонецОбласти

#Область СтраницаШины

&НаКлиенте
Процедура ШиныУзлыАгрегатыПриИзменении(Элемент)
	ВычислитьШиныУзлыАгрегатыСуммыВВалютеУправленческогоУчета();
КонецПроцедуры

&НаКлиенте
Процедура ШиныУзлыАгрегатыСобственныеАгрегатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеОтбора  = Новый Структура("МодельТС", Объект.ТранспортноеСредство);
	мсвСостояния = Новый Массив;
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
	мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
	ЗначениеОтбора.Вставить("Состояние", мсвСостояния);

	ПараметрыВыбораАгрегата = Новый Структура("Отбор", ЗначениеОтбора);
	ОткрытьФорму("Справочник.упШиныУзлыИАгрегаты.Форма.ФормаВыбора", ПараметрыВыбораАгрегата, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура АгрегатАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗначениеОтбора  = Новый Структура("ТС", Объект.ТранспортноеСредство);
	ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура АгрегатОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗначениеОтбора  = Новый Структура("ТС", Объект.ТранспортноеСредство);
	ПараметрыПолученияДанных = Новый Структура("Отбор", ЗначениеОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ШиныПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		
	Если НоваяСтрока И Не Копирование Тогда
		ТекущиеДанные = Элементы.ШиныУзлыАгрегаты.ТекущиеДанные;	
		Если ВедетсяВалютныйУчет Тогда
			ТекущиеДанные.Валюта = ?(ЗначениеЗаполнено(ВалютаРеглУчета),ВалютаРеглУчета,Объект.Валюта);	
		КонецЕсли;
		ТекущиеДанные.Количество = 1;
		ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ШиныУзлыАгрегатыСобственныеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		ТекущиеДанные = Элементы.ШиныУзлыАгрегатыСобственные.ТекущиеДанные;
		ТекущиеДанные.Количество = 1;
		ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШиныУзлыАгрегатыСобственныеАгрегатПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ШиныУзлыАгрегатыСобственные.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные.ЭтоШина = ПолучитьВидАгрегата(ТекущиеДанные.Агрегат);	
		ТекущиеДанные.Количество = 1;
		Если НЕ ТекущиеДанные.ЭтоШина Тогда
			ТекущиеДанные.ХодоваяШина = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШиныУзлыАгрегатыСобственныеСкладПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ШиныУзлыАгрегатыСобственные.ТекущиеДанные;
	Если Истина
		И НЕ ТекущиеДанные = Неопределено
		И НЕ ЗначениеЗаполнено(ТекущиеДанные.Склад) 
	Тогда
		ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШиныШинаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ШиныУзлыАгрегаты.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные.ЭтоШина = ПолучитьВидАгрегата(ТекущиеДанные.Агрегат);	
		ТекущиеДанные.Количество = 1;
		Если НЕ ТекущиеДанные.ЭтоШина Тогда
			ТекущиеДанные.ХодоваяШина = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
// Проверка является ли элемент шиной.
//
// Параметры:
//  Агрегат  - СправочникСсылка.упШиныУзлыИАгрегаты - элемент справочника.
//
// Возвращаемое значение:
//   Булево   - значение это шина.
//
Функция ПолучитьВидАгрегата(Агрегат)

	Возврат Справочники.упШиныУзлыИАгрегаты.ЭтоШина(Агрегат);

КонецФункции // ПолучитьВидАгрегата()

&НаКлиенте
Процедура ШиныПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ШиныПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ШиныСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ШиныУзлыАгрегаты");
КонецПроцедуры

&НаКлиенте
Процедура ШиныЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ШиныУзлыАгрегаты");
КонецПроцедуры

&НаКлиенте
Процедура ШиныКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ШиныУзлыАгрегаты");
КонецПроцедуры

&НаКлиенте
Процедура ШиныСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент, "ШиныУзлыАгрегаты");
КонецПроцедуры

&НаКлиенте
Процедура ШиныШинаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ШиныУзлыАгрегаты.ТекущиеДанные;
	сткПараметры  = Новый Структура();
	мсвСостояния  = Новый Массив;
	
	Если ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Снято") Тогда
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		
	ИначеЕсли ТекущиеДанные.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено") Тогда
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));	
		
	Иначе	
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.НеИспользуется"));
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));		
		мсвСостояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		
	КонецЕсли; 
	
	сткОтбор     = Новый Структура("Состояние", мсвСостояния);
	сткПараметры.Вставить("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упШиныУзлыИАгрегаты.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти 

#Область СтраницаПрицепы

&НаКлиенте
Процедура ПрицепыПрицепНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткПараметры = Новый Структура("Отбор", Новый Структура("Прицеп", Истина));
	ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ПрицепыПрицепНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПрицепНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	сткПараметры = Новый Структура("Отбор", Новый Структура("Прицеп", Истина));
	ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ПрицепыПрицепНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПрицепыПрицепПриИзменении(Элемент)
	ПользовательскоеСообщение = "";
	Прицеп = ТекущийЭлемент.ТекущиеДанные.Прицеп;
	ТекущийЭлемент.ТекущиеДанные.Прицеп = ?(ЗначениеЗаполнено(Прицеп),ПроверитьПрицепПриИзмененииНаСервере(Прицеп, Объект.Дата, ПользовательскоеСообщение), Прицеп);
	Если ЗначениеЗаполнено(ПользовательскоеСообщение) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ПользовательскоеСообщение;
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры


#КонецОбласти 

#Область СтраницаГСМДополнительногоОборудования

&НаКлиенте
Процедура ГСМДополнительногоОборудованияПриИзменении(Элемент)
	ВычислитьГСМДопОбрСуммыВВалютеУправленческогоУчета();
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораДополнительноеОборудование(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияНачальныйОстатокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ГСМДополнительногоОборудования.ТекущиеДанные;
	ТекущаяСтрока.КонечныйОстаток = ТекущаяСтрока.НачальныйОстаток + ТекущаяСтрока.Расход + ТекущаяСтрока.РасходСоСклада;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияКонечныйОстатокПриИзменении(Элемент)
	
	текСтрока = Элементы.ГСМДополнительногоОборудования.ТекущиеДанные;
	Если текСтрока.КонечныйОстаток > (текСтрока.НачальныйОстаток + текСтрока.Расход  + текСтрока.РасходСоСклада)Тогда
		текСтрока.КонечныйОстаток = текСтрока.НачальныйОстаток + текСтрока.Расход + текСтрока.РасходСоСклада;
		ТекстСообщения = НСтр("ru = 'Конечный остаток ГСМ не может быть больше начального остатка с учетом заправленного объема. Значение скорректировано.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ГСМДополнительногоОборудованияКонечныйОстаток");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока	= Элементы["ГСМДополнительногоОборудования"].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущаяСтрока.Сумма, ТекущаяСтрока.Сумма, ТекущаяСтрока.СтавкаНДС, ТекущаяСтрока.СуммаНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	ТекущаяСтрока.СуммаНДС = сткСтрока.СуммаНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока	= Элементы["ГСМДополнительногоОборудования"].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	сткСтрока 		= Новый Структура("Сумма, Цена, СтавкаНДС, СуммаНДС, Количество", ТекущаяСтрока.Сумма, ТекущаяСтрока.Сумма, ТекущаяСтрока.СтавкаНДС, ТекущаяСтрока.СуммаНДС, 1);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(сткСтрока, "СтавкаНДС", СтруктураДанные);
	ТекущаяСтрока.СуммаНДС = сткСтрока.СуммаНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.ГСМДополнительногоОборудования.ТекущиеДанные;
		текСтрока.Валюта = ВалютаРеглУчета;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ГСМДополнительногоОборудованияПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

#КонецОбласти 

#Область СтраницаГСМСобственныеДополнительногоОборудования

&НаКлиенте
Процедура ДополнительноеОборудованиеСобственноеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		текСтрока = Элементы.ДополнительноеОборудованиеСобственное.ТекущиеДанные;
		текСтрока.Количество = 1;
		текСтрока.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГСМСобственныеДополнительногоОборудованияДополнительноеОборудованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораДополнительноеОборудование(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти 

#Область СтраницаРемонтируемоеДополнительноеОборудование

&НаКлиенте
Процедура РемонтируемоеДополнительноеОборудованиеВидРемонтаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	МассивТиповРемонта = Новый Массив;
	МассивТиповРемонта.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.Ремонт"));
	МассивТиповРемонта.Добавить(ПредопределенноеЗначение("Перечисление.упТипыРемонтов.ПлановоеТО"));
	
	сткОтбора = Новый Структура("ТипРемонта", МассивТиповРемонта);
	сткПараметры = Новый Структура("Отбор", сткОтбора);
	ОткрытьФорму("Справочник.упВидыРемонтов.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ЗаполнитьОстаткамиНаТС(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите транспортное средство'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ТранспортноеСредство");
		Возврат;
	КонецЕсли; 
	
	Если Объект.РемонтируемоеДополнительноеОборудование.Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Ремонтируемое дополнительное оборудование будут перезаполнены оборудование установленном транспортном средстве. Очистить список ремонтируемого дополнительного оборудования?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРемонтируемоеДополнительноеОборудованиеОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтменен(Команда)
	
	Если Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ТекстОшибки = "";
		Если УстановитьСтатусОтмененСервер(ТекстОшибки) Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
		Иначе
		//ПоказатьПредупреждение(, ТекстОшибки);
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Ремонт ТС завершен, отмена невозможна'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусЗапланирован(Команда)
	
	ТекстОшибки = "";
	Если УстановитьСтатусЗапланированСервер(ТекстОшибки) Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
		
		Структура = Новый Структура("Статус, Ссылка",ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Запланирован"),Объект.Ссылка);
		Структура.Вставить("ВидРемонта",Объект.ВидРемонта);
		Оповестить("ИзменениеСтатусаРемонтИТОТС",Структура,ЭтаФорма);
		
	Иначе
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			//ПоказатьПредупреждение(, ТекстОшибки);
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли; 
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура СтатусНачат(Команда)
	
	ТекстОшибки = "";
	Если УстановитьСтатусНачатСервер(ТекстОшибки) Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
		
		Структура = Новый Структура("Статус, Ссылка",ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Начат"),Объект.Ссылка);
		Структура.Вставить("ВидРемонта",Объект.ВидРемонта);
		Оповестить("ИзменениеСтатусаРемонтИТОТС",Структура,ЭтаФорма);
		
	Иначе
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			//ПоказатьПредупреждение(, ТекстОшибки);
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли; 
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусЗавершен(Команда)
	
	ТекстОшибки = "";
	Если УстановитьСтатусЗавершенСервер(ТекстОшибки) Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
	Иначе
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			//ПоказатьПредупреждение(, ТекстОшибки);
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРаботыПоУмолчанию(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите вид ремонта'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ВидРемонта");
		Возврат;
	КонецЕсли; 
	
	Если Объект.Работы.Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Работы будут перезаполнены работами по умолчанию для вида ремонта. Продолжить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРаботыПоУмолчаниюОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ЗаполнитьРаботыПоУмолчаниюНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстаткамиГСМ(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ремонт завершен.'"),,НСтр("ru = 'Заполнение отменено.'"), БиблиотекаКартинок.Успешно32);
		Возврат;
	КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
			ТекстСообщения = НСтр("ru = 'Укажите транспортное средство'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ТранспортноеСредство");
			Возврат;
		КонецЕсли; 
		
		ТекстВопроса = НСтр("ru = 'Табличная часть ГСМ будет очищена. ГСМ будет заполнено в соответствии с остатками на дату документа. Продолжить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьОстаткамиГСМОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
	
	КонецПроцедуры
	
	&НаКлиенте
Процедура ЗаполнитьБезналичнаяОплата(Команда)
	ФормаОплаты = ПредопределенноеЗначение("Перечисление.упДоступныеФормыОплаты.Безналичная");
	ЗаполнитьФормуОплаты(ФормаОплаты);	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФормуОплаты(ФормаОплаты)
	ИменаТабличныхЧастей = "ГСМ,Запчасти,Работы,ШиныУзлыАгрегаты";
	МассивИмен = СтрРазделить(ИменаТабличныхЧастей, ",");
	Модифицированность = Истина;
	Для каждого ИмяТабличнойЧасти Из МассивИмен Цикл
		ЭтаТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
		Для каждого СтрокаТаблицы Из ЭтаТабличнаяЧасть Цикл
			СтрокаТаблицы["ФормаОплаты"] = ФормаОплаты;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьФормуОплаты()

&НаКлиенте
Процедура ЗаполнитьНаличнаяОплата(Команда)
	ФормаОплаты = ПредопределенноеЗначение("Перечисление.упДоступныеФормыОплаты.Наличная");
	ЗаполнитьФормуОплаты(ФормаОплаты);
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнитьОстаткамиГСМДопОбр(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ремонт завершен.'"),,НСтр("ru = 'Заполнение отменено.'"), БиблиотекаКартинок.Успешно32);
		Возврат;
	КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
			ТекстСообщения = НСтр("ru = 'Укажите транспортное средство'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ТранспортноеСредство");
			Возврат;
		КонецЕсли; 
		
		ТекстВопроса = НСтр("ru = 'Табличная часть ГСМ будет очищена. ГСМ будет заполнено в соответствии с остатками на дату документа. Продолжить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьОстаткамиГСМДопОбрОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьРасходСоСклада(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ремонт завершен.'"),,НСтр("ru = 'Заполнение отменено.'"), БиблиотекаКартинок.Успешно32);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Количество ГСМ в колонке заправлено со склада будет перезаполнено. Продолжить?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗаправленоСоСкладаОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасходСоСкладаДопОбр(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ремонт завершен.'"),,НСтр("ru = 'Заполнение отменено.'"), БиблиотекаКартинок.Успешно32);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Количество ГСМ в колонке заправлено со склада будет перезаполнено. Продолжить?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗаправленоСоСкладаДопОбрОтветНаВопрос", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПериодРаспределенияЗатратНачало", "ПериодРаспределенияЗатратОкончание"));
		
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьГСМ(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ремонт завершен.'"),,НСтр("ru = 'Подбор отменен.'"), БиблиотекаКартинок.Успешно32);
		Возврат;
	КонецЕсли;
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("Материал", "ГСМ");
	ОткрытьФорму("ОбщаяФорма.упПодборМатериалов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборГСМЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьГСМДопОбр(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ремонт завершен.'"),,НСтр("ru = 'Подбор отменен.'"), БиблиотекаКартинок.Успешно32);
		Возврат;
	КонецЕсли;
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("Материал", "ГСМ");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяТаблицы", "ГСМСобственныеДополнительногоОборудования");
	
	ОткрытьФорму("ОбщаяФорма.упПодборМатериалов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборГСМЗавершение", ЭтаФорма, ДополнительныеПараметры), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьЗапчасти(Команда)
	
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
	сткПараметры.Вставить("Материал", "Запчасти");
	сткПараметры.Вставить("ТранспортноеСредство", Объект.ТранспортноеСредство);
	ОткрытьФорму("ОбщаяФорма.упПодборМатериалов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодобратьЗапчастиЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьСоСклада(Команда)
	Если ЗаполненоТранспортноеСредство() Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
		сткПараметры.Вставить("Склад", Неопределено);
		Состояния = Новый СписокЗначений;
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
		сткПараметры.Вставить("Состояние", Состояния);
		сткПараметры.Вставить("Склад", Неопределено);
		сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упРемонтИТОТС"));
		сткПараметры.Вставить("МодельТС", Объект.ТранспортноеСредство);
		ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборАгрегатовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьУстановленныеНаТранспорт(Команда)
	Если ЗаполненоТранспортноеСредство() Тогда	
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
		сткПараметры.Вставить("Склад", Неопределено);
		Состояния = Новый СписокЗначений;
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		сткПараметры.Вставить("Состояние", Состояния);
		сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упРемонтИТОТС"));
		сткПараметры.Вставить("ТранспортноеСредство", Объект.ТранспортноеСредство);
		ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборАгрегатовЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьСоСкладаДопОборудование(Команда)
	Если ЗаполненоТранспортноеСредство() Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
		сткПараметры.Вставить("Склад", Неопределено);
		Состояния = Новый СписокЗначений;
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.ОприходованоНаСклад"));
		сткПараметры.Вставить("Состояние", Состояния);
		сткПараметры.Вставить("ТипОборудования", ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка"));
		сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упРемонтИТОТС"));
		сткПараметры.Вставить("МодельТС",Объект.ТранспортноеСредство);
		ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборДопОборудованияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьУстановленныеНаТранспортДопОборудование(Команда)
	Если ЗаполненоТранспортноеСредство() Тогда	
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ДатаОстатков", Объект.Дата);
		сткПараметры.Вставить("Склад", Неопределено);
		Состояния = Новый СписокЗначений;
		Состояния.Добавить(ПредопределенноеЗначение("Справочник.упСостоянияОборудования.УстановленоНаТС"));
		сткПараметры.Вставить("Состояние", Состояния);
		сткПараметры.Вставить("ТипОборудования", ПредопределенноеЗначение("Справочник.упДополнительноеОборудование.ПустаяСсылка"));
		сткПараметры.Вставить("ТипДокумента", ТИП("ДокументСсылка.упРемонтИТОТС"));
		сткПараметры.Вставить("ТранспортноеСредство", Объект.ТранспортноеСредство);
		ОткрытьФорму("ОбщаяФорма.упПодборАгрегатов", сткПараметры,,,,, Новый ОписаниеОповещения("ПодборДопОборудованияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВычислитьСуммыВВалютеУправленческогоУчета()
	ВычислитьГСМСуммыВВалютеУправленческогоУчета();
	ВычислитьГСМДопОбрСуммыВВалютеУправленческогоУчета();
	ВычислитьДопОбрСуммыВВалютеУправленческогоУчета();
	ВычислитьЗапчастиСуммыВВалютеУправленческогоУчета();
	ВычислитьРаботыСуммыВВалютеУправленческогоУчета();
	ВычислитьШиныУзлыАгрегатыСуммыВВалютеУправленческогоУчета();
КонецПроцедуры // ВычислитьСуммыВВалютеУправленческогоУчета()

&НаСервере
Процедура ВычислитьГСМСуммыВВалютеУправленческогоУчета()	
	Если Объект.ГСМ.Количество() Тогда
		Сумма = Объект.ГСМ.Итог("Сумма");		
		Объект.ГСМ[0].СуммаИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, Сумма, Объект.Дата);
		СуммаНДС = Объект.ГСМ.Итог("СуммаНДС");
		Объект.ГСМ[0].СуммаНДСИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, СуммаНДС, Объект.Дата);		
	КонецЕсли;
КонецПроцедуры // ВычислитьГСМСуммыВВалютеУправленческогоУчета()

&НаСервереБезКонтекста
Функция РассчитатьСуммуРегл(Валюта, Сумма, Дата)
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат Сумма;
	КонецЕсли;	
	Возврат упСервисныеФункцииВызовСервера.РассчитатьСуммуРегл(Валюта, Сумма, Дата);
КонецФункции // РассчитатьСуммуРегл()

&НаСервере
Процедура ВычислитьГСМДопОбрСуммыВВалютеУправленческогоУчета()	
	Если Объект.ГСМДополнительногоОборудования.Количество() Тогда
		Сумма = Объект.ГСМДополнительногоОборудования.Итог("Сумма");		
		Объект.ГСМДополнительногоОборудования[0].СуммаИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, Сумма, Объект.Дата);
		СуммаНДС = Объект.ГСМДополнительногоОборудования.Итог("СуммаНДС");
		Объект.ГСМДополнительногоОборудования[0].СуммаНДСИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, СуммаНДС, Объект.Дата);		
	КонецЕсли;
КонецПроцедуры // ВычислитьГСМДопОбрСуммыВВалютеУправленческогоУчета()

&НаСервере
Процедура ВычислитьДопОбрСуммыВВалютеУправленческогоУчета()
	Если Объект.ДополнительноеОборудование.Количество() Тогда
		Сумма = Объект.ДополнительноеОборудование.Итог("Сумма");		
		Объект.ДополнительноеОборудование[0].СуммаИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, Сумма, Объект.Дата);
		СуммаНДС = Объект.ДополнительноеОборудование.Итог("СуммаНДС");
		Объект.ДополнительноеОборудование[0].СуммаНДСИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, СуммаНДС, Объект.Дата);		
	КонецЕсли;
КонецПроцедуры // ВычислитьДопОбрСуммыВВалютеУправленческогоУчета()

&НаСервере
Процедура ВычислитьЗапчастиСуммыВВалютеУправленческогоУчета()
	Если Объект.Запчасти.Количество() Тогда
		Сумма = Объект.Запчасти.Итог("Сумма");		
		Объект.Запчасти[0].СуммаИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, Сумма, Объект.Дата);
		СуммаНДС = Объект.Запчасти.Итог("СуммаНДС");
		Объект.Запчасти[0].СуммаНДСИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, СуммаНДС, Объект.Дата);		
	КонецЕсли;
КонецПроцедуры // ВычислитьЗапчастиСуммыВВалютеУправленческогоУчета()

&НаСервере
Процедура ВычислитьРаботыСуммыВВалютеУправленческогоУчета()
	Если Объект.Работы.Количество() Тогда
		Сумма = Объект.Работы.Итог("Сумма");		
		Объект.Работы[0].СуммаИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, Сумма, Объект.Дата);
		СуммаНДС = Объект.Работы.Итог("СуммаНДС");
		Объект.Работы[0].СуммаНДСИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, СуммаНДС, Объект.Дата);		
	КонецЕсли;
КонецПроцедуры // ВычислитьРаботыСуммыВВалютеУправленческогоУчета()

&НаСервере
Процедура ВычислитьШиныУзлыАгрегатыСуммыВВалютеУправленческогоУчета()
	Если Объект.ШиныУзлыАгрегаты.Количество() Тогда
		Сумма = Объект.ШиныУзлыАгрегаты.Итог("Сумма");		
		Объект.ШиныУзлыАгрегаты[0].СуммаИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, Сумма, Объект.Дата);
		СуммаНДС = Объект.ШиныУзлыАгрегаты.Итог("СуммаНДС");
		Объект.ШиныУзлыАгрегаты[0].СуммаНДСИтог = РассчитатьСуммуРегл(ВалютаРеглУчета, СуммаНДС, Объект.Дата);		
	КонецЕсли;
КонецПроцедуры // ВычислитьШиныУзлыАгрегатыСуммыВВалютеУправленческогоУчета()

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаКлиенте
// Согласно указанному виду ремонта выполним очистку табличных частей.
//
Процедура ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта()
	
	ЗаполненыеОбъекты = ПолучитьТабличныеЧастиДляОчистки();
	
	Если ЗаполненыеОбъекты.Количество() Тогда
		
		ПараметрыОтвета = Новый Структура;
		ПараметрыОтвета.Вставить("Таблицы",ЗаполненыеОбъекты);
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаТаблицыОчистки", ЭтаФорма,ПараметрыОтвета);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'При изменении «Вида ремонта» найденны заполненые таблицы, данные таблиц будут очищены. Продолжить?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта()

&НаКлиенте
Процедура ПослеЗакрытияВопросаТаблицыОчистки(Результат, ПараметрыОтвета) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда		
		Возврат;
	КонецЕсли;
	
	Массив = ПараметрыОтвета.Таблицы;
	
	Для каждого ТекущаяТабличнаяЧасть Из Массив Цикл
		ТабличнаяЧасть = Объект[ТекущаяТабличнаяЧасть];
		ТабличнаяЧасть.Очистить();
	КонецЦикла;
	
КонецПроцедуры // ПослеЗакрытияВопросаТаблицыОчистки()

&НаСервере	
// Выполним проверку и получим массив табличных частей для очистки.
//
// Возвращаемое значение:
//   Массив - Значение строка, список табличных частей.
//
Функция ПолучитьТабличныеЧастиДляОчистки()
	
	МассивТиповРемонта = ПолучитьТипыРемонта();
	
	Массив = Новый Массив;
	
	Если НЕ (МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.Ремонт) = Неопределено
		ИЛИ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.ПлановоеТО) = Неопределено) Тогда
		Массив.Добавить("ДополнительноеОборудование");
		Массив.Добавить("ШиныУзлыАгрегаты");
		Массив.Добавить("ШиныУзлыАгрегатыСобственные");
		Массив.Добавить("Прицепы");
	ИначеЕсли НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.МонтажОборудования) = Неопределено Тогда		
		Массив.Добавить("Запчасти");
		Массив.Добавить("ЗапчастиСобственные");
		Массив.Добавить("Работы");
		Массив.Добавить("Прицепы");
		Массив.Добавить("ШиныУзлыАгрегаты");
		Массив.Добавить("ШиныУзлыАгрегатыСобственные");
	ИначеЕсли НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.ЗаменаШин) = Неопределено Тогда
		Массив.Добавить("Запчасти");
		Массив.Добавить("ЗапчастиСобственные");		
		Массив.Добавить("ГСМ");
		Массив.Добавить("ГСМСобственные");		
		Массив.Добавить("ГСМДополнительногоОборудования");
		Массив.Добавить("ГСМСобственныеДополнительногоОборудования");		
		Массив.Добавить("ДополнительноеОборудование");
		Массив.Добавить("Прицепы");
	ИначеЕсли НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.МонтажПрицепов) = Неопределено Тогда
		Массив.Добавить("Запчасти");
		Массив.Добавить("ЗапчастиСобственные");		
		Массив.Добавить("ГСМ");
		Массив.Добавить("ГСМСобственные");		
		Массив.Добавить("ГСМДополнительногоОборудования");
		Массив.Добавить("ГСМСобственныеДополнительногоОборудования");		
		Массив.Добавить("ДополнительноеОборудование");
		Массив.Добавить("ШиныУзлыАгрегаты");
		Массив.Добавить("ШиныУзлыАгрегатыСобственные");
	КонецЕсли;

	Возврат Массив;
	
КонецФункции // ПолучитьТабличныеЧастиДляОчистки()

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#Область УправлениеСтатусом

&НаКлиенте
// Устоновим доступность для элементов управления формы.
//
Процедура УстановитьДоступностьЭлементов()

	Элементы.СпособРемонта.ТолькоПросмотр = Ложь;
	//Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = Ложь;
	Элементы.ГруппаИзмененаВыработкаАгрегата.ТолькоПросмотр = Ложь;
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Начат") ИЛИ Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		Элементы.СпособРемонта.ТолькоПросмотр = Истина;
		Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = Истина;
		Элементы.ГруппаИзмененаВыработкаАгрегата.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	ТипИспользования = Ложь;
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРемонтаИТОТС.Завершен") Тогда 
		ТипИспользования = Истина;
	КонецЕсли;
	
	Элементы.ГСМСобственные.ТолькоПросмотр = ТипИспользования;
	Элементы.ГСМ.ТолькоПросмотр = ТипИспользования;
	
КонецПроцедуры // УстановитьДоступностьЭлементов()

&НаСервере
Функция УстановитьСтатусЗапланированСервер(ТекстОшибки)
		
	Возврат ИзменитьСтатусРемонта(Перечисления.упСтатусыРемонтаИТОТС.Запланирован, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусНачатСервер(ТекстОшибки)
	
	Если Не ЗначениеЗаполнено(Объект.НачалоРемонтаФакт) Тогда
		Объект.НачалоРемонтаФакт = ТекущаяДатаСеанса();
	КонецЕсли; 
	Возврат ИзменитьСтатусРемонта(Перечисления.упСтатусыРемонтаИТОТС.Начат, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусЗавершенСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусРемонта(Перечисления.упСтатусыРемонтаИТОТС.Завершен, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОтмененСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусРемонта(Перечисления.упСтатусыРемонтаИТОТС.Отменен, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция ИзменитьСтатусРемонта(СтатусРемонта, ТекстОшибки)
	
	НовыйСтатус = СтатусРемонта;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
        	Возврат Ложь;    
		КонецПопытки;
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции
	
#КонецОбласти 

#Область УправлениеВидимостью

&НаСервере
Функция ПолучитьВидыРемонта()

	ТаблицаВидовРемонта = Объект.РемонтируемоеДополнительноеОборудование.Выгрузить(, "ВидРемонта");
	
	Если ЗначениеЗаполнено(Объект.ВидРемонта) Тогда
		НоваяСтрока = ТаблицаВидовРемонта.Добавить();
		НоваяСтрока.ВидРемонта = Объект.ВидРемонта;
	КонецЕсли;
	
	МассивВидовРемонта = Новый Массив;
	Для каждого СтрокаВидаРемонта Из ТаблицаВидовРемонта Цикл
		Если ЗначениеЗаполнено(СтрокаВидаРемонта.ВидРемонта) Тогда
			МассивВидовРемонта.Добавить(СтрокаВидаРемонта.ВидРемонта);
		КонецЕсли;
	КонецЦикла;

	Возврат МассивВидовРемонта;
	
КонецФункции // ПолучитьВидыРемонта()

&НаСервере
Функция ПолучитьТипыРемонта()
	
	МассивВидовРемонта = ПолучитьВидыРемонта();
	
	МассивТиповРемонта = Новый Массив;
	Для каждого ВидРемонта Из МассивВидовРемонта Цикл
		Если ЗначениеЗаполнено(ВидРемонта) Тогда
			ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРемонта, "ТипРемонта");
			Если ЗначениеЗаполнено(ТипРемонта) Тогда
				МассивТиповРемонта.Добавить(ТипРемонта);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивТиповРемонта;
	
КонецФункции // ПолучитьТипыРемонта()

&НаСервере
Процедура ОбновитьВидФормыПоВидуРемонта()
	
	МассивТиповРемонта = ПолучитьТипыРемонта();
	
	Если Не МассивТиповРемонта.Количество() Тогда
		Элементы.ГруппаЗапчасти.Доступность = Ложь;
		//Элементы.ГруппаРаботы.Доступность = Ложь;
		Элементы.ГруппаГСМ.Доступность = Ложь;
		Элементы.ГруппаГСМДопОборд.Доступность = Ложь;
		Элементы.ГруппаДополнительноеОборудование.Доступность = Ложь;
		Элементы.ГруппаШины.Доступность = Ложь;
		Элементы.ГруппаПрицепы.Доступность = Ложь;
		Возврат;
	КонецЕсли;	
	
	Если НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.Ремонт) = Неопределено 
		ИЛИ НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.ПлановоеТО) = Неопределено Тогда
		Элементы.ГруппаЗапчасти.Доступность = Истина;
		//Элементы.ГруппаРаботы.Доступность = Истина;
		Элементы.ГруппаГСМ.Доступность = Истина;
		Элементы.ГруппаГСМДопОборд.Доступность = Истина;
		//Элементы.ГруппаДополнительноеОборудование.Доступность = Ложь;
		//Элементы.ГруппаШины.Доступность = Ложь;
		//Элементы.ГруппаПрицепы.Доступность = Ложь;
	КонецЕсли;
	Если НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.МонтажОборудования) = Неопределено Тогда
		//Элементы.ГруппаЗапчасти.Доступность = Ложь;
		////Элементы.ГруппаРаботы.Доступность = Ложь;
		Элементы.ГруппаГСМ.Доступность = Истина;
		Элементы.ГруппаГСМДопОборд.Доступность = Истина;
		Элементы.ГруппаДополнительноеОборудование.Доступность = Истина;
		//Элементы.ГруппаШины.Доступность = Ложь;
		//Элементы.ГруппаПрицепы.Доступность					  = Ложь;
	КонецЕсли;		
	Если НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.ЗаменаШин) = Неопределено Тогда
		//Элементы.ГруппаЗапчасти.Доступность = Ложь;
		////Элементы.ГруппаРаботы.Доступность = Истина;
		//Элементы.ГруппаГСМ.Доступность = Ложь;
		//Элементы.ГруппаГСМДопОборд.Доступность = Ложь;
		//Элементы.ГруппаДополнительноеОборудование.Доступность = Ложь;
		Элементы.ГруппаШины.Доступность = Истина;
		//Элементы.ГруппаПрицепы.Доступность					  = Ложь;
	КонецЕсли;		
	Если НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.МонтажПрицепов) = Неопределено Тогда
		//Элементы.ГруппаЗапчасти.Доступность = Ложь;
		//Элементы.ГруппаРаботы.Доступность = ?(ЭтоПрицеп(Объект.ТранспортноеСредство), Ложь, Истина);
		Элементы.ГруппаПрицепы.Доступность = ?(ЭтоПрицеп(Объект.ТранспортноеСредство), Ложь, Истина);  
		//Элементы.ГруппаГСМ.Доступность = Ложь;
		//Элементы.ГруппаГСМДопОборд.Доступность = Ложь;
		//Элементы.ГруппаДополнительноеОборудование.Доступность = Ложь;
		//Элементы.ГруппаШины.Доступность = Ложь;
		//Элементы.ШиныТипОперацииМонтажа.ТолькоПросмотр = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидФормыПоСпособуРемонта()
		
	МассивТиповРемонта = ПолучитьТипыРемонта();
	
	Элементы.ШиныТипОперацииМонтажа.ТолькоПросмотр = Ложь;
	
	Если Объект.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами Тогда
	
		Элементы.СтраницыСпособыРемонта.ТекущаяСтраница = Элементы.ГруппаСобственнымиСилами;
		
		Элементы.ГруппаГСМСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.ГруппаГСМСтраницыДопОбр.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.СтраницаГСМСобственные.Видимость = Истина;
		Элементы.СтраницаГСМСобственныеДопОбр.Видимость = Истина;
		
		Элементы.ГСМРасходСоСклада.Видимость = Истина;
		
		Элементы.ГСМРасход.ЦветФонаЗаголовка = ЦветаСтиля.ФонУправляющегоПоля;
		Элементы.ГСМВалюта.ЦветФонаЗаголовка = ЦветаСтиля.ФонУправляющегоПоля;
		Элементы.ГСМСумма.ЦветФонаЗаголовка = ЦветаСтиля.ФонУправляющегоПоля;
		Элементы.ГСМСтавкаНДС.ЦветФонаЗаголовка = ЦветаСтиля.ФонУправляющегоПоля;
		Элементы.ГСМСуммаНДС.ЦветФонаЗаголовка = ЦветаСтиля.ФонУправляющегоПоля;
		
		Если НЕ МассивТиповРемонта.Найти(Перечисления.упТипыРемонтов.ЗаменаШин) = Неопределено Тогда
			Элементы.ШиныТипОперацииМонтажа.ТолькоПросмотр = Истина;
		КонецЕсли;

	Иначе
		Элементы.СтраницыСпособыРемонта.ТекущаяСтраница = Элементы.ГруппаСервиснаяОрганизация;
		
		Элементы.ГруппаГСМСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ГруппаГСМСтраницыДопОбр.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.СтраницаГСМСобственные.Видимость = Ложь;
		Элементы.СтраницаГСМСобственныеДопОбр.Видимость = Ложь;
		
		Элементы.ГСМРасходСоСклада.Видимость = Ложь;
		
		НовыйЦвет = Новый Цвет();
		
		Элементы.ГСМРасход.ЦветФонаЗаголовка = НовыйЦвет;
		Элементы.ГСМВалюта.ЦветФонаЗаголовка = НовыйЦвет;
		Элементы.ГСМСумма.ЦветФонаЗаголовка = НовыйЦвет;
		Элементы.ГСМСтавкаНДС.ЦветФонаЗаголовка = НовыйЦвет;
		Элементы.ГСМСуммаНДС.ЦветФонаЗаголовка = НовыйЦвет;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметкуНезаполненного()
	
	Элементы.НачалоРемонтаПлан.АвтоОтметкаНезаполненного    = Ложь;
	Элементы.ОкончаниеРемонтаПлан.АвтоОтметкаНезаполненного = Ложь;
	Элементы.НачалоРемонтаФакт.АвтоОтметкаНезаполненного    = Ложь;
	Элементы.ОкончаниеРемонтаФакт.АвтоОтметкаНезаполненного = Ложь;
	
	Если Объект.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами И ЗначениеЗаполнено(Объект.МестоРемонта) Тогда
		Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Новый Тогда
			Элементы.НачалоРемонтаПлан.АвтоОтметкаНезаполненного    = Истина;
			Элементы.ОкончаниеРемонтаПлан.АвтоОтметкаНезаполненного = Истина;
		ИначеЕсли Статус = Перечисления.упСтатусыРемонтаИТОТС.Запланирован Тогда
			Элементы.НачалоРемонтаФакт.АвтоОтметкаНезаполненного    = Истина;
		ИначеЕсли Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат Тогда	
			Элементы.НачалоРемонтаФакт.АвтоОтметкаНезаполненного    = Истина;
			Элементы.ОкончаниеРемонтаФакт.АвтоОтметкаНезаполненного = Истина;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия формы подбора агрегатов.
//
Процедура ПодборАгрегатовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			НоваяСтрока = Объект.ШиныУзлыАгрегатыСобственные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			НоваяСтрока.Склад = текСтрока.МестоХранения;
			НоваяСтрока.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
			НоваяСтрока.ЭтоШина = ПолучитьВидАгрегата(НоваяСтрока.Агрегат);	
			Если НЕ НоваяСтрока.ЭтоШина Тогда
				НоваяСтрока.ХодоваяШина = Ложь;
			Иначе
				НоваяСтрока.ХодоваяШина = Истина;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // ПодборАгрегатовЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы подбора агрегатов.
//
Процедура ПодборДопОборудованияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			НоваяСтрока = Объект.ДополнительноеОборудованиеСобственное.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
			НоваяСтрока.ДополнительноеОборудование = текСтрока.Агрегат;
			НоваяСтрока.Склад = текСтрока.МестоХранения;
			НоваяСтрока.ТипОперацииМонтажа = ПредопределенноеЗначение("Перечисление.упТипыОперацийМонтажа.Установлено");
			НоваяСтрока.Количество = 1;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры // ПодборАгрегатовЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы подбора ГСМ.
//
Процедура ПодборГСМЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда		
		ИмяТаблицыЗаполнения = "ГСМСобственные";
		Если НЕ ДополнительныеПараметры = Неопределено И ДополнительныеПараметры.Свойство("ИмяТаблицы") Тогда	
			ИмяТаблицыЗаполнения = ДополнительныеПараметры.ИмяТаблицы;
		КонецЕсли;		
		Для каждого текСтрока Из Результат Цикл
			ЗаполнитьЗначенияСвойств(Объект[ИмяТаблицыЗаполнения].Добавить(), текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы подбора запчастей.
//
Процедура ПодобратьЗапчастиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для каждого текСтрока Из Результат Цикл
			ЗаполнитьЗначенияСвойств(Объект.ЗапчастиСобственные.Добавить(), текСтрока);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Заполнить ТЧ "Работы" типовыми данными.
//
Процедура ЗаполнитьРаботыПоУмолчаниюОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЗаполнитьРаботыПоУмолчаниюНаСервере();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
// Перезаполнить начальный остаток ГСМ.
//
Процедура ЗаполнитьОстаткамиГСМДопОбрОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Объект.ГСМДополнительногоОборудования.Очистить();
		ЗаполнитьОстаткамиГСМНаСервере(Неопределено, "ГСМДополнительногоОборудования");
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Перезаполнить начальный остаток ГСМ.
//
Процедура ЗаполнитьОстаткамиГСМОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Объект.ГСМ.Очистить();
		ЗаполнитьОстаткамиГСМНаСервере(Неопределено, "ГСМ");
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Перезаполнить начальный остаток ГСМ.
//
Процедура ЗаполнитьЗаправленоСоСкладаОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЗаполнитьЗаправленоСоСкладаНаСервере(Неопределено, "ГСМ");
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Перезаполнить начальный остаток ГСМ.
//
Процедура ЗаполнитьЗаправленоСоСкладаДопОбрОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЗаполнитьЗаправленоСоСкладаНаСервере(Неопределено, "ГСМДополнительногоОборудования");
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
// Выполним заполнение таблицы на форме
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьЗаполнениеШины()
	
	// - устанвоим признак что это шина
	Для каждого тбчСтрока Из Объект.ШиныУзлыАгрегатыСобственные Цикл		
		тбчСтрока.ЭтоШина = ПолучитьВидАгрегата(тбчСтрока.Агрегат);		
	КонецЦикла;
	Для каждого тбчСтрока Из Объект.ШиныУзлыАгрегаты Цикл		
		тбчСтрока.ЭтоШина = ПолучитьВидАгрегата(тбчСтрока.Агрегат);		
	КонецЦикла;
	// - устанвоим признак что это шина
	
КонецПроцедуры // ВыполнитьЗаполнениеШины()

&НаСервере
Процедура ЗаполнитьРаботыПоУмолчаниюНаСервере()
	
	МассивВидовРемонта = ПолучитьВидыРемонта();
	ИспользоватьДопОборудывание = Ложь;
	Если Объект.РемонтируемоеДополнительноеОборудование.Количество() Тогда
		ИспользоватьДопОборудывание = Истина;
	КонецЕсли;
	Объект.Работы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВидыРемонтовРаботыПоРемонту.РаботаПоРемонту КАК РаботаПоРемонту,
	|	упВидыРемонтовРаботыПоРемонту.Количество КАК Количество,
	|	упВидыРемонтовРаботыПоРемонту.СтатьяРасходов КАК СтатьяРасходов,
	|	упВидыРемонтовРаботыПоРемонту.Ссылка КАК ВидРемонта,
	|	ЕСТЬNULL(упЦеныРаботПоРемонтуСрезПоследних.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(упЦеныРаботПоРемонтуСрезПоследних.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта
	|ИЗ
	|	Справочник.упВидыРемонтов.РаботыПоРемонту КАК упВидыРемонтовРаботыПоРемонту
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упЦеныРаботПоРемонту.СрезПоследних КАК упЦеныРаботПоРемонтуСрезПоследних
	|		ПО упВидыРемонтовРаботыПоРемонту.РаботаПоРемонту = упЦеныРаботПоРемонтуСрезПоследних.РаботаПоРемонту
	|ГДЕ
	|	упВидыРемонтовРаботыПоРемонту.Ссылка В(&ВидыРемонта)";
	Запрос.УстановитьПараметр("ВидыРемонта", МассивВидовРемонта);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
		Пока Выборка.Следующий() Цикл			
			Если Объект.ВидРемонта = Выборка.ВидРемонта Тогда
				НоваяСтрока = Объект.Работы.Добавить();
				НоваяСтрока.Валюта = Объект.Валюта;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);	
				упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока, "Цена", СтруктураДанные);
			КонецЕсли;
			Если ИспользоватьДопОборудывание Тогда
				ОтборПоВидуРемонта = Новый Структура();
				ОтборПоВидуРемонта.Вставить("ВидРемонта", Выборка.ВидРемонта);
				НайденныеСтроки = Объект.РемонтируемоеДополнительноеОборудование.НайтиСтроки(ОтборПоВидуРемонта);
				Для каждого ТекущееДопОборудывание Из НайденныеСтроки Цикл
					НоваяСтрока = Объект.Работы.Добавить();
					НоваяСтрока.Валюта = Объект.Валюта;
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);	
					НоваяСтрока.ДополнительноеОборудование = ТекущееДопОборудывание.ДополнительноеОборудование;
					упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока, "Цена", СтруктураДанные);
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаправленоСоСкладаНаСервере(ИндексСтроки = Неопределено, ИмяТаблицы = Неопределено)
	
	ИмяТаблицыЗаполнения = "ГСМ";
	Если НЕ ИмяТаблицы = Неопределено Тогда
		ИмяТаблицыЗаполнения = ИмяТаблицы;
	КонецЕсли;
	ТекущаяТаблицаГСМ = Объект[ИмяТаблицыЗаполнения];
	Для каждого тбчТекущаяСтрока Из ТекущаяТаблицаГСМ Цикл
		
		Если ЗначениеЗаполнено(тбчТекущаяСтрока.ГСМ.Владелец) Тогда
			сткОтбор = Новый Структура();
			сткОтбор.Вставить("ВидГСМ",тбчТекущаяСтрока.ГСМ.Владелец);
			НайденныеСтроки = ТекущаяТаблицаГСМ.НайтиСтроки(сткОтбор);
			НайденноеКоличество = НайденныеСтроки.Количество();
			Если НайденноеКоличество = 0 Тогда
				НоваяСтрокаГСМ = ТекущаяТаблицаГСМ.Добавить();
				НоваяСтрокаГСМ.ВидГСМ = тбчТекущаяСтрока.ГСМ.Владелец;
				НоваяСтрокаГСМ.РасходСоСклада = тбчТекущаяСтрока.Количество;
				НоваяСтрокаГСМ.Валюта = Объект.Валюта;
			Иначе
				ТекущееЗначениеЗаправлено = тбчТекущаяСтрока.Количество / НайденноеКоличество;				
				Для каждого СтрокаГСМ Из НайденныеСтроки Цикл
					СтрокаГСМ.РасходСоСклада = ТекущееЗначениеЗаправлено;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры // ЗаполнитьЗаправленоСоСкладаНаСервере()

&НаСервере
Процедура ЗаполнитьОстаткамиГСМНаСервере(ИндексСтроки = Неопределено, ИмяТаблицы = Неопределено)
	ИмяТаблицыЗаполнения = "ГСМ";
	Если НЕ ИмяТаблицы = Неопределено Тогда
		ИмяТаблицыЗаполнения = ИмяТаблицы;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГСМОстатки.ВидГСМ,
	|	ГСМОстатки.КоличествоОстаток КАК Остаток
	|ИЗ
	|	РегистрНакопления.упОстаткиГСМ.Остатки(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК ГСМОстатки";
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Граница = Новый Граница(Объект.Дата, ВидГраницы.Включая);
	Запрос.УстановитьПараметр("Период",Граница);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Объект[ИмяТаблицыЗаполнения].Добавить();
			НоваяСтрока.ВидГСМ = Выборка.ВидГСМ;
			НоваяСтрока.НачальныйОстаток = Выборка.Остаток;
			НоваяСтрока.Валюта = Объект.Валюта;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьОстаткамиГСМНаСервере()

&НаСервере
Процедура ЗаблокироватьЭлементы()

	ЭтоЗаявкаНаРемонт = НЕ ЗначениеЗаполнено(Объект.ЗаявкаНаРемонтИТО);
	
	Элементы.ГруппаВремяРемонтаПлан.Видимость = ЭтоЗаявкаНаРемонт;
	Элементы.ГруппаПлановаяСтоимостьВерх.Видимость = ЭтоЗаявкаНаРемонт;
	Элементы.ГруппаВремяРемонтаПлан.Видимость = ЭтоЗаявкаНаРемонт;
	Элементы.ГруппаПланы.Видимость = ЭтоЗаявкаНаРемонт;
	Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = НЕ ЭтоЗаявкаНаРемонт;
	Элементы.ГруппаПробегИзменить.ТолькоПросмотр = НЕ Объект.ИзмененаВыработка;
		
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Если Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Отменен Тогда
			ТолькоПросмотр = Истина;
			Возврат;
		КонецЕсли;
		
		Если Не Статус = Перечисления.упСтатусыРемонтаИТОТС.Новый Тогда
			Элементы.ГруппаУстановкаСпособаРемонта.ТолькоПросмотр = Истина;
			Элементы.ГруппаВыборОбъектыРемонта.ТолькоПросмотр = Истина;
			Элементы.ГруппаВремяРемонтаПлан.ТолькоПросмотр = Истина;
			Элементы.ГруппаПлановаяСтоимость.ТолькоПросмотр = Истина;
			
		КонецЕсли;		
	КонецЕсли;
	
	ОбъектРемонтаУстановитьСтраницу();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	
	ИтоговаяСумма 		= 0;
	ИтоговаяСуммаНДС 	= 0;
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ГСМ", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ГСМДополнительногоОборудования", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ДополнительноеОборудование", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Запчасти", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "Работы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ШиныУзлыАгрегаты", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ИтоговаяСумма 	 = ИтоговаяСумма + Объект.СуммаРасходы;
	ИтоговаяСуммаНДС = ИтоговаяСуммаНДС + Объект.СуммаРасходыНДС;
	
	Объект.СуммаРасходы 	= ИтоговаяСумма;
	Объект.СуммаРасходыНДС	= ИтоговаяСуммаНДС; 
		
КонецПроцедуры // РассчитатьИтоговуюСумму()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент, ИмяТаблЧасти)
	ТекущаяСтрока	= Элементы[ИмяТаблЧасти].ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоля(Элемент.Имя, ИмяТаблЧасти),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля, ИмяТаблЧасти)
	Возврат СтрЗаменить(ИмяПоля, ИмяТаблЧасти,"");
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоПрицеп(ТС)
	Возврат ТС.Прицеп;
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьПрицепПриИзмененииНаСервере(Прицеп, ДатаДокумента, ПользовательскоеСообщение)
	Если Прицеп.Прицеп Тогда
		Возврат Прицеп;
	Иначе
		ПользовательскоеСообщение = "ТС: " + Прицеп.Наименование + " не является прицепом."; 
		Возврат Справочники.упТранспортныеСредства.ПустаяСсылка();
	КонецЕсли;
КонецФункции //ПроверитьПрицепПриИзмененииНаСервере()

&НаКлиенте
// Процедура завершения после закрытия формы выбора прицепа.
//
Процедура ИспольнительНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если НЕ Результат = Неопределено Тогда 
		ТекущийЭлемент.ТекущиеДанные.Исполнитель = Результат;	
	КонецЕсли;    
КонецПроцедуры // ИспольнительНачалоВыбораЗавершение

&НаКлиенте
// Процедура завершения после закрытия формы выбора прицепа.
//
Процедура ПрицепыПрицепНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если Результат <> Неопределено Тогда 
		ТекущийЭлемент.ТекущиеДанные.Прицеп = Результат;	
	КонецЕсли;    
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаВидРемонта(Результат, ПараметрыОтвета) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Объект.ВидРемонта = ТекущиеВидыРемонта.Вид;		
		Возврат;
	КонецЕсли;
	
	ВыполнитьПроверкуТабличныхЧастейПоВидуРемонта();
	ОбновитьВидФормыПоВидуРемонта();
	ЗаполнитьРаботыПоУмолчаниюНаСервере();
	
КонецПроцедуры // ПослеЗакрытияВопросаВидРемонта()

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Функция ЗаполненоТранспортноеСредство()
	Результат = ЗначениеЗаполнено(Объект.ТранспортноеСредство);
	Если НЕ Результат Тогда
		ТекстСообщения = Нстр("ru = 'Не заполнено транспортное средство'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Объект.Ссылка,"Объект.ТранспортноеСредство",,);	
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
// Выполним проверку сотрудника исполнителя.
//
// Параметры:
//  ТекущийСотрудник  - СправочникСсылка.упСотрудники - Ссылка на исполнителя.
//
// Возвращаемое значение:
//   Булево   - результат проверки.
//
Функция ПроверитьТипСотрудника(ТекущийСотрудник)

	ТипСотрудника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийСотрудник, "ТипСотрудника");
	Массив = Новый Массив;
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Механик"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Прочее"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упТипыСотрудников.Оператор"));
	Результат = Ложь;
	Если НЕ Массив.Найти(ТипСотрудника) = Неопределено Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПроверитьТипСотрудника()

&НаКлиенте
// Заполнить ТЧ "Работы" типовыми данными.
//
Процедура ЗаполнитьРемонтируемоеДополнительноеОборудованиеОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
		
	Если НЕ Результат = КодВозвратаДиалога.Отмена Тогда
		Если Результат = КодВозвратаДиалога.Да Тогда
			Объект.РемонтируемоеДополнительноеОборудование.Очистить();
		КонецЕсли;
		ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере()
	
	Период = Объект.Дата;
	Если ЗначениеЗаполнено(Объект.НачалоРемонтаПлан) Тогда
		Период = Объект.НачалоРемонтаПлан;
	ИначеЕсли ЗначениеЗаполнено(Объект.НачалоРемонтаФакт) Тогда
		Период = Объект.НачалоРемонтаФакт;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АгрегатыТС.Агрегат КАК ДополнительноеОборудование
	|ИЗ
	|	РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|			&Период,
	|			Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|				И Агрегат.Ссылка ССЫЛКА Справочник.упДополнительноеОборудование) КАК АгрегатыТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упДополнительноеОборудование КАК ДопОборудование
	|		ПО АгрегатыТС.Агрегат = ДопОборудование.Ссылка
	|ГДЕ
	|	АгрегатыТС.ТранспортноеСредство = &ТранспортноеСредство";
	Запрос.УстановитьПараметр("ТранспортноеСредство", Объект.ТранспортноеСредство);
	Запрос.УстановитьПараметр("Период", Период);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Объект.РемонтируемоеДополнительноеОборудование.Добавить(), Выборка);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРемонтируемоеДополнительноеОборудованиеНаСервере()

&НаСервере
Функция ПолучитьСписокВозможногоДопОборудывания()
	
	Массив = Новый Массив;
	
	Для каждого СтрокаОборудывания Из Объект.РемонтируемоеДополнительноеОборудование Цикл
		ДополнительноеОборудование = СтрокаОборудывания.ДополнительноеОборудование;
		Если Массив.Найти(ДополнительноеОборудование) = Неопределено Тогда
			Массив.Добавить(ДополнительноеОборудование);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаОборудывания Из Объект.ДополнительноеОборудование Цикл
		ДополнительноеОборудование = СтрокаОборудывания.ДополнительноеОборудование;
		Если Массив.Найти(ДополнительноеОборудование) = Неопределено Тогда
			Массив.Добавить(ДополнительноеОборудование);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаОборудывания Из Объект.ДополнительноеОборудованиеСобственное Цикл
		ДополнительноеОборудование = СтрокаОборудывания.ДополнительноеОборудование;
		Если Массив.Найти(ДополнительноеОборудование) = Неопределено Тогда
			Массив.Добавить(ДополнительноеОборудование);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Массив.Количество() Тогда
		Массив.Добавить(Справочники.упДополнительноеОборудование.ПустаяСсылка());
	КонецЕсли;
	
	Возврат Массив;
	
КонецФункции // ПолучитьСписокВозможногоДопОборудывания()

&НаКлиенте
// Заполнить ТЧ "Работы" типовыми данными.
//
Процедура ЗаполнитьДокументНаОснованииОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДокументНаОснованииНаСервере();
		Модифицированность = Истина;
	Иначе
		Объект.ЗаявкаНаРемонтИТО = ПредопределенноеЗначение("Документ.упЗаявкаНаРемонтИТОТС.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументНаОснованииНаСервере()
	
	ОбъектЗаполнения = РеквизитФормыВЗначение("Объект");
	ОбъектЗаполнения.Заполнить(Объект.ЗаявкаНаРемонтИТО);
	ЗначениеВРеквизитФормы(ОбъектЗаполнения, "Объект");
	
КонецПроцедуры // ЗаполнитьДокументНаОснованииНаСервере()

&НаСервере
Процедура ОбъектРемонтаУстановитьСтраницу()
	
	Элементы.ГруппаСтраницыОбъектыРемонта.ТекущаяСтраница = Элементы.ГруппаСтраницаТранспортноеСредствоОбъектРемонта;
	
	ИзмененаВыработкаАгрегатаВидимость = Ложь;

	Если Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ДополнительноеОборудование")Тогда
		Элементы.ГруппаСтраницыОбъектыРемонта.ТекущаяСтраница = Элементы.ГруппаСтраницаДополнительноеОборудованиеРемонтаОбъектРемонта;
		ИзмененаВыработкаАгрегатаВидимость = Истина;
		Если ЭтоРемонтТС Тогда
			Элементы.ДополнительноеОборудованиеРемонта.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		КонецЕсли;
	ИначеЕсли Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат")Тогда
		Элементы.ГруппаСтраницыОбъектыРемонта.ТекущаяСтраница = Элементы.ГруппаСтраницаАгрегатОбъектРемонта;
		Если ЭтоРемонтТС Тогда
			Элементы.Агрегат.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		КонецЕсли;
		ИзмененаВыработкаАгрегатаВидимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаРемонтОборудования.Видимость         = НЕ ИзмененаВыработкаАгрегатаВидимость;
	Элементы.ГруппаДополнительноеОборудование.Видимость = Истина
												И ВедетсяУчетДополнительногоОборудования
												И НЕ ИзмененаВыработкаАгрегатаВидимость;
	Элементы.ГруппаШины.Видимость    = Истина
								И ВедетсяУчетШинУзловИАгрегатов 
								И НЕ ИзмененаВыработкаАгрегатаВидимость;
	Элементы.ГруппаПрицепы.Видимость = НЕ ИзмененаВыработкаАгрегатаВидимость;	
	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(,Объект.ТранспортноеСредство);
		КонецЕсли;		
		
		Объект.НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,Объект.ТранспортноеСредство);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТранспортноеСредствоОбъектаРемонта()
	Если Объект.ОбъектРемонта <> ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.ТранспортноеСредство") Тогда
		Агрегат = ?(Объект.ОбъектРемонта = ПредопределенноеЗначение("Перечисление.упОбъектыРемонта.Агрегат"),
					Объект.Агрегат,
					Объект.ДополнительноеОборудованиеРемонта);
		Если ЗначениеЗаполнено(Агрегат) Тогда
			Период = Объект.Дата;
			Если ЗначениеЗаполнено(Объект.НачалоРемонтаФакт) Тогда
				Период = Объект.НачалоРемонтаФакт;
			ИначеЕсли ЗначениеЗаполнено(Объект.НачалоРемонтаПлан) Тогда	
				Период = Объект.НачалоРемонтаПлан;
			КонецЕсли;
			Объект.ТранспортноеСредствоОбъектаРемонта = ТранспортноеСредствоПоАгрегату(Период, Агрегат);	
		Иначе
			Объект.ТранспортноеСредствоОбъектаРемонта = ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТранспортноеСредствоПоАгрегату(Период, Агрегат)
	Возврат упУправлениеЗапасами.ТранспортноеСредстваПоАгрегату(Период, Агрегат);
КонецФункции

#КонецОбласти 