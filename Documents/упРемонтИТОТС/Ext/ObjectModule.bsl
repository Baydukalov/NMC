 
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ИндексАгрегат = Неопределено;
	ИндексДополнительноеОборудованиеРемонта = Неопределено;
	ИндексТранспортноеСредство = Неопределено;
	Если НЕ ЗначениеЗаполнено(ОбъектРемонта) 
		ИЛИ ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство Тогда 
		ИндексАгрегат = ПроверяемыеРеквизиты.Найти("Агрегат");		
		Если НЕ ИндексАгрегат = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексАгрегат);
		КонецЕсли;
		ИндексДополнительноеОборудованиеРемонта = ПроверяемыеРеквизиты.Найти("ДополнительноеОборудованиеРемонта");		
		Если НЕ ИндексДополнительноеОборудованиеРемонта = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексДополнительноеОборудованиеРемонта);
		КонецЕсли;
	ИначеЕсли ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование Тогда 
		ИндексАгрегат = ПроверяемыеРеквизиты.Найти("Агрегат");
		Если НЕ ИндексАгрегат = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексАгрегат);
		КонецЕсли;
		ИндексТранспортноеСредство = ПроверяемыеРеквизиты.Найти("ТранспортноеСредство");		
		Если НЕ ИндексТранспортноеСредство = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексТранспортноеСредство);
		КонецЕсли;
	ИначеЕсли ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда 
		ИндексДополнительноеОборудованиеРемонта = ПроверяемыеРеквизиты.Найти("ДополнительноеОборудованиеРемонта");
		Если НЕ ИндексДополнительноеОборудованиеРемонта = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексДополнительноеОборудованиеРемонта);
		КонецЕсли;
		ИндексТранспортноеСредство = ПроверяемыеРеквизиты.Найти("ТранспортноеСредство");
		Если НЕ ИндексТранспортноеСредство = Неопределено Тогда 
			ПроверяемыеРеквизиты.Удалить(ИндексТранспортноеСредство);
		КонецЕсли;
	КонецЕсли;
	
	Если СпособРемонта = Перечисления.упСпособыРемонтов.СторонняяОрганизация Тогда 
		Если Не ЗначениеЗаполнено(СервиснаяОрганизация) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Сервисная организация"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.СервиснаяОрганизация",, Отказ);
		КонецЕсли; 
		Если Не ЗначениеЗаполнено(АдресРемонта) И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетМестоположенийТранспортныхСредств") Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Адрес ремонта"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.АдресРемонта",, Отказ);
		КонецЕсли; 
	ИначеЕсли СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами Тогда

		Если Не ЗначениеЗаполнено(Гараж) Тогда
		    ТекстОшибки = НСтр("ru = 'Поле ""Гараж"" не заполнено'");
		    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.Гараж",, Отказ);
		КонецЕсли; 
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЗаявкаНаРемонтИТО) И НачалоРемонтаПлан > ОкончаниеРемонтаПлан Тогда
		ТекстОшибки = НСтр("ru = 'Плановое время начала работ не может быть больше планового времени окончания работ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НачалоРемонтаПлан",, Отказ);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(НачалоРемонтаФакт) И ЗначениеЗаполнено(ОкончаниеРемонтаФакт) И НачалоРемонтаФакт > ОкончаниеРемонтаФакт Тогда
		ТекстОшибки = НСтр("ru = 'Время начала работ не может быть больше времени окончания работ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.НачалоРемонтаФакт",, Отказ);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ПериодРаспределенияЗатратНачало) И ЗначениеЗаполнено(ПериодРаспределенияЗатратОкончание) И ПериодРаспределенияЗатратНачало > ПериодРаспределенияЗатратОкончание Тогда
		ТекстОшибки = НСтр("ru = 'Начало периода распределения затрат не может быть больше окончания периода распределения затрат'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ПериодРаспределенияЗатратНачало",, Отказ);
	КонецЕсли; 
	
	НомерСтроки = 1;
	Для каждого СтрокаГСМ Из ГСМ Цикл
		
		Если НЕ Отказ И СтрокаГСМ.Расход < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Заправлено"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ", НомерСтроки, "Расход"),,Отказ);	
		КонецЕсли;
		
		Если НЕ Отказ И СтрокаГСМ.РасходСоСклада < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Заправлено со склада"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ", НомерСтроки, "РасходСоСклада"),,Отказ);	
		КонецЕсли;
		
		Если НЕ Отказ И СтрокаГСМ.НачальныйОстаток < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Начальный остаток"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ", НомерСтроки, "НачальныйОстаток"),,Отказ);	
		КонецЕсли;
		
		Если НЕ Отказ И СтрокаГСМ.КонечныйОстаток < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Конечный остаток"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ", НомерСтроки, "КонечныйОстаток"),,Отказ);	
		КонецЕсли;
		
		Если НЕ Отказ И СтрокаГСМ.Сумма < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Сумма"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ", НомерСтроки, "Сумма"),,Отказ);	
		КонецЕсли;

		Если НЕ Отказ И СтрокаГСМ.СуммаНДС < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Сумма НДС"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМ", НомерСтроки, "СуммаНДС"),,Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	НомерСтроки = 1;
	Для каждого СтрокаГСМ Из ГСМСобственные Цикл
		
		Если НЕ Отказ И СтрокаГСМ.Количество < 0 Тогда
			ТекстСообщения = Нстр("ru = 'Значение поля ""Количество"" не должно быть отрицательным'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ГСМСобственные", НомерСтроки, "Количество"),,Отказ);	
		КонецЕсли;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
		
	Если НЕ Отказ Тогда
		ТаблицаЗначений = Новый ТаблицаЗначений;
		ТаблицаЗначений.Колонки.Добавить("ДополнительноеОборудование",,"ДополнительноеОборудование");
		ТаблицаЗначений.Колонки.Добавить("ТипРемонта",,"ТипРемонта");
		ТаблицаЗначений.Колонки.Добавить("Количество",,"Количество");
		
		Для каждого СтрокаДополнительноеОборудование Из РемонтируемоеДополнительноеОборудование Цикл
			
			ТекущийОтбор = Новый Структура("ДополнительноеОборудование,ВидРемонта");
			ЗаполнитьЗначенияСвойств(ТекущийОтбор, СтрокаДополнительноеОборудование);
			
			НайденныеСтроки = РемонтируемоеДополнительноеОборудование.НайтиСтроки(ТекущийОтбор);
			
			Если НайденныеСтроки.Количество() > 1 Тогда
				ТекстСообщения = Нстр("ru = 'Дублирование строки ремонтируемого дополнительного оборудования'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.РемонтируемоеДополнительноеОборудование", НомерСтроки, "ДополнительноеОборудование, ВидРемонта"),,Отказ);	
			КонецЕсли;
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;
			
			ТипРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаДополнительноеОборудование.ВидРемонта, "ТипРемонта");
			Если Перечисления.упТипыРемонтов.ПлановоеТО = ТипРемонта Тогда
				НоваяСтрока = ТаблицаЗначений.Добавить();
				НоваяСтрока.ДополнительноеОборудование = СтрокаДополнительноеОборудование.ДополнительноеОборудование;				
				НоваяСтрока.ТипРемонта = ТипРемонта;
				НоваяСтрока.Количество = 1;
			КонецЕсли;
		КонецЦикла;
		
		Если ТаблицаЗначений.Количество() Тогда
			ТаблицаЗначений.Свернуть("ДополнительноеОборудование, ТипРемонта", "Количество");
			ТаблицаЗначений.Сортировать("Количество УБЫВ");
			Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
				Если СтрокаТаблицы.Количество >1 Тогда
					ТекстОшибки = НСтр("ru = 'Выбраны виды ремонта с типом реомнта Плановое ТО больше одного это излишне.'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.РемонтируемоеДополнительноеОборудование",, Отказ);
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.Запись  
	Тогда
		// Проверка на наличие более поздних документов изменяющих состояние агрегатов и доп. оборудования.
		мсвОборудование = ШиныУзлыАгрегаты.ВыгрузитьКолонку("Агрегат");
		Для каждого Строка Из ШиныУзлыАгрегатыСобственные Цикл
			мсвОборудование.Добавить(Строка.Агрегат);
		КонецЦикла;
		
		Для каждого Строка Из ДополнительноеОборудование Цикл
			мсвОборудование.Добавить(Строка.ДополнительноеОборудование);
		КонецЦикла;

		Для каждого Строка Из ДополнительноеОборудованиеСобственное Цикл
			мсвОборудование.Добавить(Строка.ДополнительноеОборудование);
		КонецЦикла;

		упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, мсвОборудование, Дата, Отказ);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			
		упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ШиныУзлыАгрегатыСобственные, "ШиныУзлыАгрегатыСобственные", Ссылка, Отказ);
		упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ШиныУзлыАгрегаты, "ШиныУзлыАгрегаты", Ссылка, Отказ);
		
		упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ДополнительноеОборудование, "ДополнительноеОборудование", Ссылка, Отказ);
		упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ДополнительноеОборудованиеСобственное, "ДополнительноеОборудованиеСобственное", Ссылка, Отказ);
		
		// Используется заявка на ремонт и ТО ТС.
		ИспользоватьЗаявкуНаРемонтИТОТС = ЗначениеЗаполнено(ЗаявкаНаРемонтИТО);
		ДополнительныеСвойства.Вставить("ИспользоватьЗаявкуНаРемонтИТОТС", ИспользоватьЗаявкуНаРемонтИТОТС);
		
		// Это ремонт ТС.
		ЭтоРемонтТС = Ложь
					Или ОбъектРемонта = Перечисления.упОбъектыРемонта.ТранспортноеСредство
					Или Не ЗначениеЗаполнено(ОбъектРемонта);
				
		ДополнительныеСвойства.Вставить("ЭтоРемонтТС", ЭтоРемонтТС);
		
		
		Если ЗначениеЗаполнено(Ссылка) Тогда
			Движения.упАгрегатыНаСкладах.Записывать = Истина;
			Движения.упАгрегатыНаСкладах.Очистить();
			
			Движения.упОстаткиМатериалов.Записывать = Истина;
			Движения.упОстаткиМатериалов.Очистить();
			
			Движения.упАгрегатыТранспортныхСредств.Записывать = Истина;
			Движения.упАгрегатыТранспортныхСредств.Очистить();
			
			Движения.Записать();
			
		КонецЕсли;
				
		// Используется для расчета стоимости при расходе собственных зап.частей
		ДополнительныеСвойства.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
		
		// Инициализация дополнительных данных для формирования движений
		сткРеквизиты = Новый Структура("СтатьяРасходов, ПравилоРаспределения, ВариантРасходов", 
		                               "СтатьяРасходов", 
		                               "СтатьяРасходов.ПравилаРаспределения",
		                               "СтатьяРасходов.ВариантРасходов");
		сткРеквизитыВидаРемонта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидРемонта, сткРеквизиты);
		ДополнительныеСвойства.Вставить("СтатьяРасходов",       сткРеквизитыВидаРемонта.СтатьяРасходов);
		ДополнительныеСвойства.Вставить("ПравилоРаспределения", сткРеквизитыВидаРемонта.ПравилоРаспределения);
		ДополнительныеСвойства.Вставить("ВариантРасходов",      сткРеквизитыВидаРемонта.ВариантРасходов);
		
		РеквизитыОбъекта = Новый Структура("ВидРемонта, 
									|Дата,
									|ТранспортноеСредство,
									|Валюта,
									|СуммаВыработка, 
									|СуммаРасходыПланВалюта, 
									|СуммаРасходыПланНДСВалюта, 
									|СуммаРасходыПлан, 
									|СуммаРасходыПланНДС, 										
									|СуммаПланСпособРемонта, 
									|Гараж,
									|ИзмененаВыработка,
									|ПробегКм,
									|ПробегМотоЧасы,
									|МестоРемонта,
									|НаправлениеДеятельности,
									|НачалоРемонтаПлан, 
									|НачалоРемонтаФакт, 
									|Организация,
									|ОкончаниеРемонтаПлан, 
									|ОкончаниеРемонтаФакт,
									|ПериодРаспределенияЗатратНачало,
									|ПериодРаспределенияЗатратОкончание,
									|Подразделение");
		ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект); 
		РеквизитыОбъекта.Вставить("ОбъектРемонта",     ОбъектРемонта);
		Если ИзмененаВыработка Тогда
			РеквизитыОбъекта.Вставить("ИзмененаВыработкаАгрегата", НЕ ЭтоРемонтТС);		
			РеквизитыОбъекта.Вставить("ИзмененаВыработкаТС", ЭтоРемонтТС);
			Если ЭтоРемонтТС Тогда
				РеквизитыОбъекта.Вставить("МоточасыВыработка",    ПробегМотоЧасы);
				РеквизитыОбъекта.Вставить("ПробегВыработка",      ПробегКм);
				РеквизитыОбъекта.Вставить("Сумма",                СуммаВыработка);
				РеквизитыОбъекта.Вставить("ВидДвиженияВыработка", ВидДвиженияНакопления.Приход);
			КонецЕсли;
		Иначе
			РеквизитыОбъекта.Вставить("ИзмененаВыработкаАгрегата", Ложь);		
			РеквизитыОбъекта.Вставить("ИзмененаВыработкаТС",       Ложь);		
		КонецЕсли;
	
		РеквизитыОбъекта.Вставить("Агрегат",           Агрегат);
		РеквизитыОбъекта.Вставить("ДополнительноеОборудованиеРемонта", ДополнительноеОборудованиеРемонта);
		РеквизитыОбъекта.Вставить("СпособРемонта",     СпособРемонта);
		РеквизитыОбъекта.Вставить("ЗаявкаНаРемонтИТО", ЗаявкаНаРемонтИТО);
		Если ЗначениеЗаполнено(Ссылка) Тогда
			РеквизитыОбъекта.Вставить("Граница",  Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		Иначе	
			РеквизитыОбъекта.Вставить("Граница",  Дата);	
		КонецЕсли;
			
		
		// Табличные части
		РеквизитыОбъекта.Вставить("Запчасти",                                  Запчасти);
		РеквизитыОбъекта.Вставить("ЗапчастиСобственные",                       ЗапчастиСобственные);
		
		РеквизитыОбъекта.Вставить("Работы",                                    Работы);
		
		РеквизитыОбъекта.Вставить("Прицепы",                                   Прицепы);
		
		РеквизитыОбъекта.Вставить("ДополнительноеОборудование",                ДополнительноеОборудование);
		РеквизитыОбъекта.Вставить("ДополнительноеОборудованиеСобственное",     ДополнительноеОборудованиеСобственное);
		
		РеквизитыОбъекта.Вставить("ШиныУзлыАгрегаты",                          ШиныУзлыАгрегаты);
		РеквизитыОбъекта.Вставить("ШиныУзлыАгрегатыСобственные",               ШиныУзлыАгрегатыСобственные);
		
		РеквизитыОбъекта.Вставить("ГСМ",                                       ГСМ);
		РеквизитыОбъекта.Вставить("ГСМСобственные",                            ГСМСобственные);
		РеквизитыОбъекта.Вставить("ГСМДополнительногоОборудования",            ГСМДополнительногоОборудования.Выгрузить());
		РеквизитыОбъекта.Вставить("ГСМСобственныеДополнительногоОборудования", ГСМСобственныеДополнительногоОборудования.Выгрузить());
		
		РеквизитыОбъекта.Вставить("РемонтируемоеДополнительноеОборудование",   РемонтируемоеДополнительноеОборудование.Выгрузить());
						
		ДополнительныеСвойства.Вставить("РеквизитыОбъекта", РеквизитыОбъекта);

		ТаблицаГСМ = ГСМ.Выгрузить();
		Для каждого ТекущаяСтрокаГСМДопОбр Из ГСМДополнительногоОборудования Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаГСМ.Добавить(), ТекущаяСтрокаГСМДопОбр);
		КонецЦикла;
		НеКонтролироватьРасписаниеРаботыГаража = Ложь;
		Если ЗначениеЗаполнено(Гараж) Тогда
			НеКонтролироватьРасписаниеРаботыГаража = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Гараж, "НеКонтролироватьРасписаниеРаботыГаража");		
		КонецЕсли;
		ДополнительныеСвойства.Вставить("НеКонтролироватьРасписаниеРаботыГаража",    НеКонтролироватьРасписаниеРаботыГаража);
		ДополнительныеСвойства.Вставить("ГСМ",                                       ТаблицаГСМ.Скопировать());
		ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыГаража",    Ложь);
		ДополнительныеСвойства.Вставить("КонтрольФактическоеРасписаниеРаботыГаража", Ложь);
		ДополнительныеСвойства.Вставить("КонтрольПлановоеРасписаниеРаботыТС",        Ложь);
		ДополнительныеСвойства.Вставить("КонтрольПлановыеДатыТО",                    Ложь);
		ДополнительныеСвойства.Вставить("СпособРемонта",                             СпособРемонта);
		ДополнительныеСвойства.Вставить("ИспользоватьЗаявкуНаРемонтИТОТС",           ИспользоватьЗаявкуНаРемонтИТОТС);
		ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРемонта, "ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт");
		ДополнительныеСвойства.Вставить("ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт", ЗапрещеноОформлениеРемонтныхРаботБезЗаявкиНаРемонт);
		
		Документы.упРемонтИТОТС.ИнициализироватьДанныеДокумента(РеквизитыОбъекта, ДополнительныеСвойства);
		Документы.упРемонтИТОТС.ИнициализироватьДанныеАгрегатовДокумента(Дата, РеквизитыОбъекта, ДополнительныеСвойства);	
		Если ЭтоРемонтТС Тогда
			Документы.упРемонтИТОТС.ИнициализироватьДанныеПрицеповДокумента(РеквизитыОбъекта, ДополнительныеСвойства);
		КонецЕсли;
		
		Если Истина
			И Не ИспользоватьЗаявкуНаРемонтИТОТС
			И ЭтоРемонтТС
			И ДополнительныеСвойства.Свойство("ПлановыеДатыТО") 
		Тогда
			ДополнительныеСвойства.Вставить("ИспользоватьПлановыеДатыТО", Истина);
		КонецЕсли;
				
		БлокировкаДанных = Новый БлокировкаДанных;
		ДобавитьЭлементыБлокировкиДанныхПроведения(БлокировкаДанных);
		упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли; 
		ДополнительныеСвойства.Вставить("ВидРемонта", ВидРемонта);
		// Установка статуса документа.
		упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
		
		Если ДополнительныеСвойства.Свойство("Прицепы") Тогда
			Документы.упРемонтИТОТС.ВыполнитьКонтрольПрицепа(ДополнительныеСвойства, Отказ);
		КонецЕсли;
				
		// Рассчитываются суммы списани материалов (таблица "ОстаткиМатериалов"), исходя из метода оценки.
		упУправлениеЗапасами.РассчитатьСуммыСписанияМатериалов(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);	
		
		// Заполняются цены ГСМ на основании остатков на складах.
		Документы.упРемонтИТОТС.ВыполнитьОбработкуГСМСоСклада(ДополнительныеСвойства, РеквизитыОбъекта, Отказ);	
				
		упРемонтИТОТС.ИнициализироватьДанныеОстатковИЛимитовГСМ(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
		
		// Заполняются таблица расходов и итоговые суммы расходов.
		ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
		упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	ЭтоРемонтТС                     = ДополнительныеСвойства.ЭтоРемонтТС;
	ИспользоватьЗаявкуНаРемонтИТОТС = ДополнительныеСвойства.ИспользоватьЗаявкуНаРемонтИТОТС;
	РеквизитыОбъекта                = ДополнительныеСвойства.РеквизитыОбъекта;
	РеквизитыОбъекта.Вставить("Дата",  Дата);
	РеквизитыОбъекта.Вставить("Граница",  Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));
		
	
	// Проверяется хватает ли топлива на складе для заправок.
	Документы.упРемонтИТОТС.ВыполнитьКонтрольГСМРасходСоСклада(Ссылка, РеквизитыОбъекта, Отказ);
	
	// Формирование движений по регистру накопления "упОстаткиМатериалов"
	упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если ЭтоРемонтТС Тогда		
		// Формируется расписание работы ТС
		упРемонтИТОТС.СформироватьДвиженияРасписаниеРаботыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формируются движения по занятости транспорта
		упРемонтИТОТС.СформироватьДвиженияЗанятостьТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формируются движения по состоянию ТС
		упРемонтИТОТС.СформироватьДвиженияСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формируются движения по Плановые даты ТО
		упРемонтИТОТС.СформироватьДвиженияРемонтПлановыеДатыТО(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формируются движения ШиныУзлыАгрегаты
		Документы.упРемонтИТОТС.ВыполнитьКонтрольАгрегатов(Ссылка, РеквизитыОбъекта, Отказ);
		упРемонтИТОТС.СформироватьДвиженияШиныУзлыАгрегаты(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Приcоединение/отсоединение прицепа
		упУчетТранспортныхСредств.СформироватьДвиженияПрицепыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);		
		
		// СостоянияОборудования
		упУчетТранспортныхСредств.СформироватьДвиженияСостоянияОборудованияРемонтИТОТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Списывается пробег и моточасы в выработке. 
		упУчетТранспортныхСредств.СформироватьДвижениеИзмененаВыработкаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);	
				
	КонецЕсли;
	
	// Формируются движения по расписанию работы гаража
	упРемонтИТОТС.СформироватьДвиженияПлановоеРасписаниеРаботыГаража(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	упРемонтИТОТС.СформироватьДвиженияФактическоеРасписаниеРаботыГаража(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Корректировка остатков ГСМ
	упРемонтИТОТС.СформироватьДвижениеОстаткиГСМ(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются прямые фактические и плановые расходы.
	упРемонтИТОТС.СформироватьДвиженияРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Фиксация остаточной стоимости агрегатов.
	упВыработкаРесурсов.СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	// Формируется данные о пройденном ТО
	упРемонтИТОТС.СформироватьДвиженияПройденныеТО(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется и устанавливается статус документа
	упРемонтИТОТС.СформироватьДвиженияСтатусРемонтаИТОТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если ИспользоватьЗаявкуНаРемонтИТОТС Тогда
		// Предварительная установка статуса
		ДополнительныеСвойства_Заявка = Новый Структура();
		ДополнительныеСвойства_Заявка.Вставить("СтатусРемонт", ДополнительныеСвойства.Статус);
		ДополнительныеСвойства_Заявка.Вставить("Ссылка", ЗаявкаНаРемонтИТО);
		упРемонтИТОТС.ПодготовитьФормированиеДвиженияСтатусЗаявкаНаРемонтИТОТС(ДополнительныеСвойства_Заявка, ЗаявкаНаРемонтИТО, Движения, РеквизитыОбъекта, Отказ);
		
		Если НЕ Отказ Тогда
			ТекущийСтатус = ДополнительныеСвойства_Заявка.ТекущийСтатус;
			НовыйСтатус = ДополнительныеСвойства_Заявка.Статус;
			
			ДополнительныеСвойства_Заявка.Вставить("РассчитатьСтатус", Истина); 
			
			РеквизитыОбъекта.Вставить("ДатаСтатуса", ТекущаяДатаСеанса());
			// Формируется и устанавливается статус документа ЗаявкаНаРемонт.
			упРемонтИТОТС.СформироватьДвиженияСтатусЗаявкаНаРемонтИТОТС(ДополнительныеСвойства_Заявка, ЗаявкаНаРемонтИТО, Движения, РеквизитыОбъекта, Отказ);
			// Проверим доступен ли новый статус для ЗаявкаНаРемонт.
			упРаботаСоСтатусами.ПроверитьВозможностьУстановкиСтатуса(ЗаявкаНаРемонтИТО,ТекущийСтатус, НовыйСтатус, ДополнительныеСвойства_Заявка, Отказ);	
		КонецЕсли;
	КонецЕсли;
			
	Движения.Записать();
	
	// Выполняется контроль результатов проведения
	Если ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыГаража Тогда
		РегистрыСведений.упПлановоеРасписаниеРаботыГаража.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	КонецЕсли;
	Если ДополнительныеСвойства.КонтрольФактическоеРасписаниеРаботыГаража Тогда
		РегистрыСведений.упФактическоеРасписаниеРаботыГаража.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	КонецЕсли;
	Если ДополнительныеСвойства.КонтрольПлановоеРасписаниеРаботыТС Тогда
		РегистрыСведений.упЗанятостьТранспорта.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, ЭтотОбъект.ТранспортноеСредство, Отказ);
	КонецЕсли;
	
	// Контроль возникновения отрицательных остатков по складам.
	Если Истина
		И СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами 
		И Не ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО 
	Тогда
		РеквизитыОбъекта.Вставить("ГраницаИсключая", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		упУправлениеЗапасами.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
	// Контроль ремонтируемое дополнительное оборудование установленно на транспортное средство.
	Если Истина
		И НЕ ЭтоРемонтТС 
		И РемонтируемоеДополнительноеОборудование.Количество() 
	Тогда
		упРемонтИТОТС.ВыполнитьКонтрольУстановленногоОборудованияНаТС(ДополнительныеСвойства, ЭтотОбъект, ЭтотОбъект.ТранспортноеСредство, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.КонтрольПлановыеДатыТО Тогда
		РегистрыСведений.упПлановыеДатыТО.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упПроисшествиеВПути") Тогда
		
		Организация 			= ДанныеЗаполнения.Организация;
		ПроисшествиеВПути 		= ДанныеЗаполнения.Ссылка;
		ТранспортноеСредство 	= ДанныеЗаполнения.ТранспортноеСредство;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗаявкаНаРемонтИТОТС") Тогда
		
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упРемонтИТОТС.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.упРемонтИТОТС КАК упРемонтИТОТС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРемонтовИТОТС.СрезПоследних КАК упСтатусыРемонтовИТОТССрезПоследних
		|		ПО упРемонтИТОТС.Ссылка = упСтатусыРемонтовИТОТССрезПоследних.Документ
		|ГДЕ
		|	упРемонтИТОТС.ЗаявкаНаРемонтИТО = &ЗаявкаНаРемонтИТО
		|	И НЕ упСтатусыРемонтовИТОТССрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Отменен)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упСтатусыЗаявокНаРемонтИТОТССрезПоследних.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.упСтатусыЗаявокНаРемонтИТОТС.СрезПоследних(, Документ = &ЗаявкаНаРемонтИТО) КАК упСтатусыЗаявокНаРемонтИТОТССрезПоследних
		|ГДЕ
		|	упСтатусыЗаявокНаРемонтИТОТССрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаРемонтИТОТС.КВыполнению)";
		Запрос.УстановитьПараметр("ЗаявкаНаРемонтИТО", ДанныеЗаполнения);		
		ПакетЗапросов = Запрос.ВыполнитьПакет();		
		Отказ = НЕ ПакетЗапросов[0].Пустой() ИЛИ ПакетЗапросов[1].Пустой();
		
		Если Отказ Тогда
			ЗаявкаНаРемонтИТО = Документы.упЗаявкаНаРемонтИТОТС.ПустаяСсылка();
			
			ТекстСообщения = "";
			Если НЕ ПакетЗапросов[0].Пустой() Тогда
				Выборка = ПакетЗапросов[0].Выбрать();
				Пока Выборка.Следующий() Цикл
					ТекстСообщения = СтрШаблон("На основании документа %1 введен ремонт %2", ДанныеЗаполнения, Выборка.Ссылка);
				КонецЦикла;
			ИначеЕсли ПакетЗапросов[1].Пустой() Тогда
				ТекстСообщения = СтрШаблон("Документ %1 не в статусе к выполнению", ДанныеЗаполнения);
			КонецЕсли;
				ДополнительныеСвойства.Вставить("ОшибкаПриЗаполнении", Отказ);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							,
							,
							,
							Отказ);
		Иначе
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
			Автор = ПользователиКлиентСервер.ТекущийПользователь();
			ЗаявкаНаРемонтИТО = ДанныеЗаполнения;
			Работы.Загрузить(ДанныеЗаполнения.Работы.Выгрузить());
			РемонтируемоеДополнительноеОборудование.Загрузить(ДанныеЗаполнения.РемонтируемоеДополнительноеОборудование.Выгрузить());
			ЭтотОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРемонтаИТОТС.Запланирован);
			УстановитьНовыйНомер();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений") Тогда
			Подразделение = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(, ТранспортноеСредство);
		КонецЕсли;
		НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,ТранспортноеСредство);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЗаявкаНаРемонтИТО = Документы.упЗаявкаНаРемонтИТОТС.ПустаяСсылка();
	Автор             = Справочники.Пользователи.ПустаяСсылка();
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьЭлементыБлокировкиДанныхПроведения(БлокировкаДанных) 
	
	Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиМатериалов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных	= ДополнительныеСвойства.ОстаткиМатериалов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Материал", "Материал");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад"   , "Склад");
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоРемонтТС Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеМестоположенияТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияТранспортныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Транспорт", ТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
		
		Если ДополнительныеСвойства.Свойство("ОстаткиАгрегатов") Тогда
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упАгрегатыНаСкладах");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ОстаткиАгрегатов;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упАгрегатыТранспортныхСредств");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ОстаткиАгрегатов;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
			
		КонецЕсли;
					
		Если ДополнительныеСвойства.Свойство("Прицепы") Тогда   		
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрицепыТранспортныхСредств");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.Прицепы;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Прицеп", "Прицеп");
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРемонтовИТОТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	Если ДополнительныеСвойства.ИспользоватьЗаявкуНаРемонтИТОТС Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаРемонтИТОТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Документ", ЗаявкаНаРемонтИТО);
		
	ИначеЕсли Истина
			И ДополнительныеСвойства.ЭтоРемонтТС 
			И ДополнительныеСвойства.Свойство("ПлановыеДатыТО") Тогда 
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеДатыТО");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ПлановыеДатыТО;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТранспортноеСредство", "ТранспортноеСредство");
		ДополнительныеСвойства.Вставить("ИспользоватьПлановыеДатыТО", Истина);
		
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановоеРасписаниеРаботыГаража.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Ссылка);
			
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаАгрегатов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ШиныУзлыАгрегатыСобственные;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
КонецФункции	

#КонецОбласти 

#КонецЕсли
