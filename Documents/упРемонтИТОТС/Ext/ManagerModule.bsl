
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа
//
// Параметры:
//  Ссылка  - ДокументСсылка.упРемонтИТОТС - документ, которого необходимо получить.
//
// Возвращаемое значение:
//   ПеречислениеСсылка   - Ссылка на перечисление.
//
Функция ПолучитьСтатус(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыРемонтаИТОТС.Новый;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыРемонтовИТОТССрезПоследних.Статус,
	|	упРемонтИТОТС.ПометкаУдаления
	|ИЗ
	|	Документ.упРемонтИТОТС КАК упРемонтИТОТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРемонтовИТОТС.СрезПоследних(, Документ = &Документ) КАК упСтатусыРемонтовИТОТССрезПоследних
	|		ПО упСтатусыРемонтовИТОТССрезПоследних.Документ = упРемонтИТОТС.Ссылка
	|ГДЕ
	|	упРемонтИТОТС.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыРемонтаИТОТС.Отменен;
	Иначе	
		ЗаявкаНаРемонтИТО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ЗаявкаНаРемонтИТО");
		Если ЗначениеЗаполнено(ЗаявкаНаРемонтИТО) Тогда 
			Возврат Перечисления.упСтатусыРемонтаИТОТС.Запланирован;
		Иначе
			Возврат Перечисления.упСтатусыРемонтаИТОТС.Новый;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции		

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидРемонта");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Дефектная ведомость.
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упРемонтИТОТС";
  КомандаПечати.Идентификатор = "ПФ_Дефектная_ведомость";
  КомандаПечати.Представление = НСтр("ru = 'Дефектная ведомость'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 
  
  // Заказ-наряд.
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упРемонтИТОТС";
  КомандаПечати.Идентификатор = "ПФ_MXL_ЗаказНаряд";
  КомандаПечати.Представление = НСтр("ru = 'Заказ-наряд'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 
   	
КонецПроцедуры // ДобавитьКомандыПечати()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_Дефектная_ведомость") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_Дефектная_ведомость",
		НСтр("ru = 'Дефектная ведомость'"),
		ПечатьДефектнаяВедомость(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упРемонтИТОТС.ПФ_MXL_ТТН");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ЗаказНаряд") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_MXL_ЗаказНаряд",
		НСтр("ru = 'Заказ-наряд'"),
		ПечатьЗаказНаряд(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упРемонтИТОТС.ПФ_MXL_ЗаказНаряд");
	КонецЕсли;
	
КонецПроцедуры // Печать()

// Формирование печатной формы ТТН
Функция ПечатьДефектнаяВедомость(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПФ_Дефектная_ведомость";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументДефектнаяВедомость(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьДефектнаяВедомость()

Процедура ЗаполнитьТабличныйДокументДефектнаяВедомость(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
	тбДанныеРаботы = ДанныеДляПечати.Работы;
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упРемонтИТОТС.ПФ_MXL_Дефектная_ведомость");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка|Потолок");
	ОбластьСтрокаКомиссии = Макет.ПолучитьОбласть("СтрокаКомиссии|Потолок");	
	ОбластьОписаниеТС = Макет.ПолучитьОбласть("ОписаниеТС|Потолок");
	ОбластьШапкаТаблицы	= Макет.ПолучитьОбласть("ШапкаТаблицы|Потолок");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы|Потолок");
	ОбластьШапкаРабот = Макет.ПолучитьОбласть("ШапкаРабот|Потолок");
	ОбластьСтрокаРабот = Макет.ПолучитьОбласть("СтрокаРабот|Потолок");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал|Потолок");
			
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ТекущийОтбор  = Новый Структура("Документ", текСтрокаШапка.Документ);
		
		ОбластьШапка.Параметры.Руководитель = текСтрокаШапка.РуководительПредприятия;
		ОбластьШапка.Параметры.ПредставлениеОрганизация = текСтрокаШапка.ПредставлениеОрганизации;
		ОбластьШапка.Параметры.Номер = текСтрокаШапка.Номер;
		ОбластьШапка.Параметры.Дата = """"+Формат(текСтрокаШапка.Дата,"ДФ=dd")+""" "+Формат(текСтрокаШапка.Дата,"ДФ=ММММ")+" "+Формат(текСтрокаШапка.Дата,"ДФ=гггг");
		ТабличныйДокумент.Вывести(ОбластьШапка);
		Для Шаг = 1 По 2 Цикл
			ТабличныйДокумент.Вывести(ОбластьСтрокаКомиссии);			      		
		КонецЦикла;		
		
		Если текСтрокаШапка.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование
			ИЛИ текСтрокаШапка.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
			ОбластьОписаниеТС.Параметры.НаименованиеТС = текСтрокаШапка.РемонтируемыйОбъект;
		Иначе
			ОбластьОписаниеТС.Параметры.НаименованиеТС = текСтрокаШапка.ПредставлениеТС + " " + текСтрокаШапка.РегНомер;
		КонецЕсли;
		
		ОбластьОписаниеТС.Параметры.РасшифровкаТС = текСтрокаШапка.ТС;
		ТабличныйДокумент.Вывести(ОбластьОписаниеТС);
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	    				
		
		мсвСтроки = тбДанныеСтроки.НайтиСтроки(ТекущийОтбор);
				
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		
		Если КоличествоСтрок = 0 Тогда
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЕсли;
		
		Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтрокаТаблицы.Параметры.Заполнить(текЭлементСтрока);
			ОбластьСтрокаТаблицы.Параметры.СтрокаНомер = СтрокаНомер;
			
			ОбластьСтрокаТаблицы.Параметры.Номенклатура = текЭлементСтрока.ПредставлениеНоменклатуры;
			
			Если НЕ текЭлементСтрока.Агрегат Тогда
				ПредставлениеНоменклатуры = ?(ЗначениеЗаполнено(текЭлементСтрока.Номенклатура),текЭлементСтрока.ПредставлениеНоменклатуры + " (серия " + текЭлементСтрока.Номенклатура.Код + ")","");				
				ОбластьСтрокаТаблицы.Параметры.Номенклатура = ПредставлениеНоменклатуры;
			КонецЕсли;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтрокаТаблицы);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(ОбластьПодвал);
			КонецЕсли;
			Попытка
				Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
					ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
				Иначе
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
				КонецЕсли;
			Исключение
				ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			КонецПопытки;
			
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;		
		
		НайденныеСтроки = тбДанныеРаботы.НайтиСтроки(ТекущийОтбор);
		КоличествоРабот = НайденныеСтроки.Количество();
		НомерКрайнейРаботы = 0;
		Если КоличествоРабот = 0 Тогда
			НомерКрайнейРаботы = 1;
		Иначе
			НомерКрайнейРаботы = КоличествоРабот-1;
		КонецЕсли;
		Для Шаг=0 По НомерКрайнейРаботы Цикл
			ПредставлениеРабота = ?(КоличествоРабот=0,"",НайденныеСтроки[Шаг].ПредставлениеРабота);
			СтрокаСПодвалом = Новый Массив;
			Если Шаг = 0 Тогда				
				ОбластьШапкаРабот.Параметры.работы = ПредставлениеРабота;
				СтрокаСПодвалом.Добавить(ОбластьШапкаРабот);
				Попытка
					Если НЕ ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ТабличныйДокумент.Вывести(ОбластьШапкаРабот);
				Исключение
					ТабличныйДокумент.Вывести(ОбластьШапкаРабот);
				КонецПопытки;
			Иначе
				ОбластьСтрокаРабот.Параметры.Работа = ПредставлениеРабота;
				СтрокаСПодвалом.Добавить(ОбластьСтрокаРабот);
				Попытка
					Если НЕ ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ТабличныйДокумент.Вывести(ОбластьСтрокаРабот);
				Исключение
					ТабличныйДокумент.Вывести(ОбластьСтрокаРабот);
				КонецПопытки;				
			КонецЕсли;
		КонецЦикла;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапка);
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		КонецПопытки;
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
				
КонецПроцедуры // ЗаполнитьТабличныйДокументДефектнаяВедомость()

Функция ПечатьЗаказНаряд(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати 	= "ПараметрыПечати_ПФ_ЗаказНаряд";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументЗаказНаряд(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьДефектнаяВедомость()

Процедура ЗаполнитьТабличныйДокументЗаказНаряд(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
	тбДанныеРаботы = ДанныеДляПечати.Работы;
	тбДанныеПриобретенные = ДанныеДляПечати.Приобретенные;
	тбДанныеСобственные = ДанныеДляПечати.Собственные;
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упРемонтИТОТС.ПФ_MXL_ЗаказНаряд");
	
	ОбластьШапка_ = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаОбъектРемонта = Макет.ПолучитьОбласть("ШапкаОР");
	ОбластьШапкаРабот = Макет.ПолучитьОбласть("ШапкаТаблицыРабота");	
	ОбластьСтрокаРабот = Макет.ПолучитьОбласть("СтрокаТаблицыРабота");
	ОбластьШапкаПриобретенные = Макет.ПолучитьОбласть("ШапкаТаблицыПриобретенные");
	ОбластьСтрокаПриобретенные = Макет.ПолучитьОбласть("СтрокаТаблицыПриобретенные");
	ОбластьШапкаСобственные = Макет.ПолучитьОбласть("ШапкаТаблицыСобственные");
	ОбластьСтрокаСобственные = Макет.ПолучитьОбласть("СтрокаТаблицыСобственные");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		ОбластьШапка = ОбластьШапка_;
		ТекущийОтбор  = Новый Структура("Документ", текСтрокаШапка.Документ);
		Если текСтрокаШапка.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование
			ИЛИ текСтрокаШапка.ОбъектРемонта = Перечисления.упОбъектыРемонта.Агрегат Тогда
			ОбластьШапка = ОбластьШапкаОбъектРемонта;
			ОбластьШапка.Параметры.НаименованиеОбъекта = текСтрокаШапка.РемонтируемыйОбъект;
		КонецЕсли;
		ИтогСумма = 0;
		ИтогНДС = 0;
		ИтогСуммаНДС = 0;
		ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры, текСтрокаШапка);
		ОбластьШапка.Параметры.Дата = """"+Формат(текСтрокаШапка.Дата,"ДФ=dd")+""" "+Формат(текСтрокаШапка.Дата,"ДФ=ММММ")+" "+Формат(текСтрокаШапка.Дата,"ДФ=гггг");
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		// Таблица работ.
		ТабличныйДокумент.Вывести(ОбластьШапкаРабот);

		НайденныеСтроки = тбДанныеРаботы.НайтиСтроки(ТекущийОтбор);
		КоличествоРабот = НайденныеСтроки.Количество();
		Если КоличествоРабот > 0 Тогда
			НомерКрайнейРаботы = КоличествоРабот-1;
			Для Шаг=0 По НомерКрайнейРаботы Цикл
				ПредставлениеРабота = ?(КоличествоРабот=0,"",НайденныеСтроки[Шаг].ПредставлениеРабота);
				СтрокаСПодвалом = Новый Массив;
				ОбластьСтрокаРабот.Параметры.Работа = ПредставлениеРабота;
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаРабот.Параметры,НайденныеСтроки[Шаг]);
				ОбластьСтрокаРабот.Параметры.НомерСтроки = Шаг;
				СтрокаСПодвалом.Добавить(ОбластьСтрокаРабот);
				Попытка
					Если НЕ ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ТабличныйДокумент.Вывести(ОбластьСтрокаРабот);
				Исключение
					ТабличныйДокумент.Вывести(ОбластьСтрокаРабот);
				КонецПопытки;
				
				ИтогСумма = ИтогСумма + НайденныеСтроки[Шаг].Сумма;
				ИтогНДС = ИтогНДС + НайденныеСтроки[Шаг].СуммаНДС;
			КонецЦикла;
		Иначе
			ТабличныйДокумент.Вывести(ОбластьСтрокаРабот);
		КонецЕсли;

		// Таблица приобретенных материалов.
		мсвСтроки = тбДанныеПриобретенные.НайтиСтроки(ТекущийОтбор);
		
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		
		ТабличныйДокумент.Вывести(ОбластьШапкаПриобретенные);
		Если КоличествоСтрок > 0 Тогда
			Для Каждого текЭлементСтрока Из мсвСтроки Цикл
				ОбластьСтрокаПриобретенные.Параметры.Заполнить(текЭлементСтрока);
				ОбластьСтрокаПриобретенные.Параметры.НомерСтроки = СтрокаНомер;
				
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбластьСтрокаПриобретенные);
				Если СтрокаНомер = КоличествоСтрок Тогда
					СтрокаСПодвалом.Добавить(ОбластьПодвал);
				КонецЕсли;
				Попытка
					Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.Вывести(ОбластьСтрокаПриобретенные);
					Иначе
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ТабличныйДокумент.Вывести(ОбластьШапкаПриобретенные);
						ТабличныйДокумент.Вывести(ОбластьСтрокаПриобретенные);
					КонецЕсли;
				Исключение
					ТабличныйДокумент.Вывести(ОбластьСтрокаПриобретенные);
				КонецПопытки;
				
				СтрокаНомер = СтрокаНомер + 1;
				ИтогСумма = ИтогСумма + текЭлементСтрока.Сумма;
				ИтогНДС = ИтогНДС + текЭлементСтрока.СуммаНДС;
			КонецЦикла;
		Иначе
			ТабличныйДокумент.Вывести(ОбластьСтрокаПриобретенные);
		КонецЕсли; 
				
		// Таблица собственных материалов.
		мсвСтроки = тбДанныеСобственные.НайтиСтроки(ТекущийОтбор);
		
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		ТабличныйДокумент.Вывести(ОбластьШапкаСобственные);
		Если КоличествоСтрок > 0 Тогда
			Для Каждого текЭлементСтрока Из мсвСтроки Цикл
				ОбластьСтрокаСобственные.Параметры.Заполнить(текЭлементСтрока);
				ОбластьСтрокаСобственные.Параметры.НомерСтроки = СтрокаНомер;
				
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбластьСтрокаСобственные);
				Если СтрокаНомер = КоличествоСтрок Тогда
					СтрокаСПодвалом.Добавить(ОбластьПодвал);
				КонецЕсли;
				Попытка
					Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.Вывести(ОбластьСтрокаСобственные);
					Иначе
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ТабличныйДокумент.Вывести(ОбластьШапкаСобственные);
						ТабличныйДокумент.Вывести(ОбластьСтрокаСобственные);
					КонецЕсли;
				Исключение
					ТабличныйДокумент.Вывести(ОбластьСтрокаСобственные);
				КонецПопытки;
				
				СтрокаНомер = СтрокаНомер + 1;
			КонецЦикла;	
		Иначе
			ТабличныйДокумент.Вывести(ОбластьСтрокаСобственные);
		КонецЕсли;
		
		ОбластьПодвал.Параметры.ИтогСумма = ИтогСумма;
		ОбластьПодвал.Параметры.ИтогНДС = ИтогНДС;
		ОбластьПодвал.Параметры.ИтогСуммаНДС = ИтогСумма + ИтогНДС;

		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапка);
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		КонецПопытки;
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
				
КонецПроцедуры // ЗаполнитьТабличныйДокументЗаказНаряд()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение
// Заполнить дополнительные свойства документа.
//
Процедура ИнициализироватьДанныеДокумента(РеквизитыОбъекта, ДополнительныеСвойства) Экспорт
		
	// Остатки материалов.
	
	тбзОстаткиМатериалов  = Новый ТаблицаЗначений;
	тбзОстаткиМатериалов.Колонки.Добавить("Материал", Новый ОписаниеТипов("СправочникСсылка.упЗапчасти, СправочникСсылка.упГСМ"));
	тбзОстаткиМатериалов.Колонки.Добавить("ВидГСМ", Новый ОписаниеТипов("СправочникСсылка.упВидыГСМ"));
	тбзОстаткиМатериалов.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.упСклады"));
	тбзОстаткиМатериалов.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",
											  Новый КвалификаторыЧисла(12, 3, ДопустимыйЗнак.Любой)));
	
	Если РеквизитыОбъекта.СпособРемонта = Перечисления.упСпособыРемонтов.СобственнымиСилами Тогда
		
		мсвГСМ = РеквизитыОбъекта.ГСМСобственные.ВыгрузитьКолонку("ГСМ");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мсвГСМ, РеквизитыОбъекта.ГСМСобственныеДополнительногоОборудования.ВыгрузитьКолонку("ГСМ"));
		мсвГСМУникальные = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвГСМ);
		
		ВидыГСМ = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвГСМУникальные, "Владелец");
				
		Для каждого Строка Из РеквизитыОбъекта.ГСМСобственные Цикл
			
			НоваяСтрока = тбзОстаткиМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Материал = Строка.ГСМ;
			НоваяСтрока.ВидГСМ   = ВидыГСМ.Получить(Строка.ГСМ);
			
		КонецЦикла;
		
		Для каждого Строка Из РеквизитыОбъекта.ГСМСобственныеДополнительногоОборудования Цикл
			
			НоваяСтрока = тбзОстаткиМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Материал = Строка.ГСМ;
			НоваяСтрока.ВидГСМ   = ВидыГСМ.Получить(Строка.ГСМ);
			
		КонецЦикла;
		
		Для каждого Строка Из РеквизитыОбъекта.ЗапчастиСобственные Цикл
			
			НоваяСтрока = тбзОстаткиМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Материал = Строка.Запчасть;
			НоваяСтрока.ВидГСМ   = Справочники.упВидыГСМ.ПустаяСсылка();
			
		КонецЦикла;
		
		тбзОстаткиМатериалов.Свернуть("Материал, ВидГСМ, Склад", "Количество");
				
	КонецЕсли;
	
	Если тбзОстаткиМатериалов.Количество() > 0 Тогда
		ДополнительныеСвойства.Вставить("ОстаткиМатериалов", тбзОстаткиМатериалов);	
	КонецЕсли;
	
	
	// Плановые даты ТО.
	
	тбзПлановыеДатыТО = Новый ТаблицаЗначений;
	тбзПлановыеДатыТО.Колонки.Добавить("ТранспортноеСредство");
	тбзПлановыеДатыТО.Колонки.Добавить("ВидРемонта");
	
	мсвВидыРемонта = РеквизитыОбъекта.РемонтируемоеДополнительноеОборудование.ВыгрузитьКолонку("ВидРемонта");
	мсвВидыРемонта.Добавить(РеквизитыОбъекта.ВидРемонта);
	мсвВидыРемонтаУникальные = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвВидыРемонта);
	
	ТипыРемонта = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвВидыРемонтаУникальные, "ТипРемонта");
	
	Если ТипыРемонта.Получить(РеквизитыОбъекта.ВидРемонта) = Перечисления.упТипыРемонтов.ПлановоеТО Тогда
		НоваяСтрока = тбзПлановыеДатыТО.Добавить();	
		НоваяСтрока.ТранспортноеСредство = ?(РеквизитыОбъекта.ОбъектРемонта = Перечисления.упОбъектыРемонта.ДополнительноеОборудование, 
		                                      РеквизитыОбъекта.ДополнительноеОборудованиеРемонта, 
									   РеквизитыОбъекта.ТранспортноеСредство);
		НоваяСтрока.ВидРемонта           = РеквизитыОбъекта.ВидРемонта;
	КонецЕсли;
	
	Для каждого Строка Из РеквизитыОбъекта.РемонтируемоеДополнительноеОборудование Цикл
		Если ТипыРемонта.Получить(РеквизитыОбъекта.ВидРемонта) <> Перечисления.упТипыРемонтов.ПлановоеТО Тогда
			Продолжить;
			НоваяСтрока = тбзПлановыеДатыТО.Добавить();	
			НоваяСтрока.ТранспортноеСредство = Строка.ДополнительноеОборудование;
			НоваяСтрока.ВидРемонта           = Строка.ВидРемонта;
		КонецЕсли;
	КонецЦикла;
	
	Если тбзПлановыеДатыТО.Количество() > 0 Тогда
		ДополнительныеСвойства.Вставить("ПлановыеДатыТО", тбзПлановыеДатыТО);	
	КонецЕсли;
	

	// Запчасти по статьям расходов.
	тбзЗапчастиПоСтатьямРасходов = Новый ТаблицаЗначений;
	тбзЗапчастиПоСтатьямРасходов.Колонки.Добавить("Материал");
	тбзЗапчастиПоСтатьямРасходов.Колонки.Добавить("Склад");
	тбзЗапчастиПоСтатьямРасходов.Колонки.Добавить("Количество");
	тбзЗапчастиПоСтатьямРасходов.Колонки.Добавить("СтатьяРасходов");
	тбзЗапчастиПоСтатьямРасходов.Колонки.Добавить("ПравилоРаспределения");
	
	мсвСтатьиРасходов = РеквизитыОбъекта.ЗапчастиСобственные.ВыгрузитьКолонку("СтатьяРасходов");
	мсвСтатьиРасходовУникальные = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвСтатьиРасходов);
	ПравилаРаспределения = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвСтатьиРасходовУникальные, "ПравилаРаспределения");
	
	Для каждого Строка Из РеквизитыОбъекта.ЗапчастиСобственные Цикл
		
		НоваяСтрока = тбзЗапчастиПоСтатьямРасходов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Материал = Строка.Запчасть;
		Если ЗначениеЗаполнено(Строка.СтатьяРасходов) Тогда
			НоваяСтрока.СтатьяРасходов = Строка.СтатьяРасходов;
			НоваяСтрока.ПравилоРаспределения = ПравилаРаспределения.Получить(Строка.СтатьяРасходов);
		Иначе	
			НоваяСтрока.СтатьяРасходов       = ДополнительныеСвойства.СтатьяРасходов;
			НоваяСтрока.ПравилоРаспределения = ДополнительныеСвойства.ПравилоРаспределения;
		КонецЕсли;
		
		тбзЗапчастиПоСтатьямРасходов.Свернуть("Материал, Склад, ПравилоРаспределения, СтатьяРасходов", "Количество");
				
	КонецЦикла;

	Если тбзЗапчастиПоСтатьямРасходов.Количество() > 0 Тогда
		ДополнительныеСвойства.Вставить("ЗапчастиПоСтатьямРасходов", тбзЗапчастиПоСтатьямРасходов);	
	КонецЕсли;
	
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Заполнить дополнительные свойства документа.
//
Процедура ИнициализироватьДанныеАгрегатовДокумента(Дата, РеквизитыОбъекта, ДополнительныеСвойства) Экспорт
	
	
	тбзОстаткиАгрегатов = Новый ТаблицаЗначений;
	тбзОстаткиАгрегатов.Колонки.Добавить("НомерСтроки");
	тбзОстаткиАгрегатов.Колонки.Добавить("Агрегат");
	тбзОстаткиАгрегатов.Колонки.Добавить("ТипОперацииМонтажа");
	тбзОстаткиАгрегатов.Колонки.Добавить("ХодоваяШина");
	тбзОстаткиАгрегатов.Колонки.Добавить("Склад");
	тбзОстаткиАгрегатов.Колонки.Добавить("Количество");
	
	Для каждого Строка Из РеквизитыОбъекта.ШиныУзлыАгрегатыСобственные Цикл
		
		НоваяСтрока = тбзОстаткиАгрегатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
	КонецЦикла;
	
	Для каждого Строка Из РеквизитыОбъекта.ШиныУзлыАгрегаты Цикл
		
		НоваяСтрока = тбзОстаткиАгрегатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Склад = РеквизитыОбъекта.ТранспортноеСредство;
				
	КонецЦикла;
	
	Для каждого Строка Из РеквизитыОбъекта.ДополнительноеОборудованиеСобственное Цикл
		
		НоваяСтрока = тбзОстаткиАгрегатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Агрегат     = Строка.ДополнительноеОборудование;
		НоваяСтрока.ХодоваяШина = Ложь;
		НоваяСтрока.Склад       = РеквизитыОбъекта.ТранспортноеСредство;
		
	КонецЦикла;

	Для каждого Строка Из РеквизитыОбъекта.ДополнительноеОборудование Цикл
		
		НоваяСтрока = тбзОстаткиАгрегатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Агрегат     = Строка.ДополнительноеОборудование;
		НоваяСтрока.ХодоваяШина = Ложь;
		НоваяСтрока.Склад       = РеквизитыОбъекта.ТранспортноеСредство;
		
	КонецЦикла;
	
	тбзОстаткиАгрегатов.Сортировать("НомерСтроки");
	
	Если тбзОстаткиАгрегатов.Количество() > 0 Тогда
		ДополнительныеСвойства.Вставить("ОстаткиАгрегатов", тбзОстаткиАгрегатов);
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШиныУзлыАгрегаты.Агрегат КАК Агрегат,
	|	ШиныУзлыАгрегаты.ХодоваяШина КАК ХодоваяШина,
	|	ШиныУзлыАгрегаты.ПробегКм КАК ПробегКм,
	|	ШиныУзлыАгрегаты.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	ШиныУзлыАгрегаты.Сумма КАК Сумма,
	|	ШиныУзлыАгрегаты.Валюта КАК Валюта
	|ПОМЕСТИТЬ втШиныУзлыАгрегаты
	|ИЗ
	|	&ШиныУзлыАгрегаты КАК ШиныУзлыАгрегаты
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбДопОборудование.ДополнительноеОборудование КАК Агрегат,
	|	тбДопОборудование.ПробегКм КАК ПробегКм,
	|	тбДопОборудование.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	тбДопОборудование.Сумма КАК Сумма,
	|	тбДопОборудование.Валюта КАК Валюта
	|ПОМЕСТИТЬ втДополнительноеОборудование
	|ИЗ
	|	&ДопОборудование КАК тбДопОборудование
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ТранспортноеСредство КАК ТранспортноеСредство,
	|	тбчАгрегаты.Агрегат КАК Агрегат,
	|	тбчАгрегаты.ХодоваяШина КАК ХодоваяШина,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено) КАК Операция,
	|	тбчАгрегаты.ПробегКм КАК ПробегКм,
	|	тбчАгрегаты.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	тбчАгрегаты.Сумма * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1)) КАК Сумма,
	|	1 КАК Количество
	|ИЗ
	|	втШиныУзлыАгрегаты КАК тбчАгрегаты
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		Валюта = &Валюта) КАК КурсыВалютДокумента
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		) КАК КурсыВалют
	|	ПО тбчАгрегаты.Валюта = КурсыВалют.Валюта
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	&ТранспортноеСредство КАК ТранспортноеСредство,
	|	тбчДопОборудование.Агрегат КАК Агрегат,
	|	ЛОЖЬ КАК ХодоваяШина,
	|	ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено) КАК Операция,
	|	тбчДопОборудование.ПробегКм КАК ПробегКм,
	|	тбчДопОборудование.ПробегМотоЧасы КАК ПробегМотоЧасы,
	|	тбчДопОборудование.Сумма * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1)) КАК Сумма,
	|	1 КАК Количество
	|ИЗ
	|	втДополнительноеОборудование КАК тбчДопОборудование
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		Валюта = &Валюта) КАК КурсыВалютДокумента
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|		&Дата,
	|		) КАК КурсыВалют
	|	ПО тбчДопОборудование.Валюта = КурсыВалют.Валюта
	|";

	Запрос.УстановитьПараметр("ШиныУзлыАгрегаты", 	РеквизитыОбъекта.ШиныУзлыАгрегаты.Выгрузить());
	Запрос.УстановитьПараметр("ДопОборудование",      РеквизитыОбъекта.ДополнительноеОборудование.Выгрузить());
	
	Запрос.УстановитьПараметр("ТранспортноеСредство", РеквизитыОбъекта.ТранспортноеСредство);
	Запрос.УстановитьПараметр("Дата",                 Дата);
	Запрос.УстановитьПараметр("Валюта",               упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета"));  
	НаборРезультатов = Запрос.ВыполнитьПакет();
	
	// Агрегаты
	ИндексВыработкаАгрегатов = 2;
	Если Не НаборРезультатов[ИндексВыработкаАгрегатов].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ВыработкаАгрегатов", НаборРезультатов[ИндексВыработкаАгрегатов].Выгрузить());
	КонецЕсли;
	
КонецПроцедуры // ИнициализироватьДанныеАгрегатовДокумента()

// Процедура - выполнить контроль агрегатов.
//
Процедура ВыполнитьКонтрольАгрегатов(Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	Агрегаты.Ссылка КАК Ссылка,
	|	Агрегаты.НомерСтроки КАК НомерСтроки,
	|	Агрегаты.Ссылка.ТранспортноеСредство КАК ТС,
	|	Агрегаты.Агрегат КАК Агрегат,
	|	ВЫБОР
	|		КОГДА Агрегаты.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Снятие
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.упРемонтИТОТС.ШиныУзлыАгрегаты КАК Агрегаты
	|ГДЕ
	|	Агрегаты.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДопОборудование.Ссылка,
	|	ДопОборудование.НомерСтроки,
	|	ДопОборудование.Ссылка.ТранспортноеСредство,
	|	ДопОборудование.ДополнительноеОборудование,
	|	ВЫБОР
	|		КОГДА ДопОборудование.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	Документ.упРемонтИТОТС.ДополнительноеОборудование КАК ДопОборудование
	|ГДЕ
	|	ДопОборудование.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.Склад, ЗНАЧЕНИЕ(Справочник.упСклады.ПустаяСсылка)) КАК Склад,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.КоличествоОстаток, 0) КАК Количество
	|ПОМЕСТИТЬ втСклады
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК упАгрегатыНаСкладахОстатки
	|		ПО втДокумент.Агрегат = упАгрегатыНаСкладахОстатки.Агрегат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	ЕСТЬNULL(АгрегатыТССП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС,
	|	ВЫБОР
	|		КОГДА АгрегатыТССП.ТранспортноеСредство ЕСТЬ NULL
	|				ИЛИ АгрегатыТССП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Снято)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ втТС
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|				&Период,
	|				Агрегат В
	|					(ВЫБРАТЬ
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК АгрегатыТССП
	|		ПО втДокумент.Агрегат = АгрегатыТССП.Агрегат
	|			И (ВЫБОР
	|				КОГДА втДокумент.Снятие
	|					ТОГДА втДокумент.ТС = АгрегатыТССП.ТранспортноеСредство
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСклады.Агрегат КАК Агрегат,
	|	втСклады.Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка) КАК ТС,
	|	втСклады.Количество КАК Количество
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втСклады КАК втСклады
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТС.Агрегат,
	|	ЗНАЧЕНИЕ(Справочник.упСклады.ПустаяСсылка),
	|	втТС.ТС,
	|	втТС.Количество
	|ИЗ
	|	втТС КАК втТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	ЕСТЬNULL(втДанные.Склад, ЗНАЧЕНИЕ(Справочник.упСклады.ПустаяСсылка)) КАК Склад,
	|	ЕСТЬNULL(втДанные.ТС, втДокумент.ТС) КАК ТС,
	|	СУММА(ЕСТЬNULL(втДанные.Количество, 0)) КАК Количество,
	|	ЕСТЬNULL(втДокумент.Снятие, ЛОЖЬ) КАК Снятие,
	|	втДокумент.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанные КАК втДанные
	|		ПО втДокумент.Агрегат = втДанные.Агрегат
	|			И (ВЫБОР
	|				КОГДА втДокумент.Снятие
	|					ТОГДА втДокумент.ТС = втДанные.ТС
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА втДокумент.Снятие
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ НЕ втДанные.Количество = 0
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(втДокумент.Снятие, ЛОЖЬ),
	|	втДокумент.Агрегат,
	|	ЕСТЬNULL(втДанные.Склад, ЗНАЧЕНИЕ(Справочник.упСклады.ПустаяСсылка)),
	|	ЕСТЬNULL(втДанные.ТС, втДокумент.ТС),
	|	втДокумент.НомерСтроки";
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Граница);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = "";
		Если ЗначениеЗаполнено(Выборка.ТС) Тогда
			Если Выборка.Снятие И Выборка.Количество = 0 Тогда
				Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
				    	ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", не установлен на транспортное средство ""%2"".'", Выборка.Агрегат, Ссылка.ТранспортноеСредство));
				Иначе	
				    	ТекстОшибки = НСтр(СтрШаблон("ru = 'Дополнительное оборудование: ""%1"", не установлено на транспортное средство ""%2"".'", Выборка.Агрегат, Ссылка.ТранспортноеСредство));
				КонецЕсли;
			КонецЕсли;
			Если НЕ Выборка.Снятие И НЕ (Выборка.Количество = 0) Тогда
				Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
					ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", установлен на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.ТС));
				Иначе	
					ТекстОшибки = НСтр(СтрШаблон("ru = 'Дополнительное оборудование: ""%1"", установлено на транспортное средство ""%2"".'", Выборка.Агрегат, Выборка.ТС));
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Склад) Тогда
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
  				ТекстОшибки = НСтр(СтрШаблон("ru = 'Агрегат: ""%1"", находится на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));
			Иначе
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Дополнительное оборудование: ""%1"", находится на складе ""%2"".'", Выборка.Агрегат, Выборка.Склад));	
			КонецЕсли;	
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Если ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты") Тогда
				ИмяТабличнойЧасти 	= "ДополнительноеОборудование";
				ИмяПоля			= "ДополнительноеОборудование";
			Иначе
				ИмяТабличнойЧасти 	= "ШиныУзлыАгрегаты";
				ИмяПоля			= "Агрегат";
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Ссылка,СтрШаблон("%1[%2].%3", ИмяТабличнойЧасти,Формат(Выборка.НомерСтроки-1,"ЧН=0; ЧГ=0"), ИмяПоля),"Объект", Отказ);	
		КонецЕсли;	
			
		
	КонецЦикла;
	
КонецПроцедуры

// Заполнить дополнительные свойства "Прицепы".
//
Процедура ИнициализироватьДанныеПрицеповДокумента(РеквизитыОбъекта, ДополнительныеСвойства) Экспорт
	
	тбзПрицепы = Новый ТаблицаЗначений;
	тбзПрицепы.Колонки.Добавить("Прицеп");
	тбзПрицепы.Колонки.Добавить("Операция");
	тбзПрицепы.Колонки.Добавить("ТранспортноеСредство");
	
	Для каждого Строка Из РеквизитыОбъекта.Прицепы Цикл
		НоваяСтрока = тбзПрицепы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ТранспортноеСредство = РеквизитыОбъекта.ТранспортноеСредство;
	КонецЦикла;
	
	Если тбзПрицепы.Количество() > 0 Тогда
		ДополнительныеСвойства.Вставить("Прицепы", тбзПрицепы);	
	КонецЕсли;	
			
КонецПроцедуры // ИнициализироватьДанныеПрицеповДокумента()

// Процедура - выполнить контроль прицепа.
//
Процедура ВыполнитьКонтрольПрицепа(Ссылка, РеквизитыОбъекта, ДополнительныеСвойства, Отказ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Прицепы.Прицеп КАК Прицеп,
	               |	Прицепы.Операция КАК Операция,
	               |	Прицепы.ТранспортноеСредство КАК ТранспортноеСредство
	               |ПОМЕСТИТЬ втПрицепы
	               |ИЗ
	               |	&Прицепы КАК Прицепы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	втПрицепы.Прицеп КАК Прицеп,
	               |	втПрицепы.Операция КАК Операция,
	               |	втПрицепы.ТранспортноеСредство КАК ТранспортноеСредство
	               |ПОМЕСТИТЬ ТЧПрицеы
	               |ИЗ
	               |	втПрицепы КАК втПрицепы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(упПрицепы.Операция, ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.ПустаяСсылка)) КАК Операция,
	               |	ЕСТЬNULL(упПрицепы.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	               |	ТЧПрицепы.Операция КАК ТекОперация,
	               |	ТЧПрицепы.Прицеп КАК Прицеп,
	               |	ТЧПрицепы.ТранспортноеСредство КАК ТекТранспортноеСредство
	               |ИЗ
	               |	ТЧПрицепы КАК ТЧПрицепы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних(
	               |				&Дата,
	               |				Прицеп В
	               |					(ВЫБРАТЬ
	               |						ТЧПрицепы.Прицеп
	               |					ИЗ
	               |						ТЧПрицепы КАК ТЧПрицепы)) КАК упПрицепы
	               |		ПО ТЧПрицепы.Прицеп = упПрицепы.Прицеп";
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Запрос.УстановитьПараметр("Дата", Новый Граница(Ссылка.Дата, ВидГраницы.Исключая));
	Иначе
		Запрос.УстановитьПараметр("Дата", РеквизитыОбъекта.Дата);	
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Прицепы", ДополнительныеСвойства.Прицепы);

	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать(); 
		ТекстОшибки = "";
		Пока Выборка.Следующий()Цикл 	
			
			Если Выборка.Операция = Перечисления.упТипыОперацийМонтажа.Установлено 
				И Выборка.ТекОперация = Перечисления.упТипыОперацийМонтажа.Установлено Тогда 
				
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Прицеп: ""%1"" уже закреплен за ТС: ""%2"".'", Выборка.Прицеп, Выборка.ТранспортноеСредство));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
				
			ИначеЕсли Выборка.Операция = Перечисления.упТипыОперацийМонтажа.Снято
				и  Выборка.ТекОперация = Перечисления.упТипыОперацийМонтажа.Снято Тогда 
				
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Прицеп: ""%1"" уже  снят с ТС.'", Выборка.Прицеп));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
				
			ИначеЕсли Выборка.Операция = Перечисления.упТипыОперацийМонтажа.Установлено 
				и  Выборка.ТекОперация = Перечисления.упТипыОперацийМонтажа.Снято
				и Выборка.ТранспортноеСредство <> Выборка.ТекТранспортноеСредство Тогда 
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Прицеп: ""%1"" закреплен за другим ТС: ""%2"".'", Выборка.Прицеп, Выборка.ТранспортноеСредство));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
				
			ИначеЕсли Выборка.Операция = Перечисления.упТипыОперацийМонтажа.ПустаяСсылка() 
				и  Выборка.ТекОперация = Перечисления.упТипыОперацийМонтажа.Снято Тогда 
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Прицеп: ""%1"" не закреплен ни за одним ТС. Операция не выполнена.'", Выборка.Прицеп));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;		
	КонецЕсли;
	
	
КонецПроцедуры //ПолучитьСписокСвободныхПрицеповНаСервереБезКонтекста()

// Процедура - выполнить контроль ГСМ.
//
Процедура ВыполнитьКонтрольГСМРасходСоСклада(Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Если НЕ Реквизиты.СпособРемонта = ПредопределенноеЗначение("Перечисление.упСпособыРемонтов.СобственнымиСилами") Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	упРемонтИТОТС.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ вт_Документы
	|ИЗ
	|	Документ.упРемонтИТОТС КАК упРемонтИТОТС
	|ГДЕ
	|	упРемонтИТОТС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРемонтИТОТСГСМ.ВидГСМ КАК ВидГСМ,
	|	СУММА(упРемонтИТОТСГСМ.РасходСоСклада) КАК Расход
	|ПОМЕСТИТЬ вт_Расход
	|ИЗ
	|	Документ.упРемонтИТОТС.ГСМ КАК упРемонтИТОТСГСМ
	|ГДЕ
	|	упРемонтИТОТСГСМ.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|
	|СГРУППИРОВАТЬ ПО
	|	упРемонтИТОТСГСМ.ВидГСМ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ГСМДопОбр.ВидГСМ,
	|	СУММА(ГСМДопОбр.РасходСоСклада)
	|ИЗ
	|	Документ.упРемонтИТОТС.ГСМДополнительногоОборудования КАК ГСМДопОбр
	|ГДЕ
	|	ГСМДопОбр.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГСМДопОбр.ВидГСМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРемонтИТОТСГСМСобственные.ГСМ.Владелец КАК ВидГСМ,
	|	СУММА(упРемонтИТОТСГСМСобственные.Количество) КАК Остатки
	|ПОМЕСТИТЬ вт_Скалд
	|ИЗ
	|	Документ.упРемонтИТОТС.ГСМСобственные КАК упРемонтИТОТСГСМСобственные
	|ГДЕ
	|	упРемонтИТОТСГСМСобственные.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|
	|СГРУППИРОВАТЬ ПО
	|	упРемонтИТОТСГСМСобственные.ГСМ.Владелец
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ГСМСобственныеДопобр.ГСМ.Владелец,
	|	СУММА(ГСМСобственныеДопобр.Количество)
	|ИЗ
	|	Документ.упРемонтИТОТС.ГСМСобственныеДополнительногоОборудования КАК ГСМСобственныеДопобр
	|ГДЕ
	|	ГСМСобственныеДопобр.Ссылка В
	|			(ВЫБРАТЬ
	|				вт_Документы.Ссылка
	|			ИЗ
	|				вт_Документы КАК вт_Документы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГСМСобственныеДопобр.ГСМ.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Расход.ВидГСМ КАК ВидГСМ,
	|	вт_Расход.Расход КАК Расход,
	|	вт_Скалд.Остатки КАК Остатки,
	|	вт_Скалд.Остатки - вт_Расход.Расход КАК Количество
	|ПОМЕСТИТЬ вт_Использовано
	|ИЗ
	|	вт_Расход КАК вт_Расход
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Скалд КАК вт_Скалд
	|		ПО вт_Расход.ВидГСМ = вт_Скалд.ВидГСМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Расход.ВидГСМ КАК ВидГСМ,
	|	ВЫБОР
	|		КОГДА вт_Использовано.Количество ЕСТЬ NULL
	|			ТОГДА -1 * вт_Расход.Расход
	|		ИНАЧЕ вт_Использовано.Количество
	|	КОНЕЦ КАК Остаток
	|ИЗ
	|	вт_Расход КАК вт_Расход
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Использовано КАК вт_Использовано
	|		ПО (ВЫБОР
	|				КОГДА вт_Использовано.ВидГСМ ЕСТЬ NULL
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ вт_Расход.ВидГСМ = вт_Использовано.ВидГСМ
	|			КОНЕЦ)";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить();
	
	Если НЕ Выборка.Пустой() Тогда
		ВыборкаПроверки = Выборка.Выбрать();
		Пока ВыборкаПроверки.Следующий() Цикл
			ТекущееКоличество = ВыборкаПроверки.Остаток;
			Если ТекущееКоличество < 0 Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'В табличной части ГСМ -> Транспортное средство, указан вида горюче-смазочных материалов: ""%1"". Недостаточно заправлено со склада, не хватает количества на складе, ""%2"" ед.'", ВыборкаПроверки.ВидГСМ, -1*ТекущееКоличество));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
			КонецЕсли;
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - таблица ГСМ заполняется средними ценами по складу.
//
Процедура ВыполнитьОбработкуГСМСоСклада(ДополнительныеСвойства, Реквизиты, Отказ) Экспорт
	
	Если Истина 
		И ДополнительныеСвойства.Свойство("ГСМ") 
		И ДополнительныеСвойства.ГСМ.Колонки.Найти("ЦенаСклад") = Неопределено 
	Тогда
		ДополнительныеСвойства.ГСМ.Колонки.Добавить("ЦенаСклад",Новый ОписаниеТипов("Число"),"ЦенаСклад");
		ДополнительныеСвойства.ГСМ.Колонки.Добавить("НДССклад",Новый ОписаниеТипов("Число"),"НДССклад");
	КонецЕсли;
	
	Если Ложь
		Или Отказ 
		Или (Истина 
			И Реквизиты.ГСМ.Количество() = 0 
			И Реквизиты.ГСМДополнительногоОборудования.Количество() = 0)
		Или Реквизиты.СпособРемонта <> ПредопределенноеЗначение("Перечисление.упСпособыРемонтов.СобственнымиСилами")	
	Тогда 
		Возврат; 
	КонецЕсли;
	
	Статус = ДополнительныеСвойства.Статус;
	
	Если Истина
		И (Ложь
		   Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Начат 
		   Или Статус = Перечисления.упСтатусыРемонтаИТОТС.Завершен)
		И ДополнительныеСвойства.Свойство("ОстаткиМатериалов")	
	Тогда		
			
		ТаблицаЗначений = Новый ТаблицаЗначений;		
		
		// таблица содержит виды гсм, среднюю стоимость каждого вида.
		ТаблицаЗначений.Колонки.Добавить("ВидГСМ",     Новый ОписаниеТипов("СправочникСсылка.упВидыГСМ"),"ВидГСМ");
		ТаблицаЗначений.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"),"Количество");
		ТаблицаЗначений.Колонки.Добавить("Цена",       Новый ОписаниеТипов("Число"),"Цена");
		ТаблицаЗначений.Колонки.Добавить("НДС",        Новый ОписаниеТипов("Число"),"НДС");
		
		Для каждого Строка Из ДополнительныеСвойства.ОстаткиМатериалов Цикл
			
			Если НЕ ТипЗнч(Строка.Материал) = ТИП("СправочникСсылка.упГСМ")Тогда
				Продолжить;				
			КонецЕсли;
			
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.ВидГСМ     = Строка.ВидГСМ;
			НоваяСтрока.Количество = Строка.Количество;			
			НоваяСтрока.Цена       = Строка.СуммаБезНДС;
			НоваяСтрока.НДС        = Строка.СуммаСНДС - Строка.СуммаБезНДС;
			
		КонецЦикла;
		
		ТаблицаЗначений.Свернуть("ВидГСМ", "Количество, Цена, НДС");			
		
		Для каждого Строка Из ДополнительныеСвойства.ГСМ Цикл
			
			сткОтбор        = Новый Структура("ВидГСМ",Строка.ВидГСМ);
			НайденныеСтроки = ТаблицаЗначений.НайтиСтроки(сткОтбор);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокаЦен = НайденныеСтроки[0];
				Если СтрокаЦен.Количество <> 0 Тогда
					Строка.ЦенаСклад = СтрокаЦен.Цена / СтрокаЦен.Количество;
					Строка.НДССклад  = СтрокаЦен.НДС / СтрокаЦен.Количество;
				Иначе
					Строка.ЦенаСклад = 0;
					Строка.НДССклад  = 0;
				КонецЕсли;
			Иначе
				Строка.ЦенаСклад = 0;
				Строка.НДССклад  = 0;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбрабоктуГСМСоСклада()

#КонецОбласти

Функция ПолучитьДанныеДляПечатныхФорм(МассивОбъектов, НужныДоверенности = Ложь)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упРемонтИТОТС.Ссылка КАК Документ,
	|	упРемонтИТОТС.ОбъектРемонта КАК ОбъектРемонта,
	|	ВЫБОР КОГДА упРемонтИТОТС.ОбъектРемонта = ЗНАЧЕНИЕ(Перечисление.упОбъектыРемонта.ДополнительноеОборудование) ТОГДА упРемонтИТОТС.ДополнительноеОборудованиеРемонта КОГДА упРемонтИТОТС.ОбъектРемонта = ЗНАЧЕНИЕ(Перечисление.упОбъектыРемонта.Агрегат) ТОГДА упРемонтИТОТС.Агрегат ИНАЧЕ упРемонтИТОТС.ТранспортноеСредство КОНЕЦ КАК РемонтируемыйОбъект,
	|	упРемонтИТОТС.Организация,
	|	упРемонтИТОТС.Дата,
	|	упРемонтИТОТС.Номер,
	|	упРемонтИТОТС.СервиснаяОрганизация КАК СервиснаяОрганизация,
	|	упРемонтИТОТС.ВидРемонта КАК ВидРемонта,
	|	упРемонтИТОТС.ТранспортноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	ПРЕДСТАВЛЕНИЕ(упРемонтИТОТС.Организация) КАК ПредставлениеОрганизации,
	|	упРемонтИТОТС.ТранспортноеСредство.Ссылка КАК ТС,
	|	упРемонтИТОТС.ТранспортноеСредство.Код КАК ГосНомер,
	|	упРемонтИТОТС.ТранспортноеСредство.РегистрационныйНомер КАК РегНомер,
	|	ПРЕДСТАВЛЕНИЕ(упРемонтИТОТС.ТранспортноеСредство.Ссылка) КАК ПредставлениеТС,
	|	упРемонтИТОТС.ТранспортноеСредство.Модель КАК Модель,
	|	упРемонтИТОТС.ТранспортноеСредство.НомерКузова КАК НомерКузова,
	|	упРемонтИТОТС.ТранспортноеСредство.НомерДвигателя КАК НомерДвигателя,
	|	ВЫБОР
	|		КОГДА упРемонтИТОТС.НачалоРемонтаФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА упРемонтИТОТС.НачалоРемонтаПлан
	|		ИНАЧЕ упРемонтИТОТС.НачалоРемонтаФакт
	|	КОНЕЦ КАК НачалоРабот,
	|	ВЫБОР
	|		КОГДА упРемонтИТОТС.ОкончаниеРемонтаФакт = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА упРемонтИТОТС.ОкончаниеРемонтаПлан
	|		ИНАЧЕ упРемонтИТОТС.ОкончаниеРемонтаФакт
	|	КОНЕЦ КАК ОкончаниеРабот
	|ИЗ
	|	Документ.упРемонтИТОТС КАК упРемонтИТОТС
	|ГДЕ
	|	упРемонтИТОТС.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Ссылка КАК Документ,
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Агрегат КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(упРемонтИТОТСШиныУзлыАгрегатыСобственные.Агрегат) КАК ПредставлениеНоменклатуры,
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Агрегат.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	1 КАК Количество,
	|	ИСТИНА КАК Агрегат
	|ИЗ
	|	Документ.упРемонтИТОТС.ШиныУзлыАгрегатыСобственные КАК упРемонтИТОТСШиныУзлыАгрегатыСобственные
	|ГДЕ
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И упРемонтИТОТСШиныУзлыАгрегатыСобственные.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ШиныУзлыАгрегатыПриобретенные.Ссылка,
	|	ШиныУзлыАгрегатыПриобретенные.Агрегат,
	|	ПРЕДСТАВЛЕНИЕ(ШиныУзлыАгрегатыПриобретенные.Агрегат),
	|	ШиныУзлыАгрегатыПриобретенные.Агрегат.ЕдиницаИзмерения,
	|	1,
	|	ИСТИНА
	|ИЗ
	|	Документ.упРемонтИТОТС.ШиныУзлыАгрегаты КАК ШиныУзлыАгрегатыПриобретенные
	|ГДЕ
	|	ШиныУзлыАгрегатыПриобретенные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И ШиныУзлыАгрегатыПриобретенные.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапчастиПриобретенные.Ссылка,
	|	ЗапчастиПриобретенные.Запчасть,
	|	ПРЕДСТАВЛЕНИЕ(ЗапчастиПриобретенные.Запчасть),
	|	ЗапчастиПриобретенные.Запчасть.ЕдиницаИзмерения,
	|	ЗапчастиПриобретенные.Количество,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.упРемонтИТОТС.Запчасти КАК ЗапчастиПриобретенные
	|ГДЕ
	|	ЗапчастиПриобретенные.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапчастиСобственные.Ссылка,
	|	ЗапчастиСобственные.Запчасть,
	|	ПРЕДСТАВЛЕНИЕ(ЗапчастиСобственные.Запчасть),
	|	ЗапчастиСобственные.Запчасть.ЕдиницаИзмерения,
	|	ЗапчастиСобственные.Количество,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.упРемонтИТОТС.ЗапчастиСобственные КАК ЗапчастиСобственные
	|ГДЕ
	|	ЗапчастиСобственные.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Работы.Ссылка КАК Документ,
	|	Работы.РаботаПоРемонту,
	|	ПРЕДСТАВЛЕНИЕ(Работы.РаботаПоРемонту) КАК ПредставлениеРабота,
	|	Работы.Валюта,
	|	Работы.Цена,
	|	Работы.Количество,
	|	Работы.Сумма,
	|	Работы.СтавкаНДС,
	|	Работы.СуммаНДС
	|ИЗ
	|	Документ.упРемонтИТОТС.Работы КАК Работы
	|ГДЕ
	|	Работы.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШиныУзлыАгрегатыПриобретенные.Количество КАК Количество,
	|	ШиныУзлыАгрегатыПриобретенные.Агрегат КАК Материал,
	|	ШиныУзлыАгрегатыПриобретенные.Валюта,
	|	ШиныУзлыАгрегатыПриобретенные.Цена,
	|	ШиныУзлыАгрегатыПриобретенные.Агрегат.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ШиныУзлыАгрегатыПриобретенные.Сумма,
	|	ШиныУзлыАгрегатыПриобретенные.СтавкаНДС,
	|	ШиныУзлыАгрегатыПриобретенные.СуммаНДС,
	|	ШиныУзлыАгрегатыПриобретенные.Ссылка КАК Документ
	|ИЗ
	|	Документ.упРемонтИТОТС.ШиныУзлыАгрегаты КАК ШиныУзлыАгрегатыПриобретенные
	|ГДЕ
	|	ШиныУзлыАгрегатыПриобретенные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И ШиныУзлыАгрегатыПриобретенные.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапчастиПриобретенные.Количество,
	|	ЗапчастиПриобретенные.Запчасть,
	|	ЗапчастиПриобретенные.Валюта,
	|	ЗапчастиПриобретенные.Цена,
	|	ЗапчастиПриобретенные.Запчасть.ЕдиницаИзмерения,
	|	ЗапчастиПриобретенные.Сумма,
	|	ЗапчастиПриобретенные.СтавкаНДС,
	|	ЗапчастиПриобретенные.СуммаНДС,
	|	ЗапчастиПриобретенные.Ссылка
	|ИЗ
	|	Документ.упРемонтИТОТС.Запчасти КАК ЗапчастиПриобретенные
	|ГДЕ
	|	ЗапчастиПриобретенные.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Агрегат КАК Материал,
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Агрегат.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Количество КАК Количество,
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.Ссылка КАК Документ
	|ИЗ
	|	Документ.упРемонтИТОТС.ШиныУзлыАгрегатыСобственные КАК упРемонтИТОТСШиныУзлыАгрегатыСобственные
	|ГДЕ
	|	упРемонтИТОТСШиныУзлыАгрегатыСобственные.ТипОперацииМонтажа = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|	И упРемонтИТОТСШиныУзлыАгрегатыСобственные.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапчастиСобственные.Запчасть,
	|	ЗапчастиСобственные.Запчасть.ЕдиницаИзмерения,
	|	ЗапчастиСобственные.Количество,
	|	ЗапчастиСобственные.Ссылка
	|ИЗ
	|	Документ.упРемонтИТОТС.ЗапчастиСобственные КАК ЗапчастиСобственные
	|ГДЕ
	|	ЗапчастиСобственные.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();
	тбДанныеШапка  = ПакетРезультатовЗапроса[0].Выгрузить();
	тбДанныеСтроки = ПакетРезультатовЗапроса[1].Выгрузить();
	тбДанныеРабот = ПакетРезультатовЗапроса[2].Выгрузить();
	тбДанныеПриобретенные  = ПакетРезультатовЗапроса[3].Выгрузить();
	тбДанныеСобственные = ПакетРезультатовЗапроса[4].Выгрузить();
	
	// Подготовим данные таблицы шапки документа
	тбШапка = Новый ТаблицаЗначений;
	тбШапка.Колонки.Добавить("Документ");
	тбШапка.Колонки.Добавить("ОбъектРемонта");
	тбШапка.Колонки.Добавить("РемонтируемыйОбъект");	
	тбШапка.Колонки.Добавить("ПредставлениеОрганизации");
	тбШапка.Колонки.Добавить("ОрганизацияПолноеНаименование");
	тбШапка.Колонки.Добавить("РуководительПредприятия");	
	тбШапка.Колонки.Добавить("АдресОрганизация");
	тбШапка.Колонки.Добавить("Дата");
	тбШапка.Колонки.Добавить("Номер");
	тбШапка.Колонки.Добавить("ТС");
	тбШапка.Колонки.Добавить("ГосНомер");
	тбШапка.Колонки.Добавить("РегНомер");
	тбШапка.Колонки.Добавить("ПредставлениеТС");
	тбШапка.Колонки.Добавить("Модель");
	тбШапка.Колонки.Добавить("НомерКузова");
	тбШапка.Колонки.Добавить("НомерДвигателя");
	тбШапка.Колонки.Добавить("ВидРемонта");
	тбШапка.Колонки.Добавить("ГодВыпуска");
	тбШапка.Колонки.Добавить("Пробег");
	тбШапка.Колонки.Добавить("СервиснаяОрганизация");
	тбШапка.Колонки.Добавить("НачалоРабот");
	тбШапка.Колонки.Добавить("ОкончаниеРабот");

	// Заполним данные таблицы шапки документа
	Для Каждого текСтрока Из тбДанныеШапка Цикл
		НоваяСтрока = тбШапка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
		НоваяСтрока.РуководительПредприятия = "";
		// Организация
		Если ЗначениеЗаполнено(текСтрока.Организация) Тогда
			НоваяСтрока.ОрганизацияПолноеНаименование = текСтрока.ПредставлениеОрганизации; 
			НоваяСтрока.АдресОрганизация 			  = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрока.Организация, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
			
			текКонтактнаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.Организация);
			НоваяСтрока.ПредставлениеОрганизации = текСтрока.ПредставлениеОрганизации + ?(ПустаяСтрока(текКонтактнаяИнформация),"",", "+текКонтактнаяИнформация);
			НоваяСтрока.РуководительПредприятия = Строка(текСтрока.Организация.РуководительПредприятия);
		КонецЕсли;
		
		НоваяСтрока.ГодВыпуска = Формат(текСтрока.ДатаВыпуска,"ДФ=гггг");
		
		//Пробег
		сткПробег = упПутевойЛистУчетГСМ.ПробегМоточасыПоРегиструОстатков(текСтрока.ТС);
		НоваяСтрока.Пробег 	= сткПробег.Пробег;

	КонецЦикла;	
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Шапка",  тбШапка);
	СтруктураДанныхДляПечати.Вставить("Строки", тбДанныеСтроки);
	СтруктураДанныхДляПечати.Вставить("Работы", тбДанныеРабот);
	СтруктураДанныхДляПечати.Вставить("Приобретенные",тбДанныеПриобретенные);
	СтруктураДанныхДляПечати.Вставить("Собственные",тбДанныеСобственные);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции // ПолучитьДанныеДляПечатныхФорм()

Функция ПолучитьКонтактнуюИнформацию(Ссылка)
	
	текРезультат = "";
	ВидАдрес   = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
	ВидФакс    = Справочники.ВидыКонтактнойИнформации.ФаксОрганизации;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упПартнеры") Тогда
		ВидАдрес   = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера;	    
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упКонтрагенты") Тогда
		ВидАдрес   = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;	    
		ВидФакс    = Справочники.ВидыКонтактнойИнформации.ФаксКонтрагенты;
	КонецЕсли;
	
	текАдрес	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидАдрес);
	Если ЗначениеЗаполнено(текАдрес) Тогда
		текРезультат = текРезультат  + текАдрес;	
	КонецЕсли;
		
	текТелефон	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидТелефон);
	Если ЗначениеЗаполнено(текТелефон) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текТелефон);
	КонецЕсли;

	текФакс		= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидФакс);
	Если ЗначениеЗаполнено(текФакс) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текФакс);
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти

#КонецЕсли


