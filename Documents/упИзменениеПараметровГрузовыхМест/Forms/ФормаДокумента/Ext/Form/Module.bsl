#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
			
	ЭтаФорма.УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьКоличествоЗагрузочныхМетров = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	
	ПриСозданииПриЧтенииНаСервере();
	
	ДоступныеСтатусыЗаданийДляОснования = Документы.упИзменениеПараметровГрузовыхМест.ДоступныеСтатусыЗаданийДляОснования();
	
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	
	ЗаданиеНаПеревозкуГрузаДоИзменения = Объект.ЗаданиеНаПеревозкуГруза;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
		
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;	
	
	УстановитьЗаголовкиПоказателейЗагрузки();
	
	НастроитьФормуПоВидуПеревозки();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Истина
		И ГрузМодифицирован
		И ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится 
	Тогда
		Если ТекущийОбъект.ГрузовыеМеста.Количество() > 0 Тогда
			СтрокаГМ = ТекущийОбъект.ГрузовыеМеста[0];
		Иначе	
			СтрокаГМ = ТекущийОбъект.ГрузовыеМеста.Добавить();
			СтрокаГМ.ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
		КонецЕсли;
		СтрокаГМ.Высота = ВысотаГруза;
		СтрокаГМ.Ширина = ШиринаГруза;
		СтрокаГМ.Длина = ДлинаГруза;
		СтрокаГМ.Масса = Масса;
		СтрокаГМ.Объем = Объем;
		СтрокаГМ.КоличествоМест = КоличествоМест;
		СтрокаГМ.КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаданиеНаПеревозкуГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("Отбор", СтруктураОтбораЗадания());
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элемент);	
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеНаПеревозкуГрузаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ПараметрыПолученияДанных.Отбор = СтруктураОтбораЗадания();
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеНаПеревозкуГрузаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ПараметрыПолученияДанных.Отбор = СтруктураОтбораЗадания();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	УстановитьЗаголовкиПоказателейЗагрузки();
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаДлинаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, 1);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаШиринаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, 1);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаВысотаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, 1);
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	ПриИзмененииРазмеров();	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	ПриИзмененииРазмеров();	
КонецПроцедуры

&НаКлиенте
Процедура МассаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КоличествоМестПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЗагрузочныхМетровПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаОснованииНаКлиенте();
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
    УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьНаОснованииНаСервере(ТекстОшибки)
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(Объект.ЗаданиеНаПеревозкуГруза);
	ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущийОбъект.ДополнительныеСвойства, "ТекстОшибки");
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	НастроитьФормуПоВидуПеревозки();
	ЗаполнитьПараметрыГрузовыхМест();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаОснованииНаКлиенте()
	Если Ложь
		Или ГрузМодифицирован
		Или Объект.ГрузовыеМеста.Количество() > 0
	Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПрезаполнениеПараметровГрузовыхМест", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Параметры грузовых мест будут перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе	
		ЗаполнитьНаОснованииЗаявкиНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПрезаполнениеПараметровГрузовыхМест(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаОснованииЗаявкиНаКлиенте();
		ЗаданиеНаПеревозкуГрузаДоИзменения = Объект.ЗаданиеНаПеревозкуГруза;
	Иначе
		Объект.ЗаданиеНаПеревозкуГруза = ЗаданиеНаПеревозкуГрузаДоИзменения;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаОснованииЗаявкиНаКлиенте()
	ТекстОшибки = "";
	ЗаполнитьНаОснованииНаСервере(ТекстОшибки);	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиПоказателейЗагрузки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
		МассаПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.ГрузовыеМеста.Итог("Масса"), ЕИ_Масса));
		Элементы.ГрузовыеМестаМасса.Заголовок = НСтр(СтрШаблон("ru = 'Масса (%1)'", ЕИ_Масса));
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
		ОбъемПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.ГрузовыеМеста.Итог("Объем"), ЕИ_Объем));
		Элементы.ГрузовыеМестаОбъем.Заголовок = НСтр(СтрШаблон("ru = 'Объем (%1)'", ЕИ_Объем));
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда
		ЕИ_Грузов = сткПредставления.КоличествоМест;
		КоличествоМестПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.ГрузовыеМеста.Итог("КоличествоМест"), ЕИ_Грузов));
		Элементы.ГрузовыеМестаКоличествоМест.Заголовок = НСтр(СтрШаблон("ru = 'Кол. мест (%1)'", ЕИ_Грузов));
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
	Элементы.Декорация1.Видимость = УчитыватьЛинейныеРазмеры;
	Элементы.Декорация2.Видимость = УчитыватьЛинейныеРазмеры;
	
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетровПредставление = НСтр(СтрШаблон("ru = '%1'", Объект.ГрузовыеМеста.Итог("КоличествоЗагрузочныхМетров")));
	КонецЕсли;
	
КонецПроцедуры // УстановитьЗаголовкиПоказателейЗагрузки()

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ЗаданиеНаПеревозкуГрузаПриИзменении(Элемент)
	ЗаполнитьНаОснованииНаКлиенте();
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВидаПеревозки()
	
	Если ЗначениеЗаполнено(Объект.ЗаданиеНаПеревозкуГруза) Тогда
		ПараметрыВводаИнформацииОГрузовыхМестах = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ЗаданиеНаПеревозкуГруза, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах");
	Иначе
		ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ПустаяСсылка();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоВидуПеревозки() Экспорт
	
	УстановитьПараметрыВидаПеревозки();
	ИнформацияПоГрузовымМестамВводится = Ложь;
	
	ИнформацияПоГрузовымМестамВводится = Истина
									И ЗначениеЗаполнено(ПараметрыВводаИнформацииОГрузовыхМестах)
									И ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится; 

	Элементы.ГрузовыеМеста.Видимость = ИнформацияПоГрузовымМестамВводится;
	Элементы.ГруппаПараметрыГрузовогоМеста.Видимость = НЕ ИнформацияПоГрузовымМестамВводится;	
	
	Если НЕ ИнформацияПоГрузовымМестамВводится Тогда
		ЗаполнитьПараметрыГрузовыхМест();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыГрузовыхМест()
	Если Объект.ГрузовыеМеста.Количество() > 0 Тогда
		ПерваяСтрока = Объект.ГрузовыеМеста[0];
		Масса = ПерваяСтрока.Масса;
		Объем = ПерваяСтрока.Объем;
		КоличествоМест = ПерваяСтрока.КоличествоМест;
		КоличествоЗагрузочныхМетров = ПерваяСтрока.КоличествоЗагрузочныхМетров;
		ШиринаГруза = ПерваяСтрока.Ширина;
		ДлинаГруза = ПерваяСтрока.Длина;
		ВысотаГруза = ПерваяСтрока.Высота;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объем = ШиринаГруза * ВысотаГруза * ДлинаГруза * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураОтбораЗадания()
	
	Результат = Новый Структура();
	Результат.Вставить("Статус", ДоступныеСтатусыЗаданийДляОснования);
	Результат.Вставить("РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава", Ложь);

	Возврат Результат;
	
КонецФункции


#КонецОбласти