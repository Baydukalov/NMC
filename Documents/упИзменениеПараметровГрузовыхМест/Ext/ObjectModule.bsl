#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
				
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПараметрыЗаявкиНаПеревозку");
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозку", ЗаявкаНаПеревозкуГруза);
		ЭлементБлокировки.ИсточникДанных = ГрузовыеМеста;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПараметрыЗаданияНаПеревозку");
	ЭлементБлокировки.УстановитьЗначение("ЗаданиеНаПеревозку", ЗаданиеНаПеревозкуГруза);
	ЭлементБлокировки.ИсточникДанных = ГрузовыеМеста;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата", Дата);
	РеквизитыОбъекта.Вставить("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	РеквизитыОбъекта.Вставить("ЗаданиеНаПеревозкуГруза", ЗаданиеНаПеревозкуГруза);
	РеквизитыОбъекта.Вставить("ГрузовыеМеста", ГрузовыеМеста);
	
	упРасчетПоказателейЗагрузки.СформироватьДвиженияПараметрыЗаявкиНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упРасчетПоказателейЗагрузки.СформироватьДвиженияПараметрыЗаданияНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		ЗаполнитьПоЗаданиюНаПеревозкуГруза(ДанныеЗаполнения);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьПоЗаданиюНаПеревозкуГруза(ЗаданиеНаПеревозкуГрузаТекущее)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГрузаТ.Ссылка КАК ЗаданиеНаПеревозкуГруза,
	|	упЗаданиеНаПеревозкуГрузаТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упЗаданиеНаПеревозкуГрузаТ.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМеста,
	|	упЗаданиеНаПеревозкуГрузаТ.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	упЗаданиеНаПеревозкуГрузаТ.КоличествоМест КАК КоличествоМест,
	|	упЗаданиеНаПеревозкуГрузаТ.Масса КАК Масса,
	|	упЗаданиеНаПеревозкуГрузаТ.Объем КАК Объем
	|ПОМЕСТИТЬ втЗаданиеНаПеревозкуГруза
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
	|ГДЕ упЗаданиеНаПеревозкуГрузаТ.Ссылка = &ЗаданиеНаПеревозкуГруза
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ЗаданиеГрузовыеМеста.Ссылка КАК ЗаданиеНаПеревозкуГруза
	|ПОМЕСТИТЬ втГрузовыеМеста10
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданиеНаПеревозкуГруза КАК втЗаданиеНаПеревозкуГруза
	|	ПО ИСТИНА
	|		И втЗаданиеНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза = ЗаданиеГрузовыеМеста.Ссылка
	|		И втЗаданиеНаПеревозкуГруза.ПараметрыВводаИнформацииОГрузовыхМеста <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазделениеГрузовыхМест.ГрузовоеМесто КАК ГрузовоеМесто,
	|	РазделениеГрузовыхМест.Родитель КАК Родитель,
	|	РазделениеГрузовыхМест.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест05
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста05Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРазделениеГрузовыхМест.СрезПоследних(
	|		,
	|		) КАК РазделениеГрузовыхМест
	|	ПО РазделениеГрузовыхМест.РодительИсходный = втГрузовыеМеста05Т.ГрузовоеМесто
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРазделениеГрузовыхМестГрузовыеМеста.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест10
	|ИЗ
	|	втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестРодители
	|	ПО втРазделениеГрузовыхМестРодители.Родитель = втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто
	|ГДЕ втРазделениеГрузовыхМестРодители.ГрузовоеМесто ЕСТЬ NULL
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втРазделениеГрузовыхМест10Т.ГрузовоеМесто, втГрузовыеМестаТ.ГрузовоеМесто) КАК ГрузовоеМесто,
	|	втГрузовыеМестаТ.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМестаТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест10 КАК втРазделениеГрузовыхМест10Т
	|	ПО втРазделениеГрузовыхМест10Т.РодительИсходный = втГрузовыеМестаТ.ГрузовоеМесто
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданиеНаПеревозкуГрузаТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ИЗ
	|	втЗаданиеНаПеревозкуГруза КАК втЗаданиеНаПеревозкуГрузаТ
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузы.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ЕСТЬNULL(Параметры.Высота, ГрузовыеМеста.Высота) КАК Высота,
	|	ЕСТЬNULL(Параметры.Длина, ГрузовыеМеста.Длина) КАК Длина,
	|	ЕСТЬNULL(Параметры.Ширина, ГрузовыеМеста.Ширина) КАК Ширина,
	|	ЕСТЬNULL(Параметры.Масса, ГрузовыеМеста.Масса) КАК Масса,
	|	ЕСТЬNULL(Параметры.КоличествоМест, ГрузовыеМеста.КоличествоМест) КАК КоличествоМест,
	|	ЕСТЬNULL(Параметры.Объем, ГрузовыеМеста.Объем) КАК Объем,
	|	ЕСТЬNULL(Параметры.КоличествоЗагрузочныхМетров, ГрузовыеМеста.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	втГрузовыеМеста КАК втГрузы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		) КАК Параметры
	|	ПО ИСТИНА
	|		И втГрузы.ГрузовоеМесто = Параметры.ГрузовоеМесто
	|		И втГрузы.ЗаданиеНаПеревозкуГруза = Параметры.ЗаданиеНаПеревозку
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК ГрузовыеМеста
	|	ПО втГрузы.ГрузовоеМесто = ГрузовыеМеста.Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто,
	|	ЕСТЬNULL(Параметры.Высота, 0) КАК Высота,
	|	ЕСТЬNULL(Параметры.Длина, 0) КАК Длина,
	|	ЕСТЬNULL(Параметры.Ширина, 0) КАК Ширина,
	|	ЕСТЬNULL(Параметры.Масса, втЗаданиеНаПеревозкуГруза.Масса) КАК Масса,
	|	ЕСТЬNULL(Параметры.КоличествоМест, втЗаданиеНаПеревозкуГруза.КоличествоМест) КАК КоличествоМест,
	|	ЕСТЬNULL(Параметры.Объем, втЗаданиеНаПеревозкуГруза.Объем) КАК Объем,
	|	ЕСТЬNULL(Параметры.КоличествоЗагрузочныхМетров, втЗаданиеНаПеревозкуГруза.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров
	|ИЗ
	|	втЗаданиеНаПеревозкуГруза КАК втЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		) КАК Параметры
	|	ПО ИСТИНА
	|		И ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) = Параметры.ГрузовоеМесто
	|		И втЗаданиеНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза = Параметры.ЗаданиеНаПеревозку
	|ГДЕ втЗаданиеНаПеревозкуГруза.ПараметрыВводаИнформацииОГрузовыхМеста = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|";
	
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозкуГруза", ЗаданиеНаПеревозкуГрузаТекущее);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[5].Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаявкаНаПеревозкуГруза = Выборка.ЗаявкаНаПеревозкуГруза;	
	КонецЕсли;
	ЗаданиеНаПеревозкуГруза = ЗаданиеНаПеревозкуГрузаТекущее;
	
	ГрузовыеМеста.Очистить();
	
	Выборка = РезультатЗапроса[6].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ГрузовыеМеста.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;

КонецПроцедуры

#КонецЕсли 