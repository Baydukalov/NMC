#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверятьВремяВТочке = Истина;
	
	Если Истина
		И Работы.Количество() 
		И Работы.Найти(Истина, "Выполнена") = Неопределено
		И Не ДосрочноеУбытие
		И Не ДополнительныеСвойства.Свойство("ПроверятьДатыВыполненияРабот") // Если отменили все работы на МК, должна быть возможность проставить дату окончания выполнения. 
	Тогда // все работы в точке отменены.
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "НачалоВыполнения");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "НачалоРабот");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ОкончаниеВыполнения");
		ПроверятьВремяВТочке = Ложь;
	Иначе
		ТекстСообщения = "";
		Если Не ДосрочноеУбытие Тогда
			упПрохождениеТочки.ПроверитьДатуНачалаРабот(НачалоВыполнения, НачалоРабот, ОкончаниеВыполнения, ТекстСообщения, Отказ);
		Иначе
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "НачалоРабот");
		КонецЕсли;
			
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,"НачалоРабот", , Отказ);	
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ И ПроверятьВремяВТочке Тогда
		
		ТекстСообщения = "";
		упПрохождениеТочки.ПроверитьДатуПрибытияФакт(НачалоВыполнения, Рейс, СтрокаНомер, ТекстСообщения, Отказ);
		
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,"НачалоВыполнения", , Отказ);	
		КонецЕсли;
		
	КонецЕсли;
			
	Если НЕ Отказ Тогда
		ПроверитьЗаполнениеРабот(Отказ);	
	КонецЕсли;
			
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);
	
	ДополнительныеСвойства.Вставить("ВремяНачала", ОценкаПроизводительности.НачатьЗамерВремени());
	ДополнительныеСвойства.Вставить("КонтролироватьОстаткиГрузовыхМест", Ложь);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если Не ОбменДанными.Загрузка Тогда
		Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
			Если НЕ ДосрочноеУбытие Тогда
				мсвСтрокиСПроблемойДосрочноеУбытие = Работы.НайтиСтроки(Новый Структура("Проблема", Справочники.упПроблемы.ДосрочноеПрохождение));
				Если мсвСтрокиСПроблемойДосрочноеУбытие.Количество() > 0 Тогда
					ДосрочноеУбытие = Истина;
				КонецЕсли;
			КонецЕсли;
		    
		    Если ДосрочноеУбытие Тогда
				Для каждого Строка Из Работы Цикл
					Если Истина
						И НЕ Строка.Выполнена
						И НЕ Строка.Отменена
						Тогда
						Строка.Отменена = Истина;
						Строка.Проблема = Справочники.упПроблемы.ДосрочноеПрохождение;
						Строка.АдресРазгрузки = АдресТочки;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ВидТранспортнойУслуги) Тогда
				ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидТранспортнойУслуги");
			КонецЕсли;
			
			Если Истина
				И ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами
				И Не ЗначениеЗаполнено(ВидКонтейнернойОперации)
			Тогда
				ЕстьПогрузка = Ложь;
				Для каждого Строка Из Работы Цикл
					Если Строка.Операция = Перечисления.упТипыОпераций.Погрузка Тогда
						ЕстьПогрузка = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				ВидКонтейнернойОперации = ?(ЕстьПогрузка, Перечисления.упВидыКонтейнерныхОпераций.Формирование, Перечисления.упВидыКонтейнерныхОпераций.Расформирование);
			КонецЕсли;
			
		КонецЕсли;
		Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
			
			Отказ = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ЭтотОбъект);
			
			Если НЕ Отказ Тогда
				сткСтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(Рейс);
				СтатусРейса = сткСтатусРейса.Статус;
				СостояниеРейса = сткСтатусРейса.Состояние;
				ТекстОшибки = "";
				Если Ложь
					Или СтатусРейса <> Перечисления.упСтатусыРейса.ВыполненЧастично
					Или Не ОтложенноеПрохождение 
				Тогда
					Если НЕ упРаботаСоСтатусамиКлиентСервер.РазрешеноФормироватьКорректировкуРейса(СтатусРейса, РежимЗаписи)  Тогда
						Если ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
							ВидДокументаРейс = Нстр("ru = 'документ ""Операции с контейнером""'");
							СтатусРейсаТекущий = СостояниеРейса;
						Иначе					 
							ВидДокументаРейс = Нстр("ru = 'рейс'");
							СтатусРейсаТекущий = СтатусРейса;
						КонецЕсли;
						ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено проведение и отмена проведения документа, так как %1 находится в статусе ""%2""'", ВидДокументаРейс, СтатусРейсаТекущий));
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ Отказ 
				И ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки") Тогда
				ТекстОшибки = "";
				Если НЕ упКорректировкаМаршрута.ПрерываниеГрузовогоПотокаДокументомОбработано(Ссылка)  Тогда
					ТекстОшибки = НСтр("ru = 'Запрещено изменять проведение документа, по которому обработано прерывание грузового потока'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ Отказ Тогда
				сткДанныеВидаПеревозки	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах, 
																							|ВидПеревозки.ВидЗоныДляФормированияПредставленияРейса, 
																							|ВидПеревозки.СпособУчетаВозвращенныхДокументов,
																							|ВидПеревозки.ВариантФормированияМаршрута");
				
				ПараметрВводаИнформации = сткДанныеВидаПеревозки.ВидПеревозкиПараметрыВводаИнформацииОГрузовыхМестах;
				ВидЗоныДляФормированияПредставленияРейса = сткДанныеВидаПеревозки.ВидПеревозкиВидЗоныДляФормированияПредставленияРейса;
				ВариантФормированияМаршрута = сткДанныеВидаПеревозки.ВидПеревозкиВариантФормированияМаршрута;
				
				ФормироватьПоГрузовымМестам = Ложь
										Или ПараметрВводаИнформации = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку 
										Или ПараметрВводаИнформации = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса; 	
				
				упКорректировкаРейсаОбъект = Неопределено;
				Если НЕ ДополнительныеСвойства.Свойство("КорректировкаРейса", упКорректировкаРейсаОбъект) Тогда
					упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();	
					ИнициализацияКорректировкиМаршрута(упКорректировкаРейсаОбъект);
				КонецЕсли;	
				
				Если Не Отказ 
					И ПолучитьФункциональнуюОпцию("упИспользуютсяКонтейнерныеПеревозки")
					И ВидТранспортнойУслуги <> Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами
				Тогда
					ТекстОшибки = "";
					Если упКорректировкаРейсаОбъект.КонтейнерРасформирован(ТекстОшибки)  Тогда
						ТекстОшибки = СтрШаблон(НСтр("ru = 'Запрещено изменять проведение документа с грузами-контейнерами, которые были расформированы
						|(Задания: %1.)'"), ТекстОшибки);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
					КонецЕсли;
				КонецЕсли;
				
				Если Истина
					И Не Отказ
					И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения 
					И упКорректировкаРейсаОбъект.ИспользуетсяСкладскойУчетНаХабах
				Тогда
					Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АдресТочки, "Склад");
					Если ЗначениеЗаполнено(Склад) Тогда
						Для каждого текСтрока Из Работы Цикл
							ТекстСообщения = "";
							упПрохождениеТочки.ПроверитьЗаполнениеСкладскойЯчейкиРаботы(текСтрока, упКорректировкаРейсаОбъект.Задания, текСтрока.Задание, текСтрока.Операция, текСтрока.ГрузовоеМесто, текСтрока.СкладскаяЯчейка, ТекстСообщения, Отказ);
							Если Отказ Тогда
								ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);	
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ Отказ Тогда
					
					ДополнительныеСвойства.Вставить("СтатусРейсаДоКорректировки", упКорректировкаРейсаОбъект.СтатусРейсаДоКорректировки);
					ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет"));
					ДополнительныеСвойства.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах", ПараметрВводаИнформации);
					ДополнительныеСвойства.Вставить("СпособУчетаВозвращенныхДокументов", сткДанныеВидаПеревозки.ВидПеревозкиСпособУчетаВозвращенныхДокументов);
					ДополнительныеСвойства.Вставить("Статус", упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(Рейс).Статус); 
					
					Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
						ДополнительныеСвойства.Вставить("ТЧЗадания", упКорректировкаРейсаОбъект.Задания.Выгрузить(,"Задание, ГрузовоеМесто"));
					КонецЕсли;
					
					РеквизитыОбъекта = Новый Структура();
					РеквизитыОбъекта.Вставить("Дата", ОпределитьПериодФормированияДвижений());
					РеквизитыОбъекта.Вставить("Рейс", Рейс);
					РеквизитыОбъекта.Вставить("ФормироватьПоГрузовымМестам", ФормироватьПоГрузовымМестам);
					РеквизитыОбъекта.Вставить("ВидЗоныДляФормированияПредставленияРейса", ВидЗоныДляФормированияПредставленияРейса);
					РеквизитыОбъекта.Вставить("ВариантФормированияМаршрута", ВариантФормированияМаршрута);
					РеквизитыОбъекта.Вставить("ПлановыеПараметры", Новый Структура());	
					РеквизитыОбъекта.Вставить("ВидТранспортнойУслуги", ВидТранспортнойУслуги);
					
					ДополнительныеСвойства.Вставить("РеквизитыОбъекта", РеквизитыОбъекта);
					
					Если Истина 
						И РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения 
						И упКорректировкаРейсаОбъект.СтатусРейсаДоКорректировки = Перечисления.упСтатусыРейса.КВыполнению 
					Тогда	
						ДополнительныеСвойства.Вставить("МаршрутПоРейсу", упКорректировкаРейсаОбъект.МаршрутДоКорректировки.Выгрузить());	
						ДополнительныеСвойства.Вставить("РаботыПоРейсу", упКорректировкаРейсаОбъект.РаботыДоКорректировки.Выгрузить());
						ДополнительныеСвойства.Вставить("Маршрут", ДополнительныеСвойства.МаршрутПоРейсу);	
						ДополнительныеСвойства.Вставить("Работы", ДополнительныеСвойства.РаботыПоРейсу);
					КонецЕсли;	
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
				
		//Если НЕ Отказ И НЕ РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если НЕ Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			
			Если Не ЗначениеЗаполнено(РасстояниеОтПредыдущейТочки) Тогда
				РасстояниеОтПредыдущейТочки = ВычислитьПройденноеРасстояние(НачалоВыполнения,Ссылка,Рейс);
			КонецЕсли;
			
			сткРеквизиты = Новый Структура();
			сткРеквизиты.Вставить("Работы", Работы);
			сткРеквизиты.Вставить("Идентификатор", ИдентификаторТочки);
			сткРеквизиты.Вставить("АдресТочки", АдресТочки);
			сткРеквизиты.Вставить("НачалоВыполнения", НачалоВыполнения);
			сткРеквизиты.Вставить("ОкончаниеВыполнения", ОкончаниеВыполнения);
			сткРеквизиты.Вставить("НачалоРабот", НачалоРабот);
			сткРеквизиты.Вставить("РасстояниеОтПредыдущейТочки", РасстояниеОтПредыдущейТочки);
			сткРеквизиты.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", ВремяПростояПоВинеЗаказчикаЧасы);
			сткРеквизиты.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", ВремяПростояПоВинеПеревозчикаЧасы);
			сткРеквизиты.Вставить("ПараметрыНаПеревозку", Неопределено);
			сткРеквизиты.Вставить("ДосрочноеУбытие", ДосрочноеУбытие);
			сткРеквизиты.Вставить("ВидТранспортнойУслуги", ВидТранспортнойУслуги);
			сткРеквизиты.Вставить("ВидКонтейнернойОперации", ВидКонтейнернойОперации);
						
			ДополнительныеСвойства.Вставить("ТоварныйСоставГруза", ТоварныйСоставГруза.Выгрузить());
			ДополнительныеСвойства.Вставить("РазделениеГрузовыхМест",  Неопределено);
			упКорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляКорректировки(ДополнительныеСвойства, Ссылка, сткРеквизиты, Отказ);
			упКорректировкаРейсаОбъект.ДополнитьТаблицыДвижениямиПоОперациямСКонтейнерами(ДополнительныеСвойства, Ссылка, сткРеквизиты, Отказ);
			
			Если НЕ Отказ Тогда
				ТоварныйСоставГруза.Загрузить(ДополнительныеСвойства.ТоварныйСоставГруза);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		упКорректировкаМаршрута.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Неопределено, ДополнительныеСвойства.РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	РеквизитыОбъекта = ДополнительныеСвойства.РеквизитыОбъекта;
	ФормироватьПоГрузовымМестам = РеквизитыОбъекта.ФормироватьПоГрузовымМестам; 	
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упИзмененияРейсов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	КонецЕсли; 
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеПараметрыРейса");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНепереданныеДокументы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	КонецЕсли; 
			
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Работы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	
	Если ФормироватьПоГрузовымМестам Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упСостояниеПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Работы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПланПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Работы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");

	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = упЗаданиеНаПеревозку.ПолучитьЗаявкиПоЗаданиям(Работы.ВыгрузитьКолонку("Задание"));
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Заявка");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Работы;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Задание");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	ЭлементБлокировки.УстановитьЗначение("Идентификатор", ИдентификаторТочки);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	тбзПрерыванияГрузовыхПотоков = Неопределено;
	Если ДополнительныеСвойства.Свойство("ПрерыванияГрузовыхПотоков", тбзПрерыванияГрузовыхПотоков) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрерыванияГрузовыхПотоков");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = тбзПрерыванияГрузовыхПотоков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаявкаНаПеревозку", "ЗаявкаНаПеревозку");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	сткЭкипаж = упРейс.ПолучитьЭкипажРейса(Рейс);
	ТранспортноеСредство = сткЭкипаж.ТранспортноеСредство;
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеМестоположенияТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Транспорт", ТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	КонецЕсли; 
	
	ТекстОшибки = "";
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	ДатаВыполняется = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Рейс, Перечисления.упСтатусыРейса.Выполняется);
	
	Если Истина
		И ЗначениеЗаполнено(ДатаВыполняется) 
		И РеквизитыОбъекта.Дата < ДатаВыполняется Тогда
		
		Если ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
			ТекстОшибки = НСтр("ru = 'Дата убытия из точки должна быть больше даты перевода рейса в статус ""Выполняется"": " + ДатаВыполняется + "!'");
		Иначе	
			ТекстОшибки = НСтр("ru = 'Дата документа должна быть больше даты перевода рейса в статус ""Выполняется"": " + ДатаВыполняется + "!'");
		КонецЕсли;
		
	Иначе
		
		ДатаКВыполнению = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Рейс, Перечисления.упСтатусыРейса.КВыполнению);
		Если Не ЗначениеЗаполнено(ДатаКВыполнению) Или РеквизитыОбъекта.Дата <= ДатаКВыполнению Тогда
			Если ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
				ТекстОшибки = НСтр("ru = 'Дата убытия из точки должна быть больше даты перевода рейса к выполнению: " + ДатаКВыполнению + "!'");
			Иначе	
				ТекстОшибки = НСтр("ru = 'Дата документа должна быть больше даты перевода рейса к выполнению: " + ДатаКВыполнению + "!'");
			КонецЕсли; 
		ИначеЕсли Не ДополнительныеСвойства.Свойство("МобильныйКлиент") И РеквизитыОбъекта.Дата > ТекущаяДатаСеанса() Тогда // при получении прохождений с мобильного клиента возможна рассинхронизация дат на android-устройстве и в информационной базе
			Если ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
				ТекстОшибки = НСтр("ru = 'Дата убытия из точки не может быть больше текущей даты!'");
			Иначе	
				ТекстОшибки = НСтр("ru = 'Дата документа не может быть больше текущей даты!'");
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
	Если ТекстОшибки <> "" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецЕсли; 
	
	РеквизитыОбъекта.Вставить("Работы", Работы);
	РеквизитыОбъекта.Вставить("Задания", Работы);
	РеквизитыОбъекта.Вставить("Рейс", Рейс);
	РеквизитыОбъекта.Вставить("ИдентификаторТочки", ИдентификаторТочки);
	РеквизитыОбъекта.Вставить("СтрокаНомер", СтрокаНомер);
	РеквизитыОбъекта.Вставить("АдресТочки", АдресТочки );
	РеквизитыОбъекта.Вставить("ТипТочки", ТипТочки);
	РеквизитыОбъекта.Вставить("НачалоВыполнения", НачалоВыполнения);
	РеквизитыОбъекта.Вставить("ОкончаниеВыполнения", ОкончаниеВыполнения);
	РеквизитыОбъекта.Вставить("НачалоРабот", НачалоРабот);
	РеквизитыОбъекта.Вставить("РасстояниеОтПредыдущейТочки", РасстояниеОтПредыдущейТочки);
	РеквизитыОбъекта.Вставить("ВремяПростояПоВинеЗаказчикаЧасы", ВремяПростояПоВинеЗаказчикаЧасы);
	РеквизитыОбъекта.Вставить("ВремяПростояПоВинеПеревозчикаЧасы", ВремяПростояПоВинеПеревозчикаЧасы);
	РеквизитыОбъекта.Вставить("ФактическиеПараметры", Новый Структура);    
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", Неопределено);
	РеквизитыОбъекта.Вставить("ОтложенноеПрохождение", ОтложенноеПрохождение);

	ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Ложь); 
	ДополнительныеСвойства.Вставить("РассчитыватьСтатус", 0); 
	ДополнительныеСвойства.Вставить("Рейс", Рейс); 
	ДополнительныеСвойства.Вставить("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	ДополнительныеСвойства.Вставить("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус" , Новый Массив); 
	
	упПрохождениеТочки.ПроверитьСуществованиеДокументаПоТочке(Рейс, ИдентификаторТочки, Ссылка, Отказ);
		
	// Формируются движения по заданиям на перевозку.
	// При формировании движений идет обращение к регистру остатков, поэтому очистим движения.
	ДвиженияЗаданияНаПеревозку = Движения.упЗаданияКПланированию;
	ДвиженияЗаданияНаПеревозку.Записывать = Истина;
	
	ДвиженияСтатусыЗаданий = Движения.упСтатусыЗаданийНаПеревозкуГруза;
	ДвиженияСтатусыЗаданий.Очистить();	
	ДвиженияСтатусыЗаданий.Записывать = Истина;
	
	ДвиженияСтатусыЗаявок = Движения.упСтатусыЗаявокНаПеревозкуГруза;
	ДвиженияСтатусыЗаявок.Очистить();	
	ДвиженияСтатусыЗаявок.Записывать = Истина;
	
	Движения.Записать();

	//упКорректировкаМаршрута.СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	упПрохождениеТочки.СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются движения по заявкам в случае отмены заданий.
	упКорректировкаМаршрута.СформироватьДвиженияЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется маршрут.
	упКорректировкаМаршрута.СформироватьДвижениеМаршрутПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются работы.
	упКорректировкаМаршрута.СформироватьДвижениеРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется пакет документов, о которых следует отчитаться после выполнения рейса.
	упПрохождениеТочки.СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются движения по состояниям задания.
	упПрохождениеТочки.СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется план по заданиям.
	упКорректировкаМаршрута.СформироватьДвижениеПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется факт инкассации денежных средств.
	упПрохождениеТочки.СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется разделение грузов.
	упКорректировкаМаршрута.СформироватьДвижениеРазделениеГрузовыхМест(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются остатки грузовых мест на хабах, в случае мультимодальной перевозки.
	упКорректировкаМаршрута.СформироватьДвижениеОстаткиГрузовыхМест(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются параметры ВГХ грузовых мест в разрезе заявок.
	упПрохождениеТочки.СформироватьДвиженияПараметрыЗаявкиНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются параметры ВГХ грузовых мест в разрезе заданий.
	упПрохождениеТочки.СформироватьДвиженияПараметрыЗаданияНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются данные о случаях прерывания грузовых потоков (отмена/разделение).
	упКорректировкаМаршрута.СформироватьДвижениеПрерыванияГрузовыхПотоков(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	// Устанавливаются статусы рейса.
	ДополнительныеСвойства.Вставить("ДобавитьСтатус", Ложь);
	ДополнительныеСвойства.Вставить("РассчитыватьСтатус", Истина);
	ДополнительныеСвойства.Вставить("ПровестиПроверкуНаВозможныеСтатусы", Истина);
	упРейс.СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		Движения.Записать();
	КонецЕсли;
	
	// При отклонении времени прохождения точки от плана заполняется плановое окончание рейса
	упКорректировкаМаршрута.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При прохождении последней точки формируются фактические параметры рейса.
	упРейс.СформироватьФактическиеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При прохождении первой точки ТС переводится в состояние "В рейсе", последней - свободно.
	упРейс.СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При прохождении последней точки фиксируется фактическое время нахождения ТС в рейсе.
	упРейс.СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// При прохождении последней точки фиксируется фактическое время нахождения ТС в рейсе.
	Если Не ОтложенноеПрохождение Тогда
		упПрохождениеТочки.СформироватьДвиженияФактическиеМестоположенияТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
		
	// Устанавливаются статусы заданий.
	ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗадания", Истина);
	упЗаданиеНаПеревозку.СформироватьДвиженияСтатусыЗаданий(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// Устанавливаются статусы заявок.
	ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗаявки", Истина);
	упУправленияЗаявками.СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.ПроконтролироватьЗаданияКПланированию(Ссылка, ФормироватьПоГрузовымМестам, Отказ, ДополнительныеСвойства);
	
	упПрохождениеТочки.ПроконтролироватьПогрузкуРазгрузку(Рейс, МоментВремени(), Отказ);
	
	Если ДополнительныеСвойства.КонтролироватьОстаткиГрузовыхМест Тогда
		упКорректировкаМаршрута.ПроконтролироватьОтрицательныеОстаткиГрузовыхМестПоСкладу(РеквизитыОбъекта, Ссылка, Отказ);
	КонецЕсли;
	
	// В случае если это последняя точка маршрута, то устанавливается дата окончания рейса в путевом листе.
	упПрохождениеТочки.ЗаполнитьДатуОкончанияПутевогоЛиста(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При выполнении рейса на мобильном клиенте фиксируем необоходимость обновить данные по рейсу
	упКорректировкаМаршрута.СформироватьДвиженияИзмененияРейсов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени(Справочники.КлючевыеОперации.ПроведениеПрохожденияТочкиНаСервере, ДополнительныеСвойства.ВремяНачала);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	упРейс.СброситьФактическиеПараметрыРейса(Рейс);	
			
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Выполним вычисление пройденного расстояния.
//
// Параметры:
//  НачалоВыполнения  - Дата - Начало выполнения вычисления
//  ДокументСсылка  - ДокументСсылка.упПрохождениеТочки - Ссылка на документ который проводит вычисление.
//  ДокументРейс  - ДокументСсылка.упРейс - Ссылка на документ Рейс по которому проводим вычисление.
//
// Возвращаемое значение:
//   Число - Пройденное расстояние в км.
//
Функция ВычислитьПройденноеРасстояние(НачалоВыполнения,ДокументСсылка,ДокументРейс)
	
	Результат = 0;
	
	ИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг");
	Если ИспользуетсяГлобальнаяПодсистемаСпутниковыйМониторинг Тогда
		
		НачалоПериода = Дата("00010101000000");
		ОкончаниеПериода = НачалоВыполнения;
		ТранспортноеСредство = Справочники.упТранспортныеСредства.ПустаяСсылка();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упРейс.Ссылка КАК Рейс,
		|	ЕСТЬNULL(МаршрутПоРейсуСП.УбытиеФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК Убытие,
		|	ЕСТЬNULL(ПеревозчикПоРейсуСП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС
		|ИЗ
		|	Документ.упРейс КАК упРейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(&Период, Рейс = &Рейс) КАК МаршрутПоРейсуСП
		|		ПО упРейс.Ссылка = МаршрутПоРейсуСП.Рейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(&Период, Рейс = &Рейс) КАК ПеревозчикПоРейсуСП
		|		ПО упРейс.Ссылка = ПеревозчикПоРейсуСП.Рейс
		|ГДЕ
		|	упРейс.Ссылка = &Рейс
		|	И МаршрутПоРейсуСП.Пройдена
		|	И НЕ МаршрутПоРейсуСП.Регистратор = &Прохождение
		|
		|УПОРЯДОЧИТЬ ПО
		|	Убытие УБЫВ";
		
		Граница = Новый Граница(НачалоВыполнения,ВидГраницы.Исключая);
		Запрос.УстановитьПараметр("Период", Граница);
		Запрос.УстановитьПараметр("Рейс", ДокументРейс);
		Запрос.УстановитьПараметр("Прохождение", ДокументСсылка);
		
		Выборка = Запрос.Выполнить().Выбрать();					
		Если Выборка.Следующий() Тогда
			НачалоПериода = Выборка.Убытие;
			ТранспортноеСредство = Выборка.ТС;				
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			ТекущееРасстояние = упМониторинг.упВычислитьРасстояниеПоДаннымДатчиков(НачалоПериода,ОкончаниеПериода,ТранспортноеСредство);
			Если НЕ ЗначениеЗаполнено(ТекущееРасстояние) Тогда
				ТекущееРасстояние = упМониторинг.упВычислитьРасстояниеПоДаннымТрекеров(НачалоПериода,ОкончаниеПериода,ТранспортноеСредство);
			КонецЕсли;
			// Значение расстояния получаем в метрах делим на 1000 для получения км.
			Результат = ?(ТекущееРасстояние=0,0,ТекущееРасстояние/1000);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВычислитьПройденноеРасстояние()

// Процедура проверки заполнения строк работ.
//
Процедура ПроверитьЗаполнениеРабот(Отказ) Экспорт
	
	Для каждого текСтрока Из Работы Цикл
		ТекстСообщения = "";
		упПрохождениеТочки.ПроверитьЗаполнениеСтрокиРаботы(текСтрока, текСтрока.Операция, текСтрока.Задание, текСтрока.ГрузовоеМесто, ТекстСообщения, Отказ);
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);	
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция определяет дату формирования движений
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений()
	
	Если ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
		Период = ОкончаниеВыполнения;
	Иначе
		Период = Дата;
	КонецЕсли; 

	Возврат Период;
	
КонецФункции

// Процедура - Инициализация
//
// Параметры:
//  КорректировкаМаршрутаОбъект	 
//
Процедура ИнициализацияКорректировкиМаршрута(КорректировкаМаршрутаОбъект) Экспорт
	
	ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	
	КорректировкаМаршрутаОбъект.Рейс = Рейс;
		
	Если ЗначениеЗаполнено(Ссылка) Тогда
		КорректировкаМаршрутаОбъект.Ссылка = Ссылка;
		КорректировкаМаршрутаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
	Иначе
		КорректировкаМаршрутаОбъект.Ссылка = Документы.упПрохождениеТочки.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КорректировкаМаршрутаОбъект.Период)  Тогда
		КорректировкаМаршрутаОбъект.Период = ОпределитьПериодФормированияДвижений();
	КонецЕсли;
	
	сткПараметрыЗаполнения = Новый Структура();
	сткПараметрыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками);
	КорректировкаМаршрутаОбъект.Инициализация(сткПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

