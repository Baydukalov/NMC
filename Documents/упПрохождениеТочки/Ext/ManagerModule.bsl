#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Если Данные.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
		Если Данные.ВидКонтейнернойОперации = Перечисления.упВидыКонтейнерныхОпераций.Формирование Тогда
			Представление = СтрШаблон(Нстр("ru = 'Формирование контейнера %1 от %2'"), Данные.Номер, Данные.Дата);
		Иначе	
			Представление = СтрШаблон(Нстр("ru = 'Расформирование контейнера %1 от %2'"), Данные.Номер, Данные.Дата);
		КонецЕсли;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	Поля.Добавить("ВидТранспортнойУслуги");
	Поля.Добавить("ВидКонтейнернойОперации");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");            
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Рейс");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#Область КомандыВводаНаОсновании

Процедура УстановитьВидимостьКомандВводаНаОсновании(Форма, ДополнительныеПараметры) Экспорт
	
	ЭтоОперацииСКонтейнерами = Ложь;
	
	Если ДополнительныеПараметры.ЭтоФормаОбъекта Тогда
		ЭтоОперацииСКонтейнерами = Форма.Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
		
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Список") Тогда
		Отбор = Форма.Список.Отбор;
		мсвЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Форма.Список.Отбор, "ВидТранспортнойУслуги");
		Если мсвЭлементыОтбора.Количество() > 0 Тогда
			Если Истина
				И мсвЭлементыОтбора[0].ПравоеЗначение = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами
				И мсвЭлементыОтбора[0].ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			Тогда
				ЭтоОперацииСКонтейнерами = Истина;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
	Если ЭтоОперацииСКонтейнерами Тогда
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СоздатьНаОсновании", "БизнесПроцесс", "Задание", ДополнительныеПараметры);
		упВводНаОсновании.УстановитьПризнакДоступностиСпискаКоманд(Истина, ДополнительныеПараметры);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" Тогда	
		
		ВидТранспортнойУслуги = Неопределено;

		Если Параметры.Свойство("ВидТранспортнойУслуги", ВидТранспортнойУслуги) Тогда
			
			Если  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаСпискаОперацииСКонтейнерами";
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда	
		
		ВидТранспортнойУслуги = Неопределено;

		Если Параметры.Свойство("ВидТранспортнойУслуги", ВидТранспортнойУслуги) Тогда
			
			Если ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораОперацииСКонтейнерами";
			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
