#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ТекущийОбъект.Проведен Тогда
		ТолькоПросмотр = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ТекущийОбъект);
		Элементы.дрвРаботыПодобратьПроблему.Доступность = Не ТолькоПросмотр;
	КонецЕсли;
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере(Отказ = Ложь)
	ИдентификаторТочки 	= Неопределено;
	Рейс = Неопределено;
	ВидПеревозки = Неопределено;
		
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЗначениеЗаполнено(Объект.ИдентификаторТочки)
			И  ЗначениеЗаполнено(Объект.Рейс) Тогда
			ИдентификаторТочки = Объект.ИдентификаторТочки;
			Рейс = Объект.Рейс;
		КонецЕсли;
	ИначеЕсли Истина
			И Параметры.ЗначенияЗаполнения.Количество() > 0
			И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура")
			И Параметры.ЗначенияЗаполнения.Свойство("ИдентификаторТочки")
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ИдентификаторТочки)
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Рейс) Тогда
		ИдентификаторТочки = Параметры.ЗначенияЗаполнения.ИдентификаторТочки;
		Объект.ИдентификаторТочки = ИдентификаторТочки;
		Рейс = Параметры.ЗначенияЗаполнения.Рейс;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ЭтоСеансВнешнегоПользователя Тогда
			Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
	КонецЕсли;	
	
	Если Рейс = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Непосредственное создание прохождений точек запрещено!'"),,,, Отказ);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРаботы(ИдентификаторТочки, Рейс);
	
	Элементы.ГруппаГараж.Видимость = ЗначениеЗаполнено(Объект.Гараж);
	Элементы.ГруппаЧекиККМ.Видимость = Истина
	                                   И Не ЭтоСеансВнешнегоПользователя
								И ВедетсяРозничнаяПродажаВРейсе
								И (Объект.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию);
		
	упПрохождениеТочки.УстановитьЗаголовокФормыПрохожденияТочки(ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Элементы.СозданоПоСобытию.Доступность = Ложь;
		
	// Элементы связанные с вводом сумм инкассации.
	Если ЗначениеЗаполнено(Рейс) Тогда
		ВидПеревозки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Рейс, "ВидПеревозки");	
		сткПараметрыВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПеревозки, "ВариантФормированияМаршрута, 
																			|РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава, 
																			|ИспользуетсяСкладскойУчетНаХабах, 
																			|ПараметрыВводаИнформацииОГрузовыхМестах,
																			|РазрешеноДосрочноеУбытиеИзТочкиМаршрута");
		ВариантФормированияМаршрута = сткПараметрыВидаПеревозки.ВариантФормированияМаршрута;
		РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава	= сткПараметрыВидаПеревозки.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава;
		РазрешеноДосрочноеУбытиеИзТочкиМаршрута = сткПараметрыВидаПеревозки.РазрешеноДосрочноеУбытиеИзТочкиМаршрута;
		
		Элементы.дрвРаботыСкладскаяЯчейка.Видимость = Истина
											 И ЭтаФорма.ИспользуетсяСкладскойУчетНаХабах 
											 И сткПараметрыВидаПеревозки.ИспользуетсяСкладскойУчетНаХабах
											 И сткПараметрыВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится;
											 
		Элементы.ДосрочноеУбытие.Видимость = РазрешеноДосрочноеУбытиеИзТочкиМаршрута;									 
	КонецЕсли;
		
	ИспользуетсяИнкассация = Ложь;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассация = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	Элементы.дрвРаботыСумма.Видимость = ИспользуетсяИнкассация;
	Элементы.дрвРаботыВалюта.Видимость	= ИспользуетсяИнкассация;
	
	Если Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
		Элементы.дрвРаботыАдресРазгрузки.Видимость = Ложь;
	Иначе	
		Элементы.дрвРаботыАдресРазгрузки.Видимость = Истина;
	КонецЕсли;
		
	УстановитьВидимостьКнопкиИзменитьТоварныйСостав();	
	УстановитьВидимостьКнопкиИзменитьВГХ();	
	УстановитьВидимостьКнопкиДосрочноеПрохождениеТочки();
	
	упВводНаОсновании.УстановитьВидимостьКоманд(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ИспользуетсяСкладскойУчетНаХабах = ПолучитьФункциональнуюОпцию("упИспользуетсяСкладскойУчетНаХабах");
	ЭтаФорма.РазрешитьИзменениеПараметровГрузовПриИсполненииРейса = ПолучитьФункциональнуюОпцию("упРазрешитьИзменениеПараметровГрузовПриИсполненииРейса");
	ЭтаФорма.ЭтоСеансВнешнегоПользователя = ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя();
	ЭтаФорма.ВедетсяРозничнаяПродажаВРейсе = ПолучитьФункциональнуюОпцию("упВедетсяРозничнаяПродажаВРейсе");
	ПриСозданииПриЧтенииНаСервере(Отказ);
КонецПроцедуры	

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Если Ложь
			Или РаботыМодифицированы 
			Или Объект.ДосрочноеУбытие
		Тогда
			
			ПередачаДокументовТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов");
			ПолучениеДокументовТипОперации = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПолучениеДокументов");
			
			Объект.Работы.Очистить();
			
			Для каждого Задание Из Работы.ПолучитьЭлементы() Цикл
				Для каждого Операция Из Задание.ПолучитьЭлементы() Цикл
					Для каждого ГрузовоеМесто Из Операция.ПолучитьЭлементы() Цикл
						
						СтрокаРабота = Объект.Работы.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаРабота, ГрузовоеМесто);
						
						Если Ложь
							Или ГрузовоеМесто.Операция = ПередачаДокументовТипОперации 
							Или ГрузовоеМесто.Операция = ПолучениеДокументовТипОперации
						Тогда
							СтрокаРабота.КоличествоЭкземпляровДокумента = ГрузовоеМесто.Количество;
						КонецЕсли;  
						
						СтрокаРабота.Выполнена = ГрузовоеМесто.Выполнена = 1;
						
						Если Истина
							И Объект.ДосрочноеУбытие
							И НЕ СтрокаРабота.Выполнена
							И НЕ СтрокаРабота.Отменена 
						Тогда
							СтрокаРабота.Отменена = Истина;
							СтрокаРабота.Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение");
							СтрокаРабота.АдресРазгрузки = Объект.АдресТочки;
						КонецЕсли; 
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	мсвСобытийРейса = Новый Массив;
	мсвСобытийРейса.Добавить("ОбновитьРаботы");
	мсвСобытийРейса.Добавить("ИзменилсяМаршрут");
	
	Если ОповеститьОбИзмененииСтатусаРейса Тогда
		мсвСобытийРейса.Добавить("СменаСтатуса");
		ОповеститьОбИзмененииСтатусаРейса = Ложь;
	КонецЕсли;
	
	Если мсвСобытийРейса.Количество() > 0 Тогда
		События = СтрСоединить(мсвСобытийРейса, ";");
		Оповестить(События, ,Объект.Рейс);
	КонецЕсли;
	
	Для каждого текЗаявка Из ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус Цикл
		Оповестить("СменаСтатуса",,текЗаявка.Значение);	
	КонецЦикла;
	ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус.Очистить();
	
	Для каждого текЗаданием Из ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус Цикл
		Оповестить("СменаСтатуса",,текЗаданием.Значение);	
	КонецЦикла;
	ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		ОповеститьОбИзмененииСтатусаРейса = Истина;
	КонецЕсли;
	мсвЗаявкиНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаявкиНаПеревозкуГруза) Тогда
		ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус.ЗагрузитьЗначения(мсвЗаявкиНаПеревозкуГруза);
	КонецЕсли;
	мсвЗаданияНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаданияНаПеревозкуГруза) Тогда
		ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус.ЗагрузитьЗначения(мсвЗаданияНаПеревозкуГруза);
	КонецЕсли;
	
	упПрохождениеТочки.УстановитьЗаголовокФормыПрохожденияТочки(ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АдресТочкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Рейс, ТекущаяТочка", Объект.Рейс, Объект.ИдентификаторТочки);
	ОткрытьФорму("Документ.упПрохождениеТочки.Форма.ФормаВыбораТочки", ПараметрыФормы, Элементы.АдресТочки, , ,, Новый ОписаниеОповещения("ВыборТочки",ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	РейсПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора точки.
//
Процедура ВыборТочки(Результат, ДополнительныеПараметры) Экспорт
	
	Если типЗнч(Результат) = Тип("Структура") Тогда
		Объект.АдресТочки 			= Результат.АдресТочки;
		Объект.ИдентификаторТочки 	= Результат.ИдентификаторТочки;
		Объект.СтрокаНомер 			= Результат.НомерСтроки;
		ЗаполнитьРаботы(Объект.ИдентификаторТочки, Объект.Рейс);
	КонецЕсли;

КонецПроцедуры // ВыборТочки()

&НаКлиенте
Процедура дрвРаботыПриАктивизацииСтроки(Элемент)
	
	текДанные = Элементы.дрвРаботы.ТекущиеДанные;
	Если Не текДанные = Неопределено Тогда 
		Элементы.дрвРаботыИзменитьТоварныйСостав.Доступность = Элементы.дрвРаботы.ТекущиеДанные.ЭтоРабота;
		Если Элементы.дрвРаботыИзменитьВГХ.Видимость Тогда
			Элементы.дрвРаботыИзменитьВГХ.Доступность = Элементы.дрвРаботы.ТекущиеДанные.ЭтоРабота;	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыВыполненаПриИзменении(Элемент)
	
	РаботыМодифицированы = Истина;
	
	ТекущиеДанные = Элементы.дрвРаботы.ТекущиеДанные;
	
	Если ТекущиеДанные.Выполнена = 2 Тогда
		ТекущиеДанные.Выполнена = 0;
	КонецЕсли;
		
	упПрохождениеТочкиКлиентСервер.УстановитьФлаги(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПроблемаПриИзменении(Элемент)
	РаботыМодифицированы = Истина;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПроблемаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Истина
		И Объект.ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами")
		И Не упПрохождениеТочкиКлиентСервер.ОперацияПогрузки(Элементы.дрвРаботы.ТекущиеДанные.Операция)
	Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ЗапрещеноСозданиеНовойТочкиМаршрута", Истина);
		Элементы.дрвРаботыПроблема.ПараметрыВыбора = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НовыйПараметр));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыАдресРазгрузкиПриИзменении(Элемент)
	РаботыМодифицированы = Истина;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	сткДополнительныеСвойства = Новый Структура("ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре, ЕстьНеОтмененнаяРаботаТара", Ложь, Ложь);
	ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута =  Ложь;
	Если Истина
		И ЗначениеЗаполнено(ТекущиеДанные.Проблема)
		И упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(ТекущиеДанные.Операция)
	Тогда
		ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Проблема, "ЗапрещеноСозданиеНовойТочкиМаршрута");
		Если ТекущиеДанные.ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
			ТекущиеДанные.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	упПрохождениеТочкиКлиентСервер.ОбработатьРаботуПоТочке(ТекущиеДанные,,,сткДополнительныеСвойства);
	ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре = сткДополнительныеСвойства.ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре;
	ЕстьНеОтмененнаяРаботаТара = сткДополнительныеСвойства.ЕстьНеОтмененнаяРаботаТара;
	Если Истина
		И ТекущиеДанные.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара")
		И ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре 
	Тогда
		ДополнительныеПараметры  = ТекущиеДанные;
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОтменеГрузовВТаре", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияВопрос, Нстр("ru = 'Отменяется тара. 
													|Выполнить отмену грузов в таре?'"), РежимДиалогаВопрос.ДаНет);
	ИначеЕсли Истина 
			И ЗначениеЗаполнено(ТекущиеДанные.Тара)
			И НЕ ЕстьНеОтмененныеРаботыГрузовоеМестоВТаре 
			И ЕстьНеОтмененнаяРаботаТара Тогда	
		ДополнительныеПараметры  = ТекущиеДанные;
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОтменеТары", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияВопрос, Нстр("ru = 'Все грузы в таре отменены. 
													|Отменить тару?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыСуммаПриИзменении(Элемент)
	РаботыМодифицированы = Истина;
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыСкладскаяЯчейкаПриИзменении(Элемент)
	РаботыМодифицированы = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СвернутьДерево(Команда)
	
	ЭлементыДерева = Работы.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыДерева Цикл
		ЭлементыОперации = текЗадание.ПолучитьЭлементы();
		Для каждого текОперация Из ЭлементыОперации Цикл
			Элементы.дрвРаботы.Свернуть(текОперация.ПолучитьИдентификатор());
		КонецЦикла;
		Элементы.дрвРаботы.Свернуть(текЗадание.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево(Команда)
	
	ЭлементыДерева = Работы.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыДерева Цикл
		Элементы.дрвРаботы.Развернуть(текЗадание.ПолучитьИдентификатор());
		ЭлементыОперации = текЗадание.ПолучитьЭлементы();
		Для каждого текОперация Из ЭлементыОперации Цикл
			Элементы.дрвРаботы.Развернуть(текОперация.ПолучитьИдентификатор());
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	ЭлементыДерева = Работы.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыДерева Цикл
		ЭлементыОперации = текЗадание.ПолучитьЭлементы();
		Для каждого текОперация Из ЭлементыОперации Цикл
			ЭлементыМеста = текОперация.ПолучитьЭлементы();
			Для каждого текГрузовоеМесто Из ЭлементыМеста Цикл
				Если Не (текГрузовоеМесто.Отменена Или ЗначениеЗаполнено(текГрузовоеМесто.Проблема) Или ЗначениеЗаполнено(текГрузовоеМесто.АдресРазгрузки)) Тогда
					текГрузовоеМесто.Выполнена = 1;	
				КонецЕсли;
			КонецЦикла; 
			упПрохождениеТочкиКлиентСервер.УстановитьФлагРодителя(текОперация);
		КонецЦикла;
		упПрохождениеТочкиКлиентСервер.УстановитьФлагРодителя(текЗадание);
	КонецЦикла;
	РаботыМодифицированы = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	ЭлементыДерева = Работы.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыДерева Цикл
		текЗадание.Выполнена = 0;
		ЭлементыОперации = текЗадание.ПолучитьЭлементы();
		Для каждого текОперация Из ЭлементыОперации Цикл
			текОперация.Выполнена = 0;	
			ЭлементыМеста = текОперация.ПолучитьЭлементы();
			Для каждого текГрузовоеМесто Из ЭлементыМеста Цикл
				текГрузовоеМесто.Выполнена = 0;
			КонецЦикла; 
		КонецЦикла;
	КонецЦикла;
	РаботыМодифицированы = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПроблему(Команда)
	
	текТипОперации = ПолучитьТипСамойПриоритетнойОперации(Объект.Рейс, Объект.ИдентификаторТочки);
	спкТипов = Новый СписокЗначений;
	спкТипов.Добавить(текТипОперации);
	спкТипов.Добавить(ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПустаяСсылка"));
	
	сткПараметры = Новый Структура("Отбор",Новый Структура("ТипОперации",спкТипов));
	Если Объект.ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда
		сткПараметры.Отбор.Вставить("ЗапрещеноСозданиеНовойТочкиМаршрута", Истина);
	КонецЕсли;
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыВыбораПроблемы",ЭтаФорма);
	ОткрытьФорму("Справочник.упПроблемы.ФормаВыбора",сткПараметры,,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТоварныйСостав(Команда)
	
	ТекущиеДанныеРаботы = Элементы.дрвРаботы.ТекущиеДанные;
	
	сткПараметрыФормы = Новый Структура();
	сткПараметрыФормы.Вставить("ГрузовоеМесто", ТекущиеДанныеРаботы.ГрузовоеМесто);
	сткПараметрыФормы.Вставить("КоличествоГрузов", ТекущиеДанныеРаботы.КоличествоГрузов);
	сткПараметрыФормы.Вставить("ЗаданиеНаПеревозкуГруза", ТекущиеДанныеРаботы.Задание);
	сткПараметрыФормы.Вставить("Рейс", Объект.Рейс);
	сткПараметрыФормы.Вставить("Регистратор", Объект.Ссылка);
	сткПараметрыФормы.Вставить("ТоварныйСоставГрузовогоМеста", ПолучитьТоварныйСостав(ТекущиеДанныеРаботы.ГрузовоеМесто));
	сткПараметрыФормы.Вставить("ТипОперации", ТекущиеДанныеРаботы.Операция);
	сткПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОповещениеОЗакрытииФормыИзмененияТоварногоСостава = Новый ОписаниеОповещения("ОповещениеОЗакрытииФормыИзмененияТоварногоСостава", ЭтаФорма);
	
	ОткрытьФорму("ОбщаяФорма.упИзменениеТоварногоСостава", сткПараметрыФормы, ЭтаФорма,,,, ОповещениеОЗакрытииФормыИзмененияТоварногоСостава,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДосрочноеПрохождениеТочки(Команда)
	ЗаполнитьДляДосрочногоПрохожденияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОтменеТары(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		сткДополнительныеСвойства = Новый Структура("ОтменитьТару", Истина);
		упПрохождениеТочкиКлиентСервер.ОтменитьРаботуПоТаре(ДополнительныеПараметры,сткДополнительныеСвойства,Истина,,,,Объект.Рейс);
		РаботыМодифицированы = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОтменеГрузовВТаре(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		сткДополнительныеСвойства = Новый Структура("ОтменитьГрузовыеМестаВТаре", Истина);
		упПрохождениеТочкиКлиентСервер.ОтменитьРаботуПоТаре(ДополнительныеПараметры,сткДополнительныеСвойства,Истина,,,,Объект.Рейс);
		РаботыМодифицированы = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора проблемы.
//
Процедура ОбработатьЗакрытиеФормыВыбораПроблемы(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		УстановитьПроблемуНаВсеНеЗаполненныеЗадачиВТочкеНаСервере(РезультатЗакрытия);
		РаботыМодифицированы = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРаботы(ИдентификаторТочки, Рейс)
	
	Если Истина
		И ЗначениеЗаполнено(ИдентификаторТочки)
		И ЗначениеЗаполнено(Рейс) 
	Тогда
		дрвРаботы	= ПолучитьРаботыПоТочке(ИдентификаторТочки, Рейс, Объект.Ссылка);
		Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресТочки, "Склад");
		
		// Дерево работ заполняется данными
		Работы.ПолучитьЭлементы().Очистить();
		сткДанныеЗаполнения = Новый Структура("ЭтоРабота", Ложь);
		упПрохождениеТочки.ЗаполнитьДеревоРаботВТочке(дрвРаботы, Работы, сткДанныеЗаполнения);
		Элементы.дрвРаботы.Видимость = Работы.ПолучитьЭлементы().Количество();
		
		Если Истина
			И Не ЭтоСеансВнешнегоПользователя
			И ВедетсяРозничнаяПродажаВРейсе 
			И Объект.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию Тогда
			ЗаполнитьЧекиККМ();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЧекиККМ()
	
	ЧекиККМ.Очистить();
	
	мсвИдентификаторыРабот = Объект.Работы.Выгрузить(,"ИдентификаторРаботы").ВыгрузитьКолонку("ИдентификаторРаботы");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЧекККМ.Ссылка КАК ЧекККМ
	|ИЗ
	|	Документ.упЧекККМ КАК упЧекККМ
	|ГДЕ
	|	упЧекККМ.ИдентификаторРаботы В (&ИдентификаторыРабот)";
	Запрос.УстановитьПараметр("ИдентификаторыРабот", мсвИдентификаторыРабот);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ЧекиККМ.Добавить(), Выборка);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьРаботыПоТочке(Идентификатор, Рейс, ПрохождениеТочки = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Если НЕ ЗначениеЗаполнено(ПрохождениеТочки) Тогда
		Запрос.Текст = 

		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРаботыПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
		|	упРаботыПоРейсуСрезПоследних.Отменена КАК Отменена,
		|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
		|	упРаботыПоРейсуСрезПоследних.Операция КАК Операция,
		|	упРаботыПоРейсуСрезПоследних.ДополнительнаяОперация КАК ДополнительнаяОперация,
		|	упРаботыПоРейсуСрезПоследних.ВремяРабот КАК ВремяРабот,
		|	упРаботыПоРейсуСрезПоследних.ВременноеОкноНачало КАК ВременноеОкноНачало,
		|	упРаботыПоРейсуСрезПоследних.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
		|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упРаботыПоРейсуСрезПоследних.Тара КАК Тара,
		|	упРаботыПоРейсуСрезПоследних.ВремяОжидания КАК ВремяОжидания,
		|	упРаботыПоРейсуСрезПоследних.ПлановоеВремяНачалаРабот КАК ПлановоеВремяНачалаРабот,
		|	упРаботыПоРейсуСрезПоследних.КоличествоГрузов КАК КоличествоГрузов,
		|	упРаботыПоРейсуСрезПоследних.КоличествоДопОпераций КАК КоличествоДопОпераций,
		|	упРаботыПоРейсуСрезПоследних.Проблема КАК Проблема,
		|	упРаботыПоРейсуСрезПоследних.АдресРазгрузки КАК АдресРазгрузки,
		|	упРаботыПоРейсуСрезПоследних.ИдентификаторРаботы КАК ИдентификаторРаботы,
		|	упРаботыПоРейсуСрезПоследних.Выполнена КАК Выполнена,
		|	упРаботыПоРейсуСрезПоследних.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
		|	упРаботыПоРейсуСрезПоследних.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	упРаботыПоРейсуСрезПоследних.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
		|	упРаботыПоРейсуСрезПоследних.Операция.Порядок + 5 КАК ИндексКартинки,
		|	упРаботыПоРейсуСрезПоследних.Сумма КАК Сумма,
		|	упРаботыПоРейсуСрезПоследних.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута,
		|	упГрузовыеМеста.Валюта КАК Валюта,
		|	упРаботыПоРейсуСрезПоследних.ГрузРазделен КАК ГрузРазделен,
		|	упРаботыПоРейсуСрезПоследних.СкладскаяЯчейка КАК СкладскаяЯчейка,
		|	упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	ПараметрыЗаданияНаПеревозку.Высота КАК Высота,
		|	ПараметрыЗаданияНаПеревозку.Длина КАК Длина,
		|	ПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	ПараметрыЗаданияНаПеревозку.КоличествоМест КАК КоличествоМест,
		|	ПараметрыЗаданияНаПеревозку.Масса КАК Масса,
		|	ПараметрыЗаданияНаПеревозку.Объем КАК Объем,
		|	ПараметрыЗаданияНаПеревозку.Ширина КАК Ширина
		|ПОМЕСТИТЬ втРаботы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс
		|			И Идентификатор = &Идентификатор) КАК упРаботыПоРейсуСрезПоследних
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		|	ПО упРаботыПоРейсуСрезПоследних.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|	ПО упРаботыПоРейсуСрезПоследних.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
		|		,
		|		) КАК ПараметрыЗаданияНаПеревозку
		|	ПО ПараметрыЗаданияНаПеревозку.ГрузовоеМесто = упРаботыПоРейсуСрезПоследних.ГрузовоеМесто
		|		И ПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку = упРаботыПоРейсуСрезПоследних.Задание
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	МАКСИМУМ(ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок) КАК НомерВЦепиПоставокМаксимальный
		|ПОМЕСТИТЬ втМаксимальныеНомераПоставок
		|ИЗ
		|	втРаботы КАК втРаботы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
		|	ПО ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втРаботы.ЗаявкаНаПеревозкуГруза
		|СГРУППИРОВАТЬ ПО
		|	втРаботы.ЗаявкаНаПеревозкуГруза
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.АдресРазгрузки КАК АдресРазгрузки,
		|	втРаботы.Валюта КАК Валюта,
		|	втРаботы.ВременноеОкноНачало КАК ВременноеОкноНачало,
		|	втРаботы.ВременноеОкноОкончание КАК ВременноеОкноОкончание,
		|	втРаботы.ВремяОжидания КАК ВремяОжидания,
		|	втРаботы.ВремяРабот КАК ВремяРабот,
		|	втРаботы.Выполнена КАК Выполнена,
		|	втРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втРаботы.ГрузРазделен КАК ГрузРазделен,
		|	втРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
		|	втРаботы.Задание КАК Задание,
		|	втРаботы.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза1,
		|	втРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
		|	втРаботы.ИндексКартинки КАК ИндексКартинки,
		|	втРаботы.КоличествоГрузов КАК КоличествоГрузов,
		|	втРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
		|	втРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
		|	втРаботы.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	втРаботы.Операция КАК Операция,
		|	втРаботы.Отменена КАК Отменена,
		|	втРаботы.ПлановоеВремяНачалаРабот КАК ПлановоеВремяНачалаРабот,
		|	втРаботы.Проблема КАК Проблема,
		|	втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
		|	втРаботы.СкладскаяЯчейка КАК СкладскаяЯчейка,
		|	втРаботы.СтрокаНомер КАК СтрокаНомер,
		|	втРаботы.Сумма КАК Сумма,
		|	втРаботы.Тара КАК Тара,
		|	втРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	ЕСТЬNULL(втПоследниеНомераПоставок.НомерВЦепиПоставокМаксимальный, 0) КАК НомерВЦепиПоставокМаксимальный,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|			ИЛИ НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ИЛИ ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЕстьТараВСтроке,
		|	ВЫБОР
		|		КОГДА НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА втРаботы.Тара
		|		КОГДА ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
		|			ТОГДА втРаботы.ГрузовоеМесто
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|	КОНЕЦ КАК ТараГрузовогоМеста,
		|	ВЫБОР
		|		КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЭтоТара,
		|	втТипыГрузов.ВидГруза,
		|	втРаботы.Высота КАК Высота,
		|	втРаботы.Длина КАК Длина,
		|	втРаботы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	втРаботы.КоличествоМест КАК КоличествоМест,
		|	втРаботы.Масса КАК Масса,
		|	втРаботы.Объем КАК Объем,
		|	втРаботы.Ширина КАК Ширина,
		|	втРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута,
		|	Ложь КАК ИзмененыПараметры
		|ИЗ
		|	втРаботы КАК втРаботы
		|	ЛЕВОЕ СОЕДИНЕНИЕ втМаксимальныеНомераПоставок КАК втПоследниеНомераПоставок
		|	ПО втРаботы.ЗаявкаНаПеревозкуГруза = втПоследниеНомераПоставок.ЗаявкаНаПеревозкуГруза
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК втГрузовыеМеста
		|	ПО втРаботы.ГрузовоеМесто = втГрузовыеМеста.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК втТипыГрузов
		|	ПО втГрузовыеМеста.ТипГруза = втТипыГрузов.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕстьТараВСтроке УБЫВ,
		|	ТараГрузовогоМеста,
		|	ЭтоТара УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(ИзмененыПараметры)
		|ПО
		|	Задание,
		|	Операция
		|";

		Запрос.УстановитьПараметр("Рейс", Рейс);
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Иначе	
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПрохождениеТочкиРаботы.Ссылка.СтрокаНомер КАК СтрокаНомер,
		|	упПрохождениеТочкиРаботы.Отменена КАК Отменена,
		|	упПрохождениеТочкиРаботы.Задание КАК Задание,
		|	упПрохождениеТочкиРаботы.Операция КАК Операция,
		|	упПрохождениеТочкиРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
		|	упПрохождениеТочкиРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	упПрохождениеТочкиРаботы.Тара КАК Тара,
		|	упПрохождениеТочкиРаботы.КоличествоГрузов КАК КоличествоГрузов,
		|	упПрохождениеТочкиРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
		|	упПрохождениеТочкиРаботы.Проблема КАК Проблема,
		|	упПрохождениеТочкиРаботы.АдресРазгрузки КАК АдресРазгрузки,
		|	упПрохождениеТочкиРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
		|	упПрохождениеТочкиРаботы.Выполнена КАК Выполнена,
		|	упПрохождениеТочкиРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
		|	упПрохождениеТочкиРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	упПрохождениеТочкиРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
		|	упПрохождениеТочкиРаботы.Операция.Порядок + 5 КАК ИндексКартинки,
		|	упПрохождениеТочкиРаботы.Сумма КАК Сумма,
		|	упГрузовыеМеста.Валюта КАК Валюта,
		|	упПрохождениеТочкиРаботы.ГрузРазделен КАК ГрузРазделен,
		|	упПрохождениеТочкиРаботы.СкладскаяЯчейка КАК СкладскаяЯчейка,
		|	упЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	упПрохождениеТочкиРаботы.Высота КАК Высота,
		|	упПрохождениеТочкиРаботы.Длина КАК Длина,
		|	упПрохождениеТочкиРаботы.Ширина КАК Ширина,
		|	упПрохождениеТочкиРаботы.Масса КАК Масса,
		|	упПрохождениеТочкиРаботы.Объем КАК Объем,
		|	упПрохождениеТочкиРаботы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	упПрохождениеТочкиРаботы.КоличествоМест КАК КоличествоМест,
		|	упПрохождениеТочкиРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
		|	упПрохождениеТочкиРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
		|ПОМЕСТИТЬ втРаботы
		|ИЗ
		|	Документ.упПрохождениеТочки.Работы КАК упПрохождениеТочкиРаботы
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		|	ПО упПрохождениеТочкиРаботы.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
		|	ПО упПрохождениеТочкиРаботы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
		|ГДЕ упПрохождениеТочкиРаботы.Ссылка = &Ссылка
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	МАКСИМУМ(ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок) КАК НомерВЦепиПоставокМаксимальный
		|ПОМЕСТИТЬ втМаксимальныеНомераПоставок
		|ИЗ
		|	втРаботы КАК втРаботы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
		|	ПО ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втРаботы.ЗаявкаНаПеревозкуГруза
		|СГРУППИРОВАТЬ ПО
		|	втРаботы.ЗаявкаНаПеревозкуГруза
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.СтрокаНомер КАК СтрокаНомер,
		|	втРаботы.Отменена КАК Отменена,
		|	втРаботы.Задание КАК Задание,
		|	втРаботы.Операция КАК Операция,
		|	втРаботы.ДополнительнаяОперация КАК ДополнительнаяОперация,
		|	втРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втРаботы.Тара КАК Тара,
		|	втРаботы.КоличествоГрузов КАК КоличествоГрузов,
		|	втРаботы.КоличествоДопОпераций КАК КоличествоДопОпераций,
		|	втРаботы.Проблема КАК Проблема,
		|	втРаботы.АдресРазгрузки КАК АдресРазгрузки,
		|	втРаботы.ИдентификаторРаботы КАК ИдентификаторРаботы,
		|	втРаботы.Выполнена КАК Выполнена,
		|	втРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы,
		|	втРаботы.ТипРегламентногоДокумента КАК ТипРегламентногоДокумента,
		|	втРаботы.КоличествоЭкземпляровДокумента КАК КоличествоЭкземпляровДокумента,
		|	втРаботы.ИндексКартинки КАК ИндексКартинки,
		|	втРаботы.Сумма КАК Сумма,
		|	втРаботы.Валюта КАК Валюта,
		|	втРаботы.ГрузРазделен КАК ГрузРазделен,
		|	втРаботы.СкладскаяЯчейка КАК СкладскаяЯчейка,
		|	втРаботы.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	втРаботы.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
		|	ЕСТЬNULL(втМаксимальныеНомераПоставок.НомерВЦепиПоставокМаксимальный, 0) КАК НомерВЦепиПоставокМаксимальный,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|				ИЛИ НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|				ИЛИ ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЕстьТараВСтроке,
		|	ВЫБОР
		|		КОГДА НЕ втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА втРаботы.Тара
		|		КОГДА ЕСТЬNULL(втТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
		|			ТОГДА втРаботы.ГрузовоеМесто
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|	КОНЕЦ КАК ТараГрузовогоМеста,
		|	ВЫБОР
		|		КОГДА втРаботы.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЭтоТара,
		|	втТипыГрузов.ВидГруза КАК ВидГруза,
		|	втРаботы.Высота КАК Высота,
		|	втРаботы.Длина КАК Длина,
		|	втРаботы.Ширина КАК Ширина,
		|	втРаботы.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
		|	втРаботы.КоличествоМест КАК КоличествоМест,
		|	втРаботы.Объем КАК Объем,
		|	втРаботы.Масса КАК Масса,
		|	втРаботы.ИзмененыПараметры КАК ИзмененыПараметры,
		|	втРаботы.ЗапрещеноСозданиеНовойТочкиМаршрута КАК ЗапрещеноСозданиеНовойТочкиМаршрута
		|ИЗ
		|	втРаботы КАК втРаботы
		|	ЛЕВОЕ СОЕДИНЕНИЕ втМаксимальныеНомераПоставок КАК втМаксимальныеНомераПоставок
		|	ПО втРаботы.ЗаявкаНаПеревозкуГруза = втМаксимальныеНомераПоставок.ЗаявкаНаПеревозкуГруза
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК втГрузовыеМеста
		|	ПО втРаботы.ГрузовоеМесто = втГрузовыеМеста.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК втТипыГрузов
		|	ПО втГрузовыеМеста.ТипГруза = втТипыГрузов.Ссылка
		|УПОРЯДОЧИТЬ ПО
		|	ЕстьТараВСтроке УБЫВ,
		|	ТараГрузовогоМеста,
		|	ЭтоТара УБЫВ
		|ИТОГИ
		| МАКСИМУМ(ИзмененыПараметры)
		|ПО
		|	Задание,
		|	Операция
		|";
		
		Запрос.УстановитьПараметр("Ссылка", ПрохождениеТочки);
	КонецЕсли;
		
	Результат =  Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Возврат Результат;

КонецФункции

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаСервереБезКонтекста
Функция ПолучитьТипСамойПриоритетнойОперации(Рейс, ИдентификаторТочки)
	
	Возврат упПрохождениеТочки.ПолучитьТипСамойПриоритетнойОперацииВТочке(Рейс, ИдентификаторТочки);
	
КонецФункции

&НаСервере
Процедура УстановитьПроблемуНаВсеНеЗаполненныеЗадачиВТочкеНаСервере(Проблема)
	
	ЗапрещеноСозданиеНовойТочкиМаршрута = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проблема, "ЗапрещеноСозданиеНовойТочкиМаршрута");
	
	Если Не ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
		тбзАдресаПогрузкиПоРейсу = ПолучитьАдресаРазгрузкиПоРейсуНаСервере(Объект.Рейс);
	КонецЕсли;
		
	КоллекцияЗаданий = Работы.ПолучитьЭлементы();
	
	Для каждого Задание Из КоллекцияЗаданий Цикл
		
		Задание.Отменена = Истина;
		Задание.Выполнена = 0;
		
		Для каждого Операция Из Задание.ПолучитьЭлементы() Цикл
			
			Операция.Отменена = Истина;
			Операция.Выполнена = 0;
			
			Для каждого Назначение Из Операция.ПолучитьЭлементы() Цикл
				
				Назначение.Отменена = Истина;
				Назначение.Выполнена = 0;
				
				Если НЕ ЗначениеЗаполнено(Назначение.Проблема) Тогда
					Назначение.Проблема = Проблема;
					Назначение.ЗапрещеноСозданиеНовойТочкиМаршрута = ЗапрещеноСозданиеНовойТочкиМаршрута;
					Если Истина
						И Не ЗапрещеноСозданиеНовойТочкиМаршрута
						И Объект.ВидТранспортнойУслуги <> ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами")
					Тогда
						Если упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(Операция.НазначениеСтроки) И Не тбзАдресаПогрузкиПоРейсу = Неопределено Тогда
							мсвАдреса = тбзАдресаПогрузкиПоРейсу.НайтиСтроки(Новый Структура("Задание, ГрузовоеМесто",Задание.НазначениеСтроки, Назначение.НазначениеСтроки));
							Если мсвАдреса.Количество() > 0 Тогда
								Назначение.АдресРазгрузки = мсвАдреса[0].Адрес;								
							КонецЕсли;
							
						ИначеЕсли Операция.НазначениеСтроки = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") И Не тбзАдресаПогрузкиПоРейсу = Неопределено Тогда
							мсвАдреса = тбзАдресаПогрузкиПоРейсу.НайтиСтроки(Новый Структура("Задание",Задание.НазначениеСтроки));
							Если мсвАдреса.Количество() > 0 Тогда
								Назначение.АдресРазгрузки = мсвАдреса[0].Адрес;								
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли; 
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДляДосрочногоПрохожденияНаСервере()
	
	тбзАдресаПогрузкиПоРейсу = ПолучитьАдресаРазгрузкиПоРейсуНаСервере(Объект.Рейс);
	Объект.ДосрочноеУбытие = Истина;
	
	КоллекцияЗаданий = Работы.ПолучитьЭлементы();
	
	Для каждого Задание Из КоллекцияЗаданий Цикл
		
		Для каждого Операция Из Задание.ПолучитьЭлементы() Цикл
			
			Для каждого Назначение Из Операция.ПолучитьЭлементы() Цикл
				
				Если Истина
					И Назначение.Выполнена <> 1 
					И Не ЗначениеЗаполнено(Назначение.Проблема) 
				Тогда
					Назначение.Отменена = Истина;
					Назначение.Проблема = Справочники.упПроблемы.ДосрочноеПрохождение;
					Если упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(Операция.НазначениеСтроки) Тогда
						Назначение.АдресРазгрузки = Объект.АдресТочки;								
												
					ИначеЕсли Операция.НазначениеСтроки = ПредопределенноеЗначение("Перечисление.упТипыОпераций.ПередачаДокументов") Тогда
						Назначение.АдресРазгрузки = Объект.АдресТочки
												
					КонецЕсли;
					упПрохождениеТочкиКлиентСервер.УстановитьФлаги(Назначение);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдресаРазгрузкиПоРейсуНаСервере(Рейс)
	Возврат  упПрохождениеТочки.ПолучитьАдресаПогрузкиПоРейсу(Рейс);
КонецФункции

&НаСервере
Процедура РейсПриИзмененииНаСервере()
	сткПараметрыВидаПеревозки	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Рейс, "ВидПеревозки.ВариантФормированияМаршрута, 
																							|ВидПеревозки.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава, 
																							|ВидПеревозки.ИспользуетсяСкладскойУчетНаХабах,
																							|ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах,
																							|ВидПеревозки.РазрешеноДосрочноеУбытиеИзТочкиМаршрута");
	ВариантФормированияМаршрута = сткПараметрыВидаПеревозки.ВидПеревозкиВариантФормированияМаршрута;
	РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава	= сткПараметрыВидаПеревозки.ВидПеревозкиРазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава;
	РазрешеноДосрочноеУбытиеИзТочкиМаршрута = сткПараметрыВидаПеревозки.РазрешеноДосрочноеУбытиеИзТочкиМаршрута;
	Элементы.ДосрочноеУбытие.Видимость = РазрешеноДосрочноеУбытиеИзТочкиМаршрута;
	УстановитьВидимостьКнопкиИзменитьТоварныйСостав();
	УстановитьВидимостьКнопкиИзменитьВГХ();
	УстановитьВидимостьКнопкиДосрочноеПрохождениеТочки();
	Элементы.дрвРаботыСкладскаяЯчейка.Видимость = ИспользуетсяСкладскойУчетНаХабах 
													И сткПараметрыВидаПеревозки.ВидПеревозкиИспользуетсяСкладскойУчетНаХабах
													И НЕ сткПараметрыВидаПеревозки.ВидПеревозкиПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится;
	Если Истина
		И Объект.ДосрочноеУбытие
		И Не РазрешеноДосрочноеУбытиеИзТочкиМаршрута Тогда
		Объект.ДосрочноеУбытие = Ложь;
	КонецЕсли;												
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКнопкиИзменитьТоварныйСостав()
	
	Элементы.дрвРаботыИзменитьТоварныйСостав.Видимость = Истина
											   И РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава
											   И ЕстьРаботыПогрузкиИлиРазгрузки()
											   И Объект.ВидТранспортнойУслуги <> Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;		
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКнопкиИзменитьВГХ()
	
	Элементы.дрвРаботыИзменитьВГХ.Видимость = Истина
	                					  И НЕ РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава
									  И РазрешитьИзменениеПараметровГрузовПриИсполненииРейса
									  И ЕстьРаботыПогрузкиИлиРазгрузки();		
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКнопкиДосрочноеПрохождениеТочки()
	
	РазрешеноДосрочноеПрохождение = Истина 
	                                И РазрешеноДосрочноеУбытиеИзТочкиМаршрута
							  И Объект.ТипТочки = Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию
							  И Объект.ВидТранспортнойУслуги <> Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
	
	Элементы.дрвРаботыДосрочноеПрохождениеТочки.Видимость = РазрешеноДосрочноеПрохождение;
	Элементы.ДосрочноеУбытие.Видимость = РазрешеноДосрочноеПрохождение;
		
КонецПроцедуры

&НаСервере
Функция ЕстьРаботыПогрузкиИлиРазгрузки()
	
	Результат = Ложь;
	
	ЭлементыДерева = Работы.ПолучитьЭлементы();
	Для каждого текЗадание Из ЭлементыДерева Цикл
		ЭлементыОперации = текЗадание.ПолучитьЭлементы();
		Для каждого текОперация Из ЭлементыОперации Цикл
			 Результат = упПрохождениеТочкиКлиентСервер.ОсновнаяОперацияДляВидаГрузаГруз(текОперация.НазначениеСтроки);
			 Если Результат Тогда
			 	Прервать;
			 КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОЗакрытииФормыИзмененияТоварногоСостава(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ЗаданиеНаПеревозкуГруза = РезультатЗакрытия.ЗаданиеНаПеревозкуГруза;
		ГрузовоеМестоТекущее = РезультатЗакрытия.ГрузовоеМесто;
		КоличествоГрузов = РезультатЗакрытия.КоличествоГрузов;
		мсвИзмененийСостава = РезультатЗакрытия.МассивИзмененийСостава; 
		Операция = РезультатЗакрытия.Операция; 
		
		Если НЕ мсвИзмененийСостава = Неопределено Тогда
			Модифицированность = Истина;
			ЗаполнитьТЧТоварныйСоставГруза(ЗаданиеНаПеревозкуГруза, ГрузовоеМестоТекущее, Операция, КоличествоГрузов, мсвИзмененийСостава);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧТоварныйСоставГруза(ЗаданиеНаПеревозкуГруза, ГрузовоеМесто, Операция, КоличествоГрузов, мсвИзмененийСостава)
	
	мсвСтрокиГрузовоеМесто = Объект.ТоварныйСоставГруза.НайтиСтроки(Новый Структура("ГрузовоеМестоРодитель", ГрузовоеМесто));
	
	Для каждого СтрокаГрузовоеМесто Из мсвСтрокиГрузовоеМесто Цикл
		Объект.ТоварныйСоставГруза.Удалить(СтрокаГрузовоеМесто);
	КонецЦикла;
	
	Если мсвИзмененийСостава.Количество() > 0 Тогда
		Для каждого Строка Из мсвИзмененийСостава Цикл
			НоваяСтрока = Объект.ТоварныйСоставГруза.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ЗаданиеНаПеревозкуГруза = ЗаданиеНаПеревозкуГруза;
			НоваяСтрока.ГрузовоеМестоРодитель = ГрузовоеМесто;
			НоваяСтрока.КоличествоГрузов = КоличествоГрузов;
		КонецЦикла;
		
		ЭлементЗадание = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(Работы.ПолучитьЭлементы(), "НазначениеСтроки", ЗаданиеНаПеревозкуГруза);
		ЭлементОперация = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементЗадание.ПолучитьЭлементы(), "НазначениеСтроки", Операция);
		ЭлементГрузовоеМесто = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементОперация.ПолучитьЭлементы(), "НазначениеСтроки", ГрузовоеМесто);
		ЭлементГрузовоеМесто.Отменена = Истина;
		ЭлементГрузовоеМесто.Выполнена = Ложь;
		ЭлементГрузовоеМесто.ГрузРазделен = Истина;
		ЭлементГрузовоеМесто.ИндексКартинки = 12;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВГХГруза(сткВГХ)
	
	мсвСтрокиРаботы = Объект.Работы.НайтиСтроки(Новый Структура("ИдентификаторРаботы", сткВГХ.ИдентификаторРаботы));
	
	Для каждого СтрокаРабота Из мсвСтрокиРаботы Цикл
		Для каждого ПараметрВГХ Из сткВГХ Цикл
			Если ПараметрВГХ.Значение <> СтрокаРабота[ПараметрВГХ.Ключ] Тогда
				СтрокаРабота.ИзмененыПараметры = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(СтрокаРабота, сткВГХ, , "ГрузовоеМесто, Задание, ИдентификаторРаботы, Операция");
		ЭлементЗадание  = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(Работы.ПолучитьЭлементы(), "НазначениеСтроки", СтрокаРабота.Задание);
		ЭлементОперация = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементЗадание.ПолучитьЭлементы(), "НазначениеСтроки", СтрокаРабота.Операция);
		ЭлементГрузовоеМесто = упПрохождениеТочкиКлиентСервер.НайтиЗначениеВКоллекции(ЭлементОперация.ПолучитьЭлементы(), "НазначениеСтроки", СтрокаРабота.ГрузовоеМесто);
		ЗаполнитьЗначенияСвойств(ЭлементГрузовоеМесто, сткВГХ, , "ГрузовоеМесто, Задание, ИдентификаторРаботы, Операция");
		// Изменены параметры ВГХ.
		ЭлементГрузовоеМесто.ИзмененыПараметры = СтрокаРабота.ИзмененыПараметры;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТоварныйСостав(ГрузовоеМесто)
	ТоварныйСостав = РеквизитФормыВЗначение("Объект.ТоварныйСоставГруза");
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(ТоварныйСостав.Выгрузить(Новый Структура("ГрузовоеМестоРодитель", ГрузовоеМесто)));
КонецФункции

&НаКлиенте
Процедура ИзменитьВГХ(Команда)
	
	ТекущиеДанныеРаботы = Элементы.дрвРаботы.ТекущиеДанные;
	
	сткПараметрыФормы = Новый Структура();
	сткПараметрыФормы.Вставить("ГрузовоеМесто", ТекущиеДанныеРаботы.ГрузовоеМесто);
	сткПараметрыФормы.Вставить("ЗаданиеНаПеревозкуГруза", ТекущиеДанныеРаботы.Задание);
	сткПараметрыФормы.Вставить("ШиринаГруза", ТекущиеДанныеРаботы.Ширина);
	сткПараметрыФормы.Вставить("ДлинаГруза", ТекущиеДанныеРаботы.Длина);
	сткПараметрыФормы.Вставить("ВысотаГруза", ТекущиеДанныеРаботы.Высота);
	сткПараметрыФормы.Вставить("Масса", ТекущиеДанныеРаботы.Масса);
	сткПараметрыФормы.Вставить("Объем", ТекущиеДанныеРаботы.Объем);
	сткПараметрыФормы.Вставить("КоличествоМест", ТекущиеДанныеРаботы.КоличествоМест);
	сткПараметрыФормы.Вставить("КоличествоЗагрузочныхМетров", ТекущиеДанныеРаботы.КоличествоЗагрузочныхМетров);
	сткПараметрыФормы.Вставить("ИдентификаторРаботы", ТекущиеДанныеРаботы.ИдентификаторРаботы);
		 	
	ОповещениеОЗакрытииФормыИзмененияВГХ = Новый ОписаниеОповещения("ОповещениеОЗакрытииФормыИзмененияВГХ", ЭтаФорма);
	
	ОткрытьФорму("ОбщаяФорма.упИзменениеВГХГруза", сткПараметрыФормы, ЭтаФорма,,,, ОповещениеОЗакрытииФормыИзмененияВГХ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗакрытииФормыИзмененияВГХ(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ЗаполнитьВГХГруза(РезультатЗакрытия);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура дрвРаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)	
	Если Поле.Имя = "дрвРаботыНазначениеСтроки" Тогда 
		текДанные = Элементы.дрвРаботы.ТекущиеДанные;
		Если Не текДанные = Неопределено Тогда 
			упЗаданиеНаПеревозкуГруза = текДанные.НазначениеСтроки;
			Если ТипЗнч(упЗаданиеНаПеревозкуГруза) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда 
				СтандартнаяОбработка = Ложь;
				ПараметрыФормы = Новый Структура("Ключ", упЗаданиеНаПеревозкуГруза);
				ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.Форма.ФормаДокумента", ПараметрыФормы);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
