#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстОшибки = "";
	Если РазрешеноФормировать(ПараметрКоманды, ТекстОшибки) Тогда
		
		Отказ = Ложь;
		ТекстОшибки = "";
		мсвИзменениеПараметровГруза = СформироватьИзменениеПараметровГрузовыхМест(ПараметрКоманды, ТекстОшибки, Отказ);
		
		Если НЕ Отказ Тогда
			Для каждого ИзменениеПараметровГрузовыхМестТекущая Из мсвИзменениеПараметровГруза Цикл
				ОткрытьФорму("Документ.упИзменениеПараметровГрузовыхМест.ФормаОбъекта", Новый Структура("Ключ", ИзменениеПараметровГрузовыхМестТекущая), ПараметрыВыполненияКоманды.Источник);
			КонецЦикла;
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
			КонецЕсли;
		Иначе
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
	Иначе
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьИзменениеПараметровГрузовыхМест(ЗаданиеНаПеревозкуГруза, ТекстОшибки, Отказ)
	
	Возврат Документы.упЗаданиеНаПеревозкуГруза.СформироватьИзменениеПараметровГрузовыхМест(ЗаданиеНаПеревозкуГруза, ТекстОшибки, Отказ);
		
КонецФункции

&НаСервере
Функция РазрешеноФормировать(ЗаданиеНаПеревозкуГруза, ТекстОшибки)
	
	Результат = Истина;
	
	СтатусЗадания = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ЗаданиеНаПеревозкуГруза);
	
	ДоступныеСтатусы = Документы.упИзменениеПараметровГрузовыхМест.ДоступныеСтатусыЗаданийДляОснования();
	
	Если ДоступныеСтатусы.НайтиПоЗначению(СтатусЗадания) = Неопределено Тогда
		ТекстОшибки = Нстр("ru = 'Запрещено формировать изменение параметров груза по заданию в статусе ""%1""'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, СтатусЗадания);
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти 
