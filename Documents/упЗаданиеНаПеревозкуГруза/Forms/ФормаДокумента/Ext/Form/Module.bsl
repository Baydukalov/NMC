#Область ПеременныеМодуляФормы

&НаКлиенте
Перем РазрешитьАктивизациюСтроки;

&НаКлиенте
Перем РедактированиеГрузовыхМест; // признак используется для непосредственной записи измененных доп. реквизитов по грузовому месту.

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизитыУниверсальная");
	// Установка данного свойства нужна для возможности редактирования доп. реквизитов не только по документу, 
	// но и по грузовым местам.
	ДополнительныеПараметры.Вставить("ПроизвольныйОбъект", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ЭтаФорма.ИспользуетсяУчетПодразделений = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах");
	ЭтаФорма.УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьКоличествоЗагрузочныхМетров = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	ЭтаФорма.ПоказатьВсеТовары = Ложь;
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// Взаиморасчеты
	ЭтаФорма.РазрешеноЗагружатьВзаиморасчеты = ПолучитьФункциональнуюОпцию("упЗагружатьВзаиморасчетыСПартнерами");
	ЭтаФорма.ВзаиморасчетыЗагружаются = Ложь;
	ЭтаФорма.ВзаиморасчетыЗагрузить = Ложь;
	Элементы.ГруппаСостояниеВзаиморасчетов.Видимость = РазрешеноЗагружатьВзаиморасчеты;
	ЭтаФорма.РольДоступнаРазработчик = РольДоступна("упРазработчик");
	
	Если УчитыватьЛинейныеРазмеры	Тогда
		Элементы.Декорация1.Видимость = Истина;
		Элементы.Декорация2.Видимость = Истина;
	Иначе
		Элементы.Декорация1.Видимость = Ложь;
		Элементы.Декорация2.Видимость = Ложь;
	КонецЕсли;
	
	ПриСозданииПриЧтенииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	//УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ЭтаФорма.Статус = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка, ПериодУстановкиСтатуса);
	
	// Если новый документ
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		// Если копирование
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ЗаполнитьСписокГрузов(Истина);	
		Иначе	
			Объект.Организация 	= Справочники.Организации.ОрганизацияПоУмолчанию();
			Объект.ОбщийГруз = Истина;
			Объект.ВидПеревозки = Справочники.упВидыПеревозок.ЗначениеПоУмолчанию(Объект.ВидПеревозки);
		КонецЕсли;
		упУправленияЗаявками.ЗаполнитьВидТранспортнойУслуги(Объект);		
		Если ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки, Объект.Автор);
		КонецЕсли;
		Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
			ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
			
			// Если при копировании отличаются часовые пояса источника и нового документа.
			Если Истина
				И ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
				И ЗначениеЗаполнено(Объект.ЧасовойПояс)
				И ЗначениеЗаполнено(ЧасовойПоясВидаПеревозки)
				И НЕ Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки
			Тогда
				Если ЗначениеЗаполнено(Объект.ОтправлениеГрузаС) Тогда
					Объект.ОтправлениеГрузаС = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ОтправлениеГрузаС, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 	
				КонецЕсли;
				Если ЗначениеЗаполнено(Объект.ОтправлениеГрузаПо) Тогда
					Объект.ОтправлениеГрузаПо = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ОтправлениеГрузаПо, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;
				Если ЗначениеЗаполнено(Объект.ПолучениеГрузаС) Тогда
					Объект.ПолучениеГрузаС = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ПолучениеГрузаС, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;	
				Если ЗначениеЗаполнено(Объект.ПолучениеГрузаПо) Тогда
					Объект.ПолучениеГрузаПо = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ПолучениеГрузаПо, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;	
			КонецЕсли;	
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;		
		КонецЕсли;
	Иначе
		Элементы.ЗаявкаНаПеревозкуГруза.ТолькоПросмотр = Истина;
		ЗаполнитьСписокГрузов();	
	КонецЕсли;	
	упЗаданиеНаПеревозку.УстановитьЗаголовокФормыЗаданияНаПеревозку(ЭтотОбъект);
	УстановитьЗаголовкиПоказателейЗагрузки();
	
	Если ВедетсяВалютныйУчет Тогда
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	ЗаблокироватьЭлементы();
	ВидПеревозкиПриИзмененииНаСервере();
	ВремяПогрузкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Объект.ВремяПогрузки*3600);
	ВремяРазгрузкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Объект.ВремяРазгрузки*3600);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазрешитьАктивизациюСтроки = Истина;
	РедактированиеГрузовыхМест = Ложь;
	
	ПриИзмененииПризнакаОбщегоГруза(Истина);
	ЗагрузитьВзаиморасчетыСПартнерами();
	
	ОбновитьПредставлениеПоказателейЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	упОбработкаТабличнойЧастиКлиент.ПроверитьГрузовыеМеста(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	упЗаданиеНаПеревозку.ПроконтролироватьИнтервалОтправкиПолучения(Объект.ОтправлениеГрузаС, Объект.ОтправлениеГрузаПо, Объект.ПолучениеГрузаС, Объект.ПолучениеГрузаПо, Отказ);
	
	Если СтатусДляПроверки(НовыйСтатус) Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан вид перевозки'"), , "ВидПеревозки", "Объект", Отказ);
		КонецЕсли;
		
		// Проверка на дубли.
		ТабличнаяЧасть = ГрузовыеМеста;
		Для каждого Строка Из ТабличнаяЧасть Цикл
			Если НЕ ЗначениеЗаполнено(Строка.ГрузовоеМесто) Тогда
				Продолжить;
			КонецЕсли;
			ТекущийИндекс = ТабличнаяЧасть.Индекс(Строка);
			Отбор = Новый Структура("ГрузовоеМесто", Строка.ГрузовоеМесто);	  
			НайденныеСтроки = ТабличнаяЧасть.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 1 Тогда
				Для каждого СтрокаГрузовоеМесто Из НайденныеСтроки Цикл
					ИндексСтроки = ТабличнаяЧасть.Индекс(СтрокаГрузовоеМесто);
					Если ТекущийИндекс <> ИндексСтроки Тогда
						ТекстСообщения = Нстр("ru = 'В табличной части ""Грузовые места"" присутствуют строки с продублированными грузовыми местами. %1.'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.ГрузовоеМесто);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"ГрузовыеМеста[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].ГрузовыеМестаКод");
					КонецЕсли;
				КонецЦикла;		
				Отказ = Истина;
				Прервать;
			КонецЕсли;
		
		КонецЦикла;  
		
	КонецЕсли; 
	ПроверяемыеРеквизиты.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОпределитьЗоны(ТекущийОбъект);
	
	Если СтатусДляПроверки(НовыйСтатус) Тогда
		Если Не ЗначениеЗаполнено(ТекущийОбъект.ЗонаОтправления) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана зона отправления'"), , "ЗонаОтправления", "Объект", Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТекущийОбъект.ЗонаПолучения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана зона получения'"), , "ЗонаПолучения",  "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;	
	
	Если Не Отказ Тогда
				
		// отредактированные доп. реквизиты по документу сразу переносятся в объект из формы, отредактированные доп. реквизиты по грузам - сразу записываются
		//// СтандартныеПодсистемы.Свойства
		// УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
		//// Конец СтандартныеПодсистемы.Свойства
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ГрузМодифицирован", ГрузМодифицирован);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ГрузовыеМеста", Неопределено);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ТоварныйСостав", Неопределено);
		
		Если ГрузМодифицирован Тогда 
			
			
			Если ТекущийОбъект.ОбщийГруз Тогда 
				
				Если ГрузовыеМеста.Количество() > 0 Тогда
					СтрокаГрузовыеМеста = ГрузовыеМеста[0];
				Иначе	
					СтрокаГрузовыеМеста = ГрузовыеМеста.Добавить();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаГрузовыеМеста, ЭтаФорма);
				СтрокаГрузовыеМеста.Высота = ВысотаГруза;
				СтрокаГрузовыеМеста.Длина = ДлинаГруза;
				СтрокаГрузовыеМеста.Ширина = ШиринаГруза;
				СтрокаГрузовыеМеста.Валюта = ВалютаУпрУчета;
				СтрокаГрузовыеМеста.КоличествоГрузов = 1;
				СтрокаГрузовыеМеста.Идентификатор = Новый УникальныйИдентификатор();
			
				Для каждого СтрокаТовар Из ТоварныйСостав Цикл
					СтрокаТовар.Идентификатор = СтрокаГрузовыеМеста.Идентификатор;
				КонецЦикла;
				
			КонецЕсли;
		
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ГрузовыеМеста", ГрузовыеМеста);
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ТоварныйСостав", ТоварныйСостав);
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
			ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
		КонецЕсли;	
		
		НовыйСтатус = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ТекущийОбъект.ОбщийГруз Тогда 
		Элементы.ОбщийГруз.Видимость = Ложь;
	КонецЕсли;	
	
	Элементы.ЗаявкаНаПеревозкуГруза.ТолькоПросмотр = Истина;
	
	Статус = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка, ПериодУстановкиСтатуса);
	ЗаблокироватьЭлементы();
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаданияНаПеревозкуГруза(Объект.Ссылка, Статус, Элементы);
	
	мсвЗаявкиНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаявкиНаПеревозкуГруза) Тогда
		ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус.ЗагрузитьЗначения(мсвЗаявкиНаПеревозкуГруза);
	КонецЕсли;
	
	упЗаданиеНаПеревозку.УстановитьЗаголовокФормыЗаданияНаПеревозку(ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	УстановитьЗаголовкиПоказателейЗагрузки();
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьДокументаупЗаданиеНаПеревозкуГруза", Новый Структура("Ссылка", Параметры.Ключ));
	ПослеЗаписиНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиНаКлиенте()
	ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	ОбновитьПредставлениеПоказателейЗагрузки();
	ОбновитьДополнительныеРеквизитыПоГрузовомуМесту();
	Элементы.НадписьПараметрыДокумента.Заголовок = "";
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если ИмяСобытия = "СменаСтатуса" И Источник = Объект.Ссылка Тогда
		ИзменилсяСтатус();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФорма
	
&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийГрузПриИзменении(Элемент)
	
	ПриИзмененииПризнакаОбщегоГруза();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПризнакаОбщегоГруза(ПриОткрытии = Ложь)
	
	// Установка видимости
	Элементы.ГруппаНомерИТип.Видимость					= Объект.ОбщийГруз;
	Элементы.ГруппаВГХГруза.Видимость					= Объект.ОбщийГруз;
	Элементы.ГруппаПрочиеХарактеристикиГруза.Видимость 	= Объект.ОбщийГруз;
	Элементы.ГрузовыеМеста.Видимость					= НЕ Объект.ОбщийГруз;
	Элементы.ПоказатьВсеТовары.Видимость				= НЕ Объект.ОбщийГруз;
	ОбновитьПредставлениеПоказателейЗагрузки();
	
	// Текущая страница
	Если Объект.ОбщийГруз Тогда 
		Элементы.ГруппаОписаниеГруза.ТекущаяСтраница	= Элементы.ГруппаВГХГруза;
		Элементы.ГруппаТоварныйСоставГруза.Заголовок	= "Товарный состав";
		Элементы.ГруппаИнформацияОГрузе.Заголовок		= "Информация о грузе";
		Элементы.ГруппаОписаниеГруза.ОтображениеСтраниц	= ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.ТоварныйСостав.ОтборСтрок = Неопределено;
	Иначе
		Элементы.ГруппаОписаниеГруза.ТекущаяСтраница	= Элементы.ГруппаТоварныйСоставГруза;
		Элементы.ГруппаТоварныйСоставГруза.Заголовок	= "Груз";
		Элементы.ГруппаИнформацияОГрузе.Заголовок		= "Грузовые места";
		Элементы.ГруппаОписаниеГруза.ОтображениеСтраниц	= ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	// При переключении тумблера
	Если Не ПриОткрытии Тогда 
		
		// Если произошел переход на страницу грузовых мест
		Если Не Объект.ОбщийГруз И (ЗначениеЗаполнено(ТипГруза) ИЛИ ТоварныйСостав.Количество()) Тогда 
			ГрузМодифицирован = Истина;
			
			//СтрокиГрузов    = СписокГрузов.ПолучитьЭлементы();
			КоличествоСтрок = ГрузовыеМеста.Количество();
			Если КоличествоСтрок = 0 Тогда 
				СтрокаГруза = ГрузовыеМеста.Добавить();
				СтрокаГруза.Идентификатор = Новый УникальныйИдентификатор;
				СтрокаГруза.КоличествоГрузов = 1;
			ИначеЕсли КоличествоСтрок = 1 Тогда 
				СтрокаГруза = ГрузовыеМеста[0];    
			Иначе 
				СтрокаГруза = Неопределено;
			КонецЕсли;
			
			Если СтрокаГруза <> Неопределено Тогда 
				ЗаполнитьЗначенияСвойств(СтрокаГруза, ЭтотОбъект,, "Высота, Ширина");
				СтрокаГруза.Высота 	= ВысотаГруза;
				СтрокаГруза.Ширина 	= ШиринаГруза;
				СтрокаГруза.Длина  	= ДлинаГруза;
				СтрокаГруза.Валюта  = ВалютаИнкассируемаяЦенность;
				Если ЗначениеЗаполнено(СтрокаГруза.ВидГруза) Тогда
					Если СтрокаГруза.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Груз") Тогда
						СтрокаГруза.ИндексКартинки = 0;			
					Иначе
						СтрокаГруза.ИндексКартинки = 1;			
					КонецЕсли;
				Иначе
					СтрокаГруза.ИндексКартинки = 0;			
				КонецЕсли;
				
				Для Каждого текСтрока Из ТоварныйСостав Цикл 
					текСтрока.Идентификатор = СтрокаГруза.Идентификатор;
					текСтрока.ГрузовоеМесто = СтрокаГруза.ГрузовоеМесто;
					текСтрока.Код			= СтрокаГруза.Код;
				КонецЦикла;	
			КонецЕсли;	
			
		ИначеЕсли Объект.ОбщийГруз И ГрузовыеМеста.Количество() = 1  Тогда 
			ГрузМодифицирован = Истина;
	
			СтрокаГруза  = ГрузовыеМеста[0];
			
			Если Не ЗначениеЗаполнено(СтрокаГруза.ГрузовоеМесто) ИЛИ ЭтоОбщийГруз(СтрокаГруза.ГрузовоеМесто) Тогда 
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтрокаГруза,, "Высота, Ширина");
				ВысотаГруза = СтрокаГруза.Высота;
				ШиринаГруза = СтрокаГруза.Ширина;
				ДлинаГруза  = СтрокаГруза.Длина;
				ВалютаИнкассируемаяЦенность = СтрокаГруза.Валюта;
			КонецЕсли;
			ОбновитьДополнительныеРеквизитыПоГрузовомуМесту();
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура БеречьОтНагреваПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура БеречьОтИзлученияПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура БеречьОтВлагиПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ХрупкийПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапрещеноСкладироватьДругНаДругаПриИзменении(Элемент)
	
	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КлассОпасностиПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТемпературныйРежимПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЗагрузочныхМетровПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоМестПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура МассаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипГрузаПриИзменении(Элемент)
	
	ГрузМодифицирован = Истина;
	
	Если ЗначениеЗаполнено(ТипГруза) Тогда
		ЗаполнитьВГХГруза();	
	КонецЕсли;

	ПересчитатьВремяПогрузки();
	ПересчитатьВремяРазгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()

	Если Ложь
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполнено
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отказ 
		Или Объект.ПометкаУдаления Тогда 
		
		ТолькоПросмотр = Истина;
		
	ИначеЕсли Ложь
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполняется 
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВПути 
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию
		Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению Тогда 
		
		УстановитьТолькоПросмотрДляПодчиненныхЭлементов(ЭтаФорма, Истина);
		Элементы.РассчитатьМассу.Доступность = Ложь;
		Элементы.РассчитатьОбъем.Доступность = Ложь;
		
	ИначеЕсли Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое Тогда	
		
		ТолькоПросмотр = Ложь;
		УстановитьТолькоПросмотрДляПодчиненныхЭлементов(ЭтаФорма, Ложь);
		Элементы.РассчитатьМассу.Доступность = Истина;
		Элементы.РассчитатьОбъем.Доступность = Истина;
		Элементы.ЧасовойПояс.ТолькоПросмотр  = Истина;
		
	КонецЕсли;	
	
	Элементы.Статус.ТолькоПросмотр = Истина;
		
	Если Истина
		И НЕ ТолькоПросмотр
		И ЗначениеЗаполнено(Объект.Ссылка)
		И ВедетсяРаботаВРазныхЧасовыхПоясах 
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	ВидПеревозкиПриИзмененииНаСервере();
	Если НЕ Элементы.ЗапрещеноИсполнениеНесколькимиРейсами.Доступность Тогда
		Объект.ЗапрещеноИсполнениеНесколькимиРейсами = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("Партнер", Объект.Заказчик);
	ОткрытьФорму("Справочник.упСоглашенияСКлиентами.ФормаВыбора",сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.КонтрагентПолучателя) Тогда
		Объект.КонтрагентОтправителя = ПолучитьОсновногоКонтрагента(Объект.Отправитель);	
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) И ЗначениеЗаполнено(Объект.Отправитель) И ЗначениеЗаполнено(Объект.АдресОтправления) Тогда
		Объект.ГрафикРаботыАдресаОтправления = ПолучитьГрафикРаботы(Объект.Отправитель, Объект.АдресОтправления);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОтправленияПриИзменении(Элемент)
	
	АдресОтправленияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.КонтрагентПолучателя) Тогда
		Объект.КонтрагентПолучателя = ПолучитьОсновногоКонтрагента(Объект.Получатель);	
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) И ЗначениеЗаполнено(Объект.Получатель) И ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		Объект.ГрафикРаботыАдресаПолучения = ПолучитьГрафикРаботы(Объект.Получатель, Объект.АдресПолучения);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПолученияПриИзменении(Элемент)
	
	АдресПолученияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаНаПеревозкуГрузаПриИзменении(Элемент)
	
	текОбщийГруз = Объект.ОбщийГруз;
	ЗаявкаНаПеревозкуГрузаПриИзмененииНаСервере();
	Если НЕ текОбщийГруз = Объект.ОбщийГруз Тогда
		ПриИзмененииПризнакаОбщегоГруза();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаявкаНаПеревозкуГрузаПриИзмененииНаСервере()
	
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.Заполнить(Объект.ЗаявкаНаПеревозкуГруза);
	Если текОбъект.ДополнительныеСвойства.Свойство("ОшибкаПриЗаполнении") Тогда
		Объект.ЗаявкаНаПеревозкуГруза = Документы.упЗаявкаНаПеревозкуГруза.ПустаяСсылка();		
	Иначе	
		ЗначениеВРеквизитФормы(текОбъект, "Объект");
		ГрузовыеМеста.Очистить();
		ТоварныйСостав.Очистить();
		ЗаполнитьСписокГрузов();
		Если ГрузовыеМеста.Количество()>0 Тогда
			ГрузМодифицирован = Истина;	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеТоварыПриИзменении(Элемент)
	Если ПоказатьВсеТовары Тогда
		Элементы.ГруппаГрузыСтраницы.ТекущаяСтраница = Элементы.ГруппаТовары;
	Иначе	
		Элементы.ГруппаГрузыСтраницы.ТекущаяСтраница = Элементы.ГруппаГрузыТовары;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикПриИзменении(Элемент)
	Объект.Соглашение = ПолучитьОсновноеСоглашение(Объект.Заказчик,Объект.Организация);
	Объект.КонтрагентЗаказчика = ПолучитьОсновногоКонтрагента(Объект.Заказчик);
	ЗагрузитьВзаиморасчетыСПартнерами();
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаТипГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаГруз Тогда
		РедактированиеГрузовыхМест = Истина;
		ОбновитьДополнительныеРеквизитыПоГрузовомуМесту();
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДополнительно Тогда
		РедактированиеГрузовыхМест = Ложь;
		ПриСменеСтраницыДополнительныеРеквизиты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТоварныйСостав

&НаКлиенте
Процедура ТоварныйСоставПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставНоменклатураПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, ,Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставУпаковкаНоменклатурыПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставКоличествоПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСостав.ТекущиеДанные);
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставЦенаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСостав.ТекущиеДанные);
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставСтавкаНДСПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставСуммаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаСуммаПриИзменении(Элементы.ТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если НЕ Объект.ОбщийГруз Тогда
		Если Элементы.ГрузовыеМеста.ТекущиеДанные = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сначала необходимо добавить грузовое место'"), Объект.Ссылка, "ГрузовыеМеста",,Отказ);
		ИначеЕсли Копирование Тогда 	
			ГрузМодифицирован = Истина;
			упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, ,Истина, Истина);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НЕ Объект.ОбщийГруз Тогда
		Если НоваяСтрока И НЕ Копирование Тогда
			ТекущиеДанные = Элементы.ГрузовыеМеста.ТекущиеДанные;
			Элемент.ТекущиеДанные.Идентификатор = ТекущиеДанные.Идентификатор;
			Элемент.ТекущиеДанные.ГрузовоеМесто	= ТекущиеДанные.ГрузовоеМесто;
			Элемент.ТекущиеДанные.Код			= ТекущиеДанные.Код;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПослеУдаления(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, ,Истина, Истина);
	упОбработкаТабличнойЧастиКлиент.ПересчитатьИтоговыеСуммы(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	упОбработкаТабличнойЧастиКлиент.ПересчитатьИтоговыеСуммы(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыГрузовыеМеста

&НаКлиенте
Процедура ГрузовыеМестаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Если Копирование Тогда
			Элемент.ТекущиеДанные.ГрузовоеМесто = Неопределено;
			Элемент.ТекущиеДанные.Код = "";			
		Иначе	
			Элемент.ТекущиеДанные.КоличествоГрузов = 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Идентификатор = Новый УникальныйИдентификатор();
		Элементы.ТоварныйСостав.ОтборСтрок 	= Новый ФиксированнаяСтруктура("Идентификатор", Элемент.ТекущиеДанные.Идентификатор);
	КонецЕсли;
	ПересчитатьВремяПогрузки();
	ПересчитатьВремяРазгрузки();
	ПересчитатьСуммуПлановойИнкассации();
	Объект.ОценочнаяСтоимость 	= ГрузовыеМеста.Итог("ОценочнаяСтоимость");
	
	Если НЕ ОтменаРедактирования И ГрузовыеМеста.Количество()>1 Тогда
		Элементы.ОбщийГруз.Доступность = Ложь;
	КонецЕсли;
	
	Если ГрузМодифицирован Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаТипГрузаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ГрузовыеМеста.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ТипГруза) Тогда
		сткВГХГруза = ПолучитьВГХГруза(ТекущиеДанные.ТипГруза); 	
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, сткВГХГруза);
	КонецЕсли;

	ПересчитатьВремяПогрузки();
	ПересчитатьВремяРазгрузки();
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаШиринаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, КоэффициентПересчетаОбъема);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаДлинаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, КоэффициентПересчетаОбъема);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаВысотаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, КоэффициентПересчетаОбъема);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаОбъемОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Объем");
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаМассаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Масса");	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаКоличествоПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриИзменении(Элемент)
	
	ГрузМодифицирован = Истина;
	
	Если НЕ Объект.ОбщийГруз Тогда
		НадписьПараметрыДокумента = НСтр("ru = 'Итоги по документу актуализируются после записи(*)'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	ГрузМодифицирован = Истина;
	Если Копирование Тогда
		ПересчитатьВремяПогрузки();
		ПересчитатьВремяРазгрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПослеУдаления(Элемент)
	ГрузМодифицирован = Истина;
	Модифицированность = Истина;
	ПересчитатьВремяПогрузки();
	ПересчитатьВремяРазгрузки();
	Если ГрузовыеМеста.Количество()<=1 Тогда
		Элементы.ОбщийГруз.Доступность = Истина;
	КонецЕсли;
	ПересчитатьСуммуПлановойИнкассации();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередУдалением(Элемент, Отказ)
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		текГруз	= ГрузовыеМеста.НайтиПоИдентификатору(текИдентификатор);
		Если НЕ текГруз  = Неопределено Тогда
        	мсвСтрок = ТоварныйСостав.НайтиСтроки(Новый Структура("Идентификатор", текГруз.Идентификатор));
			Для каждого текСтрока Из мсвСтрок Цикл
				ТоварныйСостав.Удалить(текСтрока);							
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриАктивизацииСтроки(Элемент)
	
	Если РазрешитьАктивизациюСтроки И РедактированиеГрузовыхМест Тогда
		Если Не Элемент.ТекущиеДанные = Неопределено Тогда
			Элементы.ТоварныйСостав.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", Элемент.ТекущиеДанные.Идентификатор);
		КонецЕсли;
		ПодключитьОбработчикОжидания("ГрузовыеМестаПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриАктивизацииСтрокиОбработчикОжидания()
	
	Если Не Элементы.ГрузовыеМеста.ТекущиеДанные = Неопределено Тогда
		РазрешитьАктивизациюСтроки = Ложь;
		ТекущееГрузовоеМесто = Элементы.ГрузовыеМеста.ТекущиеДанные.ГрузовоеМесто;
		ОбновитьДополнительныеРеквизитыПоГрузовомуМесту();
		РазрешитьАктивизациюСтроки = Истина;		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыДополнительныеОперации

&НаКлиенте
Процедура ДополнительныеОперацииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПересчитатьВремяПогрузки();
	ПересчитатьВремяРазгрузки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыВесьТоварныйСостав

&НаКлиенте
Процедура ВесьТоварныйСоставПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если НЕ НоваяСтрока И ОтменаРедактирования Тогда
		ТекущиеДанные = Элементы.ВесьТоварныйСостав.ТекущиеДанные;
		ТекущиеДанные.ГрузовоеМесто = ТекущийГруз;
		ТекущийГруз			= Неопределено;
	КонецЕсли;
	Элементы.ТоварныйСостав.Обновить();	
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элементы.ВесьТоварныйСостав.ТекущиеДанные;
	Если НЕ ОтменаРедактирования Тогда
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ГрузовоеМесто) Тогда
			ТекстСообщения = Нстр("ru = 'Поле груз не заполнено'");
			текЭлементКоллекции = ТоварныйСостав.НайтиПоИдентификатору(Элементы.ВесьТоварныйСостав.ТекущаяСтрока);
			текНомерСтроки = ТоварныйСостав.Индекс(текЭлементКоллекции) + 1;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав",текНомерСтроки,"ГрузовоеМесто"),,Отказ);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Идентификатор) И НЕ Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Груз не найден в табличной части ""грузовые места"", добавление запрещено'");
			текЭлементКоллекции = ТоварныйСостав.НайтиПоИдентификатору(Элементы.ВесьТоварныйСостав.ТекущаяСтрока);
			текНомерСтроки = ТоварныйСостав.Индекс(текЭлементКоллекции) + 1;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав",текНомерСтроки,"ГрузовоеМесто"),,Отказ);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставГрузПриИзменении(Элемент)
	// Ведется поиск выбранного груза в таблице "грузовые места".
	// если он существует, то берется идентификатор строки грузового места.
	// если нет, то ошибка и идентификатор не присваивается.
	#Область ПрисвоитьСтрокеИдентификаторГруза
	ТекущиеДанные 		= Элементы.ВесьТоварныйСостав.ТекущиеДанные;
	текИдентификатор 	= ТекущиеДанные.Идентификатор;
	текНовыйГруз		= ТекущиеДанные.ГрузовоеМесто;
	Отказ = Ложь;
	
	Если ЗначениеЗаполнено(текИдентификатор) Тогда
		
		// Найти строку с текущим идентификатором в таблице Грузовые места
		мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("Идентификатор", текИдентификатор));
		
		Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
			
			текСтарыйГрузСтрокаГрузовыеМеста = мсвСтрокиГрузовыеМеста[0];
			текСтарыйГруз = текСтарыйГрузСтрокаГрузовыеМеста.ГрузовоеМесто;
			
			// Если груз поменялся
			Если НЕ текСтарыйГруз = текНовыйГруз Тогда
				
				мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", текНовыйГруз));
				
				текИдентификаторСтрокиНовогоГруза = Неопределено;
				
				// Если в "товарном составе" есть новый груз
				Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
					текНовыйГруз = мсвСтрокиГрузовыеМеста[0];
					ТекущиеДанные.Идентификатор 		= текНовыйГруз.Идентификатор;
					ТекущиеДанные.Код			 		= текНовыйГруз.Код;
					текИдентификаторСтрокиНовогоГруза 	= текНовыйГруз.ПолучитьИдентификатор();							
				Иначе
					Отказ = Истина;
				КонецЕсли;

				Если НЕ Отказ Тогда
					мсвСтрокиТоварныйСостав = ТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", текСтарыйГруз));
					
					// Если в "товарном составе" строки со старым грузовым местом нет
					Если мсвСтрокиТоварныйСостав.Количество() = 0 Тогда
						//	Удалить строку со старым грузовым местом из "грузовых мест"
						ГрузовыеМеста.Удалить(ГрузовыеМеста.Индекс(текСтарыйГрузСтрокаГрузовыеМеста));
					Иначе	
						// Пересчитать характеристики старого груза
						упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текСтарыйГрузСтрокаГрузовыеМеста.ПолучитьИдентификатор());
					КонецЕсли;
					
					// Пересчитать характеристики нового груза
					Если Не Отказ Тогда
						упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиНовогоГруза);
					КонецЕсли;
				КонецЕсли;
			
			КонецЕсли;
		КонецЕсли;
	Иначе	
		мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", текНовыйГруз));
		
		// Если текущий груз есть в "грузовых местах"
		Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
			текСтрокаГрузовыеМеста = мсвСтрокиГрузовыеМеста[0];
			ТекущиеДанные.Идентификатор = текСтрокаГрузовыеМеста.Идентификатор;
			ТекущиеДанные.Код			= текСтрокаГрузовыеМеста.Код;
			упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текСтрокаГрузовыеМеста.ПолучитьИдентификатор());	
		Иначе
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;
	Если НЕ Отказ Тогда
		ГрузМодифицирован = Истина;
	Иначе
		ТекущиеДанные.Идентификатор = Неопределено;	
	КонецЕсли;
	#КонецОбласти
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПередУдалением(Элемент, Отказ)
	 ТекущийГруз = Элементы.ВесьТоварныйСостав.ТекущиеДанные.ГрузовоеМесто;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПослеУдаления(Элемент)
		Если НЕ ТекущийГруз = Неопределено Тогда
		мсвСтрокиТоварныйСостав = ТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", ТекущийГруз));
		Если мсвСтрокиТоварныйСостав.Количество() > 0 Тогда
			мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", ТекущийГруз));
			Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
				упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, мсвСтрокиГрузовыеМеста[0].ПолучитьИдентификатор());			
				ГрузМодифицирован = Истина;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	ТекущийГруз = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда
		ГрузМодифицирован = Истина;
		текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
		Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
			упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза, ,Истина, Истина);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставНоменклатураПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
	Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
		упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза, ,Истина, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставУпаковкаНоменклатурыПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
	Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
		упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставКоличествоПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
	текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
	Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
		упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставЦенаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставСтавкаНДСПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставСуммаНДСПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаСуммаПриИзменении(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущийГруз = Элементы.ВесьТоварныйСостав.ТекущиеДанные.ГрузовоеМесто;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьВременноеОкноОтправления(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ОтправлениеГрузаС", "ОтправлениеГрузаПо"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВременноеОкноПолучения(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПолучениеГрузаС", "ПолучениеГрузаПо"));
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПоГрафикуРаботыТочкиОтправления(Команда)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ГрафикРаботы" , Объект.ГрафикРаботыАдресаОтправления);
		сткПараметры.Вставить("НачалоПериода", Объект.ОтправлениеГрузаС);
		сткПараметры.Вставить("КонецПериода" , Объект.ОтправлениеГрузаПо);
		сткПараметры.Вставить("ЦветФона"     , Элементы.ГруппаОтправление.ЦветФона); 
		
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораВременногоОкна", сткПараметры,,,,,Новый ОписаниеОповещения("ИзменитьВремяОтправленияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПоГрафикуРаботыТочкиПолучения(Команда)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ГрафикРаботы" , Объект.ГрафикРаботыАдресаПолучения);
		сткПараметры.Вставить("НачалоПериода", Объект.ПолучениеГрузаС);
		сткПараметры.Вставить("КонецПериода" , Объект.ПолучениеГрузаПо);
		сткПараметры.Вставить("ЦветФона"     , Элементы.ГруппаПолучение.ЦветФона); 
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораВременногоОкна", сткПараметры,,,,,Новый ОписаниеОповещения("ИзменитьВремяПолученияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОбъем(Команда)
	
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Объем");	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьМассу(Команда)
	
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Масса");	
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура СтатусКПланированию(Команда)
	Если УстановитьСтатусКПланированиюСервер() Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("ЗаписьДокументаупЗаданиеНаПеревозкуГруза", Новый Структура("Ссылка", Параметры.Ключ));
		ПослеЗаписиНаКлиенте();
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКВыполнению(Команда)
	Если УстановитьСтатусКВыполнениюСервер() Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("ЗаписьДокументаупЗаданиеНаПеревозкуГруза", Новый Структура("Ссылка", Параметры.Ключ));
		ПослеЗаписиНаКлиенте();
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтменена(Команда)
	
	Если Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполнено") Тогда 
		Если УстановитьСтатусОтмененаСервер() Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
			Оповестить("ЗаписьДокументаупЗаданиеНаПеревозкуГруза", Новый Структура("Ссылка", Параметры.Ключ));
			ПослеЗаписиНаКлиенте();
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Задание выполнено, отмена невозможна'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры
	
// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ОткрытьГрузовоеМесто(Команда)
	
	Если Элементы.ГрузовыеМеста.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоПросмотр", Не РольДоступнаРазработчик);
	ПараметрыФормы.Вставить("Ключ", Элементы.ГрузовыеМеста.ТекущиеДанные.ГрузовоеМесто);
	ОткрытьФорму("Справочник.упГрузовыеМеста.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовкиПоказателейЗагрузки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
		МассаПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.Масса, ЕИ_Масса));
		Элементы.ГрузовыеМестаМасса.Заголовок = НСтр(СтрШаблон("ru = 'Масса (%1)'", ЕИ_Масса));
		Масса = Объект.Масса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
		ОбъемПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.Объем, ЕИ_Объем));
		Элементы.ГрузовыеМестаОбъем.Заголовок = НСтр(СтрШаблон("ru = 'Объем (%1)'", ЕИ_Объем));
		Объем = Объект.Объем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		ЕИ_Грузов = сткПредставления.КоличествоМест;
		КоличествоМестПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.КоличествоМест, ЕИ_Грузов));
		Элементы.ГрузовыеМестаКоличествоМест.Заголовок = НСтр(СтрШаблон("ru = 'Кол. мест (%1)'", ЕИ_Грузов));
		КоличествоМест = Объект.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетровПредставление = НСтр(СтрШаблон("ru = '%1'", Объект.КоличествоЗагрузочныхМетров));
		КоличествоЗагрузочныхМетров = Объект.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
	Если ВедетсяВалютныйУчет Тогда
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.ОценочнаяСтоимость,	ВалютаУпрУчета);
	Иначе	
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.ОценочнаяСтоимость);
	КонецЕсли;  
	ОценочнаяСтоимость = Объект.ОценочнаяСтоимость;
	
	Если ИспользуютсяУслугиИнкассации Тогда
		Если ВедетсяВалютныйУчет Тогда
			СуммаИнкассацииПланПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаИнкассацииПлан, ВалютаУпрУчета);
		Иначе
			СуммаИнкассацииПланПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаИнкассацииПлан);
		КонецЕсли; 		
	КонецЕсли; 
	
	СуммаИнкассацииПлан = Объект.СуммаИнкассацииПлан;
	КоличествоГрузов					= Объект.КоличествоГрузов;
	КоличествоГрузовыхМест				= Объект.КоличествоГрузовыхМест;
	КоличествоГрузовПредставление 		= НСтр(СтрШаблон("ru = '%1'", КоличествоГрузов));
	КоличествоГрузовыхМестПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузовыхМест));
	
КонецПроцедуры // УстановитьЗаголовкиПоказателейЗагрузки()

&НаКлиенте
Процедура ОбновитьПредставлениеПоказателейЗагрузки()
		
	Если УчитыватьМассу Тогда
		Масса = Объект.Масса;
		МассаПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Масса, ЕИ_Масса));
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		Объем = Объект.Объем;
		ОбъемПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объем, ЕИ_Объем));
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		КоличествоМест = Объект.КоличествоМест;
		КоличествоМестПредставление = НСтр(СтрШаблон("ru = '%1 %2'", КоличествоМест, ЕИ_Грузов));
	КонецЕсли;
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетров = Объект.КоличествоЗагрузочныхМетров;
		КоличествоЗагрузочныхМетровПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоЗагрузочныхМетров));
	КонецЕсли;
	
	Если ВедетсяВалютныйУчет Тогда
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ОценочнаяСтоимость, ВалютаУпрУчета);
	Иначе	
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ОценочнаяСтоимость);
	КонецЕсли;  
	
	Если ИспользуютсяУслугиИнкассации Тогда
		Если ВедетсяВалютныйУчет Тогда
			СуммаИнкассацииПланПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаИнкассацииПлан, ВалютаУпрУчета);
		Иначе
			СуммаИнкассацииПланПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаИнкассацииПлан);
		КонецЕсли; 		
	КонецЕсли; 
	
	КоличествоГрузов					= Объект.КоличествоГрузов;
	КоличествоГрузовыхМест				= Объект.КоличествоГрузовыхМест;
	КоличествоГрузовПредставление 		= НСтр(СтрШаблон("ru = '%1'", КоличествоГрузов));
	КоличествоГрузовыхМестПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузовыхМест));
	
КонецПроцедуры // ОбновитьПредставлениеПоказателейЗагрузки()

&НаСервере
Функция ДоступностьГрузаПоСтатусу()
	
	Если Не ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	ТолькоПросмотрГруппыГрузы = Ложь;
	
	Если Не (Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию
		                                                                  Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс
																		  Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению) Тогда
		ТолькоПросмотрГруппыГрузы = Истина;
		
	Иначе
		Если Не Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое Тогда
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, "ПараметрыВводаИнформацииОГрузовыхМестах");
			
			Если Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию 
				                                      И (сткРеквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится 
				                                         Или сткРеквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку) Тогда
				ТолькоПросмотрГруппыГрузы = Истина;
			ИначеЕсли (Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс Или Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению)  
				                                      И (сткРеквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится 
				                                         Или сткРеквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку
				                                         Или сткРеквизиты.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса) Тогда
				ТолькоПросмотрГруппыГрузы = Истина;
			Иначе	
				ТолькоПросмотрГруппыГрузы = Ложь;
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЕсли; 

	Возврат ТолькоПросмотрГруппыГрузы;
	
КонецФункции

&НаСервере
Процедура УстановитьТолькоПросмотрДляПодчиненныхЭлементов(ЭлементФормы, ТолькоПросмотр)
			
	Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") Тогда
		
		Если ЭлементФормы = Элементы.ГруппаГруз Тогда
			ТолькоПросмотр = ДоступностьГрузаПоСтатусу();		
		КонецЕсли;
		
		Для каждого Элемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
			УстановитьТолькоПросмотрДляПодчиненныхЭлементов(Элемент, ТолькоПросмотр);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы")  Тогда
		
		ЭлементФормы.ТолькоПросмотр = ТолькоПросмотр;
		Для каждого Элемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
			УстановитьТолькоПросмотрДляПодчиненныхЭлементов(Элемент, ТолькоПросмотр);
		КонецЦикла;	
		
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("УправляемаяФорма")  Тогда
		
		Для каждого Элемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
			УстановитьТолькоПросмотрДляПодчиненныхЭлементов(Элемент, ТолькоПросмотр);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("ПолеФормы") Тогда
		
		ЭлементФормы.ТолькоПросмотр = ТолькоПросмотр;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура АдресОтправленияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.АдресОтправления) И Не ЗначениеЗаполнено(Объект.ЗонаОтправления) Тогда
		Объект.ЗонаОтправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресОтправления, "ГеографическаяЗона");
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) И ЗначениеЗаполнено(Объект.Отправитель) И ЗначениеЗаполнено(Объект.АдресОтправления) Тогда
		Объект.ГрафикРаботыАдресаОтправления = ПолучитьГрафикРаботы(Объект.Отправитель, Объект.АдресОтправления);
	КонецЕсли;
	ПересчитатьВремяПогрузки();
	
КонецПроцедуры // АдресОтправленияПриИзмененииНаСервере()

&НаСервере
Процедура АдресПолученияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.АдресПолучения) И Не ЗначениеЗаполнено(Объект.ЗонаПолучения) Тогда
		Объект.ЗонаПолучения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресПолучения, "ГеографическаяЗона");
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) И ЗначениеЗаполнено(Объект.Получатель) И ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		Объект.ГрафикРаботыАдресаПолучения = ПолучитьГрафикРаботы(Объект.Получатель, Объект.АдресПолучения);
	КонецЕсли;
	ПересчитатьВремяРазгрузки();
	
КонецПроцедуры // АдресПолученияПриИзмененииНаСервере()

&НаКлиенте
Процедура ОповеститьОбИзмененииСтатусаРодительскиеДокументы()
	Для каждого текЗаявка Из ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус Цикл
		Оповестить("СменаСтатуса",,текЗаявка.Значение);	
	КонецЦикла;
	ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус.Очистить();
КонецПроцедуры

&НаСервере
Процедура ПересчитатьВремяПогрузки();
	
	тбзТипыГрузовыхМест = Новый ТаблицаЗначений;
	тбзТипыГрузовыхМест.Колонки.Добавить("ТипГруза", Новый ОписаниеТипов("СправочникСсылка.упТипыГрузов"));
	тбзТипыГрузовыхМест.Колонки.Добавить("ВидГруза", Новый ОписаниеТипов("ПеречислениеСсылка.упВидыГрузов"));
	
	мсвТиповГрузовыхМест = Новый Массив();
	Если Объект.ОбщийГруз Тогда
		НоваяСтрока = тбзТипыГрузовыхМест.Добавить();
		НоваяСтрока.ТипГруза = ТипГруза;
		НоваяСтрока.ВидГруза = ВидГруза;
	Иначе	
		Для каждого текСтрока Из ГрузовыеМеста Цикл
			Если ЗначениеЗаполнено(текСтрока.ТипГруза) Тогда
				НоваяСтрока = тбзТипыГрузовыхМест.Добавить();
				НоваяСтрока.ТипГруза = текСтрока.ТипГруза;
				НоваяСтрока.ВидГруза = текСтрока.ВидГруза;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	упЗаданиеНаПеревозку.ПересчитатьВремяПогрузки(Объект, тбзТипыГрузовыхМест);
	ВремяПогрузкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Объект.ВремяПогрузки*3600);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьВремяРазгрузки();
	
	тбзТипыГрузовыхМест = Новый ТаблицаЗначений;
	тбзТипыГрузовыхМест.Колонки.Добавить("ТипГруза", Новый ОписаниеТипов("СправочникСсылка.упТипыГрузов"));
	тбзТипыГрузовыхМест.Колонки.Добавить("ВидГруза", Новый ОписаниеТипов("ПеречислениеСсылка.упВидыГрузов"));
	
	мсвТиповГрузовыхМест = Новый Массив();
	Если Объект.ОбщийГруз Тогда
		НоваяСтрока = тбзТипыГрузовыхМест.Добавить();
		НоваяСтрока.ТипГруза = ТипГруза;
		НоваяСтрока.ВидГруза = ВидГруза;
	Иначе	
		Для каждого текСтрока Из ГрузовыеМеста Цикл
			Если ЗначениеЗаполнено(текСтрока.ТипГруза) Тогда
				НоваяСтрока = тбзТипыГрузовыхМест.Добавить();
				НоваяСтрока.ТипГруза = текСтрока.ТипГруза;
				НоваяСтрока.ВидГруза = текСтрока.ВидГруза;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
			
	упЗаданиеНаПеревозку.ПересчитатьВремяРазгрузки(Объект, тбзТипыГрузовыхМест);
	ВремяРазгрузкиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Объект.ВремяРазгрузки*3600);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объем = ШиринаГруза * ВысотаГруза * ДлинаГруза * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусКПланированиюСервер()
	
	Если (Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое) Тогда 
		Возврат ИзменитьСтатусЗадания(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""Новое""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусКВыполнениюСервер()
	
	Если (Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию) Тогда 
		Возврат ИзменитьСтатусЗадания(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""К планированию""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОтмененаСервер()
	
	Возврат ИзменитьСтатусЗадания(Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено);
	
КонецФункции

&НаСервере
Функция ИзменитьСтатусЗадания(СтатусЗадания)
	
	НовыйСтатус = СтатусЗадания;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение	
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Изменение статуса'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
						УровеньЖурналаРегистрации.Ошибка,,Объект.Ссылка,ОписаниеОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	
			КонецЕсли;
			Возврат Ложь;  
		КонецПопытки;
		
		Статус = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка, ПериодУстановкиСтатуса);
		ЗаблокироватьЭлементы();
		
		упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаданияНаПеревозкуГруза(Объект.Ссылка, Статус, Элементы);
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус()
	
	Статус = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьСтатус(Объект.Ссылка, ПериодУстановкиСтатуса);
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаданияНаПеревозкуГруза(Объект.Ссылка, Статус, Элементы);
	
КонецПроцедуры

&НаСервере
Процедура ВидПеревозкиПриИзмененииНаСервере()
	
	ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = Ложь;
	ИспользуютсяУслугиИнкассации = Ложь; 
	СпособУчетаИнкассируемыхЦенностей = Перечисления.упСпособыУчетаИнкассируемыхЦенностей.ПустаяСсылка();
		
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		ИменаРеквизитов = "ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза, ИспользуютсяУслугиИнкассации, СпособУчетаИнкассируемыхЦенностей, ПараметрыВводаИнформацииОГрузовыхМестах";
		сткЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, ИменаРеквизитов);
		ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = сткЗначенияРеквизитов.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза;	
		ИспользуютсяУслугиИнкассации = сткЗначенияРеквизитов.ИспользуютсяУслугиИнкассации;
		СпособУчетаИнкассируемыхЦенностей = сткЗначенияРеквизитов.СпособУчетаИнкассируемыхЦенностей;
		ПараметрыВводаИнформацииОГрузовыхМестах = сткЗначенияРеквизитов.ПараметрыВводаИнформацииОГрузовыхМестах;
	КонецЕсли;
	
	Элементы.ЗапрещеноИсполнениеНесколькимиРейсами.Доступность = Ложь
                                                                  Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса
												      Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку;
	
													 
	ИспользуютсяУслугиИнкассации = ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") И ИспользуютсяУслугиИнкассации;
	
	Если ИспользуютсяУслугиИнкассации Тогда
		Если СпособУчетаИнкассируемыхЦенностей = Перечисления.упСпособыУчетаИнкассируемыхЦенностей.ВЦеломПоЗаявкеНаПеревозкуГруза Тогда
			Элементы.ГрузовыеМестаВалюта.Видимость 	= Ложь;
			Элементы.ГруппаВалюта.ТекущаяСтраница 	= Элементы.ГруппаВалютаГруз;
			Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
		Иначе	
			Если Объект.ОбщийГруз Тогда 
				Если ИспользуютсяУслугиИнкассации Тогда
					ЭтоГруз = НЕ ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.ИнкассируемаяЦенность");
					Если ЭтоГруз Тогда
						Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
					Иначе	
						Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаИнкассируемаяЦенность;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	
			
			Если ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится Тогда
				Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Истина;
			Иначе
				Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
			КонецЕсли; 
		КонецЕсли;
	Иначе
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
		Элементы.ГрузовыеМестаВалюта.Видимость = Ложь;
		Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
	КонецЕсли; 	
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаданияНаПеревозкуГруза(Объект.Ссылка, Статус, Элементы);
	
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки);
		Если ЗначениеЗаполнено(Подразделение)
				И НЕ Подразделение = Объект.Подразделение Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
	
	Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
		Если НЕ ЧасовойПоясВидаПеревозки = Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;
		КонецЕсли;
	КонецЕсли;
	
	упУправленияЗаявками.ЗаполнитьВидТранспортнойУслуги(Объект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоОбщийГруз(Груз)
	
	Возврат Груз.ОбщийГрузПоЗаявке;
	
КонецФункции	

&НаСервере
Процедура ЗаполнитьСписокГрузов(Копирование = Ложь)
	ГрузовыеМеста.Очистить();
	Если Объект.ГрузовыеМеста.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаГрузовыеМеста();
		Запрос.УстановитьПараметр("тбзГрузовыеМеста", Объект.ГрузовыеМеста.Выгрузить());
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		Запрос.УстановитьПараметр("НомерВЦепиПоставок", Объект.НомерВЦепиПоставок);
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкугруза", Объект.ЗаявкаНаПеревозкуГруза);
		Запрос.УстановитьПараметр("ПериодФормированияДвижений", Документы.упЗаданиеНаПеревозкуГруза.ОпределитьПериодФормированияДвижений(Объект.Ссылка,ПараметрыВводаИнформацииОГрузовыхМестах ,Статус ,Объект.ЧасовойПояс));
		мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
			
		ИндексЗапросаТоварныйСостав = 2;
		ИндексЗапросаГрузовыеМеста = 4;
		тбзТоварныйСостав = мсвРезультатЗапроса[ИндексЗапросаТоварныйСостав].Выгрузить();
		ВыборкаГрузовоеМесто = мсвРезультатЗапроса[ИндексЗапросаГрузовыеМеста].Выбрать();
		
		Если Объект.ОбщийГруз Тогда 
			
			Если ВыборкаГрузовоеМесто.Следующий() Тогда
				Если Копирование Тогда 
					ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
					Код  = "";
					ГрузМодифицирован = Истина;
				Иначе	
					ГрузовоеМесто = ВыборкаГрузовоеМесто.Ссылка;
				КонецЕсли;	
				
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаГрузовоеМесто,,"Высота, Ширина");
				ВысотаГруза = ВыборкаГрузовоеМесто.Высота;
				ШиринаГруза = ВыборкаГрузовоеМесто.Ширина;
				ДлинаГруза = ВыборкаГрузовоеМесто.Длина;
				ВалютаИнкассируемаяЦенность = ВыборкаГрузовоеМесто.Валюта;
				
				мсвСтрокиТоварныйСостав = тбзТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", ВыборкаГрузовоеМесто.Ссылка));
				
				Для Каждого ЭлементТовар Из мсвСтрокиТоварныйСостав Цикл 
					НоваяСтрока = ТоварныйСостав.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементТовар);
					НоваяСтрока.ГрузовоеМесто = ГрузовоеМесто;
					НоваяСтрока.Код = Код;
				КонецЦикла;
			Иначе	
				Если Копирование Тогда 
					ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
					Код  = "";
					ГрузМодифицирован = Истина;
				КонецЕсли;	
			КонецЕсли;
						
		Иначе	
			//Элементы.ОбщийГруз.Видимость = Ложь;
						
			Пока ВыборкаГрузовоеМесто.Следующий() Цикл
				
				НовыйГруз = ГрузовыеМеста.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйГруз, ВыборкаГрузовоеМесто);
				
				текИдентификатор = Новый УникальныйИдентификатор;
				НовыйГруз.Идентификатор = текИдентификатор;
				
				текГрузовоеМесто = ВыборкаГрузовоеМесто.Ссылка;
				Если Копирование Тогда 
					НовыйГруз.Код = "";
					ГрузМодифицирован = Истина;
				Иначе	
					НовыйГруз.ГрузовоеМесто = текГрузовоеМесто;
				КонецЕсли;	
				
				мсвСтрокиТоварныйСостав = тбзТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", текГрузовоеМесто));
				
				Для Каждого ЭлементТовар Из мсвСтрокиТоварныйСостав Цикл 
					НоваяСтрока = ТоварныйСостав.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементТовар);
					НоваяСтрока.Идентификатор = текИдентификатор;
					НоваяСтрока.ГрузовоеМесто = текГрузовоеМесто;
					НоваяСтрока.Код = НовыйГруз.Код;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаГрузовыеМеста()
	
	Возврат 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбзГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	тбзГрузовыеМеста.Тара КАК Тара,
	|	тбзГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	тбзГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	&Ссылка КАК ЗаданиеНаПеревозкуГруза,
	|	&ЗаявкаНаПеревозкугруза КАК ЗаявкаНаПеревозкуГруза,
	|	&НомерВЦепиПоставок КАК НомерВЦепиПоставок
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	&тбзГрузовыеМеста КАК тбзГрузовыеМеста
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГрузовыеМеста.Ссылка КАК Ссылка,
	|	упГрузовыеМеста.Код КАК Код,
	|	упГрузовыеМеста.БеречьОтВлаги КАК БеречьОтВлаги,
	|	упГрузовыеМеста.БеречьОтИзлучения КАК БеречьОтИзлучения,
	|	упГрузовыеМеста.БеречьОтНагрева КАК БеречьОтНагрева,
	|	упГрузовыеМеста.Валюта КАК Валюта,
	|	упГрузовыеМеста.КлассОпасности КАК КлассОпасности,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, упГрузовыеМеста.Масса)) КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, упГрузовыеМеста.Объем)) КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Высота, упГрузовыеМеста.Высота)) КАК Высота,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Длина, упГрузовыеМеста.Длина)) КАК Длина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Ширина, упГрузовыеМеста.Объем)) КАК Ширина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, упГрузовыеМеста.КоличествоЗагрузочныхМетров)) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, упГрузовыеМеста.КоличествоМест)) КАК КоличествоМест,
	|	упГрузовыеМеста.ОбщийГрузПоЗаявке КАК ОбщийГрузПоЗаявке,
	|	упГрузовыеМеста.Описание КАК Описание,
	|	упГрузовыеМеста.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	упГрузовыеМеста.Сумма КАК Сумма,
	|	упГрузовыеМеста.СуммаНДС КАК СуммаНДС,
	|	упГрузовыеМеста.ТемпературныйРежим КАК ТемпературныйРежим,
	|	упГрузовыеМеста.ТипГруза КАК ТипГруза,
	|	упГрузовыеМеста.Хрупкий КАК Хрупкий,
	|	упГрузовыеМеста.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки КАК ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	упТипыГрузов.ВидГруза КАК ВидГруза,
	|	втГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	упГрузовыеМеста.ЗапрещеноСкладироватьДругНаДруга КАК ЗапрещеноСкладироватьДругНаДруга
	|ПОМЕСТИТЬ втХарактеристикиГрузовыхМест
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК упТипыГрузов
	|		ПО упГрузовыеМеста.ТипГруза = упТипыГрузов.Ссылка
	|	ПО втГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		&ПериодФормированияДвижений,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втГрузовыеМеста.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втГрузовыеМеста КАК втГрузовыеМеста
	|			ГДЕ втГрузовыеМеста.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И втГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втГрузовыеМеста.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|			ИЗ
	|				втГрузовыеМеста КАК втГрузовыеМеста)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ЗаданиеНаПеревозкуГруза = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И втГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГрузовыеМестаТоварныйСоставГруза.Ссылка КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	упГрузовыеМестаТоварныйСоставГруза.НомерСтроки КАК НомерСтроки,
	|	упГрузовыеМестаТоварныйСоставГруза.Номенклатура КАК Номенклатура,
	|	упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры КАК УпаковкаНоменклатуры,
	|	упГрузовыеМестаТоварныйСоставГруза.Количество КАК Количество,
	|	упГрузовыеМестаТоварныйСоставГруза.Цена КАК Цена,
	|	упГрузовыеМестаТоварныйСоставГруза.СтавкаНДС КАК СтавкаНДС,
	|	упГрузовыеМестаТоварныйСоставГруза.СуммаНДС КАК СуммаНДС,
	|	упГрузовыеМестаТоварныйСоставГруза.СуммаБезНДС КАК СуммаБезНДС,
	|	упГрузовыеМестаТоварныйСоставГруза.Сумма КАК Сумма,
	|	упГрузовыеМестаТоварныйСоставГруза.АналитическийРазрез КАК АналитическийРазрез
	|ИЗ
	|	Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК упГрузовыеМестаТоварныйСоставГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втХарактеристикиГрузовыхМест КАК втГрузовыеМеста
	|	ПО упГрузовыеМестаТоварныйСоставГруза.Ссылка = втГрузовыеМеста.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданиеГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ втГрузовыеМестаПредыдущейЦепиПоставки
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМеста КАК втГрузовыеМестаТ
	|	ПО ИСТИНА
	|		И втГрузовыеМестаТ.ГрузовоеМесто = ЗаданиеГрузовыеМеста.ГрузовоеМесто
	|		И втГрузовыеМестаТ.НомерВЦепиПоставок = ЗаданиеГрузовыеМеста.Ссылка.НомерВЦепиПоставокКонец
	|		И втГрузовыеМестаТ.ЗаявкаНаПеревозкуГруза = ЗаданиеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаданий
	|	ПО СтатусыЗаданий.Документ = ЗаданиеГрузовыеМеста.Ссылка
	|ГДЕ ИСТИНА
	|	И НЕ (ЗаданиеГрузовыеМеста.Ссылка.ПометкаУдаления)
	|	И ЕстьNUll(СтатусыЗаданий.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)) <> ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Ссылка КАК Ссылка,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.Код КАК Код,
	|	втГрузовыеМеста.БеречьОтВлаги КАК БеречьОтВлаги,
	|	втГрузовыеМеста.БеречьОтИзлучения КАК БеречьОтИзлучения,
	|	втГрузовыеМеста.БеречьОтНагрева КАК БеречьОтНагрева,
	|	втГрузовыеМеста.Валюта КАК Валюта,
	|	втГрузовыеМеста.Высота КАК Высота,
	|	втГрузовыеМеста.Длина КАК Длина,
	|	втГрузовыеМеста.КлассОпасности КАК КлассОпасности,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	втГрузовыеМеста.Масса КАК Масса,
	|	втГрузовыеМеста.ОбщийГрузПоЗаявке КАК ОбщийГрузПоЗаявке,
	|	втГрузовыеМеста.Объем КАК Объем,
	|	втГрузовыеМеста.Описание КАК Описание,
	|	втГрузовыеМеста.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	втГрузовыеМеста.Сумма КАК Сумма,
	|	втГрузовыеМеста.СуммаНДС КАК СуммаНДС,
	|	втГрузовыеМеста.ТемпературныйРежим КАК ТемпературныйРежим,
	|	втГрузовыеМеста.ТипГруза КАК ТипГруза,
	|	втГрузовыеМеста.Хрупкий КАК Хрупкий,
	|	втГрузовыеМеста.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки КАК ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки,
	|	втГрузовыеМеста.Ширина КАК Ширина,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.ВидГруза КАК ВидГруза,
	|	втГрузовыеМеста.ВидГруза.Порядок КАК ИндексКартинки,
	|	втГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	втГрузовыеМеста.ЗапрещеноСкладироватьДругНаДруга КАК ЗапрещеноСкладироватьДругНаДруга,
	|	ВЫБОР
	|		КОГДА втГрузовыеМестаПредыдущейЦепиПоставки.ГрузовоеМесто ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗапрещенноРедактироватьПараметрыГрузов
	|ИЗ
	|	втХарактеристикиГрузовыхМест КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаПредыдущейЦепиПоставки КАК втГрузовыеМестаПредыдущейЦепиПоставки
	|	ПО втГрузовыеМеста.Ссылка = втГрузовыеМестаПредыдущейЦепиПоставки.ГрузовоеМесто
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";

	
КонецФункции

&НаКлиенте
Функция ПолучитьИдентификаторСтрокиГрузовогоМеста()
	текРезультат = Неопределено;
	ТекущиеДанные = Элементы.ВесьТоварныйСостав.ТекущиеДанные;
	мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", ТекущиеДанные.ГрузовоеМесто));
	Если мсвСтрокиГрузовыеМеста.Количество()>0 Тогда
		текРезультат	= мсвСтрокиГрузовыеМеста[0].ПолучитьИдентификатор();
	КонецЕсли;
 	Возврат текРезультат;
КонецФункции // ПолучитьИдентификаторСтрокиГрузовогоМеста()

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновноеСоглашение(Партнер,Организация)
	
	Результат = Справочники.упСоглашенияСКлиентами.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Соглашения.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.упСоглашенияСКлиентами КАК Соглашения
	|ГДЕ
	|	НЕ Соглашения.ПометкаУдаления
	|	И Соглашения.Владелец = &Владелец
	|	И ВЫБОР
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Соглашения.Организация = &Организация
	|		КОНЕЦ";
	Запрос.УстановитьПараметр("Владелец", Партнер);
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Соглашение;
	Иначе
		Результат = Справочники.упСоглашенияСКлиентами.СоглашениеПоУмолчанию(Партнер);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьОсновноеСоглашение()

&НаСервереБезКонтекста
Функция ПолучитьГрафикРаботы(Партнер, Адрес)
	Возврат Справочники.упПартнеры.ПолучитьГрафикРаботыТочкиПартнера(Партнер, Адрес); 
Конецфункции // ПолучитьГрафикРаботы()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия формы времени отправления.
//
Процедура ИзменитьВремяОтправленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Объект.ОтправлениеГрузаС  = Результат.НачалоПериода;
		Объект.ОтправлениеГрузаПо = Результат.КонецПериода;
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВремяОтправленияЗавершение()

&НаКлиенте
// Процедура завершения после закрытия формы времени получения.
//
Процедура ИзменитьВремяПолученияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Объект.ПолучениеГрузаС  = Результат.НачалоПериода;
		Объект.ПолучениеГрузаПо = Результат.КонецПериода;
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВремяПолученияЗавершение()

#КонецОбласти

&НаСервере
Процедура ЗаполнитьВГХГруза()
	
	сткВГХГруза = ПолучитьВГХГруза(ТипГруза); 
	ВысотаГруза = сткВГХГруза.Высота;
	ДлинаГруза = сткВГХГруза.Длина;
	ШиринаГруза = сткВГХГруза.Ширина;
	Объем = сткВГХГруза.Объем;
	Масса = сткВГХГруза.Масса;
	КоличествоМест	= сткВГХГруза.КоличествоМест;
	КоличествоЗагрузочныхМетров = сткВГХГруза.КоличествоЗагрузочныхМетров;
	ТемпературныйРежим = сткВГХГруза.ТемпературныйРежим;
	КлассОпасности = сткВГХГруза.КлассОпасности;
	Хрупкий = сткВГХГруза.Хрупкий;
	БеречьОтВлаги = сткВГХГруза.БеречьОтВлаги;
	БеречьОтИзлучения = сткВГХГруза.БеречьОтИзлучения;
	БеречьОтНагрева = сткВГХГруза.БеречьОтНагрева;
	ВидГруза = сткВГХГруза.ВидГруза;
	ЗапрещеноСкладироватьДругНаДруга = сткВГХГруза.ЗапрещеноСкладироватьДругНаДруга;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВГХГруза(ТипГруза)
	
	сткВГХГруза = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипГруза, "Масса, Высота, Длина, Ширина, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КлассОпасности, ТемпературныйРежим, Хрупкий, БеречьОтВлаги, БеречьОтИзлучения, БеречьОтНагрева, ВидГруза, ЗапрещеноСкладироватьДругНаДруга"); 
	
	Если Истина
		И сткВГХГруза.Свойство("ВидГруза") 
		И ЗначениеЗаполнено(сткВГХГруза.ВидГруза) Тогда
		
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(сткВГХГруза.ВидГруза));	
		Если сткВГХГруза.ВидГруза = Перечисления.упВидыГрузов.ИнкассируемаяЦенность Тогда
			сткВГХГруза.Вставить("КоличествоГрузов", 1);		
		КонецЕсли;
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(сткВГХГруза.ВидГруза));	
	Иначе
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(Перечисления.упВидыГрузов.Груз));	
	КонецЕсли;
	
	Возврат сткВГХГруза;
	
КонецФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммуПлановойИнкассации()
	Если ИспользуютсяУслугиИнкассации 
			И СпособУчетаИнкассируемыхЦенностей = ПредопределенноеЗначение("Перечисление.упСпособыУчетаИнкассируемыхЦенностей.СТочностьюДоГрузовыхМест") Тогда
		Объект.СуммаИнкассацииПлан	= ГрузовыеМеста.Итог("Сумма");	
	КонецЕсли;
КонецПроцедуры

#Область Взаиморасчеты

&НаКлиенте
Процедура ЗагрузитьВзаиморасчетыСПартнерами()
	ВзаиморасчетыЗагрузить = Истина;
	Если РазрешеноЗагружатьВзаиморасчеты 
		И НЕ ВзаиморасчетыЗагружаются Тогда
		ВзаиморасчетыЗагружаются 	= Истина;
		ВзаиморасчетыЗагрузить 		= Ложь;
		ВзаиморасчетыАдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов", 1);
		ЗагрузитьВзаиморасчетыСПартнерамиНаСервере();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВзаиморасчетыСПартнерамиНаСервере()
	Если РазрешеноЗагружатьВзаиморасчеты 
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Заказчик) Тогда
		
		Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Обновить;
		мсвПараметры = Новый Массив;
		мсвПараметры.Добавить(Объект.Заказчик);
		мсвПараметры.Добавить(Объект.Организация);
		мсвПараметры.Добавить(ВзаиморасчетыАдресХранилища);
		ФоновыеЗадания.Выполнить("упВзаиморасчеты.СостояниеВзаиморасчетовСКлиентом", мсвПараметры);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗагрузкуВзаиморасчетов()
	текРезультат = ПолучитьИзВременногоХранилища(ВзаиморасчетыАдресХранилища);
	Если НЕ текРезультат = Неопределено Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов");		
		ВзаиморасчетыЗагружаются = Ложь;
		Если НЕ ВзаиморасчетыЗагрузить Тогда
			Если НЕ ТипЗнч(текРезультат) = Тип("Булево") Тогда
				СформироватьТаблицуВзаиморасчетов(текРезультат);
				Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Успешно;
				ПоказатьОповещениеПользователя(НСтр("ru = 'Взаиморасчеты загружены.'"),,, БиблиотекаКартинок.Успешно32);
			Иначе
				Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Остановить;	
				ПоказатьОповещениеПользователя(НСтр("ru = 'Не удалось загрузить взаиморасчеты.'"),, НСтр("ru = 'Подробности см. в Журнале регистрации.'"), БиблиотекаКартинок.Ошибка32);
			КонецЕсли;		
		Иначе
			ЗагрузитьВзаиморасчетыСПартнерами();		
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьТаблицуВзаиморасчетов(СтрокаXML)
	
	СостояниеВзаиморасчетов.Очистить();
	
	тбзВзаиморасчеты = упВзаиморасчеты.СформироватьТаблицуВзаиморасчетовСКлиентами(СтрокаXML);
	
	текСхемаКомпоновкиДанных	= ПолучитьОбщийМакет("упВзаиморасчетыСКлиентами");
	
	текКомпоновщикМакетаКомпоновкиДанных	= Новый КомпоновщикМакетаКомпоновкиДанных;
	
	текМакетКомпоновкиДанных	= текКомпоновщикМакетаКомпоновкиДанных.Выполнить(текСхемаКомпоновкиДанных, текСхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	текПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	
	текПроцессорКомпоновкиДанных.Инициализировать(текМакетКомпоновкиДанных, Новый Структура("тбзВзаиморасчеты", тбзВзаиморасчеты), );
	
	текПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	текПроцессорВыводаРезультата.УстановитьДокумент(СостояниеВзаиморасчетов);
	
	текПроцессорВыводаРезультата.Вывести(текПроцессорКомпоновкиДанных);

		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВремяПогрузки(Команда)
	
	ПересчитатьВремяПогрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВремяРазгрузки(Команда)
	
	ПересчитатьВремяРазгрузки();
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	Если РедактированиеГрузовыхМест = Истина И ЗначениеЗаполнено(ТекущееГрузовоеМесто) Тогда
		ПеренестиЗначенияДополнительныхРеквизитовВОбъект(Истина);
	Иначе
		ПеренестиЗначенияДополнительныхРеквизитовВОбъект();
	КонецЕсли; 
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
&НаКлиенте
Процедура ТоварныйСоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ТоварныйСоставНоменклатура Тогда
		Если Ложь
			Или ТолькоПросмотр
			Или Элементы.ТоварныйСостав.ТолькоПросмотр
			Или Поле.ТолькоПросмотр
		Тогда
			ПоказатьЗначение(, ТоварныйСостав.НайтиПоИдентификатору(ВыбраннаяСтрока).Номенклатура);
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеРеквизитыПоГрузовомуМесту() 
	
	упСервисныеФункции.ОбновитьДополнительныеРеквизитыПоГрузовомуМесту(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСменеСтраницыДополнительныеРеквизиты()
	
	упСервисныеФункции.ПриСменеСтраницыДополнительныеРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиЗначенияДополнительныхРеквизитовВОбъект(ЗаписыватьОбъект = Ложь)
	
	упСервисныеФункции.ПеренестиЗначенияДополнительныхРеквизитовВОбъект(ЭтотОбъект, ЗаписыватьОбъект); 
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвВидыГрузов 	= ВозможныеВидыГрузов(Объект.ВидПеревозки);
	сткОтбор 		= Новый Структура("ВидГруза", мсвВидыГрузов);
	сткПараметры 	= Новый Структура("Отбор", сткОтбор);
 	ОткрытьФорму("Справочник.упТипыГрузов.ФормаВыбора", сткПараметры, Элемент);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВозможныеВидыГрузов(ВидПеревозки)
	
	спзРезультат = Новый СписокЗначений;
	спзРезультат.Добавить(Перечисления.упВидыГрузов.Груз);
	
	ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = Ложь;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	
	Если ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест Тогда
		спзРезультат.Добавить(Перечисления.упВидыГрузов.ИнкассируемаяЦенность);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		спзРезультат.Добавить(Перечисления.упВидыГрузов.Тара);
	КонецЕсли; 
	
	Возврат спзРезультат;
	
КонецФункции

&НаСервере 
Процедура ОпределитьЗоны(ТекущийОбъект)
	
	мсвАдреса = Новый Массив;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресОтправления) Тогда
		мсвАдреса.Добавить(ТекущийОбъект.АдресОтправления);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресПолучения) Тогда
		мсвАдреса.Добавить(ТекущийОбъект.АдресПолучения);
	КонецЕсли;
	
	Если мсвАдреса.Количество() > 0 Тогда
		ГеографическиеЗоныАдресов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвАдреса, "ГеографическаяЗона");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресОтправления) Тогда
		ЗонаОтправления = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресПолучения);
		Если ЗначениеЗаполнено(ЗонаОтправления) Тогда
			ТекущийОбъект.ЗонаОтправления = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресОтправления);	
		КонецЕсли;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресПолучения) Тогда
		ЗонаПолучения = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресПолучения);
		Если ЗначениеЗаполнено(ЗонаПолучения) Тогда
			ТекущийОбъект.ЗонаПолучения = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресПолучения);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОпределитьЗоны();

&НаКлиентеНаСервереБезКонтекста
Функция СтатусДляПроверки(СтатусЗадания)
	
	Возврат Ложь
		   Или СтатусЗадания <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое")
		   Или СтатусЗадания <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отказ")
		   Или СтатусЗадания <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено");
	
КонецФункции

#КонецОбласти
