#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ОтправлениеГрузаС  = ОбъектКопирования.ОтправлениеГрузаС;
	ОтправлениеГрузаПо = ОбъектКопирования.ОтправлениеГрузаПо;
	ПолучениеГрузаС    = ОбъектКопирования.ПолучениеГрузаС;
	ПолучениеГрузаПо   = ОбъектКопирования.ПолучениеГрузаПо;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидТранспортнойУслуги) Тогда
		упУправленияЗаявками.ЗаполнитьВидТранспортнойУслуги(ЭтотОбъект);
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
		
	ТекущийСтатус = Неопределено;
	Если ДополнительныеСвойства.Свойство("Статус", ТекущийСтатус) Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Если ТекущийСтатус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию Тогда
			Если НЕ ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза) Тогда
				ЗапрещеноОформлениеЗаданияНаПеревозкуБезЗаявки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ЗапрещеноОформлениеЗаданияНаПеревозкуБезЗаявки");
				Если ЗапрещеноОформлениеЗаданияНаПеревозкуБезЗаявки Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана заявка. В виде перевозки включена опция ""Запрещено оформление задания на перевозку без заявки""'"), ЭтотОбъект, "ЗаявкаНаПеревозкуГруза",, Отказ);	
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
	Если Истина
		И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения
		И ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ГрузМодифицирован", Ложь)
	Тогда
		упУправленияЗаявками.ЗаполнитьГрузовыеМеста(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		упУправленияЗаявками.ПроконтролироватьИспользованиеТары(ГрузовыеМеста, Отказ);
	КонецЕсли;
	
	ПроверитьСовместимостьВидовПеревозок(РежимЗаписи, ТекущийСтатус, Отказ);
			
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ПараметрыВводаИнформацииОГрузовыхМестах");
	ФормироватьПоГрузовымМестам = упУправленияЗаявками.ФормироватьПоГрузовымМестамПоПараметруВводаИнформации(ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию);
		
	Если ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза) Тогда
		ПараметрыВводаИнформацииОГрузовыхМестах = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаНаПеревозкуГруза, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах");
	Иначе
		ПараметрыВводаИнформацииОГрузовыхМестах = ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию;
	КонецЕсли; 
	
	ДополнительныеСвойства.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах", ПараметрыВводаИнформацииОГрузовыхМестах);
	ДополнительныеСвойства.Вставить("ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию", ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию);
	ДополнительныеСвойства.Вставить("ФормироватьПоГрузовымМестам", ФормироватьПоГрузовымМестам);
	
	Если Не (ЗначениеЗаполнено(ТекущийСтатус) И ТекущийСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена) Тогда
		сткВозврат = упРасчетПоказателейЗагрузки.РассчитатьИтоговыеПараметрыПоНаборуГрузов(ГрузовыеМеста.Выгрузить(, "ГрузовоеМесто, Тара, КоличествоГрузов"), ЗаявкаНаПеревозкуГруза, Ссылка);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткВозврат);
		ДополнительныеСвойства.Вставить("СтруктураИтоговыхПараметров", сткВозврат);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		упРаботаСоСтатусами.ЕстьДругиеРегистраторыИзменявшиеСтатус(Ссылка, Отказ);
	КонецЕсли;	

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		Отказ = Ложь;
		ТекстСообщения = "";
		Документы.упЗаявкаНаПеревозкуГруза.СформироватьЗаданияНаПервозкуГруза(ДанныеЗаполнения, ЭтотОбъект, Ложь, ТекстСообщения ,Отказ);
		Если Отказ Тогда
			ДополнительныеСвойства.Вставить("ОшибкаПриЗаполнении", Отказ);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	РеквизитыОбъекта = Новый Структура("Дата, ЗаявкаНаПеревозкуГруза, ГрузовыеМеста, ФормироватьПоГрузовымМестам, АдресОтправления, АдресПолучения, 
	                                   |ПараметрыВводаИнформацииОГрузовыхМестах, ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию, НомерВЦепиПоставок, 
								|НомерВЦепиПоставокКонец, Отправитель, Получатель, ГрафикРаботыАдресаОтправления, ГрафикРаботыАдресаПолучения, Рейс");
	
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ДополнительныеСвойства, "ПараметрыВводаИнформацииОГрузовыхМестах, ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию, ФормироватьПоГрузовымМестам"); 
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаявкиКВыполнению");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Заявка", ЗаявкаНаПеревозкуГруза);
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Задание", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упСостояниеПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Задание", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ЗаявкаНаПеревозкуГруза);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	Если ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПараметрыЗаявкиНаПеревозку");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозку", ЗаявкаНаПеревозкуГруза);
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПараметрыЗаданияНаПеревозку");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаданиеНаПеревозку", Ссылка);
		
	Рейс = упЗаданиеНаПеревозку.РейсЗадания(Ссылка, ДополнительныеСвойства, РеквизитыОбъекта);
	РеквизитыОбъекта.Рейс = Рейс;
	
	Если Ложь Тогда // Для контекстной подсказки
	    Рейс = Документы.упРейс.ПустаяСсылка();
    КонецЕсли;
    
	Если ЗначениеЗаполнено(Рейс) Тогда
		Рейс.ПолучитьОбъект().ДобавитьЭлементыБлокировкиДанныхПроведения(БлокировкаДанных);
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упИзмененияРейсов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		КонецЕсли; 
	КонецЕсли; 
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ДополнительныеСвойства.Вставить("КонтролироватьЗаявки", Ложь);										
	ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Ложь);										
	ДополнительныеСвойства.Вставить("КонтролироватьСостояниеЗадания", Ложь);
	ДополнительныеСвойства.Вставить("ДобавлятьДвиженияПриОтменеЗадания", Ложь);
	ДополнительныеСвойства.Вставить("СохранятьДвиженияЗаданияКВыполнению", Ложь);
	ДополнительныеСвойства.Вставить("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	ДополнительныеСвойства.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
	ДополнительныеСвойства.Вставить("ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза", РольДоступна("упСогласованиеЗаявкиНаПеревозкуГруза"));
	ДополнительныеСвойства.Вставить("ДоступнаРольОтменаЗаданийНаПеревозкуГруза", РольДоступна("упОтменаЗаданияНаПеревозкуГруза"));

	// Предварительная установка статуса										
	упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
	Если ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда
		текОтказ = Ложь;
		
		// Ситуация когда задание было включено в рейс, но по какой то проблеме не было не доставлено.
		// В этом случае некорректно очищать движения по заданию.
		//   так как это нарушает хронологическую последовательность действий.
		Если упРаботаСоСтатусами.ЕстьДругиеРегистраторыИзменявшиеСтатус(Ссылка, текОтказ, Ложь) Тогда
		    
			// Если отмененный документ повторно проводится.
			ДополнительныеСвойства.Вставить("СохранятьДвиженияЗаданияКВыполнению",	Истина); 
			
			Если ДополнительныеСвойства.ТекущийСтатус <> ДополнительныеСвойства.Статус Тогда 
		    		ДополнительныеСвойства.Вставить("ДобавлятьДвиженияПриОтменеЗадания",	Истина);	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыОбъекта.Дата = Документы.упЗаданиеНаПеревозкуГруза.ОпределитьПериодФормированияДвижений(Ссылка, РеквизитыОбъекта.ПараметрыВводаИнформацииОГрузовыхМестахПоЗаданию, ДополнительныеСвойства.Статус, ЧасовойПояс);
	
	упЗаданиеНаПеревозку.СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.СформироватьДвиженияПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.СформироватьДвиженияЗаданиеКПланированию(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.СформироватьДвиженияСтатусЗадания(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	ДополнительныеСвойства.Вставить("ПараметрыНаПеревозку", упРасчетПоказателейЗагрузки.РассчитатьТаблицуПараметровГрузовДляПроведения(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ));
	упЗаданиеНаПеревозку.СформироватьДвиженияПараметрыЗаявкиНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.СформироватьДвиженияПараметрыЗаданияНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	Движения.Записать();
	
	упУправленияЗаявками.ПроконтролироватьОграничениеПоУровнямВложенностиГрузов(Ссылка, Отказ);
	
	упЗаданиеНаПеревозку.ИнициироватьОбновлениеРаботПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если Отказ Тогда 
		// Неявная транзакция записи уже может быть сломана, поэтому при дальнейнем обращении к БД может быть ошибка "В данной транзакции уже происходили ошибки"
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза) Тогда
		ДополнительныеСвойства.Вставить("РассчитатьСтатус", Истина);
		ДополнительныеСвойства.Вставить("ЗаписыватьСтатус", Ложь);
		ДополнительныеСвойства.Вставить("Статус", упРаботаСоСтатусами.ПолучитьТекущийСтатус(РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза));
		упУправленияЗаявками.СформироватьДвиженияСтатусЗаявки(ДополнительныеСвойства, РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза, Движения, РеквизитыОбъекта, Отказ);	
	Иначе
		упЗаданиеНаПеревозку.ЗаписатьАдресаПартнеров(ДополнительныеСвойства, РеквизитыОбъекта);
	КонецЕсли;
		
	Если ДополнительныеСвойства.КонтролироватьЗаявки Тогда
		упУправленияЗаявками.ПроконтролироватьЗаявкиКВыполнению(ЗаявкаНаПеревозкуГруза, Отказ, Ссылка);
	КонецЕсли;
	
	Если ДополнительныеСвойства.КонтролироватьСостояниеЗадания Тогда
		упЗаданиеНаПеревозку.ПроконтролироватьСостояниеЗадания(Ссылка, Отказ);
	КонецЕсли;	

	упЗаданиеНаПеревозку.ПроконтролироватьЗаданияКПланированию(Ссылка, РеквизитыОбъекта.ФормироватьПоГрузовымМестам, Отказ, ДополнительныеСвойства);
		
	Если ЗначениеЗаполнено(РеквизитыОбъекта.Рейс) Тогда
		ДополнительныеСвойства.Вставить("ОбновитьМаршрутНаМК", Истина);
		упКорректировкаМаршрута.СформироватьДвиженияИзмененияРейсов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСовместимостьВидовПеревозок(РежимЗаписи, ТекущийСтатус, Отказ)
	
	Если Истина
		И Не Отказ
		И НЕ ПометкаУдаления 
		И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения
		И ТекущийСтатус <> Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено
		И ТекущийСтатус <> Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое
		И ЗначениеЗаполнено(ЗаявкаНаПеревозкуГруза)
	Тогда
		РеквизитыОбъекта = Новый Структура();
		РеквизитыОбъекта.Вставить("ОбъектПроверки", Ссылка);
		РеквизитыОбъекта.Вставить("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
		РеквизитыОбъекта.Вставить("ВидПеревозки", ВидПеревозки);
		
		Отказ = Не Справочники.упВидыПеревозок.ВидыПеревозокСовместимы(РеквизитыОбъекта); 
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РеквизитыОбъекта.ТекстСообщения,ЭтотОбъект,,,Отказ);
			ЗаписьЖурналаРегистрации(Документы.упЗаданиеНаПеревозкуГруза.СобытиеЖРПроверкаСовместимостиВидовПеревозок(),
				УровеньЖурналаРегистрации.Предупреждение,
				,
				,
				РеквизитыОбъекта.ТекстСообщения,
				РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#КонецЕсли