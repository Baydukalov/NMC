#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа
//
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаданиеНаПеревозкуГруза - Ссылка на документ
//
// Возвращаемое значение:
//   ПеречислениеСсылка   - Перечисления.упСтатусыЗаданияНаПеревозкуГруза
//
Функция ПолучитьСтатус(Ссылка, ПериодСтатуса = '00010101') Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Статус КАК Статус,
	|	упЗаданиеНаПеревозкуГруза.ПометкаУдаления КАК ПометкаУдаления,
	|	упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Период КАК Период
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(, Документ = &Документ) КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних
	|		ПО (упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Документ = упЗаданиеНаПеревозкуГруза.Ссылка)
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		ПериодСтатуса = Выборка.Период;
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено;
	Иначе	
		Возврат Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое;
	КонецЕсли;
	
КонецФункции		

// Функция возвращает дату установки указанного статуса
//
// Параметры:
//  Ссылка - ДокументСсылка.упЗаданиеНаПеревозкуГруза			 - Ссылка на документ
//  Статус - ПеречислениеСсылка.упСтатусыЗаданияНаПеревозкуГруза	 - Статус задания
// 
// Возвращаемое значение:
//  Дата - дата перевода документа в заданный статус. 
//
Функция ПолучитьДатуПереводаВСтатус(Ссылка, Статус) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат '00010101';
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних.Период
	|ИЗ
	|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|			,
	|			Документ = &Документ
	|				И Статус = &Статус) КАК упСтатусыЗаданийНаПеревозкуГрузаСрезПоследних";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Запрос.УстановитьПараметр("Статус"  , Статус);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат '00010101';
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Период;
	КонецЕсли; 

КонецФункции // ПолучитьДатуПереводаВСтатус()

// Функция формирует рейсы на основании заданий на перевозку
//
// Параметры:
//  МассивЗаданий - Массив - Массив ссылок на документы "упЗаданиеНаПеревозкуГруза".
//  ТаблицаЗаданий - ТаблицаЗначений - входящие задания:
//						* ЗаданиеНаПеревозку - ДокументСсылка.ЗаданиеНаПеревозкуГруза.
//       				* ГрузовоеМесто - СправочникСсылка.упГрузовыеМеста - реквизит.
//       				* КоличествоГрузов - Число - реквизит.
//  РеквизитыПоУмолчанию    - Структура - структура с полями заполнения шапки документа "упРейс".
//  ДополнительныеПараметры - Структура - произвольные параметры.
//  ТекстОшибки             - Строка - Текстовое представление ошибки.
//  Отказ                   - Булево - Флаг Успешности исполнения.
//
// Возвращаемое значение:
//   Массив - массив созданнных документов "упРейс".
//
Функция СформироватьРейсы(МассивЗаданий = Неопределено, ТаблицаЗаданий = Неопределено, РеквизитыПоУмолчанию = Неопределено, ДополнительныеПараметры = Неопределено, ТекстОшибки, Отказ) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().СформироватьРейсы(МассивЗаданий, ТаблицаЗаданий, РеквизитыПоУмолчанию, ДополнительныеПараметры, ТекстОшибки, Отказ);
	
КонецФункции	

// Функция - Сформировать изменение параметров заявки на перевозку грузов
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - 	 - 
//  ТекстОшибки			 - 	 - 
//  Отказ					 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция СформироватьИзменениеПараметровГрузовыхМест(ЗаданиеНаПеревозкуГруза,  ТекстОшибки = "", Отказ) Экспорт
	
	мсвРезультат = Новый Массив;
	
	ДокументОбъект = Документы.упИзменениеПараметровГрузовыхМест.СоздатьДокумент();
	ДокументОбъект.Дата = ТекущаяДатаСеанса();
	ДокументОбъект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	ДокументОбъект.Заполнить(ЗаданиеНаПеревозкуГруза);
	
	Если НЕ ДокументОбъект.ДополнительныеСвойства.Свойство("ЕстьОшибки") Тогда
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			мсвРезультат.Добавить(ДокументОбъект.Ссылка);
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упИзменениеПараметровГрузовыхМест,, ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			
		КонецПопытки;
		
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДокументОбъект.ДополнительныеСвойства, "ТекстОшибки");
	Иначе
		Отказ = Истина;
		ТекстОшибки = ДокументОбъект.ДополнительныеСвойства.ТекстОшибки;
	КонецЕсли;
	
	Возврат мсвРезультат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидПеревозки");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОтборПоАдресу") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Задание.Ссылка
		|ИЗ
		|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК тбСтатус
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК Задание
		|		ПО тбСтатус.Документ = Задание.Ссылка
		|ГДЕ
		|	(Задание.АдресОтправления = &Адрес
		|	ИЛИ Задание.АдресПолучения = &Адрес)
		|	И Задание.ВидПеревозки = &ВидПеревозки
		|	И Задание.Организация = &Организация
		|	И тбСтатус.Статус = &Статус
		|	И Задание.Номер ПОДОБНО ""%" + Сокрлп(Параметры.СтрокаПоиска) + "%""";
		Запрос.УстановитьПараметр("Адрес", Параметры.Отбор.Адрес);
		Запрос.УстановитьПараметр("ВидПеревозки", Параметры.Отбор.ВидПеревозки);
		Запрос.УстановитьПараметр("Организация", Параметры.Отбор.Организация);
		Запрос.УстановитьПараметр("Статус", Параметры.Отбор.Статус);
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка);		
		КонецЦикла;
		СтандартнаяОбработка = Ложь;	
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Статус") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Задание.Ссылка,
		|	Задание.Номер,
		|	Задание.Дата
		|ИЗ
		|	РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних КАК тбСтатус
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК Задание
		|		ПО тбСтатус.Документ = Задание.Ссылка
		|ГДЕ
		|	тбСтатус.Статус В (&Статус)
		|	И Задание.Номер ПОДОБНО &СтрокаПоиска";
		
		Если Параметры.Отбор.Свойство("РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава") Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И НЕ Задание.ВидПеревозки.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава";
		КонецЕсли;
		
		Если Параметры.Отбор.Свойство("АдресОтправления") Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И Задание.АдресОтправления = &АдресОтправления";
			Запрос.УстановитьПараметр("АдресОтправления", Параметры.Отбор.АдресОтправления);
		КонецЕсли;
		
		Если Параметры.Отбор.Свойство("АдресПолучения") Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И Задание.АдресПолучения = &АдресПолучения";
			Запрос.УстановитьПараметр("АдресПолучения", Параметры.Отбор.АдресПолучения);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СтрокаПоиска", "%" + Сокрлп(Параметры.СтрокаПоиска) + "%");
		Запрос.УстановитьПараметр("Статус", Параметры.Отбор.Статус);
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			СтрокаНомер = СтрШаблон("%1 (%2)", Выборка.Номер, Выборка.Дата);
			ДанныеВыбора.Добавить(Выборка.Ссылка, упСервисныеФункцииКлиентСервер.ВыделитьЧастьСтроки(СтрокаНомер, Параметры.СтрокаПоиска));		
		КонецЦикла;
		СтандартнаяОбработка = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Товарно-транспортная накладная
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ТТН";
  КомандаПечати.Представление = НСтр("ru = 'Товарно-транспортная накладная'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // Транспортная накладная
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ТН";
  КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // Счет-фактура
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "СчетФактура";
  КомандаПечати.Представление = НСтр("ru = 'Счет-Фактура'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // ТОРГ-12
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ТОРГ12";
  КомандаПечати.Представление = НСтр("ru = 'ТОРГ-12'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // ТТН_Др
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ТТН_Др";
  КомандаПечати.Представление = НСтр("ru = 'ТТН (CMR)'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // Этикетка
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "Этикетки";
  КомандаПечати.Представление = НСтр("ru = 'Этикетки (ТТН)'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // Настраиваемый комплект документов
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Порядок = 75;
  КомандаПечати.Идентификатор = "ТТН,СчетФактура,ТОРГ12,ТТН_Др,Этикетки";
  КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // ЭКСПЕДИТОРСКАЯ РАСПИСКА
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ЭкспедиторскаяРасписка";
  КомандаПечати.Представление = НСтр("ru = 'Экспедиторская Расписка'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // Поручение экспедитору
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ПоручениеЭкспедитору";
  КомандаПечати.Представление = НСтр("ru = 'Поручение Экспедитору'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // МХ-1
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "МХ_1";
  КомандаПечати.Представление = НСтр("ru = 'МХ-1'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // М-15
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "М_15";
  КомандаПечати.Представление = НСтр("ru = 'М-15'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  // Универсальный передаточный документ
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  КомандаПечати.Идентификатор = "ПФ_MXL_УниверсальныйПередаточныйДокумент";
  КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ(УПД)'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ТТН",
		НСтр("ru = 'Товарно-транспортная накладная'"),
		ПечатьТТН(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактура") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СчетФактура",
		НСтр("ru = 'Счет-Фактура'"),
		ПечатьСчетФактуры(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_СчетФактура");
	КонецЕсли;	

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ТОРГ12",
		НСтр("ru = 'ТОРГ-12'"),
		ПечатьТОРГ12(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТОРГ12");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН_Др") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ТТН_Др", 
		"ТТН (CMR)", 
		СформироватьПечатнуюФорму_ТТН_Измен(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН_ДОП");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Этикетки") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"Этикетки", 
		"Этикетки (ТТН)", 
		СформироватьПечатнуюФорму_Этикеток(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Этикетка");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭкспедиторскаяРасписка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ЭкспедиторскаяРасписка", 
		"Экспедиторская Расписка", 
		СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Экспедиторская_Расписка");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПоручениеЭкспедитору") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ПоручениеЭкспедитору", 
		"Поручение Экспедитору",
		СформироватьПечатнуюФорму_ПоручениеЭкспедитору(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Поручение_Экспедитору");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"МХ_1", 
		"МХ-1",
		СформироватьПечатнуюФорму_МХ_1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_МХ_1");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М_15") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"М_15", 
		"М-15",
		СформироватьПечатнуюФорму_М_15(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_М_15");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_УниверсальныйПередаточныйДокумент") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ПФ_MXL_УниверсальныйПередаточныйДокумент", 
		"Универсальный передаточный документ",
		ПечатьУПД(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_УниверсальныйПередаточныйДокумент");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТН") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ТН",
		НСтр("ru = 'Транспортная накладная'"),            
		ПечатьТН(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упРейс.ПФ_MXL_ТранспортнаяНакладная");
	КонецЕсли;

КонецПроцедуры

// Формирование печатной формы ТН
Функция ПечатьТН(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ТН";
	
	ДанныеДляПечати = ПолучитьДанныеДляПФ_ТН(МассивОбъектов);
	ЗаполнитьТабличныйДокументТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	
	Возврат ТабличныйДокумент;
КонецФункции

// Процедура - Заполнить табличный документ ТН
//
// Параметры:
//  ТабличныйДокумент	 - 	 - 
//  ДанныеДляПечати		 - 	 - 
//  ОбъектыПечати		 - 	 - 
//
Процедура ЗаполнитьТабличныйДокументТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати) Экспорт
	
	Автор = ПользователиКлиентСервер.ТекущийПользователь();
	Должность = "";
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСотрудники.ТипСотрудника КАК Должность,
	|	упСотрудники.Ссылка КАК Сотрудник
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудники
	|ГДЕ
	|	упСотрудники.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Автор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Должность = Выборка.Должность;
	КонецЦикла;

	тбПараметрыРейса = ДанныеДляПечати.ПараметрыРейса;
	тбПутевыеЛисты = ДанныеДляПечати.ПутевыеЛисты;
	тбПараметрыЗадания = ДанныеДляПечати.ПараметрыЗадания;
	тбПогрузкаРазгрузка = ДанныеДляПечати.ПогрузкаРазгрузка;
	дрвГрузовыеМестаПоРейсам = ДанныеДляПечати.ГрузовыеМестаПоРейсам;
	тбГрузовыеМестаПоЗаданиям = ДанныеДляПечати.ГрузовыеМестаПоЗаданиям;
	ТаблицаУпаковкиПоЗаданиям = ДанныеДляПечати.УпаковкиПоЗаданиям;
	

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упРейс.ПФ_MXL_ТранспортнаяНакладная");
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки();
			
	// Обход всех рейсов по заданиям, если рейса нет, то ссылка на него пустая.
	Для каждого СтрокаРейс Из дрвГрузовыеМестаПоРейсам.Строки Цикл
		
		ОбластьГоризонтальнаяЛицеваяСторона = Макет.ПолучитьОбласть("ГоризонтальнаяЛицеваяСторона"); 	
		ОбластьГоризонтальнаяОборотнаяСторона = Макет.ПолучитьОбласть("ГоризонтальнаяОборотнаяСторона");	
		Эталон = ПолучитьОбщийМакет("упЭталон");
		КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
		Рейс = СтрокаРейс.Рейс;
		мсвПараметрыТС = Новый Массив;
		ПеревозчикФизЛицо = Неопределено;
		ПеревозчикЮрЛицо = Неопределено;
		Водитель = Неопределено;
		ПредставлениеВодителя = "";
		Водитель2 = Неопределено;
		ПредставлениеВодителя2 = "";
		
		ПараметрыТС = "";
		НомераПутевыхЛистов = "";
		РегистрационныйНомерТС = "";
		ТипТС = "";
		Модель = "";
		ТемпературныйРежим = "";
		
		ПлановоеНачалоВыполнения = "";
		ПлановоеОкончаниеВыполнения = "";
		
		Если ЗначениеЗаполнено(Рейс) Тогда
												
			мсвСтрокиПараметровРейса = тбПараметрыРейса.НайтиСтроки(Новый Структура("Рейс",Рейс));
			Если мсвСтрокиПараметровРейса.Количество() > 0 Тогда
				
				СтрокаПараметрыРейса = мсвСтрокиПараметровРейса[0];
				
				ПлановоеНачалоВыполнения = Формат(СтрокаПараметрыРейса.ПлановоеНачалоВыполнения,"ДФ='dd.MM.yyyy hh:mm'");
				
				ПеревозчикФизЛицо = СтрокаПараметрыРейса.ПеревозчикФизЛицо;
				ПеревозчикЮрЛицо = СтрокаПараметрыРейса.ПеревозчикЮрЛицо;
				Водитель = СтрокаПараметрыРейса.Водитель;
				Если ЗначениеЗаполнено(Водитель) Тогда
					Мобильный = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Водитель, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица); 
					ПредставлениеВодителя = Строка(Водитель) + " " + Мобильный;
				КонецЕсли;
				
				Водитель2 = СтрокаПараметрыРейса.Водитель2;
				Если ЗначениеЗаполнено(Водитель2) Тогда
					Мобильный = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Водитель2, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица); 
					ПредставлениеВодителя2 = Строка(Водитель2) + " " + Мобильный;
				КонецЕсли;
				
				РегистрационныйНомерТС = СтрокаПараметрыРейса.РегистрационныйНомерТС;
											
				// Параметры ТС
				Если ЗначениеЗаполнено(СтрокаПараметрыРейса.НаименованиеТС) Тогда
					
					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.ШиринаТС) 
						И ЗначениеЗаполнено(СтрокаПараметрыРейса.ДлинаТС) 
						И  ЗначениеЗаполнено(СтрокаПараметрыРейса.ВысотаТС) Тогда
						мсвПараметрыТС.Добавить("ВГХ " + СТРОКА(СтрокаПараметрыРейса.ШиринаТС) + "х"+СТРОКА(СтрокаПараметрыРейса.ДлинаТС)+"х"+СТРОКА(СтрокаПараметрыРейса.ВысотаТС));
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.ГрузоподъемностьТС) Тогда
						мсвПараметрыТС.Добавить("грузоподъемность " + СТРОКА(СтрокаПараметрыРейса.ГрузоподъемностьТС) + сткПредставления.Масса);
					КонецЕсли;

					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.ОбъемТС) Тогда
						мсвПараметрыТС.Добавить("объем " + СТРОКА(СтрокаПараметрыРейса.ОбъемТС) + сткПредставления.Объем);
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.КоличествоМестТС) Тогда
						мсвПараметрыТС.Добавить("кол-во мест " + СТРОКА(СтрокаПараметрыРейса.КоличествоМестТС) + сткПредставления.КоличествоМест);
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.КоличествоЗагрузочныхМетровТС) Тогда
						мсвПараметрыТС.Добавить("кол-во загр. метров " + СТРОКА(СтрокаПараметрыРейса.КоличествоЗагрузочныхМетровТС));
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.ТипТС) Тогда
						ТипТС = СТРОКА(СтрокаПараметрыРейса.ТипТС);
						мсвПараметрыТС.Добавить("тип: " + ТипТС);
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаПараметрыРейса.Модель) Тогда
						Модель = СТРОКА(СтрокаПараметрыРейса.Модель);
						мсвПараметрыТС.Добавить("марка: " + Модель);						
					КонецЕсли;
					
					ПараметрыТС = "" + СтрокаПараметрыРейса.НаименованиеТС + "(" + СтрСоединить(мсвПараметрыТС, ", ") + ")";
				КонецЕсли;
												
			КонецЕсли;
			
			мсвСтрокиПутевыхЛистов = тбПутевыеЛисты.НайтиСтроки(Новый Структура("Рейс", Рейс));
			Если мсвСтрокиПутевыхЛистов.Количество() > 0 Тогда
				НомераПутевыхЛистов = "";
				Для каждого строкаНомер Из мсвСтрокиПутевыхЛистов Цикл
					НомераПутевыхЛистов = НомераПутевыхЛистов + строкаНомер.Номер + ", ";
				КонецЦикла;
				НомераПутевыхЛистов = Лев(НомераПутевыхЛистов, СтрДлина(НомераПутевыхЛистов)-2);
			КонецЕсли;
			
		КонецЕсли;
						
		Для каждого СтрокаЗадание Из СтрокаРейс.Строки Цикл
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;			
			ЗаданиеНаПеревозкуГруза = СтрокаЗадание.ЗаданиеНаПеревозкуГруза;
					
			ГрузоотправительФизЛицо = Неопределено; 
			ГрузоотправительЮрЛицо = Неопределено;
			ДатаЗаявки = Неопределено;
			НомерЗаявки = Неопределено;
			ГрузополучательФизЛицо = Неопределено;
			ГрузополучательЮрЛицо = Неопределено;
			АдресОтправления = Неопределено;
			АдресПолучения = Неопределено;
			
						
			мсвСтрокиПараметровЗаданий = тбПараметрыЗадания.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозкуГруза", ЗаданиеНаПеревозкуГруза));
			Если мсвСтрокиПараметровЗаданий.Количество() > 0 Тогда
				СтрокаПараметрыЗадания = мсвСтрокиПараметровЗаданий[0];
				ГрузоотправительФизЛицо = СтрокаПараметрыЗадания.ГрузоотправительФизЛицо;
				ГрузоотправительЮрЛицо = СтрокаПараметрыЗадания.ГрузоотправительЮрЛицо;
				ДатаЗаявки = Формат(СтрокаПараметрыЗадания.ДатаЗаявки,"ДЛФ=D");
				НомерЗаявки = СтрокаПараметрыЗадания.НомерЗаявки;
				ГрузополучательФизЛицо = СтрокаПараметрыЗадания.ГрузополучательФизЛицо;
				ГрузополучательЮрЛицо = СтрокаПараметрыЗадания.ГрузополучательЮрЛицо;
				АдресОтправления = СтрокаПараметрыЗадания.АдресОтправления;
				АдресПолучения = СтрокаПараметрыЗадания.АдресПолучения;
			КонецЕсли;	
			
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт0_1 = "";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт0_2 = ДатаЗаявки;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт0_3 = НомерЗаявки;
		
			// Раздел 1. Грузоотправитель.
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт1_1 = КонтактныеДанныеКонтрагентаБанкСчет(ГрузоотправительФизЛицо);
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт1_2 = КонтактныеДанныеКонтрагентаБанкСчет(ГрузоотправительЮрЛицо);
			
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Экспедитор = ПредставлениеВодителя;
			
			// Раздел 2. Грузополучатель.
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт2_1 = КонтактныеДанныеКонтрагентаБанкСчет(ГрузополучательФизЛицо);
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт2_2 = КонтактныеДанныеКонтрагентаБанкСчет(ГрузополучательЮрЛицо);
			
			// Раздел 3. Наименование груза.
			КоличествоГрузов = 0;
			мсвНаименованияГрузовыхМест = Новый Массив;
			Масса = 0;
			КоличествоМест = 0;
			Объем = 0;
			КоличествоЗагрузочныхМетров = 0;
			Если СтрокаЗадание.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится
					ИЛИ СтрокаЗадание.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
	
					мсвСтрокиГрузовыхМест = тбГрузовыеМестаПоЗаданиям.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозкуГруза", ЗаданиеНаПеревозкуГруза));
					
					Для каждого СтрокаГрузовоеМесто Из мсвСтрокиГрузовыхМест Цикл
						//мсвНаименованияГрузовыхМест.Добавить(Строка(СтрокаГрузовоеМесто.ГрузовоеМесто));
						ТоварныйСоставГруза = СтрокаГрузовоеМесто.ГрузовоеМесто.ТоварныйСоставГруза;
						Для каждого СтрокаТоварногоСостава Из ТоварныйСоставГруза Цикл
							мсвНаименованияГрузовыхМест.Добавить(Строка(СтрокаТоварногоСостава.Номенклатура));
						КонецЦикла;
						КоличествоГрузов = КоличествоГрузов + СтрокаГрузовоеМесто.КоличествоГрузов;
						Масса = Масса + СтрокаГрузовоеМесто.Масса;
						КоличествоМест = КоличествоМест + СтрокаГрузовоеМесто.КоличествоМест;
						Объем = Объем + СтрокаГрузовоеМесто.Объем;
						КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров + СтрокаГрузовоеМесто.КоличествоЗагрузочныхМетров;
						
						Если ПустаяСтрока(ТемпературныйРежим) Тогда
							ТемпературныйРежим = Строка(СтрокаГрузовоеМесто.ТемпературныйРежим);
						КонецЕсли;
					КонецЦикла;
			Иначе		
				
				Для каждого СтрокаГрузовоеМесто Из СтрокаЗадание.Строки Цикл
					//мсвНаименованияГрузовыхМест.Добавить(Строка(СтрокаГрузовоеМесто.ГрузовоеМесто));
					ТоварныйСоставГруза = СтрокаГрузовоеМесто.ГрузовоеМесто.ТоварныйСоставГруза;
					Для каждого СтрокаТоварногоСостава Из ТоварныйСоставГруза Цикл
						мсвНаименованияГрузовыхМест.Добавить(Строка(СтрокаТоварногоСостава.Номенклатура));
					КонецЦикла;
					КоличествоГрузов = КоличествоГрузов + СтрокаГрузовоеМесто.КоличествоГрузов;
					Масса = Масса + СтрокаГрузовоеМесто.Масса;
					КоличествоМест = КоличествоМест + СтрокаГрузовоеМесто.КоличествоМест;
					Объем = Объем + СтрокаГрузовоеМесто.Объем;
					КоличествоЗагрузочныхМетров = КоличествоЗагрузочныхМетров + СтрокаГрузовоеМесто.КоличествоЗагрузочныхМетров;
					Если ПустаяСтрока(ТемпературныйРежим) Тогда
						ТемпературныйРежим = Строка(СтрокаГрузовоеМесто.ТемпературныйРежим);
					КонецЕсли;
				КонецЦикла;
								
			КонецЕсли;
			
			КоличествоУпаковок= 0;
			МассивСтрокУпаковокПозаданиям = ТаблицаУпаковкиПоЗаданиям.НайтиСтроки(Новый Структура("ЗаданиеНаПеревозкуГруза", ЗаданиеНаПеревозкуГруза));
			Для каждого СтрокаУпаковки Из МассивСтрокУпаковокПозаданиям Цикл
				КоличествоУпаковок = КоличествоУпаковок+СтрокаУпаковки.Количество;
			КонецЦикла;
			
			
			// Параметры груза
			мсвПараметрыГруза = Новый Массив;
			
			//Если НЕ Масса = 0 Тогда
				мсвПараметрыГруза.Добавить("масса " + Формат(Масса,"ЧРД=.; ЧН=0; ЧГ=0") + сткПредставления.Масса);
			//КонецЕсли;
			
			//Если ЗначениеЗаполнено(СтрокаПараметрыРейса.ШиринаТС) 
			//		И ЗначениеЗаполнено(СтрокаПараметрыРейса.ДлинаТС) 
			//		И  ЗначениеЗаполнено(СтрокаПараметрыРейса.ВысотаТС) Тогда
			//	мсвПараметрыГруза.Добавить("ВГХ " + СТРОКА(СтрокаПараметрыРейса.ШиринаТС) + "х"+СТРОКА(СтрокаПараметрыРейса.ДлинаТС)+"х"+СТРОКА(СтрокаПараметрыРейса.ВысотаТС));
			//КонецЕсли;
			
			//Если НЕ Объем = 0 Тогда
				мсвПараметрыГруза.Добавить("объем " + Формат(Объем,"ЧРД=.; ЧН=0; ЧГ=0") + сткПредставления.Объем);
			//КонецЕсли;
			ПараметрыГруза = СтрСоединить(мсвПараметрыГруза, ", ");
			
			
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт3_1 = СтрСоединить(мсвНаименованияГрузовыхМест,", ");
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт3_2 = Строка(КоличествоГрузов);
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт3_2 = СтрШаблон("Всего мест: %1 упак", Формат(КоличествоУпаковок, "ЧРД=.; ЧН=0; ЧГ=0"));
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт3_3 = ПараметрыГруза;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт3_4 = "-";
			
			// Раздел 4. Сопроводительные документы на груз.
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт4_1 = "-";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт4_2 = "Товарно-сопроводительный документ к товару";
			
			// Раздел 5. Указания грузоотправителя.
			//ПараметрыДляПодбораТС = "";
			//мсвПараметрыПодбораТС = Новый Массив;
			//Если НЕ Масса = 0 Тогда
			//	мсвПараметрыПодбораТС.Добавить("масса " + СТРОКА(Масса) + сткПредставления.Масса);
			//КонецЕсли;
			//Если НЕ Объем = 0 Тогда
			//	мсвПараметрыПодбораТС.Добавить("объем " + СТРОКА(Объем) + сткПредставления.Объем);
			//КонецЕсли;
			//Если НЕ КоличествоМест = 0 Тогда
			//	мсвПараметрыПодбораТС.Добавить("кол-во мест " + СТРОКА(КоличествоМест) + сткПредставления.КоличествоМест);
			//КонецЕсли;
			//Если НЕ КоличествоЗагрузочныхМетров = 0 Тогда
			//	мсвПараметрыПодбораТС.Добавить("кол-во загр. метров " + СТРОКА(КоличествоЗагрузочныхМетров));
			//КонецЕсли;
			//Если ЗначениеЗаполнено(СтрокаПараметрыРейса.ТипТС) Тогда
			//	мсвПараметрыПодбораТС.Добавить("тип: " + СТРОКА(СтрокаПараметрыРейса.ТипТС));
			//КонецЕсли;
			//Если ЗначениеЗаполнено(СтрокаПараметрыРейса.Модель) Тогда
			//	мсвПараметрыПодбораТС.Добавить("марка: " + СТРОКА(СтрокаПараметрыРейса.Модель));
			//КонецЕсли;
			
			//ПараметрыДляПодбораТС		= СтрСоединить(мсвПараметрыПодбораТС, ", ");
			Пункт5_1 = СтрШаблон("Грузовой автомобиль марки %1, грузоподъемностью %2",Модель,ТипТС);
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт5_1 = ПараметрыДляПодбораТС;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт5_1 = Пункт5_1;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт5_2 = "-";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт5_3 = СтрШаблон("Перевозить при температуре %1", ТемпературныйРежим);
			
			мсвСтрокиПогрузкиРазгрузки = тбПогрузкаРазгрузка.НайтиСтроки(Новый Структура("Рейс, ЗаданиеНаПеревозкуГруза", Рейс, ЗаданиеНаПеревозкуГруза));
			ПлановоеНачалоПогрузки = ПлановоеНачалоВыполнения;
			ПлановоеНачалоРазгрузки = "";
			Если мсвСтрокиПогрузкиРазгрузки.Количество() > 0 Тогда
				СтрокаПогрузкиРазгрузки = мсвСтрокиПогрузкиРазгрузки[0];
				//Если НЕ СтрокаПогрузкиРазгрузки.НачалоПогрузки = '39991231' Тогда
				//	ПлановоеНачалоПогрузки = СтрокаПогрузкиРазгрузки.НачалоПогрузки;
				//КонецЕсли;
				
				Если НЕ СтрокаПогрузкиРазгрузки.НачалоРазгрузки = '39991231' Тогда
					ПлановоеНачалоРазгрузки = СтрокаПогрузкиРазгрузки.НачалоРазгрузки;
				КонецЕсли;
			
			КонецЕсли;
			
			ПредставлениеМасса = Формат(Масса,"ЧРД=.; ЧРГ=' '; ЧГ=0") + " " + сткПредставления.Масса;
			//ПредставлениеКоличествоМест = Формат(КоличествоМест,"ЧРД=.; ЧРГ=' '; ЧГ=0") + " " + сткПредставления.КоличествоМест;
			
			// Масса брутто: Задание на перевозку.Масса Всего мест: Задание на перевозку.Груз.ТМЦ.Товарный состав: по каждой строке Количество(ед.измерения.шт.)/Коэф.Ед.измерения.НаименованиеУп(778)
			
			// Раздел 6. Прием груза.
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_1 = АдресОтправления;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_2 = ПлановоеНачалоПогрузки;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_3 = "";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_4 = "";
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_5 = ПредставлениеМасса;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_5 = СтрШаблон("Масса брутто: %1",ПредставлениеМасса);
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_51 = ПредставлениеКоличествоМест; 
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_51 = СтрШаблон("Всего мест:%1",Формат(КоличествоУпаковок, "ЧРД=.; ЧН=0; ЧГ=0"));
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_6 = "";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт6_7 = "";
			
			// Раздел 7. Сдача груза.
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_1 = АдресПолучения;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_2 = ПлановоеНачалоРазгрузки;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_3 = "";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_4 = "";
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_5 = ПредставлениеМасса;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_5 = СтрШаблон("Масса брутто: %1",ПредставлениеМасса);
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_51 = ПредставлениеКоличествоМест;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_51 = СтрШаблон("Всего мест:%1",Формат(КоличествоУпаковок, "ЧРД=.; ЧН=0; ЧГ=0"));
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_6 = "";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт7_7 = "";
			
			// Раздел 8. Условия перевозки.
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт8_1 = "-";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт8_2 = "-";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт8_3 = "-";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт8_4 = "-";
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт8_5 = ПредставлениеМасса;
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт8_5 = СтрШаблон("Масса брутто: %1",ПредставлениеМасса);
			
			// Раздел 9. Информация о принятии заказа к исполнению.
			//ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт9_1 = "";
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт9_1 = СтрШаблон("%1 /%2/ %3",ДатаЗаявки,Строка(Автор),Строка(Должность));
			ОбластьГоризонтальнаяЛицеваяСторона.Параметры.Пункт9_2 = "";
			
			// Раздел 10. Перевозчик.
			//Перевозчик = ?(ПеревозчикФизЛицо=Неопределено,ПеревозчикЮрЛицо,ПеревозчикФизЛицо);
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_1 = ПеревозчикПредставление(ПеревозчикФизЛицо);
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_2 = ПеревозчикПредставление(ПеревозчикЮрЛицо);
			//ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_2 = "";
			//ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_3 = ПеревозчикПредставление(Водитель);
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_3 = ПредставлениеВодителя;
			
			//ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_4 = НомераПутевыхЛистов;
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_4 = "";
			//ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_5 = ПеревозчикПредставление(Водитель);
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт10_5 = ПредставлениеВодителя + " " + ПредставлениеВодителя2;
			
			// Раздел 11. Транспортное средство.
			//ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт11_1 = ПараметрыТС;
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт11_1 = Пункт5_1;
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт11_2 = "Госномер " + РегистрационныйНомерТС;
			
			// Раздел 12. Оговорки и замечания перевозчика.
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт12_1 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт12_2 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт12_3 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт12_4 = "-";
			
			// Раздел 13. Прочие условия.
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт13_1 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт13_2 = "-";
			
			// Раздел 14. Переадресовка.
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт14_1 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт14_2 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт14_3 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт14_4 = "-";
			
			// Раздел 15. Стоимость услуг перевозчика и порядок расчета провозной платы.
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт15_1 = "Цена в соответствии с договором перевозки";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт15_2 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт15_3 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт15_4 = "-";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт15_5 = "-";
			
			// Раздел 16. Дата составления, подписи сторон.
			//ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт16_1 	= "";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт16_1 	= СтрШаблон("%1 /%2/ %3",Строка(Должность),Строка(Автор),ДатаЗаявки);
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт16_11 	= "";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт16_2 	= "";
			ОбластьГоризонтальнаяОборотнаяСторона.Параметры.Пункт16_21 	= "";
			
			// Раздел 17. Отметки грузоотправителей, грузополучателей и перевозчиков.
	        			
			// Штрихкод
			Рисунок = ОбластьГоризонтальнаяЛицеваяСторона.Рисунки.BarCode;
			СтруктураШтрихкода = Новый Структура;
			СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
			СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
			СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКПечатнойФормыДляПечати(Рейс,"Документ.упРейс.ПФ_MXL_ТранспортнаяНакладная"));
			СтруктураШтрихкода.Вставить("ТипКода", 4);
			СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
			СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
			Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
			
			ТабличныйДокумент.Вывести(ОбластьГоризонтальнаяЛицеваяСторона);
			ТабличныйДокумент.Вывести(ОбластьГоризонтальнаяОборотнаяСторона);
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ЗаданиеНаПеревозкуГруза);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	  	
		КонецЦикла;
		
	КонецЦикла;
				
КонецПроцедуры

// Формирование печатной формы ТТН
Функция ПечатьТТН(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ТТН";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументТТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументТТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка  = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьЛист2 = Макет.ПолучитьОбласть("Лист2");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);
		ОбластьЗаголовок.Параметры.Заполнить(текСтрокаШапка);
		
		// Штрихкод
		Рисунок = ОбластьЗаголовок.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.ДокументОснование));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка =  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		ОбластьЛист2.Параметры.Заполнить(текСтрокаШапка);
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьШапка);
	    				
		сткОтбор  = Новый Структура("Документ", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбДанныеСтроки.НайтиСтроки(сткОтбор);
				
	   	ИтогоСуммаСНДС = 0;
		
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтроки.Параметры.Заполнить(текЭлементСтрока);
			ОбластьСтроки.Параметры.СтрокаНомер = СтрокаНомер;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтроки);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(ОбластьПодвал);
			КонецЕсли;
			Попытка
				Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
					ТабличныйДокумент.Вывести(ОбластьСтроки);
				Иначе
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабличныйДокумент.Вывести(ОбластьШапка);
					ТабличныйДокумент.Вывести(ОбластьСтроки);
				КонецЕсли;
			Исключение
				ТабличныйДокумент.Вывести(ОбластьСтроки);
			КонецПопытки;
			ИтогоСуммаСНДС = ИтогоСуммаСНДС + текЭлементСтрока.СуммаСНДС;
			
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;
		
		текЧислоСтрок = мсвСтроки.Количество();
				
		ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(текЧислоСтрок, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,ж,,,,,0");
		ОбластьПодвал.Параметры.КоличествоНаименований = ЧислоПрописью(текСтрокаШапка.КоличествоНаименований, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		
		Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
		ОбластьПодвал.Параметры.СуммаПрописьюРуб = РаботаСКурсамиВалют.СформироватьСуммуПрописью(Цел(ИтогоСуммаСНДС), Валюта, Истина);
		ОбластьПодвал.Параметры.СуммаКоп = Формат((ИтогоСуммаСНДС - Цел(ИтогоСуммаСНДС)) * 100, "ЧЦ=2; ЧВН=");
		ОбластьПодвал.Параметры.ИтогоСуммаСНДС = ИтогоСуммаСНДС;
			
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапка);
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		КонецПопытки;
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьЛист2.Параметры.Заполнить(текСтрокаШапка);

		ТабличныйДокумент.Вывести(ОбластьЛист2);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.ДокументОснование);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
				
КонецПроцедуры

// Формирование печатной формы Счет-Фактуры
Функция ПечатьСчетФактуры(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_СчетФактура";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов);
	ЗаполнитьТабличныйДокументСчетФактура(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);

  Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументСчетФактура(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
		
	тбДанныеШапка = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
	
	Если НЕ тбДанныеСтроки.Количество() Тогда
		Возврат;
	КонецЕсли;	
	
	текШаблон = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_СчетФактура");
	
	ОбластьШапка = текШаблон.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок = текШаблон.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока = текШаблон.ПолучитьОбласть("Строка");
	ОбластьИтого = текШаблон.ПолучитьОбласть("Итого");
	ОбластьПодвал = текШаблон.ПолучитьОбласть("Подвал");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		тдНовый = Новый ТабличныйДокумент;
		тдНовый.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СЧЕТ_ФАКТУРА";
				
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);
		ОбластьШапка.Параметры.ИННОрганизация = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),текСтрокаШапка.Организация.ИНН + " / " +текСтрокаШапка.Организация.КПП,"");
		ОбластьШапка.Параметры.ИННКонтрагент = ?(ЗначениеЗаполнено(текСтрокаШапка.КонтрагентЗаказчика),текСтрокаШапка.КонтрагентЗаказчика.ИНН + " / " +текСтрокаШапка.КонтрагентЗаказчика.КПП,"");		
		
		ОбластьШапка.Параметры.Номер = "СЧЕТ-ФАКТУРА №" + текСтрокаШапка.Номер + " от " + Формат(текСтрокаШапка.Дата,"ДЛФ=DD");
		ОбластьШапка.Параметры.Комментарий = Строка(текСтрокаШапка.ДокументОснование.Комментарий);
		Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
		Если ЗначениеЗаполнено(Валюта) Тогда
			ОбластьШапка.Параметры.Валюта = Валюта.НаименованиеПолное;	
		КонецЕсли;
		ОбластьШапка.Параметры.ПредставлениеОтправительАдрес = текСтрокаШапка.ПредставлениеОтправитель + ?(ПустаяСтрока(текСтрокаШапка.АдресОтгрузки), "", ", " + Сокрлп(текСтрокаШапка.АдресОтгрузки));
		ОбластьШапка.Параметры.ПредставлениеГрузополучателяАдрес = текСтрокаШапка.ПредставлениеПолучатель + ?(ПустаяСтрока(текСтрокаШапка.АдресПолучения), "", ", " + Сокрлп(текСтрокаШапка.АдресПолучения));
		
		// Штрихкод
		Рисунок = ОбластьШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.ДокументОснование));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка =  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		тдНовый.Вывести(ОбластьШапка);
		тдНовый.Вывести(ОбластьЗаголовок);
		
		сткОтбор = Новый Структура("Документ", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбДанныеСтроки.НайтиСтроки(сткОтбор);
		
		стУпаковки = Новый Соответствие;
		ИтогоСуммаНДС = 0;
		ИтогоСуммаСНДС = 0;
		ИтогоСуммаБезНДС = 0;
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтрока.Параметры.Заполнить(текЭлементСтрока);
			Если ЗначениеЗаполнено(текЭлементСтрока.СтавкаНДС) Тогда
				ОбластьСтрока.Параметры.СтавкаНДС = Строка(текЭлементСтрока.СтавкаНДС) + "%";
			КонецЕсли;
						
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтрока);
			СтрокаСПодвалом.Добавить(ОбластьИтого);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(СтрокаСПодвалом);
			КонецЕсли;
			Попытка
				Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
					тдНовый.Вывести(ОбластьСтрока);
				Иначе
					тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					тдНовый.Вывести(ОбластьЗаголовок);
					тдНовый.Вывести(ОбластьСтрока);
				КонецЕсли;
			Исключение
				тдНовый.Вывести(ОбластьСтрока);
			КонецПопытки;
			
			ИтогоСуммаНДС  = ИтогоСуммаНДС + текЭлементСтрока.СуммаНДС;
			ИтогоСуммаСНДС = ИтогоСуммаСНДС + текЭлементСтрока.СуммаСНДС;
			ИтогоСуммаБезНДС = ИтогоСуммаБезНДС + текЭлементСтрока.СуммаБезНДС;
			
			КоличествоУпаковок = стУпаковки.Получить(текЭлементСтрока.ЕдиницаИзмерения);
			стУпаковки.Вставить(текЭлементСтрока.ЕдиницаИзмерения, ?(ЗначениеЗаполнено(КоличествоУпаковок), КоличествоУпаковок, 0) + текЭлементСтрока.Количество);
		КонецЦикла;
		
		ИтогоКоличество = "";
		Для каждого текСтрока Из стУпаковки Цикл
			ИтогоКоличество = ИтогоКоличество + текСтрока.Значение + " " + текСтрока.Ключ + ", ";
		КонецЦикла;
		ИтогоКоличество = Лев(ИтогоКоличество, СтрДлина(ИтогоКоличество) - 2);
		
		ОбластьИтого.Параметры.ИтогоКоличество = ИтогоКоличество;
		ОбластьИтого.Параметры.ИтогоСуммаНДС = ИтогоСуммаНДС;
		ОбластьИтого.Параметры.ИтогоСуммаСНДС = ИтогоСуммаСНДС;
		ОбластьИтого.Параметры.ИтогоСуммаБезНДС = ИтогоСуммаБезНДС;
		тдНовый.Вывести(ОбластьИтого);
		
		ОбластьПодвал.Параметры.Заполнить(текСтрокаШапка);
		
		ОбластьПодвал.Параметры.ФИОРуководителя = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),Строка(текСтрокаШапка.Организация.РуководительПредприятия),"");
		ОбластьПодвал.Параметры.Руководитель = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),текСтрокаШапка.Организация.РуководительПредприятия,Неопределено);
		ОбластьПодвал.Параметры.ФИОГлавБухгалтера = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),Строка(текСтрокаШапка.Организация.ГлавныйБухгалтер),"");
		ОбластьПодвал.Параметры.ГлавныйБухгалтер = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),текСтрокаШапка.Организация.ГлавныйБухгалтер,Неопределено);
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.Вывести(ОбластьПодвал);
			Иначе
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
				тдНовый.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			тдНовый.Вывести(ОбластьПодвал);
		КонецПопытки;
		тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
		
		ТабличныйДокумент.Вывести(тдНовый);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.ДокументОснование);
	КонецЦикла;
				
КонецПроцедуры

// Формирование печатной формы ТОРГ-12
Функция ПечатьТОРГ12(МассивОбъектов, ОбъектыПечати)
    
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ТОРГ12";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов, Истина);
	ЗаполнитьТабличныйДокументТОРГ12(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция СформироватьПечатнуюФорму_Этикеток(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	Если ЭтоРейс Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.Отправитель) КАК Отправитель,
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.АдресОтправления) КАК АдресОтправителя,
		               |	упРейсЗадания.Задание.Отправитель КАК ОтправительСсылка,
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.Получатель) КАК Получатель,
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.АдресПолучения) КАК АдресПолучателя,
		               |	упРейсЗадания.Задание.Получатель КАК ПолучательСсылка,
		               |	упРейсЗадания.ГрузовоеМесто.Код КАК НомерГрузовогоМеста,
		               |	упРейсЗадания.ГрузовоеМесто.Штрихкод КАК Штрихкод,
		               |	упРейсЗадания.Задание КАК ДокументОснование
		               |ИЗ
		               |	Документ.упРейс.Задания КАК упРейсЗадания
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		               |		ПО упРейсЗадания.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		               |ГДЕ
		               |	упРейсЗадания.Ссылка В(&МассивОбъектов)
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель),
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресОтправления),
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель),
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресПолучения),
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Код,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Штрихкод,
		               |	упРейсЗадания.Задание
		               |ИЗ
		               |	Документ.упРейс.Задания КАК упРейсЗадания
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
		               |		ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка
		               |ГДЕ
		               |	упРейсЗадания.Ссылка В(&МассивОбъектов)
		               |	И упРейсЗадания.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.Пустаяссылка)";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Результат = Запрос.Выполнить();
		
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель) КАК Отправитель,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресОтправления) КАК АдресОтправителя,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель КАК ОтправительСсылка,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель) КАК Получатель,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресПолучения) КАК АдресПолучателя,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель КАК ПолучательСсылка,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Код КАК НомерГрузовогоМеста,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Штрихкод КАК Штрихкод,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК ДокументОснование
		               |ИЗ
		               |	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
		               |ГДЕ
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка В(&МассивОбъектов)";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Результат = Запрос.Выполнить();
		
	КонецЕсли;	
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.Этикетки";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Этикетка");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Эталон	= ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Штрихкод
		Рисунок = Шапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", Выборка.Штрихкод); //упСервисныеФункции.ПолучитьШКДокументаДляПечати(
		СтруктураШтрихкода.Вставить("ТипКода", 99);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка	=  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		Шапка.Параметры.Отправитель = Выборка.Отправитель + " " + Выборка.АдресОтправителя;
		Шапка.Параметры.РасшифровкаОтправитель = Выборка.ОтправительСсылка;
		Шапка.Параметры.Получатель = Выборка.Получатель + " " + Выборка.АдресПолучателя;
		Шапка.Параметры.РасшифровкаПолучатель = Выборка.ПолучательСсылка;
		Шапка.Параметры.НомерГрузовогоМеста = Выборка.НомерГрузовогоМеста;
		
		ТабличныйДокумент.Вывести(Шапка);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.ДокументОснование);
		
	КонецЦикла;
	
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_Этикеток()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция СформироватьПечатнуюФорму_Этикеток_QR(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	Если ЭтоРейс Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.Отправитель) КАК Отправитель,
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.АдресОтправления) КАК АдресОтправителя,
		               |	упРейсЗадания.Задание.Отправитель КАК ОтправительСсылка,
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.Получатель) КАК Получатель,
		               |	ПРЕДСТАВЛЕНИЕ(упРейсЗадания.Задание.АдресПолучения) КАК АдресПолучателя,
		               |	упРейсЗадания.Задание.Получатель КАК ПолучательСсылка,
		               |	упРейсЗадания.ГрузовоеМесто.Код КАК НомерГрузовогоМеста,
		               |	упРейсЗадания.ГрузовоеМесто.Штрихкод КАК Штрихкод,
		               |	упРейсЗадания.Задание КАК ДокументОснование,
		               |	упРейсЗадания.Ссылка КАК Ссылка
		               |ИЗ
		               |	Документ.упРейс.Задания КАК упРейсЗадания
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		               |		ПО упРейсЗадания.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		               |ГДЕ
		               |	упРейсЗадания.Ссылка В(&МассивОбъектов)
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель),
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресОтправления),
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель),
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресПолучения),
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Код,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Штрихкод,
		               |	упРейсЗадания.Задание,
		               |	упРейсЗадания.Ссылка
		               |ИЗ
		               |	Документ.упРейс.Задания КАК упРейсЗадания
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
		               |		ПО упРейсЗадания.Задание = упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка
		               |ГДЕ
		               |	упРейсЗадания.Ссылка В(&МассивОбъектов)
		               |	И упРейсЗадания.ГрузовоеМесто = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.Пустаяссылка)";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Результат = Запрос.Выполнить();
		
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель) КАК Отправитель,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресОтправления) КАК АдресОтправителя,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Отправитель КАК ОтправительСсылка,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель) КАК Получатель,
		               |	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.АдресПолучения) КАК АдресПолучателя,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.Получатель КАК ПолучательСсылка,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Код КАК НомерГрузовогоМеста,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Штрихкод КАК Штрихкод,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК ДокументОснование
		               |ИЗ
		               |	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
		               |ГДЕ
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка В(&МассивОбъектов)";
		
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Результат = Запрос.Выполнить();
		
	КонецЕсли;	
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.Этикетки";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Этикетка_QR");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		текШтрихкод = Выборка.Штрихкод;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// QRCode
		Рисунок = Шапка.Рисунки.QRCode;			
		ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(текШтрихкод, 0, 150);
		
		Если ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
			КартинкаQRКода = Новый Картинка(ДанныеQRКода);
			Рисунок.Картинка = КартинкаQRКода;
		Иначе
			Шаблон = Нстр("ru = 'Не удалось сформировать QR-код для  %1.
			|Технические подробности см. в журнале регистрации.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Выборка.Штрихкод);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;		
		
		Шапка.Параметры.Отправитель = Выборка.Отправитель + " " + Выборка.АдресОтправителя;
		Шапка.Параметры.РасшифровкаОтправитель = Выборка.ОтправительСсылка;
		Шапка.Параметры.Получатель = Выборка.Получатель + " " + Выборка.АдресПолучателя;
		Шапка.Параметры.РасшифровкаПолучатель = Выборка.ПолучательСсылка;
		Шапка.Параметры.НомерГрузовогоМеста = Выборка.НомерГрузовогоМеста;
		
		ТабличныйДокумент.Вывести(Шапка);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.ДокументОснование);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_Этикеток()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент
//
Функция СформироватьПечатнуюФорму_ТТН_Измен(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.ТТН_Измен";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН_ДОП");
	
	Шапка = Макет.ПолучитьОбласть("Шапка|Потолок");
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы|Потолок");
	ОбластьМакетаВсего = Макет.ПолучитьОбласть("ИтогоТаблицы|Потолок");
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал|Потолок");
	ОбластьМакетаНиз = Макет.ПолучитьОбласть("НижняяЧасть|Потолок");
	ОбластьМакетаТС = Макет.ПолучитьОбласть("СтрокаМашин|Потолок");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ упЗаданиеНаПеревозкуГруза.Ссылка В  (&МассивОбъектов)
	|";

	Если ЭтоРейс Тогда
		ТекстЗапроса = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание,
		|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|ГДЕ упРейсЗадания.Ссылка В  (&МассивОбъектов)
		|";

	КонецЕсли;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание КАК Задание
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсов
	|	ПО упРейсЗадания.Ссылка = упСтатусыРейсов.Документ
	|ГДЕ ИСТИНА
	|	И упРейсЗадания.Задание В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗадания КАК втЗадания)
	|	И упСтатусыРейсов.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|СГРУППИРОВАТЬ ПО
	|	упРейсЗадания.Ссылка,
	|	упРейсЗадания.Задание
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	ЕСТЬNULL(упРейсМаршрут.Адрес, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)) КАК Адрес,
	|	ЕСТЬNULL(упРейсМаршрут.ПлановоеПрибытие, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеПрибытие
	|ПОМЕСТИТЬ втАдресаЗаданий
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК упРейсМаршрут
	|	ПО втРейсы.Рейс = упРейсМаршрут.Рейс
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозку.Ссылка КАК ДокументОснование,
	|	ЗаданиеНаПеревозку.Отправитель КАК Отправитель,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|			ТОГДА """"""""
	|		ИНАЧЕ ЗаданиеНаПеревозку.Отправитель.НаименованиеПолное
	|	КОНЕЦ КАК Грузоотправитель,
	|	ЗаданиеНаПеревозку.Получатель КАК Получатель,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|			ТОГДА """"""""
	|		ИНАЧЕ ЗаданиеНаПеревозку.Получатель.НаименованиеПолное
	|	КОНЕЦ КАК Грузополучатель,
	|	ЗаданиеНаПеревозку.Заказчик КАК Заказчик,
	|	ЗаданиеНаПеревозку.Заказчик.НаименованиеПолное КАК Плательщик,
	|	ЗаданиеНаПеревозку.АдресОтправления КАК АдресОтгрузки,
	|	ЗаданиеНаПеревозку.Номер КАК Номер,
	|	ЗаданиеНаПеревозку.НомерКИС КАК НомерКИС,
	|	ЗаданиеНаПеревозку.Дата КАК Дата,
	|	ЗаданиеНаПеревозку.Организация.Представление КАК Организация,
	|	ЗаданиеНаПеревозку.Организация КАК ОрганизацияСсылка,
	|	ЗаданиеНаПеревозку.АдресПолучения КАК АдресПолучения,
	|	ЗаданиеНаПеревозку.Соглашение КАК Соглашение
	|ПОМЕСТИТЬ втОписаниеЗаданий
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозку
	|ГДЕ ЗаданиеНаПеревозку.Ссылка В (
	|		ВЫБРАТЬ
	|			втЗадания.Задание КАК Задание
	|		ИЗ
	|			втЗадания КАК втЗадания)
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбГрузы.Ссылка КАК Документ,
	|	тбГрузы.Ссылка КАК ТоварнаяНакладная,
	|	тбСоставГруза.Ссылка КАК Груз,
	|	тбСоставГруза.Номенклатура.Наименование КАК Номенклатура,
	|	тбСоставГруза.Номенклатура.Код КАК НоменклатураКод,
	|	тбСоставГруза.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|	тбСоставГруза.Номенклатура.Артикул КАК НоменклатураАртикул,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ тбСоставГруза.Количество
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА """"""""
	|		ИНАЧЕ тбСоставГруза.УпаковкаНоменклатуры.Наименование
	|	КОНЕЦ КАК ВидУпаковки,
	|	тбСоставГруза.Количество КАК Количество,
	|	тбСоставГруза.Цена КАК Цена,
	|	тбСоставГруза.Сумма КАК СуммаСНДС,
	|	тбСоставГруза.СтавкаНДС КАК СтавкаНДС,
	|	тбСоставГруза.СуммаНДС КАК СуммаНДС,
	|	тбСоставГруза.СуммаБезНДС КАК СуммаБезНДС,
	|	тбСоставГруза.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	тбСоставГруза.УпаковкаНоменклатуры.Коэффициент КАК КоличествоВОдномМесте,
	|	тбСоставГруза.Количество КАК Количество1
	|ПОМЕСТИТЬ тбСтроки
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК тбСоставГруза
	|	ПО тбГрузы.ГрузовоеМесто = тбСоставГруза.Ссылка
	|ГДЕ тбГрузы.Ссылка В (
	|		ВЫБРАТЬ
	|			втЗадания.Задание КАК Ссылка
	|		ИЗ
	|			втЗадания КАК втЗадания)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтроки.Документ КАК Документ,
	|	тбСтроки.Груз КАК Груз,
	|	тбСтроки.Номенклатура КАК Номенклатура,
	|	тбСтроки.НоменклатураКод КАК НоменклатураКод,
	|	тбСтроки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	тбСтроки.НоменклатураАртикул КАК НоменклатураАртикул,
	|	тбСтроки.КоличествоМест КАК КоличествоМест,
	|	тбСтроки.ВидУпаковки КАК ВидУпаковки,
	|	тбСтроки.Количество КАК Количество,
	|	тбСтроки.Цена КАК Цена,
	|	тбСтроки.СуммаСНДС КАК СуммаСНДС,
	|	тбСтроки.СтавкаНДС КАК СтавкаНДС,
	|	тбСтроки.СуммаНДС КАК СуммаНДС,
	|	тбСтроки.СуммаБезНДС КАК СуммаБезНДС,
	|	тбСтроки.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод,
	|	тбСтроки.КоличествоВОдномМесте КАК КоличествоВОдномМесте
	|ИЗ
	|	тбСтроки КАК тбСтроки
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК Задание,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто КАК Груз,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов КАК Количество,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.ТипГруза КАК ТипГруза,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Масса)) КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Высота, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Высота)) КАК Высота,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Длина, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Длина)) КАК Длина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Ширина, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Ширина)) КАК Ширина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Объем)) КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.КоличествоМест)) КАК КоличествоМест,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Описание КАК Описание,
	|	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим) КАК ТемпературныйРежим,
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Представление КАК Представление
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	|	ПО упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = втЗадания.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗадания КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОписаниеЗаданий.ДокументОснование КАК ДокументОснование,
	|	втОписаниеЗаданий.ДокументОснование.НомерКИС КАК ЗаявкаНомерКИС,
	|	втОписаниеЗаданий.НомерКИС КАК НомерКИС,
	|	втОписаниеЗаданий.Отправитель КАК Отправитель,
	|	втОписаниеЗаданий.Грузоотправитель КАК Грузоотправитель,
	|	втОписаниеЗаданий.Получатель КАК Получатель,
	|	втОписаниеЗаданий.Грузополучатель КАК Грузополучатель,
	|	втОписаниеЗаданий.Заказчик КАК Заказчик,
	|	втОписаниеЗаданий.Плательщик КАК Плательщик,
	|	втОписаниеЗаданий.АдресОтгрузки КАК АдресОтгрузки,
	|	втОписаниеЗаданий.Номер КАК Номер,
	|	втОписаниеЗаданий.Дата КАК Дата,
	|	втОписаниеЗаданий.Организация КАК Организация,
	|	втОписаниеЗаданий.ОрганизацияСсылка КАК ОрганизацияСсылка,
	|	втОписаниеЗаданий.АдресПолучения КАК АдресПолучения,
	|	втОписаниеЗаданий.Соглашение КАК Соглашение,
	|	втАдресаЗаданий.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|	втОписаниеЗаданий.ДокументОснование.Комментарий КАК КомментарийКГрузу,
	|	ВЫБОР
	|		КОГДА втОписаниеЗаданий.Соглашение ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГруппыТарифов.ПустаяСсылка)
	|		ИНАЧЕ втОписаниеЗаданий.Соглашение.ГруппаТарифов
	|	КОНЕЦ КАК ГруппаТарифов
	|ИЗ
	|	втОписаниеЗаданий КАК втОписаниеЗаданий
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаЗаданий КАК втАдресаЗаданий
	|	ПО ИСТИНА
	|		И втОписаниеЗаданий.ДокументОснование = втАдресаЗаданий.Задание
	|		И втОписаниеЗаданий.АдресОтгрузки = втАдресаЗаданий.Адрес
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель1,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель2, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель2,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)) КАК Перевозчик,
	|	ЕСТЬNULL(ПРЕДСТАВЛЕНИЕССЫЛКИ(упПеревозчикПоРейсуСрезПоследних.Перевозчик.Представление), """""""") КАК ПеревозчикПредставление,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|	ПО втРейсы.Рейс = упРейс.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО втРейсы.Рейс = упПеревозчикПоРейсуСрезПоследних.Рейс
	|";

	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ТаблицаСостав = ПакетЗапросов[5].Выгрузить();
	ТаблицаГрузы = ПакетЗапросов[6].Выгрузить();
	ВыборкаЗ = ПакетЗапросов[7].Выбрать();
	ВыборкаР = ПакетЗапросов[8].Выбрать();
	
	Пока ВыборкаЗ.Следующий() Цикл 
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// --- Грузоотправитель ---
		Грузоотправитель = ВыборкаЗ.Отправитель;
		ГрузоотправительТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузоотправитель, Справочники.ВидыКонтактнойИнформации.ТелефонПартнера);
		ГрузоотправительАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузоотправитель, Справочники.ВидыКонтактнойИнформации.АдресПартнера);
		
		ГрузоотправительПредставление = СокрЛП(ВыборкаЗ.Грузоотправитель) + ?(ПустаяСтрока(ГрузоотправительАдрес),"",", "+СокрЛП(ГрузоотправительАдрес))+
		?(ПустаяСтрока(ГрузоотправительТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ГрузоотправительТелефон));
		
		Шапка.Параметры.ГрузоотправительПредставление = ГрузоотправительПредставление;
		Шапка.Параметры.РасшифровкаГрузоотправитель = ВыборкаЗ.Отправитель;
		
		
		// --- Грузополучатель ---
		Грузополучатель = ВыборкаЗ.Получатель;
		ГрузополучательТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.ТелефонПартнера);
		ГрузополучательАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.АдресПартнера);
		ГрузополучательРС = "";
		
		ГрузополучательПредставление = СокрЛП(ВыборкаЗ.Грузополучатель) + ?(ПустаяСтрока(ГрузополучательАдрес),"",", "+СокрЛП(ГрузополучательАдрес))+
		?(ПустаяСтрока(ГрузополучательТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ГрузополучательТелефон));
		
		Шапка.Параметры.ГрузополучательПредставление = ГрузополучательПредставление;
		Шапка.Параметры.РасшифровкаГрузополучатель = ВыборкаЗ.Получатель;
		
		
		// Накладная
		Шапка.Параметры.Накладная = "Товарно-транспортная накладная № " + Символы.ПС + ВыборкаЗ.Номер + " от " + Формат(ВыборкаЗ.Дата,"ДФ=dd.MM.yyyy");
		
		ВыборкаР.Сбросить();
		СтуктураПоиска = Новый Структура("Задание",ВыборкаЗ.ДокументОснование);
		ДанныеРейса = Неопределено;
		Если ВыборкаР.НайтиСледующий(СтуктураПоиска) Тогда
			ДанныеРейса = Новый Структура();
			ДанныеРейса.Вставить("Водитель1",ВыборкаР.Водитель1);
			ДанныеРейса.Вставить("Водитель2",ВыборкаР.Водитель2);
			ДанныеРейса.Вставить("Перевозчик",ВыборкаР.Перевозчик);
			ДанныеРейса.Вставить("ТранспортноеСредство",ВыборкаР.ТранспортноеСредство);
			
			// Перевозчик
			Перевозчик = ДанныеРейса.Перевозчик;
			ПеревозчикТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.ТелефонПартнера);
			ПеревозчикАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.АдресПартнера);
			
			ПеревозчикПредставление = СокрЛП(ВыборкаР.ПеревозчикПредставление) + ?(ПустаяСтрока(ПеревозчикАдрес),"",", "+СокрЛП(ПеревозчикАдрес))+
			?(ПустаяСтрока(ПеревозчикТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ПеревозчикТелефон));
			
			Шапка.Параметры.Перевозчик = ПеревозчикПредставление;
			Шапка.Параметры.РасшифровкаПеревозчик = Перевозчик;
			
		КонецЕсли;
		// Место разгрузки груза
		Шапка.Параметры.МестоРазгрузкиГруза = ВыборкаЗ.АдресПолучения;
		Шапка.Параметры.РасшифровкаМестоРазгрузкиГруза = ВыборкаЗ.АдресПолучения;
		
		// Последующий перевозчик
		Шапка.Параметры.ПоследующийПеревозчик = "";
		// Место и дата погрузки груза
		Шапка.Параметры.МестоДатаПогрузкиГруза = ВыборкаЗ.АдресОтгрузки;
		Шапка.Параметры.РасшифровкаМестоДатаПогрузкиГруза = ВыборкаЗ.АдресОтгрузки;
		
		// Заказчик и его реквизиты		
		Заказчик = ВыборкаЗ.Заказчик;
		ЗаказчикТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ЗаказчикАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Заказчик, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ЗаказчикПредставление = СокрЛП(ВыборкаЗ.Плательщик) + ?(ПустаяСтрока(ЗаказчикАдрес),"",", "+СокрЛП(ЗаказчикАдрес))+
		?(ПустаяСтрока(ЗаказчикТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ЗаказчикТелефон));		
		
		Шапка.Параметры.Заказчик = ЗаказчикПредставление;
		Шапка.Параметры.РасшифровкаЗаказчик = Заказчик;
		
		
		НайденныеСтроки = ТаблицаГрузы.НайтиСтроки(СтуктураПоиска);
		тбзГрузовыеМеста = ТаблицаГрузы.Скопировать(НайденныеСтроки);
		Шапка.Параметры.УсловияХранения = "";
		
		
		// Штрихкод
		Рисунок = Шапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(ВыборкаЗ.ДокументОснование));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);

		
		ТабличныйДокумент.Вывести(Шапка);
		
		// --- СведенияОГрузе ---
		КоличествоСтрокГруза = 0;
		ОбщееКоличествоМест = 0;
		ОбщаяСуммаГруза = 0;
		ОбщийВес = 0;
		ОбщийОбъем = 0;
		ОбщийОбъемныйВес = 0;
		
		Описание = "";
		Для Каждого СтрокаГМ Из тбзГрузовыеМеста Цикл
			Описание = Описание + ?(ПустаяСтрока(Описание),"","; ") + СтрокаГМ.Описание;			
			ОбластьМакета.Параметры.ПрилогаемыеДокументы = ?(ПустаяСтрока(ВыборкаЗ.ЗаявкаНомерКИС),ВыборкаЗ.НомерКИС,ВыборкаЗ.ЗаявкаНомерКИС);
			ОбластьМакета.Параметры.НаименованиеГруза = СокрЛП(СтрокаГМ.Представление);
			ОбластьМакета.Параметры.РасшифровкаГруза = СтрокаГМ.Груз;			
			ОбластьМакета.Параметры.ВидУпаковки = "";
			ОбластьМакета.Параметры.КолвоМест = СтрокаГМ.КоличествоМест;
			ОбластьМакета.Параметры.Вес = СтрокаГМ.Масса;
			ОбластьМакета.Параметры.Объем = СтрокаГМ.Объем;
			ОбластьМакета.Параметры.ТемпературныйРежим = СтрокаГМ.ТемпературныйРежим;
			
			СтуктураПоиска = Новый Структура("Документ,Груз",ВыборкаЗ.ДокументОснование,СтрокаГМ.Груз);
			НайденныеСтроки = ТаблицаСостав.НайтиСтроки(СтуктураПоиска);
			тбзТаблицаСостав = ТаблицаСостав.Скопировать(НайденныеСтроки);
			
			СуммаГруза = 0;
			Для Каждого тбчСтрока Из тбзТаблицаСостав Цикл				
				СуммаНДС = 0;
				Сумма = тбчСтрока.Количество * тбчСтрока.Цена;
				СуммаБезНДС = Сумма;
				
				Если тбчСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18") Тогда 
					СуммаНДС = Сумма * 0.18;
					СуммаБезНДС = Сумма;
					Сумма = Сумма + СуммаНДС;
				ИначеЕсли тбчСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС18_118") Тогда 
					СуммаНДС = Сумма * 9 / 59;
					СуммаБезНДС = Сумма / 1.18;
					Сумма = Сумма;
				ИначеЕсли тбчСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20") Тогда 
					СуммаНДС = Сумма / 5;
					СуммаБезНДС = Сумма;
					Сумма = Сумма + СуммаНДС;
				ИначеЕсли тбчСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС20_120") Тогда 
					СуммаНДС = Сумма / 6;
					СуммаБезНДС = Сумма / 1.2;
					Сумма = Сумма;	
				ИначеЕсли тбчСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10") Тогда 
					СуммаНДС = Сумма / 10;
					СуммаБезНДС = Сумма;
					Сумма = Сумма + СуммаНДС;
				ИначеЕсли тбчСтрока.СтавкаНДС = ПредопределенноеЗначение("Перечисление.упСтавкиНДС.НДС10_110") Тогда 
					СуммаНДС = Сумма / 11;
					СуммаБезНДС = Сумма / 1.1;
					Сумма = Сумма;					
				КонецЕсли;
				СуммаГруза = СуммаГруза + ?(Сумма=0,тбчСтрока.СуммаСНДС,Сумма);
			КонецЦикла;
			ОбластьМакета.Параметры.Сумма = СуммаГруза;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			КоличествоСтрокГруза = КоличествоСтрокГруза + 1;
			
			ОбщееКоличествоМест = ОбщееКоличествоМест + СтрокаГМ.КоличествоМест;
			ОбщаяСуммаГруза = ОбщаяСуммаГруза + СуммаГруза;
			ОбщийВес = ОбщийВес + СтрокаГМ.Масса;
			ОбщийОбъем = ОбщийОбъем + СтрокаГМ.Объем;
			
		КонецЦикла;
		
		Пока КоличествоСтрокГруза <= 7 Цикл
			
			ОбластьМакета.Параметры.ПрилогаемыеДокументы = "";
			ОбластьМакета.Параметры.НаименованиеГруза = "";
			ОбластьМакета.Параметры.ВидУпаковки = "";
			ОбластьМакета.Параметры.КолвоМест = "";
			ОбластьМакета.Параметры.Сумма = "";
			ОбластьМакета.Параметры.Вес = "";
			ОбластьМакета.Параметры.Объем = "";
			ОбластьМакета.Параметры.ТемпературныйРежим = "";
			ТабличныйДокумент.Вывести(ОбластьМакета);
			КоличествоСтрокГруза = КоличествоСтрокГруза + 1;
			
			Если КоличествоСтрокГруза >= 7 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьМакетаВсего.Параметры.КолвоМест = ОбщееКоличествоМест;
		ОбластьМакетаВсего.Параметры.Сумма = ОбщаяСуммаГруза;
		ОбластьМакетаВсего.Параметры.Вес = ОбщийВес;
		ОбластьМакетаВсего.Параметры.Объем = ОбщийОбъем;
		ОбластьМакетаВсего.Параметры.ТемпературныйРежим = "-";
		
		ТабличныйДокумент.Вывести(ОбластьМакетаВсего);
		
		// Указания отправителя		
		УказанияОтправителя = ?(ПустаяСтрока(Описание),"",Описание);
		УказанияОтправителя = УказанияОтправителя + ?(ПустаяСтрока(УказанияОтправителя),"",Символы.ПС) + ?(ПустаяСтрока(ВыборкаЗ.КомментарийКГрузу),"",ВыборкаЗ.КомментарийКГрузу);
		
		ОбластьМакетаНиз.Параметры.УказанияОтправителя = УказанияОтправителя;
		
		// Условия оплаты
		ОбластьМакетаНиз.Параметры.УсловияОплаты = ВыборкаЗ.Соглашение;
		ОбластьМакетаНиз.Параметры.РасшифровкаУсловияОплаты = ВыборкаЗ.Соглашение;
		
		// Особые согласованные условия		
		ОбластьМакетаНиз.Параметры.ОсобыеСогласованныеУсловия = "";
		ТабличныйДокумент.Вывести(ОбластьМакетаНиз);	
		
		ОбластьМакетаТС.Параметры.РегНомерТС = "";
		ОбластьМакетаТС.Параметры.МаркаТС = "";
		ОбластьМакетаТС.Параметры.НомерМаршрута = "";
		ОбластьМакетаТС.Параметры.НомерПутевогоЛиста = "";
		ОбластьМакетаТС.Параметры.ВодУдостоверение = "";
		ВодительПредставление = "";
		Если ДанныеРейса <> Неопределено Тогда
			ТС = ДанныеРейса.ТранспортноеСредство;
			Если ЗначениеЗаполнено(ТС) Тогда
				ОбластьМакетаТС.Параметры.РегНомерТС = ТС.РегистрационныйНомер;
				ОбластьМакетаТС.Параметры.РасшифровкаТС = ТС;				
				ОбластьМакетаТС.Параметры.МаркаТС = ТС.Модель;
			КонецЕсли;
			Массив = Новый Массив;
			Если ЗначениеЗаполнено(ДанныеРейса.Водитель1) Тогда
				ВодительПредставление = СокрЛП(ДанныеРейса.Водитель1);
				ОбластьМакетаПодвал.Параметры.РасшифровкаВодитель = ДанныеРейса.Водитель1;
				Массив.Добавить(ДанныеРейса.Водитель1);
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеРейса.Водитель2) Тогда
				ВодительПредставление = ?(ПустаяСтрока(ВодительПредставление), "", "; ") + СокрЛП(ДанныеРейса.Водитель2);
				ОбластьМакетаПодвал.Параметры.РасшифровкаВодитель = ДанныеРейса.Водитель2;
				Массив.Добавить(ДанныеРейса.Водитель2);
			КонецЕсли;
			ЗапросВ = Новый Запрос("ВЫБРАТЬ
			|	ДокументыТСВод.Регистратор.Серия КАК Серия,
			|	ДокументыТСВод.Регистратор.Номер КАК Номер,
			|	ДокументыТСВод.Регистратор КАК Ссылка
			|ИЗ
			|	РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних КАК ДокументыТСВод
			|ГДЕ
			|	ДокументыТСВод.Водитель В(&МассивОбъектов)");
			ЗапросВ.УстановитьПараметр("МассивОбъектов", Массив);
			Выборка = ЗапросВ.Выполнить();
			Если НЕ Выборка.Пустой() Тогда
				Результат = Выборка.Выбрать();
				Пока Результат.Следующий() Цикл
					Если НЕ(ПустаяСтрока(Результат.Серия) И ПустаяСтрока(Результат.Номер)) Тогда
						ОбластьМакетаТС.Параметры.ВодУдостоверение = Результат.Серия + " " + Результат.Номер;
						ОбластьМакетаТС.Параметры.РасшифровкаВодУдостоверение = Результат.Ссылка;						
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаТС);
		                                     
		ОбластьМакетаПодвал.Параметры.Тариф = ВыборкаЗ.ГруппаТарифов;
		ОбластьМакетаПодвал.Параметры.РасшифровкаТариф = ВыборкаЗ.ГруппаТарифов;
		ОбластьМакетаПодвал.Параметры.СуммаПоТарифу = "";		
		ОбластьМакетаПодвал.Параметры.Скидка = "";
		ОбластьМакетаПодвал.Параметры.СуммаСоСкидкой = "";
		ОбластьМакетаПодвал.Параметры.ДопУслуги = "";
		ОбластьМакетаПодвал.Параметры.ИздержкиПриДоставке = "";
		ОбластьМакетаПодвал.Параметры.Итого = "";
		
		ОбластьМакетаПодвал.Параметры.ВодительПредставление = ВодительПредставление;
		ТабличныйДокумент.Вывести(ОбластьМакетаПодвал);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаЗ.ДокументОснование);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ТТН_Измен()

Процедура ЗаполнитьТабличныйДокументТОРГ12(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТОРГ12");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьДополнение = Макет.ПолучитьОбласть("Дополнение");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокТаб");
	ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
	ОбластьИтоги = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего = Макет.ПолучитьОбласть("Всего");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	ОбластьДополнениеОборотСтраницы = Макет.ПолучитьОбласть("ДополнениеОборот");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
			
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		тдНовый = Новый ТабличныйДокумент;
		тдНовый.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТОРГ12";
		
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);
		
		// Штрихкод
		Рисунок = ОбластьШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.ДокументОснование));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		тдНовый.Вывести(ОбластьШапка);
		
		НомерЛиста = 1;
		ОбластьДополнение.Параметры.НомерСтраницы = НомерЛиста;
		тдНовый.Вывести(ОбластьДополнение);
		тдНовый.Вывести(ОбластьЗаголовок);
	    				
		сткОтбор = Новый Структура("Документ", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбДанныеСтроки.НайтиСтроки(сткОтбор);
		
		ИтогоКоличествоЛист = 0;
		ИтогоСуммаБезНДСЛист = 0;
		ИтогоНДСЛист = 0;
		ИтогоСуммаСНДСЛист = 0;
		ИтогоМассаНетто = 0;
				
		ИтогоКоличество = 0;
		ИтогоСуммаБезНДС = 0;
		ИтогоНДС = 0;
		ИтогоСуммаСНДС = 0;
		МассаНетто = 0;
		
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		
	    Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтроки.Параметры.Заполнить(текЭлементСтрока);
			ОбластьСтроки.Параметры.СтрокаНомер = СтрокаНомер;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтроки);
			СтрокаСПодвалом.Добавить(ОбластьИтоги);
			СтрокаСПодвалом.Добавить(ОбластьВсего);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(ОбластьПодвал);
			КонецЕсли;
			Попытка
				Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
					тдНовый.Вывести(ОбластьСтроки);
				Иначе
					НомерЛиста = НомерЛиста + 1;
					ОбластьИтоги.Параметры.ИтогКоличествоПоСтранице = МассаНетто;
					ОбластьИтоги.Параметры.ИтогСуммыПоСтранице = ИтогоСуммаБезНДСЛист;
					ОбластьИтоги.Параметры.ИтогНДСПоСтранице = ИтогоНДСЛист;
					ОбластьИтоги.Параметры.ИтогСуммыСНДСПоСтранице = ИтогоСуммаСНДСЛист;
					ОбластьДополнение.Параметры.НомерСтраницы = НомерЛиста;
					
					тдНовый.Вывести(ОбластьИтоги);
					тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					Если СтрокаНомер = КоличествоСтрок Тогда
						тдНовый.Вывести(ОбластьДополнениеОборотСтраницы);						
					КонецЕсли;
					тдНовый.Вывести(ОбластьДополнение);
					тдНовый.Вывести(ОбластьЗаголовок);
					тдНовый.Вывести(ОбластьСтроки);
					// После вывода обнулим промежуточные итоги
					ИтогоКоличествоЛист = 0;
					МассаНетто = 0;
					ИтогоСуммаБезНДСЛист = 0;
					ИтогоНДСЛист = 0;
					ИтогоСуммаСНДСЛист = 0;
				КонецЕсли;
			Исключение
				тдНовый.Вывести(ОбластьСтроки);
			КонецПопытки;
			
			ИтогоКоличествоЛист = ИтогоКоличествоЛист + текЭлементСтрока.Количество;
			ИтогоМассаНетто = ИтогоМассаНетто + текЭлементСтрока.МассаНетто;
			ИтогоСуммаБезНДСЛист = ИтогоСуммаБезНДСЛист + текЭлементСтрока.СуммаБезНДС;
			ИтогоНДСЛист = ИтогоНДСЛист + текЭлементСтрока.СуммаНДС;
			ИтогоСуммаСНДСЛист = ИтогоСуммаСНДСЛист	+ текЭлементСтрока.СуммаСНДС;
			
			ИтогоКоличество = ИтогоКоличество + текЭлементСтрока.Количество;
			ИтогоСуммаБезНДС = ИтогоСуммаБезНДС + текЭлементСтрока.СуммаБезНДС;
			ИтогоНДС = ИтогоНДС + текЭлементСтрока.СуммаНДС;
			ИтогоСуммаСНДС = ИтогоСуммаСНДС + текЭлементСтрока.СуммаСНДС;
			МассаНетто = МассаНетто + текЭлементСтрока.МассаНетто;
			
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;
				
		ОбластьИтоги.Параметры.ИтогКоличествоПоСтранице = МассаНетто;
		ОбластьИтоги.Параметры.ИтогСуммыПоСтранице = ИтогоСуммаБезНДСЛист;
		ОбластьИтоги.Параметры.ИтогНДСПоСтранице = ИтогоНДСЛист;
		ОбластьИтоги.Параметры.ИтогСуммыСНДСПоСтранице = ИтогоСуммаСНДСЛист;
		тдНовый.Вывести(ОбластьИтоги);
				
		ОбластьВсего.Параметры.ИтогКоличество = ИтогоМассаНетто;
		ОбластьВсего.Параметры.ИтогСуммы = ИтогоСуммаБезНДС;
		ОбластьВсего.Параметры.ИтогНДС = ИтогоНДС;
		ОбластьВсего.Параметры.ИтогСуммыСНДС = ИтогоСуммаСНДС;
		тдНовый.Вывести(ОбластьВсего);
		
		ПрописьюЧислоСтрок = ЧислоПрописью(мсвСтроки.Количество(), "НП=Ложь", "штука,штуку,штук,м");
		ПрописьюЧислоСтрок = Сред(ПрописьюЧислоСтрок, 1, СтрДлина(ПрописьюЧислоСтрок) - 3);
		
		// Масса нетто итог.
		ОбластьПодвал.Параметры.МассаНеттоПрописью = ЧислоПрописью(ИтогоМассаНетто*1000, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0") + " кг.";		
		
		Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
		ОбластьПодвал.Параметры.СуммаПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(ИтогоСуммаСНДС, Валюта);
		
		ОбластьПодвал.Параметры.КоличествоЛистовВПриложении = НомерЛиста;
		ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ПрописьюЧислоСтрок;
		ОбластьПодвал.Параметры.Заполнить(текСтрокаШапка);
		ОбластьПодвал.Параметры.МассаБруттоПрописью = ОбластьПодвал.Параметры.МассаБруттоПрописью + " кг.";
		
		// Доверенность.
		ОбластьПодвал.Параметры.ДоверенностьНомер = текСтрокаШапка.ДоверенностьНомер;
		ОбластьПодвал.Параметры.ДоверенностьДата = текСтрокаШапка.ДоверенностьДата;
		ОбластьПодвал.Параметры.ДолжностьДоверенноеЛицо = текСтрокаШапка.ДолжностьДоверенноеЛицо;

		мсвКомуВыданаДоверенность = Новый Массив;
		Если ЗначениеЗаполнено(текСтрокаШапка.Перевозчик) Тогда
			мсвКомуВыданаДоверенность.Добавить(текСтрокаШапка.Перевозчик);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текСтрокаШапка.КонтрагентПеревозчика) Тогда
			мсвКомуВыданаДоверенность.Добавить(текСтрокаШапка.КонтрагентПеревозчика);
		КонецЕсли;

		Если ЗначениеЗаполнено(текСтрокаШапка.Водитель) Тогда
			мсвКомуВыданаДоверенность.Добавить(ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(текСтрокаШапка.Водитель));
		КонецЕсли;

		ОбластьПодвал.Параметры.КомуВыданаДоверенность = СтрСоединить(мсвКомуВыданаДоверенность,", ");
		ОбластьПодвал.Параметры.ДоверенноеЛицо = ?(ЗначениеЗаполнено(текСтрокаШапка.ДоверенноеЛицо), ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(текСтрокаШапка.ДоверенноеЛицо),"");
				
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		
		Попытка
			Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.Вывести(ОбластьПодвал);
			Иначе
				НомерЛиста = НомерЛиста + 1;
				ОбластьДополнение.Параметры.НомерСтраницы = НомерЛиста;
						
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
				тдНовый.Вывести(ОбластьДополнение);
				тдНовый.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			тдНовый.Вывести(ОбластьПодвал);
		КонецПопытки;
		тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
		
		ТабличныйДокумент.Вывести(тдНовый);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.ДокументОснование);
	КонецЦикла;

				
КонецПроцедуры

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент
//
Функция СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = НЕ ПараметрыПечати = Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.Экспедиторская_Расписка";
	ТабличныйДокумент.РазмерСтраницы = "A4";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	Если ЭтоРейс Тогда
		ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.Экспедиторская_Расписка";
	КонецЕсли;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Экспедиторская_Расписка");
	
	Шапка = Макет.ПолучитьОбласть("Шапка|Потолок1");
	Грузоотправитель = Макет.ПолучитьОбласть("Грузоотправитель|Потолок2");
	Клиент = Макет.ПолучитьОбласть("Клиент|Потолок3");
	Экспедитор = Макет.ПолучитьОбласть("Экспедитор|Потолок4");
	СтранаПроисхожденияГруза = Макет.ПолучитьОбласть("СтранаПроисхожденияГруза|Потолок5");
	Товар = Макет.ПолучитьОбласть("Товар|Потолок6");
	КоличествоМествидУпаковки = Макет.ПолучитьОбласть("КоличествоМествидУпаковки|Потолок7");
	ВГХ = Макет.ПолучитьОбласть("ВГХ|Потолок8");
	РазмерУпаковки = Макет.ПолучитьОбласть("РазмерУпаковки|Потолок9");
	РазмерУпаковкиСтрока = Макет.ПолучитьОбласть("РазмерУпаковкиСтрока|Потолок9");
	Условия = Макет.ПолучитьОбласть("Условие1|Потолок101");
	ОсобыеОтметки = Макет.ПолучитьОбласть("ОсобыеОтметки|Потолок11");
	ПодписьЭкспедитора = Макет.ПолучитьОбласть("ПодписьЭкспедитора|Потолок12");	
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ упЗаданиеНаПеревозкуГруза.Ссылка В  (&МассивОбъектов)
	|";

	Если ЭтоРейс Тогда
		ТекстЗапроса = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание,
		|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|ГДЕ упРейсЗадания.Ссылка В  (&МассивОбъектов)
		|";

	КонецЕсли;

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание КАК Задание
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсов
	|	ПО упРейсЗадания.Ссылка = упСтатусыРейсов.Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	|	ПО упРейсЗадания.Задание = втЗадания.Задание
	|ГДЕ упСтатусыРейсов.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|СГРУППИРОВАТЬ ПО
	|	упРейсЗадания.Ссылка,
	|	упРейсЗадания.Задание
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	ПРЕДСТАВЛЕНИЕ(ГрузыЗадания.ГрузовоеМесто) КАК ГрузовоеМесто,
	|	ГМ.ТемпературныйРежим КАК ТемпературныйРежим,
	|	ГМ.КлассОпасности КАК КлассОпасности,
	|	ГМ.БеречьОтВлаги КАК БеречьОтВлаги,
	|	ГМ.БеречьОтИзлучения КАК БеречьОтИзлучения,
	|	ГМ.БеречьОтНагрева КАК БеречьОтНагрева,
	|	ГМ.Хрупкий КАК Хрупкий,
	|	ГМ.ТипГруза КАК ТипГруза,
	|	ГМ.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, ГМ.Масса)) КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, ГМ.КоличествоМест)) КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, ГМ.Объем)) КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Ширина, ГМ.Ширина)) КАК Ширина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Длина, ГМ.Длина)) КАК Длина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Высота, ГМ.Высота)) КАК Высота
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузыЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК ГМ
	|		ПО ГрузыЗадания.ГрузовоеМесто = ГМ.Ссылка
	|	ПО втРейсы.Задание = ГрузыЗадания.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗадания КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И ГрузыЗадания.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И ГрузыЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И ГрузыЗадания.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И ГрузыЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.Номер КАК Номер,
	|	упЗаданиеНаПеревозкуГруза.Дата КАК Дата,
	|	упЗаданиеНаПеревозкуГруза.КоличествоМест КАК КоличествоМест,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя КАК Грузоотправитель,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя КАК Клиент,
	|	ЕСТЬNULL(ПеревозчикПоРейсуСП.КонтрагентПеревозчика, ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)) КАК Экспедитор,
	|	ПеревозчикПоРейсуСП.Перевозчик КАК Перевозчик
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО втРейсы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|	ПО втРейсы.Рейс = ПеревозчикПоРейсуСП.Рейс
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГМ.Задание КАК Задание,
	|	ГМ.ТемпературныйРежим КАК ТемпературныйРежим,
	|	ГМ.КлассОпасности КАК КлассОпасности,
	|	ГМ.БеречьОтВлаги КАК БеречьОтВлаги,
	|	ГМ.БеречьОтИзлучения КАК БеречьОтИзлучения,
	|	ГМ.БеречьОтНагрева КАК БеречьОтНагрева,
	|	ГМ.Хрупкий КАК Хрупкий
	|ИЗ
	|	втГрузовыеМеста КАК ГМ
	|СГРУППИРОВАТЬ ПО
	|	ГМ.Задание,
	|	ГМ.ТемпературныйРежим,
	|	ГМ.КлассОпасности,
	|	ГМ.БеречьОтНагрева,
	|	ГМ.БеречьОтВлаги,
	|	ГМ.Хрупкий,
	|	ГМ.БеречьОтИзлучения
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Задание КАК Задание,
	|	СУММА(втГрузовыеМеста.Масса) КАК Вес,
	|	СУММА(втГрузовыеМеста.Объем) КАК Объем,
	|	СУММА(втГрузовыеМеста.ОценочнаяСтоимость) КАК ОценочнаяСтоимость
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Задание
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Задание КАК Задание,
	|	втГрузовыеМеста.ТипГруза КАК ВидГруза,
	|	СУММА(ВЫБОР
	|		КОГДА втГрузовыеМеста.КоличествоМест = 0
	|			ТОГДА 1
	|		ИНАЧЕ втГрузовыеМеста.КоличествоМест
	|	КОНЕЦ) КАК КоличествоМест
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Задание,
	|	втГрузовыеМеста.ТипГруза
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Рейс КАК Рейс,
	|	втГрузовыеМеста.Задание КАК Задание,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Высота КАК ГрузовоеМестоВысота,
	|	втГрузовыеМеста.Длина КАК ГрузовоеМестоДлина,
	|	втГрузовыеМеста.Ширина КАК ГрузовоеМестоШирина,
	|	втГрузовыеМеста.Масса КАК ГрузовоеМестоМасса,
	|	втГрузовыеМеста.Объем КАК ГрузовоеМестоОбъем
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаЗ = ПакетЗапросов[3].Выбрать();
	ТаблицаМаркировка = ПакетЗапросов[4].Выгрузить();
	ТаблицаОбщие = ПакетЗапросов[5].Выгрузить();
	ТаблицаУпаковки = ПакетЗапросов[6].Выгрузить();
	ТаблицаВГХ = ПакетЗапросов[7].Выгрузить();
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки();
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	
	ТекстФормата = "ЧРД=.; ЧРГ=' '; ЧГ=0";
	Пока ВыборкаЗ.Следующий() Цикл 
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ДокументОснование = ВыборкаЗ.Задание;
		ТекущийОтбор = Новый Структура("Задание",ДокументОснование);
		
		Шапка.Параметры.Период = Формат(ВыборкаЗ.Дата,"ДЛФ=D");
		Шапка.Параметры.Номер = ВыборкаЗ.Номер;
		
		ТабличныйДокумент.Вывести(Шапка);
		
		// --- Грузоотправитель ---
		Грузоотправитель_ = ВыборкаЗ.Грузоотправитель;
		ГрузоотправительТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузоотправитель_, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ГрузоотправительАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузоотправитель_, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ГрузоотправительПредставление = СокрЛП(ВыборкаЗ.Грузоотправитель);
		
		Грузоотправитель.Параметры.Грузоотправитель = ГрузоотправительПредставление;
		Грузоотправитель.Параметры.РасшифровкаГрузоотправитель = ВыборкаЗ.Грузоотправитель;
		Грузоотправитель.Параметры.ГрузоотправительАдрес = ГрузоотправительАдрес;
		Грузоотправитель.Параметры.ГрузоотправительТелефон = ГрузоотправительТелефон;
		
		ТабличныйДокумент.Вывести(Грузоотправитель);
		
		// --- Клиент ---
		Клиент_ = ВыборкаЗ.Клиент;
		КлиентТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Клиент_, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		КлиентАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Клиент_, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		КлиентПредставление = СокрЛП(ВыборкаЗ.Клиент);
		
		Клиент.Параметры.КлиентПредставление = КлиентПредставление;
		Клиент.Параметры.РасшифровкаКлиент = ВыборкаЗ.Клиент;
		Клиент.Параметры.КлиентКонтакты = СокрЛП(КлиентАдрес)+?(ПустаяСтрока(КлиентТелефон),"",", тел.:" + Символы.НПП+СокрЛП(КлиентТелефон));
		
		ТабличныйДокумент.Вывести(Клиент);
		
		// --- Экспедитор ---
		Экспедитор_ = ВыборкаЗ.Экспедитор;
		ЭкспедиторТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Экспедитор_, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ЭкспедиторАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Экспедитор_, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ЭкспедиторПредставление = СокрЛП(ВыборкаЗ.Экспедитор);
		
		Экспедитор.Параметры.Экспедитор = ЭкспедиторПредставление;
		Экспедитор.Параметры.РасшифровкаЭкспедитор = ВыборкаЗ.Экспедитор;
		Экспедитор.Параметры.ЭкспедиторКонтакты = СокрЛП(ЭкспедиторАдрес)+?(ПустаяСтрока(ЭкспедиторТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ЭкспедиторТелефон));
		
		ТабличныйДокумент.Вывести(Экспедитор);
		
		ТабличныйДокумент.Вывести(СтранаПроисхожденияГруза);
		
		Маркировка = "";
		НайденныеСтроки = ТаблицаМаркировка.НайтиСтроки(ТекущийОтбор);
		Для Каждого ТекущаяМаркировка Из НайденныеСтроки Цикл			
			Если ЗначениеЗаполнено(ТекущаяМаркировка.ТемпературныйРежим) Тогда				
				Маркировка = Маркировка + ?(ПустаяСтрока(Маркировка),"",", ") + Строка(ТекущаяМаркировка.ТемпературныйРежим);
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекущаяМаркировка.КлассОпасности) Тогда				
				Маркировка = Маркировка + ?(ПустаяСтрока(Маркировка),"",", ") + Строка(ТекущаяМаркировка.КлассОпасности);
			КонецЕсли;
			Если ТекущаяМаркировка.БеречьОтВлаги Тогда				
				Маркировка = Маркировка + ?(ПустаяСтрока(Маркировка),"",", ") + "Беречь от влаги";
			КонецЕсли;
			Если ТекущаяМаркировка.БеречьОтВлаги Тогда				
				Маркировка = Маркировка + ?(ПустаяСтрока(Маркировка),"",", ") + "Беречь от излучения";
			КонецЕсли;
			Если ТекущаяМаркировка.БеречьОтВлаги Тогда				
				Маркировка = Маркировка + ?(ПустаяСтрока(Маркировка),"",", ") + "Беречь от нагрева";
			КонецЕсли;
			Если ТекущаяМаркировка.БеречьОтВлаги Тогда				
				Маркировка = Маркировка + ?(ПустаяСтрока(Маркировка),"",", ") + "Хрупкий";
			КонецЕсли;
		КонецЦикла;
		Товар.Параметры.Маркировка = Маркировка;
		ТабличныйДокумент.Вывести(Товар);
		
		КоличествоМествидУпаковкиПредставление = "";
		Если ВыборкаЗ.КоличествоМест Тогда
			КоличествоМествидУпаковкиПредставление = Формат(ВыборкаЗ.КоличествоМест,ТекстФормата) + сткПредставления.КоличествоМест;
		КонецЕсли;
		
		КоличествоМествидУпаковки.Параметры.КоличествоМествидУпаковки = КоличествоМествидУпаковкиПредставление;
		ТабличныйДокумент.Вывести(КоличествоМествидУпаковки);
		
		упЕдиницаИзмеренияЛинейныхГабаритов = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияЛинейныхГабаритов");
		ЕдДлинна = СокрЛП(упЕдиницаИзмеренияЛинейныхГабаритов);
		ЕдДлинна = "";
		упЕдиницаИзмеренияМассы = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияМассы");
		упЕдиницаИзмеренияОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияОбъема");
		Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
		ВГХ.Параметры.ВесБруттоНетто = "";
		ВГХ.Параметры.Объем = "";
		ВГХ.Параметры.Стоимость = "";		
		НайденныеСтроки = ТаблицаОбщие.НайтиСтроки(ТекущийОтбор);
		Для Каждого ТекущаяВГХ Из НайденныеСтроки Цикл			
			ВГХ.Параметры.ВесБруттоНетто = Формат(ТекущаяВГХ.Вес,ТекстФормата) + " " + сткПредставления.Масса;
			ВГХ.Параметры.Объем = Формат(ТекущаяВГХ.Объем,ТекстФормата) + " " + сткПредставления.Объем;
			Если ВедетсяВалютныйУчет Тогда
				ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ТекущаяВГХ.ОценочнаяСтоимость,Валюта);
			Иначе	
				ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ТекущаяВГХ.ОценочнаяСтоимость);
			КонецЕсли;
			ВГХ.Параметры.Стоимость = ОценочнаяСтоимостьПредставление;
		КонецЦикла;
		ТабличныйДокумент.Вывести(ВГХ);
		
		ВыводРазмераУпаковки = Ложь;		
		НайденныеСтроки = ТаблицаВГХ.НайтиСтроки(ТекущийОтбор);
		Шаг = 0;
		Для Каждого ТекущаяВГХ Из НайденныеСтроки Цикл
			Шаг = Шаг + 1;
			Если ВыводРазмераУпаковки = Ложь Тогда
				РазмерУпаковки.Параметры.РазмерУпаковки = ТекущаяВГХ.ГрузовоеМесто + " " + Формат(ТекущаяВГХ.ГрузовоеМестоВысота,ТекстФормата) + ЕдДлинна
				+ "х" + Формат(ТекущаяВГХ.ГрузовоеМестоДлина,ТекстФормата) + ЕдДлинна + "х" + Формат(ТекущаяВГХ.ГрузовоеМестоШирина,ТекстФормата) + ЕдДлинна + сткПредставления.КоличествоЗагрузочныхМетров
				+ ", вес: " + Формат(ТекущаяВГХ.ГрузовоеМестоМасса,ТекстФормата) + " " + СокрЛП(упЕдиницаИзмеренияМассы)
				+ ", объем: " + Формат(ТекущаяВГХ.ГрузовоеМестоОбъем,ТекстФормата) + " " + СокрЛП(упЕдиницаИзмеренияОбъема);
				ТабличныйДокумент.Вывести(РазмерУпаковки);
				ВыводРазмераУпаковки = Истина;
				Продолжить;
			КонецЕсли;
			РазмерУпаковкиСтрока.Параметры.РазмерУпаковкиСтрока = ТекущаяВГХ.ГрузовоеМесто + " " + Формат(ТекущаяВГХ.ГрузовоеМестоВысота,ТекстФормата) + ЕдДлинна
			+ "х" + Формат(ТекущаяВГХ.ГрузовоеМестоДлина,ТекстФормата) + ЕдДлинна + "х" + Формат(ТекущаяВГХ.ГрузовоеМестоШирина,ТекстФормата) + ЕдДлинна + сткПредставления.КоличествоЗагрузочныхМетров
			+ ", вес: " + Формат(ТекущаяВГХ.ГрузовоеМестоМасса,ТекстФормата) + " " + СокрЛП(упЕдиницаИзмеренияМассы)
			+ ", объем: " + Формат(ТекущаяВГХ.ГрузовоеМестоОбъем,ТекстФормата) + " " + СокрЛП(упЕдиницаИзмеренияОбъема);
			ТабличныйДокумент.Вывести(РазмерУпаковкиСтрока);
		КонецЦикла;
		
		Если Шаг < 3 Тогда
			Шаг_ = Шаг;			
			Для Шаг = Шаг_ По 3 Цикл
				РазмерУпаковкиСтрока.Параметры.РазмерУпаковкиСтрока = "";;
				ТабличныйДокумент.Вывести(РазмерУпаковкиСтрока);
			КонецЦикла;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Условия);	
		ТабличныйДокумент.Вывести(ОсобыеОтметки);
		ТабличныйДокумент.Вывести(ПодписьЭкспедитора);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДокументОснование);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент
//
Функция СформироватьПечатнуюФорму_ПоручениеЭкспедитору(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = НЕ ПараметрыПечати = Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.Поручение_Экспедитору";
	ТабличныйДокумент.РазмерСтраницы = "A4";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	Если ЭтоРейс Тогда
		ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.Поручение_Экспедитору";
	КонецЕсли;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Поручение_Экспедитору");
	
	Шапка = Макет.ПолучитьОбласть("Шапка|Потолок");
	ТипГрузов = Макет.ПолучитьОбласть("ТипГрузов|Потолок");
	ВидТранспорта = Макет.ПолучитьОбласть("ВидТранспорта|Потолок");
	АдресМаркеровка = Макет.ПолучитьОбласть("АдресМаркеровка|Потолок");
	ДанныеГрузовогоУпаковки = Макет.ПолучитьОбласть("ДанныеГрузовогоУпаковки|Потолок");
	ШапкаСтрокиВГХ = Макет.ПолучитьОбласть("ШапкаСтрокиВГХ|Потолок");
	СтрокаВГХ = Макет.ПолучитьОбласть("СтрокаВГХ|Потолок");
	Подвал = Макет.ПолучитьОбласть("Подвал|Потолок");
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ упЗаданиеНаПеревозкуГруза.Ссылка В  (&МассивОбъектов)
	|";

	Если ЭтоРейс Тогда
		ТекстЗапроса = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание,
		|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|ГДЕ упРейсЗадания.Ссылка В  (&МассивОбъектов)
		|";

	КонецЕсли;

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание КАК Задание
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсов
	|	ПО упРейсЗадания.Ссылка = упСтатусыРейсов.Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	|	ПО втЗадания.Задание = упРейсЗадания.Задание
	|ГДЕ упСтатусыРейсов.Статус <> ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	ПРЕДСТАВЛЕНИЕ(ГрузыЗадания.ГрузовоеМесто) КАК ГрузовоеМесто,
	|	ГМ.ТипГруза КАК ТипГруза,
	|	ГМ.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, ГМ.Масса)) КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, ГМ.КоличествоМест)) КАК КоличествоМест,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, ГМ.Объем)) КАК Объем,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Ширина, ГМ.Ширина)) КАК Ширина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Длина, ГМ.Длина)) КАК Длина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Высота, ГМ.Высота)) КАК Высота
	|ПОМЕСТИТЬ втГрузовыеМеста
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузыЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК ГМ
	|		ПО ГрузыЗадания.ГрузовоеМесто = ГМ.Ссылка
	|	ПО втРейсы.Задание = ГрузыЗадания.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗадания КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И ГрузыЗадания.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И ГрузыЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И ГрузыЗадания.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И ГрузыЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.Номер КАК Номер,
	|	упЗаданиеНаПеревозкуГруза.Дата КАК Дата,
	|	упЗаданиеНаПеревозкуГруза.КоличествоМест КАК КоличествоМест,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя.ИНН
	|	КОНЕЦ КАК ИННГрузоотправитель,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя.ИНН
	|	КОНЕЦ КАК ИННГрузополучатель,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентЗаказчика КАК Плательщик,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентЗаказчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентЗаказчика.ИНН
	|	КОНЕЦ КАК ИННПлательщик,
	|	ЕСТЬNULL(ПеревозчикПоРейсуСП.КонтрагентПеревозчика, ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)) КАК Экспедитор,
	|	ПеревозчикПоРейсуСП.Перевозчик КАК Перевозчик,
	|	упЗаданиеНаПеревозкуГруза.КонтактноеЛицоПолучателя КАК КонтактноеЛицо,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ПеревозчикПоРейсуСП.ТранспортноеСредство.ВидТранспорта)
	|	КОНЕЦ КАК ТипТС,
	|	упЗаданиеНаПеревозкуГруза.АдресПолучения КАК АдресПолучения,
	|	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГруза.АдресПолучения) КАК АдресПолученияПредставление
	|ИЗ
	|	втРейсы КАК втРейсы
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО втРейсы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|	ПО втРейсы.Рейс = ПеревозчикПоРейсуСП.Рейс
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Задание КАК Задание,
	|	СУММА(втГрузовыеМеста.Масса) КАК Вес,
	|	СУММА(втГрузовыеМеста.Объем) КАК Объем,
	|	СУММА(втГрузовыеМеста.ОценочнаяСтоимость) КАК ОценочнаяСтоимость
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Задание
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Задание КАК Задание,
	|	втГрузовыеМеста.ТипГруза КАК ТипГруза,
	|	СУММА(ВЫБОР
	|		КОГДА втГрузовыеМеста.КоличествоМест = 0
	|			ТОГДА 1
	|		ИНАЧЕ втГрузовыеМеста.КоличествоМест
	|	КОНЕЦ) КАК КоличествоМест
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|СГРУППИРОВАТЬ ПО
	|	втГрузовыеМеста.Задание,
	|	втГрузовыеМеста.ТипГруза
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Рейс КАК Рейс,
	|	втГрузовыеМеста.Задание КАК Задание,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Высота КАК Высота,
	|	втГрузовыеМеста.Длина КАК Длина,
	|	втГрузовыеМеста.Ширина КАК Ширина,
	|	втГрузовыеМеста.Масса КАК Масса,
	|	втГрузовыеМеста.Объем КАК Объем
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|";

	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаЗ = ПакетЗапросов[3].Выбрать();
	ТаблицаОбщие = ПакетЗапросов[4].Выгрузить();
	ТаблицаУпаковки = ПакетЗапросов[5].Выгрузить();
	ТаблицаВГХ = ПакетЗапросов[6].Выгрузить();
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки();
	ТекстФормата = "ЧРД=.; ЧРГ=' '; ЧГ=0";	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	
	Пока ВыборкаЗ.Следующий() Цикл 
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ДокументОснование = ВыборкаЗ.Задание;
		ТекущийОтбор = Новый Структура("Задание",ДокументОснование);
		
		Шапка.Параметры.Период = Формат(ВыборкаЗ.Дата,"ДЛФ=D");
		Шапка.Параметры.Номер  = ВыборкаЗ.Номер;
		
		// --- Грузоотправитель ---
		Грузоотправитель_ = ВыборкаЗ.Грузоотправитель;
		ГрузоотправительТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузоотправитель_, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ГрузоотправительАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузоотправитель_, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ГрузоотправительПредставление = СокрЛП(ВыборкаЗ.Грузоотправитель) + ?(ПустаяСтрока(ГрузоотправительАдрес),"",", "+СокрЛП(ГрузоотправительАдрес))+
		?(ПустаяСтрока(ГрузоотправительТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ГрузоотправительТелефон));
		
		Шапка.Параметры.ИННГрузоотправитель = ВыборкаЗ.ИННГрузоотправитель;
		Шапка.Параметры.ГрузоотправительПредставление = ГрузоотправительПредставление;
		Шапка.Параметры.РасшифровкаГрузоотправитель = Грузоотправитель_;
		
		// --- Грузополучатель ---
		Грузополучатель = ВыборкаЗ.Грузополучатель;
		ГрузополучательТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ГрузополучательАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ГрузополучательПредставление = СокрЛП(Грузополучатель) + ?(ПустаяСтрока(ГрузополучательАдрес),"",", "+СокрЛП(ГрузополучательАдрес))+
		?(ПустаяСтрока(ГрузополучательТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ГрузополучательТелефон));
		
		Шапка.Параметры.ИННГрузополучатель = ВыборкаЗ.ИННГрузополучатель;
		Шапка.Параметры.ГрузополучательПредставление = ГрузополучательПредставление;
		Шапка.Параметры.РасшифровкаГрузополучатель = Грузополучатель;
		
		// --- Плательщик ---
		Плательщик = ВыборкаЗ.Плательщик;
		ПлательщикТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Плательщик, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ПлательщикАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Плательщик, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ПлательщикПредставление = СокрЛП(Плательщик) + ?(ПустаяСтрока(ПлательщикАдрес),"",", "+СокрЛП(ПлательщикАдрес))+
		?(ПустаяСтрока(ПлательщикТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ПлательщикТелефон));
		
		Шапка.Параметры.ИННПлательщик = ВыборкаЗ.ИННПлательщик;
		Шапка.Параметры.ПлательщикПредставление = ПлательщикПредставление;
		Шапка.Параметры.РасшифровкаПлательщик = ВыборкаЗ.Плательщик;
		
		// --- ЭкспедиторПредставление ---
		Экспедитор = ВыборкаЗ.Экспедитор;
		ЭкспедиторТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Экспедитор, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		ЭкспедиторАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Экспедитор, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		ЭкспедиторПредставление = СокрЛП(Экспедитор) + ?(ПустаяСтрока(ЭкспедиторАдрес),"",", "+СокрЛП(ЭкспедиторАдрес))+
		?(ПустаяСтрока(ЭкспедиторТелефон),"",", тел.:" + Символы.НПП+СокрЛП(ЭкспедиторТелефон));
		
		Шапка.Параметры.ЭкспедиторПредставление = ЭкспедиторПредставление;
		Шапка.Параметры.РасшифровкаЭкспедитор = ВыборкаЗ.Экспедитор;
		
		
		// --- КонтактноеЛицоПредставление ---
		КонтактноеЛицо = ВыборкаЗ.КонтактноеЛицо;
		КонтактноеЛицоТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(КонтактноеЛицо, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
		КонтактноеЛицоМобильный   = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(КонтактноеЛицо, Справочники.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛица);
		КонтактноеЛицоEmail = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(КонтактноеЛицо, Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица);
		
		КонтактноеЛицоПредставление = СокрЛП(КонтактноеЛицо) + ?(ПустаяСтрока(КонтактноеЛицоТелефон),"",", "+СокрЛП(КонтактноеЛицоТелефон))+
		?(ПустаяСтрока(КонтактноеЛицоМобильный),"",", тел.:" + Символы.НПП+СокрЛП(КонтактноеЛицоМобильный))
		+ ?(ПустаяСтрока(КонтактноеЛицоEmail),"",", Email:" + Символы.НПП+СокрЛП(КонтактноеЛицоEmail));
		
		Шапка.Параметры.КонтактноеЛицоПредставление = КонтактноеЛицоПредставление;
		Шапка.Параметры.РасшифровкаКонтактноеЛицо = ВыборкаЗ.КонтактноеЛицо;
		
		ТабличныйДокумент.Вывести(Шапка);
		
		КоличествоМествидУпаковкиПредставление = "";
		НайденныеСтроки = ТаблицаУпаковки.НайтиСтроки(ТекущийОтбор);
		Для Каждого ТекущаяУпаковка Из НайденныеСтроки Цикл			
			Если ЗначениеЗаполнено(ТекущаяУпаковка.ТипГруза) Тогда
				Продолжить;
			КонецЕсли;
			ТекстПредставления = ТекущаяУпаковка.ТипГруза;
			КоличествоМествидУпаковкиПредставление = КоличествоМествидУпаковкиПредставление + ?(ПустаяСтрока(КоличествоМествидУпаковкиПредставление),"",", ") + ТекстПредставления;
		КонецЦикла;
		ТипГрузов.Параметры.ГрузовоеМесто = КоличествоМествидУпаковкиПредставление;
		ТабличныйДокумент.Вывести(ТипГрузов);
		
		ВидТранспорта.Параметры.ТипТранспорта = ВыборкаЗ.ТипТС;
		ТабличныйДокумент.Вывести(ВидТранспорта);
		
		АдресМаркеровка.Параметры.АдресПолучения = ВыборкаЗ.АдресПолученияПредставление;
		АдресМаркеровка.Параметры.РасшифровкаАдресПолучения = ВыборкаЗ.АдресПолучения;		
		ТабличныйДокумент.Вывести(АдресМаркеровка);
		
		упЕдиницаИзмеренияМассы = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияМассы");
		упЕдиницаИзмеренияОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияОбъема");
		
		// ДанныеГрузовогоУпаковки
		упЕдиницаИзмеренияГрузовыхМест = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияГрузовыхМест");
		КоличествоМествидУпаковкиПредставление = "";
		Если ВыборкаЗ.КоличествоМест Тогда
			КоличествоМествидУпаковкиПредставление = Формат(ВыборкаЗ.КоличествоМест,ТекстФормата) + сткПредставления.КоличествоМест;
		КонецЕсли;
		ДанныеГрузовогоУпаковки.Параметры.ВидУпаковки = КоличествоМествидУпаковкиПредставление;
		
		ДанныеГрузовогоУпаковки.Параметры.ВесГруза = "";
		ДанныеГрузовогоУпаковки.Параметры.ОбъемГруза = "";
		ДанныеГрузовогоУпаковки.Параметры.ОценочнаяСтоимость = "";		
		НайденныеСтроки = ТаблицаОбщие.НайтиСтроки(ТекущийОтбор);
		Для Каждого ТекущаяВГХ Из НайденныеСтроки Цикл			
			ДанныеГрузовогоУпаковки.Параметры.ВесГруза = Формат(ТекущаяВГХ.Вес,ТекстФормата) + " " + сткПредставления.Масса;
			ДанныеГрузовогоУпаковки.Параметры.ОбъемГруза = Формат(ТекущаяВГХ.Объем,ТекстФормата) + " " + сткПредставления.Объем;	
			Если ВедетсяВалютныйУчет Тогда
				ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ТекущаяВГХ.ОценочнаяСтоимость,Валюта);
			Иначе	
				ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ТекущаяВГХ.ОценочнаяСтоимость);
			КонецЕсли;
			ДанныеГрузовогоУпаковки.Параметры.ОценочнаяСтоимость = ОценочнаяСтоимостьПредставление;
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ДанныеГрузовогоУпаковки);
		
		упЕдиницаИзмеренияЛинейныхГабаритов = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияЛинейныхГабаритов");
		ЕдДлинна = СокрЛП(упЕдиницаИзмеренияЛинейныхГабаритов);
		ЕдДлинна = "";
		
		ВыводРазмераУпаковки = Ложь;		
		НайденныеСтроки = ТаблицаВГХ.НайтиСтроки(ТекущийОтбор);
		Шаг = 0;
		Для Каждого ТекущаяВГХ Из НайденныеСтроки Цикл
			Шаг = Шаг + 1;
			Если ВыводРазмераУпаковки = Ложь Тогда
				ШапкаСтрокиВГХ.Параметры.ВГХГруза = ТекущаяВГХ.ГрузовоеМесто + " " + Формат(ТекущаяВГХ.Высота,ТекстФормата)
				+ "х" + Формат(ТекущаяВГХ.Длина,ТекстФормата) + "х" + Формат(ТекущаяВГХ.Ширина,ТекстФормата) + сткПредставления.КоличествоЗагрузочныхМетров
				+ ", вес: " + Формат(ТекущаяВГХ.Масса,ТекстФормата) + " " +сткПредставления.Масса
				+ ", объем: " + Формат(ТекущаяВГХ.Объем,ТекстФормата) + " " +сткПредставления.Объем;
				ТабличныйДокумент.Вывести(ШапкаСтрокиВГХ);
				ВыводРазмераУпаковки = Истина;
				Продолжить;
			КонецЕсли;
			СтрокаВГХ.Параметры.СтрокаВГХ = ТекущаяВГХ.ГрузовоеМесто + " " + Формат(ТекущаяВГХ.Высота,ТекстФормата)
			+ "х" + Формат(ТекущаяВГХ.Длина,ТекстФормата) + "х" + Формат(ТекущаяВГХ.Ширина,ТекстФормата) + сткПредставления.КоличествоЗагрузочныхМетров
			+ ", вес: " + Формат(ТекущаяВГХ.Масса,ТекстФормата) + " " +сткПредставления.Масса
			+ ", объем: " + Формат(ТекущаяВГХ.Объем,ТекстФормата) + " " +сткПредставления.Объем;
			ТабличныйДокумент.Вывести(СтрокаВГХ);
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(Подвал);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДокументОснование);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ПоручениеЭкспедитору()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент
//
Функция СформироватьПечатнуюФорму_МХ_1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = НЕ ПараметрыПечати = Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.ПРИЕМЕ_ПЕРЕДАЧЕ_ТМЦ";
	ТабличныйДокумент.РазмерСтраницы = "A4";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	Если ЭтоРейс Тогда
		ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.ПРИЕМЕ_ПЕРЕДАЧЕ_ТМЦ";
	КонецЕсли;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_МХ_1");
	
	ОбластьШапки = Макет.ПолучитьОбласть("Шапка");
	ОбалстьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбалстьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбалстьИтогоТаблицы = Макет.ПолучитьОбласть("ИтогоТаблицы");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка В(&МассивОбъектов)";
	Если ЭтоРейс Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|ГДЕ
		|	упРейсЗадания.Ссылка В(&МассивОбъектов)";
	КонецЕсли;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(упРейсЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)) КАК Рейс,
	|	втЗадания.Задание КАК Задание
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	втЗадания КАК втЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс.Задания КАК упРейсЗадания
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсов
	|			ПО упРейсЗадания.Ссылка = упСтатусыРейсов.Документ
	|		ПО втЗадания.Задание = упРейсЗадания.Задание
	|ГДЕ
	|	ВЫБОР
	|			КОГДА упСтатусыРейсов.Статус ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ НЕ упСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗадания.Задание,
	|	ЕСТЬNULL(упРейсЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс,
	|	втРейсы.Задание,
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза.НомерКИС КАК Номер,
	|	упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза.ДатаКИС КАК Дата,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя.ИНН
	|	КОНЕЦ КАК ИННГрузоотправитель,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя.ИНН
	|	КОНЕЦ КАК ИННГрузополучатель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя.КодПоОКПО
	|	КОНЕЦ КАК Грузополучатель_ОКПО,
	|	упЗаданиеНаПеревозкуГруза.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГруза.Организация) КАК ПредставлениеОрганизации,
	|	упЗаданиеНаПеревозкуГруза.Организация.КодПоОКПО КАК ОрганизацияОКПО,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.Соглашение = ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.Соглашение.Номер
	|	КОНЕЦ КАК Соглашение_Номер,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.Соглашение = ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.Соглашение.Дата
	|	КОНЕЦ КАК Соглашение_Дата
	|ИЗ
	|	втРейсы КАК втРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ПО втРейсы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|		ПО втРейсы.Рейс = ПеревозчикПоРейсуСП.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.НомерСтроки ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ Состав.НомерСтроки + ГрузовыеМеста.НомерСтроки
	|	КОНЕЦ КАК Номер,
	|	Состав.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Состав.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Состав.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|					ТОГДА ВЫБОР
	|							КОГДА Состав.Номенклатура.ЭтоГруппа
	|								ТОГДА ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|							ИНАЧЕ Состав.Номенклатура.УпаковкаДляОтчетов
	|						КОНЕЦ
	|				ИНАЧЕ Состав.УпаковкаНоменклатуры
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	Состав.Номенклатура.Код КАК КодНоменклатуры,
	|	ЕСТЬNULL(Состав.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(Состав.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(Состав.Сумма, 0) КАК Сумма
	|ИЗ
	|	втРейсы КАК втРейсы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК Состав
	|			ПО ГрузовыеМеста.ГрузовоеМесто = Состав.Ссылка
	|		ПО втРейсы.Задание = ГрузовыеМеста.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Задание,
	|	Номер";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаЗ = ПакетЗапросов[2].Выбрать();
	ТаблицаТМЦ = ПакетЗапросов[3].Выгрузить();
	
	ФорматКоличества = "ЧН=0; ЧГ=0";
	
	Пока ВыборкаЗ.Следующий() Цикл 
		
		тдНовый = Новый ТабличныйДокумент;
		тдНовый.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПРИЕМ_ТМЦ";
		
		ДокументОснование = ВыборкаЗ.Задание;
		ТекущийОтбор = Новый Структура("Задание",ДокументОснование);
		
		ОбластьШапки.Параметры.Дата = Формат(ВыборкаЗ.Дата,"ДЛФ=D");
		ОбластьШапки.Параметры.Номер = ВыборкаЗ.Номер;
		
		// --- Получатель ---
		ОбластьШапки.Параметры.ПредставлениеКонтрагента = КонтактныеДанныеКонтрагентаМХ1(ВыборкаЗ.Грузополучатель);
		ОбластьШапки.Параметры.ОКПОКонтрагента = ВыборкаЗ.Грузополучатель_ОКПО;
		
		// Организация
		ОбластьШапки.Параметры.ПредставлениеОрганизации = КонтактныеДанныеКонтрагентаМХ1(ВыборкаЗ.Организация);
		ОбластьШапки.Параметры.ОКПООрганизации = ВыборкаЗ.ОрганизацияОКПО;
		
		ОбластьШапки.Параметры.НомерСоглашения = ВыборкаЗ.Соглашение_Номер;
		ОбластьШапки.Параметры.ДатаСоглашения = Формат(ВыборкаЗ.Соглашение_Дата,"ДЛФ=D");
		
		тдНовый.Вывести(ОбластьШапки);
		тдНовый.Вывести(ОбалстьШапкаТаблицы);
		
		НайденныеСтроки = ТаблицаТМЦ.НайтиСтроки(ТекущийОтбор);
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		КоличествоИтого = 0;
		ИтогоСумма = 0;		
		СтрокаНомер = 1;
		КоличествоСтрок = НайденныеСтроки.Количество();
		
		Если КоличествоСтрок = 0 Тогда
			ОбалстьСтрокаТаблицы.Параметры.НомерСтроки = СтрокаНомер;
			тдНовый.Вывести(ОбалстьСтрокаТаблицы);
		КонецЕсли;
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбалстьСтрокаТаблицы.Параметры.Заполнить(НайденнаяСтрока);			
			
			ОбалстьСтрокаТаблицы.Параметры.ЕдиницаИзмерения ="";
			ОбалстьСтрокаТаблицы.Параметры.КодЕдиницы ="";
				
			Если ТипЗнч(НайденнаяСтрока.ЕдиницаИзмерения)=ТИП("СправочникСсылка.упУпаковкиНоменклатуры") И ЗначениеЗаполнено(НайденнаяСтрока.ЕдиницаИзмерения) Тогда
				ОбалстьСтрокаТаблицы.Параметры.ЕдиницаИзмерения =Строка(НайденнаяСтрока.ЕдиницаИзмерения.ЕдиницаИзмерения);
				ОбалстьСтрокаТаблицы.Параметры.КодЕдиницы =Строка(НайденнаяСтрока.ЕдиницаИзмерения.ЕдиницаИзмерения.Код);
			КонецЕсли;
			ОбалстьСтрокаТаблицы.Параметры.НомерСтроки = СтрокаНомер;
			 
			ОбалстьСтрокаТаблицы.Параметры.Количество = Формат(НайденнаяСтрока.Количество,ФорматКоличества);
			ОбалстьСтрокаТаблицы.Параметры.Цена = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(НайденнаяСтрока.Цена);
			ОбалстьСтрокаТаблицы.Параметры.Сумма = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(НайденнаяСтрока.Сумма);
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбалстьСтрокаТаблицы);
			СтрокаСПодвалом.Добавить(ОбалстьИтогоТаблицы);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(СтрокаСПодвалом);
			КонецЕсли;
			Попытка
				Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
					тдНовый.Вывести(ОбалстьСтрокаТаблицы);
				Иначе
					тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					тдНовый.Вывести(ОбалстьШапкаТаблицы);
					тдНовый.Вывести(ОбалстьСтрокаТаблицы);
				КонецЕсли;
			Исключение
				тдНовый.Вывести(ОбалстьСтрокаТаблицы);
			КонецПопытки;
		
			КоличествоИтого = КоличествоИтого + НайденнаяСтрока.Количество;
			ИтогоСумма = ИтогоСумма + НайденнаяСтрока.Сумма;
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;
				
		ОбалстьИтогоТаблицы.Параметры.КоличествоИтого = Формат(КоличествоИтого,ФорматКоличества);
		ОбалстьИтогоТаблицы.Параметры.ИтогоСумма = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ИтогоСумма);
		
		тдНовый.Вывести(ОбалстьИтогоТаблицы);
				
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.Вывести(ОбластьПодвал);
			Иначе
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
				тдНовый.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			тдНовый.Вывести(ОбластьПодвал);
		КонецПопытки;
		тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
		
		ТабличныйДокумент.Вывести(тдНовый);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДокументОснование);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_МХ_1()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент
//
Функция СформироватьПечатнуюФорму_М_15(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ЭтоРейс = НЕ ПараметрыПечати = Неопределено И ПараметрыПечати.Свойство("ЭтоРейс");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаданиеНаПеревозкуГруза.НАКЛАДНАЯ";
	ТабличныйДокумент.РазмерСтраницы = "A4";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	Если ЭтоРейс Тогда
		ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.НАКЛАДНАЯ";
	КонецЕсли;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_М_15");
	
	ОбластьШапки = Макет.ПолучитьОбласть("Шапка");
	ОбалстьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбалстьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГруза.Ссылка КАК Задание
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|ГДЕ
	|	упЗаданиеНаПеревозкуГруза.Ссылка В(&МассивОбъектов)";
	Если ЭтоРейс Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|ГДЕ
		|	упРейсЗадания.Ссылка В(&МассивОбъектов)";
	КонецЕсли;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(упРейсЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка)) КАК Рейс,
	|	втЗадания.Задание КАК Задание
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	втЗадания КАК втЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс.Задания КАК упРейсЗадания
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсов
	|			ПО упРейсЗадания.Ссылка = упСтатусыРейсов.Документ
	|		ПО втЗадания.Задание = упРейсЗадания.Задание
	|ГДЕ
	|	ВЫБОР
	|			КОГДА упСтатусыРейсов.Статус ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ НЕ упСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗадания.Задание,
	|	ЕСТЬNULL(упРейсЗадания.Ссылка, ЗНАЧЕНИЕ(Документ.упРейс.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс,
	|	втРейсы.Задание,
	|	упЗаданиеНаПеревозкуГруза.Номер,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза ЕСТЬ NULL
	|				ИЛИ упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза.НомерКИС
	|	КОНЕЦ КАК НомерКИС,
	|	упЗаданиеНаПеревозкуГруза.Дата,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентОтправителя.ИНН
	|	КОНЕЦ КАК ИННГрузоотправитель,
	|	упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя.ИНН
	|	КОНЕЦ КАК ИННГрузополучатель,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.КонтрагентПолучателя.КодПоОКПО
	|	КОНЕЦ КАК Грузополучатель_ОКПО,
	|	упЗаданиеНаПеревозкуГруза.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(упЗаданиеНаПеревозкуГруза.Организация) КАК ПредставлениеОрганизации,
	|	упЗаданиеНаПеревозкуГруза.Организация.КодПоОКПО КАК ОрганизацияОКПО,
	|	упЗаданиеНаПеревозкуГруза.Соглашение КАК Соглашение,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.Соглашение = ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.Соглашение.Номер
	|	КОНЕЦ КАК Соглашение_Номер,
	|	ВЫБОР
	|		КОГДА упЗаданиеНаПеревозкуГруза.Соглашение = ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ упЗаданиеНаПеревозкуГруза.Соглашение.Дата
	|	КОНЕЦ КАК Соглашение_Дата
	|ИЗ
	|	втРейсы КАК втРейсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|		ПО втРейсы.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|		ПО втРейсы.Рейс = ПеревозчикПоРейсуСП.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Рейс КАК Рейс,
	|	втРейсы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.НомерСтроки ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ Состав.НомерСтроки + ГрузовыеМеста.НомерСтроки
	|	КОНЕЦ КАК Номер,
	|	Состав.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Состав.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Состав.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|					ТОГДА ВЫБОР
	|							КОГДА Состав.Номенклатура.ЭтоГруппа
	|								ТОГДА ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|							ИНАЧЕ Состав.Номенклатура.УпаковкаДляОтчетов.ЕдиницаИзмерения
	|						КОНЕЦ
	|				ИНАЧЕ Состав.УпаковкаНоменклатуры.ЕдиницаИзмерения
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	Состав.Номенклатура.Код КАК КодНоменклатуры,
	|	ЕСТЬNULL(Состав.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(Состав.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(Состав.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ЕСТЬNULL(Состав.СуммаНДС, 0) КАК СуммаНДС,
	|	ЕСТЬNULL(Состав.Сумма, 0) КАК Сумма
	|ИЗ
	|	втРейсы КАК втРейсы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК Состав
	|			ПО ГрузовыеМеста.ГрузовоеМесто = Состав.Ссылка
	|		ПО втРейсы.Задание = ГрузовыеМеста.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Задание,
	|	Номер";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаЗ = ПакетЗапросов[2].Выбрать();
	ТаблицаТМЦ = ПакетЗапросов[3].Выгрузить();
	
	ФорматКоличества = "ЧДЦ=3; ЧРД=.; ЧН=0.000; ЧГ=0";
	ФорматСуммы = "ЧДЦ=2; ЧРД=.; ЧН=0.00; ЧГ=0";
	Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
		Валюта = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	Иначе	
		Валюта = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
	КонецЕсли;
		
	Пока ВыборкаЗ.Следующий() Цикл 
		
		тдНовый = Новый ТабличныйДокумент;
		тдНовый.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПРИЕМ_ТМЦ";
		
		ДокументОснование = ВыборкаЗ.Задание;
		ТекущийОтбор = Новый Структура("Задание",ДокументОснование);
		
		ОбластьШапки.Параметры.НомерНаклодной = ВыборкаЗ.НомерКИС;
		ОбластьШапки.Параметры.ДатДокумента = Формат(ВыборкаЗ.Дата,"ДЛФ=D");
				
		// Организация		
		ОбластьШапки.Параметры.ПредставлениеОрганизации = КонтактныеДанныеКонтрагентаМХ1(ВыборкаЗ.Организация);
		ОбластьШапки.Параметры.ОКПООрганизации 			= Формат(ВыборкаЗ.ОрганизацияОКПО,"ЧГ=0");
		
		ОбластьШапки.Параметры.СоглашениеПолучателя = "";
		
		Если ЗначениеЗаполнено(ВыборкаЗ.Соглашение) Тогда
			ПредставлениеСоглашения = Строка(ВыборкаЗ.Соглашение);
			ПредставлениеСоглашения = ПредставлениеСоглашения + ?(ПустаяСтрока(ВыборкаЗ.Соглашение_Номер),"", " " + ВыборкаЗ.Соглашение_Номер);
			ПредставлениеСоглашения = ПредставлениеСоглашения + ?(ЗначениеЗаполнено(ВыборкаЗ.Соглашение_Дата)," от" + Формат(ВыборкаЗ.Соглашение_Дата,"ДФ=dd.MM.yyyy"),"");
			ОбластьШапки.Параметры.СоглашениеПолучателя = ПредставлениеСоглашения;
		КонецЕсли;
		
		// КонтрагентОтправителя
		ОбластьШапки.Параметры.КонтрагентОтправителя = Строка(ВыборкаЗ.Грузоотправитель);
		
		// КонтрагентПолучателя
		Грузополучатель = ВыборкаЗ.Грузополучатель;
		ОбластьШапки.Параметры.КонтрагентПолучателя = Строка(Грузополучатель);
		ГрузополучательАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Грузополучатель, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		ОбластьШапки.Параметры.АдресПолучателя = Строка(ГрузополучательАдрес);
		
		тдНовый.Вывести(ОбластьШапки);
		тдНовый.Вывести(ОбалстьШапкаТаблицы);
		
		НайденныеСтроки = ТаблицаТМЦ.НайтиСтроки(ТекущийОтбор);
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ИтогоСуммаНДС = 0;
		ИтогоСумма = 0;		
		ИтогоКоличество = 0;		
		
		СтрокаНомер = 0;
		КоличествоСтрок = НайденныеСтроки.Количество();
		
		Если КоличествоСтрок = 0 Тогда
			тдНовый.Вывести(ОбалстьСтрокаТаблицы);
		КонецЕсли;
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбалстьСтрокаТаблицы.Параметры.Заполнить(НайденнаяСтрока);
			
			ОбалстьСтрокаТаблицы.Параметры.КодЕдИзмерения = ?(ЗначениеЗаполнено(НайденнаяСтрока.ЕдиницаИзмерения),Строка(НайденнаяСтрока.ЕдиницаИзмерения.Код),"");
			ОбалстьСтрокаТаблицы.Параметры.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(НайденнаяСтрока.ЕдиницаИзмерения),Строка(НайденнаяСтрока.ЕдиницаИзмерения),"");
			Количество = Формат(НайденнаяСтрока.Количество,ФорматКоличества);
			ОбалстьСтрокаТаблицы.Параметры.Количество = Количество;
			ОбалстьСтрокаТаблицы.Параметры.КоличествоНоменклатура = Количество;
			                                       
			ОбалстьСтрокаТаблицы.Параметры.Цена = Формат(НайденнаяСтрока.Цена,ФорматСуммы);
			ОбалстьСтрокаТаблицы.Параметры.Сумма = Формат(НайденнаяСтрока.Сумма,ФорматСуммы);
			
			ОбалстьСтрокаТаблицы.Параметры.СуммаБезНДС = Формат(НайденнаяСтрока.СуммаБезНДС,ФорматСуммы);
			ОбалстьСтрокаТаблицы.Параметры.СуммаНДС = Формат(НайденнаяСтрока.СуммаНДС,ФорматСуммы);
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбалстьСтрокаТаблицы);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(СтрокаСПодвалом);
			КонецЕсли;
			Попытка
				Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
					тдНовый.Вывести(ОбалстьСтрокаТаблицы);
				Иначе
					тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					тдНовый.Вывести(ОбалстьШапкаТаблицы);
					тдНовый.Вывести(ОбалстьСтрокаТаблицы);
				КонецЕсли;
			Исключение
				тдНовый.Вывести(ОбалстьСтрокаТаблицы);
			КонецПопытки;
		
			ИтогоСуммаНДС = ИтогоСуммаНДС + НайденнаяСтрока.СуммаНДС;
			ИтогоСумма = ИтогоСумма + НайденнаяСтрока.Сумма;
			ИтогоКоличество = ИтогоКоличество + Количество;
			
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;
		
		Если СтрокаНомер Тогда
			ОбластьПодвал.Параметры.количество = формат(СтрокаНомер,"ЧН=0; ЧГ=0") + " наименований";
		КонецЕсли;
		
		ОбластьПодвал.Параметры.сумма = РаботаСКурсамиВалют.СформироватьСуммуПрописью(ИтогоСумма, Валюта, Ложь);
		ОбластьПодвал.Параметры.НДС = РаботаСКурсамиВалют.СформироватьСуммуПрописью(ИтогоСуммаНДС, Валюта, Ложь);
		
		ОбластьПодвал.Параметры.Руководитель= ?(ЗначениеЗаполнено(ВыборкаЗ.Организация),Строка(ВыборкаЗ.Организация.РуководительПредприятия),"");
		ОбластьПодвал.Параметры.РуководительДолжность= "";
		ОбластьПодвал.Параметры.ГлавныйБухгалтер= ?(ЗначениеЗаполнено(ВыборкаЗ.Организация),Строка(ВыборкаЗ.Организация.ГлавныйБухгалтер),"");
				
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.Вывести(ОбластьПодвал);
			Иначе
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
				тдНовый.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			тдНовый.Вывести(ОбластьПодвал);
		КонецПопытки;
		тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
		
		ТабличныйДокумент.Вывести(тдНовый);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДокументОснование);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_М_15()

// Формирование печатной формы УПД
Функция ПечатьУПД(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_УПД";
	ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов,Истина);
	ЗаполнитьТабличныйДокументУПД(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);

  Возврат ТабличныйДокумент;
  
КонецФункции

Процедура ЗаполнитьТабличныйДокументУПД(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати) 
	
	тбДанныеШапка = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
	
	Если НЕ тбДанныеСтроки.Количество() Тогда
		Возврат;
	КонецЕсли;	
	
	текШаблон = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_УниверсальныйПередаточныйДокумент");
	
	ОбластьШапка = текШаблон.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок = текШаблон.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока = текШаблон.ПолучитьОбласть("Строка");
	ОбластьИтого = текШаблон.ПолучитьОбласть("Итого");
	ОбластьПодвал = текШаблон.ПолучитьОбласть("Подвал");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		тдНовый = Новый ТабличныйДокумент;
		тдНовый.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УПД";
				
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);
		ОбластьШапка.Параметры.ИННОрганизация = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),текСтрокаШапка.Организация.ИНН + " / " +текСтрокаШапка.Организация.КПП,"");
		ОбластьШапка.Параметры.ИННКонтрагент = ?(ЗначениеЗаполнено(текСтрокаШапка.КонтрагентЗаказчика),текСтрокаШапка.КонтрагентЗаказчика.ИНН + " / " +текСтрокаШапка.КонтрагентЗаказчика.КПП,"");		
		
		РасчетныйСчетОрганизации = "";
		Если ЗначениеЗаполнено(текСтрокаШапка.Организация.ОсновнойБанковскийСчет) Тогда
			ТекущийСчет = текСтрокаШапка.Организация.ОсновнойБанковскийСчет;
			РасчетныйСчетОрганизации = СтрШаблон("р/с %1, в банке %2 к/с %3",ТекущийСчет.НомерСчета,ТекущийСчет.БанкДляРасчетов.Наименование,ТекущийСчет.БанкДляРасчетов.КоррСчет);
		КонецЕсли;
		ОбластьШапка.Параметры.РасчетныйСчетОрганизации = РасчетныйСчетОрганизации;
		
		ОбластьШапка.Параметры.Номер = текСтрокаШапка.Номер;
		ОбластьШапка.Параметры.Дата = Формат(текСтрокаШапка.Дата,"ДЛФ=DD");
		ОбластьШапка.Параметры.УПДНомер = текСтрокаШапка.НомерКИС;
		
		РасчетныйСчетПокупателя = "";
		Если ТипЗнч(текСтрокаШапка.Грузополучатель) = ТИП("СправочникСсылка.упКонтрагенты") И ЗначениеЗаполнено(текСтрокаШапка.Грузополучатель) Тогда
			Если ЗначениеЗаполнено(текСтрокаШапка.Грузополучатель.ОсновнойБанковскийСчет) Тогда
				ТекущийСчет = текСтрокаШапка.Грузополучатель.ОсновнойБанковскийСчет;
				РасчетныйСчетПокупателя = СтрШаблон("р/с %1, в банке %2 к/с %3",ТекущийСчет.НомерСчета,ТекущийСчет.БанкДляРасчетов.Наименование,ТекущийСчет.БанкДляРасчетов.КоррСчет);
			КонецЕсли;
		КонецЕсли;
		ОбластьШапка.Параметры.РасчетныйСчетПокупателя = РасчетныйСчетПокупателя;
		Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
		Если ЗначениеЗаполнено(Валюта) Тогда
			ОбластьШапка.Параметры.Валюта	= Валюта.НаименованиеПолное + ", " + Валюта.Код;	
		КонецЕсли;
		ОбластьШапка.Параметры.ПредставлениеОтправительАдрес = текСтрокаШапка.ПредставлениеОтправитель + ?(ПустаяСтрока(текСтрокаШапка.АдресОтгрузки), "", ", " + Сокрлп(текСтрокаШапка.АдресОтгрузки));
		ОбластьШапка.Параметры.ПредставлениеГрузополучателяАдрес = текСтрокаШапка.ПредставлениеПолучатель + ?(ПустаяСтрока(текСтрокаШапка.АдресПолучения), "", ", " + Сокрлп(текСтрокаШапка.АдресПолучения));
		
		// Штрихкод
		Рисунок = ОбластьШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.ДокументОснование));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		тдНовый.Вывести(ОбластьШапка);
	    тдНовый.Вывести(ОбластьЗаголовок);
		
		сткОтбор  = Новый Структура("Документ", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбДанныеСтроки.НайтиСтроки(сткОтбор);
		
		стУпаковки = Новый Соответствие;
		ИтогоСуммаНДС = 0;
		ИтогоСуммаСНДС = 0;
		ИтогоСуммаБезНДС = 0;
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтрока.Параметры.Заполнить(текЭлементСтрока);
			Если ЗначениеЗаполнено(текЭлементСтрока.СтавкаНДС) Тогда
				ОбластьСтрока.Параметры.СтавкаНДС = Строка(текЭлементСтрока.СтавкаНДС) + "%";
			КонецЕсли;
						
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтрока);
			СтрокаСПодвалом.Добавить(ОбластьИтого);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(СтрокаСПодвалом);
			КонецЕсли;
			Попытка
				Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
					тдНовый.Вывести(ОбластьСтрока);
				Иначе
					тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					тдНовый.Вывести(ОбластьЗаголовок);
					тдНовый.Вывести(ОбластьСтрока);
				КонецЕсли;
			Исключение
				тдНовый.Вывести(ОбластьСтрока);
			КонецПопытки;
			
			ИтогоСуммаНДС  = ИтогоСуммаНДС + текЭлементСтрока.СуммаНДС;
			ИтогоСуммаСНДС = ИтогоСуммаСНДС + текЭлементСтрока.СуммаСНДС;
			ИтогоСуммаБезНДС = ИтогоСуммаБезНДС + текЭлементСтрока.СуммаБезНДС;
			
			КоличествоУпаковок = стУпаковки.Получить(текЭлементСтрока.ЕдиницаИзмерения);
			стУпаковки.Вставить(текЭлементСтрока.ЕдиницаИзмерения, ?(ЗначениеЗаполнено(КоличествоУпаковок), КоличествоУпаковок, 0) + текЭлементСтрока.Количество);
		КонецЦикла;
		
		ИтогоКоличество = "";
		Для каждого текСтрока Из стУпаковки Цикл
			ИтогоКоличество = ИтогоКоличество + текСтрока.Значение + " " + текСтрока.Ключ + ", ";
		КонецЦикла;
		ИтогоКоличество = Лев(ИтогоКоличество, СтрДлина(ИтогоКоличество) - 2);
		
		ОбластьИтого.Параметры.ИтогоКоличество = ИтогоКоличество;
		ОбластьИтого.Параметры.ИтогоСуммаНДС = ИтогоСуммаНДС;
		ОбластьИтого.Параметры.ИтогоСуммаСНДС = ИтогоСуммаСНДС;
		ОбластьИтого.Параметры.ИтогоСуммаБезНДС = ИтогоСуммаБезНДС;
		тдНовый.Вывести(ОбластьИтого);
		
		ОбластьПодвал.Параметры.Заполнить(текСтрокаШапка);
		
		ОбластьПодвал.Параметры.ФИОРуководителя = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),Строка(текСтрокаШапка.Организация.РуководительПредприятия),"");
		ОбластьПодвал.Параметры.Руководитель = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),текСтрокаШапка.Организация.РуководительПредприятия,Неопределено);
		
		Если ЗначениеЗаполнено(текСтрокаШапка.Организация) Тогда
			Если ЗначениеЗаполнено(текСтрокаШапка.Организация.ГлавныйБухгалтер) Тогда
				ФИОГлавногоБухгалтера = Строка(текСтрокаШапка.Организация.ГлавныйБухгалтер);
			Иначе
				ФИОГлавногоБухгалтера = Строка(текСтрокаШапка.Организация.РуководительПредприятия);
			КонецЕсли;	
		Иначе
			 ФИОГлавногоБухгалтера = "";
		КонецЕсли;	
		 
		ОбластьПодвал.Параметры.ФИОГлавногоБухгалтера = ФИОГлавногоБухгалтера;
		ОбластьПодвал.Параметры.ГлавныйБухгалтер = ?(ЗначениеЗаполнено(текСтрокаШапка.Организация),текСтрокаШапка.Организация.ГлавныйБухгалтер,Неопределено);		
		ОбластьПодвал.Параметры.ПредставлениеОрганизации = Строка(текСтрокаШапка.Организация) +" ИНН/КПП "+ текСтрокаШапка.Организация.ИНН + " / " + текСтрокаШапка.Организация.КПП;		
		ОбластьПодвал.Параметры.ПредставлениеКонтрагента = Строка(текСтрокаШапка.КонтрагентЗаказчика) +" ИНН/КПП "+ текСтрокаШапка.КонтрагентЗаказчика.ИНН + " / " + текСтрокаШапка.КонтрагентЗаказчика.КПП;
		ОбластьПодвал.Параметры.ДатаОтгрузкиГруза = Формат(текСтрокаШапка.ДатаОтгрузкиГруза,"ДЛФ=DD");
		ОбластьПодвал.Параметры.Основание = Строка(текСтрокаШапка.СоглашениеНаименование) + " " + текСтрокаШапка.СоглашениеНомер + " от "
			+ Формат(текСтрокаШапка.СоглашениеДата,"ДЛФ=DD")+ " доверенность "+ текСтрокаШапка.ДоверенностьНомер + " от " + Формат(текСтрокаШапка.ДоверенностьДата,"ДЛФ=DD");
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.Вывести(ОбластьПодвал);
			Иначе
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
				тдНовый.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			тдНовый.Вывести(ОбластьПодвал);
		КонецПопытки;
		
		тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
		
		ТабличныйДокумент.Вывести(тдНовый);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.ДокументОснование);
	КонецЦикла;
		
КонецПроцедуры // ЗаполнитьТабличныйДокументУПД()

#Область СобытияЖурналаРегистрации

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Задание на перевозку груза.%1'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());

КонецФункции

Функция СобытиеЖРПроверкаСовместимостиВидовПеревозок() Экспорт
	
	СобытиеЖР = СобытиеЖурналаРегистрации();
	СобытиеЖР = СтрШаблон(СобытиеЖР, НСтр("ru = 'Проверка совместимости видов перевозок'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
	Возврат СобытиеЖР;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПечатныхФорм(МассивОбъектов, НужныДоверенности = Ложь)
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	МИНИМУМ(упРаботыПоРейсу.ПлановоеВремяНачалаРабот) КАК ПлановоеВремяНачалаРабот,
	               |	упРаботыПоРейсу.Задание
	               |ПОМЕСТИТЬ ДатыОтгрузкиПоЗаданию
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	               |ГДЕ
	               |	упРаботыПоРейсу.Задание В(&МассивОбъектов)
	               |	И упРаботыПоРейсу.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упРаботыПоРейсу.Задание
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	упДоверенность.ДолжностьДоверенноеЛицо,
	               |	упДоверенность.Номер,
	               |	упДоверенность.Дата,
	               |	ПРЕДСТАВЛЕНИЕ(упДоверенность.ДоверенноеЛицо) КАК ДоверенноеЛицо,
	               |	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика,
	               |	ПРЕДСТАВЛЕНИЕ(упПеревозчикПоРейсуСрезПоследних.Водитель1) КАК Водитель,
	               |	упДоверенность.Задание,
	               |	упПеревозчикПоРейсуСрезПоследних.Перевозчик
	               |ПОМЕСТИТЬ втДоверенность
	               |ИЗ
	               |	Документ.упДоверенность КАК упДоверенность
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
	               |		ПО упДоверенность.Рейс = упПеревозчикПоРейсуСрезПоследних.Рейс
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |		ПО упДоверенность.Рейс = упСтатусыРейсовСрезПоследних.Документ
	               |ГДЕ
	               |	&НужныДоверенности
	               |	И упДоверенность.Задание В(&МассивОбъектов)
	               |	И НЕ упДоверенность.ПометкаУдаления
	               |	И НЕ упДоверенность.Рейс.ПометкаУдаления
	               |	И НЕ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	упДоверенность.Рейс.Дата УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаданиеНаПеревозку.Ссылка КАК ДокументОснование,
	               |	ВЫБОР
	               |		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	               |			ТОГДА ЗаданиеНаПеревозку.Отправитель
	               |		ИНАЧЕ ЗаданиеНаПеревозку.КонтрагентОтправителя
	               |	КОНЕЦ КАК Грузоотправитель,
	               |	ВЫБОР
	               |		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	               |			ТОГДА ЗаданиеНаПеревозку.Получатель
	               |		ИНАЧЕ ЗаданиеНаПеревозку.КонтрагентПолучателя
	               |	КОНЕЦ КАК Грузополучатель,
	               |	ЗаданиеНаПеревозку.Заказчик.НаименованиеПолное КАК Плательщик,
	               |	ЗаданиеНаПеревозку.КонтрагентОтправителя.КодПоОКПО КАК ПоставщикПоОКПО,
	               |	ЗаданиеНаПеревозку.КонтрагентПолучателя.КодПоОКПО КАК ГрузополучательПоОКПО,
	               |	ЗаданиеНаПеревозку.АдресОтправления КАК АдресОтгрузки,
	               |	ЗаданиеНаПеревозку.Номер КАК Номер,
	               |	ЗаданиеНаПеревозку.Масса КАК МассаБруттоВсего,
	               |	ЗаданиеНаПеревозку.Дата КАК Дата,
	               |	ЗаданиеНаПеревозку.КоличествоМест КАК КоличествоМест,
	               |	ЗаданиеНаПеревозку.Организация.Представление КАК Организация,
	               |	ЗаданиеНаПеревозку.Организация КАК ОрганизацияСсылка,
	               |	ЗаданиеНаПеревозку.АдресПолучения,
	               |	ЕСТЬNULL(втДоверенность.Номер, """") КАК ДоверенностьНомер,
	               |	ЕСТЬNULL(втДоверенность.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДоверенностьДата,
	               |	ЕСТЬNULL(втДоверенность.КонтрагентПеревозчика, ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)) КАК КонтрагентПеревозчика,
	               |	ЕСТЬNULL(втДоверенность.Водитель, """") КАК Водитель,
	               |	ЕСТЬNULL(втДоверенность.ДоверенноеЛицо, """") КАК ДоверенноеЛицо,
	               |	ЕСТЬNULL(втДоверенность.ДолжностьДоверенноеЛицо, """") КАК ДолжностьДоверенноеЛицо,
	               |	ЕСТЬNULL(втДоверенность.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)) КАК Перевозчик,
	               |	ВЫБОР
	               |		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	               |			ТОГДА ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.Отправитель)
	               |		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.КонтрагентОтправителя)
	               |	КОНЕЦ КАК ПредставлениеОтправитель,
	               |	ВЫБОР
	               |		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	               |			ТОГДА ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.Получатель)
	               |		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.КонтрагентПолучателя)
	               |	КОНЕЦ КАК ПредставлениеПолучатель,
	               |	ЗаданиеНаПеревозку.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	               |	ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.КонтрагентЗаказчика) КАК ПредставлениеКонтрагентЗаказчика,
	               |	ВЫБОР
	               |		КОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.НомерКИС <> """"
	               |			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.НомерКИС
	               |		ИНАЧЕ ЗаданиеНаПеревозку.НомерКИС
	               |	КОНЕЦ КАК НомерКИС,
	               |	ЗаданиеНаПеревозку.Соглашение.Наименование,
	               |	ЗаданиеНаПеревозку.Соглашение.Дата,
	               |	ЕСТЬNULL(ДатыОтгрузкиПоЗаданию.ПлановоеВремяНачалаРабот, ЗаданиеНаПеревозку.Дата) КАК ДатаОтгрузкиГруза,
	               |	ЗаданиеНаПеревозку.Соглашение.Номер
	               |ИЗ
	               |	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозку
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втДоверенность КАК втДоверенность
	               |		ПО ЗаданиеНаПеревозку.Ссылка = втДоверенность.Задание
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ДатыОтгрузкиПоЗаданию КАК ДатыОтгрузкиПоЗаданию
	               |		ПО (ДатыОтгрузкиПоЗаданию.Задание = ЗаданиеНаПеревозку.Ссылка)
	               |ГДЕ
	               |	ЗаданиеНаПеревозку.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбГрузы.Ссылка КАК Документ,
	               |	тбСоставГруза.Номенклатура.Наименование КАК Номенклатура,
	               |	тбСоставГруза.Номенклатура.Код,
	               |	тбСоставГруза.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	               |	тбСоставГруза.Номенклатура.Артикул КАК НоменклатураАртикул,
	               |	ВЫБОР
	               |		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	               |			ТОГДА 0
	               |		ИНАЧЕ тбСоставГруза.Количество
	               |	КОНЕЦ КАК КоличествоМест,
	               |	ВЫБОР
	               |		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	               |			ТОГДА """"
	               |		ИНАЧЕ тбСоставГруза.УпаковкаНоменклатуры.Наименование
	               |	КОНЕЦ КАК ВидУпаковки,
	               |	тбСоставГруза.Количество КАК Количество,
	               |	тбСоставГруза.Цена,
	               |	ЕСТЬNULL(тбСоставГруза.Номенклатура.Масса, 0) КАК МассаБрутто,
	               |	ЕСТЬNULL(тбСоставГруза.Номенклатура.МассаНетто, 0) КАК МассаНетто,
	               |	тбСоставГруза.Сумма КАК СуммаСНДС,
	               |	тбСоставГруза.СтавкаНДС,
	               |	тбСоставГруза.СуммаНДС,
	               |	тбСоставГруза.СуммаБезНДС,
	               |	тбСоставГруза.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	               |	тбСоставГруза.УпаковкаНоменклатуры.Коэффициент КАК КоличествоВОдномМесте,
	               |	ВЫБОР
	               |		КОГДА тбСоставГруза.Количество <> 0
	               |			ТОГДА тбСоставГруза.СуммаБезНДС / тбСоставГруза.Количество
	               |		ИНАЧЕ тбСоставГруза.Цена
	               |	КОНЕЦ КАК ЦенаБезНДС
	               |ПОМЕСТИТЬ тбСтроки
	               |ИЗ
	               |	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК тбСоставГруза
	               |		ПО тбГрузы.ГрузовоеМесто = тбСоставГруза.Ссылка
	               |ГДЕ
	               |	тбГрузы.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тбСтроки.Документ,
	               |	тбСтроки.Номенклатура,
	               |	тбСтроки.НоменклатураКод,
	               |	тбСтроки.ЕдиницаИзмерения,
	               |	тбСтроки.НоменклатураАртикул,
	               |	тбСтроки.КоличествоМест,
	               |	тбСтроки.ВидУпаковки,
	               |	тбСтроки.Количество,
	               |	тбСтроки.МассаБрутто,
	               |	тбСтроки.МассаНетто,
	               |	тбСтроки.Цена,
	               |	тбСтроки.ЦенаБезНДС,
	               |	тбСтроки.СуммаСНДС,
	               |	ВЫБОР тбСтроки.СтавкаНДС
	               |		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18)
	               |			ТОГДА 18
	               |		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18_118)
	               |			ТОГДА 18
				   |		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС20)
	               |			ТОГДА 20
	               |		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС20_120)
	               |			ТОГДА 20
	               |		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС10)
	               |			ТОГДА 10
	               |		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС10_110)
	               |			ТОГДА 10
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СтавкаНДС,
	               |	тбСтроки.СуммаНДС,
	               |	тбСтроки.СуммаБезНДС,
	               |	тбСтроки.ЕдиницаИзмеренияКод,
	               |	тбСтроки.КоличествоВОдномМесте
	               |ИЗ
	               |	тбСтроки КАК тбСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ тбСтроки.Номенклатура) КАК КоличествоНаименований,
	               |	тбСтроки.Документ
	               |ИЗ
	               |	тбСтроки КАК тбСтроки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тбСтроки.Документ";
				   
	Запрос.УстановитьПараметр("НужныДоверенности", НужныДоверенности);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();
	тбДанныеШапка = ПакетРезультатовЗапроса[2].Выгрузить();
	тбДанныеСтроки = ПакетРезультатовЗапроса[4].Выгрузить();
	тбДанныеКоличествоНаименований = ПакетРезультатовЗапроса[5].Выгрузить();
	
	// Подготовим данные таблицы шапки документа
	тбШапка = Новый ТаблицаЗначений;
	тбШапка.Колонки.Добавить("ДокументОснование");
	тбШапка.Колонки.Добавить("ПредставлениеОрганизация");
	тбШапка.Колонки.Добавить("Организация");
	тбШапка.Колонки.Добавить("ОрганизацияПолноеНаименование");
	тбШапка.Колонки.Добавить("АдресОрганизация");
	тбШапка.Колонки.Добавить("ПредставлениеОтправитель");
	тбШапка.Колонки.Добавить("ОтправительАдрес");
	тбШапка.Колонки.Добавить("ПоставщикПоОКПО");
	тбШапка.Колонки.Добавить("ПредставлениеПолучатель");
	тбШапка.Колонки.Добавить("ПолучательАдрес");	
	тбШапка.Колонки.Добавить("Грузополучатель");
	тбШапка.Колонки.Добавить("ГрузополучательПоОКПО");
	тбШапка.Колонки.Добавить("ПредставлениеПлательщик");
	тбШапка.Колонки.Добавить("ПлательщикОКПО");
	тбШапка.Колонки.Добавить("ФИОГлавБухгалтера");
	тбШапка.Колонки.Добавить("Дата");
	тбШапка.Колонки.Добавить("ВодительДокумент");
	тбШапка.Колонки.Добавить("Экспедитор");
	тбШапка.Колонки.Добавить("ТранспортноеСредство");
	тбШапка.Колонки.Добавить("ГосНомер");
	тбШапка.Колонки.Добавить("Номер");
	тбШапка.Колонки.Добавить("АдресОтгрузки");
	тбШапка.Колонки.Добавить("АдресПолучения");
	тбШапка.Колонки.Добавить("КоличествоНаименований");
	тбШапка.Колонки.Добавить("ВсегоМестПрописью");
	тбШапка.Колонки.Добавить("МассаБруттоПрописью");
	тбШапка.Колонки.Добавить("ДоверенностьНомер");
	тбШапка.Колонки.Добавить("ДоверенностьДата");
	тбШапка.Колонки.Добавить("КонтрагентПеревозчика");
	тбШапка.Колонки.Добавить("Водитель");
	тбШапка.Колонки.Добавить("ДоверенноеЛицо");
	тбШапка.Колонки.Добавить("ДолжностьДоверенноеЛицо");
	тбШапка.Колонки.Добавить("Перевозчик");
	тбШапка.Колонки.Добавить("КонтрагентЗаказчика");
	тбШапка.Колонки.Добавить("ПредставлениеКонтрагентЗаказчика");
	тбШапка.Колонки.Добавить("АдресКонтрагент");
	тбШапка.Колонки.Добавить("НомерКИС"); 
	тбШапка.Колонки.Добавить("ДатаОтгрузкиГруза"); 
	тбШапка.Колонки.Добавить("СоглашениеНаименование");
	тбШапка.Колонки.Добавить("СоглашениеДата");
	тбШапка.Колонки.Добавить("СоглашениеНомер");
	
	// Заполним данные таблицы шапки документа
	Для Каждого текСтрока Из тбДанныеШапка Цикл
		НоваяСтрока = тбШапка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
		
		ОтборПоДокументу = Новый Структура("Документ",текСтрока.ДокументОснование);
		НайденныеСтроки = тбДанныеСтроки.НайтиСтроки(ОтборПоДокументу);
		КопияТаблицыСтрок = тбДанныеСтроки.Скопировать(НайденныеСтроки,"КоличествоМест,МассаБрутто");
		КоличествоМест_Всего = КопияТаблицыСтрок.Итог("КоличествоМест");
		МассаБрутто_Всего = КопияТаблицыСтрок.Итог("МассаБрутто");
		
		// Количество мест.
		НоваяСтрока.ВсегоМестПрописью = ЧислоПрописью(текСтрока.КоличествоМест, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		Если ЗначениеЗаполнено(КоличествоМест_Всего) Тогда
			НоваяСтрока.ВсегоМестПрописью = ЧислоПрописью(КоличествоМест_Всего, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		КонецЕсли;
		НоваяСтрока.МассаБруттоПрописью = ЧислоПрописью(текСтрока.МассаБруттоВсего*1000, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		Если ЗначениеЗаполнено(МассаБрутто_Всего) Тогда
		НоваяСтрока.МассаБруттоПрописью = ЧислоПрописью(МассаБрутто_Всего*1000, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		КонецЕсли;
		
		// Грузоотправитель
		Если ЗначениеЗаполнено(текСтрока.Грузоотправитель) Тогда
			НоваяСтрока.ПредставлениеОтправитель = текСтрока.ПредставлениеОтправитель;
			текАдрес = ПолучитьКонтактнуюИнформацию(текСтрока.Грузоотправитель);
			
			НоваяСтрока.ОтправительАдрес = ?(ЗначениеЗаполнено(текАдрес), текАдрес, "");
		Иначе
			НоваяСтрока.ПредставлениеОтправитель = "";	
		КонецЕсли;	
		
		// Грузополучатель
		Если ЗначениеЗаполнено(текСтрока.Грузополучатель) Тогда
			НоваяСтрока.ПредставлениеПолучатель = текСтрока.ПредставлениеПолучатель;
			текАдрес = ПолучитьКонтактнуюИнформацию(текСтрока.Грузополучатель);
			
			НоваяСтрока.ПолучательАдрес = ?(ЗначениеЗаполнено(текАдрес), текАдрес, "");
		Иначе
			НоваяСтрока.ПредставлениеПолучатель = "";
		КонецЕсли;	
				
		// Плательщик
		Если ЗначениеЗаполнено(текСтрока.Плательщик) Тогда
			НоваяСтрока.ПредставлениеПлательщик = текСтрока.Плательщик;
		КонецЕсли;	
		
		// Организация
		НоваяСтрока.Организация = текСтрока.ОрганизацияСсылка;
		Если ЗначениеЗаполнено(текСтрока.ОрганизацияСсылка) Тогда
			НоваяСтрока.ОрганизацияПолноеНаименование = текСтрока.Организация; 
			НоваяСтрока.АдресОрганизация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрока.ОрганизацияСсылка, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
			
			текКонтактнаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.ОрганизацияСсылка);
			НоваяСтрока.ПредставлениеОрганизация = ""+Строка(текСтрока.Организация) +  ?(ПустаяСтрока(текКонтактнаяИнформация), "", ", "+текКонтактнаяИнформация);
		КонецЕсли;	

		// КонтрагентЗаказчика
		НоваяСтрока.КонтрагентЗаказчика = текСтрока.КонтрагентЗаказчика;
		Если ЗначениеЗаполнено(текСтрока.КонтрагентЗаказчика) Тогда
			НоваяСтрока.ПредставлениеКонтрагентЗаказчика = текСтрока.ПредставлениеКонтрагентЗаказчика; 
			НоваяСтрока.АдресКонтрагент = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрока.КонтрагентЗаказчика, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);			
		КонецЕсли;
		
		сткОтбор	= Новый Структура("Документ", текСтрока.ДокументОснование);
		
		мсвСтрок = тбДанныеКоличествоНаименований.НайтиСтроки(сткОтбор);
		Если мсвСтрок.Количество()>0 Тогда
			НоваяСтрока.КоличествоНаименований = мсвСтрок[0].КоличествоНаименований;
		Иначе
			НоваяСтрока.КоличествоНаименований = 0;
		КонецЕсли;
		
	КонецЦикла;	
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Шапка",  тбШапка);
	СтруктураДанныхДляПечати.Вставить("Строки", тбДанныеСтроки);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция ПеревозчикПредставление(Перевозчик)
	Результат = "";
	
	Если ЗначениеЗаполнено(Перевозчик) Тогда
		МассивКИ = Новый Массив;
		//КонтактнаяИнформация = ПолучитьКонтактнуюИнформацию(Перевозчик);
		//Результат = Строка(Перевозчик) + ?(ЗначениеЗаполнено(КонтактнаяИнформация), ", " + КонтактнаяИнформация, "");
		СокращенноеЮрНаименование = Перевозчик.НаименованиеПолное;
		Если НЕ ПустаяСтрока(СокращенноеЮрНаименование) Тогда
			МассивКИ.Добавить(СокращенноеЮрНаименование);
		КонецЕсли;
		Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Перевозчик, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
		Если ЗначениеЗаполнено(Адрес) Тогда
			МассивКИ.Добавить(Строка(Адрес));
		КонецЕсли;
		
		Результат = СтрСоединить(МассивКИ, ", ");
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПолучитьКонтактнуюИнформацию(Ссылка)
	
	текРезультат = "";
	ВидАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
	ВидФакс = Справочники.ВидыКонтактнойИнформации.ФаксОрганизации;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упПартнеры") Тогда
		ВидАдрес = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера;	    
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упКонтрагенты") Тогда
		ВидАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;	    
		ВидФакс = Справочники.ВидыКонтактнойИнформации.ФаксКонтрагенты;
	КонецЕсли;
	
	текАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидАдрес);
	Если ЗначениеЗаполнено(текАдрес) Тогда
		текРезультат = текРезультат  + текАдрес;	
	КонецЕсли;
		
	текТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидТелефон);
	Если ЗначениеЗаполнено(текТелефон) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текТелефон);
	КонецЕсли;

	текФакс = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидФакс);
	Если ЗначениеЗаполнено(текФакс) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текФакс);
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

// Функция - Получить данные для п ф ТН
//
// Параметры:
//  Документы	 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ПолучитьДанныеДляПФ_ТН(мсвОбъекты) Экспорт
	
	Результат = Неопределено;
	
	Если мсвОбъекты.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		
		Разделитель =
		"
		|;
		|/////////////////////////////////////////////////////////////
		|";
		
		ТекстыЗапросовПакета = Новый Массив;
		
		Если ТипЗнч(мсвОбъекты[0]) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	упЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза
			               |ПОМЕСТИТЬ втЗаданияНаПеревозкуГруза
			               |ИЗ
			               |	Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
			               |ГДЕ
			               |	упЗаданиеНаПеревозкуГруза.Ссылка В(&Задания)
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ РАЗЛИЧНЫЕ
			               |	Работы.Рейс КАК Рейс,
			               |	Работы.Рейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах,
			               |	Работы.Задание КАК ЗаданиеНаПеревозкуГруза,
			               |	Работы.КоличествоГрузов КАК КоличествоГрузов,
			               |	Работы.ГрузовоеМесто КАК ГрузовоеМесто
			               |ПОМЕСТИТЬ втРейсыПоЗаданиям
			               |ИЗ
			               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
			               |			,
			               |			Задание В
			               |				(ВЫБРАТЬ
			               |					ЗПГ.ЗаданиеНаПеревозкуГруза КАК Ссылка
			               |				ИЗ
			               |					втЗаданияНаПеревозкуГруза КАК ЗПГ)) КАК Работы
			               |ГДЕ
			               |	НЕ Работы.Отменена
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	тбРейсы.Рейс КАК Рейс,
			               |	ВЫБОР
			               |		КОГДА НЕ ЕСТЬNULL(тбФактическиеПараметры.НачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
			               |			ТОГДА тбФактическиеПараметры.НачалоВыполнения
			               |		КОГДА НЕ ЕСТЬNULL(тбПлановыеПараметры.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
			               |			ТОГДА тбПлановыеПараметры.ПлановоеНачалоВыполнения
			               |		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
			               |	КОНЕЦ КАК ДатаНачалаРейса,
			               |	тбРейсы.ЗаданиеНаПеревозкуГруза,
			               |	тбРейсы.ГрузовоеМесто
			               |ПОМЕСТИТЬ втРейсыПоЗаданиямДатыНачала
			               |ИЗ
			               |	втРейсыПоЗаданиям КАК тбРейсы
			               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК тбПлановыеПараметры
			               |		ПО тбРейсы.Рейс = тбПлановыеПараметры.Рейс
			               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК тбФактическиеПараметры
			               |		ПО тбРейсы.Рейс = тбФактическиеПараметры.Рейс
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	МАКСИМУМ(тбРейсы.Рейс) КАК Рейс,
			               |	тбРейсы.ЗаданиеНаПеревозкуГруза,
			               |	тбРейсы.ГрузовоеМесто
			               |ПОМЕСТИТЬ втПоследниеРейсыПоЗаданиям
			               |ИЗ
			               |	втРейсыПоЗаданиямДатыНачала КАК тбРейсы
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			               |			МАКСИМУМ(тбРейсы.ДатаНачалаРейса) КАК ДатаНачалаРейса,
			               |			тбРейсы.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
			               |			тбРейсы.ГрузовоеМесто КАК ГрузовоеМесто
			               |		ИЗ
			               |			втРейсыПоЗаданиямДатыНачала КАК тбРейсы
			               |		
			               |		СГРУППИРОВАТЬ ПО
			               |			тбРейсы.ЗаданиеНаПеревозкуГруза,
			               |			тбРейсы.ГрузовоеМесто) КАК тбМаксДатыНачалаРейсовПоЗаданиям
			               |		ПО тбРейсы.ДатаНачалаРейса = тбМаксДатыНачалаРейсовПоЗаданиям.ДатаНачалаРейса
			               |			И тбРейсы.ЗаданиеНаПеревозкуГруза = тбМаксДатыНачалаРейсовПоЗаданиям.ЗаданиеНаПеревозкуГруза
			               |			И тбРейсы.ГрузовоеМесто = тбМаксДатыНачалаРейсовПоЗаданиям.ГрузовоеМесто
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	тбРейсы.ЗаданиеНаПеревозкуГруза,
			               |	тбРейсы.ГрузовоеМесто
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	втРейсыПоЗаданиям.Рейс,
			               |	втРейсыПоЗаданиям.ПараметрыВводаИнформацииОГрузовыхМестах,
			               |	втРейсыПоЗаданиям.ЗаданиеНаПеревозкуГруза,
			               |	втРейсыПоЗаданиям.КоличествоГрузов,
			               |	втРейсыПоЗаданиям.ГрузовоеМесто
			               |ПОМЕСТИТЬ втГрузовыеМестаПоПоследнимРейсам
			               |ИЗ
			               |	втРейсыПоЗаданиям КАК втРейсыПоЗаданиям
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследниеРейсыПоЗаданиям КАК втПоследниеРейсыПоЗаданиям
			               |		ПО втРейсыПоЗаданиям.ЗаданиеНаПеревозкуГруза = втПоследниеРейсыПоЗаданиям.ЗаданиеНаПеревозкуГруза
			               |			И втРейсыПоЗаданиям.ГрузовоеМесто = втПоследниеРейсыПоЗаданиям.ГрузовоеМесто
			               |			И втРейсыПоЗаданиям.Рейс = втПоследниеРейсыПоЗаданиям.Рейс
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ РАЗЛИЧНЫЕ
			               |	втПоследниеРейсыПоЗаданиям.Рейс
			               |ПОМЕСТИТЬ втРейсы
			               |ИЗ
			               |	втПоследниеРейсыПоЗаданиям КАК втПоследниеРейсыПоЗаданиям";
			
			Запрос.УстановитьПараметр("Задания", мсвОбъекты);
			ЗапросИндексПараметрыРейса = 6;
			ЗапросИндексПараметрыЗадания = 7;
			ЗапросИндексПутевыеЛисты = 8;
			ЗапросИндексНачалоПогрузкиРазгрузки = 9;
			ЗапросИндексГрузовыеМестаПоРейсам = 10;
			ЗапросИндексГрузовыеМестаПоЗаданиям = 11;
			ЗапросИндексУпаковкиПоЗаданиям = 15;

		Иначе	
			ТекстЗапроса = "ВЫБРАТЬ
			|	упРейс.Ссылка КАК Рейс,
			|	упРейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
			|ПОМЕСТИТЬ втРейсы
			|ИЗ
			|	Документ.упРейс КАК упРейс
			|ГДЕ
			|	упРейс.Ссылка В(&Рейсы)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	втРейсы.Рейс,
			|	втРейсы.ПараметрыВводаИнформацииОГрузовыхМестах,
			|	ЕСТЬNULL(РаботыПоРейсу.Задание, ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)) КАК ЗаданиеНаПеревозкуГруза,
			|	ЕСТЬNULL(РаботыПоРейсу.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)) КАК ГрузовоеМесто,
			|	ЕСТЬNULL(РаботыПоРейсу.КоличествоГрузов, 0) КАК КоличествоГрузов
			|ПОМЕСТИТЬ втГрузовыеМестаПоПоследнимРейсам
			|ИЗ
			|	втРейсы КАК втРейсы
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
			|				,
			|				Рейс В
			|					(ВЫБРАТЬ
			|						втРейсы.Рейс
			|					ИЗ
			|						втРейсы КАК втРейсы)) КАК РаботыПоРейсу
			|		ПО втРейсы.Рейс = РаботыПоРейсу.Рейс
			|ГДЕ
			|	НЕ РаботыПоРейсу.Отменена
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втГрузовыеМестаПоПоследнимРейсам.ЗаданиеНаПеревозкуГруза
			|ПОМЕСТИТЬ втЗаданияНаПеревозкуГруза
			|ИЗ
			|	втГрузовыеМестаПоПоследнимРейсам КАК втГрузовыеМестаПоПоследнимРейсам
			|
			|СГРУППИРОВАТЬ ПО
			|	втГрузовыеМестаПоПоследнимРейсам.ЗаданиеНаПеревозкуГруза";
			
			Запрос.УстановитьПараметр("Рейсы", мсвОбъекты);
			ЗапросИндексПараметрыРейса = 3;
			ЗапросИндексПараметрыЗадания = 4;
			ЗапросИндексПутевыеЛисты = 5;
			ЗапросИндексНачалоПогрузкиРазгрузки = 6;
			ЗапросИндексГрузовыеМестаПоРейсам = 7;
			ЗапросИндексГрузовыеМестаПоЗаданиям = 8;
			ЗапросИндексУпаковкиПоЗаданиям = 12;
		
		КонецЕсли;
		ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
		ТекстЗапроса = "ВЫБРАТЬ
		               |	втРейсы.Рейс,
		               |	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) * 1000 КАК Масса,
		               |	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Наименование, """") КАК НаименованиеТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Ширина, 0) КАК ШиринаТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Длина, 0) КАК ДлинаТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Высота, 0) КАК ВысотаТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Объем, 0) КАК ОбъемТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Грузоподъемность, 0) КАК ГрузоподъемностьТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.КоличествоМест, 0) КАК КоличествоМестТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетровТС,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.РегистрационныйНомер, """") КАК РегистрационныйНомерТС,
		               |	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
		               |	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель,
		               |	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель2, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель2,
		               |	ВЫБОР
		               |		КОГДА НЕ упПеревозчикПоРейсуСрезПоследних.Перевозчик.ЮридическоеЛицо
		               |			ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |					ТОГДА ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
		               |				ИНАЧЕ ВЫБОР
		               |						КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика ЕСТЬ NULL
		               |							ТОГДА ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |						КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика.ЮрФизЛицо В (ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо), ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицоНеРезидент))
		               |							ТОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика
		               |						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |					КОНЕЦ
		               |			КОНЕЦ
		               |	КОНЕЦ КАК ПеревозчикЮрЛицо,
		               |	упПеревозчикПоРейсуСрезПоследних.ТипТС,
		               |	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Модель КАК Модель,
		               |	ВЫБОР
		               |		КОГДА упПеревозчикПоРейсуСрезПоследних.Перевозчик.ЮридическоеЛицо
		               |			ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |					ТОГДА ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
		               |				ИНАЧЕ ВЫБОР
		               |						КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика ЕСТЬ NULL
		               |							ТОГДА ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |						КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика.ЮрФизЛицо В (ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо), ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ИндивидуальныйПредприниматель))
		               |							ТОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика
		               |						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |					КОНЕЦ
		               |			КОНЕЦ
		               |	КОНЕЦ КАК ПеревозчикФизЛицо,
		               |	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения,
		               |	упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения КАК ПлановоеОкончаниеВыполнения
		               |ИЗ
		               |	втРейсы КАК втРейсы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
		               |		ПО втРейсы.Рейс = упПлановыеПараметрыРейса.Рейс
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
		               |		ПО втРейсы.Рейс = упПеревозчикПоРейсуСрезПоследних.Рейс
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА НЕ ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
		               |			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.ДатаКИС
		               |		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		               |	КОНЕЦ КАК ДатаЗаявки,
		               |	ВЫБОР
		               |		КОГДА НЕ ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
		               |			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.НомерКИС
		               |		ИНАЧЕ """"
		               |	КОНЕЦ КАК НомерЗаявки,
		               |	ВЫБОР
		               |		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			ТОГДА ВЫБОР
		               |					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |					ИНАЧЕ ВЫБОР
		               |							КОГДА НЕ ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
		               |								ТОГДА ЗаданиеНаПеревозку.Отправитель
		               |							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						КОНЕЦ
		               |				КОНЕЦ
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
		               |					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя
		               |				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			КОНЕЦ
		               |	КОНЕЦ КАК ГрузоотправительФизЛицо,
		               |	ВЫБОР
		               |		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			ТОГДА ВЫБОР
		               |					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |					ИНАЧЕ ВЫБОР
		               |							КОГДА НЕ ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
		               |								ТОГДА ЗаданиеНаПеревозку.Получатель
		               |							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						КОНЕЦ
		               |				КОНЕЦ
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
		               |					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя
		               |				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			КОНЕЦ
		               |	КОНЕЦ КАК ГрузополучательФизЛицо,
		               |	ВЫБОР
		               |		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			ТОГДА ВЫБОР
		               |					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |					ИНАЧЕ ВЫБОР
		               |							КОГДА ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
		               |								ТОГДА ЗаданиеНаПеревозку.Отправитель
		               |							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						КОНЕЦ
		               |				КОНЕЦ
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
		               |					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя
		               |				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			КОНЕЦ
		               |	КОНЕЦ КАК ГрузоотправительЮрЛицо,
		               |	ВЫБОР
		               |		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			ТОГДА ВЫБОР
		               |					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |					ИНАЧЕ ВЫБОР
		               |							КОГДА ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
		               |								ТОГДА ЗаданиеНаПеревозку.Получатель
		               |							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		               |						КОНЕЦ
		               |				КОНЕЦ
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
		               |					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя
		               |				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
		               |			КОНЕЦ
		               |	КОНЕЦ КАК ГрузополучательЮрЛицо,
		               |	ЗаданиеНаПеревозку.АдресПолучения.ПолноеНаименование КАК АдресПолучения,
		               |	ЗаданиеНаПеревозку.АдресОтправления.ПолноеНаименование КАК АдресОтправления,
		               |	ЗаданиеНаПеревозку.КонтактноеЛицоПолучателя,
		               |	ЗаданиеНаПеревозку.Масса,
		               |	ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза,
		               |	ЗаданиеНаПеревозку.Ссылка КАК ЗаданиеНаПеревозкуГруза,
		               |	ЗаданиеНаПеревозку.КоличествоМест
		               |ИЗ
		               |	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозку
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
		               |		ПО ЗаданиеНаПеревозку.Ссылка = втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упПутевойЛистРейсы.Ссылка.Номер,
		               |	втРейсы.Рейс
		               |ИЗ
		               |	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
		               |		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛистРейсы.Ссылка)
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
		               |		ПО упПутевойЛистРейсы.Рейс = втРейсы.Рейс
		               |ГДЕ
		               |	НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)
		               |	И НЕ упПутевойЛистРейсы.Ссылка.ПометкаУдаления
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втГрузовыеМестаПоПоследнимРейсам.Рейс,
		               |	втГрузовыеМестаПоПоследнимРейсам.ЗаданиеНаПеревозкуГруза,
		               |	МИНИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		               |				ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие
		               |			ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		               |		КОНЕЦ) КАК НачалоПогрузки,
		               |	МИНИМУМ(ВЫБОР
		               |			КОГДА упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
		               |				ТОГДА упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие
		               |			ИНАЧЕ ДАТАВРЕМЯ(3999, 12, 31)
		               |		КОНЕЦ) КАК НачалоРазгрузки
		               |ИЗ
		               |	втГрузовыеМестаПоПоследнимРейсам КАК втГрузовыеМестаПоПоследнимРейсам
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК упРаботыПоРейсуСрезПоследних
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК упМаршрутПоРейсуСрезПоследних
		               |			ПО упРаботыПоРейсуСрезПоследних.Идентификатор = упМаршрутПоРейсуСрезПоследних.Идентификатор
		               |		ПО втГрузовыеМестаПоПоследнимРейсам.ЗаданиеНаПеревозкуГруза = упРаботыПоРейсуСрезПоследних.Задание
		               |			И втГрузовыеМестаПоПоследнимРейсам.Рейс = упРаботыПоРейсуСрезПоследних.Рейс
		               |			И (ВЫБОР
		               |				КОГДА втГрузовыеМестаПоПоследнимРейсам.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса))
		               |					ТОГДА ИСТИНА
		               |				ИНАЧЕ втГрузовыеМестаПоПоследнимРейсам.ГрузовоеМесто = упРаботыПоРейсуСрезПоследних.ГрузовоеМесто
		               |			КОНЕЦ)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втГрузовыеМестаПоПоследнимРейсам.Рейс,
		               |	втГрузовыеМестаПоПоследнимРейсам.ЗаданиеНаПеревозкуГруза
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втГрузовыеМестаПоПоследнимРейсам.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
		               |	втГрузовыеМестаПоПоследнимРейсам.ГрузовоеМесто,
		               |	втГрузовыеМестаПоПоследнимРейсам.Рейс КАК Рейс,
		               |	втГрузовыеМестаПоПоследнимРейсам.КоличествоГрузов КАК КоличествоГрузов,
		               |	втГрузовыеМестаПоПоследнимРейсам.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах,
		               |	ЕСТЬNULL(упГрузовыеМеста.Масса, 0) * втГрузовыеМестаПоПоследнимРейсам.КоличествоГрузов КАК Масса,
		               |	ЕСТЬNULL(упГрузовыеМеста.Объем, 0) * втГрузовыеМестаПоПоследнимРейсам.КоличествоГрузов КАК Объем,
		               |	ЕСТЬNULL(упГрузовыеМеста.КоличествоМест, 0) * втГрузовыеМестаПоПоследнимРейсам.КоличествоГрузов КАК КоличествоМест,
		               |	ЕСТЬNULL(упГрузовыеМеста.КоличествоЗагрузочныхМетров, 0) * втГрузовыеМестаПоПоследнимРейсам.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
		               |	ЕСТЬNULL(упГрузовыеМеста.ТемпературныйРежим, ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)) КАК ТемпературныйРежим
		               |ИЗ
		               |	втГрузовыеМестаПоПоследнимРейсам КАК втГрузовыеМестаПоПоследнимРейсам
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		               |		ПО втГрузовыеМестаПоПоследнимРейсам.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		               |ИТОГИ
		               |	МАКСИМУМ(ПараметрыВводаИнформацииОГрузовыхМестах)
		               |ПО
		               |	Рейс,
		               |	ЗаданиеНаПеревозкуГруза
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК ЗаданиеНаПеревозкуГруза,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов,
		               |	ЕСТЬNULL(упГрузовыеМеста.Масса, 0) * упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов КАК Масса,
		               |	ЕСТЬNULL(упГрузовыеМеста.КоличествоМест, 0) * упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов КАК КоличествоМест,
		               |	ЕСТЬNULL(упГрузовыеМеста.Объем, 0) * упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов КАК Объем,
		               |	ЕСТЬNULL(упГрузовыеМеста.КоличествоЗагрузочныхМетров, 0) * упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов КАК КоличествоЗагрузочныхМетров,
		               |	ЕСТЬNULL(упГрузовыеМеста.ТемпературныйРежим, ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка)) КАК ТемпературныйРежим
		               |ИЗ
		               |	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
		               |		ПО упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
		               |		ПО упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка,
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто,
		               |	упГрузовыеМестаТоварныйСоставГруза.Номенклатура,
		               |	упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры,
		               |	упГрузовыеМестаТоварныйСоставГруза.Количество,
		               |	ВЫБОР
		               |		КОГДА ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка) = упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры
		               |			ТОГДА 0
		               |		КОГДА упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры.Коэффициент = 0
		               |			ТОГДА 0
		               |		ИНАЧЕ упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры.Коэффициент
		               |	КОНЕЦ КАК Коэффициент
		               |ПОМЕСТИТЬ вт_Товары
		               |ИЗ
		               |	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК упГрузовыеМестаТоварныйСоставГруза
		               |		ПО упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упГрузовыеМестаТоварныйСоставГруза.Ссылка
		               |ГДЕ
		               |	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка В
		               |			(ВЫБРАТЬ
		               |				втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза КАК Ссылка
		               |			ИЗ
		               |				втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза)
		               |	И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.ТипГруза В
		               |			(ВЫБРАТЬ
		               |				ТипыГрузов.Ссылка КАК Ссылка
		               |			ИЗ
		               |				Справочник.упТипыГрузов КАК ТипыГрузов
		               |			ГДЕ
		               |				НЕ ТипыГрузов.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ИнкассируемаяЦенность))
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	упУпаковкиНоменклатуры.Владелец КАК Номеклатура,
		               |	упУпаковкиНоменклатуры.Коэффициент КАК Коэффициент
		               |ПОМЕСТИТЬ вт_ТоварУпаковки
		               |ИЗ
		               |	Справочник.упУпаковкиНоменклатуры КАК упУпаковкиНоменклатуры
		               |ГДЕ
		               |	упУпаковкиНоменклатуры.Владелец В
		               |			(ВЫБРАТЬ
		               |				вт_Товары.Номенклатура
		               |			ИЗ
		               |				вт_Товары КАК вт_Товары)
		               |	И упУпаковкиНоменклатуры.ЕдиницаИзмерения.Код = ""778""
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	вт_Товары.Ссылка,
		               |	вт_Товары.ГрузовоеМесто,
		               |	вт_Товары.Номенклатура,
		               |	вт_Товары.УпаковкаНоменклатуры,
		               |	вт_Товары.Количество,
		               |	вт_ТоварУпаковки.Коэффициент,
		               |	ВЫБОР
		               |		КОГДА вт_Товары.Коэффициент = 0
		               |				ИЛИ вт_ТоварУпаковки.Коэффициент = 0
		               |			ТОГДА 0
		               |		ИНАЧЕ вт_Товары.Количество / вт_ТоварУпаковки.Коэффициент
		               |	КОНЕЦ КАК КоличествоУпаковок
		               |ПОМЕСТИТЬ вт_ТоварКоличество
		               |ИЗ
		               |	вт_ТоварУпаковки КАК вт_ТоварУпаковки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ вт_Товары КАК вт_Товары
		               |		ПО вт_ТоварУпаковки.Номеклатура = вт_Товары.Номенклатура
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	вт_ТоварКоличество.Ссылка КАК ЗаданиеНаПеревозкуГруза,
		               |	СУММА(вт_ТоварКоличество.КоличествоУпаковок) КАК Количество
		               |ИЗ
		               |	вт_ТоварКоличество КАК вт_ТоварКоличество
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	вт_ТоварКоличество.Ссылка";
					   
		ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
		Запрос.Текст = СтрСоединить(ТекстыЗапросовПакета, Разделитель);
		РезультатПакет = Запрос.ВыполнитьПакет();
		
		СтруктураДанныхДляПечати = Новый Структура;
		СтруктураДанныхДляПечати.Вставить("ПараметрыРейса", РезультатПакет[ЗапросИндексПараметрыРейса].Выгрузить());
		СтруктураДанныхДляПечати.Вставить("ПараметрыЗадания", РезультатПакет[ЗапросИндексПараметрыЗадания].Выгрузить());
		СтруктураДанныхДляПечати.Вставить("ПутевыеЛисты", РезультатПакет[ЗапросИндексПутевыеЛисты].Выгрузить());
		СтруктураДанныхДляПечати.Вставить("ПогрузкаРазгрузка", РезультатПакет[ЗапросИндексНачалоПогрузкиРазгрузки].Выгрузить());
		СтруктураДанныхДляПечати.Вставить("ГрузовыеМестаПоРейсам", РезультатПакет[ЗапросИндексГрузовыеМестаПоРейсам].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам));
		СтруктураДанныхДляПечати.Вставить("ГрузовыеМестаПоЗаданиям", РезультатПакет[ЗапросИндексГрузовыеМестаПоЗаданиям].Выгрузить());
		СтруктураДанныхДляПечати.Вставить("УпаковкиПоЗаданиям", РезультатПакет[ЗапросИндексУпаковкиПоЗаданиям].Выгрузить());
	КонецЕсли;
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция КонтактныеДанныеКонтрагентаБанкСчет(Контрагент)
	
	мсвКонтактныеДанные = Новый Массив;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		//мсвКонтактныеДанные.Добавить(Строка(Контрагент));
		СокращенноеЮрНаименование = Контрагент.НаименованиеПолное;
		Если НЕ ПустаяСтрока(СокращенноеЮрНаименование) Тогда
			мсвКонтактныеДанные.Добавить(СокращенноеЮрНаименование);
		КонецЕсли;
		ИНН = Контрагент.ИНН;
		Если НЕ ПустаяСтрока(ИНН) Тогда
			мсвКонтактныеДанные.Добавить(ИНН);
		КонецЕсли;
		Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
		Если ЗначениеЗаполнено(Адрес) Тогда
			мсвКонтактныеДанные.Добавить(Строка(Адрес));
		КонецЕсли;
		ОсновнойБанковскийСчет = Контрагент.ОсновнойБанковскийСчет;
		Если ЗначениеЗаполнено(ОсновнойБанковскийСчет) Тогда
			БанковскиеРеквизиты = СтрШаблон("р/с %1, в банке %2 к/с %3",ОсновнойБанковскийСчет.НомерСчета,Строка(ОсновнойБанковскийСчет.Банк),ОсновнойБанковскийСчет.Банк.КоррСчет);
			Если ЗначениеЗаполнено(ОсновнойБанковскийСчет.БанкДляРасчетов) Тогда
				БанковскиеРеквизиты = СтрШаблон("р/с %1, в банке %2 к/с %3",ОсновнойБанковскийСчет.НомерСчета,Строка(ОсновнойБанковскийСчет.БанкДляРасчетов),ОсновнойБанковскийСчет.БанкДляРасчетов.КоррСчет);
			КонецЕсли;
			мсвКонтактныеДанные.Добавить(БанковскиеРеквизиты);
			
			//мсвКонтактныеДанные.Добавить(Строка(Адрес));
		КонецЕсли;
		
		//Телефон	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, ?(ТипЗнч(Контрагент)=ТИП("СправочникСсылка.упКонтрагенты"),Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,Справочники.ВидыКонтактнойИнформации.ТелефонПартнера));
		//Если ЗначениеЗаполнено(Телефон) Тогда
		//	мсвКонтактныеДанные.Добавить(Телефон);				
		//КонецЕсли;
	КонецЕсли;

	Возврат СтрСоединить(мсвКонтактныеДанные, ", ");
		
КонецФункции // КонтактныеДанныеКонтрагентаБанкСчет()

Функция КонтактныеДанныеКонтрагентаМХ1(Контрагент)
	
	мсвКонтактныеДанные = Новый Массив;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
			ВидКИАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
			ВидКИТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
			
		ИначеЕсли ТипЗнч(Контрагент) = Тип("СправочникСсылка.упКонтрагенты") Тогда
			ВидКИАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
			ВидКИТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
			
		Иначе	
		    Возврат "";
		КонецЕсли;		
			
		сткДанныеПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "НаименованиеПолное, Наименование, ИНН, ОсновнойБанковскийСчет");
		
		Если ЗначениеЗаполнено(сткДанныеПолучателя.НаименованиеПолное) Тогда
			мсвКонтактныеДанные.Добавить(Строка(сткДанныеПолучателя.НаименованиеПолное));	
		ИначеЕсли ЗначениеЗаполнено(сткДанныеПолучателя.Наименование) Тогда
			мсвКонтактныеДанные.Добавить(Строка(сткДанныеПолучателя.Наименование));	
		КонецЕсли;
			
		Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, ВидКИАдрес); 
		Если ЗначениеЗаполнено(Адрес) Тогда
			мсвКонтактныеДанные.Добавить(Адрес);				
		КонецЕсли;
	
		Если ЗначениеЗаполнено(сткДанныеПолучателя.ИНН) Тогда
			мсвКонтактныеДанные.Добавить(Нстр("ru = 'ИНН '")+сткДанныеПолучателя.ИНН);					
		КонецЕсли;
		
		Если ЗначениеЗаполнено(сткДанныеПолучателя.ОсновнойБанковскийСчет) Тогда
			РеквизитыБанка = Новый Структура();
			РеквизитыБанка.Вставить("Банк");
			РеквизитыБанка.Вставить("НомерСчета");
			РеквизитыБанка.Вставить("Банк", "Банк");
			РеквизитыБанка.Вставить("БанкКоррСчет", "Банк.КоррСчет");
			РеквизитыБанка.Вставить("БанкБИК", "Банк.Код");
			сткДанныеБанковскогоСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(сткДанныеПолучателя.ОсновнойБанковскийСчет, РеквизитыБанка);
			
			Если ЗначениеЗаполнено(сткДанныеБанковскогоСчета.НомерСчета) Тогда
				мсвКонтактныеДанные.Добавить(Нстр("ru = 'р/с '")+сткДанныеБанковскогоСчета.НомерСчета);				
			КонецЕсли;
			Если ЗначениеЗаполнено(сткДанныеБанковскогоСчета.Банк) Тогда
				мсвКонтактныеДанные.Добавить(Нстр("ru = 'в банке '")+сткДанныеБанковскогоСчета.Банк);				
			КонецЕсли;
			Если ЗначениеЗаполнено(сткДанныеБанковскогоСчета.БанкБИК) Тогда
				мсвКонтактныеДанные.Добавить(Нстр("ru = 'БИК '")+сткДанныеБанковскогоСчета.БанкБИК);				
			КонецЕсли;
			Если ЗначениеЗаполнено(сткДанныеБанковскогоСчета.БанкКоррСчет) Тогда
				мсвКонтактныеДанные.Добавить(Нстр("ru = 'к/с '")+сткДанныеБанковскогоСчета.БанкКоррСчет);				
			КонецЕсли;
		КонецЕсли;
		
		Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, ВидКИТелефон);
		Если ЗначениеЗаполнено(Телефон) Тогда
			мсвКонтактныеДанные.Добавить(Телефон);				
		КонецЕсли;
	КонецЕсли;

	Возврат СтрСоединить(мсвКонтактныеДанные, ", ");
		
КонецФункции

// Функция определяет дату формирования движений в зависимости от параметра ввода о грузовых местах
//
// Параметры:
//  ПараметрВводаГруза - ПеречислениеСсылка.упПараметрыВводаИнформацииОГрузовыхМестах
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений(Ссылка, ПараметрВводаГруза, Статус, ЧасовойПоясДокумента) Экспорт
	
	Период = упСервисныеФункции.ПриведеннаяКЧасовомуПоясуДокументаДатаСеанса(ЧасовойПоясДокумента);
	
	Если Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Отменено Тогда
		Возврат Период;
	КонецЕсли; 
	
	Если Ложь 
		Или ПараметрВводаГруза = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится
		Или ПараметрВводаГруза = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку
	Тогда
		ПереведеноКПланированию = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьДатуПереводаВСтатус(Ссылка, Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
		Если ЗначениеЗаполнено(ПереведеноКПланированию) Тогда
			Период = Мин(ПереведеноКПланированию, Период);
		КонецЕсли; 
	ИначеЕсли ПараметрВводаГруза = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса Тогда
		ВключеноВРейс = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьДатуПереводаВСтатус(Ссылка, Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ВключеноВРейс);
		Если ЗначениеЗаполнено(ВключеноВРейс) Тогда
			Период = Мин(ВключеноВРейс, Период);
		КонецЕсли;
	ИначеЕсли ПараметрВводаГруза = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
		ПереведеноКВыполнению = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьДатуПереводаВСтатус(Ссылка, Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КПланированию);
		Если ЗначениеЗаполнено(ПереведеноКВыполнению) Тогда
			Период = Мин(ПереведеноКВыполнению, Период);
		КонецЕсли;
	КонецЕсли; 

	Возврат Период;
	
КонецФункции

#КонецОбласти

#КонецЕсли
