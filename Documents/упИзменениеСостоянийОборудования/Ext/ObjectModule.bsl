
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура обработчик события ПередЗаписью объекта
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.Запись  
	Тогда
		// Проверка на наличие более поздних документов изменяющих состояние агрегатов и доп. оборудования.
		мсвОборудование = Оборудование.ВыгрузитьКолонку("Оборудование");
		упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, мсвОборудование, Дата, Отказ);
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(Оборудование, "Оборудование", Ссылка, Отказ);
			
		Если ЗначениеЗаполнено(Ссылка) Тогда
			Движения.упСостоянияОборудования.Записывать = Истина;
			Движения.упСостоянияОборудования.Очистить();
			
			Движения.упАгрегатыТранспортныхСредств.Записывать = Истина;
			Движения.упАгрегатыТранспортныхСредств.Очистить();
			
			Движения.упАгрегатыНаСкладах.Записывать = Истина;
			Движения.упАгрегатыНаСкладах.Очистить();
			
			Движения.Записать();
			
		КонецЕсли;

	КонецЕсли;	
		
КонецПроцедуры

// Процедура проверяет заполненность реквизитов объекта
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
					
	Если Истина 
		И НЕ Отказ
	Тогда 
		Если Оборудование.Количество() = 0 Тогда 
			ТекстОшибки = НСтр("ru = 'Не указан список оборудования'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "Оборудование",, Отказ);
		КонецЕсли;	
			
		Сч = 1;
		Для Каждого ТекущееОборудывание Из Оборудование Цикл 			
			Если НЕ ЗначениеЗаполнено(ТекущееОборудывание.Оборудование) Тогда 
				ТекстОшибки = НСтр("ru = 'Не указано Оборудование'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Оборудование", Сч, "Оборудование"),, Отказ);
			КонецЕсли;	
			Состояния = Справочники.упСостоянияОборудования.ПолучитьСостоянияПоУмолчанию(ТекущееОборудывание.Оборудование);
			Если НЕ ЗначениеЗаполнено(ТекущееОборудывание.ТранспортноеСредство) 
				И ТекущееОборудывание.Состояние = Состояния.СостояниеУстановленоНаТСПоУмолчанию Тогда 
				ТекстОшибки = НСтр("ru = 'Не указано Транспортное средство'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Оборудование", Сч, "ТранспортноеСредство"),, Отказ);
			КонецЕсли;	
			Если НЕ ЗначениеЗаполнено(ТекущееОборудывание.Склад) 
				И ТекущееОборудывание.Состояние = Состояния.СостояниеОприходованоНаСкладПоУмолчанию Тогда 
				ТекстОшибки = НСтр("ru = 'Не указан Склад'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Оборудование", Сч, "Склад"),, Отказ);
			КонецЕсли;	
			Сч = Сч + 1;
		КонецЦикла;	
		
	КонецЕсли;
	
	Если Истина
		И Отказ
		И ЗначениеЗаполнено(ТекстОшибки)
	Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Проверка заполнения Изменение состояний оборудования'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
	 					УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработчик события ОбработкаПроведения объекта
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Документы.упИзменениеСостоянийОборудования.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	Если ДополнительныеСвойства.Свойство("Оборудование") Тогда
		БлокировкаДанных = Новый БлокировкаДанных;		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияОборудования");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.Оборудование;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "Оборудование");
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упАгрегатыТранспортныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.Оборудование;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Оборудование");
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упАгрегатыНаСкладах");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.Оборудование;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Оборудование");
		
		упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли; 
	КонецЕсли;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Оборудование", Оборудование.Выгрузить());
	// СостоянияОборудования
	упУчетТранспортныхСредств.СформироватьДвиженияИзменениеСостоянияОборудования(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли 