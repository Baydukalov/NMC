#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("МетодОценки", упСервисныеФункции.ПолучитьЗначениеКонстанты("упМетодОценкиСтоимостиМатериалов"));
	Документы.упПоступлениеМатериалов.ИнициализироватьДанныеДокумента(Дата, Ссылка, ДополнительныеСвойства);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ДополнительныеСвойства.Свойство("ОстаткиМатериалов") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упОстаткиМатериалов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных	= ДополнительныеСвойства.ОстаткиМатериалов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Материал", "Материал");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад"   , "Склад");
	КонецЕсли; 
	
	Если ДополнительныеСвойства.Свойство("ЦеныЗапчастей") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЦеныЗапчастей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных	= ДополнительныеСвойства.ЦеныЗапчастей;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Запчасть", "Запчасть");
	КонецЕсли; 
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата", Дата);
	
	// Формирование движений по регистру накопления "упОстаткиМатериалов"
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный ИЛИ Отказ Тогда
		упСервисныеФункции.УдалитьДвижения(Движения);
		упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
		Движения.упОстаткиМатериалов.Записать();
	Иначе
		упКонтрольОстатков.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	КонецЕсли;	
	
	// Формирование записей по регистру сведений "упЦеныЗапчастей"
	упУправлениеЗапасами.СформироватьДвиженияЦеныЗапчастей(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	Движения.упЦеныЗапчастей.Записать();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата", Дата);
	
	упКонтрольОстатков.КонтрольОстатковМатериалов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
