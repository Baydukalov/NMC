#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Заполнение дополнительных свойств документа.
//
Процедура ИнициализироватьДанныеДокумента(Дата, ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗапчасти.Запчасть,
	|	упЗапчасти.Цена,
	|	упЗапчасти.Валюта,
	|	СУММА(упЗапчасти.Количество) КАК Количество,
	|	СУММА(упЗапчасти.Сумма) КАК СуммаСНДС,
	|	СУММА(упЗапчасти.Сумма) - СУММА(упЗапчасти.СуммаНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ втЗапчасти
	|ИЗ
	|	Документ.упПоступлениеМатериалов.Запчасти КАК упЗапчасти
	|ГДЕ
	|	упЗапчасти.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	упЗапчасти.Запчасть,
	|	упЗапчасти.Цена,
	|	упЗапчасти.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГСМ.ГСМ КАК Материал,
	|	упГСМ.Валюта,
	|	СУММА(упГСМ.Количество) КАК Количество,
	|	СУММА(упГСМ.Сумма) КАК СуммаСНДС,
	|	СУММА(упГСМ.Сумма) - СУММА(упГСМ.СуммаНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ втМатериалы
	|ИЗ
	|	Документ.упПоступлениеМатериалов.ГСМ КАК упГСМ
	|ГДЕ
	|	упГСМ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	упГСМ.ГСМ,
	|	упГСМ.Цена,
	|	упГСМ.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапчасти.Запчасть,
	|	втЗапчасти.Валюта,
	|	втЗапчасти.Количество,
	|	втЗапчасти.СуммаСНДС,
	|	втЗапчасти.СуммаБезНДС
	|ИЗ
	|	втЗапчасти КАК втЗапчасти
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	втМатериалы.Материал,
	|	&Партия КАК Партия,
	|	упПоступлениеМатериалов.Склад,
	|	СУММА(втМатериалы.Количество) КАК Количество,
	|	СУММА(втМатериалы.СуммаБезНДС * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1))) КАК СуммаБезНДС,
	|	СУММА(втМатериалы.СуммаСНДС * ЕСТЬNULL(КурсыВалют.Курс, 1) * ЕСТЬNULL(КурсыВалютДокумента.Кратность, 1) / (ЕСТЬNULL(КурсыВалютДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалют.Кратность, 1))) КАК СуммаСНДС
	|ИЗ
	|	втМатериалы КАК втМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыВалютДокумента
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
	|		ПО втМатериалы.Валюта = КурсыВалют.Валюта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПоступлениеМатериалов КАК упПоступлениеМатериалов
	|		ПО (упПоступлениеМатериалов.Ссылка = &Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	упПоступлениеМатериалов.Склад,
	|	втМатериалы.Материал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗапчасти.Запчасть,
	|	втЗапчасти.Цена,
	|	втЗапчасти.Валюта
	|ИЗ
	|	втЗапчасти КАК втЗапчасти";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Валюта", упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Партия", ?(ДополнительныеСвойства.МетодОценки = Перечисления.упМетодыОценкиСтоимости.ФИФО, ДокументСсылка, документы.упПоступлениеМатериалов.ПустаяСсылка()));
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	Если Не Результат[КоличествоТаблиц - 2].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ОстаткиМатериалов", Результат[КоличествоТаблиц - 2].Выгрузить());
	КонецЕсли; 
	
	Если Не Результат[КоличествоТаблиц - 1].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ЦеныЗапчастей", Результат[КоличествоТаблиц - 1].Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
