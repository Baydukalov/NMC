
#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИнициализироватьДокумент(ДанныеЗаполнения);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
			
	ДокументЗаполненКорректно(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ПроцедурыИФункцииОбеспеченияПроведения

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует движения по регистрам.
//
// Параметры: 
//  Нет.
//
Процедура ДвиженияПоРегистрам()
	
	Движения.упПравилаРасчетаТарифов.Записывать = Истина;
	Движения.упПравилаРасчетаТарифов.Очистить();
	
	Для Каждого текСтрока Из ПравилаРасчета Цикл
		НовДвижение = Движения.упПравилаРасчетаТарифов.Добавить();
		НовДвижение.Регистратор				= Ссылка;
		НовДвижение.Период					= ДатаВступленияВСилу;
		НовДвижение.Организация				= Организация;
		НовДвижение.ПравилоРасчета			= текСтрока.ПравилоРасчета;
		НовДвижение.СтатьяДоходовРасходов	= текСтрока.СтатьяДоходовРасходов;
		НовДвижение.Валюта					= текСтрока.Валюта;
		НовДвижение.ГруппаТарифов			= ГруппаТарифов;
	КонецЦикла;
	
Конецпроцедуры // ДвиженияПоРегистрам()


// Проверяет корректно ли осуществлено заполнение документа.
//
// Параметры:
//  Отказ - Булево - признак отказа.
//
Процедура ДокументЗаполненКорректно(Отказ)
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	|	тбСтатьиДоходовРасходов.СтатьяДоходовРасходов
	|ПОМЕСТИТЬ втСтатьиДоходовРасходов
	|ИЗ
	|	Документ.упУстановкаПравилРасчетаДоходовИРасходов.ПравилаРасчета КАК тбСтатьиДоходовРасходов
	|ГДЕ
	|	тбСтатьиДоходовРасходов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтатьиДоходовРасходов.СтатьяДоходовРасходов
	|ИЗ
	|	втСтатьиДоходовРасходов КАК тбСтатьиДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСтатьиДоходовРасходов.СтатьяДоходовРасходов
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(тбСтатьиДоходовРасходов.СтатьяДоходовРасходов) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтатьиДоходовРасходов.СтатьяДоходовРасходов
	|ИЗ
	|	РегистрСведений.упПравилаРасчетаТарифов КАК тбТарифы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтатьиДоходовРасходов КАК тбСтатьиДоходовРасходов
	|		ПО тбТарифы.СтатьяДоходовРасходов = тбСтатьиДоходовРасходов.СтатьяДоходовРасходов
	|ГДЕ
	|	тбТарифы.Период = &Период
	|	И тбТарифы.Организация = &Организация
	|	И тбТарифы.ГруппаТарифов = &ГруппаТарифов
	|	И НЕ тбТарифы.Регистратор = &Ссылка";
	текЗапрос.УстановитьПараметр("Ссылка", 		  ЭтотОбъект.Ссылка);
	текЗапрос.УстановитьПараметр("Период", 		  НачалоДня(ЭтотОбъект.ДатаВступленияВСилу));
	текЗапрос.УстановитьПараметр("Организация",   ЭтотОбъект.Организация);
	текЗапрос.УстановитьПараметр("ГруппаТарифов", ЭтотОбъект.ГруппаТарифов);
	
	текРезультат = текЗапрос.ВыполнитьПакет();
	
	тбзПовторяющиесяСтроки 		 = текРезультат[1].Выгрузить();
	Если тбзПовторяющиесяСтроки.Количество()>0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В табличной части есть строки с повторяющейся номенклатурой'"),
				ЭтотОбъект,
				"",
				,
				Отказ);
		Возврат;
	КонецЕсли;
	
	тбзЗначенияПоказателейНаДату = текРезультат[2].Выгрузить();
	
	Для каждого текСтрока Из тбзЗначенияПоказателейНаДату Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Для статьи:"""+текСтрока.СтатьяДоходовРасходов+""" значение на дату:"+Формат(ЭтотОбъект.ДатаВступленияВСилу,"ДФ=dd.MM.yyyy")+" уже установлено"+"'"),
				ЭтотОбъект,
				"",
				,
				Отказ);
	КонецЦикла;
	
КонецПроцедуры // ДокументЗаполненКорректно()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)

	Организация		= Справочники.Организации.ОрганизацияПоУмолчанию();
	ТипДокумента	= ПредопределенноеЗначение("Перечисление.упТипыДокументаУстановкаПравилРасчета.Доходы");
	ДатаВступленияВСилу	= ТекущаяДата();

КонецПроцедуры

#КонецОбласти 
