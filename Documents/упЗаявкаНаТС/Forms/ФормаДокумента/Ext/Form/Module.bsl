#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли; 
	
	Статус = Документы.упЗаявкаНаТС.ПолучитьСтатус(Объект.Ссылка);
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиНаТС(Объект.Ссылка, Статус, Элементы, Объект.Тендер);
	УстановитьПредставлениеСтатуса();
	УстановитьЗаголовки();
		
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект, Параметры, Ложь);
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.Рейс));

	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать
	
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах	= ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	ЭтаФорма.УчитыватьМассу                     = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем                     = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест            = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьЛинейныеРазмеры           = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	ЭтаФорма.ВедетсяВалютныйУчет				= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ЭтаФорма.ЭтоПеревозчик                      = РольДоступна("упПеревозчик");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		УстановитьВалютуУчета();
		Объект.Валюта = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
		ЭтаФорма.ВалютаУпрУчета = Объект.Валюта;
	КонецЕсли;
	
	ЭтаФорма.КоэффициентПересчетаОбъема 		= упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры	

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;	
	
	НовыйСтатус = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Истина
		И ИмяСобытия = "СменаСтатуса" 
		И Источник = Объект.Ссылка 
	Тогда
		ИзменилсяСтатус();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство, Документы.упРейс.ПолучитьВремяНачалаРейса(ТекущийОбъект.Рейс));

	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФорма
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикПриИзменении(Элемент)
	Объект.КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Объект.Перевозчик);		
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		СтруктураСтатьяРасходов = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
		ТекущаяСтрока.ПравилоРаспределения = СтруктураСтатьяРасходов.ПравилаРаспределения;
		ТекущаяСтрока.СтавкаНДС = СтруктураСтатьяРасходов.СтавкаНДС;
		ПересчитатьСтроку(Элемент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		
		ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
		
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		
		ТекущаяСтрока.Количество 	= 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткОтбор = Новый Структура();
	Если ЗначениеЗаполнено(Объект.Перевозчик) Тогда
		сткОтбор.Вставить("Партнер", Объект.Перевозчик);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ТипТС) Тогда
		сткОтбор.Вставить("ТипТС", Объект.ТипТС);
	КонецЕсли;
	сткПараметры = Новый Структура("Отбор",сткОтбор);
	ОткрытьФорму("Справочник.упТранспортныеСредства.ФормаВыбора", сткПараметры, Элемент, УникальныйИдентификатор);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СтатусОтправить(Команда)
	
	Если УстановитьСтатусОтправитьНаСервере() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("СменаСтатусаЗаявкиНаТС",, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтклонить(Команда)
	
	Если УстановитьСтатусОтклонитьНаСервере() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("СменаСтатусаЗаявкиНаТС",, Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлановыеРасходы(Команда)
	
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьПлановыеРасходыКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыеРасходы(Команда) 
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		РассчитатьПлановыеРасходыНаСервере();
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Объект.Соглашение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыеРасходыСРасшифровкой(Команда)
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		мсвПриемников = РассчитатьПлановыеРасходыСРасшифровкойНаСервере();
		РассчитатьИтоговуюСуммуРасходы();
		Для каждого Приемник Из мсвПриемников Цикл
			Приемник.Приемник.Показать("Расшифровка расчетов");	
		КонецЦикла;
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Объект.Соглашение");
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура СтатусПредложение(Команда)
	
	упСервисныеФункцииКлиент.СформироватьПредложениеПеревозчика(Объект.Ссылка);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьПредставлениеСтатуса()
	
	Если ЭтоПеревозчик Тогда
		Если Статус = Перечисления.упСтатусыЗаявкиНаТС.Создана Тогда
			СтатусПредставление = Неопределено;
		ИначеЕсли Статус = Перечисления.упСтатусыЗаявкиНаТС.Отправлена Тогда
			СтатусПредставление = Перечисления.упСтатусыЗаявкиНаТСДляПеревозчика.Новая;
		ИначеЕсли Статус = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение Тогда
			СтатусПредставление = Перечисления.упСтатусыЗаявкиНаТСДляПеревозчика.ОтправленоПредложение;
		ИначеЕсли Статус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
			СтатусПредставление = Перечисления.упСтатусыЗаявкиНаТСДляПеревозчика.Подтверждена;
		ИначеЕсли Статус = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Тогда
			СтатусПредставление = Перечисления.упСтатусыЗаявкиНаТСДляПеревозчика.Отклонена;	
		КонецЕсли;
	Иначе
		СтатусПредставление = Статус;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда         
		ЕИ_Грузов = сткПредставления.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
		СимволРазделения = "х";
	КонецЕсли;
	
КонецПроцедуры // УстановитьЗаголовки()

&НаСервере
Процедура ЗаблокироватьЭлементы()
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Если Статус = Перечисления.упСтатусыЗаявкиНаТС.Отклонена Тогда 
			ТолькоПросмотр = Истина;
		ИначеЕсли Статус = Перечисления.упСтатусыЗаявкиНаТС.Отправлена
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаТС.ПолученоПредложение Тогда
			Для Каждого текЭлемент Из Элементы Цикл 
				Если ТипЗнч(текЭлемент) = Тип("ПолеФормы")
					ИЛИ ТипЗнч(текЭлемент) = Тип("ТаблицаФормы") Тогда 
					текЭлемент.ТолькоПросмотр = Истина;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;	
	КонецЕсли;	
	
	Если Истина
		И НЕ ТолькоПросмотр
		И ВедетсяРаботаВРазныхЧасовыхПоясах
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
	//Если Объект.Тендер Тогда
	//	Элементы.СтатусПодтвердить.Доступность = Ложь;
	//КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусОтправитьНаСервере()
	
	Если (Статус = Перечисления.упСтатусыЗаявкиНаТС.Создана) Тогда 
		Возврат ИзменитьСтатусЗаявкиНаТС(Перечисления.упСтатусыЗаявкиНаТС.Отправлена);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""Создана""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

&НаСервере
Функция УстановитьСтатусОтклонитьНаСервере()
	
	Возврат ИзменитьСтатусЗаявкиНаТС(Перечисления.упСтатусыЗаявкиНаТС.Отклонена);

КонецФункции

&НаСервере
Функция ИзменитьСтатусЗаявкиНаТС(СтатусЗаявкиНаТС)
	
	НовыйСтатус = СтатусЗаявкиНаТС;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение
        	Возврат Ложь;    
		КонецПопытки;
		
		ИзменилсяСтатус();
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус()
	Статус = Документы.упЗаявкаНаТС.ПолучитьСтатус(Объект.Ссылка);
	УстановитьПредставлениеСтатуса();
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиНаТС(Объект.Ссылка, Статус, Элементы, Объект.Тендер);	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.МаксимальнаяШирина * Объект.МаксимальнаяВысота * Объект.МаксимальнаяДлина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	
	ЗаполнитьЗаявкуНаТС();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаявкуНаТС()
	
	Документы.упРейс.ЗаполнитьЗаявкуНаТС(Объект, Объект.Рейс);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ВзаимодействияКлиент.КонтактПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи, "упЗаявкаНаТС");
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
// Процедура перезаполнения плановых расходов.
//
Процедура ЗаполнитьПлановыеРасходыКлиент(Результат, ДополнительныеПараметры) Экспорт
	
	Если Объект.ПлановыеРасходы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполненииПлановыхРасходов", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Плановые расходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПлановыеРасходыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ОбработатьВопросОПерезаполненииПлановыхРасходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПлановыеРасходыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыеРасходыНаСервере()
	
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ЗаполнитьПлановыеРасходы();
	ЗначениеВРеквизитФормы(текОбъект,"Объект");
				
КонецПроцедуры

Процедура РассчитатьПлановыеРасходыНаСервере()

	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.РассчитатьПлановыеРасходы();
	ЗначениеВРеквизитФормы(текОбъект,"Объект");
	
КонецПроцедуры

&НаСервере
Функция РассчитатьПлановыеРасходыСРасшифровкойНаСервере()
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;
	
КонецФункции

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ПлановыеРасходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.Рейс));
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) 
			И НЕ (ЗначениеЗаполнено(Объект.Перевозчик)
					И ЗначениеЗаполнено(Объект.ТипТС)) Тогда
		сткРеквизитыТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ТранспортноеСредство, "ТипТС, Партнер");
		Если НЕ ЗначениеЗаполнено(Объект.ТипТС) Тогда
			Объект.ТипТС = сткРеквизитыТС.ТипТС;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.Перевозчик) Тогда
			Объект.Перевозчик = сткРеквизитыТС.Партнер;
			Объект.КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Объект.Перевозчик);		
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеРасходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект, Объект.Соглашение);
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

#КонецОбласти
