
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Тендер", Тендер);
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		ДополнительныеСвойства.Вставить("Ссылка", Ссылка);
		
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Статус = Перечисления.упСтатусыЗаявкиНаТС.ПустаяСсылка();	
			ДополнительныеСвойства.Вставить("Статус", Статус);
		КонецЕсли;
		
		упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Этап", Этап);
			
	упРейс.СформироватьДвиженияСтатусЗаявкиНаТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если Не Отказ И ЗначениеЗаполнено(Этап)   Тогда
		Если ДополнительныеСвойства.ЗаписыватьСтатус И ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаявкиНаТС.Подтверждена Тогда
			БлокировкаДанных = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упЭтапПодбораПеревозчика");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Этап);
			
			Попытка
				БлокировкаДанных.Заблокировать();
			Исключение
				Возврат;
			КонецПопытки;
			
			ЭтапОбъект = Этап.ПолучитьОбъект();
			ЭтапОбъект.ЗавершенУспешно = Истина;
			ЭтапОбъект.Записать();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ДополнительныеСвойства.Свойство("Статус") Тогда 
		Возврат;
	Иначе
		ПроверяемыйСтатус = ДополнительныеСвойства.Статус;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ЗначениеЗаполнено(Этап) Тогда
		ТекстСообщения = НСтр("ru = 'Для заявок на ТС, оформляемых с использованием механизма поэтапного подбора перевозчика, отмена проведения запрещена.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли; 
	
	Если Не Отказ Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упСтатусыЗаявокНаТС.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.упСтатусыЗаявокНаТС КАК упСтатусыЗаявокНаТС
		|ГДЕ
		|	НЕ упСтатусыЗаявокНаТС.Регистратор = &Ссылка
		|	И упСтатусыЗаявокНаТС.Документ = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'Имеется другой документ, изменивший статус заявки (%1). Отмена проведения запрещена.'");
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(ТекстСообщения, Выборка.Регистратор),,,, Отказ);
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
			
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

// Процедура заполнения табличной части "ПлановыеРасходы".
//
Процедура ЗаполнитьПлановыеРасходы() Экспорт
	
	ПлановыеРасходы.Очистить();
	
	тбзРасходы = упРаспределениеРасходов.ПолучитьСписокСтатейРасходов(Ссылка, ТипТС, Соглашение);
			
	Для каждого текСтатьяРасходов Из тбзРасходы Цикл
		
		текСтрока = ПлановыеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(текСтрока, текСтатьяРасходов);
		текСтрока.Валюта				= Валюта;
		текСтрока.Сумма					= 0;
		текСтрока.СуммаНДС				= 0;
			
	КонецЦикла;
	
КонецПроцедуры

// Процедура пересчета итоговых сумм.
//
Процедура РассчитатьПлановыеРасходы() Экспорт
	
	ВходныеПараметры = Новый Структура();  
	упТарификацияВызовСервера.Рассчитать(ЭтотОбъект, ВходныеПараметры);
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(ЭтотОбъект, "ПлановыеРасходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	
КонецПроцедуры

#КонецОбласти  

#КонецЕсли




