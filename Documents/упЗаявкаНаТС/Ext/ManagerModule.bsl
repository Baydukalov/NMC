
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Данные.Этап) Тогда
		Если Данные.Тендер Тогда
			Представление = СтрШаблон(НСтр("ru = 'Тендерная заявка на ТС %1 от %2 (%3)'"), Данные.Номер, Данные.Дата, Данные.Этап);
		Иначе	
			Представление = СтрШаблон(НСтр("ru = 'Заявка на ТС %1 от %2 (%3)'"), Данные.Номер, Данные.Дата,  Данные.Этап);
		КонецЕсли;
	ИначеЕсли Данные.Тендер Тогда
		Представление = СтрШаблон(НСтр("ru = 'Тендерная заявка на ТС %1 от %2'"), Данные.Номер, Данные.Дата);
	Иначе
		Представление = СтрШаблон(НСтр("ru = 'Заявка на ТС %1 от %2'"), Данные.Номер, Данные.Дата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Данные.ЧасовойПояс) Тогда
		Представление = упСервисныеФункции.СформироватьЗаголовокСЧасовымПоясом(Представление, Данные.ЧасовойПояс);
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("Этап");
	Поля.Добавить("Тендер");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ЧасовойПояс");
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Заполнение реквизитов предложения перевозчика по заявке на ТС.
//
// Параметры:
//  ПредложениеОбъект - ДокументОбъект.упПредложениеПеревозчика. 
//
Процедура ОбработкаЗаполненияПредложенияПеревозчикаПоЗаявкеНаТС(ПредложениеОбъект) Экспорт
	
	Если Не ЗначениеЗаполнено(ПредложениеОбъект.ЗаявкаНаТС) Тогда
		Возврат;
	КонецЕсли;
	
	сткРеквизитыЗаявкиНаТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПредложениеОбъект.ЗаявкаНаТС, "Перевозчик, КонтрагентПеревозчика, Соглашение, ТипТС, НачалоРейса, ОкончаниеРейса, ЧасовойПояс");
	ЗаполнитьЗначенияСвойств(ПредложениеОбъект, сткРеквизитыЗаявкиНаТС);
	ПредложениеОбъект.ВремяПодачи     = сткРеквизитыЗаявкиНаТС.НачалоРейса;
	ПредложениеОбъект.ВремяЗавершения = сткРеквизитыЗаявкиНаТС.ОкончаниеРейса;
	
КонецПроцедуры

// Функция возвращает текущий статус документа.
//
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаявкаНаТС - Ссылка на документ.
//
// Возвращаемое значение:
//   Перечисление   - ПеречислениеСсылка.упСтатусыЗаявкиНаТС - Статус.
//
Функция ПолучитьСтатус(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыЗаявкиНаТС.Создана;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыЗаявокНаТССрезПоследних.Статус,
	|	упЗаявкаНаТС.ПометкаУдаления
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних(, Документ = &Документ) КАК упСтатусыЗаявокНаТССрезПоследних
	|		ПО упСтатусыЗаявокНаТССрезПоследних.Документ = упЗаявкаНаТС.Ссылка
	|ГДЕ
	|	упЗаявкаНаТС.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыЗаявкиНаТС.Отклонена;
	Иначе	
		Возврат Перечисления.упСтатусыЗаявкиНаТС.Создана;
	КонецЕсли;
	
КонецФункции		

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Взаимодействия.

// Возвращает партнера и контактных лиц сделки.
// 
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаявкаНаТС - Ссылка на документ.
//
// Возвращаемое значение:
//   Ссылка   - СправочникСсылка - Контакт.
//
Функция ПолучитьКонтакты(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоКонтактам();
	Запрос.УстановитьПараметр("Предмет",Ссылка);
	
	НачатьТранзакцию();
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Результат = Неопределено;
		Иначе
			Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контакт");
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Получить текст запроса по контактам.
// 
// Параметры:
//  ТекстВременнаяТаблица  - Строка - текст.
//  Объединить  - Булево - Выполнить объединение.
//
// Возвращаемое значение:
//   Строка   - СтрокаЗапроса - Текст.
//
Функция ТекстЗапросаПоКонтактам(ТекстВременнаяТаблица = "", Объединить = Ложь) Экспорт
	
	ШаблонВыбрать = ?(Объединить,"ВЫБРАТЬ РАЗЛИЧНЫЕ","ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ");
	
	ТекстЗапроса = "
	|%ШаблонВыбрать%
	|	упЗаявкаНаТС.Перевозчик КАК Контакт " + ТекстВременнаяТаблица + "
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|ГДЕ
	|	упЗаявкаНаТС.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаТС.Перевозчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаТС.КонтрагентПеревозчика
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|ГДЕ
	|	упЗаявкаНаТС.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаТС.КонтрагентПеревозчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ШаблонВыбрать%",ШаблонВыбрать);
	
	Если Объединить Тогда
		
		ТекстЗапроса = "
		| ОБЪЕДИНИТЬ ВСЕ
		|" + ТекстЗапроса;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Рейс");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти 

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // ДОГОВОР  ЗАЯВКА НА ЗАКАЗ ТРАНСПОРТНОГО СРЕДСТВА
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.упЗаявкаНаТС";
  КомандаПечати.Идентификатор = "ЗаявкаНаТС";
  КомандаПечати.Представление = НСтр("ru = 'Заявка на ТС'");
  КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  
КонецПроцедуры // ДобавитьКомандыПечати()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаНаТС") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаявкаНаТС",
		НСтр("ru = 'Заявка на ТС'"),
		СформироватьПечатнуюФорму_ЗаявкаНаТС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаявкаНаТС.ПФ_MXL_ЗаявкаНаТС");
	КонецЕсли;

КонецПроцедуры // Печать()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект печати.
//
// Возвращаемое значение:
//  ТабличныйДокумент
//
Функция СформироватьПечатнуюФорму_ЗаявкаНаТС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упЗаявкаНаТС.ЗАЯВКА_НА_ТС";
	ТабличныйДокумент.РазмерСтраницы = "A4";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСверху = 15;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.ПолеСправа = 15;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаявкаНаТС.ПФ_MXL_ЗаявкаНаТС");
	
	ОбластьШапки = Макет.ПолучитьОбласть("Шапка");
	ОбалстьЗаказчик = Макет.ПолучитьОбласть("Заказчик");
	ОбластьГрузоотправитель = Макет.ПолучитьОбласть("Грузоотправитель");
	ОбалстьГрузополучатель = Макет.ПолучитьОбласть("Грузополучатель");
	ОбластьТипТС = Макет.ПолучитьОбласть("ТипТС");
	ОбластьВидыЗагрузок = Макет.ПолучитьОбласть("ВидыЗагрузок");
	ОбластьОписаниеГруза = Макет.ПолучитьОбласть("ОписаниеГруза");
	ОбластьДанныеВодителя = Макет.ПолучитьОбласть("ДанныеВодителя");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаТС.Ссылка КАК Заявка,
	|	упРейс.Ссылка КАК Рейс
	|ПОМЕСТИТЬ вт_Документы
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|	ПО упЗаявкаНаТС.Рейс = упРейс.Ссылка
	|ГДЕ упЗаявкаНаТС.Ссылка В  (&МассивОбъектов)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаботыПоРейсуПогрузка.Рейс КАК Рейс,
	|	МИНИМУМ(ЕСТЬNULL(РаботыПоРейсуПогрузка.ПлановоеВремяНачалаРабот, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодПогрузки,
	|	МАКСИМУМ(ЕСТЬNULL(РаботыПоРейсуРазгрузка.ПлановоеВремяНачалаРабот, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодРазгрузки
	|ПОМЕСТИТЬ вт_ПериодыРейса
	|ИЗ
	|	вт_Документы КАК вт_Документы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК РаботыПоРейсуПогрузка
	|	ПО вт_Документы.Рейс = РаботыПоРейсуПогрузка.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК РаботыПоРейсуРазгрузка
	|	ПО вт_Документы.Рейс = РаботыПоРейсуРазгрузка.Рейс
	|ГДЕ ИСТИНА
	|	И НЕ (РаботыПоРейсуПогрузка.Отменена)
	|	И РаботыПоРейсуПогрузка.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	И НЕ (РаботыПоРейсуРазгрузка.Отменена)
	|	И РаботыПоРейсуРазгрузка.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|СГРУППИРОВАТЬ ПО
	|	РаботыПоРейсуПогрузка.Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявкаНаТС.Ссылка КАК Заявка,
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.Организация КАК Заказчик,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упРейс.Организация) КАК ПредставлениеЗаказчик,
	|	упРейс.Организация.ИНН КАК ЗаказчикИНН,
	|	упРейс.Организация.КПП КАК ЗаказчикКПП,
	|	упЗаявкаНаТС.Номер КАК Номер,
	|	упЗаявкаНаТС.Дата КАК Дата,
	|	ЕСТЬNULL(вт_ПериодыРейса.ПериодПогрузки, ДАТАВРЕМЯ(1, 1, 1)) КАК ПериодПогрузки,
	|	ЕСТЬNULL(вт_ПериодыРейса.ПериодРазгрузки, ДАТАВРЕМЯ(1, 1, 1)) КАК ПериодРазгрузки,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упЗаявкаНаТС.КонтрагентПеревозчика) КАК Перевозчик
	|ИЗ
	|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПериодыРейса КАК вт_ПериодыРейса
	|		ПО упРейс.Ссылка = вт_ПериодыРейса.Рейс
	|	ПО упЗаявкаНаТС.Рейс = упРейс.Ссылка
	|ГДЕ упЗаявкаНаТС.Ссылка В (
	|		ВЫБРАТЬ
	|			вт_Документы.Заявка КАК Заявка
	|		ИЗ
	|			вт_Документы КАК вт_Документы)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
	|	упРаботыПоРейсуСрезПоследних.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				вт_Документы.Рейс КАК Рейс
	|			ИЗ
	|				вт_Документы КАК вт_Документы)) КАК упРаботыПоРейсуСрезПоследних
	|ГДЕ ИСТИНА
	|	И НЕ (упРаботыПоРейсуСрезПоследних.Отменена)
	|	И упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияТ.Рейс КАК Рейс,
	|	втЗаданияТ.ГрузовоеМесто.ТипГруза КАК ТипГруза,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, втЗаданияТ.ГрузовоеМесто.Масса))) КАК Масса
	|ИЗ
	|	втЗадания КАК втЗаданияТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗадания КАК втЗадания
	|			ГДЕ втЗадания.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И втЗаданияТ.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И втЗаданияТ.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И втЗаданияТ.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И втЗаданияТ.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|СГРУППИРОВАТЬ ПО
	|	втЗаданияТ.Рейс,
	|	втЗаданияТ.ГрузовоеМесто.ТипГруза
	|";

	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаЗаказчик = ПакетЗапросов[2].Выбрать();
	ТаблицаРейс = ПакетЗапросов[4].Выгрузить();
	
	ФорматКоличества = "ЧДЦ=3; ЧРД=.; ЧН=0.000; ЧГ=0";
	ФорматСуммы = "ЧДЦ=2; ЧРД=.; ЧН=0.00; ЧГ=0";
	Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	КоэффициентПересчетаМассы = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаМассы");
	КоэффициентПересчетаМассы = ?(КоэффициентПересчетаМассы = 0, 1, КоэффициентПересчетаМассы);
	
	
	Пока ВыборкаЗаказчик.Следующий() Цикл 
		
		тдНовый = Новый ТабличныйДокумент;
		тдНовый.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗАЯВКА_НА_ТС";
		
		ДокументОснование = ВыборкаЗаказчик.Заявка;
		Рейс = ВыборкаЗаказчик.Рейс;
		ТекущийОтбор = Новый Структура("Заявка,Рейс",ДокументОснование,Рейс);
		
		ОбластьШапки.Параметры.ДеньЗаявки = Формат(ВыборкаЗаказчик.Дата,"ДФ='dd'");
		ОбластьШапки.Параметры.МесяцЗаявки = Формат(ВыборкаЗаказчик.Дата,"ДФ='MMMM'");		
		ОбластьШапки.Параметры.годЗаявки = Формат(ВыборкаЗаказчик.Дата,"ДФ=yyyy");
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		тдНовый.Вывести(ОбластьШапки);
		ЮрАдресОрганизации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ВыборкаЗаказчик.Заказчик, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
		ТелефонОрганизации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ВыборкаЗаказчик.Заказчик, Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
		ФаксОрганизации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ВыборкаЗаказчик.Заказчик, Справочники.ВидыКонтактнойИнформации.ФаксОрганизации);
		ОбалстьЗаказчик.Параметры.ПлательщикНаименование = ВыборкаЗаказчик.ПредставлениеЗаказчик;
		ОбалстьЗаказчик.Параметры.ПлательщикЮрАдрес = ЮрАдресОрганизации;
		ОбалстьЗаказчик.Параметры.ПлательщикТелефон = ТелефонОрганизации + " / " + ФаксОрганизации;
		ОбалстьЗаказчик.Параметры.ПлательщикИНН = ВыборкаЗаказчик.ЗаказчикИНН + " / " + ВыборкаЗаказчик.ЗаказчикКПП;
		
		тдНовый.Вывести(ОбалстьЗаказчик);
		
		// Грузоотправитель
		ТаблицаЗначений = ПолучитьГрузоотправителейРейса(Рейс);		
		СтрокаНомер = 0;
		КоличествоСтрок = ТаблицаЗначений.Количество();
		Если КоличествоСтрок Тогда			
			Для каждого ТекущийГрузоотправитель Из ТаблицаЗначений Цикл
				ОбластьГрузоотправитель.Параметры.ГрузоотправительНаименование = ТекущийГрузоотправитель.Грузоотправитель;
				ОбластьГрузоотправитель.Параметры.ГрузоотправительИНН = ТекущийГрузоотправитель.ИНН;
				ОбластьГрузоотправитель.Параметры.ГрузоотправительЮрАдрес = ТекущийГрузоотправитель.ЮридическийАдрес;
				ОбластьГрузоотправитель.Параметры.ГрузоотправительФактАдрес = ТекущийГрузоотправитель.АдресОтправления;
				ОбластьГрузоотправитель.Параметры.ГрузоотправительТелефон = ТекущийГрузоотправитель.Телефон;
				
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбластьГрузоотправитель);
				Если СтрокаНомер = КоличествоСтрок Тогда
					СтрокаСПодвалом.Добавить(СтрокаСПодвалом);
				КонецЕсли;
				Попытка
					Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
						тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					тдНовый.Вывести(ОбластьГрузоотправитель);
				Исключение
					тдНовый.Вывести(ОбластьГрузоотправитель);
				КонецПопытки;
				СтрокаНомер = СтрокаНомер + 1;
			КонецЦикла;
			
		Иначе
			тдНовый.Вывести(ОбластьГрузоотправитель);
		КонецЕсли;
		
		// Грузополучатель
		
		ТаблицаЗначений = ПолучитьГрузополучателейРейса(Рейс);
		СтрокаНомер = 0;
		КоличествоСтрок = ТаблицаЗначений.Количество();
		Если КоличествоСтрок Тогда			
			Для каждого ТекущийГрузополучатель Из ТаблицаЗначений Цикл
				ЗаполнитьЗначенияСвойств(ОбалстьГрузополучатель.Параметры,ТекущийГрузополучатель);
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбалстьГрузополучатель);
				Если СтрокаНомер = КоличествоСтрок Тогда
					СтрокаСПодвалом.Добавить(СтрокаСПодвалом);
				КонецЕсли;
				Попытка
					Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
						тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					тдНовый.Вывести(ОбалстьГрузополучатель);
				Исключение
					тдНовый.Вывести(ОбалстьГрузополучатель);
				КонецПопытки;
				СтрокаНомер = СтрокаНомер + 1;
			КонецЦикла;
			
		Иначе
			тдНовый.Вывести(ОбалстьГрузополучатель);
		КонецЕсли;
		
		СтруктураТС = ПолучитьЗаявкиТСРейса(ДокументОснование);
		ОбластьТипТС.Параметры.ТипТС = СтруктураТС.ТипТС;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьТипТС);
		Попытка
			Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			тдНовый.Вывести(ОбластьТипТС);
		Исключение
			тдНовый.Вывести(ОбластьТипТС);
		КонецПопытки;
		
		ГрузополучательВсегоГрузовыхМест = "";
		ТекущийОтбор = Новый Структура("Рейс",Рейс);
		НайденныеСтроки = ТаблицаРейс.НайтиСтроки(ТекущийОтбор);
		Масса = 0;
		Массив = Новый Массив;
		Для каждого ТекущаяМассаРейса Из ТаблицаРейс Цикл
			Масса = Масса + (ТекущаяМассаРейса.Масса*КоэффициентПересчетаМассы);
			Если Массив.Найти(ТекущаяМассаРейса.ТипГруза)=Неопределено Тогда
				Массив.Добавить(ТекущаяМассаРейса.ТипГруза);
			КонецЕсли;
		КонецЦикла;
		Если НЕ Масса = 0  Тогда
			ГрузополучательВсегоГрузовыхМест = Формат(Масса,"ЧРД=.; ЧГ=0");
		КонецЕсли;
		ГрузополучательХарактерГруза = "";
		Если Массив.Количество()  Тогда
			ГрузополучательХарактерГруза = СтрСоединить(Массив,"; ");
		КонецЕсли;
		
		ОбластьВидыЗагрузок.Параметры.ГрузополучательВсегоГрузовыхМест = ГрузополучательВсегоГрузовыхМест;
		ОбластьВидыЗагрузок.Параметры.ГрузополучательХарактерГруза = ГрузополучательХарактерГруза;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьВидыЗагрузок);
		Попытка
			Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			тдНовый.Вывести(ОбластьВидыЗагрузок);
		Исключение
			тдНовый.Вывести(ОбластьВидыЗагрузок);
		КонецПопытки;
		
		Структура = ПолучитьРегламентныеДокументыРейса(Рейс);
		
		ОбластьОписаниеГруза.Параметры.ГрузополучательДатаПодачиТС = Формат(ВыборкаЗаказчик.ПериодПогрузки,"ДЛФ=D");
		ОбластьОписаниеГруза.Параметры.ГрузополучательДатаразгрузкиТС = Формат(ВыборкаЗаказчик.ПериодРазгрузки,"ДЛФ=D");
		ОбластьОписаниеГруза.Параметры.ЗагрузкаДок1 = Структура.Грузоотправитель;
		ОбластьОписаниеГруза.Параметры.ВыгрузкаДок1 = Структура.Заказчик;
		ОбластьОписаниеГруза.Параметры.ДопИнформацияПоРейсу = СтруктураТС.ТемпературныйРежим;		 
		ОбластьОписаниеГруза.Параметры.СтоимостьПеревозки = СтруктураТС.СуммаЦелая;
		ОбластьОписаниеГруза.Параметры.СтоимостьПеревозкиОписание = СтрШаблон("рублей %1 копеек с НДС",СтруктураТС.СуммаДробная);
		ОбластьОписаниеГруза.Параметры.СрокОплаты = Структура.СрокОплаты;
		ОбластьОписаниеГруза.Параметры.ФормаОплаты = "Безналичный с НДС";
		ОбластьОписаниеГруза.Параметры.ФИОВодителя = СтруктураТС.Водитель;		 
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьОписаниеГруза);
		Попытка
			Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			тдНовый.Вывести(ОбластьОписаниеГруза);
		Исключение
			тдНовый.Вывести(ОбластьОписаниеГруза);
		КонецПопытки;		
		
		ОбластьДанныеВодителя.Параметры.ПаспортВодителя = СтруктураТС.ПаспортныеДанные;
		ОбластьДанныеВодителя.Параметры.РегДанныеТС = СтруктураТС.МаркаТягача;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьДанныеВодителя);
		Попытка
			Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			тдНовый.Вывести(ОбластьДанныеВодителя);
		Исключение
			тдНовый.Вывести(ОбластьДанныеВодителя);
		КонецПопытки;
		
		ОбластьПодвал.Параметры.Заказчик = ВыборкаЗаказчик.ПредставлениеЗаказчик;
		ОбластьПодвал.Параметры.Перевозчик = ВыборкаЗаказчик.Перевозчик;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если НЕ тдНовый.ПроверитьВывод(СтрокаСПодвалом) Тогда
				тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			тдНовый.Вывести(ОбластьПодвал);
		Исключение
			тдНовый.Вывести(ОбластьПодвал);
		КонецПопытки;
				
		тдНовый.ВывестиГоризонтальныйРазделительСтраниц();
		
		ТабличныйДокумент.Вывести(тдНовый);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДокументОснование);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ЗаявкаНаТС()

// Выполним анализ заявки на тс для получения данных в ПФ.
//
// Параметры:
//  ДокументОснование  - ДокументСсылка.упЗаявкаНаТС - Ссылка на документ отображения.
//
// Возвращаемое значение:
//   Структура - Ключи, данные, которыми заполним ПФ.
//
Функция ПолучитьЗаявкиТСРейса(ДокументОснование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	упПредложенияПеревозчикаСрезПоследних.ЗаявкаНаТС КАК Документ,
		|	упПредложенияПеревозчикаСрезПоследних.ПредложениеПеревозчика КАК Предложение
		|ПОМЕСТИТЬ вт_Предложения
		|ИЗ
		|	РегистрСведений.упПредложенияПеревозчика.СрезПоследних(&Период, ЗаявкаНаТС = &ДокументОснование) КАК упПредложенияПеревозчикаСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упСтатусыЗаявокНаТССрезПоследних.Регистратор КАК Регистратор,
		|	упСтатусыЗаявокНаТССрезПоследних.Документ КАК Заявка
		|ПОМЕСТИТЬ вт_Статус
		|ИЗ
		|	РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних(&Период, Документ = &ДокументОснование) КАК упСтатусыЗаявокНаТССрезПоследних
		|ГДЕ
		|	НЕ упСтатусыЗаявокНаТССрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Отклонена)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПодтверждениеЗаявкиНаТС.Рейс КАК Рейс,
		|	упПодтверждениеЗаявкиНаТС.ТемпературныйРежим КАК ТемпературныйРежим,
		|	упПодтверждениеЗаявкиНаТС.ТранспортноеСредство.ТипТС КАК ТипТС,
		|	упПодтверждениеЗаявкиНаТС.ТранспортноеСредство КАК ТС,
		|	упПодтверждениеЗаявкиНаТС.Водитель1 КАК Водитель,
		|	2 КАК Приоритет,
		|	ЕСТЬNULL(ПрицепыТССП_2.Прицеп, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК Прицеп,
		|	ВЫБОР
		|		КОГДА вт_Предложения.Предложение ЕСТЬ NULL
		|			ТОГДА вт_Статус.Заявка.СуммаРасходы
		|		ИНАЧЕ вт_Предложения.Предложение.СуммаРасходы
		|	КОНЕЦ КАК Сумма
		|ИЗ
		|	вт_Статус КАК вт_Статус
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПодтверждениеЗаявкиНаТС КАК упПодтверждениеЗаявкиНаТС
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних КАК ПрицепыТССП_2
		|			ПО упПодтверждениеЗаявкиНаТС.ТранспортноеСредство = ПрицепыТССП_2.ТранспортноеСредство
		|				И (ПрицепыТССП_2.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено))
		|		ПО вт_Статус.Регистратор = упПодтверждениеЗаявкиНаТС.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Предложения КАК вт_Предложения
		|		ПО вт_Статус.Заявка = вт_Предложения.Документ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_Статус.Заявка.Рейс,
		|	ЗНАЧЕНИЕ(Справочник.упТемпературныеРежимы.ПустаяСсылка),
		|	упПредложениеПеревозчика.ТипТС,
		|	упПредложениеПеревозчика.ТранспортноеСредство,
		|	упПредложениеПеревозчика.Водитель1,
		|	3,
		|	ЕСТЬNULL(ПрицепыТССП_3.Прицеп, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)),
		|	упПредложениеПеревозчика.СуммаРасходы
		|ИЗ
		|	вт_Статус КАК вт_Статус
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних КАК ПрицепыТССП_3
		|			ПО упПредложениеПеревозчика.ТранспортноеСредство = ПрицепыТССП_3.ТранспортноеСредство
		|				И (ПрицепыТССП_3.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено))
		|		ПО вт_Статус.Регистратор = упПредложениеПеревозчика.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упПеревозчикПоРейсуСрезПоследних.Рейс,
		|	упПеревозчикПоРейсуСрезПоследних.ТипТС.ТемпературныйРежим,
		|	упПеревозчикПоРейсуСрезПоследних.ТипТС,
		|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство,
		|	упПеревозчикПоРейсуСрезПоследних.Водитель1,
		|	4,
		|	ЕСТЬNULL(ПрицепыТССП_4.Прицеп, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)),
		|	вт_Статус.Заявка.СуммаРасходы
		|ИЗ
		|	вт_Статус КАК вт_Статус
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних КАК ПрицепыТССП_4
		|				ПО упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство = ПрицепыТССП_4.ТранспортноеСредство
		|					И (ПрицепыТССП_4.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено))
		|			ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
		|		ПО вт_Статус.Заявка.Рейс = упРейс.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	упЗаявкаНаТС.Рейс,
		|	упЗаявкаНаТС.ТемпературныйРежим,
		|	упЗаявкаНаТС.ТипТС,
		|	упЗаявкаНаТС.ТранспортноеСредство,
		|	ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка),
		|	5,
		|	ЕСТЬNULL(ПрицепыТССП_5.Прицеп, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)),
		|	упЗаявкаНаТС.СуммаРасходы
		|ИЗ
		|	вт_Статус КАК вт_Статус
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПрицепыТранспортныхСредств.СрезПоследних КАК ПрицепыТССП_5
		|			ПО упЗаявкаНаТС.ТранспортноеСредство = ПрицепыТССП_5.ТранспортноеСредство
		|				И (ПрицепыТССП_5.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено))
		|		ПО вт_Статус.Регистратор = упЗаявкаНаТС.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТипТС = "";
	ТемпературныйРежим = "";
	Водитель = "";
	ПаспортныеДанные = "";
	МаркаТягача = "";
	СтоимостьПеревозки = 0;
	Если Выборка.Следующий() Тогда
		СтоимостьПеревозки = Выборка.Сумма;
		Если ЗначениеЗаполнено(Выборка.ТипТС) Тогда
			ТипТС = Выборка.ТипТС.Наименование + СокрЛП(Выборка.ТипТС.ТипКузова);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ТемпературныйРежим) Тогда
			ТемпературныйРежим = СокрЛП(Выборка.ТемпературныйРежим);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Водитель) Тогда
			Водитель = СокрЛП(Выборка.Водитель);
			ПаспортныеДанные = "";
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДополнительныеРеквизиты.Ссылка КАК Реквизит
			|ПОМЕСТИТЬ вт_Реквизиты
			|ИЗ
			|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизиты
			|ГДЕ
			|	ДополнительныеРеквизиты.НаборСвойств = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_упСотрудники)
			|	И ДополнительныеРеквизиты.Наименование ПОДОБНО ""%паспорт%""
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	вт_Реквизиты.Реквизит,
			|	упСотрудникиДополнительныеРеквизиты.Значение,
			|	упСотрудникиДополнительныеРеквизиты.ТекстоваяСтрока КАК Представление
			|ИЗ
			|	Справочник.упСотрудники.ДополнительныеРеквизиты КАК упСотрудникиДополнительныеРеквизиты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Реквизиты КАК вт_Реквизиты
			|		ПО упСотрудникиДополнительныеРеквизиты.Свойство = вт_Реквизиты.Реквизит
			|ГДЕ
			|	упСотрудникиДополнительныеРеквизиты.Ссылка = &Водитель";
			
			Запрос.УстановитьПараметр("Водитель", Выборка.Водитель);
			
			ВыборкаПДВ = Запрос.Выполнить().Выбрать();			
			Пока ВыборкаПДВ.Следующий() Цикл
				Если ПустаяСтрока(ВыборкаПДВ.Представление)Тогда
					ПаспортныеДанные = ВыборкаПДВ.Представление;
				Иначе
					ПаспортныеДанные = Строка(ВыборкаПДВ.Значение);
				КонецЕсли;
				Если НЕ ПустаяСтрока(ПаспортныеДанные)Тогда
					Прервать;
				КонецЕсли;				
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ТС) Тогда
			МаркаТягача = "";
			МассивМарки = Новый Массив;
			Если ЗначениеЗаполнено(Выборка.ТС.Модель) Тогда
				МассивМарки.Добавить(Выборка.ТС.Модель);
			КонецЕсли;
			Если ЗначениеЗаполнено(Выборка.ТС.РегистрационныйНомер) Тогда
				МассивМарки.Добавить(Выборка.ТС.РегистрационныйНомер);
			КонецЕсли;
			Если ЗначениеЗаполнено(Выборка.Прицеп) Тогда
				Если ЗначениеЗаполнено(Выборка.Прицеп.Модель) Тогда
					МассивМарки.Добавить(Выборка.Прицеп.Модель);
				КонецЕсли;
				Если ЗначениеЗаполнено(Выборка.Прицеп.РегистрационныйНомер) Тогда
					МассивМарки.Добавить(Выборка.Прицеп.РегистрационныйНомер);
				КонецЕсли;
			КонецЕсли;
			МаркаТягача = СтрСоединить(МассивМарки," ");
		КонецЕсли;
		
	КонецЕсли;
	
	СуммаЦелая = Окр(СтоимостьПеревозки,0);
	СуммаДробная = СтоимостьПеревозки - СуммаЦелая;
	
	Структура = Новый Структура();
	Структура.Вставить("ТипТС",ТипТС);
	Структура.Вставить("ТемпературныйРежим",ТемпературныйРежим);
	Структура.Вставить("Водитель",Водитель);
	Структура.Вставить("ПаспортныеДанные",ПаспортныеДанные);
	Структура.Вставить("МаркаТягача",МаркаТягача);
	Структура.Вставить("СуммаЦелая",Формат(СуммаЦелая,"ЧДЦ=; ЧН=0; ЧГ=0"));
	МассивДроби = СтрРазделить(Формат(СуммаДробная,"ЧРД=.; ЧН=0.00; ЧГ=0"),".");
	Если МассивДроби.Количество()>1 Тогда
		Структура.Вставить("СуммаДробная",МассивДроби[1]);
	Иначе
		Структура.Вставить("СуммаДробная","00");
	КонецЕсли;
	
	
	Возврат Структура;

КонецФункции // ПолучитьЗаявкиТСРейса()

// Получим регламентные документы рейса.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ для которого, сформирована заявка на ТС.
//
// Возвращаемое значение:
//   Структура - Данные регламентных документов рейса.
//
Функция ПолучитьРегламентныеДокументыРейса(Рейс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	упРаботыПоРейсуСрезПоследних.Рейс,
		|	упРаботыПоРейсуСрезПоследних.Задание.Заказчик КАК Партнер
		|ПОМЕСТИТЬ вт_Заказчик
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упРаботыПоРейсуСрезПоследних
		|ГДЕ
		|	НЕ упРаботыПоРейсуСрезПоследних.Отменена
		|	И упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|
		|СГРУППИРОВАТЬ ПО
		|	упРаботыПоРейсуСрезПоследних.Рейс,
		|	упРаботыПоРейсуСрезПоследних.Задание.Заказчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Заказчик.Рейс,
		|	ПакетДокументов_Г.ТипРегламентногоДокумента КАК Документ,
		|	СУММА(ПакетДокументов_Г.КоличествоПолучитьОтУчастника) КАК Количество,
		|	ИСТИНА КАК Грузоотправитель,
		|	ПакетДокументов_Г.ВозвратДокументовПослеИсполненияРейса КАК ВозвратДокументов
		|ИЗ
		|	вт_Заказчик КАК вт_Заказчик
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упПакетыРегламентныхДокументов.ПакетДокументов КАК ПакетДокументов_Г
		|		ПО вт_Заказчик.Партнер.ПакетРегламентныхДокументов = ПакетДокументов_Г.Ссылка
		|			И (ПакетДокументов_Г.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Грузоотправитель))
		|ГДЕ
		|	НЕ ПакетДокументов_Г.КоличествоПолучитьОтУчастника ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_Заказчик.Рейс,
		|	ПакетДокументов_Г.ТипРегламентногоДокумента,
		|	ПакетДокументов_Г.Ссылка,
		|	ПакетДокументов_Г.ВозвратДокументовПослеИсполненияРейса
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_Заказчик.Рейс,
		|	ПакетДокументов_З.ТипРегламентногоДокумента,
		|	СУММА(ПакетДокументов_З.КоличествоПередатьУчастнику),
		|	ЛОЖЬ,
		|	ПакетДокументов_З.ВозвратДокументовПослеИсполненияРейса
		|ИЗ
		|	вт_Заказчик КАК вт_Заказчик
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упПакетыРегламентныхДокументов.ПакетДокументов КАК ПакетДокументов_З
		|		ПО вт_Заказчик.Партнер.ПакетРегламентныхДокументов = ПакетДокументов_З.Ссылка
		|			И (ПакетДокументов_З.Участник = ЗНАЧЕНИЕ(Перечисление.упУчастникиДокументооборотаПеревозки.Заказчик))
		|ГДЕ
		|	НЕ ПакетДокументов_З.КоличествоПередатьУчастнику ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_Заказчик.Рейс,
		|	ПакетДокументов_З.ТипРегламентногоДокумента,
		|	ПакетДокументов_З.Ссылка,
		|	ПакетДокументов_З.ВозвратДокументовПослеИсполненияРейса";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивГрузоотправитель = Новый Массив;
	МассивЗаказчик = Новый Массив;
	МассивЗаказчик_В = Новый Массив;
	       
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		Документ = Строка(Выборка.Документ);
		Количество = Формат(Выборка.Количество,"ЧРД=.; ЧН=0; ЧГ=0");
		КоличествоПрописью = ЧислоПрописью(Выборка.Количество,"Л = ru_RU",НСтр("ru = ',,,,,,,,0'"));
		
		Если Выборка.Грузоотправитель Тогда			
			МассивГрузоотправитель.Добавить(СтрШаблон("%1 (экз.) %2 (%3)",Документ,Количество,СокрЛП(КоличествоПрописью)));
		Иначе
			МассивЗаказчик.Добавить(СтрШаблон("%1 (экз.) %2 (%3)",Документ,Количество,СокрЛП(КоличествоПрописью)));
			Если Выборка.ВозвратДокументов > 0 Тогда			
				МассивЗаказчик_В.Добавить(Документ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Структура = Новый Структура;
	Структура.Вставить("Грузоотправитель",СтрСоединить(МассивГрузоотправитель,"; "));
	Структура.Вставить("Заказчик",СтрСоединить(МассивЗаказчик,"; "));
	Структура.Вставить("СрокОплаты","по оригиналам " + СтрСоединить(МассивЗаказчик_В,", "));
	
	Возврат Структура;

КонецФункции // ПолучитьРегламентныеДокументыРейса()

// Получим всех грузоотправителей рейса.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ для которого, сформирована заявка на ТС.
//
// Возвращаемое значение:
//   ТаблицаЗначений  - В строках список грузоотправителей рейса.
//
Функция ПолучитьГрузоотправителейРейса(Рейс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаботыПоРейсуСП.Рейс,
	|	РаботыПоРейсуСП.Задание,
	|	РаботыПоРейсуСП.ПлановоеВремяНачалаРабот КАК Период,
	|	ЕСТЬNULL(МаршрутПоРейсуСП.Адрес, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)) КАК Адрес,
	|	РаботыПоРейсуСП.Задание.КонтрагентОтправителя КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(РаботыПоРейсуСП.Задание.КонтрагентОтправителя)
	|	КОНЕЦ КАК ГрузоотправительПредставление,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ РаботыПоРейсуСП.Задание.КонтрагентОтправителя.ИНН
	|	КОНЕЦ КАК ГрузоотправительИНН,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ РаботыПоРейсуСП.Задание.КонтрагентОтправителя.КПП
	|	КОНЕЦ КАК ГрузоотправительКПП,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|		ИНАЧЕ РаботыПоРейсуСП.Задание.КонтактноеЛицоОтправителя
	|	КОНЕЦ КАК ГрузоотправительКЛ
	|ПОМЕСТИТЬ вт_Данные
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК РаботыПоРейсуСП
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК МаршрутПоРейсуСП
	|		ПО РаботыПоРейсуСП.Рейс = МаршрутПоРейсуСП.Рейс
	|			И РаботыПоРейсуСП.Идентификатор = МаршрутПоРейсуСП.Идентификатор
	|ГДЕ
	|	НЕ РаботыПоРейсуСП.Отменена
	|	И РаботыПоРейсуСП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Данные.Рейс,
	|	вт_Данные.Грузоотправитель
	|ИЗ
	|	вт_Данные КАК вт_Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Данные.Рейс,
	|	вт_Данные.Грузоотправитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Данные.Рейс,
	|	вт_Данные.Задание,
	|	вт_Данные.Период КАК Период,
	|	вт_Данные.Адрес,
	|	вт_Данные.Грузоотправитель,
	|	вт_Данные.ГрузоотправительПредставление,
	|	вт_Данные.ГрузоотправительИНН,
	|	вт_Данные.ГрузоотправительКПП,
	|	вт_Данные.ГрузоотправительКЛ
	|ИЗ
	|	вт_Данные КАК вт_Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	Выборка = ПакетЗапросов[1].Выбрать();
	ТаблицаГрузоотправитель = ПакетЗапросов[2].Выгрузить();
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	ТаблицаЗначений.Колонки.Добавить("Грузоотправитель",,"Грузоотправитель");
	ТаблицаЗначений.Колонки.Добавить("ИНН",,"ИНН");
	ТаблицаЗначений.Колонки.Добавить("ЮридическийАдрес",,"ЮридическийАдрес");
	ТаблицаЗначений.Колонки.Добавить("АдресОтправления",,"АдресОтправления");
	ТаблицаЗначений.Колонки.Добавить("Телефон",,"Телефон");
	
	
	
	Пока Выборка.Следующий() Цикл
		
		ТекущийОтбор = Новый Структура;
		ТекущийОтбор.Вставить("Рейс",Выборка.Рейс);
		ТекущийОтбор.Вставить("Грузоотправитель",Выборка.Грузоотправитель);
		
		НайденныеСтроки = ТаблицаГрузоотправитель.НайтиСтроки(ТекущийОтбор);
		
		Если НайденныеСтроки.Количество() Тогда
			
			ТекущаяСтрока = НайденныеСтроки[0];
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.Грузоотправитель = ТекущаяСтрока.ГрузоотправительПредставление;
			НоваяСтрока.ИНН = ТекущаяСтрока.ГрузоотправительИНН + " / " + ТекущаяСтрока.ГрузоотправительКПП;
			ЮрАдресКонтрагента = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузоотправитель, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
			НоваяСтрока.ЮридическийАдрес = ЮрАдресКонтрагента;
			НоваяСтрока.АдресОтправления = ТекущаяСтрока.Адрес;
			ТелефонКонтрагента = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузоотправитель, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
			ФаксКонтрагенты = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузоотправитель, Справочники.ВидыКонтактнойИнформации.ФаксКонтрагенты);
			EmailКонтрагента = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузоотправитель, Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
			
			МассивКИ = Новый Массив;
			Если ЗначениеЗаполнено(ТелефонКонтрагента) Тогда
				МассивКИ.Добавить("тел: " + ТелефонКонтрагента);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ФаксКонтрагенты) Тогда
				МассивКИ.Добавить(ФаксКонтрагенты);
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекущаяСтрока.ГрузоотправительКЛ) Тогда
				МассивКИ.Добавить(Строка(ТекущаяСтрока.ГрузоотправительКЛ));
			КонецЕсли;
			Если ЗначениеЗаполнено(EmailКонтрагента) Тогда
				МассивКИ.Добавить(EmailКонтрагента);
			КонецЕсли;
			
			НоваяСтрока.Телефон = СтрСоединить(МассивКИ,", ");			
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаЗначений;
	
КонецФункции // ПолучитьГрузоотправителейРейса()

// Получим всех грузополучателей Рейса.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ для которого, сформирована заявка на ТС.
//
// Возвращаемое значение:
//   ТаблицаЗначений  - в строках список Грузополучателей Рейса
//
Функция ПолучитьГрузополучателейРейса(Рейс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаботыПоРейсуСП.Рейс,
	|	РаботыПоРейсуСП.Задание,
	|	РаботыПоРейсуСП.ПлановоеВремяНачалаРабот КАК Период,
	|	ЕСТЬNULL(МаршрутПоРейсуСП.Адрес, ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)) КАК Адрес,
	|	РаботыПоРейсуСП.Задание.КонтрагентПолучателя КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(РаботыПоРейсуСП.Задание.КонтрагентПолучателя)
	|	КОНЕЦ КАК ГрузополучательПредставление,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ РаботыПоРейсуСП.Задание.КонтрагентПолучателя.ИНН
	|	КОНЕЦ КАК ГрузополучательИНН,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ РаботыПоРейсуСП.Задание.КонтрагентПолучателя.КПП
	|	КОНЕЦ КАК ГрузополучательКПП,
	|	ВЫБОР
	|		КОГДА РаботыПоРейсуСП.Задание.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|		ИНАЧЕ РаботыПоРейсуСП.Задание.КонтактноеЛицоПолучателя
	|	КОНЕЦ КАК ГрузополучательКЛ
	|ПОМЕСТИТЬ вт_Данные
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК РаботыПоРейсуСП
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних КАК МаршрутПоРейсуСП
	|		ПО РаботыПоРейсуСП.Рейс = МаршрутПоРейсуСП.Рейс
	|			И РаботыПоРейсуСП.Идентификатор = МаршрутПоРейсуСП.Идентификатор
	|ГДЕ
	|	НЕ РаботыПоРейсуСП.Отменена
	|	И РаботыПоРейсуСП.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Данные.Рейс,
	|	вт_Данные.Грузополучатель
	|ИЗ
	|	вт_Данные КАК вт_Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Данные.Рейс,
	|	вт_Данные.Грузополучатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Данные.Рейс,
	|	вт_Данные.Задание,
	|	вт_Данные.Период КАК Период,
	|	вт_Данные.Адрес,
	|	вт_Данные.Грузополучатель,
	|	вт_Данные.ГрузополучательПредставление,
	|	вт_Данные.ГрузополучательИНН,
	|	вт_Данные.ГрузополучательКПП,
	|	вт_Данные.ГрузополучательКЛ
	|ИЗ
	|	вт_Данные КАК вт_Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс,
	|	упРаботыПоРейсуСрезПоследних.Задание.КонтрагентПолучателя КАК Грузополучатель,
	|	СУММА(упРаботыПоРейсуСрезПоследних.КоличествоГрузов) КАК КоличествоМест,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто.ТипГруза КАК ТипГруза
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|					(ВЫБРАТЬ
	|						вт_Данные.Рейс
	|					ИЗ
	|						вт_Данные КАК вт_Данные)
	|				И Задание В
	|					(ВЫБРАТЬ
	|						вт_Данные.Задание
	|					ИЗ
	|						вт_Данные КАК вт_Данные)) КАК упРаботыПоРейсуСрезПоследних
	|ГДЕ
	|	упРаботыПоРейсуСрезПоследних.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	И НЕ упРаботыПоРейсуСрезПоследних.Отменена
	|
	|СГРУППИРОВАТЬ ПО
	|	упРаботыПоРейсуСрезПоследних.Рейс,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто.ТипГруза,
	|	упРаботыПоРейсуСрезПоследних.Задание.КонтрагентПолучателя";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	Выборка = ПакетЗапросов[1].Выбрать();
	ТаблицаГрузополучатель = ПакетЗапросов[2].Выгрузить();
	ТаблицаГрузовыеМеста = ПакетЗапросов[3].Выгрузить();
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	ТаблицаЗначений.Колонки.Добавить("ГрузополучательНаименование",,"ГрузополучательНаименование");
	ТаблицаЗначений.Колонки.Добавить("ГрузополучательПаллета",,"ГрузополучательПаллета");
	ТаблицаЗначений.Колонки.Добавить("ГрузополучательФактАдресВыгрузки",,"ГрузополучательФактАдресВыгрузки");
	ТаблицаЗначений.Колонки.Добавить("ГрузополучательТелфонКонтактногоЛица",,"ГрузополучательТелфонКонтактногоЛица");
	
	Пока Выборка.Следующий() Цикл
		
		ТекущийОтбор = Новый Структура;
		ТекущийОтбор.Вставить("Рейс",Выборка.Рейс);
		ТекущийОтбор.Вставить("Грузополучатель",Выборка.Грузополучатель);
		
		НайденныеСтроки = ТаблицаГрузополучатель.НайтиСтроки(ТекущийОтбор);
		
		Если НайденныеСтроки.Количество() Тогда
			
			ТекущаяСтрока = НайденныеСтроки[0];
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.ГрузополучательНаименование = ТекущаяСтрока.ГрузополучательПредставление;
			НоваяСтрока.ГрузополучательФактАдресВыгрузки = ТекущаяСтрока.Адрес;
			ТелефонКонтрагента = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузополучатель, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
			ФаксКонтрагенты = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузополучатель, Справочники.ВидыКонтактнойИнформации.ФаксКонтрагенты);
			EmailКонтрагента = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТекущаяСтрока.Грузополучатель, Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
			
			МассивКИ = Новый Массив;
			Если ЗначениеЗаполнено(ТелефонКонтрагента) Тогда
				МассивКИ.Добавить("тел: " + ТелефонКонтрагента);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ФаксКонтрагенты) Тогда
				МассивКИ.Добавить(ФаксКонтрагенты);
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекущаяСтрока.ГрузополучательКЛ) Тогда
				МассивКИ.Добавить(Строка(ТекущаяСтрока.ГрузополучательКЛ));
			КонецЕсли;
			Если ЗначениеЗаполнено(EmailКонтрагента) Тогда
				МассивКИ.Добавить(EmailКонтрагента);
			КонецЕсли;
			
			НоваяСтрока.ГрузополучательТелфонКонтактногоЛица = СтрСоединить(МассивКИ,", ");
			
			ТекущийОтбор = Новый Структура;
			ТекущийОтбор.Вставить("Рейс",ТекущаяСтрока.Рейс);
			ТекущийОтбор.Вставить("Грузополучатель",ТекущаяСтрока.Грузополучатель);
			НайденныеСтрокиГрузов = ТаблицаГрузовыеМеста.НайтиСтроки(ТекущийОтбор);
			ГрузополучательПаллета = "";
			Если НайденныеСтрокиГрузов.Количество() Тогда
				ТаблицаГрузовРейса = ТаблицаГрузовыеМеста.Скопировать(НайденныеСтрокиГрузов,"КоличествоМест,ТипГруза");
				ТаблицаГрузовРейса.Свернуть("ТипГруза","КоличествоМест");
				МассивПаллет = Новый Массив;
				Для каждого ТекущийТипГруза Из ТаблицаГрузовРейса Цикл				
					Если ТекущийТипГруза.КоличествоМест = 0 Тогда
						Продолжить;
					КонецЕсли;
					МассивПаллет.Добавить(Формат(ТекущийТипГруза.КоличествоМест,"ЧРД=.; ЧН=0; ЧГ=0") + " " + Строка(ТекущийТипГруза.ТипГруза));
				КонецЦикла;
				
				Если МассивПаллет.Количество() Тогда
					ГрузополучательПаллета = СтрСоединить(МассивПаллет,", ");
				Иначе
					ГрузополучательПаллета = Строка(упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияГрузовыхМест"));
				КонецЕсли;
				
			КонецЕсли;
			НоваяСтрока.ГрузополучательПаллета = ГрузополучательПаллета;
			
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаЗначений;
	
КонецФункции // ПолучитьГрузополучателейРейса()

#КонецОбласти

#КонецЕсли