
#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИнициализироватьДокумент(ДанныеЗаполнения);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
			
	ДокументЗаполненКорректно(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ПроцедурыИФункцииОбеспеченияПроведения

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует движения по регистрам.
//
// Параметры: 
//  Нет.
//
Процедура ДвиженияПоРегистрам()
	
	Движения.упЗначенияПоказателейРасчета.Записывать = Истина;
	Движения.упЗначенияПоказателейРасчета.Очистить();
	
	Для Каждого текСтрока Из Показатели Цикл
		НовДвижение = Движения.упЗначенияПоказателейРасчета.Добавить();
		НовДвижение.Регистратор				= Ссылка;
		НовДвижение.Период					= Дата;
		НовДвижение.Организация				= Организация;
		НовДвижение.ПоказательРасчета		= текСтрока.Показатель;
		НовДвижение.Значение				= текСтрока.Значение;
	КонецЦикла;
	
Конецпроцедуры // ДвиженияПоРегистрам()

// Проверяет корректно ли осуществлено заполнение документа.
//
// Параметры:
//  Отказ - Булево - признак отказа.
//
Процедура ДокументЗаполненКорректно(Отказ)
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	                  |	тбПоказатели.Показатель,
	                  |	тбПоказатели.Значение,
	                  |	тбПоказатели.Ссылка.Организация КАК Организация
	                  |ПОМЕСТИТЬ втСтрокиПоказателей
	                  |ИЗ
	                  |	Документ.упУстановкаЗначенийПоказателейРасчета.Показатели КАК тбПоказатели
	                  |ГДЕ
	                  |	тбПоказатели.Ссылка = &Ссылка
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	тбСтроки.Показатель
	                  |ИЗ
	                  |	втСтрокиПоказателей КАК тбСтроки
	                  |
	                  |СГРУППИРОВАТЬ ПО
	                  |	тбСтроки.Показатель
	                  |
	                  |ИМЕЮЩИЕ
	                  |	КОЛИЧЕСТВО(тбСтроки.Показатель) > 1
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	тбСтроки.Показатель
	                  |ИЗ
	                  |	РегистрСведений.упЗначенияПоказателейРасчета КАК тбЗначенияПоказателей
	                  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтрокиПоказателей КАК тбСтроки
	                  |		ПО тбЗначенияПоказателей.ПоказательРасчета = тбСтроки.Показатель
	                  |ГДЕ
	                  |	тбЗначенияПоказателей.Период = &Период
	                  |	И тбЗначенияПоказателей.Организация = &Организация
	                  |	И НЕ тбЗначенияПоказателей.Регистратор = &Ссылка";
	текЗапрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	текЗапрос.УстановитьПараметр("Период", НачалоДня(ЭтотОбъект.Дата));
	текЗапрос.УстановитьПараметр("Организация", ЭтотОбъект.Организация);
	текРезультат = текЗапрос.ВыполнитьПакет();
	
	тбзПовторяющиесяСтроки 		 = текРезультат[1].Выгрузить();
	Если тбзПовторяющиесяСтроки.Количество()>0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В табличной части есть строки с повторяющимися показателями'"),
				ЭтотОбъект,
				"",
				,
				Отказ);
		Возврат;
	КонецЕсли;
	
	тбзЗначенияПоказателейНаДату = текРезультат[2].Выгрузить();
	
	Для каждого текСтрока Из тбзЗначенияПоказателейНаДату Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Для показателя:"""+текСтрока.Показатель+""" значение на дату:"+Формат(Дата,"ДФ=dd.MM.yyyy")+" уже установлено"+"'"),
				ЭтотОбъект,
				"",
				,
				Отказ);
	КонецЦикла;
	
КонецПроцедуры // ДокументЗаполненКорректно()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Инициализация и заполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)

	Организация		= Справочники.Организации.ОрганизацияПоУмолчанию();

КонецПроцедуры

#КонецОбласти
