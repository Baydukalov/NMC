#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура обработчик события ПередЗаписью объекта
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Документы.упЗаявкаНаПеревозкуГруза.ОпределитьПоляШапкиЗаявкиПоГрузовыМестам(ЭтотОбъект);
	
	Статус = Неопределено;
	ИзменяетсяСтатус = Ложь;
			
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		Статус = ДополнительныеСвойства.Статус;
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
		ИзменяетсяСтатус = Истина;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ИзменяетсяСтатус", ИзменяетсяСтатус);
	
	Если Истина
		И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения
		И ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ГрузМодифицирован", Ложь)
	Тогда
		упУправленияЗаявками.ЗаполнитьГрузовыеМеста(ЭтотОбъект, Отказ);
	КонецЕсли;	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		упУправленияЗаявками.ПроконтролироватьИспользованиеТары(ГрузовыеМеста, Отказ);
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Ложь
		Или НЕ ЗначениеЗаполнено(ПорядокИсполненияЗаявки) 
		Или НЕ ЗначениеЗаполнено(ВидТранспортнойУслуги) 
	Тогда
		упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(ЭтотОбъект);
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	ИспользуютсяУслугиИнкассации = Ложь;
	СпособУчетаИнкассируемыхЦенностей = Неопределено;
	
	// Расчеты с заказчиками
	ВестиУчетРасчетовСЗаказчиками	= ПолучитьФункциональнуюОпцию("упВестиУчетРасчетовСЗаказчиками") И ПлановыеДоходы.Количество() > 0;
	ДополнительныеСвойства.Вставить("ВестиУчетРасчетовСЗаказчиками", ВестиУчетРасчетовСЗаказчиками);
	
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		сткРеквизиты = Новый Структура();
		сткРеквизиты.Вставить("ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза");
		сткРеквизиты.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению");
		сткРеквизиты.Вставить("ИспользуютсяУслугиИнкассации");
		сткРеквизиты.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах");
		сткРеквизиты.Вставить("ИспользуютсяМультимодальныеПеревозки");
		сткРеквизиты.Вставить("СпособУчетаИнкассируемыхЦенностей");
		сткРеквизиты.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки");
		сткРеквизиты.Вставить("СоздаватьЗаданияНаПеревозкуПоочередно");
		Если ВестиУчетРасчетовСЗаказчиками Тогда
			сткРеквизиты.Вставить("ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов");	
		КонецЕсли;
		сткРеквизиты.Вставить("КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению");
		
		сткДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПеревозки, сткРеквизиты);
		ДополнительныеСвойства.Вставить("ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза", сткДанные.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза);
		ДополнительныеСвойства.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению", сткДанные.АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению);
		ДополнительныеСвойства.Вставить("КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению", сткДанные.КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению);
		ДополнительныеСвойства.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", сткДанные.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки);
		ДополнительныеСвойства.Вставить("СоздаватьЗаданияНаПеревозкуПоочередно", сткДанные.СоздаватьЗаданияНаПеревозкуПоочередно);
		ИспользуютсяУслугиИнкассации = ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") И сткДанные.ИспользуютсяУслугиИнкассации;
		СпособУчетаИнкассируемыхЦенностей = сткДанные.СпособУчетаИнкассируемыхЦенностей;
		
		Если ВестиУчетРасчетовСЗаказчиками Тогда
			ДополнительныеСвойства.Вставить("ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов", сткДанные.ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов);	
		КонецЕсли;
		
	Иначе	
		ДополнительныеСвойства.Вставить("ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза", Ложь);
		ДополнительныеСвойства.Вставить("АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению", Ложь);
		ДополнительныеСвойства.Вставить("КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению", Ложь);
		ДополнительныеСвойства.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", Ложь);
		ДополнительныеСвойства.Вставить("СоздаватьЗаданияНаПеревозкуПоочередно", Ложь);
		ДополнительныеСвойства.Вставить("ЗапретитьПереводЗаявкиКВыполнениюБезРассчитанныхПлановыхДоходов", Ложь);	
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ИспользуютсяУслугиИнкассации", ИспользуютсяУслугиИнкассации);
	ДополнительныеСвойства.Вставить("СпособУчетаИнкассируемыхЦенностей", СпособУчетаИнкассируемыхЦенностей);
		
	Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		
		Если Истина
			И Не Отказ
			И ДополнительныеСвойства.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки
		Тогда
			Отказ = упКорректировкаЭтаповПеревозки.ЗапрещеноРедактироватьЭтапыМультимодальнойПеревозки(ЭтотОбъект);
		КонецЕсли;
		
		АналитическийРазрезПоПартнерам = Неопределено;
		Если ВестиУчетРасчетовСЗаказчиками Тогда
			АналитическийРазрезПоПартнерам = упУправленияЗаявками.ПодобратьАналитическийРазрезПоПартнерам(Организация, Заказчик, КонтрагентЗаказчика, Соглашение, Отказ);
			Если НЕ Отказ Тогда
				ДополнительныеСвойства.Вставить("АналитическийРазрезПоПартнерам", АналитическийРазрезПоПартнерам);
			КонецЕсли;
		КонецЕсли;
				
		Если НЕ Отказ Тогда
			// Предварительная установка статуса										
			ДополнительныеСвойства.Вставить("ЭтоПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
			ДополнительныеСвойства.Вставить("ДоступнаРольСогласованиеЗаявкиНаПеревозкуГруза", РольДоступна("упСогласованиеЗаявкиНаПеревозкуГруза"));
			ДополнительныеСвойства.Вставить("ДоступнаРольОтменаЗаявкиНаПеревозкуГруза", РольДоступна("упОтменаЗаявкиНаПеревозкуГруза"));
			ДополнительныеСвойства.Вставить("Ссылка", Ссылка);
			
			Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПустаяСсылка();	
				ДополнительныеСвойства.Вставить("Статус", Статус);
			КонецЕсли;
			
			упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
			
			Если Истина
				И НЕ Отказ 
				И ДополнительныеСвойства.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПустаяСсылка()
			Тогда
				упРаботаСоСтатусами.ЕстьДругиеРегистраторыИзменявшиеСтатус(Ссылка, Отказ);
				Если НЕ Отказ Тогда
					упУправленияЗаявками.ЕстьЗаданияПоЗаявке(Ссылка, Отказ);	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Истина
		И Статус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Статус", Неопределено) <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена
	Тогда

		Для Каждого текГруз Из ГрузовыеМеста Цикл 
			Если Не ЗначениеЗаполнено(текГруз.КоличествоГрузов) Тогда 
				текГруз.КоличествоГрузов = 1;
			КонецЕсли;
		КонецЦикла;
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
			ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
			упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);	
		КонецЕсли;
								
		сткВозврат = упРасчетПоказателейЗагрузки.РассчитатьИтоговыеПараметрыПоНаборуГрузов(ГрузовыеМеста.Выгрузить(, "ГрузовоеМесто, Тара, КоличествоГрузов"), Ссылка);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткВозврат,,?(СпособУчетаИнкассируемыхЦенностей = Перечисления.упСпособыУчетаИнкассируемыхЦенностей.ВЦеломПоЗаявкеНаПеревозкуГруза,"СуммаИнкассацииПлан",""));
		
	КонецЕсли;	
		
КонецПроцедуры

// Процедура проверяет заполненность реквизитов объекта
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекстОшибки = "";
	МассивПроверяемых = Новый Массив;
	МассивПроверяемых.Добавить("Организация");
	МассивПроверяемых.Добавить("Заказчик");
	Если ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет") Тогда
		МассивПроверяемых.Добавить("ПлановыеДоходы.Валюта");	
	КонецЕсли;
	
	МассивНайденных = Новый Массив;
	Для каждого ТекущийРеквизит Из ПроверяемыеРеквизиты Цикл
		ТекущийНайденный = МассивПроверяемых.Найти(ТекущийРеквизит);
		Если ТекущийНайденный = Неопределено Тогда
			МассивНайденных.Добавить(ТекущийРеквизит);
		КонецЕсли;
	КонецЦикла;

	Для каждого ТекущийРеквизит Из МассивНайденных Цикл
		ТекущийИндексРеквизита = ПроверяемыеРеквизиты.Найти(ТекущийРеквизит);
		ПроверяемыеРеквизиты.Удалить(ТекущийИндексРеквизита);
	КонецЦикла;
	
	упЗаданиеНаПеревозку.ПроконтролироватьИнтервалОтправкиПолучения(ОтправлениеГрузаС, ОтправлениеГрузаПо, ПолучениеГрузаС, ПолучениеГрузаПо, Отказ);
			
	Если Не ДополнительныеСвойства.Свойство("Статус") Тогда 
		Возврат;
	Иначе
		ПроверяемыйСтатус = ДополнительныеСвойства.Статус;
	КонецЕсли;
	
	Если Истина
		И ПроверяемыйСтатус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена
		И ПроверяемыйСтатус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
		И НЕ ЗначениеЗаполнено(ВидПеревозки)
	Тогда
		ТекстОшибки = НСтр("ru = 'Поле ""Вид перевозки"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "ВидПеревозки",, Отказ);
	КонецЕсли;
    	
	Если Истина
		И ПорядокИсполненияЗаявки = ПредопределенноеЗначение("Перечисление.упПорядокИсполненияЗаявок.ПутевымЛистом")
		И ПроверяемыйСтатус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена
	Тогда
		ТекстОшибки = НСтр("ru = 'Поле ""Количество рабочих смен"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "КоличествоРабочихСмен",, Отказ);
	КонецЕсли;
				
	Если Истина 
		И НЕ Отказ
		И ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ПеревозкаГрузов")
		И ПроверяемыйСтатус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена 
	Тогда 
		Если ГрузовыеМеста.Количество() = 0 Тогда 
			Если ОбщийГруз Тогда 
				ТекстОшибки = НСтр("ru = 'Не указан груз'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "ТипГруза",, Отказ);
			Иначе	
				ТекстОшибки = НСтр("ru = 'Не указан список грузов'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "СписокГрузов",, Отказ);
			КонецЕсли;	
		КонецЕсли;	
			
		Сч = 1;
		Для Каждого текГруз Из ГрузовыеМеста Цикл 
			
			Если Не ЗначениеЗаполнено(текГруз.ГрузовоеМесто.ТипГруза) Тогда 
				Если ОбщийГруз Тогда 
					ТекстОшибки = НСтр("ru = 'Не указан тип груза'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "ТипГруза",, Отказ);
				Иначе	
					ТекстОшибки = НСтр("ru = 'Не указан тип груза'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СписокГрузов", Сч, "ТипГруза"),, Отказ);
				КонецЕсли;	
			КонецЕсли;	
						
			Сч = Сч + 1;
			
			Для Каждого текСтрокаСостава Из текГруз.ГрузовоеМесто.ТоварныйСоставГруза Цикл 
				Если Не ЗначениеЗаполнено(текСтрокаСостава.Номенклатура) Тогда 
					Если ОбщийГруз Тогда 
						ТекстОшибки = НСтр("ru = 'Не указана номенклатура'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав", Сч - 1, "Номенклатура"),, Отказ);
					Иначе	
						ТекстОшибки = НСтр("ru = 'Не указана номенклатура'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СписокГрузов", Сч, "Номенклатура"),, Отказ);
					КонецЕсли;	
				КонецЕсли;	
				Если Не ЗначениеЗаполнено(текСтрокаСостава.Количество) Тогда 
					Если ОбщийГруз Тогда 
						ТекстОшибки = НСтр("ru = 'Не указано количество'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав", Сч - 1, "Количество"),, Отказ);
					Иначе	
						ТекстОшибки = НСтр("ru = 'Не указано количество'");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СписокГрузов", Сч, "Количество"),, Отказ);
					КонецЕсли;	
				КонецЕсли;	
				
				Сч = Сч + 1;
			КонецЦикла;	
		КонецЦикла;	
						 
	КонецЕсли;
	
	Если Истина
		И Отказ
		И ЗначениеЗаполнено(ТекстОшибки)
	Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Проверка заполнения заявки на перевозку груза'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
	 					УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработчик события ОбработкаПроведения объекта
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Если проводится заявка с уже установленным статусом "Отменена"
	Если Истина
		И Не ДополнительныеСвойства.ИзменяетсяСтатус
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Статус", Неопределено) = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена
	Тогда
		// Движения остаются как есть, можно только менять реквизиты.
		Возврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаявкиКВыполнению");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Заявка", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упДоходы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", Ссылка);
	
	Если ДополнительныеСвойства.ИспользуютсяУслугиИнкассации Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упИнкассацияДенежныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", Ссылка);
		ЭлементБлокировки.УстановитьЗначение("Валюта", Валюта);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ВестиУчетРасчетовСЗаказчиками Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеЭтапыПеревозки");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ЗаявкаНаПеревозкуГруза", Ссылка);
	КонецЕсли;
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
		
	РеквизитыОбъекта = Новый Структура(); 
	РеквизитыОбъекта.Вставить("ГрузовыеМеста");
	РеквизитыОбъекта.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах");
	РеквизитыОбъекта.Вставить("СхемаМультимодальнойПеревозки");
	РеквизитыОбъекта.Вставить("ИспользуютсяМультимодальныеПеревозки", Ложь);
	РеквизитыОбъекта.Вставить("ДлинаЦепиПоставок");
	РеквизитыОбъекта.Вставить("АдресОтправления");
	РеквизитыОбъекта.Вставить("АдресПолучения");
	РеквизитыОбъекта.Вставить("Отправитель");
	РеквизитыОбъекта.Вставить("ОтправлениеГрузаС");
	РеквизитыОбъекта.Вставить("ПолучениеГрузаПо");
	РеквизитыОбъекта.Вставить("ГрафикРаботыАдресаПолучения");
	РеквизитыОбъекта.Вставить("Получатель");
	РеквизитыОбъекта.Вставить("Соглашение");
	РеквизитыОбъекта.Вставить("ГрафикРаботыАдресаОтправления");
	РеквизитыОбъекта.Вставить("ГрафикРаботыАдресаПолучения");
	РеквизитыОбъекта.Вставить("Валюта");
	РеквизитыОбъекта.Вставить("СуммаИнкассацииПлан");
	РеквизитыОбъекта.Вставить("ПлановыеДоходы");
	РеквизитыОбъекта.Вставить("ВидТранспортнойУслуги");
	РеквизитыОбъекта.Вставить("КоличествоРабочихСмен");
	ПодготовитьДополнительнуюИнформацию(РеквизитыОбъекта);
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);
	
	РеквизитыОбъекта.Вставить("Дата", ОпределитьПериодФормированияДвижений(ДополнительныеСвойства.Статус, ЧасовойПояс));
	
	упУправленияЗаявками.УстановитьПризнакНеобходимостиРасчетаСтатуса(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьПлановыеЭтапыМультимодальнойПеревозки(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
			
	упУправленияЗаявками.ПровестиПроверкиПриПереводеЗаявкиВСтатусКВыполнению(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьДвижениеЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьДвиженияДоходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьДвиженияРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.СформироватьДвиженияПлановыеЭтапыМультимодальнойПеревозки(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	упУправленияЗаявками.ПроконтролироватьОграничениеПоУровнямВложенностиГрузов(Ссылка, Отказ);
	
	упРасчетыСЗаказчиками.ПроконтролироватьРасчетыСЗаказчиками(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
	упУправленияЗаявками.ПроконтролироватьЗаявкиКВыполнению(Ссылка, Отказ, Ссылка);
	
	упУправленияЗаявками.СформироватьДвиженияСтатусЗаявки(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упУправленияЗаявками.ЗаписатьАдресаПартнеров(ДополнительныеСвойства, РеквизитыОбъекта);
	
КонецПроцедуры

// Процедура обработчик события ПриКопировании объекта
//
Процедура ПриКопировании(ОбъектКопирования)
	
	ОтправлениеГрузаС  = ОбъектКопирования.ОтправлениеГрузаС;
	ОтправлениеГрузаПо = ОбъектКопирования.ОтправлениеГрузаПо;
	ПолучениеГрузаС    = ОбъектКопирования.ПолучениеГрузаС;
	ПолучениеГрузаПо   = ОбъектКопирования.ПолучениеГрузаПо;
	
	Если ДополнительныеОперации.Количество() > 0 Тогда
		ДополнительныеОперации.Очистить();
	КонецЕсли;
	
	Если ПлановыеДоходы.Количество() > 0 Тогда
		ПлановыеДоходы.Очистить();
	КонецЕсли;
	
	Если ДополнительныеРеквизиты.Количество() > 0 Тогда
		ДополнительныеРеквизиты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда
		
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		
		// Заполнение шапки
		СписокСвойств = "
		|ДатаКИС,
		|ВидПеревозки,
		|Организация,
		|Заказчик,
		|КонтрагентЗаказчика,
		|КонтактноеЛицоЗаказчика,
		|Соглашение,
		|АдресОтправления,
		|ЗонаОтправления,
		|Отправитель,
		|КонтрагентОтправителя,
		|КонтактноеЛицоОтправителя,
		|ОтправлениеГрузаС,
		|ОтправлениеГрузаПо,
		|ГрафикРаботыАдресаОтправления,
		|АдресПолучения,
		|ЗонаПолучения,
		|Получатель,
		|КонтрагентПолучателя,
		|КонтактноеЛицоПолучателя,
		|ПолучениеГрузаС,
		|ПолучениеГрузаПо,
		|ГрафикРаботыАдресаПолучения,
		|Приоритет,
		|КоличествоЗагрузочныхМетров,
		|КоличествоМест,Логист,Масса,Менеджер,НомерКИС,Объем,ОценочнаяСтоимость,Сумма,СуммаДоходы,СуммаДоходыНДС,СуммаДоходыСкидка,
		|СуммаНДС,Логист,Менеджер";
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения,СписокСвойств);
				
		// Заполнение тбч ПлановыеДоходы
		Для Каждого тбчСтрока Из ДанныеЗаполнения.ПлановыеДоходы Цикл
			НоваяСтрока = ПлановыеДоходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,тбчСтрока);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда	
		
		Отказ = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА упРейсТ.ВидПеревозки.ВидПеревозкиКонтейнера = ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)
		|			ТОГДА упРейсТ.ВидПеревозки
		|		ИНАЧЕ упРейсТ.ВидПеревозки.ВидПеревозкиКонтейнера
		|	КОНЕЦ КАК ВидПеревозки,
		|	упРейсТ.Организация КАК Организация,
		|	ИСТИНА КАК ОбщийГруз
		|ИЗ
		|	Документ.упРейс КАК упРейсТ
		|ГДЕ упРейсТ.Ссылка = &Рейс
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПеревозчикПоРейсу.ТранспортноеСредство КАК КонтейнерГрузовогоМеста,
		|	ЕСТЬNULL(ГрузовыеМестаКонтейнеров.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)) КАК ГрузовоеМесто,
		|	0 КАК Высота,
		|	0 КАК Длина,
		|	0 КАК КоличествоЗагрузочныхМетров,
		|	0 КАК КоличествоМест,
		|	ВЫБОР
		|		КОГДА &УчитыватьМассу
		|			ТОГДА ЕСТЬNULL(ТранспортныеСредства.СнаряженнаяМасса, 0) + ЕСТЬNULL(упПлановыеПараметрыРейсаТ.Масса, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Масса,
		|	ВЫБОР
		|		КОГДА &УчитыватьОбъем
		|			ТОГДА ТранспортныеСредства.Объем
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Объем,
		|	0 КАК Ширина,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(упТипыТСТ.ТипГрузаКонтейнера, ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)
		|			ТОГДА ТипыГрузов.ТипГруза
		|		ИНАЧЕ упТипыТСТ.ТипГрузаКонтейнера
		|	КОНЕЦ КАК ТипГруза,
		|	ПеревозчикПоРейсу.Перевозчик КАК Перевозчик,
		|	ПеревозчикПоРейсу.КонтрагентПеревозчика КАК КонтрагентПеревозчика,
		|	ПеревозчикПоРейсу.Соглашение КАК Соглашение,
		|	ТранспортныеСредства.РегистрационныйНомер КАК РегистрационныйНомер
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс) КАК ПеревозчикПоРейсу
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упГрузовыеМестаКонтейнеров КАК ГрузовыеМестаКонтейнеров
		|	ПО ИСТИНА
		|		И ПеревозчикПоРейсу.ТранспортноеСредство = ГрузовыеМестаКонтейнеров.Контейнер
		|		И ГрузовыеМестаКонтейнеров.Документ = &Рейс
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК ТранспортныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыТС КАК упТипыТСТ
		|		ПО упТипыТСТ.Ссылка = ТранспортныеСредства.ТипТС
		|	ПО ТранспортныеСредства.Ссылка = ПеревозчикПоРейсу.ТранспортноеСредство
		|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
		|		упТипыГрузовТ.Ссылка КАК ТипГруза
		|	ИЗ
		|		Справочник.упТипыГрузов КАК упТипыГрузовТ
		|	ГДЕ упТипыГрузовТ.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Груз)) КАК ТипыГрузов
		|	ПО ИСТИНА
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейсаТ
		|	ПО упПлановыеПараметрыРейсаТ.Рейс = &Рейс
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	УпМаршрутПоРейсу_СрезПоследнихТ.Адрес КАК Адрес,
		|	УпМаршрутПоРейсу_СрезПоследнихТ.Адрес.ГеографическаяЗона КАК ГеографическаяЗона
		|ИЗ
		|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс) КАК УпМаршрутПоРейсу_СрезПоследнихТ
		|ГДЕ НЕ (УпМаршрутПоРейсу_СрезПоследнихТ.Отменена)
		|УПОРЯДОЧИТЬ ПО
		|	УпМаршрутПоРейсу_СрезПоследнихТ.СтрокаНомер
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	УпРаботыПоРейсу_СрезПоследнихТ.Задание КАК Задание,
		|	упЗаданиеНаПеревозкуГрузаТ.Заказчик КАК Заказчик,
		|	упЗаданиеНаПеревозкуГрузаТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
		|	упЗаданиеНаПеревозкуГрузаТ.Отправитель КАК Отправитель,
		|	упЗаданиеНаПеревозкуГрузаТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
		|	упЗаданиеНаПеревозкуГрузаТ.Получатель КАК Получатель,
		|	упЗаданиеНаПеревозкуГрузаТ.КонтрагентПолучателя КАК КонтрагентПолучателя
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс) КАК УпРаботыПоРейсу_СрезПоследнихТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГрузаТ
		|	ПО упЗаданиеНаПеревозкуГрузаТ.Ссылка = УпРаботыПоРейсу_СрезПоследнихТ.Задание
		|ГДЕ НЕ (УпРаботыПоРейсу_СрезПоследнихТ.Отменена)
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втЗаданияТ.Заказчик КАК Заказчик,
		|	втЗаданияТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика
		|ИЗ
		|	втЗадания КАК втЗаданияТ
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втЗаданияТ.Получатель КАК Получатель,
		|	втЗаданияТ.КонтрагентПолучателя КАК КонтрагентПолучателя
		|ИЗ
		|	втЗадания КАК втЗаданияТ
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втЗаданияТ.Отправитель КАК Отправитель,
		|	втЗаданияТ.КонтрагентОтправителя КАК КонтрагентОтправителя
		|ИЗ
		|	втЗадания КАК втЗаданияТ
		|";

		Запрос.УстановитьПараметр("Рейс", ДанныеЗаполнения);
		Запрос.УстановитьПараметр("УчитыватьМассу", ПолучитьФункциональнуюОпцию("упУчитыватьМассу"));
		Запрос.УстановитьПараметр("УчитыватьОбъем", ПолучитьФункциональнуюОпцию("упУчитыватьОбъем"));
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		ИндексЗапросаРейс = 0;
		ИндексЗапросаЗаявка = 1;
		ИндексЗапросаАдреса = 2;
		ИндексЗапросаЗаказчик = 4;
		ИндексЗапросаПолучатель = 5;
		ИндексЗапросаОтправитель = 6;
		
		// Данные для заполнения шапки документа.
		Выборка = РезультатЗапроса[ИндексЗапросаРейс].Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект,Выборка);			
		КонецЕсли; 
		
		Если Не Отказ Тогда
			
			// Адрес отправления/получения.
			Выборка = РезультатЗапроса[ИндексЗапросаАдреса].Выбрать();
			Если Выборка.Следующий() Тогда
				ЭтотОбъект.АдресОтправления = Выборка.Адрес;
				ЭтотОбъект.ЗонаОтправления = Выборка.ГеографическаяЗона;
			КонецЕсли;
			Если Выборка.Следующий() Тогда
				ЭтотОбъект.АдресПолучения = Выборка.Адрес;
				ЭтотОбъект.ЗонаПолучения = Выборка.ГеографическаяЗона;
			КонецЕсли;
			
		КонецЕсли;
		
		// Данные по грузовому месту.
		Выборка = РезультатЗапроса[ИндексЗапросаЗаявка].Выбрать();
		Если Выборка.Следующий() Тогда
			
			// Заказчик.
			ВыборкаЗаказчик = РезультатЗапроса[ИндексЗапросаЗаказчик].Выбрать();
			
			Если ВыборкаЗаказчик.Количество() = 1 Тогда
				Если ВыборкаЗаказчик.Следующий() Тогда
					ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаЗаказчик);
				КонецЕсли;
			Иначе	
				ЭтотОбъект.Заказчик = Выборка.Перевозчик;
				ЭтотОбъект.КонтрагентЗаказчика = Выборка.КонтрагентПеревозчика;
				ЭтотОбъект.Соглашение = Выборка.Соглашение;	
			КонецЕсли;
			
			// Отправитель.
			ВыборкаОтправитель = РезультатЗапроса[ИндексЗапросаОтправитель].Выбрать();
			
			Если ВыборкаОтправитель.Количество() = 1 Тогда
				Если ВыборкаОтправитель.Следующий() Тогда
					ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаОтправитель);
				КонецЕсли;
			КонецЕсли;

			
			// Получатель.
			ВыборкаПолучатель = РезультатЗапроса[ИндексЗапросаПолучатель].Выбрать();
			
			Если ВыборкаПолучатель.Количество() = 1 Тогда
				Если ВыборкаПолучатель.Следующий() Тогда
					ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаПолучатель);
				КонецЕсли;
			КонецЕсли;
						
			ЭтоНовоеГрузовоеМесто = Ложь;
			
			// Если грузовое место для контейнера уже создано.
			Если ЗначениеЗаполнено(Выборка.ГрузовоеМесто) Тогда
				ГрузовоеМестоСсылка = Выборка.ГрузовоеМесто;
				ГрузовоеМестоОбъект = ГрузовоеМестоСсылка.ПолучитьОбъект();
			Иначе	
				// Создается новое грузовое место с параметрами контейнера.
				ГрузовоеМестоОбъект = Справочники.упГрузовыеМеста.СоздатьЭлемент();
				ЭтоНовоеГрузовоеМесто = Истина;
			КонецЕсли;	
						
			СсылкаНаОснование = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСсылку(Новый УникальныйИдентификатор);
			ЭтотОбъект.УстановитьСсылкуНового(СсылкаНаОснование);
			
			ГрузовоеМестоОбъект.ДокументОснование = СсылкаНаОснование;
			ГрузовоеМестоОбъект.Код = СтрШаблон(Нстр("ru = '%1'"), Выборка.РегистрационныйНомер);
			ГрузовоеМестоОбъект.Контейнер = Истина;
			
			ЗаполнитьЗначенияСвойств(ГрузовоеМестоОбъект, Выборка);
			
			Попытка
				ГрузовоеМестоОбъект.Записать();
				ГрузовоеМестоСсылка = ГрузовоеМестоОбъект.Ссылка;
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
				ЗаписьЖурналаРегистрации(Документы.упЗаявкаНаПеревозкуГруза.СобытиеОшибка(НСтр("ru = 'Заполнение'")), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				ДополнительныеСвойства.Вставить("ОшибкаПриЗаполнении", Отказ);
			КонецПопытки;
			
			Если Истина
				И Не Отказ
				И ЭтоНовоеГрузовоеМесто
			Тогда
				// Грузовое место связывается с контейнером.
				ЗаписьГрузовыеМестаКонтейнеров = РегистрыСведений.упГрузовыеМестаКонтейнеров.СоздатьМенеджерЗаписи();
				ЗаписьГрузовыеМестаКонтейнеров.Контейнер = Выборка.КонтейнерГрузовогоМеста;
				ЗаписьГрузовыеМестаКонтейнеров.ГрузовоеМесто = ГрузовоеМестоСсылка;
				ЗаписьГрузовыеМестаКонтейнеров.Документ = ДанныеЗаполнения;
				
				Попытка
					ЗаписьГрузовыеМестаКонтейнеров.Записать();
				Исключение
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
					ЗаписьЖурналаРегистрации(Документы.упЗаявкаНаПеревозкуГруза.СобытиеОшибка(НСтр("ru = 'Заполнение'")), УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					ДополнительныеСвойства.Вставить("ОшибкаПриЗаполнении", Отказ);
				КонецПопытки;
			КонецЕсли;
					
			// В табличную часть заявки добавляется грузовое место.
			НоваяСтрока = ГрузовыеМеста.Добавить();
			НоваяСтрока.КоличествоГрузов = 1;
			НоваяСтрока.ГрузовоеМесто = ГрузовоеМестоСсылка;
			НоваяСтрока.АдресОтправления = ЭтотОбъект.АдресОтправления;
			НоваяСтрока.АдресПолучения = ЭтотОбъект.АдресПолучения;
						
		КонецЕсли;
						
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДополнительнуюИнформацию(РеквизитыОбъекта)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах,
	|	ВЫБОР
	|		КОГДА упВидыПеревозок.ИспользуютсяМультимодальныеПеревозки
	|				И упИспользуютсяМультимодальныеПеревозки.Значение
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользуютсяМультимодальныеПеревозки
	|ПОМЕСТИТЬ втВидПеревозки
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок,
	|	Константа.упИспользуютсяМультимодальныеПеревозки КАК упИспользуютсяМультимодальныеПеревозки
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах,
	|	втВидПеревозки.ИспользуютсяМультимодальныеПеревозки,
	|	ЕСТЬNULL(МАКСИМУМ(ПромежуточныеТочкиМаршрута.НомерСтроки), 0) + 1 КАК ДлинаЦепиПоставок
	|ИЗ
	|	втВидПеревозки КАК втВидПеревозки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
	|		ПО (втВидПеревозки.ИспользуютсяМультимодальныеПеревозки)
	|			И (ПромежуточныеТочкиМаршрута.Ссылка = &Схема)
	|
	|СГРУППИРОВАТЬ ПО
	|	втВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах,
	|	втВидПеревозки.ИспользуютсяМультимодальныеПеревозки";
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("Схема", СхемаМультимодальнойПеревозки);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, Выборка);	
	КонецЕсли;
КонецПроцедуры // ПодготовитьДополнительнуюИнформацию()

// Прроцедура заполнения таблицы значений "ПлановыеДоходы".
//
Процедура ЗаполнитьПлановыеДоходыТиповымиСтатьями() Экспорт
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	                  |	тбВидыПеревозокСписокТиповыхСтатейДоходов.СтатьяДоходов,
	                  |	1 КАК Количество
	                  |ПОМЕСТИТЬ втСтатьиДоходов
	                  |ИЗ
	                  |	Справочник.упВидыПеревозок.СписокСтатейДоходов КАК тбВидыПеревозокСписокТиповыхСтатейДоходов
	                  |ГДЕ
	                  |	тбВидыПеревозокСписокТиповыхСтатейДоходов.Ссылка = &ВидПеревозки
	                  |
	                  |ОБЪЕДИНИТЬ
	                  |
	                  |ВЫБРАТЬ
	                  |	упСоглашенияСКлиентамиСтатьиДоходов.СтатьяДоходов,
	                  |	1
	                  |ИЗ
	                  |	Справочник.упСоглашенияСКлиентами.СтатьиДоходов КАК упСоглашенияСКлиентамиСтатьиДоходов
	                  |ГДЕ
	                  |	упСоглашенияСКлиентамиСтатьиДоходов.Ссылка = &Соглашение
	                  |
	                  |ОБЪЕДИНИТЬ ВСЕ
	                  |
	                  |ВЫБРАТЬ
	                  |	упЗаявкаНаПеревозкуГрузаДополнительныеОперации.ДополнительнаяОперация.СтатьяДоходов,
	                  |	упЗаявкаНаПеревозкуГрузаДополнительныеОперации.Количество
	                  |ИЗ
	                  |	Документ.упЗаявкаНаПеревозкуГруза.ДополнительныеОперации КАК упЗаявкаНаПеревозкуГрузаДополнительныеОперации
	                  |ГДЕ
	                  |	упЗаявкаНаПеревозкуГрузаДополнительныеОперации.Ссылка = &Ссылка
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	втСтатьиДоходов.СтатьяДоходов,
	                  |	СУММА(втСтатьиДоходов.Количество) КАК Количество,
	                  |	упСтатьиДоходовРасходов.СтавкаНДС
	                  |ИЗ
	                  |	втСтатьиДоходов КАК втСтатьиДоходов
	                  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСтатьиДоходовРасходов КАК упСтатьиДоходовРасходов
	                  |		ПО втСтатьиДоходов.СтатьяДоходов = упСтатьиДоходовРасходов.Ссылка
	                  |
	                  |СГРУППИРОВАТЬ ПО
	                  |	втСтатьиДоходов.СтатьяДоходов,
	                  |	упСтатьиДоходовРасходов.СтавкаНДС";
	текЗапрос.УстановитьПараметр("ВидПеревозки", 	ВидПеревозки);
	текЗапрос.УстановитьПараметр("Соглашение", 		Соглашение);
	текЗапрос.УстановитьПараметр("Ссылка", 			Ссылка);
	
	ПлановыеДоходы.Загрузить(текЗапрос.Выполнить().Выгрузить());
		
КонецПроцедуры

// Функция определяет дату формирования движений
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений(Статус, ЧасовойПоясДокумента)
	
	Период = упСервисныеФункции.ПриведеннаяКЧасовомуПоясуДокументаДатаСеанса(ЧасовойПоясДокумента);
	
	Если Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена Тогда
		Возврат Период;
	КонецЕсли; 
	
	ПереданоКВыполнению = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьДатуПереводаВСтатус(Ссылка, Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
	Если ЗначениеЗаполнено(ПереданоКВыполнению) Тогда
		Период = Мин(ПереданоКВыполнению, Период);
	КонецЕсли; 
	
	Возврат Период;
	
КонецФункции

#КонецОбласти

#КонецЕсли 