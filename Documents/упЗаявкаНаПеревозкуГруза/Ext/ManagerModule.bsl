#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа
//
// Параметры:
//  Ссылка  - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.упСтатусыЗаявкиНаПеревозкуГруза - Результат выполнения.
//
Функция ПолучитьСтатус(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Статус,
	|	упЗаявкаНаПеревозкуГруза.ПометкаУдаления
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(, Документ = &Документ) КАК упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних
	|		ПО (упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Документ = упЗаявкаНаПеревозкуГруза.Ссылка)
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена;
	Иначе	
		Возврат Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая;
	КонецЕсли;
	
КонецФункции

// Функция возвращает текущий статус документа в WMS
//
// Параметры:
//  Ссылка  - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упСтатусыДокументовWMS - Результат выполнения.
//
Функция ПолучитьСтатусWMS(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыЗаявокНаПеревозкуГрузаWMSСрезПоследних.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.упСтатусыЗаявокНаПеревозкуГрузаWMS.СрезПоследних(, Документ = &Документ) КАК упСтатусыЗаявокНаПеревозкуГрузаWMSСрезПоследних";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Статус;
	Иначе	
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Функция возвращает дату установки указанного статуса
//
// Параметры:
//  Ссылка - ДокументСсылка.упЗаявкаНаПеревозкуГруза			 - Ссылка на документ
//  Статус - ПеречислениеСсылка.упСтатусыЗаявокНаПеревозкуГруза	 - Статус задания
// 
// Возвращаемое значение:
//  Дата - дата перевода документа в заданный статус.
//
Функция ПолучитьДатуПереводаВСтатус(Ссылка, Статус) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат '00010101';
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Период
	|ИЗ
	|	РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|			,
	|			Документ = &Документ
	|				И Статус = &Статус) КАК упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Запрос.УстановитьПараметр("Статус"  , Статус);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат '00010101';
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Период;
	КонецЕсли; 

КонецФункции // ПолучитьДатуПереводаВСтатус()

// Функция возвращает массив подходящих схем мультимодальных перевозок.
//
// Параметры:
//   ПараметрыОтбора - Структура - Значения отбора.
//   УпорядочитьПоВажности - Булево - Флаг исполнения.
//
// Возвращаемое значение:
//   Массив - Ссылки - Доступные схемы.
//
Функция ВернутьДоступныеСхемыМультимодальныхПеревозок(ПараметрыОтбора, УпорядочитьПоВажности = Ложь) Экспорт
	
	мсвДоступныхСхем = Новый Массив;
	
	Запрос = Новый Запрос;
	
	// Подбирается схема возврата	
	Если ПараметрыОтбора.Свойство("ВозвратПоЦепиПоставок") И ПараметрыОтбора.ВозвратПоЦепиПоставок Тогда
		
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упСхемы.Ссылка КАК Схема,
		|	&АдресОтправления КАК АдресОтправления,
		|	&АдресПолучения КАК АдресПолучения,
		|	ВЫБОР
		|		КОГДА &АдресОтправления <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
		|				И &АдресПолучения <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
		|	&ЗонаОтправления КАК ЗонаОтправления,
		|	&ЗонаПолучения КАК ЗонаПолучения,
		|	упСхемы.АдресОтправления КАК СхемаАдресОтправления,
		|	упСхемы.АдресПолучения КАК СхемаАдресПолучения,
		|	упСхемы.ЗонаОтправления КАК СхемаЗонаОтправления,
		|	упСхемы.ЗонаПолучения КАК СхемаЗонаПолучения,
		|	&ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
		|	ВЫБОР
		|		КОГДА упСхемы.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Высокая)
		|			ТОГДА 0
		|		КОГДА упСхемы.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Низкая)
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Важность,
		|	ЕСТЬNULL(упВидыПеревозокСхема.ПараметрыВводаИнформацииОГрузовыхМестах, упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах) КАК ПараметрыВводаИнформацииОГрузовыхМестах,
		|	упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестахЗаявки
		|ПОМЕСТИТЬ втСхемыБезОтбораПоАдресамИЗонам
		|ИЗ
		|	Справочник.упСхемыМультимодальныхПеревозок КАК упСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.упИспользуютсяМультимодальныеПеревозки КАК упИспользуютсяМультимодальныеПеревозки
		|	ПО упИспользуютсяМультимодальныеПеревозки.Значение = ИСТИНА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокСхема
		|	ПО упВидыПеревозокСхема.Ссылка = упСхемы.ВидПеревозки
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокЗаявка
		|	ПО упВидыПеревозокЗаявка.Ссылка = &ВидПеревозки
		|ГДЕ ИСТИНА
		|	И НЕ (упСхемы.ПометкаУдаления)
		|	И упСхемы.Активно
		|	И упСхемы.ВидПеревозкиОтбор В (&ВидПеревозки,
		|			ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка))
		|	И упСхемы.ОрганизацияОтбор В (&Организация,
		|			ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|	И упСхемы.ЗаказчикОтбор В (&Заказчик,
		|			ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
		|	И упСхемы.СоглашениеОтбор В (&Соглашение,
		|			ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка))
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|//Промежуточные точки по схемам
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втСхемы.Схема КАК Схема,
		|	ПромежуточныеТочкиМаршрута.НомерСтроки КАК НомерСтроки,
		|	ПромежуточныеТочкиМаршрута.Адрес КАК Адрес,
		|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
		|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
		|	ЕСТЬNULL(упВидыПеревозокСхема.ПараметрыВводаИнформацииОГрузовыхМестах, упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах) КАК ПараметрыВводаИнформацииОГрузовыхМестах
		|ПОМЕСТИТЬ втПромежуточныеТочкиПоСхемамВозврата
		|ИЗ
		|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
		|	ПО втСхемы.Схема = ПромежуточныеТочкиМаршрута.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокСхема
		|	ПО упВидыПеревозокСхема.Ссылка = ПромежуточныеТочкиМаршрута.ВидПеревозки
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокЗаявка
		|	ПО упВидыПеревозокЗаявка.Ссылка = &ВидПеревозки
		|
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|//Для промежуточных точек определяются номера последних точек
		|ВЫБРАТЬ
		|	втПромежуточныеТочкиПоСхемамВозврата.Схема КАК Схема,
		|	втПромежуточныеТочкиПоСхемамВозврата.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
		|	МАКСИМУМ(втПромежуточныеТочкиПоСхемамВозврата.НомерСтроки) КАК НомерПоследнейТочки
		|ПОМЕСТИТЬ втНомераПоследнихПромежуточныхТочек
		|ИЗ
		|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиПоСхемамВозврата
		|СГРУППИРОВАТЬ ПО
		|	втПромежуточныеТочкиПоСхемамВозврата.Схема,
		|	втПромежуточныеТочкиПоСхемамВозврата.ВозвратПоЦепиПоставок
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|//Для последних промежуточных точек схем возврата, заполняются адреса
		|ВЫБРАТЬ
		|	втПромежуточныеТочки.Схема КАК Схема,
		|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
		|	втПромежуточныеТочки.Адрес.ГеографическаяЗона КАК ЗонаОтправления
		|ПОМЕСТИТЬ втАдресаОтправленияПоСхемамВозврата
		|ИЗ
		|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочки
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
		|	ПО ИСТИНА
		|		И втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
		|		И втПромежуточныеТочки.НомерСтроки = втНомераПоследнихТочек.НомерПоследнейТочки
		|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|//Схемы перевозок с адресами и зонами с учетом возврата по цепи
		|ВЫБРАТЬ
		|	втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
		|	втСхемыБезОтбораПоАдресамИЗонам.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
		|	втСхемыБезОтбораПоАдресамИЗонам.Схема КАК Схема,
		|	втСхемыБезОтбораПоАдресамИЗонам.Важность КАК Важность,
		|	ВЫБОР
		|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
		|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
		|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.АдресОтправления
		|	КОНЕЦ КАК СхемаАдресОтправления,
		|	ВЫБОР
		|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
		|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресПолучения
		|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
		|	КОНЕЦ КАК СхемаАдресПолучения,
		|	ВЫБОР
		|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
		|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
		|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.ЗонаОтправления
		|	КОНЕЦ КАК СхемаЗонаОтправления,
		|	ВЫБОР
		|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
		|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаПолучения
		|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
		|	КОНЕЦ КАК СхемаЗонаПолучения,
		|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаОтправления КАК ЗонаОтправления,
		|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаПолучения КАК ЗонаПолучения,
		|	втСхемыБезОтбораПоАдресамИЗонам.АдресОтправления КАК АдресОтправления,
		|	втСхемыБезОтбораПоАдресамИЗонам.АдресПолучения КАК АдресПолучения
		|ПОМЕСТИТЬ втСхемыСАдресамиСУчетомВозвратовПоЦепи
		|ИЗ
		|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемыБезОтбораПоАдресамИЗонам
		|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаОтправленияПоСхемамВозврата КАК втАдресаОтправленияПоСхемамВозврата
		|	ПО ИСТИНА
		|		И втСхемыБезОтбораПоАдресамИЗонам.Схема = втАдресаОтправленияПоСхемамВозврата.Схема
		|		И втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|//Схемы с фильтром по адресам и зонам
		|ВЫБРАТЬ
		|	втСхемы.АдресОтправления КАК АдресОтправления,
		|	втСхемы.АдресПолучения КАК АдресПолучения,
		|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
		|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
		|	втСхемы.Схема КАК Схема,
		|	втСхемы.Важность КАК Важность
		|ПОМЕСТИТЬ втСхемыСОтборомПоАдресам
		|ИЗ
		|	втСхемыСАдресамиСУчетомВозвратовПоЦепи КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонОтправления
		|	ПО ИСТИНА
		|		И втСхемы.СхемаЗонаОтправления = упИерархияГеографическихЗонОтправления.Родитель
		|		И втСхемы.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Зона
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонПолучения
		|	ПО ИСТИНА
		|		И втСхемы.СхемаЗонаПолучения = упИерархияГеографическихЗонПолучения.Родитель
		|		И втСхемы.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Зона
		|ГДЕ ИСТИНА
		|	И (ЛОЖЬ
		|		ИЛИ втСхемы.СхемаАдресОтправления = втСхемы.АдресОтправления
		|		ИЛИ втСхемы.СхемаАдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
		|	И (ЛОЖЬ
		|		ИЛИ втСхемы.СхемаАдресПолучения = втСхемы.АдресПолучения
		|		ИЛИ втСхемы.СхемаАдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|//Промежуточные точки, убраны те, схемы которых не прошли проверку по адресам
		|ВЫБРАТЬ
		|	втПромежуточныеТочкиПоСхемамВозвратаТ.Адрес КАК Адрес,
		|	втПромежуточныеТочкиПоСхемамВозвратаТ.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
		|	втПромежуточныеТочкиПоСхемамВозвратаТ.НомерСтроки КАК НомерСтроки,
		|	втПромежуточныеТочкиПоСхемамВозвратаТ.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
		|	втПромежуточныеТочкиПоСхемамВозвратаТ.Схема КАК Схема
		|ПОМЕСТИТЬ втПромежуточныеТочки
		|ИЗ
		|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиПоСхемамВозвратаТ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втСхемыСОтборомПоАдресамТ
		|	ПО втПромежуточныеТочкиПоСхемамВозвратаТ.Схема = втСхемыСОтборомПоАдресамТ.Схема
		|;
		|//{Запрос: 7 ////////////////////////////////////////
		|//Схемы без промежуточных точек
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втСхемы.АдресОтправления КАК АдресОтправления,
		|	втСхемы.АдресПолучения КАК АдресПолучения
		|ПОМЕСТИТЬ втЦепочкаАдресовПеревозок
		|ИЗ
		|	втСхемыСОтборомПоАдресам КАК втСхемы
		|ГДЕ ИСТИНА
		|	И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|	И НЕ (втСхемы.Схема В (
		|			ВЫБРАТЬ
		|				втПромежуточныеТочки.Схема КАК Схема
		|			ИЗ
		|				втПромежуточныеТочки КАК втПромежуточныеТочки))
		|	И НЕ (втСхемы.ВозвратПоЦепиПоставок)
		|ОБЪЕДИНИТЬ ВСЕ
		|//Первые промежуточные звенья цепи с адресами отправления получения
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втСхемы.АдресОтправления КАК АдресОтправления,
		|	втПромежуточныеТочки.Адрес КАК АдресПолучения
		|ИЗ
		|	втСхемыСОтборомПоАдресам КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
		|	ПО ИСТИНА
		|		И втСхемы.Схема = втПромежуточныеТочки.Схема
		|		И втПромежуточныеТочки.НомерСтроки = 1
		|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
		|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|ОБЪЕДИНИТЬ ВСЕ
		|//Промежуточные звенья цепи с адресами отправления получения(без первых и последних)
		|ВЫБРАТЬ
		|	втПромежуточныеТочкиНачало.Схема КАК Схема,
		|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
		|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения
		|ИЗ
		|	втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
		|	ПО ИСТИНА
		|		И втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
		|		И втПромежуточныеТочкиНачало.НомерСтроки = втПромежуточныеТочкиОкончание.НомерСтроки - 1
		|		И НЕ (втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок)
		|		И втПромежуточныеТочкиНачало.ПроверятьСовпадениеЭтаповЦепиПоставок
		|ОБЪЕДИНИТЬ ВСЕ
		|//Последние звенья цепи с адресами отправления получения
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
		|	втСхемы.АдресПолучения КАК АдресПолучения
		|ИЗ
		|	втСхемыСОтборомПоАдресам КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
		|	ПО ИСТИНА
		|		И втСхемы.Схема = втПромежуточныеТочки.Схема
		|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
		|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
		|	ПО ИСТИНА
		|		И втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
		|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
		|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|ОБЪЕДИНИТЬ ВСЕ
		|//Звенья между промежуточными точками (точка отправления - последняя промежуточная точка)
		|ВЫБРАТЬ
		|	втПромежуточныеТочкиНачало.Схема КАК Схема,
		|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
		|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения
		|ИЗ
		|	втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
		|	ПО ИСТИНА
		|		И втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
		|		И втПромежуточныеТочкиНачало.НомерСтроки - 1 = втПромежуточныеТочкиОкончание.НомерСтроки
		|		И втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок
		|		И втПромежуточныеТочкиНачало.ПроверятьСовпадениеЭтаповЦепиПоставок
		|ОБЪЕДИНИТЬ ВСЕ
		|//Звено получения(точка отправления и первая промежуточная точка)
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
		|	втСхемы.АдресПолучения КАК АдресПолучения
		|ИЗ
		|	втСхемыСОтборомПоАдресам КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
		|	ПО ИСТИНА
		|		И втСхемы.Схема = втПромежуточныеТочки.Схема
		|		И втПромежуточныеТочки.НомерСтроки = 1
		|		И втСхемы.ВозвратПоЦепиПоставок
		|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|;
		|
		|ВЫБРАТЬ
		|	втСхемыТ.Схема КАК Схема,
		|	СУММА(втСхемыТ.ГрузовыеМестаНеВводится) КАК ГрузовыеМестаНеВводится,
		|	СУММА(втСхемыТ.ГрузовыеМестаВводятся) КАК ГрузовыеМестаВводятся
		|ПОМЕСТИТЬ втСхемыСНесовместимымиВидамиПеревозок
		|ИЗ
		|	(ВЫБРАТЬ
		|		втСхемыТ.Схема КАК Схема,
		|		ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестахЗаявки = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаНеВводится,
		|		ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестахЗаявки <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаВводятся
		|	ИЗ
		|		втСхемыБезОтбораПоАдресамИЗонам КАК втСхемыТ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		втПромежуточныеТочкиТ.Схема КАК Схема,
		|		ВЫБОР
		|			КОГДА втПромежуточныеТочкиТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаНеВводится,
		|		ВЫБОР
		|			КОГДА втПромежуточныеТочкиТ.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаВводятся
		|	ИЗ
		|		втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиТ) КАК втСхемыТ
		|СГРУППИРОВАТЬ ПО
		|	втСхемыТ.Схема
		|ИМЕЮЩИЕ 
		|СУММА(втСхемыТ.ГрузовыеМестаНеВводится) > 0
		|И СУММА(втСхемыТ.ГрузовыеМестаВводятся) > 0
		|;
		|
		|//{Запрос: 8 ////////////////////////////////////////
		|//Возможно один из этапов цепи перевозок полностью адресно совпадает с переданными в параметрах,
		|//адресом отправления и адресом получения: такие схемы исключаются из подходящих.
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втПеревозкиСхем.Схема КАК Схема
		|ПОМЕСТИТЬ втИсключаемыеСхемы
		|ИЗ
		|	втЦепочкаАдресовПеревозок КАК втПеревозкиСхем
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втСхемы
		|	ПО ИСТИНА
		|		И втПеревозкиСхем.АдресОтправления = втСхемы.АдресОтправления
		|		И втПеревозкиСхем.АдресПолучения = втСхемы.АдресПолучения
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 
		|	втСхемыСНесовместимымиВидамиПеревозокТ.Схема КАК Схема
		|ИЗ
		|	втСхемыСНесовместимымиВидамиПеревозок КАК втСхемыСНесовместимымиВидамиПеревозокТ		
		|;
		|//{Запрос: 9 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втСхемы.Важность КАК Важность
		|ИЗ
		|	втСхемыСОтборомПоАдресам КАК втСхемы
		|ГДЕ НЕ (втСхемы.Схема В (
		|		ВЫБРАТЬ
		|			втИсключаемыеСхемы.Схема КАК Схема
		|		ИЗ
		|			втИсключаемыеСхемы КАК втИсключаемыеСхемы))
		|";


	Иначе	
				
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упСхемы.Ссылка КАК Схема,
		|	упСхемы.АдресОтправления КАК АдресОтправления,
		|	упСхемы.АдресПолучения КАК АдресПолучения,
		|	ВЫБОР
		|		КОГДА &АдресОтправления <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
		|				И &АдресПолучения <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
		|	ВЫБОР
		|		КОГДА упСхемы.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Высокая)
		|			ТОГДА 0
		|		КОГДА упСхемы.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Низкая)
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Важность,
		|	ЕСТЬNULL(упВидыПеревозокСхема.ПараметрыВводаИнформацииОГрузовыхМестах, упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах) КАК ПараметрыВводаИнформацииОГрузовыхМестах,
		|	упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестахЗаявки
		|ПОМЕСТИТЬ втСхемы
		|ИЗ
		|	Справочник.упСхемыМультимодальныхПеревозок КАК упСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонОтправления
		|	ПО ИСТИНА
		|		И упСхемы.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Родитель
		|		И &ЗонаОтправления = упИерархияГеографическихЗонОтправления.Зона
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонПолучения
		|	ПО ИСТИНА
		|		И упСхемы.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Родитель
		|		И &ЗонаПолучения = упИерархияГеографическихЗонПолучения.Зона
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокСхема
		|	ПО упВидыПеревозокСхема.Ссылка = упСхемы.ВидПеревозки
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокЗаявка
		|	ПО упВидыПеревозокЗаявка.Ссылка = &ВидПеревозки
		|ГДЕ ИСТИНА
		|	И НЕ (упСхемы.ПометкаУдаления)
		|	И упСхемы.Активно
		|	И упСхемы.ВидПеревозкиОтбор В (&ВидПеревозки,
		|			ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка))
		|	И упСхемы.ОрганизацияОтбор В (&Организация,
		|			ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|	И упСхемы.ЗаказчикОтбор В (&Заказчик,
		|			ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
		|	И упСхемы.СоглашениеОтбор В (&Соглашение,
		|			ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка))
		|	И упСхемы.АдресОтправления В (&АдресОтправления,
		|			ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
		|	И упСхемы.АдресПолучения В (&АдресПолучения,
		|			ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|// Возможно один из этапов цепи перевозок полностью адресно совпадает с переданными в параметрах,
		|//  адресом отправления и адресом получения: такие схемы исключаются из подходящих.
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	ПромежуточныеТочкиМаршрута.НомерСтроки КАК НомерСтроки,
		|	ПромежуточныеТочкиМаршрута.Адрес КАК Адрес,
		|	ЕСТЬNULL(упВидыПеревозокСхема.ПараметрыВводаИнформацииОГрузовыхМестах, упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах) КАК ПараметрыВводаИнформацииОГрузовыхМестах
		|ПОМЕСТИТЬ втПромежуточныеТочки
		|ИЗ
		|	втСхемы КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
		|	ПО ИСТИНА
		|		И втСхемы.Схема = ПромежуточныеТочкиМаршрута.Ссылка
		|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокСхема
		|	ПО упВидыПеревозокСхема.Ссылка = ПромежуточныеТочкиМаршрута.ВидПеревозки
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокЗаявка
		|	ПО упВидыПеревозокЗаявка.Ссылка = &ВидПеревозки
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПромежуточныеТочки.Схема КАК Схема,
		|	МАКСИМУМ(втПромежуточныеТочки.НомерСтроки) КАК НомерПоследнейТочки
		|ПОМЕСТИТЬ втНомераПоследнихТочек
		|ИЗ
		|	втПромежуточныеТочки КАК втПромежуточныеТочки
		|СГРУППИРОВАТЬ ПО
		|	втПромежуточныеТочки.Схема
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втСхемы.АдресОтправления КАК АдресОтправления,
		|	втСхемы.АдресПолучения КАК АдресПолучения
		|ПОМЕСТИТЬ втПеревозкиСхем
		|ИЗ
		|	втСхемы КАК втСхемы
		|ГДЕ ИСТИНА
		|	И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
		|	И НЕ (втСхемы.Схема В (
		|			ВЫБРАТЬ
		|				втПромежуточныеТочки.Схема КАК Схема
		|			ИЗ
		|				втПромежуточныеТочки КАК втПромежуточныеТочки))
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втСхемы.АдресОтправления КАК АдресОтправления,
		|	втПромежуточныеТочки.Адрес КАК АдресПолучения
		|ИЗ
		|	втСхемы КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
		|	ПО ИСТИНА
		|		И втСхемы.Схема = втПромежуточныеТочки.Схема
		|		И втПромежуточныеТочки.НомерСтроки = 1
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втПромежуточныеТочкиНачало.Схема КАК Схема,
		|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
		|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения
		|ИЗ
		|	втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
		|	ПО ИСТИНА
		|		И втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
		|		И втПромежуточныеТочкиНачало.НомерСтроки = втПромежуточныеТочкиОкончание.НомерСтроки - 1
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема,
		|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
		|	втСхемы.АдресПолучения КАК АдресПолучения
		|ИЗ
		|	втСхемы КАК втСхемы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
		|	ПО втСхемы.Схема = втПромежуточныеТочки.Схема
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихТочек КАК втНомераПоследнихТочек
		|	ПО втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСхемыТ.Схема КАК Схема,
		|	СУММА(втСхемыТ.ГрузовыеМестаНеВводится) КАК ГрузовыеМестаНеВводится,
		|	СУММА(втСхемыТ.ГрузовыеМестаВводятся) КАК ГрузовыеМестаВводятся
		|ПОМЕСТИТЬ втСхемыСНесовместимымиВидамиПеревозок
		|ИЗ
		|	(ВЫБРАТЬ
		|		втСхемыТ.Схема КАК Схема,
		|		ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестахЗаявки = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаНеВводится,
		|		ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестахЗаявки <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаВводятся
		|	ИЗ
		|		втСхемы КАК втСхемыТ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		втПромежуточныеТочкиТ.Схема КАК Схема,
		|		ВЫБОР
		|			КОГДА втПромежуточныеТочкиТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаНеВводится,
		|		ВЫБОР
		|			КОГДА втПромежуточныеТочкиТ.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ГрузовыеМестаВводятся
		|	ИЗ
		|		втПромежуточныеТочки КАК втПромежуточныеТочкиТ) КАК втСхемыТ
		|СГРУППИРОВАТЬ ПО
		|	втСхемыТ.Схема
		|ИМЕЮЩИЕ ИСТИНА
		|	И СУММА(втСхемыТ.ГрузовыеМестаНеВводится) > 0
		|	И СУММА(втСхемыТ.ГрузовыеМестаВводятся) > 0
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПеревозкиСхем.Схема КАК Схема
		|ПОМЕСТИТЬ втИсключаемыеСхемы
		|ИЗ
		|	втПеревозкиСхем КАК втПеревозкиСхем
		|ГДЕ ИСТИНА
		|	И втПеревозкиСхем.АдресОтправления = &АдресОтправления
		|	И втПеревозкиСхем.АдресПолучения = &АдресПолучения
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втСхемыСНесовместимымиВидамиПеревозокТ.Схема КАК Схема
		|ИЗ
		|	втСхемыСНесовместимымиВидамиПеревозок КАК втСхемыСНесовместимымиВидамиПеревозокТ
		|;
		|//{Запрос: 6 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСхемы.Схема КАК Схема
		|ИЗ
		|	втСхемы КАК втСхемы
		|ГДЕ НЕ (втСхемы.Схема В (
		|		ВЫБРАТЬ
		|			втИсключаемыеСхемы.Схема КАК Схема
		|		ИЗ
		|			втИсключаемыеСхемы КАК втИсключаемыеСхемы))
		|";

	КонецЕсли;
	
	Если УпорядочитьПоВажности Тогда
		Запрос.Текст = Запрос.Текст + Символы.ПС + 
		"УПОРЯДОЧИТЬ ПО 
		|  Важность";
	КонецЕсли;	
 
	Для каждого текПараметр Из ПараметрыОтбора Цикл
		Запрос.УстановитьПараметр(текПараметр.Ключ, текПараметр.Значение); 
	КонецЦикла; 
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			мсвДоступныхСхем.Добавить(Выборка.Схема);
		КонецЦикла; 
	КонецЕсли;
	
	Возврат мсвДоступныхСхем;
		
КонецФункции // ВернутьДоступныеСхемыМультимодальныхПеревозок()

// Функция - Подбирает значения схем мультмодальных перевозок для передаваемой
//  таблицы параметров.
//
// Параметры:
//  тбзТаблицаСхем		 - ТаблицаЗначений	 - таблица значений с колонками:
//  	* ИдентификаторСтроки - Число - идентификатор строки
//  	* Организация - СправочникСсылка.Организации - организация.
//  	* ВидПеревозки - СправочникСсылка.упВидыПеревозок - вид перевозки.
//  	* Заказчик - СправочникСсылка.упПартнеры - заказчик.
//  	* Соглашение - СправочникСсылка.упСоглашенияСКлиентами - соглашение.
//  	* АдресОтправления - СправочникСсылка.упАдреса - адрес отправления.
//  	* АдресПолучения - СправочникСсылка.упАдреса - адрес получения.
//  	* ЗонаОтправления - СправочникСсылка.упГеографическиеЗоны - зона отправления.
//  	* ЗонаПолучения - СправочникСсылка.упГеографическиеЗоны - зона получения.
//  УпорядочитьПоВажности - Булево			 - Истина, если необходимо упорядочить в
//  			возвращаемой таблице схемы по важности.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица значений с подобранными схемами перевозки, колонки:
//  * Схема - СправочникСсылка.упСхемыМультимодальныхПеревозок - схема перевозки.
//  * ИдентификаторСтроки - Число - идентификатор.
//
Функция ЗначенияСхемМультимодальныхПеревозок(тбзТаблицаСхем, УпорядочитьПоВажности = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбДанныеДляПодбораСхем.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	тбДанныеДляПодбораСхем.Организация КАК Организация,
	|	тбДанныеДляПодбораСхем.ВидПеревозки КАК ВидПеревозки,
	|	тбДанныеДляПодбораСхем.Заказчик КАК Заказчик,
	|	тбДанныеДляПодбораСхем.Соглашение КАК Соглашение,
	|	тбДанныеДляПодбораСхем.АдресОтправления КАК АдресОтправления,
	|	тбДанныеДляПодбораСхем.АдресПолучения КАК АдресПолучения,
	|	тбДанныеДляПодбораСхем.ЗонаОтправления КАК ЗонаОтправления,
	|	тбДанныеДляПодбораСхем.ЗонаПолучения КАК ЗонаПолучения,
	|	тбДанныеДляПодбораСхем.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок
	|ПОМЕСТИТЬ втДанныеДляПодбораСхем
	|ИЗ
	|	&тбДанныеДляПодбораСхем КАК тбДанныеДляПодбораСхем
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//Если в заявке не заполнены зоны отправления/получения, они берутся из адресов
	|ВЫБРАТЬ
	|	втДанныеДляПодбораСхем.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втДанныеДляПодбораСхем.Организация КАК Организация,
	|	втДанныеДляПодбораСхем.ВидПеревозки КАК ВидПеревозки,
	|	втДанныеДляПодбораСхем.Заказчик КАК Заказчик,
	|	втДанныеДляПодбораСхем.Соглашение КАК Соглашение,
	|	втДанныеДляПодбораСхем.АдресОтправления КАК АдресОтправления,
	|	втДанныеДляПодбораСхем.АдресПолучения КАК АдресПолучения,
	|	ВЫБОР
	|		КОГДА втДанныеДляПодбораСхем.ЗонаОтправления = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(АдресаОтправления.ГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка))
	|		ИНАЧЕ втДанныеДляПодбораСхем.ЗонаОтправления
	|	КОНЕЦ КАК ЗонаОтправления,
	|	ВЫБОР
	|		КОГДА втДанныеДляПодбораСхем.ЗонаПолучения = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(АдресаПолучения.ГеографическаяЗона, ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка))
	|		ИНАЧЕ втДанныеДляПодбораСхем.ЗонаПолучения
	|	КОНЕЦ КАК ЗонаПолучения,
	|	втДанныеДляПодбораСхем.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок
	|ПОМЕСТИТЬ втДанныеДляПодбораСхемСЗонами
	|ИЗ
	|	втДанныеДляПодбораСхем КАК втДанныеДляПодбораСхем
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК АдресаПолучения
	|	ПО втДанныеДляПодбораСхем.АдресПолучения = АдресаПолучения.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК АдресаОтправления
	|	ПО втДанныеДляПодбораСхем.АдресОтправления = АдресаОтправления.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|//Первоначальный отбор подходящих схем, не учитывая адреса и зоны
	|ВЫБРАТЬ
	|	упСхемы.Ссылка КАК Схема,
	|	упСхемы.АдресОтправления КАК СхемаАдресОтправления,
	|	упСхемы.АдресПолучения КАК СхемаАдресПолучения,
	|	упСхемы.ЗонаОтправления КАК СхемаЗонаОтправления,
	|	упСхемы.ЗонаПолучения КАК СхемаЗонаПолучения,
	|	ВЫБОР
	|		КОГДА втДанныеДляПодбораСхем.АдресОтправления <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|				И втДанныеДляПодбораСхем.АдресПолучения <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	ВЫБОР
	|		КОГДА упСхемы.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Высокая)
	|			ТОГДА 0
	|		КОГДА упСхемы.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Низкая)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Важность,
	|	втДанныеДляПодбораСхем.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втДанныеДляПодбораСхем.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втДанныеДляПодбораСхем.ЗонаОтправления КАК ЗонаОтправления,
	|	втДанныеДляПодбораСхем.ЗонаПолучения КАК ЗонаПолучения,
	|	втДанныеДляПодбораСхем.АдресОтправления КАК АдресОтправления,
	|	втДанныеДляПодбораСхем.АдресПолучения КАК АдресПолучения,
	|	ЕСТЬNULL(упВидыПеревозокСхема.ПараметрыВводаИнформацииОГрузовыхМестах, упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах) КАК ПараметрыВводаИнформацииОГрузовыхМестах,
	|	упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестахЗаявки,
	|	втДанныеДляПодбораСхем.ВидПеревозки
	|ПОМЕСТИТЬ втСхемыБезОтбораПоАдресамИЗонам
	|ИЗ
	|	втДанныеДляПодбораСхемСЗонами КАК втДанныеДляПодбораСхем
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок КАК упСхемы
	|	ПО ИСТИНА
	|		И (ЛОЖЬ
	|			ИЛИ втДанныеДляПодбораСхем.ВидПеревозки = упСхемы.ВидПеревозкиОтбор
	|			ИЛИ упСхемы.ВидПеревозкиОтбор = ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка))
	|		И (ЛОЖЬ
	|			ИЛИ втДанныеДляПодбораСхем.Организация = упСхемы.ОрганизацияОтбор
	|			ИЛИ упСхемы.ОрганизацияОтбор = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|		И (ЛОЖЬ
	|			ИЛИ втДанныеДляПодбораСхем.Заказчик = упСхемы.ЗаказчикОтбор
	|			ИЛИ упСхемы.ЗаказчикОтбор = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|		И (ЛОЖЬ
	|			ИЛИ втДанныеДляПодбораСхем.Соглашение = упСхемы.СоглашениеОтбор
	|			ИЛИ упСхемы.СоглашениеОтбор = ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка))
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокСхема
	|	ПО упВидыПеревозокСхема.Ссылка = упСхемы.ВидПеревозки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокЗаявка
	|	ПО упВидыПеревозокЗаявка.Ссылка = втДанныеДляПодбораСхем.ВидПеревозки
	|		
	|ГДЕ ИСТИНА
	|	И НЕ (упСхемы.ПометкаУдаления)
	|	И упСхемы.Активно
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|//Промежуточные точки по схемам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСхемы.Схема КАК Схема,
	|	ПромежуточныеТочкиМаршрута.НомерСтроки КАК НомерСтроки,
	|	ПромежуточныеТочкиМаршрута.Адрес КАК Адрес,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	ЕСТЬNULL(упВидыПеревозокСхема.ПараметрыВводаИнформацииОГрузовыхМестах, упВидыПеревозокЗаявка.ПараметрыВводаИнформацииОГрузовыхМестах) КАК ПараметрыВводаИнформацииОГрузовыхМестах
	|ПОМЕСТИТЬ втПромежуточныеТочкиПоСхемам
	|ИЗ
	|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
	|	ПО втСхемы.Схема = ПромежуточныеТочкиМаршрута.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокСхема
	|	ПО упВидыПеревозокСхема.Ссылка = ПромежуточныеТочкиМаршрута.ВидПеревозки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозокЗаявка
	|	ПО упВидыПеревозокЗаявка.Ссылка = втСхемы.ВидПеревозки
	|
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|//Для промежуточных точек определяются номера последних точек
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиПоСхемам.Схема КАК Схема,
	|	втПромежуточныеТочкиПоСхемам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	МАКСИМУМ(втПромежуточныеТочкиПоСхемам.НомерСтроки) КАК НомерПоследнейТочки
	|ПОМЕСТИТЬ втНомераПоследнихПромежуточныхТочек
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочкиПоСхемам
	|СГРУППИРОВАТЬ ПО
	|	втПромежуточныеТочкиПоСхемам.Схема,
	|	втПромежуточныеТочкиПоСхемам.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|//Для последних промежуточных точек схем возврата, заполняются адреса
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочки.Адрес.ГеографическаяЗона КАК ЗонаОтправления,
	|	втПромежуточныеТочки.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ втАдресаОтправленияПоСхемамВозврата
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И втПромежуточныеТочки.НомерСтроки = втНомераПоследнихТочек.НомерПоследнейТочки
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|//Схемы перевозок с адресами и зонами с учетом возврата по цепи
	|ВЫБРАТЬ
	|	втСхемыБезОтбораПоАдресамИЗонам.Важность КАК Важность,
	|	втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемыБезОтбораПоАдресамИЗонам.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втСхемыБезОтбораПоАдресамИЗонам.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	втСхемыБезОтбораПоАдресамИЗонам.Схема КАК Схема,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
	|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.АдресОтправления
	|	КОНЕЦ КАК СхемаАдресОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресПолучения
	|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
	|	КОНЕЦ КАК СхемаАдресПолучения,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
	|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.ЗонаОтправления
	|	КОНЕЦ КАК СхемаЗонаОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаПолучения
	|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
	|	КОНЕЦ КАК СхемаЗонаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаОтправления КАК ЗонаОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаПолучения КАК ЗонаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.АдресОтправления КАК АдресОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.АдресПолучения КАК АдресПолучения
	|ПОМЕСТИТЬ втСхемыСАдресамиСУчетомВозвратовПоЦепи
	|ИЗ
	|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемыБезОтбораПоАдресамИЗонам
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаОтправленияПоСхемамВозврата КАК втАдресаОтправленияПоСхемамВозврата
	|	ПО ИСТИНА
	|		И втСхемыБезОтбораПоАдресамИЗонам.Схема = втАдресаОтправленияПоСхемамВозврата.Схема
	|		И втСхемыБезОтбораПоАдресамИЗонам.ИдентификаторСтроки = втАдресаОтправленияПоСхемамВозврата.ИдентификаторСтроки
	|		И втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|//Схемы с фильтром по адресам и зонам
	|ВЫБРАТЬ
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.Важность КАК Важность,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	втСхемы.Схема КАК Схема
	|ПОМЕСТИТЬ втСхемыСОтборомПоАдресам
	|ИЗ
	|	втСхемыСАдресамиСУчетомВозвратовПоЦепи КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонОтправления
	|	ПО ИСТИНА
	|		И втСхемы.СхемаЗонаОтправления = упИерархияГеографическихЗонОтправления.Родитель
	|		И втСхемы.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Зона
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонПолучения
	|	ПО ИСТИНА
	|		И втСхемы.СхемаЗонаПолучения = упИерархияГеографическихЗонПолучения.Родитель
	|		И втСхемы.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Зона
	|ГДЕ ИСТИНА
	|	И (ЛОЖЬ
	|		ИЛИ втСхемы.СхемаАдресОтправления = втСхемы.АдресОтправления
	|		ИЛИ втСхемы.СхемаАдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|	И (ЛОЖЬ
	|		ИЛИ втСхемы.СхемаАдресПолучения = втСхемы.АдресПолучения
	|		ИЛИ втСхемы.СхемаАдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|//Промежуточные точки, убраны те, схемы которых не прошли проверку по адресам
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Адрес КАК Адрес,
	|	втПромежуточныеТочки.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втПромежуточныеТочки.НомерСтроки КАК НомерСтроки,
	|	втПромежуточныеТочки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втПромежуточныеТочки.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	втПромежуточныеТочки.Схема КАК Схема
	|ПОМЕСТИТЬ втПромежуточныеТочки
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втСхемыСОтборомПоАдресамТ
	|	ПО втПромежуточныеТочки.Схема = втСхемыСОтборомПоАдресамТ.Схема
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|//Схемы без промежуточных точек
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ втПеревозкиСхем
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|ГДЕ ИСТИНА
	|	И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|	И НЕ (втСхемы.Схема В (
	|			ВЫБРАТЬ
	|				втПромежуточныеТочки.Схема КАК Схема
	|			ИЗ
	|				втПромежуточныеТочки КАК втПромежуточныеТочки))
	|	И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|ОБЪЕДИНИТЬ ВСЕ
	|//Первые промежуточные звенья цепи с адресами отправления получения
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втПромежуточныеТочки.Адрес КАК АдресПолучения,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
	|	ПО ИСТИНА
	|		И втСхемы.Схема = втПромежуточныеТочки.Схема
	|		И втПромежуточныеТочки.НомерСтроки = 1
	|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Промежуточные звенья цепи с адресами отправления получения(без первых и последних)
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиНачало.Схема КАК Схема,
	|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения,
	|	втПромежуточныеТочкиОкончание.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
	|	ПО ИСТИНА
	|		И втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
	|		И втПромежуточныеТочкиНачало.НомерСтроки = втПромежуточныеТочкиОкончание.НомерСтроки - 1
	|		И НЕ (втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок)
	|		И втПромежуточныеТочкиНачало.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Последние звенья цепи с адресами отправления получения
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
	|	ПО ИСТИНА
	|		И втСхемы.Схема = втПромежуточныеТочки.Схема
	|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Звенья между промежуточными точками (точка отправления - последняя промежуточная точка)
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиНачало.Схема КАК Схема,
	|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения,
	|	втПромежуточныеТочкиОкончание.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
	|	ПО ИСТИНА
	|		И втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
	|		И втПромежуточныеТочкиНачало.НомерСтроки - 1 = втПромежуточныеТочкиОкончание.НомерСтроки
	|		И втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок
	|		И втПромежуточныеТочкиНачало.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Звено получения(точка отправления и первая промежуточная точка)
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
	|	ПО ИСТИНА
	|		И втСхемы.Схема = втПромежуточныеТочки.Схема
	|		И втПромежуточныеТочки.НомерСтроки = 1
	|		И втСхемы.ВозвратПоЦепиПоставок
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|;
	|
	|ВЫБРАТЬ
	|	втСхемыТ.Схема КАК Схема,
	|	СУММА(втСхемыТ.ГрузовыеМестаНеВводится) КАК ГрузовыеМестаНеВводится,
	|	СУММА(втСхемыТ.ГрузовыеМестаВводятся) КАК ГрузовыеМестаВводятся
	|ПОМЕСТИТЬ втСхемыСНесовместимымиВидамиПеревозок
	|ИЗ
	|	(ВЫБРАТЬ
	|		втСхемыТ.Схема КАК Схема,
	|		ВЫБОР
	|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ + ВЫБОР
	|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестахЗаявки = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ГрузовыеМестаНеВводится,
	|		ВЫБОР
	|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ + ВЫБОР
	|			КОГДА втСхемыТ.ПараметрыВводаИнформацииОГрузовыхМестахЗаявки <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ГрузовыеМестаВводятся
	|	ИЗ
	|		втСхемыБезОтбораПоАдресамИЗонам КАК втСхемыТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		втПромежуточныеТочкиТ.Схема КАК Схема,
	|		ВЫБОР
	|			КОГДА втПромежуточныеТочкиТ.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ГрузовыеМестаНеВводится,
	|		ВЫБОР
	|			КОГДА втПромежуточныеТочкиТ.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ГрузовыеМестаВводятся
	|	ИЗ
	|		втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочкиТ) КАК втСхемыТ
	|СГРУППИРОВАТЬ ПО
	|	втСхемыТ.Схема
	|ИМЕЮЩИЕ 
	|СУММА(втСхемыТ.ГрузовыеМестаНеВводится) > 0
	|И СУММА(втСхемыТ.ГрузовыеМестаВводятся) > 0
	|;
	|
	|
	|//{Запрос: 10 ////////////////////////////////////////
	|//Возможно один из этапов цепи перевозок полностью адресно совпадает с переданными в параметрах,
	|//адресом отправления и адресом получения: такие схемы исключаются из подходящих.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПеревозкиСхем.Схема КАК Схема,
	|	втПеревозкиСхем.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ втИсключаемыеСхемы
	|ИЗ
	|	втПеревозкиСхем КАК втПеревозкиСхем
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДляПодбораСхем КАК втДанныеДляПодбораСхем
	|	ПО ИСТИНА
	|		И втПеревозкиСхем.АдресОтправления = втДанныеДляПодбораСхем.АдресОтправления
	|		И втПеревозкиСхем.АдресПолучения = втДанныеДляПодбораСхем.АдресПолучения
	|		И втПеревозкиСхем.ИдентификаторСтроки = втДанныеДляПодбораСхем.ИдентификаторСтроки
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПеревозкиСхем.Схема КАК Схема,
	|	втПеревозкиСхем.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втПеревозкиСхем КАК втПеревозкиСхем
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСхемыСНесовместимымиВидамиПеревозок КАК втСхемыСНесовместимымиВидамиПеревозок
	|	ПО втПеревозкиСхем.Схема = втСхемыСНесовместимымиВидамиПеревозок.Схема
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|ГДЕ НЕ ((втСхемы.Схема, втСхемы.ИдентификаторСтроки) В (
	|			ВЫБРАТЬ
	|			втИсключаемыеСхемы.Схема КАК Схема,
	|			втИсключаемыеСхемы.ИдентификаторСтроки КАК ИдентификаторСтроки
	|		ИЗ
	|			втИсключаемыеСхемы КАК втИсключаемыеСхемы))
	|";

	Если УпорядочитьПоВажности Тогда
		Запрос.Текст = Запрос.Текст + Символы.ПС + 
		"УПОРЯДОЧИТЬ ПО 
		|  Важность";
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("тбДанныеДляПодбораСхем", тбзТаблицаСхем);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Функция проверяет соотвествие схемы к мультимодальной перевозки заявки.
//
// Параметры:
//   ЗаявкаНаПеревозкуГруза - ДокументСсылка.упЗаявкаНаПеревозкуГруза - Ссылка на документ.
//
// Возвращаемое значение:
//   Булево - Флаг - Результат проверки.
//
Функция СхемыМультимодальнойПеревозкиЗаявкеПодобраныПравильно(ЗаявкаНаПеревозкуГруза) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|//Данные для подбора схем мультимодальной перевозки
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА тчГрузовыеМеста.СхемаМультимодальнойПеревозки = ЗНАЧЕНИЕ(Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка)
	|			ТОГДА Заявка.СхемаМультимодальнойПеревозки
	|		ИНАЧЕ тчГрузовыеМеста.СхемаМультимодальнойПеревозки
	|	КОНЕЦ КАК Схема,
	|	Заявка.ВидПеревозки.ИспользуютсяМультимодальныеПеревозки КАК ИспользуютсяМультимодальныеПеревозки,
	|	Заявка.ВидПеревозки КАК ВидПеревозки,
	|	Заявка.Организация КАК Организация,
	|	Заявка.Заказчик КАК Заказчик,
	|	Заявка.Соглашение КАК Соглашение,
	|	ВЫБОР
	|		КОГДА тчГрузовыеМеста.АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА Заявка.АдресОтправления
	|		ИНАЧЕ тчГрузовыеМеста.АдресОтправления
	|	КОНЕЦ КАК АдресОтправления,
	|	ВЫБОР
	|		КОГДА тчГрузовыеМеста.АдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА Заявка.АдресПолучения
	|		ИНАЧЕ тчГрузовыеМеста.АдресПолучения
	|	КОНЕЦ КАК АдресПолучения,
	|	ВЫБОР
	|		КОГДА тчГрузовыеМеста.АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА Заявка.ЗонаОтправления
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА НЕ тчГрузовыеМеста.АдресОтправления.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|					ТОГДА тчГрузовыеМеста.АдресОтправления.ГеографическаяЗона
	|				ИНАЧЕ
	|					Заявка.ЗонаОтправления
	|			КОНЕЦ		
	|	КОНЕЦ КАК ЗонаОтправления,
	|	ВЫБОР
	|		КОГДА тчГрузовыеМеста.АдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА Заявка.ЗонаПолучения
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА НЕ тчГрузовыеМеста.АдресПолучения.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|					ТОГДА тчГрузовыеМеста.АдресПолучения.ГеографическаяЗона
	|				ИНАЧЕ
	|					Заявка.ЗонаПолучения
	|			КОНЕЦ		
	|	КОНЕЦ КАК ЗонаПолучения,
	//|	ВЫБОР
	//|		КОГДА Заявка.ЗонаОтправления = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	//|			ТОГДА тчГрузовыеМеста.АдресОтправления.ГеографическаяЗона
	//|		ИНАЧЕ Заявка.ЗонаОтправления
	//|	КОНЕЦ КАК ЗонаОтправления,
	//|	ВЫБОР
	//|		КОГДА Заявка.ЗонаПолучения = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	//|			ТОГДА тчГрузовыеМеста.АдресПолучения.ГеографическаяЗона
	//|		ИНАЧЕ Заявка.ЗонаПолучения
	//|	КОНЕЦ КАК ЗонаПолучения,
	|	тчГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок
	|ПОМЕСТИТЬ втДанныеДляПодбораСхем
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК тчГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК Заявка
	|	ПО тчГрузовыеМеста.Ссылка = Заявка.Ссылка
	|ГДЕ
	|	Заявка.Ссылка = &Ссылка
	|	И (НЕ (тчГрузовыеМеста.СхемаМультимодальнойПеревозки = ЗНАЧЕНИЕ(Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка))
	|		ИЛИ НЕ (Заявка.СхемаМультимодальнойПеревозки = ЗНАЧЕНИЕ(Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка)))
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втДанныеДляПодбораСхем.Схема КАК Схема,
	|	втДанныеДляПодбораСхем.АдресОтправления КАК АдресОтправления,
	|	втДанныеДляПодбораСхем.АдресПолучения КАК АдресПолучения,
	|	ВЫБОР
	|		КОГДА втДанныеДляПодбораСхем.АдресОтправления <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			И втДанныеДляПодбораСхем.АдресПолучения <> ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	втДанныеДляПодбораСхем.ЗонаОтправления КАК ЗонаОтправления,
	|	втДанныеДляПодбораСхем.ЗонаПолучения КАК ЗонаПолучения,
	|	упСхемы.АдресОтправления КАК СхемаАдресОтправления,
	|	упСхемы.АдресПолучения КАК СхемаАдресПолучения,
	|	упСхемы.ЗонаОтправления КАК СхемаЗонаОтправления,
	|	упСхемы.ЗонаПолучения КАК СхемаЗонаПолучения,
	|	втДанныеДляПодбораСхем.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок
	|ПОМЕСТИТЬ втСхемыБезОтбораПоАдресамИЗонам
	|ИЗ
	|	втДанныеДляПодбораСхем КАК втДанныеДляПодбораСхем
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.упИспользуютсяМультимодальныеПеревозки КАК упИспользуютсяМультимодальныеПеревозки
	|	ПО упИспользуютсяМультимодальныеПеревозки.Значение = ИСТИНА
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок КАК упСхемы
	|	ПО втДанныеДляПодбораСхем.Схема = упСхемы.Ссылка
	|		И втДанныеДляПодбораСхем.ИспользуютсяМультимодальныеПеревозки = ИСТИНА
	|		И НЕ (упСхемы.ПометкаУдаления)
	|		И упСхемы.Активно
	|		И упСхемы.ВидПеревозкиОтбор В (втДанныеДляПодбораСхем.ВидПеревозки,
	|				ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка))
	|		И упСхемы.ОрганизацияОтбор В (втДанныеДляПодбораСхем.Организация,
	|				ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|		И упСхемы.ЗаказчикОтбор В (втДанныеДляПодбораСхем.Заказчик,
	|				ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|		И упСхемы.СоглашениеОтбор В (втДанныеДляПодбораСхем.Соглашение,
	|				ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка))
	|ГДЕ
	|	НЕ (упСхемы.ПометкаУдаления)
	|	И упСхемы.Активно
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|//Промежуточные точки по схемам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСхемы.Схема КАК Схема,
	|	ПромежуточныеТочкиМаршрута.НомерСтроки КАК НомерСтроки,
	|	ПромежуточныеТочкиМаршрута.Адрес КАК Адрес,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ПОМЕСТИТЬ втПромежуточныеТочкиПоСхемамВозврата
	|ИЗ
	|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
	|	ПО втСхемы.Схема = ПромежуточныеТочкиМаршрута.Ссылка
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|//Для промежуточных точек определяются номера последних точек
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиПоСхемамВозврата.Схема КАК Схема,
	|	втПромежуточныеТочкиПоСхемамВозврата.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	МАКСИМУМ(втПромежуточныеТочкиПоСхемамВозврата.НомерСтроки) КАК НомерПоследнейТочки
	|ПОМЕСТИТЬ втНомераПоследнихПромежуточныхТочек
	|ИЗ
	|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиПоСхемамВозврата
	|СГРУППИРОВАТЬ ПО
	|	втПромежуточныеТочкиПоСхемамВозврата.Схема,
	|	втПромежуточныеТочкиПоСхемамВозврата.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|//Для последних промежуточных точек схем возврата, заполняются адреса
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочки.Адрес.ГеографическаяЗона КАК ЗонаОтправления
	|ПОМЕСТИТЬ втАдресаОтправленияПоСхемамВозврата
	|ИЗ
	|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И втПромежуточныеТочки.НомерСтроки = втНомераПоследнихТочек.НомерПоследнейТочки
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|//Схемы перевозок с адресами и зонами с учетом возврата по цепи
	|ВЫБРАТЬ
	|	втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемыБезОтбораПоАдресамИЗонам.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	втСхемыБезОтбораПоАдресамИЗонам.Схема КАК Схема,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
	|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.АдресОтправления
	|	КОНЕЦ КАК СхемаАдресОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресПолучения
	|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
	|	КОНЕЦ КАК СхемаАдресПолучения,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
	|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.ЗонаОтправления
	|	КОНЕЦ КАК СхемаЗонаОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаПолучения
	|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
	|	КОНЕЦ КАК СхемаЗонаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаОтправления КАК ЗонаОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаПолучения КАК ЗонаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.АдресОтправления КАК АдресОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.АдресПолучения КАК АдресПолучения
	|ПОМЕСТИТЬ втСхемыСАдресамиСУчетомВозвратовПоЦепи
	|ИЗ
	|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемыБезОтбораПоАдресамИЗонам
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаОтправленияПоСхемамВозврата КАК втАдресаОтправленияПоСхемамВозврата
	|	ПО втСхемыБезОтбораПоАдресамИЗонам.Схема = втАдресаОтправленияПоСхемамВозврата.Схема
	|		И втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|//Схемы с фильтром по адресам и зонам
	|ВЫБРАТЬ
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок КАК ПроверятьСовпадениеЭтаповЦепиПоставок,
	|	втСхемы.Схема КАК Схема
	|ПОМЕСТИТЬ втСхемыСОтборомПоАдресам
	|ИЗ
	|	втСхемыСАдресамиСУчетомВозвратовПоЦепи КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонОтправления
	|	ПО втСхемы.СхемаЗонаОтправления = упИерархияГеографическихЗонОтправления.Родитель
	|		И втСхемы.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Зона
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонПолучения
	|	ПО втСхемы.СхемаЗонаПолучения = упИерархияГеографическихЗонПолучения.Родитель
	|		И втСхемы.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Зона
	|ГДЕ
	|	(втСхемы.СхемаАдресОтправления = втСхемы.АдресОтправления
	|		ИЛИ втСхемы.СхемаАдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|	И (втСхемы.СхемаАдресПолучения = втСхемы.АдресПолучения
	|		ИЛИ втСхемы.СхемаАдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|//Схемы без промежуточных точек
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения
	|ПОМЕСТИТЬ втЦепочкаАдресовПеревозок
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|ГДЕ
	|	втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|	И НЕ (втСхемы.Схема В (
	|			ВЫБРАТЬ
	|				втПромежуточныеТочкиПоСхемамВозврата.Схема КАК Схема
	|			ИЗ
	|				втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиПоСхемамВозврата))
	|	И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|ОБЪЕДИНИТЬ ВСЕ
	|//Первые промежуточные звенья цепи с адресами отправления получения
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втПромежуточныеТочки.Адрес КАК АдресПолучения
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочки
	|	ПО втСхемы.Схема = втПромежуточныеТочки.Схема
	|		И втПромежуточныеТочки.НомерСтроки = 1
	|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Промежуточные звенья цепи с адресами отправления получения(без первых и последних)
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиНачало.Схема КАК Схема,
	|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения
	|ИЗ
	|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиОкончание
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиНачало
	|	ПО втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
	|		И втПромежуточныеТочкиНачало.НомерСтроки = втПромежуточныеТочкиОкончание.НомерСтроки - 1
	|		И НЕ (втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок)
	|		И втПромежуточныеТочкиНачало.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Последние звенья цепи с адресами отправления получения
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочки
	|	ПО втСхемы.Схема = втПромежуточныеТочки.Схема
	|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И НЕ (втСхемы.ВозвратПоЦепиПоставок)
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Звенья между промежуточными точками (точка отправления - последняя промежуточная точка)
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиНачало.Схема КАК Схема,
	|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения
	|ИЗ
	|	втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиОкончание
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочкиНачало
	|	ПО втПромежуточныеТочкиОкончание.Схема = втПромежуточныеТочкиНачало.Схема
	|		И втПромежуточныеТочкиНачало.НомерСтроки - 1 = втПромежуточныеТочкиОкончание.НомерСтроки
	|		И втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок
	|		И втПромежуточныеТочкиНачало.ПроверятьСовпадениеЭтаповЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|//Звено получения(точка отправления и первая промежуточная точка)
	|ВЫБРАТЬ
	|	втСхемы.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения
	|ИЗ
	|	втСхемыСОтборомПоАдресам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочкиПоСхемамВозврата КАК втПромежуточныеТочки
	|	ПО втСхемы.Схема = втПромежуточныеТочки.Схема
	|		И втПромежуточныеТочки.НомерСтроки = 1
	|		И втСхемы.ВозвратПоЦепиПоставок
	|		И втСхемы.ПроверятьСовпадениеЭтаповЦепиПоставок
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|//Возможно один из этапов цепи перевозок полностью адресно совпадает с переданными в параметрах,
	|//адресом отправления и адресом получения: такие схемы исключаются из подходящих.
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПеревозкиСхем.Схема
	|ПОМЕСТИТЬ втИсключаемыеСхемы
	|ИЗ
	|	втЦепочкаАдресовПеревозок КАК втПеревозкиСхем
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втСхемы
	|		ПО втПеревозкиСхем.АдресОтправления = втСхемы.АдресОтправления
	|			И втПеревозкиСхем.АдресПолучения = втСхемы.АдресПолучения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеДляПодбораСхем.Схема
	|ИЗ
	|	втДанныеДляПодбораСхем КАК втДанныеДляПодбораСхем
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втСхемы
	|			ЛЕВОЕ СОЕДИНЕНИЕ втИсключаемыеСхемы КАК втИсключаемыеСхемы
	|			ПО втСхемы.Схема = втИсключаемыеСхемы.Схема
	|		ПО втДанныеДляПодбораСхем.Схема = втСхемы.Схема
	|ГДЕ
	|	(втСхемы.Схема ЕСТЬ NULL
	|			ИЛИ НЕ втИсключаемыеСхемы.Схема ЕСТЬ NULL)
	|";

	Запрос.УстановитьПараметр("Ссылка", ЗаявкаНаПеревозкуГруза);
	Результат = Запрос.Выполнить();
	Возврат Результат.Пустой();

КонецФункции // ПроверитьСоответствиеСхемыМультимодальнойПеревозкиЗаявке()

//// Функция создает задания на перевозку на основании заявки на перевозку
////
////   Параметры:
////     ЗаявкаНаПеревозкуГруза   - ДокументСсылка.упЗаявкаНаПеревозкуГруза
////     ОбъектЗаданиеНаПеревозку - ДокументОбъект.упЗаданиеНаПеревозкуГруза, Неопределено - параметр передается при вызове функции из формы документа задания
////     ТекстОшибки              - Строка
////     Отказ                    - Булево
////
////   Возвращаемое значение:
////     Массив - Массив созданных заданий на перевозку
////  
//Функция СформироватьЗаданияНаПервозкуГруза(ЗаявкаНаПеревозкуГруза, ОбъектЗаданиеНаПеревозку = Неопределено, ТекстОшибки = "", Отказ) Экспорт
//	
//	Возврат упСЛК.СоздатьОбъект().СформироватьЗаданияНаПервозкуГруза(ЗаявкаНаПеревозкуГруза, ОбъектЗаданиеНаПеревозку, ТекстОшибки, Отказ);
//		
//КонецФункции // СформироватьЗаданияНаПервозкуГруза()

Функция ЭтапыПеревозки(ЗаявкаНаПеревозкуГруза, ТекстОшибки, Отказ)
	
	Результат = Неопределено;
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаявкаНаПеревозкуГруза, Новый Структура("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", "ВидПеревозки.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки"));
	
	Если сткРеквизиты.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|ВЫБРАТЬ
		|	УпПлановыеЭтапыМультимодальнойПеревозки_СрезПослед.*
		|ИЗ
		|	РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(
		|		,
		|		ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза) КАК УпПлановыеЭтапыМультимодальнойПеревозки_СрезПослед
		|ГДЕ НЕ УпПлановыеЭтапыМультимодальнойПеревозки_СрезПослед.Отменен
		|";

		;
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
		Результат = Запрос.Выполнить().Выгрузить(); 
		
	Иначе	
		Результат = СформироватьЭтапыПеревозки(ЗаявкаНаПеревозкуГруза,ТекстОшибки, Отказ);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Этапы мультимодальной перевозки
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза	 - заявка на перевозку груза.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - расшифровка этапов с точностью до грузового места, колонки соответствуют структуре РС.упПлановыеЭтапыПеревозки.
//
Функция СформироватьЭтапыПеревозки(ЗаявкаНаПеревозкуГруза, ТекстОшибки, Отказ) Экспорт
	
	РезультатТаблица = СтруктураТаблицыЭтапыПеревозки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЭтапыПеревозки();
	Запрос.УстановитьПараметр("Ссылка", ЗаявкаНаПеревозкуГруза);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	Если Не РезультатЗапроса[КоличествоТаблиц - 2].Пустой() Тогда
		 		
		ВыгрузкаГрузовыеМеста = РезультатЗапроса[КоличествоТаблиц - 1].Выгрузить();
	
		дрвДокументы = РезультатЗапроса[КоличествоТаблиц - 2].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
				
		ФормироватьВременныеОкнаПоГрафикамРаботыАдресов	= Ложь;
	
		тбзГрафикиРабот = Новый ТаблицаЗначений; // интервалы временных окон с графиками
		тбзГрафикиРабот.Колонки.Добавить("График", Новый ОписаниеТипов("СправочникСсылка.Календари"));
		тбзГрафикиРабот.Колонки.Добавить("ДатаС",  Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
		тбзГрафикиРабот.Колонки.Добавить("ДатаПо", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));								  
										  
		мсвГрафикиРабот = Новый Массив; // все графики задействованные в схемах и заявке
		ДатаНачалаРасписаний = '39991231';   // наименьшая дата для формирования расписаний по графикам
		ДатаОкончанияРасписаний 	= '00010101';   // наибольшая дата для формирования расписаний по графикам								  
		
		ОпределятьВременныеОкнаЗаданий = Ложь;
		
		// Рассчитываются расстояния, время прохождения, и временные окона, заполняется таблица интервалов работ по графикам.
		Для каждого ПризнакВозвратаПоЦепиПоставокСтрока Из дрвДокументы.Строки Цикл
			
			Для каждого СхемаСтрока Из ПризнакВозвратаПоЦепиПоставокСтрока.Строки Цикл
				
				ОтправлениеГрузаС = '00010101';
				ОтправлениеГрузаПо = '00010101';
				ПолучениеГрузаС = '00010101';
				ПолучениеГрузаПо = '00010101';
				ОбщееВремяВПути = 0;
				РассчитыватьВременныеОкна = СхемаСтрока.РассчитыватьВременныеОкна > 0; 
				ОпределятьВременныеОкнаЗаданий = ОпределятьВременныеОкнаЗаданий Или СхемаСтрока.ОпределятьВременныеОкнаЗаданий;
				
				ФормироватьВременныеОкнаПоГрафикамРаботыАдресов = Ложь
														Или ФормироватьВременныеОкнаПоГрафикамРаботыАдресов
														Или (Истина
															И РассчитыватьВременныеОкна 
															И СхемаСтрока.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов > 0);
																
				ВремяПолученияГрузаСПоНомеруЦепи = Новый Соответствие; // Для случая когда для одного звена цепи формируется несколько заданий.
															// Например, при возврате тары.
				ТекстЛог = "";
				ЭтоМультимодальнаяПеревозка = Ложь;
				Для каждого ЗаданиеСтрока Из СхемаСтрока.Строки Цикл
					
					ЗаявкаНаПеревозкуГрузаАдресПолучения = ЗаданиеСтрока.ЗаявкаНаПеревозкуГрузаАдресПолучения;
					
					Если ЗаданиеСтрока.ЕстьНарушенияПоСхеме Тогда
						Отказ = Истина;
						ТекстОшибки = СтрШаблон(НСтр("ru = 'В документе %1 некорректно подобрана цепь поставки'"), ЗаданиеСтрока.ЗаявкаНаПеревозкуГруза);
						Прервать;
					КонецЕсли; 
					
					ВремяВПути                 = 0;
					ВремяПрохожденияТочки      = 0;
					ВремяВПутиСхемыПеревозки   = 0;
					Расстояние                 = 0;
					ВремяВПутиПоКоординатам    = 0;
					ВремяВПутиПоМаршрутизатору = 0;
					ВремяНахожденияВТочке      = ЗаданиеСтрока.ВремяНахожденияВТочке;
				
					// Если это мультимодальная перевозка.
					Если ЗаданиеСтрока.НомерВЦепиПоставок > 0 Тогда
						
						ЭтоМультимодальнаяПеревозка = Истина;
						
						Если РассчитыватьВременныеОкна Тогда
							
							// Если это первое звено поставки.
							Если ЗаданиеСтрока.НомерВЦепиПоставок = 1 Тогда
								ОтправлениеГрузаС	= ЗаданиеСтрока.ОтправлениеГрузаС;
								ОтправлениеГрузаПо	= ЗаданиеСтрока.ОтправлениеГрузаПо;
							Иначе
								
								ВремяПолучениеГрузаПредыдущегоЗвена = ВремяПолученияГрузаСПоНомеруЦепи.Получить(ЗаданиеСтрока.НомерВЦепиПоставок - 1);
								Если НЕ ВремяПолучениеГрузаПредыдущегоЗвена = Неопределено Тогда
									ПолучениеГрузаС = ВремяПолучениеГрузаПредыдущегоЗвена;
								Иначе
									ВремяПолученияГрузаСПоНомеруЦепи.Вставить(ЗаданиеСтрока.НомерВЦепиПоставок - 1, ПолучениеГрузаС);	
								КонецЕсли;
								
								Если ЗначениеЗаполнено(ОтправлениеГрузаС) Тогда
									ОтправлениеГрузаС	= ПолучениеГрузаС + ВремяНахожденияВТочке * 3600;
								КонецЕсли;		
								Если ЗначениеЗаполнено(ОтправлениеГрузаПо) Тогда
									ОтправлениеГрузаПо	= ПолучениеГрузаПо + ВремяНахожденияВТочке  * 3600;
								КонецЕсли;		
							КонецЕсли;	
							
							ПолучениеГрузаС  = ОтправлениеГрузаС;
							ПолучениеГрузаПо = ОтправлениеГрузаПо;
							
							Если ЗначениеЗаполнено(ПолучениеГрузаС)
								ИЛИ ЗначениеЗаполнено(ПолучениеГрузаПо) Тогда
								
								// Увеличивается на время прохождения точки отправления(зависит от вида адреса).
								ВремяПрохожденияТочки = ЗаданиеСтрока.ВремяПрохожденияТочки;
								ВремяВПути = ВремяВПути + ВремяПрохожденияТочки; 
								
								// Если в схеме задано время в пути, то увеличивается на него.(Между первой и промежуточной 
								// и между промежуточными точками время хранится в промежуточной точке, между промежуточной 
								// и конечной точкой - время хранится в конечной точке)	
								Если НЕ ЗаданиеСтрока.ВремяВПути = 0 Тогда
									
									ВремяВПутиСхемыПеревозки = ЗаданиеСтрока.ВремяВПути;
									ВремяВПути = ВремяВПути + ЗаданиеСтрока.ВремяВПути * 3600; 	
									
								// Иначе рассчитывается время на путь между адресами отправления и получения.
								Иначе	
									
									мсвТочек = Новый Массив;
									мсвТочек.Добавить(ЗаданиеСтрока.АдресОтправления);
									мсвТочек.Добавить(ЗаданиеСтрока.АдресПолучения);
									// Анализируется регистр "упРасстоянияМеждуТочками" или рассчитывается маршрутизатором.
									мсвРезультаты = упМаршрутизация.ПолучитьРасстояниеИВремяМассиваТочек(мсвТочек);	
									
									Для каждого СтрокаРасстояниеМеждуАдресами Из мсвРезультаты Цикл
										
										ВремяВПутиПоМаршрутизатору	= СтрокаРасстояниеМеждуАдресами.ВремяМаршрута;
										ВремяВПутиПоМаршрутизатору	= ?(ВремяВПутиПоМаршрутизатору = Неопределено, 0, ВремяВПутиПоМаршрутизатору);
										
										// Если нет в регистре и не удалось рассчитать.
										Если ВремяВПутиПоМаршрутизатору = 0 Тогда
											
											// Расстояние рассчитывается по формулам, используются координаты точек отправления и получения.
											Расстояние = упРаботаСКартамиКлиентСервер.ПолучитьРасстояниеМеждуТочками(СтрокаРасстояниеМеждуАдресами.НачальнаяШирота, СтрокаРасстояниеМеждуАдресами.НачальнаяДолгота, СтрокаРасстояниеМеждуАдресами.КонечнаяШирота, СтрокаРасстояниеМеждуАдресами.КонечнаяДолгота)/1000;
											
											// Время рассчитывается приближенно, из расчета, что скорость движения составляет 60 км/ч.
											ВремяВПутиПоКоординатам	= Расстояние / 60;
											ВремяВПути = ВремяВПути + ВремяВПутиПоКоординатам * 3600; 
											
										Иначе	
											ВремяВПути = ВремяВПути + ВремяВПутиПоМаршрутизатору; 	
										КонецЕсли;
									КонецЦикла;	
								КонецЕсли;
							КонецЕсли;
							
							Если ЗначениеЗаполнено(ПолучениеГрузаС) Тогда
								ПолучениеГрузаС = ПолучениеГрузаС + ВремяВПути;
							КонецЕсли;
							
							Если ЗначениеЗаполнено(ПолучениеГрузаПо) Тогда
								ПолучениеГрузаПо = ПолучениеГрузаПо + ВремяВПути;
							КонецЕсли;
							
							ОбщееВремяВПути = ОбщееВремяВПути + ВремяВПути;
							
						КонецЕсли;
												
					// Если это не мультимодальная перевозка.	
					ИначеЕсли ЗаданиеСтрока.НомерВЦепиПоставок = 0 Тогда
						ОтправлениеГрузаС	= ЗаданиеСтрока.ОтправлениеГрузаС;
						ОтправлениеГрузаПо	= ЗаданиеСтрока.ОтправлениеГрузаПо;
						ПолучениеГрузаС 	= ЗаданиеСтрока.ПолучениеГрузаС;
						ПолучениеГрузаПо	= ЗаданиеСтрока.ПолучениеГрузаПо;
					КонецЕсли;
														
					ТекстАдреса = НСтр("ru = 'Звено %1. 
									|Время нахождения в точке отправления				%8ч.
									|c %3	по	%4	%2
									|с %6	по	%7	%5
									|
									|Расчет времени в звене %1:
									|'");
					
					ТекстАдреса = СтрШаблон(ТекстАдреса, 
										ЗаданиеСтрока.НомерВЦепиПоставок,
										ЗаданиеСтрока.АдресОтправления,
										ОтправлениеГрузаС,
										ОтправлениеГрузаПо,
										ЗаданиеСтрока.АдресПолучения,
										ПолучениеГрузаС,
										ПолучениеГрузаПо,
										ВремяНахожденияВТочке);
					ТекстВремя		= НСтр("ru = 'Время прохождение точки отправления(вид адреса)         %1ч.
											   |Время в пути в схеме перевозки(точка отправления)       %2ч.
											   |Время в пути по маршрутизатору                          %5ч.
											   |Время в пути по координатам(расстояние:%4км)            %3ч.
											   |------------------
											   |Всего время в пути                                      %6ч.
											   |************************************************************************************************
											   |'");

					ТекстВремя = СтрШаблон(ТекстВремя,
										Формат(Окр(ВремяПрохожденияТочки / 3600,2), "ЧЦ=5; ЧДЦ=2; ЧН=; ЧВН="), 
										Формат(ВремяВПутиСхемыПеревозки, "ЧЦ=5; ЧДЦ=2; ЧН=; ЧВН="),
										Формат(ВремяВПутиПоКоординатам, "ЧЦ=5; ЧДЦ=2; ЧН=; ЧВН="),
										Расстояние,
										Формат(Окр(ВремяВПутиПоМаршрутизатору / 3600,2), "ЧЦ=5; ЧДЦ=2; ЧН=; ЧВН="),
										Формат(Окр(ВремяВПути / 3600,2), "ЧЦ=5; ЧДЦ=2; ЧН=; ЧВН="));
					ТекстЛог = ТекстЛог + ТекстАдреса + ТекстВремя; 		
					
					ЗаданиеСтрока.ОбщееВремяВПути = ОбщееВремяВПути;
					
					Если Истина
						И РассчитыватьВременныеОкна
						И ЗаданиеСтрока.НомерВЦепиПоставок > 0
						И ЗаданиеСтрока.ПоследняяЦепьПоставки 
					Тогда
						
						Если Истина
							И ЗаявкаНаПеревозкуГрузаАдресПолучения = ЗаданиеСтрока.АдресПолучения
							И ПолучениеГрузаС > ЗаданиеСтрока.ПолучениеГрузаПо 
						Тогда
							Отказ = Истина;
							ТекстОшибки = НСтр("ru = 'Расчетное временное окно получения (с %1) на последнем плече превышает временное окно получения в заявке (с %2 по %3). Адрес отправления: %4, адрес получения: %5.'");
							ТекстОшибки = СтрШаблон(ТекстОшибки, 
											    ПолучениеГрузаС, 
											    ЗаданиеСтрока.ПолучениеГрузаС, 
											    ЗаданиеСтрока.ПолучениеГрузаПо, 
											    ЗаданиеСтрока.АдресОтправления, 
											    ЗаданиеСтрока.АдресПолучения);
							Прервать;
						КонецЕсли;						
						
						ПолучениеГрузаС  = ЗаданиеСтрока.ПолучениеГрузаС;
						ПолучениеГрузаПо = ЗаданиеСтрока.ПолучениеГрузаПо;

					КонецЕсли;
					 					
					ЗаданиеСтрока.ОтправлениеГрузаС  = ОтправлениеГрузаС;	
					ЗаданиеСтрока.ОтправлениеГрузаПо = ОтправлениеГрузаПо;
					ЗаданиеСтрока.ПолучениеГрузаС    = ПолучениеГрузаС;
					ЗаданиеСтрока.ПолучениеГрузаПо   = ПолучениеГрузаПо;	
					
					упУправленияЗаявками.ДобавитьСтрокуВТаблицуГрафикиРабот(тбзГрафикиРабот, 
																 ЗаданиеСтрока.ГрафикРаботыАдресаОтправления, 
																 ЗаданиеСтрока.ОтправлениеГрузаС,
																 ЗаданиеСтрока.ОтправлениеГрузаПо,
																 ДатаНачалаРасписаний,
																 ДатаОкончанияРасписаний,
																 мсвГрафикиРабот);
																 
					упУправленияЗаявками.ДобавитьСтрокуВТаблицуГрафикиРабот(тбзГрафикиРабот, 
																 ЗаданиеСтрока.ГрафикРаботыАдресаПолучения, 
																 ЗаданиеСтрока.ПолучениеГрузаС,
																 ЗаданиеСтрока.ПолучениеГрузаПо,
																 ДатаНачалаРасписаний,
																 ДатаОкончанияРасписаний,
																 мсвГрафикиРабот);

				КонецЦикла;
				
				Если ЭтоМультимодальнаяПеревозка Тогда
					ЗаписьЖурналаРегистрации(СобытиеФормированиеЗаданий(), ?(Отказ,УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация), Метаданные.Документы.упЗаданиеНаПеревозкуГруза, ЗаявкаНаПеревозкуГруза, ТекстЛог);	
				КонецЕсли;
				
				Если Отказ Тогда 
					Прервать;
				КонецЕсли;
					
			КонецЦикла;
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		
		КонецЦикла;
		
		Если Истина
			И НЕ Отказ 
			И ОпределятьВременныеОкнаЗаданий
		Тогда
			// Заполняются временные окна в точках с учетом расписаний по графикам работ.		
			Если ФормироватьВременныеОкнаПоГрафикамРаботыАдресов Тогда
			
				тбзГрафикиРаботСРасписанием = упУправленияЗаявками.БлижайшиеОкнаРасписанийДляЗаданныхИнтервалов(тбзГрафикиРабот, ДатаНачалаРасписаний, ДатаОкончанияРасписаний, мсвГрафикиРабот);
							
				Если Истина
					И НЕ тбзГрафикиРаботСРасписанием = Неопределено
					И тбзГрафикиРаботСРасписанием.Количество() > 0 
				Тогда
					Для каждого ПризнакВозвратаПоЦепиПоставокСтрока Из дрвДокументы.Строки Цикл
						Для каждого СхемаСтрока Из ПризнакВозвратаПоЦепиПоставокСтрока.Строки Цикл
							Для каждого ЗаданиеСтрока Из СхемаСтрока.Строки Цикл
								
								// Добавляются ограничения по расписаниям работы
								упУправленияЗаявками.ЗаполнитьОграниченияВременныхОконПоРасписаниям(ЗаданиеСтрока, 
																						Истина,
																						тбзГрафикиРаботСРасписанием,
																						ЗаданиеСтрока.ГрафикРаботыАдресаОтправления,
																						ЗаданиеСтрока.ОтправлениеГрузаС,
																						ЗаданиеСтрока.ОтправлениеГрузаПо);
																						
								упУправленияЗаявками.ЗаполнитьОграниченияВременныхОконПоРасписаниям(ЗаданиеСтрока, 
																						Ложь,
																						тбзГрафикиРаботСРасписанием,
																						ЗаданиеСтрока.ГрафикРаботыАдресаПолучения,
																						ЗаданиеСтрока.ПолучениеГрузаС,
																						ЗаданиеСтрока.ПолучениеГрузаПо);
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;

				КонецЕсли;

			КонецЕсли;
			
			// Проводится контроль временных окон последнего задания и накладываются ограничения на временные окна.
			Для каждого ПризнакВозвратаПоЦепиПоставокСтрока Из дрвДокументы.Строки Цикл
				Для каждого СхемаСтрока Из ПризнакВозвратаПоЦепиПоставокСтрока.Строки Цикл
					РассчитыватьВременныеОкна = СхемаСтрока.РассчитыватьВременныеОкна > 0; 
					Для каждого ЗаданиеСтрока Из СхемаСтрока.Строки Цикл
						
						ЗаявкаНаПеревозкуГрузаАдресПолучения = ЗаданиеСтрока.ЗаявкаНаПеревозкуГрузаАдресПолучения;
					
						// Проводится контроль последнего задания
						Если Истина
							И РассчитыватьВременныеОкна
							И ЗаданиеСтрока.НомерВЦепиПоставок > 0
							И ЗаданиеСтрока.ПоследняяЦепьПоставки 
						Тогда
							
							// Проконтролировать что полученное ВО получения не превышало ВО получения заявки.
							Если Истина
								И ЗаявкаНаПеревозкуГрузаАдресПолучения = ЗаданиеСтрока.АдресПолучения // Это условие позволит не проверять время у задания по возвратной таре
								И ЗаданиеСтрока.ПолучениеГрузаС >= ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаПо Тогда
								
								Отказ = Истина;
								ТекстОшибки = СтрШаблон(НСтр("ru = 'Общее время в пути %1ч. превышает временное окно получения с %2	по %3'"), Окр(ЗаданиеСтрока.ОбщееВремяВПути / 3600,2), ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаС, ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаПо);
								Прервать;
							КонецЕсли;
							
							Если НЕ Отказ Тогда
								// Проконтролировать корректность заполнения временных окон у последнего задания.
								упЗаданиеНаПеревозку.ПроконтролироватьВременныеОкнаЗадания(ЗаданиеСтрока.ОтправлениеГрузаС, 
																				ЗаданиеСтрока.ОтправлениеГрузаПо,
																				ЗаданиеСтрока.ПолучениеГрузаС,
																				ЗаданиеСтрока.ПолучениеГрузаПо,
																				ТекстОшибки,
																				Отказ);
							КонецЕсли;
							
							Если Отказ Тогда
								ЗаписьЖурналаРегистрации(СобытиеФормированиеЗаданий(), УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаданиеНаПеревозкуГруза, ЗаявкаНаПеревозкуГруза, ТекстОшибки);		
							КонецЕсли;
													
						КонецЕсли;	
						
						упУправленияЗаявками.УстановитьОграничениеНаВременноеОкно(ЗаданиеСтрока, Истина, ЗаданиеСтрока.ВариантОпределенияВременногоОкнаОтправления);
						упУправленияЗаявками.УстановитьОграничениеНаВременноеОкно(ЗаданиеСтрока, Истина, ЗаданиеСтрока.ВариантОпределенияВременногоОкнаПолучения);
											
						Если Отказ Тогда
							Прервать;
						КонецЕсли;					
					КонецЦикла;
					Если Отказ Тогда
						Прервать;
					КонецЕсли;		
				КонецЦикла;
				Если Отказ Тогда
					Прервать;
				КонецЕсли;		
			КонецЦикла;	
		КонецЕсли;
								
		Если НЕ Отказ Тогда
			СчетчикВершин = 1;
			
			сткОтбор = Новый Структура();
			сткОтбор.Вставить("СхемаМультимодальнойПеревозки");
			сткОтбор.Вставить("АдресОтправления");
			сткОтбор.Вставить("АдресПолучения");
			сткОтбор.Вставить("НомерВЦепиПоставок");
			сткОтбор.Вставить("ГрафикРаботыАдресаОтправления");
			сткОтбор.Вставить("ГрафикРаботыАдресаПолучения");
			сткОтбор.Вставить("Отправитель");
			сткОтбор.Вставить("КонтрагентОтправителя");
			сткОтбор.Вставить("КонтактноеЛицоОтправителя");
			сткОтбор.Вставить("Получатель");
			сткОтбор.Вставить("КонтрагентПолучателя");
			сткОтбор.Вставить("КонтактноеЛицоПолучателя");
			сткОтбор.Вставить("ОтправлениеГрузаС");
			сткОтбор.Вставить("ОтправлениеГрузаПо");
			сткОтбор.Вставить("ПолучениеГрузаС");
			сткОтбор.Вставить("ПолучениеГрузаПо");
			
			Для каждого СхемаСтрока Из дрвДокументы.Строки Цикл
				СхемаПодобрана = ЗначениеЗаполнено(СхемаСтрока.СхемаМультимодальнойПеревозки);
				Для каждого ПризнакВозвратаПоЦепиПоставокСтрока Из СхемаСтрока.Строки Цикл
					//Если СхемаПодобрана Тогда
					//	СчетчикВершин = СчетчикВершин + 1;
					//КонецЕсли; 
					
					Для каждого ЗаданиеСтрока Из ПризнакВозвратаПоЦепиПоставокСтрока.Строки Цикл
						ЗаполнитьЗначенияСвойств(сткОтбор, ЗаданиеСтрока);
						сткОтбор.ОтправлениеГрузаС = ЗаданиеСтрока.ГрузовоеМестоОтправлениеГрузаС;
						сткОтбор.ОтправлениеГрузаПо = ЗаданиеСтрока.ГрузовоеМестоОтправлениеГрузаПо;
						сткОтбор.ПолучениеГрузаС = ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаС;
						сткОтбор.ПолучениеГрузаПо = ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаПо;
						
						ГрузыПоПараметрам = ВыгрузкаГрузовыеМеста.НайтиСтроки(сткОтбор);
						Если ГрузыПоПараметрам.Количество() Тогда
							Для каждого ГрузовоеМесто Из ГрузыПоПараметрам Цикл
								НоваяСтрока = РезультатТаблица.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗаданиеСтрока);
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ГрузовоеМесто,,"ОтправлениеГрузаС, ОтправлениеГрузаПо, ПолучениеГрузаС, ПолучениеГрузаПо");
								
								Если НоваяСтрока.НомерВЦепиПоставок = 0 И ЗаданиеСтрока.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки Тогда
									НоваяСтрока.НомерВЦепиПоставок = СчетчикВершин;
									НоваяСтрока.НомерВЦепиПоставокКонец = СчетчикВершин + 1;
								ИначеЕсли НоваяСтрока.НомерВЦепиПоставок > 0 Тогда
									НоваяСтрока.НомерВЦепиПоставок = СчетчикВершин;
									НоваяСтрока.НомерВЦепиПоставокКонец = СчетчикВершин + 1;	
								КонецЕсли; 
								
								Если Истина
									И НоваяСтрока.АдресОтправления = НоваяСтрока.АдресПолучения
									И Не ЗначениеЗаполнено(НоваяСтрока.Отправитель) 
									И Не ЗначениеЗаполнено(НоваяСтрока.КонтрагентОтправителя) 
								Тогда
									НоваяСтрока.Отправитель = НоваяСтрока.Получатель;
									НоваяСтрока.КонтрагентОтправителя = НоваяСтрока.КонтрагентПолучателя;
								КонецЕсли;
							КонецЦикла; 
							
						Иначе
							Продолжить;
						КонецЕсли;
						
						СчетчикВершин = СчетчикВершин + 1;
						Если Не СхемаПодобрана Тогда
							СчетчикВершин = СчетчикВершин + 1;
						КонецЕсли;
					КонецЦикла;
					
					Если СхемаПодобрана Тогда
						СчетчикВершин = СчетчикВершин + 1;
					КонецЕсли;
					
					Если Отказ Тогда
						Прервать;
					КонецЕсли;
						
				КонецЦикла;
				
				Если Отказ Тогда
					Прервать;
				КонецЕсли;
			
			КонецЦикла;
			
			
			
			
			
			
		//	СчетчикВершин = 0;
		//	
		//	Для каждого ПризнакВозвратаПоЦепиПоставокСтрока Из дрвДокументы.Строки Цикл
		//		
		//		Для каждого СхемаСтрока Из ПризнакВозвратаПоЦепиПоставокСтрока.Строки Цикл
		//			СчРебер = 0;
		//			
		//			Для каждого ЗаданиеСтрока Из СхемаСтрока.Строки Цикл
		//				ПараметрыЗадания = Новый Структура();
		//				ПараметрыЗадания.Вставить("СхемаМультимодальнойПеревозки");
		//				ПараметрыЗадания.Вставить("АдресОтправления");
		//				ПараметрыЗадания.Вставить("АдресПолучения");
		//				ПараметрыЗадания.Вставить("НомерВЦепиПоставок");
		//				ПараметрыЗадания.Вставить("ГрафикРаботыАдресаОтправления");
		//				ПараметрыЗадания.Вставить("ГрафикРаботыАдресаПолучения");
		//				ПараметрыЗадания.Вставить("Отправитель");
		//				ПараметрыЗадания.Вставить("КонтрагентОтправителя");
		//				ПараметрыЗадания.Вставить("КонтактноеЛицоОтправителя");
		//				ПараметрыЗадания.Вставить("Получатель");
		//				ПараметрыЗадания.Вставить("КонтрагентПолучателя");
		//				ПараметрыЗадания.Вставить("КонтактноеЛицоПолучателя");
		//				
		//				ЗаполнитьЗначенияСвойств(ПараметрыЗадания, ЗаданиеСтрока);
		//				
		//				ПараметрыЗадания.Вставить("ОтправлениеГрузаС",  ЗаданиеСтрока.ГрузовоеМестоОтправлениеГрузаС);
		//				ПараметрыЗадания.Вставить("ОтправлениеГрузаПо", ЗаданиеСтрока.ГрузовоеМестоОтправлениеГрузаПо);
		//				ПараметрыЗадания.Вставить("ПолучениеГрузаС",    ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаС);
		//				ПараметрыЗадания.Вставить("ПолучениеГрузаПо",   ЗаданиеСтрока.ГрузовоеМестоПолучениеГрузаПо);
		//				
		//				ГрузыПоПараметрам = ВыгрузкаГрузовыеМеста.НайтиСтроки(ПараметрыЗадания);
		//				
		//				Если ГрузыПоПараметрам.Количество() Тогда
		//					Для каждого ГрузовоеМесто Из ГрузыПоПараметрам Цикл
		//						НоваяСтрока = РезультатТаблица.Добавить();
		//						ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗаданиеСтрока);
		//						ЗаполнитьЗначенияСвойств(НоваяСтрока, ГрузовоеМесто,,"ОтправлениеГрузаС, ОтправлениеГрузаПо, ПолучениеГрузаС, ПолучениеГрузаПо");
		//						
		//						Если НоваяСтрока.НомерВЦепиПоставок > 0 Тогда
		//							//Если ГрузовоеМесто.БезСхемы Тогда
		//							//	НоваяСтрока.НомерВЦепиПоставок = НоваяСтрока.НомерВЦепиПоставок + СчетчикВершин + СчРебер;
		//							//	НоваяСтрока.НомерВЦепиПоставокКонец = НоваяСтрока.НомерВЦепиПоставокКонец + СчетчикВершин + СчРебер;	
		//							//Иначе
		//								НоваяСтрока.НомерВЦепиПоставок = НоваяСтрока.НомерВЦепиПоставок + СчетчикВершин;
		//								НоваяСтрока.НомерВЦепиПоставокКонец = НоваяСтрока.НомерВЦепиПоставокКонец + СчетчикВершин;	
		//							//КонецЕсли; 
		//							//СчРебер = Макс(СчРебер, НоваяСтрока.НомерВЦепиПоставокКонец);
		//						КонецЕсли; 
		//						
		//						Если Истина
		//							И НоваяСтрока.АдресОтправления = НоваяСтрока.АдресПолучения
		//							И Не ЗначениеЗаполнено(НоваяСтрока.Отправитель) 
		//							И Не ЗначениеЗаполнено(НоваяСтрока.КонтрагентОтправителя) 
		//						Тогда
		//							НоваяСтрока.Отправитель = НоваяСтрока.Получатель;
		//							НоваяСтрока.КонтрагентОтправителя = НоваяСтрока.КонтрагентПолучателя;
		//						КонецЕсли;
		//					КонецЦикла; 
		//					СчРебер = Макс(СчРебер, НоваяСтрока.НомерВЦепиПоставокКонец);
		//					
		//				Иначе
		//					Продолжить;
		//				КонецЕсли;
		//
		//			КонецЦикла;
		//			
		//			СчетчикВершин = СчетчикВершин + СчРебер;
		//			
		//			Если Отказ Тогда
		//				Прервать;
		//			КонецЕсли;
		//				
		//		КонецЦикла;
		//		
		//		Если Отказ Тогда
		//			Прервать;
		//		КонецЕсли;
		//	
		//	КонецЦикла;
		КонецЕсли;
		
		Если Отказ Тогда
			ТекстОшибки = ?(ЗначениеЗаполнено(ТекстОшибки), ТекстОшибки, НСтр("ru = 'При формировании задания на перевозку произошла ошибка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
			ЗаписьЖурналаРегистрации(СобытиеФормированиеЗаданий(), УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаданиеНаПеревозкуГруза,, ТекстОшибки);
		КонецЕсли;
	
	КонецЕсли;
			
	Возврат РезультатТаблица;
	
КонецФункции

Функция СтруктураТаблицыЭтапыПеревозки() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	
	МетаданныеРегистр = Метаданные.РегистрыСведений.упПлановыеЭтапыПеревозки;
	
	Для каждого Строка Из МетаданныеРегистр.Измерения Цикл
		НоваяКолонка = Результат.Колонки.Добавить(Строка.Имя, Строка.Тип);
	КонецЦикла;
	
	Для каждого Строка Из МетаданныеРегистр.Ресурсы Цикл
		НоваяКолонка = Результат.Колонки.Добавить(Строка.Имя, Строка.Тип);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаЭтапыПеревозки()
	
	Результат = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявка.Ссылка КАК Заявка,
	|	упЗаявка.Заказчик КАК Заказчик,
	|	упЗаявка.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	упЗаявка.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	упЗаявка.Соглашение КАК Соглашение,
	|	упЗаявка.Отправитель КАК Отправитель,
	|	упЗаявка.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	упЗаявка.АдресОтправления КАК АдресОтправления,
	|	упЗаявка.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	упЗаявка.Получатель КАК Получатель,
	|	упЗаявка.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	упЗаявка.АдресПолучения КАК АдресПолучения,
	|	упЗаявка.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	упЗаявка.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	упЗаявка.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	упЗаявка.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	упЗаявка.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	упЗаявка.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	упЗаявка.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	упЗаявка.Организация КАК Организация,
	|	упЗаявка.Подразделение КАК Подразделение,
	|	упЗаявка.ОбщийГруз КАК ОбщийГруз,
	|	упЗаявка.ЗонаОтправления КАК ЗонаОтправления,
	|	упЗаявка.ЗонаПолучения КАК ЗонаПолучения,
	|	упЗаявка.ВидПеревозки КАК ВидПеревозки,
	|	упЗаявка.ВидПеревозки.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки
	|		И упЗаявка.ВидПеревозки.ИспользуютсяМультимодальныеПеревозки КАК ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки,
	|	упЗаявка.СхемаМультимодальнойПеревозки КАК Схема,
	|	упИспользуютсяМультимодальныеПеревозки.Значение КАК ИспользуютсяМультимодальныеПеревозки,
	|	ВЫБОР
	|		КОГДА упЗаявка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВводитсяВЗаявкеНаПеревозку,
	|	упЗаявка.ЧасовойПояс КАК ЧасовойПояс
	|ПОМЕСТИТЬ втЗаявкиПодготовка
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявка,
	|	Константа.упИспользуютсяМультимодальныеПеревозки КАК упИспользуютсяМультимодальныеПеревозки
	|ГДЕ упЗаявка.Ссылка = &Ссылка
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявки.ВводитсяВЗаявкеНаПеревозку КАК ВводитсяВЗаявкеНаПеревозку,
	|	втЗаявки.Организация КАК Организация,
	|	втЗаявки.Подразделение КАК Подразделение,
	|	втЗаявки.ВидПеревозки КАК ВидПеревозки,
	|	втЗаявки.Заказчик КАК Заказчик,
	|	втЗаявки.Соглашение КАК Соглашение,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявки.ЗонаОтправления
	|		ИНАЧЕ ВЫБОР
	|			КОГДА НЕ ГрузовыеМеста.АдресОтправления.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|				ТОГДА ГрузовыеМеста.АдресОтправления.ГеографическаяЗона
	|			ИНАЧЕ втЗаявки.ЗонаОтправления
	|		КОНЕЦ
	|	КОНЕЦ КАК ЗонаОтправления,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.АдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявки.ЗонаПолучения
	|		ИНАЧЕ ВЫБОР
	|			КОГДА НЕ ГрузовыеМеста.АдресПолучения.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|				ТОГДА ГрузовыеМеста.АдресПолучения.ГеографическаяЗона
	|			ИНАЧЕ втЗаявки.ЗонаПолучения
	|		КОНЕЦ
	|	КОНЕЦ КАК ЗонаПолучения,
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.Тара КАК Тара,
	|	ГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	ГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	ГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ГрузовыеМеста.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	ГрузовыеМеста.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	ГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	ГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	ГрузовыеМеста.Получатель КАК Получатель,
	|	ГрузовыеМеста.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	ГрузовыеМеста.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ГрузовыеМеста.Отправитель КАК Отправитель,
	|	ГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	ГрузовыеМеста.СхемаМультимодальнойПеревозки КАК Схема,
	|	втЗаявки.ИспользуютсяМультимодальныеПеревозки КАК ИспользуютсяМультимодальныеПеревозки,
	|	СУММА(ГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	|	ГрузовыеМеста.ВозвратнаяТара КАК ВозвратнаяТара,
	|	втЗаявки.ЧасовойПояс КАК ЧасовойПояс,
	|	ГрузовыеМеста.АдресВозврата КАК АдресВозврата
	|ПОМЕСТИТЬ втГрузовыеМеста04
	|ИЗ
	|	втЗаявкиПодготовка КАК втЗаявки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|	ПО втЗаявки.Заявка = ГрузовыеМеста.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	втЗаявки.ВводитсяВЗаявкеНаПеревозку,
	|	втЗаявки.Организация,
	|	втЗаявки.Подразделение,
	|	втЗаявки.ВидПеревозки,
	|	втЗаявки.Заказчик,
	|	втЗаявки.Соглашение,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявки.ЗонаОтправления
	|		ИНАЧЕ ВЫБОР
	|			КОГДА НЕ ГрузовыеМеста.АдресОтправления.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|				ТОГДА ГрузовыеМеста.АдресОтправления.ГеографическаяЗона
	|			ИНАЧЕ втЗаявки.ЗонаОтправления
	|		КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.АдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявки.ЗонаПолучения
	|		ИНАЧЕ ВЫБОР
	|			КОГДА НЕ ГрузовыеМеста.АдресПолучения.ГеографическаяЗона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
	|				ТОГДА ГрузовыеМеста.АдресПолучения.ГеографическаяЗона
	|			ИНАЧЕ втЗаявки.ЗонаПолучения
	|		КОНЕЦ
	|	КОНЕЦ,
	|	ГрузовыеМеста.ГрузовоеМесто,
	|	ГрузовыеМеста.Тара,
	|	ГрузовыеМеста.АдресОтправления,
	|	ГрузовыеМеста.АдресПолучения,
	|	ГрузовыеМеста.ГрафикРаботыАдресаОтправления,
	|	ГрузовыеМеста.ГрафикРаботыАдресаПолучения,
	|	ГрузовыеМеста.ОтправлениеГрузаС,
	|	ГрузовыеМеста.ОтправлениеГрузаПо,
	|	ГрузовыеМеста.ПолучениеГрузаС,
	|	ГрузовыеМеста.ПолучениеГрузаПо,
	|	ГрузовыеМеста.Получатель,
	|	ГрузовыеМеста.КонтрагентПолучателя,
	|	ГрузовыеМеста.КонтактноеЛицоПолучателя,
	|	ГрузовыеМеста.Отправитель,
	|	ГрузовыеМеста.КонтрагентОтправителя,
	|	ГрузовыеМеста.КонтактноеЛицоОтправителя,
	|	ГрузовыеМеста.ВозвратПоЦепиПоставок,
	|	ГрузовыеМеста.СхемаМультимодальнойПеревозки,
	|	втЗаявки.ИспользуютсяМультимодальныеПеревозки,
	|	ГрузовыеМеста.ВозвратнаяТара,
	|	втЗаявки.ЧасовойПояс,
	|	ГрузовыеМеста.АдресВозврата
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазделениеГрузовыхМест.ГрузовоеМесто КАК ГрузовоеМесто,
	|	РазделениеГрузовыхМест.Родитель КАК Родитель,
	|	РазделениеГрузовыхМест.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест05
	|ИЗ
	|	втГрузовыеМеста04 КАК втГрузовыеМеста05Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРазделениеГрузовыхМест.СрезПоследних(
	|		,
	|		) КАК РазделениеГрузовыхМест
	|	ПО РазделениеГрузовыхМест.РодительИсходный = втГрузовыеМеста05Т.ГрузовоеМесто
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРазделениеГрузовыхМестГрузовыеМеста.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест10
	|ИЗ
	|	втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестРодители
	|	ПО втРазделениеГрузовыхМестРодители.Родитель = втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто
	|ГДЕ втРазделениеГрузовыхМестРодители.ГрузовоеМесто ЕСТЬ NULL
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста05Т.АдресВозврата КАК АдресВозврата,
	|	втГрузовыеМеста05Т.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста05Т.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста05Т.ВводитсяВЗаявкеНаПеревозку КАК ВводитсяВЗаявкеНаПеревозку,
	|	втГрузовыеМеста05Т.ВидПеревозки КАК ВидПеревозки,
	|	втГрузовыеМеста05Т.ВозвратнаяТара КАК ВозвратнаяТара,
	|	втГрузовыеМеста05Т.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста05Т.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста05Т.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ЕСТЬNULL(втРазделениеГрузовыхМест10Т.ГрузовоеМесто, втГрузовыеМеста05Т.ГрузовоеМесто) КАК ГрузовоеМесто,
	|	втГрузовыеМеста05Т.Заказчик КАК Заказчик,
	|	втГрузовыеМеста05Т.ЗонаОтправления КАК ЗонаОтправления,
	|	втГрузовыеМеста05Т.ЗонаПолучения КАК ЗонаПолучения,
	|	втГрузовыеМеста05Т.ИспользуютсяМультимодальныеПеревозки КАК ИспользуютсяМультимодальныеПеревозки,
	|	втГрузовыеМеста05Т.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста05Т.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМеста05Т.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втГрузовыеМеста05Т.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втГрузовыеМеста05Т.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втГрузовыеМеста05Т.Организация КАК Организация,
	|	втГрузовыеМеста05Т.Отправитель КАК Отправитель,
	|	втГрузовыеМеста05Т.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втГрузовыеМеста05Т.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втГрузовыеМеста05Т.Подразделение КАК Подразделение,
	|	втГрузовыеМеста05Т.Получатель КАК Получатель,
	|	втГрузовыеМеста05Т.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втГрузовыеМеста05Т.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста05Т.Соглашение КАК Соглашение,
	|	втГрузовыеМеста05Т.Схема КАК Схема,
	|	втГрузовыеМеста05Т.Тара КАК Тара,
	|	втГрузовыеМеста05Т.ЧасовойПояс КАК ЧасовойПояс
	|ПОМЕСТИТЬ втГрузовыеМеста05
	|ИЗ
	|	втГрузовыеМеста04 КАК втГрузовыеМеста05Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест10 КАК втРазделениеГрузовыхМест10Т
	|	ПО втРазделениеГрузовыхМест10Т.РодительИсходный = втГрузовыеМеста05Т.ГрузовоеМесто
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	упСхемы.ВидПеревозки КАК ВидПеревозки,
	|	упСхемы.АдресОтправления КАК СхемаАдресОтправления,
	|	упСхемы.АдресПолучения КАК СхемаАдресПолучения,
	|	упСхемы.ЗонаОтправления КАК СхемаЗонаОтправления,
	|	упСхемы.ЗонаПолучения КАК СхемаЗонаПолучения,
	|	втГрузовыеМеста.ЗонаОтправления КАК ЗонаОтправления,
	|	втГрузовыеМеста.ЗонаПолучения КАК ЗонаПолучения,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	упСхемы.ОпределятьВременныеОкнаЗаданий КАК ОпределятьВременныеОкнаЗаданий,
	|	упСхемы.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	упСхемы.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	упСхемы.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов КАК ФормироватьВременныеОкнаПоГрафикамРаботыАдресов
	|ПОМЕСТИТЬ втСхемыБезОтбораПоАдресамИЗонам
	|ИЗ
	|	втГрузовыеМеста05 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок КАК упСхемы
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.ИспользуютсяМультимодальныеПеревозки
	|		И втГрузовыеМеста.Схема = упСхемы.Ссылка
	|		И НЕ (упСхемы.ПометкаУдаления)
	|		И упСхемы.Активно
	|		И упСхемы.ВидПеревозкиОтбор В (втГрузовыеМеста.ВидПеревозки,
	|				ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка))
	|		И упСхемы.ОрганизацияОтбор В (втГрузовыеМеста.Организация,
	|				ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|		И упСхемы.ЗаказчикОтбор В (втГрузовыеМеста.Заказчик,
	|				ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|		И упСхемы.СоглашениеОтбор В (втГрузовыеМеста.Соглашение,
	|				ЗНАЧЕНИЕ(Справочник.упСоглашенияСКлиентами.ПустаяСсылка))
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСхемы.Схема КАК Схема,
	|	ПромежуточныеТочкиМаршрута.Адрес КАК Адрес,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	ПромежуточныеТочкиМаршрута.ВремяВПути КАК ВремяВПути,
	|	ЕСТЬNULL(упАдреса.ВидАдреса.ВремяПрохожденияТочки, 0) КАК ВремяПрохожденияТочки,
	|	ПромежуточныеТочкиМаршрута.ВидПеревозки КАК ВидПеревозки,
	|	ПромежуточныеТочкиМаршрута.НомерСтроки КАК НомерСтроки,
	|	ПромежуточныеТочкиМаршрута.Отправитель КАК Отправитель,
	|	ПромежуточныеТочкиМаршрута.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ПромежуточныеТочкиМаршрута.Получатель КАК Получатель,
	|	ПромежуточныеТочкиМаршрута.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	ПромежуточныеТочкиМаршрута.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ПромежуточныеТочкиМаршрута.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ПромежуточныеТочкиМаршрута.ЧасовойПояс КАК ЧасовойПояс,
	|	ПромежуточныеТочкиМаршрута.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	ПромежуточныеТочкиМаршрута.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	ПромежуточныеТочкиМаршрута.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке,
	|	ПромежуточныеТочкиМаршрута.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ПромежуточныеТочкиМаршрута.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения
	|ПОМЕСТИТЬ втПромежуточныеТочкиПоСхемам
	|ИЗ
	|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК ПромежуточныеТочкиМаршрута
	|	ПО втСхемы.Схема = ПромежуточныеТочкиМаршрута.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
	|	ПО ПромежуточныеТочкиМаршрута.Адрес = упАдреса.Ссылка
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПромежуточныеТочкиПоСхемам.Схема КАК Схема,
	|	втПромежуточныеТочкиПоСхемам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	МАКСИМУМ(втПромежуточныеТочкиПоСхемам.НомерСтроки) КАК НомерПоследнейТочки
	|ПОМЕСТИТЬ втНомераПоследнихПромежуточныхТочек
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочкиПоСхемам
	|СГРУППИРОВАТЬ ПО
	|	втПромежуточныеТочкиПоСхемам.Схема,
	|	втПромежуточныеТочкиПоСхемам.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Схема КАК Схема,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочки.Адрес.ГеографическаяЗона КАК ЗонаОтправления,
	|	втПромежуточныеТочки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаОтправления
	|ПОМЕСТИТЬ втАдресаОтправленияПоСхемамВозврата
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И втПромежуточныеТочки.НомерСтроки = втНомераПоследнихТочек.НомерПоследнейТочки
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок
	|		И втНомераПоследнихТочек.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемыБезОтбораПоАдресамИЗонам.Схема КАК Схема,
	|	втСхемыБезОтбораПоАдресамИЗонам.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
	|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.АдресОтправления
	|	КОНЕЦ КАК СхемаАдресОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресПолучения
	|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаАдресОтправления
	|	КОНЕЦ КАК СхемаАдресПолучения,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
	|		ИНАЧЕ втАдресаОтправленияПоСхемамВозврата.ЗонаОтправления
	|	КОНЕЦ КАК СхемаЗонаОтправления,
	|	ВЫБОР
	|		КОГДА НЕ втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|			ТОГДА втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаПолучения
	|		ИНАЧЕ втСхемыБезОтбораПоАдресамИЗонам.СхемаЗонаОтправления
	|	КОНЕЦ КАК СхемаЗонаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаОтправления КАК ЗонаОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.ЗонаПолучения КАК ЗонаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.АдресОтправления КАК АдресОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.АдресПолучения КАК АдресПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втСхемыБезОтбораПоАдресамИЗонам.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втСхемыБезОтбораПоАдресамИЗонам.ОпределятьВременныеОкнаЗаданий КАК ОпределятьВременныеОкнаЗаданий,
	|	втСхемыБезОтбораПоАдресамИЗонам.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов КАК ФормироватьВременныеОкнаПоГрафикамРаботыАдресов
	|ПОМЕСТИТЬ втСхемыСАдресамиСУчетомВозвратовПоЦепи
	|ИЗ
	|	втСхемыБезОтбораПоАдресамИЗонам КАК втСхемыБезОтбораПоАдресамИЗонам
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаОтправленияПоСхемамВозврата КАК втАдресаОтправленияПоСхемамВозврата
	|	ПО ИСТИНА
	|		И втСхемыБезОтбораПоАдресамИЗонам.Схема = втАдресаОтправленияПоСхемамВозврата.Схема
	|		И втСхемыБезОтбораПоАдресамИЗонам.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСхемы.АдресОтправления КАК АдресОтправления,
	|	втСхемы.АдресПолучения КАК АдресПолучения,
	|	втСхемы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втСхемы.ВидПеревозки КАК ВидПеревозки,
	|	втСхемы.Схема КАК Схема,
	|	втСхемы.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втСхемы.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втСхемы.ОпределятьВременныеОкнаЗаданий КАК ОпределятьВременныеОкнаЗаданий,
	|	втСхемы.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов КАК ФормироватьВременныеОкнаПоГрафикамРаботыАдресов
	|ПОМЕСТИТЬ втСхемыСОтборомПоАдресам
	|ИЗ
	|	втСхемыСАдресамиСУчетомВозвратовПоЦепи КАК втСхемы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонОтправления
	|	ПО ИСТИНА
	|		И втСхемы.СхемаЗонаОтправления = упИерархияГеографическихЗонОтправления.Родитель
	|		И втСхемы.ЗонаОтправления = упИерархияГеографическихЗонОтправления.Зона
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗонПолучения
	|	ПО ИСТИНА
	|		И втСхемы.СхемаЗонаПолучения = упИерархияГеографическихЗонПолучения.Родитель
	|		И втСхемы.ЗонаПолучения = упИерархияГеографическихЗонПолучения.Зона
	|ГДЕ ИСТИНА
	|	И (ЛОЖЬ
	|		ИЛИ втСхемы.СхемаАдресОтправления = втСхемы.АдресОтправления
	|		ИЛИ втСхемы.СхемаАдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|	И (ЛОЖЬ
	|		ИЛИ втСхемы.СхемаАдресПолучения = втСхемы.АдресПолучения
	|		ИЛИ втСхемы.СхемаАдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка))
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПромежуточныеТочки.Адрес КАК Адрес,
	|	втПромежуточныеТочки.ВидПеревозки КАК ВидПеревозки,
	|	втПромежуточныеТочки.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втПромежуточныеТочки.ВремяВПути КАК ВремяВПути,
	|	втПромежуточныеТочки.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	втПромежуточныеТочки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втПромежуточныеТочки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втПромежуточныеТочки.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втПромежуточныеТочки.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втПромежуточныеТочки.НомерСтроки КАК НомерСтроки,
	|	втПромежуточныеТочки.Отправитель КАК Отправитель,
	|	втПромежуточныеТочки.Получатель КАК Получатель,
	|	втПромежуточныеТочки.Схема КАК Схема,
	|	втНомераПоследнихПромежуточныхТочек.НомерПоследнейТочки - втПромежуточныеТочки.НомерСтроки + 1 КАК НомерСтрокиВЦепиВозврата,
	|	втПромежуточныеТочки.ЧасовойПояс КАК ЧасовойПояс,
	|	втПромежуточныеТочки.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втПромежуточныеТочки.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втПромежуточныеТочки.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке,
	|	втПромежуточныеТочки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втПромежуточныеТочки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения
	|ПОМЕСТИТЬ втПромежуточныеТочки
	|ИЗ
	|	втПромежуточныеТочкиПоСхемам КАК втПромежуточныеТочки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втСхемыСОтборомПоАдресамТ
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втСхемыСОтборомПоАдресамТ.Схема
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок = втСхемыСОтборомПоАдресамТ.ВозвратПоЦепиПоставок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихПромежуточныхТочек
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втНомераПоследнихПромежуточныхТочек.Схема
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок = втНомераПоследнихПромежуточныхТочек.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ВводитсяВЗаявкеНаПеревозку КАК ВводитсяВЗаявкеНаПеревозку1,
	|	втГрузовыеМеста.Организация КАК Организация,
	|	втГрузовыеМеста.Подразделение КАК Подразделение,
	|	втГрузовыеМеста.ВидПеревозки КАК ВидПеревозки,
	|	втГрузовыеМеста.Заказчик КАК Заказчик,
	|	втГрузовыеМеста.Соглашение КАК Соглашение,
	|	втГрузовыеМеста.ЗонаОтправления КАК ЗонаОтправления,
	|	втГрузовыеМеста.ЗонаПолучения КАК ЗонаПолучения,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМеста.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втГрузовыеМеста.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втГрузовыеМеста.Получатель КАК Получатель,
	|	втГрузовыеМеста.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втГрузовыеМеста.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втГрузовыеМеста.Отправитель КАК Отправитель,
	|	втГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.ИспользуютсяМультимодальныеПеревозки КАК ИспользуютсяМультимодальныеПеревозки1,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ВЫБОР
	|		КОГДА втПроверкаСхемы.ВидПеревозки = ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)
	|			ТОГДА втГрузовыеМеста.ВидПеревозки
	|		КОГДА втПроверкаСхемы.ВидПеревозки ЕСТЬ NULL
	|			ТОГДА втГрузовыеМеста.ВидПеревозки
	|		ИНАЧЕ втПроверкаСхемы.ВидПеревозки
	|	КОНЕЦ КАК ВидПеревозкиПоследнейПоставки,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.Схема <> втПроверкаСхемы.Схема
	|				И НЕ втГрузовыеМеста.Схема = ЗНАЧЕНИЕ(Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьНарушенияПоСхеме,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВводитсяВЗаявкеНаПеревозку,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.Схема = втПроверкаСхемы.Схема
	|				И втГрузовыеМеста.ИспользуютсяМультимодальныеПеревозки
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользуютсяМультимодальныеПеревозки,
	|	втГрузовыеМеста.ВозвратнаяТара КАК ВозвратнаяТара,
	|	втГрузовыеМеста.ЧасовойПояс КАК ЧасовойПояс,
	|	втПроверкаСхемы.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втПроверкаСхемы.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втПроверкаСхемы.ОпределятьВременныеОкнаЗаданий КАК ОпределятьВременныеОкнаЗаданий,
	|	втПроверкаСхемы.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов КАК ФормироватьВременныеОкнаПоГрафикамРаботыАдресов,
	|	втГрузовыеМеста.АдресВозврата КАК АдресВозврата,
	|	втГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок
	|ПОМЕСТИТЬ втГрузовыеМеста10
	|ИЗ
	|	втГрузовыеМеста05 КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСхемыСОтборомПоАдресам КАК втПроверкаСхемы
	|	ПО ИСТИНА
	|		И втГрузовыеМеста.Схема = втПроверкаСхемы.Схема
	|		И втГрузовыеМеста.ВозвратПоЦепиПоставок = втПроверкаСхемы.ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.ИспользуютсяМультимодальныеПеревозки КАК ИспользуютсяМультимодальныеПеревозки,
	|	втГрузовыеМеста.ВидПеревозкиПоследнейПоставки КАК ВидПеревозкиПоследнейПоставки,
	|	втГрузовыеМеста.ЕстьНарушенияПоСхеме КАК ЕстьНарушенияПоСхеме,
	|	втГрузовыеМеста.ВводитсяВЗаявкеНаПеревозку КАК ВводитсяВЗаявкеНаПеревозку,
	|	втГрузовыеМеста.ИспользуютсяМультимодальныеПеревозки КАК ИспользуютсяМультимодальныеПеревозки1,
	|	втГрузовыеМеста.ОпределятьВременныеОкнаЗаданий КАК ОпределятьВременныеОкнаЗаданий,
	|	втГрузовыеМеста.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов КАК ФормироватьВременныеОкнаПоГрафикамРаботыАдресов
	|ПОМЕСТИТЬ втПараметрыСхемы
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|;
	|//{Запрос: 14 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втПромежуточныеТочки.НомерСтроки КАК НомерВЦепиПоставок,
	|	втПромежуточныеТочки.НомерСтроки + 1 КАК НомерВЦепиПоставокКонец,
	|	втПромежуточныеТочки.ВидПеревозки КАК ВидПеревозки,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	ЕСТЬNULL(упАдреса.ВидАдреса.ВремяПрохожденияТочки, 0) КАК ВремяПрохожденияТочки,
	|	втПромежуточныеТочки.ВремяВПути КАК ВремяВПути,
	|	втГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втГрузовыеМеста.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втПромежуточныеТочки.Адрес КАК АдресПолучения,
	|	втПромежуточныеТочки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаПо,
	|	втГрузовыеМеста.Отправитель КАК Отправитель,
	|	втГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втПромежуточныеТочки.Получатель КАК Получатель,
	|	втПромежуточныеТочки.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втПромежуточныеТочки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЛОЖЬ КАК ПоследняяЦепьПоставки,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втПромежуточныеТочки.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста.ЧасовойПояс КАК ЧасовойПояс,
	|	втГрузовыеМеста.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втПромежуточныеТочки.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	0 КАК ВремяНахожденияВТочке
	|ПОМЕСТИТЬ втМодальныеПеревозкиГрузов
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И втПромежуточныеТочки.НомерСтроки = 1
	|		И НЕ (втПромежуточныеТочки.ВозвратПоЦепиПоставок)
	|		И НЕ (втГрузовыеМеста.ЕстьНарушенияПоСхеме)
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
	|	ПО втГрузовыеМеста.АдресОтправления = упАдреса.Ссылка
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втПромежуточныеТочкиОкончание.НомерСтроки КАК НомерВЦепиПоставок,
	|	втПромежуточныеТочкиОкончание.НомерСтроки + 1 КАК НомерВЦепиПоставокКонец,
	|	втПромежуточныеТочкиОкончание.ВидПеревозки КАК ВидПеревозки,
	|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочкиНачало.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	втПромежуточныеТочкиОкончание.ВремяВПути КАК ВремяВПути,
	|	втПромежуточныеТочкиНачало.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаПо,
	|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения,
	|	втПромежуточныеТочкиОкончание.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаПо,
	|	втПромежуточныеТочкиНачало.Отправитель КАК Отправитель,
	|	втПромежуточныеТочкиНачало.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втПромежуточныеТочкиОкончание.Получатель КАК Получатель,
	|	втПромежуточныеТочкиОкончание.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втПромежуточныеТочкиНачало.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втПромежуточныеТочкиОкончание.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЛОЖЬ КАК ПоследняяЦепьПоставки,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втПромежуточныеТочкиНачало.ЧасовойПояс КАК ЧасовойПояс,
	|	втПромежуточныеТочкиНачало.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втПромежуточныеТочкиОкончание.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втПромежуточныеТочкиНачало.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
	|	ПО ИСТИНА
	|		И втПромежуточныеТочкиОкончание.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочкиОкончание.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И НЕ (втПромежуточныеТочкиОкончание.ВозвратПоЦепиПоставок)
	|		И НЕ (втГрузовыеМеста.ЕстьНарушенияПоСхеме)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
	|	ПО ИСТИНА
	|		И втПромежуточныеТочкиНачало.НомерСтроки = втПромежуточныеТочкиОкончание.НомерСтроки - 1
	|		И втПромежуточныеТочкиНачало.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И НЕ (втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втПромежуточныеТочки.НомерСтроки + 1 КАК НомерВЦепиПоставок,
	|	втПромежуточныеТочки.НомерСтроки + 2 КАК НомерВЦепиПоставокКонец,
	|	ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка) КАК ВидПеревозки,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочки.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	упСхемыМультимодальныхПеревозок.ВремяВПутиДоКонечнойТочки КАК ВремяВПути,
	|	втПромежуточныеТочки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаПо,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.ВозвратнаяТара
	|			ТОГДА ВЫБОР
	|				КОГДА НЕ втГрузовыеМеста.АдресВозврата = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|					ТОГДА втГрузовыеМеста.АдресВозврата
	|				ИНАЧЕ втПромежуточныеТочки.Адрес
	|			КОНЕЦ
	|		ИНАЧЕ втГрузовыеМеста.АдресПолучения
	|	КОНЕЦ КАК АдресПолучения,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.ВозвратнаяТара
	|			ТОГДА ВЫБОР
	|				КОГДА НЕ втГрузовыеМеста.АдресВозврата = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|				ИНАЧЕ втПромежуточныеТочки.ГрафикРаботыАдресаОтправления
	|			КОНЕЦ
	|		ИНАЧЕ втГрузовыеМеста.ГрафикРаботыАдресаПолучения
	|	КОНЕЦ КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втПромежуточныеТочки.Отправитель КАК Отправитель,
	|	втПромежуточныеТочки.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втГрузовыеМеста.Получатель КАК Получатель,
	|	втГрузовыеМеста.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втПромежуточныеТочки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМеста.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ИСТИНА КАК ПоследняяЦепьПоставки,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втПромежуточныеТочки.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втПромежуточныеТочки.ЧасовойПояс КАК ЧасовойПояс,
	|	втПромежуточныеТочки.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втГрузовыеМеста.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втПромежуточныеТочки.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И НЕ (втПромежуточныеТочки.ВозвратПоЦепиПоставок)
	|		И НЕ (втГрузовыеМеста.ЕстьНарушенияПоСхеме)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераПоследнихПромежуточныхТочек КАК втНомераПоследнихТочек
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втНомераПоследнихТочек.Схема
	|		И втПромежуточныеТочки.НомерСтроки = втНомераПоследнихТочек.НомерПоследнейТочки
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок = втНомераПоследнихТочек.ВозвратПоЦепиПоставок
	|		И НЕ (втПромежуточныеТочки.ВозвратПоЦепиПоставок)
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок КАК упСхемыМультимодальныхПеревозок
	|	ПО упСхемыМультимодальныхПеревозок.Ссылка = втПромежуточныеТочки.Схема
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втПромежуточныеТочкиНачало.НомерСтрокиВЦепиВозврата КАК НомерВЦепиПоставок,
	|	втПромежуточныеТочкиНачало.НомерСтрокиВЦепиВозврата + 1 КАК НомерВЦепиПоставокКонец,
	|	втПромежуточныеТочкиНачало.ВидПеревозки КАК ВидПеревозки,
	|	втПромежуточныеТочкиНачало.Адрес КАК АдресОтправления,
	|	втПромежуточныеТочкиНачало.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	втПромежуточныеТочкиОкончание.ВремяВПути КАК ВремяВПути,
	|	втПромежуточныеТочкиНачало.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаОтправления,
	|	ВЫБОР
	|		КОГДА втПромежуточныеТочкиНачало.НомерСтрокиВЦепиВозврата > 1
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ втГрузовыеМеста.ОтправлениеГрузаС
	|	КОНЕЦ КАК ОтправлениеГрузаС,
	|	ВЫБОР
	|		КОГДА втПромежуточныеТочкиНачало.НомерСтрокиВЦепиВозврата > 1
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ втГрузовыеМеста.ОтправлениеГрузаПо
	|	КОНЕЦ КАК ОтправлениеГрузаПо,
	|	втПромежуточныеТочкиОкончание.Адрес КАК АдресПолучения,
	|	втПромежуточныеТочкиОкончание.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаПолучения,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаПо,
	|	втПромежуточныеТочкиНачало.Отправитель КАК Отправитель,
	|	втПромежуточныеТочкиНачало.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втПромежуточныеТочкиОкончание.Получатель КАК Получатель,
	|	втПромежуточныеТочкиОкончание.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втПромежуточныеТочкиНачало.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втПромежуточныеТочкиОкончание.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЛОЖЬ КАК ПоследняяЦепьПоставки,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втПромежуточныеТочкиОкончание.ЧасовойПояс КАК ЧасовойПояс,
	|	втПромежуточныеТочкиНачало.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втПромежуточныеТочкиОкончание.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втПромежуточныеТочкиНачало.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиОкончание
	|	ПО ИСТИНА
	|		И втПромежуточныеТочкиОкончание.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочкиОкончание.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И втПромежуточныеТочкиОкончание.ВозвратПоЦепиПоставок
	|		И НЕ (втГрузовыеМеста.ЕстьНарушенияПоСхеме)
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочкиНачало
	|	ПО ИСТИНА
	|		И втПромежуточныеТочкиНачало.НомерСтроки - 1 = втПромежуточныеТочкиОкончание.НомерСтроки
	|		И втПромежуточныеТочкиНачало.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И втПромежуточныеТочкиНачало.ВозвратПоЦепиПоставок
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втПромежуточныеТочки.НомерСтрокиВЦепиВозврата КАК НомерВЦепиПоставок,
	|	втПромежуточныеТочки.НомерСтрокиВЦепиВозврата + 1 КАК НомерВЦепиПоставокКонец,
	|	втПромежуточныеТочки.ВидПеревозки КАК ВидПеревозки,
	|	втПромежуточныеТочки.Адрес КАК АдресОтправления,
	|	ЕСТЬNULL(упАдреса.ВидАдреса.ВремяПрохожденияТочки, 0) КАК ВремяПрохожденияТочки,
	|	втПромежуточныеТочки.ВремяВПути КАК ВремяВПути,
	|	втПромежуточныеТочки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаОтправления,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаПо,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втПромежуточныеТочки.Получатель КАК Отправитель,
	|	втПромежуточныеТочки.КонтрагентПолучателя КАК КонтрагентОтправителя,
	|	втГрузовыеМеста.Получатель КАК Получатель,
	|	втГрузовыеМеста.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втПромежуточныеТочки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМеста.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ИСТИНА КАК ПоследняяЦепьПоставки,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втПромежуточныеТочки.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста.ЧасовойПояс КАК ЧасовойПояс,
	|	втПромежуточныеТочки.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втГрузовыеМеста.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втПромежуточныеТочки.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПромежуточныеТочки КАК втПромежуточныеТочки
	|	ПО ИСТИНА
	|		И втПромежуточныеТочки.Схема = втГрузовыеМеста.Схема
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок = втГрузовыеМеста.ВозвратПоЦепиПоставок
	|		И втПромежуточныеТочки.НомерСтроки = 1
	|		И втПромежуточныеТочки.ВозвратПоЦепиПоставок
	|		И НЕ (втГрузовыеМеста.ЕстьНарушенияПоСхеме)
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
	|	ПО втГрузовыеМеста.АдресОтправления = упАдреса.Ссылка
	|;
	|//{Запрос: 15 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМодальныеПеревозкиГрузов.Схема КАК Схема,
	|	втМодальныеПеревозкиГрузов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втМодальныеПеревозкиГрузов.Тара КАК Тара,
	|	втМодальныеПеревозкиГрузов.АдресОтправления КАК АдресОтправления,
	|	втМодальныеПеревозкиГрузов.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	втМодальныеПеревозкиГрузов.ВремяВПути КАК ВремяВПути,
	|	втМодальныеПеревозкиГрузов.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втМодальныеПеревозкиГрузов.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втМодальныеПеревозкиГрузов.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втМодальныеПеревозкиГрузов.АдресПолучения КАК АдресПолучения,
	|	втМодальныеПеревозкиГрузов.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втМодальныеПеревозкиГрузов.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втМодальныеПеревозкиГрузов.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втМодальныеПеревозкиГрузов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втМодальныеПеревозкиГрузов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втМодальныеПеревозкиГрузов.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА втМодальныеПеревозкиГрузов.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|				И втМодальныеПеревозкиГрузов.НомерВЦепиПоставок < 2
	|			ТОГДА втЗаявки.Отправитель
	|		ИНАЧЕ втМодальныеПеревозкиГрузов.Отправитель
	|	КОНЕЦ КАК Отправитель,
	|	ВЫБОР
	|		КОГДА втМодальныеПеревозкиГрузов.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|				И втМодальныеПеревозкиГрузов.НомерВЦепиПоставок < 2
	|			ТОГДА втЗаявки.КонтрагентОтправителя
	|		ИНАЧЕ втМодальныеПеревозкиГрузов.КонтрагентОтправителя
	|	КОНЕЦ КАК КонтрагентОтправителя,
	|	ВЫБОР
	|		КОГДА втМодальныеПеревозкиГрузов.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|				И втМодальныеПеревозкиГрузов.ПоследняяЦепьПоставки
	|			ТОГДА втЗаявки.Получатель
	|		ИНАЧЕ втМодальныеПеревозкиГрузов.Получатель
	|	КОНЕЦ КАК Получатель,
	|	ВЫБОР
	|		КОГДА втМодальныеПеревозкиГрузов.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|				И втМодальныеПеревозкиГрузов.ПоследняяЦепьПоставки
	|			ТОГДА втЗаявки.КонтрагентПолучателя
	|		ИНАЧЕ втМодальныеПеревозкиГрузов.КонтрагентПолучателя
	|	КОНЕЦ КАК КонтрагентПолучателя,
	|	ВЫБОР
	|		КОГДА втМодальныеПеревозкиГрузов.КонтактноеЛицоОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|				И втМодальныеПеревозкиГрузов.НомерВЦепиПоставок < 2
	|			ТОГДА втЗаявки.КонтактноеЛицоОтправителя
	|		ИНАЧЕ втМодальныеПеревозкиГрузов.КонтактноеЛицоОтправителя
	|	КОНЕЦ КАК КонтактноеЛицоОтправителя,
	|	ВЫБОР
	|		КОГДА втМодальныеПеревозкиГрузов.КонтактноеЛицоПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|				И втМодальныеПеревозкиГрузов.ПоследняяЦепьПоставки
	|			ТОГДА втЗаявки.КонтактноеЛицоПолучателя
	|		ИНАЧЕ втМодальныеПеревозкиГрузов.КонтактноеЛицоПолучателя
	|	КОНЕЦ КАК КонтактноеЛицоПолучателя,
	|	втМодальныеПеревозкиГрузов.ПоследняяЦепьПоставки КАК ПоследняяЦепьПоставки,
	|	втМодальныеПеревозкиГрузов.КоличествоГрузов КАК КоличествоГрузов,
	|	втМодальныеПеревозкиГрузов.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втМодальныеПеревозкиГрузов.ЧасовойПояс КАК ЧасовойПояс,
	|	втМодальныеПеревозкиГрузов.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втМодальныеПеревозкиГрузов.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втМодальныеПеревозкиГрузов.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке
	|ПОМЕСТИТЬ втАдресаПеревозкиГрузов
	|ИЗ
	|	втМодальныеПеревозкиГрузов КАК втМодальныеПеревозкиГрузов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкиПодготовка КАК втЗаявки
	|	ПО ИСТИНА
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Схема КАК Схема,
	|	втГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	0 КАК ВремяПрохожденияТочки,
	|	0 КАК ВремяВПути,
	|	втГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втГрузовыеМеста.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	0 КАК НомерВЦепиПоставок,
	|	0 КАК НомерВЦепиПоставокКонец,
	|	ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка) КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|			ТОГДА втЗаявки.Отправитель
	|		ИНАЧЕ втГрузовыеМеста.Отправитель
	|	КОНЕЦ КАК Отправитель,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА втЗаявки.КонтрагентОтправителя
	|		ИНАЧЕ втГрузовыеМеста.КонтрагентОтправителя
	|	КОНЕЦ КАК КонтрагентОтправителя,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|			ТОГДА втЗаявки.Получатель
	|		ИНАЧЕ втГрузовыеМеста.Получатель
	|	КОНЕЦ КАК Получатель,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА втЗаявки.КонтрагентПолучателя
	|		ИНАЧЕ втГрузовыеМеста.КонтрагентПолучателя
	|	КОНЕЦ КАК КонтрагентПолучателя,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.КонтактноеЛицоОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|			ТОГДА втЗаявки.КонтактноеЛицоОтправителя
	|		ИНАЧЕ втГрузовыеМеста.КонтактноеЛицоОтправителя
	|	КОНЕЦ КАК КонтактноеЛицоОтправителя,
	|	ВЫБОР
	|		КОГДА втГрузовыеМеста.КонтактноеЛицоПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|			ТОГДА втЗаявки.КонтактноеЛицоПолучателя
	|		ИНАЧЕ втГрузовыеМеста.КонтактноеЛицоПолучателя
	|	КОНЕЦ КАК КонтактноеЛицоПолучателя,
	|	ИСТИНА КАК ПоследняяЦепьПоставки,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ЛОЖЬ КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста.ЧасовойПояс КАК ЧасовойПояс,
	|	втГрузовыеМеста.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втГрузовыеМеста.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	0 КАК ВремяНахожденияВТочке
	|ИЗ
	|	втГрузовыеМеста10 КАК втГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкиПодготовка КАК втЗаявки
	|	ПО ИСТИНА
	|ГДЕ ИСТИНА
	|	И НЕ (втГрузовыеМеста.ГрузовоеМесто В (
	|			ВЫБРАТЬ
	|				втМодальныеПеревозкиГрузов.ГрузовоеМесто КАК ГрузовоеМесто
	|			ИЗ
	|				втМодальныеПеревозкиГрузов КАК втМодальныеПеревозкиГрузов))
	|	И НЕ (втГрузовыеМеста.ЕстьНарушенияПоСхеме)
	|;
	|//{Запрос: 16 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАдресаПеревозкиГрузов.Схема КАК Схема,
	|	втАдресаПеревозкиГрузов.АдресОтправления КАК АдресОтправления,
	|	втАдресаПеревозкиГрузов.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	втАдресаПеревозкиГрузов.ВремяВПути КАК ВремяВПути,
	|	втАдресаПеревозкиГрузов.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втАдресаПеревозкиГрузов.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втАдресаПеревозкиГрузов.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втАдресаПеревозкиГрузов.АдресПолучения КАК АдресПолучения,
	|	втАдресаПеревозкиГрузов.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втАдресаПеревозкиГрузов.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втАдресаПеревозкиГрузов.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втАдресаПеревозкиГрузов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втАдресаПеревозкиГрузов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втАдресаПеревозкиГрузов.ВидПеревозки КАК ВидПеревозки,
	|	втАдресаПеревозкиГрузов.Отправитель КАК Отправитель,
	|	втАдресаПеревозкиГрузов.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втАдресаПеревозкиГрузов.Получатель КАК Получатель,
	|	втАдресаПеревозкиГрузов.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втАдресаПеревозкиГрузов.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втАдресаПеревозкиГрузов.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втАдресаПеревозкиГрузов.ПоследняяЦепьПоставки КАК ПоследняяЦепьПоставки,
	|	втАдресаПеревозкиГрузов.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втАдресаПеревозкиГрузов.ЧасовойПояс КАК ЧасовойПояс,
	|	втАдресаПеревозкиГрузов.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втАдресаПеревозкиГрузов.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втАдресаПеревозкиГрузов.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке
	|ПОМЕСТИТЬ втДокументы05
	|ИЗ
	|	втАдресаПеревозкиГрузов КАК втАдресаПеревозкиГрузов
	|;
	|//{Запрос: 17 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявки.Заявка КАК ЗаявкаНаПеревозкуГруза,
	|	втЗаявки.АдресПолучения КАК ЗаявкаНаПеревозкуГрузаАдресПолучения,
	|	втДокументы.Схема КАК СхемаМультимодальнойПеревозки,
	|	втПараметрыСхемы.ЕстьНарушенияПоСхеме КАК ЕстьНарушенияПоСхеме,
	|	втЗаявки.Организация КАК Организация,
	|	втЗаявки.Подразделение КАК Подразделение,
	|	втЗаявки.Заказчик КАК Заказчик,
	|	втЗаявки.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЗаявки.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЗаявки.Соглашение КАК Соглашение,
	|	ВЫБОР
	|		КОГДА втДокументы.ВидПеревозки = ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|				КОГДА втДокументы.ПоследняяЦепьПоставки
	|					ТОГДА втПараметрыСхемы.ВидПеревозкиПоследнейПоставки
	|				ИНАЧЕ втЗаявки.ВидПеревозки
	|			КОНЕЦ
	|		ИНАЧЕ втДокументы.ВидПеревозки
	|	КОНЕЦ КАК ВидПеревозки,
	|	втДокументы.Отправитель КАК Отправитель,
	|	втДокументы.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втДокументы.Получатель КАК Получатель,
	|	втДокументы.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втДокументы.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втДокументы.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ВЫБОР
	|		КОГДА втДокументы.АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявки.ЗонаОтправления
	|		ИНАЧЕ втДокументы.АдресОтправления.ГеографическаяЗона
	|	КОНЕЦ КАК ЗонаОтправления,
	|	ВЫБОР
	|		КОГДА втДокументы.АдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявки.ЗонаПолучения
	|		ИНАЧЕ втДокументы.АдресПолучения.ГеографическаяЗона
	|	КОНЕЦ КАК ЗонаПолучения,
	|	втДокументы.АдресОтправления КАК АдресОтправления,
	|	втДокументы.ВремяПрохожденияТочки КАК ВремяПрохожденияТочки,
	|	втДокументы.ВремяВПути КАК ВремяВПути,
	|	втДокументы.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втДокументы.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втДокументы.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втДокументы.АдресПолучения КАК АдресПолучения,
	|	втДокументы.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втДокументы.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втДокументы.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втДокументы.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втДокументы.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втДокументы.ПоследняяЦепьПоставки КАК ПоследняяЦепьПоставки,
	|	втЗаявки.ОбщийГруз КАК ОбщийГруз,
	|	втДокументы.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втДокументы.ЧасовойПояс КАК ЧасовойПояс,
	|	втЗаявки.ЧасовойПояс КАК ЧасовойПоясЗаявки,
	|	втЗаявки.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки КАК ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки,
	|	ВЫБОР
	|		КОГДА втПараметрыСхемы.ОпределятьВременныеОкнаЗаданий
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОпределятьВременныеОкнаЗаданий,
	|	втДокументы.ВариантОпределенияВременногоОкнаОтправления КАК ВариантОпределенияВременногоОкнаОтправления,
	|	втДокументы.ВариантОпределенияВременногоОкнаПолучения КАК ВариантОпределенияВременногоОкнаПолучения,
	|	втДокументы.ВремяНахожденияВТочке КАК ВремяНахожденияВТочке,
	|	ВЫБОР
	|		КОГДА втПараметрыСхемы.ОпределятьВременныеОкнаЗаданий
	|				И (НЕ втДокументы.ВариантОпределенияВременногоОкнаОтправления = ЗНАЧЕНИЕ(Перечисление.упВариантыОпределенияВременныхОкон.ПостояннаяДоступность)
	|					ИЛИ НЕ втДокументы.ВариантОпределенияВременногоОкнаПолучения = ЗНАЧЕНИЕ(Перечисление.упВариантыОпределенияВременныхОкон.ПостояннаяДоступность))
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РассчитыватьВременныеОкна,
	|	втДокументы.ОтправлениеГрузаС КАК ГрузовоеМестоОтправлениеГрузаС,
	|	втДокументы.ОтправлениеГрузаПо КАК ГрузовоеМестоОтправлениеГрузаПо,
	|	втДокументы.ПолучениеГрузаС КАК ГрузовоеМестоПолучениеГрузаС,
	|	втДокументы.ПолучениеГрузаПо КАК ГрузовоеМестоПолучениеГрузаПо,
	|	ВЫБОР
	|		КОГДА втПараметрыСхемы.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ФормироватьВременныеОкнаПоГрафикамРаботыАдресов,
	|	0 КАК ОбщееВремяВПути
	|ИЗ
	|	втДокументы05 КАК втДокументы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкиПодготовка КАК втЗаявки
	|	ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыСхемы КАК втПараметрыСхемы
	|	ПО втДокументы.Схема = втПараметрыСхемы.Схема
	|УПОРЯДОЧИТЬ ПО
	|	втДокументы.Схема,
	|	втДокументы.НомерВЦепиПоставок
	|ИТОГИ
	|	СУММА(ОпределятьВременныеОкнаЗаданий),
	|	СУММА(РассчитыватьВременныеОкна),
	|	СУММА(ФормироватьВременныеОкнаПоГрафикамРаботыАдресов),
	|	МАКСИМУМ(ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки)
	|ПО
	|	СхемаМультимодальнойПеревозки,
	|	ВозвратПоЦепиПоставок
	|;
	|//{Запрос: 18 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдресаПеревозкиГрузов.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втАдресаПеревозкиГрузов.Схема КАК СхемаМультимодальнойПеревозки,
	|	втАдресаПеревозкиГрузов.Тара КАК Тара,
	|	ЕСТЬNULL(тбГрузовыеМеста.ТипГруза, ЗНАЧЕНИЕ(Справочник.упТипыГрузов.ПустаяСсылка)) КАК ТипГруза,
	|	ЕСТЬNULL(тбТипыГрузов.ВидГруза, ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.ПустаяСсылка)) КАК ВидГруза,
	|	втАдресаПеревозкиГрузов.АдресОтправления КАК АдресОтправления,
	|	втАдресаПеревозкиГрузов.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втАдресаПеревозкиГрузов.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втАдресаПеревозкиГрузов.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втАдресаПеревозкиГрузов.АдресПолучения КАК АдресПолучения,
	|	втАдресаПеревозкиГрузов.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втАдресаПеревозкиГрузов.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втАдресаПеревозкиГрузов.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втАдресаПеревозкиГрузов.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втАдресаПеревозкиГрузов.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втАдресаПеревозкиГрузов.КоличествоГрузов КАК КоличествоГрузов,
	|	втАдресаПеревозкиГрузов.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втАдресаПеревозкиГрузов.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втАдресаПеревозкиГрузов.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втАдресаПеревозкиГрузов.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втАдресаПеревозкиГрузов.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втАдресаПеревозкиГрузов.Отправитель КАК Отправитель,
	|	втАдресаПеревозкиГрузов.Получатель КАК Получатель
	|ИЗ
	|	втАдресаПеревозкиГрузов КАК втАдресаПеревозкиГрузов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК тбГрузовыеМеста
	|	ПО тбГрузовыеМеста.Ссылка = втАдресаПеревозкиГрузов.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК тбТипыГрузов
	|	ПО тбТипыГрузов.Ссылка = тбГрузовыеМеста.ТипГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкиПодготовка КАК втЗаявки
	|	ПО ИСТИНА
	|";
	
	Возврат Результат;
		                  
КонецФункции

Функция ТекстЗапросаФормированиеЗаданийПоЭтапамПеревозки()
		
	Результат = "
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПеревозки.АдресОтправления КАК АдресОтправления,
	|	ЭтапыПеревозки.АдресПолучения КАК АдресПолучения,
	|	ЭтапыПеревозки.ВидПеревозки КАК ВидПеревозки,
	|	ЭтапыПеревозки.ТипПеревозки КАК ТипПеревозки,
	|	ЭтапыПеревозки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ЭтапыПеревозки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ЭтапыПеревозки.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ЭтапыПеревозки.Заказчик КАК Заказчик,
	|	ЭтапыПеревозки.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ЭтапыПеревозки.ЗонаОтправления КАК ЗонаОтправления,
	|	ЭтапыПеревозки.ЗонаПолучения КАК ЗонаПолучения,
	|	ЭтапыПеревозки.КоличествоГрузов КАК КоличествоГрузов,
	|	ЭтапыПеревозки.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	ЭтапыПеревозки.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ЭтапыПеревозки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ЭтапыПеревозки.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	ЭтапыПеревозки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	ЭтапыПеревозки.Организация КАК Организация,
	|	ЭтапыПеревозки.Отправитель КАК Отправитель,
	|	ЭтапыПеревозки.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	ЭтапыПеревозки.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	ЭтапыПеревозки.Подразделение КАК Подразделение,
	|	ЭтапыПеревозки.Получатель КАК Получатель,
	|	ЭтапыПеревозки.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	ЭтапыПеревозки.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	ЭтапыПеревозки.Тара КАК Тара,
	|	ЭтапыПеревозки.ЧасовойПояс КАК ЧасовойПояс,
	|	ЭтапыПеревозки.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки
	|ПОМЕСТИТЬ втЭтапыПеревозки
	|ИЗ
	|	&ЭтапыПеревозки КАК ЭтапыПеревозки
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиТ.АдресОтправления КАК АдресОтправления,
	|	втЭтапыПеревозкиТ.АдресПолучения КАК АдресПолучения,
	|	втЭтапыПеревозкиТ.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыПеревозкиТ.ТипПеревозки КАК ТипПеревозки,
	|	втЭтапыПеревозкиТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыПеревозкиТ.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыПеревозкиТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЭтапыПеревозкиТ.Заказчик КАК Заказчик,
	|	втЭтапыПеревозкиТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозкиТ.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыПеревозкиТ.ЗонаПолучения КАК ЗонаПолучения,
	|	упЗаявкиКВыполнениюОстатки.КоличествоОстаток КАК КоличествоГрузов,
	|	втЭтапыПеревозкиТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыПеревозкиТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыПеревозкиТ.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозкиТ.Организация КАК Организация,
	|	втЭтапыПеревозкиТ.Отправитель КАК Отправитель,
	|	втЭтапыПеревозкиТ.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыПеревозкиТ.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыПеревозкиТ.Подразделение КАК Подразделение,
	|	втЭтапыПеревозкиТ.Получатель КАК Получатель,
	|	втЭтапыПеревозкиТ.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыПеревозкиТ.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыПеревозкиТ.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыПеревозкиТ.Тара КАК Тара,
	|	втЭтапыПеревозкиТ.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыПеревозкиТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыПеревозкиТ.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЗаявкиНаПеревозкуГруза.Соглашение КАК Соглашение,
	|	ЗаявкиНаПеревозкуГруза.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втЭтапыПеревозкиОстатки
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозкиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК ЗаявкиНаПеревозкуГруза
	|	ПО ЗаявкиНаПеревозкуГруза.Ссылка = втЭтапыПеревозкиТ.ЗаявкаНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК ВидыПеревозок
	|	ПО ВидыПеревозок.Ссылка = ЗаявкиНаПеревозкуГруза.ВидПеревозки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|		,
	|		Заявка = &ЗаявкаНаПеревозкуГруза) КАК упЗаявкиКВыполнениюОстатки
	|	ПО ИСТИНА
	|		И втЭтапыПеревозкиТ.ГрузовоеМесто = упЗаявкиКВыполнениюОстатки.ГрузовоеМесто
	|		И втЭтапыПеревозкиТ.Тара = упЗаявкиКВыполнениюОстатки.Тара
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставок = упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец = упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец
	|		И ВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|		И упЗаявкиКВыполнениюОстатки.КоличествоОстаток > 0
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиТ.АдресОтправления КАК АдресОтправления,
	|	втЭтапыПеревозкиТ.АдресПолучения КАК АдресПолучения,
	|	втЭтапыПеревозкиТ.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыПеревозкиТ.ТипПеревозки КАК ТипПеревозки,
	|	втЭтапыПеревозкиТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыПеревозкиТ.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыПеревозкиТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втЭтапыПеревозкиТ.Заказчик КАК Заказчик,
	|	втЭтапыПеревозкиТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозкиТ.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыПеревозкиТ.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыПеревозкиТ.КоличествоГрузов КАК КоличествоГрузов,
	|	втЭтапыПеревозкиТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыПеревозкиТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыПеревозкиТ.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозкиТ.Организация КАК Организация,
	|	втЭтапыПеревозкиТ.Отправитель КАК Отправитель,
	|	втЭтапыПеревозкиТ.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыПеревозкиТ.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыПеревозкиТ.Подразделение КАК Подразделение,
	|	втЭтапыПеревозкиТ.Получатель КАК Получатель,
	|	втЭтапыПеревозкиТ.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыПеревозкиТ.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыПеревозкиТ.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втЭтапыПеревозкиТ.Тара КАК Тара,
	|	втЭтапыПеревозкиТ.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыПеревозкиТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыПеревозкиТ.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЗаявкиНаПеревозкуГруза.Соглашение КАК Соглашение,
	|	ЗаявкиНаПеревозкуГруза.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	втЭтапыПеревозки КАК втЭтапыПеревозкиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК ЗаявкиНаПеревозкуГруза
	|	ПО ЗаявкиНаПеревозкуГруза.Ссылка = втЭтапыПеревозкиТ.ЗаявкаНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК ВидыПеревозок
	|	ПО ВидыПеревозок.Ссылка = ЗаявкиНаПеревозкуГруза.ВидПеревозки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|		,
	|		Заявка = &ЗаявкаНаПеревозкуГруза) КАК упЗаявкиКВыполнениюОстатки
	|	ПО ИСТИНА
	|		И ВидыПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку)
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставок = упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок
	|		И втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец = упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец
	|		И упЗаявкиКВыполнениюОстатки.КоличествоОстаток > 0
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЭтапыПеревозкиТ.АдресОтправления КАК АдресОтправления,
	|	втЭтапыПеревозкиТ.АдресПолучения КАК АдресПолучения,
	|	втЭтапыПеревозкиТ.ВидПеревозки КАК ВидПеревозки,
	|	втЭтапыПеревозкиТ.ТипПеревозки КАК ТипПеревозки,
	|	втЭтапыПеревозкиТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втЭтапыПеревозкиТ.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЭтапыПеревозкиТ.Заказчик КАК Заказчик,
	|	втЭтапыПеревозкиТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЭтапыПеревозкиТ.ЗонаОтправления КАК ЗонаОтправления,
	|	втЭтапыПеревозкиТ.ЗонаПолучения КАК ЗонаПолучения,
	|	втЭтапыПеревозкиТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЭтапыПеревозкиТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЭтапыПеревозкиТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЭтапыПеревозкиТ.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЭтапыПеревозкиТ.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЭтапыПеревозкиТ.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЭтапыПеревозкиТ.Организация КАК Организация,
	|	втЭтапыПеревозкиТ.Отправитель КАК Отправитель,
	|	втЭтапыПеревозкиТ.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втЭтапыПеревозкиТ.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втЭтапыПеревозкиТ.Подразделение КАК Подразделение,
	|	втЭтапыПеревозкиТ.Получатель КАК Получатель,
	|	втЭтапыПеревозкиТ.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втЭтапыПеревозкиТ.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втЭтапыПеревозкиТ.ЧасовойПояс КАК ЧасовойПояс,
	|	втЭтапыПеревозкиТ.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	упЗаявкаНаПеревозкуГрузаТ.ЧасовойПояс КАК ЧасовойПоясЗаявки,
	|	упЗаявкаНаПеревозкуГрузаТ.Приоритет КАК Приоритет,
	|	втЭтапыПеревозкиТ.Соглашение КАК Соглашение,
	|	втЭтапыПеревозкиТ.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упЗаявкаНаПеревозкуГрузаТ.ЗапрещеноИсполнениеНесколькимиРейсами
	|ИЗ
	|	втЭтапыПеревозкиОстатки КАК втЭтапыПеревозкиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГрузаТ
	|	ПО упЗаявкаНаПеревозкуГрузаТ.Ссылка = втЭтапыПеревозкиТ.ЗаявкаНаПеревозкуГруза
	|УПОРЯДОЧИТЬ ПО
	|	НомерВЦепиПоставок,
	|	НомерВЦепиПоставокКонец
	|ИТОГИ
	|ПО
	|	СхемаМультимодальнойПеревозки
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПеревозкиТ.*,
	|	упГрузовыеМестаТ.ТипГруза КАК ТипГруза,
	|	упГрузовыеМестаТ.ТипГруза.ВидГруза КАК ВидГруза
	|ИЗ
	|	втЭтапыПеревозкиОстатки КАК втЭтапыПеревозкиТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМестаТ
	|	ПО втЭтапыПеревозкиТ.ГрузовоеМесто = упГрузовыеМестаТ.Ссылка
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|// Выборка дополнительных операций
	|ВЫБРАТЬ
	|	ДополнительныеОперации.ДополнительнаяОперация КАК ДополнительнаяОперация,
	|	ДополнительныеОперации.ТипТочки КАК ТипТочки,
	|	ДополнительныеОперации.Количество КАК Количество
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ДополнительныеОперации КАК ДополнительныеОперации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.упИспользуютсяДополнительныеОперации КАК упИспользуютсяДополнительныеОперации
	|	ПО упИспользуютсяДополнительныеОперации.Значение = ИСТИНА
	|ГДЕ ДополнительныеОперации.Ссылка = &ЗаявкаНаПеревозкуГруза
	|";

	Возврат Результат;

КонецФункции

Функция СобытиеФормированиеЗаданий()
	Возврат НСтр("ru = 'Формирование заданий'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
КонецФункции

// Функция создает задания на перевозку на основании заявки на перевозку
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза			 - заявка, основание.
//  ОбъектЗаданиеНаПеревозку	 - ДокументОбъект.упЗаданиеНаПеревозкуГруза, Неопределено	 - параметр передается 
//                               при вызове функции из формы документа задания
//  ПроверитьЗаполнение		 - Булево											 - признак проверки.
//  ТекстОшибки			 - Строка											 - описание ошибки.
//  Отказ					 - Булево											 - Истина, в случае ошибки.
// 
// Возвращаемое значение:
//  Массив - Массив созданных заданий на перевозку
//
Функция СформироватьЗаданияНаПервозкуГруза(ЗаявкаНаПеревозкуГруза, ОбъектЗаданиеНаПеревозку = Неопределено, ПроверитьЗаполнение = Ложь, ТекстОшибки = "", Отказ) Экспорт
	
	//Если УправлениеПеревозками <> "1" Тогда 
	//	ВызватьИсключение НСтр("ru = 'Модуль управления перевозками не доступен'");
	//КонецЕсли;
	
	мсвРезультатов = Новый Массив();
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упЗаявкаНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Ссылка", ЗаявкаНаПеревозкуГруза);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаявкиКВыполнению");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Заявка", ЗаявкаНаПеревозкуГруза);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", ЗаявкаНаПеревозкуГруза);
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, ЗаявкаНаПеревозкуГруза, Отказ);
	
	Статус = упРаботаСоСтатусами.ПолучитьТекущийСтатус(ЗаявкаНаПеревозкуГруза);
	
	Если Истина
		И НЕ Отказ
		И Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
	Тогда
		
		ЭтапыПеревозки = ЭтапыПеревозки(ЗаявкаНаПеревозкуГруза, ТекстОшибки, Отказ);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаФормированиеЗаданийПоЭтапамПеревозки();
		Запрос.УстановитьПараметр("ЭтапыПеревозки", ЭтапыПеревозки);
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
		
		Результат = Запрос.ВыполнитьПакет();
				
		ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
		
		ИндексЗапросаДокументы = 2;
		ИндексЗапросаГрузовыеМеста = 3;
		ИндексЗапросаДопОперации = 4;
		
		ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах");
		
		Если Не Результат[ИндексЗапросаДокументы].Пустой() Тогда
			
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаявкаНаПеревозкуГруза, Новый Структура("СоздаватьЗаданияНаПеревозкуПоочередно", "ВидПеревозки.СоздаватьЗаданияНаПеревозкуПоочередно"));
			СоздаватьЗаданияНаПеревозкуПоочередно = сткРеквизиты.СоздаватьЗаданияНаПеревозкуПоочередно;
			
			ВыгрузкаГрузовыеМеста = Результат[ИндексЗапросаГрузовыеМеста].Выгрузить();
			
			ВыгрузкаДопОперации = Неопределено;
			Если Не Результат[ИндексЗапросаДопОперации].Пустой() Тогда
				ВыгрузкаДопОперации = Результат[ИндексЗапросаДопОперации].Выгрузить();
			КонецЕсли;
			
			дрвДокументы = Результат[ИндексЗапросаДокументы].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			НомерВЦепиПоставки = Неопределено;
			ПрерватьФормированиеЗаданий = Ложь;
			
			Для каждого СхемаСтрока Из дрвДокументы.Строки Цикл
				
				Для каждого ЗаданиеСтрока Из СхемаСтрока.Строки Цикл
					
					Если Истина
						И СоздаватьЗаданияНаПеревозкуПоочередно
						И НомерВЦепиПоставки <> Неопределено
						И НомерВЦепиПоставки <> СхемаСтрока.НомерВЦепиПоставок
					Тогда
						ПрерватьФормированиеЗаданий = Истина;
						Прервать;	
					КонецЕсли;
					
					НомерВЦепиПоставки = ЗаданиеСтрока.НомерВЦепиПоставок;
					
					// Документ
					Если ОбъектЗаданиеНаПеревозку = Неопределено Тогда
						ДокументОбъект = Документы.упЗаданиеНаПеревозкуГруза.СоздатьДокумент();
						ДокументОбъект.Дата = ТекущаяДатаСеанса();
						ДокументОбъект.Автор = ТекущийПользователь;
						ДокументОбъект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
					Иначе	
						ДокументОбъект = ОбъектЗаданиеНаПеревозку;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(ДокументОбъект, ЗаданиеСтрока);
					
					Если Истина
						И ДокументОбъект.АдресОтправления = ДокументОбъект.АдресПолучения
						И Не ЗначениеЗаполнено(ДокументОбъект.Отправитель) 
						И Не ЗначениеЗаполнено(ДокументОбъект.КонтрагентОтправителя) 
						Тогда
						ДокументОбъект.Отправитель = ДокументОбъект.Получатель;
						ДокументОбъект.КонтрагентОтправителя = ДокументОбъект.КонтрагентПолучателя;
					КонецЕсли;
					
					// Дополнительные операции
					Если ВыгрузкаДопОперации <> Неопределено И ЗаданиеСтрока.НомерВЦепиПоставок < 2 Тогда
						ДокументОбъект.ДополнительныеОперации.Загрузить(ВыгрузкаДопОперации);
					КонецЕсли;
					
					// Грузовые места
					тбзТипыГрузовыхМест = Новый ТаблицаЗначений;
					тбзТипыГрузовыхМест.Колонки.Добавить("ТипГруза", Новый ОписаниеТипов("СправочникСсылка.упТипыГрузов"));
					тбзТипыГрузовыхМест.Колонки.Добавить("ВидГруза", Новый ОписаниеТипов("ПеречислениеСсылка.упВидыГрузов"));
					
					сткОтбор = Новый Структура();
					сткОтбор.Вставить("СхемаМультимодальнойПеревозки");
					сткОтбор.Вставить("АдресОтправления");
					сткОтбор.Вставить("АдресПолучения");
					сткОтбор.Вставить("НомерВЦепиПоставок");
					сткОтбор.Вставить("ГрафикРаботыАдресаОтправления");
					сткОтбор.Вставить("ГрафикРаботыАдресаПолучения");
					сткОтбор.Вставить("Отправитель");
					сткОтбор.Вставить("КонтрагентОтправителя");
					сткОтбор.Вставить("КонтактноеЛицоОтправителя");
					сткОтбор.Вставить("Получатель");
					сткОтбор.Вставить("КонтрагентПолучателя");
					сткОтбор.Вставить("КонтактноеЛицоПолучателя");
					сткОтбор.Вставить("ОтправлениеГрузаС");
					сткОтбор.Вставить("ОтправлениеГрузаПо");
					сткОтбор.Вставить("ПолучениеГрузаС");
					сткОтбор.Вставить("ПолучениеГрузаПо");
					
					ЗаполнитьЗначенияСвойств(сткОтбор, ЗаданиеСтрока);
					
					ИскомыйГруз = ВыгрузкаГрузовыеМеста.НайтиСтроки(сткОтбор);
					Если ИскомыйГруз.Количество() Тогда
						Для каждого текГрузовоеМесто Из ИскомыйГруз Цикл
							ЗаполнитьЗначенияСвойств(ДокументОбъект.ГрузовыеМеста.Добавить(), текГрузовоеМесто);
							
							НоваяСтрока = тбзТипыГрузовыхМест.Добавить();
							НоваяСтрока.ТипГруза = текГрузовоеМесто.ТипГруза;
							НоваяСтрока.ВидГруза = текГрузовоеМесто.ВидГруза;
						КонецЦикла; 
					Иначе
						Продолжить;
					КонецЕсли;
					
					упЗаданиеНаПеревозку.ПересчитатьВремяПогрузки(ДокументОбъект, тбзТипыГрузовыхМест);
					упЗаданиеНаПеревозку.ПересчитатьВремяРазгрузки(ДокументОбъект, тбзТипыГрузовыхМест);
					
					ЧасовойПоясЗаявки = ЗаданиеСтрока.ЧасовойПоясЗаявки;
					ЧасовойПоясЗадания = ДокументОбъект.ЧасовойПояс;
					
					Если ВедетсяРаботаВРазныхЧасовыхПоясах 
						И ЗначениеЗаполнено(ЧасовойПоясЗаявки) Тогда
						Если ЗначениеЗаполнено(ЧасовойПоясЗадания)  Тогда
							Если НЕ ЧасовойПоясЗаявки = ЧасовойПоясЗадания Тогда
								Если ЗначениеЗаполнено(ДокументОбъект.ОтправлениеГрузаС) Тогда
									ДокументОбъект.ОтправлениеГрузаС = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(ДокументОбъект.ОтправлениеГрузаС, ЧасовойПоясЗаявки, ЧасовойПоясЗадания); 	
								КонецЕсли;
								Если ЗначениеЗаполнено(ДокументОбъект.ОтправлениеГрузаПо) Тогда
									ДокументОбъект.ОтправлениеГрузаПо = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(ДокументОбъект.ОтправлениеГрузаПо, ЧасовойПоясЗаявки, ЧасовойПоясЗадания); 
								КонецЕсли;
								Если ЗначениеЗаполнено(ДокументОбъект.ПолучениеГрузаС) Тогда
									ДокументОбъект.ПолучениеГрузаС = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(ДокументОбъект.ПолучениеГрузаС, ЧасовойПоясЗаявки, ЧасовойПоясЗадания); 
								КонецЕсли;	
								Если ЗначениеЗаполнено(ДокументОбъект.ПолучениеГрузаПо) Тогда
									ДокументОбъект.ПолучениеГрузаПо = упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(ДокументОбъект.ПолучениеГрузаПо, ЧасовойПоясЗаявки, ЧасовойПоясЗадания); 
								КонецЕсли;	
							КонецЕсли;	
						Иначе		
							ДокументОбъект.ЧасовойПояс = ЧасовойПоясЗаявки;
						КонецЕсли;
					КонецЕсли;
					
					Если ОбъектЗаданиеНаПеревозку = Неопределено Тогда
						Попытка
							
							Если ЗначениеЗаполнено(ДокументОбъект.АдресОтправления) И ЗначениеЗаполнено(ДокументОбъект.АдресПолучения) Тогда
								РежимЗаписи = РежимЗаписиДокумента.Проведение;
								ДокументОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Новое);
							Иначе
								РежимЗаписи = РежимЗаписиДокумента.Запись;
							КонецЕсли;
							
							//Если Истина
							//	И ПроверитьЗаполнение
							//	И НЕ ДокументОбъект.ПроверитьЗаполнение() 
							//Тогда
							//	ТекстОшибки = НСтр("ru = 'Ошибка при проверке заполнения сформированного задания'");
							//	Отказ = Истина;	
							//КонецЕсли;
							
							Если Не Отказ Тогда
								ДокументОбъект.Записать(РежимЗаписи);	
								мсвРезультатов.Добавить(ДокументОбъект.Ссылка);
							КонецЕсли;
							
						Исключение
							Отказ = Истина;
							ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
							Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
								ТекстОшибки = ОписаниеОшибки();
							КонецЕсли;
						КонецПопытки;
					Иначе
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				Если Ложь
					Или Отказ
					Или ПрерватьФормированиеЗаданий
					Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	Иначе
		
		Отказ = Истина;
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Статус заявки должен быть ""К выполнению"".(%1)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ЗаявкаНаПеревозкуГруза);
				
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'При формировании задания на перевозку произошла ошибка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Формирование заданий", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаданиеНаПеревозкуГруза,, ТекстОшибки);
	КонецЕсли;
	
	Возврат мсвРезультатов;
	
КонецФункции // СформироватьЗаданияНаПервозкуГруза()

//// Функция - Сформировать заявку на возврат грузов
////
////    ЗаявкаНаПеревозкуГруза   - ДокументСсылка.упЗаявкаНаПеревозкуГруза - Ссылка на документ.
////    ТекстОшибки              - Строка - Представление ошибки.
////    Отказ                    - Булево - Флаг, фиксации выполненых действий.
////
////  Возвращаемое значение:
////    Массив - ДокументыСсылка - Массив созданных заявок на перевозку.
////
//Функция СформироватьЗаявкуНаВозвратГрузов(ЗаявкаНаПеревозкуГруза, ТекстОшибки = "", Отказ) Экспорт
//	
//	Возврат упСЛК.СоздатьОбъект().СформироватьЗаявкуНаВозвратГрузов(ЗаявкаНаПеревозкуГруза, ТекстОшибки, Отказ);
//	
//КонецФункции

// Функция - Сформировать заявку на возврат грузов
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза	 - Ссылка на документ.
//  ПроверитьЗаполнение		 - Булево								 - Признак проверки.
//  ТекстОшибки			 - Строка								 - Представление ошибки.
//  Отказ					 - Булево								 - Флаг, фиксации выполненых действий.
// 
// Возвращаемое значение:
//  Массив - ДокументыСсылка - Массив созданных заявок на перевозку.
//
Функция СформироватьЗаявкуНаВозвратГрузов(ЗаявкаНаПеревозкуГруза, ПроверитьЗаполнение = Ложь, ТекстОшибки = "", Отказ) Экспорт
	
	//ПроверитьОграничениеПоДоступностиКомпонент(2);
	
	Результат = Новый Массив;
	
	// Проверить условия на статус заявки и наличие грузовых мест для возврата
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СтатусыЗаявок.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) КАК Статус,
	|	тбЗаявкаНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	тбЗаявкаНаПеревозкуГруза.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	тбЗаявкаНаПеревозкуГруза.Валюта КАК Валюта,
	|	тбЗаявкаНаПеревозкуГруза.ВидПеревозки КАК ВидПеревозки,
	|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК ГрафикРаботыАдресаОтправления,
	|	тбЗаявкаНаПеревозкуГруза.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаПолучения,
	|	тбЗаявкаНаПеревозкуГруза.Заказчик КАК Заказчик,
	|	тбЗаявкаНаПеревозкуГруза.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	тбЗаявкаНаПеревозкуГруза.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	тбЗаявкаНаПеревозкуГруза.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	тбЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	тбЗаявкаНаПеревозкуГруза.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	тбЗаявкаНаПеревозкуГруза.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	тбЗаявкаНаПеревозкуГруза.ОбщийГруз КАК ОбщийГруз,
	|	тбЗаявкаНаПеревозкуГруза.КоличествоГрузов КАК КоличествоГрузов,
	|	тбЗаявкаНаПеревозкуГруза.Организация КАК Организация,
	|	тбЗаявкаНаПеревозкуГруза.Отправитель КАК Отправитель,
	|	тбЗаявкаНаПеревозкуГруза.Получатель КАК Получатель,
	|	тбЗаявкаНаПеревозкуГруза.Соглашение КАК Соглашение,
	|	тбЗаявкаНаПеревозкуГруза.ЗонаОтправления КАК ЗонаОтправления
	|ПОМЕСТИТЬ втЗаявкаНаПеревозкуГрузов
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК тбЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаявок
	|	ПО тбЗаявкаНаПеревозкуГруза.Ссылка = СтатусыЗаявок.Документ
	|ГДЕ тбЗаявкаНаПеревозкуГруза.Ссылка = &ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза,
	|	ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ЗаданиеНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	ЗаданиеНаПеревозкуГруза.Отправитель КАК Отправитель,
	|	ЗаданиеНаПеревозкуГруза.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ЗаданиеНаПеревозкуГруза.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ЗаданиеНаПеревозкуГруза.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления
	|ПОМЕСТИТЬ втЗаданияНаПеревозкуГруза
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаданийНаПеревозкуГруза
	|	ПО СтатусыЗаданийНаПеревозкуГруза.Документ = ЗаданиеНаПеревозкуГруза.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкаНаПеревозкуГрузов КАК втЗаявкаНаПеревозкуГрузов
	|	ПО ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втЗаявкаНаПеревозкуГрузов.Ссылка
	|ГДЕ НЕ (СтатусыЗаданийНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено))
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	ГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	ГрузовыеМеста.ВозвратнаяТара КАК ВозвратнаяТара,
	|	ГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМеста.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	ГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	ГрузовыеМеста.Отправитель КАК Отправитель,
	|	ГрузовыеМеста.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	ГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявке10
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкаНаПеревозкуГрузов КАК втЗаявкаНаПеревозкуГрузов
	|	ПО ГрузовыеМеста.Ссылка = втЗаявкаНаПеревозкуГрузов.Ссылка
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазделениеГрузовыхМест.ГрузовоеМесто КАК ГрузовоеМесто,
	|	РазделениеГрузовыхМест.Родитель КАК Родитель,
	|	РазделениеГрузовыхМест.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест05
	|ИЗ
	|	втГрузовыеМестаПоЗаявке10 КАК втГрузовыеМеста05Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРазделениеГрузовыхМест.СрезПоследних(
	|		,
	|		) КАК РазделениеГрузовыхМест
	|	ПО РазделениеГрузовыхМест.РодительИсходный = втГрузовыеМеста05Т.ГрузовоеМесто
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРазделениеГрузовыхМестГрузовыеМеста.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест10
	|ИЗ
	|	втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестРодители
	|	ПО втРазделениеГрузовыхМестРодители.Родитель = втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто
	|ГДЕ втРазделениеГрузовыхМестРодители.ГрузовоеМесто ЕСТЬ NULL
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	ГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	ГрузовыеМеста.ВозвратнаяТара КАК ВозвратнаяТара,
	|	ГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	ЕСТЬNULL(упРазделениеГрузовыхМестСрезПоследних.ГрузовоеМесто, ГрузовыеМеста.ГрузовоеМесто) КАК ГрузовоеМесто,
	|	ГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	ГрузовыеМеста.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	ГрузовыеМеста.Отправитель КАК Отправитель,
	|	ГрузовыеМеста.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	ГрузовыеМеста.Тара КАК Тара
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявке
	|ИЗ
	|	втГрузовыеМестаПоЗаявке10 КАК ГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест10 КАК упРазделениеГрузовыхМестСрезПоследних
	|	ПО упРазделениеГрузовыхМестСрезПоследних.РодительИсходный = ГрузовыеМеста.ГрузовоеМесто
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
	|	втЗаданияНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	втЗаданияНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЗаданияНаПеревозкуГруза.Отправитель КАК Отправитель,
	|	втЗаданияНаПеревозкуГруза.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втЗаданияНаПеревозкуГруза.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втЗаданияНаПеревозкуГруза.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления
	|ПОМЕСТИТЬ втПоследнееЗаданиеВЦепиПоставок
	|ИЗ
	|	втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		МАКСИМУМ(втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок) КАК НомерВЦепиПоставок
	|	ИЗ
	|		втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза) КАК втМаксимальныйНомерВЦепиПоставок
	|	ПО втЗаданияНаПеревозкуГруза.НомерВЦепиПоставок = втМаксимальныйНомерВЦепиПоставок.НомерВЦепиПоставок
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияПоРейсу.Рейс КАК Рейс
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	(ВЫБРАТЬ
	|		упРейсЗадания.Ссылка КАК Рейс
	|	ИЗ
	|		Документ.упРейс.Задания КАК упРейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставок
	|		ПО упРейсЗадания.Задание = втПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|			,
	|			) КАК СтатусыРейсов
	|		ПО упРейсЗадания.Ссылка = СтатусыРейсов.Документ
	|	ГДЕ НЕ (СтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен))
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упКорректировкаРейсаЗадания.Ссылка.Рейс КАК Рейс
	|	ИЗ
	|		Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставок
	|		ПО упКорректировкаРейсаЗадания.Задание = втПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза
	|	ГДЕ упКорректировкаРейсаЗадания.Ссылка.Проведен) КАК ЗаданияПоРейсу
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|//Грузовые места для возврата
	|ВЫБРАТЬ
	|	РаботыПоРейсу.ГрузовоеМесто КАК ГрузовоеМесто,
	|	РаботыПоРейсу.КоличествоГрузов КАК КоличествоГрузов,
	|	втПоследнееЗаданиеВЦепиПоставок.АдресОтправления КАК АдресОтправления,
	|	РаботыПоРейсу.Задание КАК Задание
	|ПОМЕСТИТЬ втГрузовыеМестаДляВозврата
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК РаботыПоРейсу
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставок
	|	ПО РаботыПоРейсу.Задание = втПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК МаршрутПоРейсу
	|	ПО МаршрутПоРейсу.Идентификатор = РаботыПоРейсу.Идентификатор
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаПоЗаявке КАК ЗаявкаГрузовыеМеста
	|	ПО ЗаявкаГрузовыеМеста.ГрузовоеМесто = РаботыПоРейсу.ГрузовоеМесто
	|ГДЕ ИСТИНА
	|	И РаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	И МаршрутПоРейсу.Адрес = втПоследнееЗаданиеВЦепиПоставок.АдресОтправления
	|	И (ЛОЖЬ
	|		ИЛИ НЕ (РаботыПоРейсу.ПроблемаПричинаВозникновенияЭтойРаботы = ЗНАЧЕНИЕ(Справочник.упПроблемы.ПустаяСсылка))
	|		ИЛИ (ИСТИНА
	|			И РаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|			И ЗаявкаГрузовыеМеста.ВозвратнаяТара))
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаГрузовыеМеста.АдресОтправления КАК АдресПолучения,
	|	ИСТИНА КАК ВозвратПоЦепиПоставок,
	|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК ГрафикРаботыАдресаОтправления,
	|	ЗаявкаГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаПолучения,
	|	ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка) КАК КонтактноеЛицоОтправителя,
	|	ЗаявкаГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоПолучателя,
	|	ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка) КАК КонтрагентОтправителя,
	|	ЗаявкаГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентПолучателя,
	|	ЗаявкаГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка) КАК Отправитель,
	|	ЗаявкаГрузовыеМеста.Отправитель КАК Получатель,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ОтправлениеГрузаС,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаПо,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПолучениеГрузаС,
	|	ЗаявкаГрузовыеМеста.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	ЗаявкаГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМестаДляВозврата.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМестаДляВозврата.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаДляВозврата.КоличествоГрузов КАК КоличествоГрузов
	|ИЗ
	|	втГрузовыеМестаДляВозврата КАК втГрузовыеМестаДляВозврата
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаПоЗаявке КАК ЗаявкаГрузовыеМеста
	|	ПО ЗаявкаГрузовыеМеста.ГрузовоеМесто = втГрузовыеМестаДляВозврата.ГрузовоеМесто
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	тбЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка,
	|	СтатусыЗаявок.Статус КАК Статус
	|ПОМЕСТИТЬ втЗаявкиВозврата
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК тбЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаявок
	|	ПО тбЗаявкаНаПеревозкуГруза.Ссылка = СтатусыЗаявок.Документ
	|ГДЕ ИСТИНА
	|	И тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
	|	И НЕ (СтатусыЗаявок.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена),
	|			ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована),
	|			ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая),
	|			ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отказ),
	|			ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)))
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявкаНаПеревозкуГрузов.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	КОЛИЧЕСТВО(*) КАК КоличествоПромежуточныхТочек
	|ПОМЕСТИТЬ втКоличествоПромежуточныхТочек
	|ИЗ
	|	втЗаявкаНаПеревозкуГрузов КАК втЗаявкаНаПеревозкуГрузов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСхемыМультимодальныхПеревозок.ПромежуточныеТочкиМаршрута КАК СхемыМультимодальныхПеревозок_ПромежуточныеТочки
	|	ПО втЗаявкаНаПеревозкуГрузов.СхемаМультимодальнойПеревозки = СхемыМультимодальныхПеревозок_ПромежуточныеТочки.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	втЗаявкаНаПеревозкуГрузов.Ссылка
	|;
	|//{Запрос: 12 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втГрузовыеМестаДляВозвратаТ.Задание КАК Задание
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	втГрузовыеМестаДляВозврата КАК втГрузовыеМестаДляВозвратаТ
	|;
	|//{Запрос: 13 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследнееЗаданиеВЦепиПоставок.АдресОтправления КАК АдресОтправления,
	|	ЗаявкаНаПеревозкуГрузов.АдресОтправления КАК АдресПолучения,
	|	ЗаявкаНаПеревозкуГрузов.Валюта КАК Валюта,
	|	ЗаявкаНаПеревозкуГрузов.ВидПеревозки КАК ВидПеревозки,
	|	ПоследнееЗаданиеВЦепиПоставок.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ЗаявкаНаПеревозкуГрузов.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаПолучения,
	|	ЗаявкаНаПеревозкуГрузов.Заказчик КАК Заказчик,
	|	ЗаявкаНаПеревозкуГрузов.КоличествоГрузов КАК КоличествоГрузов,
	|	ЗаявкаНаПеревозкуГрузов.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	ПоследнееЗаданиеВЦепиПоставок.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	ЗаявкаНаПеревозкуГрузов.КонтактноеЛицоОтправителя КАК КонтактноеЛицоПолучателя,
	|	ЗаявкаНаПеревозкуГрузов.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	ПоследнееЗаданиеВЦепиПоставок.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	ЗаявкаНаПеревозкуГрузов.КонтрагентОтправителя КАК КонтрагентПолучателя,
	|	ЗаявкаНаПеревозкуГрузов.ОбщийГруз КАК ОбщийГруз,
	|	ЗаявкаНаПеревозкуГрузов.Организация КАК Организация,
	|	ПоследнееЗаданиеВЦепиПоставок.Отправитель КАК Отправитель,
	|	ЗаявкаНаПеревозкуГрузов.Отправитель КАК Получатель,
	|	ЗаявкаНаПеревозкуГрузов.Соглашение КАК Соглашение,
	|	ЗаявкаНаПеревозкуГрузов.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	ЗаявкаНаПеревозкуГрузов.Статус КАК Статус,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втКоличествоПромежуточныхТочек.КоличествоПромежуточныхТочек, 0) > 1
	|			ТОГДА ЗаявкаНаПеревозкуГрузов.СхемаМультимодальнойПеревозки
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка)
	|	КОНЕЦ КАК СхемаМультимодальнойПеревозки,
	|	упАдреса.ГеографическаяЗона КАК ЗонаОтправления,
	|	ЗаявкаНаПеревозкуГрузов.ЗонаОтправления КАК ЗонаПолучения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втКоличествоПромежуточныхТочек.КоличествоПромежуточныхТочек, 0) > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВозвратПоЦепиПоставок,
	|	ЕСТЬNULL(втЗаявкиВозврата.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ПустаяСсылка)) КАК СтатусЗаявкиНаВозвратГруза,
	|	втЗаявкиВозврата.Ссылка КАК ЗаявкаНаВозвратГруза
	|ИЗ
	|	втПоследнееЗаданиеВЦепиПоставок КАК ПоследнееЗаданиеВЦепиПоставок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкаНаПеревозкуГрузов КАК ЗаявкаНаПеревозкуГрузов
	|	ПО ПоследнееЗаданиеВЦепиПоставок.ЗаявкаНаПеревозкуГруза = ЗаявкаНаПеревозкуГрузов.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	|	ПО ПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза = втЗадания.Задание
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
	|	ПО ПоследнееЗаданиеВЦепиПоставок.АдресОтправления = упАдреса.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЗаявкиВозврата КАК втЗаявкиВозврата
	|	ПО ЗаявкаНаПеревозкуГрузов.Ссылка = втЗаявкиВозврата.ЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоПромежуточныхТочек КАК втКоличествоПромежуточныхТочек
	|	ПО ЗаявкаНаПеревозкуГрузов.Ссылка = втКоличествоПромежуточныхТочек.ЗаявкаНаПеревозкуГруза
	|";
	
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексЗапросаГрузовыеМеста = 9;
	ИндексЗапросаЗаявка = 13;
	
	Если мсвРезультатЗапроса[ИндексЗапросаГрузовыеМеста].Пустой() Тогда
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Нет грузовых мест для возврата(Берутся отмененные при разгрузке грузовые места последнего в цепочке поставок задания).'");
	    Возврат Результат;
	КонецЕсли;
	
	ВыборкаЗаявка	= мсвРезультатЗапроса[ИндексЗапросаЗаявка].Выбрать();
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	Пока ВыборкаЗаявка.Следующий() Цикл
		
		Если НЕ (ВыборкаЗаявка.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
				ИЛИ ВыборкаЗаявка.Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично) Тогда
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'На основании заявки в статусе ""%1"" нельзя сформировать заявку на возврат груза
										|(Статус должен быть ""Выполнена"" или ""Выполнена частично"").'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВыборкаЗаявка.Статус);
	    	Прервать;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаЗаявка.СтатусЗаявкиНаВозвратГруза) Тогда	
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'На основании заявки уже существует заявка на возврат груза 
										|%1'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВыборкаЗаявка.ЗаявкаНаВозвратГруза);
	    	Прервать;
		КонецЕсли;
		 		
		ДокументОбъект = Документы.упЗаявкаНаПеревозкуГруза.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Автор = ТекущийПользователь;
		ДокументОбъект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();

		ЗаполнитьЗначенияСвойств(ДокументОбъект, ВыборкаЗаявка);
	
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			ДокументОбъект.ЧасовойПояс = упСервисныеФункции.ЧасовойПоясВидаПеревозки(ДокументОбъект.ВидПеревозки);	
		КонецЕсли;  
				
		ВыборкаГрузовыеМеста = мсвРезультатЗапроса[ИндексЗапросаГрузовыеМеста].Выбрать();
		Пока ВыборкаГрузовыеМеста.Следующий() Цикл
			НоваяСтрока	= ДокументОбъект.ГрузовыеМеста.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаГрузовыеМеста);
		КонецЦикла;
		
		Попытка
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			ДокументОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая);
			//Если Истина
			//	И ПроверитьЗаполнение
			//	И НЕ ДокументОбъект.ПроверитьЗаполнение()
			//Тогда
			//	Отказ = Истина;
			//	ТекстОшибки = НСтр("ru = 'Ошибка при проверке заполнения сформированной заявки'");
			//КонецЕсли;
			
			Если Не Отказ Тогда
				ДокументОбъект.Записать(РежимЗаписи);	
				Результат.Добавить(ДокументОбъект.Ссылка);
			КонецЕсли;
			
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации("Формирование заявки на возврат груза", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла; 
	
	Если Отказ Тогда
		Если НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'При формировании заявки на возврат груза произошла ошибка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Формирование заявки на возврат груза", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция - Сформировать заявку на возврат тары
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза	 - Ссылка на Документ.
//  ПроверитьЗаполнение		 - Булево								 - Признак проверки.
//  ТекстОшибки			 - Строка								 - Представление ошибки.
//  Отказ					 - Булево								 - Флаг, фиксации выполненых действий.
// 
// Возвращаемое значение:
//  Массив - ДокументыСсылка - Массив созданных заявок на перевозку.
//
Функция СформироватьЗаявкуНаВозвратТары(ЗаявкаНаПеревозкуГруза, ПроверитьЗаполнение = Ложь, ТекстОшибки = "", Отказ) Экспорт
	
	//ПроверитьОграничениеПоДоступностиКомпонент(2);
	
	Результат = Новый Массив;
	
	// Проверить условия на статус заявки и наличие грузовых мест для возврата
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|// Заявка - основание для ввода заявки на возврат тары.
	|ВЫБРАТЬ
	|	тбЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СтатусыЗаявок.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) КАК Статус,
	|	тбЗаявкаНаПеревозкуГруза.Валюта КАК Валюта,
	|	тбЗаявкаНаПеревозкуГруза.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	тбЗаявкаНаПеревозкуГруза.Заказчик КАК Заказчик,
	|	тбЗаявкаНаПеревозкуГруза.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	тбЗаявкаНаПеревозкуГруза.Соглашение КАК Соглашение,
	|	тбЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	тбЗаявкаНаПеревозкуГруза.Отправитель КАК Отправитель,
	|	тбЗаявкаНаПеревозкуГруза.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	тбЗаявкаНаПеревозкуГруза.ЗонаОтправления КАК ЗонаОтправления,
	|	тбЗаявкаНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	тбЗаявкаНаПеревозкуГруза.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	тбЗаявкаНаПеревозкуГруза.ОбщийГруз КАК ОбщийГруз,
	|	тбЗаявкаНаПеревозкуГруза.Организация КАК Организация,
	|	тбЗаявкаНаПеревозкуГруза.ВидПеревозки КАК ВидПеревозки,
	|	тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	тбЗаявкаНаПеревозкуГруза.Номер КАК НомерЗаявки
	|ПОМЕСТИТЬ втЗаявкаНаПеревозкуГрузов
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК тбЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаявок
	|	ПО тбЗаявкаНаПеревозкуГруза.Ссылка = СтатусыЗаявок.Документ
	|ГДЕ тбЗаявкаНаПеревозкуГруза.Ссылка = &ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|// Все задания по заявке-основанию, последнее задание
	|// используется для заполнения данных отправителя.
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза,
	|	ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ЗаданиеНаПеревозкуГруза.АдресПолучения КАК АдресПолучения,
	|	ЗаданиеНаПеревозкуГруза.АдресОтправления КАК АдресОтправления,
	|	ЗаданиеНаПеревозкуГруза.Отправитель КАК Отправитель,
	|	ЗаданиеНаПеревозкуГруза.Получатель КАК Получатель,
	|	ЗаданиеНаПеревозкуГруза.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	ЗаданиеНаПеревозкуГруза.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	ЗаданиеНаПеревозкуГруза.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	ЗаданиеНаПеревозкуГруза.ЗонаПолучения КАК ЗонаПолучения
	|ПОМЕСТИТЬ втЗаданияНаПеревозкуГруза
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаданийНаПеревозкуГруза
	|	ПО СтатусыЗаданийНаПеревозкуГруза.Документ = ЗаданиеНаПеревозкуГруза.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкаНаПеревозкуГрузов КАК втЗаявкаНаПеревозкуГрузов
	|	ПО ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = втЗаявкаНаПеревозкуГрузов.Ссылка
	|ГДЕ НЕ (СтатусыЗаданийНаПеревозкуГруза.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено))
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|// Тара по заявке-основанию,
	|ВЫБРАТЬ
	|	ГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	ГрузовыеМеста.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втЗаявкаНаПеревозкуГрузов.АдресОтправления
	|		ИНАЧЕ ГрузовыеМеста.АдресОтправления
	|	КОНЕЦ КАК АдресОтправления,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.ГрафикРаботыАдресаОтправления = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
	|			ТОГДА втЗаявкаНаПеревозкуГрузов.ГрафикРаботыАдресаОтправления
	|		ИНАЧЕ ГрузовыеМеста.ГрафикРаботыАдресаОтправления
	|	КОНЕЦ КАК ГрафикРаботыАдресаОтправления,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.КонтактноеЛицоОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка)
	|			ТОГДА втЗаявкаНаПеревозкуГрузов.КонтактноеЛицоОтправителя
	|		ИНАЧЕ ГрузовыеМеста.КонтактноеЛицоОтправителя
	|	КОНЕЦ КАК КонтактноеЛицоОтправителя,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА втЗаявкаНаПеревозкуГрузов.КонтрагентОтправителя
	|		ИНАЧЕ ГрузовыеМеста.КонтрагентОтправителя
	|	КОНЕЦ КАК КонтрагентОтправителя,
	|	ВЫБОР
	|		КОГДА ГрузовыеМеста.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|			ТОГДА втЗаявкаНаПеревозкуГрузов.Отправитель
	|		ИНАЧЕ ГрузовыеМеста.Отправитель
	|	КОНЕЦ КАК Отправитель
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявке10
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМеста
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявкаНаПеревозкуГрузов КАК втЗаявкаНаПеревозкуГрузов
	|	ПО ГрузовыеМеста.Ссылка = втЗаявкаНаПеревозкуГрузов.Ссылка
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазделениеГрузовыхМест.ГрузовоеМесто КАК ГрузовоеМесто,
	|	РазделениеГрузовыхМест.Родитель КАК Родитель,
	|	РазделениеГрузовыхМест.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест05
	|ИЗ
	|	втГрузовыеМестаПоЗаявке10 КАК втГрузовыеМеста05Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упРазделениеГрузовыхМест.СрезПоследних(
	|		,
	|		) КАК РазделениеГрузовыхМест
	|	ПО РазделениеГрузовыхМест.РодительИсходный = втГрузовыеМеста05Т.ГрузовоеМесто
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втРазделениеГрузовыхМестГрузовыеМеста.РодительИсходный КАК РодительИсходный
	|ПОМЕСТИТЬ втРазделениеГрузовыхМест10
	|ИЗ
	|	втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест05 КАК втРазделениеГрузовыхМестРодители
	|	ПО втРазделениеГрузовыхМестРодители.Родитель = втРазделениеГрузовыхМестГрузовыеМеста.ГрузовоеМесто
	|ГДЕ втРазделениеГрузовыхМестРодители.ГрузовоеМесто ЕСТЬ NULL
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМестаПоЗаявкеТ.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМестаПоЗаявкеТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	ЕСТЬNULL(втРазделениеГрузовыхМест10Т.ГрузовоеМесто, втГрузовыеМестаПоЗаявкеТ.ГрузовоеМесто) КАК ГрузовоеМесто,
	|	втГрузовыеМестаПоЗаявкеТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втГрузовыеМестаПоЗаявкеТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМестаПоЗаявкеТ.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втГрузовыеМестаПоЗаявкеТ.Отправитель КАК Отправитель
	|ПОМЕСТИТЬ втГрузовыеМестаПоЗаявке
	|ИЗ
	|	втГрузовыеМестаПоЗаявке10 КАК втГрузовыеМестаПоЗаявкеТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втРазделениеГрузовыхМест10 КАК втРазделениеГрузовыхМест10Т
	|	ПО втРазделениеГрузовыхМест10Т.РодительИсходный = втГрузовыеМестаПоЗаявкеТ.ГрузовоеМесто
	|ГДЕ ЕСТЬNULL(втРазделениеГрузовыхМест10Т.ГрузовоеМесто.ТипГруза.ВидГруза, втГрузовыеМестаПоЗаявкеТ.ГрузовоеМесто.ТипГруза.ВидГруза) = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|// Последние задания по цепи поставок.
	|ВЫБРАТЬ
	|	втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза,
	|	втЗаданияНаПеревозкуГруза.АдресПолучения КАК АдресПолучения,
	|	втЗаданияНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	втЗаданияНаПеревозкуГруза.Получатель КАК Получатель,
	|	втЗаданияНаПеревозкуГруза.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втЗаданияНаПеревозкуГруза.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втЗаданияНаПеревозкуГруза.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втЗаданияНаПеревозкуГруза.ЗонаПолучения КАК ЗонаПолучения
	|ПОМЕСТИТЬ втПоследнееЗаданиеВЦепиПоставок
	|ИЗ
	|	втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		МАКСИМУМ(втЗаданияНаПеревозкуГруза.ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок) КАК НомерВЦепиПоставок
	|	ИЗ
	|		втЗаданияНаПеревозкуГруза КАК втЗаданияНаПеревозкуГруза) КАК втМаксимальныйНомерВЦепиПоставок
	|	ПО втЗаданияНаПеревозкуГруза.НомерВЦепиПоставок = втМаксимальныйНомерВЦепиПоставок.НомерВЦепиПоставок
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|// Рейсы в которых выполнялись последние задания.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияПоРейсу.Рейс КАК Рейс,
	|	ЗаданияПоРейсу.Задание КАК Задание
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	(ВЫБРАТЬ
	|		упРейсЗадания.Ссылка КАК Рейс,
	|		упРейсЗадания.Задание КАК Задание
	|	ИЗ
	|		Документ.упРейс.Задания КАК упРейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставок
	|		ПО упРейсЗадания.Задание = втПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(
	|			,
	|			) КАК СтатусыРейсов
	|		ПО упРейсЗадания.Ссылка = СтатусыРейсов.Документ
	|	ГДЕ НЕ (СтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен))
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		упКорректировкаРейсаЗадания.Ссылка.Рейс КАК Рейс,
	|		упКорректировкаРейсаЗадания.Задание КАК Задание
	|	ИЗ
	|		Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставок
	|		ПО упКорректировкаРейсаЗадания.Задание = втПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза
	|	ГДЕ упКорректировкаРейсаЗадания.Ссылка.Проведен) КАК ЗаданияПоРейсу
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|// Тара разгруженная в последней цепи поставки,
	|// вся она будет возвращена в адрес отправления заявки.
	|ВЫБРАТЬ
	|	РаботыПоРейсу.ГрузовоеМесто КАК ГрузовоеМесто,
	|	РаботыПоРейсу.КоличествоГрузов КАК КоличествоГрузов,
	|	РаботыПоРейсу.Задание КАК Задание,
	|	втПоследнееЗаданиеВЦепиПоставок.АдресПолучения КАК АдресОтправления,
	|	втПоследнееЗаданиеВЦепиПоставок.Получатель КАК Отправитель,
	|	втПоследнееЗаданиеВЦепиПоставок.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втГрузовыеМестаДляВозврата
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		(Рейс, Задание) В (
	|				ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс,
	|				втРейсы.Задание КАК Задание
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК РаботыПоРейсу
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставок
	|	ПО РаботыПоРейсу.Задание = втПоследнееЗаданиеВЦепиПоставок.ЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втРейсы.Рейс КАК Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)) КАК МаршрутПоРейсу
	|	ПО МаршрутПоРейсу.Идентификатор = РаботыПоРейсу.Идентификатор
	|ГДЕ ИСТИНА
	|	И РаботыПоРейсу.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	И МаршрутПоРейсу.Адрес = втПоследнееЗаданиеВЦепиПоставок.АдресПолучения
	|	И РаботыПоРейсу.Выполнена
	|	И РаботыПоРейсу.ГрузовоеМесто.ТипГруза.ВидГруза = ЗНАЧЕНИЕ(Перечисление.упВидыГрузов.Тара)
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|// Проверяется наличие уже введенных заявок на возврат тары.
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	тбЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СтатусыЗаявок.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) КАК Статус
	|ПОМЕСТИТЬ втЗаявкиВозврата
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК тбЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(
	|		,
	|		) КАК СтатусыЗаявок
	|	ПО тбЗаявкаНаПеревозкуГруза.Ссылка = СтатусыЗаявок.Документ
	|ГДЕ ИСТИНА
	|	И тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
	|	И НЕ (тбЗаявкаНаПеревозкуГруза.Ссылка.ПометкаУдаления)
	|	И НЕ (ЕСТЬNULL(СтатусыЗаявок.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отказ),
	|			ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)))
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|// Заявки на возврат тары.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втПоследнееЗаданиеВЦепиПоставокТ.АдресПолучения КАК АдресОтправления,
	|	втПоследнееЗаданиеВЦепиПоставокТ.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаОтправления,
	|	втПоследнееЗаданиеВЦепиПоставокТ.КонтактноеЛицоПолучателя КАК КонтактноеЛицоОтправителя,
	|	втПоследнееЗаданиеВЦепиПоставокТ.КонтрагентПолучателя КАК КонтрагентОтправителя,
	|	втПоследнееЗаданиеВЦепиПоставокТ.Получатель КАК Отправитель,
	|	втГрузовыеМестаПоЗаявкеТ.АдресОтправления КАК АдресПолучения,
	|	втГрузовыеМестаПоЗаявкеТ.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМестаПоЗаявкеТ.КонтактноеЛицоОтправителя КАК КонтактноеЛицоПолучателя,
	|	втГрузовыеМестаПоЗаявкеТ.КонтрагентОтправителя КАК КонтрагентПолучателя,
	|	втГрузовыеМестаПоЗаявкеТ.Отправитель КАК Получатель,
	|	втЗаявкаНаПеревозкуГрузовТ.НомерЗаявки КАК НомерЗаявки,
	|	втЗаявкаНаПеревозкуГрузовТ.Валюта КАК Валюта,
	|	втЗаявкаНаПеревозкуГрузовТ.Заказчик КАК Заказчик,
	|	втЗаявкаНаПеревозкуГрузовТ.ЗонаОтправления КАК ЗонаПолучения,
	|	втЗаявкаНаПеревозкуГрузовТ.КонтактноеЛицоЗаказчика КАК КонтактноеЛицоЗаказчика,
	|	втЗаявкаНаПеревозкуГрузовТ.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	|	втЗаявкаНаПеревозкуГрузовТ.ОбщийГруз КАК ОбщийГруз,
	|	втЗаявкаНаПеревозкуГрузовТ.Организация КАК Организация,
	|	втЗаявкаНаПеревозкуГрузовТ.Соглашение КАК Соглашение,
	|	втПоследнееЗаданиеВЦепиПоставокТ.ЗонаПолучения КАК ЗонаОтправления,
	|	втЗаявкиВозвратаТ.Ссылка КАК ЗаявкаВозврата,
	|	втЗаявкиВозвратаТ.Статус КАК СтатусЗаявкиВозврата,
	|	втЗаявкаНаПеревозкуГрузовТ.ВидПеревозки КАК ВидПеревозки,
	|	втЗаявкаНаПеревозкуГрузовТ.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	втЗаявкаНаПеревозкуГрузовТ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГрузаОснование,
	|	втЗаявкаНаПеревозкуГрузовТ.Статус КАК Статус
	|ИЗ
	|	втГрузовыеМестаДляВозврата КАК втГрузовыеМестаДляВозвратаТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузовыеМестаПоЗаявке КАК втГрузовыеМестаПоЗаявкеТ
	|	ПО ИСТИНА
	|		И втГрузовыеМестаДляВозвратаТ.ГрузовоеМесто = втГрузовыеМестаПоЗаявкеТ.ГрузовоеМесто
	|		И втГрузовыеМестаДляВозвратаТ.ЗаявкаНаПеревозкуГруза = втГрузовыеМестаПоЗаявкеТ.ЗаявкаНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПоследнееЗаданиеВЦепиПоставок КАК втПоследнееЗаданиеВЦепиПоставокТ
	|	ПО втГрузовыеМестаДляВозвратаТ.Задание = втПоследнееЗаданиеВЦепиПоставокТ.ЗаданиеНаПеревозкуГруза
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЗаявкаНаПеревозкуГрузов КАК втЗаявкаНаПеревозкуГрузовТ
	|	ПО втГрузовыеМестаДляВозвратаТ.ЗаявкаНаПеревозкуГруза = втЗаявкаНаПеревозкуГрузовТ.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втЗаявкиВозврата КАК втЗаявкиВозвратаТ
	|	ПО втГрузовыеМестаДляВозвратаТ.ЗаявкаНаПеревозкуГруза = втЗаявкиВозвратаТ.ЗаявкаНаПеревозкуГруза
	|;
	|//{Запрос: 11 ////////////////////////////////////////
	|// Тара для заявки на возврат.
	|ВЫБРАТЬ
	|	втГрузовыеМестаДляВозвратаТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	втГрузовыеМестаДляВозвратаТ.Задание КАК Задание,
	|	втГрузовыеМестаДляВозвратаТ.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМестаДляВозвратаТ.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМестаДляВозвратаТ.Отправитель КАК Отправитель,
	|	втГрузовыеМестаДляВозвратаТ.ГрузовоеМесто.Код КАК Код
	|ИЗ
	|	втГрузовыеМестаДляВозврата КАК втГрузовыеМестаДляВозвратаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрузовыеМестаПоЗаявке КАК втГрузовыеМестаПоЗаявкеТ
	|	ПО втГрузовыеМестаДляВозвратаТ.ГрузовоеМесто = втГрузовыеМестаПоЗаявкеТ.ГрузовоеМесто
	|";

	     	
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексЗапросаЗаявка = 10;
	ИндексЗапросаГрузовыеМеста = 11;
		
	Если мсвРезультатЗапроса[ИндексЗапросаГрузовыеМеста].Пустой() Тогда
		Отказ = Истина;
		ТекстОшибки = Нстр("ru = 'Нет тары для возврата(Берется разгруженная в последнем задании тара).'");
		ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
	    Возврат Результат;
	КонецЕсли;
	
	ВыборкаЗаявка = мсвРезультатЗапроса[ИндексЗапросаЗаявка].Выбрать();
	тбзГрузовыеМеста = мсвРезультатЗапроса[ИндексЗапросаГрузовыеМеста].Выгрузить();
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Пока ВыборкаЗаявка.Следующий() Цикл
		
		Если Истина
			И ВыборкаЗаявка.Статус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
			И ВыборкаЗаявка.Статус <> Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично
		Тогда
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'На основании заявки в статусе ""%1"" нельзя сформировать заявку на возврат тары
								|(Статус должен быть ""Выполнена"" или ""Выполнена частично"").'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВыборкаЗаявка.Статус);
			ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
			
			Прервать;
			
		ИначеЕсли ЗначениеЗаполнено(ВыборкаЗаявка.СтатусЗаявкиВозврата) Тогда	
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'На основании заявки уже существует заявка на возврат тары 
								|%1'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВыборкаЗаявка.ЗаявкаВозврата);
			ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
	    		Прервать;
			
		ИначеЕсли ЗначениеЗаполнено(ВыборкаЗаявка.ЗаявкаНаПеревозкуГрузаОснование) Тогда	
			
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'На основании заявки на возврат тары нельзя сформировать заявку на возврат тары'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВыборкаЗаявка.ЗаявкаВозврата);
			ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
	    		Прервать;
			
		КонецЕсли;
		 		
		ДокументОбъект = Документы.упЗаявкаНаПеревозкуГруза.СоздатьДокумент();
		ДокументОбъект.Дата  = ТекущаяДатаСеанса();
		ДокументОбъект.Автор = ТекущийПользователь;
		ДокументОбъект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();

		ЗаполнитьЗначенияСвойств(ДокументОбъект, ВыборкаЗаявка);
	
		Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
			ДокументОбъект.ЧасовойПояс = упСервисныеФункции.ЧасовойПоясВидаПеревозки(ДокументОбъект.ВидПеревозки);	
		КонецЕсли;  
				
		Попытка
			РежимЗаписи = РежимЗаписиДокумента.Запись;
			ДокументОбъект.Записать(РежимЗаписи);
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
			Прервать;	
		КонецПопытки;

		
		ДокументОбъект.КонтрагентЗаказчика = Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(ДокументОбъект.Заказчик);	
		ДокументОбъект.КонтрагентПолучателя = ДокументОбъект.КонтрагентЗаказчика;	
		ДокументОбъект.ВидПеревозки = Справочники.упВидыПеревозок.ПодобратьВидПеревозки(ДокументОбъект.Ссылка);
		
		сткОтбор = Новый Структура();
		сткОтбор.Вставить("АдресОтправления");
		сткОтбор.Вставить("Отправитель");
		ЗаполнитьЗначенияСвойств(сткОтбор, ВыборкаЗаявка);
		
		КомментарийПоГрузу = "";
		
		ИскомыйГруз = тбзГрузовыеМеста.НайтиСтроки(сткОтбор);
		мсвКодыГрузов	= Новый Массив;
		Если ИскомыйГруз.Количество() Тогда
			Для каждого текГрузовоеМесто Из ИскомыйГруз Цикл
				НоваяСтрокаГрузовоеМесто	 = ДокументОбъект.ГрузовыеМеста.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаГрузовоеМесто, ВыборкаЗаявка);
				ЗаполнитьЗначенияСвойств(НоваяСтрокаГрузовоеМесто, текГрузовоеМесто);
				мсвКодыГрузов.Добавить(СтрШаблон(Нстр("ru = 'Возврат тары: %1; по заявке №%2'"),текГрузовоеМесто.Код, ВыборкаЗаявка.НомерЗаявки));
			КонецЦикла; 
		Иначе
			Продолжить;
		КонецЕсли;
		
		КомментарийПоГрузу = СтрСоединить(мсвКодыГрузов, Символы.ПС);
		ДокументОбъект.Комментарий = КомментарийПоГрузу;
		
		сткОтбор = Новый Структура;
		сткОтбор.Вставить("Организация");
		сткОтбор.Вставить("ВидПеревозки");
		сткОтбор.Вставить("Заказчик");
		сткОтбор.Вставить("Соглашение");
		сткОтбор.Вставить("АдресОтправления");
		сткОтбор.Вставить("АдресПолучения");
		сткОтбор.Вставить("ЗонаОтправления");
		сткОтбор.Вставить("ЗонаПолучения");	
		сткОтбор.Вставить("ВозвратПоЦепиПоставок");	
		сткОтбор.Вставить("Ссылка");	
		ЗаполнитьЗначенияСвойств(сткОтбор, ДокументОбъект);
	
		мсвДоступныхСхем = Документы.упЗаявкаНаПеревозкуГруза.ВернутьДоступныеСхемыМультимодальныхПеревозок(сткОтбор, Истина);
		
		Если мсвДоступныхСхем.Количество() Тогда
			Схема = мсвДоступныхСхем[0];
			сткРеквизитыСхемы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Схема, "АдресПолученияТарыДляВозвратнойЗаявки, АдресПолученияТарыДляВозвратнойЗаявки.ГеографическаяЗона");
			Если ЗначениеЗаполнено(сткРеквизитыСхемы.АдресПолученияТарыДляВозвратнойЗаявки) Тогда
				ДокументОбъект.АдресПолучения = сткРеквизитыСхемы.АдресПолученияТарыДляВозвратнойЗаявки;
				ДокументОбъект.ЗонаПолучения =  сткРеквизитыСхемы.АдресПолученияТарыДляВозвратнойЗаявкиГеографическаяЗона
			КонецЕсли;
			ДокументОбъект.СхемаМультимодальнойПеревозки = Схема;
			
			Для каждого ГрузовоеМесто Из ДокументОбъект.ГрузовыеМеста Цикл
				ГрузовоеМесто.СхемаМультимодальнойПеревозки = Схема;
				Если ЗначениеЗаполнено(сткРеквизитыСхемы.АдресПолученияТарыДляВозвратнойЗаявки) Тогда
					ГрузовоеМесто.АдресПолучения = сткРеквизитыСхемы.АдресПолученияТарыДляВозвратнойЗаявки;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось подобрать подходящую цепь поставок.'"),, "СхемаМультимодальнойПеревозки");
		КонецЕсли;
				
		Попытка
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			ДокументОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая);
			//Если Истина
			//	И ПроверитьЗаполнение
			//	И НЕ ДокументОбъект.ПроверитьЗаполнение()
			//Тогда
			//	Отказ = Истина;
			//	ТекстОшибки = НСтр("ru = 'Ошибка при проверке заполнения сформированной заявки'");
			//КонецЕсли;
			
			Если Не Отказ Тогда
				ДокументОбъект.Записать(РежимЗаписи);	
				Результат.Добавить(ДокументОбъект.Ссылка);
			КонецЕсли;
			
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла; 
	
	Если Отказ Тогда
		Если НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'При формировании заявки на возврат тары произошла ошибка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Формирование заявки на возврат тары", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаявкаНаПеревозкуГруза,, ТекстОшибки);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Функция формирует фактические доходы.
//
//   Параметры:
//     мсвЗаявокНаПеревозкуГрузов - Массив - ДокументСсылка.упЗаявкаНаПеревозкуГруза, Массив ссылок на документы.
//     ТекстОшибки - Строка - Представление ошибки.
//     Отказ - Булево - Флаг, фиксации выполненых действий.
//
//   Возвращаемое значение:
//     Массив - ДокументыСсылка - Массив созданных упФактическиеДоходы.
//  
Функция СформироватьФактическиеДоходы(мсвЗаявокНаПеревозкуГрузов, ТекстОшибки = "", Отказ) Экспорт
	
	текРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаявкаНаПеревозкуГруза.Организация КАК Организация,
	               |	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	               |	упЗаявкаНаПеревозкуГруза.Заказчик КАК Заказчик,
	               |	упЗаявкаНаПеревозкуГруза.Соглашение КАК Соглашение,
	               |	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	               |	упЗаявкаНаПеревозкуГруза.Подразделение КАК Подразделение
	               |ИЗ
	               |	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	               |ГДЕ
	               |	упЗаявкаНаПеревозкуГруза.Ссылка В(&мсвЗаявкиНаПеревозкуГруза)
	               |ИТОГИ ПО
	               |	Организация,
	               |	Заказчик,
	               |	Соглашение,
	               |	Подразделение,
	               |	КонтрагентЗаказчика";
	Запрос.УстановитьПараметр("мсвЗаявкиНаПеревозкуГруза", мсвЗаявокНаПеревозкуГрузов);
	текВыборкаОрганизация	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока текВыборкаОрганизация.Следующий() Цикл
		текВыборкаЗаказчик = текВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока текВыборкаЗаказчик.Следующий() Цикл
			текВыборкаСоглашение = текВыборкаЗаказчик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока текВыборкаСоглашение.Следующий() Цикл
				текВыборкаПодразделение = текВыборкаСоглашение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока текВыборкаПодразделение.Следующий() Цикл
									
					текФактическиеДоходы = Документы.упФактическиеДоходы.СоздатьДокумент();
					текФактическиеДоходы.Дата 			= ТекущаяДатаСеанса();
					текФактическиеДоходы.Организация	= текВыборкаПодразделение.Организация;
					текФактическиеДоходы.Подразделение	= текВыборкаПодразделение.Подразделение;
					текФактическиеДоходы.Заказчик		= текВыборкаПодразделение.Заказчик;
					текФактическиеДоходы.Соглашение		= текВыборкаПодразделение.Соглашение;
					
					текВыборкаКонтрагент = текВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока текВыборкаКонтрагент.Следующий() Цикл
						текФактическиеДоходы.КонтрагентЗаказчика	= текВыборкаКонтрагент.КонтрагентЗаказчика;
						
						текВыборкаЗаявка = текВыборкаКонтрагент.Выбрать();
						Если текВыборкаЗаявка.Количество() = 1 Тогда
							текФактическиеДоходы.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоОднойЗаявке;
							Если текВыборкаЗаявка.Следующий() Тогда
								текФактическиеДоходы.ЗаявкаНаПеревозкуГруза = текВыборкаЗаявка.ЗаявкаНаПеревозкуГруза;	
							КонецЕсли;
						Иначе	
							текФактическиеДоходы.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам;	
							Пока текВыборкаЗаявка.Следующий() Цикл
								НоваяСтрока = текФактическиеДоходы.ЗаявкиНаПеревозкуГруза.Добавить();
								НоваяСтрока.ЗаявкаНаПеревозкуГруза = текВыборкаЗаявка.ЗаявкаНаПеревозкуГруза;
							КонецЦикла;
						КонецЕсли;
						
						текФактическиеДоходы.ЗаполнитьСтатьямиДоходовПоЗаявкам();
						
						Попытка
							текФактическиеДоходы.Записать(РежимЗаписиДокумента.Проведение);
							текРезультат.Добавить(текФактическиеДоходы.Ссылка);
						Исключение
							Отказ = Истина;
							ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
							Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
								ТекстОшибки = ОписаниеОшибки();
							КонецЕсли;
							ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упФактическиеДоходы,, ТекстОшибки);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
						КонецПопытки;
					КонецЦикла;	
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	
	Возврат текРезультат;
	
КонецФункции

// Функция формирует фактические доходы по переданной таблице расходов.
//
// Параметры:
//  тбзРасходы	 - ТаблицаЗначений	 - таблица расходов
//  	* ЗаявкаНаПеревозкуГруза - ДокументСсылка.упЗаявкаНаПеревозкуГруза - заявка, основание.
//  	* Валюта - СправочникСсылка.Валюты - валюта.
//  	* Количество - Число - количество
//  	* СтавкаНДС - ПеречислениеСсылка.упСтавкиНДС - ставка НДС.
//  	* СтатьяДоходов - СправочникСсылка.упСтатьиДоходовРасходов - статья доходов.
//  	* Сумма - Число - сумма.
//  	* СуммаНДС - Число - сумма НДС.
//  	* Цена - Число - цена
//  ТекстОшибки - Строка			 - Представление ошибки.
//  Отказ		 - Булево			 - Флаг, фиксации выполненых действий.
// 
// Возвращаемое значение:
//  Массив - ДокументыСсылка - Массив созданных упФактическиеДоходы.
//
Функция СформироватьФактическиеДоходыПоРасходам(тбзРасходы, ТекстОшибки = "", Отказ) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	втРасходы.ЗаявкаНаПеревозкуГруза,
	               |	втРасходы.Валюта,
	               |	втРасходы.Количество,
	               |	втРасходы.СтавкаНДС,
	               |	втРасходы.СтатьяДоходов,
	               |	втРасходы.Сумма,
	               |	втРасходы.СуммаНДС,
	               |	втРасходы.Цена
	               |ПОМЕСТИТЬ втРасходы
	               |ИЗ
	               |	&втРасходы КАК втРасходы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упЗаявкаНаПеревозкуГруза.Организация КАК Организация,
	               |	упЗаявкаНаПеревозкуГруза.Заказчик КАК Заказчик,
	               |	упЗаявкаНаПеревозкуГруза.Соглашение КАК Соглашение,
	               |	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК КонтрагентЗаказчика,
	               |	втРасходы.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
				   |	втРасходы.ЗаявкаНаПеревозкуГруза КАК КоличествоЗаявок,
	               |	втРасходы.Валюта,
	               |	втРасходы.Количество,
	               |	втРасходы.СтавкаНДС,
	               |	втРасходы.СтатьяДоходов,
	               |	втРасходы.Сумма,
	               |	втРасходы.СуммаНДС,
	               |	втРасходы.Цена
	               |ИЗ
	               |	втРасходы КАК втРасходы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	               |		ПО втРасходы.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГруза.Ссылка
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КоличествоЗаявок)
	               |ПО
	               |	Организация,
	               |	Заказчик,
	               |	Соглашение,
	               |	КонтрагентЗаказчика";
	Запрос.УстановитьПараметр("втРасходы", тбзРасходы);
	ВыборкаОрганизация	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОрганизация.Следующий() Цикл
		ВыборкаЗаказчик = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗаказчик.Следующий() Цикл
			ВыборкаСоглашение = ВыборкаЗаказчик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСоглашение.Следующий() Цикл
			
				ФактическиеДоходыОбъект = Документы.упФактическиеДоходы.СоздатьДокумент();
				ФактическиеДоходыОбъект.Дата 			= ТекущаяДатаСеанса();
				ФактическиеДоходыОбъект.Организация	= ВыборкаСоглашение.Организация;
				ФактическиеДоходыОбъект.Заказчик		= ВыборкаСоглашение.Заказчик;
				ФактическиеДоходыОбъект.Соглашение		= ВыборкаСоглашение.Соглашение;
				
				ВыборкаКонтрагент = ВыборкаСоглашение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаКонтрагент.Следующий() Цикл
					ФактическиеДоходыОбъект.КонтрагентЗаказчика	= ВыборкаКонтрагент.КонтрагентЗаказчика;
					
					ВыборкаЗаявка = ВыборкаКонтрагент.Выбрать();
										
					Пока ВыборкаЗаявка.Следующий() Цикл
						НоваяСтрока = ФактическиеДоходыОбъект.Доходы.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗаявка);
					КонецЦикла;	
						
					Если ВыборкаКонтрагент.КоличествоЗаявок = 1 Тогда
						ФактическиеДоходыОбъект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоОднойЗаявке;
						Если ФактическиеДоходыОбъект.Доходы.Количество() > 0 Тогда
							ФактическиеДоходыОбъект.ЗаявкаНаПеревозкуГруза = ФактическиеДоходыОбъект.Доходы[0].ЗаявкаНаПеревозкуГруза;	
						КонецЕсли;
					Иначе	
						ФактическиеДоходыОбъект.СпособВводаФактическихДоходов = Перечисления.упСпособыВводаФактическихДоходов.ДоходыПоНесколькимЗаявкам;	
					КонецЕсли;
					
					Попытка
						ФактическиеДоходыОбъект.Записать(РежимЗаписиДокумента.Проведение);
						Результат.Добавить(ФактическиеДоходыОбъект.Ссылка);
					Исключение
						Отказ = Истина;
						ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
							ТекстОшибки = ОписаниеОшибки();
						КонецЕсли;
						ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упФактическиеДоходы,, ТекстОшибки);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
					КонецПопытки;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	
	Возврат Результат;
	
КонецФункции

// Функция - Сформировать корректировку этапов мультимодальной перевозки
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - 	 - 
//  ТекстОшибки			 - 	 - 
//  Отказ					 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция СформироватьКорректировкуЭтаповМультимодальнойПеревозки(ЗаявкаНаПеревозкуГруза,  ТекстОшибки = "", Отказ) Экспорт
	
	мсвРезультат = Новый Массив;
	
	КорректировкаЭтаповПеревозки = Документы.упКорректировкаЭтаповПеревозки.СоздатьДокумент();
	КорректировкаЭтаповПеревозки.Дата = ТекущаяДатаСеанса();
	КорректировкаЭтаповПеревозки.ЗаявкаНаПеревозкуГруза = ЗаявкаНаПеревозкуГруза;
	КорректировкаЭтаповПеревозки.Заполнить(ЗаявкаНаПеревозкуГруза);
	
	Попытка
		КорректировкаЭтаповПеревозки.Записать(РежимЗаписиДокумента.Проведение);
		мсвРезультат.Добавить(КорректировкаЭтаповПеревозки.Ссылка);
	Исключение
		Отказ = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упКорректировкаЭтаповПеревозки,, ТекстОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
	КонецПопытки;
	
	Возврат мсвРезультат;
	
КонецФункции

// Формируется или открывается существующий документ "Заявка на перевозку груза" на основании рейса 
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текстовое представлениеошибки.
//  Отказ  - Булево - Флаг что произошел отказ создания движения.
//
// Возвращаемое значение:
//   ДокументСсылка   - Ссылка на документ.
//
Функция СформироватьЗаявкуНаПеревозкуГрузаДляОперацииСКонтейнером(Рейс, ТекстОшибки = Неопределено, Отказ) Экспорт
	
	Результат = Неопределено;
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, "ВидПеревозки.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера, ВидПеревозки");
	Если Не сткРеквизиты.ВидПеревозкиТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера Тогда
		Отказ = Истина;
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Для вида перевозки:""%1"" не требуется формирование заявки на перевозку контейнера'"), сткРеквизиты.ВидПеревозки);
		Возврат Результат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(, ) КАК упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних
	               |		ПО упЗаявкаНаПеревозкуГруза.Ссылка = упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Документ
	               |ГДЕ
	               |	ИСТИНА
	               |	И НЕ упЗаявкаНаПеревозкуГруза.ПометкаУдаления
	               |	И упЗаявкаНаПеревозкуГруза.ДокументОснование = &Рейс
	               |	И ЕСТЬNULL(упСтатусыЗаявокНаПеревозкуГрузаСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая)) <> ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена)
	               |;
	               |
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	УпПеревозчикПоРейсу_СрезПоследнихТ.ТранспортноеСредство КАК ТранспортноеСредство
				|ИЗ
				|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
				|		,
				|		Рейс = &Рейс) КАК УпПеревозчикПоРейсу_СрезПоследнихТ
				|ГДЕ УпПеревозчикПоРейсу_СрезПоследнихТ.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)
				|";


	Запрос.УстановитьПараметр("Рейс", Рейс);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
		
	РезультатЗапросаНазначенКонтейнер = РезультатЗапроса[1];
	
	Если РезультатЗапросаНазначенКонтейнер.Пустой() Тогда
		ТекстОшибкиДляЖР = "";
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'В операции должен быть назначен контейнер'");
		упСервисныеФункции.ПредставленияОписанияОшибки(, ТекстОшибки, ТекстОшибкиДляЖР);
		ЗаписьЖурналаРегистрации(Документы.упЗаявкаНаПеревозкуГруза.СобытиеОшибка(НСтр("ru = 'Добавление'")), УровеньЖурналаРегистрации.Ошибка, ,Рейс, ТекстОшибкиДляЖР, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецЕсли;
	
	Если Не Отказ Тогда
		ВыборкаЗаявка = РезультатЗапроса[0].Выбрать();
		Если ВыборкаЗаявка.Следующий() Тогда
			Результат = ВыборкаЗаявка.Ссылка;
		Иначе	
			НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
			
			БлокировкаДанных = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПеревозчикПоРейсу");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
			
			ТекстОшибки = "";
			
			Попытка
				БлокировкаДанных.Заблокировать();
			Исключение
				ТекстОшибкиДляЖР = "";
				Отказ = Истина;
				упСервисныеФункции.ПредставленияОписанияОшибки(, ТекстОшибки, ТекстОшибкиДляЖР);
				ЗаписьЖурналаРегистрации(Документы.упЗаявкаНаПеревозкуГруза.СобытиеОшибка(НСтр("ru = 'ОшибкаБлокировки'")), УровеньЖурналаРегистрации.Ошибка, ,Рейс, ТекстОшибкиДляЖР, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецПопытки;
			
			Если Не Отказ Тогда
				
				НовыйДокумент = Документы.упЗаявкаНаПеревозкуГруза.СоздатьДокумент();
				НовыйДокумент.Автор = ПользователиКлиентСервер.ТекущийПользователь();
				НовыйДокумент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов;
				НовыйДокумент.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
				НовыйДокумент.Дата = ТекущаяДатаСеанса();
				НовыйДокумент.ДокументОснование = Рейс;
				НовыйДокумент.Заполнить(Рейс);
				Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
					НовыйДокумент.ЧасовойПояс = упСервисныеФункции.ЧасовойПоясВидаПеревозки(НовыйДокумент.ВидПеревозки);	
				КонецЕсли;  
				
				Отказ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовыйДокумент.ДополнительныеСвойства, "ОшибкаПриЗаполнении", Ложь);
				Если ТекстОшибки <> Неопределено Тогда
					ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовыйДокумент.ДополнительныеСвойства, "ТекстОшибки", Неопределено);
				КонецЕсли;
				
				Если Не Отказ Тогда
					Попытка
						НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
						Результат = НовыйДокумент.Ссылка;
					Исключение
						ТекстОшибкиДляЖР = "";
						Отказ = Истина;
						упСервисныеФункции.ПредставленияОписанияОшибки(НовыйДокумент, ТекстОшибки, ТекстОшибкиДляЖР);
						ЗаписьЖурналаРегистрации(Документы.упЗаявкаНаПеревозкуГруза.СобытиеОшибка(НСтр("ru = 'Добавление'")), УровеньЖурналаРегистрации.Ошибка, ,Рейс, ТекстОшибкиДляЖР, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
			
			Если Не Отказ Тогда
				ЗафиксироватьТранзакцию();
			Иначе	
				ОтменитьТранзакцию();
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции // СформироватьРегистрацияЗадолженностиПоДокументамРейса()


// Функция - Есть заявка на возврат
//
// Параметры:
//  ЗаявкаНаПеревозкуГруза	 - ДокументСсылка.упЗаявкаНаПеревозкуГруза	 - заявка
// 
// Возвращаемое значение:
//  Булево - Истина, если заявка есть
//
Функция ЕстьЗаявкаНаВозврат(ЗаявкаНаПеревозкуГруза) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	               |	тбЗаявкаНаПеревозкуГруза.Ссылка КАК Ссылка,
	               |	СтатусыЗаявок.Статус КАК Статус
	               |ИЗ
	               |	Документ.упЗаявкаНаПеревозкуГруза КАК тбЗаявкаНаПеревозкуГруза
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза.СрезПоследних(, ) КАК СтатусыЗаявок
	               |		ПО тбЗаявкаНаПеревозкуГруза.Ссылка = СтатусыЗаявок.Документ
	               |ГДЕ
	               |	ИСТИНА
	               |	И тбЗаявкаНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
	               |	И НЕ СтатусыЗаявок.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отказ), ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Отменена))";
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", ЗаявкаНаПеревозкуГруза);
	РезультатЗапроса	= Запрос.Выполнить();
	Возврат НЕ РезультатЗапроса.Пустой();
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидПеревозки");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Комментарий");
	Результат.Добавить("Менеджер");
	Результат.Добавить("УровеньКачестваДоставки");
	Результат.Добавить("КомментарийПоКачеству");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, РеквизитыЗапрещенныеКИзменениюПослеФормированияЗаданий());
	
	Возврат Результат;
	
КонецФункции

// Реквизиты, редактирование которых запрещено после наличия действующих заданий по заявке на перевозку
//
Функция РеквизитыЗапрещенныеКИзменениюПослеФормированияЗаданий() Экспорт
	
	Результат = Новый Массив;

	Результат.Добавить("Заказчик");
	Результат.Добавить("КонтрагентЗаказчика");
	Результат.Добавить("КонтактноеЛицоЗаказчика");
	Результат.Добавить("Соглашение");
	
	Результат.Добавить("Подразделение");
	Результат.Добавить("НаправлениеДеятельности");
	Результат.Добавить("ВидПеревозки");
	Результат.Добавить("Приоритет");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, РеквизитыПрисутствующиеВШапкеИВТабличнойЧасти());
	
	Возврат Результат;

КонецФункции

// Реквизиты, присутствующие в шапке и в табличной части "Грузовые места"
//
Функция РеквизитыПрисутствующиеВШапкеИВТабличнойЧасти() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ОтправлениеГрузаС");
	Результат.Добавить("ОтправлениеГрузаПо");
	Результат.Добавить("ПолучениеГрузаС");
	Результат.Добавить("ПолучениеГрузаПо");
	Результат.Добавить("АдресОтправления");
	Результат.Добавить("АдресПолучения");
	
	Результат.Добавить("Получатель");
	Результат.Добавить("КонтрагентПолучателя");
	Результат.Добавить("КонтактноеЛицоПолучателя");
	Результат.Добавить("ГрафикРаботыАдресаОтправления");
	
	Результат.Добавить("Отправитель");
	Результат.Добавить("КонтрагентОтправителя");
	Результат.Добавить("КонтактноеЛицоОтправителя");
	Результат.Добавить("ГрафикРаботыАдресаПолучения");
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ЗагрузкаДанныхИзФайла

// Получить массив реквизитов обработки.
//
// Возвращаемое значение:
//   Массив   - Перечислим имена реквизитов.
//
Функция ПолучитьСписокОбрабатываемыхРеквизитовОбъекта()

	Массив = Новый Массив;
	Массив.Добавить("АдресОтправления");
	Массив.Добавить("АдресПолучения");
	Массив.Добавить("Заказчик");
	Массив.Добавить("Отправитель");
	Массив.Добавить("Получатель");
	Массив.Добавить("Соглашение");
	
	Возврат Массив;

КонецФункции // ПолучитьСписокОбрабатываемыхРеквизитовОбъекта()

// Устанавливает параметры загрузки данных из файла
//
// Параметры:
//     Параметры - Структура - Список параметров. Поля:
//         * Заголовок - Строка - Заголовок окна.
//         * ОбязательныеКолонки - Массив - Список имен колонок обязательных для заполнения.
//         * ТипДанныхКолонки - Соответствие, Ключ - Имя колонки, Значение - Описание типа данных.
//
Процедура ОпределитьПараметрыЗагрузкиДанныхИзФайла(Параметры) Экспорт
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПростойШаблон = Обработки.ЗагрузкаДанныхИзФайла.ПолучитьМакет("ПростойШаблон");
	ОбластьЗаголовок = ПростойШаблон.ПолучитьОбласть("Заголовок");
		
	Позиция = 1;
	
	мсвИсключения = Новый Массив;
	мсвИсключения.Добавить("Автор");
	мсвИсключения.Добавить("ОбщийГруз");
	
	ИспользуютсяЗаявкиНаСпецтехнику = ПолучитьФункциональнуюОпцию("упИспользуютсяЗаявкиНаСпецтехнику"); 
	ИспользуютсяЗаявкиНаПассажирскиеПеревозки = ПолучитьФункциональнуюОпцию("упИспользуютсяЗаявкиНаПассажирскиеПеревозки"); 
	
	Если Истина
		И НЕ ИспользуютсяЗаявкиНаСпецтехнику
		И НЕ ИспользуютсяЗаявкиНаПассажирскиеПеревозки
	Тогда
		мсвИсключения.Добавить("ВидТранспортнойУслуги");
		мсвИсключения.Добавить("КоличествоРабочихСмен");
		мсвИсключения.Добавить("ПорядокИсполненияЗаявки");
	КонецЕсли;
	
	мсвНеОбязательныеПоля = Новый Массив;
	мсвНеОбязательныеПоля.Добавить("ВидПеревозки");
	мсвНеОбязательныеПоля.Добавить("ЗонаОтправления");
	мсвНеОбязательныеПоля.Добавить("ЗонаПолучения");
	
	ТипАдрес = Новый ОписаниеТипов("СправочникСсылка.упАдреса");
	ВидАдрес = Новый ОписаниеТипов("СправочникСсылка.упВидыАдресов");
	
	Для каждого текРеквизит Из Метаданные.Документы.упЗаявкаНаПеревозкуГруза.Реквизиты Цикл
		Если НЕ мсвИсключения.Найти(текРеквизит.Имя) = Неопределено Тогда
			Продолжить;	
		КонецЕсли;               
		
		Если текРеквизит.Тип = ТипАдрес Тогда			
			// Для каждого адреса создадим колонку с видом адреса.			
			текРеквизитИмя = "Вид_" + текРеквизит.Имя;
			текРеквизитСиноним = "Вид " + текРеквизит.Синоним;
			Параметры.ТипДанныхКолонки.Вставить(текРеквизитИмя, ВидАдрес);			
			ОбластьЗаголовок.ТекущаяОбласть.Имя = текРеквизитИмя;
			Если ЗначениеЗаполнено(текРеквизит.Синоним) Тогда 
				ОбластьЗаголовок.Параметры.Заголовок = текРеквизитСиноним;
			Иначе
				ОбластьЗаголовок.Параметры.Заголовок = текРеквизитИмя;
			КонецЕсли;
			Если текРеквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку И мсвНеОбязательныеПоля.Найти(текРеквизит.Имя) = Неопределено Тогда
				ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Истина);
			Иначе
				ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Ложь);
			КонецЕсли;
			ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = Обработки.ЗагрузкаДанныхИзФайла.упШиринаКолонкиПоТипу(ВидАдрес);
			ТабличныйДокумент.Присоединить(ОбластьЗаголовок);
			Позиция = Позиция + 1;
		КонецЕсли;
		
		Параметры.ТипДанныхКолонки.Вставить(текРеквизит.Имя, текРеквизит.Тип);
		
		ОбластьЗаголовок.ТекущаяОбласть.Имя = текРеквизит.Имя;
		Если ЗначениеЗаполнено(текРеквизит.Синоним) Тогда 
			ОбластьЗаголовок.Параметры.Заголовок = текРеквизит.Синоним;
		Иначе
			ОбластьЗаголовок.Параметры.Заголовок = текРеквизит.Имя;
		КонецЕсли;
		Если текРеквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку И мсвНеОбязательныеПоля.Найти(текРеквизит.Имя) = Неопределено Тогда
			ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Истина);
		Иначе
			ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Ложь);
		КонецЕсли;
		
		ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = Обработки.ЗагрузкаДанныхИзФайла.упШиринаКолонкиПоТипу(текРеквизит.Тип);
		
		ТабличныйДокумент.Присоединить(ОбластьЗаголовок);
		
		
		Позиция = Позиция + 1;
	КонецЦикла;
	
	мсвИсключения = Новый Массив;
	мсвИсключения.Добавить("ОбщийГрузПоЗаявке");
	мсвИсключения.Добавить("ДокументОснование");
	// Добавить код.
	
	Параметры.ТипДанныхКолонки.Вставить("ГМ_Код", Новый ОписаниеТипов("Строка"));    		
	
	ОбластьЗаголовок.ТекущаяОбласть.Имя 	= "ГМ_Код";
	ОбластьЗаголовок.Параметры.Заголовок 	= "ГМ_Номер" + текРеквизит.Синоним;
	ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Ложь);
	ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = Обработки.ЗагрузкаДанныхИзФайла.упШиринаКолонкиПоТипу(Новый ОписаниеТипов("Строка"));
	ТабличныйДокумент.Присоединить(ОбластьЗаголовок);
	
	Позиция = Позиция + 1;

	
	// Добавить остальные реквизиты.
	Для каждого текРеквизит Из Метаданные.Справочники.упГрузовыеМеста.Реквизиты Цикл
		Если НЕ мсвИсключения.Найти(текРеквизит.Имя) = Неопределено Тогда
			Продолжить;	
		КонецЕсли;

		Параметры.ТипДанныхКолонки.Вставить("ГМ_"+текРеквизит.Имя, текРеквизит.Тип);    		
		
		ОбластьЗаголовок.ТекущаяОбласть.Имя = "ГМ_" + текРеквизит.Имя;
		Если ЗначениеЗаполнено(текРеквизит.Синоним) Тогда 
			ОбластьЗаголовок.Параметры.Заголовок = "ГМ_" + текРеквизит.Синоним;
		Иначе
			ОбластьЗаголовок.Параметры.Заголовок = "ГМ_" + текРеквизит.Имя;
		КонецЕсли;
		Если текРеквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку Тогда
			ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Истина);
		Иначе
			ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Ложь);
		КонецЕсли;
		
		ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = Обработки.ЗагрузкаДанныхИзФайла.упШиринаКолонкиПоТипу(текРеквизит.Тип);
		
		ТабличныйДокумент.Присоединить(ОбластьЗаголовок);
	
		Позиция = Позиция + 1;
	КонецЦикла;
	
	Параметры.Вставить("Макет", ТабличныйДокумент);
	// Флаг "ДобавитьПолеРеквизитов" - указывает, в реквизит формы ТаблицаСопоставленияДанных добавить служебную колонку
	// "ТекущиеЗначенияСтроки" - с типом значение "СписокЗначений", в колонке хранить все текстовые значение полей загрузки, 
	// обрбаотчке в СопоставитьЗагружаемыеДанныеИзФайла() формат хранения: структура() Имя колонки, текстовое значение реквизита.
	Параметры.Вставить("ДобавитьПолеРеквизитов", Истина);
	
КонецПроцедуры

// Выполним поиск адреса в справочнике.
//
// Параметры:
//  Представление  - Строка - Текстовое представление адреса.
//
// Возвращаемое значение:
//   СправочникСсылка.упАдреса - ССылка на элемент справочника.
//
Функция ПолучитьАдрес(Представление,ВидАдреса=Неопределено)
	
	ТекущийВидАдреса = Справочники.упВидыАдресов.ПустаяСсылка();
	Если НЕ ВидАдреса=Неопределено И НЕ ПустаяСтрока(ВидАдреса) Тогда
		ТекущийВидАдреса = Справочники.упВидыАдресов.НайтиПоНаименованию(СокрЛП(ВидАдреса),Истина);
		Если НЕ ЗначениеЗаполнено(ТекущийВидАдреса) Тогда
			ОбъектВидАдреса = Справочники.упВидыАдресов.СоздатьЭлемент();
			ОбъектВидАдреса.Наименование = ВидАдреса;
			Попытка
				ОбъектВидАдреса.Записать();
				ТекущийВидАдреса = ОбъектВидАдреса.Ссылка;
			Исключение
				// Продолжаем заполнение.
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	ТекущийАдрес = Справочники.упАдреса.ПустаяСсылка();
	СписокЗначений = Справочники.упАдреса.СформироватьДанныеВыбораАдреса(Представление);
	Если СписокЗначений.Количество() Тогда
		ТекущийАдрес = СписокЗначений[0].Значение;
	Иначе
		
		#Если ВнешнееСоединение Тогда
			Структура = Неопределено; 
		#Иначе
			Структура = упГеокодированиеКлиентСервер.НайтиКоординатыАдреса(Представление);
		#КонецЕсли
		
		Если НЕ Структура = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	упАдреса.Ссылка
			|ИЗ
			|	Справочник.упАдреса КАК упАдреса
			|ГДЕ
			|	упАдреса.Широта = &Широта
			|	И упАдреса.Долгота = &Долгота";					
			Запрос.УстановитьПараметр("Долгота", Структура.Долгота);
			Запрос.УстановитьПараметр("Широта", Структура.Широта);					
			Выборка = Запрос.Выполнить().Выбрать();					
			Пока Выборка.Следующий() Цикл
				ТекущийАдрес = Выборка.Ссылка;
			КонецЦикла;				
		КонецЕсли;
		
		Если Структура = Неопределено ИЛИ НЕ ЗначениеЗаполнено(ТекущийАдрес) Тогда
			// 3.неудачное геокодирование - создаем новый адрес без координат.
			// 5.если нашли - подставляем найденный адрес, иначе - создаем новый.
			НовыйАдрес = Справочники.упАдреса.СоздатьЭлемент();
			НовыйАдрес.Наименование = Представление;
			НовыйАдрес.ПолноеНаименование = Представление;
			НовыйАдрес.ПолноеПредставление = Представление;
			НовыйАдрес.ВидАдреса = ТекущийВидАдреса;
			Если НЕ Структура = Неопределено Тогда
				НовыйАдрес.Долгота = Структура.Долгота;
				НовыйАдрес.Широта = Структура.Широта;
			КонецЕсли;
			Попытка
				НовыйАдрес.Записать();
			Исключение
				// Продолжаем заполнение.
			Конецпопытки;
			ТекущийАдрес = НовыйАдрес.Ссылка;						
		КонецЕсли;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Адрес",ТекущийАдрес);
	Структура.Вставить("ВидАдреса",ТекущийВидАдреса);
	
	Возврат Структура;

КонецФункции // ПолучитьАдрес()

// Выполним поиск партнера.
//
// Параметры:
//  Представление  - Строка - Строковое представление партнера.
//  РеквизитыУстановки  - Строка - Через запятую перечислить имена реквизитов, для заполнения значением ИСТИНА.
//
// Возвращаемое значение:
//  СправочникСсылка.упПартнеры - Ссылка на элемент.
//
Функция ПолучитьПартнера(Представление,РеквизитыУстановки = Неопределено)

	ТекущийПартнер = Справочники.упПартнеры.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|ГДЕ
	|	упПартнеры.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Представление);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекущийПартнер = Выборка.Ссылка;
	Иначе
		ОбъектПартнера = Справочники.упПартнеры.СоздатьЭлемент();
		ОбъектПартнера.Наименование = Представление;
		ОбъектПартнера.НаименованиеПолное = Представление;
		Если НЕ РеквизитыУстановки = Неопределено Тогда
			МассивР = СтрРазделить(РеквизитыУстановки,",");
			Для каждого ТекущийРеквизит Из МассивР Цикл			
				ОбъектПартнера[ТекущийРеквизит] = Истина;			
			КонецЦикла;			
		КонецЕсли;		
		Ошибка = Ложь;
		Попытка
			ОбъектПартнера.Записать();
		Исключение
			Ошибка = Истина;
		КонецПопытки;
		Если НЕ Ошибка Тогда
			ТекущийПартнер = ОбъектПартнера.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущийПартнер;

КонецФункции // ПолучитьПартнера()

// Производит сопоставление загружаемых данных с данными в ИБ.
//
// Параметры:
//   ЗагружаемыеДанные - ТаблицаЗначений - таблица значений с загружаемыми данными:
//     * СопоставленныйОбъект - СправочникСсылка - Ссылка на сопоставленный объект. Заполняется внутри процедуры.
//     * <другие колонки>     - Произвольный - Состав колонок соответствует макету "ЗагрузкаИзФайла".
//
Процедура СопоставитьЗагружаемыеДанныеИзФайла(ЗагружаемыеДанные) Экспорт
	
	Если НЕ ЗагружаемыеДанные.Колонки.Найти("ТекущиеЗначенияСтроки") = Неопределено Тогда
		
		МассивРеквизитов = ПолучитьСписокОбрабатываемыхРеквизитовОбъекта();
		
		СоответствиеЗначений = Новый Соответствие;
		
		Для каждого ТекущаяСтрока Из ЗагружаемыеДанные Цикл
			ТекущиеЗначенияСтроки = ТекущаяСтрока.ТекущиеЗначенияСтроки;
			Для каждого ЗначениеКолонки Из ТекущиеЗначенияСтроки Цикл
				ТекущаяСтруктура = ЗначениеКолонки.Значение;
				Если МассивРеквизитов.Найти(ТекущаяСтруктура.Имя) = Неопределено Тогда
					СоответствиеЗначений.Вставить(ТекущаяСтруктура.Имя,ТекущаяСтруктура.Значение);
					Продолжить;
				КонецЕсли;
				
				Если ТекущаяСтруктура.Имя = "АдресОтправления" И НЕ ПустаяСтрока(ТекущаяСтруктура.Значение) Тогда
					ИмяВидАдреса = "Вид_"+ТекущаяСтруктура.Имя;
					ВидАдреса = СоответствиеЗначений.Получить(ИмяВидАдреса);
					Структура = ПолучитьАдрес(ТекущаяСтруктура.Значение,ВидАдреса);
					АдресОтправления = Структура.Адрес;
					ВидАдреса = Структура.ВидАдреса;
					ТекущаяСтрока.АдресОтправления = АдресОтправления;
					ТекущаяСтрока.ЗонаОтправления = АдресОтправления.ГеографическаяЗона;
					Если НЕ ЗначениеЗаполнено(ТекущаяСтрока[ИмяВидАдреса]) И ЗначениеЗаполнено(ВидАдреса) Тогда
						ТекущаяСтрока[ИмяВидАдреса] = ВидАдреса;
					КонецЕсли;
				КонецЕсли;
				Если ТекущаяСтруктура.Имя = "АдресПолучения" И НЕ ПустаяСтрока(ТекущаяСтруктура.Значение) Тогда
					ИмяВидАдреса = "Вид_"+ТекущаяСтруктура.Имя;
					ВидАдреса = СоответствиеЗначений.Получить(ИмяВидАдреса);
					Структура = ПолучитьАдрес(ТекущаяСтруктура.Значение,ВидАдреса);
					АдресПолучения = Структура.Адрес;
					ВидАдреса = Структура.ВидАдреса;
					ТекущаяСтрока.АдресПолучения = АдресПолучения;
					ТекущаяСтрока.ЗонаПолучения = АдресПолучения.ГеографическаяЗона;
					Если НЕ ЗначениеЗаполнено(ТекущаяСтрока[ИмяВидАдреса]) И ЗначениеЗаполнено(ВидАдреса) Тогда
						ТекущаяСтрока[ИмяВидАдреса] = ВидАдреса;
					КонецЕсли;
				КонецЕсли;
				
				Если ТекущаяСтруктура.Имя = "Заказчик" И НЕ ПустаяСтрока(ТекущаяСтруктура.Значение) Тогда
					Заказчик = ПолучитьПартнера(ТекущаяСтруктура.Значение,"Заказчик,Отправитель,Получатель");
					ТекущаяСтрока.Заказчик = Заказчик;
				КонецЕсли;
				Если ТекущаяСтруктура.Имя = "Отправитель" И НЕ ПустаяСтрока(ТекущаяСтруктура.Значение) Тогда
					Отправитель = ПолучитьПартнера(ТекущаяСтруктура.Значение,"Отправитель,Получатель");
					ТекущаяСтрока.Отправитель = Отправитель;
				КонецЕсли;
				Если ТекущаяСтруктура.Имя = "Получатель" И НЕ ПустаяСтрока(ТекущаяСтруктура.Значение) Тогда
					Получатель = ПолучитьПартнера(ТекущаяСтруктура.Значение,"Отправитель,Получатель");
					ТекущаяСтрока.Получатель = Получатель;
				КонецЕсли;
				
				Если ТекущаяСтруктура.Имя = "Соглашение" 
					И НЕ ПустаяСтрока(ТекущаяСтруктура.Значение) И ЗначениеЗаполнено(ТекущаяСтрока.Заказчик) Тогда
					Соглашение = Справочники.упСоглашенияСКлиентами.ПустаяСсылка();
					
					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	упСоглашенияСКлиентами.Ссылка КАК Ссылка
					|ИЗ
					|	Справочник.упСоглашенияСКлиентами КАК упСоглашенияСКлиентами
					|ГДЕ
					|	упСоглашенияСКлиентами.Наименование = &Наименование
					|	И упСоглашенияСКлиентами.Владелец = &Партнер";
					
					Запрос.УстановитьПараметр("Наименование", ТекущаяСтруктура.Значение);
					Запрос.УстановитьПараметр("Партнер", ТекущаяСтрока.Заказчик);
					
					Выборка = Запрос.Выполнить().Выбрать();
					
					Если Выборка.Следующий() Тогда
						Соглашение = Выборка.Ссылка;
					Иначе
						ОбъектСоглашения = Справочники.упСоглашенияСКлиентами.СоздатьЭлемент();
						ОбъектСоглашения.Наименование = ТекущаяСтруктура.Значение;
						ОбъектСоглашения.Владелец = ТекущаяСтрока.Заказчик;						
						Ошибка = Ложь;
						Попытка
							ОбъектСоглашения.Записать();
						Исключение
							Ошибка = Истина;
						КонецПопытки;
						Если НЕ Ошибка Тогда
							Соглашение = ОбъектСоглашения.Ссылка;
						КонецЕсли;
					КонецЕсли;			
					
					ТекущаяСтрока.Соглашение = Соглашение;
				КонецЕсли;
			КонецЦикла;
			ТекущаяСтрока.ТекущиеЗначенияСтроки = Новый СписокЗначений;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Загрузка данных из файла.
//
// Параметры:
//   ЗагружаемыеДанные - ТаблицаЗначений с колонками:
//     * ОбъектСопоставления         - СправочникСсылка - Ссылка на сопоставленный объект.
//     * РезультатСопоставленияСтроки - Строка       - Cтатусом загрузки, возможны варианты: Создан, Обновлен, Пропущен.
//     * ОписаниеОшибки               - Строка       - расшифровка ошибки загрузки данных.
//     * Идентификатор                - Число        - Уникальный номер строки.
//     * <другие колонки>             - Произвольный - Строки за загружаемого файла в соответствие с макетом.
// ПараметрыЗагрузки                  - Структура    - Параметры загрузки.
//     * СоздаватьНовые               - Булево       - Требуется ли создавать новые элементы справочника.
//     * ОбновлятьСуществующие        - Булево       - Требуется ли обновлять элементы справочника.
// Отказ                              - Булево       - Отмена загрузки.
//
Процедура ЗагрузитьИзФайла(ЗагружаемыеДанные, ПараметрыЗагрузки, Отказ) Экспорт
	
	Если ПараметрыЗагрузки.СоздаватьНовые Тогда
		
		Для каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл
			
			Попытка
				НачатьТранзакцию();
				текДокумент = Документы.упЗаявкаНаПеревозкуГруза.СоздатьДокумент();
				текДокумент.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
				текДокумент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов;
											
				// Шапка.
				текДокумент.Дата	= ТекущаяДатаСеанса();
				текДокумент.УстановитьНовыйНомер();
				текДокумент.ОбщийГруз = Истина;
				ЗаполнитьЗначенияСвойств(текДокумент, СтрокаТаблицы);
				
				Если ПолучитьФункциональнуюОпцию("НеИспользоватьНесколькоОрганизаций") Тогда
     				текДокумент.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
				КонецЕсли; 
				Если ПолучитьФункциональнуюОпцию("упИспользуетсяОдинВидПеревозки") Тогда
     				текДокумент.ВидПеревозки = Справочники.упВидыПеревозок.Основной;
				КонецЕсли; 
				
				текДокументСсылка = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСсылку(Новый УникальныйИдентификатор);
				текДокумент.УстановитьСсылкуНового(текДокументСсылка);
				текДокумент.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая);
				
				Если НЕ ЗначениеЗаполнено(текДокумент.ЗонаОтправления) И ЗначениеЗаполнено(текДокумент.АдресОтправления) Тогда
					текДокумент.ЗонаОтправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текДокумент.АдресОтправления, "ГеографическаяЗона");
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(текДокумент.ЗонаПолучения) И ЗначениеЗаполнено(текДокумент.АдресПолучения) Тогда
					текДокумент.ЗонаПолучения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текДокумент.АдресПолучения, "ГеографическаяЗона");
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(текДокумент.КонтрагентЗаказчика) И ЗначениеЗаполнено(текДокумент.Заказчик) Тогда
					текДокумент.КонтрагентЗаказчика = Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(текДокумент.Заказчик);
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(текДокумент.КонтрагентОтправителя) И ЗначениеЗаполнено(текДокумент.Отправитель) Тогда
					текДокумент.КонтрагентОтправителя = Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(текДокумент.Отправитель);
				КонецЕсли;

				Если НЕ ЗначениеЗаполнено(текДокумент.КонтрагентПолучателя) И ЗначениеЗаполнено(текДокумент.Получатель) Тогда
					текДокумент.КонтрагентПолучателя = Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(текДокумент.Получатель);
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
					текДокумент.ЧасовойПояс = упСервисныеФункции.ЧасовойПоясВидаПеревозки(текДокумент.ВидПеревозки);	
				КонецЕсли;  
			
				СтрокаТаблицы.ОбъектСопоставления = текДокументСсылка;
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Создан";
				
				// Грузовое место.
				текГрузОбъект = Справочники.упГрузовыеМеста.СоздатьЭлемент();
				Если Не ЗначениеЗаполнено(СтрокаТаблицы.ГМ_Код) Тогда 
					текГрузОбъект.Код = Формат(текДокумент.Дата, "ДФ=гг") + СокрЛП(текДокумент.Номер);
				Иначе
					текГрузОбъект.Код               = СтрокаТаблицы.ГМ_Код;	
				КонецЕсли;
				
				текГрузОбъект.ДокументОснование 		  = текДокументСсылка;
				текГрузОбъект.ОценочнаяСтоимость          = СтрокаТаблицы.ГМ_ОценочнаяСтоимость;
				текГрузОбъект.Сумма                       = СтрокаТаблицы.ГМ_Сумма;
				текГрузОбъект.СуммаНДС                    = СтрокаТаблицы.ГМ_СуммаНДС;
				текГрузОбъект.БеречьОтВлаги               = СтрокаТаблицы.ГМ_БеречьОтВлаги;
				текГрузОбъект.БеречьОтИзлучения           = СтрокаТаблицы.ГМ_БеречьОтИзлучения;
				текГрузОбъект.БеречьОтНагрева             = СтрокаТаблицы.ГМ_БеречьОтНагрева;
				текГрузОбъект.Высота                      = СтрокаТаблицы.ГМ_Высота;
				текГрузОбъект.Ширина                      = СтрокаТаблицы.ГМ_Ширина;
				текГрузОбъект.Длина                       = СтрокаТаблицы.ГМ_Длина;
				текГрузОбъект.КлассОпасности              = СтрокаТаблицы.ГМ_КлассОпасности;
				текГрузОбъект.КоличествоЗагрузочныхМетров = ?(СтрокаТаблицы.ГМ_КоличествоЗагрузочныхМетров = 0, текДокумент.КоличествоЗагрузочныхМетров, СтрокаТаблицы.ГМ_КоличествоЗагрузочныхМетров);
				текГрузОбъект.Масса                       = ?(СтрокаТаблицы.ГМ_Масса = 0, текДокумент.Масса, СтрокаТаблицы.ГМ_Масса);
				текГрузОбъект.КоличествоМест              = ?(СтрокаТаблицы.ГМ_КоличествоМест = 0, текДокумент.КоличествоМест, СтрокаТаблицы.ГМ_КоличествоМест);
				текГрузОбъект.Объем                       = ?(СтрокаТаблицы.ГМ_Объем = 0, текДокумент.Объем, СтрокаТаблицы.ГМ_Объем);
				текГрузОбъект.ОбщийГрузПоЗаявке           = Истина;
				текГрузОбъект.ТемпературныйРежим          = СтрокаТаблицы.ГМ_ТемпературныйРежим;
				текГрузОбъект.ТипГруза                    = СтрокаТаблицы.ГМ_ТипГруза;
				текГрузОбъект.Хрупкий                     = СтрокаТаблицы.ГМ_Хрупкий;
				текГрузОбъект.Записать();
				
				текСтрока = текДокумент.ГрузовыеМеста.Добавить();
				текСтрока.ГрузовоеМесто = текГрузОбъект.Ссылка;
				текСтрока.АдресОтправления	        = СтрокаТаблицы.АдресОтправления;
				текСтрока.АдресПолучения	        = СтрокаТаблицы.АдресПолучения;
				текСтрока.Отправитель               = текДокумент.Отправитель;
				текСтрока.КонтактноеЛицоОтправителя = текДокумент.КонтактноеЛицоОтправителя; 
				текСтрока.ОтправлениеГрузаС         = текДокумент.ОтправлениеГрузаС;
				текСтрока.ОтправлениеГрузаПо        = текДокумент.ОтправлениеГрузаПо;
				текСтрока.КонтактноеЛицоОтправителя = текДокумент.КонтактноеЛицоОтправителя;
				текСтрока.Получатель                = текДокумент.Получатель;
				текСтрока.КонтактноеЛицоПолучателя  = текДокумент.КонтактноеЛицоПолучателя;
				текСтрока.КонтактноеЛицоПолучателя  = текДокумент.КонтактноеЛицоПолучателя;
				текСтрока.ПолучениеГрузаС           = текДокумент.ПолучениеГрузаС;
				текСтрока.ПолучениеГрузаПо          = текДокумент.ПолучениеГрузаПо;
				текСтрока.КонтрагентПолучателя      = текДокумент.КонтрагентПолучателя;
				текСтрока.СхемаМультимодальнойПеревозки = текДокумент.СхемаМультимодальнойПеревозки;
				текСтрока.КоличествоГрузов = СтрокаТаблицы.КоличествоГрузов;
				
				Попытка
					текДокумент.Записать(РежимЗаписиДокумента.Запись);
					Если ТранзакцияАктивна() Тогда
						// зафиксируем транзакцию, после начнем новую.
						ЗафиксироватьТранзакцию();
					КонецЕсли;
				Исключение
					// Продолжаем загрузку данных.
				КонецПопытки;				
				
				Если НЕ ТранзакцияАктивна() Тогда
					НачатьТранзакцию();
				КонецЕсли;
				
				Если Не текДокумент.ПроверитьЗаполнение() Тогда
					текДокумент.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена);	
					текДокумент.Записать();
				КонецЕсли; 
							
				ЗафиксироватьТранзакцию();
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упЗаданиеНаПеревозкуГруза,, ТекстОшибки);
				ОтменитьТранзакцию();
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
				СтрокаТаблицы.ОписаниеОшибки = НСтр("ru = 'Невозможна запись из-за некорректности данных'");
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция СобытиеОшибка(ПодСобытие = "") Экспорт
	Возврат упУправленияЗаявками.СобытиеОшибкаЗаявкаНаПеревозкуГруза(ПодСобытие);
КонецФункции

Процедура ОпределитьПоляШапкиЗаявкиПоГрузовыМестам(ЗаявкаОбъект) Экспорт
	
	Если ЗаявкаОбъект.ГрузовыеМеста.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	мсвРеквизитов = Документы.упЗаявкаНаПеревозкуГруза.РеквизитыПрисутствующиеВШапкеИВТабличнойЧасти();
	
	соотВариантыРеквизитов = Новый Соответствие;
	
	Для каждого текГруз Из ЗаявкаОбъект.ГрузовыеМеста Цикл
		Для каждого текРеквизит Из мсвРеквизитов Цикл
			мсвЗначений = соотВариантыРеквизитов.Получить(текРеквизит);
			Если мсвЗначений = Неопределено Тогда
				соотВариантыРеквизитов.Вставить(текРеквизит, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(текГруз[текРеквизит]));				
			Иначе
				мсвЗначений.Добавить(текГруз[текРеквизит]);
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла;
	
	Для каждого текВариант Из соотВариантыРеквизитов Цикл
		Если текВариант.Значение.Количество() > 1 Тогда
			мсвЗначений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(текВариант.Значение);
			Если мсвЗначений.Количество() = 1 Тогда
				ЗаявкаОбъект[текВариант.Ключ] = мсвЗначений[0];
			Иначе	
				ЗаявкаОбъект[текВариант.Ключ] = Неопределено;
			КонецЕсли; 
		Иначе
			ЗаявкаОбъект[текВариант.Ключ] = текВариант.Значение[0];
		КонецЕсли; 
	КонецЦикла; 
	
	мсвОбщихЗон = Справочники.упАдреса.ПолучитьОбщиеЗоныАдресов(соотВариантыРеквизитов.Получить("АдресОтправления"));
	Если мсвОбщихЗон.Количество() Тогда
		ЗаявкаОбъект.ЗонаОтправления = мсвОбщихЗон[0];
	КонецЕсли;	
	
	мсвОбщихЗон = Справочники.упАдреса.ПолучитьОбщиеЗоныАдресов(соотВариантыРеквизитов.Получить("АдресПолучения"));
	Если мсвОбщихЗон.Количество() Тогда
		ЗаявкаОбъект.ЗонаПолучения = мсвОбщихЗон[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Нет...
	
  //// Товарно-транспортная накладная
  //КомандаПечати = КомандыПечати.Добавить();
  //КомандаПечати.МенеджерПечати = "Документ.упЗаданиеНаПеревозкуГруза";
  //КомандаПечати.Идентификатор = "ТТН";
  //КомандаПечати.Представление = НСтр("ru = 'Товарно-транспортная накладная'");
  //КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
  
  
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Нет...
	
	//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
	//	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
	//	КоллекцияПечатныхФорм,
	//	"ТТН",
	//	НСтр("ru = 'Товарно-транспортная накладная'"),
	//	ПечатьТТН(МассивОбъектов, ОбъектыПечати),
	//	,
	//	"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН");
	//КонецЕсли;	
	
	

КонецПроцедуры // Печать()

#КонецОбласти

#Область КомандыВводаНаОсновании

Процедура УстановитьВидимостьКомандВводаНаОсновании(Форма, ДополнительныеПараметры) Экспорт
	
	ВедетсяУчетТары = Неопределено;
	
	Если ДополнительныеПараметры.ЭтоФормаОбъекта Тогда
		ВедетсяУчетТары = Форма.ВедетсяУчетТары;
	Иначе
		ВедетсяУчетТары = ПолучитьФункциональнуюОпцию("упВедетсяУчетТары");
	КонецЕсли;
	
	ЭтоПеревозкаГрузов = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		ЭтоПеревозкаГрузов = Форма.Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Список") Тогда
		Отбор = Форма.Список.Отбор;
		мсвЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Форма.Список.Отбор, "ВидТранспортнойУслуги");
		Если мсвЭлементыОтбора.Количество() > 0 Тогда
			Если Истина
				И мсвЭлементыОтбора[0].ПравоеЗначение = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов
				И мсвЭлементыОтбора[0].ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			Тогда
				ЭтоПеревозкаГрузов = Истина;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
	Если Не ЭтоПеревозкаГрузов Тогда
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("ОбработатьПрерыванияГрузовыхПотоков", , , ДополнительныеПараметры); 
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьЗаявкуНаВозвратГруза", , , ДополнительныеПараметры); 
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьЗадание", , , ДополнительныеПараметры); 
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьВходящуюПретензию", , , ДополнительныеПараметры); 
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьИсходящуюПретензию", , , ДополнительныеПараметры); 
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьФактическиеДоходы", , , ДополнительныеПараметры); 
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьЗаявкуНаВозвратТары", , , ДополнительныеПараметры); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ИнтерфейсДляРаботыСПодсистемойВзаимодействия

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Взаимодействия.

// Возвращает партнера и контактных лиц сделки.
// 
// Параметры:
//  Ссылка  - ДокументСсылка.упЗаявкаНаПеревозкуГруза - Ссылка на документ.
//
// Возвращаемое значение:
//   Массив   - Контакты - Ссылки на контакты.
//
Функция ПолучитьКонтакты(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоКонтактам();
	Запрос.УстановитьПараметр("Предмет",Ссылка);
	
	НачатьТранзакцию();
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Результат = Неопределено;
		Иначе
			Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контакт");
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Вовращает текст запроса по контактам.
//
// Параметры:
//  ТекстВременнаяТаблица  - Строка - Данные.
//  Объединить - Булево - Выпонлить.
//
// Возвращаемое значение:
//   Строка - Результат выполнения.
//
Функция ТекстЗапросаПоКонтактам(ТекстВременнаяТаблица = "", Объединить = Ложь) Экспорт
	
	ШаблонВыбрать = ?(Объединить,"ВЫБРАТЬ РАЗЛИЧНЫЕ","ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ");
	
	ТекстЗапроса = "
	|%ШаблонВыбрать%
	|	упЗаявкаНаПеревозкуГруза.Заказчик КАК Контакт " + ТекстВременнаяТаблица + "
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.Заказчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтактноеЛицоЗаказчика
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтактноеЛицоЗаказчика = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтактноеЛицоОтправителя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтактноеЛицоОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтактноеЛицоПолучателя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтактноеЛицоПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтактныеЛицаПартнеров.ПустаяСсылка))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтрагентОтправителя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.КонтрагентПолучателя
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.Отправитель
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкаНаПеревозкуГруза.Получатель
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка = &Предмет
	|	И (НЕ упЗаявкаНаПеревозкуГруза.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ШаблонВыбрать%",ШаблонВыбрать);
	
	Если Объединить Тогда
		
		ТекстЗапроса = "
		| ОБЪЕДИНИТЬ ВСЕ
		|" + ТекстЗапроса;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		ДокументСсылка = Неопределено;
		ВидТранспортнойУслуги = Неопределено;
		ЗначенияЗаполнения = Неопределено;
		
		// Открывается существующая заявка.
		Если Параметры.Свойство("Ключ", ДокументСсылка) Тогда
			ВидТранспортнойУслуги = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидТранспортнойУслуги"); 
			
		// Создается новая.	
		ИначеЕсли Истина
			И Параметры.Свойство("ЗначенияЗаполнения",ЗначенияЗаполнения)
			И ЗначенияЗаполнения.Свойство("ВидТранспортнойУслуги",ВидТранспортнойУслуги) Тогда
			
		// Копируется.	
		ИначеЕсли Истина
			И Параметры.Свойство("ЗначениеКопирования",ЗначенияЗаполнения) 
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЗначенияЗаполнения, "ВидТранспортнойУслуги") Тогда
			
			ВидТранспортнойУслуги =   ЗначенияЗаполнения.ВидТранспортнойУслуги
			
		КонецЕсли;
		
		Если ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ПеревозкаПассажиров") Тогда	
			СтандартнаяОбработка = Ложь;
			Параметры.Вставить("ПорядокИсполненияЗаявки", ПредопределенноеЗначение("Перечисление.упПорядокИсполненияЗаявок.ПутевымЛистом"));
			ВыбраннаяФорма = "ФормаДокументаПассажирскиеПеревозки";
			
		ИначеЕсли  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.УслугиСпецтехники") Тогда		
			СтандартнаяОбработка = Ложь;
			Параметры.Вставить("ПорядокИсполненияЗаявки", ПредопределенноеЗначение("Перечисление.упПорядокИсполненияЗаявок.ПутевымЛистом"));
			ВыбраннаяФорма = "ФормаДокументаСпецтехника";
			
		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаСписка" Тогда	
		
		ВидТранспортнойУслуги = Неопределено;

		Если Параметры.Свойство("ВидТранспортнойУслуги", ВидТранспортнойУслуги) Тогда
			
			Если  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ПеревозкаПассажиров") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаСпискаПассажирскиеПеревозки";
				
			ИначеЕсли  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.УслугиСпецтехники") Тогда		
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаСпискаСпецТехника";
				
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда	
		
		ВидТранспортнойУслуги = Неопределено;

		Если Параметры.Свойство("ВидТранспортнойУслуги", ВидТранспортнойУслуги) Тогда
			
			Если  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ПеревозкаПассажиров") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораПассажирскиеПеревозки";
				
			ИначеЕсли  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.УслугиСпецтехники") Тогда		
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораСпецТехника";
				
			КонецЕсли;

		КонецЕсли;
	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 