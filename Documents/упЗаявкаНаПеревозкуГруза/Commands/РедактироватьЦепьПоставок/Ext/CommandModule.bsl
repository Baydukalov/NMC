
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстОшибки = "";
	Если ВозможноРедактированиеЦепиПоставок(ТекстОшибки, ПараметрКоманды) Тогда
		ОткрытьФорму("Обработка.упРМПланированиеЗаданийНаПеревозку.Форма.ФормаРедактированияЭтаповПеревозки", Новый Структура("ЗаявкаНаПеревозку", ПараметрКоманды), ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	Иначе
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Ложь);	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ВозможноРедактированиеЦепиПоставок(ТекстОшибки, Ссылка)
	
	сткИменаРеквизитов = Новый Структура();
	сткИменаРеквизитов.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", "ВидПеревозки.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки");
	сткИменаРеквизитов.Вставить("ИспользуютсяМультимодальныеПеревозки", "ВидПеревозки.ИспользуютсяМультимодальныеПеревозки");
	сткИменаРеквизитов.Вставить("ДокументОснование", "ДокументОснование");
		
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, сткИменаРеквизитов);
	
	Если Не (сткРеквизиты.ИспользуютсяМультимодальныеПеревозки И сткРеквизиты.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки) Тогда
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Учет плана по этапам перевозки для документа ""%1"" не предусмотрен.'", Ссылка));
		Возврат Ложь;
	КонецЕсли;
	
	Статус = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатус(Ссылка);
	Если Не (Ложь
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется
		Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВПути)
	Тогда
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено редактирование цепи поставок по документу ""%1"". Документ находится в статусе ""%2""'", Ссылка, Статус));
		Возврат Ложь;
	КонецЕсли; 
	
	Если Истина
		И ЗначениеЗаполнено(сткРеквизиты.ДокументОснование)
		И ТипЗнч(сткРеквизиты.ДокументОснование) = Тип("ДокументСсылка.упРейс")
	Тогда
		ВидТранспортнойУслуги = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(сткРеквизиты.ДокументОснование, "ВидТранспортнойУслуги");
		Если ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено редактирование цепи поставок по документу ""%1"". Документ создан на основании ""Операции с контейнером""'", Ссылка));
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

