
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ПорядокИсполненияЗаявки = ПорядокИсполненияЗаявки(ПараметрКоманды);
	
	Если ПорядокИсполненияЗаявки = ПредопределенноеЗначение("Перечисление.упПорядокИсполненияЗаявок.Рейсом") Тогда
		
		Отказ 		= Ложь;
		ТекстОшибки = "";
		мсвФактическиеДоходы = СформироватьФактическиеДоходы(ПараметрКоманды, ТекстОшибки, Отказ);
		Если НЕ Отказ Тогда
			Для каждого текФактическиеДоходы Из мсвФактическиеДоходы Цикл
				ПараметрыФормы = Новый Структура("Ключ", текФактическиеДоходы);
				ОткрытьФорму("Документ.упФактическиеДоходы.Форма.ФормаДокумента", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
			КонецЦикла;
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Документ не сформирован.'"),, ТекстОшибки, БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		
		ОписаниеОшибки = Нстр("ru = 'Запрещено формировать фактические доходы по заявке с порядком исполнения ""путевым листом""'");
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ОписаниеОшибки, Истина);	
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьФактическиеДоходы(мсвЗаявокНаПеревозкуГрузов, ТекстОшибки, Отказ)

	мсвФактическиеДоходы = Документы.упЗаявкаНаПеревозкуГруза.СформироватьФактическиеДоходы(мсвЗаявокНаПеревозкуГрузов, ТекстОшибки, Отказ);
	
	Возврат мсвФактическиеДоходы;

КонецФункции

&НаСервере
Функция ПорядокИсполненияЗаявки(мсвЗаявок)
	
	ствПорядкиИсполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвЗаявок, "ПорядокИсполненияЗаявки");
	
	Результат = Перечисления.упПорядокИсполненияЗаявок.Рейсом;
	
	Для каждого ПорядкиИсполнения Из ствПорядкиИсполнения Цикл
		Если ПорядкиИсполнения.Значение = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом Тогда
			Результат = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;	
КонецФункции

#КонецОбласти 





