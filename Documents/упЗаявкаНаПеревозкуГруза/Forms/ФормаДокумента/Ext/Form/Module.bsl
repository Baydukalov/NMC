#Область ПеременныеМодуляФормы

&НаКлиенте
Перем РазрешитьАктивизациюСтроки;

&НаКлиенте
Перем РедактированиеГрузовыхМест; // признак используется для непосредственной записи измененных доп. реквизитов по грузовому месту.

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизитыУниверсальная");
	// Установка данного свойства нужна для возможности редактирования доп. реквизитов не только по документу, 
	// но и по грузовым местам.
	ДополнительныеПараметры.Вставить("ПроизвольныйОбъект"      , Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ИнициализироватьСтруктуруИнформационныеГруппы();
	
	упЗаявкаФормаДокумента.ПриСозданииНаСервереФормаДокумента(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ЭтаФорма.УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьКоличествоЗагрузочныхМетров = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
		
	Если УчитыватьЛинейныеРазмеры	Тогда
		Элементы.Декорация1.Видимость = Истина;
		Элементы.Декорация2.Видимость = Истина;
	Иначе
		Элементы.Декорация1.Видимость = Ложь;
		Элементы.Декорация2.Видимость = Ложь;
	КонецЕсли;
	
	РежимФормированияКонтейнеров = упРейс.ЭтоОперацияСКонтейнером(Объект.ДокументОснование);
	
	ПриСозданииПриЧтенииНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	упЗаявкаФормаДокумента.ПриСозданииПриЧтенииНаСервереИнициализацияНовогоОбъектаФормаДокумента(ЭтаФорма, Объект, Параметры);
		
	ГрузовыеМеста.Очистить();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда 
			
			// ВГХ.
			ШиринаГруза = Объект.ДокументОснование.Ширина;
			ДлинаГруза = Объект.ДокументОснование.Длина;
			ВысотаГруза = Объект.ДокументОснование.Высота;
			
			// Прочие характеристики.
			СписокСвойств = "КлассОпасности,ТемпературныйРежим,Хрупкий,БеречьОтВлаги,БеречьОтИзлучения,БеречьОтНагрева, ЗапрещеноСкладироватьДругНаДруга, ТипГруза";
			ЗаполнитьЗначенияСвойств(ЭтаФорма,Объект.ДокументОснование,СписокСвойств);
			
		КонецЕсли;
				
		// Если копирование
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ЗаполнитьСписокГрузов(Истина);	
		Иначе	
			Объект.ОбщийГруз = Истина;
		КонецЕсли;
						
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		УстановитьПараметрыВводаГруза(Истина, Истина);
	Иначе
		ЗаполнитьСписокГрузов();			
		УстановитьПараметрыВводаГруза(Ложь, Истина);
	КонецЕсли;	
	
	УстановитьЗаголовкиПоказателейЗагрузки();
	
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	ЭтаФорма.ПоказатьВсеТовары = Ложь;
	
	упЗаявкаФормаДокумента.ПриСозданииПриЧтенииНаСервереИнициализацияРеквизитовФормыФормаДокумента(ЭтаФорма, Объект, Параметры);
	
	// Качество
	ЭтаФорма.Элементы.КомментарийПоКачеству.Доступность = ЗначениеЗаполнено(Объект.УровеньКачестваДоставки);

	// Тара.
	ВедетсяУчетТары = ПолучитьФункциональнуюОпцию("упВедетсяУчетТары");
	ЭтаФорма.Элементы.ГрузовыеМестаВозвратнаяТара.Видимость = ВедетсяУчетТары;
	ЭтаФорма.Элементы.ГрузовыеМестаАдресВозврата.Видимость = ВедетсяУчетТары;
	
	УстановитьПараметрыВыбораАдресаОтправления(ЭтаФорма);
	УстановитьПараметрыВыбораАдресаПолучения(ЭтаФорма);
	УстановитьСтатусWMS();

	упВводНаОсновании.УстановитьВидимостьКоманд(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазрешитьАктивизациюСтроки = Истина;
	РедактированиеГрузовыхМест = Ложь;
	
	ЭтоНовыйДокумент = ЗначениеЗаполнено(Объект.Ссылка);
	ПриИзмененииПризнакаОбщегоГруза(Истина);
	
	упЗаявкаФормаДокументаКлиент.ФормаДокументаПриОткрытии(ЭтаФорма, Отказ);
	
	ОбновитьПредставлениеПоказателейЗагрузки();
	
	УстановитьДоступностьВыбораОбщейСхемы();
			
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ОбщийГруз И Не ЗначениеЗаполнено(ТипГруза) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан груз'"), , "ТипГруза",, Отказ);
	ИначеЕсли Не Объект.ОбщийГруз И ГрузовыеМеста.Количество() = 0 Тогда	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан список грузов'"), , "СписокГрузов",, Отказ);
	КонецЕсли;	
	
	упЗаявкаФормаДокумента.ОбработкаПроверкиЗаполненияНаСервереФормаДокумента(ЭтаФорма, Объект, Отказ, ПроверяемыеРеквизиты);
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// отредактированные доп. реквизиты по документу сразу переносятся в объект из формы, отредактированные доп. реквизиты по грузам - сразу записываются
	// СтандартныеПодсистемы.Свойства
	//УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	//Документы.упЗаявкаНаПеревозкуГруза.ОпределитьЗоны(ТекущийОбъект);
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ПроверяемыйСтатус = НовыйСтатус;
	Иначе
		ПроверяемыйСтатус = Статус;
	КонецЕсли;	
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ГрузМодифицирован", ГрузМодифицирован);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ГрузовыеМеста", Неопределено);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТоварныйСостав", Неопределено);
	
	Если ГрузМодифицирован Тогда 
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ОбменДаннымиЗагрузка", ПроверяемыйСтатус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена);
		
		Если ТекущийОбъект.ОбщийГруз Тогда 
			
			Если ГрузовыеМеста.Количество() > 0 Тогда
				СтрокаГрузовыеМеста = ГрузовыеМеста[0];
			Иначе	
				СтрокаГрузовыеМеста = ГрузовыеМеста.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтрокаГрузовыеМеста, ЭтаФорма);
			СтрокаГрузовыеМеста.Высота = ВысотаГруза;
			СтрокаГрузовыеМеста.Длина = ДлинаГруза;
			СтрокаГрузовыеМеста.Ширина = ШиринаГруза;
			СтрокаГрузовыеМеста.Валюта = ТекущийОбъект.Валюта;
			СтрокаГрузовыеМеста.КоличествоГрузов = 1;
			СтрокаГрузовыеМеста.Идентификатор = Новый УникальныйИдентификатор();
			
			СтрокаГрузовыеМеста.АдресПолучения = ТекущийОбъект.АдресПолучения;
			СтрокаГрузовыеМеста.ГрафикРаботыАдресаПолучения = ТекущийОбъект.ГрафикРаботыАдресаПолучения;
			СтрокаГрузовыеМеста.ПолучениеГрузаС = ТекущийОбъект.ПолучениеГрузаС;
			СтрокаГрузовыеМеста.ПолучениеГрузаПо	= ТекущийОбъект.ПолучениеГрузаПо;
			СтрокаГрузовыеМеста.Получатель = ТекущийОбъект.Получатель;
			СтрокаГрузовыеМеста.КонтактноеЛицоПолучателя = ТекущийОбъект.КонтактноеЛицоПолучателя;
			СтрокаГрузовыеМеста.КонтрагентПолучателя = ТекущийОбъект.КонтрагентПолучателя;
			
			СтрокаГрузовыеМеста.АдресОтправления = ТекущийОбъект.АдресОтправления;
			СтрокаГрузовыеМеста.ГрафикРаботыАдресаОтправления = ТекущийОбъект.ГрафикРаботыАдресаОтправления;
			СтрокаГрузовыеМеста.ОтправлениеГрузаС = ТекущийОбъект.ОтправлениеГрузаС;
			СтрокаГрузовыеМеста.ОтправлениеГрузаПо = ТекущийОбъект.ОтправлениеГрузаПо;
			СтрокаГрузовыеМеста.Отправитель = ТекущийОбъект.Отправитель;
			СтрокаГрузовыеМеста.КонтактноеЛицоОтправителя = ТекущийОбъект.КонтактноеЛицоОтправителя;
			СтрокаГрузовыеМеста.КонтрагентОтправителя = ТекущийОбъект.КонтрагентОтправителя;
			
			Для каждого СтрокаТовар Из ТоварныйСостав Цикл
				СтрокаТовар.Идентификатор = СтрокаГрузовыеМеста.Идентификатор;
			КонецЦикла;
			
		КонецЕсли;
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ГрузовыеМеста", ГрузовыеМеста);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ТоварныйСостав", ТоварныйСостав);
		
	КонецЕсли;	
	
	упЗаявкаФормаДокумента.ПередЗаписьюНаСервереФормаДокумента(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ПроверяемыйСтатус = НовыйСтатус;
	Иначе
		ПроверяемыйСтатус = Статус;
	КонецЕсли;	
	
	// Пока закомментировано, возможно это пригодится
	//Если ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению")
	//	ИЛИ ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания")
	//	ИЛИ ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс")
	//	ИЛИ ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется")
	//	ИЛИ ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВПути")
	//	ИЛИ ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена")
	//	ИЛИ ПроверяемыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично") Тогда 
	//	ПроверятьАдреса = Истина;
	//Иначе
		ПроверятьАдреса = Ложь;
	//КонецЕсли;
	
	Если ПроверяемыйСтатус <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена") Тогда 
		
		упОбработкаТабличнойЧастиКлиент.ПроверитьГрузовыеМеста(ЭтаФорма, Отказ, ПроверятьАдреса, РазличныеАдресаПолучения, РазличныеАдресаОтправления);
						
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	упЗаявкаФормаДокументаКлиент.ПослеЗаписиФормаДокумента(ЭтаФорма, Объект, ПараметрыЗаписи);
	НадписьПараметрыДокумента = "";
	ПослеЗаписиНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиНаКлиенте()
	ОбновитьПредставлениеПоказателейЗагрузки();
	ОбновитьДополнительныеРеквизитыПоГрузовомуМестуНаКлиенте();
	Элементы.НадписьПараметрыДокумента.Заголовок = "";
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	упЗаявкаФормаДокумента.ПослеЗаписиНаСервереФормаДокумента(ЭтаФорма, Объект, ТекущийОбъект, ПараметрыЗаписи);
		
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьКоличествоЗагрузочныхМетров);
	Если УчитыватьМассу Тогда
		МассаПредставление = сткПредставления.Масса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ОбъемПредставление = сткПредставления.Объем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		КоличествоМестПредставление = сткПредставления.КоличествоМест;
	КонецЕсли;
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетровПредставление = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
	Если Истина
		И ТекущийОбъект.ОбщийГруз
		И ГрузовыеМеста.Количество() > 0
	Тогда
		ГрузовоеМесто = ГрузовыеМеста[0].ГрузовоеМесто; 	
	КонецЕсли;
	
	упВводНаОсновании.УстановитьВидимостьКоманд(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	//	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "РедактированиеЭтаповПеревозки" Тогда
		ГрузовыеМеста.Очистить();
		ЗаполнитьСписокГрузов();
	КонецЕсли;
	
	упЗаявкаФормаДокументаКлиент.ОбработкаОповещенияФормаДокумента(ЭтаФорма, Объект, ИмяСобытия, Параметр, Источник);
			
	Если ИмяСобытия = "ЗаписьОтчетаИнкассатора" И Источник = Объект.Ссылка Тогда
		ЗаполнитьОтчетыИнкассаторов();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	//УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура УровеньКачестваДоставкиПриИзменении(Элемент)
	Элементы.КомментарийПоКачеству.Доступность = ЗначениеЗаполнено(Объект.УровеньКачестваДоставки);
КонецПроцедуры

&НаКлиенте
Процедура СхемаМультимодальнойПеревозкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	сткОтбор = Новый Структура;
	сткОтбор.Вставить("Организация");
	сткОтбор.Вставить("ВидПеревозки");
	сткОтбор.Вставить("Заказчик");
	сткОтбор.Вставить("Соглашение");
	сткОтбор.Вставить("АдресОтправления");
	сткОтбор.Вставить("АдресПолучения");
	сткОтбор.Вставить("ЗонаОтправления");
	сткОтбор.Вставить("ЗонаПолучения");	
	сткОтбор.Вставить("ВозвратПоЦепиПоставок");	
	сткОтбор.Вставить("Ссылка");	
	ЗаполнитьЗначенияСвойств(сткОтбор, Объект);

	мсвДоступныхСхем = ВернутьДоступныеСхемыМультимодальныхПеревозокНаСервере(сткОтбор);
	
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", мсвДоступныхСхем));
	ОткрытьФорму("Справочник.упСхемыМультимодальныхПеревозок.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("СхемаМультимодальнойПеревозкиНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОтправленияПриИзменении(Элемент)
	
	упЗаявкаФормаДокументаКлиент.АдресОтправленияПриИзмененииФормаДокумента(ЭтаФорма, Объект, Элемент);
	Если ЗначениеЗаполнено(Объект.АдресОтправления) Тогда
		Объект.ЗонаОтправления = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.АдресОтправления, "ГеографическаяЗона");
	КонецЕсли; 
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	ОбработатьИзменениеРеквизитаШапки("ГрафикРаботыАдресаОтправления");
//	
//	УстановитьПараметрыАдресаОтправления(Ложь);
//	
//	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПолученияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		Объект.ЗонаПолучения = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.АдресПолучения, "ГеографическаяЗона");
	КонецЕсли;
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	Если Истина
		И Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) 
		И ЗначениеЗаполнено(Объект.Получатель) 
		И ЗначениеЗаполнено(Объект.АдресПолучения) 
	Тогда
		Объект.ГрафикРаботыАдресаПолучения = ПолучитьГрафикРаботы(Объект.Получатель, Объект.АдресПолучения);
		ОбработатьИзменениеРеквизитаШапки("ГрафикРаботыАдресаПолучения");
	КонецЕсли;
	
	//УстановитьПараметрыАдресаПолучения(Ложь);
	//ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправлениеГрузаСПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановкаВременныхОконПоГрузам("ОтправлениеГрузаС");
	//ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтправлениеГрузаПоПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановкаВременныхОконПоГрузам("ОтправлениеГрузаПо");
	//ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрафикРаботыАдресаОтправленияПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановкаВременныхОконПоГрузам("ГрафикРаботыАдресаОтправления");
	//ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеГрузаСПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановкаВременныхОконПоГрузам("ПолучениеГрузаС");
	//ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеГрузаПоПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановкаВременныхОконПоГрузам("ПолучениеГрузаПо");
	//ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрафикРаботыАдресаПолученияПриИзменении(Элемент)
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановкаВременныхОконПоГрузам("ГрафикРаботыАдресаПолучения");
	//ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объем = ШиринаГруза * ВысотаГруза * ДлинаГруза * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийГрузПриИзменении(Элемент)
	
	ПриИзмененииПризнакаОбщегоГруза();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПризнакаОбщегоГруза(ПриОткрытии = Ложь)
	
	// Установка видимости
	Элементы.ГруппаНомерИТип.Видимость = Объект.ОбщийГруз;
	Элементы.ГруппаВГХГруза.Видимость = Объект.ОбщийГруз;
	Элементы.ГруппаПрочиеХарактеристикиГруза.Видимость = Объект.ОбщийГруз;
	Элементы.ГрузовыеМеста.Видимость = НЕ Объект.ОбщийГруз;
	Элементы.ПоказатьВсеТовары.Видимость = НЕ Объект.ОбщийГруз;
	Элементы.ГруппаХарактеристикиГруза.Видимость = НЕ Объект.ОбщийГруз;
	Элементы.ПоказатьИнформациюПоГрузовомуМесту.Видимость = НЕ Объект.ОбщийГруз;
	
	ОбновитьПредставлениеПоказателейЗагрузки();
	
	// Текущая страница
	Если Объект.ОбщийГруз Тогда 
		Элементы.ГруппаОписаниеГруза.ТекущаяСтраница	= Элементы.ГруппаВГХГруза;
		Элементы.ГруппаТоварныйСоставГруза.Заголовок	= "Товарный состав";
		Элементы.ГруппаИнформацияОГрузе.Заголовок = "Информация о грузе";
		Элементы.ГруппаИнформацияОГрузе.ОтображатьЗаголовок = Истина;
		Элементы.ГруппаОписаниеГруза.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.ТоварныйСостав.ОтборСтрок = Неопределено;
		
	Иначе
		Элементы.ГруппаОписаниеГруза.ТекущаяСтраница	= Элементы.ГруппаТоварныйСоставГруза;
		Элементы.ГруппаТоварныйСоставГруза.Заголовок	= "Груз";
		//Элементы.ГруппаИнформацияОГрузе.Заголовок = "Грузовые места";
		Элементы.ГруппаИнформацияОГрузе.ОтображатьЗаголовок = Ложь;
		Элементы.ГруппаОписаниеГруза.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		ИзменитьОтображаемуюИнформацию();
	КонецЕсли;
	
	// При переключении тумблера
	Если Не ПриОткрытии Тогда 
		
		// Если произошел переход на страницу грузовых мест
		Если Не Объект.ОбщийГруз И (ЗначениеЗаполнено(ТипГруза) ИЛИ ТоварныйСостав.Количество()) Тогда 
			ГрузМодифицирован = Истина;
			
			//СтрокиГрузов    = СписокГрузов.ПолучитьЭлементы();
			КоличествоСтрок = ГрузовыеМеста.Количество();
			Если КоличествоСтрок = 0 Тогда 
				СтрокаГруза = ГрузовыеМеста.Добавить();
				СтрокаГруза.Идентификатор = Новый УникальныйИдентификатор;
				СтрокаГруза.КоличествоГрузов = 1;
				ЗаполнитьЗначенияСвойств(СтрокаГруза, Объект);
				
			ИначеЕсли КоличествоСтрок = 1 Тогда 
				СтрокаГруза = ГрузовыеМеста[0];    
			Иначе 
				СтрокаГруза = Неопределено;
			КонецЕсли;
			
			Если СтрокаГруза <> Неопределено Тогда 
				ЗаполнитьЗначенияСвойств(СтрокаГруза, ЭтотОбъект,, "Высота, Ширина");
				СтрокаГруза.Высота = ВысотаГруза;
				СтрокаГруза.Ширина = ШиринаГруза;
				СтрокаГруза.Длина = ДлинаГруза;
				Если ЗначениеЗаполнено(СтрокаГруза.ВидГруза) Тогда
					Если СтрокаГруза.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Груз") Тогда
						СтрокаГруза.ИндексКартинки = 0;			
					Иначе
						СтрокаГруза.ИндексКартинки = 1;			
					КонецЕсли;
				Иначе
					СтрокаГруза.ИндексКартинки = 0;			
				КонецЕсли;
				
				Для Каждого текСтрока Из ТоварныйСостав Цикл 
					текСтрока.Идентификатор = СтрокаГруза.Идентификатор;
					текСтрока.ГрузовоеМесто = СтрокаГруза.ГрузовоеМесто;
					текСтрока.Код = СтрокаГруза.Код;
				КонецЦикла;	
			КонецЕсли;	
			
		ИначеЕсли Объект.ОбщийГруз И ГрузовыеМеста.Количество() = 1  Тогда 
			ГрузМодифицирован = Истина;
	
			СтрокаГруза  = ГрузовыеМеста[0];
			
			Если Не ЗначениеЗаполнено(СтрокаГруза.ГрузовоеМесто) Или НЕ ЭтоОбщийГруз(СтрокаГруза.ГрузовоеМесто) Тогда 
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтрокаГруза,, "Высота, Ширина");
				ВысотаГруза = СтрокаГруза.Высота;
				ШиринаГруза = СтрокаГруза.Ширина;
				ДлинаГруза = СтрокаГруза.Длина;
				
				Если ЗначениеЗаполнено(ВидГруза) Тогда
					ЭтоГруз = НЕ ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.ИнкассируемаяЦенность");
					Если ЭтоГруз Тогда
						Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
					Иначе	
						Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаИнкассируемаяЦенность;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			ОбновитьДополнительныеРеквизитыПоГрузовомуМестуНаКлиенте();
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура БеречьОтНагреваПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура БеречьОтИзлученияПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура БеречьОтВлагиПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ХрупкийПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КлассОпасностиПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТемпературныйРежимПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЗагрузочныхМетровПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоМестПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура МассаПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипГрузаПриИзменении(Элемент)

	Если ЗначениеЗаполнено(ТипГруза) Тогда
		ЗаполнитьВГХГруза();	
	КонецЕсли;
	
	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура КодПриИзменении(Элемент)

	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеТоварыПриИзменении(Элемент)
	Если ПоказатьВсеТовары Тогда
		Элементы.ГруппаГрузыСтраницы.ТекущаяСтраница = Элементы.ГруппаТовары;
	Иначе	
		Элементы.ГруппаГрузыСтраницы.ТекущаяСтраница = Элементы.ГруппаГрузыТовары;	
	КонецЕсли;
	Элементы.ПоказатьИнформациюПоГрузовомуМесту.Видимость = НЕ ПоказатьВсеТовары;
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПриИзменении(Элемент)
	
	упЗаявкаФормаДокументаКлиент.ОтправительПриИзмененииФормаДокумента(ЭтаФорма, Объект, Элемент);
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	ОбработатьИзменениеРеквизитаШапки("КонтрагентОтправителя");
	ОбработатьИзменениеРеквизитаШапки("ГрафикРаботыАдресаОтправления");
	//УстановитьПараметрыАдресаОтправления(Ложь, Ложь, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОтправителяПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);

	//УстановитьПараметрыАдресаОтправления(Ложь, Ложь, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоОтправителяПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановитьПараметрыАдресаОтправления(Ложь, Ложь, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	
	Если Не ЗначениеЗаполнено(Объект.КонтрагентПолучателя) Тогда
		Объект.КонтрагентПолучателя = ПолучитьОсновногоКонтрагента(Объект.Получатель);	
		ОбработатьИзменениеРеквизитаШапки("КонтрагентПолучателя");
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) И ЗначениеЗаполнено(Объект.Получатель) И ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		Объект.ГрафикРаботыАдресаПолучения = ПолучитьГрафикРаботы(Объект.Получатель, Объект.АдресПолучения);
		ОбработатьИзменениеРеквизитаШапки("ГрафикРаботыАдресаПолучения");
	КонецЕсли; 

	//УстановитьПараметрыАдресаПолучения(Ложь, Ложь, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПолучателяПриИзменении(Элемент)
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановитьПараметрыАдресаПолучения(Ложь, Ложь, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоПолучателяПриИзменении(Элемент)
	
	ОбработатьИзменениеРеквизитаШапки(Элемент.Имя);
	//УстановитьПараметрыАдресаПолучения(Ложь, Ложь, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикПриИзменении(Элемент)
	
	упЗаявкаФормаДокументаКлиент.ЗаказчикПриИзмененииФормаДокумента(ЭтаФорма, Объект, Элемент);	

КонецПроцедуры

&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	Если ВедетсяВалютныйУчет Тогда
		УстановитьВалютуРеглУчета();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СуммаИнкассацииПланПриИзменении(Элемент)
	
	УстановитьПредставлениеСуммовыхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	
	УстановитьПредставлениеСуммовыхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОценочнаяСтоимостьПриИзменении(Элемент)
	
	ГрузМодифицирован = Истина;
	Объект.ОценочнаяСтоимость = ОценочнаяСтоимость;
	УстановитьПредставлениеСуммовыхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	Объект.СуммаИнкассацииПлан = Сумма;
	УстановитьПредставлениеСуммовыхПоказателей();
КонецПроцедуры

&НаКлиенте
Процедура ВозвратПоЦепиПоставокПриИзменении(Элемент)
	УстановитьПараметрыСхемыМультимодальнойПеревозки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаГруз Тогда
		РедактированиеГрузовыхМест = Истина;
		ОбновитьДополнительныеРеквизитыПоГрузовомуМестуНаКлиенте();
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДополнительно Тогда
		РедактированиеГрузовыхМест = Ложь;
		ПриСменеСтраницыДополнительныеРеквизиты();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СхемаМультимодальнойПеревозкиПриИзменении(Элемент)
	
	Если Объект.СхемаМультимодальнойПеревозки = ПредопределенноеЗначение("Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка") Тогда
		УстановитьПараметрыСхемыМультимодальнойПеревозки(Ложь,,Истина);	
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИнформациюПоГрузовомуМестуПриИзменении(Элемент)
	ИзменитьОтображаемуюИнформацию();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыПлановыеДоходы

&НаКлиенте
Процедура ПлановыеДоходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;

		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьИтоговуюСуммуДоходы();
	УстановитьПредставлениеСуммовыхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуДоходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСкидкаПроцентПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСуммаПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыЦенаПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыКоличествоПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСкидкаСуммаПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСтавкаНДСПриИзменении(Элемент)
 	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСтатьяДоходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
	СтатьяДоходов	= ТекущаяСтрока.СтатьяДоходов;
	Если ЗначениеЗаполнено(СтатьяДоходов) Тогда
		ТекущаяСтрока.СтавкаНДС = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяДоходов, "СтавкаНДС");
		ПересчитатьСтроку(Элемент);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТоварныйСостав

&НаКлиенте
Процедура ТоварныйСоставПриИзменении(Элемент)	
	ГрузМодифицирован = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставНоменклатураПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, ,Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставУпаковкаНоменклатурыПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставКоличествоПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСостав.ТекущиеДанные);
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставЦенаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставСтавкаНДСПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставСуммаПриИзменении(Элемент)
 	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаСуммаПриИзменении(Элементы.ТоварныйСостав.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если НЕ Объект.ОбщийГруз Тогда
		Если Элементы.ГрузовыеМеста.ТекущиеДанные = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сначала необходимо добавить грузовое место'"), Объект.Ссылка, "ГрузовыеМеста",,Отказ);
		ИначеЕсли Копирование Тогда 	
			ГрузМодифицирован = Истина;
			упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, ,Истина, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НЕ Объект.ОбщийГруз Тогда
		Если НоваяСтрока И НЕ Копирование Тогда
			ТекущиеДанные = Элементы.ГрузовыеМеста.ТекущиеДанные;
			Элемент.ТекущиеДанные.Идентификатор = ТекущиеДанные.Идентификатор;
			Элемент.ТекущиеДанные.ГрузовоеМесто	= ТекущиеДанные.ГрузовоеМесто;
			Элемент.ТекущиеДанные.Код			= ТекущиеДанные.Код;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПослеУдаления(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, ,Истина, Истина);
	упОбработкаТабличнойЧастиКлиент.ПересчитатьИтоговыеСуммы(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	упОбработкаТабличнойЧастиКлиент.ПересчитатьИтоговыеСуммы(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыГрузовыеМеста

&НаКлиенте
Процедура ГрузовыеМестаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	РазрешитьАктивизациюСтроки = Ложь;
	
	Если НоваяСтрока Тогда //И НЕ Копирование Тогда
		Если Копирование Тогда
			Элемент.ТекущиеДанные.ГрузовоеМесто = Неопределено;
			Элемент.ТекущиеДанные.Код = "";			
		Иначе
			Элемент.ТекущиеДанные.КоличествоГрузов = 1;
			Элемент.ТекущиеДанные.АдресОтправления = Объект.АдресОтправления;
			Элемент.ТекущиеДанные.АдресПолучения = Объект.АдресПолучения;
			Элемент.ТекущиеДанные.ГрафикРаботыАдресаОтправления = Объект.ГрафикРаботыАдресаОтправления;
			Элемент.ТекущиеДанные.ГрафикРаботыАдресаПолучения = Объект.ГрафикРаботыАдресаПолучения;
			Элемент.ТекущиеДанные.ОтправлениеГрузаС = Объект.ОтправлениеГрузаС;
			Элемент.ТекущиеДанные.ОтправлениеГрузаПо = Объект.ОтправлениеГрузаПо;
			Элемент.ТекущиеДанные.ПолучениеГрузаС = Объект.ПолучениеГрузаС;
			Элемент.ТекущиеДанные.ПолучениеГрузаПо = Объект.ПолучениеГрузаПо;	
			Элемент.ТекущиеДанные.Отправитель = Объект.Отправитель;	
			Элемент.ТекущиеДанные.КонтрагентОтправителя = Объект.КонтрагентОтправителя;	
			Элемент.ТекущиеДанные.КонтактноеЛицоОтправителя = Объект.КонтактноеЛицоОтправителя;	
			Элемент.ТекущиеДанные.Получатель = Объект.Получатель;	
			Элемент.ТекущиеДанные.КонтрагентПолучателя = Объект.КонтрагентПолучателя;	
			Элемент.ТекущиеДанные.КонтактноеЛицоПолучателя = Объект.КонтактноеЛицоПолучателя;	
			Элемент.ТекущиеДанные.СхемаМультимодальнойПеревозки = Объект.СхемаМультимодальнойПеревозки;	
			Элемент.ТекущиеДанные.Валюта = ВалютаУпрУчета;
		КонецЕсли; 
		Элемент.ТекущиеДанные.Идентификатор = Новый УникальныйИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриИзменении(Элемент)	
	
	ГрузМодифицирован = Истина;		
	
	Если НЕ Объект.ОбщийГруз Тогда
		НадписьПараметрыДокумента = НСтр("ru = 'Итоги по документу актуализируются после записи(*)'");
		ОбновитьПредставлениеПоказателейЗагрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	Объект.ОценочнаяСтоимость = ГрузовыеМеста.Итог("ОценочнаяСтоимость");
	ПересчитатьСуммуПлановойИнкассации();
	УстановитьПредставлениеСуммовыхПоказателей();
	Если ГрузМодифицирован Тогда
		Модифицированность = Истина;	
	КонецЕсли;
	
	РазрешитьАктивизациюСтроки = Истина;
	
	Если РазрешитьАктивизациюСтроки И РедактированиеГрузовыхМест Тогда
		ПодключитьОбработчикОжидания("ГрузовыеМестаПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	КонецЕсли;
	
	УстановитьДоступностьВыбораОбщейСхемы();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НЕ ОтменаРедактирования И ГрузовыеМеста.Количество()>1 Тогда
		Элементы.ОбщийГруз.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПослеУдаления(Элемент)
	ГрузМодифицирован = Истина;
	Если ГрузовыеМеста.Количество()<=1 Тогда
		Элементы.ОбщийГруз.Доступность = Истина;
	КонецЕсли;
	Модифицированность = Истина;
	ПересчитатьСуммуПлановойИнкассации();
	УстановитьПредставлениеСуммовыхПоказателей();
	УстановитьДоступностьВыбораОбщейСхемы();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПередУдалением(Элемент, Отказ)
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		текГруз = ГрузовыеМеста.НайтиПоИдентификатору(текИдентификатор);
		Если НЕ текГруз = Неопределено Тогда
        	мсвСтрок = ТоварныйСостав.НайтиСтроки(Новый Структура("Идентификатор", текГруз.Идентификатор));
			Для каждого текСтрока Из мсвСтрок Цикл
				ТоварныйСостав.Удалить(текСтрока);							
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриАктивизацииСтроки(Элемент)
	
	Если РазрешитьАктивизациюСтроки И РедактированиеГрузовыхМест Тогда
		ПодключитьОбработчикОжидания("ГрузовыеМестаПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаПриАктивизацииСтрокиОбработчикОжидания()
	
	ТекущиеДанныеГрузовыеМеста = Элементы.ГрузовыеМеста.ТекущиеДанные;
	
	Если Истина
		И РазрешитьАктивизациюСтроки
		И ТекущиеДанныеГрузовыеМеста <> Неопределено 
	Тогда
		РазрешитьАктивизациюСтроки = Ложь;
		Если ЗначениеЗаполнено(ТекущиеДанныеГрузовыеМеста.Идентификатор)	Тогда
			Элементы.ТоварныйСостав.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", ТекущиеДанныеГрузовыеМеста.Идентификатор);
		КонецЕсли;
		ТекущееГрузовоеМесто = ТекущиеДанныеГрузовыеМеста.ГрузовоеМесто;
		ОбновитьДополнительныеРеквизитыПоГрузовомуМесту();
		РазрешитьАктивизациюСтроки = Истина;		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаШиринаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, КоэффициентПересчетаОбъема);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаДлинаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, КоэффициентПересчетаОбъема);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаВысотаПриИзменении(Элемент)
	упОбработкаТабличнойЧастиКлиент.ГрузРазмерПриИзменении(Элементы.ГрузовыеМеста.ТекущиеДанные, КоэффициентПересчетаОбъема);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаОбъемОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Объем");
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаМассаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Масса");	
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаКоличествоПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаТипГрузаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ГрузовыеМеста.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ТипГруза) Тогда
		сткВГХГруза = ПолучитьВГХГруза(ТекущиеДанные.ТипГруза); 	
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, сткВГХГруза);
	КонецЕсли;
	Если НЕ ТекущиеДанные.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара") Тогда
		ТекущиеДанные.ВозвратнаяТара 	= Ложь;	
		ТекущиеДанные.АдресВозврата 	= ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
	ИначеЕсли ТекущиеДанные.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара") Тогда	
		АдресВозврата = ПолучитьАдресВозвратаСхемы(ТекущиеДанные.СхемаМультимодальнойПеревозки);
		Если ЗначениеЗаполнено(АдресВозврата) Тогда
			ТекущиеДанные.ВозвратнаяТара 	= Истина;	
			ТекущиеДанные.АдресВозврата 	= АдресВозврата;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаТипГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаСуммаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаСхемаМультимодальнойПеревозкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	сткОтбор = Новый Структура;
	сткОтбор.Вставить("Организация");
	сткОтбор.Вставить("ВидПеревозки");
	сткОтбор.Вставить("Заказчик");
	сткОтбор.Вставить("Соглашение");
	сткОтбор.Вставить("ЗонаОтправления");
	сткОтбор.Вставить("ЗонаПолучения");	
	ЗаполнитьЗначенияСвойств(сткОтбор, Объект);
	сткОтбор.Вставить("АдресОтправления", Элементы.ГрузовыеМеста.ТекущиеДанные.АдресОтправления);
	сткОтбор.Вставить("АдресПолучения", Элементы.ГрузовыеМеста.ТекущиеДанные.АдресПолучения);

	мсвДоступныхСхем = ВернутьДоступныеСхемыМультимодальныхПеревозокНаСервере(сткОтбор);
	сткПараметры = Новый Структура("Отбор", Новый Структура("Ссылка", мсвДоступныхСхем));
	ОткрытьФорму("Справочник.упСхемыМультимодальныхПеревозок.ФормаВыбора", сткПараметры,Элемент,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ГрузовыеМестаВозвратнаяТараПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ГрузовыеМеста.ТекущиеДанные;
	Если НЕ ТекущиеДанные.ВозвратнаяТара Тогда
		ТекущиеДанные.АдресВозврата = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыВесьТоварныйСостав

&НаКлиенте
Процедура ВесьТоварныйСоставПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если НЕ НоваяСтрока И ОтменаРедактирования Тогда
		ТекущиеДанные = Элементы.ВесьТоварныйСостав.ТекущиеДанные;
		ТекущиеДанные.ГрузовоеМесто = ТекущийГруз;
		ТекущийГруз			= Неопределено;
	КонецЕсли;
	Элементы.ТоварныйСостав.Обновить();	
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элементы.ВесьТоварныйСостав.ТекущиеДанные;
	Если НЕ ОтменаРедактирования Тогда
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ГрузовоеМесто) Тогда
			ТекстСообщения = Нстр("ru = 'Поле груз не заполнено'");
			текЭлементКоллекции = ТоварныйСостав.НайтиПоИдентификатору(Элементы.ВесьТоварныйСостав.ТекущаяСтрока);
			текНомерСтроки = ТоварныйСостав.Индекс(текЭлементКоллекции) + 1;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав",текНомерСтроки,"ГрузовоеМесто"),,Отказ);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Идентификатор) И НЕ Отказ Тогда
			ТекстСообщения = Нстр("ru = 'Груз не найден в табличной части ""грузовые места"", добавление запрещено'");
			текЭлементКоллекции = ТоварныйСостав.НайтиПоИдентификатору(Элементы.ВесьТоварныйСостав.ТекущаяСтрока);
			текНомерСтроки = ТоварныйСостав.Индекс(текЭлементКоллекции) + 1;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварныйСостав",текНомерСтроки,"ГрузовоеМесто"),,Отказ);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставГрузПриИзменении(Элемент)
	
	// Ведется поиск выбранного груза в таблице "грузовые места".
	// если он существует, то берется идентификатор строки грузового места.
	// если нет, то ошибка и идентификатор не присваивается.
	#Область ПрисвоитьСтрокеИдентификаторГруза
	ТекущиеДанные 		= Элементы.ВесьТоварныйСостав.ТекущиеДанные;
	текИдентификатор 	= ТекущиеДанные.Идентификатор;
	текНовыйГруз		= ТекущиеДанные.ГрузовоеМесто;
	Отказ = Ложь;
	
	Если ЗначениеЗаполнено(текИдентификатор) Тогда
		
		// Найти строку с текущим идентификатором в таблице Грузовые места
		мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("Идентификатор", текИдентификатор));
		
		Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
			
			текСтарыйГрузСтрокаГрузовыеМеста = мсвСтрокиГрузовыеМеста[0];
			текСтарыйГруз = текСтарыйГрузСтрокаГрузовыеМеста.ГрузовоеМесто;
			
			// Если груз поменялся
			Если НЕ текСтарыйГруз = текНовыйГруз Тогда
				
				мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", текНовыйГруз));
				
				текИдентификаторСтрокиНовогоГруза = Неопределено;
				
				// Если в "товарном составе" есть новый груз
				Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
					текНовыйГруз = мсвСтрокиГрузовыеМеста[0];
					ТекущиеДанные.Идентификатор 		= текНовыйГруз.Идентификатор;
					ТекущиеДанные.Код			 		= текНовыйГруз.Код;
					текИдентификаторСтрокиНовогоГруза 	= текНовыйГруз.ПолучитьИдентификатор();							
				Иначе
					Отказ = Истина;
				КонецЕсли;

				Если НЕ Отказ Тогда
					мсвСтрокиТоварныйСостав = ТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", текСтарыйГруз));
					
					// Если в "товарном составе" строки со старым грузовым местом нет
					Если мсвСтрокиТоварныйСостав.Количество() = 0 Тогда
						//	Удалить строку со старым грузовым местом из "грузовых мест"
						ГрузовыеМеста.Удалить(ГрузовыеМеста.Индекс(текСтарыйГрузСтрокаГрузовыеМеста));
					Иначе	
						// Пересчитать характеристики старого груза
						упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текСтарыйГрузСтрокаГрузовыеМеста.ПолучитьИдентификатор());
					КонецЕсли;
					
					// Пересчитать характеристики нового груза
					Если Не Отказ Тогда
						упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиНовогоГруза);
					КонецЕсли;
				КонецЕсли;
			
			КонецЕсли;
		КонецЕсли;
	Иначе	
		мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", текНовыйГруз));
		
		// Если текущий груз есть в "грузовых местах"
		Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
			текСтрокаГрузовыеМеста = мсвСтрокиГрузовыеМеста[0];
			ТекущиеДанные.Идентификатор = текСтрокаГрузовыеМеста.Идентификатор;
			ТекущиеДанные.Код			= текСтрокаГрузовыеМеста.Код;
			упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текСтрокаГрузовыеМеста.ПолучитьИдентификатор());	
		Иначе
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;
	Если НЕ Отказ Тогда
		ГрузМодифицирован = Истина;
	Иначе
		ТекущиеДанные.Идентификатор = Неопределено;	
	КонецЕсли;
	#КонецОбласти

КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПередУдалением(Элемент, Отказ)
	 ТекущийГруз = Элементы.ВесьТоварныйСостав.ТекущиеДанные.ГрузовоеМесто;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПослеУдаления(Элемент)
	Если НЕ ТекущийГруз = Неопределено Тогда
		мсвСтрокиТоварныйСостав = ТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", ТекущийГруз));
		Если мсвСтрокиТоварныйСостав.Количество() > 0 Тогда
			мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", ТекущийГруз));
			Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
				упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, мсвСтрокиГрузовыеМеста[0].ПолучитьИдентификатор());			
				ГрузМодифицирован = Истина;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	ТекущийГруз = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда
		ГрузМодифицирован = Истина;
		текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
		Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
			упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза, ,Истина, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставНоменклатураПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
	Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
		упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза, ,Истина, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставУпаковкаНоменклатурыПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
	Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
		упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставКоличествоПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
	текИдентификаторСтрокиГруза = ПолучитьИдентификаторСтрокиГрузовогоМеста();
	Если НЕ текИдентификаторСтрокиГруза = Неопределено Тогда
		упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, текИдентификаторСтрокиГруза);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставЦенаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставСтавкаНДСПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставСуммаПриИзменении(Элемент)
	ГрузМодифицирован = Истина;
	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаСуммаПриИзменении(Элементы.ВесьТоварныйСостав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ВесьТоварныйСоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущийГруз = Элементы.ВесьТоварныйСостав.ТекущиеДанные.ГрузовоеМесто;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьВидПеревозки(Команда)
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда 
		ТекстВопроса =
			НСтр("ru = 'Документ не записан. Выполнение команды
			           |""Подобрать вид перевозки"" возможно только после записи данных.
			           |
			           |Документ будут записан.'");
		Обработчик = Новый ОписаниеОповещения("ПодобратьВидПеревозкиПослеОтветаНаВопросЗаписать", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ОбщийГруз = Объект.ОбщийГруз;
	ПодобратьВидПеревозкиНаСервере();
	Если ОбщийГруз <> Объект.ОбщийГруз Тогда 
		ПриИзмененииПризнакаОбщегоГруза(Ложь);
	КонецЕсли;	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьНаправлениеДеятельности(Команда)
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда 
		ТекстВопроса =
			НСтр("ru = 'Документ не записан. Выполнение команды
			           |""Подобрать направление деятельности"" возможно только после записи данных.
			           |
			           |Документ будут записан.'");
		Обработчик = Новый ОписаниеОповещения("ПодобратьНаправлениеДеятельностиПослеОтветаНаВопросЗаписать", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ПодобратьНаправлениеДеятельностиНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьЦепьПоставок(Команда)
	
	ВыполнитьПодборЦепиПоставок();	
	Модифицированность = Истина;
	ГрузМодифицирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВременноеОкноОтправления(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ОтправлениеГрузаС", "ОтправлениеГрузаПо"), Новый ОписаниеОповещения("ЗавершениеРедактированияПериодаОтправления", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВременноеОкноПолучения(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПолучениеГрузаС", "ПолучениеГрузаПо"), Новый ОписаниеОповещения("ЗавершениеРедактированияПериодаПолучения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПоГрафикуРаботыТочкиОтправления(Команда)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ГрафикРаботы" , Объект.ГрафикРаботыАдресаОтправления);
		сткПараметры.Вставить("НачалоПериода", Объект.ОтправлениеГрузаС);
		сткПараметры.Вставить("КонецПериода" , Объект.ОтправлениеГрузаПо);
		сткПараметры.Вставить("ЦветФона"     , Элементы.ГруппаОтправление.ЦветФона); 
		
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораВременногоОкна", сткПараметры,,,,,Новый ОписаниеОповещения("ИзменитьВремяОтправленияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПоГрафикуРаботыТочкиПолучения(Команда)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ГрафикРаботы" , Объект.ГрафикРаботыАдресаПолучения);
		сткПараметры.Вставить("НачалоПериода", Объект.ПолучениеГрузаС);
		сткПараметры.Вставить("КонецПериода" , Объект.ПолучениеГрузаПо);
		сткПараметры.Вставить("ЦветФона"     , Элементы.ГруппаПолучение.ЦветФона); 
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораВременногоОкна", сткПараметры,,,,,Новый ОписаниеОповещения("ИзменитьВремяПолученияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОбъем(Команда)
	
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Объем");	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьМассу(Команда)
	
	упОбработкаТабличнойЧастиКлиент.ПересчитатьХарактеристикиГруза(ЭтаФорма, Элементы.ГрузовыеМеста.ТекущаяСтрока, "Масса");	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусНовая(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.НеЗагружена") Тогда 
		Если УстановитьСтатусНоваяСервер() Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
			ПослеЗаписиНаКлиенте();
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""Новая""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПодобранаЦепьПоставок(Команда)
	
	Если Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая") Тогда 
		Если УстановитьСтатусПодобранаЦепьПоставокСервер() Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
			ПослеЗаписиНаКлиенте();
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""Новая""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКВыполнению(Команда)
	
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		Если УстановитьСтатусКВыполнениюСервер() Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
			ПослеЗаписиНаКлиенте();
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не заполнен вид перевозки'"), БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусСогласована(Команда)
	
	Если Ложь
		Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Новая") 
		Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.НеСогласована")
		Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок")
	Тогда 
		Если УстановитьСтатусСогласованаСервер() Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
			ПослеЗаписиНаКлиенте();
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Статус заявки должен быть ""Новая"", ""Подобрана цепь поставок"" или ""Не согласована""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтменена(Команда)
	
	Если Истина
		И Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена")
		И Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично")
	Тогда 
		Если УстановитьСтатусОтмененаСервер() Тогда 
			ОповеститьОбИзменении(Объект.Ссылка);
			ПослеЗаписиНаКлиенте();
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заявка выполнена, отмена невозможна'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТиповымиСтатьямиДоходов(Команда)
	
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьТиповымиСтатьямиДоходовКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДоходы(Команда)
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		РассчитатьДоходыНаСервере();
		РассчитатьИтоговуюСуммуДоходы();
		УстановитьПредставлениеСуммовыхПоказателей();
		//РассчитатьПроцентСкидкиИтого();
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с клиентом'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект.Ссылка, "Объект.Соглашение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДоходыСРасшифровкой(Команда)
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		мсвПриемников = РассчитатьДоходыСРасшифровкойНаСервере();
		РассчитатьИтоговуюСуммуДоходы();
		//РассчитатьПроцентСкидкиИтого();
		УстановитьПредставлениеСуммовыхПоказателей();
		Для каждого Приемник Из мсвПриемников Цикл
			Приемник.Приемник.Показать("Расшифровка расчетов");	
		КонецЦикла;
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с клиентом'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,Объект.Ссылка,"Объект.Соглашение");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	Если РедактированиеГрузовыхМест = Истина И ЗначениеЗаполнено(ТекущееГрузовоеМесто) Тогда
		ПеренестиЗначенияДополнительныхРеквизитовВОбъект(Истина);
	Иначе
		ПеренестиЗначенияДополнительныхРеквизитовВОбъект();
	КонецЕсли; 
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПодобратьЦепьПоставокВТЧГрузы(Команда)
	ВыполнитьПодборЦепиПоставокДляТабличнойЧастиНаСервере();	
	УстановитьДоступностьВыбораОбщейСхемы();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСхемуМультимодальнойПеревозки(Команда)
	мсвВыделенныеСтроки = Элементы.ГрузовыеМеста.ВыделенныеСтроки;
	Для каждого Строка Из мсвВыделенныеСтроки Цикл
		СтрокаГрузовоеМесто = Элементы.ГрузовыеМеста.ДанныеСтроки(Строка);
		СтрокаГрузовоеМесто.СхемаМультимодальнойПеревозки = ПредопределенноеЗначение("Справочник.упСхемыМультимодальныхПеревозок.ПустаяСсылка");
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображаемуюИнформацию()
	
	Если ИнформационнаяГруппаГрузовыеМестаТекущая = сткИнформационныеГруппы.ИнформацияСкрыта Тогда
		Элементы.ГруппаТоварныйСостав.Видимость = Ложь;
		Элементы.ГруппаХарактеристикиГруза.Видимость = Ложь;
	ИначеЕсли ИнформационнаяГруппаГрузовыеМестаТекущая = сткИнформационныеГруппы.ТоварныйСостав Тогда
		Элементы.ГруппаТоварныйСостав.Видимость = Истина;
		Элементы.ГруппаХарактеристикиГруза.Видимость = Ложь;	
		Элементы.ГруппаСведениОГрузовыхМестах.ТекущаяСтраница = Элементы.ГруппаТоварныйСостав;
	ИначеЕсли ИнформационнаяГруппаГрузовыеМестаТекущая = сткИнформационныеГруппы.ХарактеристикиГруза Тогда		
		Элементы.ГруппаТоварныйСостав.Видимость = Ложь;
		Элементы.ГруппаХарактеристикиГруза.Видимость = Истина;
		Элементы.ГруппаСведениОГрузовыхМестах.ТекущаяСтраница = Элементы.ГруппаХарактеристикиГруза;
		ОбновитьДополнительныеРеквизитыПоГрузовомуМестуНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьИзменениеРеквизитаШапки(ИмяРеквизита)
	
	текЗначение = Объект[ИмяРеквизита];
	ПерезаполнитьРеквизитВТаблице = Ложь;
	Если ЗначениеЗаполнено(текЗначение) Тогда
		ПерезаполнитьРеквизитВТаблице = Истина;
	Иначе
		мсвЗначений = Новый Массив;
		Для каждого текГруз Из ГрузовыеМеста Цикл
			мсвЗначений.Добавить(текГруз[ИмяРеквизита]);
		КонецЦикла; 
		Если мсвЗначений.Количество() > 1 Тогда
			мсвЗначений = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗначений);
		КонецЕсли;
		Если мсвЗначений.Количество() = 1 Тогда
			ПерезаполнитьРеквизитВТаблице = Истина;
		КонецЕсли; 
	КонецЕсли;	
	Если ПерезаполнитьРеквизитВТаблице Тогда
		Для каждого текГруз Из ГрузовыеМеста Цикл
			текГруз[ИмяРеквизита] = текЗначение;
		КонецЦикла;
	КонецЕсли; 

	ГрузМодифицирован = Истина;
		
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаКлиенте
// Выполнить обращение на стороне клиента, для получения цепи поставок.
//
Процедура ВыполнитьПодборЦепиПоставок()

	сткОтбор = Новый Структура;
	сткОтбор.Вставить("Организация");
	сткОтбор.Вставить("ВидПеревозки");
	сткОтбор.Вставить("Заказчик");
	сткОтбор.Вставить("Соглашение");
	сткОтбор.Вставить("АдресОтправления");
	сткОтбор.Вставить("АдресПолучения");
	сткОтбор.Вставить("ЗонаОтправления");
	сткОтбор.Вставить("ЗонаПолучения");	
	сткОтбор.Вставить("ВозвратПоЦепиПоставок");	
	сткОтбор.Вставить("Ссылка");	
	ЗаполнитьЗначенияСвойств(сткОтбор, Объект);
	
	мсвДоступныхСхем = ВернутьДоступныеСхемыМультимодальныхПеревозокНаСервере(сткОтбор, Истина);
	
	Если мсвДоступныхСхем.Количество() Тогда
		Объект.СхемаМультимодальнойПеревозки = мсвДоступныхСхем[0];
		УстановитьПараметрыСхемыМультимодальнойПеревозки(Ложь);
	Иначе
		Объект.СхемаМультимодальнойПеревозки = "";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось подобрать подходящую цепь поставок.'"),, "СхемаМультимодальнойПеревозки");
	КонецЕсли;

КонецПроцедуры // ВыполнитьПодборЦепиПоставок()

&НаСервере
Процедура ВыполнитьПодборЦепиПоставокДляТабличнойЧастиНаСервере()
	
	тбзТаблицаСхем = Новый ТаблицаЗначений;
	тбзТаблицаСхем.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(12, 0, ДопустимыйЗнак.Неотрицательный)));
	тбзТаблицаСхем.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	тбзТаблицаСхем.Колонки.Добавить("ВидПеревозки", Новый ОписаниеТипов("СправочникСсылка.упВидыПеревозок"));
	тбзТаблицаСхем.Колонки.Добавить("Заказчик", Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
	тбзТаблицаСхем.Колонки.Добавить("Соглашение", Новый ОписаниеТипов("СправочникСсылка.упСоглашенияСКлиентами"));
	тбзТаблицаСхем.Колонки.Добавить("АдресОтправления", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	тбзТаблицаСхем.Колонки.Добавить("АдресПолучения", Новый ОписаниеТипов("СправочникСсылка.упАдреса"));
	тбзТаблицаСхем.Колонки.Добавить("ЗонаОтправления", Новый ОписаниеТипов("СправочникСсылка.упГеографическиеЗоны"));
	тбзТаблицаСхем.Колонки.Добавить("ЗонаПолучения", Новый ОписаниеТипов("СправочникСсылка.упГеографическиеЗоны"));
	тбзТаблицаСхем.Колонки.Добавить("СхемаМультимодальнойПеревозки", Новый ОписаниеТипов("СправочникСсылка.упСхемыМультимодальныхПеревозок"));
	тбзТаблицаСхем.Колонки.Добавить("ВозвратПоЦепиПоставок", Новый ОписаниеТипов("Булево"));
	
	Для каждого ИдентификаторСтроки Из Элементы.ГрузовыеМеста.ВыделенныеСтроки Цикл
		Строка =  ГрузовыеМеста.НайтиПоИдентификатору(ИдентификаторСтроки);
		НоваяСтрока = тбзТаблицаСхем.Добавить();
		НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
		НоваяСтрока.Организация = Объект.Организация;
		НоваяСтрока.ВидПеревозки = Объект.ВидПеревозки;
		НоваяСтрока.Заказчик = Объект.Заказчик;
		НоваяСтрока.Соглашение = Объект.Соглашение;
		НоваяСтрока.АдресОтправления = ?(ЗначениеЗаполнено(Строка.АдресОтправления),Строка.АдресОтправления,Объект.АдресОтправления);
		НоваяСтрока.АдресПолучения = ?(ЗначениеЗаполнено(Строка.АдресПолучения),Строка.АдресПолучения,Объект.АдресПолучения);
		НоваяСтрока.ЗонаОтправления = ?(ЗначениеЗаполнено(Строка.АдресОтправления), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.АдресОтправления, "ГеографическаяЗона"), Объект.ЗонаОтправления);
		НоваяСтрока.ЗонаПолучения = ?(ЗначениеЗаполнено(Строка.АдресПолучения), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.АдресПолучения, "ГеографическаяЗона"), Объект.ЗонаПолучения);
		НоваяСтрока.СхемаМультимодальнойПеревозки = Строка.СхемаМультимодальнойПеревозки;
		НоваяСтрока.ВозвратПоЦепиПоставок = Строка.ВозвратПоЦепиПоставок;
	КонецЦикла;
	
	тбзСхемы = Документы.упЗаявкаНаПеревозкуГруза.ЗначенияСхемМультимодальныхПеревозок(тбзТаблицаСхем, Истина);
	
	Для каждого ИдентификаторСтроки Из Элементы.ГрузовыеМеста.ВыделенныеСтроки Цикл
		мсвСтрокиСоСхемой = тбзСхемы.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ИдентификаторСтроки));	
		Строка = ГрузовыеМеста.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если мсвСтрокиСоСхемой.Количество() > 0 Тогда
			Строка.СхемаМультимодальнойПеревозки = мсвСтрокиСоСхемой[0].Схема;
		Иначе
			Строка.СхемаМультимодальнойПеревозки = Справочники.упСхемыМультимодальныхПеревозок.ПустаяСсылка();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось подобрать подходящую цепь поставок.'"), Объект.Ссылка, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ГрузовыеМеста", ИдентификаторСтроки + 1, "СхемаМультимодальнойПеревозки"));
		КонецЕсли;
		ГрузМодифицирован = Истина;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиПоказателейЗагрузки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
		МассаПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.Масса, ЕИ_Масса));
		Элементы.ГрузовыеМестаМасса.Заголовок = НСтр(СтрШаблон("ru = 'Масса (%1)'", ЕИ_Масса));
		Масса = Объект.Масса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
		ОбъемПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.Объем, ЕИ_Объем));
		Элементы.ГрузовыеМестаОбъем.Заголовок = НСтр(СтрШаблон("ru = 'Объем (%1)'", ЕИ_Объем));
		Объем = Объект.Объем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		ЕИ_Грузов = сткПредставления.КоличествоМест;
		КоличествоМестПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объект.КоличествоМест, ЕИ_Грузов));
		Элементы.ГрузовыеМестаКоличествоМест.Заголовок = НСтр(СтрШаблон("ru = 'Кол. мест (%1)'", ЕИ_Грузов));
		КоличествоМест = Объект.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетровПредставление = НСтр(СтрШаблон("ru = '%1'", Объект.КоличествоЗагрузочныхМетров));
		КоличествоЗагрузочныхМетров = Объект.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли;
	
	КоличествоГрузов = Объект.КоличествоГрузов;
	КоличествоГрузовыхМест = Объект.КоличествоГрузовыхМест;
	КоличествоГрузовПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузов));
	КоличествоГрузовыхМестПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузовыхМест));
		
КонецПроцедуры // УстановитьЗаголовкиПоказателейЗагрузки()

&НаКлиенте
Процедура УстановитьПредставлениеСуммовыхПоказателей() Экспорт
	
	Если ВедетсяВалютныйУчет Тогда
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.ОценочнаяСтоимость, ВалютаУпрУчета);
		ПлановыеДоходыСНДСПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаДоходы, ВалютаУпрУчета);
		ПлановыеДоходыНДСПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаДоходыНДС, ВалютаУпрУчета);
		СкидкаПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаДоходыСкидка, ВалютаУпрУчета);
	Иначе	
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.ОценочнаяСтоимость);
		ПлановыеДоходыСНДСПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаДоходы);
		ПлановыеДоходыНДСПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаДоходыНДС);
		СкидкаПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаДоходыСкидка);
	КонецЕсли;  
	СкидкаПредставление = СтрШаблон("%1 (%2%%)", Объект.СуммаДоходыСкидка, ?(Объект.СуммаДоходы = 0, 0, Окр(Объект.СуммаДоходыСкидка / Объект.СуммаДоходы *  100, 2)));
	
	СуммаИнкассацииПланПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(Объект.СуммаИнкассацииПлан, Объект.Валюта);
	
	ОценочнаяСтоимость = Объект.ОценочнаяСтоимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеПоказателейЗагрузки()
		
	Если УчитыватьМассу Тогда
		Масса = Объект.Масса;
		МассаПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Масса, ЕИ_Масса));
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		Объем = Объект.Объем;
		ОбъемПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объем, ЕИ_Объем));
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		КоличествоМест = Объект.КоличествоМест;
		КоличествоМестПредставление = НСтр(СтрШаблон("ru = '%1 %2'", КоличествоМест, ЕИ_Грузов));
	КонецЕсли;
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетров = Объект.КоличествоЗагрузочныхМетров;
		КоличествоЗагрузочныхМетровПредставление = НСтр(СтрШаблон("ru = '%1 %2'", КоличествоЗагрузочныхМетров, ЕИ_Метров));
	КонецЕсли;
	
	КоличествоГрузов = Объект.КоличествоГрузов;
	КоличествоГрузовыхМест = Объект.КоличествоГрузовыхМест;
	КоличествоГрузовПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузов));
	КоличествоГрузовыхМестПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузовыхМест));
	
КонецПроцедуры // ОбновитьПредставлениеПоказателейЗагрузки()

&НаСервереБезКонтекста
Функция ВернутьДоступныеСхемыМультимодальныхПеревозокНаСервере(ПараметрыОтбора, УпорядочитьПоВажности = Ложь)
	
	Возврат Документы.упЗаявкаНаПеревозкуГруза.ВернутьДоступныеСхемыМультимодальныхПеревозок(ПараметрыОтбора, УпорядочитьПоВажности);
	
КонецФункции // ВернутьДоступныеСхемыМультимодальныхПеревозокНаСервере()

&НаКлиенте
// Процедура завершения после выбора схемы мультимодальной перевозки.
//
Процедура СхемаМультимодальнойПеревозкиНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.СхемаМультимодальнойПеревозки = Результат;
		УстановитьПараметрыСхемыМультимодальнойПеревозки(Ложь);
	КонецЕсли;
	ГрузМодифицирован = Истина;
	
КонецПроцедуры // СхемаМультимодальнойПеревозкиНачалоВыбораЗавершение()

&НаСервереБезКонтекста
Функция ЭтоОбщийГруз(Груз)
	
	Возврат Груз.ОбщийГрузПоЗаявке;
	
КонецФункции	

&НаСервере
Процедура ЗаблокироватьЭлементы() Экспорт

	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Если Ложь
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отказ 
		Тогда 
			ТолькоПросмотр = Истина;
			
		ИначеЕсли Ложь 
			Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется 
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполнена
			ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВыполненаЧастично
		Тогда
			мсвСвойств = Новый Массив;
			мсвСвойств.Добавить(Новый Структура("Ключ, Значение", "ТолькоПросмотр", Истина));
			мсвСвойств.Добавить(Новый Структура("Ключ, Значение", "Доступность", Ложь));
		
			мсвИсключений = Новый Массив;
			мсвИсключений.Добавить("ГруппаСтатус"); 
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаШапка, мсвСвойств, мсвИсключений);
		
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаПеревозка, мсвСвойств);
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаГруз, мсвСвойств);
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаДополнительныеОперации, мсвСвойств);
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаПлановыеДоходы, мсвСвойств);
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаИнкассация, мсвСвойств); 
			
		//ИначеЕсли Ложь
		//	Или Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.СформированыЗадания
		//	ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ВключенаВРейс
		//	ИЛИ Статус = Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Выполняется 
		//Тогда 
		//	Для Каждого текЭлемент Из Элементы Цикл 
		//		Если Ложь
		//			Или ТипЗнч(текЭлемент) = Тип("ПолеФормы")
		//			ИЛИ ТипЗнч(текЭлемент) = Тип("ТаблицаФормы") 
		//		Тогда 
		//			текЭлемент.ТолькоПросмотр = Истина;
		//		КонецЕсли;
		//	КонецЦикла;	
		//	Элементы.РассчитатьМассу.Доступность               = Ложь;
		//	Элементы.РассчитатьОбъем.Доступность               = Ложь;
			
		КонецЕсли;	
	КонецЕсли;	
	
	Если Истина
		И НЕ ТолькоПросмотр
		И ЗначениеЗаполнено(Объект.Ссылка)
		И ВедетсяРаботаВРазныхЧасовыхПоясах
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
	Если Истина
		И НЕ ТолькоПросмотр
		И ЗначениеЗаполнено(Объект.Ссылка)
	Тогда
	
		ЕстьЗаявкаНаВозврат = Документы.упЗаявкаНаПеревозкуГруза.ЕстьЗаявкаНаВозврат(Объект.Ссылка);
		
		Элементы.Организация.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		Элементы.ВидПеревозки.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		Элементы.ВидПеревозкиКонтекстноеМенюПодобратьВидПеревозки.Доступность = НЕ ЕстьЗаявкаНаВозврат;
		Элементы.СхемаМультимодальнойПеревозки.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		Элементы.ПодобратьЦепьПоставок.Доступность = НЕ ЕстьЗаявкаНаВозврат;
		Элементы.ГрузовыеМестаПодобратьЦепьПоставокВТЧГрузы.Доступность = Истина
			                                                                 И РазличныеСхемыМультимодальнойПеревозкиДляГрузов
																И НЕ ЕстьЗаявкаНаВозврат;
		Элементы.АдресОтправления.ТолькоПросмотр = Ложь
		                                           Или ЕстьЗаявкаНаВозврат
										   Или РежимФормированияКонтейнеров;
		Элементы.АдресПолучения.ТолькоПросмотр = Ложь
		                                           Или ЕстьЗаявкаНаВозврат
										   Или РежимФормированияКонтейнеров;
		Элементы.ГрузовыеМестаАдресОтправления.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		Элементы.ГрузовыеМестаАдресПолучения.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		Элементы.ЗонаОтправления.ТолькоПросмотр = Ложь
		                                           Или ЕстьЗаявкаНаВозврат
										   Или РежимФормированияКонтейнеров;
		Элементы.ЗонаПолучения.ТолькоПросмотр = Ложь
		                                           Или ЕстьЗаявкаНаВозврат
										   Или РежимФормированияКонтейнеров;
		Элементы.ГрузовыеМестаАдресВозврата.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		Элементы.ГрузовыеМестаВозвратнаяТара.ТолькоПросмотр = ЕстьЗаявкаНаВозврат;
		
	КонецЕсли;
	
	Если Истина
		И НЕ ТолькоПросмотр
		И ЕстьИзмененияПлановыхЭтаповПеревозки 
	Тогда
		Элементы.ГрузовыеМеста.ТолькоПросмотр = Истина;
		Элементы.ГруппаГрузОтправление.ТолькоПросмотр = Истина;
		Элементы.ГруппаГрузПолучение.ТолькоПросмотр = Истина;
		Элементы.ГруппаОтправлениеПолучение.ТолькоПросмотр = Истина;
		Элементы.ГруппаЦепьПоставок.ТолькоПросмотр = Истина;
		Элементы.ГруппаЗаказчик.ТолькоПросмотр = Истина;
		Элементы.ГрузовыеМестаПредставлениеСтроки.Видимость = Истина;
		// Пока товарный состав заблокирован, потому что если одинаковых грузовых мест
		// несколько, то редактирование товарного состава одного грузового места, не приводит
		// к редактированию товарного состава другого грузового места, что неверно.
		//
		Элементы.ГруппаТоварныйСостав.ТолькоПросмотр = Истина; 
		
	Иначе	
		//ЗаблокироватьАдресПолучения(Истина);
		//ЗаблокироватьАдресОтправления(Истина);
		ЗаблокироватьАдресПолучения();
		ЗаблокироватьАдресОтправления();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	
	ОбщийГруз = Объект.ОбщийГруз;
	
	ВидПеревозкиПриИзмененииНаСервере();
	
	ЭтоНовыйДокумент = ЗначениеЗаполнено(Объект.Ссылка);

	Если ОбщийГруз <> Объект.ОбщийГруз Тогда 
		ПриИзмененииПризнакаОбщегоГруза(Ложь);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.СхемаМультимодальнойПеревозки) Тогда 
		ВыполнитьПодборЦепиПоставок();	
		Модифицированность = Истина;
	КонецЕсли;
	
	Если НЕ Элементы.ЗапрещеноИсполнениеНесколькимиРейсами.Доступность Тогда
		Объект.ЗапрещеноИсполнениеНесколькимиРейсами = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеДеятельностиПриИзменении(Элемент)
	
	НаправлениеДеятельностиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВводаГруза(ЭтоНовыйДокумент = Ложь, ЭтоОтрытиеФормы = Ложь)
	
	сткРеквизитыВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки,"ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке, 
	                                                                                           |ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке");
	РазличныеАдресаОтправления = сткРеквизитыВидаПеревозки.ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке;
	РазличныеАдресаПолучения = сткРеквизитыВидаПеревозки.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке;
	
	//
	//Если сткРеквизитыВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку Тогда 
	//	Элементы.ОбщийГруз.Доступность = Истина;
	//	Если Объект.ОбщийГруз И ЭтоНовыйДокумент Тогда 
	//		Объект.ОбщийГруз = Ложь;
	//	КонецЕсли;
	//Иначе	
	//	Объект.ОбщийГруз = Истина;
	//	Элементы.ОбщийГруз.Доступность = Ложь;
	//КонецЕсли;
	Если Ложь
		Или ГрузовыеМеста.Количество() > 1
		Или РежимФормированияКонтейнеров
	Тогда
		Элементы.ОбщийГруз.Доступность = Ложь;
	КонецЕсли; 
	
	// При выборе вида перевозки без услуг инкассации, удаляются соответствующие грузы  	
	Если Истина
		И НЕ ЭтоОтрытиеФормы
		И НЕ ИспользуютсяУслугиИнкассации
	Тогда
		мсвГрузовыеМестаИнкассации = ГрузовыеМеста.НайтиСтроки(Новый Структура("ВидГруза",Перечисления.упВидыГрузов.ИнкассируемаяЦенность));
		Для каждого СтрокаГрузовоеМестоИнкассации Из мсвГрузовыеМестаИнкассации Цикл
			ГрузовыеМеста.Удалить(СтрокаГрузовоеМестоИнкассации);
			ГрузМодифицирован = Истина;
		КонецЦикла;
	КонецЕсли;

	//УстановитьПараметрыАдресаОтправления(ЭтоНовыйДокумент, ЭтоОтрытиеФормы);
	//УстановитьПараметрыАдресаПолучения(ЭтоНовыйДокумент, ЭтоОтрытиеФормы);
	ЗаблокироватьАдресПолучения();
	ЗаблокироватьАдресОтправления();
	УстановитьПараметрыСхемыМультимодальнойПеревозки(ЭтоНовыйДокумент, ЭтоОтрытиеФормы);
		
КонецПроцедуры

// Процедура - Установить параметры схемы мультимодальной перевозки
//
// Параметры:
//  ЭтоНовыйДокумент - 	 - 
//  ЭтоОтрытиеФормы	 - 	 - 
//  Очистить		 - Булево	 - Истина, если произведена очистка схемы мультимодальной перевозки. 
//
&НаСервере
Процедура УстановитьПараметрыСхемыМультимодальнойПеревозки(ЭтоНовыйДокумент = Ложь, ЭтоОтрытиеФормы = Ложь, Очистить = Ложь)
	
	РазличныеСхемыМультимодальнойПеревозкиДляГрузов = Ложь;
	
	ПараметрыВводаИнформацииОГрузовыхМестах = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки,"ПараметрыВводаИнформацииОГрузовыхМестах");
	АдресВозврата = Справочники.упАдреса.ПустаяСсылка();

	Если Истина
		И РазличныеАдресаПолучения 
		И РазличныеАдресаОтправления 
		И ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку 
	Тогда 
		
		Если ЗначениеЗаполнено(Объект.СхемаМультимодальнойПеревозки) Тогда 
			
			АдресВозврата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СхемаМультимодальнойПеревозки, "АдресВозврата");
			
			Если Истина
				И Не ЭтоНовыйДокумент 
				И Не ЭтоОтрытиеФормы 
			Тогда 
				КоличествоСтрок = ГрузовыеМеста.Количество();
				Для Каждого текГруз Из ГрузовыеМеста Цикл 
					
					Если Не Очистить Тогда
						текГруз.СхемаМультимодальнойПеревозки = Объект.СхемаМультимодальнойПеревозки;	
					КонецЕсли;
					
					Если текГруз.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара") Тогда
						Если ЗначениеЗаполнено(АдресВозврата) Тогда
							текГруз.АдресВозврата = АдресВозврата;
							текГруз.ВозвратнаяТара = Истина;
						КонецЕсли;
					Иначе	
						текГруз.АдресВозврата = Справочники.упАдреса.ПустаяСсылка();
						текГруз.ВозвратнаяТара = Ложь;
					КонецЕсли;
					ГрузМодифицирован = Истина;
				КонецЦикла;
			КонецЕсли;
			
			Если Очистить Тогда
				РазличныеСхемыМультимодальнойПеревозкиДляГрузов = Истина;
			КонецЕсли;
		Иначе	
			РазличныеСхемыМультимодальнойПеревозкиДляГрузов = Истина;
		КонецЕсли;
		
		Если Не ЭтоОтрытиеФормы Тогда
			
			Если Ложь
				Или Не ЭтоНовыйДокумент
				Или Очистить 
			Тогда
				Для Каждого текГруз Из ГрузовыеМеста Цикл 
					Если Не ЭтоНовыйДокумент Тогда
						текГруз.ВозвратПоЦепиПоставок = Объект.ВозвратПоЦепиПоставок;
					КонецЕсли;
					
					Если Очистить Тогда
						текГруз.СхемаМультимодальнойПеревозки = Справочники.упСхемыМультимодальныхПеревозок.ПустаяСсылка();
					КонецЕсли;
					ГрузМодифицирован = Истина;
				КонецЦикла;	
			КонецЕсли;			
			
		КонецЕсли;
		
	Иначе	
		Если НЕ ЭтоОтрытиеФормы  Тогда
			
			Если ЗначениеЗаполнено(Объект.СхемаМультимодальнойПеревозки) Тогда 
				АдресВозврата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СхемаМультимодальнойПеревозки, "АдресВозврата");
			КонецЕсли;
						
			Для Каждого текГруз Из ГрузовыеМеста Цикл 
				текГруз.СхемаМультимодальнойПеревозки = ?(Очистить, Справочники.упСхемыМультимодальныхПеревозок.ПустаяСсылка(), Объект.СхемаМультимодальнойПеревозки);
				текГруз.ВозвратПоЦепиПоставок = ?(Очистить, Ложь, Объект.ВозвратПоЦепиПоставок);
				Если текГруз.ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.Тара") Тогда
					Если ЗначениеЗаполнено(АдресВозврата) Тогда
						текГруз.АдресВозврата = АдресВозврата;
						текГруз.ВозвратнаяТара = Истина;
					КонецЕсли;
				Иначе	
					текГруз.АдресВозврата = Справочники.упАдреса.ПустаяСсылка();
					текГруз.ВозвратнаяТара = Ложь;
				КонецЕсли;

				ГрузМодифицирован = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	
	Элементы.ГрузовыеМестаСхемаМультимодальнойПеревозки.Доступность = РазличныеСхемыМультимодальнойПеревозкиДляГрузов;
	Элементы.ГрузовыеМестаПодобратьЦепьПоставокВТЧГрузы.Доступность = РазличныеСхемыМультимодальнойПеревозкиДляГрузов;
	Элементы.ГрузовыеМестаКонтекстноеМенюОчиститьСхемуМультимодальнойПеревозки.Доступность = РазличныеСхемыМультимодальнойПеревозкиДляГрузов;
	Элементы.ГрузовыеМестаВозвратПоЦепиПоставок.Доступность = РазличныеСхемыМультимодальнойПеревозкиДляГрузов;
			
КонецПроцедуры

&НаСервере
Процедура ВидПеревозкиПриИзмененииНаСервере()
	упЗаявкаФормаДокумента.ВидПеревозкиПриИзмененииНаСервере(ЭтаФорма, Объект);
	УстановитьПараметрыВводаГруза(Ложь);
КонецПроцедуры

&НаСервере
Процедура НаправлениеДеятельностиПриИзмененииНаСервере()
	// Нет.
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоВидуПеревозки() Экспорт
	
	ИспользуютсяМультимодальныеПеревозки = Ложь;
	ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = Ложь;
	ИспользуютсяУслугиИнкассации = Ложь; 
	СпособУчетаИнкассируемыхЦенностей = Перечисления.упСпособыУчетаИнкассируемыхЦенностей.ПустаяСсылка();
	ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки = Ложь;
			
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		сткЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, 
																"ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза, 
																|ИспользуютсяМультимодальныеПеревозки, 
																|ИспользуютсяУслугиИнкассации, 
																|СпособУчетаИнкассируемыхЦенностей, 
																|ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки,
																|ПараметрыВводаИнформацииОГрузовыхМестах");
		ИспользуютсяМультимодальныеПеревозки = сткЗначенияРеквизитов.ИспользуютсяМультимодальныеПеревозки;
		ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза = сткЗначенияРеквизитов.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза;	
		ИспользуютсяУслугиИнкассации = сткЗначенияРеквизитов.ИспользуютсяУслугиИнкассации;
		СпособУчетаИнкассируемыхЦенностей = сткЗначенияРеквизитов.СпособУчетаИнкассируемыхЦенностей;
		ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки = сткЗначенияРеквизитов.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки;
		ПараметрыВводаИнформацииОГрузовыхМестах = сткЗначенияРеквизитов.ПараметрыВводаИнформацииОГрузовыхМестах;
	КонецЕсли;
	
	Элементы.ЗапрещеноИсполнениеНесколькимиРейсами.Доступность = Ложь
		                                                        Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса
													 Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку;
	
	ИспользуютсяУслугиИнкассации = ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") И ИспользуютсяУслугиИнкассации;
	
	Если ИспользуютсяУслугиИнкассации Тогда
		Если СпособУчетаИнкассируемыхЦенностей = Перечисления.упСпособыУчетаИнкассируемыхЦенностей.ВЦеломПоЗаявкеНаПеревозкуГруза Тогда
			ЗаполнитьОтчетыИнкассаторов();
			Элементы.ГрузовыеМестаВалюта.Видимость = Ложь;
			Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
			Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
			Элементы.Сумма.ТолькоПросмотр = Истина;
			Элементы.ГруппаИнкассация.Видимость = Истина;	
			Элементы.ВалютаИнкассируемаяЦенность.ТолькоПросмотр = Истина;
		Иначе	
			Элементы.ГруппаИнкассация.Видимость = Ложь;	
			Если ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится Тогда
				Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Истина;
			Иначе
				Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
			КонецЕсли;
			ЭтоГруз = НЕ ВидГруза = ПредопределенноеЗначение("Перечисление.упВидыГрузов.ИнкассируемаяЦенность");
			Если ЭтоГруз Тогда
				Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
			Иначе	
				Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаИнкассируемаяЦенность;
			КонецЕсли;
			Элементы.Сумма.ТолькоПросмотр = Ложь;
			Элементы.ВалютаИнкассируемаяЦенность.ТолькоПросмотр = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ГруппаИнкассация.Видимость = Ложь;
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
		Элементы.ГрузовыеМестаВалюта.Видимость = Ложь;
		Элементы.ГрузовыеМестаВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
	КонецЕсли; 	
	Элементы.СуммаИнкассацииПланПредставление.Видимость = ИспользуютсяУслугиИнкассации;
	
	Если ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки") Тогда 
		Элементы.ГруппаЦепьПоставок.Видимость = ИспользуютсяМультимодальныеПеревозки;
		Элементы.ГрузовыеМестаПодобратьЦепьПоставокВТЧГрузы.Видимость = ИспользуютсяМультимодальныеПеревозки;
		Элементы.ГрузовыеМестаКонтекстноеМенюОчиститьСхемуМультимодальнойПеревозки.Видимость = ИспользуютсяМультимодальныеПеревозки;
		Элементы.ГрузовыеМестаГруппаЦепьПоставок.Видимость = ИспользуютсяМультимодальныеПеревозки;
		Элементы.ГрузовыеМестаВозвратнаяТара.Видимость = ИспользуютсяМультимодальныеПеревозки;
	КонецЕсли;
		
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗаявкиНаПеревозкуГруза(Объект.Ссылка, Статус, Элементы, ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза, ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки);
	
	сткНастроек = Новый Структура;
	сткНастроек.Вставить("ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки", ЭтаФорма.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);
		
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусНоваяСервер()
	
	Возврат ИзменитьСтатусЗаявки(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Новая);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусПодобранаЦепьПоставокСервер()
	
	Возврат ИзменитьСтатусЗаявки(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.ПодобранаЦепьПоставок);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусКВыполнениюСервер()
	
	сткВозврата = упЗаданиеНаПеревозку.РазрешеноПеревестиКВыполнению(Статус, упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки, "ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза"));
	Если сткВозврата.Результат Тогда
	    Возврат ИзменитьСтатусЗаявки(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(сткВозврата.ТекстОшибки, Объект.Ссылка, "Статус");
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусСогласованаСервер()
	
	Если Объект.ВидПеревозки.АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению Тогда 
		Возврат ИзменитьСтатусЗаявки(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.КВыполнению);
	Иначе	
		Возврат ИзменитьСтатусЗаявки(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Согласована);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОтмененаСервер()
	
	Возврат ИзменитьСтатусЗаявки(Перечисления.упСтатусыЗаявкиНаПеревозкуГруза.Отменена);
	
КонецФункции

&НаСервере
Функция ИзменитьСтатусЗаявки(СтатусЗаявки)
	
	Возврат упЗаявкаФормаДокумента.ИзменитьСтатусЗаявки(ЭтаФорма, Объект, СтатусЗаявки);
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус() Экспорт
	упЗаявкаФормаДокумента.ИзменилсяСтатус(ЭтаФорма, Объект);
КонецПроцедуры

&НаКлиенте
// Процедура заполнения таблицы "ПлановыеДоходы" типовыми статьями.
//
Процедура ЗаполнитьТиповымиСтатьямиДоходовКлиент(Результат, ДополнительныеПараметры) Экспорт
	Если Объект.ПлановыеДоходы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ВопросОПерезаполненииПлановыхДоходов", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Плановые доходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьТиповымиСтатьямиДоходовНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТиповымиСтатьямиДоходовНаСервере()
	
	упЗаявкаФормаДокумента.ЗаполнитьТиповымиСтатьямиДоходовНаСервере(ЭтаФорма, Объект);
			
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ВопросОПерезаполненииПлановыхДоходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьТиповымиСтатьямиДоходовНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РассчитатьДоходыНаСервере()
	упТарификацияВызовСервера.Рассчитать(Объект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокГрузов(Копирование = Ложь)
	Если Объект.ГрузовыеМеста.Количество() > 0 Тогда
		
		ТоварныйСостав.Очистить();
		
		ЕстьИзмененияПлановыхЭтаповПеревозки = Ложь;
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ЕстьИзмененияПлановыхЭтаповПеревозки = упУправленияЗаявками.ЕстьИзмененияПлановыхЭтаповПеревозки(Объект.Ссылка);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаГрузовыеМеста(ЕстьИзмененияПлановыхЭтаповПеревозки);
		Если ЕстьИзмененияПлановыхЭтаповПеревозки Тогда
			Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", Объект.Ссылка);	
		Иначе	
			Запрос.УстановитьПараметр("тбзГрузовыеМеста", Объект.ГрузовыеМеста.Выгрузить());	
		КонецЕсли;
		мсвРезультатЗапроса = Запрос.ВыполнитьПакет();
		КоличествоТаблиц = мсвРезультатЗапроса.Количество();
		
		ИндексЗапросаТоварныйСостав = КоличествоТаблиц - 2;
		ИндексЗапросаГрузовыеМеста = КоличествоТаблиц - 1;
		тбзТоварныйСостав = мсвРезультатЗапроса[ИндексЗапросаТоварныйСостав].Выгрузить();
		ВыборкаГрузовоеМесто = мсвРезультатЗапроса[ИндексЗапросаГрузовыеМеста].Выбрать();
		
		Если Объект.ОбщийГруз Тогда 
			
			Если ВыборкаГрузовоеМесто.Следующий() Тогда
				
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаГрузовоеМесто,,"Высота, Ширина");
				
				Если Копирование Тогда 
					ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
					Код  = "";
					ГрузМодифицирован = Истина;
				Иначе	
					ГрузовоеМесто = ВыборкаГрузовоеМесто.Ссылка;
				КонецЕсли;	
								
				ВысотаГруза = ВыборкаГрузовоеМесто.Высота;
				ШиринаГруза = ВыборкаГрузовоеМесто.Ширина;
				ДлинаГруза = ВыборкаГрузовоеМесто.Длина;
				
				мсвСтрокиТоварныйСостав = тбзТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", ВыборкаГрузовоеМесто.Ссылка));
				
				Для Каждого ЭлементТовар Из мсвСтрокиТоварныйСостав Цикл 
					НоваяСтрока = ТоварныйСостав.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементТовар);
					НоваяСтрока.ГрузовоеМесто = ГрузовоеМесто;
					НоваяСтрока.Код = Код;
				КонецЦикла;
			Иначе	
				Если Копирование Тогда 
					ГрузовоеМесто = Справочники.упГрузовыеМеста.ПустаяСсылка();
					Код  = "";
					ГрузМодифицирован = Истина;
				КонецЕсли;	
			КонецЕсли;
						
		Иначе	
			//Элементы.ОбщийГруз.Видимость = Ложь;
						
			Пока ВыборкаГрузовоеМесто.Следующий() Цикл
				
				НовыйГруз = ГрузовыеМеста.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйГруз, ВыборкаГрузовоеМесто);
				
				НовыйГруз.ПредставлениеСтроки = ПолучитьПредставлениеЭтапа(ВыборкаГрузовоеМесто);
				
				текИдентификатор = Новый УникальныйИдентификатор;
				НовыйГруз.Идентификатор = текИдентификатор;
				
				текГрузовоеМесто = ВыборкаГрузовоеМесто.Ссылка;
				Если Копирование Тогда 
					НовыйГруз.Код     = "";
					ГрузМодифицирован = Истина;
				Иначе	
					НовыйГруз.ГрузовоеМесто = текГрузовоеМесто;
				КонецЕсли;	
				
				ВидГруза = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НовыйГруз.ТипГруза, "ВидГруза");
				Если ЗначениеЗаполнено(ВидГруза) Тогда
					НовыйГруз.ИндексКартинки = Перечисления.упВидыГрузов.Индекс(ВидГруза);
				Иначе	
					НовыйГруз.ИндексКартинки = Перечисления.упВидыГрузов.Индекс(Перечисления.упВидыГрузов.Груз);
				КонецЕсли; 
				
				мсвСтрокиТоварныйСостав = тбзТоварныйСостав.НайтиСтроки(Новый Структура("ГрузовоеМесто", текГрузовоеМесто));
				
				Для Каждого ЭлементТовар Из мсвСтрокиТоварныйСостав Цикл 
					НоваяСтрока = ТоварныйСостав.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементТовар);
					НоваяСтрока.Идентификатор = текИдентификатор;
					НоваяСтрока.ГрузовоеМесто = текГрузовоеМесто;
					НоваяСтрока.Код = НовыйГруз.Код;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция ПолучитьПредставлениеЭтапа(СтрокаЭтап)
	
	Возврат НСтр(СтрШаблон("ru = '%1 - %2'", СтрокаЭтап.НомерВЦепиПоставок, СтрокаЭтап.НомерВЦепиПоставокКонец));
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаГрузовыеМеста(ЕстьИзмененияПлановыхЭтаповПеревозки)
	
	Если ЕстьИзмененияПлановыхЭтаповПеревозки Тогда
		
		// Данные по грузовым местам выбираются следующим образом:
		//  * Отправление/получение - из первых и последних этапов;
		//  * Количиство грузов     - максимальное из всех этапов по грузовому месту;
		//  * Схема                 - из первого этапа;
		//  * АдресВозврата         - пустой, не подбирается;
		//  * ВозвратнаяТара, ВозвратПоЦепиПоставок - всегда Ложь;
		Результат = "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеЭтапыПеревозки.АдресОтправления КАК АдресОтправления,
		|	ПлановыеЭтапыПеревозки.АдресПолучения КАК АдресПолучения,
		|	ПлановыеЭтапыПеревозки.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
		|	ПлановыеЭтапыПеревозки.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
		|	ПлановыеЭтапыПеревозки.ГрузовоеМесто КАК ГрузовоеМесто,
		|	ПлановыеЭтапыПеревозки.ЗонаОтправления КАК ЗонаОтправления,
		|	ПлановыеЭтапыПеревозки.ЗонаПолучения КАК ЗонаПолучения,
		|	ПлановыеЭтапыПеревозки.КоличествоГрузов КАК КоличествоГрузов,
		|	ПлановыеЭтапыПеревозки.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
		|	ПлановыеЭтапыПеревозки.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
		|	ПлановыеЭтапыПеревозки.КонтрагентОтправителя КАК КонтрагентОтправителя,
		|	ПлановыеЭтапыПеревозки.КонтрагентПолучателя КАК КонтрагентПолучателя,
		|	ПлановыеЭтапыПеревозки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
		|	ПлановыеЭтапыПеревозки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
		|	ПлановыеЭтапыПеревозки.Отправитель КАК Отправитель,
		|	ПлановыеЭтапыПеревозки.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
		|	ПлановыеЭтапыПеревозки.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
		|	ПлановыеЭтапыПеревозки.Получатель КАК Получатель,
		|	ПлановыеЭтапыПеревозки.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
		|	ПлановыеЭтапыПеревозки.ПолучениеГрузаС КАК ПолучениеГрузаС,
		|	ПлановыеЭтапыПеревозки.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
		|	ПлановыеЭтапыПеревозки.Тара КАК Тара,
		|	ЛОЖЬ КАК ВозвратПоЦепиПоставок,
		|	ЛОЖЬ КАК ВозвратнаяТара,
		|	ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК АдресВозврата,
		|	0 КАК НомерСтроки
		|ПОМЕСТИТЬ втГрузовыеМеста
		|ИЗ
		|	РегистрСведений.упПлановыеЭтапыПеревозки.СрезПоследних(
		|		,
		|		ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза) КАК ПлановыеЭтапыПеревозки
		|ГДЕ НЕ (ПлановыеЭтапыПеревозки.Отменен)
		|";

	Иначе
		Результат = "ВЫБРАТЬ
	        |	тбзГрузовыеМеста.ГрузовоеМесто,
	        |	тбзГрузовыеМеста.Тара,
	        |	тбзГрузовыеМеста.АдресПолучения,
	        |	тбзГрузовыеМеста.КоличествоГрузов,
	        |	тбзГрузовыеМеста.НомерСтроки,
	        |	тбзГрузовыеМеста.АдресОтправления,
	        |	тбзГрузовыеМеста.ГрафикРаботыАдресаОтправления,
	        |	тбзГрузовыеМеста.ГрафикРаботыАдресаПолучения,
	        |	тбзГрузовыеМеста.ОтправлениеГрузаС,
	        |	тбзГрузовыеМеста.ОтправлениеГрузаПо,
	        |	тбзГрузовыеМеста.ПолучениеГрузаС,
	        |	тбзГрузовыеМеста.ПолучениеГрузаПо,
	        |	тбзГрузовыеМеста.Отправитель,
	        |	тбзГрузовыеМеста.КонтрагентОтправителя,
	        |	тбзГрузовыеМеста.КонтактноеЛицоОтправителя,
	        |	тбзГрузовыеМеста.Получатель,
	        |	тбзГрузовыеМеста.КонтрагентПолучателя,
	        |	тбзГрузовыеМеста.КонтактноеЛицоПолучателя,
	        |	тбзГрузовыеМеста.СхемаМультимодальнойПеревозки,
	        |	тбзГрузовыеМеста.ВозвратПоЦепиПоставок,
	        |	тбзГрузовыеМеста.ВозвратнаяТара,
		   |	тбзГрузовыеМеста.АдресВозврата,
		   |	1 КАК НомерВЦепиПоставок,
		   |	2 КАК НомерВЦепиПоставокКонец
		   |ПОМЕСТИТЬ втГрузовыеМеста
	        |ИЗ
	        |	&тбзГрузовыеМеста КАК тбзГрузовыеМеста
		   |";
	КонецЕсли;
	
	Результат = Результат + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГрузовыеМеста.Ссылка КАК Ссылка,
	|	упГрузовыеМеста.Код КАК Код,
	|	упГрузовыеМеста.БеречьОтВлаги КАК БеречьОтВлаги,
	|	упГрузовыеМеста.БеречьОтИзлучения КАК БеречьОтИзлучения,
	|	упГрузовыеМеста.БеречьОтНагрева КАК БеречьОтНагрева,
	|	упГрузовыеМеста.Валюта КАК Валюта,
	|	упГрузовыеМеста.Высота КАК Высота,
	|	упГрузовыеМеста.Длина КАК Длина,
	|	упГрузовыеМеста.КлассОпасности КАК КлассОпасности,
	|	упГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	упГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	упГрузовыеМеста.Масса КАК Масса,
	|	упГрузовыеМеста.ОбщийГрузПоЗаявке КАК ОбщийГрузПоЗаявке,
	|	упГрузовыеМеста.Объем КАК Объем,
	|	упГрузовыеМеста.Описание КАК Описание,
	|	упГрузовыеМеста.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	упГрузовыеМеста.Сумма КАК Сумма,
	|	упГрузовыеМеста.СуммаНДС КАК СуммаНДС,
	|	упГрузовыеМеста.ТемпературныйРежим КАК ТемпературныйРежим,
	|	упГрузовыеМеста.ТипГруза КАК ТипГруза,
	|	упГрузовыеМеста.Хрупкий КАК Хрупкий,
	|	упГрузовыеМеста.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки КАК ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки,
	|	упГрузовыеМеста.Ширина КАК Ширина,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМеста.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втГрузовыеМеста.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втГрузовыеМеста.Отправитель КАК Отправитель,
	|	втГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМеста.Получатель КАК Получатель,
	|	втГрузовыеМеста.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втГрузовыеМеста.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	упТипыГрузов.ВидГруза КАК ВидГруза,
	|	втГрузовыеМеста.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	упГрузовыеМеста.ЗапрещеноСкладироватьДругНаДруга КАК ЗапрещеноСкладироватьДругНаДруга,
	|	втГрузовыеМеста.ВозвратнаяТара КАК ВозвратнаяТара,
	|	втГрузовыеМеста.АдресВозврата КАК АдресВозврата,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ втХарактеристикиГрузовыхМест
	|ИЗ
	|	втГрузовыеМеста КАК втГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упТипыГрузов КАК упТипыГрузов
	|		ПО упГрузовыеМеста.ТипГруза = упТипыГрузов.Ссылка
	|	ПО втГрузовыеМеста.ГрузовоеМесто = упГрузовыеМеста.Ссылка
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГрузовыеМестаТоварныйСоставГруза.Ссылка КАК ГрузовоеМесто,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	упГрузовыеМестаТоварныйСоставГруза.НомерСтроки КАК НомерСтроки,
	|	упГрузовыеМестаТоварныйСоставГруза.Номенклатура КАК Номенклатура,
	|	упГрузовыеМестаТоварныйСоставГруза.УпаковкаНоменклатуры КАК УпаковкаНоменклатуры,
	|	упГрузовыеМестаТоварныйСоставГруза.Количество КАК Количество,
	|	упГрузовыеМестаТоварныйСоставГруза.Цена КАК Цена,
	|	упГрузовыеМестаТоварныйСоставГруза.СтавкаНДС КАК СтавкаНДС,
	|	упГрузовыеМестаТоварныйСоставГруза.СуммаНДС КАК СуммаНДС,
	|	упГрузовыеМестаТоварныйСоставГруза.СуммаБезНДС КАК СуммаБезНДС,
	|	упГрузовыеМестаТоварныйСоставГруза.Сумма КАК Сумма,
	|	упГрузовыеМестаТоварныйСоставГруза.АналитическийРазрез КАК АналитическийРазрез
	|ИЗ
	|	Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК упГрузовыеМестаТоварныйСоставГруза
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		втГрузовыеМеста.Ссылка КАК Ссылка,
	|		втГрузовыеМеста.Тара КАК Тара
	|	ИЗ
	|		втХарактеристикиГрузовыхМест КАК втГрузовыеМеста) КАК втГрузовыеМеста
	|	ПО упГрузовыеМестаТоварныйСоставГруза.Ссылка = втГрузовыеМеста.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрузовыеМеста.Ссылка КАК Ссылка,
	|	втГрузовыеМеста.Тара КАК Тара,
	|	втГрузовыеМеста.Код КАК Код,
	|	втГрузовыеМеста.БеречьОтВлаги КАК БеречьОтВлаги,
	|	втГрузовыеМеста.БеречьОтИзлучения КАК БеречьОтИзлучения,
	|	втГрузовыеМеста.БеречьОтНагрева КАК БеречьОтНагрева,
	|	втГрузовыеМеста.Валюта КАК Валюта,
	|	втГрузовыеМеста.Высота КАК Высота,
	|	втГрузовыеМеста.Длина КАК Длина,
	|	втГрузовыеМеста.КлассОпасности КАК КлассОпасности,
	|	втГрузовыеМеста.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
	|	втГрузовыеМеста.КоличествоМест КАК КоличествоМест,
	|	втГрузовыеМеста.Масса КАК Масса,
	|	втГрузовыеМеста.ОбщийГрузПоЗаявке КАК ОбщийГрузПоЗаявке,
	|	втГрузовыеМеста.Объем КАК Объем,
	|	втГрузовыеМеста.Описание КАК Описание,
	|	втГрузовыеМеста.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
	|	втГрузовыеМеста.Сумма КАК Сумма,
	|	втГрузовыеМеста.СуммаНДС КАК СуммаНДС,
	|	втГрузовыеМеста.ТемпературныйРежим КАК ТемпературныйРежим,
	|	втГрузовыеМеста.ТипГруза КАК ТипГруза,
	|	втГрузовыеМеста.Хрупкий КАК Хрупкий,
	|	втГрузовыеМеста.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки КАК ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки,
	|	втГрузовыеМеста.Ширина КАК Ширина,
	|	втГрузовыеМеста.АдресПолучения КАК АдресПолучения,
	|	втГрузовыеМеста.КоличествоГрузов КАК КоличествоГрузов,
	|	втГрузовыеМеста.НомерСтроки КАК НомерСтроки,
	|	втГрузовыеМеста.АдресОтправления КАК АдресОтправления,
	|	втГрузовыеМеста.ГрафикРаботыАдресаОтправления КАК ГрафикРаботыАдресаОтправления,
	|	втГрузовыеМеста.ГрафикРаботыАдресаПолучения КАК ГрафикРаботыАдресаПолучения,
	|	втГрузовыеМеста.ОтправлениеГрузаС КАК ОтправлениеГрузаС,
	|	втГрузовыеМеста.ОтправлениеГрузаПо КАК ОтправлениеГрузаПо,
	|	втГрузовыеМеста.ПолучениеГрузаС КАК ПолучениеГрузаС,
	|	втГрузовыеМеста.ПолучениеГрузаПо КАК ПолучениеГрузаПо,
	|	втГрузовыеМеста.Отправитель КАК Отправитель,
	|	втГрузовыеМеста.КонтрагентОтправителя КАК КонтрагентОтправителя,
	|	втГрузовыеМеста.КонтактноеЛицоОтправителя КАК КонтактноеЛицоОтправителя,
	|	втГрузовыеМеста.Получатель КАК Получатель,
	|	втГрузовыеМеста.КонтрагентПолучателя КАК КонтрагентПолучателя,
	|	втГрузовыеМеста.КонтактноеЛицоПолучателя КАК КонтактноеЛицоПолучателя,
	|	втГрузовыеМеста.ВидГруза КАК ВидГруза,
	|	втГрузовыеМеста.СхемаМультимодальнойПеревозки КАК СхемаМультимодальнойПеревозки,
	|	втГрузовыеМеста.ВозвратПоЦепиПоставок КАК ВозвратПоЦепиПоставок,
	|	втГрузовыеМеста.ЗапрещеноСкладироватьДругНаДруга КАК ЗапрещеноСкладироватьДругНаДруга,
	|	втГрузовыеМеста.ВозвратнаяТара КАК ВозвратнаяТара,
	|	втГрузовыеМеста.АдресВозврата КАК АдресВозврата,
	|	втГрузовыеМеста.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втГрузовыеМеста.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ИЗ
	|	втХарактеристикиГрузовыхМест КАК втГрузовыеМеста
	|УПОРЯДОЧИТЬ ПО
	|	втГрузовыеМеста.НомерСтроки,
	|	НомерВЦепиПоставок
	|";

	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИдентификаторСтрокиГрузовогоМеста()
	текРезультат = Неопределено;
	ТекущиеДанные = Элементы.ВесьТоварныйСостав.ТекущиеДанные;
	мсвСтрокиГрузовыеМеста = ГрузовыеМеста.НайтиСтроки(Новый Структура("ГрузовоеМесто", ТекущиеДанные.ГрузовоеМесто));
	Если мсвСтрокиГрузовыеМеста.Количество() > 0 Тогда
		текРезультат	= мсвСтрокиГрузовыеМеста[0].ПолучитьИдентификатор();
	КонецЕсли;
 	Возврат текРезультат;
КонецФункции // ПолучитьИдентификаторСтрокиГрузовогоМеста()

&НаСервере
Процедура ЗаполнитьОтчетыИнкассаторов()
	
	ОтчетыИнкассаторов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упИнкассацияДенежныхСредств.СуммаФакт КАК СуммаИнкассацииФакт,
	|	упИнкассацияДенежныхСредств.Регистратор КАК Отчет,
	|	упИнкассацияДенежныхСредств.Валюта
	|ИЗ
	|	РегистрНакопления.упИнкассацияДенежныхСредств КАК упИнкассацияДенежныхСредств
	|ГДЕ
	|	упИнкассацияДенежныхСредств.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
	|	И упИнкассацияДенежныхСредств.Регистратор ССЫЛКА Документ.упОтчетИнкассатора";
	Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", Объект.Ссылка);
	ОтчетыИнкассаторов.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВГХГруза()
	
	сткВГХГруза = ПолучитьВГХГруза(ТипГруза); 
	ВысотаГруза = сткВГХГруза.Высота;
	ДлинаГруза = сткВГХГруза.Длина;
	ШиринаГруза = сткВГХГруза.Ширина;
	Объем = сткВГХГруза.Объем;
	Масса = сткВГХГруза.Масса;
	КоличествоМест	= сткВГХГруза.КоличествоМест;
	КоличествоЗагрузочныхМетров	= сткВГХГруза.КоличествоЗагрузочныхМетров;
	ТемпературныйРежим	= сткВГХГруза.ТемпературныйРежим;
	КлассОпасности = сткВГХГруза.КлассОпасности;
	Хрупкий = сткВГХГруза.Хрупкий;
	БеречьОтВлаги = сткВГХГруза.БеречьОтВлаги;
	БеречьОтИзлучения = сткВГХГруза.БеречьОтИзлучения;
	БеречьОтНагрева = сткВГХГруза.БеречьОтНагрева;
	ВидГруза = сткВГХГруза.ВидГруза;
	ЗапрещеноСкладироватьДругНаДруга = сткВГХГруза.ЗапрещеноСкладироватьДругНаДруга;
	
	ЭтоГруз = ВидГруза <> ПредопределенноеЗначение("Перечисление.упВидыГрузов.ИнкассируемаяЦенность");
	Если ЭтоГруз Тогда
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
	Иначе	
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаИнкассируемаяЦенность;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВГХГруза(ТипГруза)
	
	сткВГХГруза = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипГруза, "Масса, Высота, Длина, Ширина, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КлассОпасности, ТемпературныйРежим, Хрупкий, БеречьОтВлаги, БеречьОтИзлучения, БеречьОтНагрева, ВидГруза, ЗапрещеноСкладироватьДругНаДруга"); 
	Если сткВГХГруза.Свойство("ВидГруза") 
			И ЗначениеЗаполнено(сткВГХГруза.ВидГруза) Тогда
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(сткВГХГруза.ВидГруза));	
		Если сткВГХГруза.ВидГруза = Перечисления.упВидыГрузов.ИнкассируемаяЦенность Тогда
			сткВГХГруза.Вставить("КоличествоГрузов", 1);		
		КонецЕсли;
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(сткВГХГруза.ВидГруза));	
	Иначе
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(Перечисления.упВидыГрузов.Груз));	
	КонецЕсли;
		
	Возврат сткВГХГруза;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьАдресВозвратаСхемы(СхемаМультимодальнойПеревозки)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СхемаМультимодальнойПеревозки, "АдресВозврата")
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьГрафикРаботы(Партнер, Адрес)
	Возврат Справочники.упПартнеры.ПолучитьГрафикРаботыТочкиПартнера(Партнер, Адрес); 
Конецфункции // ПолучитьГрафикРаботы()

&НаСервере	
Процедура ПодобратьВидПеревозкиНаСервере()
	
	Объект.ВидПеревозки = Справочники.упВидыПеревозок.ПодобратьВидПеревозки(Объект.Ссылка);
	ВидПеревозкиПриИзмененииНаСервере();
	
КонецПроцедуры // ПодобратьВидПеревозкиНаСервере()

&НаСервере	
Процедура ПодобратьНаправлениеДеятельностиНаСервере()
	
	Объект.НаправлениеДеятельности = Справочники.упНаправленияДеятельности.ПодобратьНаправлениеДеятельности(Объект.Ссылка);
	НаправлениеДеятельностиПриИзмененииНаСервере();
	
КонецПроцедуры // ПодобратьНаправлениеДеятельностиНаСервере()

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура изменения признака общего груза после утвердительного ответа.
//
Процедура ПодобратьВидПеревозкиПослеОтветаНаВопросЗаписать(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщийГруз = Объект.ОбщийГруз;
	ПодобратьВидПеревозкиНаСервере();
	Если ОбщийГруз <> Объект.ОбщийГруз Тогда 
		ПриИзмененииПризнакаОбщегоГруза(Ложь);
	КонецЕсли;	
	Модифицированность = Истина;
	
КонецПроцедуры // ПодобратьВидПеревозкиПослеОтветаНаВопросЗаписать()

&НаКлиенте
// Процедура изменения признака общего груза после утвердительного ответа.
//
Процедура ПодобратьНаправлениеДеятельностиПослеОтветаНаВопросЗаписать(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
		
	ПодобратьНаправлениеДеятельностиНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры // ПодобратьНаправлениеДеятельностиПослеОтветаНаВопросЗаписать()

&НаКлиенте
// Процедура изменения время отправления после утвердительного ответа.
//
Процедура ИзменитьВремяОтправленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Объект.ОтправлениеГрузаС  = Результат.НачалоПериода;
		Объект.ОтправлениеГрузаПо = Результат.КонецПериода;
		ОбработатьИзменениеРеквизитаШапки("ОтправлениеГрузаС");
		ОбработатьИзменениеРеквизитаШапки("ОтправлениеГрузаПо");
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВремяОтправленияЗавершение()

&НаКлиенте
// Процедура изменения времени получения после утвердительного ответа.
//
Процедура ИзменитьВремяПолученияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Объект.ПолучениеГрузаС  = Результат.НачалоПериода;
		Объект.ПолучениеГрузаПо = Результат.КонецПериода;
		ОбработатьИзменениеРеквизитаШапки("ПолучениеГрузаС");
		ОбработатьИзменениеРеквизитаШапки("ПолучениеГрузаПо");
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВремяПолученияЗавершение()

&НаКлиенте
Функция ПолучитьИмяПоля(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеДоходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуРеглУчета()
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка, Соглашение", Объект.Ссылка, Объект.Соглашение);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоля(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаСервере
Функция РассчитатьДоходыСРасшифровкойНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;
КонецФункции

&НаКлиенте
Процедура ПересчитатьСуммуПлановойИнкассации()
	Если Истина
		И ИспользуютсяУслугиИнкассации 
		И СпособУчетаИнкассируемыхЦенностей = ПредопределенноеЗначение("Перечисление.упСпособыУчетаИнкассируемыхЦенностей.СТочностьюДоГрузовыхМест")
	Тогда
		Объект.СуммаИнкассацииПлан = ГрузовыеМеста.Итог("Сумма");	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Вызывается после окончания редактирования временного окна отправления.
//
Процедура ЗавершениеРедактированияПериодаОтправления(Ответ, ПараметрыВыполнения) Экспорт
	
	ОбработатьИзменениеРеквизитаШапки("ОтправлениеГрузаС");
	ОбработатьИзменениеРеквизитаШапки("ОтправлениеГрузаПо");	
	
КонецПроцедуры

&НаКлиенте
// Вызывается после окончания редактирования временного окна получения.
//
Процедура ЗавершениеРедактированияПериодаПолучения(Ответ, ПараметрыВыполнения) Экспорт
	
	ОбработатьИзменениеРеквизитаШапки("ПолучениеГрузаС");
	ОбработатьИзменениеРеквизитаШапки("ПолучениеГрузаПо");	
	
КонецПроцедуры

#Область Взаиморасчеты

&НаКлиенте
Процедура ЗагрузитьВзаиморасчетыСПартнерами() Экспорт
	упЗаявкаФормаДокументаКлиент.ЗагрузитьВзаиморасчетыСПартнерами(ЭтаФорма, Объект);
	Если ЭтаФорма.ПодключитьОбработчикОжиданияВзаиморасчеты Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов", 1);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗагрузкуВзаиморасчетов() Экспорт
	Результат = ПолучитьИзВременногоХранилища(ВзаиморасчетыАдресХранилища);
	Если НЕ Результат = Неопределено Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов");		
		упЗаявкаФормаДокументаКлиент.ЗаполнитьФормуВзаиморасчетами(ЭтаФорма, Результат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвВидыГрузов 	= ВозможныеВидыГрузов(Объект.ВидПеревозки);
	сткОтбор 		= Новый Структура("ВидГруза", мсвВидыГрузов);
	сткПараметры 	= Новый Структура("Отбор", сткОтбор);
 	ОткрытьФорму("Справочник.упТипыГрузов.ФормаВыбора", сткПараметры, Элемент);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВозможныеВидыГрузов(ВидПеревозки)
	
	спзРезультат = Новый СписокЗначений;
	спзРезультат.Добавить(Перечисления.упВидыГрузов.Груз);
	
	ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = Ложь;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	
	Если ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест Тогда
		спзРезультат.Добавить(Перечисления.упВидыГрузов.ИнкассируемаяЦенность);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		спзРезультат.Добавить(Перечисления.упВидыГрузов.Тара);
	КонецЕсли; 
	
	Возврат спзРезультат;
	
КонецФункции

&НаКлиенте
Процедура СхемаМультимодальнойПеревозкиОчистка(Элемент, СтандартнаяОбработка)
	УстановитьПараметрыСхемыМультимодальнойПеревозки(Ложь,,Истина);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура ЗаполнитьВГХГрузовыхМест()
	//сткВГХГрузовыхМест = упУправленияЗаявками.ВГХГрузовыхМест(ГрузовыеМеста.Выгрузить(,"Тара, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КоличествоГрузов, ОценочнаяСтоимость, ВидГруза, Сумма"), ИспользуютсяУслугиИнкассации);
	//сткВГХГрузовыхМест = упРасчетПоказателейЗагрузки.РассчитатьИтоговыеПараметрыПоНаборуГрузов(ГрузовыеМеста.Выгрузить(,"Код, ВидГруза, Тара, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КоличествоГрузов, ОценочнаяСтоимость, Сумма, Длина, Ширина, Высота"), Истина);
	//ЗаполнитьЗначенияСвойств(ЭтаФорма, сткВГХГрузовыхМест);
	НадписьПараметрыДокумента = НСтр("ru = 'Итоги по документу актуализируются после записи(*)'");
КонецПроцедуры

&НаКлиенте
Процедура ЗонаОтправленияПриИзменении(Элемент)
	
	УстановитьПараметрыВыбораАдресаОтправления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораАдресаОтправления(ЭтаФорма)
	
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(ЭтаФорма.Элементы.АдресОтправления, "ГеографическаяЗона", ЭтаФорма.Объект.ЗонаОтправления, Не ЗначениеЗаполнено(ЭтаФорма.Объект.ЗонаОтправления));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗонаПолученияПриИзменении(Элемент)
	
	УстановитьПараметрыВыбораАдресаПолучения(ЭтаФорма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораАдресаПолучения(ЭтаФорма)
	
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(ЭтаФорма.Элементы.АдресПолучения, "ГеографическаяЗона", ЭтаФорма.Объект.ЗонаПолучения, Не ЗначениеЗаполнено(ЭтаФорма.Объект.ЗонаПолучения));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДополнительныеРеквизитыПоГрузовомуМестуНаКлиенте()
	
	Если Ложь
		Или ИнформационнаяГруппаГрузовыеМестаТекущая = сткИнформационныеГруппы.ХарактеристикиГруза
		Или Объект.ОбщийГруз
		//И НЕ Элементы.ГруппаДополнительныеРеквизитыГрузовогоМеста.Скрыта()
	Тогда
		ОбновитьДополнительныеРеквизитыПоГрузовомуМесту();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДополнительныеРеквизитыПоГрузовомуМесту() 
	
	упСервисныеФункции.ОбновитьДополнительныеРеквизитыПоГрузовомуМесту(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСменеСтраницыДополнительныеРеквизиты()
	
	упСервисныеФункции.ПриСменеСтраницыДополнительныеРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиЗначенияДополнительныхРеквизитовВОбъект(ЗаписыватьОбъект = Ложь)
	
	упСервисныеФункции.ПеренестиЗначенияДополнительныхРеквизитовВОбъект(ЭтотОбъект, ЗаписыватьОбъект); 
	
КонецПроцедуры

Процедура РассчитатьИтоговуюСуммуДоходы() Экспорт
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаДоходы",			"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыСкидка",	"СкидкаСумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыНДС",		"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ПлановыеДоходы", сткСоответствиеРеквизитов, ЭтаФорма.ВедетсяВалютныйУчет);
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСтруктуруИнформационныеГруппы()
	
	сткСтруктураИнициализации = Новый Структура();
	сткСтруктураИнициализации.Вставить("ИнформацияСкрыта", 0);
	сткСтруктураИнициализации.Вставить("ТоварныйСостав", 1);
	сткСтруктураИнициализации.Вставить("ХарактеристикиГруза", 2);
	
	сткИнформационныеГруппы = Новый ФиксированнаяСтруктура(сткСтруктураИнициализации);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВыбораОбщейСхемы()
	
	Если РазличныеСхемыМультимодальнойПеревозкиДляГрузов Тогда
		ДоступностьВыбораОбщейСхемы = Истина;
		
		мсвСхемы = Новый Массив;
		Для каждого ГрузовоеМестоСтрока Из ГрузовыеМеста Цикл
			Если мсвСхемы.Найти(ГрузовоеМестоСтрока.СхемаМультимодальнойПеревозки) = Неопределено Тогда
				мсвСхемы.Добавить(ГрузовоеМестоСтрока.СхемаМультимодальнойПеревозки);
			КонецЕсли;
			Если мсвСхемы.Количество() > 1 Тогда
				ДоступностьВыбораОбщейСхемы = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Элементы.СхемаМультимодальнойПеревозки.Доступность = ДоступностьВыбораОбщейСхемы;
		Элементы.ПодобратьЦепьПоставок.Доступность = ДоступностьВыбораОбщейСхемы;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусWMS()
	
	СтатусWMS = Документы.упЗаявкаНаПеревозкуГруза.ПолучитьСтатусWMS(Объект.Ссылка); 
	
КонецПроцедуры

#КонецОбласти

#Область Удалить

&НаСервере
Процедура _УстановитьПараметрыАдресаОтправления(ЭтоНовыйДокумент = Ложь, ЭтоОтрытиеФормы = Ложь, ИзменилсяАдрес = Истина)
	
	Если РазличныеАдресаОтправления Тогда 
		Если ЗначениеЗаполнено(Объект.АдресОтправления) Тогда 
			Если Истина
				И Не ЭтоНовыйДокумент 
				И Не ЭтоОтрытиеФормы
			Тогда 
				КоличествоСтрок = ГрузовыеМеста.Количество();
				Для Каждого текГруз Из ГрузовыеМеста Цикл 
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.АдресОтправления)
					Тогда
						текГруз.АдресОтправления = Объект.АдресОтправления;	
					КонецЕсли;
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.ГрафикРаботыАдресаОтправления)
					Тогда
						текГруз.ГрафикРаботыАдресаОтправления = Объект.ГрафикРаботыАдресаОтправления;
					КонецЕсли;
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.ОтправлениеГрузаС)
					Тогда
						текГруз.ОтправлениеГрузаС = Объект.ОтправлениеГрузаС;
					КонецЕсли;	
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.ОтправлениеГрузаПо) 
					Тогда
						текГруз.ОтправлениеГрузаПо = Объект.ОтправлениеГрузаПо;
					КонецЕсли;
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.Отправитель)
					Тогда
						текГруз.Отправитель	= Объект.Отправитель;
					КонецЕсли;
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.КонтактноеЛицоОтправителя)
					Тогда
						текГруз.КонтактноеЛицоОтправителя	= Объект.КонтактноеЛицоОтправителя;
					КонецЕсли;
					
					Если Ложь
						Или КоличествоСтрок = 1 
						Или Не ЗначениеЗаполнено(текГруз.КонтрагентОтправителя)
					Тогда
						текГруз.КонтрагентОтправителя	= Объект.КонтрагентОтправителя;
					КонецЕсли;

					ГрузМодифицирован = Истина;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	Иначе	
		Если НЕ ЭтоОтрытиеФормы Тогда
			Для Каждого текГруз Из ГрузовыеМеста Цикл 
				текГруз.АдресОтправления = Объект.АдресОтправления;
				текГруз.ГрафикРаботыАдресаОтправления = Объект.ГрафикРаботыАдресаОтправления;
				текГруз.ОтправлениеГрузаС = Объект.ОтправлениеГрузаС;
				текГруз.ОтправлениеГрузаПо = Объект.ОтправлениеГрузаПо;
				текГруз.Отправитель = Объект.Отправитель;
				текГруз.КонтактноеЛицоОтправителя = Объект.КонтактноеЛицоОтправителя;
				текГруз.КонтрагентОтправителя = Объект.КонтрагентОтправителя;
				
				ГрузМодифицирован = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	
	Если ИзменилсяАдрес Тогда
		ЗаблокироватьАдресОтправления();
		Если Истина
			И ЗначениеЗаполнено(Объект.АдресОтправления) 
			И НЕ ЭтоОтрытиеФормы
		Тогда
			Объект.ЗонаОтправления = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.АдресОтправления, "ГеографическаяЗона");
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _УстановкаВременныхОконПоГрузам(ИмяРеквизита)
	
	Для Каждого текГруз Из ГрузовыеМеста Цикл 
		текГруз[ИмяРеквизита] = Объект[ИмяРеквизита];
		ГрузМодифицирован 		= Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура _УстановитьПараметрыАдресаПолучения(ЭтоНовыйДокумент = Ложь, ЭтоОтрытиеФормы = Ложь, ИзменилсяАдрес = Истина)

	Если РазличныеАдресаПолучения Тогда 
		Если ЗначениеЗаполнено(Объект.АдресПолучения) Тогда 
			Если Истина 
				И Не ЭтоНовыйДокумент 
				И Не ЭтоОтрытиеФормы 
			Тогда 
				КоличествоСтрок = ГрузовыеМеста.Количество();
				Для Каждого текГруз Из ГрузовыеМеста Цикл 
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.АдресПолучения) Тогда
						текГруз.АдресПолучения = Объект.АдресПолучения;	
					КонецЕсли;
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.ГрафикРаботыАдресаПолучения) Тогда
						текГруз.ГрафикРаботыАдресаПолучения = Объект.ГрафикРаботыАдресаПолучения;	
					КонецЕсли;
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.ПолучениеГрузаС) Тогда
						текГруз.ПолучениеГрузаС 	= Объект.ПолучениеГрузаС;	
					КонецЕсли;
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.ПолучениеГрузаПо) Тогда
						текГруз.ПолучениеГрузаПо	= Объект.ПолучениеГрузаПо;	
					КонецЕсли;
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.Получатель) Тогда
						текГруз.Получатель = Объект.Получатель;	
					КонецЕсли;
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.КонтактноеЛицоПолучателя) Тогда
						текГруз.КонтактноеЛицоПолучателя = Объект.КонтактноеЛицоПолучателя;	
					КонецЕсли;
					Если КоличествоСтрок = 1 Или Не ЗначениеЗаполнено(текГруз.КонтрагентПолучателя) Тогда
						текГруз.КонтрагентПолучателя	= Объект.КонтрагентПолучателя;	
					КонецЕсли;
					
					ГрузМодифицирован 		= Истина;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	Иначе	
		Если НЕ ЭтоОтрытиеФормы  Тогда
			Для Каждого текГруз Из ГрузовыеМеста Цикл 
				текГруз.АдресПолучения = Объект.АдресПолучения;
				текГруз.ГрафикРаботыАдресаПолучения = Объект.ГрафикРаботыАдресаПолучения;
				текГруз.ПолучениеГрузаС = Объект.ПолучениеГрузаС;
				текГруз.ПолучениеГрузаПо	= Объект.ПолучениеГрузаПо;
				текГруз.Получатель = Объект.Получатель;
				текГруз.КонтактноеЛицоПолучателя = Объект.КонтактноеЛицоПолучателя;
				текГруз.КонтрагентПолучателя = Объект.КонтрагентПолучателя;
				ГрузМодифицирован = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	
	Если ИзменилсяАдрес Тогда
		
		ЗаблокироватьАдресПолучения();
		
		Если НЕ ЭтоОтрытиеФормы
			И ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
			Объект.ЗонаПолучения = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.АдресПолучения, "ГеографическаяЗона");
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
//Процедура _ЗаблокироватьАдресПолучения(БлокироватьТолькоАдрес = Ложь)
Процедура ЗаблокироватьАдресПолучения()
	
	РазличныеАдресаДляГрузовыхМест = Истина
							   И РазличныеАдресаПолучения;
							   //И НЕ ЗначениеЗаполнено(Объект.АдресПолучения);
	
	Элементы.ГрузовыеМестаАдресПолучения.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаПолучательСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаКонтрагентПолучателяСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаКонтактноеЛицоПолучателяСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаАдресПолученияСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаАдресПолученияБыстрыйДоступ.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	
	//Если НЕ БлокироватьТолькоАдрес Тогда
		Элементы.ГрузовыеМестаГрафикРаботыАдресаПолучения.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаПолучениеГрузаС.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаПолучениеГрузаПо.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаПолучатель.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаКонтактноеЛицоПолучателя.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаКонтрагентПолучателя.Видимость = РазличныеАдресаДляГрузовыхМест;
	//КонецЕсли;
	
	Элементы.ГрузовыеМестаАдресПолученияБыстрыйДоступ.Видимость = РазличныеАдресаДляГрузовыхМест;
	
КонецПроцедуры

&НаСервере
//Процедура _ЗаблокироватьАдресОтправления(БлокироватьТолькоАдрес = Ложь)
Процедура ЗаблокироватьАдресОтправления()
	
	РазличныеАдресаДляГрузовыхМест = Истина
							   И РазличныеАдресаОтправления;
							   //И НЕ ЗначениеЗаполнено(Объект.АдресОтправления);
	
	Элементы.ГрузовыеМестаАдресОтправления.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаОтправительСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаКонтрагентОтправителяСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаКонтактноеЛицоОтправителяСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаАдресОтправленияСтрока.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	Элементы.ГрузовыеМестаАдресОтправленияБыстрыйДоступ.ТолькоПросмотр = Не РазличныеАдресаДляГрузовыхМест;
	
	//Если БлокироватьТолькоАдрес Тогда
		Элементы.ГрузовыеМестаГрафикРаботыАдресаОтправления.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаОтправлениеГрузаС.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаОтправлениеГрузаПо.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаОтправитель.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаКонтактноеЛицоОтправителя.Видимость = РазличныеАдресаДляГрузовыхМест;
		Элементы.ГрузовыеМестаКонтрагентОтправителя.Видимость = РазличныеАдресаДляГрузовыхМест;
	//КонецЕсли;
	
	Элементы.ГрузовыеМестаАдресОтправленияБыстрыйДоступ.Видимость = РазличныеАдресаДляГрузовыхМест;
	
КонецПроцедуры

#КонецОбласти 