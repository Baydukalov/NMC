#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;

	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНепереданныеДокументы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	ДвиженияНепереданныеДокументы = Движения.упНепереданныеДокументы;
	ДвиженияНепереданныеДокументы.Записывать = Истина;
	
	Для каждого СтрокаТекущая Из ДокументыПоРейсу Цикл
		Движение = ДвиженияНепереданныеДокументы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		ЗаполнитьЗначенияСвойств(Движение, СтрокаТекущая);
		Движение.Период = Дата;
		Движение.Рейс = Рейс;
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда
		Рейс = ДанныеЗаполнения;
		Отказ = Ложь;
		СпособУчетаВозвращенныхДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидПеревозки.СпособУчетаВозвращенныхДокументов");
		упРейс.ЗаполнитьНепереданныеДокументыПоРейсу(ДокументыПоРейсу, Рейс, СпособУчетаВозвращенныхДокументов, Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли 