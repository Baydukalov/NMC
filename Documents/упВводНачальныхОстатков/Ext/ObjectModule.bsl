#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	Документы.упВводНачальныхОстатков.ИнициализироватьДанныеДокумента(ЭтотОбъект, ДополнительныеСвойства);
	
	РеквизитыОбъекта = Новый Структура("Дата,
								|Организация,
								|Валюта,
								|Склад");
	
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект);
		
	// Формирование движений по регистру накопления "упОстаткиМатериалов"
	упУправлениеЗапасами.СформироватьДвиженияОстаткиМатериалов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	// Формирование движений по регистру накопления "упАгрегатыНаСкладах"
	упУправлениеЗапасами.СформироватьДвиженияАгрегатов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	ПроверитьОстаткиАгрегатовИДопОборудования(Отказ);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиАгрегатовНаСкладах Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДополнительноеОборудование.ДополнительноеОборудование");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДополнительноеОборудование.Количество");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Материалы.Количество");
		
	ИначеЕсли ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиОборудованияНаСкладах Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Агрегаты.Агрегат");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Агрегаты.Количество");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Материалы.Количество");
		
	ИначеЕсли ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиМатериаловНаСкладах Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Агрегаты.Агрегат");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Агрегаты.Количество");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДополнительноеОборудование.ДополнительноеОборудование");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДополнительноеОборудование.Количество");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьОстаткиАгрегатовИДопОборудования(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	Агрегаты.Агрегат КАК Агрегат,
	|	Агрегаты.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втНачальныеОстатки
	|ИЗ
	|	Документ.упВводНачальныхОстатков.Агрегаты КАК Агрегаты
	|ГДЕ Агрегаты.Ссылка = &ВводНачальныхОстатков
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ДопОборудование.ДополнительноеОборудование КАК Агрегат,
	|	ДопОборудование.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.упВводНачальныхОстатков.ДополнительноеОборудование КАК ДопОборудование
	|ГДЕ ДопОборудование.Ссылка = &ВводНачальныхОстатков
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНачальныеОстаткиТ.Агрегат КАК Агрегат,
	|	втНачальныеОстаткиТ.НомерСтроки КАК НомерСтроки,
	|	АгрегатыТранспортныхСредств.ТранспортноеСредство КАК ТранспортноеСредство
	|ИЗ
	|	втНачальныеОстатки КАК втНачальныеОстаткиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упАгрегатыТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		) КАК АгрегатыТранспортныхСредств
	|	ПО ИСТИНА
	|		И втНачальныеОстаткиТ.Агрегат = АгрегатыТранспортныхСредств.Агрегат
	|		И ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено) = АгрегатыТранспортныхСредств.Операция
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНачальныеОстаткиТ.Агрегат КАК Агрегат,
	|	втНачальныеОстаткиТ.НомерСтроки КАК НомерСтроки,
	|	АгрегатыНаСкладах.Склад КАК Склад
	|ИЗ
	|	втНачальныеОстатки КАК втНачальныеОстаткиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|		&Период,
	|		Склад <> &Склад) КАК АгрегатыНаСкладах
	|	ПО АгрегатыНаСкладах.Агрегат = втНачальныеОстаткиТ.Агрегат
	|";
	Запрос.УстановитьПараметр("ВводНачальныхОстатков", Ссылка);	
	Запрос.УстановитьПараметр("Период", Дата);	
	Запрос.УстановитьПараметр("Склад", Склад);	
	
	мсвРезультат = Запрос.ВыполнитьПакет();
	
	Если НЕ мсвРезультат[1].Пустой() Тогда
		Выборка = мсвРезультат[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиАгрегатовНаСкладах Тогда
				
				ТекстСообщения = Нстр("ru = 'Агрегат ""%1"" установлен на %2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,  Выборка.Агрегат, Выборка.ТранспортноеСредство);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Агрегаты", Выборка.НомерСтроки , "Агрегат"));
				
			ИначеЕсли ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиОборудованияНаСкладах Тогда
				
				ТекстСообщения = Нстр("ru = 'Доп. оборудование ""%1"" установлено на %2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,  Выборка.Агрегат, Выборка.ТранспортноеСредство);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ДополнительноеОборудование", Выборка.НомерСтроки , "ДополнительноеОборудование"));
				
			КонецЕсли;			
					
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ввод начальных остатков'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),	УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
									
		КонецЦикла;	
	КонецЕсли;
	
	Если НЕ мсвРезультат[2].Пустой() Тогда
		Выборка = мсвРезультат[2].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиАгрегатовНаСкладах Тогда
				
				ТекстСообщения = Нстр("ru = 'Агрегат ""%1"" находится на складе %2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,  Выборка.Агрегат, Выборка.Склад);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Агрегаты", Выборка.НомерСтроки , "Агрегат"));
				
			ИначеЕсли ТипОперацииВводаНачальныхОстатков = Перечисления.упТипыОперацийВводаНачальныхОстатков.ОстаткиОборудованияНаСкладах Тогда
				
				ТекстСообщения = Нстр("ru = 'Доп. оборудование ""%1"" находится на складе %2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,  Выборка.Агрегат, Выборка.Склад);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ДополнительноеОборудование", Выборка.НомерСтроки , "ДополнительноеОборудование"));
				
			КонецЕсли;			
					
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ввод начальных остатков'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),	УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
		КонецЦикла;	
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
