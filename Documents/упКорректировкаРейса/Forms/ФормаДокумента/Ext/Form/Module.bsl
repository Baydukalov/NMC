#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОптимизацияМаршрута") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Объект.ОптимизацияМаршрута = Параметры.ОптимизацияМаршрута;
		КонецЕсли;
	КонецЕсли;
	
	ПримененныеПравилаВключенияКонтрольныхТочек = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ПримененныеПравилаВключенияКонтрольныхТочек");

	ДатаПоследнегоИзмененияМаршрутаРабот = упПрохождениеТочки.ПоследняяДатаИзмененияМаршрутаРаботРейса(Объект.Рейс).Период;
	
	// СтандартныеПодсистемы.Свойства
 	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
		
	ЭтаФорма.ВедетсяУчетДокументов = ПолучитьФункциональнуюОпцию("упВедетсяУчетДокументовТранспортныхСредствВодителей");
	НастроитьПанельНавигации();	
	ПриСозданииПриЧтенииНаСервере();
	
	ОформляетсяПутевойЛист = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
	КонецЕсли;	
	ОбновитьДанныеФормы();
	сткНовыйМаршрутРаботы = Неопределено;
	Если Параметры.Свойство("сткНовыйМаршрутРаботы", сткНовыйМаршрутРаботы) Тогда
		НовыйМаршрут = сткНовыйМаршрутРаботы.Маршрут;
		НовыеРаботы = сткНовыйМаршрутРаботы.Работы;
		Для каждого СтрокаНовыйМаршрут Из НовыйМаршрут Цикл
			мсвСтрокиМаршрута = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("Идентификатор", СтрокаНовыйМаршрут.Идентификатор));
			Если мсвСтрокиМаршрута.Количество() > 0 Тогда
				СтрокаМаршрута = мсвСтрокиМаршрута[0];
			Иначе
				СтрокаМаршрута = упКорректировкаРейса.Маршрут.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокаМаршрута, СтрокаНовыйМаршрут);
		КонецЦикла;
		упКорректировкаРейса.Маршрут.Сортировать("СтрокаНомер");
		Для каждого СтрокаНоваяРабота Из НовыеРаботы Цикл
			мсвСтрокиРаботы = упКорректировкаРейса.Работы.НайтиСтроки(Новый Структура("ИдентификаторРаботы", СтрокаНоваяРабота.ИдентификаторРаботы));
			Если мсвСтрокиРаботы.Количество() > 0 Тогда
				СтрокаРаботы = мсвСтрокиРаботы[0];
			Иначе	
				СтрокаРаботы = упКорректировкаРейса.Работы.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокаРаботы, СтрокаНоваяРабота);
		КонецЦикла;
	КонецЕсли;
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, упКорректировкаРейса.ТранспортноеСредство, упКорректировкаРейса.ПлановоеНачалоВыполнения);
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(упКорректировкаРейса.ВидПеревозки, "СхемаВыбораПеревозчика, ВидЗоныДляФормированияПредставленияРейса, ВыборПеревозчика, ВыборТС");
	ЭтаФорма.ВидЗоныДляФормированияПредставленияРейса = сткРеквизиты.ВидЗоныДляФормированияПредставленияРейса;
	ЭтаФорма.ВыборПеревозчика                         = сткРеквизиты.ВыборПеревозчика;
	ЭтаФорма.УправлятьПроцессомПодбораПеревозчика     = ЗначениеЗаполнено(сткРеквизиты.СхемаВыбораПеревозчика);
	Если Объект.Проведен Тогда
		ТекущийОбъект = РеквизитФормыВЗначение("Объект");
		РедактированиеЗапрещено = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ТекущийОбъект);
		ЭтаФорма.ТолькоПросмотр = РедактированиеЗапрещено;
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.СобытиеИзмененияСледующейТочки) Тогда
		ЭтаФорма.Событие = Объект.СобытиеИзмененияСледующейТочки;
	ИначеЕсли ЗначениеЗаполнено(Объект.СобытиеПропускаТочки) Тогда
		ЭтаФорма.Событие = Объект.СобытиеПропускаТочки;
	ИначеЕсли ЗначениеЗаполнено(Объект.СобытиеЗапросНаОптимизациюМаршрута) Тогда
		ЭтаФорма.Событие = Объект.СобытиеЗапросНаОптимизациюМаршрута;
	КонецЕсли;
	
	ОбновитьДанныеОПеревозке();
	СтатусПутевогоЛистаПоРейсу = упРейс.СтатусПутевогоЛистаПоРейсу(Объект.Рейс);
	ЭтаФорма.ПеревозчикТолькоПросмотр = НЕ СтатусПутевогоЛистаПоРейсу = Неопределено;
	ЭтаФорма.СотрудникиТолькоПросмотр = НЕ СтатусПутевогоЛистаПоРейсу = Неопределено И НЕ СтатусПутевогоЛистаПоРейсу = Перечисления.упСтатусыПутевогоЛиста.Новый;
	Если ПеревозчикТолькоПросмотр Тогда
		ТекстСообщения = Нстр("ru = 'На основании рейса создан путевой лист, данные по перевозчику доступны только для просмотра'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	Если Параметры.Свойство("ДобавляемыеЗадания") Тогда
		текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		Если Ложь Тогда // Для контекстной подсказки
		    текКорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
		КонецЕсли;
		Если Параметры.ПредпочитаемаяПозицияПогрузки <> Неопределено Тогда
			текКорректировкаМаршрутаОбъект.ЗаполнитьКорректировкуЗаданиями(Параметры.ДобавляемыеЗадания, Параметры.ПредпочитаемаяПозицияПогрузки, Параметры.ПредпочитаемаяПозицияРазгрузки);
		Иначе
			текКорректировкаМаршрутаОбъект.ДобавитьЗадания(Параметры.ДобавляемыеЗадания);
		КонецЕсли; 
		ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
		ЭтаФорма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
	ЗаблокироватьЭлементы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Объект.Рейс) И Не ЗначениеЗаполнено(упКорректировкаРейса.ТранспортноеСредство) Тогда
		Если Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Рейс, "ПеревозкаТранспортнойКомпанией") Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо указать транспортное средство!'"),, "упКорректировкаРейса.ТранспортноеСредство",, Отказ)
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ПроверитьЗаполнениеМаршрута(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПропуститьПроверкуПередЗаписью Тогда
		ПропуститьПроверкуПередЗаписью = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Для каждого Строка Из упКорректировкаРейса.Задания Цикл
			Если Строка.ФлагРаспределения Тогда
				ТекстСообщения = Нстр("ru = '%1%2 не распределено по точкам маршрута'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.Задание, ?(ЗначениеЗаполнено(Строка.ГрузовоеМесто), Нстр("ru = ' с '") + Строка.ГрузовоеМесто,""));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
								Объект.Ссылка,
								ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("упКорректировкаРейса.Задания",Строка.НомерСтроки,"Задание"),
								,
								Отказ);
				Прервать;	
			КонецЕсли;
		КонецЦикла; 
		
		Если Истина
			И Не Отказ
			И ТипЗнч(упКорректировкаРейса.сткПеревозчикПоРейсу) = Тип("Структура")
			И ТипЗнч(упКорректировкаРейса.сткПеревозчикПоРейсуДоКорректировки) = Тип("Структура")
			И упКорректировкаРейса.ТранспортноеСредство <> упКорректировкаРейса.сткПеревозчикПоРейсуДоКорректировки.ТранспортноеСредство
			//И НЕ ОформляетсяПутевойЛист
		Тогда
			ТребуетсяОформлениеПутевогоЛиста = ТребуетсяОформлениеПутевогоЛистаНаСервере();
			
			Если ТребуетсяОформлениеПутевогоЛиста Тогда
				ДополнительныеПараметры = Новый Структура("ПараметрыЗаписи", ПараметрыЗаписи);
				ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросСоздатьПутевойЛист", ЭтаФорма, ДополнительныеПараметры);
				ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Нельзя изменять транспортное средство без создания путевого листа! Создать новый документ ""Путевой лист""?'"), РежимДиалогаВопрос.ДаНет);	
				Отказ = Истина;
			КонецЕсли; 
		КонецЕсли;
		
		Если Истина
			И НЕ Отказ 
			И ЗначениеЗаполнено(Объект.Рейс) 
			И упРейсВызовСервера.ЕстьНаличиеНезавершенныхНаМобильномКлиентеДокументов(Объект.Рейс)
		Тогда
			Отказ = Истина; // Решение о проведении документа будет принято в зависимости от ответа пользователя
			
			ТекстВопроса = НСтр(СтрШаблон("ru = 'По рейсу имеются непроведенные документы ""Прохождение точки"", созданные с мобильного клиента.%1Рекомендуется производить корректировку рейса по окончанию водителем работ в точке.%2Все равно продолжить?'", Символы.ПС, Символы.ПС));
			
			ВариантыОтвета = Новый СписокЗначений();
			ВариантыОтвета.Добавить("Провести", НСтр("ru = 'Провести (не рекомендуется)'"));
			ВариантыОтвета.Добавить("Отмена", НСтр("ru = 'Отмена'"));
			
			ПоказатьВопрос(Новый ОписаниеОповещения("ВывестиПредупреждениеОНаличииНепроведенныхПрохожденийТочекЗавершение", ЭтаФорма, ПараметрыЗаписи), ТекстВопроса, ВариантыОтвета); 
		КонецЕсли;	
	КонецЕсли;	
	
	упСервисныеФункцииКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтотОбъект, ПараметрыЗаписи, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиПредупреждениеОНаличииНепроведенныхПрохожденийТочекЗавершение(ОтветНаВопрос, ПараметрыЗаписи) Экспорт
	
	Если ОтветНаВопрос = "Провести" Тогда
		упСервисныеФункцииКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтотОбъект, ПараметрыЗаписи);
		//ПараметрыЗаписи.Вставить("ПропуститьПроверку", Истина);
		//Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	сткДанныеПоследнегоИзмененияМаршрутаРабот = упПрохождениеТочки.ПоследняяДатаИзмененияМаршрутаРаботРейса(Объект.Рейс);
	
	Если Истина
		И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение
		И сткДанныеПоследнегоИзмененияМаршрутаРабот.Период <> ДатаПоследнегоИзмененияМаршрутаРабот 
		И сткДанныеПоследнегоИзмененияМаршрутаРабот.Регистратор <> ТекущийОбъект.Ссылка 
	Тогда
		ТекстСообщения = Нстр("ru = 'За время работы с корректировкой, маршрут и работы по рейсу были изменены документом %1. Проведение запрещено.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, сткДанныеПоследнегоИзмененияМаршрутаРабот.Регистратор);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,сткДанныеПоследнегоИзмененияМаршрутаРабот.Регистратор,,,Отказ);
	КонецЕсли;
	
	Если ОформляетсяПутевойЛист Тогда
		ОформляетсяПутевойЛист = Ложь;
		ТекущийОбъект.ДополнительныеСвойства.Вставить("СоздатьПутевойЛист", Истина);
	КонецЕсли;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если НЕ ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		ТекущийОбъект.ДополнительныеСвойства.Вставить("КорректировкаРейса", текКорректировкаМаршрутаОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзменилсяМаршрут",,Объект.Рейс);	
	
	Если ОповеститьОбИзмененииСтатусаРейса Тогда
		Оповестить("СменаСтатуса",,Объект.Рейс);	
		ОповеститьОбИзмененииСтатусаРейса = Ложь;
	КонецЕсли;
	
	упСервисныеФункцииКлиент.ВыполнитьДействияПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		ОповеститьОбИзмененииСтатусаРейса = Истина;
	КонецЕсли;
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		
	КонецЕсли;
	
	НастроитьПанельНавигации();
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, упКорректировкаРейса.ТранспортноеСредство, упКорректировкаРейса.ПлановоеНачалоВыполнения);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	мсвСобытий = СтрРазделить(ИмяСобытия, упСервисныеФункцииКлиент.РазделительИменСобытий());
	
	Для каждого ИмяОрабатываемогоСобытия Из мсвСобытий Цикл
		Если ИмяОрабатываемогоСобытия = "ИзменилсяМаршрут" И НЕ Источник = Объект.Ссылка  Тогда
			ЗаблокироватьЭлементы();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		ДобавитьЗаданияНаСервере(ВыбранноеЗначение);	
		ПроверитьЗадатьВопросОДобавленииСвязанных();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьЗаголовокМаршрута();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	Если упКорректировкаРейса.Задания.Количество() > 0 
			ИЛИ упКорректировкаРейса.Маршрут.Количество() > 0 
			ИЛИ упКорректировкаРейса.Работы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ВопросОПерезаполненииТабличныхЧастей", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Табличные части документа будут перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ДатаПоследнегоИзмененияМаршрутаРабот = упПрохождениеТочки.ПоследняяДатаИзмененияМаршрутаРаботРейса(Объект.Рейс).Период;
		ОбновитьДанныеФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РейсНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтатусыРейса = Новый Массив;
	СтатусыРейса.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению"));
	СтатусыРейса.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен"));
	СтатусыРейса.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.ВыполненЧастично"));
	СтатусыРейса.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Завершен"));
	СтатусыРейса.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Отменен"));
	сткПараметры = Новый Структура("СтатусыРейса",Новый ФиксированныйМассив(СтатусыРейса));
	ОткрытьФорму("Документ.упРейс.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#Область ЭлементыФормы_Страница_Задания

&НаКлиенте
Процедура ЗаданияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	текСтрока = Элементы.Задания.ТекущиеДанные;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		текСтрока.СтрокаДокумента	= Истина;
		текСтрока.КоличествоГрузов	= 1;
	КонецЕсли;

	сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаданияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	ОбработатьСтрокуЗаданияНаСервере(текСтрока.НомерСтроки, НоваяСтрока,, ОтменаРедактирования, Отказ);
	Если НЕ Отказ Тогда
		Модифицированность = Истина;	
	КонецЕсли;
	ПроверитьЗадатьВопросОДобавленииСвязанных();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗадатьВопросОДобавленииСвязанных()
	
	Если НеобходимоДобавитьСвязанныеГрузовыеМеста() Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОСвязанныхГрузовыхМестах",ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru = 'Существуют связанные грузовые места, не включенные в рейс. Добавить их в рейс?'"), РежимДиалогаВопрос.ДаНет);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаданияПередУдалением(Элемент, Отказ)
	
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
				
		текСтрока = упКорректировкаРейса.Задания.НайтиПоИдентификатору(текИдентификатор); 
				
		Если Не текСтрока = Неопределено Тогда
			
			Если НЕ текСтрока.СтрокаДокумента Тогда
				ТекстСообщения = Нстр("ru = 'Нельзя удалить строку номер %1. Разрешено удалять только строки добавленные в текущем документе.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текСтрока.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				Продолжить;
			КонецЕсли;
			
			сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
			Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
				сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
			КонецЦикла;
			ОбработатьСтрокуЗаданияНаСервере(текСтрока.НомерСтроки,,Истина,,Отказ);
			Если НЕ Отказ Тогда
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеПриИзменении(Элемент = Неопределено)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если Не текСтрока = Неопределено Тогда
		  ЗаданиеПриИзмененииНаСервере(текСтрока.НомерСтроки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	текОтбор 	= Новый Структура();
	ЗаполнитьСтруктуруОтбора(текОтбор);
	сткПараметры 	= Новый Структура("Отбор", текОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.ЗаданияЗадание);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	текОтбор 	= ПараметрыПолученияДанных.Отбор;
	ЗаполнитьСтруктуруОтбора(текОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	текОтбор 	= ПараметрыПолученияДанных.Отбор;
	ЗаполнитьСтруктуруОтбора(текОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияГрузовоеМестоПриИзменении(Элемент)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если Не текСтрока = Неопределено Тогда
		  ЗаданиеПриИзмененииНаСервере(текСтрока.НомерСтроки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияКоличествоГрузовПриИзменении(Элемент)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если Не текСтрока = Неопределено Тогда
		  ЗаданиеПриИзмененииНаСервере(текСтрока.НомерСтроки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если текСтрока <> Неопределено 	И НЕ текСтрока.СтрокаДокумента Тогда
		Если Поле.Имя = "ЗаданияЗадание" Тогда
			ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Задание), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ЗаданияГрузовоеМесто" Тогда	
			ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ГрузовоеМесто), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ЗаданияПроблема" Тогда	
			ОткрытьФорму("Справочник.упПроблемы.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Проблема), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы_Страница_Маршрут

&НаКлиенте
Процедура МаршрутПриАктивизацииСтроки(Элемент)
	СтрокаТочка = Элемент.ТекущиеДанные;
	
	Если СтрокаТочка <> Неопределено Тогда
		Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", Элемент.ТекущиеДанные.Идентификатор);
		упКорректировкаМаршрутаКлиент.УстановитьДоступностьКнопокПеремещенияТочекМаршрута(Элемент,
			                                                                                  упКорректировкаРейса.Маршрут,
																			  Элементы.МаршрутПереместитьТочкуВверх,
																			  Элементы.МаршрутПереместитьТочкуВниз);
		
		сткТочка = Новый Структура("Пройдена, Отменена, Идентификатор");
		ЗаполнитьЗначенияСвойств(сткТочка, СтрокаТочка);
		РазрешеноРазделятьРаботы = РазрешеноРазделятьРаботы(сткТочка);
		Элементы.РаботыРазделитьРаботы.Доступность = РазрешеноРазделятьРаботы;
		
		// Установить доступность кнопки отмены точки.
		Элементы.МаршрутОтменитьТочку.Доступность = Истина
											И (Ложь
												Или СтрокаТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаОтправления")
												Или СтрокаТочка.ТипТочки = ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаЗавершения"))
											И НЕ СтрокаТочка.Пройдена
											И НЕ СтрокаТочка.СтрокаДокумента;
	КонецЕсли;
	
	УстановитьЗаголовокМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	текСтрока = Элемент.ТекущиеДанные;
	Если НоваяСтрока ИЛИ Копирование Тогда
		текСтрока.ТипТочки 			= ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.ТочкаПоЗаданию");
		текСтрока.ИндексКартинки	= упПрохождениеТочкиКлиентСервер.ИндексКартинкиТочки(текСтрока.ТипТочки);
		текСтрока.СтрокаДокумента	= Истина;
		текСтрока.СтрокаНомер = текСтрока.НомерСтроки;
	КонецЕсли;
	
	сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока Тогда
		
		Отказ = Ложь;
		ДобавитьТочкуВМаршрутНаСервере(Элемент.ТекущиеДанные.НомерСтроки, Отказ);
		Если НЕ Отказ Тогда
			Модифицированность = Истина;	
		КонецЕсли;
	
		Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", Элемент.ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
	текСтрока = Элемент.ТекущиеДанные;
	ОбработатьСтрокуТочкаМаршрутаНаСервере(текСтрока.НомерСтроки,НоваяСтрока,,ОтменаРедактирования, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередУдалением(Элемент, Отказ)
	
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		
		текТочка = упКорректировкаРейса.Маршрут.НайтиПоИдентификатору(текИдентификатор); 
				
		Если НЕ текТочка.СтрокаДокумента Тогда
			 ТекстСообщения = Нстр("ru = 'Точка в строке %1 добавлена другим документом, её нельзя удалить.'");
			 ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текТочка.СтрокаНомер);
			 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
		
		Если Не текТочка = Неопределено И НЕ Отказ Тогда
			Если упКорректировкаРейса.ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками И текТочка.ТипТочки <> ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.КонтрольнаяТочка") Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
			сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
			
			Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
				сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, текТочка[текПоле.Ключ]);
			КонецЦикла;
			ОбработатьСтрокуТочкаМаршрутаНаСервере(текТочка.НомерСтроки,, Истина,, Отказ);
		КонецЕсли;
			
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура МаршрутПослеУдаления(Элемент)
	ОбновитьПредставлениеМаршрутаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриИзменении(Элемент)
	МодифицированаМаршрутФорма 	= Истина;
	Модифицированность			= Истина;
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Не упКорректировкаРейса.ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками Тогда
		Возврат;
	КонецЕсли;
	
	текСтрока = Элементы.Маршрут.ТекущиеДанные;
	Если текСтрока <> Неопределено И  текСтрока.ТипТочки <> ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.КонтрольнаяТочка") Тогда
		
		Если Поле.Имя = "МаршрутАдрес" Тогда
			ОткрытьФорму("Справочник.упАдреса.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Адрес), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	СтрокаРабота = Элемент.ТекущиеДанные;
	СтрокаРабота.СтрокаДокумента = НоваяСтрока;
		
	сткЗначенияСтрокиРаботДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиРаботДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиРаботДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиРаботДоРедактирования.Вставить(текПоле.Ключ, СтрокаРабота[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура РаботыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НоваяСтрока Тогда
		ДобавитьРаботуНаСервере(Элемент.ТекущиеДанные.НомерСтроки, Элементы.Маршрут.ТекущиеДанные.Идентификатор, Отказ);
		Если НЕ Отказ Тогда
			Модифицированность = Истина;	
		КонецЕсли;
	Иначе
		СтрокаРабота = Элементы.Работы.ТекущиеДанные;
		Если СтрокаРабота.КоличествоГрузов = 0	
				И ЗначениеЗаполнено(СтрокаРабота.ГрузовоеМесто) Тогда
			СтрокаРабота.КоличествоГрузов = 1;
		КонецЕсли;
		
		СтрокаТочка = Элементы.Маршрут.ТекущиеДанные;
		
		СтрокаРабота.ЗапрещеноСозданиеНовойТочкиМаршрута = Ложь;
		
		Если Истина
			И ЗначениеЗаполнено(СтрокаРабота.Проблема)
			И упПрохождениеТочкиКлиентСервер.ОперацияРазгрузки(СтрокаРабота.Операция)
		Тогда
			СтрокаРабота.ЗапрещеноСозданиеНовойТочкиМаршрута = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтрокаРабота.Проблема, "ЗапрещеноСозданиеНовойТочкиМаршрута");
			Если СтрокаРабота.ЗапрещеноСозданиеНовойТочкиМаршрута Тогда
				СтрокаРабота.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
			КонецЕсли;
		КонецЕсли;
		
		Если Истина
			И СтрокаРабота.Проблема <> ПредопределенноеЗначение("Справочник.упПроблемы.ДосрочноеПрохождение")
			И ЗначениеЗаполнено(СтрокаРабота.АдресРазгрузки)
			И СтрокаТочка.Адрес = СтрокаРабота.АдресРазгрузки
		Тогда
			
			СтрокаРабота.АдресРазгрузки  = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
			ТекстСообщения = Нстр("ru = 'Адрес разгрузки не должен совпадать с адресом текущей точки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("упКорректировкаРейса.Работы",СтрокаРабота.НомерСтроки,"АдресРазгрузки"),,Отказ);
			
		КонецЕсли;
		
		Если Истина
			И СтрокаТочка <> Неопределено 
			И СтрокаРабота <> Неопределено 
		Тогда
			
			СтарыйАдрес = Неопределено;
			СтараяПроблема = Неопределено;
			СтароеЗапрещеноСозданиеНовойТочкиМаршрута = Неопределено;
			
			Если НЕ упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования  = Неопределено Тогда
				СтарыйАдрес = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования.АдресРазгрузки;
				СтараяПроблема = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования.Проблема;
				СтароеЗапрещеноСозданиеНовойТочкиМаршрута = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования.ЗапрещеноСозданиеНовойТочкиМаршрута;
			КонецЕсли;
			
			сткВозврата = Неопределено;
			упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(СтрокаРабота, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, СтараяПроблема, СтарыйАдрес, сткВозврата, СтрокаТочка.СтрокаНомер, упКорректировкаРейса.АдресаПогрузкиПоРейсу);
			ОбработатьСтрокуРаботыНаСервере(СтрокаРабота.НомерСтроки, СтрокаТочка.НомерСтроки,,,ОтменаРедактирования,Отказ,,сткВозврата);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РаботыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Элементы.Маршрут.ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сначала необходимо добавить маршрут'"), Объект.Ссылка, "Маршрут",,Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РаботыПередУдалением(Элемент, Отказ)
	
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	
	мсвИдентификаторыРабот 	= Новый Массив;
	мсвСтроки				= Новый Массив;
	
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		СтрокаРабота = упКорректировкаРейса.Работы.НайтиПоИдентификатору(текИдентификатор);
		
		
		Если НЕ СтрокаРабота.СтрокаДокумента Тогда
			 ТекстСообщения = Нстр("ru = 'Работа в строке %1 добавлена другим документом, её нельзя удалить.'");
			 ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаРабота.СтрокаНомер);
			 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЕсли;
		
		Если НЕ СтрокаРабота = Неопределено И НЕ Отказ Тогда
			Если упКорректировкаРейса.ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками 
				И упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(СтрокаРабота.Операция) Тогда
				Отказ = Истина;
				Прервать;
			КонецЕсли;
			мсвИдентификаторыРабот.Добавить(СтрокаРабота.ИдентификаторРаботы);
			мсвСтроки.Добавить(СтрокаРабота);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		сткЗначенияСтрокиРаботДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования;
		Для каждого СтрокаРабота Из мсвСтроки Цикл
			Для каждого текПоле Из сткЗначенияСтрокиРаботДоРедактирования Цикл
				сткЗначенияСтрокиРаботДоРедактирования.Вставить(текПоле.Ключ, СтрокаРабота[текПоле.Ключ]);
			КонецЦикла;
			ОбработатьСтрокуРаботыНаСервере(СтрокаРабота.НомерСтроки,Элементы.Маршрут.ТекущиеДанные.НомерСтроки,Истина,,Отказ, мсвИдентификаторыРабот);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РаботыПриИзменении(Элемент)
	
	МодифицированаМаршрутФорма 	= Истина;
	МодифицированаРаботыФорма	= Истина;
	Модифицированность			= Истина;

КонецПроцедуры

&НаКлиенте
Процедура РаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.Работы.ТекущиеДанные;
	Если текСтрока <> Неопределено И упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(текСтрока.Операция) Тогда
		Если Поле.Имя = "РаботыЗадание" Тогда
			ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Задание), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "РаботыГрузовоеМесто" И ЗначениеЗаполнено(текСтрока.ГрузовоеМесто) Тогда 	
			ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ГрузовоеМесто), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РаботыОперацияПриИзменении(Элемент)
	Элементы.Работы.ТекущиеДанные.ТипОперации = упПрохождениеТочкиКлиентСервер.ИндексКартинкиОперации(Элементы.Работы.ТекущиеДанные.Операция);
КонецПроцедуры

&НаКлиенте
Процедура РаботыЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвЗаданий = Новый Массив;
	Для каждого текЗадание Из упКорректировкаРейса.Задания Цикл
		мсвЗаданий.Добавить(текЗадание.Задание);
	КонецЦикла; 
	мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
	сткОтбор     = Новый Структура("Ссылка", мсвЗаданий);
	сткПараметры = Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.РаботыЗадание);
КонецПроцедуры

&НаКлиенте
Процедура РаботыЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	текТочка 	= Элементы.Маршрут.ТекущиеДанные;
	текОтбор 	= ПараметрыПолученияДанных.Отбор;
	ЗаполнитьСтруктуруОтбора(текОтбор, текТочка.Адрес);
	текОтбор.Вставить("ОтборПоАдресу");
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура упКорректировкаРейсаПеревозчикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("ВидПеревозки, Организация, ТипТС", упКорректировкаРейса.ВидПеревозки, упКорректировкаРейса.Организация, упКорректировкаРейса.ТипТС);
	ОткрытьФорму("Справочник.упПартнеры.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТипТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("ВидПеревозки, Перевозчик, Организация", упКорректировкаРейса.ВидПеревозки, упКорректировкаРейса.Перевозчик, упКорректировкаРейса.Организация);
	ОткрытьФорму("Справочник.упТипыТС.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТипТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТипаТС();
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТипТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТипаТС();
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	сткПараметры = Новый Структура("ВидПеревозки, Перевозчик, Организация, ТипТС", упКорректировкаРейса.ВидПеревозки, упКорректировкаРейса.Перевозчик, упКорректировкаРейса.Организация, упКорректировкаРейса.ТипТС);
	ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТранспортноеСредствоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И ВыбранноеЗначение <> упКорректировкаРейса.ТранспортноеСредство Тогда
		
		мсвПараметрыОбработки = ПараметрыОбработкиПриВыбореТранспортногоСредства(ВыбранноеЗначение);
					
		Для каждого сткПараметрыОбработки Из мсвПараметрыОбработки Цикл
			Если НЕ сткПараметрыОбработки.ИмяПроцедуры = Неопределено Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения(сткПараметрыОбработки.ИмяПроцедуры, ЭтаФорма, сткПараметрыОбработки.ДополнительныеПараметры);
				ПоказатьВопрос(ОписаниеОповещения, сткПараметрыОбработки.ТекстВопроса, РежимДиалогаВопрос.ДаНет);	 
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТранспортноеСредствоАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТС();
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаТранспортноеСредствоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТС();
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаПеревозчикПриИзменении(Элемент)
	Если ЗначениеЗаполнено(упКорректировкаРейса.Перевозчик) Тогда
		Если НЕ ЗначениеЗаполнено(упКорректировкаРейса.Соглашение) Тогда
			упКорректировкаРейса.Соглашение = ПолучитьСоглашениеПоУмолчанию();
		КонецЕсли;	
		упКорректировкаРейса.КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(упКорректировкаРейса.Перевозчик);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура упКорректировкаРейсаСоглашениеПриИзменении(Элемент)
		СоглашениеПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПереместитьТочкуВверх(Команда)
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	Отказ = Ложь;
	
	ИндексСтроки = упКорректировкаРейса.Маршрут.Индекс(ТекущиеДанные);
	мсвПредшественников = СтрРазделить(ТекущиеДанные.Предшественники, ",", Ложь);
	ЗаменяемаяСтрока = упКорректировкаРейса.Маршрут[ИндексСтроки - 1];
	Если мсвПредшественников.Найти(Строка(ЗаменяемаяСтрока.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ЗаменяемаяСтрока.Адрес, ТекущиеДанные.Адрес);
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтроки = СдвинутьТочкуВМаршрутеНаСервере(ТекущиеДанные.НомерСтроки,,Ложь);
	Если НЕ Отказ Тогда
		Элементы.Маршрут.ТекущаяСтрока = ИдентификаторСтроки;
		Модифицированность = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьТочкуВниз(Команда)
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	Отказ = Ложь;
	
	ИндексСтроки = упКорректировкаРейса.Маршрут.Индекс(ТекущиеДанные);
	ЗаменяемаяСтрока = упКорректировкаРейса.Маршрут[ИндексСтроки + 1];
	мсвПредшественников = СтрРазделить(ЗаменяемаяСтрока.Предшественники, ",", Ложь);
	Если мсвПредшественников.Найти(Строка(ТекущиеДанные.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ТекущиеДанные.Адрес, ЗаменяемаяСтрока.Адрес);
		Возврат;
	КонецЕсли; 	
	
	ИдентификаторСтроки = СдвинутьТочкуВМаршрутеНаСервере(ТекущиеДанные.НомерСтроки,Ложь,Ложь);
	Если НЕ Отказ Тогда
		Элементы.Маршрут.ТекущаяСтрока = ИдентификаторСтроки;
		Модифицированность = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьТочку(Команда)
	Отказ = Ложь;	
	МаршрутТекущий = Элементы.Маршрут.ТекущиеДанные;
	Если МаршрутТекущий = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если МаршрутТекущий.Отменена Тогда
		Элементы.Маршрут.ТекущиеДанные.Отменена = Ложь;
		СнятьОтменуРабот(МаршрутТекущий.Идентификатор);
	ИначеЕсли МаршрутТекущий.Пройдена Тогда	
		ТекстСообщения = Нстр("ru = 'Нельзя отменять пройденную точку'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	Иначе	
		ТипОперацииТекущий = ПолучитьТипСамойПриоритетнойОперации(Объект.Рейс, МаршрутТекущий.Идентификатор);
		сткПараметры = Новый Структура("Отбор",Новый Структура("ТипОперации",ТипОперацииТекущий));
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыВыбораПроблемы",ЭтаФорма);
		ОткрытьФорму("Справочник.упПроблемы.ФормаВыбора",сткПараметры,,,,,ОповещениеОЗакрытии);
	КонецЕсли;
		
	Если НЕ Отказ Тогда
		Модифицированность = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеМаршрута(Команда)
	ОбновитьПредставлениеМаршрутаНаСервере()
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗаданияПоТочкам(Команда)
	РаспределитьЗаданияПоТочкамНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗаданияПоНеПройденнымТочкам(Команда)
	РаспределитьЗаданияПоНеПройденнымТочкамНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтрольнуюТочку(Команда)
	
	Элементы.Маршрут.ДобавитьСтроку();
	
	НоваяСтрока 				= Элементы.Маршрут.ТекущиеДанные;
	НоваяСтрока.ТипТочки 		= ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.КонтрольнаяТочка");
	НоваяСтрока.ИндексКартинки	= упПрохождениеТочкиКлиентСервер.ИндексКартинкиТочки(НоваяСтрока.ТипТочки);
	НоваяСтрока.СтрокаДокумента	= Истина;
	НоваяСтрока.СтрокаНомер	= НоваяСтрока.НомерСтроки;
	
	сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
		сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ПодборЗаданий(Команда)
	
	текОтбор 	= Новый Структура();
	ЗаполнитьСтруктуруОтбора(текОтбор);
	сткПараметры 	= Новый Структура("Отбор, МножественныйВыбор", текОтбор, Истина);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, ЭтаФорма);
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗадания(Команда)
	
	спзЗаданияДляОтмены 		= Новый СписокЗначений;
	ОткрытьФормуВыбораПроблемы 	= Ложь;
	
	Для каждого ВыделеннаяСтрока Из Элементы.Задания.ВыделенныеСтроки Цикл
		СтрокаЗадание = упКорректировкаРейса.Задания.НайтиПоИдентификатору(ВыделеннаяСтрока);
		
		Если СтрокаЗадание.Отменено Тогда // удалена возможность возврата отмененных ранее заданий в рамках одного рейса 
			Если ЗначениеЗаполнено(СтрокаЗадание.ГрузовоеМесто) Тогда
				ТекстСообщения = СтрШаблон("ru = 'Работы по грузовому месту ""%1"" уже отменены!'", СтрокаЗадание.ГрузовоеМесто);
			Иначе
				ТекстСообщения = СтрШаблон("ru = 'Работы по документу ""%1"" уже отменены!'", СтрокаЗадание.Задание);
			КонецЕсли; 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстСообщения));
			
		Иначе	
			спзЗаданияДляОтмены.Добавить(Новый Структура("Задание, ГрузовоеМесто, Отменено", СтрокаЗадание.Задание, СтрокаЗадание.ГрузовоеМесто, СтрокаЗадание.Отменено));
			Если Не СтрокаЗадание.Отменено И Не ОткрытьФормуВыбораПроблемы Тогда
				ОткрытьФормуВыбораПроблемы = Истина;
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ОткрытьФормуВыбораПроблемы Тогда
		сткПараметры = Новый Структура("Отбор",Новый Структура("ТипОперации", упПрохождениеТочкиКлиентСервер.ОперацияПогрузкиПоГрузовомуМесту(СтрокаЗадание.ГрузовоеМесто)));
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОтменитьЗаданияНаКлиенте", ЭтаФорма);
		ОткрытьФорму("Справочник.упПроблемы.ФормаВыбора",сткПараметры,,,,,ОповещениеОЗакрытии);
	Иначе	
		ОтменитьЗаданияНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредшественников(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Маршрут", 			упКорректировкаРейса.Маршрут);
	сткПараметры.Вставить("ИдентификаторТочки", Элементы.Маршрут.ТекущиеДанные.Идентификатор);
	ОткрытьФорму("Обработка.упКорректировкаРейса.Форма.Предшественники", сткПараметры);
КонецПроцедуры

&НаКлиенте
Процедура РазделитьРаботы(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Идентификатор", 		Элементы.Маршрут.ТекущиеДанные.Идентификатор);
	сткПараметры.Вставить("Работы", 			упКорректировкаРейса.Работы);
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ОбработатьРазделениеТочки", ЭтаФорма);
	
	ОткрытьФорму("Обработка.упКорректировкаРейса.Форма.РазделениеРаботВТочке", сткПараметры, ЭтаФорма, УникальныйИдентификатор, , , ОбработчикЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	упСервисныеФункцииКлиент.ПровестиИЗакрыть(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключаемыйЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
// Процедура перезаполнения табличных частей после утверидтельного ответа.
//
Процедура ВопросОПерезаполненииТабличныхЧастей(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДатаПоследнегоИзмененияМаршрутаРабот = упПрохождениеТочки.ПоследняяДатаИзмененияМаршрутаРаботРейса(Объект.Рейс).Период;
		ОбновитьДанныеФормы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора проблемы.
//
Процедура ОбработатьЗакрытиеФормыВыбораПроблемы(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		
		СтрокаТочка = Элементы.Маршрут.ТекущиеДанные;
				
		ЕстьСтрокиПоТочке = Ложь;
		
		Для каждого СтрокаРабота Из упКорректировкаРейса.Работы  Цикл
			Если НЕ СтрокаРабота.Идентификатор = СтрокаТочка.Идентификатор Тогда
				Продолжить;
			КонецЕсли;
			
			ЕстьСтрокиПоТочке = Истина;
			СтараяПроблема 	= СтрокаРабота.Проблема;
			СтарыйАдрес		= СтрокаРабота.АдресРазгрузки; 
			СтрокаРабота.Проблема = РезультатЗакрытия;
							
			сткВозврата 	= Неопределено;
			упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(СтрокаРабота, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, СтараяПроблема, СтарыйАдрес, сткВозврата, СтрокаТочка.СтрокаНомер, упКорректировкаРейса.АдресаПогрузкиПоРейсу);	
			Отказ = Ложь;
			ОбработатьСтрокуРаботыПриОтменеТочкиНаСервере(СтрокаРабота.НомерСтроки, СтрокаТочка.НомерСтроки,,,Ложь,Отказ,,сткВозврата);	
			
		КонецЦикла;
		
		Если НЕ ЕстьСтрокиПоТочке Тогда
			СтрокаТочка.Отменена = Истина;
		КонецЕсли;
		
		Если упКорректировкаРейса.Маршрут.Количество() > СтрокаТочка.НомерСтроки Тогда
			СпланироватьЗадачиПоМаршруту(СтрокаТочка.НомерСтроки, Ложь, Истина);	
		КонецЕсли;
		
				
		РаботыМодифицированы = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - Обработать разделение точки
//
// Параметры:
//  РезультатЗакрытия		 - 	 - 
//  ДополнительныеПараметры	 - 	 - 
//
Процедура ОбработатьРазделениеТочки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		ИдентификаторТочки 	= РезультатЗакрытия.ИдентификаторТочки;
		СписокОпераций		= РезультатЗакрытия.СписокОпераций;
		РазделитьРаботыВТочкеНаСервере(ИдентификаторТочки, СписокОпераций);
		Модифицированность 	= Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьЗаголовокМаршрута()

	Элементы.ГруппаМаршрут.Заголовок = СтрШаблон("Маршрут (%1)", Формат(упКорректировкаРейса.Маршрут.Количество(),"ЧН=0; ЧГ=0"));

КонецПроцедуры // УстановитьЗаголовокМаршрута()

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ОбновитьДанныеФормы()
	
	текКорректировкаМаршрутаОбъект 	= РеквизитФормыВЗначение("упКорректировкаРейса");
	текОбъект = РеквизитФормыВЗначение("Объект");
	текОбъект.ИнициализацияКорректировкиМаршрута(текКорректировкаМаршрутаОбъект);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	ЗначениеВРеквизитФормы(текОбъект, "Объект");
	
	Элементы.ЗаданияГрузовоеМесто.Видимость = текКорректировкаМаршрутаОбъект.ФормироватьПоГрузовымМестам;
	Элементы.ЗаданияКоличествоГрузов.Видимость = текКорректировкаМаршрутаОбъект.ФормироватьПоГрузовымМестам;
	Элементы.РаботыГрузовоеМесто.Видимость = (текКорректировкаМаршрутаОбъект.ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится);
	Элементы.РаботыКоличествоГрузов.Видимость = (текКорректировкаМаршрутаОбъект.ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится);;	
	
	// Элементы связанные с вводом сумм инкассации.
	ВидПеревозки = текКорректировкаМаршрутаОбъект.ВидПеревозки;
	ИспользуетсяИнкассация = Ложь;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассация = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	Элементы.РаботыСумма.Видимость = ИспользуетсяИнкассация;
	Элементы.РаботыВалюта.Видимость = ИспользуетсяИнкассация;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаданияНаСервере(мсвЗаданийНаПеревозкуГруза)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ДобавитьЗадания(мсвЗаданийНаПеревозкуГруза);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТочкуВМаршрутНаСервере(СтрокаНомер, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьДобавлениеТочкиВМаршрут(СтрокаНомер,,Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРаботуНаСервере(НомерСтроки, ИдентификаторТочки, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьДобавлениеРаботы(НомерСтроки, ИдентификаторТочки, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Функция СдвинутьТочкуВМаршрутеНаСервере(СтрокаНомер, СдвигВверх = Истина, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	ИдентификаторСтроки = текКорректировкаМаршрутаОбъект.ОбработатьСдвигТочкиМаршрута(СтрокаНомер, СдвигВверх, Ложь, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	Возврат ИдентификаторСтроки;
КонецФункции

&НаКлиенте
Процедура СнятьОтменуРабот(ИдентификаторТочки)
		
	СтрокаТочка = Элементы.Маршрут.ТекущиеДанные;
	
	Для каждого СтрокаРабота Из упКорректировкаРейса.Работы  Цикл
		Если НЕ СтрокаРабота.Идентификатор = СтрокаТочка.Идентификатор Тогда
			Продолжить;
		КонецЕсли;
		
		СтараяПроблема 	= СтрокаРабота.Проблема;
		СтарыйАдрес		= СтрокаРабота.АдресРазгрузки; 
		СтрокаРабота.Проблема 		= ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
		СтрокаРабота.АдресРазгрузки = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		
		сткВозврата 	= Неопределено;
		упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(СтрокаРабота, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, СтараяПроблема, СтарыйАдрес, сткВозврата, СтрокаТочка.СтрокаНомер, упКорректировкаРейса.АдресаПогрузкиПоРейсу);	
		Отказ = Ложь;
		ОбработатьСтрокуРаботыНаСервере(СтрокаРабота.НомерСтроки, СтрокаТочка.НомерСтроки,,,Ложь,Отказ,,сткВозврата);	
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуЗаданияНаСервере(НомерСтроки, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиЗадания(НомерСтроки, НоваяСтрока,Удалить, ОтменаРедактирования, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТочкаМаршрутаНаСервере(НомерСтроки, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено, ПерепланироватьРаботыМаршрута = Ложь)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиМаршрута(НомерСтроки, НоваяСтрока, Удалить, ОтменаРедактирования, Отказ, мсвВыделенныеСтроки, ПерепланироватьРаботыМаршрута);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура СпланироватьЗадачиПоМаршруту(ИндексСтроки, ПересчитыватьПредыдущиеТочки = Истина, ПересчитыватьТекущуюТочку = Ложь)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.СпланироватьЗадачиПоМаршруту(ИндексСтроки, ПересчитыватьПредыдущиеТочки, ПересчитыватьТекущуюТочку);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуРаботыНаСервере(НомерСтроки, НомерСтрокиТочка, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено, сткВосстановлениеСтроки = Неопределено)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиРаботы(НомерСтроки, НомерСтрокиТочка, НоваяСтрока, Удалить, ОтменаРедактирования, Отказ, мсвВыделенныеСтроки, сткВосстановлениеСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуРаботыПриОтменеТочкиНаСервере(НомерСтроки, НомерСтрокиТочка, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено, сткВосстановлениеСтроки = Неопределено)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиРаботы(НомерСтроки, НомерСтрокиТочка, НоваяСтрока, Удалить, ОтменаРедактирования, Отказ, мсвВыделенныеСтроки, сткВосстановлениеСтроки, Ложь, Ложь, Ложь);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ЗаданиеПриИзмененииНаСервере(НомерСтроки)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ЗаполнитьВспомогательныеПоляЗаданийНаСервере(НомерСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтруктуруОтбора(Отбор, Адрес = Неопределено)
	
	Если НЕ Адрес = Неопределено Тогда
		Отбор.Вставить("Адрес", Адрес);
	КонецЕсли;
	Отбор.Вставить("Статус", 		ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию")); 
	//Отбор.Вставить("Организация", 	упКорректировкаРейса.Организация); 
	Отбор.Вставить("ВидПеревозки", 	упКорректировкаРейса.ВидПеревозки); 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеМаршрутаНаСервере()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ВидЗоныДляФормированияПредставленияРейса = ВидЗоныДляФормированияПредставленияРейса;
	текКорректировкаМаршрутаОбъект.УстановитьПредставлениеМаршурта();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
	
	РазрешеноКорректироватьМаршрут 	= упКорректировкаМаршрута.РазрешеноКорректироватьМаршрут(Объект.Дата, Объект.Ссылка, Объект.Рейс);
	Элементы.МаршрутПереместитьТочкуВверх.Доступность 		= РазрешеноКорректироватьМаршрут;
	Элементы.МаршрутПереместитьТочкуВниз.Доступность 		= РазрешеноКорректироватьМаршрут;
	Элементы.РаспределитьЗадания.Доступность 				= РазрешеноКорректироватьМаршрут;
	Элементы.МаршрутДобавитьКонтрольнуюТочку.Доступность 	= РазрешеноКорректироватьМаршрут;
	Элементы.МаршрутОтменитьТочку.Доступность 				= РазрешеноКорректироватьМаршрут;
	
	Элементы.Задания.ТолькоПросмотр 	= НЕ РазрешеноКорректироватьМаршрут;
	Элементы.Маршрут.ТолькоПросмотр 	= НЕ РазрешеноКорректироватьМаршрут;
	Элементы.Работы.ТолькоПросмотр 		= НЕ РазрешеноКорректироватьМаршрут;
	
	Элементы.упКорректировкаРейсаПеревозчик.ТолькоПросмотр 				= ПеревозчикТолькоПросмотр;
	Элементы.упКорректировкаРейсаКонтрагентПеревозчика.ТолькоПросмотр 	= ПеревозчикТолькоПросмотр;
	Элементы.упКорректировкаРейсаСоглашение.ТолькоПросмотр 				= ПеревозчикТолькоПросмотр;
	Элементы.упКорректировкаРейсаТипТС.ТолькоПросмотр 					= ПеревозчикТолькоПросмотр;
	Элементы.упКорректировкаРейсаТранспортноеСредство.ТолькоПросмотр 	= ПеревозчикТолькоПросмотр;
	Элементы.упКорректировкаРейсаВодитель1.ТолькоПросмотр 				= СотрудникиТолькоПросмотр;
	Элементы.упКорректировкаРейсаВодитель2.ТолькоПросмотр 				= СотрудникиТолькоПросмотр;
	Элементы.упКорректировкаРейсаЭкспедитор.ТолькоПросмотр 				= СотрудникиТолькоПросмотр;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТипСамойПриоритетнойОперации(Рейс, ИдентификаторТочки)
	
	Возврат упПрохождениеТочки.ПолучитьТипСамойПриоритетнойОперацииВТочке(Рейс, ИдентификаторТочки);
	
КонецФункции

&НаСервере
Процедура РаспределитьЗаданияПоТочкамНаСервере()
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Отказ = Ложь;
	текКорректировкаМаршрутаОбъект.РаспределитьЗаданияПоТочкам(Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
		
КонецПроцедуры

&НаСервере
Процедура РаспределитьЗаданияПоНеПройденнымТочкамНаСервере()
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Отказ = Ложь;
	текКорректировкаМаршрутаОбъект.РаспределитьЗаданияПоНепройденнымТочкам(Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаданияНаКлиенте(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
					
	Для каждого ЭлементЗадание Из спзЗаданияДляОтмены Цикл
		
		СтрокаЗадание = ЭлементЗадание.Значение;
		
		сткОтбора = Новый Структура();
		сткОтбора.Вставить("Задание", 	СтрокаЗадание.Задание);
		сткОтбора.Вставить("Операция", 	упПрохождениеТочкиКлиентСервер.ОперацияПогрузкиПоГрузовомуМесту(СтрокаЗадание.ГрузовоеМесто));
		Если ЗначениеЗаполнено(СтрокаЗадание.ГрузовоеМесто) Тогда
			сткОтбора.Вставить("ГрузовоеМесто", СтрокаЗадание.ГрузовоеМесто);
		КонецЕсли;
		
		Проблема = ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");
		
		Если СтрокаЗадание.Отменено Тогда
			сткОтбора.Вставить("Отменена", 	Истина);	
		Иначе	
			сткОтбора.Вставить("Отменена", 	Ложь);	
			Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
				Проблема = РезультатЗакрытия;
			Иначе
				Продолжить;
			КонецЕсли;
			
			сткОтбораВыполненныеСтроки = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(сткОтбора);
			сткОтбораВыполненныеСтроки.Вставить("Выполнена", Истина);
			
			мсвВыполненныеСтрокиПогрузки = упКорректировкаРейса.Работы.НайтиСтроки(сткОтбораВыполненныеСтроки);
			
			Если мсвВыполненныеСтрокиПогрузки.Количество() > 0 Тогда
				ТекстСообщения = Нстр("ru = 'По %1 %2 есть выполненные строки погрузки, отмена невозможна'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаЗадание.Задание, СтрокаЗадание.ГрузовоеМесто);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		мсвСтрокиРабот = упКорректировкаРейса.Работы.НайтиСтроки(сткОтбора);
		Для каждого СтрокаРабота Из мсвСтрокиРабот Цикл
			мсвСтрокиТочки  = упКорректировкаРейса.Маршрут.НайтиСтроки(Новый Структура("Идентификатор", СтрокаРабота.Идентификатор));
			
			Если мсвСтрокиТочки.Количество() > 0 Тогда
				
				СтрокаТочка = мсвСтрокиТочки[0];
				
				СтараяПроблема 	= СтрокаРабота.Проблема;
				СтарыйАдрес		= СтрокаРабота.АдресРазгрузки; 
				СтрокаРабота.Проблема = Проблема;
				
				сткВозврата 	= Неопределено;
				упПрохождениеТочкиКлиентСервер.ОбработатьИзменениеРаботы(СтрокаРабота, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, СтараяПроблема, СтарыйАдрес, сткВозврата, СтрокаТочка.СтрокаНомер, упКорректировкаРейса.АдресаПогрузкиПоРейсу);	
				Отказ = Ложь;
				ОбработатьСтрокуРаботыНаСервере(СтрокаРабота.НомерСтроки, СтрокаТочка.НомерСтроки,,,Ложь,Отказ,,сткВозврата);	
				
				РаботыМодифицированы = Истина;
				
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
		
	
	
КонецПроцедуры

Процедура СоглашениеПриИзмененииНаСервере()
	
	ОбновитьДанныеОПеревозке();
	
КонецПроцедуры // СоглашениеПриИзмененииНаСервере()	

&НаСервере
Функция ПараметрыОбработкиПриВыбореТранспортногоСредства(ТранспортноеСредство)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	сткПараметрыОбработки = текКорректировкаМаршрутаОбъект.ОбработатьИзменениеТранспортногоСредстваРейса(ТранспортноеСредство, упКорректировкаРейса.ЗонаПогрузки, упКорректировкаРейса.ЗонаРазгрузки);		
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	Возврат сткПараметрыОбработки;
КонецФункции

&НаКлиенте
// Продолжение добавления точки после утвердительного ответа.
//
Процедура ОбработатьВопросОДобавлениеТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	ДобавитьТочку = РезультатВопроса = КодВозвратаДиалога.Да;
	ДобавитьТочкуПриИзмененииТранспортногоСредстваНаСервере(ДобавитьТочку, ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
// Продолжение удаления точки после утвердительного ответа.
//
Процедура ОбработатьВопросОбУдаленииТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	УдалитьТочку = РезультатВопроса = КодВозвратаДиалога.Да;
	УдалитьТочкуПриИзмененииТранспортногоСредстваНаСервере(УдалитьТочку, ДополнительныеПараметры);
КонецПроцедуры

&НаСервере
Процедура ДобавитьТочкуПриИзмененииТранспортногоСредстваНаСервере(ДобавитьТочку, ДополнительныеПараметры)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ДобавитьТочкуПриИзмененииТранспортногоСредства(ДобавитьТочку, ДополнительныеПараметры);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура УдалитьТочкуПриИзмененииТранспортногоСредстваНаСервере(УдалитьТочку, ДополнительныеПараметры)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.УдалитьТочкуПриИзмененииТранспортногоСредства(УдалитьТочку, ДополнительныеПараметры);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(упКорректировкаРейса.ТранспортноеСредство) Тогда
		
		Если ВедетсяУчетДокументов Тогда
			текОтказ = Ложь;
			упРейс.ПроверитьНаличиеНеобходимыхДокуметовПоТС(упКорректировкаРейса.ТранспортноеСредство, текОтказ);	
		КонецЕсли;
		
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(упКорректировкаРейса.ТранспортноеСредство, "Партнер, ТипТС, ТипТС.КоэффициентРасчетаВремениВПути");
		упКорректировкаРейса.ТипТС = сткРеквизиты.ТипТС;
		упКорректировкаРейса.КоэффициентРасчетаВремениВПути = сткРеквизиты.ТипТСКоэффициентРасчетаВремениВПути;
		
		
		Если сткРеквизиты.Партнер <> упКорректировкаРейса.Перевозчик Тогда
			упКорректировкаРейса.Перевозчик = сткРеквизиты.Партнер;
			упКорректировкаРейса.Соглашение = ПолучитьСоглашениеПоУмолчанию();
			упКорректировкаРейса.КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(упКорректировкаРейса.Перевозчик);
			СоглашениеПриИзмененииНаСервере();
		Иначе
			упКорректировкаРейса.Перевозчик = сткРеквизиты.Партнер;
		КонецЕсли; 
		
	Иначе
		текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		текКорректировкаМаршрутаОбъект.УдалитьТочкиОтправленияЗавершения();
		ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
	КонецЕсли;
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, упКорректировкаРейса.ТранспортноеСредство, упКорректировкаРейса.ПлановоеНачалоВыполнения);
	ОбновитьДанныеОПеревозке();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОПеревозке()
				
	ИспользоватьВиртуальныеТранспортныеСредства = ПолучитьФункциональнуюОпцию("упИспользоватьВиртуальныеТранспортныеСредства");
	ТолькоПросмотрПеревозчика = Не ИспользоватьВиртуальныеТранспортныеСредства Или ЗначениеЗаполнено(упКорректировкаРейса.ТранспортноеСредство);
	Элементы.упКорректировкаРейсаПеревозчик.ТолькоПросмотр            = ТолькоПросмотрПеревозчика;
	Элементы.упКорректировкаРейсаТипТС.ТолькоПросмотр                 = (Не ИспользоватьВиртуальныеТранспортныеСредства Или ЗначениеЗаполнено(упКорректировкаРейса.ТранспортноеСредство));  
		
КонецПроцедуры // ОбновитьДанныеОПеревозке()

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаСервере
Функция ПолучитьСоглашениеПоУмолчанию()

	Возврат Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(упКорректировкаРейса.Перевозчик, упКорректировкаРейса.Организация);

КонецФункции // ПолучитьСоглашениеПоУмолчанию()

&НаСервере
Процедура НастроитьПанельНавигации()

	сткНастроек = Новый Структура;
	сткНастроек.Вставить("УправлятьПроцессомПодбораПеревозчика", УправлятьПроцессомПодбораПеревозчика);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);

КонецПроцедуры

&НаКлиенте
Функция НеобходимоДобавитьСвязанныеГрузовыеМеста()
	ЗаполнитьНедобавленныеСвязанныеГрузовыеМеста();
	Возврат упКорректировкаРейса.НеДобавленныеСвязанныеГрузовыеМеста.Количество() > 0;
КонецФункции

&НаСервере
Процедура ЗаполнитьНедобавленныеСвязанныеГрузовыеМеста()

	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ЗаполнитьНедобавленныеСвязанныеГрузовыеМеста();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОСвязанныхГрузовыхМестах(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = Ложь;
		Для каждого СтрокаГрузовоеМесто Из упКорректировкаРейса.НеДобавленныеСвязанныеГрузовыеМеста Цикл
			НоваяСтрока = упКорректировкаРейса.Задания.Добавить();
			НоваяСтрока.СтрокаДокумента = Истина;
			НоваяСтрока.Задание			= СтрокаГрузовоеМесто.Задание;
			НоваяСтрока.ГрузовоеМесто	= СтрокаГрузовоеМесто.ГрузовоеМесто;
			НоваяСтрока.КоличествоГрузов= СтрокаГрузовоеМесто.КоличествоГрузов;
			
			сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
			
			Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
				сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаГрузовоеМесто);
			ОбработатьСтрокуСвязанногоГрузовогоМестаНаСервере(НоваяСтрока.НомерСтроки, Отказ);
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуСвязанногоГрузовогоМестаНаСервере(НомерСтроки, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиЗадания(НомерСтроки, Истина, Ложь, Ложь, Отказ);
	текКорректировкаМаршрутаОбъект.ЗаполнитьВспомогательныеПоляЗаданийНаСервере(НомерСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОНарушенииМаршрута(Предшественник, Потомок)
	
	ТекстСообщения = СтрШаблон("ru = 'Ошибка изменения маршрута! Точка с адресом ""%1"" должна предшествовать точке с адресом ""%2""'", Предшественник, Потомок);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстСообщения));
	
КонецПроцедуры

&НаСервере
Процедура РазделитьРаботыВТочкеНаСервере(ИдентификаторТочки, СписокОпераций)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Отказ			= Ложь;
	упПрохождениеТочки.РазделитьРаботыВТочке(текКорректировкаМаршрутаОбъект, ИдентификаторТочки, СписокОпераций,,,Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Функция РазрешеноРазделятьРаботы(СтрокаТочка)
	Результат = упПрохождениеТочки.РазрешеноРазделятьРаботы(СтрокаТочка, упКорректировкаРейса.Работы);
	Возврат Результат;		
КонецФункции

&НаКлиенте
Процедура ПереключитьОжиданиеПослеРабот(Команда)
	
	упРейсКлиентСервер.ПереключитьОжиданиеПослеРабот(Элементы.Маршрут.ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросСоздатьПутевойЛист(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОформляетсяПутевойЛист = Истина;
		упСервисныеФункцииКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтотОбъект, ДополнительныеПараметры);
		//Записать(ДополнительныеПараметры.ПараметрыЗаписи);
		//Если ПровестиИЗакрыть Тогда
		//	ПровестиИЗакрыть = Ложь;
		//	Закрыть();
		//КонецЕсли;
	Иначе	
		ТекстСообщения = Нстр("ru = 'Документ не проведен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ТребуетсяОформлениеПутевогоЛистаНаСервере()
	
	Результат = Ложь;
		
	Если Истина
		И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПутевыхЛистов")
		И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(упКорректировкаРейса.Рейс, "ПеревозкаТранспортнойКомпанией")
		И упРейс.ТребуетсяОформлениеПутевогоЛистаДляПеревозчика(упКорректировкаРейса.ВидПеревозки, упКорректировкаРейса.Перевозчик)
		И Не упРейс.ЕстьПутевойЛист(упКорректировкаРейса.Рейс)
	Тогда
		Результат = Истина;
	КонецЕсли; 

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПараметрыВыбораТС()
	ПараметрыВыбораМассив = Новый Массив;
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("ВидПеревозки", упКорректировкаРейса.ВидПеревозки));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Перевозчик", упКорректировкаРейса.Перевозчик));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Организация", упКорректировкаРейса.Организация));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("ТипТС", упКорректировкаРейса.ТипТС));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("РегНомер", Истина));
	Элементы.упКорректировкаРейсаТранспортноеСредство.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыВыбораТипаТС()
	ПараметрыВыбораМассив = Новый Массив;
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("ВидПеревозки", упКорректировкаРейса.ВидПеревозки));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Перевозчик", упКорректировкаРейса.Перевозчик));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Организация", упКорректировкаРейса.Организация));
	Элементы.упКорректировкаРейсаТипТС.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
КонецПроцедуры

#КонецОбласти