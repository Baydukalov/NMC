#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда
		// Заполнение шапки.
		Рейс = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	сткДанныеВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах, ВидПеревозки.ВидЗоныДляФормированияПредставленияРейса");
	ФормироватьПоГрузовымМестам = Ложь
							Или сткДанныеВидаПеревозки.ВидПеревозкиПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку
							Или сткДанныеВидаПеревозки.ВидПеревозкиПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса;
	
	Статус = Документы.упРейс.ПолучитьСтатус(Ссылка);
	
	Если Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
	КонецЕсли; 
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Если НЕ ФормироватьПоГрузовымМестам Тогда
			
			ИндексРеквизита = ПроверяемыеРеквизиты.Найти("Задания.ГрузовоеМесто");
			Если НЕ ИндексРеквизита = Неопределено Тогда
				ПроверяемыеРеквизиты.Удалить(ИндексРеквизита);		
			КонецЕсли;
			
			ИндексРеквизита = ПроверяемыеРеквизиты.Найти("Задания.КоличествоГрузов");
			Если НЕ ИндексРеквизита = Неопределено Тогда
				ПроверяемыеРеквизиты.Удалить(ИндексРеквизита);		
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	упКорректировкаРейсаОбъект = Неопределено;
	Если НЕ ДополнительныеСвойства.Свойство("КорректировкаРейса", упКорректировкаРейсаОбъект) Тогда
		упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();	
		ИнициализацияКорректировкиМаршрута(упКорректировкаРейсаОбъект);
		ДополнительныеСвойства.Вставить("КорректировкаРейса", упКорректировкаРейсаОбъект);
	КонецЕсли;
	
	Если Не Отказ 
		И ПолучитьФункциональнуюОпцию("упИспользуютсяКонтейнерныеПеревозки")
	Тогда
		ТекстОшибки = "";
		Если упКорректировкаРейсаОбъект.КонтейнерРасформирован(ТекстОшибки)  Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Запрещено изменять проведение документа с грузами-контейнерами, которые были расформированы
			|(Задания: %1.)'"), ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И Не Отказ
		И упКорректировкаРейсаОбъект.ЕстьОтложенныеРаботы
		И РежимЗаписи <> РежимЗаписиДокумента.Запись 
	Тогда
		ТекстОшибки = НСтр("ru = 'Запрещено проводить корректировку по рейсу у которого есть отложенные работы'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
	КонецЕсли;	
	
	Если Истина
		И Не Отказ
		И РежимЗаписи <> РежимЗаписиДокумента.Запись 
	Тогда
		
		// Формируется структура Дополнительные свойства и реквизиты объекта, для восстановления
		// плановых параметров, в случае отмены проведения
		Если НЕ ДополнительныеСвойства.Свойство("ФормироватьПоГрузовымМестам") Тогда
			сткДанныеВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах, 
																	    |ВидПеревозки.ВидЗоныДляФормированияПредставленияРейса, 
																	    |ВидПеревозки.СпособУчетаВозвращенныхДокументов,
																	    |ВидПеревозки.ВариантФормированияМаршрута");
			ФормироватьПоГрузовымМестам = Ложь
									Или сткДанныеВидаПеревозки.ВидПеревозкиПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку 
									Или сткДанныеВидаПеревозки.ВидПеревозкиПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса;
			
			ВидЗоныДляФормированияПредставленияРейса = сткДанныеВидаПеревозки.ВидПеревозкиВидЗоныДляФормированияПредставленияРейса; 
			ВариантФормированияМаршрута = сткДанныеВидаПеревозки.ВидПеревозкиВариантФормированияМаршрута; 
		КонецЕсли;
				
		ДополнительныеСвойства.Вставить("СтатусРейсаДоКорректировки", упКорректировкаРейсаОбъект.СтатусРейсаДоКорректировки);
		ДополнительныеСвойства.Вставить("СпособУчетаВозвращенныхДокументов", сткДанныеВидаПеревозки.ВидПеревозкиСпособУчетаВозвращенныхДокументов);
		ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет"));
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			 ДополнительныеСвойства.Вставить("ТЧЗадания", Задания.Выгрузить(,"Задание, ГрузовоеМесто"));
		КонецЕсли;
		
		РеквизитыОбъекта = Новый Структура();
		РеквизитыОбъекта.Вставить("Дата", Дата);
		РеквизитыОбъекта.Вставить("Рейс", Рейс);
		РеквизитыОбъекта.Вставить("ФормироватьПоГрузовымМестам", ФормироватьПоГрузовымМестам);
		РеквизитыОбъекта.Вставить("ВидЗоныДляФормированияПредставленияРейса", ВидЗоныДляФормированияПредставленияРейса);
		РеквизитыОбъекта.Вставить("ВариантФормированияМаршрута", ВариантФормированияМаршрута);
		РеквизитыОбъекта.Вставить("ПлановыеПараметры", Новый Структура());	
		РеквизитыОбъекта.Вставить("ВидТранспортнойУслуги", упКорректировкаРейсаОбъект.ВидТранспортнойУслуги);	
		
		ДополнительныеСвойства.Вставить("РеквизитыОбъекта" , РеквизитыОбъекта);
		
		Если Истина
			И РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения 
			И упКорректировкаРейсаОбъект.СтатусРейсаДоКорректировки = Перечисления.упСтатусыРейса.КВыполнению 
		Тогда	
			ДополнительныеСвойства.Вставить("МаршрутПоРейсу", упКорректировкаРейсаОбъект.МаршрутДоКорректировки.Выгрузить());	
			ДополнительныеСвойства.Вставить("РаботыПоРейсу", упКорректировкаРейсаОбъект.РаботыДоКорректировки.Выгрузить());
			ДополнительныеСвойства.Вставить("Маршрут", ДополнительныеСвойства.МаршрутПоРейсу);	
			ДополнительныеСвойства.Вставить("Работы", ДополнительныеСвойства.РаботыПоРейсу);

		КонецЕсли;	
		
	КонецЕсли;
		
	Если Истина
		И Не Отказ
		И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения
	Тогда	
		сткРеквизиты = Новый Структура();
		упКорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляКорректировки(ДополнительныеСвойства, Ссылка, сткРеквизиты, Отказ);
		Если НЕ Отказ Тогда
			ДополнительныеСвойства.Вставить("ПлановоеНачалоВыполнения", упКорректировкаРейсаОбъект.ПлановоеНачалоВыполнения);
			ДополнительныеСвойства.Вставить("ПлановоеОкончаниеВыполнения", упКорректировкаРейсаОбъект.ПлановоеОкончаниеВыполнения);
						
			// Заполнить табличную часть документа "Задания".
			мсвСтроки = упКорректировкаРейсаОбъект.Задания.НайтиСтроки(Новый Структура("СтрокаДокумента", Истина));
			Задания.Очистить();
			Для каждого СтрокаЗадания Из мсвСтроки Цикл
				НоваяСтрока = Задания.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗадания);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
	Если Истина
		И Не Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.Запись
	Тогда
		СтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Рейс);
		ДополнительныеСвойства.Вставить("Статус", СтатусРейса); 
		
		Отказ = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ЭтотОбъект);
		
		Если НЕ Отказ Тогда
			ТекстОшибки = "";
			Если НЕ упРаботаСоСтатусамиКлиентСервер.РазрешеноФормироватьКорректировкуРейса(СтатусРейса, РежимЗаписи)  Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено проводить корректировку по рейсу в статусе ""%1""'", СтатусРейса));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			ТекстОшибки = "";
			Если упКорректировкаМаршрута.ЗаданияНаПеревозкуБылиИзмененыПослеПроведенияКорректировки(Ссылка, Рейс, Дата) Тогда
				ТекстОшибки = НСтр("ru = 'Запрещено изменять проведение корректировки, потому что существуют задания на перевозку груза, состояние которых было изменено после проведения корректировки'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Истина
			И НЕ Отказ
			И РежимЗаписи = РежимЗаписиДокумента.Проведение
			И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПутевыхЛистов") 
			И ДополнительныеСвойства.Свойство("ИзмененоТранспортноеСредство")
			И упКорректировкаРейсаОбъект <> Неопределено
		Тогда
			сткПараметрыПутевогоЛиста = упРейс.ПараметрыПутевогоЛистаПоРейсу(Рейс);
								
			Если Истина
				И ЗначениеЗаполнено(сткПараметрыПутевогоЛиста.ПутевойЛист)
				И сткПараметрыПутевогоЛиста.ТранспортноеСредство <> упКорректировкаРейсаОбъект.ТранспортноеСредство
			Тогда
				ТекстОшибки = НСтр("ru = 'Запрещено изменять транспортное средство, потому что по рейсу уже создан путевой лист для другого транспортного средства.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);	
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	РеквизитыОбъекта = ДополнительныеСвойства.РеквизитыОбъекта;
	ФормироватьПоГрузовымМестам = РеквизитыОбъекта.ФормироватьПоГрузовымМестам;
	
	тбзЗадания = ДополнительныеСвойства.Задания;
	
	текТранспортноеСредство = упРейс.ПолучитьЭкипажРейса(Рейс).ТранспортноеСредство;									  
									  
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упИзмененияРейсов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	КонецЕсли; 
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПеревозчикПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеПараметрыРейса");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНепереданныеДокументы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	КонецЕсли; 
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тбзЗадания;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	Если ФормироватьПоГрузовымМестам Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упСостояниеПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тбзЗадания;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	Если ФормироватьПоГрузовымМестам Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПланПоЗаданиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тбзЗадания;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	Если ФормироватьПоГрузовымМестам Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = упЗаданиеНаПеревозку.ПолучитьЗаявкиПоЗаданиям(тбзЗадания.ВыгрузитьКолонку("Задание"));
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Заявка");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тбзЗадания;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Задание");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	тбзПрерыванияГрузовыхПотоков = Неопределено;
	Если ДополнительныеСвойства.Свойство("ПрерыванияГрузовыхПотоков", тбзПрерыванияГрузовыхПотоков) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрерыванияГрузовыхПотоков");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = тбзПрерыванияГрузовыхПотоков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаявкаНаПеревозку", "ЗаявкаНаПеревозку");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(текТранспортноеСредство) Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", текТранспортноеСредство);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Транспорт", текТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияТранспортныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", текТранспортноеСредство);
	КонецЕсли;
	
	ПримененныеПравилаВключенияКонтрольныхТочек = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек) Тогда
		Если Истина
			И ТипЗнч(ПримененныеПравилаВключенияКонтрольныхТочек) = Тип("СписокЗначений")
			И ПримененныеПравилаВключенияКонтрольныхТочек.Количество() > 0
		Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПримененныеПравилаВключенияКонтрольныхТочек");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);	
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
		
	ДатаКВыполнению = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Рейс, Перечисления.упСтатусыРейса.КВыполнению);
	Если Не ЗначениеЗаполнено(ДатаКВыполнению) Или РеквизитыОбъекта.Дата <= ДатаКВыполнению Тогда
		ТекстОшибки = НСтр("ru = 'Дата документа должна быть больше даты перевода рейса к выполнению!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		Возврат;
	КонецЕсли;
	РеквизитыОбъекта.Вставить("ДатаДокумента", Дата); // для совместимости с другими документами с вызовом общих процедур формирования движений
	РеквизитыОбъекта.Вставить("ФактическиеПараметры", Новый Структура);    
	РеквизитыОбъекта.Вставить("Маршрут", ДополнительныеСвойства.Маршрут);    
	РеквизитыОбъекта.Вставить("Работы", ДополнительныеСвойства.Работы);    
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", текТранспортноеСредство);    
	РеквизитыОбъекта.Вставить("ПлановыеПараметры", Новый Структура("ПлановоеНачалоВыполнения, ПлановоеОкончаниеВыполнения",ДополнительныеСвойства.ПлановоеНачалоВыполнения,ДополнительныеСвойства.ПлановоеОкончаниеВыполнения));	
	
	ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Ложь); 
	ДополнительныеСвойства.Вставить("РассчитыватьСтатус", 0); 
	ДополнительныеСвойства.Вставить("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	ДополнительныеСвойства.Вставить("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	ДополнительныеСвойства.Вставить("ТЧЗадания", Задания.Выгрузить(,"Задание, ГрузовоеМесто, Тара"));
	
	ДвиженияСтатусыЗаданий = Движения.упСтатусыЗаданийНаПеревозкуГруза;
	ДвиженияСтатусыЗаданий.Очистить();	
	ДвиженияСтатусыЗаданий.Записывать = Истина;
	
	ДвиженияСтатусыЗаявок = Движения.упСтатусыЗаявокНаПеревозкуГруза;
	ДвиженияСтатусыЗаявок.Очистить();	
	ДвиженияСтатусыЗаявок.Записывать = Истина;

	Движения.Записать();
	
	// Формируются движения по заданиям на перевозку.
	упПрохождениеТочки.СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются движения по заявкам в случае отмены заданий.
	упКорректировкаМаршрута.СформироватьДвиженияЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется маршрут.
	упКорректировкаМаршрута.СформироватьДвижениеМаршрутПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются работы.
	упКорректировкаМаршрута.СформироватьДвижениеРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются данные перевозчика рейса.
	упКорректировкаМаршрута.СформироватьДвижениеПеревозчикПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	 	
	// Формируется пакет документов, о которых следует отчитаться после выполнения рейса.
	упПрохождениеТочки.СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются движения по состояниям задания.
	упПрохождениеТочки.СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется план по заданиям.
	упКорректировкаМаршрута.СформироватьДвижениеПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется факт инкассации денежных средств.
	упПрохождениеТочки.СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются данные о случаях прерывания грузовых потоков (отмена/разделение).
	упКорректировкаМаршрута.СформироватьДвижениеПрерыванияГрузовыхПотоков(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	
	// Устанавливаются статусы заданий.
	ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗадания", Истина);
	упЗаданиеНаПеревозку.СформироватьДвиженияСтатусыЗаданий(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	// Устанавливаются статусы рейса.
	ДополнительныеСвойства.Вставить("ДобавитьСтатус", Ложь);
	ДополнительныеСвойства.Вставить("РассчитыватьСтатус", Истина);
	упРейс.СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При отклонении времени прохождения точки от плана заполняется плановое окончание рейса
	упКорректировкаМаршрута.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При прохождении последней точки формируются фактические параметры рейса.
	упРейс.СформироватьФактическиеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При прохождении первой точки ТС переводится в состояние "В рейсе", последней - свободно.
	упРейс.СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	упКорректировкаМаршрута.СформироватьДвиженияПлановаяЗанятостьТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	// Устанавливаются статусы заявок.
	ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗаявки", Истина);
	упУправленияЗаявками.СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.ПроконтролироватьЗаданияКПланированию(Ссылка, ФормироватьПоГрузовымМестам, Отказ, ДополнительныеСвойства);
	
	// В случае если это последняя точка маршрута, то устанавливается дата окончания рейса в путевом листе.
	упПрохождениеТочки.ЗаполнитьДатуОкончанияПутевогоЛиста(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// При выполнении рейса на мобильном клиенте фиксируем необоходимость обновить данные по рейсу.
	упКорректировкаМаршрута.СформироватьДвиженияИзмененияРейсов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Если изменяется транспортное средство и по рейсу нет путевых листов, то создается новый путевой лист.
	Если ДополнительныеСвойства.Свойство("СоздатьПутевойЛист") Тогда
		упКорректировкаМаршрута.СформироватьПутевойЛистПоРейсуКорректировки(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
	// Заполняются использованные правила при добавлении контрольных точек.
	упКорректировкаМаршрута.СформироватьДвиженияПримененныеПравилаВключенияКонтрольныхТочек(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		упКорректировкаМаршрута.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Неопределено, ДополнительныеСвойства.РеквизитыОбъекта, Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Процедура - Инициализация.
//
// Параметры:
//  КорректировкаМаршрутаОбъект - Структура - Данные.
//
Процедура ИнициализацияКорректировкиМаршрута(КорректировкаМаршрутаОбъект) Экспорт
	
	ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	
	КорректировкаМаршрутаОбъект.Рейс 	= Рейс;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		КорректировкаМаршрутаОбъект.Ссылка 	= Ссылка;
		КорректировкаМаршрутаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
	Иначе
		КорректировкаМаршрутаОбъект.Ссылка 	= Документы.упКорректировкаРейса.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КорректировкаМаршрутаОбъект.Период)  Тогда
		КорректировкаМаршрутаОбъект.Период = Дата;
	КонецЕсли;
	
	сткПараметрыЗаполнения = Новый Структура();
	сткПараметрыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками);
	КорректировкаМаршрутаОбъект.Инициализация(сткПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли