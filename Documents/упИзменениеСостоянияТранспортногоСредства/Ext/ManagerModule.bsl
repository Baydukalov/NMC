#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура формирует движения по регистру "упЗанятостьТранспорта".
//
// Параметры:
//  ДополнительныеСвойства - Структура - Свойства.
//	Ссылка - ДокументСсылка - Ссылка на элемент.
//  Движения - КоллекцияДвижений - Коллекция движений документа.
//  РеквизитыОбъекта - Структура - Реквизиты объекта.
//  Отказ - Булево - флаг ошибки.
//
Процедура СформироватьДвиженияЗанятостьТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки.
	    Ссылка = Документы.упИзменениеСостоянияТранспортногоСредства.ПустаяСсылка();
	    РеквизитыОбъекта = Ссылка;
		Движения = Ссылка.ПолучитьОбъект().Движения;
		ДополнительныеСвойства = Ссылка.ПолучитьОбъект().ДополнительныеСвойства;
	КонецЕсли;
	
	ВыполнитьОтказДляТС = Ложь;
	Если НЕ Ссылка.ТранспортноеСредство.СобственноеТранспортноеСредство Тогда
		ВыполнитьОтказДляТС = НЕ ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
	КонецЕсли;
	
	Если Отказ ИЛИ ВыполнитьОтказДляТС Тогда 
		Возврат; 
	КонецЕсли;
	СрезПоследних = упУчетТранспортныхСредств.СрезПоследнихЗанятостьТранспорта(Ссылка.МоментВремени(), Документы.упИзменениеСостоянияТранспортногоСредства.ПустаяСсылка());
	Если Ложь Тогда // Для контекстной подсказки.
	    СрезПоследних = Новый  ТаблицаЗначений;
	КонецЕсли;
	ЗанятостьТранспорта = Движения.упЗанятостьТранспорта;
	ЗанятостьТранспорта.Записывать = Истина;
	Если Ложь
		Или Ссылка.Состояние.ТипСостоянияТС = Перечисления.упТипыСостоянийТС.Недоступно
		Или Ссылка.Состояние.ТипСостоянияТС = Перечисления.упТипыСостоянийТС.ВРейсе
		Или Ссылка.Состояние.ТипСостоянияТС = Перечисления.упТипыСостоянийТС.Ремонт
	Тогда
		Если СрезПоследних.Количество() = 0 Тогда
			ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
			ДвиженияСостояние.Период = Ссылка.Дата;
			ДвиженияСостояние.Документ = Документы.упИзменениеСостоянияТранспортногоСредства.ПустаяСсылка();
			ДвиженияСостояние.Транспорт = Ссылка.ТранспортноеСредство;
			ДвиженияСостояние.ПланАктуален = Истина;
			ДвиженияСостояние.ДатаНачала = Ссылка.ДатаИзмененияСостояния;
			ДвиженияСостояние.ДатаОкончания = Ссылка.ПлановоеОкончание;
			ДвиженияСостояние.ЗонаНачала = Ссылка.Адрес.ГеографическаяЗона;
			ДвиженияСостояние.ЗонаОкончания = ДвиженияСостояние.ЗонаНачала;
			ДвиженияСостояние.СостояниеТранспорта = Ссылка.Состояние;
		КонецЕсли; 
	ИначеЕсли Ложь
		Или Ссылка.Состояние.ТипСостоянияТС = Перечисления.упТипыСостоянийТС.Свободно
	Тогда
		Если СрезПоследних.Количество() > 0 Тогда
			ДвиженияСостояние = ЗанятостьТранспорта.Добавить();
			ЗаполнитьЗначенияСвойств(ДвиженияСостояние, Ссылка); 
			ДвиженияСостояние.Период = Ссылка.Дата;
			ДвиженияСостояние.ПланАктуален = Ложь;
			ДвиженияСостояние.ФактНачала = Истина;
			ДвиженияСостояние.ФактОкончания = Истина;
			ДвиженияСостояние.ДатаОкончания = Ссылка.Дата;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Адрес");
	БлокируемыеРеквизиты.Добавить("ДатаИзмененияСостояния");
	БлокируемыеРеквизиты.Добавить("ТранспортноеСредство");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
  // Нет.
   	
КонецПроцедуры // ДобавитьКомандыПечати()

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать.
//  ПараметрыПечати - Структура - дополнительные настройки печати.
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати - СписокЗначений - объекты печати: 
//											  * значение - ссылка на объект.
//                                            * представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Нет.
	
КонецПроцедуры // Печать()

#КонецОбласти

#КонецЕсли
