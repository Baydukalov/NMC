#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Отказ = Ложь;
	ТекстОшибки = "";
	ДокументСсылка = СформироватьЗаявкуНаПеревозкуГруза(ПараметрКоманды, ТекстОшибки, Отказ);
	
	Если Не Отказ Тогда
		ОткрытьФорму("Документ.упЗаявкаНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка), ПараметрыВыполненияКоманды.Источник);
	Иначе
		Если ТекстОшибки = "" Тогда
			ТекстОшибки = НСтр("ru = 'Документ не сформирован.'");		    
		КонецЕсли; 
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьЗаявкуНаПеревозкуГруза(Рейс, ТекстОшибки, Отказ)
	
	сткСтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатусРейса(Рейс);
	СтатусРейса = сткСтатусРейса.Статус;
	СостояниеРейса = сткСтатусРейса.Состояние;
	
	Если Ложь
		Или СтатусРейса = Перечисления.упСтатусыРейса.Новый
		Или СтатусРейса = Перечисления.упСтатусыРейса.КПланированиюТС
		Или СтатусРейса = Перечисления.упСтатусыРейса.НазначеноТС
		Или СтатусРейса = Перечисления.упСтатусыРейса.Отменен
	Тогда
		ТекстОшибки = СтрШаблон(Нстр("ru = 'Запрещено формировать заявку на перевозку груза на основании ""Операции с контейнером"" в статусе ""%1""'"), СостояниеРейса);
		Отказ = Истина;
	Иначе
		Возврат Документы.упЗаявкаНаПеревозкуГруза.СформироватьЗаявкуНаПеревозкуГрузаДляОперацииСКонтейнером(Рейс, ТекстОшибки, Отказ);
	КонецЕсли;
	
КонецФункции

#КонецОбласти 