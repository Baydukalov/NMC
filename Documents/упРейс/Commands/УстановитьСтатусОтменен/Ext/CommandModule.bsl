
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
		Кнопки =РежимДиалогаВопрос.ОКОтмена;
		
		Оповещение = Новый ОписаниеОповещения("УстановитьСтатусОтмены", ЭтотОбъект, ПараметрКоманды );
		
		ПараметрыФормы = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыФормы.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыФормы.Заголовок = НСтр("ru = 'Исменение статуса'");
		
		Текст = 
		НСтр("ru = 'Установка статуса ""Отменен"",
		|является необратимой операцией.
		|
		|Продолжить?'");
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, Текст, Кнопки, ПараметрыФормы);

	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтмены(Результат, ПараметрКоманды) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Значение = КодВозвратаДиалога.ОК Тогда
			
			Отказ = Ложь;
						
			сткПроверка = ДокументыПрепятствующиеОтменеРейса(ПараметрКоманды);
			
			Если сткПроверка.ЕстьДокументыОтменаВручную Тогда
				Отказ = Истина;
				ТекстОшибки = Нстр("ru = 'По рейсу есть проведенные документы, введенные на основании(корректировки, доверенности, путевой лист или подтверждения заявок на транспортное средство). Отмена рейса запрещена.'");
			КонецЕсли;
			
			Если Не Отказ Тогда
				Если сткПроверка.ЕстьДокументыОтменаАвтоматом Тогда	
					мсвРейсы = сткПроверка.Рейсы;
					Если мсвРейсы.Количество() > 0  Тогда
						ДополнительныеПараметры = Новый Структура();
						ДополнительныеПараметры.Вставить("Рейсы",  мсвРейсы); 
						ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОтменеЗаявок",ЭтотОбъект, ДополнительныеПараметры);
						ТекстВопроса = Нстр("ru = 'По рейсу есть проведенные документы введенные на основании (заявки на ТС, предложения перевозчикам). Отменить эти документы?'");
						ПоказатьВопрос(ОписаниеОповещенияОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
					КонецЕсли;
				Иначе
					упРаботаСоСтатусамиКлиент.УстановитьСтатусДокументам(ПараметрКоманды, ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Отменен"));
				КонецЕсли;
			Иначе
				упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
			КонецЕсли;
		
		КонецЕсли; // При "Отмена" ничего не делаем.
		
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.упРейс"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОтменеЗаявок(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		мсвРейсы = ДополнительныеПараметры.Рейсы;
		мсвРейсыДляУстановкиСтатуса = Новый Массив;
		
		Для каждого сткРейс Из мсвРейсы Цикл
			
			Отказ = Ложь;
			ТекстОшибки = "";
			мсвЗаявокНаТС 		= сткРейс.ДокументыОтменаАвтоматомЗаявкиНаТС; 
			мсвПредложений 		= сткРейс.ДокументыОтменаАвтоматомПредложения; 
			ОтменитьДокументы(мсвПредложений, мсвЗаявокНаТС, ТекстОшибки, Отказ);
			
			Если НЕ Отказ Тогда
				Для каждого ЗаявкаНаТС Из мсвЗаявокНаТС Цикл
					Оповестить("СменаСтатуса",,ЗаявкаНаТС);
				КонецЦикла;
				мсвРейсыДляУстановкиСтатуса.Добавить(сткРейс.Рейс);
			КонецЕсли;
			
		КонецЦикла;
		
		Если мсвРейсыДляУстановкиСтатуса.Количество() > 0 Тогда
			упРаботаСоСтатусамиКлиент.УстановитьСтатусДокументам(мсвРейсыДляУстановкиСтатуса, ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Отменен"));
			Для каждого сткРейс Из мсвРейсы Цикл
				Оповестить("СменаСтатуса",,сткРейс.Рейс);
				ОповеститьОбИзменении(сткРейс.Рейс);
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ мсвРейсыДляУстановкиСтатуса.Количество() = мсвРейсы.Количество() Тогда
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(НСтр("ru = 'Не удалось отменить некоторые рейсы, подробности см. в журнале регистрации'"), Истина);
		КонецЕсли;
							
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьДокументы(мсвПредложения, мсвЗаявкиНаТС, ТекстОшибки, Отказ)

	упРейс.ОтменитьДокументыПоРейсу(мсвПредложения, мсвЗаявкиНаТС, ТекстОшибки, Отказ);
					
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДокументыПрепятствующиеОтменеРейса(Рейсы)
	
	сткДокументы = упРейс.ДокументыПрепятствующиеОтменеРейса(Рейсы);
	
	Возврат сткДокументы;
	
КонецФункции

#КонецОбласти 