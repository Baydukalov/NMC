
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	сткДоверенности = СформироватьДоверенности(ПараметрКоманды, ОписаниеОшибки, Отказ);
	
	Если НЕ Отказ Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Доверенности успешно сформированы.'"),,, БиблиотекаКартинок.Информация32);
		ТекстВопроса = НСтр("ru = 'Хотите открыть форму просмотра сформированных доверенностей?'");
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, Новый Структура("сткДоверенности", сткДоверенности)), ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Доверенности не сформированы.'"),, ОписаниеОшибки, БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Печатная форма сформированных довереннвостей.
//
Процедура ОбработкаКомандыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	сткДоверенности = ДополнительныеПараметры.сткДоверенности;
		
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.упДоверенность", "ПФ_MXL_Доверенность", сткДоверенности.Доверенности, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СформироватьДоверенности(Рейс, ОписаниеОшибки, Отказ)
	
	сткРезультат = Новый Структура("Доверенности", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеревозчикПоРейсу.Рейс,
	|	ПеревозчикПоРейсу.Водитель1,
	|	ПеревозчикПоРейсу.Водитель2,
	|	ПеревозчикПоРейсу.Экспедитор
	|ПОМЕСТИТЬ втСотрудники
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК ПеревозчикПоРейсу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание
	|ПОМЕСТИТЬ втЗаданияРейса
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка = &Рейс
	|     И НЕ упРейсЗадания.Задание.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	упРейсЗадания.Задание,
	|	упРейсЗадания.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияРейса.Задание,
	|	втСотрудники.Водитель1,
	|	втСотрудники.Водитель2,
	|	втСотрудники.Экспедитор
	|ПОМЕСТИТЬ втСотрудникиЗаданий
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСотрудники КАК втСотрудники
	|		ПО втЗаданияРейса.Рейс = втСотрудники.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗадания.Задание
	|ПОМЕСТИТЬ втЗаданияБезДоверенности
	|ИЗ
	|	втЗаданияРейса КАК втЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упДоверенность КАК упДоверенность
	|		ПО втЗадания.Задание = упДоверенность.Задание
	|			И (НЕ упДоверенность.ПометкаУдаления)
	|ГДЕ
	|	упДоверенность.Ссылка ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияБезДоверенности.Задание,
	|	ЕСТЬNULL(втСотрудникиЗаданий.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель1,
	|	ЕСТЬNULL(втСотрудникиЗаданий.Водитель2, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель2,
	|	ЕСТЬNULL(втСотрудникиЗаданий.Экспедитор, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Экспедитор
	|ИЗ
	|	втЗаданияБезДоверенности КАК втЗаданияБезДоверенности
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСотрудникиЗаданий КАК втСотрудникиЗаданий
	|		ПО втЗаданияБезДоверенности.Задание = втСотрудникиЗаданий.Задание";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Отказ = Истина;
		ОписаниеОшибки = Нстр("ru = 'В документе ""%1"" нет заданий, по которым необходимо сформировать Доверенности'");
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки, Рейс);
	Иначе
		Выборка = Результат.Выбрать();
		Доверенности = Новый Массив;
		
		Пока Выборка.Следующий() Цикл
			текДоверенность 				= Документы.упДоверенность.СоздатьДокумент();
			текДоверенность.Дата 			= ТекущаяДата();
			текДоверенность.Рейс			= Рейс;
			текДоверенность.Задание			= Выборка.Задание;
			текДоверенность.ДоверенноеЛицо 	= ?(ЗначениеЗаполнено(Выборка.Экспедитор), Выборка.Экспедитор, Выборка.Водитель1);
			текДоверенность.ДолжностьДоверенноеЛицо = ?(ЗначениеЗаполнено(Выборка.Экспедитор), "Экспедитор", "Водитель");
			
			Попытка
				текДоверенность.Записать(РежимЗаписиДокумента.Проведение);
				Доверенности.Добавить(текДоверенность.Ссылка);
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
					ОписаниеОшибки = ОписаниеОшибки();
				КонецЕсли;
			КонецПопытки; 	
			
			Если Отказ Тогда
				ЗаписьЖурналаРегистрации("ФормированиеДоверенностиПоРейсу", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упДоверенность,, ОписаниеОшибки);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		сткРезультат.Доверенности = Доверенности;
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

#КонецОбласти 