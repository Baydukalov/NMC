
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = "Выберите файл";
	Диалог.ПолноеИмяФайла = ""; 
	Фильтр = "XML (*.xml)|*.xml"; 
	Диалог.Фильтр = Фильтр; 
    Диалог.МножественныйВыбор = Ложь;
	Диалог.Каталог = "C:\";
	Если Диалог.Выбрать() Тогда
		ИмяФайла = Диалог.ПолноеИмяФайла;		
		
		ТекстXML = ВыполнимОбработкуДанных(ПараметрКоманды);
		
		ТекстовыйФайлЗапись = Новый ЗаписьТекста(ИмяФайла,КодировкаТекста.UTF8);            
		ТекстовыйФайлЗапись.ЗаписатьСтроку(ТекстXML); 
		ТекстовыйФайлЗапись.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Обработаем данные и подготовим текст XML. 
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ для выгрузки.
//
// Возвращаемое значение:
//   Строка   - Получим текст XML.
//
Функция ВыполнимОбработкуДанных(Рейс)
	
	МассивДанных = ПолучитьДанныеПоРейсу(Рейс);	
	
	ЗаписьXML = Новый ЗаписьXML;
	
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();		
	ЗаписьXML.ЗаписатьНачалоЭлемента("Shipment");		
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	Для каждого ТекущийРейс Из МассивДанных Цикл			
		Для каждого Структура Из ТекущийРейс.Shipment Цикл
			Если ТипЗнч(Структура.Значение) = ТИП("Массив") Тогда
				ЗаписьXML.ЗаписатьНачалоЭлемента(Структура.Ключ);
				Для каждого ЭлементМассива Из Структура.Значение Цикл
					ЗаписьXML.ЗаписатьНачалоЭлемента("row");
					Для каждого СтруктураМассива Из ЭлементМассива Цикл
						ЗаписьXML.ЗаписатьНачалоЭлемента(СтруктураМассива.Ключ);				
						Если ЗначениеЗаполнено(Структура.Значение) тогда
							ЗаписьXML.ЗаписатьТекст(XMLСтрока(СтруктураМассива.Значение));
						Иначе
							ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
							ЗаписьXML.ЗаписатьАтрибут("xsi:nil", "true");
						КонецЕсли;
						ЗаписьXML.ЗаписатьКонецЭлемента();						
					КонецЦикла;
					ЗаписьXML.ЗаписатьКонецЭлемента();
				КонецЦикла;
				ЗаписьXML.ЗаписатьКонецЭлемента();		
			Иначе	
				ЗаписьXML.ЗаписатьНачалоЭлемента(Структура.Ключ);				
				Если ЗначениеЗаполнено(Структура.Значение) тогда
					ЗаписьXML.ЗаписатьТекст(XMLСтрока(Структура.Значение));
				Иначе
					ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
					ЗаписьXML.ЗаписатьАтрибут("xsi:nil", "true");
				КонецЕсли;
				ЗаписьXML.ЗаписатьКонецЭлемента();		
			КонецЕсли;
		КонецЦикла;			
	КонецЦикла;
	ЗаписьXML.ЗаписатьКонецЭлемента();
	СтрXML = ЗаписьXML.Закрыть();
	
	Возврат СтрXML;
	
КонецФункции // ВыполнимОбработкуДанных()

&НаСервере
Функция ПолучитьДанныеПоРейсу(Рейс)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРасходы.Рейс,
	|	СУММА(упРасходы.СуммаПланСНДС) КАК СуммаПлан,
	|	СУММА(упРасходы.СуммаФактСНДС) КАК СуммаФакт
	|ПОМЕСТИТЬ вт_Расходы
	|ИЗ
	|	РегистрНакопления.упРасходы КАК упРасходы
	|ГДЕ
	|	упРасходы.Рейс = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	упРасходы.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейс.Ссылка,
	|	упРейс.Номер КАК Number,
	|	упРейс.Дата КАК Date,
	|	ВЫБОР
	|		КОГДА СтатусыРейсовСП.Статус ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(СтатусыРейсовСП.Статус)
	|	КОНЕЦ КАК Status,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.Перевозчик ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ПеревозчикПоРейсуСП.Перевозчик)
	|	КОНЕЦ КАК Carrier,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.Перевозчик ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ПеревозчикПоРейсуСП.Соглашение)
	|	КОНЕЦ КАК CarrierAgreement,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.Перевозчик ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ПеревозчикПоРейсуСП.ТранспортноеСредство)
	|	КОНЕЦ КАК Vehicle,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.Перевозчик ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ПеревозчикПоРейсуСП.Водитель1)
	|	КОНЕЦ КАК FirstDriver,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.Перевозчик ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ПеревозчикПоРейсуСП.Водитель2)
	|	КОНЕЦ КАК SecondDriver,
	|	ВЫБОР
	|		КОГДА ПеревозчикПоРейсуСП.Перевозчик ЕСТЬ NULL
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ПеревозчикПоРейсуСП.Экспедитор)
	|	КОНЕЦ КАК FreightForwarder,
	|	ВЫБОР
	|		КОГДА ФактическиеПараметрыРейса.Масса ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ПлановыеПараметрыРейса.Масса, 0)
	|		ИНАЧЕ ФактическиеПараметрыРейса.Масса
	|	КОНЕЦ КАК Weight,
	|	ВЫБОР
	|		КОГДА ФактическиеПараметрыРейса.Объем ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ПлановыеПараметрыРейса.Объем, 0)
	|		ИНАЧЕ ФактическиеПараметрыРейса.Объем
	|	КОНЕЦ КАК Volume,
	|	ПлановыеПараметрыРейса.ОценочнаяСтоимость КАК EvaluativeCost,
	|	ВЫБОР
	|		КОГДА ФактическиеПараметрыРейса.КоличествоМест ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ПлановыеПараметрыРейса.КоличествоМест, 0)
	|		ИНАЧЕ ФактическиеПараметрыРейса.КоличествоМест
	|	КОНЕЦ КАК CargoQuantity,
	|	ВЫБОР
	|		КОГДА ФактическиеПараметрыРейса.КоличествоЗагрузочныхМетров ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0)
	|		ИНАЧЕ ФактическиеПараметрыРейса.КоличествоЗагрузочныхМетров
	|	КОНЕЦ КАК LoadMetresQuantity,
	|	ЕСТЬNULL(вт_Расходы.СуммаПлан, 0) КАК ExpensesPlan,
	|	ЕСТЬNULL(вт_Расходы.СуммаФакт, 0) КАК ExpensesFact,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упРейс.Автор) КАК Author
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК СтатусыРейсовСП
	|		ПО упРейс.Ссылка = СтатусыРейсовСП.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК ПеревозчикПоРейсуСП
	|		ПО упРейс.Ссылка = ПеревозчикПоРейсуСП.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК ПлановыеПараметрыРейса
	|		ПО упРейс.Ссылка = ПлановыеПараметрыРейса.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК ФактическиеПараметрыРейса
	|		ПО упРейс.Ссылка = ФактическиеПараметрыРейса.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Расходы КАК вт_Расходы
	|		ПО упРейс.Ссылка = вт_Расходы.Рейс
	|ГДЕ
	|	упРейс.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задания.Ссылка КАК Рейс,
	|	упЗаявкаНаПеревозкуГруза.Ссылка КАК Заявка,
	|	упЗаявкаНаПеревозкуГруза.ДатаКИС КАК DateERP,
	|	упЗаявкаНаПеревозкуГруза.НомерКИС КАК NumberERP,
	|	СУММА(упЗаявкаНаПеревозкуГруза.ОценочнаяСтоимость) КАК EvaluativeCost
	|ИЗ
	|	Документ.упРейс.Задания КАК Задания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|		ПО Задания.Задание.ЗаявкаНаПеревозкуГруза = упЗаявкаНаПеревозкуГруза.Ссылка
	|ГДЕ
	|	НЕ упЗаявкаНаПеревозкуГруза.Ссылка ЕСТЬ NULL
	|	И Задания.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Задания.Ссылка,
	|	упЗаявкаНаПеревозкуГруза.Ссылка,
	|	упЗаявкаНаПеревозкуГруза.ДатаКИС,
	|	упЗаявкаНаПеревозкуГруза.НомерКИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Рейс,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упМаршрутПоРейсуСрезПоследних.Адрес) КАК ShippingAddress,
	|	упМаршрутПоРейсуСрезПоследних.Пройдена КАК PointPassed
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Ссылка) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ
	|	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтрокаНомер";
	
	Запрос.УстановитьПараметр("Ссылка", Рейс);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	Выборка = ПакетЗапросов[1].Выбрать();
	ВыборкаЗаявок = ПакетЗапросов[2].Выбрать();
	ВыборкаМаршрута = ПакетЗапросов[3].Выбрать();
	
	ФиксСтруктураРейсов = "Shipment";
	ФиксСтруктура = "Number,Date,Status,Carrier,CarrierAgreement,Vehicle,FirstDriver,SecondDriver,FreightForwarder,EvaluativeCost,Weight,Volume,CargoQuantity,LoadMetresQuantity,
	|ExpensesPlan,ExpensesFact,Author,Route,Orders";
	
	ФиксСтруктураЗаявки = "NumberERP,DateERP";
	ФиксСтруктураМаршрут = "ShippingAddress,PointPassed";
	
	Shipment = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураДанных = Новый Структура(ФиксСтруктура);
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);	
		Route = Новый Массив;
		Пока ВыборкаМаршрута.Следующий() Цикл
			СтруктураДанныхМаршрут = Новый Структура(ФиксСтруктураМаршрут);
			ЗаполнитьЗначенияСвойств(СтруктураДанныхМаршрут, ВыборкаМаршрута);
			Route.Добавить(СтруктураДанныхМаршрут);
		КонецЦикла;
		СтруктураДанных["Route"] = Route;
		Orders = Новый Массив;
		Пока ВыборкаЗаявок.Следующий() Цикл
			СтруктураДанныхЗаявка = Новый Структура(ФиксСтруктураЗаявки);
			ЗаполнитьЗначенияСвойств(СтруктураДанныхЗаявка, ВыборкаЗаявок);
			Orders.Добавить(СтруктураДанныхЗаявка);
		КонецЦикла;
		СтруктураДанных["Orders"] = Orders;
		СтруктураДанныхРейсов = Новый Структура(ФиксСтруктураРейсов,СтруктураДанных);
		Shipment.Добавить(СтруктураДанныхРейсов);
	КонецЦикла;
	
	Возврат Shipment;
	
КонецФункции // ПолучитьДанныеПоРейсу()

#КонецОбласти 
