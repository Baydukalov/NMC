#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Отказ = Ложь;
	сткОтбора = СтруктураОтбора(ПараметрКоманды, Отказ);
	Если НЕ Отказ Тогда
		ДополнительныеПараметры = Новый Структура("Рейсы", ПараметрКоманды);
		
		сткОтбора.Вставить("ПометкаУдаления", Ложь);
		мсвСтатусы = Новый Массив;
		мсвСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Новый"));
		мсвСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.упСтатусыПутевогоЛиста.Выдан"));
		сткОтбора.Вставить("Статус", мсвСтатусы);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Отбор", 				сткОтбора);
		ПараметрыФормы.Вставить("МножественныйВыбор", 	Ложь);
		ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ОбработчикЗакрытияФормыВыбораПутевогоЛиста", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Документ.упПутевойЛист.ФормаВыбора", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка, ОбработчикЗакрытияФормы);	
	Иначе	
		ТекстОшибки = НСтр("ru = 'Рейсы не удовлетворяют условиям для включения в путевой лист.'");
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СтруктураОтбора(мсвРейсы, Отказ)

	Возврат упПутевойЛистУчетГСМ.ПроверитьВозможностьДобавленияРейсовВПутевойЛист(мсвРейсы, Отказ);	
	
КонецФункции

&НаКлиенте
Процедура ОбработчикЗакрытияФормыВыбораПутевогоЛиста(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатЗакрытия = Неопределено Тогда
		Отказ = Ложь;
		ДобавитьРейсыВПутевойЛист(ДополнительныеПараметры.Рейсы, РезультатЗакрытия, Отказ);
		Если НЕ Отказ Тогда
			Оповестить("ПутевойЛист",,РезультатЗакрытия);
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(РезультатЗакрытия);
			ПоказатьОповещениеПользователя(НСтр("ru = 'Рейсы добавлены в путевой лист'"), НавигационнаяСсылка, РезультатЗакрытия, БиблиотекаКартинок.Информация32);
		Иначе	
			ТекстОшибки = НСтр("ru = 'Не удалось включить рейс в путевой лист.'");
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРейсыВПутевойЛист(мсвРейсы, ПутевойЛист, Отказ)
	упПутевойЛистУчетГСМ.ДобавитьРейсыВПутевойЛист(мсвРейсы, ПутевойЛист, Отказ);
КонецПроцедуры

#КонецОбласти 
