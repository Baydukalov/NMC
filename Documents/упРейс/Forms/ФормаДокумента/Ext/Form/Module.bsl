#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		
		ЗначенияЗаполнения = Неопределено;
		РазрешеноФормированиеКонтейнеров = Ложь;
		Если Параметры.Свойство("ЗначенияЗаполнения", ЗначенияЗаполнения) Тогда
			ВидТранспортнойУслуги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияЗаполнения, "ВидТранспортнойУслуги");
			Если ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
				Объект.ВидТранспортнойУслуги = ВидТранспортнойУслуги;
				РазрешеноФормированиеКонтейнеров = Истина
			КонецЕсли;
		КонецЕсли;
		
		Объект.ВидПеревозки = Справочники.упВидыПеревозок.ЗначениеПоУмолчанию(Объект.ВидПеревозки, , , РазрешеноФормированиеКонтейнеров);
		
		упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(Объект, Не РазрешеноФормированиеКонтейнеров);
		
		Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
			Объект.Организация = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки, "Организация");
		КонецЕсли;
		Если ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки, Объект.Автор);
		КонецЕсли;
		Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
			Объект.ЧасовойПояс	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
		КонецЕсли;
			
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	РежимФормированияКонтейнеров = Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	ЭтаФорма.ПеревозкаТранспортнойКомпанией = Объект.ПеревозкаТранспортнойКомпанией;
	УстановитьСтатусФормы();
	ОбновитьДанныеВидПеревозки();
	ОбновитьДанныеВидТранспортнойУслуги();
	ОбновитьДанныеФормы(, Истина);
	ОбновитьДанныеОПеревозке();
	Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(Соглашение) Тогда
		УстановитьВалютуУчета();
		ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
	КонецЕсли;
	
	ОбновитьПлановыйРасходПоРейсу();
	НастроитьСтраницуДанныеТахографа();
	УстановитьКнопкиСтатусаРейса();
	упОбновитьВидимостьМаршрутаРабот(Статус);
	УстановитьЗаголовкиПоказателейЗагрузки();
	ОбновитьПредставлениеПоказателейЗагрузки();
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТранспортноеСредство, упКорректировкаРейса.ПлановоеНачалоВыполнения);
	
	УстановитьСтатусWMS();

	ЗаблокироватьЭлементы();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// Интеграция.Packer3D
	Если ИспользуетсяИнтеграцияСPacker3D И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьУкладкуГрузовНаСервере();
	КонецЕсли;
	// Конец Интеграция.Packer3D
	
	упВводНаОсновании.УстановитьВидимостьКоманд(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСеанса.ТекущийВнешнийПользователь) Тогда
		// СтандартныеПодсистемы.Печать
		УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.Печать
	КонецЕсли;	
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства	
	
	ЭтаФорма.ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	ЭтаФорма.ИспользуетсяУчетПодразделений = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	ЭтаФорма.ВедетсяУчетДокументов = ПолучитьФункциональнуюОпцию("упВедетсяУчетДокументовТранспортныхСредствВодителей");
	ЭтаФорма.УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьКоличествоЗагрузочныхМетров = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	ЭтаФорма.ЗаголовокЗаданиеМасса = Элементы.ЗаданияМасса.Заголовок;
	ЭтаФорма.ЗаголовокЗаданиеОбъем = Элементы.ЗаданияОбъем.Заголовок;
	ЭтаФорма.ЗаголовокЗаданиеКоличествоМест = Элементы.ЗаданияКоличествоМест.Заголовок;
	ЭтаФорма.ИспользуютсяДополнительныеОперации = ПолучитьФункциональнуюОпцию("упИспользуютсяДополнительныеОперации");
	ЭтаФорма.ИспользуютсяУслугиИнкассации = ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации");
	ЭтаФорма.ИспользуетсяЗагрузкаДанныхИзФайловТахографа = ПолучитьФункциональнуюОпцию("упИспользуетсяЗагрузкаДанныхИзФайловТахографа");
	ЭтаФорма.ИспользуетсяСкладскойУчетНаХабах = ПолучитьФункциональнуюОпцию("упИспользуетсяСкладскойУчетНаХабах");
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
		
	// Интеграция.Packer3D	
	ИспользуетсяИнтеграцияСPacker3D = ПолучитьФункциональнуюОпцию("упИспользуетсяИнтеграцияСPacker3D");
	РазрешитьАктивизациюИнтеграцииСPacker3D = ИспользуетсяИнтеграцияСPacker3D;
	Элементы.ГруппаУкладкаГрузов.Видимость = ИспользуетсяИнтеграцияСPacker3D;
	// Конец Интеграция.Packer3D	

	Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Элементы.ГруппаЗадания.Видимость = Ложь;
		Элементы.МаршрутОткрытьФормуРедактированияМаршрута.Видимость = Ложь;
	КонецЕсли; 
	
	Элементы.ГруппаДанныеТахографа.Видимость = ИспользуетсяЗагрузкаДанныхИзФайловТахографа;
	
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект, Параметры, Ложь);
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Взаиморасчеты
	ЭтаФорма.РазрешеноЗагружатьВзаиморасчеты = ПолучитьФункциональнуюОпцию("упЗагружатьВзаиморасчетыСПартнерами");
	ЭтаФорма.ВзаиморасчетыЗагружаются = Ложь;
	ЭтаФорма.ВзаиморасчетыЗагрузить = Ложь;
	Элементы.ГруппаСостояниеВзаиморасчетов.Видимость = РазрешеноЗагружатьВзаиморасчеты;
	
	ПриСозданииПриЧтенииНаСервере();
	
	НастроитьПанельНавигации();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПеревозкаТранспортнойКомпанией И Объект.Задания.Количество() Тогда
		текСтрока = Объект.Задания[0];
		сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
		Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
			сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
		КонецЦикла;
	КонецЕсли;
	ЗагрузитьВзаиморасчетыСПартнерами();
	//ПересчитатьПорядокОтгрузки();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Интеграция.Packer3D
	Если ИспользуетсяИнтеграцияСPacker3D Тогда
		ПодключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания", 0.2, Истина);
	КонецЕсли; 
	// Конец Интеграция.Packer3D
	
	УстановитьЗаголовокМаршрута();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Статус = Перечисления.упСтатусыРейса.Отменен Или НовыйСтатус = Перечисления.упСтатусыРейса.Отменен Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
	КонецЕсли; 
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

	// Проверка ТЧ.Задания на дубли
	тбзЗадания = упКорректировкаРейса.Задания.Выгрузить(,"Задание, ГрузовоеМесто");
	тбзЗадания.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число"));
	
	тбзЗадания.ЗаполнитьЗначения(1,"Количество");
	тбзЗадания.Свернуть("Задание, ГрузовоеМесто", "Количество");
	
	Для каждого текСтрока Из тбзЗадания Цикл
		Если текСтрока.Количество > 1 Тогда
			ТекстСообщения = Нстр("ru = 'Содержит дублирующие строки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							Объект.Ссылка,
							"Задание",
							,
							Отказ);
			
			Прервать; 
		КонецЕсли;
	КонецЦикла;
	
	Для каждого текСтрока Из ДополнительныеСотрудники Цикл
		Если Не ЗначениеЗаполнено(текСтрока.Сотрудник) Тогда
			ТекстСообщения = Нстр("ru = 'Не указан дополнительный сотрудник!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
							Объект.Ссылка,
							"текСтрока.Сотрудник",
							,
							Отказ);

		КонецЕсли; 
	КонецЦикла; 
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.НовыйСтатус = НовыйСтатус;
	текКорректировкаМаршрутаОбъект.ПроверитьЗаполнениеМаршрута(Отказ);
	ПроверяемыеРеквизиты.Добавить("Проверен");
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	мсвСобытий = СтрРазделить(ИмяСобытия, упСервисныеФункцииКлиент.РазделительИменСобытий());
	
	ИзменилсяМаршрут = НЕ мсвСобытий.Найти("ИзменилсяМаршрут") = Неопределено;
	ОбновитьРаботы	 = НЕ мсвСобытий.Найти("ОбновитьРаботы") = Неопределено;
		
	ОбновитьДанныФормы 		 = (ИзменилсяМаршрут ИЛИ ОбновитьРаботы) И Источник = Объект.Ссылка;
	Если ОбновитьДанныФормы Тогда
		ОбновитьДанныеФормы();
	КонецЕсли;
	
	ОбновитьДанныеОПеревозке = ОбновитьРаботы И Источник = Объект.Ссылка;
	Если ОбновитьДанныеОПеревозке Тогда
		ОбновитьДанныеОПеревозке();
	КонецЕсли;
		
	Для каждого ИмяОбрабатываемогоСобытия Из мсвСобытий Цикл
		
		Если ИмяОбрабатываемогоСобытия = "СформированыЗаявкиНаТС" Тогда
			Если ТипЗнч(Параметр) = Тип("Массив") Тогда
				Для каждого текЗаявкаНаТС Из Параметр Цикл
					ОткрытьФорму("Документ.упЗаявкаНаТС.ФормаОбъекта", Новый Структура("Ключ", текЗаявкаНаТС), Источник);
				КонецЦикла;
			КонецЕсли;
		ИначеЕсли ИмяОбрабатываемогоСобытия = "ИзменилсяМаршрут" И Источник = Объект.Ссылка  Тогда
			ЗаблокироватьЭлементы();
			УстановитьПредставленияПоказателейЗагрузки();
			ОбновитьПредставлениеПоказателейЗагрузки();
		ИначеЕсли ИмяОбрабатываемогоСобытия = "СменаСтатуса" И Источник = Объект.Ссылка Тогда
			ИзменилсяСтатус();
		ИначеЕсли ИмяОбрабатываемогоСобытия = "ИзменилсяПлановыйРасход" Тогда
			ОбновитьПлановыйРасходПоРейсу();
		ИначеЕсли ИмяОбрабатываемогоСобытия = "СформированыОтчетыИнкассаторов" Тогда
			Если ТипЗнч(Параметр) = Тип("Структура") Тогда
				текОтказ = Параметр.Отказ;
				Если текОтказ Тогда
					ТекстСообщения = Нстр("ru = 'Для расчета показателя:%1 не задана организация'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Параметр.ТекстОшибки, Объект.Ссылка);														
				КонецЕсли;
				мсвОтчетыИнкассаторов = Параметр.мсвОтчетыИнкассаторов;
				Для каждого текОтчетИнкассатора Из мсвОтчетыИнкассаторов Цикл
					ОткрытьФорму("Документ.упОтчетИнкассатора.Форма.ФормаДокумента", Новый Структура("Ключ", текОтчетИнкассатора), Источник);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если НЕ ЭтоЗаполнениеРасходов Тогда
		// Определяются нерассчитанные строки.
		Для каждого Строка Из Объект.ПлановыеРасходы Цикл
			Если Строка.Сумма = 0 Тогда
				ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОСтрокахСНулевымиСуммами",ЭтаФорма);
				ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Удалить строки плановых расходов с нулевыми суммами?'"), РежимДиалогаВопрос.ДаНет);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Если НЕ ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		// Заполнить табличную часть документа "Задания"
		мсвСтроки = текКорректировкаМаршрутаОбъект.Задания.НайтиСтроки(Новый Структура("СтрокаДокумента", Истина));
		ТекущийОбъект.Задания.Очистить();
		Для каждого СтрокаЗадания Из мсвСтроки Цикл
			НоваяСтрока = ТекущийОбъект.Задания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗадания);
		КонецЦикла;
	КонецЕсли; 	
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("КорректировкаРейса", текКорректировкаМаршрутаОбъект);
	Если Истина
		И СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе") 
		И (Ложь
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.ВыполненЧастично")
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен")
			Или НовыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.ВыполненЧастично")
			Или НовыйСтатус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен")
			)
	Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ТранспортноеСредство", ТранспортноеСредство);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НачалоВыполнения", текКорректировкаМаршрутаОбъект.НачалоВыполнения);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ОкончаниеВыполнения", текКорректировкаМаршрутаОбъект.ОкончаниеВыполнения);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ЕстьОтмененныеРаботы", ЕстьОтмененныеРаботы());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;

	ТекущийОбъект.ДополнительныеСвойства.Вставить("НеПроверятьДокументыНаОсновании", Истина);
			
	Если ДополнительныеСотрудники.Количество() Тогда
		Сч = 1;
		Для каждого текСотрудник Из ДополнительныеСотрудники Цикл
			текСотрудник.СтрокаНомер = Сч;
			Сч = Сч + 1;
		КонецЦикла; 
	КонецЕсли; 
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ДополнительныеСотрудники", ДополнительныеСотрудники.Выгрузить());
	
	сткЭкипаж = Новый Структура();
	сткЭкипаж.Вставить("Перевозчик"           , Перевозчик);
	сткЭкипаж.Вставить("КонтрагентПеревозчика", КонтрагентПеревозчика);
	сткЭкипаж.Вставить("Соглашение"           , Соглашение);
	сткЭкипаж.Вставить("ТипТС"                , ТипТС);
	сткЭкипаж.Вставить("ТранспортноеСредство" , ТранспортноеСредство);
	сткЭкипаж.Вставить("Водитель1"            , Водитель1);
	сткЭкипаж.Вставить("Водитель2"            , Водитель2);
	сткЭкипаж.Вставить("Экспедитор"           , Экспедитор);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ИзмененЭкипаж", сткЭкипаж);
	
	НовыйСтатус = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьСтатусФормы();
	УстановитьКнопкиСтатусаРейса();
	ЗаблокироватьЭлементы();
	
	мсвЗаявкиНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаявкиНаПеревозкуГруза) Тогда
		ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус.ЗагрузитьЗначения(мсвЗаявкиНаПеревозкуГруза);
	КонецЕсли;
	
	мсвЗаданияНаПеревозкуГруза = Неопределено;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", мсвЗаданияНаПеревозкуГруза) Тогда
		ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус.ЗагрузитьЗначения(мсвЗаданияНаПеревозкуГруза);
	КонецЕсли;
	
	ОбновитьДанныеФормы();
	ОбновитьПлановыйРасходПоРейсу();
	
	УстановитьПредставленияПоказателейЗагрузки();
	ОбновитьПредставлениеПоказателейЗагрузки();
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТранспортноеСредство, упКорректировкаРейса.ПлановоеНачалоВыполнения);
	НастроитьПанельНавигации();
	НастроитьСтраницуДанныеТахографа();
	
	РазблокироватьДанныеДляРедактирования(ТекущийОбъект.Ссылка, УникальныйИдентификатор);
	
	упВводНаОсновании.УстановитьВидимостьКоманд(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	//ОбновитьПредставлениеПоказателейЗагрузки();
	ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	ВзаимодействияКлиент.КонтактПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи,"упРейс");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОСтрокахСНулевымиСуммами(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Расходы	= Объект.ПлановыеРасходы;
		КоличествоСтрок = Расходы.Количество();
		Для й = 1 По КоличествоСтрок Цикл
			Если Расходы[КоличествоСтрок - й].Сумма = 0 Тогда
				Расходы.Удалить(КоличествоСтрок - й);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область ОбработчикиСобытийУкладкуГрузовPacker3D

&НаКлиенте
Процедура УкладкаГрузовПриАктивизацииПоля(Элемент)
	
	Если УкладкаГрузов.Количество() И РазрешитьАктивизациюИнтеграцииСPacker3D Тогда
		ПодключитьОбработчикОжидания("УкладкаГрузовПриАктивизацииПоляОбработчикОжидания", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы_Шапка 

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	
	ВидПеревозкиПоЗаданиям = ВидПеревозкиПоЗаданиям();
	Если Истина
		И ЗначениеЗаполнено(ВидПеревозкиПоЗаданиям)
		И ВидПеревозкиПоЗаданиям <> Объект.ВидПеревозки 
	Тогда
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПриИзмененииВидаПеревозки", ЭтаФорма);	
		ПоказатьВопрос(ОписаниеОповещенияОЗакрытии, Нстр("ru = 'Виды перевозок в рейсе и заданиях различаются. Очистить таблицу заданий?'"),РежимДиалогаВопрос.ОКОтмена);
	Иначе	
		ВидПеревозкиПриИзмененииНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		ОрганизацияВидаПеревозки = ПолучитьЗначениеРеквизита(ВыбранноеЗначение, "Организация");
		Если ЗначениеЗаполнено(ОрганизацияВидаПеревозки) Тогда
			Объект.Организация = ОрганизацияВидаПеревозки;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТипТС) Или ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			ПроверитьСоответствиеТранспортаВидуПеревозки(ВыбранноеЗначение);
		ИначеЕсли ЗначениеЗаполнено(Перевозчик) Тогда
			ПроверитьСоответствиеПеревозчикаВидуПеревозки(ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если РежимФормированияКонтейнеров Тогда
		Отбор = Новый Структура("РазрешеноФормированиеКонтейнеров", Истина);
		ПараметрыФормы = Новый Структура("Отбор", Отбор);
		ОткрытьФорму("Справочник.упВидыПеревозок.ФормаВыбора", ПараметрыФормы, Элемент); 
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если РежимФормированияКонтейнеров Тогда
		Отбор = ПараметрыПолученияДанных.Отбор;
		Отбор.Вставить("РазрешеноФормированиеКонтейнеров", Истина);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Если РежимФормированияКонтейнеров Тогда
		Отбор = ПараметрыПолученияДанных.Отбор;
		Отбор.Вставить("РазрешеноФормированиеКонтейнеров", Истина);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПлановоеНачалоВыполненияПриИзменении(Элемент)
	ПлановоеНачалоВыполненияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПлановоеНачалоВыполненияПриИзмененииНаСервере()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеПлановогоНачалаВыполнения();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыполненияПриИзменении(Элемент)
	ИзменениеФактическогоВремениВыполненияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеВыполненияПриИзменении(Элемент)
	ИзменениеФактическогоВремениВыполненияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы_СтраницаЗадания

&НаКлиенте
Процедура ЗаданияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		
	текСтрока = Элементы.Задания.ТекущиеДанные;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		текСтрока.СтрокаДокумента	= Истина;
		текСтрока.КоличествоГрузов	= 1;
	КонецЕсли;

	сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	текСтрока = Элементы.Задания.ТекущиеДанные;
	ОбработатьСтрокуЗаданияНаСервере(текСтрока.НомерСтроки, НоваяСтрока,, ОтменаРедактирования, Отказ);
	Если НЕ Отказ Тогда
		Модифицированность = Истина;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаданияПередУдалением(Элемент, Отказ)
	
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		текСтрока = упКорректировкаРейса.Задания.НайтиПоИдентификатору(текИдентификатор); 
		
		Если НЕ текСтрока.СтрокаДокумента Тогда
				ТекстСообщения = Нстр("ru = 'Нельзя удалить строку номер %1. Разрешено удалять только строки добавленные в текущем документе.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текСтрока.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				Продолжить;
		КонецЕсли;
		
		Если Не текСтрока = Неопределено Тогда
			сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
			Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
				сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
			КонецЦикла;
			ОбработатьСтрокуЗаданияНаСервере(текСтрока.НомерСтроки,,Истина,,Отказ);
			Если НЕ Отказ Тогда
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеПриИзменении(Элемент)
	
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если Не текСтрока = Неопределено Тогда
		  ЗаполнитьВспомогательныеПоляЗаданийНаСервере(текСтрока.НомерСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	текОтбор 	= Новый Структура();
	ЗаполнитьСтруктуруОтбора(текОтбор);
	сткПараметры 	= Новый Структура("Отбор", текОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.ЗаданияЗадание);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	текОтбор 	= Параметры.Отбор;
	ЗаполнитьСтруктуруОтбора(текОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияЗаданиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	текОтбор 	= ПараметрыПолученияДанных.Отбор;
	ЗаполнитьСтруктуруОтбора(текОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияГрузовоеМестоПриИзменении(Элемент)
	
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если Не текСтрока = Неопределено Тогда
		ЗаполнитьВспомогательныеПоляЗаданийНаСервере(текСтрока.НомерСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияКоличествоГрузовПриИзменении(Элемент)
	
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если Не текСтрока = Неопределено Тогда
		  ЗаполнитьВспомогательныеПоляЗаданийНаСервере(текСтрока.НомерСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	текСтрока = Элементы.Задания.ТекущиеДанные;
	Если текСтрока <> Неопределено 
		И (НЕ текСтрока.СтрокаДокумента ИЛИ Элемент.ТолькоПросмотр) Тогда
		Если Поле.Имя = "ЗаданияЗадание" Тогда
			ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Задание), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ЗаданияГрузовоеМесто" Тогда	
			ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ГрузовоеМесто), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "ЗаданияТара" Тогда	
			ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Тара), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыФормы_СтраницаПеревозчик

&НаКлиенте
Процедура ПеревозчикПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Перевозчик) Тогда
		Если НЕ ЗначениеЗаполнено(Соглашение) Тогда
			Соглашение = ПолучитьСоглашениеПоУмолчанию();
		КонецЕсли;	
		КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Перевозчик);
	КонецЕсли;
	СоглашениеПриИзмененииНаСервере();
	ЗагрузитьВзаиморасчетыСПартнерами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	сткПараметры = Новый Структура("ВидПеревозки, Организация, ТипТС", Объект.ВидПеревозки, Объект.Организация, ТипТС);
	ОткрытьФорму("Справочник.упПартнеры.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	
	СоглашениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ТранспортноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(ВыбранноеЗначение) И ВыбранноеЗначение <> ТранспортноеСредство Тогда
		
		мсвПараметрыОбработки = ПараметрыОбработкиПриВыбореТранспортногоСредства(ВыбранноеЗначение);
		//ОбновитьПредставлениеПоказателейЗагрузки();
		
		Для каждого сткПараметрыОбработки Из мсвПараметрыОбработки Цикл
			Если НЕ сткПараметрыОбработки.ИмяПроцедуры = Неопределено Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения(сткПараметрыОбработки.ИмяПроцедуры, ЭтаФорма, сткПараметрыОбработки.ДополнительныеПараметры);
				ПоказатьВопрос(ОписаниеОповещения, сткПараметрыОбработки.ТекстВопроса, РежимДиалогаВопрос.ДаНет);	 
			КонецЕсли;
		КонецЦикла;
		//УстановитьЗаголовокМаршрута();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И ВыбранноеЗначение <> ТипТС И Не ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		ПараметрыОбработкиПриВыбореТипаТранспортногоСредства(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыОбработкиПриВыбореТипаТранспортногоСредства(ТипТС)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеТипаТранспортногоСредстваРейса(ТипТС);		
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	ПересчитатьИтогиПоРейсу();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбранПунктМенюВыбораТС", ЭтаФорма, Новый Структура("ЭлементФормы", Элемент));
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить("ОбычныйСписок", "Обычный список");
	СписокВариантов.Добавить("СписокДоступности", "Список доступности");
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокВариантов, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТС();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТС();
КонецПроцедуры

&НаСервере
Функция ПараметрыОбработкиПриВыбореТранспортногоСредства(ТранспортноеСредство)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	сткПараметрыОбработки = текКорректировкаМаршрутаОбъект.ОбработатьИзменениеТранспортногоСредстваРейса(ТранспортноеСредство, ЗонаПогрузки, ЗонаРазгрузки);		
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	ПересчитатьИтогиПоРейсу();
	Возврат сткПараметрыОбработки;
КонецФункции

&НаКлиенте
Процедура ТипТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	сткПараметры = Новый Структура("ВидПеревозки, Перевозчик, Организация", Объект.ВидПеревозки, Перевозчик, Объект.Организация);
	Если РежимФормированияКонтейнеров Тогда
		ОткрытьФорму("Справочник.упТипыТС.Форма.ФормаВыбораКонтейнера", сткПараметры, Элемент);
	Иначе	
		ОткрытьФорму("Справочник.упТипыТС.Форма.ФормаВыбора", сткПараметры, Элемент);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТипаТС();
КонецПроцедуры

&НаКлиенте
Процедура ТипТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗаполнитьПараметрыВыбораТипаТС();
КонецПроцедуры

#КонецОбласти 

#Область ЭлементыФормы_СтраницаМаршрут

&НаКлиенте
Процедура МаршрутПриАктивизацииСтроки(Элемент)

	СтрокаТочка = Элемент.ТекущиеДанные;
	Если Не СтрокаТочка = Неопределено Тогда
		Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", СтрокаТочка.Идентификатор);
		Если Ложь 
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Новый") 
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КПланированиюТС") 
			Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.НазначеноТС") 
			Или (Истина
			     И СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе")
				И Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению")) Тогда
				
			упКорректировкаМаршрутаКлиент.УстановитьДоступностьКнопокПеремещенияТочекМаршрута(Элемент,
			                                                                                  упКорректировкаРейса.Маршрут,
																			  Элементы.МаршрутПереместитьТочкуВверх,
																			  Элементы.МаршрутПереместитьТочкуВниз);
			
			сткТочка = Новый Структура("Пройдена, Отменена, Идентификатор");
			ЗаполнитьЗначенияСвойств(сткТочка, СтрокаТочка);
			РазрешеноРазделятьРаботы = РазрешеноРазделятьРаботыНаСервере(сткТочка);
			Элементы.РаботыРазделитьРаботы.Доступность = РазрешеноРазделятьРаботы;
		КонецЕсли; 
	КонецЕсли;
	
	УстановитьЗаголовокМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	текСтрока = Элемент.ТекущиеДанные;
	Если НоваяСтрока ИЛИ Копирование Тогда
		текСтрока.СтрокаДокумента	= Истина;
	КонецЕсли;
	
	сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, текСтрока[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные.Пройдена И ТекущиеДанные.Отменена Тогда
		// Ошибка платформы, при которой не срабатывает событие "ПриИзменении"
		// для полей "МаршрутОтменена", "МаршрутПройдена".
		Если упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования.Пройдена Тогда
			ТекущиеДанные.Пройдена = Ложь;
		Иначе
			ТекущиеДанные.Отменена = Ложь;
		КонецЕсли; 
		упПрохождениеТочкиКлиентСервер.ИзменитьПризнакПрохожденияТочки(ТекущиеДанные, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
		УстановитьКнопкиСтатусаРейса();
		Модифицированность = Истина;
	КонецЕсли; 
	
	Если НоваяСтрока Тогда
		Отказ = Ложь;
		ДобавитьТочкуВМаршрутНаСервере(ТекущиеДанные.НомерСтроки,Отказ);
		Если НЕ Отказ Тогда
			Модифицированность = Истина;	
		КонецЕсли;
	
		Элементы.Работы.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
	//текСтрока = ТекущиеДанные;
	ОбработатьСтрокуТочкаМаршрутаНаСервере(ТекущиеДанные.НомерСтроки,,,ОтменаРедактирования, Отказ, ,Истина);
	//ОбновитьПредставлениеПоказателейЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередУдалением(Элемент, Отказ)
	
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		
		текТочка = упКорректировкаРейса.Маршрут.НайтиПоИдентификатору(текИдентификатор); 
		
		Если НЕ текТочка.СтрокаДокумента Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если Не текТочка = Неопределено И НЕ Отказ Тогда
			Если упКорректировкаРейса.ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками 
				И текТочка.ТипТочки <> ПредопределенноеЗначение("Перечисление.упТипыТочекМаршрута.КонтрольнаяТочка") Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
			сткЗначенияСтрокиМаршрутаДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиМаршрутаДоРедактирования;
			
			Для каждого текПоле Из сткЗначенияСтрокиМаршрутаДоРедактирования Цикл
				сткЗначенияСтрокиМаршрутаДоРедактирования.Вставить(текПоле.Ключ, текТочка[текПоле.Ключ]);
			КонецЦикла;
			ОбработатьСтрокуТочкаМаршрутаНаСервере(текТочка.НомерСтроки,, Истина,, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПослеУдаления(Элемент)
	ОбработатьУдалениеТочкиНаСервере();
	//ОбновитьПредставлениеПоказателейЗагрузки();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Истина
			И Поле.Имя = "МаршрутАдрес"
			И ЗначениеЗаполнено(ТекущиеДанные.Адрес) Тогда
			ОткрытьФорму("Справочник.упАдреса.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.Адрес), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПриИзменении(Элемент)
	Модифицированность			= Истина;
КонецПроцедуры

&НаКлиенте
Процедура МаршрутАдресПриИзменении(Элемент)
	
	текСтрока = Элементы.Маршрут.ТекущиеДанные;
	ОбработатьИзменениеАдресаМаршрута(текСтрока.НомерСтроки)
		
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПройденаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	Если ТекущиеДанные.Пройдена И ТекущиеДанные.Отменена Тогда
		ТекущиеДанные.Отменена = Ложь;
	КонецЕсли; 
	упПрохождениеТочкиКлиентСервер.ИзменитьПризнакПрохожденияТочки(ТекущиеДанные, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
	УстановитьКнопкиСтатусаРейса();
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура МаршрутОтмененаПриИзменении(Элемент)
	ТекущиеДанные 			= Элементы.Маршрут.ТекущиеДанные;
	Если ТекущиеДанные.Пройдена И ТекущиеДанные.Отменена Тогда
		ТекущиеДанные.Пройдена = Ложь;
	КонецЕсли;
	упПрохождениеТочкиКлиентСервер.ИзменитьПризнакПрохожденияТочки(ТекущиеДанные, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
	УстановитьКнопкиСтатусаРейса();
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеАдресаМаршрута(НомерСтроки)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеАдресаМаршрута(НомерСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

//&НаКлиенте
//Процедура МаршрутПлановоеПрибытиеПриИзменении(Элемент)
//	СпланироватьЗадачиПоМаршруту(Элементы.Маршрут.ТекущиеДанные.НомерСтроки);
//КонецПроцедуры

&НаКлиенте
Процедура РаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		
	СтрокаРабота = Элемент.ТекущиеДанные;
	Если НоваяСтрока ИЛИ Копирование Тогда
	     СтрокаРабота.Идентификатор 		= Элементы.Маршрут.ТекущиеДанные.Идентификатор;
		 СтрокаРабота.СтрокаДокумента 	= Истина;
	КонецЕсли;
	 	
	сткЗначенияСтрокиРаботДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования;
	
	Для каждого текПоле Из сткЗначенияСтрокиРаботДоРедактирования Цикл
		Если НоваяСтрока ИЛИ Копирование Тогда
			сткЗначенияСтрокиРаботДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
		Иначе
			сткЗначенияСтрокиРаботДоРедактирования.Вставить(текПоле.Ключ, СтрокаРабота[текПоле.Ключ]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока Тогда
		СтрокаРабота = Элемент.ТекущиеДанные;
		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Идентификатор) Тогда
			СтрокаРабота.Идентификатор 		= Элементы.Маршрут.ТекущиеДанные.Идентификатор;
		 	СтрокаРабота.СтрокаДокумента 	= Истина;
		КонецЕсли;
		
		ДобавитьРаботуНаСервере(СтрокаРабота.НомерСтроки, Элементы.Маршрут.ТекущиеДанные.Идентификатор, Отказ);
		Если НЕ Отказ Тогда
			Модифицированность = Истина;	
		КонецЕсли;
	Иначе
		СтрокаРабота = Элементы.Работы.ТекущиеДанные;
		Если СтрокаРабота.КоличествоГрузов = 0	
				И ЗначениеЗаполнено(СтрокаРабота.ГрузовоеМесто) Тогда
			СтрокаРабота.КоличествоГрузов = 1;
		КонецЕсли;
		
		СтрокаТочка = Элементы.Маршрут.ТекущиеДанные;
		Если НЕ СтрокаТочка = Неопределено 
			И НЕ СтрокаРабота = Неопределено Тогда
			ОбработатьСтрокуРаботыНаСервере(СтрокаРабота.НомерСтроки, СтрокаТочка.НомерСтроки,,,ОтменаРедактирования,Отказ);	
		КонецЕсли;
	КонецЕсли;
	Если НЕ Отказ И НЕ ОтменаРедактирования Тогда
		ПересчитатьИтогиПоРейсу();
		//ОбновитьПредставлениеПоказателейЗагрузки();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура РаботыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Элементы.Маршрут.ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сначала необходимо добавить маршрут'"), Объект.Ссылка, "Маршрут",,Отказ);
	ИначеЕсли СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе")
			И Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрещено добавлять работы'"), Объект.Ссылка, "Маршрут",,Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыПередУдалением(Элемент, Отказ)
	
	мсвВыделенныеСтроки = Элемент.ВыделенныеСтроки;
	
	мсвИдентификаторыРабот 	= Новый Массив;
	мсвСтроки				= Новый Массив;
	
	Для каждого текИдентификатор Из мсвВыделенныеСтроки Цикл
		СтрокаРабота = упКорректировкаРейса.Работы.НайтиПоИдентификатору(текИдентификатор);
		Если НЕ СтрокаРабота = Неопределено Тогда
			Если упКорректировкаРейса.ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками 
				И упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(СтрокаРабота.Операция) Тогда
				Отказ = Истина;
				Прервать;
			КонецЕсли;
			мсвИдентификаторыРабот.Добавить(СтрокаРабота.ИдентификаторРаботы);
			мсвСтроки.Добавить(СтрокаРабота);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		сткЗначенияСтрокиРаботДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиРаботДоРедактирования;
		Для каждого СтрокаРабота Из мсвСтроки Цикл
			Для каждого текПоле Из сткЗначенияСтрокиРаботДоРедактирования Цикл
				сткЗначенияСтрокиРаботДоРедактирования.Вставить(текПоле.Ключ, СтрокаРабота[текПоле.Ключ]);
			КонецЦикла;
			ОбработатьСтрокуРаботыНаСервере(СтрокаРабота.НомерСтроки, Элементы.Маршрут.ТекущиеДанные.НомерСтроки ,Истина,,Отказ, мсвИдентификаторыРабот);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РаботыПослеУдаления(Элемент)
	ПересчитатьИтогиПоРейсу();
	//ОбновитьПредставлениеПоказателейЗагрузки();
КонецПроцедуры

&НаКлиенте
Процедура РаботыПриИзменении(Элемент)
	
	Модифицированность			= Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
		текСтрока = Элементы.Работы.ТекущиеДанные;
	Если текСтрока <> Неопределено И упПрохождениеТочкиКлиентСервер.ОсновнаяОперация(текСтрока.Операция) Тогда
		Если Поле.Имя = "РаботыЗадание" Тогда
			ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаОбъекта", Новый Структура("Ключ", текСтрока.Задание), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Поле.Имя = "РаботыГрузовоеМесто" И ЗначениеЗаполнено(текСтрока.ГрузовоеМесто) Тогда 	
			ОткрытьФорму("Справочник.упГрузовыеМеста.ФормаОбъекта", Новый Структура("Ключ", текСтрока.ГрузовоеМесто), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура РаботыЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	мсвЗаданий = Новый Массив;
	Для каждого текЗадание Из упКорректировкаРейса.Задания Цикл
		мсвЗаданий.Добавить(текЗадание.Задание);
	КонецЦикла; 
	мсвЗаданий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвЗаданий);
	сткОтбор     = Новый Структура("Ссылка", мсвЗаданий);
	сткПараметры = Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.РаботыЗадание);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботаЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	текТочка 	= Элементы.Маршрут.ТекущиеДанные;
	текОтбор 	= Параметры.Отбор;
	ЗаполнитьСтруктуруОтбора(текОтбор, текТочка.Адрес);
	текОтбор.Вставить("ОтборПоАдресу");
КонецПроцедуры

&НаКлиенте
Процедура РаботаЗаданиеПриИзменении(Элемент)
	ПересчитатьДанныеРаботыНаСервере(новый Структура("Задание"), Элементы.Работы.ТекущиеДанные.НомерСтроки);
КонецПроцедуры

&НаКлиенте
Процедура РаботаГрузовоеМестоПриИзменении(Элемент)
	
	ПересчитатьДанныеРаботыНаСервере(новый Структура("ГрузовоеМесто"), Элементы.Работы.ТекущиеДанные.НомерСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыКоличествоГрузовПриИзменении(Элемент)
	
	ПересчитатьДанныеРаботыНаСервере(новый Структура("КоличествоГрузов"), Элементы.Работы.ТекущиеДанные.НомерСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботыОперацияПриИзменении(Элемент)
	Элементы.Работы.ТекущиеДанные.ТипОперации = ПолучитьТипОперацииНаСервере(Элементы.Работы.ТекущиеДанные.Операция);
	ПересчитатьДанныеРаботыНаСервере(Новый Структура("Операция"), Элементы.Работы.ТекущиеДанные.НомерСтроки);
КонецПроцедуры

&НаКлиенте
Процедура РаботыВыполненаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Работы.ТекущиеДанные;
	ТекущиеДанные.Отменена = НЕ ТекущиеДанные.Выполнена;
	упПрохождениеТочкиКлиентСервер.ИзменитьПризнакВыполненияРаботы(ТекущиеДанные, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
	УстановитьКнопкиСтатусаРейса();
КонецПроцедуры

&НаКлиенте
Процедура РаботыОтмененаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Работы.ТекущиеДанные;
	ТекущиеДанные.Выполнена = НЕ ТекущиеДанные.Отменена;
	упПрохождениеТочкиКлиентСервер.ИзменитьПризнакВыполненияРаботы(ТекущиеДанные, упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
	УстановитьКнопкиСтатусаРейса();
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы_СтраницаПлановыеРасходы

&НаКлиенте
Процедура ПлановыеРасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		ТекущаяСтрока.Количество 	= 1;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЗаданиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	мсвЗадания = Новый Массив;
	Для каждого СтрокаЗадание Из Объект.Задания Цикл
		Если мсвЗадания.Найти(СтрокаЗадание.Задание) = Неопределено Тогда
  			мсвЗадания.Добавить(СтрокаЗадание.Задание);
  		КонецЕсли;	
	КонецЦикла;
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
	сткПараметры 	= Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Документ.упЗаданиеНаПеревозкуГруза.ФормаВыбора", сткПараметры, Элементы.ПлановыеРасходыЗадание);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЗаданиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	мсвЗадания = Новый Массив;
	Для каждого СтрокаЗадание Из Объект.Задания Цикл
		Если мсвЗадания.Найти(СтрокаЗадание.Задание) = Неопределено Тогда
  			мсвЗадания.Добавить(СтрокаЗадание.Задание);
  		КонецЕсли;	
	КонецЦикла;
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
    ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЗаданиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
		мсвЗадания = Новый Массив;
	Для каждого СтрокаЗадание Из Объект.Задания Цикл
		Если мсвЗадания.Найти(СтрокаЗадание.Задание) = Неопределено Тогда
  			мсвЗадания.Добавить(СтрокаЗадание.Задание);
  		КонецЕсли;	
	КонецЦикла;
	сткОтбор 	= Новый Структура();
	сткОтбор.Вставить("Ссылка", мсвЗадания);
    ПараметрыПолученияДанных.Вставить("Отбор", сткОтбор);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЦенаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		СтруктураСтатьяРасходов = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(СтатьяРасходов, "СтавкаНДС, ПравилаРаспределения");
		ТекущаяСтрока.ПравилоРаспределения = СтруктураСтатьяРасходов.ПравилаРаспределения;
		ТекущаяСтрока.СтавкаНДС = СтруктураСтатьяРасходов.СтавкаНДС;
		ПересчитатьСтроку(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Водитель1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Водитель1НачалоВыбораОписаниеОповещения = Новый ОписаниеОповещения("Водитель1НачалоВыбораЗавершение", ЭтаФорма);
	ВыполнитьВызовФормыВыбораСотрудника(Водитель1НачалоВыбораОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура Водитель2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Водитель2НачалоВыбораОписаниеОповещения = Новый ОписаниеОповещения("Водитель2НачалоВыбораЗавершение", ЭтаФорма);
	ВыполнитьВызовФормыВыбораСотрудника(Водитель2НачалоВыбораОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ЭкспедиторНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭкспедиторНачалоВыбораОписаниеОповещения = Новый ОписаниеОповещения("ЭкспедиторНачалоВыбораЗавершение", ЭтаФорма);
	ВыполнитьВызовФормыВыбораСотрудника(ЭкспедиторНачалоВыбораОписаниеОповещения);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область УкладкуГрузовPacker3D

&НаКлиенте
Процедура ДобавитьРасчетPacker3D(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Добавить расчета укладки грузов'"),,
		НСтр("ru = 'Выполните сохранение данных рейса, перед добавлением нового расчета укладки грузов.'"),БиблиотекаКартинок.Информация32);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаДобавитьРасчетPacker3D", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Добавить в список новый расчет укладки грузов?'; en = 'Do you want to continue?'"), Режим, 0);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьУкладкуГрузов(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОбновитьУкладкуГрузов", ЭтаФорма);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Обновить данные укладки грузов, все не запущенные расчеты будут удалены из списка. Продолжить?'; en = 'Do you want to continue?'"), Режим, 0);	
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПереместитьТочкуВверх(Команда)
	
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	Отказ = Ложь;
	
	ИндексСтроки = упКорректировкаРейса.Маршрут.Индекс(ТекущиеДанные);
	мсвПредшественников = СтрРазделить(ТекущиеДанные.Предшественники, ",", Ложь);
	ЗаменяемаяСтрока = упКорректировкаРейса.Маршрут[ИндексСтроки - 1];
	Если мсвПредшественников.Найти(Строка(ЗаменяемаяСтрока.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ЗаменяемаяСтрока.Адрес, ТекущиеДанные.Адрес);
		Возврат;
	КонецЕсли; 

	ИдентификаторСтроки = СдвинутьТочкуВМаршрутеНаСервере(ТекущиеДанные.НомерСтроки,,Ложь);
	Если НЕ Отказ Тогда
		Элементы.Маршрут.ТекущаяСтрока = ИдентификаторСтроки;
		Модифицированность = Истина;	
	КонецЕсли;
	ПересчитатьПорядокОтгрузки();
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьТочкуВниз(Команда)
	
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	Отказ = Ложь;
		
	ИндексСтроки = упКорректировкаРейса.Маршрут.Индекс(ТекущиеДанные);
	ЗаменяемаяСтрока = упКорректировкаРейса.Маршрут[ИндексСтроки + 1];
	мсвПредшественников = СтрРазделить(ЗаменяемаяСтрока.Предшественники, ",", Ложь);
	Если мсвПредшественников.Найти(Строка(ТекущиеДанные.Идентификатор)) <> Неопределено Тогда
		СообщитьОНарушенииМаршрута(ТекущиеДанные.Адрес, ЗаменяемаяСтрока.Адрес);
		Возврат;
	КонецЕсли; 	
	
	ИдентификаторСтроки = СдвинутьТочкуВМаршрутеНаСервере(ТекущиеДанные.НомерСтроки,Ложь,Ложь);
	Если НЕ Отказ Тогда
		Элементы.Маршрут.ТекущаяСтрока = ИдентификаторСтроки;
		Модифицированность = Истина;	
	КонецЕсли;
	ПересчитатьПорядокОтгрузки();	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТочкуПоЗаданию(Команда)
	
	ДобавитьТочкуПоЗаданиюНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтрольнуюТочку(Команда)
	
	ДобавитьКонтрольнуюТочкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияМаршрута(Команда)
	
	Если Модифицированность Или Объект.Ссылка.Пустая() Тогда
		ТекстВопроса =
			НСтр("ru = 'Документ не записан. Выполнение команды  возможно только после записи данных. Записать документ?'");
		Обработчик = Новый ОписаниеОповещения("РедактированиеМаршрутаПослеОтветаНаВопросЗаписать", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	РедактироватьМаршрут();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПлановыеРасходы(Команда) 
	
	Если ЗначениеЗаполнено(Соглашение) Тогда
		РассчитатьПлановыеРасходыНаСервере();
		РассчитатьИтоговуюСуммуРасходы();
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Соглашение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьРасходыСРасшифровкой(Команда)
	
	Если ЗначениеЗаполнено(Соглашение) Тогда
		мсвПриемников = РассчитатьПлановыеРасходыСРасшифровкойНаСервере();
		РассчитатьИтоговуюСуммуРасходы();
		Для каждого Приемник Из мсвПриемников Цикл
			Приемник.Приемник.Показать("Расшифровка расчетов");	
		КонецЦикла;
	Иначе	
		ТекстОшибки = НСтр("ru='Не задано соглашение с перевозчиком'");	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Объект.Ссылка,
		"Соглашение");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтатусКПланированиюТС(Команда)
	
	ТекстОшибки = "";
	Если УстановитьСтатусКПланированиюТССервер(ТекстОшибки) Тогда 
		ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
		ОповеститьОбИзменении(Объект.Ссылка);
		//ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	Иначе
		//ПоказатьПредупреждение(,ТекстОшибки);
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусКВыполнению(Команда)
	
	ТекстОшибки = "";
	ЭтоОшибкаВыполнения = Истина;
	
	ТребуетсяОформлениеПутевогоЛиста = Ложь;
	
	Если УстановитьСтатусКВыполнениюСервер(ТекстОшибки, ЭтоОшибкаВыполнения, ТребуетсяОформлениеПутевогоЛиста) Тогда 
		ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
		ОповеститьОбИзменении(Объект.Ссылка);
		//ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	Иначе
		
		Если ТребуетсяОформлениеПутевогоЛиста Тогда
			ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросСоздатьПутевойЛист", ЭтаФорма);
			ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Начать исполнение рейса без ввода документа ""Путевой лист"" нельзя! Создать новый документ ""Путевой лист""?'"), РежимДиалогаВопрос.ДаНет);	
		Иначе
			Если ЭтоОшибкаВыполнения Тогда
				ПоказатьПредупреждение(, ТекстОшибки);
			Иначе
				упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтменить(Команда)
	
	Если Статус <> ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен") Тогда 
		ТекстОшибки = "";
		
		сткПроверка = ДокументыПрепятствующиеОтменеРейса(Объект.Ссылка);
		
		Отказ = Ложь;
		
		Если сткПроверка.ЕстьДокументыОтменаВручную Тогда
			Отказ = Истина;
			ТекстОшибки = Нстр("ru = 'По рейсу есть проведенные документы, введенные на основании(корректировки, доверенности, путевой лист или подтверждения заявок на транспортное средство). Отмена рейса запрещена.'");
		КонецЕсли;
			
		Если Не Отказ Тогда
			Если сткПроверка.ЕстьДокументыОтменаАвтоматом Тогда	
				мсвРейсы = сткПроверка.Рейсы;
				Если мсвРейсы.Количество() > 0  Тогда
					ДополнительныеПараметры = Новый Структура();
					ДополнительныеПараметры.Вставить("ДокументыОтменаАвтоматомЗаявкиНаТС",  мсвРейсы[0].ДокументыОтменаАвтоматомЗаявкиНаТС); 
					ДополнительныеПараметры.Вставить("ДокументыОтменаАвтоматомПредложения", мсвРейсы[0].ДокументыОтменаАвтоматомПредложения); 
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОтменеЗаявок",ЭтаФорма, ДополнительныеПараметры);
					ТекстВопроса = Нстр("ru = 'По рейсу есть проведенные документы введенные на основании (заявки на ТС, предложения перевозчикам). Отменить эти документы?'");
					ПоказатьВопрос(ОписаниеОповещенияОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				КонецЕсли;
			Иначе
				УстановитьСтатусОтменен(ТекстОшибки);
				ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
			КонецЕсли;
		Иначе
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Рейс выполнен, отмена невозможна'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусВыполнен(Команда)
	ТекстОшибки = "";
	ЭтоОшибкаВыполнения = Истина;
	Если УстановитьСтатусВыполненСервер(ТекстОшибки, ЭтоОшибкаВыполнения) Тогда 
		ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
		ОповеститьОбИзменении(Объект.Ссылка);
		//ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	Иначе
		Если ЭтоОшибкаВыполнения Тогда
			ПоказатьПредупреждение(, ТекстОшибки);
		Иначе
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусВыполненЧастично(Команда)
	ТекстОшибки = "";
	ЭтоОшибкаВыполнения = Истина;
	Если УстановитьСтатусВыполненЧастичноСервер(ТекстОшибки, ЭтоОшибкаВыполнения) Тогда 
		ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
		ОповеститьОбИзменении(Объект.Ссылка);
		//ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	Иначе
		Если ЭтоОшибкаВыполнения Тогда
			ПоказатьПредупреждение(, ТекстОшибки);
		Иначе
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтатусЗавершен(Команда)
	ТекстОшибки = "";
	Если УстановитьСтатусЗавершенСервер(ТекстОшибки) Тогда 
		ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
		ОповеститьОбИзменении(Объект.Ссылка);
		//ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	Иначе
		//ПоказатьПредупреждение(,ТекстОшибки);
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлановыеРасходы(Команда)
	ЭтоЗаполнениеРасходов	= Истина;
	ОповещениеЗаполнения = Новый ОписаниеОповещения("ЗаполнитьПлановыеРасходыКлиент",ЭтаФорма);
	упСервисныеФункцииКлиент.ВопросОСохраненииИзмененныхДанных(ЭтаФорма, ОповещениеЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеМаршрута(Команда)
	
	УстановитьПредставлениеМаршурта();
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура ОбновитьПлановыйРасходПоРейсуКоманда(Команда)
	ОбновитьПлановыйРасходПоРейсу();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредшественников(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Маршрут", 			упКорректировкаРейса.Маршрут);
	сткПараметры.Вставить("ИдентификаторТочки", Элементы.Маршрут.ТекущиеДанные.Идентификатор);
	ОткрытьФорму("Обработка.упКорректировкаРейса.Форма.Предшественники", сткПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	ОтметитьВсеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	СнятьВсеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РазделитьРаботы(Команда)
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("Идентификатор", 			Элементы.Маршрут.ТекущиеДанные.Идентификатор);
	сткПараметры.Вставить("Работы", 				упКорректировкаРейса.Работы);
	
	ОбработчикЗакрытия = Новый ОписаниеОповещения("ОбработатьРазделениеТочки", ЭтаФорма);
	
	ОткрытьФорму("Обработка.упКорректировкаРейса.Форма.РазделениеРаботВТочке", сткПараметры, ЭтаФорма, УникальныйИдентификатор, , , ОбработчикЗакрытия);

КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
// 
//  Если доступа к реквизиту нет, то в случае "ВыбратьРазрешенные = Ложь" возникнет исключение прав доступа,
//  а в случае "ВыбратьРазрешенные = Истина" вернется значение Неопределено.
//
//  Если необходимо зачитать реквизит независимо от прав текущего пользователя,
//  то следует использовать предварительный переход в привилегированный режим.
//
// Функция не предназначена для получения значений реквизитов пустых ссылок.
// 
// Параметры:
//  Ссылка    - ЛюбаяСсылка - объект, значения реквизитов которого необходимо получить.
//  ИмяРеквизита - Строка - имя получаемого реквизита.
//  ВыбратьРазрешенные - Булево - если Истина, то запрос к объекту выполняется с учетом прав пользователя, и в случае,
//                                если выборка будет пустая, то вернется значение Неопределено.
// 
// Возвращаемое значение:
//  Произвольный - зависит от типа значения прочитанного реквизита.
//
Функция ПолучитьЗначениеРеквизита(Ссылка, ИмяРеквизита)

	Возврат упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);

КонецФункции // ПолучитьЗначениеРеквизита()

&НаСервере
Процедура ОбработатьСтрокуЗаданияНаСервере(НомерСтроки, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиЗадания(НомерСтроки, НоваяСтрока,Удалить, ОтменаРедактирования, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуТочкаМаршрутаНаСервере(НомерСтроки, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено, ПересчитатьИтогиПоРейсу = Ложь)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Если Ложь Тогда // Для контекстной подсказки
	    текКорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
	КонецЕсли;
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиМаршрута(НомерСтроки, НоваяСтрока, Удалить, ОтменаРедактирования, Отказ, мсвВыделенныеСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
	Если ПересчитатьИтогиПоРейсу Тогда
		ПересчитатьИтогиПоРейсу();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокуРаботыНаСервере(НомерСтроки, НомерСтрокиТочка, НоваяСтрока = Ложь, Удалить = Ложь, ОтменаРедактирования = Ложь, Отказ, мсвВыделенныеСтроки = Неопределено)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Если Ложь Тогда // Для контекстной подсказки
	    текКорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
	КонецЕсли;
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеСтрокиРаботы(НомерСтроки, НомерСтрокиТочка, НоваяСтрока, Удалить, ОтменаРедактирования, Отказ, мсвВыделенныеСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросПриИзмененииВидаПеревозки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		ОчиститьЗадания();
	КонецЕсли;
КонецПроцедуры

#Область ОбработкаТаблицыФормы_ЗАДАНИЯ

&НаСервере
Функция СдвинутьТочкуВМаршрутеНаСервере(СтрокаНомер, СдвигВверх = Истина, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	ИдентификаторСтроки = текКорректировкаМаршрутаОбъект.ОбработатьСдвигТочкиМаршрута(СтрокаНомер, СдвигВверх, Ложь, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	Возврат ИдентификаторСтроки;
КонецФункции

&НаСервере
Процедура ОбработатьУдалениеТочкиНаСервере()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьУдалениеТочки();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
	ПересчитатьИтогиПоРейсу();
КонецПроцедуры

&НаСервере
// Процедура дозаполняет данные о заданиях
//
Процедура ЗаполнитьВспомогательныеПоляЗаданийНаСервере(НомерСтроки = Неопределено)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Если Ложь Тогда // Для контекстной подсказки
	    текКорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
	КонецЕсли;
	текКорректировкаМаршрутаОбъект.ЗаполнитьВспомогательныеПоляЗаданийНаСервере(НомерСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры // ЗаполнитьВспомогательныеПоляЗаданийНаСервере()

&НаСервере
Процедура ОчиститьЗадания()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОчиститьЗаданияВРейсе();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаТаблицыФормы_МАРШРУТЫ

&НаСервере
Процедура ДобавитьТочкуПоЗаданиюНаСервере()
	Элементы.Маршрут.ТекущаяСтрока = ДобавитьТочкуВМаршрут(Перечисления.упТипыТочекМаршрута.ТочкаПоЗаданию);	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКонтрольнуюТочкуНаСервере()
	Элементы.Маршрут.ТекущаяСтрока = ДобавитьТочкуВМаршрут(Перечисления.упТипыТочекМаршрута.КонтрольнаяТочка);
КонецПроцедуры

&НаСервере
Функция ДобавитьТочкуВМаршрут(ТипТочки = Неопределено)
	
	НоваяСтрока = упКорректировкаРейса.Маршрут.Добавить();
	НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
	НоваяСтрока.СтрокаНомер = НоваяСтрока.НомерСтроки;
	НоваяСтрока.СтрокаДокумента	= Истина;
	Если ЗначениеЗаполнено(ТипТочки) Тогда
		НоваяСтрока.ТипТочки = ТипТочки;	
		НоваяСтрока.ИндексКартинки = Перечисления.упТипыТочекМаршрута.Индекс(ТипТочки);
	КонецЕсли;
	
	Возврат НоваяСтрока.ПолучитьИдентификатор();
	
КонецФункции

&НаСервере
Процедура ДобавитьТочкуВМаршрутНаСервере(СтрокаНомер, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьДобавлениеТочкиВМаршрут(СтрокаНомер,,Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРаботуНаСервере(НомерСтроки, ИдентификаторТочки, Отказ)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьДобавлениеРаботы(НомерСтроки, ИдентификаторТочки, Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

//&НаСервере
//Процедура СпланироватьЗадачиПоМаршруту(НомерСтроки)
//	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
//	текКорректировкаМаршрутаОбъект.СпланироватьЗадачиПоМаршруту(НомерСтроки-1, Ложь);
//	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
//КонецПроцедуры

&НаСервере
Процедура ПересчитатьДанныеРаботыНаСервере(сткИзмененныеПоля, НомерСтроки)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ПересчитатьДанныеРаботы(сткИзмененныеПоля, ,НомерСтроки);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТипОперацииНаСервере(Операция)
	Возврат Перечисления.упТипыОпераций.Индекс(Операция);
КонецФункции

#КонецОбласти

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьЗаголовкиПоказателейЗагрузки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	Если УчитыватьМассу Тогда
		прМасса = НСтр(СтрШаблон("ru = '%1 (%2)'", ЗаголовокЗаданиеМасса, сткПредставления.Масса));
		Элементы.ЗаданияМасса.Заголовок = прМасса;
		МассаПредставление = сткПредставления.Масса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		прОбъем = НСтр(СтрШаблон("ru = '%1 (%2)'", ЗаголовокЗаданиеОбъем, сткПредставления.Объем));
		Элементы.ЗаданияОбъем.Заголовок = прОбъем;
		ОбъемПредставление = сткПредставления.Объем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		прКоличествоМест = НСтр(СтрШаблон("ru = '%1 (%2)'", ЗаголовокЗаданиеКоличествоМест, сткПредставления.КоличествоМест));
		Элементы.ЗаданияКоличествоМест.Заголовок = прКоличествоМест;
		КоличествоМестПредставление = сткПредставления.КоличествоМест;
	КонецЕсли;	
	Если ВедетсяВалютныйУчет Тогда
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ОценочнаяСтоимость, ВалютаУпрУчета);
		Если ИспользуютсяУслугиИнкассации Тогда
			СуммаИнкассацииПредставление	= упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаИнкассации, ВалютаУпрУчета);
		КонецЕсли;
	Иначе	
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ОценочнаяСтоимость);
		Если ИспользуютсяУслугиИнкассации Тогда
			СуммаИнкассацииПредставление	= упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаИнкассации);
		КонецЕсли;	
	КонецЕсли;  
	
	Если ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		Элементы.РаботыМасса.Видимость = УчитыватьМассу;
		Если УчитыватьМассу Тогда
			Элементы.РаботыМасса.Заголовок = прМасса;
		КонецЕсли; 
		Элементы.РаботыОбъем.Видимость = УчитыватьОбъем;
		Если УчитыватьОбъем Тогда
			Элементы.РаботыОбъем.Заголовок = прОбъем;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры // УстановитьЗаголовкиПоказателейЗагрузки()

&НаСервере
Процедура УстановитьСтатусWMS()
	
	СтатусWMS = Документы.упРейс.ПолучитьСтатусWMS(Объект.Ссылка); 
	
КонецПроцедуры

&НаСервере
Процедура СоглашениеПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Соглашение) И ПеревозкаТранспортнойКомпанией <> упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Соглашение, "ПеревозкаТранспортнойКомпанией") Тогда
		ПеревозкаТранспортнойКомпанией = Не ПеревозкаТранспортнойКомпанией;
		Объект.ПеревозкаТранспортнойКомпанией = ПеревозкаТранспортнойКомпанией;
		ОбновитьДанныеОПеревозке();
		УстановитьКнопкиСтатусаРейса();
	КонецЕсли;
	
КонецПроцедуры // СоглашениеПриИзмененииНаСервере()	

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		
		Если ВедетсяУчетДокументов Тогда
			текОтказ = Ложь;
			упРейс.ПроверитьНаличиеНеобходимыхДокуметовПоТС(ТранспортноеСредство, текОтказ);	
		КонецЕсли;
		
		сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТранспортноеСредство, "Партнер, ТипТС, ОсновнойВодитель, ВторойОсновнойВодитель, ОсновнойЭкспедитор");
		ТипТС        = сткРеквизиты.ТипТС;
		Водитель1    = сткРеквизиты.ОсновнойВодитель;
		Водитель2    = сткРеквизиты.ВторойОсновнойВодитель;
		Экспедитор   = сткРеквизиты.ОсновнойЭкспедитор;
		
		Если сткРеквизиты.Партнер <> Перевозчик Тогда
			Перевозчик = сткРеквизиты.Партнер;
			Соглашение = ПолучитьСоглашениеПоУмолчанию();
			КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Перевозчик);
			СоглашениеПриИзмененииНаСервере();
		Иначе
			Перевозчик = сткРеквизиты.Партнер;
		КонецЕсли; 
		
		Если ПрисваиватьРейсуПодразделениеТранспортногоСредства Тогда
			Объект.Подразделение = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(,ТранспортноеСредство);
		КонецЕсли;		
		
		Объект.НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,ТранспортноеСредство);
		
	Иначе
		текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		текКорректировкаМаршрутаОбъект.УдалитьТочкиОтправленияЗавершения();
		ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
	КонецЕсли;
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТранспортноеСредство, упКорректировкаРейса.ПлановоеНачалоВыполнения);
	ОбновитьДанныеОПеревозке();
	
КонецПроцедуры

//&НаКлиенте
&НаСервере
Процедура ОбновитьПредставлениеПоказателейЗагрузки()
	
	РасстояниеПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(Расстояние);
	НулевойПробегПредставление	=  упСервисныеФункцииКлиентСервер.СформироватьПредставлениеРасстояния(НулевойПробег);
	ВремяПредставление      = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(Время*3600);
	ВремяВПутиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяВПути*3600);
	ВремяОтдыхаВПутиПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеВремени(ВремяОтдыхаВПути*3600);
	
	Если УчитыватьМассу Тогда
		МассаПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Масса, МассаПредставление));
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ОбъемПредставление = НСтр(СтрШаблон("ru = '%1 %2'", Объем, ОбъемПредставление));
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		КоличествоМестПредставление = НСтр(СтрШаблон("ru = '%1 %2'", КоличествоМест, КоличествоМестПредставление));
	КонецЕсли;
	Если УчитыватьКоличествоЗагрузочныхМетров Тогда
		КоличествоЗагрузочныхМетровПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоЗагрузочныхМетров));
	КонецЕсли;
	
	КоличествоГрузовыхМестПредставление = НСтр(СтрШаблон("ru = '%1'", КоличествоГрузовыхМест));
	
	Если ВедетсяВалютныйУчет Тогда
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ОценочнаяСтоимость, 	ВалютаУпрУчета);
		Если ИспользуютсяУслугиИнкассации Тогда
			СуммаИнкассацииПредставление	= упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаИнкассации, 	ВалютаУпрУчета);
		КонецЕсли;
	Иначе	
		ОценочнаяСтоимостьПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(ОценочнаяСтоимость);
		Если ИспользуютсяУслугиИнкассации Тогда
			СуммаИнкассацииПредставление	= упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаИнкассации);
		КонецЕсли;	

	КонецЕсли;  
	
КонецПроцедуры // ОбновитьПредставлениеПоказателейЗагрузки()

&НаКлиенте
Процедура ОповеститьОбИзмененииСтатусаРодительскиеДокументы()
	Для каждого текЗаявка Из ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус Цикл
		Оповестить("СменаСтатуса",,текЗаявка.Значение);	
	КонецЦикла;
	ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус.Очистить();
	
	Для каждого текЗаданием Из ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус Цикл
		Оповестить("СменаСтатуса",,текЗаданием.Значение);
	КонецЦикла;
	ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус.Очистить();
	
	Оповестить("ЗаписьДокументаупРейс", Новый Структура("Ссылка, Перевозчик, ТипТС, ТранспортноеСредство", Параметры.Ключ, Перевозчик, ТипТС, ТранспортноеСредство));
	ЗаполнитьВспомогательныеПоляЗаданийНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтруктуруОтбора(Отбор, Адрес = Неопределено)
	
	Если НЕ Адрес = Неопределено Тогда
		Отбор.Вставить("Адрес", Адрес);
	ИначеЕсли Объект.ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда	
		АдресОтправления = Неопределено;
		АдресПолучения = Неопределено;
		
		Для каждого ТочкаМаршрута Из упКорректировкаРейса.Маршрут Цикл
			
			Если ТочкаМаршрута.Отменена Тогда
				Продолжить;
			Иначе
				Если АдресОтправления = Неопределено Тогда
					АдресОтправления = ТочкаМаршрута.Адрес;
					
				ИначеЕсли АдресПолучения = Неопределено Тогда	
					АдресПолучения =  ТочкаМаршрута.Адрес;
					Прервать;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Истина
			И АдресОтправления <> Неопределено
			И АдресПолучения <> Неопределено
		Тогда
			Отбор.Вставить("АдресОтправления", АдресОтправления);
			Отбор.Вставить("АдресПолучения", АдресПолучения);
		КонецЕсли;
		
	КонецЕсли;
	Отбор.Вставить("Статус", ПредопределенноеЗначение("Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию")); 
	Отбор.Вставить("Организация", Объект.Организация); 
	Отбор.Вставить("ВидПеревозки", Объект.ВидПеревозки); 
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьПлановыеРасходыНаСервере()
	
	ВходныеПараметры = Новый Структура();
	
	упТарификацияВызовСервера.Рассчитать(Объект, ВходныеПараметры);
	
	ОбновитьПлановыйРасходПоРейсу();

	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОПеревозке()
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыРейса.КВыполнению 
		Или Статус = Перечисления.упСтатусыРейса.Выполняется 
		Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
		Или Статус = Перечисления.упСтатусыРейса.Выполнен 
		Или Статус = Перечисления.упСтатусыРейса.Завершен 
		Или ЕстьПодтверждениеЗаявкиНаТС 
	Тогда
		
		Элементы.ВидПеревозки.ТолькоПросмотр = Истина;
		Элементы.Перевозчик.ТолькоПросмотр = Истина;
		Элементы.КонтрагентПеревозчика.ТолькоПросмотр = Истина;
		Элементы.Соглашение.ТолькоПросмотр = Истина;
		Элементы.ТипТС.ТолькоПросмотр = Истина;  
		Элементы.ТранспортноеСредство.ТолькоПросмотр  = Истина;
		Элементы.Водитель1.ТолькоПросмотр = Истина;
		Элементы.Водитель2.ТолькоПросмотр = Истина;
		Элементы.Экспедитор.ТолькоПросмотр = Истина;
		
		Элементы.ВладелецКонтейнера.ТолькоПросмотр = Истина;	
		Элементы.КонтрагентВладельцаКонтейнера.ТолькоПросмотр = Истина;	
		Элементы.СоглашениеКонтрагентаКонтейнера.ТолькоПросмотр = Истина;	
		Элементы.ТипКонтейнера.ТолькоПросмотр = Истина;	
		Элементы.Контейнер.ТолькоПросмотр = Истина;	


	Иначе
			
		Элементы.ВидПеревозки.ТолькоПросмотр = Ложь;
		
		ОформляетсяЗаявкаНаТС = ВыборТС = Перечисления.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС;
		ИспользоватьВиртуальныеТранспортныеСредства = ПолучитьФункциональнуюОпцию("упИспользоватьВиртуальныеТранспортныеСредства");
		ТолькоПросмотрПеревозчика = (Ложь
								Или Не ИспользоватьВиртуальныеТранспортныеСредства 
								Или ЗначениеЗаполнено(ТранспортноеСредство) 
								Или ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ОформляетсяТендер);
		ТолькоПросмотрСоглашения = (Ложь
								Или ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ОформляетсяТендер
								Или Статус = Перечисления.упСтатусыРейса.КВыполнению);
		
		Элементы.Перевозчик.ТолькоПросмотр = ТолькоПросмотрПеревозчика;
		Элементы.Соглашение.ТолькоПросмотр = ТолькоПросмотрСоглашения;
		Элементы.КонтрагентПеревозчика.ТолькоПросмотр = ТолькоПросмотрПеревозчика;
		Элементы.ТипТС.ТолькоПросмотр = (Не ИспользоватьВиртуальныеТранспортныеСредства Или ЗначениеЗаполнено(ТранспортноеСредство));  
         		
		Элементы.ТипТС.Доступность = Не ПеревозкаТранспортнойКомпанией;
		Элементы.ТранспортноеСредство.Доступность = Не ПеревозкаТранспортнойКомпанией;	
		
		Если ПеревозкаТранспортнойКомпанией Тогда
			Элементы.ТипТС.ПодсказкаВвода = НСтр("ru = 'определяется перевозчиком'");
			Элементы.ТранспортноеСредство.ПодсказкаВвода = НСтр("ru = 'определяется перевозчиком'");
		Иначе	
			Элементы.ТипТС.ПодсказкаВвода = Неопределено;
			Элементы.ТранспортноеСредство.ПодсказкаВвода = Неопределено;
		КонецЕсли; 
		
		Если ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ОформляетсяТендер Тогда
			Элементы.Перевозчик.ПодсказкаВвода = НСтр("ru = 'требуется оформление заявки на ТС'");
			Элементы.КонтрагентПеревозчика.ПодсказкаВвода = НСтр("ru = 'требуется оформление заявки на ТС'");
			Элементы.Соглашение.ПодсказкаВвода = НСтр("ru = 'требуется оформление заявки на ТС'");
			Элементы.ТранспортноеСредство.ПодсказкаВвода  = НСтр("ru = 'требуется оформление заявки на ТС'");
		ИначеЕсли ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально Тогда	
			Если ОформляетсяЗаявкаНаТС  Тогда
				Элементы.ТранспортноеСредство.Доступность = Ложь;	
				Элементы.ТранспортноеСредство.ПодсказкаВвода  = НСтр("ru = 'требуется оформление заявки на ТС'");
			КонецЕсли;

			Элементы.Перевозчик.ПодсказкаВвода = Неопределено;
			Элементы.КонтрагентПеревозчика.ПодсказкаВвода = Неопределено;
			Элементы.Соглашение.ПодсказкаВвода = Неопределено;
		Иначе	
			Элементы.Перевозчик.ПодсказкаВвода = Неопределено;
			Элементы.КонтрагентПеревозчика.ПодсказкаВвода = Неопределено;
			Элементы.Соглашение.ПодсказкаВвода = Неопределено;
		КонецЕсли; 
	КонецЕсли;
	

КонецПроцедуры // ОбновитьДанныеОПеревозке()

&НаСервере
Процедура ОбновитьДанныеВидПеревозки()
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПеревозки, "СхемаВыбораПеревозчика, 
																	|ВидЗоныДляФормированияПредставленияРейса, 
																	|ВыборПеревозчика, 
																	|ВыборТС, 
																	|ПараметрыВводаИнформацииОГрузовыхМестах, 
																	|ВариантФормированияМаршрута,
																	|ИспользуетсяСкладскойУчетНаХабах,
																	|ПрисваиватьРейсуПодразделениеТранспортногоСредства,
																	|СпособПрохожденияТочекМаршрута,
																	|РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу,
																	|ИспользуютсяМультимодальныеПеревозки,
																	|ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера");
	ПараметрыВводаИнформацииОГрузовыхМестах = сткРеквизиты.ПараметрыВводаИнформацииОГрузовыхМестах;
	ФормироватьПоГрузовымМестам = Ложь
	                              Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку 
				               Или ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса;
	ВидЗоныДляФормированияПредставленияРейса = сткРеквизиты.ВидЗоныДляФормированияПредставленияРейса;
	ВыборПеревозчика = сткРеквизиты.ВыборПеревозчика;
	ВыборТС = сткРеквизиты.ВыборТС;
	ВариантФормированияМаршрута = сткРеквизиты.ВариантФормированияМаршрута;
	СпособПрохожденияТочекМаршрута = сткРеквизиты.СпособПрохожденияТочекМаршрута;
	ВидПеревозкиИспользуетсяСкладскойУчетНаХабах = сткРеквизиты.ИспользуетсяСкладскойУчетНаХабах;
	ИспользуетсяСкладскойУчетНаХабахТекущий = Истина
	                                          И ИспользуетсяСкладскойУчетНаХабах
									  И ВидПеревозкиИспользуетсяСкладскойУчетНаХабах
									  И ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится;
	ИспользуютсяМультимодальныеПеревозки = сткРеквизиты.ИспользуютсяМультимодальныеПеревозки;								  
	УправлятьПроцессомПодбораПеревозчика = ЗначениеЗаполнено(сткРеквизиты.СхемаВыбораПеревозчика);
	ПрисваиватьРейсуПодразделениеТранспортногоСредства = ?(ИспользуетсяУчетПодразделений, сткРеквизиты.ПрисваиватьРейсуПодразделениеТранспортногоСредства, Ложь);
	РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу = Истина 
	                                                 И сткРеквизиты.РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу <> Неопределено
	                                                 И сткРеквизиты.РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу;
	ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = сткРеквизиты.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера;
	
	Элементы.ЗаданияГрузовоеМесто.Видимость = ФормироватьПоГрузовымМестам;
	Элементы.ЗаданияТара.Видимость = ФормироватьПоГрузовымМестам;
	Элементы.ЗаданияКоличествоГрузов.Видимость = ФормироватьПоГрузовымМестам;
	Элементы.РаботыГрузовоеМесто.Видимость = (ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится);
	Элементы.РаботыКоличествоГрузов.Видимость = (ПараметрыВводаИнформацииОГрузовыхМестах <> Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится);	
	Элементы.РаботыСкладскаяЯчейка.Видимость = ИспользуетсяСкладскойУчетНаХабахТекущий;
	Элементы.МаршрутОтложена.Видимость = РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу;
	Элементы.РаботыОтложена.Видимость = РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу; 
	Если  СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
		Элементы.МаршрутПройдена.Видимость = Истина;
		Элементы.МаршрутОтменена.Видимость = Истина;
		Элементы.РаботыВыполнена.Видимость = Истина;
		Элементы.РаботыОтменена.Видимость = Истина;
		Элементы.МаршрутОтметитьВсе.Видимость = Истина;
		Элементы.МаршрутСнятьВсе.Видимость = Истина;
	Иначе
		Элементы.МаршрутПройдена.Видимость = Ложь;
		Элементы.МаршрутОтменена.Видимость = Ложь;
		Элементы.РаботыВыполнена.Видимость = Ложь;
		Элементы.РаботыОтменена.Видимость = Ложь;	
		Элементы.МаршрутОтметитьВсе.Видимость = Ложь;
		Элементы.МаршрутСнятьВсе.Видимость = Ложь;
	КонецЕсли;
		
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	сткРеквизитыЗаполнения = Новый Структура();
	сткРеквизитыЗаполнения.Вставить("ВидЗоныДляФормированияПредставленияРейса", ВидЗоныДляФормированияПредставленияРейса);
	сткРеквизитыЗаполнения.Вставить("ВидПеревозки", Объект.ВидПеревозки);
	сткРеквизитыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками);
	сткРеквизитыЗаполнения.Вставить("ФормироватьПоГрузовымМестам", ФормироватьПоГрузовымМестам);
	сткРеквизитыЗаполнения.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах", ПараметрыВводаИнформацииОГрузовыхМестах);
	сткРеквизитыЗаполнения.Вставить("ВариантФормированияМаршрута", ВариантФормированияМаршрута);
	сткРеквизитыЗаполнения.Вставить("ИспользуетсяСкладскойУчетНаХабах", ИспользуетсяСкладскойУчетНаХабахТекущий);
	сткРеквизитыЗаполнения.Вставить("СпособПрохожденияТочекМаршрута", СпособПрохожденияТочекМаршрута);
	текКорректировкаМаршрутаОбъект.ОбновитьДанныеКорректировки(сткРеквизитыЗаполнения);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеВидТранспортнойУслуги()
	
	Если Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
		Элементы.МаршрутДобавитьКонтрольнуюТочку.Видимость = Ложь;
		Элементы.МаршрутОткрытьФормуРедактированияМаршрута.Видимость = Ложь;
		Элементы.ГруппаПеревозчик.Видимость = Ложь;
		Элементы.ГруппаПараметрыПеревозки.Видимость = Ложь;
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.ГруппаВладелецКонтейнера;
	Иначе	
		Элементы.СтраницыПеревозчик.ТекущаяСтраница = Элементы.ГруппаПеревозчикРейс;
	КонецЕсли;
	
	Если РежимФормированияКонтейнеров Тогда
		Элементы.ВидПеревозки.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	Иначе
		Элементы.ВидПеревозки.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.Авто;
	КонецЕсли;
			
КонецПроцедуры

&НаСервере 
Процедура упОбновитьВидимостьМаршрутаРабот(Статус)

	Если Статус = Перечисления.упСтатусыРейса.Новый 
		ИЛИ Статус = Перечисления.упСтатусыРейса.КПланированиюТС
		ИЛИ Статус = Перечисления.упСтатусыРейса.НазначеноТС
		ИЛИ Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда
		
		Если  СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе 
				И Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда
			Элементы.МаршрутПрибытиеФакт.Видимость 	= Истина;
			Элементы.МаршрутУбытиеФакт.Видимость 	= Истина;
	    Иначе	
			Элементы.МаршрутПрибытиеФакт.Видимость = Ложь;
			Элементы.МаршрутУбытиеФакт.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.МаршрутПрибытиеФакт.Видимость = Истина;
		Элементы.МаршрутУбытиеФакт.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры	
	
&НаСервере
Процедура ПроверитьСоответствиеТранспортаВидуПеревозки(ВыбранныйВидПеревозки)
	
	СписокТранспорта = Новый ТаблицаЗначений;
	СписокТранспорта.Колонки.Добавить("Перевозчик"           , Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
	СписокТранспорта.Колонки.Добавить("ТипТС"                , Новый ОписаниеТипов("СправочникСсылка.упТипыТС"));
	СписокТранспорта.Колонки.Добавить("ТранспортноеСредство" , Новый ОписаниеТипов("СправочникСсылка.упТранспортныеСредства"));
	СписокТранспорта.Колонки.Добавить("Количество"           , Новый ОписаниеТипов("Число"));
	СписокТранспорта.Колонки.Добавить("КоличествоЦиклов"     , Новый ОписаниеТипов("Число"));
	СписокТранспорта.Колонки.Добавить("ПланироватьЦиклически", Новый ОписаниеТипов("Булево"));
	НоваяСтрока = СписокТранспорта.Добавить();
	НоваяСтрока.Перевозчик            = Перевозчик;
	НоваяСтрока.ТипТС                 = ТипТС;
	НоваяСтрока.ТранспортноеСредство  = ТранспортноеСредство;
	НоваяСтрока.Количество            = 1;
	НоваяСтрока.КоличествоЦиклов      = 1;
	НоваяСтрока.ПланироватьЦиклически = Ложь;

	Запрос = Новый Запрос;
	Запрос.Текст = Справочники.упВидыПеревозок.ПолучитьТекстЗапросаСоответствиеТранспортаВидуПеревозки();
	Запрос.УстановитьПараметр("ВидПеревозки", ВыбранныйВидПеревозки);
	Запрос.УстановитьПараметр("ИспользоватьВиртуальныеТранспортныеСредства", ПолучитьФункциональнуюОпцию("упИспользоватьВиртуальныеТранспортныеСредства"));
	Запрос.УстановитьПараметр("ТранспортныеСредства", СписокТранспорта);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ЭтаФорма.Перевозчик            = Неопределено;
		ЭтаФорма.КонтрагентПеревозчика = Неопределено;
		ЭтаФорма.Соглашение            = Неопределено;
		ЭтаФорма.ТипТС                 = Неопределено;
		ЭтаФорма.ТранспортноеСредство = Неопределено;
		
		ТекстСообщения = НСтр("ru = 'Данные о перевозчике и транспорте изменены из-за несоответствия настройкам вида перевозки.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоответствиеПеревозчикаВидуПеревозки(ВыбранныйВидПеревозки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = Справочники.упВидыПеревозок.ПолучитьТекстЗапросаСоответствиеПеревозчикаВидуПеревозки();
	Запрос.УстановитьПараметр("ВидПеревозки", ВыбранныйВидПеревозки);
	Запрос.УстановитьПараметр("Организация" , Объект.Организация);
	Запрос.УстановитьПараметр("Перевозчик"  , Перевозчик);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ЭтаФорма.Перевозчик            = Неопределено;
		ЭтаФорма.КонтрагентПеревозчика = Неопределено;
		ЭтаФорма.Соглашение            = Неопределено;
		ЭтаФорма.ТипТС                 = Неопределено;
		ЭтаФорма.ТранспортноеСредство  = Неопределено;
		
		ТекстСообщения = НСтр("ru = 'Данные о перевозчике и транспорте изменены из-за несоответствия настройкам вида перевозки.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
	
	ЭтоСеансВнешнегоПользователя = ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя();
	Если ЭтоСеансВнешнегоПользователя Тогда
		мсвСвойств = Новый Массив;
		мсвСвойств.Добавить(Новый Структура("Ключ, Значение", "ТолькоПросмотр", Истина));
		мсвСвойств.Добавить(Новый Структура("Ключ, Значение", "Доступность", Ложь));
		
		Элементы.ГруппаГлавное.Видимость = Ложь;
		Элементы.ГруппаЗадания.Видимость = Ложь;
		Элементы.ГруппаПлановыеРасходы.Видимость = Ложь;
		Элементы.ГруппаСостояниеВзаиморасчетов.Видимость = Ложь;
		Элементы.ГруппаУкладкаГрузов.Видимость = Ложь;
		Элементы.ГруппаДанныеТахографа.Видимость = Ложь;
		Элементы.ГруппаДополнительно.Видимость = Ложь;
		упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаМаршрут, мсвСвойств);
		упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаШапка, мсвСвойств);
	    Возврат;
	КонецЕсли; 
	
	Если Ложь
		Или Статус = Перечисления.упСтатусыРейса.Выполнен
		Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
		Или Статус = Перечисления.упСтатусыРейса.Выполняется
		Или Статус = Перечисления.упСтатусыРейса.КВыполнению
		Или Статус = Перечисления.упСтатусыРейса.Завершен
		Или Статус = Перечисления.упСтатусыРейса.Отменен
		Или ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками")
	Тогда 
		Если Ложь
			Или Статус = Перечисления.упСтатусыРейса.Завершен
			Или Статус = Перечисления.упСтатусыРейса.Отменен
			Или ПолучитьФункциональнуюОпцию("упНеИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") 
		Тогда
		    ТолькоПросмотр = Истина;
		КонецЕсли; 
		
		мсвСвойств = Новый Массив;
		мсвСвойств.Добавить(Новый Структура("Ключ, Значение", "ТолькоПросмотр", Истина));
		мсвСвойств.Добавить(Новый Структура("Ключ, Значение", "Доступность", Ложь));
		
		мсвИсключений = Новый Массив;
		мсвИсключенийБезПодчинения = Новый Массив;
		мсвИсключений.Добавить("ГруппаСтатус"); 
		
		Если Истина
			И Статус = Перечисления.упСтатусыРейса.КВыполнению 
			И СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе")
		Тогда
			мсвИсключенийБезПодчинения.Добавить("ГруппаШапка");
			мсвИсключенийБезПодчинения.Добавить("ГруппаШапкаЛевая");
			мсвИсключенийБезПодчинения.Добавить("ГруппаВремя");
			мсвИсключенийБезПодчинения.Добавить("ГруппаГлавноеЛевая");
		Иначе
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаЗадания, мсвСвойств);
		КонецЕсли;
		
		упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаШапка, мсвСвойств, мсвИсключений, мсвИсключенийБезПодчинения);
		
		мсвИсключений = Новый Массив;
		Если (Ложь
			Или Статус = Перечисления.упСтатусыРейса.КВыполнению
			Или Статус = Перечисления.упСтатусыРейса.Выполняется) 
		Тогда
			мсвИсключений.Добавить("МаршрутОткрытьФормуРедактированияМаршрута"); 	
			мсвИсключений.Добавить("МаршрутПодобратьИДобавитьКонтрольныеТочки"); 	
		КонецЕсли;
		
		мсвИсключенийСПодчинением = Новый Массив;
		Если СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ОтдельнымДокументом") Тогда
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаМаршрут, мсвСвойств, мсвИсключений);	
		Иначе	
			Если Ложь
				Или Статус = Перечисления.упСтатусыРейса.Выполнен
				Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
			Тогда 
				
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаМаршрут, мсвСвойств, мсвИсключений);		
				
			ИначеЕсли Статус = Перечисления.упСтатусыРейса.КВыполнению Тогда	
				
				Элементы.Работы.КоманднаяПанель.Доступность = Ложь;
								
				МассивСвойствДоступность = Новый Массив;
				МассивСвойствДоступность.Добавить(Новый Структура("Ключ, Значение", "ТолькоПросмотр", Ложь));
				МассивСвойствДоступность.Добавить(Новый Структура("Ключ, Значение", "Доступность", Истина));
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.МаршрутПройдена, 		МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.МаршрутОтменена, 		МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.РаботыВыполнена, 		МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.РаботыОтменена, 			МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.МаршрутПрибытиеФакт, 	МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.МаршрутУбытиеФакт, 		МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.НачалоВыполнения, 		МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ОкончаниеВыполнения, 	МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаВремяРейсаФакт, 	МассивСвойствДоступность);
				упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.МаршрутОтметитьВсе, 		МассивСвойствДоступность);
				
				мсвИсключенийСПодчинением.Добавить("ГруппаПеревозчик");
			КонецЕсли;
			
		КонецЕсли;
		
		упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаПеревозчик, мсвСвойств, мсвИсключенийСПодчинением);
		упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаПараметрыПеревозки, мсвСвойств);
		
		Если Ложь 
			Или Статус = Перечисления.упСтатусыРейса.Завершен
			Или Статус = Перечисления.упСтатусыРейса.Отменен
		Тогда
			упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаПлановыеРасходы, мсвСвойств);
		КонецЕсли; 
						
	КонецЕсли;
	
	Элементы.ОграничениеМаршрутизатора.ТолькоПросмотр = Не ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД");
	
	// Интеграция.Packer3D
	Если ИспользуетсяИнтеграцияСPacker3D Тогда
		РазрешитьАктивизациюИнтеграцииСPacker3D = Ложь;
		Если Ложь
			Или Статус = Перечисления.упСтатусыРейса.НазначеноТС
			Или Статус = Перечисления.упСтатусыРейса.КВыполнению
			Или Статус = Перечисления.упСтатусыРейса.Выполняется
			Или Статус = Перечисления.упСтатусыРейса.Выполнен
			Или Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
			Или Статус = Перечисления.упСтатусыРейса.Завершен
			Или Статус = Перечисления.упСтатусыРейса.Отменен
		Тогда
			РазрешитьАктивизациюИнтеграцииСPacker3D = Истина;
		КонецЕсли;
		МассивСвойств = Новый Массив;
		МассивСвойств.Добавить(Новый Структура("Ключ, Значение", "ТолькоПросмотр", НЕ РазрешитьАктивизациюИнтеграцииСPacker3D));
		МассивСвойств.Добавить(Новый Структура("Ключ, Значение", "Доступность", РазрешитьАктивизациюИнтеграцииСPacker3D));
		упСервисныеФункцииКлиентСервер.ИзменитьЗначениеСвойстваЭлементаФормыСПодчиненными(Элементы.ГруппаУкладкаГрузов, МассивСвойств);
	КонецЕсли;
	// Конец Интеграция.Packer3D
	
	Если Истина
		И НЕ ТолькоПросмотр
		И ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусКПланированиюТССервер(ТекстОшибки)
	
	Если (Статус = Перечисления.упСтатусыРейса.Новый) Тогда 
		Возврат ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.КПланированиюТС, ТекстОшибки);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Рейса должен быть в статусе ""Новый""'"), Объект.Ссылка, "Статус");
	КонецЕсли;
	Возврат Ложь;

КонецФункции

&НаСервере
Функция УстановитьСтатусКВыполнениюСервер(ТекстОшибки, ЭтоОшибкаВыполнения, ТребуетсяОформлениеПутевогоЛиста)
	
	ЭтоОшибкаВыполнения = Ложь;
	Если ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки")	Тогда 
		Если Не Документы.упРейс.ПоследовательностьВыполненияЭтаповМультимодальнойПеревозкиСоблюдена(Объект.Ссылка, ТекстОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'Документу не может быть назначен статус ""К выполнению""!'") + Символы.ПС + ТекстОшибки;
			Возврат Ложь;
		КонецЕсли;
		
		Если Не Документы.упРейс.ВсеПрерыванияГрузовогоПотокаОбработаны(Объект.Ссылка, ТекстОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'Документу не может быть назначен статус ""К выполнению""!'") + Символы.ПС + ТекстОшибки;
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И Не ПеревозкаТранспортнойКомпанией 
		И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПутевыхЛистов") 
		И упРейс.ТребуетсяОформлениеПутевогоЛиста(Объект.Ссылка) 
		И Не упРейс.ЕстьПутевойЛист(Объект.Ссылка)
	Тогда
		ТребуетсяОформлениеПутевогоЛиста = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Результат = ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.КВыполнению, ТекстОшибки);
	ЭтоОшибкаВыполнения = Результат;
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОтмененСервер(ТекстОшибки)
	
	Возврат ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.Отменен, ТекстОшибки);

КонецФункции

&НаКлиенте
Процедура УстановитьСтатусОтменен(ТекстОшибки)
	Если УстановитьСтатусОтмененСервер(ТекстОшибки) Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
		//ОповеститьОбИзмененииСтатусаРодительскиеДокументы();
	Иначе
		упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусВыполненСервер(ТекстОшибки, ЭтоОшибкаВыполнения)
	
	ЭтоОшибкаВыполнения = Ложь;
	
	Результат = ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.Выполнен, ТекстОшибки);
	
	ЭтоОшибкаВыполнения = Результат;
		
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусВыполненЧастичноСервер(ТекстОшибки, ЭтоОшибкаВыполнения)
	
	ЭтоОшибкаВыполнения = Ложь;
	
	Результат = ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.ВыполненЧастично, ТекстОшибки);
	
	ЭтоОшибкаВыполнения = Результат;
		
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция УстановитьСтатусЗавершенСервер(ТекстОшибки)
	
	Если Не Документы.упРейс.ПроверитьНаличиеНедополученныхДокументов(Объект.Ссылка, Объект.ВидПеревозки, ТекстОшибки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.Завершен, ТекстОшибки);
	
КонецФункции

&НаСервере
Функция ИзменитьСтатусРейса(СтатусРейса, ТекстОшибки = "")
	
	НовыйСтатус = СтатусРейса;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Ошибка проведения", УровеньЖурналаРегистрации.Ошибка, Документы.упРейс, Объект.Ссылка, ОписаниеОшибки);
			ТекстОшибки = Нстр("ru = 'Не удалось провести ""%1""'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Объект.Ссылка);
			
		   	Возврат Ложь;    
		КонецПопытки;
		
		ИзменилсяСтатус();
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус()
	
	УстановитьСтатусФормы();
	ОбновитьДанныеОПеревозке();
	ЗаблокироватьЭлементы();
	УстановитьКнопкиСтатусаРейса();
	упОбновитьВидимостьМаршрутаРабот(Статус);
	
КонецПроцедуры

&НаКлиенте
// Процедура перезаполнения плановых расходов.
Процедура ЗаполнитьПлановыеРасходыКлиент(Результат, ДополнительныеПараметры) Экспорт
	ЭтоЗаполнениеРасходов	= Ложь;
	Если Объект.ПлановыеРасходы.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработатьВопросОПерезаполненииПлановыхРасходов", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Таблица ""Плановые расходы"" будет перезаполнена. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПлановыеРасходыНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыеРасходыНаСервере()
	
	тбзРасходы = упРаспределениеРасходов.ПолучитьСписокСтатейРасходов(Объект.Ссылка, ТипТС, Соглашение);
	
	Если НЕ тбзРасходы = Неопределено Тогда
		тбзЗадания	= упКорректировкаРейса.Задания.Выгрузить(Новый Структура("Отменено", Ложь), "Задание");
		тбзЗадания.Свернуть("Задание");
		Если тбзЗадания.Количество() > 0  Тогда
			текОбъект = РеквизитФормыВЗначение("Объект");
			текОбъект.ЗаполнитьПлановыеРасходы(тбзРасходы, тбзЗадания);
			ЗначениеВРеквизитФормы(текОбъект,"Объект");
			ЗаполнитьВспомогательныеПоляЗаданийНаСервере();	
		КонецЕсли;
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Процедура ВидПеревозкиПриИзмененииНаСервере()
	ОбновитьДанныеВидПеревозки();
	ОбновитьДанныеОПеревозке();
	ОбновитьДанныеФормы();
	
	Если ЗначениеЗаполнено(Объект.ВидПеревозки)Тогда
		ОрганизацияВидаПеревозки = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки, "Организация");
		Если ЗначениеЗаполнено(ОрганизацияВидаПеревозки)Тогда
			Объект.Организация = ОрганизацияВидаПеревозки;
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.ВидПеревозки) И Не ЗначениеЗаполнено(Перевозчик) И ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком Тогда
		Перевозчик = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВидПеревозки, "Перевозчик");
		Соглашение = ПолучитьСоглашениеПоУмолчанию();
		КонтрагентПеревозчика = ПолучитьОсновногоКонтрагента(Перевозчик);
		СоглашениеПриИзмененииНаСервере();
	КонецЕсли;
	
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки);
		Если ЗначениеЗаполнено(Подразделение)
				И НЕ Подразделение = Объект.Подразделение Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
	
	Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
		Если НЕ ЧасовойПоясВидаПеревозки = Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;
		КонецЕсли;
	КонецЕсли;
	упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(Объект);

КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогиПоРейсу(ПересчитатьПорядокОтгрузки = Ложь)
	
	ОбновитьДанныеФормы(Истина, ПересчитатьПорядокОтгрузки);		
	УстановитьЗаголовкиПоказателейЗагрузки();
	ОбновитьПредставлениеПоказателейЗагрузки();	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормы(ПересчитатьИтогиПоРейсу = Ложь, ПересчитатьПорядокОтгрузки = Ложь)

	Если ПересчитатьПорядокОтгрузки Тогда
		ПересчитатьПорядокОтгрузки();
	КонецЕсли;
	
	Если ПересчитатьИтогиПоРейсу Тогда
		
		сткПлановыеПараметрыРейса = Новый Структура();
		сткПлановыеПараметрыРейса.Вставить("ЗонаПогрузки", ЗонаПогрузки);
		сткПлановыеПараметрыРейса.Вставить("ПлановоеОкончаниеВыполнения", упКорректировкаРейса.ПлановоеОкончаниеВыполнения);
		упРейс.ЗаполнитьПлановыеПараметрыРейса(Объект.Ссылка, 
												упКорректировкаРейса.Задания.Выгрузить(,"Задание"), 
												упКорректировкаРейса.Маршрут.Выгрузить(), 
												упКорректировкаРейса.Работы.Выгрузить(), 
												ВедетсяВалютныйУчет, 
												сткПлановыеПараметрыРейса, 
												Объект.Дата, 
												ВидЗоныДляФормированияПредставленияРейса,
												упКорректировкаРейса.ВариантФормированияМаршрута,
												Новый Структура("ПараметрыВводаИнформацииОГрузовыхМестах", ПараметрыВводаИнформацииОГрузовыхМестах));
												
		ЗаполнитьЗначенияСвойств(ЭтаФорма, сткПлановыеПараметрыРейса);										
												
	Иначе	
		текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
		сткРеквизитыЗаполнения = Новый Структура();
		сткРеквизитыЗаполнения.Вставить("ИспользуютсяДополнительныеОперации", ИспользуютсяДополнительныеОперации);
		сткРеквизитыЗаполнения.Вставить("Организация", Объект.Организация);
		сткРеквизитыЗаполнения.Вставить("УчитыватьКоличествоЗагрузочныхМетров", УчитыватьКоличествоЗагрузочныхМетров);
		сткРеквизитыЗаполнения.Вставить("УчитыватьКоличествоМест", УчитыватьКоличествоМест);
		сткРеквизитыЗаполнения.Вставить("УчитыватьМассу", УчитыватьМассу);
		сткРеквизитыЗаполнения.Вставить("УчитыватьОбъем", УчитыватьОбъем);
		сткРеквизитыЗаполнения.Вставить("ЧасовойПояс", 	  Объект.ЧасовойПояс);
		
		текКорректировкаМаршрутаОбъект.Рейс 				= Объект.Ссылка;
		текКорректировкаМаршрутаОбъект.Ссылка				= Объект.Ссылка;
		текКорректировкаМаршрутаОбъект.Период 				= Объект.Дата;
		текКорректировкаМаршрутаОбъект.Инициализация(сткРеквизитыЗаполнения);
		текКорректировкаМаршрутаОбъект.ПроставитьПорядокОтгрузки();
		текКорректировкаМаршрутаОбъект.ЧасовойПояс			= Объект.ЧасовойПояс;
		ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
		
		// Элементы связанные с вводом сумм инкассации.
		ВидПеревозки = текКорректировкаМаршрутаОбъект.ВидПеревозки;
		ИспользуетсяИнкассация = Ложь;
		Если ЗначениеЗаполнено(ВидПеревозки) Тогда
			ИспользуетсяИнкассация = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
		КонецЕсли;
		Элементы.РаботыСумма.Видимость 	= ИспользуетсяИнкассация;
		Элементы.РаботыВалюта.Видимость = ИспользуетсяИнкассация;
		
		ДополнительныеСотрудники.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упДополнительныеСотрудникиРейсаСрезПоследних.Сотрудник,
		|	упДополнительныеСотрудникиРейсаСрезПоследних.СтрокаНомер КАК СтрокаНомер
		|ИЗ
		|	РегистрСведений.упДополнительныеСотрудникиРейса.СрезПоследних(, Рейс = &Рейс) КАК упДополнительныеСотрудникиРейсаСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтрокаНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПеревозчикПоРейсу.Перевозчик,
		|	упПеревозчикПоРейсу.Соглашение,
		|	упПеревозчикПоРейсу.КонтрагентПеревозчика,
		|	упПеревозчикПоРейсу.ТипТС,
		|	упПеревозчикПоРейсу.ТранспортноеСредство,
		|	упПеревозчикПоРейсу.Водитель1,
		|	упПеревозчикПоРейсу.Водитель2,
		|	упПеревозчикПоРейсу.Экспедитор,
		|	упПеревозчикПоРейсу.Регистратор
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПлановыеПараметрыРейса.Время,
		|	упПлановыеПараметрыРейса.ВремяВПути,
		|	упПлановыеПараметрыРейса.ВремяОтдыхаВПути,
		|	упПлановыеПараметрыРейса.ЗонаПогрузки,
		|	упПлановыеПараметрыРейса.ЗонаРазгрузки,
		|	упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров,
		|	упПлановыеПараметрыРейса.КоличествоМест,
		|	упПлановыеПараметрыРейса.Масса,
		|	упПлановыеПараметрыРейса.Объем,
		|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения,
		|	упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения,
		|	упПлановыеПараметрыРейса.ПредставлениеМаршрута,
		|	упПлановыеПараметрыРейса.Расстояние,
		|	упПлановыеПараметрыРейса.ОценочнаяСтоимость,
		|	упПлановыеПараметрыРейса.СуммаИнкассации,
		|	упПлановыеПараметрыРейса.НулевойПробег,
		|	упПлановыеПараметрыРейса.КоличествоГрузовыхМест
		|ИЗ
		|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
		|ГДЕ
		|	упПлановыеПараметрыРейса.Рейс = &Рейс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упФактическиеПараметрыРейса.НачалоВыполнения,
		|	упФактическиеПараметрыРейса.ОкончаниеВыполнения,
		|	упФактическиеПараметрыРейса.СуммаИнкассации
		|ИЗ
		|	РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
		|ГДЕ
		|	упФактическиеПараметрыРейса.Рейс = &Рейс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упТахографДанныеОДеятельностиВодителей.Рейс
		|ИЗ
		|	РегистрСведений.упТахографДанныеОДеятельностиВодителей КАК упТахографДанныеОДеятельностиВодителей
		|ГДЕ
		|	&ИспользуетсяЗагрузкаДанныхИзФайловТахографа
		|	И упТахографДанныеОДеятельностиВодителей.Рейс = &Рейс";
		Запрос.УстановитьПараметр("Рейс", Объект.Ссылка);
		Запрос.УстановитьПараметр("ИспользуетсяЗагрузкаДанныхИзФайловТахографа", ИспользуетсяЗагрузкаДанныхИзФайловТахографа);
		Результат = Запрос.ВыполнитьПакет();
		КоличествоТаблиц = Результат.Количество();
		ИндексЗапросаДопСотрудники 			= 0;
		ИндексЗапросаПеревозчик 			= 1;
		ИндексЗапросаПлановыеПараметры 		= 2;
		ИндексЗапросаФактическиеПараметры 	= 3;
		ИндексЗапросаДанныеТахографа 		= 4;
		
		Если Не Результат[ИндексЗапросаДопСотрудники].Пустой() Тогда
			ВыборкаДопСотрудники = Результат[ИндексЗапросаДопСотрудники].Выбрать(); 
			Пока ВыборкаДопСотрудники.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ДополнительныеСотрудники.Добавить(), ВыборкаДопСотрудники);
			КонецЦикла;
		КонецЕсли;
		
		Если Не Результат[ИндексЗапросаПеревозчик].Пустой() Тогда
			ВыборкаЭкипаж = Результат[ИндексЗапросаПеревозчик].Выбрать(); 
			ВыборкаЭкипаж.Следующий();
			ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаЭкипаж);
			ЕстьПодтверждениеЗаявкиНаТС = (ТипЗнч(ВыборкаЭкипаж.Регистратор) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС"));
			Если ЕстьПодтверждениеЗаявкиНаТС Тогда
				ПодтверждениеЗаявкиНаТС = ВыборкаЭкипаж.Регистратор;
			КонецЕсли; 
		КонецЕсли;
		
		Если Не Результат[ИндексЗапросаПлановыеПараметры].Пустой() Тогда
			ВыборкаПлановыхПараметров = Результат[ИндексЗапросаПлановыеПараметры].Выбрать(); 
			ВыборкаПлановыхПараметров.Следующий();
			ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаПлановыхПараметров);
			ЗаполнитьЗначенияСвойств(упКорректировкаРейса, ВыборкаПлановыхПараметров, "ПлановоеНачалоВыполнения, ПлановоеОкончаниеВыполнения");
		КонецЕсли; 
		
		Если Не Результат[ИндексЗапросаФактическиеПараметры].Пустой() Тогда
			ВыборкаФактическихПараметров = Результат[ИндексЗапросаФактическиеПараметры].Выбрать(); 
			ВыборкаФактическихПараметров.Следующий();
			ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаФактическихПараметров);
			Если НЕ Статус = Перечисления.упСтатусыРейса.КВыполнению
					ИЛИ НЕ СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
				ЗаполнитьЗначенияСвойств(упКорректировкаРейса, ВыборкаФактическихПараметров, "НачалоВыполнения, ОкончаниеВыполнения");	
			КонецЕсли;
			
		КонецЕсли; 
		
		// Данные тахографа
		ТекущаяВидимостьГруппы = Элементы.ГруппаДанныеТахографа.Видимость;
		
		ВидимостьГруппы = ИспользуетсяЗагрузкаДанныхИзФайловТахографа И НЕ Результат[ИндексЗапросаДанныеТахографа].Пустой();	
		
		Если НЕ ТекущаяВидимостьГруппы = ВидимостьГруппы Тогда
			Элементы.ГруппаДанныеТахографа.Видимость = 	ВидимостьГруппы;
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьПлановыйРасходПоРейсу()
	
	сткПлановыеРасходыПоРейсу = упРаспределениеРасходов.ПлановыеРасходыПоРейсуВведенныеНеДокументомРейс(Объект.Ссылка);
	СуммаНДС	= сткПлановыеРасходыПоРейсу.СуммаПланСНДС - сткПлановыеРасходыПоРейсу.СуммаПланБезНДС + Объект.ПлановыеРасходы.Итог("СуммаНДС"); 
	СуммаСНДС	= сткПлановыеРасходыПоРейсу.СуммаПланСНДС + Объект.ПлановыеРасходы.Итог("Сумма");
	
	Если ВедетсяВалютныйУчет Тогда
		ПлановыеРасходыПоРейсуНДСПредставление 	= упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаНДС, 	ВалютаУпрУчета);
		ПлановыеРасходыПоРейсуСНДСПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаСНДС, 	ВалютаУпрУчета);
	Иначе	
		ПлановыеРасходыПоРейсуНДСПредставление 	= упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаНДС);
		ПлановыеРасходыПоРейсуСНДСПредставление = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеСуммыСВалютой(СуммаСНДС);
	КонецЕсли;  
		
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеМаршурта()
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ВидЗоныДляФормированияПредставленияРейса = ВидЗоныДляФормированияПредставленияРейса;
	текКорректировкаМаршрутаОбъект.УстановитьПредставлениеМаршурта();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСоглашениеПоУмолчанию()

	Возврат Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(Перевозчик, Объект.Организация);

КонецФункции // ПолучитьСоглашениеПоУмолчанию()

&НаСервереБезКонтекста
Функция ПолучитьОсновногоКонтрагента(Партнер)
	Возврат Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(Партнер);	
КонецФункции

&НаКлиенте
Процедура РедактироватьМаршрут()
	
	ОписаниеОшибки = "";
	Если НЕ упРаботаСоСтатусамиКлиентСервер.РазрешеноРедактироватьМаршрутРейса(Статус,ОписаниеОшибки)  Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки);
		Возврат;
	КонецЕсли;
			
	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Рейс", Объект.Ссылка);
	сткПараметры.Вставить("НачалоПериодаПланирования",    упКорректировкаРейса.ПлановоеНачалоВыполнения);
	сткПараметры.Вставить("ОкончаниеПериодаПланирования", упКорректировкаРейса.ПлановоеОкончаниеВыполнения);
	сткПараметры.Вставить("РежимКорректировки", РежимКорректировки());
	
	сткПараметрыПланирования = Новый Структура();
	сткПараметрыПланирования.Вставить("МетодРешенияЗК", ПредопределенноеЗначение("Перечисление.упМетодыРешенияЗК.ЖадныйАлгоритм"));
	сткПараметрыПланирования.Вставить("ВариантОптимизацииМаршрута", ПредопределенноеЗначение("Перечисление.упВариантыОптимизацииМаршрута.ПоРасстоянию"));
	сткПараметрыПланирования.Вставить("ПространственныйВес", 0.4);
	сткПараметрыПланирования.Вставить("ВременнойВес", 0.4);
	сткПараметрыПланирования.Вставить("ВесСрочности", 0.2);
	сткПараметры.Вставить("ПараметрыПланирования", сткПараметрыПланирования);
	ОткрытьФорму("Обработка.упРМПланированиеРейсов.Форма.ФормаРедактированияРейса", сткПараметры,,,,, Новый ОписаниеОповещения("РедактированиеМаршрутаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры	

&НаСервере
Процедура РедактироватьМаршрутНаСервере()
	
	ЭтаФорма.Прочитать();
	УстановитьСтатусФормы();
	УстановитьКнопкиСтатусаРейса();
	ЗаблокироватьЭлементы();
	ОбновитьДанныеФормы();
	ОбновитьПлановыйРасходПоРейсу();
	УстановитьПредставленияПоказателейЗагрузки();
	ОбновитьПредставлениеПоказателейЗагрузки();
		
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура ПересчитатьСтроку(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеРасходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчетаОбъекта(Объект, Соглашение);
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ПлановыеРасходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
	ОбновитьПлановыйРасходПоРейсу();
КонецПроцедуры // РассчитатьИтоговуюСумму()

#Область ОповещенияПоСобытиям

#Область ОповещенияПоСобытиямУкладкуГрузовPacker3D

&НаКлиенте
Процедура УкладкаГрузовПриАктивизацииПоляОбработчикОжидания()
	
	ТекущаяСтрокаУкладкаГрузов = Элементы.УкладкаГрузов.ТекущиеДанные;
	ТекущийЭлементУкладкаГрузов = Элементы.УкладкаГрузов.ТекущийЭлемент;
	Если НЕ ТекущаяСтрокаУкладкаГрузов = Неопределено И НЕ ТекущийЭлементУкладкаГрузов = Неопределено Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Если ТекущийЭлементУкладкаГрузов.Имя = "УкладкаГрузовЗапускРасчета" Тогда
			
			Если Модифицированность Тогда
				Элементы.УкладкаГрузов.ТекущийЭлемент = Элементы.УкладкаГрузовДатаОбработки;
				ТекстСообщения = НСтр("ru = 'Выполните сохранение данных рейса, перед запуском нового расчета укладки грузов.Запуск не доступен.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Объект.Ссылка);
				Возврат;
			КонецЕсли;
			
			Если ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Запустить расчет'") Тогда
				
				ДополнительныеПараметры = Новый Структура("ДатаРегистрации",ТекущаяСтрокаУкладкаГрузов.ДатаРегистрации);
				ДополнительныеПараметры.Вставить("Идентификатор",ТекущаяСтрокаУкладкаГрузов.ПолучитьИдентификатор());
				Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗапуститьРасчетPacker3D", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьВопрос(Оповещение, НСтр("ru = 'Выполнить запуск расчета укладки грузов?'; en = 'Do you want to continue?'"), Режим, 0);
				
			ИначеЕсли ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Остановить расчет'") Тогда
				
				ДополнительныеПараметры = Новый Структура("КлючРасчета",ТекущаяСтрокаУкладкаГрузов.КлючРасчета);
				ДополнительныеПараметры.Вставить("Идентификатор",ТекущаяСтрокаУкладкаГрузов.ПолучитьИдентификатор());
				Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОстановкаРасчетPacker3D", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьВопрос(Оповещение, НСтр("ru = 'Остановить выполнение расчета укладки грузов?'; en = 'Do you want to continue?'"), Режим, 0);
				
			ИначеЕсли ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Возобновить расчет'") Тогда
				
				ДополнительныеПараметры = Новый Структура("КлючРасчета",ТекущаяСтрокаУкладкаГрузов.КлючРасчета);
				ДополнительныеПараметры.Вставить("Идентификатор",ТекущаяСтрокаУкладкаГрузов.ПолучитьИдентификатор());
				Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаВозобновитьРасчетPacker3D", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьВопрос(Оповещение, НСтр("ru = 'Запустить остановленный расчет укладки грузов?'; en = 'Do you want to continue?'"), Режим, 0);
				
			КонецЕсли;
			
		ИначеЕсли ТекущийЭлементУкладкаГрузов.Имя = "УкладкаГрузовОстановкаРасчета" Тогда
			
			Если ТекущаяСтрокаУкладкаГрузов.ОстановкаРасчета=0 Тогда
				
				Если ТекущаяСтрокаУкладкаГрузов.Состояние = ПредопределенноеЗначение("Перечисление.упСостоянияПроцессов.ПустаяСсылка") Тогда
					
					ДополнительныеПараметры = Новый Структура("Идентификатор",ТекущаяСтрокаУкладкаГрузов.ПолучитьИдентификатор());
					Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОстановкаРасчетНезапущеногоPacker3D", ЭтотОбъект, ДополнительныеПараметры);
					ПоказатьВопрос(Оповещение, НСтр("ru = 'Расчет укладки грузов не был запущен. Удалить из списка выбранный расчет укладки грузов?'; en = 'Do you want to continue?'"), Режим, 0);
					
				Иначе
					
					ДополнительныеПараметры = Новый Структура("КлючРасчета",ТекущаяСтрокаУкладкаГрузов.КлючРасчета);
					ДополнительныеПараметры.Вставить("Идентификатор",ТекущаяСтрокаУкладкаГрузов.ПолучитьИдентификатор());
					ДополнительныеПараметры.Вставить("Отменить",Истина); 
					Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОстановкаРасчетPacker3D", ЭтотОбъект, ДополнительныеПараметры);
					ТекстВопроса = НСтр("ru = 'Отменить выполнение расчета укладки грузов?'; en = 'Do you want to continue?'");
					Если ТекущаяСтрокаУкладкаГрузов.Состояние = ПредопределенноеЗначение("Перечисление.упСостоянияПроцессов.Завершен") Тогда
						ТекстВопроса = НСтр("ru = 'Расчет укладки грузов завершен. Отправить запрос на SOAP-сервере Packer3D для удаления выбранного расчета укладки грузов?'; en = 'Do you want to continue?'");
					КонецЕсли;
					ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
					
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ТекущийЭлементУкладкаГрузов.Имя = "УкладкаГрузовРезультат" Тогда
			
			Если ТекущаяСтрокаУкладкаГрузов.Результат = НСтр("ru = 'Посмотреть результат'") Тогда
				
				СтрокаЗапуска = ПолучитьСсылкуНаСтраницуСайтаСРезультатом(ТекущаяСтрокаУкладкаГрузов.КлючРасчета);
				Если НЕ ПустаяСтрока(СтрокаЗапуска) Тогда
					ПерейтиПоНавигационнойСсылке(СтрокаЗапуска);				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущийЭлементУкладкаГрузов.Имя = "УкладкаГрузовРезультат" 
			ИЛИ ТекущийЭлементУкладкаГрузов.Имя = "УкладкаГрузовОстановкаРасчета" 
			ИЛИ ТекущийЭлементУкладкаГрузов.Имя = "УкладкаГрузовЗапускРасчета" 
		Тогда
			// изменяем текущий элемент для возможности его повторной активизации
			Элементы.УкладкаГрузов.ТекущийЭлемент = Элементы.УкладкаГрузовДатаОбработки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // УкладкаГрузовПриАктивизацииПоляОбработчикОжидания()

&НаСервереБезКонтекста
// Получить ссылку на страницу сайта с результатом.
//
// Параметры:
//  КлючРасчета  - Строка - строка, содержащая уникальный ключ расчёта,
//                 который используется в остальных функциях для получания статуса и результата расчёта.
//
// Возвращаемое значение:
//   Строка - строка ссылки на страницу результата.
//
Функция ПолучитьСсылкуНаСтраницуСайтаСРезультатом(КлючРасчета)
	
	СтрокаЗапуска = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Packer3D.АдресВебСтраницы КАК АдресВебСтраницы
	|ИЗ
	|	РегистрСведений.упРасчетыУкладкиГрузовPacker3D КАК Packer3D
	|ГДЕ
	|	Packer3D.КлючРасчета = &КлючРасчета";
	
	Запрос.УстановитьПараметр("КлючРасчета", КлючРасчета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтрокаЗапуска = Выборка.АдресВебСтраницы;
	КонецЕсли;
	
	Возврат СтрокаЗапуска;
	
КонецФункции // ПолучитьСсылкуНаСтраницуСайтаСРезультатом()

&НаКлиенте
Процедура УкладкаГрузовОбновитьСтатусОбработчикОжидания()
	
	Если НЕ РазрешитьАктивизациюИнтеграцииСPacker3D Тогда
		ОтключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания");
		Возврат;
	КонецЕсли;
		
	МассивРасчетов = Новый Массив;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("Состояние",ПредопределенноеЗначение("Перечисление.упСостоянияПроцессов.Запущен"));
	ОтборУкладки = УкладкаГрузов.НайтиСтроки(СтруктураОтбора);
	Для каждого ТекущаяУкладка Из ОтборУкладки Цикл
		Структура = Новый Структура();
		Структура.Вставить("КлючРасчета",ТекущаяУкладка.КлючРасчета);
		Структура.Вставить("Идентификатор",ТекущаяУкладка.ПолучитьИдентификатор());
		МассивРасчетов.Добавить(Структура);
	КонецЦикла;
	
	Если МассивРасчетов.Количество() Тогда
		ОбновитьСтатусыУкладкаГрузов(МассивРасчетов);
		ПодключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания", 30, Истина);
	КонецЕсли;
	
КонецПроцедуры // УкладкаГрузовОбновитьСтатусОбработчикОжидания()

&НаСервере
Процедура ОбновитьСтатусыУкладкаГрузов(СписокРасчетов)
	
	Для каждого ЗапущенныйРасчет Из СписокРасчетов Цикл
		Структура = упИнтеграцияССервисомУкладкиГрузов.ОбновитьСтатусРасчетаУкладкиГрузов(Объект.Ссылка,ЗапущенныйРасчет.КлючРасчета);
		ТекущийРасчет = УкладкаГрузов.НайтиПоИдентификатору(ЗапущенныйРасчет.Идентификатор);
		Если НЕ ТекущийРасчет = Неопределено Тогда
			ТекущийРасчет.ДатаОбработки = Структура.ДатаОбработки;
			ТекущийРасчет.Состояние = Структура.Состояние;
			Если ТекущийРасчет.Состояние = Перечисления.упСостоянияПроцессов.Завершен Тогда
				ТекущийРасчет.ЗапускРасчета = НСтр("ru = 'Расчет завершен'");
				ТекущийРасчет.ОстановкаРасчета = 1;
				ТекущийРасчет.Результат = НСтр("ru = 'Посмотреть результат'");
				ТекущийРасчет.ИндексКартинки = 2;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСтатусыУкладкаГрузов()

&НаСервере
// Запустим расчеты укладки грузов.
//
// Параметры:
//  ДатаРегистрации  - Дата - Дата добавления расчета.
//
// Возвращаемое значение:
//   Структура - Результат обращения к сервису.
//
Функция ВыполнитьЗапускРасчетаУкладкиГрузов(ДатаРегистрации)

	РезультатЗапуска = упИнтеграцияССервисомУкладкиГрузов.ЗапуститьНовыйРасчетУкладкиГрузов(Объект.Ссылка,ДатаРегистрации);
	
	Возврат РезультатЗапуска;

КонецФункции // ВыполнитьЗапускРасчетаУкладкиГрузов()

&НаСервере
// Остановить расчеты укладки грузов.
//
// Параметры:
//  КлючРасчета  - Строка - идентификатор расчета.
//  Отменить  - Булево - Вне зависимости от статуса отменить расчет.
//
// Возвращаемое значение:
//   Структура - Результат обращения к сервису.
//
Функция ОстановитьРасчетаУкладкиГрузов(КлючРасчета, Отменить)

	РезультатОстановки = упИнтеграцияССервисомУкладкиГрузов.ОстановитьРасчетУкладкиГрузов(Объект.Ссылка,КлючРасчета, Отменить);
	
	Возврат РезультатОстановки;

КонецФункции // ОстановитьРасчетаУкладкиГрузов()

&НаСервере
// Возобновить расчеты укладки грузов.
//
// Параметры:
//  КлючРасчета  - Строка - идентификатор расчета.
//
// Возвращаемое значение:
//   Структура - Результат обращения к сервису.
//
Функция ВозобновитьРасчетаУкладкиГрузов(КлючРасчета)

	РезультатВозобновления = упИнтеграцияССервисомУкладкиГрузов.ВозобновитьРасчетаУкладкиГрузов(Объект.Ссылка,КлючРасчета);
	
	Возврат РезультатВозобновления;

КонецФункции // ВозобновитьРасчетаУкладкиГрузов()

&НаКлиенте
// Запустить расчет укладки груза.
//
Процедура ПослеЗакрытияВопросаЗапуститьРасчетPacker3D(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания");
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("Состояние",ПредопределенноеЗначение("Перечисление.упСостоянияПроцессов.Запущен"));
	ОтборУкладки = УкладкаГрузов.НайтиСтроки(СтруктураОтбора);
	Если ОтборУкладки.Количество()>=5 Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Расчет укладки грузов'"),,
		НСтр("ru = 'Количество запущенных расчетов укладки грузов не должно превыщать пяти.'"),БиблиотекаКартинок.Информация32);
		Возврат;
	КонецЕсли;
	
	Структура = ВыполнитьЗапускРасчетаУкладкиГрузов(ДополнительныеПараметры.ДатаРегистрации);
	Если НЕ Структура = Неопределено Тогда
		
		Если Структура.Свойство("Ошибки") Тогда
			СписокОшибок = Структура.Ошибки;
			Для каждого ТекущаяОшибка Из СписокОшибок Цикл
				ТекстСообщения = СтрШаблон(НСтр("ru = '%1 Расчет укладки грузов не запущен.'"),ТекущаяОшибка.Текст);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Объект.Ссылка,ТекущаяОшибка.Поле);
			КонецЦикла;
		Иначе
			
			ТекущаяСтрокаУкладкаГрузов = УкладкаГрузов.НайтиПоИдентификатору(ДополнительныеПараметры.Идентификатор);
			ТекущаяСтрокаУкладкаГрузов.КлючРасчета = Структура.КлючРасчета;
			ТекущаяСтрокаУкладкаГрузов.ДатаОбработки = Структура.ДатаОбработки;
			ТекущаяСтрокаУкладкаГрузов.Состояние = Структура.Состояние;
			ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Остановить расчет'");
			ТекущаяСтрокаУкладкаГрузов.ИндексКартинки = 1;
			ТекущаяСтрокаУкладкаГрузов.ОстановкаРасчета = 0;			
			ПодключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания", 0.2, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.УкладкаГрузов.ТекущийЭлемент = Элементы.УкладкаГрузовДатаОбработки;
	
КонецПроцедуры // ПослеЗакрытияВопросаЗапуститьРасчетPacker3D()

&НаКлиенте
// Возобновить расчет укладки груза.
//
Процедура ПослеЗакрытияВопросаВозобновитьРасчетPacker3D(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ОтключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания");
	Структура = ВозобновитьРасчетаУкладкиГрузов(ДополнительныеПараметры.КлючРасчета);
	Если НЕ Структура = Неопределено Тогда
		ТекущаяСтрокаУкладкаГрузов = УкладкаГрузов.НайтиПоИдентификатору(ДополнительныеПараметры.Идентификатор);
		ТекущаяСтрокаУкладкаГрузов.ДатаОбработки = Структура.ДатаОбработки;
		ТекущаяСтрокаУкладкаГрузов.Состояние = Структура.Состояние;
		ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Остановить расчет'");
		ТекущаяСтрокаУкладкаГрузов.ИндексКартинки = 1;
		ТекущаяСтрокаУкладкаГрузов.ОстановкаРасчета = 0;		
		ПодключитьОбработчикОжидания("УкладкаГрузовОбновитьСтатусОбработчикОжидания", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопросаВозобновитьРасчетPacker3D()

&НаКлиенте
// Остановка расчета укладки груза.
//
Процедура ПослеЗакрытияВопросаОстановкаРасчетНезапущеногоPacker3D(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаУкладкаГрузов = УкладкаГрузов.НайтиПоИдентификатору(ДополнительныеПараметры.Идентификатор);
	УкладкаГрузов.Удалить(ТекущаяСтрокаУкладкаГрузов);
	
КонецПроцедуры // ПослеЗакрытияВопросаОстановкаРасчетНезапущеногоPacker3D()

&НаКлиенте
// Остановка расчета укладки груза.
//
Процедура ПослеЗакрытияВопросаОстановкаРасчетPacker3D(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ОтменитьРасчет = ?(ДополнительныеПараметры.Свойство("Отменить"),Истина,Неопределено);
	Структура = ОстановитьРасчетаУкладкиГрузов(ДополнительныеПараметры.КлючРасчета,ОтменитьРасчет);
	Если НЕ Структура = Неопределено Тогда
		ТекущаяСтрокаУкладкаГрузов = УкладкаГрузов.НайтиПоИдентификатору(ДополнительныеПараметры.Идентификатор);
		ТекущаяСтрокаУкладкаГрузов.ДатаОбработки = Структура.ДатаОбработки;
		ТекущаяСтрокаУкладкаГрузов.Состояние = Структура.Состояние;
		Если Структура.Состояние = ПредопределенноеЗначение("Перечисление.упСостоянияПроцессов.Отменен")Тогда
			ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Расчет отменен'");
			ТекущаяСтрокаУкладкаГрузов.ОстановкаРасчета = 1;
			ТекущаяСтрокаУкладкаГрузов.ИндексКартинки = 3;
			ТекущаяСтрокаУкладкаГрузов.Результат = "";
		Иначе
			ТекущаяСтрокаУкладкаГрузов.ЗапускРасчета = НСтр("ru = 'Возобновить расчет'");
			ТекущаяСтрокаУкладкаГрузов.ИндексКартинки = 0;
			ТекущаяСтрокаУкладкаГрузов.ОстановкаРасчета = 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопросаОстановкаРасчетPacker3D()

&НаКлиенте
// Добавить расчет укладки груза.
//
Процедура ПослеЗакрытияВопросаДобавитьРасчетPacker3D(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = УкладкаГрузов.Добавить();
	НоваяСтрока.ЗапускРасчета = НСтр("ru = 'Запустить расчет'");
	НоваяСтрока.ОстановкаРасчета = 0;
	НоваяСтрока.ИндексКартинки = 0;
	НоваяСтрока.ДатаРегистрации = ТекущаяДата();
	НоваяСтрока.ДатаОбработки = НоваяСтрока.ДатаРегистрации;
	НоваяСтрока.КлючРасчета = "";
	НоваяСтрока.Состояние = ПредопределенноеЗначение("Перечисление.упСостоянияПроцессов.ПустаяСсылка");
	
	Элементы.УкладкаГрузов.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	Элементы.УкладкаГрузов.ТекущийЭлемент = Элементы.УкладкаГрузовДатаОбработки;
	
КонецПроцедуры // ПослеЗакрытияВопросаДобавитьРасчетPacker3D()

&НаКлиенте
// Обновить расчеты укладки груза.
//
Процедура ПослеЗакрытияВопросаОбновитьУкладкуГрузов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// Если это новый документ, ничего не выполняем.
        Возврат;
	КонецЕсли;
	
	УкладкаГрузов.Очистить();
	
	МассивРасчетов = ПолучитьУкладкуГрузовНаСервере(Объект.Ссылка);
	
	Для каждого ТекущийРасчет Из МассивРасчетов Цикл
		НоваяСтрока = УкладкаГрузов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекущийРасчет);
	КонецЦикла;
	
КонецПроцедуры // ПослеЗакрытияВопросаОбновитьУкладкуГрузов()

#Область ОбработкаУкладкуГрузовPacker3D

&НаСервере
Процедура ОбновитьУкладкуГрузовНаСервере()
	
	Если НЕ РазрешитьАктивизациюИнтеграцииСPacker3D Тогда
		Возврат;
	КонецЕсли;
	
	МассивРасчетов = ПолучитьУкладкуГрузовНаСервере(Объект.Ссылка);
	
	Для каждого ТекущийРасчет Из МассивРасчетов Цикл
		НоваяСтрока = УкладкаГрузов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекущийРасчет);
	КонецЦикла;

КонецПроцедуры // ОбновитьУкладкуГрузовНаСервере()

&НаСервереБезКонтекста
// Получим данные по укладке груза для рейса.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ.
//
// Возвращаемое значение:
//   Массив - Массив структур, с данными о расчетах укладки грузов.
//
Функция ПолучитьУкладкуГрузовНаСервере(Рейс)
	
	МассивРасчетов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРасчетыУкладкиГрузовPacker3D.КлючРасчета КАК КлючРасчета,
	|	упРасчетыУкладкиГрузовPacker3D.СтатусРасчета,
	|	упРасчетыУкладкиГрузовPacker3D.Состояние КАК Состояние,
	|	упРасчетыУкладкиГрузовPacker3D.АдресВебСтраницы КАК Адрес,
	|	упРасчетыУкладкиГрузовPacker3D.ДатаОбработки КАК ДатаОбработки,
	|	упРасчетыУкладкиГрузовPacker3D.ДатаРегистрации КАК ДатаРегистрации
	|ИЗ
	|	РегистрСведений.упРасчетыУкладкиГрузовPacker3D КАК упРасчетыУкладкиГрузовPacker3D
	|ГДЕ
	|	упРасчетыУкладкиГрузовPacker3D.Рейс = &Рейс
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРегистрации";
	
	Запрос.УстановитьПараметр("Рейс", Рейс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	НоваяСтрока = Неопределено;
	ПоляРасчета = "ИндексКартинки,ОстановкаРасчета,ЗапускРасчета,Результат,КлючРасчета,СтатусРасчета,Состояние,Адрес,ДатаОбработки,ДатаРегистрации";
	Пока Выборка.Следующий() Цикл
		Структура = Новый Структура(ПоляРасчета);
		Структура.ОстановкаРасчета = 0;
		ЗаполнитьЗначенияСвойств(Структура,Выборка);
		Если Выборка.Состояние = Перечисления.упСостоянияПроцессов.Запущен Тогда
			Структура.ЗапускРасчета = НСтр("ru = 'Остановить расчет'");
			Структура.ИндексКартинки = 1;
		ИначеЕсли Выборка.Состояние = Перечисления.упСостоянияПроцессов.Остановлен Тогда
			Структура.ЗапускРасчета = НСтр("ru = 'Возобновить расчет'");
			Структура.ОстановкаРасчета = 1;
			Структура.ИндексКартинки = 1;
		ИначеЕсли Выборка.Состояние = Перечисления.упСостоянияПроцессов.Завершен Тогда
			Структура.ЗапускРасчета = НСтр("ru = 'Расчет завершен'");
			Структура.Результат = НСтр("ru = 'Посмотреть результат'");
			Структура.ОстановкаРасчета = 1;
			Структура.ИндексКартинки = 2;
		ИначеЕсли Выборка.Состояние = Перечисления.упСостоянияПроцессов.Отменен Тогда
			Структура.ЗапускРасчета = НСтр("ru = 'Расчет отменен'");
			Структура.ОстановкаРасчета = 1;
			Структура.ИндексКартинки = 3;
		КонецЕсли;
		МассивРасчетов.Добавить(Структура);
	КонецЦикла;
	
	Возврат МассивРасчетов;
	
КонецФункции // ПолучитьУкладкуГрузовНаСервере()

#КонецОбласти

#КонецОбласти

&НаКлиенте
// Продолжение редактирования маршрута после утвердительного ответа.
//
Процедура РедактированиеМаршрутаПослеОтветаНаВопросЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	РедактироватьМаршрут();
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы редактирования рейса.
//
Процедура РедактированиеМаршрутаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РедактироватьМаршрутНаСервере();
	//ОбновитьПредставлениеПоказателейЗагрузки();
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
// Продолжение перезаполнения плановых расходов после утвердительного ответа.
//
Процедура ОбработатьВопросОПерезаполненииПлановыхРасходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 ЗаполнитьПлановыеРасходыНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Продолжение добавления точки после утвердительного ответа.
//
Процедура ОбработатьВопросОДобавлениеТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	ДобавитьТочку = РезультатВопроса = КодВозвратаДиалога.Да;
	ДобавитьТочкуПриИзмененииТранспортногоСредстваНаСервере(ДобавитьТочку, ДополнительныеПараметры);
	УстановитьЗаголовокМаршрута();
КонецПроцедуры

&НаКлиенте
// Продолжение удаления точки после утвердительного ответа.
//
Процедура ОбработатьВопросОбУдаленииТочки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	УдалитьТочку = РезультатВопроса = КодВозвратаДиалога.Да;
	УдалитьТочкуПриИзмененииТранспортногоСредстваНаСервере(УдалитьТочку, ДополнительныеПараметры);
	УстановитьЗаголовокМаршрута();
КонецПроцедуры

&НаСервере
Процедура ДобавитьТочкуПриИзмененииТранспортногоСредстваНаСервере(ДобавитьТочку, ДополнительныеПараметры)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ДобавитьТочкуПриИзмененииТранспортногоСредства(ДобавитьТочку, ДополнительныеПараметры);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Процедура УдалитьТочкуПриИзмененииТранспортногоСредстваНаСервере(УдалитьТочку, ДополнительныеПараметры)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.УдалитьТочкуПриИзмененииТранспортногоСредства(УдалитьТочку, ДополнительныеПараметры);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");		
КонецПроцедуры

&НаКлиенте
Процедура ВыбранПунктМенюВыбораТС(Результат, Параметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОткрытьСписокДоступности = Результат.Значение = "СписокДоступности";
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", Новый Структура());
	
	ЗаполнитьПараметрыФормыДляСпискаТС(ПараметрыФормы, ОткрытьСписокДоступности);
	
	Если ОткрытьСписокДоступности Тогда
		ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбораДоступныхТС", ПараметрыФормы, Параметры.ЭлементФормы);
	Иначе
		Если РежимФормированияКонтейнеров Тогда
			ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбораКонтейнера", ПараметрыФормы, Параметры.ЭлементФормы);
		Иначе	
			ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбора", ПараметрыФормы, Параметры.ЭлементФормы);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыФормыДляСпискаТС(ПараметрыФормы, ОткрытьСписокДоступности = Ложь)

	ОтборСтруктура = Неопределено;
	
	Если ТипЗнч(ОтборСтруктура) = Тип("Структура") Тогда
		ОтборСтруктура = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыФормы, "Отбор", Неопределено);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.ВидПеревозки) Тогда
		ПараметрыФормы.Вставить("ВидПеревозки", Объект.ВидПеревозки); 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
	КонецЕсли;	
	
	Если ОтборСтруктура <> Неопределено Тогда
		Если ЗначениеЗаполнено(Перевозчик) Тогда
			ОтборСтруктура.Вставить("Партнер", Перевозчик);
		КонецЕсли;
		Если ЗначениеЗаполнено(ТипТС) Тогда
			ОтборСтруктура.Вставить("ТипТС", ТипТС);
		КонецЕсли;
	КонецЕсли;
		
	ПараметрыФормы.Вставить("ТипТС", ТипТС);
	ПараметрыФормы.Вставить("Перевозчик", Перевозчик);
	//ПараметрыФормы.Вставить("Отбор", сткОтбор);
		
	Если ОткрытьСписокДоступности Тогда
		//сткПараметры.Вставить("МножественныйВыбор", Ложь);
		ПараметрыФормы.Вставить("ТекущаяСтрока", ТранспортноеСредство);
		ДопИнтервалДней = 1;
		НачалоИнтервалаВыделения = упКорректировкаРейса.ПлановоеНачалоВыполнения;
		Если Не ЗначениеЗаполнено(НачалоИнтервалаВыделения) Тогда
			НачалоИнтервалаВыделения = ТекущаяДата();
		КонецЕсли; 
		КонецИнтервалаВыделения = упКорректировкаРейса.ПлановоеОкончаниеВыполнения;
		Если Не ЗначениеЗаполнено(КонецИнтервалаВыделения) Тогда
			КонецИнтервалаВыделения = НачалоИнтервалаВыделения + 1*24*60*60;
		КонецЕсли; 
		ПараметрыФормы.Вставить("НачалоИнтервалаВыделения", НачалоИнтервалаВыделения);
		ПараметрыФормы.Вставить("КонецИнтервалаВыделения", КонецИнтервалаВыделения);
		ПараметрыФормы.Вставить("НачалоПериода", НачалоИнтервалаВыделения - ДопИнтервалДней*24*60*60);
		ПараметрыФормы.Вставить("ОкончаниеПериода", КонецИнтервалаВыделения + ДопИнтервалДней*24*60*60);
	КонецЕсли;
	
	Если РежимФормированияКонтейнеров Тогда
		ПараметрыФормы.Вставить("РежимФормированияКонтейнеров", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбОтменеЗаявок(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		мсвПредложений 	= ДополнительныеПараметры.ДокументыОтменаАвтоматомПредложения;
		мсвЗаявокНаТС	= ДополнительныеПараметры.ДокументыОтменаАвтоматомЗаявкиНаТС;
		
		Отказ = Ложь;
		ТекстОшибки = "";
		
		ОтменитьДокументы(мсвПредложений, мсвЗаявокНаТС, ТекстОшибки, Отказ);
		
		Если НЕ Отказ Тогда
			Для каждого ЗаявкаНаТС Из мсвЗаявокНаТС Цикл
				Оповестить("СменаСтатуса",,ЗаявкаНаТС);
			КонецЦикла;

			УстановитьСтатусОтменен(ТекстОшибки);
		Иначе
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - Обработать разделение точки
//
// Параметры:
//  РезультатЗакрытия		 - 	 - 
//  ДополнительныеПараметры	 - 	 - 
//
Процедура ОбработатьРазделениеТочки(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		ИдентификаторТочки 	= РезультатЗакрытия.ИдентификаторТочки;
		СписокОпераций		= РезультатЗакрытия.СписокОпераций;
		РазделитьРаботыВТочкеНаСервере(ИдентификаторТочки, СписокОпераций);
		Модифицированность 	= Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура НастроитьПанельНавигации()
	ИспользоватьСпособПрохожденияМаршрутаОтдельнымДокументом =  Истина
													И упРаботаСоСтатусамиКлиентСервер.РейсПереданКВыполнению(Статус)
													И СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ОтдельнымДокументом;
	сткНастроек = Новый Структура;
	сткНастроек.Вставить("УправлятьПроцессомПодбораПеревозчика", УправлятьПроцессомПодбораПеревозчика);
	сткНастроек.Вставить("ИспользоватьСпособПрохожденияМаршрутаОтдельнымДокументом", ИспользоватьСпособПрохожденияМаршрутаОтдельнымДокументом);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);
КонецПроцедуры

&НаСервере
Функция РассчитатьПлановыеРасходыСРасшифровкойНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	сткПриемникТабличныйДокумент = Новый Структура("Приемник, ЗапрашиваемыйУровеньСообщений",ТабличныйДокумент,2);
	мсвПриемников = Новый Массив();
	мсвПриемников.Добавить(сткПриемникТабличныйДокумент);
	сткТрассировка = упСервисныеФункцииКлиентСервер.Новый_СтруктураТрассировки(мсвПриемников, "РасчетыПоПравиламРасчета");
	упТарификацияВызовСервера.Рассчитать(Объект, , сткТрассировка);
	
	Возврат мсвПриемников;
КонецФункции

#Область Взаиморасчеты

&НаКлиенте
Процедура ЗагрузитьВзаиморасчетыСПартнерами()
	ЭтаФорма.ВзаиморасчетыЗагрузить = Истина;
	Если РазрешеноЗагружатьВзаиморасчеты 
		И НЕ ВзаиморасчетыЗагружаются Тогда
		ВзаиморасчетыЗагружаются 	= Истина;
		ВзаиморасчетыЗагрузить 		= Ложь;
		ВзаиморасчетыАдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов", 1);
		ЗагрузитьВзаиморасчетыСПартнерамиНаСервере();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВзаиморасчетыСПартнерамиНаСервере()
	Если РазрешеноЗагружатьВзаиморасчеты 
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Перевозчик) Тогда
		
		Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Обновить;
		мсвПараметры = Новый Массив;
		мсвПараметры.Добавить(Перевозчик);
		мсвПараметры.Добавить(Объект.Организация);
		мсвПараметры.Добавить(ВзаиморасчетыАдресХранилища);
		ФоновыеЗадания.Выполнить("упВзаиморасчеты.СостояниеВзаиморасчетовСПоставщиками", мсвПараметры);	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗагрузкуВзаиморасчетов()
	текРезультат = ПолучитьИзВременногоХранилища(ВзаиморасчетыАдресХранилища);
	Если НЕ текРезультат = Неопределено Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗагрузкуВзаиморасчетов");		
		ВзаиморасчетыЗагружаются = Ложь;
		Если НЕ ВзаиморасчетыЗагрузить Тогда
			Если НЕ ТипЗнч(текРезультат) = Тип("Булево") Тогда
				СформироватьТаблицуВзаиморасчетов(текРезультат);
				Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Успешно;
				ПоказатьОповещениеПользователя(НСтр("ru = 'Взаиморасчеты загружены.'"),,, БиблиотекаКартинок.Успешно32);
			Иначе
				Элементы.ГруппаСостояниеВзаиморасчетов.Картинка = БиблиотекаКартинок.Остановить;	
				ПоказатьОповещениеПользователя(НСтр("ru = 'Не удалось загрузить взаиморасчеты.'"),, НСтр("ru = 'Подробности см. в Журнале регистрации.'"), БиблиотекаКартинок.Ошибка32);
			КонецЕсли;		
		Иначе
			ЗагрузитьВзаиморасчетыСПартнерами();		
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьТаблицуВзаиморасчетов(СтрокаXML)
	
	СостояниеВзаиморасчетов.Очистить();
	
	тбзВзаиморасчеты = упВзаиморасчеты.СформироватьТаблицуВзаиморасчетовСПоставщиками(СтрокаXML);
	
	текСхемаКомпоновкиДанных	= ПолучитьОбщийМакет("упВзаиморасчетыСПоставщиками");
	
	текКомпоновщикМакетаКомпоновкиДанных	= Новый КомпоновщикМакетаКомпоновкиДанных;
	
	текМакетКомпоновкиДанных	= текКомпоновщикМакетаКомпоновкиДанных.Выполнить(текСхемаКомпоновкиДанных, текСхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	текПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	
	текПроцессорКомпоновкиДанных.Инициализировать(текМакетКомпоновкиДанных, Новый Структура("тбзВзаиморасчеты", тбзВзаиморасчеты), );
	
	текПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	текПроцессорВыводаРезультата.УстановитьДокумент(СостояниеВзаиморасчетов);
	
	текПроцессорВыводаРезультата.Вывести(текПроцессорКомпоновкиДанных);

		
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВыполнитьВызовФормыВыбораСотрудника(ОписаниеОповещения)

	сткПараметры = Новый Структура;
	сткПараметры.Вставить("Перевозчик", Перевозчик);
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		сткПараметры.Вставить("ТС", ТранспортноеСредство);
	КонецЕсли;
	ОткрытьФорму("Справочник.упСотрудники.ФормаВыбора", сткПараметры,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры // ВыполнитьВызовФормыВыбораСотрудника()

&НаКлиенте
// Процедура завершения после закрытия формы выбора сотрудника.
//
Процедура Водитель1НачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Водитель1 = Результат;
		Если ВедетсяУчетДокументов Тогда
			Отказ = Ложь;
			ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель1, Отказ);	
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора сотрудника.
//
Процедура Водитель2НачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Водитель2 = Результат;
		Если ВедетсяУчетДокументов Тогда
			Отказ = Ложь;
			ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель2, Отказ);	
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после закрытия формы выбора сотрудника.
//
Процедура ЭкспедиторНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Экспедитор = Результат;
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель, Отказ)
	
	упРейс.ПроверитьНаличиеНеобходимыхДокуметовПоВодителюИТС(ТранспортноеСредство, Водитель, Отказ);	
		
КонецПроцедуры

&НаСервере
Функция ВидПеревозкиПоЗаданиям()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Возврат  текКорректировкаМаршрутаОбъект.ВидПеревозкиПоЗаданиям();
КонецФункции

&НаСервереБезКонтекста
Функция ДокументыПрепятствующиеОтменеРейса(Рейс)
	
	мсвРейсы = Новый Массив;
	мсвРейсы.Добавить(Рейс);
	сткДокументы = упРейс.ДокументыПрепятствующиеОтменеРейса(мсвРейсы);
	
	Возврат сткДокументы;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьДокументы(мсвПредложения, мсвЗаявкиНаТС, ТекстОшибки, Отказ)

	упРейс.ОтменитьДокументыПоРейсу(мсвПредложения, мсвЗаявкиНаТС, ТекстОшибки, Отказ);
			
КонецПроцедуры

&НаСервере
Процедура ПересчитатьПорядокОтгрузки()
	
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ПроставитьПорядокОтгрузки();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаданияПослеУдаления(Элемент)
	
	ПересчитатьИтогиПоРейсу(Истина);
	//ОбновитьПредставлениеПоказателейЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПересчитыватьИтоги = Истина;
	
	Если НЕ ОтменаРедактирования Тогда
		Если НеобходимоДобавитьСвязанныеГрузовыеМеста() Тогда
			ПересчитыватьИтоги = Ложь;	
			ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросОСвязанныхГрузовыхМестах",ЭтаФорма);
			ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru = 'Существуют связанные грузовые места, не включенные в рейс. Добавить их в рейс?'"), РежимДиалогаВопрос.ДаНет);	
		КонецЕсли;
	КонецЕсли;

	Если НЕ (НоваяСтрока И ОтменаРедактирования) И ПересчитыватьИтоги Тогда
		ПересчитатьИтогиПоРейсу(Истина);
		//ОбновитьПредставлениеПоказателейЗагрузки();
	КонецЕсли;
	
	УстановитьЗаголовокМаршрута();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСтраницуДанныеТахографа()
	Если ИспользуетсяЗагрузкаДанныхИзФайловТахографа Тогда
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ДанныеТахографа.Параметры.УстановитьЗначениеПараметра("Рейс", Объект.Ссылка);		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставленияПоказателейЗагрузки()
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	Если УчитыватьМассу Тогда
		МассаПредставление = сткПредставления.Масса;
	КонецЕсли;
	Если УчитыватьОбъем Тогда
		ОбъемПредставление = сткПредставления.Объем;
	КонецЕсли;
	Если УчитыватьКоличествоМест Тогда
		КоличествоМестПредставление = сткПредставления.КоличествоМест;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция НеобходимоДобавитьСвязанныеГрузовыеМеста()
	ЗаполнитьНедобавленныеСвязанныеГрузовыеМеста();
	Возврат упКорректировкаРейса.НеДобавленныеСвязанныеГрузовыеМеста.Количество() > 0;
КонецФункции

&НаСервере
Процедура ЗаполнитьНедобавленныеСвязанныеГрузовыеМеста()

	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Если Ложь Тогда // Для контекстной подсказки
	    текКорректировкаМаршрутаОбъект = Обработки.упКорректировкаРейса.Создать();
	КонецЕсли;
	текКорректировкаМаршрутаОбъект.ЗаполнитьНедобавленныеСвязанныеГрузовыеМеста();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОСвязанныхГрузовыхМестах(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = Ложь;
		Для каждого СтрокаГрузовоеМесто Из упКорректировкаРейса.НеДобавленныеСвязанныеГрузовыеМеста Цикл
			НоваяСтрока = упКорректировкаРейса.Задания.Добавить();
			НоваяСтрока.СтрокаДокумента = Истина;
			НоваяСтрока.Задание			= СтрокаГрузовоеМесто.Задание;
			НоваяСтрока.ГрузовоеМесто	= СтрокаГрузовоеМесто.ГрузовоеМесто;
			НоваяСтрока.КоличествоГрузов= СтрокаГрузовоеМесто.КоличествоГрузов;
			
			сткЗначенияСтрокиЗаданийДоРедактирования = упКорректировкаРейса.сткЗначенияСтрокиЗаданийДоРедактирования;
			
			Для каждого текПоле Из сткЗначенияСтрокиЗаданийДоРедактирования Цикл
				сткЗначенияСтрокиЗаданийДоРедактирования.Вставить(текПоле.Ключ, Неопределено);
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаГрузовоеМесто);
			ЗаполнитьВспомогательныеПоляЗаданийНаСервере(НоваяСтрока.НомерСтроки);
			ОбработатьСтрокуЗаданияНаСервере(НоваяСтрока.НомерСтроки, Истина,,Ложь, Отказ);
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПересчитатьИтогиПоРейсу(Истина);
	//ОбновитьПредставлениеПоказателейЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокМаршрута()

	Элементы.ГруппаМаршрут.Заголовок = СтрШаблон("Маршрут (%1)", Формат(упКорректировкаРейса.Маршрут.Количество(),"ЧН=0; ЧГ=0"));

КонецПроцедуры // УстановитьЗаголовокМаршрута()

&НаКлиенте
Функция РежимКорректировки()
	
	Возврат упПрохождениеТочкиКлиентСервер.РежимКорректировки(СпособПрохожденияТочекМаршрута, Статус);
	
КонецФункции

&НаСервере
Функция ЕстьОтмененныеРаботы()
	сткОтбор = Новый Структура("Отменена", Истина);
	мсвСтроки = упКорректировкаРейса.Работы.НайтиСтроки(сткОтбор);
	Возврат мсвСтроки.Количество() > 0;
КонецФункции

&НаКлиенте
Процедура СообщитьОНарушенииМаршрута(Предшественник, Потомок)
	
	ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка изменения маршрута! Точка с адресом ""%1"" должна предшествовать точке с адресом ""%2""'"), Предшественник, Потомок);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеФактическогоВремениВыполненияНаСервере()
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	текКорректировкаМаршрутаОбъект.ОбработатьИзменениеФактическогоВремениВыполнения();
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе")
			И Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.КВыполнению") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Запрещено добавлять точки'"), Объект.Ссылка, "Маршрут",,Отказ);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтметитьВсеНаСервере()
	упПрохождениеТочкиКлиентСервер.ИзменитьВсеТочкиПройденыРаботыВыполнены(упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
	УстановитьКнопкиСтатусаРейса();
КонецПроцедуры

&НаСервере
Процедура СнятьВсеНаСервере()
	упПрохождениеТочкиКлиентСервер.ИзменитьВсеТочкиНеПройденыНеВыполнены(упКорректировкаРейса.Маршрут, упКорректировкаРейса.Работы, упКорректировкаРейса.Задания);
	УстановитьКнопкиСтатусаРейса();
КонецПроцедуры

&НаСервере
Процедура РазделитьРаботыВТочкеНаСервере(ИдентификаторТочки, СписокОпераций)
	текКорректировкаМаршрутаОбъект = РеквизитФормыВЗначение("упКорректировкаРейса");
	Отказ			= Ложь;
	упПрохождениеТочки.РазделитьРаботыВТочке(текКорректировкаМаршрутаОбъект, ИдентификаторТочки, СписокОпераций,,,Отказ);
	ЗначениеВРеквизитФормы(текКорректировкаМаршрутаОбъект, "упКорректировкаРейса");	
КонецПроцедуры

&НаСервере
Функция РазрешеноРазделятьРаботыНаСервере(СтрокаТочка)
	Результат = упПрохождениеТочки.РазрешеноРазделятьРаботы(СтрокаТочка, упКорректировкаРейса.Работы);
	Возврат Результат;		
КонецФункции

&НаКлиенте
Процедура ПереключитьОжиданиеПослеРабот(Команда)
	
	упРейсКлиентСервер.ПереключитьОжиданиеПослеРабот(Элементы.Маршрут.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросСоздатьПутевойЛист(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ТекстОшибки = "";
		Результат = СоздатьПутевойЛистИПеревестиКВыполнению(ТекстОшибки);
		
		Если НЕ Результат Тогда
			ПоказатьПредупреждение(, ТекстОшибки);
		ИначеЕсли ЗначениеЗаполнено(ТекстОшибки) Тогда	
			упСервисныеФункцииКлиент.ПоказатьОповещениеПользователяНаше(ТекстОшибки, Истина);
		КонецЕсли;
		
		Если Результат Тогда
			ПослеЗаписи(Новый Структура("РежимЗаписи, РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный));
			ОповеститьОбИзменении(Объект.Ссылка);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СоздатьПутевойЛистИПеревестиКВыполнению(ТекстОшибки)
	
	Отказ = Ложь;
	мсвРейсы =  Новый Массив;
	мсвРейсы.Добавить(Объект.Ссылка);
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	ПутевойЛист = упПутевойЛистУчетГСМ.СформироватьПутевойЛистПоРейсам(мсвРейсы, , ,Отказ);
	
	Если НЕ Отказ Тогда
		Результат = ИзменитьСтатусРейса(Перечисления.упСтатусыРейса.КВыполнению, ТекстОшибки);
		Если Ложь
			Или НЕ Результат
			Или ЗначениеЗаполнено(ТекстОшибки)
		Тогда
			Отказ = Истина;	
		КонецЕсли;
	Иначе
		ТекстОшибки = Нстр("ru = 'Не удалось сформировать путевой лист по рейсу, подробности в журнале регистрации'");
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	Иначе	
		ОтменитьТранзакцию();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьКнопкиСтатусаРейса()
	сткПараметрыУстановкиКнопокСтатуса = Новый Структура;
	сткПараметрыУстановкиКнопокСтатуса.Вставить("ПеревозкаТранспортнойКомпанией", ПеревозкаТранспортнойКомпанией);
	сткПараметрыУстановкиКнопокСтатуса.Вставить("СпособПрохожденияТочекМаршрута", СпособПрохожденияТочекМаршрута);
	сткПараметрыУстановкиКнопокСтатуса.Вставить("ЕстьОтмененныеРаботы",ЕстьОтмененныеРаботы());
	сткПараметрыУстановкиКнопокСтатуса.Вставить("ЕстьОтложенныеРаботы", упКорректировкаРейса.ЕстьОтложенныеРаботы);
	сткПараметрыУстановкиКнопокСтатуса.Вставить("ЭтоОперацияСКонтейнером", Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами);
	сткПараметрыУстановкиКнопокСтатуса.Вставить("ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера", ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера);
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаРейса(Объект.Ссылка, Статус, Элементы, сткПараметрыУстановкиКнопокСтатуса);
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусФормы()
	ЭтаФорма.Статус = Документы.упРейс.ПолучитьСтатус(Объект.Ссылка, Состояние);
	Если ЗначениеЗаполнено(Состояние) Тогда
		Если Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
			ЭтаФорма.СтатусПредставление = НСтр(СтрШаблон("ru = '%1'", Состояние));
		Иначе
			ЭтаФорма.СтатусПредставление = НСтр(СтрШаблон("ru = '%1 (%2)'", Статус, Состояние));
		КонецЕсли;
	Иначе
		ЭтаФорма.СтатусПредставление = НСтр(СтрШаблон("ru = '%1'", Статус));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыВыбораТС()
	ПараметрыФормы = Новый Структура();
	ЗаполнитьПараметрыФормыДляСпискаТС(ПараметрыФормы);
	ПараметрыВыбораМассив = Новый Массив;
	Для каждого ПараметрФормы Из ПараметрыФормы Цикл
		Если ПараметрФормы.Ключ = "Отбор" Тогда
			Продолжить;
		КонецЕсли;
		ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора(ПараметрФормы.Ключ, ПараметрФормы.Значение));
	КонецЦикла;
	
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("РегНомер", Истина));
	
	Элементы.ТранспортноеСредство.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыВыбораТипаТС()
	ПараметрыВыбораМассив = Новый Массив;
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("ВидПеревозки", Объект.ВидПеревозки));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Перевозчик", Перевозчик));
	ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Организация", Объект.Организация));
	Элементы.ТипТС.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаСервереБезКонтекста
// Получим список сотрудников
//
// Параметры:
//  Перевозчик  - СправочникСсылка.упПартнеры - Ссылка на перевозчика.
//
// Возвращаемое значение:
//   СписокЗначений   - Ссылки на сотрудников.
//
Функция ПолучитьСотрудников(Перевозчик)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСотрудники.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(упСотрудники.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.упСотрудники КАК упСотрудники
	|ГДЕ
	|	ВЫБОР
	|			КОГДА упСотрудники.СотрудникЧислитсяВНесколькихОрганизациях
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ упСотрудники.Партнер = &Партнер
	|		КОНЕЦ";
	Запрос.УстановитьПараметр("Партнер", Перевозчик);
	
	Выборка = Запрос.Выполнить().Выбрать();
	СписокЗначений = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		СписокЗначений.Добавить(Выборка.Ссылка,Выборка.Представление);
	КонецЦикла;
	
	Возврат СписокЗначений;
	
КонецФункции // ПолучитьСотрудников()

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти