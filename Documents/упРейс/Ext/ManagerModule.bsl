#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Если Данные.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами Тогда
		Представление = СтрШаблон(Нстр("ru = 'Операции с контейнером %1 от %2'"), Данные.Номер, Данные.Дата);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	Поля.Добавить("ВидТранспортнойУслуги");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если ВидФормы = "ФормаОбъекта" Тогда
		//ДокументСсылка = Неопределено;
		//ВидТранспортнойУслуги = Неопределено;
		//ЗначенияЗаполнения = Неопределено;
		//
		//// Открывается существующая заявка.
		//Если Параметры.Свойство("Ключ", ДокументСсылка) Тогда
		//	ВидТранспортнойУслуги = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидТранспортнойУслуги"); 
		//	
		//// Создается новая.	
		//ИначеЕсли Истина
		//	И Параметры.Свойство("ЗначенияЗаполнения",ЗначенияЗаполнения)
		//	И ЗначенияЗаполнения.Свойство("ВидТранспортнойУслуги",ВидТранспортнойУслуги) Тогда
		//	
		//// Копируется.	
		//ИначеЕсли Истина
		//	И Параметры.Свойство("ЗначениеКопирования",ЗначенияЗаполнения) 
		//	И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЗначенияЗаполнения, "ВидТранспортнойУслуги") Тогда
		//	
		//	ВидТранспортнойУслуги =   ЗначенияЗаполнения.ВидТранспортнойУслуги
		//	
		//КонецЕсли;
		//
		//Если ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда	
		//	СтандартнаяОбработка = Ложь;
		//	ВыбраннаяФорма = "ФормаДокументаПассажирскиеПеревозки";
		//КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаСписка" Тогда	
		
		ВидТранспортнойУслуги = Неопределено;

		Если Параметры.Свойство("ВидТранспортнойУслуги", ВидТранспортнойУслуги) Тогда
			
			Если  ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаСпискаОперацииСКонтейнерами";
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда	
		
		ВидТранспортнойУслуги = Неопределено;

		Если Параметры.Свойство("ВидТранспортнойУслуги", ВидТранспортнойУслуги) Тогда
			
			Если ВидТранспортнойУслуги = ПредопределенноеЗначение("Перечисление.упВидыТранспортныхУслуг.ОперацииСКонтейнерами") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораОперацииСКонтейнерами";
			КонецЕсли;

		КонецЕсли;
	
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
	
#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа
//
// Параметры:
//  Ссылка  - ДокументСсылка.упРейс - документ, которого необходимо получить.
//
// Возвращаемое значение:
//   ПеречислениеСсылка   - Ссылка на перечисление.
//
Функция ПолучитьСтатус(Ссылка, Состояние = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыРейса.Новый;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСтатусыРейсовСрезПоследних.Статус КАК Статус,
	|	упРейс.ПометкаУдаления КАК ПометкаУдаления,
	|	упСтатусыРейсовСрезПоследних.Состояние КАК Состояние
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, Документ = &Документ) КАК упСтатусыРейсовСрезПоследних
	|		ПО (упСтатусыРейсовСрезПоследних.Документ = упРейс.Ссылка)
	|ГДЕ
	|	упРейс.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Состояние = Выборка.Состояние;
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Состояние = Выборка.Состояние;
		Возврат Перечисления.упСтатусыРейса.Отменен;
	Иначе	
		Возврат Перечисления.упСтатусыРейса.Новый;
	КонецЕсли;
	
КонецФункции		

// Функция возвращает текущий статус документа в WMS
//
// Параметры:
//  Ссылка  - ДокументСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упСтатусыДокументовWMS - Результат выполнения.
//
Функция ПолучитьСтатусWMS(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыРейсовWMS.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.упСтатусыРейсовWMS.СрезПоследних(, Документ = &Документ) КАК упСтатусыРейсовWMS";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Статус;
	Иначе	
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции


// Функция возвращает дату установки указанного статуса
//
// Параметры:
//  Ссылка - ДокументСсылка.упРейс					 - Ссылка на документ.
//  Статус - ПеречислениеСсылка.упСтатусыРейса, Массив	 - статусы заданий.
// 
// Возвращаемое значение:
//  Дата - дата перевода в статус. 
//
Функция ПолучитьДатуПереводаВСтатус(Ссылка, Статус) Экспорт
	
	Результат 	= '00010101';
	мсвСтатусы 		= Новый Массив;
	Отказ			= Ложь;
	
	Если ТипЗнч(Статус) = Тип("Массив") Тогда
		мсвСтатусы = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Статус);			
	ИначеЕсли ТипЗнч(Статус) = Тип("ПеречислениеСсылка.упСтатусыРейса") Тогда	
		мсвСтатусы.Добавить(Статус);	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ссылка) 
			ИЛИ мсвСтатусы.Количество() = 0 Тогда 
		Отказ = Истина;;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	упСтатусыРейсов.Период
		|ИЗ
		|	РегистрСведений.упСтатусыРейсов.СрезПоследних(
		|			,
		|			Документ = &Документ
		|				И Статус В (&мсвСтатусы)) КАК упСтатусыРейсов";
		Запрос.УстановитьПараметр("Документ", 	Ссылка);
		Запрос.УстановитьПараметр("мсвСтатусы", мсвСтатусы);
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Результат = Выборка.Период
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПолучитьДатуПереводаВСтатус()

// Функция возвращает гараж, из готового выезжает транспортное средство в начале рейса.
//
// Параметры:
//  Ссылка  - ДокументСсылка.упРейс - документ, которого необходимо получить.
//
// Возвращаемое значение:
//   СправочникСсылка.упГаражи   - Ссылка на гараж.
//
Функция ПолучитьГаражВыезда(Ссылка) Экспорт
	
	Гараж = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Гараж
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|ГДЕ
	|	упМаршрутПоРейсуСрезПоследних.ТипТочки = ЗНАЧЕНИЕ(Перечисление.упТипыТочекМаршрута.ТочкаОтправления)";
	Запрос.УстановитьПараметр("Рейс", Ссылка);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Гараж = Выборка.Гараж;
	КонецЕсли; 
	
	Возврат Гараж;
	
КонецФункции

// Формирование заявки на транспортное средство
//
// Параметры:
//  тбзПеревозчики  - ТаблицаЗначений - Данные.
//  Рейс  - ДокументСсылка.упРейс - документ, которого необходимо получить.
//  ЭтоТендер - Булево - Истина, если на право на выполнение рейса выдается на конкурсной основе.
//  Этап  - Ссылка - Этап объекта.
//
// Возвращаемое значение:
//   ДокументСсылка.упЗаявкаНаТС   - Ссылка на документ.
//
Функция СформироватьЗаявкиНаТС(тбзПеревозчики, Рейс, ЭтоТендер, Этап = Неопределено) Экспорт
	
	мсвЗаявкиНаТС = Новый Массив;
	КонтрагентНеСуществует = тбзПеревозчики.Колонки.Найти("КонтрагентПеревозчика")=Неопределено;
	Для каждого текПеревозчик Из тбзПеревозчики Цикл
		
		текДокументЗаявкаНаТС = Документы.упЗаявкаНаТС.СоздатьДокумент();
		текДокументЗаявкаНаТС.Дата 			= ТекущаяДатаСеанса();
		текДокументЗаявкаНаТС.Рейс			= Рейс;
		текДокументЗаявкаНаТС.Тендер		= ЭтоТендер;
		текДокументЗаявкаНаТС.Этап      	= Этап;
		
		текКонтрагентПеревозчика = Справочники.упКонтрагенты.ПустаяСсылка();
		Если КонтрагентНеСуществует Тогда
			текКонтрагентПеревозчика = Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(текПеревозчик.Перевозчик);
		Иначе
			текКонтрагентПеревозчика = текПеревозчик.КонтрагентПеревозчика;
		КонецЕсли; 
		
		ЗаполнитьЗаявкуНаТС(текДокументЗаявкаНаТС, Рейс, текПеревозчик.Перевозчик, текКонтрагентПеревозчика, текПеревозчик.Соглашение);
		
		// Для заполнения и расчета плановых расходов
		//текДокументЗаявкаНаТС.Записать(РежимЗаписиДокумента.Запись);
		
		текДокументЗаявкаНаТС.Записать(РежимЗаписиДокумента.Проведение);
		
		мсвЗаявкиНаТС.Добавить(текДокументЗаявкаНаТС.Ссылка);
		
	КонецЦикла;
			
	Возврат мсвЗаявкиНаТС;		
	
КонецФункции // СформироватьЗаявкиНаТС()

Функция СоздатьЗаявкуНаТСПоЭтапу(Этап, ЭтапСхемы = Неопределено, Рейс, ЭтоТендер, СписокПеревозчиков = Неопределено, ВидТранспорта, ТекстОшибки, Отказ)
	
	Результат = Ложь;
	
	Если ЭтапСхемы = Неопределено Тогда
		ЭтапСхемы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап, "ЭтапСхемыВыбораПеревозчика");
	КонецЕсли; 
	
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
		
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упЭтапПодбораПеревозчика");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Этап,,, Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Этап, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат Ложь;
	КонецПопытки;
	
	тзПеревозчики = Документы.упЭтапПодбораПеревозчика.ПодобратьПеревозчиков(Рейс, ЭтапСхемы, ЭтоТендер, СписокПеревозчиков, ВидТранспорта);
	
	Если тзПеревозчики.Количество() Тогда
		мсвЗаявки = СформироватьЗаявкиНаТС(тзПеревозчики, Рейс, ЭтоТендер, Этап);
		Если мсвЗаявки.Количество() Тогда
			Результат = Истина;
			ЭтапОбъект = Этап.ПолучитьОбъект();
			Для каждого текЗаявка Из мсвЗаявки Цикл
				НоваяСтрока = ЭтапОбъект.Заявки.Добавить();
				НоваяСтрока.ЗаявкаНаТС = текЗаявка;
			КонецЦикла;
			
			ЭтапОбъект.Записать();
		КонецЕсли; 
	КонецЕсли; 
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе
		ЗафиксироватьТранзакцию();
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

// Выполнить заполнение заявки
//
// Параметры:
//  ЗаявкаНаТСОбъект  - ДокументОбъект - Документ.
//  Рейс  - ДокументСсылка.упРейс - документ, которого необходимо получить.
//  Перевозчик  - СправочниСсылка - Ссылка на перевозчика.
//  КонтрагентПеревозчика  - СправочниСсылка - Контрагент.
//  Соглашение  - СправочниСсылка - Соглашение.
//
Процедура ЗаполнитьЗаявкуНаТС(ЗаявкаНаТСОбъект,Рейс, Перевозчик = Неопределено, КонтрагентПеревозчика = Неопределено, Соглашение = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения,
	|	упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.ТипТС,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство,
	|	упПеревозчикПоРейсуСрезПоследних.Перевозчик,
	|	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика,
	|	упПеревозчикПоРейсуСрезПоследних.Соглашение
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[0].Пустой() Тогда
		Выборка = Результат[0].Выбрать();
		Выборка.Следующий();
		ЗаявкаНаТСОбъект.НачалоРейса	= Выборка.ПлановоеНачалоВыполнения;
		ЗаявкаНаТСОбъект.ОкончаниеРейса	= Выборка.ПлановоеОкончаниеВыполнения;
	КонецЕсли; 
	Если Не Результат[1].Пустой() Тогда
		Выборка = Результат[1].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЗаявкаНаТСОбъект, Выборка);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Перевозчик) Тогда
		ЗаявкаНаТСОбъект.Перевозчик		= Перевозчик;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтрагентПеревозчика) Тогда
		ЗаявкаНаТСОбъект.КонтрагентПеревозчика = КонтрагентПеревозчика;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Соглашение) Тогда
		ЗаявкаНаТСОбъект.Соглашение		= Соглашение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаявкаНаТСОбъект.Соглашение) Тогда
		ЗаявкаНаТСОбъект.Валюта			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявкаНаТСОбъект.Соглашение, "ГруппаТарифов.Валюта");
	КонецЕсли;
		
	сткПараметрыГрузаОбщие	= ПолучитьПараметрыГрузаОбщиеПоРейсу(Рейс);
	
	ЗаполнитьЗначенияСвойств(ЗаявкаНаТСОбъект, сткПараметрыГрузаОбщие);
	ЗаявкаНаТСОбъект.МаксимальнаяВысота	= сткПараметрыГрузаОбщие.Высота;
	ЗаявкаНаТСОбъект.МаксимальнаяДлина	= сткПараметрыГрузаОбщие.Длина;
	ЗаявкаНаТСОбъект.МаксимальнаяШирина	= сткПараметрыГрузаОбщие.Ширина;
	ЗаявкаНаТСОбъект.ОценочнаяСтоимость	= сткПараметрыГрузаОбщие.ОценочнаяСтоимость;
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах") Тогда
		ЗаявкаНаТСОбъект.ЧасовойПояс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ЧасовойПояс");
	КонецЕсли;  
	
КонецПроцедуры

// Функция - формирует заявку на ТС на основании рейса.
//
// Параметры:
//  Рейс		 - 	ДокументСсылка.упРейс - ссылка на рейс.
//  ЭтоТендер	 - 	Булево - Истина, если на право на выполнение рейса выдается на конкурсной основе.
//  СписокПеревозчиков	 - 	Массив - список перевозчиков.
//  ТекстОшибки	 - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  Массив - содержит ДокументСсылка.упЗаявкаНаТС.
//
Функция СформироватьЗаявкуНаТСПоРейсу(Рейс, ЭтоТендер = Ложь, СписокПеревозчиков = Неопределено, ТекстОшибки = "", Отказ) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.ВидПеревозки,
	|	упВидыПеревозок.ИспользуютсяПравилаПодбораПеревозчиков КАК ИспользуютсяПравилаПодбораПеревозчиков,
	|	упВидыПеревозок.СхемаВыбораПеревозчика КАК СхемаВыбораПеревозчика,
	|	ВЫБОР
	|		КОГДА упИспользуютсяИтерацииОформленияЗаявокНаТС.Значение
	|				И упВидыПеревозок.ВыборПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально))
	|				И упВидыПеревозок.ВыборТС = ЗНАЧЕНИЕ(Перечисление.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС)
	|				И НЕ упВидыПеревозок.СхемаВыбораПеревозчика = ЗНАЧЕНИЕ(Справочник.упСхемыВыбораПеревозчика.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИтеративныйПодборПеревозчика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|			ТОГДА упВидыПеревозок.Перевозчик
	|		ИНАЧЕ ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|	КОНЕЦ КАК Перевозчик,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Соглашение, ЗНАЧЕНИЕ(Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка)) КАК Соглашение,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТипТС, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)) КАК ТипТС,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство
	|ПОМЕСТИТЬ втРейс
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, Документ = &Рейс) КАК упСтатусыРейсов
	|		ПО упРейс.Ссылка = упСтатусыРейсов.Документ
	|			И (упСтатусыРейсов.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.КПланированиюТС))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозок
	|		ПО упРейс.ВидПеревозки = упВидыПеревозок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних
	|		ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс,
	|	Константа.упИспользуютсяИтерацииОформленияЗаявокНаТС КАК упИспользуютсяИтерацииОформленияЗаявокНаТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейс.Рейс,
	|	упЭтапПодбораПеревозчика.Ссылка КАК Этап,
	|	упЭтапПодбораПеревозчика.НомерЭтапа,
	|	упЭтапПодбораПеревозчика.Отклонен,
	|	упЭтапПодбораПеревозчика.Завершен,
	|	упЭтапПодбораПеревозчика.ЗавершенУспешно,
	|	упЭтапПодбораПеревозчика.ЭтапСхемыВыбораПеревозчика,
	|	упЭтапПодбораПеревозчика.Тендер,
	|	упЭтапПодбораПеревозчика.ВидТранспорта
	|ПОМЕСТИТЬ втЭтапыПодбора
	|ИЗ
	|	втРейс КАК втРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЭтапПодбораПеревозчика КАК упЭтапПодбораПеревозчика
	|		ПО втРейс.Рейс = упЭтапПодбораПеревозчика.Рейс
	|			И (втРейс.ИтеративныйПодборПеревозчика)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втЭтапыПодбора.НомерЭтапа) КАК КрайнийНомерЭтапа
	|ПОМЕСТИТЬ втКрайнийНомерЭтапа
	|ИЗ
	|	втЭтапыПодбора КАК втЭтапыПодбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЭтапыПодбора.Этап,
	|	втЭтапыПодбора.НомерЭтапа,
	|	втЭтапыПодбора.Отклонен,
	|	втЭтапыПодбора.Завершен,
	|	втЭтапыПодбора.ЗавершенУспешно,
	|	втЭтапыПодбора.ЭтапСхемыВыбораПеревозчика,
	|	втЭтапыПодбора.Тендер,
	|	втЭтапыПодбора.ВидТранспорта
	|ПОМЕСТИТЬ втКрайнийЭтапПодготовка
	|ИЗ
	|	втЭтапыПодбора КАК втЭтапыПодбора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКрайнийНомерЭтапа КАК втКрайнийНомерЭтапа
	|		ПО втЭтапыПодбора.НомерЭтапа = втКрайнийНомерЭтапа.КрайнийНомерЭтапа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаявки.НомерСтроки,
	|	упЗаявки.ЗаявкаНаТС
	|ПОМЕСТИТЬ втЗаявкиПоЭтапу
	|ИЗ
	|	втКрайнийЭтапПодготовка КАК втКрайнийЭтап
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЭтапПодбораПеревозчика.Заявки КАК упЗаявки
	|		ПО втКрайнийЭтап.Этап = упЗаявки.Ссылка
	|			И (НЕ втКрайнийЭтап.Тендер)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаявкиПоЭтапу.ЗаявкаНаТС
	|ПОМЕСТИТЬ втКрайняяИтерация
	|ИЗ
	|	втЗаявкиПоЭтапу КАК втЗаявкиПоЭтапу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(втЗаявкиПоЭтапу.НомерСтроки) КАК НомерСтроки
	|		ИЗ
	|			втЗаявкиПоЭтапу КАК втЗаявкиПоЭтапу) КАК ВложенныйЗапрос
	|		ПО втЗаявкиПоЭтапу.НомерСтроки = ВложенныйЗапрос.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(упСтатусыЗаявокНаТССрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Создана)) КАК Статус
	|ПОМЕСТИТЬ втСтатусКрайнейИтерации
	|ИЗ
	|	втКрайняяИтерация КАК втКрайняяИтерация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних(
	|				,
	|				Документ В
	|					(ВЫБРАТЬ
	|						втКрайняяИтерация.ЗаявкаНаТС
	|					ИЗ
	|						втКрайняяИтерация)) КАК упСтатусыЗаявокНаТССрезПоследних
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКрайнийЭтап.Этап,
	|	втКрайнийЭтап.НомерЭтапа,
	|	втКрайнийЭтап.Отклонен,
	|	втКрайнийЭтап.Завершен,
	|	втКрайнийЭтап.ЗавершенУспешно,
	|	втКрайнийЭтап.ЭтапСхемыВыбораПеревозчика,
	|	втКрайнийЭтап.Тендер,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втСтатусКрайнейИтерации.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Отклонена)) = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Отклонена)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИтерацияНеЗавершена,
	|	втКрайнийЭтап.ВидТранспорта
	|ПОМЕСТИТЬ втКрайнийЭтап
	|ИЗ
	|	втКрайнийЭтапПодготовка КАК втКрайнийЭтап
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтатусКрайнейИтерации КАК втСтатусКрайнейИтерации
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейс.СхемаВыбораПеревозчика,
	|	втРейс.Перевозчик,
	|	втРейс.Соглашение,
	|	втРейс.ТипТС,
	|	втРейс.ТранспортноеСредство,
	|	ЕСТЬNULL(втКрайнийЭтап.НомерЭтапа, 0) КАК КрайнийНомерЭтапа
	|ПОМЕСТИТЬ втТекущееСостояниеПоРейсу
	|ИЗ
	|	втРейс КАК втРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКрайнийЭтап КАК втКрайнийЭтап
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРейс.ИтеративныйПодборПеревозчика
	|	И (ЕСТЬNULL(втКрайнийЭтап.Отклонен, ИСТИНА)
	|			ИЛИ ЕСТЬNULL(втКрайнийЭтап.Завершен, ИСТИНА))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК ИтеративныйПодборПеревозчика,
	|	ЛОЖЬ КАК ИспользуютсяПравилаПодбораПеревозчиков,
	|	втТекущееСостояние.СхемаВыбораПеревозчика,
	|	втТекущееСостояние.Перевозчик,
	|	втТекущееСостояние.Соглашение,
	|	втТекущееСостояние.ТипТС,
	|	втТекущееСостояние.ТранспортноеСредство,
	|	ЗНАЧЕНИЕ(Документ.упЭтапПодбораПеревозчика.ПустаяСсылка) КАК ТекущийЭтап,
	|	ЭтапыСхемы.НомерСтроки КАК НомерЭтапа,
	|	ЭтапыСхемы.РазыгрыватьТендер КАК Тендер,
	|	ЭтапыСхемы.ВидТранспорта,
	|	ЭтапыСхемы.Этап КАК ЭтапСхемыВыбораПеревозчика
	|ИЗ
	|	втТекущееСостояниеПоРейсу КАК втТекущееСостояние
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упСхемыВыбораПеревозчика.Этапы КАК ЭтапыСхемы
	|		ПО (втТекущееСостояние.КрайнийНомерЭтапа + 1 = ЭтапыСхемы.НомерСтроки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	втРейс.ИспользуютсяПравилаПодбораПеревозчиков,
	|	втРейс.СхемаВыбораПеревозчика,
	|	втРейс.Перевозчик,
	|	втРейс.Соглашение,
	|	втРейс.ТипТС,
	|	втРейс.ТранспортноеСредство,
	|	втКрайнийЭтап.Этап,
	|	втКрайнийЭтап.НомерЭтапа,
	|	втКрайнийЭтап.Тендер,
	|	втКрайнийЭтап.ВидТранспорта,
	|	втКрайнийЭтап.ЭтапСхемыВыбораПеревозчика
	|ИЗ
	|	втРейс КАК втРейс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКрайнийЭтап КАК втКрайнийЭтап
	|		ПО (НЕ втКрайнийЭтап.Отклонен)
	|			И (НЕ втКрайнийЭтап.Завершен)
	|			И (НЕ втКрайнийЭтап.ЗавершенУспешно)
	|			И (НЕ втКрайнийЭтап.Тендер)
	|			И (НЕ втКрайнийЭтап.ИтерацияНеЗавершена)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ,
	|	втРейс.ИспользуютсяПравилаПодбораПеревозчиков,
	|	NULL,
	|	втРейс.Перевозчик,
	|	втРейс.Соглашение,
	|	втРейс.ТипТС,
	|	втРейс.ТранспортноеСредство,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	втРейс КАК втРейс
	|ГДЕ
	|	НЕ втРейс.ИтеративныйПодборПеревозчика";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	РезультатВыполнить = Запрос.Выполнить();
	Если Не РезультатВыполнить.Пустой() Тогда
		Выборка = РезультатВыполнить.Выбрать();
		Выборка.Следующий();
		Если Выборка.ИтеративныйПодборПеревозчика Тогда
			Если Выборка.Тендер И СписокПеревозчиков = Неопределено Тогда
				Отказ       = Истина;
				ТекстОшибки = НСтр("ru = 'Тендерные заявки оформляются только вручную.'");
				
			Иначе	
				Если ЗначениеЗаполнено(Выборка.ТекущийЭтап) Тогда
					Результат = СоздатьЗаявкуНаТСПоЭтапу(Выборка.ТекущийЭтап, Выборка.ЭтапСхемыВыбораПеревозчика, Рейс, Выборка.Тендер, СписокПеревозчиков, Выборка.ВидТранспорта, ТекстОшибки, Отказ);
					Если Не Отказ И Не Результат Тогда
						ТекущийЭтап = Выборка.ТекущийЭтап.ПолучитьОбъект();
						ТекущийЭтап.Завершен = Истина;
						ТекущийЭтап.Записать();
						
						СформироватьЗаявкуНаТСПоРейсу(Рейс, Ложь, СписокПеревозчиков, ТекстОшибки, Отказ);
					КонецЕсли;
				Иначе
					НовыйЭтап = Документы.упЭтапПодбораПеревозчика.СоздатьДокумент();
					НовыйЭтап.Дата = ТекущаяДатаСеанса();
					НовыйЭтап.Рейс = Рейс;
					ЗаполнитьЗначенияСвойств(НовыйЭтап, Выборка);
					НовыйЭтап.Записать();
					Результат = СоздатьЗаявкуНаТСПоЭтапу(НовыйЭтап.Ссылка, НовыйЭтап.ЭтапСхемыВыбораПеревозчика, Рейс, НовыйЭтап.Тендер, СписокПеревозчиков, НовыйЭтап.ВидТранспорта, ТекстОшибки, Отказ);
					Если Не Отказ И Не Результат Тогда
						НовыйЭтап.Завершен = Истина;
						НовыйЭтап.Записать();
						
						СформироватьЗаявкуНаТСПоРейсу(Рейс, Ложь, СписокПеревозчиков, ТекстОшибки, Отказ);
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли;
		ИначеЕсли Выборка.ИспользуютсяПравилаПодбораПеревозчиков Тогда
			Попытка
				Если НЕ ТранзакцияАктивна() Тогда
					НачатьТранзакцию();
				КонецЕсли;
				Структура = ПолучимСписокПеревозчиковСогласноПравилам(Рейс);
				Если Структура = Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'По правилу подбора перевозчика нет подходящих значений. Заявка на ТС не сформирована.'");
				Иначе
					тбзПеревозчики = Новый ТаблицаЗначений;
					тбзПеревозчики.Колонки.Добавить("Перевозчик", Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
					тбзПеревозчики.Колонки.Добавить("Соглашение", Новый ОписаниеТипов("СправочникСсылка.упСоглашенияСПеревозчиками"));
					тбзПеревозчики.Колонки.Добавить("КонтрагентПеревозчика", Новый ОписаниеТипов("СправочникСсылка.упКонтрагенты"));
					текСтрока = тбзПеревозчики.Добавить(); 
					ЗаполнитьЗначенияСвойств(текСтрока, Структура);
					текРезультат = СформироватьЗаявкиНаТС(тбзПеревозчики, Рейс, ЭтоТендер);
				КонецЕсли;
				Если ТранзакцияАктивна() Тогда
					ЗафиксироватьТранзакцию();
				КонецЕсли;
			Исключение
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				Отказ = Истина;
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Ошибка при подборе перевозчика, согласно правил: %1'"), ОписаниеОшибки());
			КонецПопытки;
		Иначе
			Если ЗначениеЗаполнено(Выборка.Перевозчик) И ЗначениеЗаполнено(Выборка.Соглашение) Тогда
				тбзПеревозчики = Новый ТаблицаЗначений;
				тбзПеревозчики.Колонки.Добавить("Перевозчик", Новый ОписаниеТипов("СправочникСсылка.упПартнеры"));
				тбзПеревозчики.Колонки.Добавить("Соглашение", Новый ОписаниеТипов("СправочникСсылка.упСоглашенияСПеревозчиками"));
				
				текСтрока = тбзПеревозчики.Добавить();
				текСтрока.Перевозчик = Выборка.Перевозчик;
				текСтрока.Соглашение = Выборка.Соглашение; 
				
				текРезультат = СформироватьЗаявкиНаТС(тбзПеревозчики, Рейс, ЭтоТендер);
			Иначе	
				// Отказ 			= Истина;
				ТекстОшибки 	= НСтр("ru = 'Поля ""Перевозчик"" и ""Соглашение"" должны быть заполнены. Заявка на ТС не сформирована.'");
			КонецЕсли;
			
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат текРезультат;	
	
КонецФункции // СформироватьЗаявкуНаТСПоРейсу()

// Возвращает массив заявок на ТС и флаг необходимости формирования заявок.
//
// Параметры:
//  Рейс		 - 	ДокументСсылка.упРейс - ссылка на рейс.
//  ОписаниеОшибки	 - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  Структура - Результат формирования.
//
Функция СформироватьЗаявкуНаТСПоКоманде(Рейс, ОписаниеОшибки, Отказ) Экспорт
	
	сткРезультат = Новый Структура("ЗаявкиНаТС, ФормироватьЗаявки", Неопределено, Ложь);
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, Новый Структура("ВидПеревозки, ВыборПеревозчика, ВыборТС", "ВидПеревозки", "ВидПеревозки.ВыборПеревозчика", "ВидПеревозки.ВыборТС"));
	
	Если сткРеквизиты.ВыборТС = Перечисления.упВариантыПодбораТС.НепосредственныйПодборВРейсе 
		И сткРеквизиты.ВыборПеревозчика <> Перечисления.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально Тогда
		Отказ = Истина;
		ОписаниеОшибки = НСтр("ru = 'Для вида перевозки ""%1"" выбор ТС осуществляется непосредственно в рейсе.'");
		ОписаниеОшибки = СтрШаблон(ОписаниеОшибки, сткРеквизиты.ВидПеревозки);
		
	Иначе
		текСтатус = Документы.упРейс.ПолучитьСтатус(Рейс);
		Если текСтатус = Перечисления.упСтатусыРейса.КПланированиюТС Тогда
			Если сткРеквизиты.ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ОформляетсяТендер Тогда
				сткРезультат.ФормироватьЗаявки = Истина;
			Иначе
				мсвЗаявкиНаТС = Документы.упРейс.СформироватьЗаявкуНаТСПоРейсу(Рейс,,, ОписаниеОшибки, Отказ);
				Если мсвЗаявкиНаТС = Неопределено Тогда
					Если сткРеквизиты.ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально Тогда
						сткРезультат.ФормироватьЗаявки = Истина;
					Иначе	
						Отказ = Истина;
					КонецЕсли; 
				Иначе
					сткРезультат.ЗаявкиНаТС = мсвЗаявкиНаТС;
				КонецЕсли;
			КонецЕсли;
						
		Иначе
			Отказ = Истина;
			ОписаниеОшибки = НСтр("ru = 'На основании документа со статусом ""%1"" заявки на ТС не формируются.'");
			ОписаниеОшибки =  СтрШаблон(ОписаниеОшибки, текСтатус);
			
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат сткРезультат;
	
КонецФункции // СформироватьЗаявкуНаТСПоКоманде()

// Выполним поиск правил для подбора перевозчиков.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на документ рейс.
//
// Возвращаемое значение:
//   Структура - Ключи поля для заполнения документа, наиболее подходящий перевозчик согласно правил.
//
Функция ПолучимСписокПеревозчиковСогласноПравилам(Рейс) Экспорт
	СтруктураВозврата = Неопределено;
	ПравилоПодбораПеревозчиковНаРейсы = Справочники.упПравилаПодбораПеревозчиковНаРейсы.ПустаяСсылка();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Рейс,
	|	упРейс.ВидПеревозки КАК ВидПеревозки,
	|	упРейс.Организация КАК Организация,
	|	упРейс.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения
	|ПОМЕСТИТЬ вт_Рейс
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|ГДЕ
	|	упРейс.Ссылка = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПравилаПодбораПеревозчиковНаРейсы.Ссылка КАК Ссылка,
	|	упПравилаПодбораПеревозчиковНаРейсы.Важность КАК Важность,
	|	упПравилаПодбораПеревозчиковНаРейсы.ПериодАнализаДанных КАК ПериодАнализаДанных,
	|	вт_Рейс.ПлановоеНачалоВыполнения КАК ПлановоеНачалоВыполнения,
	|	упПравилаПодбораПеревозчиковНаРейсы.СхемаРейс КАК СхемаКомпоновкиДанных
	|ИЗ
	|	Справочник.упПравилаПодбораПеревозчиковНаРейсы КАК упПравилаПодбораПеревозчиковНаРейсы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Рейс КАК вт_Рейс
	|		ПО (упПравилаПодбораПеревозчиковНаРейсы.ВидПеревозкиОтбор В (ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка), вт_Рейс.ВидПеревозки))
	|			И (упПравилаПодбораПеревозчиковНаРейсы.ОрганизацияОтбор В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), вт_Рейс.Организация))
	|			И (упПравилаПодбораПеревозчиковНаРейсы.НаправлениеДеятельностиОтбор В (ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка), вт_Рейс.НаправлениеДеятельности))
	|			И (ВЫБОР
	|				КОГДА упПравилаПодбораПеревозчиковНаРейсы.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ упПравилаПодбораПеревозчиковНаРейсы.ДатаОкончанияДействия > вт_Рейс.ПлановоеНачалоВыполнения
	|			КОНЕЦ)
	|ГДЕ
	|	упПравилаПодбораПеревозчиковНаРейсы.Активно
	|	И НЕ упПравилаПодбораПеревозчиковНаРейсы.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Важность УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПериодАнализаДанных = Неопределено;
	ПлановоеНачалоВыполнения = Дата("00010101000000");
	Пока Выборка.Следующий() Цикл
		СхемаКомпоновкиДанных = Выборка.СхемаКомпоновкиДанных.Получить();
		Если СхемаКомпоновкиДанных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Ссылка", Рейс, ВидСравненияКомпоновкиДанных.Равно,, Истина);
		
		КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
		КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		
		// Компоновка макета.
		КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		// Инициализация процессора компоновки.
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
		
		// Таблица значений, в которую будет получен результат.
		Результат = Новый ТаблицаЗначений;
		
		// Получение результата.
		ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
		ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
		
		Если Результат.Количество() Тогда
			Если ТранзакцияАктивна() Тогда
				БлокировкаДанных = Новый БлокировкаДанных;
				// Документ
				ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.упРейс");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Рейс);
				// Справочник
				ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.упПравилаПодбораПеревозчиковНаРейсы");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				БлокировкаДанных.Заблокировать();
			КонецЕсли;
			
			ПравилоПодбораПеревозчиковНаРейсы = Выборка.Ссылка;
			ПериодАнализаДанных = Выборка.ПериодАнализаДанных;
			ПлановоеНачалоВыполнения = Выборка.ПлановоеНачалоВыполнения;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПравилоПодбораПеревозчиковНаРейсы) Тогда
		НачалоПериода = ПлановоеНачалоВыполнения;
		КонецПериода = ПлановоеНачалоВыполнения;
		Если ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.ДеньНачалаРейса Тогда
			НачалоПериода = НачалоДня(ПлановоеНачалоВыполнения);
			КонецПериода = КонецДня(ПлановоеНачалоВыполнения);
		ИначеЕсли ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.НеделяНачалаРейса Тогда
			НачалоПериода = НачалоНедели(ПлановоеНачалоВыполнения);
			КонецПериода = КонецНедели(ПлановоеНачалоВыполнения);
		ИначеЕсли ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.МесяцНачалаРейса Тогда
			НачалоПериода = НачалоМесяца(ПлановоеНачалоВыполнения);
			КонецПериода = КонецМесяца(ПлановоеНачалоВыполнения);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("ПравилоПодбораПеревозчиковНаРейсы", ПравилоПодбораПеревозчиковНаРейсы);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.Ссылка КАК ПравилоПодбора,
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.НомерСтроки КАК НомерСтроки,
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.Перевозчик КАК Перевозчик,
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.Соглашение КАК Соглашение,
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.КонтрагентПеревозчика КАК КонтрагентПеревозчика,
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.Доля КАК Доля
		|ПОМЕСТИТЬ вт_Правило
		|ИЗ
		|	Справочник.упПравилаПодбораПеревозчиковНаРейсы.Перевозчики КАК упПравилаПодбораПеревозчиковНаРейсыПеревозчики
		|ГДЕ
		|	упПравилаПодбораПеревозчиковНаРейсыПеревозчики.Ссылка = &ПравилоПодбораПеревозчиковНаРейсы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПлановыеПараметрыРейса.Рейс КАК Рейс
		|ПОМЕСТИТЬ вт_ПланРейсы
		|ИЗ
		|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних(, ) КАК упСтатусыРейсовСрезПоследних
		|		ПО упПлановыеПараметрыРейса.Рейс = упСтатусыРейсовСрезПоследних.Документ
		|ГДЕ
		|	упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ упСтатусыРейсовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРейса.Отменен)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упЗаявкаНаТС.Рейс КАК Рейс,
		|	упЗаявкаНаТС.Перевозчик КАК Перевозчик,
		|	упЗаявкаНаТС.Дата КАК Дата
		|ПОМЕСТИТЬ вт_Заявки
		|ИЗ
		|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Правило КАК вт_Правило
		|		ПО упЗаявкаНаТС.Перевозчик = вт_Правило.Перевозчик
		|			И (вт_Правило.Соглашение В (ЗНАЧЕНИЕ(Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка), упЗаявкаНаТС.Соглашение))
		|			И (вт_Правило.КонтрагентПеревозчика В (ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка), упЗаявкаНаТС.КонтрагентПеревозчика))
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаявокНаТС.СрезПоследних(, ) КАК упСтатусыЗаявокНаТССрезПоследних
		|		ПО упЗаявкаНаТС.Ссылка = упСтатусыЗаявокНаТССрезПоследних.Документ
		|			И (упСтатусыЗаявокНаТССрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Отправлена), ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаявкиНаТС.Создана)))
		|ГДЕ
		|	упЗаявкаНаТС.НачалоРейса МЕЖДУ &НачалоПериода И &КонецПериода
		|	И упЗаявкаНаТС.Рейс В
		|			(ВЫБРАТЬ
		|				вт_ПланРейсы.Рейс КАК Рейс
		|			ИЗ
		|				вт_ПланРейсы КАК вт_ПланРейсы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПеревозчикПоРейсу.Рейс КАК Рейс,
		|	упПеревозчикПоРейсу.Перевозчик КАК Перевозчик
		|ПОМЕСТИТЬ вт_Рейсы
		|ИЗ
		|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
		|			,
		|			Рейс В
		|				(ВЫБРАТЬ
		|					вт_ПланРейсы.Рейс КАК Рейс
		|				ИЗ
		|					вт_ПланРейсы КАК вт_ПланРейсы)) КАК упПеревозчикПоРейсу
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Правило КАК вт_Правило
		|		ПО упПеревозчикПоРейсу.Перевозчик = вт_Правило.Перевозчик
		|			И (вт_Правило.Соглашение В (ЗНАЧЕНИЕ(Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка), упПеревозчикПоРейсу.Соглашение))
		|			И (вт_Правило.КонтрагентПеревозчика В (ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка), упПеревозчикПоРейсу.КонтрагентПеревозчика))
		|ГДЕ
		|	НЕ упПеревозчикПоРейсу.Перевозчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	упПеревозчикПоРейсу.Рейс,
		|	упПеревозчикПоРейсу.Перевозчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Заявки.Рейс КАК Рейс,
		|	вт_Заявки.Перевозчик КАК Перевозчик
		|ПОМЕСТИТЬ вт_Данные
		|ИЗ
		|	вт_Заявки КАК вт_Заявки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_Рейсы.Рейс,
		|	вт_Рейсы.Перевозчик
		|ИЗ
		|	вт_Рейсы КАК вт_Рейсы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейс.Ссылка КАК Рейс,
		|	вт_Данные.Перевозчик КАК Перевозчик
		|ПОМЕСТИТЬ вт_Д1
		|ИЗ
		|	вт_Данные КАК вт_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упРейс КАК упРейс
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПравилаПодбораПеревозчиковНаРейсы КАК упПравилаПодбораПеревозчиковНаРейсы
		|			ПО (упПравилаПодбораПеревозчиковНаРейсы.ОрганизацияОтбор В (ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка), упРейс.Организация)
		|					И упПравилаПодбораПеревозчиковНаРейсы.ВидПеревозкиОтбор В (ЗНАЧЕНИЕ(Справочник.упВидыПеревозок.ПустаяСсылка), упРейс.ВидПеревозки)
		|					И упПравилаПодбораПеревозчиковНаРейсы.НаправлениеДеятельностиОтбор В (ЗНАЧЕНИЕ(Справочник.упНаправленияДеятельности.ПустаяСсылка), упРейс.НаправлениеДеятельности))
		|		ПО вт_Данные.Рейс = упРейс.Ссылка
		|ГДЕ
		|	упПравилаПодбораПеревозчиковНаРейсы.Ссылка = &ПравилоПодбораПеревозчиковНаРейсы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Д1.Перевозчик КАК Перевозчик,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ вт_Д1.Рейс) КАК Количество
		|ПОМЕСТИТЬ вт_Количество
		|ИЗ
		|	вт_Д1 КАК вт_Д1
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_Д1.Перевозчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(вт_Количество.Количество) КАК Итого
		|ПОМЕСТИТЬ вт_ВсегоИтого
		|ИЗ
		|	вт_Количество КАК вт_Количество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Правило.ПравилоПодбора КАК ПравилоПодбора,
		|	вт_Правило.НомерСтроки КАК НомерСтроки,
		|	вт_Правило.Перевозчик КАК Перевозчик,
		|	вт_Правило.Соглашение КАК Соглашение,
		|	вт_Правило.КонтрагентПеревозчика КАК КонтрагентПеревозчика,
		|	вт_Правило.Доля КАК Доля,
		|	ВЫБОР
		|		КОГДА вт_Количество.Количество ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА вт_ВсегоИтого.Итого ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА вт_ВсегоИтого.Итого = 0
		|			ТОГДА 0
		|		КОГДА вт_Количество.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ вт_Количество.Количество / вт_ВсегоИтого.Итого * 100
		|	КОНЕЦ КАК ДоляТекущая
		|ИЗ
		|	вт_Правило КАК вт_Правило
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Количество КАК вт_Количество
		|		ПО вт_Правило.Перевозчик = вт_Количество.Перевозчик
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ВсегоИтого КАК вт_ВсегоИтого
		|		ПО (вт_ВсегоИтого.Итого > 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Доля УБЫВ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ТаблицаЗначенийПодбораПеревозчика = Новый ТаблицаЗначений;
		ТаблицаЗначенийПодбораПеревозчика.Колонки.Добавить("Перевозчик",,"Перевозчик");
		ТаблицаЗначенийПодбораПеревозчика.Колонки.Добавить("Соглашение",,"Соглашение");
		ТаблицаЗначенийПодбораПеревозчика.Колонки.Добавить("КонтрагентПеревозчика",,"КонтрагентПеревозчика");
		ТаблицаЗначенийПодбораПеревозчика.Колонки.Добавить("Доля",,"Доля"); // - процент участия перевозчика в рейсах.
		ТаблицаЗначенийПодбораПеревозчика.Колонки.Добавить("ДоляТекущая",,"ДоляТекущая"); // - текущий процент участия перевозчика в рейсах.
		
		МассивСтрокДоли = Новый Массив;
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаЗначенийПодбораПеревозчика.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если Выборка.ДоляТекущая < Выборка.Доля Тогда
				МассивСтрокДоли.Добавить(НоваяСтрока);
			КонецЕсли;
		КонецЦикла;
		Если МассивСтрокДоли.Количество() Тогда
			ТаблицаДолей = ТаблицаЗначенийПодбораПеревозчика.Скопировать(МассивСтрокДоли, "Перевозчик, Соглашение, КонтрагентПеревозчика, ДоляТекущая");
			ТаблицаДолей.Сортировать("ДоляТекущая");
			СтруктураВозврата = Новый Структура("Перевозчик, Соглашение, КонтрагентПеревозчика");
			ЗаполнитьЗначенияСвойств(СтруктураВозврата, ТаблицаДолей[0]);
		ИначеЕсли ТаблицаЗначенийПодбораПеревозчика.Количество() Тогда
			СтруктураВозврата = Новый Структура("Перевозчик, Соглашение, КонтрагентПеревозчика");
			ЗаполнитьЗначенияСвойств(СтруктураВозврата, ТаблицаЗначенийПодбораПеревозчика[0]);
		КонецЕсли;	
	КонецЕсли;
	
	Если НЕ СтруктураВозврата = Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(СтруктураВозврата.Соглашение) Тогда
			Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "Организация");
			Соглашение = Справочники.упСоглашенияСПеревозчиками.СоглашениеПоУмолчанию(СтруктураВозврата.Перевозчик, Организация);
			СтруктураВозврата.Вставить("Соглашение", Соглашение);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтруктураВозврата.КонтрагентПеревозчика) Тогда
			КонтрагентПеревозчика = Справочники.упКонтрагенты.ПолучитьОсновногоКонтрагента(СтруктураВозврата.Перевозчик);
			СтруктураВозврата.Вставить("КонтрагентПеревозчика", КонтрагентПеревозчика);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции // ПолучимСписокПеревозчиковСогласноПравилам()

// Формировать следующее прохождение точки.
//
// Параметры:
//  Рейс		 - 	ДокументСсылка.упРейс - ссылка на рейс.
//  ТекстОшибки	 - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  ДокументСсылка - Ссылка на документ.
//
Функция СформироватьСледующееПрохождениеТочки(Рейс, ТекстОшибки, Отказ) Экспорт

	текРезультат = упПрохождениеТочки.СформироватьПрохождениеСледующейТочки(Рейс, ТекстОшибки, Отказ);
	
	Возврат текРезультат;
	
КонецФункции // СформироватьСледующееПрохождениеТочки()

// Формировать путевой лист.
//
// Параметры:
//  Рейс		 - 	ДокументСсылка.упРейс - ссылка на рейс.
//  ТекстОшибки	 - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  ДокументСсылка - Ссылка на документ.
//
Функция СформироватьПутевойЛист(Рейс, ТекстОшибки, Отказ) Экспорт

	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПутевойЛистРейсы.Ссылка КАК ПутевойЛист
	               |ИЗ
	               |	Документ.упПутевойЛист.Рейсы КАК упПутевойЛистРейсы
	               |ГДЕ
	               |	упПутевойЛистРейсы.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Результат = текВыборка.ПутевойЛист;
	Иначе
		мсвРейсы = Новый Массив;
		мсвРейсы.Добавить(Рейс);
		ДокументПутевойЛист = Документы.упПутевойЛист.СоздатьДокумент();
		ДокументПутевойЛист.Дата	= ТекущаяДата();
		ДокументПутевойЛист.Заполнить(мсвРейсы);
		ДокументПутевойЛист.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		Попытка
			ДокументПутевойЛист.Записать();
			упРаботаСоСтатусами.УстановитьСтатусДокументу(ДокументПутевойЛист.Ссылка, Перечисления.упСтатусыПутевогоЛиста.Новый, ТекстОшибки, Отказ);
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ФормированиеПутевогоЛиста", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упПутевойЛист,, ТекстОшибки);
		КонецПопытки; 
	КонецЕсли;
	
	Возврат Результат;	

КонецФункции // СформироватьПутевойЛист()

// Функция - Сформировать фактические расходы.
//
// Параметры:
//  Рейсы		 - Массив - массив рейсов.
//  ТекстОшибки	 - Строка - текст ошибки.
//  Отказ		 - Булево - Истина, в случае ошибки.
// 
// Возвращаемое значение:
//  Массив - массив ссылок на фактические расходы.
//
Функция СформироватьФактическиеРасходы(Рейсы, ТекстОшибки, Отказ) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокументРейс.Организация КАК Организация,
	               |	ПеревозчикПоРейсу.Перевозчик КАК Партнер,
	               |	ПеревозчикПоРейсу.КонтрагентПеревозчика КАК Контрагент,
	               |	ПеревозчикПоРейсу.Соглашение КАК Соглашение,
	               |	ДокументРейс.Ссылка КАК Рейс,
	               |	ДокументРейс.Ссылка КАК КоличествоРейсов,
	               |	упСтатусыРейсовСрезПоследних.Статус
	               |ИЗ
	               |	Документ.упРейс КАК ДокументРейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&Рейсы)) КАК ПеревозчикПоРейсу
	               |		ПО ДокументРейс.Ссылка = ПеревозчикПоРейсу.Рейс
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРейсов.СрезПоследних КАК упСтатусыРейсовСрезПоследних
	               |		ПО ДокументРейс.Ссылка = упСтатусыРейсовСрезПоследних.Документ
	               |ГДЕ
	               |	ДокументРейс.Ссылка В(&Рейсы)
	               |ИТОГИ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КоличествоРейсов)
	               |ПО
	               |	Организация,
	               |	Партнер,
	               |	Контрагент,
	               |	Соглашение";
	
	Запрос.УстановитьПараметр("Рейсы", Рейсы);
	ВыборкаОрганизация	= Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Пока ВыборкаОрганизация.Следующий() Цикл
		ВыборкаПартнер	= ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПартнер.Следующий() Цикл
			ВыборкаКонтрагент	= ВыборкаПартнер.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКонтрагент.Следующий() Цикл
				ВыборкаСоглашение	= ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСоглашение.Следующий() Цикл
					
					мсвРейсы = Новый Массив;
					
					Выборка	= ВыборкаСоглашение.Выбрать();
					Пока Выборка.Следующий() Цикл
						Если Выборка.Статус = Перечисления.упСтатусыРейса.Выполнен
								ИЛИ Выборка.Статус = Перечисления.упСтатусыРейса.Завершен Тогда
							мсвРейсы.Добавить(Выборка.Рейс);							
						Иначе
							ТекстСообщения = Нстр("ru = 'Для документа %1 в статусе ""%2"" фактические расходы сформированы не будут(Формируются только по рейсам в статусах ""Выполнен"" и ""Завершен"").'");
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Рейс, Выборка.Статус);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
							ЗаписьЖурналаРегистрации("Формирование фактических расходов", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упФактическиеРасходы,, ТекстСообщения);
						КонецЕсли;
					КонецЦикла;

					Если Отказ Тогда
						Прервать;
					КонецЕсли;
					
					ФактическиеРасходы = Документы.упФактическиеРасходы.СоздатьДокумент();
					ФактическиеРасходы.Дата 		= ТекущаяДатаСеанса();
					ФактическиеРасходы.Организация	= ВыборкаСоглашение.Организация;
					ФактическиеРасходы.Партнер		= ВыборкаСоглашение.Партнер;
					ФактическиеРасходы.Контрагент	= ВыборкаСоглашение.Контрагент;
					ФактическиеРасходы.Соглашение	= ВыборкаСоглашение.Соглашение;
														
										
					Если ВыборкаСоглашение.КоличествоРейсов = 1 Тогда
						ФактическиеРасходы.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоОдномуРейсу;	
						Если мсвРейсы.Количество() > 0 Тогда
							ФактическиеРасходы.Рейс = мсвРейсы[0];
						КонецЕсли;
						тбзПлановыеРасходы = упРейс.ПлановыеРасходыПоЗаданиямРейсов(мсвРейсы, ВедетсяВалютныйУчет);
						
					Иначе	
						ФактическиеРасходы.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.РасходыПоНесколькимРейсам;	
						тбзПлановыеРасходы = упРаспределениеРасходов.ПлановыеРасходыПоВыполненнымЗаданиямРейсов(мсвРейсы, ВедетсяВалютныйУчет);
					КонецЕсли;
					Для каждого СтрокаПлановыйРасход Из тбзПлановыеРасходы Цикл
							НоваяСтрока = ФактическиеРасходы.Расходы.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПлановыйРасход);
							НоваяСтрока.Количество = 1;
							НоваяСтрока.Цена = СтрокаПлановыйРасход.Сумма;
							СтруктураДанные	= Новый Структура();
							упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(НоваяСтрока,"Сумма",СтруктураДанные);
					КонецЦикла;
				
					Если ФактическиеРасходы.Расходы.Количество() = 0 И мсвРейсы.Количество() > 1 Тогда
						Для каждого СтрокаРейс Из мсвРейсы Цикл
							НоваяСтрока = ФактическиеРасходы.Рейсы.Добавить();
							НоваяСтрока.Рейс = СтрокаРейс;
						КонецЦикла;
						ФактическиеРасходы.СпособВводаРасходов = Перечисления.упСпособыВводаФактическихРасходов.ОбобщенныеРасходыПоНесколькимРейсам;
					КонецЕсли;
										
					Попытка
						ФактическиеРасходы.Записать(РежимЗаписиДокумента.Запись);
						мсвРезультат.Добавить(ФактическиеРасходы.Ссылка);
					Исключение
						Отказ = Истина;
						ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
							ТекстОшибки = ОписаниеОшибки();
						КонецЕсли;
						ЗаписьЖурналаРегистрации("Формирование фактических расходов", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упФактическиеРасходы,, ТекстОшибки);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
					КонецПопытки;
				КонецЦикла;
				Если Отказ Тогда 
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла; 
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла; 

	Возврат мсвРезультат;
	
КонецФункции

// Функция - Сформировать списание планов по заданиям
//
// Параметры:
//  Рейс 		 - 	ДокументСсылка.упРейс - ссылка на рейс.
//  ТекстОшибки - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  Массив 
//    * ДокументСсылка.упСписаниеПлановПоЗаданиюНаПеревозку - Ссылка на документ.
//
Функция СформироватьСписаниеПлановПоЗаданиям(Рейс, ТекстОшибки, Отказ) Экспорт
	
	Результат = Новый Массив;
	
	ИмяСобытия = Нстр("ru = 'СписаниеПлановПоЗаданию.СписаниеПоРейсу'");
	
	Попытка
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	тбРаботы.Задание КАК Задание,
		|	тбРаботы.ГрузовоеМесто КАК ГрузовоеМесто,
		|	тбРаботы.Отменена КАК Отменена,
		|	тбРаботы.ГрузРазделен КАК ГрузРазделен,
		|	тбРаботы.Операция КАК Операция,
		|	тбРаботы.Идентификатор КАК Идентификатор,
		|	тбРаботы.ПроблемаПричинаВозникновенияЭтойРаботы КАК ПроблемаПричинаВозникновенияЭтойРаботы
		|ПОМЕСТИТЬ втРаботы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
		|		,
		|		Рейс = &Рейс) КАК тбРаботы
		|;
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упРаботыПоРейсу.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ втРегистраторы
		|ИЗ
		|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
		|ГДЕ ИСТИНА
		|	И упРаботыПоРейсу.Рейс = &Рейс
		|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботы.Задание КАК Задание,
		|	ВЫБОР
		|		КОГДА &ПустыеГрузовыеМеста
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|		ИНАЧЕ втРаботы.ГрузовоеМесто
		|	КОНЕЦ КАК ГрузовоеМесто,
		|	СУММА(ВЫБОР
		|		КОГДА втРаботы.Отменена
		|				И НЕ втРаботы.ГрузРазделен
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК КоличествоОтменена,
		|	СУММА(1) КАК КоличествоОпераций
		|ПОМЕСТИТЬ втЗаданияСОтмененнойПогрузкой
		|ИЗ
		|	втРаботы КАК втРаботы
		|ГДЕ ЛОЖЬ
		|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
		|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
		|СГРУППИРОВАТЬ ПО
		|	втРаботы.Задание,
		|	ВЫБОР
		|		КОГДА &ПустыеГрузовыеМеста
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
		|		ИНАЧЕ втРаботы.ГрузовоеМесто
		|	КОНЕЦ
		|;
		|//{Запрос: 3 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОтмененныеЗаданияТ.ГрузовоеМесто КАК ГрузовоеМесто,
		|	втОтмененныеЗаданияТ.Задание КАК Задание
		|ПОМЕСТИТЬ втОтмененныеЗадания
		|ИЗ
		|	втЗаданияСОтмененнойПогрузкой КАК втОтмененныеЗаданияТ
		|ГДЕ втОтмененныеЗаданияТ.КоличествоОпераций = втОтмененныеЗаданияТ.КоличествоОтменена
		|;
		|//{Запрос: 4 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упРейсЗадания.Задание КАК Задание
		|ПОМЕСТИТЬ втЗадания
		|ИЗ
		|	Документ.упРейс.Задания КАК упРейсЗадания
		|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
		|	ПО ИСТИНА
		|		И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
		|		И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
		|ГДЕ ИСТИНА
		|	И упРейсЗадания.Ссылка = &Рейс
		|	И втОтмененныеЗадания.Задание ЕСТЬ NULL
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	упКорректировкаРейсаЗадания.Задание КАК Задание
		|ИЗ
		|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
		|	ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
		|	ПО ИСТИНА
		|		И упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
		|		И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
		|ГДЕ втОтмененныеЗадания.Задание ЕСТЬ NULL
		|;
		|//{Запрос: 5 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упЗаданияКПланированиюОстатки.Задание КАК Задание,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(упЗаданияКПланированиюОстатки.Задание) КАК ЗаданиеПредставление
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(
		|		,
		|		Задание В (
		|			ВЫБРАТЬ
		|				втЗадания.Задание КАК Задание
		|			ИЗ
		|				втЗадания КАК втЗадания)) КАК упЗаданияКПланированиюОстатки
		|ГДЕ упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > 0
		|";
		
		Запрос.УстановитьПараметр("Рейс", Рейс);
		
		сткРеквизиты = Новый Структура();                	
		сткРеквизиты.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах", "ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах");
		
		сткДанныеВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, сткРеквизиты);
		ПараметрыВводаИнформацииОГрузовыхМестах = сткДанныеВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах; 
		ПустыеГрузовыеМеста = ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса;					   
		
		Запрос.УстановитьПараметр("ПустыеГрузовыеМеста", ПустыеГрузовыеМеста);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			тбзЗадания = РезультатЗапроса.Выгрузить();
			
			БлокировкаДанных = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = тбзЗадания;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упСостояниеПоЗаданиям");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = тбзЗадания;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПланПоЗаданиям");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = тбзЗадания;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = тбзЗадания;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Задание");
			
			Попытка
				БлокировкаДанных.Заблокировать();
			Исключение
				Отказ = Истина;
				ТекстОшибки = СтрШаблон(Нстр("ru = 'Ошибка при установке блокировки на ресурсы для документа %1:%2'"), Рейс, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ,Рейс, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецПопытки;
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Истина
				И Выборка.Следующий()
				И Не Отказ 
				Цикл
				ЗаданиеПредставление = Выборка.ЗаданиеПредставление;
				
				НовыйДокумент = Документы.упСписаниеПлановПоЗаданиюНаПеревозку.СоздатьДокумент();
				НовыйДокумент.Автор = ПользователиКлиентСервер.ТекущийПользователь();
				НовыйДокумент.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
				НовыйДокумент.Дата = ТекущаяДатаСеанса();
				НовыйДокумент.ЗаданиеНаПеревозку = Выборка.Задание;
				НовыйДокумент.Заполнить(Выборка.Задание);
				НовыйДокумент.ДополнительныеСвойства.Вставить("ВыводитьСообщенияПользователю", Ложь);
				Попытка
					НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
					Результат.Добавить(НовыйДокумент.Ссылка);
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
						ТекстОшибки = ОписаниеОшибки();
					КонецЕсли;
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ,ЗаданиеПредставление, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				КонецПопытки;
				
			КонецЦикла;	
			
		КонецЕсли;
		
		Если Не Отказ Тогда
			ЗафиксироватьТранзакцию();
		Иначе	
			ОтменитьТранзакцию();
		КонецЕсли;
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ,Рейс, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецПопытки;	
	
	Возврат Результат;
	
КонецФункции

// Формировать отчет.
//
// Параметры:
//  Рейс		 - 	ДокументСсылка.упРейс - ссылка на рейс.
//  ТекстОшибки	 - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  Структура - Результат проверки.
//
Функция ЕстьВозможностьСформированияОтчетИнкассатораПоРейсу(Рейс, ТекстОшибки, Отказ) Экспорт
	
	сткРезультат = Новый Структура("Водитель",Неопределено);
	
	сткРеквизитыИмена = Новый Структура();
	сткРеквизитыИмена.Вставить("ИспользуютсяУслугиИнкассации", "ВидПеревозки.ИспользуютсяУслугиИнкассации");
	сткРеквизитыИмена.Вставить("ВидПеревозки", "ВидПеревозки");
	
	сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рейс, сткРеквизитыИмена);
	
	ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = Ложь;
	
	ВидПеревозки = сткРеквизиты.ВидПеревозки;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	
	ИспользуютсяУслугиИнкассации = сткРеквизиты.ИспользуютсяУслугиИнкассации;
	
	Если ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест Тогда
		ТекстОшибки = Нстр("ru = 'Для вида перевозки ""%1"" суммы по услугам инкассации вводятся с точностью до грузовых мест. Для этого используется документ ""Прохождение точки"".'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВидПеревозки);
		Отказ = Истина;
		ЗаписьЖурналаРегистрации("ФормированиеОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
	ИначеЕсли Не ИспользуютсяУслугиИнкассации Тогда
		ТекстОшибки = Нстр("ru = 'Для вида перевозки ""%1"" не используются услуги инкассации'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВидПеревозки);
		Отказ = Истина;
		ЗаписьЖурналаРегистрации("ФормированиеОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
	КонецЕсли;
	
	Если Не Отказ Тогда
		текСтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Рейс);
		Если Не (текСтатусРейса = Перечисления.упСтатусыРейса.НазначеноТС Или текСтатусРейса = Перечисления.упСтатусыРейса.КВыполнению
			     Или текСтатусРейса = Перечисления.упСтатусыРейса.Выполняется Или текСтатусРейса = Перечисления.упСтатусыРейса.ВыполненЧастично
				 Или текСтатусРейса = Перечисления.упСтатусыРейса.Выполнен) Тогда
			ТекстОшибки = НСтр("ru = '%1 находится в статусе ""%2"". Разрешено формировать отчеты инкассатора только для рейсов в статусе ""Назначено ТС"" и выше'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Рейс, текСтатусРейса);
			Отказ = Истина;
			ЗаписьЖурналаРегистрации("ФормированиеОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		сткЭкипаж 	= упРейс.ПолучитьЭкипажРейса(Рейс);
		
		Если НЕ ЗначениеЗаполнено(сткЭкипаж.Водитель1) Тогда
			ТекстОшибки = Нстр("ru = 'Для рейса %1 не задан первый водитель'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Рейс);
			Отказ = Истина;
			ЗаписьЖурналаРегистрации("ФормированиеОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
		Иначе
			сткРезультат.Вставить("Водитель", сткЭкипаж.Водитель1);
		КонецЕсли;
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

// Формировать отчет.
//
// Параметры:
//  мсвЗаявкиНаПеревозкуГруза - Массив - массив ссылок.
//  Рейс - 	ДокументСсылка.упРейс - ссылка на рейс.
//  Водитель - 	СправочникСсылка - ссылка объект.
//  ТекстОшибки	 - 	Строка - текст ошибки.
//  Отказ		 - 	Булево - Истина, в случае ошибки.
//
// Возвращаемое значение:
//  Массив - Результат проверки.
//
Функция СформироватьОтчетыИнкассатора(мсвЗаявкиНаПеревозкуГруза, Рейс, Водитель, ТекстОшибки, Отказ) Экспорт
	
	мсвРезультат = Новый Массив();
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозкуГруза,
	|	упОтчетИнкассатора.Ссылка КАК ОтчетИнкассатора,
	|	упЗаявкаНаПеревозкуГруза.СуммаИнкассацииПлан,
	|	ВЫБОР
	|		КОГДА упОтчетИнкассатора.Ссылка ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ФормироватьОтчетИнкассатора,
	|	упЗаявкаНаПеревозкуГруза.Валюта,
	|	упЗаявкаНаПеревозкуГруза.Организация
	|ИЗ
	|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упОтчетИнкассатора КАК упОтчетИнкассатора
	|		ПО упЗаявкаНаПеревозкуГруза.Ссылка = упОтчетИнкассатора.ЗаявкаНаПеревозкуГруза
	|			И (упОтчетИнкассатора.Рейс = &Рейс)
	|ГДЕ
	|	упЗаявкаНаПеревозкуГруза.Ссылка В(&мсвЗаявкиНаПеревозкуГруза)";
	Запрос.УстановитьПараметр("мсвЗаявкиНаПеревозкуГруза",	мсвЗаявкиНаПеревозкуГруза);
	Запрос.УстановитьПараметр("Рейс", 						Рейс);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Пока текВыборка.Следующий() Цикл
		
		Если текВыборка.ФормироватьОтчетИнкассатора Тогда
			
			ДокументОтчетИнкассатора = Документы.упОтчетИнкассатора.СоздатьДокумент();
			ДокументОтчетИнкассатора.Дата 					= ТекущаяДатаСеанса();
			ДокументОтчетИнкассатора.Организация			= текВыборка.Организация;
			ДокументОтчетИнкассатора.ЗаявкаНаПеревозкуГруза = текВыборка.ЗаявкаНаПеревозкуГруза;
			ДокументОтчетИнкассатора.Рейс 					= Рейс;
			ДокументОтчетИнкассатора.Валюта 				= текВыборка.Валюта;
			ДокументОтчетИнкассатора.Сотрудник				= Водитель;
						
			Попытка
				ДокументОтчетИнкассатора.Записать();
				мсвРезультат.Добавить(ДокументОтчетИнкассатора.Ссылка);
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ЗаписьЖурналаРегистрации("ФормированиеОтчетаИнкассатора", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОтчетИнкассатора,, ТекстОшибки);
				Прервать;
			КонецПопытки; 
		Иначе	
			 мсвРезультат.Добавить(текВыборка.ОтчетИнкассатора);
		КонецЕсли;
			
	КонецЦикла;
		
	Возврат мсвРезультат;
	 		
КонецФункции

Функция ПолучитьПараметрыГрузаОбщиеПоРейсу(Рейс)
	
	сткРезультат = Новый Структура("Высота, Длина, Ширина, Масса, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КлассОпасности, ТемпературныйРежим, Хрупкий,  БеречьОтВлаги, БеречьОтИзлучения, БеречьОтНагрева, МаксимальнаяВысота, МаксимальнаяДлина, МаксимальнаяШирина, ОценочнаяСтоимость", 
	0, 0, 0, 0, Справочники.упКлассыОпасности.ПустаяСсылка(), Справочники.упТемпературныеРежимы.ПустаяСсылка(), Ложь, Ложь, Ложь, 0, 0, 0);
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто,
	|	упРейсЗадания.Тара КАК Тара,
	|	упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка = &Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) * упРейсЗадания.КоличествоГрузов КАК Масса,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Высота, 0)) КАК Высота,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Длина, 0)) КАК Длина,
	|	ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Ширина, 0)) КАК Ширина,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) * упРейсЗадания.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Объем,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) * упРейсЗадания.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.Тара = ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) * упРейсЗадания.КоличествоГрузов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЗагрузочныхМетров,
	|	упРейсЗадания.ГрузовоеМесто.КлассОпасности КАК КлассОпасности,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.ГрузовоеМесто.Хрупкий
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Хрупкий,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.ГрузовоеМесто.БеречьОтВлаги
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БеречьОтВлаги,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.ГрузовоеМесто.БеречьОтИзлучения
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БеречьОтИзлучения,
	|	ВЫБОР
	|		КОГДА упРейсЗадания.ГрузовоеМесто.БеречьОтНагрева
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК БеречьОтНагрева,
	|	упРейсЗадания.ГрузовоеМесто.ТемпературныйРежим.ТемператураОт КАК ТемператураОт,
	|	упРейсЗадания.ГрузовоеМесто.ТемпературныйРежим.ТемператураДо КАК ТемператураДо,
	|	упРейсЗадания.ГрузовоеМесто.ТемпературныйРежим КАК ТемпературныйРежим,
	|	упРейсЗадания.ГрузовоеМесто.ОценочнаяСтоимость КАК ОценочнаяСтоимость
	|ПОМЕСТИТЬ втХарактеристики
	|ИЗ
	|	втЗадания КАК упРейсЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|				,
	|				ЗаявкаНаПеревозку В
	|					(ВЫБРАТЬ
	|						втЗадания.Задание.ЗаявкаНаПеревозкуГруза
	|					ИЗ
	|						втЗадания КАК втЗадания
	|					ГДЕ
	|						НЕ втЗадания.Задание.ЗаявкаНаПеревозкуГруза = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|				,
	|				ЗаданиеНаПеревозку В
	|					(ВЫБРАТЬ
	|						втЗадания.Задание
	|					ИЗ
	|						втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО упРейсЗадания.Задание = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|			И упРейсЗадания.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|ГДЕ
	|	упРейсЗадания.Задание.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах В (ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса), ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	МАКСИМУМ(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Высота, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Высота, 0))),
	|	МАКСИМУМ(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Длина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Длина, 0))),
	|	МАКСИМУМ(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Ширина, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Ширина, 0))),
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоЗагрузочныхМетров, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоЗагрузочныхМетров, 0)) * упГрузовыеМеста.КоличествоГрузов),
	|	упГрузовыеМеста.ГрузовоеМесто.КлассОпасности,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.Хрупкий
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.БеречьОтВлаги
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.БеречьОтИзлучения
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.БеречьОтНагрева
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	упГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим.ТемператураОт,
	|	упГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим.ТемператураДо,
	|	упГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим,
	|	упГрузовыеМеста.ГрузовоеМесто.ОценочнаяСтоимость
	|ИЗ
	|	втЗадания КАК упРейсЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упГрузовыеМеста
	|		ПО упРейсЗадания.Задание = упГрузовыеМеста.Ссылка
	|			И (упРейсЗадания.Задание.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|				,
	|				ЗаявкаНаПеревозку В
	|					(ВЫБРАТЬ
	|						втЗадания.Задание.ЗаявкаНаПеревозкуГруза
	|					ИЗ
	|						втЗадания КАК втЗадания
	|					ГДЕ
	|						НЕ втЗадания.Задание.ЗаявкаНаПеревозкуГруза = ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|		ПО (упГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку)
	|			И (упГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|				,
	|				ЗаданиеНаПеревозку В
	|					(ВЫБРАТЬ
	|						втЗадания.Задание
	|					ИЗ
	|						втЗадания КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|		ПО (упГрузовыеМеста.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку)
	|			И (упГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто)
	|
	|СГРУППИРОВАТЬ ПО
	|	упГрузовыеМеста.ГрузовоеМесто.КлассОпасности,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.Хрупкий
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.БеречьОтВлаги
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.БеречьОтИзлучения
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА упГрузовыеМеста.ГрузовоеМесто.БеречьОтНагрева
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	упГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим.ТемператураОт,
	|	упГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим.ТемператураДо,
	|	упГрузовыеМеста.ГрузовоеМесто.ТемпературныйРежим,
	|	упГрузовыеМеста.ГрузовоеМесто.ОценочнаяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(втХарактеристики.КлассОпасности.Класс) КАК КлассОпасностиКласс
	|ПОМЕСТИТЬ втМинимальныйКлассОпасности
	|ИЗ
	|	втХарактеристики КАК втХарактеристики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втХарактеристики.ТемператураОт) КАК ТемператураОт,
	|	МИНИМУМ(втХарактеристики.ТемператураДо) КАК ТемператураДо
	|ПОМЕСТИТЬ втДиапазонТемператур
	|ИЗ
	|	втХарактеристики КАК втХарактеристики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втХарактеристики.Масса) КАК Масса,
	|	МАКСИМУМ(втХарактеристики.Высота) КАК Высота,
	|	МАКСИМУМ(втХарактеристики.Длина) КАК Длина,
	|	МАКСИМУМ(втХарактеристики.Ширина) КАК Ширина,
	|	СУММА(втХарактеристики.Объем) КАК Объем,
	|	СУММА(втХарактеристики.КоличествоМест) КАК КоличествоМест,
	|	СУММА(втХарактеристики.КоличествоЗагрузочныхМетров) КАК КоличествоЗагрузочныхМетров,
	|	ВЫБОР
	|		КОГДА СУММА(втХарактеристики.БеречьОтВлаги) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БеречьОтВлаги,
	|	ВЫБОР
	|		КОГДА СУММА(втХарактеристики.Хрупкий) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Хрупкий,
	|	ВЫБОР
	|		КОГДА СУММА(втХарактеристики.БеречьОтИзлучения) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БеречьОтИзлучения,
	|	ВЫБОР
	|		КОГДА СУММА(втХарактеристики.БеречьОтНагрева) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БеречьОтНагрева,
	|	СУММА(втХарактеристики.ОценочнаяСтоимость) КАК ОценочнаяСтоимость
	|ИЗ
	|	втХарактеристики КАК втХарактеристики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	втХарактеристики.КлассОпасности КАК КлассОпасности
	|ИЗ
	|	втХарактеристики КАК втХарактеристики,
	|	втМинимальныйКлассОпасности КАК втМинимальныйКлассОпасности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	упТемпературныеРежимы.Ссылка КАК ТемпературныйРежим,
	|	упТемпературныеРежимы.ТемператураДо - упТемпературныеРежимы.ТемператураОт КАК Порядок
	|ИЗ
	|	Справочник.упТемпературныеРежимы КАК упТемпературныеРежимы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДиапазонТемператур КАК втДиапазонТемператур
	|		ПО упТемпературныеРежимы.ТемператураОт >= втДиапазонТемператур.ТемператураОт
	|			И упТемпературныеРежимы.ТемператураДо <= втДиапазонТемператур.ТемператураДо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок УБЫВ";
	текЗапрос.УстановитьПараметр("Рейс", Рейс);
	мсвРезультат 	= текЗапрос.ВыполнитьПакет();
	текВыборкаХарактеристики = мсвРезультат[3].Выбрать();
	
	Если текВыборкаХарактеристики.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(сткРезультат, текВыборкаХарактеристики);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упУчитыватьКлассыОпасностиГрузов") Тогда 
		текВыборкаКлассыОпасности	= мсвРезультат[4].Выбрать();
		Если текВыборкаКлассыОпасности.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткРезультат, текВыборкаКлассыОпасности);	
		КонецЕсли;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упКонтролироватьТемпературныеРежимы") Тогда 
		текВыборкаТемпературныйРежим = мсвРезультат[5].Выбрать();
		Если текВыборкаТемпературныйРежим.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(сткРезультат, текВыборкаТемпературныйРежим);	
		КонецЕсли;
	КонецЕсли;	
	
	Возврат сткРезультат;
	
КонецФункции

// Процедура добавляет задания на перевозку груза в рейс.
//
// Параметры:
//  МассивЗаданий           - Массив - Массив ссылок на документы "упЗаданиеНаПеревозкуГруза".
//  ТаблицаЗаданий          - ТаблицаЗначений - задания:
//       * ЗаданиеНаПеревозку    - ДокументСсылка.ЗаданиеНаПеревозкуГруза.
//       * ГрузовоеМесто         - СправочникСсылка.упГрузовыеМеста.
//       * КоличествоГрузов      - Число.
//  Рейс                    - ДокументСсылка.упРейс - Документ.
//  ПериодПланирования      - Структура - Содержит даты начала и (или) окончания периода планирования.
//  ТекстОшибки             - Строка - Текст.
//  Отказ                   - Булево - Флаг.
//
Функция ДобавитьЗаданияВРейс(МассивЗаданий = Неопределено, ТаблицаЗаданий = Неопределено, Рейс, ПериодПланирования, ТекстОшибки, Отказ) Экспорт
	
	Если Ложь Тогда // Для контекстной подсказки
	    Рейс = Документы.упРейс.ПустаяСсылка();
	КонецЕсли;
	// такая же логика реализована в упЗащищеннаяОбработка.СформироватьРейсы
	ГрузовыеМеста = ГрузовыеМестаЗаданийРейса(МассивЗаданий, ТаблицаЗаданий);
	ВидПеревозки = Рейс.ВидПеревозки;
	
	сткРеквизиты = Новый Структура();
	сткРеквизиты.Вставить("ПравилоСвязиТиповГрузовВРейсе");
	сткРеквизиты.Вставить("АвтоматическиДобавлятьЗаданияНаВозврат");
	сткРеквизиты.Вставить("АвтоматическиДобавлятьЗаданияИнкассации");
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПеревозки, сткРеквизиты);
	Если Не ЗначенияРеквизитов.ПравилоСвязиТиповГрузовВРейсе.Пустая() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|// 820т17аипв
		|ВЫБРАТЬ
		|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка КАК ЗависимоеЗадание,
		|	МАКСИМУМ(ЗаданиеОсновноеГрузовыеМеста.Ссылка) КАК ОсновноеЗадание
		|ИЗ
		|	Справочник.упПравилаСвязиТиповГрузов.Связи КАК ПравилаСвязиТиповГрузов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеОсновноеГрузовыеМеста
		|		ПО ПравилаСвязиТиповГрузов.ОсновнойТипГруза.Ссылка = ЗаданиеОсновноеГрузовыеМеста.ГрузовоеМесто.ТипГруза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ЗаданиеЗависимоеГрузовыеМеста
		|		ПО ПравилаСвязиТиповГрузов.ЗависимыйТипГруза.Ссылка = ЗаданиеЗависимоеГрузовыеМеста.ГрузовоеМесто.ТипГруза
		|			И ЗаданиеОсновноеГрузовыеМеста.Ссылка <> ЗаданиеЗависимоеГрузовыеМеста.Ссылка
		|			И ЗаданиеОсновноеГрузовыеМеста.Ссылка.НомерВЦепиПоставок = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.НомерВЦепиПоставок
		|			И ЗаданиеОсновноеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = ЗаданиеЗависимоеГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза
		|			И ПравилаСвязиТиповГрузов.Ссылка = ЗаданиеОсновноеГрузовыеМеста.Ссылка.ВидПеревозки.ПравилоСвязиТиповГрузовВРейсе
		|ГДЕ
		|	ЗаданиеОсновноеГрузовыеМеста.Ссылка В(&Задания) И Не ЗаданиеЗависимоеГрузовыеМеста.Ссылка В(&Задания)
		|СГРУППИРОВАТЬ ПО
		|	ЗаданиеЗависимоеГрузовыеМеста.Ссылка
		|";
		Запрос.УстановитьПараметр("Задания", ГрузовыеМеста.ВыгрузитьКолонку("Задание"));
		ЗависимыеЗадания = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЗависимоеЗадание");
		ГрузовыеМестаЗависимых = ГрузовыеМестаЗаданийРейса(ЗависимыеЗадания);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ГрузовыеМестаЗависимых, ГрузовыеМеста);
		Если ТаблицаЗаданий = Неопределено Тогда
			//ЗависимыеЗадания = ГрузовыеМестаЗависимых.ВыгрузитьКолонку("Задание");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивЗаданий, ЗависимыеЗадания);
		Иначе
			ГрузовыеМестаЗависимых.Колонки.Задание.Имя = "ЗаданиеНаПеревозку";
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ГрузовыеМестаЗависимых, ТаблицаЗаданий);
		КонецЕсли; 
	КонецЕсли; 
	
	Если Ложь
		Или ЗначенияРеквизитов.АвтоматическиДобавлятьЗаданияИнкассации
		Или ЗначенияРеквизитов.АвтоматическиДобавлятьЗаданияНаВозврат
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ЗаданияНаВозврат.Ссылка КАК Задание
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданияНаПеревозку
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.Ссылка КАК Ссылка
		|	ИЗ
		|		Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
		|			,
		|			) КАК СтатусыЗаданийНаВозврат
		|		ПО ИСТИНА
		|			И СтатусыЗаданийНаВозврат.Документ = Т.Ссылка
		|			И ЕСТЬNULL(СтатусыЗаданийНаВозврат.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)) = ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.КПланированию)
		|			И НЕ (Т.Ссылка В (&Задания))
		|	ГДЕ Т.ГрузовоеМесто.ТипГруза.ВидГруза В (&ВидыГрузов)) КАК ЗаданияНаВозврат
		|	ПО ИСТИНА
		|		И ЗаданияНаПеревозку.АдресОтправления = ЗаданияНаВозврат.Ссылка.АдресПолучения
		|		И ЗаданияНаПеревозку.АдресПолучения = ЗаданияНаВозврат.Ссылка.АдресОтправления
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок.ВидыТочекОтправления КАК ВидыТочекОтправления
		|	ПО ИСТИНА
		|		И ВидыТочекОтправления.Ссылка = ЗаданияНаПеревозку.ВидПеревозки
		|		И ЗаданияНаПеревозку.АдресОтправления.ВидАдреса = ВидыТочекОтправления.ВидТочкиОтправления
		|ГДЕ ИСТИНА
		|	И ЗаданияНаПеревозку.Ссылка В (&Задания)
		|	И НЕ (ВидыТочекОтправления.ВидТочкиОтправления ЕСТЬ NULL)
		|";
	
		мсвВидыГрузов = Новый Массив;
		
		Если ЗначенияРеквизитов.АвтоматическиДобавлятьЗаданияИнкассации Тогда
			мсвВидыГрузов.Добавить(Перечисления.упВидыГрузов.ИнкассируемаяЦенность);	
		КонецЕсли;
		
		Если ЗначенияРеквизитов.АвтоматическиДобавлятьЗаданияНаВозврат Тогда
			мсвВидыГрузов.Добавить(Перечисления.упВидыГрузов.Груз);	
		КонецЕсли;
		Запрос.УстановитьПараметр("ВидыГрузов", мсвВидыГрузов);
		Запрос.УстановитьПараметр("Задания", ГрузовыеМеста.ВыгрузитьКолонку("Задание"));
		ЗависимыеЗадания = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Задание");
		ГрузовыеМестаЗависимых = ГрузовыеМестаЗаданийРейса(ЗависимыеЗадания);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ГрузовыеМестаЗависимых, ГрузовыеМеста);
		Если ТаблицаЗаданий = Неопределено Тогда
			//ЗависимыеЗадания = ГрузовыеМестаЗависимых.ВыгрузитьКолонку("Задание");
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивЗаданий, ЗависимыеЗадания);
		Иначе
			ГрузовыеМестаЗависимых.Колонки.Задание.Имя = "ЗаданиеНаПеревозку";
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ГрузовыеМестаЗависимых, ТаблицаЗаданий);
		КонецЕсли; 
	КонецЕсли; 
	
	ЗаданияКУдалениюИзСписка = Новый Массив;
	Для каждого текСтрока Из ГрузовыеМеста Цикл
		ЗаданияКУдалениюИзСписка.Добавить(Новый Структура("Задание, ГрузовоеМесто, КоличествоГрузов", текСтрока.Задание, текСтрока.ГрузовоеМесто, текСтрока.КоличествоГрузов));
	КонецЦикла;
	
	Если ГрузовыеМеста.Количество() > 0 Тогда
		РейсОбъект = Рейс.ПолучитьОбъект();
		Задания = РейсОбъект.Задания;
		Для каждого Строка Из ГрузовыеМеста Цикл
			НоваяСтрока = Задания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла; 
		Задания.Свернуть("Задание, ГрузовоеМесто, Тара", "КоличествоГрузов");
		РейсОбъект.ДополнительныеСвойства.Вставить("ДобавленныеЗадания", ?(ТаблицаЗаданий = Неопределено, МассивЗаданий, ТаблицаЗаданий));
		РейсОбъект.ДополнительныеСвойства.Вставить("ПериодПланирования", ПериодПланирования);
				
		Попытка
			РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Отказ = Истина;
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстСообщения = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упРейс,, ТекстСообщения);
			ТекстОшибки = СтрШаблон("%1%2%3", ТекстОшибки, Символы.ПС, ТекстСообщения);
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли; 
		КонецПопытки;
	КонецЕсли; 
	Возврат ЗаданияКУдалениюИзСписка;
	
КонецФункции

Функция ГрузовыеМестаЗаданийРейса(Знач МассивЗаданий = Неопределено, Знач ТаблицаЗаданий = Неопределено)
	
	Запрос = Новый Запрос;
	Если ТаблицаЗаданий = Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	упЗаданияКПланированиюОстатки.Задание,
		|	упЗаданияКПланированиюОстатки.ГрузовоеМесто,
		|	упЗаданияКПланированиюОстатки.Тара,
		|	упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток КАК КоличествоГрузов
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(, Задание В (&МассивЗаданий)) КАК упЗаданияКПланированиюОстатки
		|ГДЕ
		|	упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > 0";
		Запрос.УстановитьПараметр("МассивЗаданий", МассивЗаданий);
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задания.ЗаданиеНаПеревозку,
		|	Задания.ГрузовоеМесто,
		|	Задания.КоличествоГрузов
		|ПОМЕСТИТЬ втИсходныеДанные
		|ИЗ
		|	&Задания КАК Задания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упЗаданияКПланированиюОстатки.Задание,
		|	упЗаданияКПланированиюОстатки.ГрузовоеМесто,
		|	упЗаданияКПланированиюОстатки.Тара,
		|	ВЫБОР
		|		КОГДА втИсходныеДанные.КоличествоГрузов > упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток
		|			ТОГДА упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток
		|		ИНАЧЕ втИсходныеДанные.КоличествоГрузов
		|	КОНЕЦ КАК КоличествоГрузов
		|ИЗ
		|	РегистрНакопления.упЗаданияКПланированию.Остатки(
		|			,
		|			Задание В
		|				(ВЫБРАТЬ
		|					втИсходныеДанные.ЗаданиеНаПеревозку
		|				ИЗ
		|					втИсходныеДанные КАК втИсходныеДанные)) КАК упЗаданияКПланированиюОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсходныеДанные КАК втИсходныеДанные
		|		ПО упЗаданияКПланированиюОстатки.Задание = втИсходныеДанные.ЗаданиеНаПеревозку
		|			И упЗаданияКПланированиюОстатки.ГрузовоеМесто = втИсходныеДанные.ГрузовоеМесто
		|			И (упЗаданияКПланированиюОстатки.КоличествоКПланированиюОстаток > 0)";
		Запрос.УстановитьПараметр("Задания", ТаблицаЗаданий);
	КонецЕсли;
	ГрузовыеМеста = Запрос.Выполнить().Выгрузить();
	Возврат ГрузовыеМеста;

КонецФункции // ДобавитьЗаданияВРейс()

// Процедура удаляет из рейса задания на перевозку груза.
//
// Параметры:
//  МассивЗаданий           - Массив - Массив ссылок на документы "упЗаданиеНаПеревозкуГруза".
//  ТаблицаЗаданий          - ТаблицаЗначений - задания:
//       * ЗаданиеНаПеревозку    - ДокументСсылка.ЗаданиеНаПеревозкуГруза.
//       * ГрузовоеМесто         - СправочникСсылка.упГрузовыеМеста.
//       * КоличествоГрузов      - Число.
//  Рейс                    - ДокументСсылка.упРейс - Документ.
//  ТекстОшибки             - Строка - Текст.
//  Отказ                   - Булево - Флаг.
//
Процедура УдалитьЗаданияИзРейса(МассивЗаданий = Неопределено, ТаблицаЗаданий = Неопределено, Рейс, ТекстОшибки, Отказ, ДополнительныеПараметры = Неопределено) Экспорт
	
	РейсОбъект = Рейс.ПолучитьОбъект();
	Если РейсОбъект = Неопределено Тогда
		Возврат; // Страховка от того, что рейс уже удален, для формы планирования
	КонецЕсли; 
	Задания = РейсОбъект.Задания;
				
	Если ТаблицаЗаданий = Неопределено Тогда
		Для каждого текЗадание Из МассивЗаданий Цикл
			сткПоиска = Новый Структура("Задание", текЗадание);
			// удаление строк с заданиями из ТЧ "Задания"
			ИскомыеСтроки = Задания.НайтиСтроки(сткПоиска);
			КоличествоСтрок = ИскомыеСтроки.Количество();
			Сч = 0;
			Пока Сч < КоличествоСтрок Цикл
				ИскомаяСтрока = ИскомыеСтроки.Получить(Сч);
				Задания.Удалить(ИскомаяСтрока);
				Сч = Сч + 1;
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		
		Для каждого текСтрока Из ТаблицаЗаданий Цикл
			сткПоиска = Новый Структура("Задание, ГрузовоеМесто", текСтрока.ЗаданиеНаПеревозку, текСтрока.ГрузовоеМесто);
			// удаление строк с заданиями из ТЧ "Задания"
			ИскомыеСтроки = Задания.НайтиСтроки(сткПоиска);
			КоличествоСтрок = ИскомыеСтроки.Количество();
			Сч = 0;
			Пока Сч < КоличествоСтрок Цикл
				ИскомаяСтрока = ИскомыеСтроки.Получить(Сч);
				Задания.Удалить(ИскомаяСтрока);
				Сч = Сч + 1;
			КонецЦикла;
		КонецЦикла
		
	КонецЕсли;
	
	РейсОбъект.ДополнительныеСвойства.Вставить("ИсключенныеЗадания", ?(ТаблицаЗаданий = Неопределено, МассивЗаданий, ТаблицаЗаданий));
	
	РазрешеноУстановитьОтменено = Истина;
	Если ДополнительныеПараметры <> Неопределено Тогда
		РазрешеноУстановитьОтменено = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "РазрешеноУстановитьОтменено", Истина);
		РассчитыватьСтатус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "РассчитыватьСтатус", Неопределено); 
		Если РассчитыватьСтатус <> Неопределено Тогда
			РейсОбъект.ДополнительныеСвойства.Вставить("РассчитыватьСтатус", РассчитыватьСтатус);
		КонецЕсли;
		ЗаписыватьСтатус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЗаписыватьСтатус", Неопределено); 
		Если ЗаписыватьСтатус <> Неопределено Тогда
			РейсОбъект.ДополнительныеСвойства.Вставить("ЗаписыватьСтатус", ЗаписыватьСтатус);
		КонецЕсли;
	КонецЕсли;
	
	Если Истина
		И Задания.Количество() = 0
		И РазрешеноУстановитьОтменено
	Тогда
		РейсОбъект.ДополнительныеСвойства.Вставить("Статус", Перечисления.упСтатусыРейса.Отменен);
	КонецЕсли;
	
	Попытка
		РейсОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Если ТипЗнч(РейсОбъект) = Тип("ДокументОбъект.упРейс") Тогда
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РейсОбъект.ДополнительныеСвойства, "ТекстОшибки", Неопределено);
			ТекстОшибки = СтрШаблон("%1%2%3", ТекстОшибки, Символы.ПС, РейсОбъект.Ссылка);
		КонецЕсли;
		Отказ = Истина;
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстСообщения = ОписаниеОшибки();
		КонецЕсли;
		ЗаписьЖурналаРегистрации("Запись", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упРейс,, ТекстСообщения);
		Если ТекстОшибки = Неопределено Тогда
			ТекстОшибки = СтрШаблон("%1%2%3", ТекстОшибки, Символы.ПС, ТекстСообщения);
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры // УдалитьЗаданияИзРейса()

// Функция определяет рейсы с пересекающимися маршрутами (без учета пересечения депо).
//
// Параметры:
//  МассивРейсов - Массив - Ссылки ДокументСсылка.упРейс.
//
// Возвращаемое значение:
//  Массив - массив пересекающихся рейсов.
//
Функция ВернутьПересекающиесяВТочкахРейсы(МассивРейсов) Экспорт
	
	мсвПересекающихсяРейсов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание.АдресПолучения КАК Адрес
	|ПОМЕСТИТЬ втВершины
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка В(&СписокРейсов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВершины1.Рейс
	|ИЗ
	|	втВершины КАК втВершины1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВершины КАК втВершины2
	|		ПО втВершины1.Рейс <> втВершины2.Рейс
	|			И втВершины1.Адрес = втВершины2.Адрес";
	Запрос.УстановитьПараметр("СписокРейсов", МассивРейсов);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		мсвПересекающихсяРейсов = Результат.Выгрузить().ВыгрузитьКолонку("Рейс");
	КонецЕсли; 
	
	Возврат мсвПересекающихсяРейсов
	
КонецФункции // ВернутьПересекающиесяВТочкахРейсы() 

// Функция определяет рейсы с пересекающимися маршрутами (без учета пересечения депо).
//
// Параметры:
//  МассивРейсов - Массив - Ссылки ДокументСсылка.упРейс.
//
// Возвращаемое значение:
//  Массив - массив пересекающихся рейсов.
//
Функция ВернутьПересекающиесяЗаявкиВТочкахРейсы(МассивРейсов) Экспорт
	
	мсвПересекающихсяРейсов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК Рейс,
	|	упРейсЗадания.Задание.АдресПолучения КАК Адрес,
	|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза
	|ПОМЕСТИТЬ втВершины
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка В(&СписокРейсов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВершины1.Рейс КАК Рейс
	|ИЗ
	|	втВершины КАК втВершины1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВершины КАК втВершины2
	|		ПО втВершины1.Рейс <> втВершины2.Рейс
	|			И втВершины1.Адрес = втВершины2.Адрес
	|			И втВершины1.ЗаявкаНаПеревозкуГруза = втВершины2.ЗаявкаНаПеревозкуГруза";
	Запрос.УстановитьПараметр("СписокРейсов", МассивРейсов);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		мсвПересекающихсяРейсов = Результат.Выгрузить().ВыгрузитьКолонку("Рейс");
	КонецЕсли; 
	
	Возврат мсвПересекающихсяРейсов
	
КонецФункции // ВернутьПересекающиесяВТочкахРейсы() 


// Получить массив ссылок на задания рейса.
//
// Параметры:
//  МассивРейсов - Массив - Ссылки ДокументСсылка.упРейс.
//
// Возвращаемое значение:
//  Массив - массив заданий.
//
Функция ВернутьЗаданияРейсов(МассивРейсов) Экспорт
	
	мсвЗаданий = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРейсЗадания.Задание КАК Задание
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка В(&МассивРейсов)";
	Запрос.УстановитьПараметр("МассивРейсов", МассивРейсов);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		мсвЗаданий = Результат.Выгрузить().ВыгрузитьКолонку("Задание");
	КонецЕсли; 
	
	Возврат мсвЗаданий;

КонецФункции // ВернутьЗаданияРейсов()
 
// Функция выполняет проверку соблюдения последовательности выполнения этапов перевозки: 
//		все задания, предшествующие включенным в рейс, должны быть выполнены.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текст.
//
// Возвращаемое значение:
//   Булево - Результат проверки.
//
Функция ПоследовательностьВыполненияЭтаповМультимодальнойПеревозкиСоблюдена(Рейс, ТекстОшибки) Экспорт
	
	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК Заявка,
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.Задание.НомерВЦепиПоставок КАК ЗаданиеНомерВЦепиПоставок,
	|	упРейсЗадания.Задание.НомерВЦепиПоставокКонец КАК ЗаданиеНомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ втЗаданияРейса
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ упРейсЗадания.Ссылка = &Ссылка
	| И упРейсЗадания.Задание.НомерВЦепиПоставок > 0
	| И упРейсЗадания.Задание.НомерВЦепиПоставокКонец > 0
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗаданияРейсаТ.Заявка КАК Заявка
	|ПОМЕСТИТЬ втЗаявки
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейсаТ
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкиКВыполнениюОстатки.Заявка КАК Заявка,
	|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	упЗаявкиКВыполнениюОстатки.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец
	|ПОМЕСТИТЬ втОстаткиКВыполнению
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению.Остатки(
	|		,
	|		Заявка В (
	|			ВЫБРАТЬ
	|				втЗаявки.Заявка КАК Заявка
	|			ИЗ
	|				втЗаявки КАК втЗаявки)) КАК упЗаявкиКВыполнениюОстатки
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упЗаявкиКВыполнению.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(СтатусыЗаданий.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)) КАК Статус,
	|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	ЗаданиеНаПеревозкуГруза.НомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	упЗаявкиКВыполнению.Заявка КАК Заявка
	|ПОМЕСТИТЬ втЗаданияПоЗаявке
	|ИЗ
	|	РегистрНакопления.упЗаявкиКВыполнению КАК упЗаявкиКВыполнению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаявки КАК втЗаявки
	|	ПО упЗаявкиКВыполнению.Заявка = втЗаявки.Заявка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
	|			,
	|			) КАК СтатусыЗаданий
	|		ПО СтатусыЗаданий.Документ = ЗаданиеНаПеревозкуГруза.Ссылка
	|	ПО ЗаданиеНаПеревозкуГруза.Ссылка = упЗаявкиКВыполнению.Регистратор
	|ГДЕ упЗаявкиКВыполнению.Регистратор ССЫЛКА Документ.упЗаданиеНаПеревозкуГруза
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияРейсаТ.Задание КАК Задание,
	|	втЗаданияРейсаТ.Заявка КАК Заявка,
	|	втОстаткиКВыполнениюТ.НомерВЦепиПоставок КАК ПланНомерВЦепиПоставок,
	|	втОстаткиКВыполнениюТ.НомерВЦепиПоставокКонец КАК ПланНомерВЦепиПоставокКонец
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейсаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиКВыполнению КАК втОстаткиКВыполнениюТ
	|	ПО ИСТИНА
	|		И втОстаткиКВыполнениюТ.Заявка = втЗаданияРейсаТ.Заявка
	|		И втОстаткиКВыполнениюТ.НомерВЦепиПоставокКонец = втЗаданияРейсаТ.ЗаданиеНомерВЦепиПоставок
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияРейсаТ.Задание КАК Задание,
	|	втЗаданияРейсаТ.Заявка КАК Заявка,
	|	втЗаданияРейсаТ.ЗаданиеНомерВЦепиПоставок КАК НомерВЦепиПоставок,
	|	втЗаданияРейсаТ.ЗаданиеНомерВЦепиПоставокКонец КАК НомерВЦепиПоставокКонец,
	|	втЗаданияПоЗаявке.НомерВЦепиПоставок КАК ЗаданиеНомерВЦепиПоставок,
	|	втЗаданияПоЗаявке.НомерВЦепиПоставокКонец КАК ЗаданиеНомерВЦепиПоставокКонец,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втЗаданияПоЗаявке.Регистратор) КАК ПредставлениеЗадания,
	|	втЗаданияПоЗаявке.Статус КАК ЗаданиеСтатус
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейсаТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияПоЗаявке КАК втЗаданияПоЗаявке
	|	ПО ИСТИНА
	|		И втЗаданияРейсаТ.ЗаданиеНомерВЦепиПоставок = втЗаданияПоЗаявке.НомерВЦепиПоставокКонец
	|		И втЗаданияРейсаТ.Заявка = втЗаданияПоЗаявке.Заявка
	|		И втЗаданияРейсаТ.Задание <> втЗаданияПоЗаявке.Регистратор
	|		И НЕ (втЗаданияПоЗаявке.Статус В (ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено),
	|				ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Выполнено)))
	|";

	ИндексЗапросаЕстьПланыПоЗаявке = 4;
	ИндексЗапросаЕстьНеВыполненныеЗадания = 5;
	
	Запрос.УстановитьПараметр("Ссылка", Рейс);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
		
	ТекстОшибки = НСтр("ru = 'Ошибка соблюдения последовательности выполнения мультимодальной перевозки:'") + Символы.ПС;
		
	Если Не РезультатЗапроса[ИндексЗапросаЕстьПланыПоЗаявке].Пустой() Тогда
		Выборка = РезультатЗапроса[ИндексЗапросаЕстьПланыПоЗаявке].Выбрать();
		Выборка.Следующий();
		Представление = НСтр(СтрШаблон("ru = 'Не выполнены все запланированные этапы цепи поставок по %1!'", Выборка.Заявка));
		ТекстОшибки = ТекстОшибки + Представление + Символы.ПС;
		Результат = Ложь;
	Иначе
		Если Не РезультатЗапроса[ИндексЗапросаЕстьНеВыполненныеЗадания].Пустой() Тогда
			Выборка = РезультатЗапроса[ИндексЗапросаЕстьНеВыполненныеЗадания].Выбрать();
			Выборка.Следующий();
			Представление = НСтр(СтрШаблон("ru = '%1 должно быть в статусе ""Выполнено""!'", Выборка.ПредставлениеЗадания));
			ТекстОшибки = ТекстОшибки + Представление + Символы.ПС;
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПоследовательностьВыполненияЭтаповМультимодальнойПеревозкиСоблюдена()

// Функция выполняет проверку соблюдения последовательности выполнения этапов перевозки: 
//		все задания, предшествующие включенным в рейс, должны быть выполнены.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ.
//  ТекстОшибки  - Строка - Текст.
//
// Возвращаемое значение:
//   Булево - Результат проверки.
//
Функция ВсеПрерыванияГрузовогоПотокаОбработаны(Рейс, ТекстОшибки) Экспорт
	
	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.ЗаявкаНаПеревозку КАК ЗаявкаНаПеревозку,
	|	УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.ГрузовоеМесто КАК ГрузовоеМесто,
	|	УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.упПрерыванияГрузовыхПотоков.СрезПоследних(
	|		,
	|		(ЗаявкаНаПеревозку, ГрузовоеМесто) В (
	|				ВЫБРАТЬ
	|				упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|				упРейсЗадания.ГрузовоеМесто КАК ГрузовоеМесто
	|			ИЗ
	|				Документ.упРейс.Задания КАК упРейсЗадания
	|			ГДЕ ИСТИНА
	|				И упРейсЗадания.Ссылка = &Ссылка
	|				И упРейсЗадания.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|			ОБЪЕДИНИТЬ ВСЕ
	|			ВЫБРАТЬ
	|				упРейсЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза,
	|				ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка) КАК ГрузовоеМесто
	|			ИЗ
	|				Документ.упРейс.Задания КАК упРейсЗадания
	|			ГДЕ ИСТИНА
	|				И упРейсЗадания.Ссылка = &Ссылка
	|				И упРейсЗадания.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится))
	|			И Регистратор <> &Ссылка) КАК УпПрерыванияГрузовыхПотоков_СрезПоследнихТ
	|ГДЕ НЕ (УпПрерыванияГрузовыхПотоков_СрезПоследнихТ.Обработано)
	|";


	Запрос.УстановитьПараметр("Ссылка", Рейс);
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		ТекстОшибки = НСтр("ru = 'Существую необработанные прерывания грузового потока:'") + Символы.ПС;
		Пока Выборка.Следующий() Цикл
			Представление = НСтр(СтрШаблон("ru = '%1 по %2(Добавлено документом %3)'", Выборка.ЗаявкаНаПеревозку, Выборка.ГрузовоеМесто, Выборка.Регистратор));
			ТекстОшибки = ТекстОшибки + Представление + Символы.ПС;
		КонецЦикла;
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПоследовательностьВыполненияЭтаповМультимодальнойПеревозкиСоблюдена()

// Получить список заявок на перевозку по рейсу.
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Результат получения.
//
Функция ПолучитьСписокЗаявокНаПеревозкуГрузаПоРейсу(Рейс) Экспорт
	
	тбзЗаявкиНаПеревозкуГруза = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза КАК Ссылка,
	               |	МАКСИМУМ(НЕ упРаботыПоРейсу.Отменена) КАК НеОтменена
	               |ИЗ
	               |	РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс = &Ссылка) КАК упРаботыПоРейсу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	упРаботыПоРейсу.Задание.ЗаявкаНаПеревозкуГруза
	               |
	               |ИМЕЮЩИЕ
	               |	МАКСИМУМ(НЕ упРаботыПоРейсу.Отменена) = ИСТИНА";
	Запрос.УстановитьПараметр("Ссылка", Рейс);
	тбзЗаявкиНаПеревозкуГруза	= Запрос.Выполнить().Выгрузить();
	
	Возврат тбзЗаявкиНаПеревозкуГруза;
		
КонецФункции

// Получить время начало по рейсу.
//
// Параметры:
//  Рейс - ДокументСсылка.упРейс - Ссылка на документ.
//
// Возвращаемое значение:
//   Дата - ДатаВремя.
//
Функция ПолучитьВремяНачалаРейса(Рейс) Экспорт 
	
	ВремяНачалаРейса = '00010101';
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА упФактическиеПараметрыРейса.НачалоВыполнения ЕСТЬ NULL 
	|				ИЛИ упФактическиеПараметрыРейса.НачалоВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения
	|		ИНАЧЕ упФактическиеПараметрыРейса.НачалоВыполнения
	|	КОНЕЦ КАК ВремяНачалаРейса
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	|		ПО упПлановыеПараметрыРейса.Рейс = упФактическиеПараметрыРейса.Рейс
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс = &Рейс";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ВремяНачалаРейса = Выборка.ВремяНачалаРейса;
	КонецЕсли;
	
	Возврат ВремяНачалаРейса;

КонецФункции // ПолучитьВремяНачалаРейса()

// Получить время начало по рейсам.
//
// Параметры:
//  мсвРейс	 - Массив	 - рейсы.
// 
// Возвращаемое значение:
//  Дата - ДатаВремя.
//
Функция ПолучитьВремяНачалаПоРейсам(мсвРейс) Экспорт 
	ВремяНачалаРейса = '00010101';
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА упФактическиеПараметрыРейса.НачалоВыполнения ЕСТЬ NULL
	|				ИЛИ упФактическиеПараметрыРейса.НачалоВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения
	|		ИНАЧЕ упФактическиеПараметрыРейса.НачалоВыполнения
	|	КОНЕЦ КАК ВремяНачалаРейса
	|ИЗ
	|	РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упФактическиеПараметрыРейса КАК упФактическиеПараметрыРейса
	|		ПО упПлановыеПараметрыРейса.Рейс = упФактическиеПараметрыРейса.Рейс
	|ГДЕ
	|	упПлановыеПараметрыРейса.Рейс В (&мсвРейс)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1))";
	Запрос.УстановитьПараметр("мсвРейс", мсвРейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ВремяНачалаРейса = Выборка.ВремяНачалаРейса;
	КонецЕсли;
	
	Возврат ВремяНачалаРейса;

КонецФункции // ПолучитьВремяНачалаРейса()

// Проверка наличия недополученных документов перед завершением рейса
//
// Параметры:
//  Рейс			 - ДокументСсылка.упРейс 	 - рейс.
//  ВидПеревозки	 - СправочникСсылка.упВидыПеревозок 	 - вид перевозки.
//  ТекстОшибки	 - Строка	 - текст ошибки.
// 
// Возвращаемое значение:
//  Булево - Истина, есть недополученные документы.
//
Функция ПроверитьНаличиеНедополученныхДокументов(Рейс, ВидПеревозки = Неопределено, ТекстОшибки) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Или ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
		Если ВидПеревозки = Неопределено Тогда
			ПроверятьНаличиеДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "ВидПеревозки.ЗапрещеноЗавершениеРейсаПриНаличииНедополученныхДокументов");
		Иначе
			ПроверятьНаличиеДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ЗапрещеноЗавершениеРейсаПриНаличииНедополученныхДокументов");
		КонецЕсли; 
		
		Если ПроверятьНаличиеДокументов Тогда
			Если ПолучитьФункциональнуюОпцию("упИспользуетсяПолучениеПередачаДокументовВТочкахРейса") Тогда
				ТаблицаОстатков = Документы.упДвижениеДокументовРейса.ПолучитьОстаткиПоДвижениямДокументовРейса(Рейс);
				Если ТаблицаОстатков <> Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'По рейсу есть непереданные/недополученные документы. Перевод рейса в статус ""Завершен"" запрещено'");
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
				ТаблицаОстатков = Документы.упДвижениеДокументовРейса.ПолучитьОстаткиПоНевозвращеннымДокументам(Рейс);
				Если ТаблицаОстатков <> Неопределено Тогда
					ТекстОшибки = НСтр("ru = 'По рейсу есть невозвращенные документы. Перевод рейса в статус ""Завершен"" запрещено'");
					Возврат Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции	
	
////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидПеревозки");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Взаимодействия.

// Возвращает партнера и контактных лиц сделки.
//
// Параметры:
//  Ссылка - ДокументСсылка - Ссылка на документ.
//
// Возвращаемое значение:
//   Массив - Контакты.
//
Функция ПолучитьКонтакты(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоКонтактам();
	Запрос.УстановитьПараметр("Предмет",Ссылка);
	
	НачатьТранзакцию();
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Результат = Неопределено;
		Иначе
			Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контакт");
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Текст запроса по контактам.
//
// Параметры:
//  ТекстВременнаяТаблица - Строка - Текст.
//  Объединить - Объединить - Флаг.
//
// Возвращаемое значение:
//   Строка - Текст запроса.
//
Функция ТекстЗапросаПоКонтактам(ТекстВременнаяТаблица = "", Объединить = Ложь) Экспорт
	
	ШаблонВыбрать = ?(Объединить,"ВЫБРАТЬ РАЗЛИЧНЫЕ","ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ");
	
	ТекстЗапроса = "
	|%ШаблонВыбрать%
	|	упПредложениеПеревозчика.Перевозчик КАК Контакт " + ТекстВременнаяТаблица + "
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.Ссылка = &Предмет
	|	И (НЕ упПредложениеПеревозчика.Перевозчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПредложениеПеревозчика.Водитель1
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.Ссылка = &Предмет
	|	И (НЕ упПредложениеПеревозчика.Водитель1 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПредложениеПеревозчика.Водитель2
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.Ссылка = &Предмет
	|	И (НЕ упПредложениеПеревозчика.Водитель2 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПредложениеПеревозчика.Экспедитор
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.Ссылка = &Предмет
	|	И (НЕ упПредложениеПеревозчика.Экспедитор = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПредложениеПеревозчика.КонтрагентПеревозчика
	|ИЗ
	|	Документ.упПредложениеПеревозчика КАК упПредложениеПеревозчика
	|ГДЕ
	|	упПредложениеПеревозчика.Ссылка = &Предмет
	|	И (НЕ упПредложениеПеревозчика.КонтрагентПеревозчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ШаблонВыбрать%",ШаблонВыбрать);
	
	Если Объединить Тогда
		
		ТекстЗапроса = "
		| ОБЪЕДИНИТЬ ВСЕ
		|" + ТекстЗапроса;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыФункцииПечати

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Комплект документов на принтер
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "упУправлениеПечатьюКлиент.ПечатьШаблонаПечатныхФормПоЗаданиюНаПеревозкуНаПринтер";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "ПечатьШаблонаПечатныхФормПоЗаданиюНаПеревозкуНаПринтер";
	КомандаПечати.СразуНаПринтер = Истина;
	КомандаПечати.Представление = НСтр("ru = 'Пакет печатных форм по рейсу (на принтер)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "упУправлениеПечатьюКлиент.ПечатьШаблонаПечатныхФормПоЗаданиюНаПеревозку";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "ПечатьШаблонаПечатныхФормПоЗаданиюНаПеревозку";
	КомандаПечати.Представление = НСтр("ru = 'Пакет печатных форм по рейсу'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 2;
	
	// М-15
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "М_15";
	КомандаПечати.Представление = НСтр("ru = 'М-15'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// МХ-1
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "МХ_1";
	КомандаПечати.Представление = НСтр("ru = 'МХ-1'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Поручение Экспедитору
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "ПоручениеЭкспедитору";
	КомандаПечати.Представление = НСтр("ru = 'Поручение Экспедитору'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ЭКСПЕДИТОРСКАЯ РАСПИСКА
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "ЭкспедиторскаяРасписка";
	КомандаПечати.Представление = НСтр("ru = 'Экспедиторская Расписка'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ТТН_Др
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "ТТН_Др";
	КомандаПечати.Представление = НСтр("ru = 'ТТН (CMR)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Этикетка
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "Этикетки";
	КомандаПечати.Представление = НСтр("ru = 'Этикетки (ТТН)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Этикетка
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "Грузовой_Манифест";
	КомандаПечати.Представление = НСтр("ru = 'Грузовой манифест'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Этикетка
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "Этикетки_QR";
	КомандаПечати.Представление = НСтр("ru = 'Этикетки с QR-кодами (ТТН)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		// Доверенность
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.упРейс";
		КомандаПечати.Идентификатор = "Доверенность";
		КомандаПечати.Представление = НСтр("ru = 'Доверенность'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		
		
		// Реестр доверенностей
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.упРейс";
		КомандаПечати.Идентификатор = "РеестрДоверенностей";
		КомандаПечати.Представление = НСтр("ru = 'Реестр доверенностей'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КонецЕсли; 
	
	// Маршрутный лист
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "МаршрутныйЛист";
	КомандаПечати.Представление = НСтр("ru = 'Маршрутный лист'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
	Если ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") Тогда
		// Отчет инкассатора
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.упРейс";
		КомандаПечати.Идентификатор = "ОтчетИнкассатора";
		КомандаПечати.Представление = НСтр("ru = 'Отчет инкассатора'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок= 3;	
	КонецЕсли;
	
	// Товарно-транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "ТТН";
	КомандаПечати.Представление = НСтр("ru = 'Товарно-транспортная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "ТН";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Сопроводительная ведомость
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Идентификатор = "СВ";
	КомандаПечати.Представление = НСтр("ru = 'Сопроводительная ведомость'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Настраиваемый комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.упРейс";
	КомандаПечати.Порядок = 75;
	ТекстИдентификатор = "ТТН_Др,Этикетки,Грузовой_Манифест,МаршрутныйЛист,ТТН,ТН,СВ,МХ_1,М_15";
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками") Тогда
		ТекстИдентификатор = ТекстИдентификатор + ",Доверенность,РеестрДоверенностей";
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации") Тогда
		ТекстИдентификатор = ТекстИдентификатор +",ОтчетИнкассатора";
	КонецЕсли;
	
	КомандаПечати.Идентификатор = ТекстИдентификатор;
	КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений  - значение - ссылка на объект;
//             представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М_15") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"М_15", 
		"М-15",
		СформироватьПечатнуюФорму_М_15(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_М_15");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"МХ_1", 
		"МХ-1",
		СформироватьПечатнуюФорму_МХ_1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_МХ_1");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПоручениеЭкспедитору") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ПоручениеЭкспедитору", 
		"Поручение Экспедитору",
		СформироватьПечатнуюФорму_ПоручениеЭкспедитору(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Поручение_Экспедитору");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭкспедиторскаяРасписка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ЭкспедиторскаяРасписка", 
		"Экспедиторская Расписка",
		СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Экспедиторская_Расписка");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН_Др") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"ТТН_Др", 
		"ТТН (CMR)",
		СформироватьПечатнуюФорму_ТТН_Измен(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН_ДОП");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Этикетки") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"Этикетки", 
		"Этикетки (ТТН)",
		Документы.упЗаданиеНаПеревозкуГруза.СформироватьПечатнуюФорму_Этикеток(МассивОбъектов, ОбъектыПечати, Новый Структура("ЭтоРейс", Истина)),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Этикетка");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Этикетки_QR") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"Этикетки_QR", 
		"Этикетки с QR-кодами (ТТН)",
		Документы.упЗаданиеНаПеревозкуГруза.СформироватьПечатнуюФорму_Этикеток_QR(МассивОбъектов, ОбъектыПечати, Новый Структура("ЭтоРейс", Истина)),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_Этикетка_QR");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Грузовой_Манифест") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"Грузовой_Манифест", 
		"Грузовой манифест",
		Документы.упРейс.СформироватьПечатнуюФорму_Грузовой_Манифест(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упРейс.ПФ_MXL_Грузовой_Манифест");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Доверенность") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"Доверенность", 
		"Доверенность",
		Документы.упДоверенность.ПечатьДоверенность(МассивОбъектов, ОбъектыПечати, Новый Структура("ЭтоРейс", Истина)),
		,
		"Документ.упДоверенность.ПФ_MXL_Доверенность");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеестрДоверенностей") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"РеестрДоверенностей", 
		"Реестр доверенностей",
		Документы.упДоверенность.ПечатьРеестраДоверенностей(МассивОбъектов, ОбъектыПечати, Новый Структура("ЭтоРейс", Истина)),
		,
		"Документ.упДоверенность.ПФ_MXL_РеестрДоверенностей");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МаршрутныйЛист") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"МаршрутныйЛист", 
		"Маршрутный лист",
		СформироватьПечатнуюФорму_МаршрутныйЛист(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.упРейс.ПФ_MXL_МаршрутныйЛист");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОтчетИнкассатора") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ОтчетИнкассатора",
		НСтр("ru = 'Отчет инкассатора'"),
		Документы.упОтчетИнкассатора.ПечатьОтчетИнкассатора(МассивОбъектов, ОбъектыПечати, Новый Структура("ЭтоРейс", Истина)),
		,
		"Документ.упОтчетИнкассатора.ПФ_MXL_ОтчетИнкассатора");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ТТН",
		НСтр("ru = 'Товарно-транспортная накладная'"),
		ПечатьТТН(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН");
	КонецЕсли;	

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТН") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ТН",
		НСтр("ru = 'Транспортная накладная'"),            
		ПечатьТН(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упРейс.ПФ_MXL_ТранспортнаяНакладная");
	КонецЕсли;	

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СВ") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СВ",
		НСтр("ru = 'Сопроводительная ведомость'"),            
		ПечатьСВ(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.упРейс.ПФ_MXL_СопроводительнаяВедомость");             
	КонецЕсли;	
	
КонецПроцедуры // Печать()

// Формирование печатной формы ТТН
Функция ПечатьТТН(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ТТН";

	Если МассивОбъектов.Количество() Тогда 		
		ДанныеДляПечати = ПолучитьДанныеДляПечатныхФорм(МассивОбъектов[0].Задания.ВыгрузитьКолонку("Задание"));
		ЗаполнитьТабличныйДокументТТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	КонецЕсли;
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументТТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка = ДанныеДляПечати.Шапка;
	тбДанныеСтроки = ДанныеДляПечати.Строки;
	тбПлановыеДанныеРейса = ДанныеДляПечати.Рейс;
	тбПутевыеЛисты = ДанныеДляПечати.ПутевойЛист; 
	тбДанныеВодитель = ДанныеДляПечати.Водитель;
	тбДанныеПрицепа = ДанныеДляПечати.Прицеп;
	тбДанныеГруза = ДанныеДляПечати.Груз;
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьЛист2 = Макет.ПолучитьОбласть("Лист2");
	ОбластьШапкаГруза = Макет.ПолучитьОбласть("ШапкаЛист2");
	ОбластьСтрокиГруза = Макет.ПолучитьОбласть("СтрокаЛист2");
	ОбластьПодвалЛист2 = Макет.ПолучитьОбласть("ПодвалЛист2");
	ОбластьПодвалПродолжение = Макет.ПолучитьОбласть("ПодвалПродолжение");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100; 
	
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);
		ОбластьЗаголовок.Параметры.Заполнить(текСтрокаШапка);
		
		// Штрихкод
		Рисунок = ОбластьЗаголовок.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКПечатнойФормыДляПечати(текСтрокаШапка.ДокументОснование,"Документ.упЗаданиеНаПеревозкуГруза.ПФ_MXL_ТТН"));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);

		
		ОбластьЛист2.Параметры.Заполнить(текСтрокаШапка);
		ОбластьПодвал.Параметры.Заполнить(текСтрокаШапка);
		
		// ДоверенноеЛицо
		ДоверенноеЛицо = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрокаШапка.ДоверенноеЛицо, "Наименование"));
		ДоверенноеЛицо = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(ДоверенноеЛицо);
		ОбластьПодвал.Параметры.ДоверенноеЛицо = ДоверенноеЛицо;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьШапка);
	    				
		сткОтбор  = Новый Структура("Документ", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбДанныеСтроки.НайтиСтроки(сткОтбор);
				
	   	ИтогоСуммаСНДС = 0;
		МассаБруттоВсего = 0; // Масса в тоннах.
		МассаНеттоВсего  = 0;
		КоличествоМестВсего = 0;
			
		Если мсвСтроки.Количество() > 0 Тогда
			КоличествоМестВсего = мсвСтроки[0].КоличествоМестВсего;
		КонецЕсли;
		
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтроки.Параметры.Заполнить(текЭлементСтрока);
			ОбластьСтроки.Параметры.СтрокаНомер = СтрокаНомер;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтроки);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(ОбластьПодвал);
			КонецЕсли;
			Попытка
				Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
					ТабличныйДокумент.Вывести(ОбластьСтроки);
				Иначе
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабличныйДокумент.Вывести(ОбластьШапка);
					ТабличныйДокумент.Вывести(ОбластьСтроки);
				КонецЕсли;
			Исключение
				ТабличныйДокумент.Вывести(ОбластьСтроки);
			КонецПопытки;
			ИтогоСуммаСНДС = ИтогоСуммаСНДС + текЭлементСтрока.СуммаСНДС;
			МассаБруттоВсего = МассаБруттоВсего + текЭлементСтрока.МассаБруттоВсего;
			МассаНеттоВсего = МассаНеттоВсего + текЭлементСтрока.МассаНеттоВсего;
			
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;
		
		ОбластьПодвал.Параметры.МассаБруттоВсего = МассаБруттоВсего;
		ОбластьПодвал.Параметры.МассаБруттоВсегоПрописью = ЧислоПрописью(МассаБруттоВсего*1000, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0") + " кг.";
		
		ОбластьПодвал.Параметры.МассаНеттоВсего = МассаНеттоВсего;
		ОбластьПодвал.Параметры.МассаНеттоВсегоПрописью = ЧислоПрописью(МассаНеттоВсего*1000, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0") + " кг.";
		
		// Нет возможности заполнить Количество мест по товару
		//ОбластьПодвал.Параметры.КоличествоМестВсегоПрописью = ЧислоПрописью(КоличествоМестВсего, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");		
		текЧислоСтрок = мсвСтроки.Количество();
				
		ОбластьПодвал.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(текЧислоСтрок, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,ж,,,,,0");
		ОбластьПодвал.Параметры.КоличествоНаименований = ЧислоПрописью(текСтрокаШапка.КоличествоНаименований, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		
		Валюта = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
		ОбластьПодвал.Параметры.СуммаПрописьюРуб = РаботаСКурсамиВалют.СформироватьСуммуПрописью(Цел(ИтогоСуммаСНДС), Справочники.Валюты.ПустаяСсылка(), Истина);
		ОбластьПодвал.Параметры.СуммаКоп = Формат((ИтогоСуммаСНДС - Цел(ИтогоСуммаСНДС)) * 100, "ЧЦ=2; ЧН=00; ЧВН=");
		ОбластьПодвал.Параметры.ИтогоСуммаСНДС = ИтогоСуммаСНДС;
		
		сткОтбор  = Новый Структура("ЗаданиеНаПеревозку", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбПлановыеДанныеРейса.НайтиСтроки(сткОтбор);
		
		Водитель = Справочники.упСотрудники.ПустаяСсылка();
		
		Если  мсвСтроки.Количество() > 0 Тогда
			
			строкаРейса = мсвСтроки[0];
			ПлановаяДатаДоставки = строкаРейса.ПлановоеОкончаниеВыполнения;
			ОбластьЛист2.Параметры.ПлановаяДатаДоставки = ПлановаяДатаДоставки;		
			
			контактанаяИнформация = ПолучитьКонтактнуюИнформацию(строкаРейса.КонтрагентПеревозчика);
			ОбластьЛист2.Параметры.КонтрагентПеревозчика =  "" + строкаРейса.КонтрагентПеревозчика + ?(ЗначениеЗаполнено(контактанаяИнформация), ", " + контактанаяИнформация, "");;
			
			// Водитель.
			Если ЗначениеЗаполнено(строкаРейса.Водитель) Тогда
				ФИОВодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(строкаРейса.Водитель, "Наименование");	
				Водитель = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(ФИОВодителя);
			КонецЕсли;
			ОбластьЛист2.Параметры.Водитель = Водитель;
			ОбластьЛист2.Параметры.ГосНомер = строкаРейса.ГосНомер;
			ОбластьЛист2.Параметры.Модель = строкаРейса.Модель;
		КонецЕсли;
		
		// Водительское удостоверение
		мсвСтрок = тбДанныеВодитель.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ВодительскоеУдостоверение));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];
			ОбластьЛист2.Параметры.ВодительскоеУдостоверение = СтрокаДанные.РегистраторСерия + " " + СтрокаДанные.РегистраторНомер;
		КонецЕсли;

		мсвСтрок = тбДанныеВодитель.НайтиСтроки(Новый Структура("ТипДокумента",Справочники.упТипыДокументовТСВодителей.ЛицензионнаяКарточка));
		Если мсвСтрок.Количество()>0 Тогда
			СтрокаДанные = мсвСтрок[0];		
			ОбластьЛист2.Параметры.ВодительскоеУдостоверение = СтрокаДанные.РегистраторСерия + " " + СтрокаДанные.РегистраторНомер; 
		КонецЕсли;
		
		// Прицеп.		
		Если тбДанныеПрицепа.Количество() > 0 Тогда
			количествоСтрок = тбДанныеПрицепа.Количество();  
			выводСтрок = 0;
			
			// В макете предусмотрен вывод только для 2-х прицепов.
			Пока выводСтрок <= 2 И выводСтрок < количествоСтрок Цикл
				текСтрока = тбДанныеПрицепа[выводСтрок];
				ОбластьЛист2.Параметры["ПрицепМодель" + (выводСтрок+1)] =  текСтрока.ПрицепМодель;
				ОбластьЛист2.Параметры["ПрицепГосНомер" + (выводСтрок+1)] =  текСтрока.ПрицепРегистрационныйНомер;		
				
				выводСтрок = выводСтрок + 1;
			КонецЦикла;			
		КонецЕсли;
		
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвал);
		Попытка
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапка);
				ТабличныйДокумент.Вывести(ОбластьПодвал);
			КонецЕсли;
		Исключение
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		КонецПопытки;
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьЛист2.Параметры.Заполнить(текСтрокаШапка);
		
		// Заполним путевой лист.
		Если тбПутевыеЛисты.Количество() > 0 Тогда
			НомераПутевыхЛистов = "";
			Для каждого строкаНомер Из тбПутевыеЛисты Цикл
				НомераПутевыхЛистов = НомераПутевыхЛистов + строкаНомер.Номер + ", ";
			КонецЦикла;
			НомераПутевыхЛистов = Лев(НомераПутевыхЛистов, СтрДлина(НомераПутевыхЛистов)-2);
			ОбластьЛист2.Параметры.НомерПутевогоЛиста = НомераПутевыхЛистов;
		КонецЕсли;

		ТабличныйДокумент.Вывести(ОбластьЛист2);
		ТабличныйДокумент.Вывести(ОбластьШапкаГруза);
		
		// Выводим строки груза.
		сткОтбор = Новый Структура("Документ", текСтрокаШапка.ДокументОснование);
		мсвСтроки = тбДанныеГруза.НайтиСтроки(сткОтбор);			
		
		СтрокаНомер = 1;
		КоличествоСтрок = мсвСтроки.Количество();
		Для Каждого текЭлементСтрока Из мсвСтроки Цикл
			ОбластьСтрокиГруза.Параметры.Заполнить(текЭлементСтрока);
			ОбластьСтрокиГруза.Параметры.СтрокаНомер = СтрокаНомер;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтрокиГруза);
			Если СтрокаНомер = КоличествоСтрок Тогда
				СтрокаСПодвалом.Добавить(ОбластьПодвалЛист2);
			КонецЕсли;
			Попытка
				Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
					ТабличныйДокумент.Вывести(ОбластьСтрокиГруза);
				Иначе
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабличныйДокумент.Вывести(ОбластьШапкаГруза);
					ТабличныйДокумент.Вывести(ОбластьСтрокиГруза);
				КонецЕсли;
			Исключение
				ТабличныйДокумент.Вывести(ОбластьСтрокиГруза);
			КонецПопытки;			
			
			СтрокаНомер = СтрокаНомер + 1;
		КонецЦикла;
				
		// Выводим подвал 2 лист.
		ОбластьПодвалЛист2.Параметры.Заполнить(текСтрокаШапка);
		ОбластьПодвалЛист2.Параметры.МассаБруттоВсего = МассаБруттоВсего;
		ОбластьПодвалЛист2.Параметры.МассаБруттоВсегоПрописью = ЧислоПрописью(МассаБруттоВсего*1000, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		ОбластьПодвалЛист2.Параметры.КоличествоМестВсегоПрописью = ЧислоПрописью(КоличествоМестВсего, "Л=ru_RU; ЧДЦ=; ДП=Ложь; НД=Ложь",",,,с,,,,,0");
		ОбластьПодвалЛист2.Параметры.Водитель = Водитель;

		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьПодвалЛист2);
		Попытка
			Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвалЛист2);
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапкаГруза);
				ТабличныйДокумент.Вывести(ОбластьПодвалЛист2);
			КонецЕсли;
		Исключение
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		КонецПопытки;

		ТабличныйДокумент.Вывести(ОбластьПодвалПродолжение);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.ДокументОснование);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
				
КонецПроцедуры

// Формирование печатной формы ТН
Функция ПечатьТН(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ТН";

	ДанныеДляПечати = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьДанныеДляПФ_ТН(МассивОбъектов);
	Документы.упЗаданиеНаПеревозкуГруза.ЗаполнитьТабличныйДокументТН(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);

  	Возврат ТабличныйДокумент;
КонецФункции

// Формирование печатной формы СВ
Функция ПечатьСВ(МассивОбъектов, ОбъектыПечати)
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_СВ";

	Для каждого ЭлементМассиваОбъектов ИЗ МассивОбъектов Цикл 		
		ДанныеДляПечати = ПолучитьДанныеДляПФ_СВ(ЭлементМассиваОбъектов);
		ЗаполнитьТабличныйДокументСВ(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	КонецЦикла;
  	Возврат ТабличныйДокумент;
КонецФункции

Процедура ЗаполнитьТабличныйДокументСВ(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеСраниц = ДанныеДляПечати.ДанныеСраниц;
	тбПутевыеЛисты = ДанныеДляПечати.ПутевыеЛисты;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упРейс.ПФ_MXL_СопроводительнаяВедомость");
	
	ОбластьШапка1 = Макет.ПолучитьОбласть("ОбластьШапка1");
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка"); 
	ОбластьСтрокиПункта4 = Макет.ПолучитьОбласть("ОбластьСтрокиПункта4"); 
	ОбластьШапка2 = Макет.ПолучитьОбласть("ОбластьШапка2"); 
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
	ОбластьКонтейнер = Макет.ПолучитьОбласть("ОбластьКонтейнер");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто
	|ПОМЕСТИТЬ СписокГМ
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	|ГДЕ
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = &Задание
	|
	|СГРУППИРОВАТЬ ПО
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокГМ.ГрузовоеМесто.Код КАК Номер,
	|	СписокГМ.ГрузовоеМесто.Масса КАК Грузоподъемность
	|ИЗ
	|	СписокГМ КАК СписокГМ";
	
	Для Каждого текСтрокаДанных Из тбДанныеСраниц Цикл  
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Пункт4_1 = "";
		// ОбластьШапка1 
		ОбластьШапка1.Параметры.Заполнить(текСтрокаДанных);
		
		ОбластьШапка1.Параметры.Пункт3_3 = ?(ЗначениеЗаполнено(текСтрокаДанных.Масса),"масса " + СТРОКА(текСтрокаДанных.Масса),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.Ширина) И ЗначениеЗаполнено(текСтрокаДанных.Длина) И  ЗначениеЗаполнено(текСтрокаДанных.Высота), ", ВГХ " + СТРОКА(текСтрокаДанных.Ширина) + "х"+СТРОКА(текСтрокаДанных.Длина)+"х"+СТРОКА(текСтрокаДанных.Высота),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.Объем),", объем "+СТРОКА(текСтрокаДанных.Объем),""); 
		
		// Пункт1_1 Грузоотправитель Физ
		Если текСтрокаДанных.Пункт1_1 = Неопределено Тогда
			Адрес1_1 = "";
			Телефон1_1 = "";
		Иначе
			Адрес1_1 = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт1_1, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
			Телефон1_1 = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт1_1, ?(ТипЗнч(текСтрокаДанных.Пункт1_1)=ТИП("СправочникСсылка.упКонтрагенты"),Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,Справочники.ВидыКонтактнойИнформации.ТелефонПартнера));
		КонецЕсли;
		Пункт1_1 = текСтрокаДанных.Пункт1_1;
		Пункт1_1 = ?(ЗначениеЗаполнено(Адрес1_1), СТРОКА(Пункт1_1) +", " + Адрес1_1, СТРОКА(Пункт1_1));	
		Пункт1_1 = ?(ЗначениеЗаполнено(Телефон1_1), СТРОКА(Пункт1_1)+", " + СокрЛП(Телефон1_1), СТРОКА(Пункт1_1));
		ОбластьШапка1.Параметры.Пункт1_1 = Пункт1_1;
		
		// Пункт2_1   Грузополучатель Физ
		Если текСтрокаДанных.Пункт2_1 = Неопределено Тогда
			Адрес2_1 = "";
			Телефон2_1 = "";
		Иначе
			Адрес2_1 = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт2_1, Справочники.ВидыКонтактнойИнформации.АдресПартнера); 
			Телефон2_1 = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт2_1, ?(ТипЗнч(текСтрокаДанных.Пункт2_1)=ТИП("СправочникСсылка.упКонтрагенты"),Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,Справочники.ВидыКонтактнойИнформации.ТелефонПартнера));
		КонецЕсли;
		
		Пункт2_1 = текСтрокаДанных.Пункт2_1;
		Пункт2_1 = ?(ЗначениеЗаполнено(Адрес2_1), СТРОКА(Пункт2_1) +", " + Адрес2_1, СТРОКА(Пункт2_1));	
		Пункт2_1 = ?(ЗначениеЗаполнено(Телефон2_1), СТРОКА(Пункт2_1)+", " + СокрЛП(Телефон2_1), СТРОКА(Пункт2_1));
		ОбластьШапка1.Параметры.Пункт2_1 = Пункт2_1;
		
		
		// Пункт1_2 Грузоотправитель Юр
		Если текСтрокаДанных.Пункт1_2 = Неопределено Тогда
			текАдрес = "";
			текТелефон = "";
		Иначе 
			текАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт1_2, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
			текТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт1_2, ?(ТипЗнч(текСтрокаДанных.Пункт1_2)=ТИП("СправочникСсылка.упКонтрагенты"),Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,Справочники.ВидыКонтактнойИнформации.ТелефонПартнера));
		КонецЕсли;
		Пункт1_2 = текСтрокаДанных.Пункт1_2;
		Пункт1_2 = ?(ЗначениеЗаполнено(текАдрес), СТРОКА(Пункт1_2) +", " + текАдрес, СТРОКА(Пункт1_2));	
		Пункт1_2 = ?(ЗначениеЗаполнено(текТелефон), СТРОКА(Пункт1_2)+", " + СокрЛП(текТелефон), СТРОКА(Пункт1_2));
		ОбластьШапка1.Параметры.Пункт1_2 = Пункт1_2;
		
		// Пункт2_2   Грузополучатель Юр
		Если текСтрокаДанных.Пункт2_2 = Неопределено Тогда
			Адрес2_2 = "";
			Телефон2_2 = "";
		Иначе 
			Адрес2_2 = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт2_2, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента); 
			Телефон2_2 = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрокаДанных.Пункт2_2, ?(ТипЗнч(текСтрокаДанных.Пункт2_2)=ТИП("СправочникСсылка.упКонтрагенты"),Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,Справочники.ВидыКонтактнойИнформации.ТелефонПартнера));
		КонецЕсли;
		Пункт2_2 = текСтрокаДанных.Пункт2_2;
		Пункт2_2 = ?(ЗначениеЗаполнено(Адрес2_2), СТРОКА(Пункт2_2) +", " + Адрес2_2, СТРОКА(Пункт2_2));	
		Пункт2_2 = ?(ЗначениеЗаполнено(Телефон2_2), СТРОКА(Пункт2_2)+", " + СокрЛП(Телефон2_2), СТРОКА(Пункт2_2));
		ОбластьШапка1.Параметры.Пункт2_2 = Пункт2_2;
		
		ТабличныйДокумент.Вывести(ОбластьШапка1);
		
		// ОбластьСтрокиПункта4 +
		ТабличныйДокумент.Вывести(ОбластьКонтейнер);
		ТабличныйДокумент.Вывести(ОбластьШапка);  
		ТЧЗадания = текСтрокаДанных.СсылкаРейс.Задания;
		СтрокаНомер = 1;
		
		Если текСтрокаДанных.ВидПеревозкиИГМ = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится
			ИЛИ  текСтрокаДанных.ВидПеревозкиИГМ = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса Тогда
			МассивЗаданий = ТЧЗадания.ВыгрузитьКолонку("Задание");
			Запрос.УстановитьПараметр("Задание", текСтрокаДанных.Задание);
			СписокГрузовыхМест = Запрос.Выполнить().Выбрать();
			
			КоличествоСтрок = СписокГрузовыхМест.Количество();
			Пока СписокГрузовыхМест.Следующий() Цикл  
				Пункт4_1 = СписокГрузовыхМест.Номер  + ", "+ СписокГрузовыхМест.Грузоподъемность; 			 
				ОбластьСтрокиПункта4.Параметры.Пункт4_1 = Пункт4_1;
				
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбластьСтрокиПункта4);
				Если СтрокаНомер = КоличествоСтрок Тогда
					СтрокаСПодвалом.Добавить(ОбластьПодвал);
				КонецЕсли;
				
				Попытка
					Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.Вывести(ОбластьСтрокиПункта4);
					Иначе
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ТабличныйДокумент.Вывести(ОбластьШапка);
						ТабличныйДокумент.Вывести(ОбластьСтрокиПункта4);
					КонецЕсли;
				Исключение
					ТабличныйДокумент.Вывести(ОбластьСтрокиПункта4);
				КонецПопытки;
				
				СтрокаНомер = СтрокаНомер+1;
			КонецЦикла;
			
		Иначе
			Для каждого стр Из ТЧЗадания Цикл
				Пункт4_1 = стр.ГрузовоеМесто.Код  + ", "+ стр.ГрузовоеМесто.Масса;
				ОбластьСтрокиПункта4.Параметры.Пункт4_1 = Пункт4_1;
				
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбластьСтрокиПункта4);
				Если СтрокаНомер = КоличествоСтрок Тогда
					СтрокаСПодвалом.Добавить(ОбластьПодвал);
				КонецЕсли;
				
				Попытка
					Если ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
						ТабличныйДокумент.Вывести(ОбластьСтрокиПункта4);
					Иначе
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ТабличныйДокумент.Вывести(ОбластьШапка);
						ТабличныйДокумент.Вывести(ОбластьСтрокиПункта4);
					КонецЕсли;
				Исключение
					ТабличныйДокумент.Вывести(ОбластьСтрокиПункта4);
				КонецПопытки;
				
				СтрокаНомер = СтрокаНомер+1;
				
			КонецЦикла;
		КонецЕсли;
		// ОбластьСтрокиПункта4 -
		
		// ОбластьШапка2 
		ОбластьШапка2.Параметры.Заполнить(текСтрокаДанных);		
		ОбластьШапка2.Параметры.Пункт5_1 = ?(ЗначениеЗаполнено(текСтрокаДанных.Наименование),текСтрокаДанных.Наименование + " ( " 
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.Ширина) И ЗначениеЗаполнено(текСтрокаДанных.Длина) И  ЗначениеЗаполнено(текСтрокаДанных.Высота), "ВГХ " + СТРОКА(текСтрокаДанных.Ширина) + "х"+СТРОКА(текСтрокаДанных.Длина)+"х"+СТРОКА(текСтрокаДанных.Высота),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.Масса), ", масса " + СТРОКА(текСтрокаДанных.Масса),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.Объем), ", объем " + СТРОКА(текСтрокаДанных.Объем),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.КоличествоМест),", кол-во мест " + СТРОКА(текСтрокаДанных.КоличествоМест),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.КоличествоЗагрузочныхМетров),", кол-во загр. метров " + СТРОКА(текСтрокаДанных.КоличествоЗагрузочныхМетров) ,""), "")  
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.ТипТС),", тип: " + СТРОКА(текСтрокаДанных.ТипТС),"")
		+ ?(ЗначениеЗаполнено(текСтрокаДанных.Модель),", марка: " + СТРОКА(текСтрокаДанных.Модель)+ ")","");
		
		ОбластьШапка2.Параметры.Пункт11_1 = ОбластьШапка2.Параметры.Пункт5_1;
		
		Если ЗначениеЗаполнено(ОбластьШапка2.Параметры.Пункт10_1) Тогда
			КИ = ПолучитьКонтактнуюИнформацию(ОбластьШапка2.Параметры.Пункт10_1);
			ОбластьШапка2.Параметры.Пункт10_1 = СТРОКА(ОбластьШапка2.Параметры.Пункт10_1) +?(ЗначениеЗаполнено(КИ), ", " + КИ, "");
		КонецЕсли;
		
		КИ = ПолучитьКонтактнуюИнформацию(ОбластьШапка2.Параметры.Пункт10_2);
		ОбластьШапка2.Параметры.Пункт10_2 = СТРОКА(ОбластьШапка2.Параметры.Пункт10_2) +?(ЗначениеЗаполнено(КИ), ", " + КИ, "");
		
		// Пункт10_5 - Номера путевых листов.
		Если тбПутевыеЛисты.Количество() > 0 Тогда
			НомераПутевыхЛистов = "";
			Для каждого строкаНомер Из тбПутевыеЛисты Цикл
				НомераПутевыхЛистов = НомераПутевыхЛистов + строкаНомер.Номер + ", ";
			КонецЦикла;
			НомераПутевыхЛистов = Лев(НомераПутевыхЛистов, СтрДлина(НомераПутевыхЛистов)-2);
			ОбластьШапка2.Параметры.Пункт10_5 = НомераПутевыхЛистов;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапка2);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаДанных.СсылкаРейс);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
	
КонецПроцедуры
 
// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат формирования. 
//
Функция СформироватьПечатнуюФорму_Грузовой_Манифест(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт	
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.Грузовой_Манифест";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упРейс.ПФ_MXL_Грузовой_Манифест");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРейс.Ссылка КАК Ссылка,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель1 КАК Водитель1,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель2 КАК Водитель2,
	|	ВЫБОР
	|		КОГДА упРейс.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПустыеГрузовыеМеста
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&МассивОбъектов)) КАК упПеревозчикПоРейсуСрезПоследних
	|	ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
	|ГДЕ упРейс.Ссылка В  (&МассивОбъектов)
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упРаботыПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упРаботыПоРейсуСрезПоследних.Регистратор КАК Регистратор,
	|	упРаботыПоРейсуСрезПоследних.Отменена КАК Отменена,
	|	упРаботыПоРейсуСрезПоследних.Задание КАК Задание,
	|	упРаботыПоРейсуСрезПоследних.Операция КАК Операция,
	|	упРаботыПоРейсуСрезПоследних.ГрузовоеМесто КАК ГрузовоеМесто
	|ПОМЕСТИТЬ втВсеРаботы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (&МассивОбъектов)) КАК упРаботыПоРейсуСрезПоследних
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсыИзОбъекта
	|	ПО втРейсыИзОбъекта.Ссылка = упРаботыПоРейсуСрезПоследних.Рейс
	|;
	|//{Запрос: 2 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРаботыПоРейсу.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ втРегистраторы
	|ИЗ
	|	РегистрСведений.упРаботыПоРейсу КАК упРаботыПоРейсу
	|ГДЕ ИСТИНА
	|	И упРаботыПоРейсу.Регистратор ССЫЛКА Документ.упКорректировкаРейса
	|	И упРаботыПоРейсу.Рейс В  (&МассивОбъектов)
	|;
	|//{Запрос: 3 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРаботы.Задание КАК Задание,
	|	ВЫБОР
	|		КОГДА втРейсыИзОбъекта.ПустыеГрузовыеМеста
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ КАК ГрузовоеМесто,
	|	СУММА(ВЫБОР
	|		КОГДА втРаботы.Отменена
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоОтменена,
	|	СУММА(1) КАК КоличествоОпераций
	|ПОМЕСТИТЬ втОтмененныеЗадания
	|ИЗ
	|	втВсеРаботы КАК втРаботы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейсы КАК втРейсыИзОбъекта
	|	ПО втРаботы.Рейс = втРейсыИзОбъекта.Ссылка
	|ГДЕ ЛОЖЬ
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|	ИЛИ втРаботы.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)
	|СГРУППИРОВАТЬ ПО
	|	втРаботы.Задание,
	|	ВЫБОР
	|		КОГДА втРейсыИзОбъекта.ПустыеГрузовыеМеста
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)
	|		ИНАЧЕ втРаботы.ГрузовоеМесто
	|	КОНЕЦ
	|;
	|//{Запрос: 4 ////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.Ссылка КАК Рейс
	|ПОМЕСТИТЬ втЗаданияРейса
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упРейсЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упРейсЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|ГДЕ ИСТИНА
	|	И упРейсЗадания.Ссылка В  (&МассивОбъектов)
	|	И НЕ (ВЫБОР
	|			КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	|					И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упКорректировкаРейсаЗадания.Задание КАК Задание,
	|	упКорректировкаРейсаЗадания.Ссылка.Рейс КАК Рейс
	|ИЗ
	|	Документ.упКорректировкаРейса.Задания КАК упКорректировкаРейсаЗадания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторы КАК втРегистраторы
	|	ПО упКорректировкаРейсаЗадания.Ссылка = втРегистраторы.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОтмененныеЗадания КАК втОтмененныеЗадания
	|	ПО ИСТИНА
	|		И упКорректировкаРейсаЗадания.Задание = втОтмененныеЗадания.Задание
	|		И упКорректировкаРейсаЗадания.ГрузовоеМесто = втОтмененныеЗадания.ГрузовоеМесто
	|ГДЕ НЕ (ВЫБОР
	|		КОГДА ЕСТЬNULL(втОтмененныеЗадания.КоличествоОтменена, 0) = ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0)
	|				И ЕСТЬNULL(втОтмененныеЗадания.КоличествоОпераций, 0) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)
	|;
	|//{Запрос: 5 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка КАК Задание,
	|	СУММА(упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов) КАК КоличествоГрузов,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Масса, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Масса, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Масса))) КАК Вес,
	|	СУММА(ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.Объем, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.Объем, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.Объем))) КАК Объем,
	|	СУММА(ВЫБОР
	|		КОГДА ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.КоличествоМест)) = 0
	|			ТОГДА упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.КоличествоМест
	|		ИНАЧЕ ЕСТЬNULL(упПараметрыЗаявкиНаПеревозку.КоличествоМест, ЕСТЬNULL(упПараметрыЗаданияНаПеревозку.КоличествоМест, упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто.КоличествоМест))
	|	КОНЕЦ) КАК КоличествоМест
	|ПОМЕСТИТЬ втГрузЗадания
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаявкиНаПеревозку.СрезПоследних(
	|		,
	|		ЗаявкаНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание.ЗаявкаНаПеревозкуГруза КАК ЗаданиеЗаявкаНаПеревозкуГруза
	|			ИЗ
	|				втЗаданияРейса КАК втЗадания
	|			ГДЕ втЗадания.Задание.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка))) КАК упПараметрыЗаявкиНаПеревозку
	|	ПО ИСТИНА
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка.ЗаявкаНаПеревозкуГруза = упПараметрыЗаявкиНаПеревозку.ЗаявкаНаПеревозку
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаявкиНаПеревозку.ГрузовоеМесто
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПараметрыЗаданияНаПеревозку.СрезПоследних(
	|		,
	|		ЗаданиеНаПеревозку В (
	|			ВЫБРАТЬ
	|				втЗадания.Задание КАК Задание
	|			ИЗ
	|				втЗаданияРейса КАК втЗадания)) КАК упПараметрыЗаданияНаПеревозку
	|	ПО ИСТИНА
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка = упПараметрыЗаданияНаПеревозку.ЗаданиеНаПеревозку
	|		И упЗаданиеНаПеревозкуГрузаГрузовыеМеста.ГрузовоеМесто = упПараметрыЗаданияНаПеревозку.ГрузовоеМесто
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаданияРейса КАК втЗаданияРейса
	|	ПО втЗаданияРейса.Задание = упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка
	|;
	|//{Запрос: 6 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияРейса.Рейс КАК Рейс,
	|	втЗаданияРейса.Задание КАК Задание,
	|	упРейсРаботы.Операция КАК Операция,
	|	упРейсРаботы.ПлановоеВремяНачалаРабот КАК ПлановоеВремяНачалаРабот,
	|	упРейсМаршрут.Адрес КАК Адрес
	|ПОМЕСТИТЬ втМаршрутРейса
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейса
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(
	|		,
	|		Рейс В (
	|			ВЫБРАТЬ
	|				втЗаданияРейса.Рейс КАК Рейс
	|			ИЗ
	|				втЗаданияРейса КАК втЗаданияРейса)) КАК упРейсРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упМаршрутПоРейсу.СрезПоследних(
	|			,
	|			Рейс В (
	|				ВЫБРАТЬ
	|					втЗаданияРейса.Рейс КАК Рейс
	|				ИЗ
	|					втЗаданияРейса КАК втЗаданияРейса)) КАК упРейсМаршрут
	|		ПО ИСТИНА
	|			И упРейсРаботы.Рейс = упРейсМаршрут.Рейс
	|			И упРейсРаботы.Идентификатор = упРейсМаршрут.Идентификатор
	|	ПО ИСТИНА
	|		И втЗаданияРейса.Рейс = упРейсРаботы.Рейс
	|		И втЗаданияРейса.Задание = упРейсРаботы.Задание
	|СГРУППИРОВАТЬ ПО
	|	упРейсРаботы.Операция,
	|	упРейсРаботы.ПлановоеВремяНачалаРабот,
	|	втЗаданияРейса.Задание,
	|	упРейсМаршрут.Адрес,
	|	втЗаданияРейса.Рейс
	|;
	|//{Запрос: 7 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	М1.Рейс КАК Рейс,
	|	М1.Задание КАК Задание,
	|	НАЧАЛОПЕРИОДА(М1.ПлановоеВремяНачалаРабот, ДЕНЬ) КАК ПлановоеВремяНачалаРабот,
	|	М2.Адрес КАК АдресПогрузки,
	|	М1.Адрес КАК АдресВыгрузки
	|ПОМЕСТИТЬ втАдресаЗаданий
	|ИЗ
	|	втМаршрутРейса КАК М1
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаршрутРейса КАК М2
	|	ПО ИСТИНА
	|		И М1.Рейс = М2.Рейс
	|		И М1.Задание = М2.Задание
	|ГДЕ ИСТИНА
	|	И М1.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка)
	|	И М2.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|СГРУППИРОВАТЬ ПО
	|	М1.Рейс,
	|	М1.Задание,
	|	М2.Адрес,
	|	М1.Адрес,
	|	НАЧАЛОПЕРИОДА(М1.ПлановоеВремяНачалаРабот, ДЕНЬ)
	|;
	|//{Запрос: 8 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияРейса.Рейс КАК Рейс,
	|	втЗаданияРейса.Рейс.Номер КАК Номер,
	|	втЗаданияРейса.Рейс.Дата КАК Дата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втЗаданияРейса.Задание) КАК КоличествоПунктов,
	|	СУММА(втГрузЗадания.Объем) КАК СуммарныйОбъем,
	|	СУММА(втГрузЗадания.Вес) КАК СуммарныйВес,
	|	СУММА(втГрузЗадания.КоличествоМест) КАК КоличествоМест
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейса
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузЗадания КАК втГрузЗадания
	|	ПО втЗаданияРейса.Задание = втГрузЗадания.Задание
	|СГРУППИРОВАТЬ ПО
	|	втЗаданияРейса.Рейс,
	|	втЗаданияРейса.Рейс.Номер,
	|	втЗаданияРейса.Рейс.Дата
	|;
	|//{Запрос: 9 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсы.Ссылка КАК Рейс,
	|	втРейсы.Водитель1 КАК Сотрудник,
	|	2 КАК НомерСтроки,
	|	втРейсы.Водитель1.ТипСотрудника КАК Должность,
	|	втРейсы.Водитель1.Представление КАК Представление
	|ИЗ
	|	втРейсы КАК втРейсы
	|ГДЕ втРейсы.Водитель1 <> ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втРейсы.Ссылка КАК Рейс,
	|	втРейсы.Водитель2 КАК Сотрудник,
	|	3 КАК НомерСтроки,
	|	втРейсы.Водитель2.ТипСотрудника КАК Должность,
	|	втРейсы.Водитель2.Представление КАК Представление
	|ИЗ
	|	втРейсы КАК втРейсы
	|ГДЕ втРейсы.Водитель2 <> ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|УПОРЯДОЧИТЬ ПО
	|	Рейс,
	|	НомерСтроки
	|;
	|//{Запрос: 10 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаданияРейса.Рейс КАК Рейс,
	|	втЗаданияРейса.Задание КАК Задание,
	|	упЗаданиеНаПеревозкуГруза.Номер КАК НомерЗаявкиТТЕ,
	|	упЗаданиеНаПеревозкуГруза.Соглашение.ГруппаТарифов КАК ГруппаТарифов,
	|	упЗаданиеНаПеревозкуГруза.НомерКИС КАК НомерНакладной,
	|	втАдресаЗаданий.АдресПогрузки КАК АдресПогрузки,
	|	втАдресаЗаданий.АдресВыгрузки КАК АдресВыгрузки,
	|	втГрузЗадания.Вес КАК Вес,
	|	втГрузЗадания.Объем КАК Объем,
	|	втГрузЗадания.КоличествоМест КАК КоличествоМест,
	|	втАдресаЗаданий.ПлановоеВремяНачалаРабот КАК ДатаВыгрузки,
	|	втАдресаЗаданий.АдресВыгрузки.Комментарий КАК ТребованияТочки,
	|	упЗаданиеНаПеревозкуГруза.Комментарий КАК Комментарий
	|ИЗ
	|	втЗаданияРейса КАК втЗаданияРейса
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза КАК упЗаданиеНаПеревозкуГруза
	|	ПО втЗаданияРейса.Задание = упЗаданиеНаПеревозкуГруза.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАдресаЗаданий КАК втАдресаЗаданий
	|	ПО ИСТИНА
	|		И втЗаданияРейса.Задание = втАдресаЗаданий.Задание
	|		И втЗаданияРейса.Рейс = втАдресаЗаданий.Рейс
	|	ЛЕВОЕ СОЕДИНЕНИЕ втГрузЗадания КАК втГрузЗадания
	|	ПО втЗаданияРейса.Задание = втГрузЗадания.Задание
	|";

	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = ПакетЗапросов.Количество();
	
	ВыборкаШапка = ПакетЗапросов[КоличествоТаблиц - 3].Выбрать();
	ВыборкаСотрудники = ПакетЗапросов[КоличествоТаблиц - 2].Выбрать();
	ТабличнаяЧасть = ПакетЗапросов[КоличествоТаблиц - 1].Выбрать();
	
	Пока ВыборкаШапка.Следующий() Цикл		
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Шапка.Параметры.Номер = ВыборкаШапка.Номер;
		Шапка.Параметры.Дата = Формат(ВыборкаШапка.Дата,"ДЛФ=DD");
		Шапка.Параметры.СуммарныйВес = ВыборкаШапка.СуммарныйВес;
		Шапка.Параметры.СуммарныйОбъем = ВыборкаШапка.СуммарныйОбъем;
		Шапка.Параметры.КоличествоМест = ВыборкаШапка.КоличествоМест;
		Шапка.Параметры.КоличествоПунктов = ВыборкаШапка.КоличествоПунктов;		
		
		текОтбор = Новый Структура("Рейс",ВыборкаШапка.Рейс);
		ВыборкаСотрудники.Сбросить();
		Сч = 0;
		Пока ВыборкаСотрудники.НайтиСледующий(текОтбор) Цикл 
			Сч = Сч + 1;
			Шапка.Параметры["Водитель" + Строка(Сч)] = ВыборкаСотрудники.Представление;
			Шапка.Параметры["РасшифровкаВодитель" + Строка(Сч)] = ВыборкаСотрудники.Сотрудник;
			Шапка.Параметры["дВодитель" + Строка(Сч)] = ВыборкаСотрудники.Должность;
			Если Сч >= 2 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;	
				
		// Штрихкод
		Рисунок = Шапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(ВыборкаШапка.Рейс));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
				
		ТабличныйДокумент.Вывести(Шапка);
		
		ТабличнаяЧасть.Сбросить();
		
		Нпп = 0;
		Пока ТабличнаяЧасть.НайтиСледующий(текОтбор) Цикл 
			Нпп = Нпп + 1;
			ОбластьСтрокаТаблицы.Параметры.нПорядок = Строка(Нпп);
			ОбластьСтрокаТаблицы.Параметры.нЗаявки = ТабличнаяЧасть.НомерЗаявкиТТЕ;			
			ОбластьСтрокаТаблицы.Параметры.ГруппаТарифов = ТабличнаяЧасть.ГруппаТарифов;
			ОбластьСтрокаТаблицы.Параметры.НомерНакладной = ТабличнаяЧасть.НомерНакладной;
			ОбластьСтрокаТаблицы.Параметры.АдресПогрузки = ТабличнаяЧасть.АдресПогрузки;
			ОбластьСтрокаТаблицы.Параметры.АдресВыгрузки = ТабличнаяЧасть.АдресВыгрузки;
			ОбластьСтрокаТаблицы.Параметры.Вес = ТабличнаяЧасть.Вес;
			ОбластьСтрокаТаблицы.Параметры.Объем = ТабличнаяЧасть.Объем;
			ОбластьСтрокаТаблицы.Параметры.КолМест = ТабличнаяЧасть.КоличествоМест;
			ОбластьСтрокаТаблицы.Параметры.ДатаВыгрузки = ТабличнаяЧасть.ДатаВыгрузки;
			ОбластьСтрокаТаблицы.Параметры.ТребованияТочки = ТабличнаяЧасть.ТребованияТочки;
			ОбластьСтрокаТаблицы.Параметры.Коменнтарий = ТабличнаяЧасть.Комментарий;
			
			ОбластьСтрокаТаблицы.Параметры.РасшифровкаЗаявка = ТабличнаяЧасть.Задание;
			ОбластьСтрокаТаблицы.Параметры.РасшифровкаГруппаТарифов = ТабличнаяЧасть.ГруппаТарифов;
			ОбластьСтрокаТаблицы.Параметры.РасшифровкаАдресПогрузки = ТабличнаяЧасть.АдресПогрузки;
			ОбластьСтрокаТаблицы.Параметры.РасшифровкаАдресВыгрузки = ТабличнаяЧасть.АдресВыгрузки;
			
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			
		КонецЦикла;		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаШапка.Рейс);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_Грузовой_Манифест()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат выполнения.
//
Функция СформироватьПечатнуюФорму_ТТН_Измен(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ПараметрыПечати = Новый Структура("ЭтоРейс",Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.ТТН_Измен";
	
	МенеджерДокументов = Документы.упЗаданиеНаПеревозкуГруза;
	
	ТабличныйДокумент = МенеджерДокументов.СформироватьПечатнуюФорму_ТТН_Измен(МассивОбъектов,ОбъектыПечати,ПараметрыПечати);
		
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ТТН_Измен()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат выполнения.
//
Функция СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ПараметрыПечати = Новый Структура("ЭтоРейс",Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.Экспедиторская_Расписка";
	
	МенеджерДокументов = Документы.упЗаданиеНаПеревозкуГруза;
	
	ТабличныйДокумент = МенеджерДокументов.СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка(МассивОбъектов,ОбъектыПечати,ПараметрыПечати);
	
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ЭкспедиторскаяРасписка()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат выполнения.
//
Функция СформироватьПечатнуюФорму_М_15(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ПараметрыПечати = Новый Структура("ЭтоРейс",Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.М_15";
	
	МенеджерДокументов = Документы.упЗаданиеНаПеревозкуГруза;
	
	ТабличныйДокумент = МенеджерДокументов.СформироватьПечатнуюФорму_М_15(МассивОбъектов,ОбъектыПечати,ПараметрыПечати);	
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_М_15()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат выполнения.
//
Функция СформироватьПечатнуюФорму_МХ_1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ПараметрыПечати = Новый Структура("ЭтоРейс",Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.МХ_1";
	
	МенеджерДокументов = Документы.упЗаданиеНаПеревозкуГруза;
	
	ТабличныйДокумент = МенеджерДокументов.СформироватьПечатнуюФорму_МХ_1(МассивОбъектов,ОбъектыПечати,ПараметрыПечати);	
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_МХ_1()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - результат выполнения.
//
Функция СформироватьПечатнуюФорму_ПоручениеЭкспедитору(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ПараметрыПечати = Новый Структура("ЭтоРейс",Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "Документ.упРейс.Поручение_Экспедитору";
	
	МенеджерДокументов = Документы.упЗаданиеНаПеревозкуГруза;
	
	ТабличныйДокумент = МенеджерДокументов.СформироватьПечатнуюФорму_ПоручениеЭкспедитору(МассивОбъектов,ОбъектыПечати,ПараметрыПечати);
	
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФорму_ПоручениеЭкспедитору()

// Функция формирует печатную форму документа.
//
// Параметры:
//  Тип - Строка - тип печатной формы: Заказ или Счет.
//  МассивОбъектов - Массив - массив документов для печати.
//  ОбъектыПечати - СписокЗначений - Объект.
//
// Возвращаемое значение:
//  ТабличныйДокумент - Результат выполнения.
//
Функция СформироватьПечатнуюФорму_МаршрутныйЛист(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт	
	
	УстановитьПривилегированныйРежим(Истина);	
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева = 0;
	ТабличныйДокумент.ПолеСнизу = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_Рейс_МаршрутныйЛист";
	ДанныеДляПечати = ПолучитьДанныеДляМаршрутногоЛиста(МассивОбъектов);
	ЗаполнитьТабличныйДокументМаршрутныйЛист(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
    
  	Возврат ТабличныйДокумент;

КонецФункции	

Процедура ЗаполнитьТабличныйДокументМаршрутныйЛист(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	тбДанныеШапка = ДанныеДляПечати.Шапка;
	дрвДанныеМаршрут = ДанныеДляПечати.Маршрут;
	тбДанныеПолучатели = ДанныеДляПечати.Получатели;
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.упРейс.ПФ_MXL_МаршрутныйЛист");
	
	Эталон = ПолучитьОбщийМакет("упЭталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
		
	Для Каждого текСтрокаШапка Из тбДанныеШапка Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Общие данные по рейсу
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Заполнить(текСтрокаШапка);
		
		// Штрихкод
		Рисунок = ОбластьШапка.Рисунки.BarCode;
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить("Ширина", Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Высота", Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
		СтруктураШтрихкода.Вставить("Штрихкод", упСервисныеФункции.ПолучитьШКДокументаДляПечати(текСтрокаШапка.Ссылка));
		СтруктураШтрихкода.Вставить("ТипКода", 4);
		СтруктураШтрихкода.Вставить("ОтображатьТекст", Истина);
		СтруктураШтрихкода.Вставить("ПрозрачныйФон", Ложь);
		Рисунок.Картинка =  МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(СтруктураШтрихкода);
		
		// QRCode
		Рисунок = ОбластьШапка.Рисунки.QRCode;
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") Тогда
			
			ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(упСервисныеФункции.ПолучитьСтрокуДанныхQRКода(текСтрокаШапка.Ссылка),0,150);
			
			Если ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
				КартинкаQRКода = Новый Картинка(ДанныеQRКода);
				Рисунок.Картинка = КартинкаQRКода;
			Иначе
				Шаблон = Нстр("ru = 'Не удалось сформировать QR-код для документа %1.
				|Технические подробности см. в журнале регистрации.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, текСтрокаШапка.Ссылка);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		Иначе
			ОбластьШапка.Рисунки.Удалить(Рисунок);
		КонецЕсли;
				
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		// Транспортное средство
		ОбластьТранспортноеСредство = Макет.ПолучитьОбласть("ТранспортноеСредство");
		ОбластьТранспортноеСредство.Параметры.Заполнить(текСтрокаШапка);
		ТабличныйДокумент.Вывести(ОбластьТранспортноеСредство);
		
		// Сотрудники
		ОбластьСотрудникиШапка = Макет.ПолучитьОбласть("СотрудникиШапка");
		ТабличныйДокумент.Вывести(ОбластьСотрудникиШапка);
		
		ОбластьСотрудникиСтрока = Макет.ПолучитьОбласть("СотрудникиСтрока");
		Если ЗначениеЗаполнено(текСтрокаШапка.Водитель1) Тогда
			ОбластьСотрудникиСтрока.Параметры.Сотрудник = текСтрокаШапка.Водитель1;	
			ОбластьСотрудникиСтрока.Параметры.Должность = текСтрокаШапка.ДолжностьВодитель1;	
			ТабличныйДокумент.Вывести(ОбластьСотрудникиСтрока);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текСтрокаШапка.Водитель2) Тогда
			ОбластьСотрудникиСтрока.Параметры.Сотрудник = текСтрокаШапка.Водитель2;	
			ОбластьСотрудникиСтрока.Параметры.Должность = текСтрокаШапка.ДолжностьВодитель2;	
			ТабличныйДокумент.Вывести(ОбластьСотрудникиСтрока);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(текСтрокаШапка.Экспедитор) Тогда
			ОбластьСотрудникиСтрока.Параметры.Сотрудник = текСтрокаШапка.Экспедитор;	
			ОбластьСотрудникиСтрока.Параметры.Должность = текСтрокаШапка.ДолжностьЭкспедитор;	
			ТабличныйДокумент.Вывести(ОбластьСотрудникиСтрока);
		КонецЕсли;
		
		// Маршрут
		мсвРейсы = дрвДанныеМаршрут.Строки.НайтиСтроки(Новый Структура("Рейс", текСтрокаШапка.Ссылка));
		
		Для каждого Рейс Из мсвРейсы Цикл
			
			Точки = Рейс.Строки;
			
			Для каждого Точка Из Точки Цикл
				ОбластьМаршрутШапка	= Макет.ПолучитьОбласть("МаршрутШапка");
				ОбластьМаршрутШапка.Параметры.Заполнить(Точка);
				
				КонтактнаяИнформация = "";
				
				// Получатели
								
				мсвПолучатели = тбДанныеПолучатели.НайтиСтроки(Новый Структура("Рейс, Идентификатор", текСтрокаШапка.Ссылка, Точка.Идентификатор));
				
				Для каждого Получатель Из мсвПолучатели Цикл
					
					текТелефон = "";
					Если ЗначениеЗаполнено(Получатель.КонтактноеЛицо) Тогда
						текТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель.КонтактноеЛицо, Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица);
						
						текТелефон = ?(СтрДлина(текТелефон)=0, ", тел.","") + текТелефон;
						
						текМобильныйТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель.КонтактноеЛицо, Справочники.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛица);
						
						текТелефон = ?(СтрДлина(текТелефон)=0, ", тел.",", ") + текМобильныйТелефон;
						
					КонецЕсли;
					
					текКонтактнаяИнформация =  "" + Получатель.Контрагент  + ?(СтрДлина(Получатель.Контрагент) > 0 И СтрДлина(Получатель.КонтактноеЛицо) > 0, ", ","") + Получатель.КонтактноеЛицо + текТелефон;

					Если ЗначениеЗаполнено(текКонтактнаяИнформация) Тогда
						КонтактнаяИнформация = КонтактнаяИнформация + ?(СтрДлина(КонтактнаяИнформация) > 0, Символы.ПС, "") + текКонтактнаяИнформация;
					КонецЕсли;
					
				КонецЦикла;
				
				ОбластьМаршрутШапка.Параметры.КонтактныеДанныеПартнера = КонтактнаяИнформация;
				
				ТабличныйДокумент.Вывести(ОбластьМаршрутШапка);
				
				Операции = Точка.Строки;
				Для каждого Операция Из Операции Цикл
					
					ОбластьМаршрутТипОперации = Макет.ПолучитьОбласть("МаршрутТипОперации");
					ОбластьМаршрутТипОперации.Параметры.Заполнить(Операция);
					ТабличныйДокумент.Вывести(ОбластьМаршрутТипОперации);

					Работы = Операция.Строки;
					Для каждого Работа Из Работы Цикл
						ОбластьМаршрутГрузовоеМесто = Макет.ПолучитьОбласть("МаршрутГрузовоеМесто");
						текОбъектОперации = Неопределено;
						текКоличество = 0;
						Если ЗначениеЗаполнено(Работа.ГрузовоеМесто) ИЛИ (Работа.КоличествоГрузов > 0) Тогда
							текОбъектОперации = Работа.ГрузовоеМесто;
							текКоличество = Работа.КоличествоГрузов;
						ИначеЕсли ЗначениеЗаполнено(Работа.ДополнительнаяОперация) Тогда	
							текОбъектОперации = Работа.ДополнительнаяОперация;
							текКоличество = Работа.КоличествоДопОпераций;
						Иначе
							текОбъектОперации = Работа.ТипРегламентногоДокумента;
							текКоличество = Работа.КоличествоЭкземпляровДокумента;
						КонецЕсли;
						
						ОбластьМаршрутГрузовоеМесто.Параметры.ОбъектОперации = текОбъектОперации;
						ОбластьМаршрутГрузовоеМесто.Параметры.НомерЗаявки = Работа.НомерЗаявки;
						ОбластьМаршрутГрузовоеМесто.Параметры.Количество = текКоличество;
						ТабличныйДокумент.Вывести(ОбластьМаршрутГрузовоеМесто);						
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;	
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, текСтрокаШапка.Ссылка);

КонецПроцедуры

// Возвращает структуру данных для печати машрутного листа.
//
Функция ПолучитьДанныеДляМаршрутногоЛиста(МассивОбъектов) Экспорт
		
	Запрос = Новый Запрос;	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейс.Ссылка КАК Ссылка,
	|	упРейс.Организация КАК Организация,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТранспортноеСредство,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель1,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель2, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Водитель2,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Экспедитор, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Экспедитор,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.РегистрационныйНомер, """") КАК РегистрационныйНомер,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Время, 0) КАК Время,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) КАК Масса,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеНачалоВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеНачалоВыполнения,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.ПлановоеОкончаниеВыполнения, ДАТАВРЕМЯ(1, 1, 1)) КАК ПлановоеОкончаниеВыполнения,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Расстояние, 0) КАК Расстояние,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Модель КАК МодельТС,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель1.ТипСотрудника КАК ДолжностьВодитель1,
	|	упПеревозчикПоРейсуСрезПоследних.Водитель2.ТипСотрудника КАК ДолжностьВодитель2,
	|	упПеревозчикПоРейсуСрезПоследних.Экспедитор.ТипСотрудника КАК ДолжностьЭкспедитор
	|ПОМЕСТИТЬ втРейс
	|ИЗ
	|	Документ.упРейс КАК упРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&Ссылка)) КАК упПеревозчикПоРейсуСрезПоследних
	|		ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО упРейс.Ссылка = упПлановыеПараметрыРейса.Рейс
	|ГДЕ
	|	упРейс.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|	упМаршрутПоРейсуСрезПоследних.ПлановоеУбытие КАК ПлановоеУбытие,
	|	упМаршрутПоРейсуСрезПоследних.НачалоРаботПлан КАК НачалоРаботПлан,
	|	упМаршрутПоРейсуСрезПоследних.Рейс КАК Рейс,
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.ГрузовоеМесто, ЗНАЧЕНИЕ(Справочник.упГрузовыеМеста.ПустаяСсылка)) КАК ГрузовоеМесто,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.ДополнительнаяОперация, ЗНАЧЕНИЕ(Справочник.упДополнительныеОперации.ПустаяСсылка)) КАК ДополнительнаяОперация,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.Операция, ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ПустаяСсылка)) КАК Операция,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.Задание, ЗНАЧЕНИЕ(Документ.упЗаданиеНаПеревозкуГруза.ПустаяСсылка)) КАК Задание,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.ТипРегламентногоДокумента, ЗНАЧЕНИЕ(Справочник.упТипыРегламентныхДокументов.ПустаяСсылка)) КАК ТипРегламентногоДокумента,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.КоличествоЭкземпляровДокумента, 0) КАК КоличествоЭкземпляровДокумента,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.КоличествоГрузов, 0) КАК КоличествоГрузов,
	|	ЕСТЬNULL(упРаботыПоРейсуСрезПоследних.КоличествоДопОпераций, 0) КАК КоличествоДопОпераций,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.Адрес
	|ПОМЕСТИТЬ втМаршрут
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс В (&Ссылка)) КАК упМаршрутПоРейсуСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних КАК упРаботыПоРейсуСрезПоследних
	|		ПО упМаршрутПоРейсуСрезПоследних.Рейс = упРаботыПоРейсуСрезПоследних.Рейс
	|			И упМаршрутПоРейсуСрезПоследних.Идентификатор = упРаботыПоРейсуСрезПоследних.Идентификатор
	|ГДЕ
	|	НЕ упМаршрутПоРейсуСрезПоследних.Отменена
	|	И НЕ упРаботыПоРейсуСрезПоследних.Отменена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втМаршрут.Идентификатор) КАК КоличествоТочек,
	|	втМаршрут.Рейс
	|ПОМЕСТИТЬ втКоличествоТочек
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втМаршрут.КоличествоГрузов) КАК КоличествоГрузов,
	|	втМаршрут.Рейс
	|ПОМЕСТИТЬ втКоличествоГрузов
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|ГДЕ
	|	втМаршрут.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втМаршрут.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втМаршрут.Рейс КАК Рейс,
	|	втМаршрут.Идентификатор КАК Идентификатор,
	|	втМаршрут.Задание КАК Задание,
	|	втМаршрут.Адрес
	|ПОМЕСТИТЬ втЗаданияВТочках
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втМаршрут.ПлановоеПрибытие КАК ПлановоеПрибытие,
	|	втМаршрут.ПлановоеУбытие КАК ПлановоеУбытие,
	|	втМаршрут.НачалоРаботПлан КАК НачалоРаботПлан,
	|	втМаршрут.Рейс КАК Рейс,
	|	втМаршрут.Идентификатор КАК Идентификатор,
	|	втМаршрут.ГрузовоеМесто,
	|	втМаршрут.ДополнительнаяОперация,
	|	втМаршрут.Операция КАК Операция,
	|	втМаршрут.Задание,
	|	втМаршрут.ТипРегламентногоДокумента,
	|	втМаршрут.КоличествоЭкземпляровДокумента,
	|	втМаршрут.КоличествоГрузов,
	|	втМаршрут.КоличествоДопОпераций,
	|	втМаршрут.СтрокаНомер КАК СтрокаНомер,
	|	втМаршрут.Адрес КАК Адрес,
	|	втМаршрут.Задание.Номер КАК НомерЗаявки
	|ИЗ
	|	втМаршрут КАК втМаршрут
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтрокаНомер
	|ИТОГИ
	|	МАКСИМУМ(ПлановоеПрибытие),
	|	МАКСИМУМ(ПлановоеУбытие),
	|	МАКСИМУМ(НачалоРаботПлан),
	|	МАКСИМУМ(СтрокаНомер),
	|	МАКСИМУМ(Адрес)
	|ПО
	|	Рейс,
	|	Идентификатор,
	|	Операция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейс.Ссылка,
	|	втРейс.Организация,
	|	втРейс.ТранспортноеСредство,
	|	втРейс.Водитель1,
	|	втРейс.Водитель2,
	|	втРейс.Экспедитор,
	|	втРейс.РегистрационныйНомер,
	|	втРейс.Время,
	|	втРейс.КоличествоЗагрузочныхМетров,
	|	втРейс.КоличествоМест,
	|	втРейс.Масса,
	|	втРейс.Объем,
	|	втРейс.ПлановоеНачалоВыполнения,
	|	втРейс.ПлановоеОкончаниеВыполнения,
	|	втРейс.Расстояние,
	|	ЕСТЬNULL(втКоличествоГрузов.КоличествоГрузов, 0) КАК КоличествоГрузов,
	|	ЕСТЬNULL(втКоличествоТочек.КоличествоТочек, 0) КАК КоличествоТочекМаршрута,
	|	втРейс.МодельТС,
	|	втРейс.ДолжностьВодитель1,
	|	втРейс.ДолжностьВодитель2,
	|	втРейс.ДолжностьЭкспедитор
	|ИЗ
	|	втРейс КАК втРейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоТочек КАК втКоличествоТочек
	|		ПО втРейс.Ссылка = втКоличествоТочек.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоГрузов КАК втКоличествоГрузов
	|		ПО втРейс.Ссылка = втКоличествоГрузов.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА втЗаданияВТочках.Адрес = втЗаданияВТочках.Задание.АдресОтправления
	|			ТОГДА втЗаданияВТочках.Задание.КонтактноеЛицоОтправителя
	|		КОГДА втЗаданияВТочках.Адрес = втЗаданияВТочках.Задание.АдресПолучения
	|			ТОГДА втЗаданияВТочках.Задание.КонтактноеЛицоПолучателя
	|		ИНАЧЕ втЗаданияВТочках.Задание.КонтактноеЛицоЗаказчика
	|	КОНЕЦ КАК КонтактноеЛицо,
	|	ВЫБОР
	|		КОГДА втЗаданияВТочках.Адрес = втЗаданияВТочках.Задание.АдресОтправления
	|			ТОГДА втЗаданияВТочках.Задание.КонтрагентОтправителя
	|		КОГДА втЗаданияВТочках.Адрес = втЗаданияВТочках.Задание.АдресПолучения
	|			ТОГДА втЗаданияВТочках.Задание.КонтрагентПолучателя
	|		ИНАЧЕ втЗаданияВТочках.Задание.КонтрагентЗаказчика
	|	КОНЕЦ КАК Контрагент,
	|	втЗаданияВТочках.Идентификатор КАК Идентификатор,
	|	втЗаданияВТочках.Рейс КАК Рейс
	|ИЗ
	|	втЗаданияВТочках КАК втЗаданияВТочках";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов);
	
	ПакетРезультатовЗапроса	= Запрос.ВыполнитьПакет();
	
	дрвДанныеМаршрут = ПакетРезультатовЗапроса[5].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	тбДанныеШапка = ПакетРезультатовЗапроса[6].Выгрузить();
	дрвДанныеПолучатели	= ПакетРезультатовЗапроса[7].Выгрузить();
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Шапка", тбДанныеШапка);
	СтруктураДанныхДляПечати.Вставить("Маршрут",	дрвДанныеМаршрут);
	СтруктураДанныхДляПечати.Вставить("Получатели", дрвДанныеПолучатели);

	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Выпонлим подготовку списка печатных форм для пакетной печати.
//
// Параметры:
//  Рейс  - ДокументСсылка.упРейс - Ссылка на объект печати.
//  ПараметрыДанных  - Структура - Параметры для выполнения получения данных.
//  ОписаниеОшибки  - Строка - Текст описания ошибки.
//  Отказ  - Булево - Флаг о состоянии выполнения.
//
// Возвращаемое значение:
//   Массив - Перечислены объекты для печати.
//
Функция ПолучитьДанныеДляПакетаПечатныхФорм(МассивОбъектов, ПараметрыДанных, ОписаниеОшибки, Отказ) Экспорт
	
	Рейс = МассивОбъектов;
	Если ТипЗнч(МассивОбъектов)=ТИП("Массив") Тогда
		Рейс = МассивОбъектов[0];
	КонецЕсли;
 
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаботыПоРейсу.Рейс КАК Рейс,
	|	РаботыПоРейсу.Задание КАК Задание,
	|	МАКСИМУМ(МаршрутПоРейсу.СтрокаНомер) КАК ПорядокОтгрузки
	|ПОМЕСТИТЬ втЗадания
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс В (&МассивОбъектов)) КАК МаршрутПоРейсу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упРаботыПоРейсу.СрезПоследних(, Рейс В (&МассивОбъектов)) КАК РаботыПоРейсу
	|		ПО МаршрутПоРейсу.Рейс = РаботыПоРейсу.Рейс
	|			И МаршрутПоРейсу.Идентификатор = РаботыПоРейсу.Идентификатор
	|			И (РаботыПоРейсу.Операция В (ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Погрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.Разгрузка), ЗНАЧЕНИЕ(Перечисление.упТипыОпераций.ВыемкаЦенностей)))
	|ГДЕ
	|	НЕ МаршрутПоРейсу.Отменена
	|	И НЕ РаботыПоРейсу.Отменена
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботыПоРейсу.Рейс,
	|	РаботыПоРейсу.Задание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШаблоныПакетовПечатныхФорм.МакетыПФ.(
	|		Ссылка КАК Шаблон,
	|		ИмяМакета,
	|		Порядок,
	|		Количество
	|	),
	|	ЗаданиеНаПеревозкуГруза.Ссылка КАК Задание,
	|	ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозкуГруза.Ссылка) КАК ПредставлениеЗадание,
	|	ЕСТЬNULL(втЗадания.ПорядокОтгрузки, 0) КАК ПорядокОтгрузки,
	|	ЕСТЬNULL(ШаблоныПакетовПечатныхФорм.РазбиратьПоКопиям, ЛОЖЬ) КАК РазбиратьПоКопиям
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упПартнеры КАК Партнеры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упШаблоныПакетовПечатныхФорм КАК ШаблоныПакетовПечатныхФорм
	|			ПО Партнеры.ПакетПечатныхФормЗадание = ШаблоныПакетовПечатныхФорм.Ссылка
	|		ПО ЗаданиеНаПеревозкуГруза.Получатель = Партнеры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗадания КАК втЗадания
	|		ПО ЗаданиеНаПеревозкуГруза.Ссылка = втЗадания.Задание
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеревозчикПоРейсуСП.Рейс КАК Рейс,
	|	ПРЕДСТАВЛЕНИЕ(ПеревозчикПоРейсуСП.Рейс) КАК ПредставлениеРейса,
	|	ПеревозчикПоРейсуСП.Перевозчик КАК Перевозчик,
	|	упШаблоныПакетовПечатныхФорм.Ссылка КАК ПакетПечатныхФорм,
	|	упШаблоныПакетовПечатныхФорм.МакетыПФ.(
	|		Ссылка КАК Шаблон,
	|		ИмяМакета,
	|		Порядок,
	|		Количество
	|	),
	|	ЕСТЬNULL(упШаблоныПакетовПечатныхФорм.РазбиратьПоКопиям, ЛОЖЬ) КАК РазбиратьПоКопиям
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс В (&МассивОбъектов)) КАК ПеревозчикПоРейсуСП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упШаблоныПакетовПечатныхФорм КАК упШаблоныПакетовПечатныхФорм
	|		ПО ПеревозчикПоРейсуСП.Перевозчик.ПакетПечатныхФормРейс = упШаблоныПакетовПечатныхФорм.Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", Рейс);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаЗаданий = ПакетЗапросов[1].Выбрать();
	ВыборкаРейсы = ПакетЗапросов[2].Выбрать();
		
	ПрефиксВнешнихПечатныхФорм = "ВнешняяПечатнаяФорма.";	
	
	Массив = Новый Массив;
	
	Пока ВыборкаРейсы.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(ВыборкаРейсы.Перевозчик) ИЛИ НЕ ЗначениеЗаполнено(ВыборкаРейсы.ПакетПечатныхФорм) Тогда
			Продолжить;
		КонецЕсли;
		ИсточникВнешнихПечатныхФорм = ВыборкаРейсы.Рейс.Метаданные().ПолноеИмя();
		
		Структура = Новый Структура("Документ",ВыборкаРейсы.Рейс);
		Структура.Вставить("МенеджерПечати",ИсточникВнешнихПечатныхФорм);
		Структура.Вставить("Представление",ВыборкаРейсы.ПредставлениеРейса);	
		
		ВнешниеПечатныеФормы = УправлениеПечатью.СписокПечатныхФормИзВнешнихИсточников(ИсточникВнешнихПечатныхФорм);
		
		МассивИдентификатор = Новый Массив;
		ТаблицаМПФ = ВыборкаРейсы.МакетыПФ.Выгрузить();
		ТаблицаМПФ.Сортировать("Порядок");
		
		Если ВыборкаРейсы.РазбиратьПоКопиям Тогда
			мсвКоличестваЭкземпляров = ТаблицаМПФ.ВыгрузитьКолонку("Количество");
			максКоличество = 0;
			Для каждого текЗначение Из мсвКоличестваЭкземпляров Цикл
				максКоличество = Макс(текЗначение, максКоличество);
			КонецЦикла;
			
			Для Шаг = 1 По максКоличество Цикл
				Для каждого ТекущаяПФ Из ТаблицаМПФ Цикл
					Если ТекущаяПФ.Количество > 0 Тогда
						ИмяМакета = ТекущаяПФ.ИмяМакета;
						Если НЕ ВнешниеПечатныеФормы.НайтиПоЗначению(ИмяМакета) = Неопределено Тогда
							МассивИдентификатор.Добавить(ПрефиксВнешнихПечатныхФорм + ИмяМакета);
						Иначе
							МассивИдентификатор.Добавить(ИмяМакета);
						КонецЕсли;
						ТекущаяПФ.Количество = ТекущаяПФ.Количество - 1;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			Для каждого ТекущаяПФ Из ТаблицаМПФ Цикл
				ИмяМакета = ТекущаяПФ.ИмяМакета;
				Количество = ТекущаяПФ.Количество;
				Для Шаг = 1 По Количество Цикл
					Если НЕ ВнешниеПечатныеФормы.НайтиПоЗначению(ИмяМакета) = Неопределено Тогда
						МассивИдентификатор.Добавить(ПрефиксВнешнихПечатныхФорм + ИмяМакета);
					Иначе
						МассивИдентификатор.Добавить(ИмяМакета);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;	
				
		Если МассивИдентификатор.Количество() Тогда
			Структура.Вставить("ИдентификаторыПечатныхФорм",СтрСоединить(МассивИдентификатор,","));
			Массив.Добавить(Структура);
		КонецЕсли;
		
	КонецЦикла;
	
	Пока ВыборкаЗаданий.Следующий() Цикл
		
		ИсточникВнешнихПечатныхФорм = ВыборкаЗаданий.Задание.Метаданные().ПолноеИмя();
		
		Структура = Новый Структура("Документ",ВыборкаЗаданий.Задание);
		Структура.Вставить("МенеджерПечати",ИсточникВнешнихПечатныхФорм);
		Структура.Вставить("Представление",ВыборкаЗаданий.ПредставлениеЗадание);		
		
		ВнешниеПечатныеФормы = УправлениеПечатью.СписокПечатныхФормИзВнешнихИсточников(ИсточникВнешнихПечатныхФорм);
		
		МассивИдентификатор = Новый Массив;
		ТаблицаМПФ = ВыборкаЗаданий.МакетыПФ.Выгрузить();
		ТаблицаМПФ.Сортировать("Порядок");
		
		Если ВыборкаЗаданий.РазбиратьПоКопиям Тогда
			мсвКоличестваЭкземпляров = ТаблицаМПФ.ВыгрузитьКолонку("Количество");
			максКоличество = 0;
			Для каждого текЗначение Из мсвКоличестваЭкземпляров Цикл
				максКоличество = Макс(текЗначение, максКоличество);
			КонецЦикла;
			
			Для Шаг = 1 По максКоличество Цикл
				Для каждого ТекущаяПФ Из ТаблицаМПФ Цикл
					Если ТекущаяПФ.Количество > 0 Тогда
						ИмяМакета = ТекущаяПФ.ИмяМакета;
						Если НЕ ВнешниеПечатныеФормы.НайтиПоЗначению(ИмяМакета) = Неопределено Тогда
							МассивИдентификатор.Добавить(ПрефиксВнешнихПечатныхФорм + ИмяМакета);
						Иначе
							МассивИдентификатор.Добавить(ИмяМакета);
						КонецЕсли;
						ТекущаяПФ.Количество = ТекущаяПФ.Количество - 1;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;		
			
		Иначе
			Для каждого ТекущаяПФ Из ТаблицаМПФ Цикл
				ИмяМакета = ТекущаяПФ.ИмяМакета;
				Количество = ТекущаяПФ.Количество;
				Для Шаг = 1 По Количество Цикл
					Если НЕ ВнешниеПечатныеФормы.НайтиПоЗначению(ИмяМакета) = Неопределено Тогда
						МассивИдентификатор.Добавить(ПрефиксВнешнихПечатныхФорм + ИмяМакета);
					Иначе
						МассивИдентификатор.Добавить(ИмяМакета);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
		Если МассивИдентификатор.Количество() Тогда
			Структура.Вставить("ИдентификаторыПечатныхФорм",СтрСоединить(МассивИдентификатор,","));
			Массив.Добавить(Структура);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Массив;

КонецФункции // ПолучитьДанныеДляПакетаПечатныхФорм()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПечатныхФорм(МассивОбъектов)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаданиеНаПеревозку.Ссылка КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.Отправитель
	|		ИНАЧЕ ЗаданиеНаПеревозку.КонтрагентОтправителя
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.Получатель
	|		ИНАЧЕ ЗаданиеНаПеревозку.КонтрагентПолучателя
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентЗаказчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.Заказчик
	|		ИНАЧЕ ЗаданиеНаПеревозку.КонтрагентЗаказчика
	|	КОНЕЦ КАК Плательщик,
	|	ЗаданиеНаПеревозку.Заказчик КАК ЗаказчикЗадания,
	|	ЗаданиеНаПеревозку.Заказчик.НаименованиеПолное КАК ЗаказчикЗаданияПолноеНаименование,
	|	ЗаданиеНаПеревозку.КонтрагентОтправителя.КодПоОКПО КАК ОтправительОКПО,
	|	ЗаданиеНаПеревозку.КонтрагентПолучателя.КодПоОКПО КАК ПолучательОКПО,
	|	ЗаданиеНаПеревозку.КонтрагентЗаказчика.КодПоОКПО КАК ПлательщикОКПО,
	|	ЗаданиеНаПеревозку.АдресОтправления КАК АдресОтгрузки,
	|	ЗаданиеНаПеревозку.АдресПолучения КАК АдресПолучения,
	|	ЗаданиеНаПеревозку.Номер КАК Номер,
	|	ЗаданиеНаПеревозку.Дата КАК Дата,
	|	ЗаданиеНаПеревозку.Организация.Представление КАК Организация,
	|	ЗаданиеНаПеревозку.Организация КАК ОрганизацияСсылка,	               
	|	ЕСТЬNULL(Доверенность.ДоверенноеЛицо, Значение(Справочник.упСотрудники.ПустаяСсылка)) КАК ДоверенноеЛицо,
	|	ЕСТЬNULL(ПРЕДСТАВЛЕНИЕ(Доверенность.ДолжностьДоверенноеЛицо), """") КАК ДолжностьДоверенноеЛицо,
	|	ЕСТЬNULL(Доверенность.Номер, """") КАК НомерДоверенности,
	|	ЕСТЬNULL(Доверенность.Дата, """") КАК ДоверенностьДата
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозку
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упДоверенность КАК Доверенность
	|		ПО ЗаданиеНаПеревозку.Ссылка = Доверенность.Задание
	|ГДЕ
	|	ЗаданиеНаПеревозку.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбГрузы.Ссылка КАК Документ,
	|    тбСоставГруза.Ссылка.КоличествоМест КАК КоличествоМестГруза,
	|	тбСоставГруза.Ссылка.Код КАК КодГруза,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(тбСоставГруза.Количество * тбСоставГруза.Номенклатура.Масса, 0)
	|		ИНАЧЕ ЕСТЬNULL(тбСоставГруза.Количество * тбСоставГруза.Номенклатура.Масса * тбСоставГруза.УпаковкаНоменклатуры.Коэффициент, 0)
	|	КОНЕЦ КАК МассаБруттоВсего,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(тбСоставГруза.Количество * тбСоставГруза.Номенклатура.МассаНетто, 0)
	|		ИНАЧЕ ЕСТЬNULL(тбСоставГруза.Количество * тбСоставГруза.Номенклатура.МассаНетто * тбСоставГруза.УпаковкаНоменклатуры.Коэффициент, 0)
	|	КОНЕЦ КАК МассаНеттоВсего,
	|	тбГрузы.Ссылка.КоличествоМест КАК КоличествоМестВсего,
	|	тбСоставГруза.Номенклатура.Наименование КАК Номенклатура,
	|	тбСоставГруза.Номенклатура.Код,
	|	тбСоставГруза.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|	тбСоставГруза.Номенклатура.Артикул КАК НоменклатураАртикул,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ тбГрузы.КоличествоГрузов
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ тбСоставГруза.УпаковкаНоменклатуры.Наименование
	|	КОНЕЦ КАК ВидУпаковки,
	|	тбСоставГруза.Количество КАК Количество,
	|	тбСоставГруза.Цена,
	|	тбСоставГруза.Сумма КАК СуммаСНДС,
	|	тбСоставГруза.СтавкаНДС,
	|	тбСоставГруза.СуммаНДС,
	|	тбСоставГруза.СуммаБезНДС,
	|	тбСоставГруза.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	тбСоставГруза.УпаковкаНоменклатуры.Коэффициент КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА тбСоставГруза.УпаковкаНоменклатуры = ЗНАЧЕНИЕ(Справочник.упУпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА тбСоставГруза.Количество * тбСоставГруза.Номенклатура.Масса
	|		ИНАЧЕ тбСоставГруза.Количество * тбСоставГруза.УпаковкаНоменклатуры.Масса
	|	КОНЕЦ КАК МассаБрутто
	|ПОМЕСТИТЬ тбСтроки
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК тбГрузы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста.ТоварныйСоставГруза КАК тбСоставГруза
	|		ПО тбГрузы.ГрузовоеМесто = тбСоставГруза.Ссылка
	|ГДЕ
	|	тбГрузы.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтроки.Документ,
	|	тбСтроки.МассаБруттоВсего,
	|	тбСтроки.МассаНеттоВсего,
	|	тбСтроки.КоличествоМестВсего,
	|	тбСтроки.Номенклатура,
	|	тбСтроки.НоменклатураКод,
	|	тбСтроки.ЕдиницаИзмерения,
	|	тбСтроки.НоменклатураАртикул,
	|	тбСтроки.КоличествоМест,
	|	тбСтроки.ВидУпаковки,
	|	тбСтроки.Количество,
	|	тбСтроки.Цена,
	|	тбСтроки.МассаБрутто,
	|	тбСтроки.СуммаСНДС,
	|	ВЫБОР тбСтроки.СтавкаНДС
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18)
	|			ТОГДА 18
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС18_118)
	|			ТОГДА 18
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС20)
	|			ТОГДА 20
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС20_120)
	|			ТОГДА 20
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС10)
	|			ТОГДА 10
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.упСтавкиНДС.НДС10_110)
	|			ТОГДА 10
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтавкаНДС,
	|	тбСтроки.СуммаНДС,
	|	тбСтроки.СуммаБезНДС,
	|	тбСтроки.ЕдиницаИзмеренияКод,
	|	тбСтроки.КоличествоВОдномМесте
	|ИЗ
	|	тбСтроки КАК тбСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ тбСтроки.Номенклатура) КАК КоличествоНаименований,
	|	тбСтроки.Документ
	|ИЗ
	|	тбСтроки КАК тбСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСтроки.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданияРейса.Ссылка КАК Рейс,
	|	ЗаданияРейса.Задание КАК ЗаданиеНаПеревозку,
	|	ПлановыеДанныеРейса.ПлановоеОкончаниеВыполнения
	|ПОМЕСТИТЬ втРейсы
	|ИЗ
	|	Документ.упРейс.Задания КАК ЗаданияРейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК ПлановыеДанныеРейса
	|		ПО (ПлановыеДанныеРейса.Рейс = ЗаданияРейса.Ссылка)
	|ГДЕ
	|	ЗаданияРейса.Задание В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА упПеревозчикПоРейсу.КонтрагентПеревозчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА упПеревозчикПоРейсу.Перевозчик
	|		ИНАЧЕ упПеревозчикПоРейсу.КонтрагентПеревозчика
	|	КОНЕЦ КАК КонтрагентПеревозчика,
	|	упПеревозчикПоРейсу.Регистратор КАК Рейс,
	|	ВЫБОР
	|		КОГДА упПеревозчикПоРейсу.Водитель1 = ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)
	|			ТОГДА упПеревозчикПоРейсу.Водитель2
	|		ИНАЧЕ упПеревозчикПоРейсу.Водитель1
	|	КОНЕЦ КАК Водитель,
	|	упПеревозчикПоРейсу.ТранспортноеСредство.РегистрационныйНомер КАК ГосНомер,
	|	упПеревозчикПоРейсу.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ПРЕДСТАВЛЕНИЕ(упПеревозчикПоРейсу.ТранспортноеСредство.Модель) КАК Модель,
	|	втРейсы.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	втРейсы.ПлановоеОкончаниеВыполнения КАК ПлановоеОкончаниеВыполнения
	|ПОМЕСТИТЬ втРейсПодготовка
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|			,
	|			Рейс В
	|				(ВЫБРАТЬ
	|					втРейсы.Рейс
	|				ИЗ
	|					втРейсы КАК втРейсы)) КАК упПеревозчикПоРейсу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРейсы КАК втРейсы
	|		ПО упПеревозчикПоРейсу.Регистратор = втРейсы.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРейсПодготовка.КонтрагентПеревозчика,
	|	втРейсПодготовка.Рейс,
	|	втРейсПодготовка.Водитель,
	|	втРейсПодготовка.ГосНомер,
	|	втРейсПодготовка.ТранспортноеСредство,
	|	втРейсПодготовка.Модель,
	|	втРейсПодготовка.ЗаданиеНаПеревозку,
	|	втРейсПодготовка.ПлановоеОкончаниеВыполнения
	|ИЗ
	|	втРейсПодготовка КАК втРейсПодготовка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛист.Номер КАК Номер
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка)
	|ГДЕ
	|	упПутевойЛист.Рейс В
	|			(ВЫБРАТЬ
	|				втРейсы.Рейс
	|			ИЗ
	|				втРейсы КАК втРейсы)
	|	И НЕ упПутевойЛист.ПометкаУдаления			
	|	И НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)	               
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезПоследних.ТипДокумента,
	|	СрезПоследних.Водитель,
	|	СрезПоследних.ДействуетПо,
	|	СрезПоследних.Регистратор.Серия,
	|	СрезПоследних.Регистратор.Номер,
	|	СрезПоследних.Регистратор.ДействуетС,
	|	СрезПоследних.Регистратор.ДействуетПо,
	|	СрезПоследних.Регистратор.Выдан,
	|	СрезПоследних.Регистратор.Дата
	|ИЗ
	|	РегистрСведений.упДействующиеДокументыТСВодителей.СрезПоследних(
	|			,
	|			ВЫБОР
	|				КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ВодительскоеУдостоверение)
	|					ТОГДА Водитель В
	|							(ВЫБРАТЬ
	|								втРейсПодготовка.Водитель КАК Водитель
	|							ИЗ
	|								втРейсПодготовка КАК втРейсПодготовка)
	|				КОГДА ТипДокумента = ЗНАЧЕНИЕ(Справочник.упТипыДокументовТСВодителей.ЛицензионнаяКарточка)
	|					ТОГДА ТранспортноеСредство В
	|							(ВЫБРАТЬ
	|								втРейсПодготовка.ТранспортноеСредство КАК ТранспортноеСредство
	|							ИЗ
	|								втРейсПодготовка КАК втРейсПодготовка)
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ) КАК СрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(упПрицепыТранспортныхСредств.Прицеп.Модель) КАК ПрицепМодель,
	|	упПрицепыТранспортныхСредств.Прицеп.РегистрационныйНомер КАК ПрицепРегистрационныйНомер,
	|	упПрицепыТранспортныхСредств.ТранспортноеСредство КАК ТранспортноеСредство
	|ИЗ
	|	РегистрСведений.упПрицепыТранспортныхСредств КАК упПрицепыТранспортныхСредств
	|ГДЕ
	|	упПрицепыТранспортныхСредств.ТранспортноеСредство В
	|			(ВЫБРАТЬ
	|				втРейсПодготовка.ТранспортноеСредство
	|			ИЗ
	|				втРейсПодготовка КАК втРейсПодготовка)
	|	И упПрицепыТранспортныхСредств.Операция = ЗНАЧЕНИЕ(Перечисление.упТипыОперацийМонтажа.Установлено)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тбСтроки.КодГруза,
	|	тбСтроки.Документ,
	|	СУММА(тбСтроки.МассаБруттоВсего) КАК МассаБруттоВсего,
	|	МАКСИМУМ(тбСтроки.КоличествоМестГруза) КАК КоличествоМест,
	|	МАКСИМУМ(тбСтроки.ВидУпаковки) КАК ВидУпаковки
	|ИЗ
	|	тбСтроки КАК тбСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	тбСтроки.КодГруза,
	|	тбСтроки.Документ";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();
	тбДанныеШапка = ПакетРезультатовЗапроса[0].Выгрузить();
	тбДанныеСтроки = ПакетРезультатовЗапроса[2].Выгрузить();
	тбДанныеКоличествоНаименований = ПакетРезультатовЗапроса[3].Выгрузить();
	тбПлановыеДанныеРейса = ПакетРезультатовЗапроса[6].Выгрузить();	
	тбДанныеПутевойЛист  = ПакетРезультатовЗапроса[7].Выгрузить();
	тбДанныеВодитель = ПакетРезультатовЗапроса[8].Выгрузить();
	тбДанныеПрицепа = ПакетРезультатовЗапроса[9].Выгрузить();
	тбДанныеГруза = ПакетРезультатовЗапроса[10].Выгрузить();
	
	// Подготовим данные таблицы шапки документа
	тбШапка = Новый ТаблицаЗначений;
	тбШапка.Колонки.Добавить("ДокументОснование");
	тбШапка.Колонки.Добавить("ПредставлениеОрганизация");
	тбШапка.Колонки.Добавить("ОрганизацияПолноеНаименование");
	тбШапка.Колонки.Добавить("АдресОрганизация");
	тбШапка.Колонки.Добавить("ЗаказчикЗадания");
	тбШапка.Колонки.Добавить("ЗаказчикЗаданияПолноеНаименование");
	тбШапка.Колонки.Добавить("ПредставлениеОтправитель");
	тбШапка.Колонки.Добавить("ОтправительОКПО");
	тбШапка.Колонки.Добавить("ПредставлениеПолучатель");
	тбШапка.Колонки.Добавить("ПолучательОКПО");
	тбШапка.Колонки.Добавить("ПредставлениеПлательщик");
	тбШапка.Колонки.Добавить("ПлательщикОКПО");
	тбШапка.Колонки.Добавить("ФИОГлавБухгалтера");
	тбШапка.Колонки.Добавить("Дата");
	тбШапка.Колонки.Добавить("ДоверенностьДата");
	тбШапка.Колонки.Добавить("НомерДоверенности");
	тбШапка.Колонки.Добавить("ДолжностьДоверенноеЛицо");
	тбШапка.Колонки.Добавить("ДоверенноеЛицо");
	тбШапка.Колонки.Добавить("ВодительДокумент");
	тбШапка.Колонки.Добавить("Экспедитор");
	// тбШапка.Колонки.Добавить("ТранспортноеСредство");
	тбШапка.Колонки.Добавить("Номер");
	тбШапка.Колонки.Добавить("АдресОтгрузки");
	тбШапка.Колонки.Добавить("АдресПолучения");
	тбШапка.Колонки.Добавить("КоличествоНаименований");
	
	// Заполним данные таблицы шапки документа
	Для Каждого текСтрока Из тбДанныеШапка Цикл
		НоваяСтрока = тбШапка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока);
		
		// Грузоотправитель
		Если ЗначениеЗаполнено(текСтрока.Грузоотправитель) Тогда
			контактанаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.Грузоотправитель);
			
			НоваяСтрока.ПредставлениеОтправитель = "" + текСтрока.Грузоотправитель + ?(ЗначениеЗаполнено(контактанаяИнформация), ", " + контактанаяИнформация, "");
		Иначе
			НоваяСтрока.ПредставлениеОтправитель = "";	
		КонецЕсли;	
		
		// Грузополучатель
		Если ЗначениеЗаполнено(текСтрока.Грузополучатель) Тогда
			контактанаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.Грузополучатель);
			
			НоваяСтрока.ПредставлениеПолучатель = "" + текСтрока.Грузополучатель + ?(ЗначениеЗаполнено(контактанаяИнформация), ", " + контактанаяИнформация, "");
		Иначе
			НоваяСтрока.ПредставлениеПолучатель = "";
		КонецЕсли;	
		
		
		// Плательщик
		Если ЗначениеЗаполнено(текСтрока.Плательщик) Тогда
			контактанаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.Плательщик);
			
			НоваяСтрока.ПредставлениеПлательщик = "" + текСтрока.Плательщик + ?(ЗначениеЗаполнено(контактанаяИнформация), ", " + контактанаяИнформация, "");
		КонецЕсли;	
		
		// Организация
		Если ЗначениеЗаполнено(текСтрока.ОрганизацияСсылка) Тогда
			НоваяСтрока.ОрганизацияПолноеНаименование = текСтрока.Организация; 
			НоваяСтрока.АдресОрганизация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(текСтрока.ОрганизацияСсылка, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
			
			текКонтактнаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.ОрганизацияСсылка);
			НоваяСтрока.ПредставлениеОрганизация = ""+текСтрока.Организация +  ?(ПустаяСтрока(текКонтактнаяИнформация), "", ", "+текКонтактнаяИнформация);
		КонецЕсли;	

		// Заказчик задания.
		Если ЗначениеЗаполнено(текСтрока.ЗаказчикЗадания) Тогда
			контактанаяИнформация = ПолучитьКонтактнуюИнформацию(текСтрока.ЗаказчикЗадания);
			
			НоваяСтрока.ЗаказчикЗадания = "" + текСтрока.ЗаказчикЗадания + ?(ЗначениеЗаполнено(контактанаяИнформация), ", " + контактанаяИнформация, "");
		КонецЕсли;
		
		сткОтбор	= Новый Структура("Документ", текСтрока.ДокументОснование);
		
		мсвСтрок = тбДанныеКоличествоНаименований.НайтиСтроки(сткОтбор);
		Если мсвСтрок.Количество()>0 Тогда
			НоваяСтрока.КоличествоНаименований = мсвСтрок[0].КоличествоНаименований;
		Иначе
			НоваяСтрока.КоличествоНаименований = 0;
		КонецЕсли;
		
	КонецЦикла;	
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("Шапка", тбШапка);
	СтруктураДанныхДляПечати.Вставить("Строки", тбДанныеСтроки);
	СтруктураДанныхДляПечати.Вставить("Рейс", тбПлановыеДанныеРейса);	
	СтруктураДанныхДляПечати.Вставить("ПутевойЛист", тбДанныеПутевойЛист);
	СтруктураДанныхДляПечати.Вставить("Водитель", тбДанныеВодитель);
	СтруктураДанныхДляПечати.Вставить("Прицеп", тбДанныеПрицепа);
	СтруктураДанныхДляПечати.Вставить("Груз", тбДанныеГруза);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция ПолучитьДанныеДляПФ_СВ(Рейсы)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упРейсЗадания.Ссылка КАК СсылкаРейс,
	|	упРейсЗадания.Ссылка.Номер КАК НомерДокумента,
	|	упРейсЗадания.Ссылка.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ВидПеревозкиИГМ,
	|	упРейсЗадания.Задание КАК Задание,
	|	упРейсЗадания.КоличествоГрузов КАК КоличествоГрузов
	|ПОМЕСТИТЬ Рейс
	|ИЗ
	|	Документ.упРейс.Задания КАК упРейсЗадания
	|ГДЕ
	|	упРейсЗадания.Ссылка В(&Рейсы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.Дата
	|		ИНАЧЕ ЗаданиеНаПеревозку.Дата
	|	КОНЕЦ КАК Пункт0_2,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.Номер
	|		ИНАЧЕ ЗаданиеНаПеревозку.Номер
	|	КОНЕЦ КАК Пункт0_3,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Отправитель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК Пункт1_1,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Получатель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК Пункт2_1,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Отправитель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК Пункт1_2,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Получатель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК Пункт2_2,
	|	ЗаданиеНаПеревозку.АдресПолучения КАК Пункт7_1,
	|	ЗаданиеНаПеревозку.АдресОтправления КАК Пункт6_1,
	|	ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА Рейс.ВидПеревозкиИГМ = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
	|					ИЛИ Рейс.ВидПеревозкиИГМ = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса)
	|				ТОГДА упЗаданиеНаПеревозкуГрузаГрузовыеМеста.КоличествоГрузов
	|			ИНАЧЕ Рейс.КоличествоГрузов
	|		КОНЕЦ) КАК Пункт3_2,
	|	МИНИМУМ(ЗаданиеНаПеревозку.Масса) КАК Пункт6_5,
	|	МИНИМУМ(ЗаданиеНаПеревозку.КоличествоМест) КАК Пункт6_51,
	|	Рейс.СсылкаРейс,
	|	Рейс.НомерДокумента,
	|	Рейс.ВидПеревозкиИГМ,
	|	ЗаданиеНаПеревозку.Ссылка
	|ПОМЕСТИТЬ ПромежуточнаяТЗ
	|ИЗ
	|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозку
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК упЗаданиеНаПеревозкуГрузаГрузовыеМеста
	|		ПО ЗаданиеНаПеревозку.Ссылка = упЗаданиеНаПеревозкуГрузаГрузовыеМеста.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Рейс КАК Рейс
	|		ПО ЗаданиеНаПеревозку.Ссылка = Рейс.Задание
	|ГДЕ
	|	ЗаданиеНаПеревозку.Ссылка В
	|			(ВЫБРАТЬ
	|				Рейс.Задание
	|			ИЗ
	|				Рейс КАК Рейс)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА """"
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Отправитель.НаименованиеПолное
	|							ИНАЧЕ """"
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.НаименованиеПолное
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.Номер
	|		ИНАЧЕ ЗаданиеНаПеревозку.Номер
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза <> ЗНАЧЕНИЕ(Документ.упЗаявкаНаПеревозкуГруза.ПустаяСсылка)
	|			ТОГДА ЗаданиеНаПеревозку.ЗаявкаНаПеревозкуГруза.Дата
	|		ИНАЧЕ ЗаданиеНаПеревозку.Дата
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА """"
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Получатель.НаименованиеПолное
	|							ИНАЧЕ """"
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.НаименованиеПолное
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА """"
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Отправитель.НаименованиеПолное
	|							ИНАЧЕ """"
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.НаименованиеПолное
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ЗаданиеНаПеревозку.АдресПолучения,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА """"
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Получатель.НаименованиеПолное
	|							ИНАЧЕ """"
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.НаименованиеПолное
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ЗаданиеНаПеревозку.АдресОтправления,
	|	Рейс.СсылкаРейс,
	|	Рейс.НомерДокумента,
	|	Рейс.ВидПеревозкиИГМ,
	|	ЗаданиеНаПеревозку.Ссылка,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Отправитель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Отправитель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ ЗаданиеНаПеревозку.Отправитель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Отправитель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентОтправителя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА НЕ ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Получатель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ФизЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗаданиеНаПеревозку.Получатель = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ЗаданиеНаПеревозку.Получатель.ЮридическоеЛицо
	|								ТОГДА ЗаданиеНаПеревозку.Получатель
	|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.упЮрФизЛицо.ЮрЛицо)
	|					ТОГДА ЗаданиеНаПеревозку.КонтрагентПолучателя
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПромежуточнаяТЗ.Пункт0_2 КАК Пункт0_2,
	|	ПромежуточнаяТЗ.Пункт0_3 КАК Пункт0_3,
	|	ПромежуточнаяТЗ.Пункт1_1 КАК Пункт1_1,
	|	ПромежуточнаяТЗ.Пункт2_1 КАК Пункт2_1,
	|	ПромежуточнаяТЗ.Пункт1_2 КАК Пункт1_2,
	|	ПромежуточнаяТЗ.Пункт2_2 КАК Пункт2_2,
	|	ПромежуточнаяТЗ.Пункт7_1 КАК Пункт7_1,
	|	ПромежуточнаяТЗ.Пункт6_1 КАК Пункт6_1,
	|	ПромежуточнаяТЗ.Пункт6_5 КАК Пункт6_5,
	|	ПромежуточнаяТЗ.Пункт6_51 КАК Пункт6_51,
	|	ПромежуточнаяТЗ.ЗаявкаНаПеревозкуГруза КАК ЗаявкаНаПеревозкуГруза,
	|	ПромежуточнаяТЗ.Пункт3_2 КАК Пункт3_2,
	|	ПромежуточнаяТЗ.СсылкаРейс КАК СсылкаРейс,
	|	ПромежуточнаяТЗ.ВидПеревозкиИГМ,
	|	ПромежуточнаяТЗ.НомерДокумента КАК НомерДокумента,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Масса, 0) * 1000 КАК Масса,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.Объем, 0) КАК Объем,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Наименование, """") КАК Наименование,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Ширина, 0) КАК Ширина,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Длина, 0) КАК Длина,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Высота, 0) КАК Высота,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоМест, 0) КАК КоличествоМест,
	|	ЕСТЬNULL(упПлановыеПараметрыРейса.КоличествоЗагрузочныхМетров, 0) КАК КоличествоЗагрузочныхМетров,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.РегистрационныйНомер, """") КАК Пункт11_2,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Пункт10_3,
	|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Водитель1, ЗНАЧЕНИЕ(Справочник.упСотрудники.ПустаяСсылка)) КАК Пункт10_4,
	|	ВЫБОР
	|		КОГДА упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика = ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика, ЗНАЧЕНИЕ(Справочник.упКонтрагенты.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|	КОНЕЦ КАК Пункт10_2,
	|	ПромежуточнаяТЗ.Ссылка КАК Задание,
	|	упПеревозчикПоРейсуСрезПоследних.ТипТС,
	|	упПеревозчикПоРейсуСрезПоследних.ТранспортноеСредство.Модель КАК Модель,
	|	ВЫБОР КОГДА упПеревозчикПоРейсуСрезПоследних.Перевозчик.ЮридическоеЛицо ТОГДА
	|		упПеревозчикПоРейсуСрезПоследних.Перевозчик 
	|	ИНАЧЕ ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка) КОНЕЦ КАК Пункт10_1
	|ИЗ
	|	ПромежуточнаяТЗ КАК ПромежуточнаяТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПлановыеПараметрыРейса КАК упПлановыеПараметрыРейса
	|		ПО ПромежуточнаяТЗ.СсылкаРейс = упПлановыеПараметрыРейса.Рейс
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(
	|				,
	|				Рейс В
	|					(ВЫБРАТЬ
	|						Рейс.СсылкаРейс
	|					ИЗ
	|						Рейс КАК Рейс)) КАК упПеревозчикПоРейсуСрезПоследних
	|		ПО ПромежуточнаяТЗ.СсылкаРейс = упПеревозчикПоРейсуСрезПоследних.Рейс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПутевойЛист.Номер КАК Номер
	|ИЗ
	|	Документ.упПутевойЛист КАК упПутевойЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыПутевыхЛистов.СрезПоследних КАК упСтатусыПутевыхЛистовСрезПоследних
	|		ПО (упСтатусыПутевыхЛистовСрезПоследних.Документ = упПутевойЛист.Ссылка)
	|ГДЕ
	|	упПутевойЛист.Рейс В
	|			(ВЫБРАТЬ
	|				Рейс.СсылкаРейс
	|			ИЗ
	|				Рейс КАК Рейс)
	|	И НЕ упПутевойЛист.ПометкаУдаления			
	|	И НЕ упСтатусыПутевыхЛистовСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыПутевогоЛиста.Отменен)	               
	|";
	
	Запрос.УстановитьПараметр("Рейсы", Рейсы);
	РезультатПакет = Запрос.ВыполнитьПакет();
	
	тбДанныеСраниц = РезультатПакет[2].Выгрузить();
	тбПутевыеЛисты = РезультатПакет[3].Выгрузить();
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("ДанныеСраниц",  тбДанныеСраниц);
	СтруктураДанныхДляПечати.Вставить("ПутевыеЛисты",  тбПутевыеЛисты);
		
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция ПолучитьКонтактнуюИнформацию(Ссылка)
	
	текРезультат = "";
	
	ВидАдрес   = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
	ВидФакс    = Справочники.ВидыКонтактнойИнформации.ФаксОрганизации;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.упПартнеры") Тогда
		ВидАдрес   = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера;	    
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.упКонтрагенты") Тогда
		ВидАдрес   = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;	    
		ВидФакс    = Справочники.ВидыКонтактнойИнформации.ФаксКонтрагенты;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.упКонтактныеЛицаПартнеров") Тогда
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица;	    			
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.упСотрудники") Тогда
		ВидТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица;	    		
	КонецЕсли;
	
	текАдрес	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидАдрес);
	Если ЗначениеЗаполнено(текАдрес) Тогда
		текРезультат = текРезультат  + текАдрес;	
	КонецЕсли;
		
	текТелефон	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидТелефон);
	Если ЗначениеЗаполнено(текТелефон) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текТелефон);
	КонецЕсли;

	текФакс		= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Ссылка, ВидФакс);
	Если ЗначениеЗаполнено(текФакс) Тогда
		текРезультат = текРезультат  + ?(ПустаяСтрока(текРезультат), "", ", ") + СокрЛП(текФакс);
	КонецЕсли;
	
	Возврат текРезультат;
	
КонецФункции

#КонецОбласти

#Область КомандыВводаНаОсновании

Процедура УстановитьВидимостьКомандВводаНаОсновании(Форма, ДополнительныеПараметры) Экспорт
	
	ЭтоОперацииСКонтейнерами = Ложь;
	ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = Ложь;
	
	Если ДополнительныеПараметры.ЭтоФормаОбъекта Тогда
		ЭтоОперацииСКонтейнерами = Форма.Объект.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами;
		ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = Форма.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Список") Тогда
		Отбор = Форма.Список.Отбор;
		мсвЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Форма.Список.Отбор, "ВидТранспортнойУслуги");
		Если мсвЭлементыОтбора.Количество() > 0 Тогда
			Если Истина
				И мсвЭлементыОтбора[0].ПравоеЗначение = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами
				И мсвЭлементыОтбора[0].ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			Тогда
				ЭтоОперацииСКонтейнерами = Истина;
				ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера = Истина;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
	Если ЭтоОперацииСКонтейнерами Тогда
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СоздатьНаОсновании", "БизнесПроцесс", "Задание", ДополнительныеПараметры);
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьВыполнениеОперации",,, ДополнительныеПараметры);
		Если ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера Тогда
			упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьЗаявкуНаПеревозкуГруза",,, ДополнительныеПараметры);
		КонецЕсли;
		упВводНаОсновании.УстановитьПризнакДоступностиСпискаКоманд(Истина, ДополнительныеПараметры);
	Иначе
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьЗаявкуНаПеревозкуГруза",,, ДополнительныеПараметры);
		упВводНаОсновании.ДобавитьКомандуВСписокКоманд("СформироватьВыполнениеОперации",,, ДополнительныеПараметры);
		упВводНаОсновании.УстановитьПризнакДоступностиСпискаКоманд(Ложь, ДополнительныеПараметры);
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
