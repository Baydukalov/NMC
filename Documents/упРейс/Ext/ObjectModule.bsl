
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Статус = Неопределено;
	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);	
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		Статус      = ДополнительныеСвойства.Статус;
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	ИначеЕсли Не Проведен И Не ПометкаУдаления Тогда
		Статус      = Перечисления.упСтатусыРейса.Новый;
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если ПометкаУдаления Тогда
			ДополнительныеСвойства.Вставить("РассчитыватьПлановыеРасходыВсего");
			Статус = Перечисления.упСтатусыРейса.ПустаяСсылка();	
			ДополнительныеСвойства.Вставить("Статус", Статус);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе	
		Если Истина
			И ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений")
			И НЕ ЗначениеЗаполнено(Подразделение)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ПрисваиватьРейсуПодразделениеТранспортногоСредства") 
		Тогда
			ТранспортноеСредство = Неопределено;
			Если ДополнительныеСвойства.Свойство("ИзмененЭкипаж") Тогда
				ТранспортноеСредство = ДополнительныеСвойства.ИзмененЭкипаж.ТранспортноеСредство;
			КонецЕсли; 
			Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
				Подразделение           = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(,ТранспортноеСредство);
				НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,ТранспортноеСредство);
			КонецЕсли;
		КонецЕсли;
		
		Если Ложь
			Или НЕ ЗначениеЗаполнено(ПорядокИсполненияЗаявки) 
			Или НЕ ЗначениеЗаполнено(ВидТранспортнойУслуги) 
		Тогда
			упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
			ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
			упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);
		КонецЕсли;
	Иначе
		Задания.Очистить();
		Если НЕ ДополнительныеСвойства.Свойство("НеПроверятьДокументыНаОсновании") Тогда
			мсвРейсы = Новый Массив;
			мсвРейсы.Добавить(Ссылка);
			сткПроверка = упРейс.ДокументыПрепятствующиеОтменеРейса(мсвРейсы);
			Если сткПроверка.ЕстьДокументыОтменаВручную 
				ИЛИ сткПроверка.ЕстьДокументыОтменаАвтоматом Тогда
				Отказ = Истина;
				ТекстОшибки = Нстр("ru = 'По рейсу есть проведенные документы введенные на основании. Отмена рейса запрещена.'");
				ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстОшибки);
				ЗаписьЖурналаРегистрации("ОшибкаПриЗаписи", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
			
		Если НЕ Отказ 
			И ПолучитьФункциональнуюОпцию("упИспользуютсяМультимодальныеПеревозки") Тогда
			ТекстОшибки = "";
			Если НЕ упКорректировкаМаршрута.ПрерываниеГрузовогоПотокаДокументомОбработано(Ссылка)  Тогда
				ТекстОшибки = НСтр("ru = 'Запрещено изменять проведение документа, по которому обработано прерывание грузового потока'");
				ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
	
		Если Истина
			И Не Отказ
			И ЗначениеЗаполнено(Ссылка) 
			И Не (ДополнительныеСвойства.Свойство("МаршрутРаботыПодготовлены") И ДополнительныеСвойства.МаршрутРаботыПодготовлены) Тогда
			упКорректировкаРейсаОбъект = Неопределено;
			Если Не ДополнительныеСвойства.Свойство("КорректировкаРейса", упКорректировкаРейсаОбъект) Тогда
				упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();
				упКорректировкаРейсаОбъект.Ссылка = Ссылка;
				упКорректировкаРейсаОбъект.Рейс = Ссылка;
				упКорректировкаРейсаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
				
				Если Не ЗначениеЗаполнено(упКорректировкаРейсаОбъект.Период)  Тогда
					упКорректировкаРейсаОбъект.Период = ОпределитьПериодФормированияДвижений(Статус);
				КонецЕсли;
				сткПараметрыЗаполнения = Новый Структура();
				сткПараметрыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками"));
				упКорректировкаРейсаОбъект.Инициализация(сткПараметрыЗаполнения);
			КонецЕсли;	
			сткРеквизиты = Новый Структура();
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "СпособПрохожденияТочекМаршрута") = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе") Тогда
				//Если Статус = Неопределено Тогда
				//	Статус = Документы.упРейс.ПолучитьСтатус(Ссылка);
				//	Если (Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.ВыполненЧастично") Или Статус = ПредопределенноеЗначение("Перечисление.упСтатусыРейса.Выполнен"))Тогда
						Если ДополнительныеСвойства.Свойство("ТранспортноеСредство") Тогда
							упКорректировкаРейсаОбъект.ТранспортноеСредство = ДополнительныеСвойства.ТранспортноеСредство;	
						КонецЕсли;
						упКорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляРейсаПрохождениеМаршрутаВРейсе(ДополнительныеСвойства, Ссылка, сткРеквизиты, Отказ);
					//КонецЕсли;
				//КонецЕсли;	
			Иначе
				упКорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляРейса(ДополнительныеСвойства, Ссылка, сткРеквизиты, Отказ);
			КонецЕсли;
			ДополнительныеСвойства.Вставить("ПлановоеНачалоВыполнения", упКорректировкаРейсаОбъект.ПлановоеНачалоВыполнения);
			ДополнительныеСвойства.Вставить("ПлановоеОкончаниеВыполнения", упКорректировкаРейсаОбъект.ПлановоеОкончаниеВыполнения);
			ДополнительныеСвойства.Вставить("ЕстьОтложенныеРаботы", упКорректировкаРейсаОбъект.ЕстьОтложенныеРаботы);
			ДополнительныеСвойства.Вставить("НеПроверятьДокументыНаОсновании", Истина);
			ДополнительныеСвойства.Вставить("ЭтоОперацияСКонтейнером", ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ОперацииСКонтейнерами);
						
		КонецЕсли; 
		
		ДополнительныеСвойства.Вставить("ПеревозкаТранспортнойКомпанией" , ПеревозкаТранспортнойКомпанией); 
		ДополнительныеСвойства.Вставить("ТребуетсяОформлениеПутевогоЛиста", ?(ЗначениеЗаполнено(Ссылка), упРейс.ТребуетсяОформлениеПутевогоЛиста(Ссылка), Ложь)); 
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("РассчитыватьПлановыеРасходыВсего") Тогда
		// Рассчитать и записать итоговые расходы по рейсу
		упРейс.СформироватьДвижениеВсегоРасходыПоРейсу(Ссылка, Отказ);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	сткДанныеВидаПеревозки = Неопределено;
	ТранспортноеСредство = Неопределено;
	ФормироватьПоГрузовымМестам = Неопределено;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ДобавитьЭлементыБлокировкиДанныхПроведения(БлокировкаДанных, сткДанныеВидаПеревозки, ТранспортноеСредство, ФормироватьПоГрузовымМестам);
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ВидЗоныДляФормированияПредставленияРейса = сткДанныеВидаПеревозки.ВидЗоныДляФормированияПредставленияРейса; 
	ВариантФормированияМаршрута = сткДанныеВидаПеревозки.ВариантФормированияМаршрута;
	СпособПрохожденияТочекМаршрута = сткДанныеВидаПеревозки.СпособПрохожденияТочекМаршрута;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата", ОпределитьПериодФормированияДвижений(?(ДополнительныеСвойства.Свойство("Статус"), ДополнительныеСвойства.Статус, Неопределено)));
	РеквизитыОбъекта.Вставить("ДатаДокумента", Дата);
	Если ДополнительныеСвойства.Свойство("ДатаСтатусаМобильныйКлиент") Тогда
		РеквизитыОбъекта.Вставить("ДатаСтатуса", ДополнительныеСвойства.ДатаСтатусаМобильныйКлиент);
	Иначе
		РеквизитыОбъекта.Вставить("ДатаСтатуса", ТекущаяДатаСеанса());
	КонецЕсли;
	РеквизитыОбъекта.Вставить("ТранспортноеСредство", ТранспортноеСредство);
	РеквизитыОбъекта.Вставить("Водитель1",);
	РеквизитыОбъекта.Вставить("Водитель2",);
	РеквизитыОбъекта.Вставить("Экспедитор",);
	РеквизитыОбъекта.Вставить("ВидПеревозки", ВидПеревозки);
	РеквизитыОбъекта.Вставить("Задания", Задания);
	РеквизитыОбъекта.Вставить("ФормироватьПоГрузовымМестам", ФормироватьПоГрузовымМестам);
	РеквизитыОбъекта.Вставить("ВариантФормированияМаршрута", ВариантФормированияМаршрута);
	РеквизитыОбъекта.Вставить("ВидЗоныДляФормированияПредставленияРейса", ВидЗоныДляФормированияПредставленияРейса);
	РеквизитыОбъекта.Вставить("Рейс", Ссылка);
	РеквизитыОбъекта.Вставить("ПлановыеРасходы", ПлановыеРасходы);
	РеквизитыОбъекта.Вставить("ПлановыеПараметры", Новый Структура());	
	РеквизитыОбъекта.Вставить("ВидТранспортнойУслуги", ВидТранспортнойУслуги);	
		
	Если ДополнительныеСвойства.Свойство("ПлановоеНачалоВыполнения") Тогда
		РеквизитыОбъекта.ПлановыеПараметры.Вставить("ПлановоеНачалоВыполнения", ДополнительныеСвойства.ПлановоеНачалоВыполнения);	
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("ПлановоеОкончаниеВыполнения") Тогда
		РеквизитыОбъекта.ПлановыеПараметры.Вставить("ПлановоеОкончаниеВыполнения", ДополнительныеСвойства.ПлановоеОкончаниеВыполнения);	
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ИзмененЭкипаж") Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ДополнительныеСвойства.ИзмененЭкипаж); 
	КонецЕсли; 
	РеквизитыОбъекта.Вставить("ФактическиеПараметры"                    , Новый Структура);   
	
	Если ДополнительныеСвойства.Свойство("НачалоВыполнения") Тогда
		РеквизитыОбъекта.ФактическиеПараметры.Вставить("НачалоВыполнения", ДополнительныеСвойства.НачалоВыполнения);	
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("ОкончаниеВыполнения") Тогда
		РеквизитыОбъекта.ФактическиеПараметры.Вставить("ОкончаниеВыполнения", ДополнительныеСвойства.ОкончаниеВыполнения);	
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("УчитыватьГрафикиРаботыОсновныхВодителей") Тогда
		ДополнительныеСвойства.Вставить("УчитыватьГрафикиРаботыОсновныхВодителей", Ложь);
	КонецЕсли;

	ДополнительныеСвойства.Вставить("КонтролироватьЗадания", Ложь);																					
	ДополнительныеСвойства.Вставить("ЗаданияНаПеревозкуГрузаПоКоторымИзменилсяСтатус", Новый Массив); 
	ДополнительныеСвойства.Вставить("ЗаявкиНаПеревозкуГрузаПоКоторымИзменилсяСтатус" , Новый Массив); 
	ДополнительныеСвойства.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах", сткДанныеВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах); 
	ДополнительныеСвойства.Вставить("СпособУчетаВозвращенныхДокументов", сткДанныеВидаПеревозки.СпособУчетаВозвращенныхДокументов);
	ДополнительныеСвойства.Вставить("СпособРегистрацииЗадолженностиПоДокументамРейса", сткДанныеВидаПеревозки.СпособРегистрацииЗадолженностиПоДокументамРейса);
	ДополнительныеСвойства.Вставить("Дата", РеквизитыОбъекта.ДатаСтатуса);																					
	ДополнительныеСвойства.Вставить("ЧасовойПояс", ЧасовойПояс);																					
	ДополнительныеСвойства.Вставить("СпособПрохожденияТочекМаршрута", СпособПрохожденияТочекМаршрута);
	ДополнительныеСвойства.Вставить("ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера", сткДанныеВидаПеревозки.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера);
	
	Если НЕ ДополнительныеСвойства.Свойство("Маршрут") Тогда
		ДополнительныеСвойства.Вставить("Маршрут", Неопределено);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("Работы") Тогда
		ДополнительныеСвойства.Вставить("Работы", Неопределено);
	КонецЕсли;
		
	КоэффициентРасчетаВремениВПути = 0;
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		КоэффициентРасчетаВремениВПути = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТранспортноеСредство, "ТипТС.КоэффициентРасчетаВремениВПути");
		ДополнительныеСвойства.Вставить("КоэффициентРасчетаВремениВПути", КоэффициентРасчетаВремениВПути);
	КонецЕсли;
	
	// Предварительная установка статуса.										
	упРаботаСоСтатусами.УстановкаСтатусаРейса(ДополнительныеСвойства, Ссылка, Отказ);
	
	// Проверяется и устанавливается подтверждение по рейсу.
	ДополнительныеСвойства.Вставить("РейсПодтвержден", Ложь);
	Если Не ЭтоНовый() И ЕстьПодтверждениеЗаявкиНаТС(Ссылка) Тогда
		ДополнительныеСвойства.РейсПодтвержден = Истина;
	КонецЕсли; 
	
	// Формируются движения по регистру "упЗаданияКПланированию"
	упРейс.СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
    // Формируются маршрут и работы по рейсу.
	упРейс.СформироватьМаршрутРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если Истина
		И ЗначениеЗаполнено(ТранспортноеСредство)        
		И ДополнительныеСвойства.Свойство("ЗаполнитьЭкипажПоУмолчанию")
		И ДополнительныеСвойства.ЗаполнитьЭкипажПоУмолчанию
	Тогда
		Если Не ДополнительныеСвойства.Свойство("ИзмененЭкипаж") Тогда
			ДополнительныеСвойства.Вставить("ИзмененЭкипаж");
		КонецЕсли; 
		ПлановоеПрибытие = '00010101';
		ПлановоеУбытие = '00010101';
		Если НЕ ДополнительныеСвойства.Маршрут = Неопределено 
				И ДополнительныеСвойства.Маршрут.Количество() > 0 Тогда
			ПлановоеПрибытие = ДополнительныеСвойства.Маршрут[0].ПлановоеПрибытие;// Прибытие в первую точку.
			ПлановоеУбытие = ДополнительныеСвойства.Маршрут[ДополнительныеСвойства.Маршрут.Количество() - 1].ПлановоеУбытие // Убытие из последней точки.
		КонецЕсли;
		упПодборТС.ПолучитьЭкипажПоУмолчаниюДляРейса(ПлановоеПрибытие, ПлановоеУбытие, ДополнительныеСвойства.УчитыватьГрафикиРаботыОсновныхВодителей, ДополнительныеСвойства.ИзмененЭкипаж);
					
		ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ДополнительныеСвойства.ИзмененЭкипаж); 
	КонецЕсли;

	// Формируются данные перевозчика рейса.
	упРейс.СформироватьДвижениеПеревозчикПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если Истина
		И ДополнительныеСвойства.Свойство("Статус") 
		И ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.КВыполнению 
		//И ДополнительныеСвойства.ЗаписыватьСтатус
	Тогда
		Если сткДанныеВидаПеревозки.КонтролироватьГрафикиРаботыВодителейПриПередачеРейсаКВыполнению Тогда
			ПроконтролироватьГрафикиРаботыВодителей(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ);
		КонецЕсли;		
		ВестиУчетРасчетовСЗаказчиками		= ПолучитьФункциональнуюОпцию("упВестиУчетРасчетовСЗаказчиками");
		Если ВестиУчетРасчетовСЗаказчиками Тогда
			упРасчетыСЗаказчиками.ПроконтролироватьОстатокПредоплатыПоРейсу(Ссылка, Отказ);	
		КонецЕсли;
	КонецЕсли; 
	
	// Фиксируются данные о дополнительных сотрудниках рейса.
	упРейс.СформироватьДвиженияДополнительныеСотрудникиРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется расписание работы ТС.
	упРейс.СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
		упРейс.СформироватьДвиженияФактическиеМестоположенияТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;	
	
	// Формируется расписание работы ТС.
	упРейс.СформироватьДвиженияРасписаниеРаботыТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется расписание работы водителей.
	упРейс.СформироватьДвижениеРасписаниеРаботыВодителей(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	//// Формируются движения по состоянию ТС.
	упРейс.СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируются плановые расходы по рейсу.
	упРейс.СформироватьПлановыеРасходыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется пакет документов, о которых следует отчитаться после выполнения рейса.
	упРейс.СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
	Если СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
		// Формируется план по заданиям.
		упКорректировкаМаршрута.СформироватьДвижениеПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;	
	
	// Формируются данные о случаях прерывания грузовых потоков (отмена/разделение).
	упКорректировкаМаршрута.СформироватьДвижениеПрерыванияГрузовыхПотоков(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);

	Движения.Записать();
		
	// Формируется и устанавливается статус рейса.
	упРейс.СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
		Движения.Записать();
	КонецЕсли; 

	Если Ложь
		Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.Новый
		Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.КПланированиюТС
		Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.НазначеноТС
		Или (ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.КВыполнению И ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса"))
		Или (ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.Отменен И ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса"))
		Или (ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.Выполнен И ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса"))
		Или (ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.ВыполненЧастично И ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") 
			И СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе)Тогда
		
		// Устанавливаются статусы заданий.
		упЗаданиеНаПеревозку.СформироватьДвиженияСтатусыЗаданий(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Устанавливаются статусы заявок.
		упУправленияЗаявками.СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	КонецЕсли;
	
	// Рассчитать и записать итоговые расходы по рейсу.
	упРейс.СформироватьДвижениеВсегоРасходыПоРейсу(Ссылка, Отказ);
	
	// Формируются плановые параметры рейса.
	упРейс.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Если СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
		упРейс.СформироватьФактическиеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);	
	КонецЕсли;
		
	Если Ложь
		Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.ВыполненЧастично
		Или ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.Выполнен
	Тогда 
		РегистрыСведений.упЗанятостьТранспорта.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, ТранспортноеСредство, Отказ);
	КонецЕсли; 
	
	Если ДополнительныеСвойства.Статус = Перечисления.упСтатусыРейса.КВыполнению И ЗначениеЗаполнено(сткДанныеВидаПеревозки.ПравилоСвязиТиповГрузовВРейсе) Тогда 
		упРейс.ВыполнитьКонтрольСвязанныхЗаданий(ЭтотОбъект, Отказ);
	КонецЕсли; 
	
	упРейс.СформироватьДвижениеВыработкаСотрудников(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	упЗаданиеНаПеревозку.ПроконтролироватьЗаданияКПланированию(Ссылка, ФормироватьПоГрузовымМестам, Отказ, ДополнительныеСвойства);
		
	// Заполняются использованные правила при добавлении контрольных точек.
	упКорректировкаМаршрута.СформироватьДвиженияПримененныеПравилаВключенияКонтрольныхТочек(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		Статус = ДополнительныеСвойства.Статус;	
	Иначе
		Статус = Документы.упРейс.ПолучитьСтатус(Ссылка);
	КонецЕсли;	
	
	упРаботаСоСтатусами.УстановкаСтатусаРейса(ДополнительныеСвойства, Ссылка, Отказ);
	
	Если НЕ Отказ И Статус = Перечисления.упСтатусыЗаданияНаПеревозкуГруза.ПустаяСсылка() Тогда
		упРаботаСоСтатусами.ЕстьДругиеРегистраторыИзменявшиеСтатус(Ссылка, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроконтролироватьГрафикиРаботыВодителей(ДополнительныеСвойства, Ссылка, РеквизитыОбъекта, Отказ) Экспорт

	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	мсвВодители = Новый Массив;
	Если ЗначениеЗаполнено(РеквизитыОбъекта.Водитель1) Тогда
		мсвВодители.Добавить(РеквизитыОбъекта.Водитель1);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыОбъекта.Водитель2) Тогда
		мсвВодители.Добавить(РеквизитыОбъекта.Водитель2);	
	КонецЕсли;
	
	мсвНепрошедниеКонтрольВодители = Новый Массив;
	
	Если мсвВодители.Количество() > 0 Тогда
		упРейс.ВодительМожетБытьНазначенНаРейсПоПлановымДатам(мсвВодители, ДополнительныеСвойства.ПлановоеНачалоВыполнения, ДополнительныеСвойства.ПлановоеОкончаниеВыполнения, ДополнительныеСвойства.УчитыватьГрафикиРаботыОсновныхВодителей, мсвНепрошедниеКонтрольВодители, Ложь, Ссылка);
	КонецЕсли;

	Для Каждого Водитель Из мсвНепрошедниеКонтрольВодители Цикл
		ТекстСообщения = НСтр("ru = 'График работы водителя %1 не допускает перевода этого рейса к выполнению.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Водитель);
		ДополнительныеСвойства.Вставить("ТекстОшибки", ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,, Отказ);
	КонецЦикла;
	
КонецПроцедуры // ПроконтролироватьГрафикиРаботыВодителей()

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипОснования = ТипЗнч(ДанныеЗаполнения);
	Если ТипОснования = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		
		Статус = Документы.упЗаданиеНаПеревозкуГруза.ПолучитьСтатус(ДанныеЗаполнения);
		
		Если Статус <> Перечисления.упСтатусыЗаданияНаПеревозкуГруза.КВыполнению
			И Статус <> Перечисления.упСтатусыЗаданияНаПеревозкуГруза.Выполняется Тогда 
			ВызватьИсключение НСтр("ru = 'Статус документа задание на перевозку груза должен быть ""К выполнению"".'");
		КонецЕсли;
		
		ЗонаПогрузки 	= ДанныеЗаполнения.ЗонаОтправления;
		ЗонаРазгрузки	= ДанныеЗаполнения.ЗонаПолучения;
		Организация		= ДанныеЗаполнения.Организация;
		ВидПеревозки	= ДанныеЗаполнения.ВидПеревозки;

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Статус = Документы.упРейс.ПолучитьСтатус(Ссылка);
	Если Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат;
	КонецЕсли; 

		
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьЭлементыБлокировкиДанныхПроведения(БлокировкаДанных, сткДанныеВидаПеревозки = Неопределено, ТранспортноеСредство = Неопределено, ФормироватьПоГрузовымМестам = Неопределено) Экспорт 
	
	сткРеквизитыВидаПеревозки = Новый Структура();
	сткРеквизитыВидаПеревозки.Вставить("ПараметрыВводаИнформацииОГрузовыхМестах");
	сткРеквизитыВидаПеревозки.Вставить("ВидЗоныДляФормированияПредставленияРейса");
	сткРеквизитыВидаПеревозки.Вставить("КонтролироватьГрафикиРаботыВодителейПриПередачеРейсаКВыполнению");
	сткРеквизитыВидаПеревозки.Вставить("ВариантФормированияМаршрута");
	сткРеквизитыВидаПеревозки.Вставить("СпособУчетаВозвращенныхДокументов");
	сткРеквизитыВидаПеревозки.Вставить("СпособПрохожденияТочекМаршрута");
	сткРеквизитыВидаПеревозки.Вставить("ПравилоСвязиТиповГрузовВРейсе");
	сткРеквизитыВидаПеревозки.Вставить("СпособРегистрацииЗадолженностиПоДокументамРейса");
	сткРеквизитыВидаПеревозки.Вставить("ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера");
	
	сткДанныеВидаПеревозки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПеревозки, сткРеквизитыВидаПеревозки);
	ФормироватьПоГрузовымМестам = Ложь
							Или сткДанныеВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку 
							Или сткДанныеВидаПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса;
	ТранспортноеСредство = Неопределено;
	
	ДополнительныеСвойства.Вставить("ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера", сткДанныеВидаПеревозки.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера);
	
	Если ДополнительныеСвойства.Свойство("ИзмененЭкипаж") Тогда
		ТранспортноеСредство = ДополнительныеСвойства.ИзмененЭкипаж.ТранспортноеСредство;
	КонецЕсли; 
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПеревозчикПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеПараметрыРейса");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеПараметрыРейса");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНепереданныеДокументы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	КонецЕсли; 
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Задания;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
	Если ФормироватьПоГрузовымМестам Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	Если сткРеквизитыВидаПеревозки.СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПланПоЗаданиям");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Задания;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
		Если ФормироватьПоГрузовымМестам Тогда
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = упЗаданиеНаПеревозку.ПолучитьЗаявкиПоЗаданиям(Задания.ВыгрузитьКолонку("Задание"));
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Заявка");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Задания;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Задание");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	тбзПрерыванияГрузовыхПотоков = Неопределено;
	Если ДополнительныеСвойства.Свойство("ПрерыванияГрузовыхПотоков", тбзПрерыванияГрузовыхПотоков) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПрерыванияГрузовыхПотоков");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = тбзПрерыванияГрузовыхПотоков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаявкаНаПеревозку", "ЗаявкаНаПеревозку");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		
		Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетРасходовНаТС") Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходыНаТС");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		КонецЕсли;	
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыТС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		
		Если сткРеквизитыВидаПеревозки.СпособПрохожденияТочекМаршрута = Перечисления.упСпособыПрохожденияТочекМаршрута.ВРейсе Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеМестоположенияТС");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		КонецЕсли;	
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Транспорт", ТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияТранспортныхСредств");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	КонецЕсли;
	
	// Блокировки в этом месте неправильно, т.к. экипаж далее может измениться
	//Если ЗначениеЗаполнено(Водитель1) Тогда
	//	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыЭкипажей");
	//	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	//	ЭлементБлокировки.УстановитьЗначение("Водитель", Водитель1);
	//КонецЕсли;
	//Если ЗначениеЗаполнено(Водитель2) Тогда
	//	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыЭкипажей");
	//	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	//	ЭлементБлокировки.УстановитьЗначение("Водитель", Водитель2);
	//КонецЕсли;
	//Если ЗначениеЗаполнено(Экспедитор) Тогда
	//	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРасписаниеРаботыЭкипажей");
	//	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	//	ЭлементБлокировки.УстановитьЗначение("Водитель", Экспедитор);
	//КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упВсегоРасходыПоРейсу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаСотрудников");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);
	
	ПримененныеПравилаВключенияКонтрольныхТочек = Неопределено;
	
	Если ДополнительныеСвойства.Свойство("ПримененныеПравилаВключенияКонтрольныхТочек", ПримененныеПравилаВключенияКонтрольныхТочек) Тогда
		Если Истина
			И ТипЗнч(ПримененныеПравилаВключенияКонтрольныхТочек) = Тип("СписокЗначений")
			И ПримененныеПравилаВключенияКонтрольныхТочек.Количество() > 0
		Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПримененныеПравилаВключенияКонтрольныхТочек");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Рейс", Ссылка);	
		КонецЕсли;
	КонецЕсли;
	
	Возврат БлокировкаДанных;
	
КонецФункции

// Процедура заполнения ТЧ "ПлановыеРасходы".
//
Процедура ЗаполнитьПлановыеРасходы(тбзСтатьиРасходов, тбзЗадания) Экспорт
	
	ПлановыеРасходы.Очистить();
			
	Для каждого текСтатьяРасходов Из тбзСтатьиРасходов Цикл
		
		Если текСтатьяРасходов.ЗаполнятьПоЗаданиям Тогда
			Для каждого СтрокаЗадание Из тбзЗадания Цикл
				текСтрока = ПлановыеРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(текСтрока, текСтатьяРасходов);
				текСтрока.Валюта		= Справочники.Валюты.ПустаяСсылка();
				текСтрока.Задание		= СтрокаЗадание.Задание;
				текСтрока.Сумма			= 0;
				текСтрока.СуммаНДС		= 0;
			КонецЦикла;
		Иначе	
			текСтрока = ПлановыеРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(текСтрока, текСтатьяРасходов);
			текСтрока.Валюта		= Справочники.Валюты.ПустаяСсылка();
			текСтрока.Сумма			= 0;
			текСтрока.СуммаНДС	= 0;
		КонецЕсли;
					
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполнения ТЧ "ПлановыеРасходы".
//
Процедура ЗаполнитьПлановыеРасходыПоЗаданиям(тбзСтатьиРасходов, тбзЗадания) Экспорт
	
	ПлановыеРасходы.Очистить();
			
	Для каждого текСтатьяРасходов Из тбзСтатьиРасходов Цикл
		
		Для каждого СтрокаЗадание Из тбзЗадания Цикл
			текСтрока = ПлановыеРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(текСтрока, текСтатьяРасходов);
			текСтрока.Валюта		= Справочники.Валюты.ПустаяСсылка();
			текСтрока.Задание		= СтрокаЗадание.Задание;
			текСтрока.Сумма			= 0;
			текСтрока.СуммаНДС		= 0;
		КонецЦикла;
							
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьПодтверждениеЗаявкиНаТС(Рейс)
	
    ЕстьПодтверждение = Ложь;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПеревозчикПоРейсуСрезПоследних.Регистратор
	|ИЗ
	|	РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упПеревозчикПоРейсуСрезПоследних";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.упПодтверждениеЗаявкиНаТС") Тогда
			ЕстьПодтверждение = Истина;
		КонецЕсли; 
	КонецЕсли;	
	
	Возврат ЕстьПодтверждение;
	
КонецФункции

// Функция определяет дату формирования движений
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений(Статус)
	
	Период = ТекущаяДатаСеанса();
	Если Статус = Перечисления.упСтатусыРейса.Отменен Тогда
		Возврат Период;
	КонецЕсли; 
	
	ПереведенКВыполнению = Документы.упРейс.ПолучитьДатуПереводаВСтатус(Ссылка, Перечисления.упСтатусыРейса.КВыполнению);
	Если ЗначениеЗаполнено(ПереведенКВыполнению) Тогда
		Период = Мин(ПереведенКВыполнению, Период);
	КонецЕсли;
	
	Возврат Период;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
