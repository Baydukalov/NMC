
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыСобытияМониторинга.Новое");
		Объект.Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;	
	
	Если Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ВходящийЗвонок
		ИЛИ Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ИсходящийЗвонок Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗвонок;
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.НачалоРаботВТочкеМаршрута
		ИЛИ Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ПрибытиеВТочкуМаршрута
		ИЛИ Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.УбытиеСТочкиМаршрута
		ИЛИ Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ТочкаПропущена
		ИЛИ Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ИзменениеСледующейТочкиПоМаршруту Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСобытиеСТочкой;
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.СообщениеДиспетчеру Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВходящееСообщение;
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.СообщениеОтДиспетчера Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОтправленноеСообщение;
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ОтсутствиеЛицензииНаПодключение 
		Или Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаОптимизациюМаршрута Тогда 	
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОтсутствиеЛицензииНаПодключение;
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаДосрочноеЗавершениеРейса Тогда 	
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросБезАдреса;	
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаДосрочноеУбытиеИзТочкиМаршрута
		Или Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаНазначениеТочкиСледующейПоМаршруту Тогда 	
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросСАдресом;		
	ИначеЕсли Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗаправкаТоплива
		ИЛИ Объект.ТипСобытия = Справочники.упТипыСобытийМониторинга.СливТоплива Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСобытиеТоплива;
	ИначеЕсли ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипСобытия, "НастраиваемоеСобытиеAndroidКлиента") Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастраиваемоеСобытиеМониторинга;
	Иначе	
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПустая;
	КонецЕсли;	
	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.ДатаЗаписи);
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ответственный) 
		И Объект.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыСобытияМониторинга.Обрабатывается") Тогда
		Объект.Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство, ТекущийОбъект.ДатаЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
    Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
        РезультатВыполнения = Неопределено;
        ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
        ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
    КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере() 
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ответственный) 
		И Объект.Статус = ПредопределенноеЗначение("Перечисление.упСтатусыСобытияМониторинга.Обрабатывается") Тогда
		ТекстСообщения = НСтр("ru = 'Укажите ответственного пользователя.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.Ответственный");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
    ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()    	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство, Объект.ДатаЗаписи);
КонецПроцедуры

#КонецОбласти
