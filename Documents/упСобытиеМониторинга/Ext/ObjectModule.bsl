#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru = 'Дата в документе %1 не заполнена'"), Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,"Объект.Дата",Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТранспортноеСредство) И НЕ ТипСобытия = Справочники.упТипыСобытийМониторинга.ОтсутствиеЛицензииНаПодключение Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru = 'Транспортное средство в документе %1 не заполнено'"), Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,"Объект.ТранспортноеСредство",Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипСобытия)Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru = 'Тип события в документе %1 не указан'"), Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,"Объект.ТипСобытия",Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда 
		ДатаЗаписи = ТекущаяДатаСеанса();
		Если НЕ ЗначениеЗаполнено(Статус) Тогда
			Статус = ПредопределенноеЗначение("Перечисление.упСтатусыСобытияМониторинга.Новое");
		КонецЕсли;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(Ответственный) 
		И Статус = ПредопределенноеЗначение("Перечисление.упСтатусыСобытияМониторинга.Обрабатывается") Тогда
		Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтатусЗапроса) И Не ЗначениеЗаполнено(КонечнаяДата) Тогда
		КонечнаяДата = ТекущаяДатаСеанса();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
		ЭтоЗапросНаПодтверждение = ЗначениеЗаполнено(СтатусЗапроса) И ЗначениеЗаполнено(Рейс) И ЗначениеЗаполнено(ИдентификаторЗадачи);
		
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДатыСобытий");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		ЭлементБлокировки.УстановитьЗначение("ТипСобытия", ТипСобытия);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упОтветыНаЗапросыМобильныйКлиент");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", ИдентификаторТочки);
		
		Если ЭтоЗапросНаПодтверждение Тогда
			Если СтатусЗапроса = Перечисления.упСтатусыЗапросовМобильныйКлиент.Подтвержден Тогда
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упИзмененияРейсов");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
			Иначе
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упУведомленияМобильныйКлиент");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Трекер", Трекер);
				ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
				ЭлементБлокировки.УстановитьЗначение("ИдентификаторЗадачи", ИдентификаторЗадачи);
			КонецЕсли; 
		КонецЕсли; 
		
		упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Ссылка, Отказ);
		
		// регистр упДатыСобытий
		Движения.упДатыСобытий.Записывать = Истина;
		Движения.упДатыСобытий.Очистить();
		
		Движение = Движения.упДатыСобытий.Добавить();
		Движение.Период               = Дата;
		Движение.ТранспортноеСредство = ТранспортноеСредство;
		Движение.ТипСобытия           = ТипСобытия;
		Движение.Событие              = Ссылка;
		Движение.Рейс                 = Рейс;
		
		Если ЭтоЗапросНаПодтверждение Тогда
			// регистр упОтветыНаЗапросыМобильныйКлиент
			Движения.упОтветыНаЗапросыМобильныйКлиент.Записывать = Истина;
			Движения.упОтветыНаЗапросыМобильныйКлиент.Очистить();
			
			Движение = Движения.упОтветыНаЗапросыМобильныйКлиент.Добавить();
			Движение.Период               = Дата;
			Движение.Рейс                 = Рейс;
			Движение.Идентификатор        = ИдентификаторТочки;
			Движение.СобытиеМониторинга   = Ссылка;
			
			Если ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаДосрочноеЗавершениеРейса Тогда
				ЗаголовокУведомления = НСтр("ru = 'Досрочное завершение рейса'");
			ИначеЕсли ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаДосрочноеУбытиеИзТочкиМаршрута Тогда
				ЗаголовокУведомления = НСтр("ru = 'Досрочное убытие из точки'");
			ИначеЕсли ТипСобытия = Справочники.упТипыСобытийМониторинга.ЗапросНаНазначениеТочкиСледующейПоМаршруту Тогда
				ЗаголовокУведомления = НСтр("ru = 'Назначение точки следующей'");
			Иначе
				ЗаголовокУведомления = НСтр("ru = 'Ответ на запрос'");
			КонецЕсли; 
			Если СтатусЗапроса = Перечисления.упСтатусыЗапросовМобильныйКлиент.Подтвержден Тогда
				упИзмененияРейсов = РегистрыСведений.упИзмененияРейсов.СоздатьМенеджерЗаписи();
				упИзмененияРейсов.Рейс = Рейс;
				упИзмененияРейсов.Прочитать();
				упИзмененияРейсов.Рейс = Рейс;
				упИзмененияРейсов.ТребуетсяОбновление = Истина;
				упИзмененияРейсов.Заголовок = ЗаголовокУведомления;
				Если ЗначениеЗаполнено(РезультатОбработки) Тогда
					ТекстУведомления = СтрШаблон(НСтр("ru = 'Запрос подтвержден с комментарием: %1.'"), РезультатОбработки);
				Иначе
					ТекстУведомления = НСтр("ru = 'Запрос подтвержден.'");
				КонецЕсли;
				упИзмененияРейсов.ТекстУведомления = ТекстУведомления;
				упИзмененияРейсов.Записать();
				
			Иначе
				упУведомленияМобильныйКлиент = РегистрыСведений.упУведомленияМобильныйКлиент.СоздатьМенеджерЗаписи();
				упУведомленияМобильныйКлиент.Трекер              = Трекер;
				упУведомленияМобильныйКлиент.Рейс                = Рейс;
				упУведомленияМобильныйКлиент.ИдентификаторЗадачи = ИдентификаторЗадачи;
				упУведомленияМобильныйКлиент.Прочитать();
				
				упУведомленияМобильныйКлиент.Трекер              = Трекер;
				упУведомленияМобильныйКлиент.Рейс                = Рейс;
				упУведомленияМобильныйКлиент.ИдентификаторЗадачи = ИдентификаторЗадачи;
				упУведомленияМобильныйКлиент.Отправлено = Ложь;
				упУведомленияМобильныйКлиент.Заголовок = ЗаголовокУведомления;
				Если СтатусЗапроса = Перечисления.упСтатусыЗапросовМобильныйКлиент.Отклонен Тогда
					Если ЗначениеЗаполнено(РезультатОбработки) Тогда
						ТекстУведомления = СтрШаблон(НСтр("ru = 'Запрос отклонен с комментарием: %1.'"), РезультатОбработки);
					Иначе
						ТекстУведомления = НСтр("ru = 'Запрос отклонен.'");
					КонецЕсли; 
				Иначе	
					Если ЗначениеЗаполнено(РезультатОбработки) Тогда
						ТекстУведомления = СтрШаблон(НСтр("ru = 'Запрос отклонен без права на повторный запрос с комментарием: %1.'"), РезультатОбработки);
					Иначе
						ТекстУведомления = НСтр("ru = 'Запрос отклонен без права на повторный запрос.'");
					КонецЕсли; 
				КонецЕсли; 
				упУведомленияМобильныйКлиент.ТекстУведомления = ТекстУведомления;
				упУведомленияМобильныйКлиент.Записать();
				
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ДанныеЗаполнения = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		Статус = ПредопределенноеЗначение("Перечисление.упСтатусыСобытияМониторинга.Новое")
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли



