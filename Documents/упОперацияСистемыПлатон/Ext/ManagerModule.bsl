#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки данных из файла
//
// Параметры:
//     Параметры - Структура - Список параметров. Поля:
//         * Заголовок - Строка - Заголовок окна.
//         * ОбязательныеКолонки - Массив - Список имен колонок обязательных для заполнения.
//         * ТипДанныхКолонки - Соответствие, Ключ - Имя колонки, Значение - Описание типа данных.
//
Процедура ОпределитьПараметрыЗагрузкиДанныхИзФайла(Параметры) Экспорт
КонецПроцедуры

// Производит сопоставление загружаемых данных с данными в ИБ.
//
// Параметры:
//   ЗагружаемыеДанные - ТаблицаЗначений - таблица значений с загружаемыми данными:
//     * СопоставленныйОбъект - СправочникСсылка - Ссылка на сопоставленный объект. Заполняется внутри процедуры.
//     * <другие колонки>     - Произвольный - Состав колонок соответствует макету "ЗагрузкаИзФайла".
//
Процедура СопоставитьЗагружаемыеДанныеИзФайла(ЗагружаемыеДанные) Экспорт

	тбзДанныеДляСопоставления = Новый ТаблицаЗначений;
	ТипУникальногоНомера = Метаданные.Документы.упОперацияСистемыПлатон.Реквизиты.УникальныйНомер.Тип;
	тбзДанныеДляСопоставления.Колонки.Добавить("УникальныйНомер", ТипУникальногоНомера);

	Для каждого Строка Из ЗагружаемыеДанные Цикл
		НоваяСтрока = тбзДанныеДляСопоставления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;																	 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбзДанныеДляСопоставления.УникальныйНомер
	               |ПОМЕСТИТЬ втДанныеДляСопоставления
	               |ИЗ
	               |	&тбзДанныеДляСопоставления КАК тбзДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	упОперацияСистемыПлатон.Ссылка,
	               |	упОперацияСистемыПлатон.УникальныйНомер
	               |ИЗ
	               |	Документ.упОперацияСистемыПлатон КАК упОперацияСистемыПлатон
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДляСопоставления КАК втДанныеДляСопоставления
	               |		ПО упОперацияСистемыПлатон.УникальныйНомер = втДанныеДляСопоставления.УникальныйНомер
	               |ГДЕ
	               |	НЕ упОперацияСистемыПлатон.ПометкаУдаления";
	Запрос.УстановитьПараметр("тбзДанныеДляСопоставления", тбзДанныеДляСопоставления);
	Выборка	= Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Строки = ЗагружаемыеДанные.НайтиСтроки(Новый Структура("УникальныйНомер",Выборка.УникальныйНомер));
		Для каждого Строка Из Строки Цикл
			Строка.ОбъектСопоставления = Выборка.Ссылка;
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

// Загрузка данных из файла.
//
// Параметры:
//   ЗагружаемыеДанные - ТаблицаЗначений с колонками:
//     * ОбъектСопоставления         - СправочникСсылка - Ссылка на сопоставленный объект.
//     * РезультатСопоставленияСтроки - Строка       - Cтатусом загрузки, возможны варианты: Создан, Обновлен, Пропущен.
//     * ОписаниеОшибки               - Строка       - расшифровка ошибки загрузки данных.
//     * Идентификатор                - Число        - Уникальный номер строки.
//     * <другие колонки>             - Произвольный - Строки за загружаемого файла в соответствие с макетом.
// ПараметрыЗагрузки                  - Структура    - Параметры загрузки.
//     * СоздаватьНовые               - Булево       - Требуется ли создавать новые элементы справочника.
//     * ОбновлятьСуществующие        - Булево       - Требуется ли обновлять элементы справочника.
// Отказ                              - Булево       - Отмена загрузки.
//
Процедура ЗагрузитьИзФайла(ЗагружаемыеДанные, ПараметрыЗагрузки, Отказ) Экспорт
	
	мсвРегистрационныеНомера = ЗагружаемыеДанные.ВыгрузитьКолонку("ТранспортноеСредство");
	мсвРегистрационныеНомера = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мсвРегистрационныеНомера);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТранспортныеСредства.Ссылка,
	               |	упТранспортныеСредства.РегистрационныйНомер
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |ГДЕ
	               |	упТранспортныеСредства.РегистрационныйНомер В(&РегистрационныеНомера)";
	Запрос.УстановитьПараметр("РегистрационныеНомера", мсвРегистрационныеНомера);
	тбзТранспортныеСредства = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл
		
		Попытка
			
			ДатаНачалаДвижения 		= ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(СтрокаТаблицы.ДатаНачалаДвижения));
			ДатаОкончанияДвижения	= ПреобразоватьМосковскоеВремяВМестное(упСервисныеФункцииКлиентСервер.СтрокаВДату(СтрокаТаблицы.ДатаОкончанияДвижения));
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОбъектСопоставления) Тогда 
				Если ПараметрыЗагрузки.СоздаватьНовые Тогда 
					НачатьТранзакцию();
					ДокументОбъектОперацияСистемыПлатон = Документы.упОперацияСистемыПлатон.СоздатьДокумент();
					ДокументОбъектОперацияСистемыПлатон.Дата 			= ДатаНачалаДвижения;
					ДокументОбъектОперацияСистемыПлатон.УникальныйНомер	= Сокрлп(СтрокаТаблицы.УникальныйНомер);
					СтрокаТаблицы.ОбъектСопоставления = ДокументОбъектОперацияСистемыПлатон;
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Создан";
				Иначе
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
					Продолжить;
				КонецЕсли;
			Иначе
				Если НЕ ПараметрыЗагрузки.ОбновлятьСуществующие Тогда 
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
					Продолжить;
				КонецЕсли;
				
				НачатьТранзакцию();
				
				ДокументОбъектОперацияСистемыПлатон = СтрокаТаблицы.ОбъектСопоставления.ПолучитьОбъект();
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Обновлен";
				Если ДокументОбъектОперацияСистемыПлатон = Неопределено Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Документа с уникальным номером %1 не существует.'"), СтрокаТаблицы.УникальныйНомер);
					ВызватьИсключение ТекстСообщения;
				КонецЕсли;
			КонецЕсли;
			
			// Заполнение реквизитов документа.
					
			// Транспортное средство.
			мсвСтрокиТранспортноеСредство = тбзТранспортныеСредства.НайтиСтроки(Новый Структура("РегистрационныйНомер", СтрокаТаблицы.ТранспортноеСредство));
			Если мсвСтрокиТранспортноеСредство.Количество() > 0 Тогда
				ДокументОбъектОперацияСистемыПлатон.ТранспортноеСредство = мсвСтрокиТранспортноеСредство[0].Ссылка;
			КонецЕсли;
								
			// Даты начала и окончания движения.
			ДокументОбъектОперацияСистемыПлатон.ДатаНачалаДвижения 		= ДатаНачалаДвижения;
			ДокументОбъектОперацияСистемыПлатон.ДатаОкончанияДвижения 	= ДатаОкончанияДвижения;
			
			// Номер бортового устройства.
			ДокументОбъектОперацияСистемыПлатон.НомерБортовогоУстройства = Сокрлп(СтрокаТаблицы.НомерБортовогоУстройства);
			
			// Путь, пройденный ТС по автомобильным дорогам общего пользования федерального значения (км).
			ОписаниеТипаРасстояние = Метаданные.ОпределяемыеТипы.Расстояние.Тип;
			ДокументОбъектОперацияСистемыПлатон.Расстояние = ОписаниеТипаРасстояние.ПривестиЗначение(СтрокаТаблицы.Расстояние);
			
			// Наименование автомобильной дороги общего пользования федерального значения.
			ДокументОбъектОперацияСистемыПлатон.Трасса = Сокрлп(СтрокаТаблицы.Трасса);
						
			// Широта и долгота вьезда.
			ОписаниеТипаГеографическаяКоордината = Метаданные.ОпределяемыеТипы.ГеографическаяКоордината.Тип;
			
			КоординатыВьезда = СтрокаТаблицы.КоординатыВьезда;
			мсвКоординатыВьезда = СтрРазделить(КоординатыВьезда, ",");
			Если мсвКоординатыВьезда.Количество() > 1 Тогда
				ДокументОбъектОперацияСистемыПлатон.ШиротаВьезда 	= ОписаниеТипаГеографическаяКоордината.ПривестиЗначение(мсвКоординатыВьезда[0]);
				ДокументОбъектОперацияСистемыПлатон.ДолготаВьезда 	= ОписаниеТипаГеографическаяКоордината.ПривестиЗначение(мсвКоординатыВьезда[0]);	
			КонецЕсли;
			
			// Широта и долгота съезда.
			КоординатыСъезда = СтрокаТаблицы.КоординатыСъезда;
			мсвКоординатыCъезда = СтрРазделить(КоординатыСъезда, ",");
			Если мсвКоординатыCъезда.Количество() > 1 Тогда
				ДокументОбъектОперацияСистемыПлатон.ШиротаСъезда 	= ОписаниеТипаГеографическаяКоордината.ПривестиЗначение(мсвКоординатыCъезда[0]);
				ДокументОбъектОперацияСистемыПлатон.ДолготаСъезда 	= ОписаниеТипаГеографическаяКоордината.ПривестиЗначение(мсвКоординатыCъезда[1]);	
			КонецЕсли;
		
			Если ДокументОбъектОперацияСистемыПлатон.ПроверитьЗаполнение() Тогда
				ДокументОбъектОперацияСистемыПлатон.Записать(РежимЗаписиДокумента.Проведение);
				СтрокаТаблицы.ОбъектСопоставления = ДокументОбъектОперацияСистемыПлатон.Ссылка;
				ЗафиксироватьТранзакцию();
				Продолжить;
			Иначе
				ОтменитьТранзакцию();
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
				СообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
				Если СообщенияПользователю.Количество()>0 Тогда 
					текстСообщений = "";
					Для каждого СообщениеПользователю Из СообщенияПользователю Цикл
						текстСообщений  = текстСообщений + СообщениеПользователю.Текст + Символы.ПС;
					КонецЦикла;
					СтрокаТаблицы.ОписаниеОшибки = текстСообщений;
				КонецЕсли;
			КонецЕсли;
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Загрузка документов ОперацияСистемыПлатон", УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.упОперацияСистемыПлатон,, ТекстОшибки);
			ОтменитьТранзакцию();
			СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
			СтрокаТаблицы.ОписаниеОшибки = НСтр("ru = 'Невозможна запись из-за некорректности данных'");
		КонецПопытки;
	КонецЦикла;
		
КонецПроцедуры

Функция ПреобразоватьМосковскоеВремяВМестное(МСКДатаВремя)
	Универсальная = УниверсальноеВремя(МСКДатаВремя, "GMT+03:00");
	Возврат МестноеВремя(Универсальная, ЧасовойПоясСеанса());
КонецФункции


#КонецОбласти 

#КонецЕсли
