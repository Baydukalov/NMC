#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

&НаСервере
Процедура РаспределятьПоМесяцамНаСервере(ЭтотОбъект) Экспорт
	
	ЭтотОбъект.РезультатыРаспределения.Очистить();
			
	Для каждого Строка Из ЭтотОбъект.ПрочиеРасходы Цикл
		
		СуммаРаспределить = Строка.Сумма;
		СуммаНДСРаспределеить = Строка.СуммаНДС;
		СуммаВалютаРаспределить = Строка.СуммаВалюта;
		СуммаВалютаНДСРаспределить = Строка.СуммаВалютаНДС;
		НачалоСписания = НачалоДня(Строка.НачалоПериода);
	
		Если НачалоСписания = НачалоМесяца(НачалоСписания) Тогда
			ОкончаниеСписания = КонецМесяца(ДобавитьМесяц(НачалоСписания, Строка.КоличествоМесяцев - 1));
			КоличествоМесяцевСписания = Строка.КоличествоМесяцев;	
		Иначе	
			ОкончаниеСписания = ДобавитьМесяц(НачалоСписания, Строка.КоличествоМесяцев) - 1;
			КоличествоМесяцевСписания = Строка.КоличествоМесяцев + 1;	
		КонецЕсли;
		
		ОкончаниеСписания = КонецДня(ОкончаниеСписания);
		СтатьяРасходов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.СтатьяРасходов, "СтатьяСписанияРасходов");
		
		ДоляПоследнегоМесяца = День(ОкончаниеСписания) / День(КонецМесяца(ОкончаниеСписания));
		
		Для й = 1 По КоличествоМесяцевСписания Цикл
						
			НоваяСтрока = ЭтотОбъект.РезультатыРаспределения.Добавить();
			НоваяСтрока.Валюта = Строка.Валюта;
			НоваяСтрока.Подразделение = Строка.Подразделение;
			НоваяСтрока.АналитическийРазрез = Строка.АналитическийРазрез;
			НоваяСтрока.СтатьяРасходов = СтатьяРасходов;
			
			Если й > 1 Тогда
				НачалоСписания = НачалоМесяца(ДобавитьМесяц(НачалоСписания, 1));
			КонецЕсли;
			
			НоваяСтрока.Дата = НачалоСписания;
			НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
			
			Если КонецМесяца(НачалоСписания) = КонецМесяца(ОкончаниеСписания) Тогда
				ДоляТекущегоМесяца = ДоляПоследнегоМесяца;
				КоличествоМесяцев  = ДоляПоследнегоМесяца;
			Иначе
				ДоляТекущегоМесяца = (РазностьДатВДнях(КонецМесяца(НачалоСписания), НачалоСписания) + 1) / День(КонецМесяца(НачалоСписания));
				КоличествоМесяцевСередины = 0;
				ТекущаяДата = ДобавитьМесяц(КонецМесяца(НачалоСписания), 1);
				Пока КонецМесяца(ОкончаниеСписания) >= ТекущаяДата Цикл
					КоличествоМесяцевСередины = КоличествоМесяцевСередины + 1;
					ТекущаяДата = ДобавитьМесяц(ТекущаяДата, 1);
				КонецЦикла;
				КоличествоМесяцев = КоличествоМесяцевСередины - 1 + ДоляПоследнегоМесяца + ДоляТекущегоМесяца;
			КонецЕсли;
			СуммаСписать = ?(КоличествоМесяцев = 0, 0, Окр(СуммаРаспределить * ДоляТекущегоМесяца / КоличествоМесяцев, 2, 1));
			СуммаНДССписать = ?(КоличествоМесяцев = 0, 0, Окр(СуммаНДСРаспределеить * ДоляТекущегоМесяца / КоличествоМесяцев, 2, 1));
			СуммаВалютаСписать = ?(КоличествоМесяцев = 0, 0, Окр(СуммаВалютаРаспределить * ДоляТекущегоМесяца / КоличествоМесяцев, 2, 1));
			СуммаВалютаНДССписать = ?(КоличествоМесяцев = 0, 0, Окр(СуммаВалютаНДСРаспределить * ДоляТекущегоМесяца / КоличествоМесяцев, 2, 1));
			
			НоваяСтрока.Сумма = Мин(СуммаСписать, СуммаРаспределить);
			НоваяСтрока.СуммаНДС = Мин(СуммаНДССписать, СуммаНДСРаспределеить);
			НоваяСтрока.СуммаВалюта = Мин(СуммаВалютаСписать, СуммаВалютаРаспределить);
			НоваяСтрока.СуммаВалютаНДС = Мин(СуммаВалютаНДССписать, СуммаВалютаНДСРаспределить);
			СуммаРаспределить = СуммаРаспределить - НоваяСтрока.Сумма;
			СуммаНДСРаспределеить = СуммаНДСРаспределеить - НоваяСтрока.СуммаНДС;
			СуммаВалютаРаспределить = СуммаВалютаРаспределить - НоваяСтрока.СуммаВалюта;
			СуммаВалютаНДСРаспределить = СуммаВалютаНДСРаспределить - НоваяСтрока.СуммаВалютаНДС;
			
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция РазностьДатВДнях(Знач Дата1, Знач Дата2)
	
	ДлинаСуток = 86400; // в секундах
	Возврат Окр((НачалоДня(Дата1) - НачалоДня(Дата2)) / ДлинаСуток);
	
КонецФункции

#КонецОбласти

#КонецЕсли