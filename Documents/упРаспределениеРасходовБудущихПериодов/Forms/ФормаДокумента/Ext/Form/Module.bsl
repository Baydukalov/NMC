#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// Если новый документ
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор = ПользователиКлиентСервер.ТекущийПользователь();
		ЭтоКопирование  = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
		Если НЕ ЭтоКопирование Тогда
			Объект.Организация 	= Справочники.Организации.ОрганизацияПоУмолчанию();
			Если ИспользуетсяУчетПодразделений 
				И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
				Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Справочники.упВидыПеревозок.ПустаяСсылка(), Объект.Автор);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ИспользуетсяУчетПодразделений = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ПрочиеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПересчитатьРаспределениеРасходов", ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Распределение расходов будет пересчитано?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	Для каждого Строка Из Объект.ПрочиеРасходы Цикл
		Если НЕ ЗначениеЗаполнено(Строка.НачалоПериода) Тогда
			Строка.НачалоПериода = Объект.НачалоПериода;	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КоличествоМесяцевПриИзменении(Элемент)
	Для каждого Строка Из Объект.ПрочиеРасходы Цикл
		Если НЕ ЗначениеЗаполнено(Строка.КоличествоМесяцев) Тогда
			Строка.КоличествоМесяцев = Объект.КоличествоМесяцев;	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПрочиеРасходыПослеУдаления(Элемент)
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросПересчитатьРаспределениеРасходов", ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Распределение расходов будет пересчитано?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	Если ВсеПоляЗаполнены() Тогда
		ОбработкаОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамПрочихРасходов", ЭтаФорма);
		сткПараметры = ПараметрыФормыПодбора();
		ОткрытьФорму("РегистрНакопления.упПрочиеРасходы.Форма.ФормаОстатки", сткПараметры, ЭтаФорма, УникальныйИдентификатор,,,ОбработкаОповещения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамБезАналитическогоРазреза(Команда)
	Если ВсеПоляЗаполнены() Тогда
		ОбработкаОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаКлиенте", ЭтаФорма);
		сткПараметры = ПараметрыФормыПодбора();
		ОткрытьФорму("РегистрНакопления.упПрочиеРасходы.Форма.ФормаОстаткиБезАналитическогоРазреза", сткПараметры, ЭтаФорма, УникальныйИдентификатор,,,ОбработкаОповещения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВсеПоляЗаполнены()
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = Нстр("ru = 'Поле ""Организация"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация",,Отказ);
	КонецЕсли;
		
	Возврат Не Отказ;

КонецФункции

&НаКлиенте
Функция ПараметрыФормыПодбора()
	сткПараметры	= Новый Структура();
	сткПараметры.Вставить("Организация", Объект.Организация);
	сткПараметры.Вставить("Подразделение", Объект.Подразделение);
	сткПараметры.Вставить("СтатьяРасходов", Объект.СтатьяРасходов);
	сткПараметры.Вставить("Период", Объект.НачалоПериода);
	сткПараметры.Вставить("ВариантРасходов", ПредопределенноеЗначение("Перечисление.упВариантыРасходов.РасходыБудущихПериодов"));
	сткПараметры.Вставить("МножественныйВыбор", Истина);
	Возврат сткПараметры;
КонецФункции

&НаКлиенте
Процедура РаспределятьПоМесяцам(Команда)
	РаспределятьПоМесяцамНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ЗаполнитьПоОстаткамПрочихРасходов(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
 
	Если Истина
		И ТипЗнч(РезультатЗакрытия) = Тип("Массив") 
		И РезультатЗакрытия.Количество() > 0
	Тогда
		ОчиститьТабличныеЧасти();
		ЗаполнитьПоОстаткамСАналитическимРазрезомНаСервере(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РаспределятьПоМесяцамНаСервере()
	
	Документы.упРаспределениеРасходовБудущихПериодов.РаспределятьПоМесяцамНаСервере(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТабличныеЧасти()
	Объект.ПрочиеРасходы.Очистить();
	Объект.РезультатыРаспределения.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаКлиенте(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Истина
		И ТипЗнч(РезультатЗакрытия) = Тип("Массив") 
		И РезультатЗакрытия.Количество() > 0
	Тогда
		ОчиститьТабличныеЧасти();
		ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаСервере(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамБезАналитическогоРазрезаНаСервере(мсвПрочиеРасходы)
	упРаспределениеРасходов.ЗаполнитьПрочиеРасходыРасходовБудущихПериодов(Объект, мсвПрочиеРасходы);		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамСАналитическимРазрезомНаСервере(мсвПрочиеРасходы)
	упРаспределениеРасходов.ЗаполнитьПрочиеРасходыРасходовБудущихПериодовСАналитическимиРазрезами(Объект, мсвПрочиеРасходы);		
КонецПроцедуры


&НаКлиенте
Процедура ОбработатьОтветНаВопросПересчитатьРаспределениеРасходов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.РезультатыРаспределения.Очистить();
		РаспределятьПоМесяцамНаСервере();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти