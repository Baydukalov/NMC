#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ДополнительныеСвойства.Вставить("ВедетсяВалютныйУчет", ВедетсяВалютныйУчет);
	упРаспределениеРасходов.ЗаполнитьДоходыРасходы(ЭтотОбъект, Отказ);	
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Записать();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПретензииПоЗаявкамНаПеревозкуГруза");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Претензия", Ссылка);
	 
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПретензииПоРейсам");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Претензия", Ссылка);
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыВходящихПретензий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	ТранспортноеСредство = Неопределено;
	
	Если ЗначениеЗаполнено(Рейс) Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);

		Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетРасходовНаТС") Тогда
			
			сткПараметрыПеревозки = упРейс.ПараметрыПеревозкиРейса(Рейс);
			ТранспортноеСредство  = сткПараметрыПеревозки.ТранспортноеСредство;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упРасходыНаТС");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
		КонецЕсли;	
	
	КонецЕсли;
			
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
		
	РеквизитыОбъекта = Новый Структура("ЗаявкаНаПеревозкуГруза,
								|Дата,
								|Рейс,
								|ПричинаПретензии,
								|Партнер, 
								|Расходы,
								|Организация,
								|Подразделение");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ЭтотОбъект); 
	РеквизитыОбъекта.Вставить("ТранспортноеСредство",    ТранспортноеСредство);
	РеквизитыОбъекта.Вставить("НаправлениеДеятельности", Справочники.упНаправленияДеятельности.ПустаяСсылка());
	упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
	// Формируются прямые фактические расходы.
	упВходящаяПретензия.СформироватьДвиженияРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);		
	
	// Прочие расходы.
	упУправлениеЗапасами.СформироватьДвиженияПрочиеРасходы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Движения по заявке на перевозку.
	упВходящаяПретензия.СформироватьДвиженияПоЗаявкеВходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Движения по документу рейс.
	упВходящаяПретензия.СформироватьДвиженияПоРейсуВходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	// Формируется и устанавливается статус документа.
	упВходящаяПретензия.СформироватьДвиженияСтатусВходящейПретензии(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
	Движения.Записать();
	

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		// Заполнение шапки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упЗаявкаНаПеревозкуГруза.Заказчик КАК Партнер,
		|	упЗаявкаНаПеревозкуГруза.КонтрагентЗаказчика КАК Контрагент,
		|	упЗаявкаНаПеревозкуГруза.Организация,
		|	упЗаявкаНаПеревозкуГруза.Соглашение,
		|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ДокументОснование,
		|	упЗаявкаНаПеревозкуГруза.Ссылка КАК ЗаявкаНаПеревозкуГруза
		|ИЗ
		|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
		|ГДЕ
		|	упЗаявкаНаПеревозкуГруза.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,Запрос.Выполнить().Выгрузить()[0]);

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда
		// Заполнение шапки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упРейс.Ссылка КАК ДокументОснование,
		|	упРейс.Ссылка КАК Рейс,
		|	упПеревозчикПоРейсуСрезПоследних.Перевозчик КАК Партнер,
		|	упПеревозчикПоРейсуСрезПоследних.КонтрагентПеревозчика КАК Контрагент,
		|	упПеревозчикПоРейсуСрезПоследних.Соглашение,
		|	упРейс.Организация
		|ИЗ
		|	Документ.упРейс КАК упРейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних КАК упПеревозчикПоРейсуСрезПоследних
		|		ПО упПеревозчикПоРейсуСрезПоследних.Регистратор = упРейс.Ссылка
		|			И упПеревозчикПоРейсуСрезПоследних.Рейс = упРейс.Ссылка
		|ГДЕ
		|	упРейс.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,Запрос.Выполнить().Выгрузить()[0]);
				
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
