#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если ТекущийОбъект.Проведен Тогда
		ТолькоПросмотр = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ТекущийОбъект);
	КонецЕсли;
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор       = ПользователиКлиентСервер.ТекущийПользователь();
		Объект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		// Если копирование
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация 			= Справочники.Организации.ОрганизацияПоУмолчанию();	
			КонецЕсли;
			Объект.НачалоВыполнения 	= Объект.Дата;
		КонецЕсли;
		
		Если ЭтаФорма.ИспользуетсяУчетПодразделений
					И НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
				Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.Рейс, Объект.Автор);
		КонецЕсли;
	
	КонецЕсли;
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.Рейс));	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ИспользуетсяУчетПодразделений	= ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
		
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьЭлементовФормы();
	УстановитьВидимостьАдресаВозврата();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ТекстСообщения = "";
		упПроисшествиеВПути.ПроверитьДаты(Объект.НачалоВыполнения, Объект.ОкончаниеВыполнения, ТекстСообщения, Отказ);
		Если Отказ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ТекущийОбъект,"НачалоВыполнения", , Отказ);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, ТекущийОбъект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(ТекущийОбъект.Рейс));
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзменилсяМаршрут",,Объект.Рейс);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РейсПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство)
		ИЛИ ЗначениеЗаполнено(Объект.Водитель1)
		ИЛИ ЗначениеЗаполнено(Объект.Водитель2)
		ИЛИ ЗначениеЗаполнено(Объект.Экспедитор) Тогда
		ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбОчистке",ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Заполненные реквизиты будут очищены. Продолжить?'"),РежимДиалогаВопрос.ДаНет);		
	Иначе	
		ЗаполнитьПараметрыПервозчика();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	Если НЕ Объект.Действие = ПредопределенноеЗначение("Перечисление.упДействияПриПроисшествииВПути.ВозвратНаСклад") Тогда
		Объект.АдресВозврата 	= ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");	
		Объект.Проблема			= ПредопределенноеЗначение("Справочник.упПроблемы.ПустаяСсылка");	
	КонецЕсли;
	УстановитьВидимостьАдресаВозврата();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	ТранспортноеСредствоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
// Прцедура заполнения параметров перевозки.
//
Процедура ОбработатьОтветНаВопросОбОчистке(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПараметрыПервозчика();
		УстановитьДоступностьЭлементовФормы();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	ДанныеПеревозчикаДоступны = Истина;
	
	Если НЕ ЗначениеЗаполнено(Объект.Рейс) Тогда
		ДанныеПеревозчикаДоступны = Ложь;
	КонецЕсли;
	
	Элементы.ТранспортноеСредство.ТолькоПросмотр 	= ДанныеПеревозчикаДоступны;
	Элементы.Водитель1.ТолькоПросмотр				= ДанныеПеревозчикаДоступны;
	Элементы.Водитель2.ТолькоПросмотр				= ДанныеПеревозчикаДоступны;
	Элементы.Экспедитор.ТолькоПросмотр				= ДанныеПеревозчикаДоступны;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьАдресаВозврата()

	Если Объект.Действие = ПредопределенноеЗначение("Перечисление.упДействияПриПроисшествииВПути.ВозвратНаСклад") Тогда
		Элементы.ГруппаСтраницыАдресВозврата.ТекущаяСтраница = Элементы.ГруппаСтраницаАдресВозвратаЕсть;
	Иначе	
		Элементы.ГруппаСтраницыАдресВозврата.ТекущаяСтраница = Элементы.ГруппаСтраницаАдресаВозвратаНет;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыПервозчика()
	
	ДокументПроисшествиеВПути = РеквизитФормыВЗначение("Объект");
	ДокументПроисшествиеВПути.Заполнить(Объект.Рейс);
	ЗначениеВРеквизитФормы(ДокументПроисшествиеВПути, "Объект");
	
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.Рейс);
		Если ЗначениеЗаполнено(Подразделение)
				И НЕ Подразделение = Объект.Подразделение Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()    	
	упСервисныеФункцииВызовСервера.ОтобразитьПрицепыТранспортногоСредства(ЭтотОбъект, Объект.ТранспортноеСредство,Документы.упРейс.ПолучитьВремяНачалаРейса(Объект.Рейс)); 
КонецПроцедуры

#КонецОбласти