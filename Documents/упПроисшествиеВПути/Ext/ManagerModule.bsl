
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формировать по прохождению точки.
//
// Параметры:
//  ПрохождениеТочки  - ДокументСсылка.упПрохождениеТочки - Ссылка на документ основание.
//  ОписаниеОшибки  - Строка - Представление ошибки.
//  Отказ  - Булево - Флаг фиксации результата формирования.
//
// Возвращаемое значение:
//   Ссылка   - Ссылка на документ.
//
Функция СформироватьНаОснованииПрохожденияТочки(ПрохождениеТочки, ОписаниеОшибки, Отказ) Экспорт
	
	текРезультат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упПроисшествиеВПути.Ссылка
	               |ИЗ
	               |	Документ.упПроисшествиеВПути КАК упПроисшествиеВПути
	               |ГДЕ
	               |	упПроисшествиеВПути.ПрохождениеТочки = &ПрохождениеТочки
	               |	И НЕ упПроисшествиеВПути.ПометкаУдаления";
	Запрос.УстановитьПараметр("ПрохождениеТочки", ПрохождениеТочки);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Ссылка;
	Иначе	
		ПроисшествиеВПутиОбъект = Документы.упПроисшествиеВПути.СоздатьДокумент();
		ПроисшествиеВПутиОбъект.ИсточникСозданияОбъекта = упСервисныеФункцииКлиентСервер.ИсточникСозданияОбъекта();
		ПроисшествиеВПутиОбъект.Дата				= ТекущаяДатаСеанса();
		ПроисшествиеВПутиОбъект.Автор       		= ПользователиКлиентСервер.ТекущийПользователь();
		ПроисшествиеВПутиОбъект.НачалоВыполнения 	= ПроисшествиеВПутиОбъект.Дата;
		ПроисшествиеВПутиОбъект.Заполнить(ПрохождениеТочки);
		
		Попытка
			ПроисшествиеВПутиОбъект.Записать(РежимЗаписиДокумента.Запись);	
			текРезультат = ПроисшествиеВПутиОбъект.Ссылка;
		Исключение
			Отказ = Истина;
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда 
				ОписаниеОшибки = ОписаниеОшибки();
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Возврат текРезультат;
			
КонецФункции // СформироватьНаОснованииПрохожденияТочки()

#КонецОбласти

#КонецЕсли
