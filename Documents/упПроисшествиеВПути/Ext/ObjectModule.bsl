
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упПрохождениеТочки") Тогда
		ПрохождениеТочки 	= ДанныеЗаполнения;
		Рейс 				= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Рейс");
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.упРейс") Тогда
		Рейс = ДанныеЗаполнения;	
	КонецЕсли;
	
	Дата			 	= ТекущаяДатаСеанса();
	НачалоВыполнения 	= Дата;
	ОкончаниеВыполнения	= Дата + 1;
	
	Если ЗначениеЗаполнено(Рейс) Тогда
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рейс, "Организация");
	Иначе	
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ИдентификаторТочки = Новый УникальныйИдентификатор;
	
	сткЭкипаж = упРейс.ПолучитьЭкипажРейса(Рейс);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткЭкипаж);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ФормируютсяДвижения	= Ложь;
	
	РеквизитыОбъекта = ДополнительныеСвойства.РеквизитыОбъекта;
	
	Если (Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад
		ИЛИ Действие = Перечисления.упДействияПриПроисшествииВПути.ПродолжениеРейса) 
			И ЗначениеЗаполнено(Рейс) И ЗначениеЗаполнено(ПрохождениеТочки)	Тогда
		ФормируютсяДвижения = Истина;
	КонецЕсли;
	
	Если ФормируютсяДвижения Тогда
		СтрокаНомер		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрохождениеТочки, "СтрокаНомер");
		мсвДокументы 	= упПрохождениеТочки.НайтиПрохождениеБолееПозднейТочки(СтрокаНомер, Рейс);
		
		Если мсвДокументы.Количество() > 0  Тогда
			ТекстСообщения = Нстр("ru = 'Происшествие с действием ""%1"" запрещено, так как приведёт к нарушению порядка прохождения точек маршрута.
			|Что бы получить такую возможность отмените перед этим проведение следующих документов:'");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Действие);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			
			Для каждого текДокумент Из мсвДокументы Цикл
				ТекстСообщения = "		"+текДокумент;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,текДокумент,,,Отказ);	
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ И ФормируютсяДвижения Тогда
		
		БлокировкаДанных = Новый БлокировкаДанных;
		
		Задания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства,"Задания");

		Если ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент") Тогда
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упИзмененияРейсов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		КонецЕсли; 
		
		Если Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад  Тогда
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыРейсов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);

			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПлановыеПараметрыРейса");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
			
			Если ПолучитьФункциональнуюОпцию("упИспользуетсяВозвратДокументовПослеИсполненияРейса") Тогда
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упНепереданныеДокументы");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
			КонецЕсли; 
			
			
			Если НЕ Задания = Неопределено Тогда
						
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упЗаданияКПланированию");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных	= Задания;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", "Задание");
				Если ДополнительныеСвойства.ФормироватьПоГрузовымМестам Тогда
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
				КонецЕсли;
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упСостояниеПоЗаданиям");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных	= Задания;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", 		 "Задание");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упПланПоЗаданиям");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных	= Задания;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Задание", 		 "Задание");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГрузовоеМесто", "ГрузовоеМесто");
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаявокНаПеревозкуГруза");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных	= упЗаданиеНаПеревозку.ПолучитьЗаявкиПоЗаданиям(Задания.ВыгрузитьКолонку("Задание"));
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Заявка");
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных	= Задания;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Задание");
				
			КонецЕсли;
			Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упФактическиеМестоположенияТС");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
				
				ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упЗанятостьТранспорта");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Транспорт", ТранспортноеСредство);
				ЭлементБлокировки.УстановитьЗначение("Документ", Рейс);
				
			КонецЕсли; 
			
		КонецЕсли;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упРаботыПоРейсу");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упМаршрутПоРейсу");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
		
		Попытка
			БлокировкаДанных.Заблокировать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
			ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Возврат;
		КонецПопытки;
			
	КонецЕсли;
	
	Если НЕ Отказ И ФормируютсяДвижения Тогда
				
		РеквизитыОбъекта.Вставить("Дата" , 		ОпределитьПериодФормированияДвижений());
		РеквизитыОбъекта.Вставить("Рейс", 		Рейс);
		РеквизитыОбъекта.Вставить("АдресТочки", АдресПроисшествия);
		
		Если Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад Тогда
			
			// Формируются движения по заданиям в случае отмены заданий.
			упПрохождениеТочки.СформироватьДвиженияПоЗаданиямНаПеревозку(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Формируются движения по заявкам в случае отмены заданий.
			упКорректировкаМаршрута.СформироватьДвиженияЗаявкиКВыполнению(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
		КонецЕсли;
		
		// Формируется маршрут.
		упКорректировкаМаршрута.СформироватьДвижениеМаршрутПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		// Формируются работы.
		упКорректировкаМаршрута.СформироватьДвижениеРаботыПоРейсу(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		
		Если Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад Тогда
			// Формируется пакет документов, о которых следует отчитаться после выполнения рейса.
			упПрохождениеТочки.СформироватьДвиженияНепереданныеДокументы(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Формируются движения по состояниям задания.
			упПрохождениеТочки.СформироватьДвиженияСостояниеПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Формируется план по заданиям.
			упКорректировкаМаршрута.СформироватьДвижениеПланПоЗаданиям(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Формируется факт инкассации денежных средств.
			упПрохождениеТочки.СформироватьДвиженияИнкассацияДенежныхСредств(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Формируются остатки грузовых мест на хабах, в случае мультимодальной перевозки.
			упКорректировкаМаршрута.СформироватьДвижениеОстаткиГрузовыхМест(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		КонецЕсли;
		
		Движения.Записать();
		
		Если Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад Тогда
			
			// Устанавливаются статусы рейса.
			ДополнительныеСвойства.Вставить("ДобавитьСтатус", 		Ложь);
			ДополнительныеСвойства.Вставить("РассчитыватьСтатус", 	Истина);
			ДополнительныеСвойства.Вставить("ПровестиПроверкуНаВозможныеСтатусы", 	Истина);
			упРейс.СформироватьДвижениеСтатусыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			Если ДополнительныеСвойства.Свойство("ИзменилсяСтатусРейса") Тогда
				Движения.Записать();
			КонецЕсли;
			
			// При отклонении времени прохождения точки от плана заполняется плановое окончание рейса
			упКорректировкаМаршрута.СформироватьПлановыеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// При прохождении последней точки формируются фактические параметры рейса.
			упРейс.СформироватьФактическиеПараметрыРейса(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// При прохождении первой точки ТС переводится в состояние "В рейсе", последней - свободно.
			упРейс.СформироватьДвижениеСостояниеТС(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// При прохождении последней точки фиксируется фактическое время нахождения ТС в рейсе.
			упРейс.СформироватьДвиженияЗанятостиТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// При прохождении последней точки фиксируется фактическое время нахождения ТС в рейсе.
			упПрохождениеТочки.СформироватьДвиженияФактическиеМестоположенияТранспорта(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Устанавливаются статусы заданий.
			ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗадания", Истина);
			упЗаданиеНаПеревозку.СформироватьДвиженияСтатусыЗаданий(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// Устанавливаются статусы заявок.
			ДополнительныеСвойства.Вставить("ОчиститьЗаписиСтатусЗаявки", Истина);
			упУправленияЗаявками.СформироватьДвиженияСтатусыЗаявок(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
			
			// В случае если это последняя точка маршрута, то устанавливается дата окончания рейса в путевом листе.
			упПрохождениеТочки.ЗаполнитьДатуОкончанияПутевогоЛиста(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
		КонецЕсли;
		
		// При выполнении рейса на мобильном клиенте фиксируем необоходимость обновить данные по рейсу
		упКорректировкаМаршрута.СформироватьДвиженияИзмененияРейсов(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ)
							
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 

	Изменил = упСервисныеФункцииКлиентСервер.АвторПоследнегоИзмененияОбъекта(ДополнительныеСвойства);
	
	Если НЕ РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Отказ = упКорректировкаМаршрута.ЗапрещеноРедактироватьМаршрутИлиРаботыРейса(ЭтотОбъект);
		Если НЕ Отказ Тогда
			СтатусРейса = упРаботаСоСтатусами.ПолучитьТекущийСтатус(Рейс);
			ТекстОшибки = "";
			Если НЕ упРаботаСоСтатусамиКлиентСервер.РазрешеноФормироватьКорректировкуРейса(СтатусРейса, РежимЗаписи)  Тогда
				ТекстОшибки = НСтр(СтрШаблон("ru = 'Запрещено проведение и отмена проведения документа, так как рейс находится в статусе ""%1""'", СтатусРейса));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыОбъекта = Новый Структура();
	
	Если НЕ Отказ И НЕ РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Если ЗначениеЗаполнено(Рейс) И ЗначениеЗаполнено(ПрохождениеТочки) Тогда
			упКорректировкаРейсаОбъект = Неопределено;
			Если НЕ ДополнительныеСвойства.Свойство("КорректировкаРейса", упКорректировкаРейсаОбъект) Тогда
				упКорректировкаРейсаОбъект = Обработки.упКорректировкаРейса.Создать();	
				ИнициализацияКорректировкиМаршрута(упКорректировкаРейсаОбъект);
				ДополнительныеСвойства.Вставить("СтатусРейсаДоКорректировки", 	упКорректировкаРейсаОбъект.СтатусРейсаДоКорректировки);
			КонецЕсли;	
					
			сткРеквизиты = Новый Структура();
			сткРеквизиты.Вставить("Идентификатор"		, ИдентификаторТочки);
			сткРеквизиты.Вставить("НачалоВыполнения"    , НачалоВыполнения);
			сткРеквизиты.Вставить("ОкончаниеВыполнения" , ОкончаниеВыполнения);
			сткРеквизиты.Вставить("АдресВозврата"		, АдресВозврата);
			сткРеквизиты.Вставить("ПроблемаВПути"		, ПроблемаВПути);
			сткРеквизиты.Вставить("Проблема"			, Проблема);
			сткРеквизиты.Вставить("АдресПроисшествия"	, АдресПроисшествия);
			сткРеквизиты.Вставить("АдресВозврата"		, АдресВозврата);
			сткРеквизиты.Вставить("Действие"			, Действие);
			упКорректировкаРейсаОбъект.СформироватьТаблицыДвиженийДляКорректировки(ДополнительныеСвойства, Ссылка, сткРеквизиты, Отказ);
			РеквизитыОбъекта.Вставить("ВидТранспортнойУслуги", упКорректировкаРейсаОбъект.ВидТранспортнойУслуги);
		КонецЕсли; 
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РеквизитыОбъекта" , РеквизитыОбъекта);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Действие = Перечисления.упДействияПриПроисшествииВПути.ВозвратНаСклад Тогда
		
		ПроверяемыеРеквизиты.Добавить("Проблема");
		
		Если НЕ ЗначениеЗаполнено(ПрохождениеТочки) Тогда
			Отказ = Истина;
			ТекстСообщения = Нстр("ru = 'Для действия ""Возврат на склад"" должнен быть заполнен реквизит ""Прохождение точки""'");		
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			ТекстСообщения = "";
			Если ОкончаниеВыполнения < НачалоВыполнения	Тогда
				Отказ = Истина;
				ТекстСообщения = Нстр("ru = 'Дата убытия из точки %1 раньше даты прибытия %2'");		
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОкончаниеВыполнения, НачалоВыполнения);
			КонецЕсли;	
			
			Если Отказ Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,"ОкончаниеВыполнения", , Отказ);	
			КонецЕсли;
		КонецЕсли; 
		
		Если НЕ Отказ Тогда
			ТекстСообщения 	= "";
			СтрокаНомер 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрохождениеТочки, "СтрокаНомер");
			упПрохождениеТочки.ПроверитьДатуПрибытияФакт(НачалоВыполнения, Рейс, СтрокаНомер, ТекстСообщения, Отказ);
			
			Если Отказ Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,"НачалоВыполнения", , Отказ);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Инициализация
//
// Параметры:
//  КорректировкаМаршрутаОбъект	 
//
Процедура ИнициализацияКорректировкиМаршрута(КорректировкаМаршрутаОбъект) Экспорт
	
	ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками = ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками");
	
	КорректировкаМаршрутаОбъект.Рейс 	= Рейс;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		КорректировкаМаршрутаОбъект.Ссылка 	= Ссылка;
		КорректировкаМаршрутаОбъект.ЗаполнитьПериодДатойДвиженийПоМаршруту();
	Иначе
		КорректировкаМаршрутаОбъект.Ссылка 	= Документы.упПроисшествиеВПути.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КорректировкаМаршрутаОбъект.Период)  Тогда
		КорректировкаМаршрутаОбъект.Период = Дата;
	КонецЕсли;
	
	сткПараметрыЗаполнения = Новый Структура();
	сткПараметрыЗаполнения.Вставить("ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками", ИспользуетсяГлобальнаяКомпонентаУправлениеПеревозками);
	КорректировкаМаршрутаОбъект.Инициализация(сткПараметрыЗаполнения);
	
КонецПроцедуры

// Функция определяет дату формирования движений
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений()
	
	Если ЗначениеЗаполнено(ОкончаниеВыполнения) Тогда
		Период = ОкончаниеВыполнения;
	Иначе
		Период = Дата;
	КонецЕсли; 

	Возврат Период;
	
КонецФункции


#КонецОбласти

#КонецЕсли	