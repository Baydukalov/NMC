#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Автор 		= ПользователиКлиентСервер.ТекущийПользователь();
		Объект.Организация 	= Справочники.Организации.ОрганизацияПоУмолчанию();
		Если ИспользуетсяУчетПодразделений Тогда
			Объект.Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки, Объект.Автор);
		КонецЕсли;
		
		упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(Объект);
		
		Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
			ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
			
			// Если при копировании отличаются часовые пояса источника и нового документа.
			Если Истина
				И ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
				И ЗначениеЗаполнено(Объект.ЧасовойПояс)
				И ЗначениеЗаполнено(ЧасовойПоясВидаПеревозки)
				И НЕ Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки
			Тогда
				Если ЗначениеЗаполнено(Объект.ДатаАктуальности) Тогда
					Объект.ДатаАктуальности		= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ДатаАктуальности, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 	
				КонецЕсли;
				Если ЗначениеЗаполнено(Объект.ДатаДоставки) Тогда
					Объект.ДатаДоставки	= упСервисныеФункции.ПеревестиВремяПриСменеЧасовогоПояса(Объект.ДатаДоставки, Объект.ЧасовойПояс, ЧасовойПоясВидаПеревозки); 
				КонецЕсли;
			КонецЕсли;	
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;		
		КонецЕсли;
			
	КонецЕсли;	
	
	Статус = Документы.упЗапросУсловийПеревозки.ПолучитьСтатус(Объект.Ссылка);
	
	ЗаблокироватьЭлементы();
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗапросаУсловийПеревозки(Объект.Ссылка, Статус, Элементы);
	УстановитьПредставлениеЕдиницИзмеренияПоказателей();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
		
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();
		Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
			УстановитьВалютуУчета();
		КонецЕсли;
	КонецЕсли;
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЭтаФорма.ИспользуетсяУчетПодразделений = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПодразделений"); 
	ЭтаФорма.ВедетсяРаботаВРазныхЧасовыхПоясах = ПолучитьФункциональнуюОпцию("упВедетсяРаботаВРазныхЧасовыхПоясах"); 
	ЭтаФорма.ВедетсяВалютныйУчет = ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	
	УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	
	Если УчитыватьЛинейныеРазмеры Тогда
		Элементы.Декорация5.Видимость = Истина;
		Элементы.Декорация6.Видимость = Истина;	
	Иначе
		Элементы.Декорация5.Видимость = Ложь;
		Элементы.Декорация6.Видимость = Ложь;
	КонецЕсли;
	
	ПриСозданииПриЧтенииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РассчитатьПроцентСкидкиИтого();
	// СтандартныеПодсистемы.Свойства
    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОпределитьЗоны(ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ЗначениеЗаполнено(НовыйСтатус) Тогда 
		ТекущийОбъект.ДополнительныеСвойства.Вставить("Статус", НовыйСтатус);
	КонецЕсли;	

	НовыйСтатус = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Статус = Документы.упЗапросУсловийПеревозки.ПолучитьСтатус(Объект.Ссылка);
	ЗаблокироватьЭлементы();
	
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗапросаУсловийПеревозки(Объект.Ссылка, Статус, Элементы);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	упРейс.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "СменаСтатуса" И Источник = Объект.Ссылка Тогда
		ИзменилсяСтатус();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры //ОбработкаПроверкиЗаполненияНаСервере()

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПлановыеРасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;

		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьИтоговуюСуммуРасходы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуРасходы();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтрокуРасходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыЦенаПриИзменении(Элемент)
	ПересчитатьСтрокуРасходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСуммаПриИзменении(Элемент)
	ПересчитатьСтрокуРасходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтрокуРасходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеРасходыСтатьяРасходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтатьяРасходов	= ТекущаяСтрока.СтатьяРасходов;
	Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
		ТекущаяСтрока.СтавкаНДС = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "СтавкаНДС");
		ПересчитатьСтрокуРасходы(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если ВедетсяВалютныйУчет И НоваяСтрока И Не Копирование Тогда
		ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
		Если ВедетсяВалютныйУчет Тогда
			ТекущаяСтрока.Валюта 		= ВалютаРеглУчета;	
		Иначе
			ТекущаяСтрока.Валюта 		= ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");	
		КонецЕсли;
		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьИтоговуюСуммуДоходы();
	РассчитатьПроцентСкидкиИтого();
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыПослеУдаления(Элемент)
	РассчитатьИтоговуюСуммуДоходы();
	РассчитатьПроцентСкидкиИтого();
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыКоличествоПриИзменении(Элемент)
	ПересчитатьСтрокуДоходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыЦенаПриИзменении(Элемент)
	ПересчитатьСтрокуДоходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСкидкаПроцентПриИзменении(Элемент)
	ПересчитатьСтрокуДоходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСкидкаСуммаПриИзменении(Элемент)
	ПересчитатьСтрокуДоходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСуммаПриИзменении(Элемент)
	ПересчитатьСтрокуДоходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСтавкаНДСПриИзменении(Элемент)
	ПересчитатьСтрокуДоходы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПлановыеДоходыСтатьяДоходовПриИзменении(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
	СтатьяДоходов	= ТекущаяСтрока.СтатьяДоходов;
	Если ЗначениеЗаполнено(СтатьяДоходов) Тогда
		ТекущаяСтрока.СтавкаНДС = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтатьяДоходов, "СтавкаНДС");
		ПересчитатьСтрокуДоходы(Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	Если ВедетсяВалютныйУчет И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		УстановитьВалютуУчета();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ПриИзмененииРазмеров();
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	ВидПеревозкиПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ЗонаОтправленияПриИзменении(Элемент)
	
	УстановитьПараметрыВыбораАдресаОтправления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗонаПолученияПриИзменении(Элемент)
	УстановитьПараметрыВыбораАдресаПолучения(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура АдресОтправленияПриИзменении(Элемент)
	УстановитьПараметрыАдресаОтправления();
КонецПроцедуры

&НаКлиенте
Процедура АдресПолученияПриИзменении(Элемент)
	УстановитьПараметрыАдресаПолучения();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПодобратьВидПеревозки(Команда)
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда 
		ТекстВопроса =
			НСтр("ru = 'Документ не записан. Выполнение команды
			           |""Подобрать вид перевозки"" возможно только после записи данных.
			           |
			           |Документ будут записан.'");
		Обработчик = Новый ОписаниеОповещения("ПодобратьВидПеревозкиПослеОтветаНаВопросЗаписать", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;

	ПодобратьВидПеревозкиНаСервере();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОбработан(Команда)
	
	Если УстановитьСтатусОбработанСервер() Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтменен(Команда)
	
	Если УстановитьСтатусОтмененСервер() Тогда 
		ОповеститьОбИзменении(Объект.Ссылка);
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Статус не изменен'"),, НСтр("ru = 'Не удалось провести документ'"), БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВременноеОкноОтправления(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ОтправлениеГрузаС", "ОтправлениеГрузаПо"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВременноеОкноПолучения(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ПолучениеГрузаС", "ПолучениеГрузаПо"));
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПоГрафикуРаботыТочкиОтправления(Команда)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаОтправления) Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ГрафикРаботы" , Объект.ГрафикРаботыАдресаОтправления);
		сткПараметры.Вставить("НачалоПериода", Объект.ОтправлениеГрузаС);
		сткПараметры.Вставить("КонецПериода" , Объект.ОтправлениеГрузаПо);
		сткПараметры.Вставить("ЦветФона"     , Элементы.ГруппаОтправление.ЦветФона); 
		
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораВременногоОкна", сткПараметры,,,,,Новый ОписаниеОповещения("ИзменитьВремяОтправленияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПоГрафикуРаботыТочкиПолучения(Команда)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыАдресаПолучения) Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("ГрафикРаботы" , Объект.ГрафикРаботыАдресаПолучения);
		сткПараметры.Вставить("НачалоПериода", Объект.ПолучениеГрузаС);
		сткПараметры.Вставить("КонецПериода" , Объект.ПолучениеГрузаПо);
		сткПараметры.Вставить("ЦветФона"     , Элементы.ГруппаПолучение.ЦветФона); 
		ОткрытьФорму("Обработка.упИнтерфейсУправленияВременем.Форма.ФормаВыбораВременногоОкна", сткПараметры,,,,,Новый ОписаниеОповещения("ИзменитьВремяПолученияЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьПредставлениеЕдиницИзмеренияПоказателей()
	
	УчитыватьМассу                             = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	УчитыватьОбъем                             = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	УчитыватьКоличествоМест                    = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	УчитыватьЛинейныеРазмеры                   = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");

	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕдМассы = сткПредставления.Масса;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕдОбъема = сткПредставления.Объем;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда
		ЕдКоличестваМест = сткПредставления.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕдДлины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли; 
	
КонецПроцедуры // УстановитьПредставлениеЕдиницИзмеренияПоказателей()

&НаСервере
Процедура ЗаблокироватьЭлементы()

	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Если Ложь
			Или Статус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Закрыт 
			Или Статус = Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен 
		Тогда
			ТолькоПросмотр = Истина;
		КонецЕсли;	
	КонецЕсли;	
	
	Если Истина
		И НЕ ТолькоПросмотр 
		И ВедетсяРаботаВРазныхЧасовыхПоясах
	Тогда
		ТолькоПросмотр = НЕ упСервисныеФункции.СовпадаютЧасовойПоясПользователяИДокумента(Объект.ЧасовойПояс);
	КонецЕсли;

КонецПроцедуры // ЗаблокироватьЭлементы()

&НаСервере
Функция УстановитьСтатусОтмененСервер()
	
	Возврат ИзменитьСтатусЗапроса(Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен);
	
КонецФункции

&НаСервере
Функция УстановитьСтатусОбработанСервер()
	
	Возврат ИзменитьСтатусЗапроса(Перечисления.упСтатусыЗапросаУсловийПеревозки.Обработан);
	
КонецФункции

&НаСервере
Функция ИзменитьСтатусЗапроса(СтатусЗапроса)
	
	НовыйСтатус = СтатусЗапроса;
	
	Если ПроверитьЗаполнение() Тогда 
		Попытка
			Записать();		
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект.Ссылка,,,);

        	Возврат Ложь;    
		КонецПопытки;
		
		Возврат Истина;
		
	КонецЕсли;
	
	НовыйСтатус = Неопределено;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменилсяСтатус()
	
	Статус = Документы.упЗапросУсловийПеревозки.ПолучитьСтатус(Объект.Ссылка);
	ЗаблокироватьЭлементы();
	упРаботаСоСтатусами.УстановитьКнопкиСтатусаЗапросаУсловийПеревозки(Объект.Ссылка, Статус, Элементы);	
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоговуюСуммуРасходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаРасходы",		"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаРасходыНДС",	"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ПлановыеРасходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаСервере
Процедура РассчитатьИтоговуюСуммуДоходы()
	сткСоответствиеРеквизитов = Новый Структура();
	сткСоответствиеРеквизитов.Вставить("СуммаДоходы",			"Сумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыСкидка",	"СкидкаСумма");
	сткСоответствиеРеквизитов.Вставить("СуммаДоходыНДС",		"СуммаНДС");
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьИтогиПоСтрокам(Объект, "ПлановыеДоходы", сткСоответствиеРеквизитов, ВедетсяВалютныйУчет);
КонецПроцедуры // РассчитатьИтоговуюСумму()

&НаСервере	
Процедура ПодобратьВидПеревозкиНаСервере()
	
	Объект.ВидПеревозки = Справочники.упВидыПеревозок.ПодобратьВидПеревозки(Объект.Ссылка);
	ВидПеревозкиПриИзмененииНаСервере();
	
КонецПроцедуры // ПодобратьВидПеревозкиНаСервере()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.Ширина * Объект.Высота * Объект.Длина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура подбора вида перевозки после утвердительного ответа пользователя.
//
Процедура ПодобратьВидПеревозкиПослеОтветаНаВопросЗаписать(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ПодобратьВидПеревозкиНаСервере();
	Модифицированность = Истина;
	
КонецПроцедуры // ПодобратьВидПеревозкиПослеОтветаНаВопросЗаписать()

#КонецОбласти 

&НаКлиенте
Функция ПолучитьИмяПоляДоходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеДоходы","");
КонецФункции

&НаКлиенте
Функция ПолучитьИмяПоляРасходы(ИмяПоля)
	Возврат СтрЗаменить(ИмяПоля, "ПлановыеРасходы","");
КонецФункции

&НаСервере
Процедура УстановитьВалютуУчета()
	ВалютаРеглУчета = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Соглашение).ВалютаСоглашения(Объект.Соглашение);	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПроцентСкидкиИтого()
	ИтогПроцентСкидки  =  0;
	ИтогПлановыеДоходыСумма = Объект.ПлановыеДоходы.Итог("Сумма");
	Если НЕ Объект.ПлановыеДоходы.Итог("Сумма") = 0 Тогда
		ИтогПроцентСкидки = Объект.ПлановыеДоходы.Итог("СкидкаСумма") / ИтогПлановыеДоходыСумма *  100;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтрокуРасходы(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеРасходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка", Объект.Ссылка);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуРасходовПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляРасходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтрокуДоходы(Элемент)
	ТекущаяСтрока	= Элементы.ПлановыеДоходы.ТекущиеДанные;
	СтруктураДанные	= Новый Структура("Ссылка, Соглашение", Объект.Ссылка, Объект.Соглашение);
	упОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСтрокуПриИзмененииЗначения(ТекущаяСтрока,ПолучитьИмяПоляДоходы(Элемент.Имя),СтруктураДанные);
КонецПроцедуры

&НаСервере
Процедура ВидПеревозкиПриИзмененииНаСервере()
	Если ИспользуетсяУчетПодразделений Тогда
		Подразделение = Справочники.упСтруктураПредприятия.ПодразделениеПоУмолчанию(Объект.ВидПеревозки);
		Если Истина
			И ЗначениеЗаполнено(Подразделение)
			И НЕ Подразделение = Объект.Подразделение 
		Тогда
			Объект.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
	
	Если ВедетсяРаботаВРазныхЧасовыхПоясах Тогда
		ЧасовойПоясВидаПеревозки	= упСервисныеФункции.ЧасовойПоясВидаПеревозки(Объект.ВидПеревозки);	
		Если НЕ ЧасовойПоясВидаПеревозки = Объект.ЧасовойПояс Тогда
			Объект.ЧасовойПояс = ЧасовойПоясВидаПеревозки;
		КонецЕсли;
	КонецЕсли;
	
	упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыАдресаПолучения()
	
	Если	ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		Объект.ЗонаПолучения = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.АдресПолучения, "ГеографическаяЗона");
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыАдресаОтправления()
	Если ЗначениеЗаполнено(Объект.АдресОтправления)Тогда
		Объект.ЗонаОтправления = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(Объект.АдресОтправления, "ГеографическаяЗона");
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораАдресаОтправления(ЭтаФорма)
	
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(ЭтаФорма.Элементы.АдресОтправления, "ГеографическаяЗона", ЭтаФорма.Объект.ЗонаОтправления, Не ЗначениеЗаполнено(ЭтаФорма.Объект.ЗонаОтправления));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораАдресаПолучения(ЭтаФорма)
	
	упСервисныеФункцииКлиентСервер.УстановитьПараметрВыбораПоляФормы(ЭтаФорма.Элементы.АдресПолучения, "ГеографическаяЗона", ЭтаФорма.Объект.ЗонаПолучения, Не ЗначениеЗаполнено(ЭтаФорма.Объект.ЗонаПолучения));
	
КонецПроцедуры

&НаКлиенте
// Процедура изменения время отправления после утвердительного ответа.
//
Процедура ИзменитьВремяОтправленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Объект.ОтправлениеГрузаС  = Результат.НачалоПериода;
		Объект.ОтправлениеГрузаПо = Результат.КонецПериода;
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВремяОтправленияЗавершение()

&НаКлиенте
// Процедура изменения времени получения после утвердительного ответа.
//
Процедура ИзменитьВремяПолученияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		Объект.ПолучениеГрузаС  = Результат.НачалоПериода;
		Объект.ПолучениеГрузаПо = Результат.КонецПериода;
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры // ИзменитьВремяПолученияЗавершение()

&НаКлиенте
Процедура ТипГрузаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТипГруза) Тогда
		ЗаполнитьВГХГруза();	
	КонецЕсли;
	ГрузМодифицирован = Истина;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВГХГруза()
	
	сткВГХГруза = ПолучитьВГХГруза(Объект.ТипГруза); 
	ЗаполнитьЗначенияСвойств(Объект, сткВГХГруза);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВГХГруза(ТипГруза)
	
	сткВГХГруза = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипГруза, "Масса, Высота, Длина, Ширина, Объем, КоличествоМест, КоличествоЗагрузочныхМетров, КлассОпасности, ТемпературныйРежим, Хрупкий, БеречьОтВлаги, БеречьОтИзлучения, БеречьОтНагрева, ВидГруза, ЗапрещеноСкладироватьДругНаДруга"); 
	Если сткВГХГруза.Свойство("ВидГруза") 
			И ЗначениеЗаполнено(сткВГХГруза.ВидГруза) Тогда
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(сткВГХГруза.ВидГруза));	
		Если сткВГХГруза.ВидГруза = Перечисления.упВидыГрузов.ИнкассируемаяЦенность Тогда
			сткВГХГруза.Вставить("КоличествоГрузов", 1);		
		КонецЕсли;
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(сткВГХГруза.ВидГруза));	
	Иначе
		сткВГХГруза.Вставить("ИндексКартинки", Перечисления.упВидыГрузов.Индекс(Перечисления.упВидыГрузов.Груз));	
	КонецЕсли;
		
	Возврат сткВГХГруза;
	
КонецФункции

&НаКлиенте
Процедура НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	мсвВидыГрузов 	= ВозможныеВидыГрузов(Объект.ВидПеревозки);
	сткОтбор 		= Новый Структура("ВидГруза", мсвВидыГрузов);
	сткПараметры 	= Новый Структура("Отбор", сткОтбор);
 	ОткрытьФорму("Справочник.упТипыГрузов.ФормаВыбора", сткПараметры, Элемент);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВозможныеВидыГрузов(ВидПеревозки)
	
	спзРезультат = Новый СписокЗначений;
	спзРезультат.Добавить(Перечисления.упВидыГрузов.Груз);
	
	ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = Ложь;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест = упИнкассация.ИспользуетсяСпособУчетаИнкассируемыхЦенностейСТочностьюДоГрузовыхМест(ВидПеревозки);	
	КонецЕсли;
	
	Если ИспользуетсяИнкассацияСТочностьюДоГрузовыхМест Тогда
		спзРезультат.Добавить(Перечисления.упВидыГрузов.ИнкассируемаяЦенность);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упВедетсяУчетТары") Тогда
		спзРезультат.Добавить(Перечисления.упВидыГрузов.Тара);
	КонецЕсли; 
	
	Возврат спзРезультат;
	
КонецФункции

&НаКлиенте
Процедура ТипГрузаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораТипаГруза(Элемент, СтандартнаяОбработка)
КонецПроцедуры

&НаСервере 
Процедура ОпределитьЗоны(ТекущийОбъект)
	
	мсвАдреса = Новый Массив;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресОтправления) Тогда
		мсвАдреса.Добавить(ТекущийОбъект.АдресОтправления);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресПолучения) Тогда
		мсвАдреса.Добавить(ТекущийОбъект.АдресПолучения);
	КонецЕсли;
	
	Если мсвАдреса.Количество() > 0 Тогда
		ГеографическиеЗоныАдресов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(мсвАдреса, "ГеографическаяЗона");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресОтправления) Тогда
		ЗонаОтправления = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресПолучения);
		Если ЗначениеЗаполнено(ЗонаОтправления) Тогда
			ТекущийОбъект.ЗонаОтправления = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресОтправления);	
		КонецЕсли;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.АдресПолучения) Тогда
		ЗонаПолучения = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресПолучения);
		Если ЗначениеЗаполнено(ЗонаПолучения) Тогда
			ТекущийОбъект.ЗонаПолучения = ГеографическиеЗоныАдресов.Получить(ТекущийОбъект.АдресПолучения);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОпределитьЗоны();

#КонецОбласти 