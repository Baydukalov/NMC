
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текущий статус документа.
//
// Параметры:
//  Ссылка - ДокументСсылка.упЗапросУсловийПеревозки - Ссылка на документ.
//
// Возвращаемое значение:
//   ПеречислениеСсылка - Перечисления.упСтатусыЗапросаУсловийПеревозки.
//
Функция ПолучитьСтатус(Ссылка) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат Перечисления.упСтатусыЗапросаУсловийПеревозки.Новый;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	упСтатусыЗапросовУсловийПеревозокСрезПоследних.Статус,
	|	упЗапросУсловийПеревозки.ПометкаУдаления
	|ИЗ
	|	Документ.упЗапросУсловийПеревозки КАК упЗапросУсловийПеревозки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗапросовУсловийПеревозок.СрезПоследних(, Документ = &Документ) КАК упСтатусыЗапросовУсловийПеревозокСрезПоследних
	|		ПО упСтатусыЗапросовУсловийПеревозокСрезПоследних.Документ = упЗапросУсловийПеревозки.Ссылка
	|ГДЕ
	|	упЗапросУсловийПеревозки.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Статус) Тогда 
		Возврат Выборка.Статус;
	ИначеЕсли Выборка.ПометкаУдаления = Истина Тогда 	
		Возврат Перечисления.упСтатусыЗапросаУсловийПеревозки.Отменен;
	Иначе	
		Возврат Перечисления.упСтатусыЗапросаУсловийПеревозки.Новый;
	КонецЕсли;
	
КонецФункции		


////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидПеревозки");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
