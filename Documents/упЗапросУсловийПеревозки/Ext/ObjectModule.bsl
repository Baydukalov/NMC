#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если Ложь
		Или НЕ ЗначениеЗаполнено(ПорядокИсполненияЗаявки) 
		Или НЕ ЗначениеЗаполнено(ВидТранспортнойУслуги) 
	Тогда
		упУправленияЗаявками.ЗаполнитьПорядокИсполненияЗаявки(ЭтотОбъект);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("Статус") Тогда 
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСтатусыЗапросовУсловийПеревозок");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	РеквизитыОбъекта = Новый Структура();
	РеквизитыОбъекта.Вставить("Дата", ОпределитьПериодФормированияДвижений(ЧасовойПояс));

	упРаботаСоСтатусами.УстановкаСтатуса(ДополнительныеСвойства, Ссылка, Отказ);
	
	упЗапросУсловийПеревозки.СформироватьДвиженияСтатусЗапросаУсловийПеревозки(ДополнительныеСвойства, Ссылка, Движения, РеквизитыОбъекта, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция определяет дату формирования движений в зависимости от параметра ввода о грузовых местах
//
// Параметры:
//  ПараметрВводаГруза - ПеречислениеСсылка.упПараметрыВводаИнформацииОГрузовыхМестах
//
// Возвращаемое значение:
//   Дата
//
Функция ОпределитьПериодФормированияДвижений(ЧасовойПоясДокумента)
	
	Период = упСервисныеФункции.ПриведеннаяКЧасовомуПоясуДокументаДатаСеанса(ЧасовойПоясДокумента);

	Возврат Период;
	
КонецФункции

#КонецОбласти

#КонецЕсли 