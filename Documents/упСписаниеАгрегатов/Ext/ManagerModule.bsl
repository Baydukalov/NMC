#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура заполнения дополнительных свойств документа.
//
Процедура ИнициализироватьДанныеДокумента(Дата, ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументАгрегатов.Ссылка.Склад КАК Склад,
	|	ДокументАгрегатов.Агрегат КАК Агрегат
	|ПОМЕСТИТЬ втАгрегаты
	|ИЗ
	|	Документ.упСписаниеАгрегатов.Агрегаты КАК ДокументАгрегатов
	|ГДЕ
	|	ДокументАгрегатов.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упСписаниеАгрегатовДополнительноеОборудование.Ссылка.Склад,
	|	упСписаниеАгрегатовДополнительноеОборудование.ДополнительноеОборудование
	|ИЗ
	|	Документ.упСписаниеАгрегатов.ДополнительноеОборудование КАК упСписаниеАгрегатовДополнительноеОборудование
	|ГДЕ
	|	упСписаниеАгрегатовДополнительноеОборудование.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втАгрегаты.Склад КАК Склад,
	|	втАгрегаты.Агрегат КАК Агрегат,
	|	1 КАК Количество
	|ИЗ
	|	втАгрегаты КАК втАгрегаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	упВыработкаАгрегатов.Агрегат КАК Агрегат,
	|	упВыработкаАгрегатов.ПробегКмОстаток КАК ПробегКм,
	|	упВыработкаАгрегатов.ПробегМотоЧасыОстаток КАК ПробегМоточасы,
	|	упВыработкаАгрегатов.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.упВыработкаАгрегатов.Остатки(
	|			&Период,
	|			Агрегат В
	|				(ВЫБРАТЬ
	|					втАгрегаты.Агрегат
	|				ИЗ
	|					втАгрегаты КАК втАгрегаты)) КАК упВыработкаАгрегатов";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДополнительныеСвойства.Граница);
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = Результат.Количество();
	
	ДополнительныеСвойства.Вставить("ОстаткиАгрегатов", Результат[КоличествоТаблиц - 2].Выгрузить());
		
	Если Результат[КоличествоТаблиц - 1].Пустой() Тогда
		ДополнительныеСвойства.Вставить("ВыработкаАгрегатов", Неопределено);
	Иначе
		ДополнительныеСвойства.Вставить("ВыработкаАгрегатов", Результат[КоличествоТаблиц - 1].Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

// Процедура - выполнить контроль агрегатов.
//
Процедура ВыполнитьКонтрольАгрегатов(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ) Экспорт
	
	Если Отказ Тогда 
		Возврат; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упСписаниеАгрегатовАгрегаты.Ссылка.Склад КАК Склад,
	|	упСписаниеАгрегатовАгрегаты.Агрегат КАК Агрегат
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.упСписаниеАгрегатов.Агрегаты КАК упСписаниеАгрегатовАгрегаты
	|ГДЕ
	|	упСписаниеАгрегатовАгрегаты.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упСписаниеАгрегатовДополнительноеОборудование.Ссылка.Склад,
	|	упСписаниеАгрегатовДополнительноеОборудование.ДополнительноеОборудование
	|ИЗ
	|	Документ.упСписаниеАгрегатов.ДополнительноеОборудование КАК упСписаниеАгрегатовДополнительноеОборудование
	|ГДЕ
	|	упСписаниеАгрегатовДополнительноеОборудование.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Агрегат КАК Агрегат,
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.КоличествоОстаток, 0) КАК Количество
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.упАгрегатыНаСкладах.Остатки(
	|				&Период,
	|				(Склад, Агрегат) В
	|					(ВЫБРАТЬ
	|						втДокумент.Склад,
	|						втДокумент.Агрегат
	|					ИЗ
	|						втДокумент КАК втДокумент)) КАК упАгрегатыНаСкладахОстатки
	|		ПО втДокумент.Склад = упАгрегатыНаСкладахОстатки.Склад
	|			И втДокумент.Агрегат = упАгрегатыНаСкладахОстатки.Агрегат
	|ГДЕ
	|	ЕСТЬNULL(упАгрегатыНаСкладахОстатки.КоличествоОстаток, 0) < 1";
	Запрос.УстановитьПараметр("Период", ДополнительныеСвойства.Граница);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТипАгрегата = ?(ТипЗнч(Выборка.Агрегат) = Тип("СправочникСсылка.упШиныУзлыИАгрегаты"), "Агрегат", "Доп. оборудование");
		     ТекстОшибки = НСтр(СтрШаблон("ru = '%1 ""%2"" отсутствует в наличии на складе ""%3""'", ТипАгрегата, Выборка.Агрегат, Реквизиты.Склад));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);		
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
