
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(Агрегаты, "Агрегаты", Ссылка, Отказ);
	упУправлениеЗапасами.ТабличнаяЧастьНеСодержитДублей(ДополнительноеОборудование, "ДополнительноеОборудование", Ссылка, Отказ);
	
	Если Истина
		И НЕ Отказ 
		И РежимЗаписи <> РежимЗаписиДокумента.Запись  
	Тогда
		// Проверка на наличие более поздних документов изменяющих состояние агрегатов и доп. оборудования.
		мсвОборудование = Агрегаты.ВыгрузитьКолонку("Агрегат");
		Для каждого Строка Из ДополнительноеОборудование Цикл
			мсвОборудование.Добавить(Строка.ДополнительноеОборудование);
		КонецЦикла;
		упУчетТранспортныхСредств.ЭтоПоследнийДокументИзменявшийСостояниеОборудования(Ссылка, мсвОборудование, Дата, Отказ);
	КонецЕсли;

	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ДополнительныеСвойства.Вставить("Граница", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Включая));
	Документы.упСписаниеАгрегатов.ИнициализироватьДанныеДокумента(Дата, Ссылка, ДополнительныеСвойства);
		
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упАгрегатыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ОстаткиАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.упВыработкаАгрегатов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ОстаткиАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Агрегат", "Агрегат");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСостоянияОборудования");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ДополнительныеСвойства.ОстаткиАгрегатов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование", "Агрегат");
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,, Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка,,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Возврат;
	КонецПопытки;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Дата", Дата);
	Реквизиты.Вставить("Склад", Склад);	
	
	Документы.упСписаниеАгрегатов.ВыполнитьКонтрольАгрегатов(ДополнительныеСвойства, Ссылка, Реквизиты, Отказ);
	
	упУправлениеЗапасами.СформироватьДвиженияАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	упВыработкаРесурсов.СформироватьДвиженияВыработкаАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
	// СостоянияОборудования
	упУчетТранспортныхСредств.СформироватьДвиженияСостоянияОборудованияСписаниеАгрегатов(ДополнительныеСвойства, Ссылка, Движения, Реквизиты, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли