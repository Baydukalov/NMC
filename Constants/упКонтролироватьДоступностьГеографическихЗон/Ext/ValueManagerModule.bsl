
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Значение И Не Константы.упКонтролироватьДоступностьГеографическихЗон.Получить() Тогда 
		ЗаписатьПравилаДоступности();
	КонецЕсли;
	
КонецПроцедуры

// Процедура записывает информацию о правилах доступности
// 
// Параметры:
//  Нет.
//
Процедура ЗаписатьПравилаДоступности()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка КАК Зона,
	|	упПравилаДоступностиГеографическихЗон.Ссылка КАК Правила,
	|	упПравилаДоступностиГеографическихЗон.ЗапрещенаРаботаВУказанныхЗонах
	|ПОМЕСТИТЬ втЗоныДляПерезаписи
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны,
	|	Справочник.упПравилаДоступностиГеографическихЗон КАК упПравилаДоступностиГеографическихЗон
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правила,
	|	Зона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПравилаДоступностиГеографическихЗонГеографическиеЗоны.Ссылка КАК Правила,
	|	упИерархияГеографическихЗон.Зона КАК Зона
	|ПОМЕСТИТЬ втЗоныПравил
	|ИЗ
	|	Справочник.упПравилаДоступностиГеографическихЗон.ГеографическиеЗоны КАК упПравилаДоступностиГеографическихЗонГеографическиеЗоны
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	|		ПО упПравилаДоступностиГеографическихЗонГеографическиеЗоны.ГеографическаяЗона = упИерархияГеографическихЗон.Родитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правила,
	|	Зона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗоныДляПерезаписи.Правила,
	|	втЗоныДляПерезаписи.Зона КАК Зона
	|ИЗ
	|	втЗоныДляПерезаписи КАК втЗоныДляПерезаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗоныПравил КАК втЗоныПравил
	|		ПО втЗоныДляПерезаписи.Зона = втЗоныПравил.Зона
	|			И втЗоныДляПерезаписи.Правила = втЗоныПравил.Правила
	|ГДЕ
	|	(втЗоныДляПерезаписи.ЗапрещенаРаботаВУказанныхЗонах
	|				И втЗоныПравил.Зона ЕСТЬ NULL 
	|			ИЛИ НЕ втЗоныДляПерезаписи.ЗапрещенаРаботаВУказанныхЗонах
	|				И НЕ втЗоныПравил.Зона ЕСТЬ NULL )
	|ИТОГИ ПО
	|	Зона";
	
	ВыборкаЗоны = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗоны.Следующий() Цикл 
		НаборЗаписей = РегистрыСведений.упДоступныеГеографическиеЗоны.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ГеографическаяЗона.Установить(ВыборкаЗоны.Зона);
		
		Выборка = ВыборкаЗоны.Выбрать();
		Пока Выборка.Следующий() Цикл 
			Запись = НаборЗаписей.Добавить();
			Запись.ПравилаДоступности = Выборка.Правила;
			Запись.ГеографическаяЗона = Выборка.Зона;
		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;	
	
КонецПроцедуры // ЗаписатьПравилаДоступности()
