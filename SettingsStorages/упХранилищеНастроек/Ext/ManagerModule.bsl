
#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обработчик чтения варианта настроек.
//
// Параметры:
//   КлючНастройки - Строка - Полное имя настройки с точкой.
//   КлючВарианта - Строка - Ключ варианта настройки.
//   Настройки - Произвольный - Настройки вариант.
//   ОписаниеНастроек - ОписаниеНастроек - Описание.
//   Пользователь - Строка - Имя пользователя ИБ.
//
Процедура ОбработкаЗагрузки(КлючНастройки, КлючВарианта, Настройки, ОписаниеНастроек, Пользователь)
	
	Если Не ПравоЧтения() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	упВариантыНастроек.Наименование,
	|	упВариантыНастроек.Настройки
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Объект = &Объект
	|	И упВариантыНастроек.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("Объект",        КлючНастройки);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ОписаниеНастроек = Неопределено Тогда
			ОписаниеНастроек = Новый ОписаниеНастроек;
			ОписаниеНастроек.КлючОбъекта  = КлючНастройки;
			ОписаниеНастроек.КлючНастроек = КлючВарианта;
			ОписаниеНастроек.Пользователь = Пользователь;
		КонецЕсли;
		ОписаниеНастроек.Представление = Выборка.Наименование;
		Настройки = Выборка.Настройки.Получить();
	КонецЕсли;
	
КонецПроцедуры

// Обработчик записи варианта настроек.
//
// Параметры:
//   КлючНастройки - Строка - Полное имя настройки с точкой.
//   КлючВарианта - Строка - Ключ варианта настройки.
//   Настройки - Произвольный - Настройки вариант.
//   ОписаниеНастроек - ОписаниеНастроек - Описание.
//   Пользователь - Строка - Имя пользователя ИБ.
//
Процедура ОбработкаСохранения(КлючНастройки, КлючВарианта, Настройки, ОписаниеНастроек, Пользователь)
	
	Если Не ПравоСохранения() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для сохранения вариантов отчетов'");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.Ссылка
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.Объект = &Объект
	|	И упВариантыНастроек.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("Объект",       КлючНастройки);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ТипЗнч(Настройки) = Тип("Структура") И Настройки.Свойство("ВидПеревозки") Тогда
			ВариантОбъект.ВидПеревозки = Настройки.ВидПеревозки;
			Настройки.Удалить("ВидПеревозки");
		КонецЕсли; 
		ВариантОбъект.Настройки = Новый ХранилищеЗначения(Настройки);
		Если ОписаниеНастроек <> Неопределено Тогда
			ВариантОбъект.Наименование = ОписаниеНастроек.Представление;
		КонецЕсли;
		ВариантОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Возвращает Истина если у пользователя есть право на сохранение вариантов настроек.
//
// Параметры:
//   Нет.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ПравоСохранения()
	Возврат ПравоДоступа("СохранениеДанныхПользователя", Метаданные) И ПравоДоступа("Добавление", Метаданные.Справочники.упВариантыНастроек);
КонецФункции

// Возвращает Истина если у пользователя есть право чтения вариантов настроек.
//
// Параметры:
//   Нет.
//
// Возвращаемое значение:
//   Булево - Результат выполнения.
//
Функция ПравоЧтения()
	Возврат ПравоДоступа("Чтение", Метаданные.Справочники.упВариантыНастроек);
КонецФункции

#КонецОбласти 
