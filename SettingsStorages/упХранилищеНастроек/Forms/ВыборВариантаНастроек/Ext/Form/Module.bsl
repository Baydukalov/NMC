
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	КлючВарианта        = Параметры.КлючТекущихНастроек;
	КлючНастройки       = Параметры.КлючОбъекта;
	ВидПеревозки        = Параметры.ВидПеревозки;
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	НастройкаИнформация = Новый Структура("Объект, ТипОтчета, НастройкаПолноеИмя, НастройкаИмя");
	НастройкаИнформация.НастройкаПолноеИмя = КлючНастройки;
	
	ПозицияТочки = Найти(КлючНастройки, ".");
	Если ПозицияТочки = 0 Тогда
		Префикс = "";
		НастройкаИнформация.НастройкаИмя = КлючВарианта;
	Иначе
		Префикс = Лев(КлючНастройки, ПозицияТочки - 1);
		НастройкаИнформация.НастройкаИмя = Сред(КлючНастройки, ПозицияТочки + 1);
	КонецЕсли;
	НастройкаИнформация.Объект = КлючНастройки;
	НастройкаИнформация = Новый ФиксированнаяСтруктура(НастройкаИнформация);
	
	ПолныеПраваНаВариантыНастроек = упВариантыНастроек.ПолныеПраваНаВариантыНастроек();
	
	ГруппаДерева = ДеревоВариантовНастроек.ПолучитьЭлементы().Добавить();
	ГруппаДерева.НомерГруппы = 1;
	ГруппаДерева.Наименование = НСтр("ru = 'Используемые'");
	ГруппаДерева.ИндексКартинки = 0;
	ГруппаДерева.КартинкаАвтора = -1;
	
	ГруппаДерева = ДеревоВариантовНастроек.ПолучитьЭлементы().Добавить();
	ГруппаДерева.НомерГруппы = 2;
	ГруппаДерева.Наименование = НСтр("ru = 'Не используемые'");
	ГруппаДерева.ИндексКартинки = 0;
	ГруппаДерева.КартинкаАвтора = -1;
	
	ГруппаДерева = ДеревоВариантовНастроек.ПолучитьЭлементы().Добавить();
	ГруппаДерева.НомерГруппы = 3;
	ГруппаДерева.Наименование = НСтр("ru = 'Помеченные на удаление'");
	ГруппаДерева.ИндексКартинки = 1;
	ГруппаДерева.КартинкаАвтора = -1;
	
	Если НЕ ПолныеПраваНаВариантыНастроек Тогда
		Элементы.ПоказыватьЛичныеВариантыНастроекДругихАвторов.Видимость = Ложь;
		Элементы.ПоказыватьЛичныеВариантыНастроекДругихАвторовКМ.Видимость = Ложь;
		ПоказыватьЛичныеВариантыНастроекДругихАвторов = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокВариантов();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Показывать = Настройки.Получить("ПоказыватьЛичныеВариантыНастроекДругихАвторов");
	Если Показывать <> ПоказыватьЛичныеВариантыНастроекДругихАвторов Тогда
		ПоказыватьЛичныеВариантыНастроекДругихАвторов = Показывать;
		Элементы.ПоказыватьЛичныеВариантыНастроекДругихАвторов.Пометка = Показывать;
		Элементы.ПоказыватьЛичныеВариантыНастроекДругихАвторовКМ.Пометка = Показывать;
		ЗаполнитьСписокВариантов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта() Тогда
		ЗаполнитьСписокВариантов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборАвторПриИзменении(Элемент)
	
	ОтборВключен = ЗначениеЗаполнено(ОтборАвтор);
	
	Группы = ДеревоВариантовНастроек.ПолучитьЭлементы();
	Для Каждого Группа Из Группы Цикл
		ВложенныеВарианты = Группа.ПолучитьЭлементы();
		Для Каждого Вариант Из ВложенныеВарианты Цикл
			Вариант.СкрытОтбором = ОтборВключен И Вариант.Автор <> ОтборАвтор;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекПриАктивизацииСтроки(Элемент)
	
	Вариант = Элементы.ДеревоВариантовНастроек.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		ВариантОписание = "";
	Иначе
		ВариантОписание = Вариант.Описание;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьВариантДляИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Вариант = Элементы.ДеревоВариантовНастроек.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		Возврат;
	КонецЕсли;
	
	Если Вариант.ИндексКартинки = 4 Тогда
		ТекстВопроса = НСтр("ru = 'Снять с ""%1"" пометку на удаление?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
	КонецЕсли;
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", Вариант.Наименование);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Вариант", Вариант);
	Обработчик = Новый ОписаниеОповещения("ДеревоВариантовНастроекПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ПоказыватьЛичныеВариантыНастроекДругихАвторов(Команда)
	
	ПоказыватьЛичныеВариантыОтчетовДругихАвторов = НЕ ПоказыватьЛичныеВариантыОтчетовДругихАвторов;
	Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторов.Пометка = ПоказыватьЛичныеВариантыОтчетовДругихАвторов;
	Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторовКМ.Пометка = ПоказыватьЛичныеВариантыОтчетовДругихАвторов;
	
	ЗаполнитьСписокВариантов();
	
	Для Каждого ГруппаДерева Из ДеревоВариантовНастроек.ПолучитьЭлементы() Цикл
		Если ГруппаДерева.СкрытОтбором = Ложь Тогда
			Элементы.ДеревоВариантовНастроек.Развернуть(ГруппаДерева.ПолучитьИдентификатор(), Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСписокВариантов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	Вариант = Элементы.ДеревоВариантовНастроек.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КлючВарианта", Вариант.КлючВарианта);
	Если Вариант.ИндексКартинки = 4 Тогда
		ТекстВопроса = НСтр("ru = 'Выбранный вариант отчета помечен на удаление.
		|Выбрать этот варианта отчета?'");
		Обработчик = Новый ОписаниеОповещения("ВыбратьИЗакрытьЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	Иначе
		ВыбратьИЗакрытьЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрытьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Закрыть(Новый ВыборНастроек(ДополнительныеПараметры.КлючВарианта));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантДляИзменения()
	
	Вариант = Элементы.ДеревоВариантовНастроек.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ПравоИзмененияВарианта(Вариант, ПолныеПраваНаВариантыНастроек) Тогда
		ТекстПредупреждения = НСтр("ru = 'Недостаточно прав доступа для изменения варианта ""%1"".'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%1", Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, Вариант.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовНастроекПередУдалениемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьВариантНаСервере(ДополнительныеПараметры.Вариант.Ссылка, ДополнительныеПараметры.Вариант.ИндексКартинки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПравоИзмененияВарианта(Вариант, ПолныеПраваНаВарианты)
	
	Возврат ПолныеПраваНаВарианты ИЛИ Вариант.АвторТекущийПользователь;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВариантов()
	
	ТекущийКлючВарианта = КлючВарианта;
	Если ЗначениеЗаполнено(Элементы.ДеревоВариантовНастроек.ТекущаяСтрока) Тогда
		ТекущаяСтрокаДерева = ДеревоВариантовНастроек.НайтиПоИдентификатору(Элементы.ДеревоВариантовНастроек.ТекущаяСтрока);
		Если ЗначениеЗаполнено(ТекущаяСтрокаДерева.КлючВарианта) Тогда
			ТекущийКлючВарианта = ТекущаяСтрокаДерева.КлючВарианта;
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.Ссылка,
	|	упВариантыНастроек.Наименование,
	|	упВариантыНастроек.КлючВарианта,
	|	ВЫРАЗИТЬ(упВариантыНастроек.Описание КАК СТРОКА(200)) КАК Описание,
	|	упВариантыНастроек.Автор,
	|	упВариантыНастроек.ТолькоДляАвтора,
	|	упВариантыНастроек.ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА упВариантыНастроек.ПометкаУдаления = ИСТИНА
	|			ТОГДА 3
	|		КОГДА упВариантыНастроек.Автор = &ТекущийПользователь
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК НомерГруппы,
	|	ВЫБОР
	|		КОГДА упВариантыНастроек.ПометкаУдаления
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА упВариантыНастроек.Автор = &ТекущийПользователь
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АвторТекущийПользователь
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|	упВариантыНастроек.ВидПеревозки = &ВидПеревозки
	|	И упВариантыНастроек.Объект = &Объект
	|	И (упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь)";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидПеревозки"       , ВидПеревозки);
	Запрос.УстановитьПараметр("Объект"             , НастройкаИнформация.Объект);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	
	Если ПоказыватьЛичныеВариантыНастроекДругихАвторов Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
		|			ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь)", "");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаВариантов = Запрос.Выполнить().Выгрузить();

	// Вывод вариантов из таблицы в дерево.
	Для Каждого ГруппаДерева Из ДеревоВариантовНастроек.ПолучитьЭлементы() Цикл
		ВариантыГруппы = ТаблицаВариантов.Скопировать(Новый Структура("НомерГруппы", ГруппаДерева.НомерГруппы));
		ВариантыГруппы.Сортировать("Наименование ВОЗР");
		ГруппаДерева.СкрытОтбором = (ВариантыГруппы.Количество() = 0);
		ГруппаДереваНаборСтрок = ГруппаДерева.ПолучитьЭлементы();
		ГруппаДереваНаборСтрок.Очистить();
		Для Каждого СтрокаТаблицы Из ВариантыГруппы Цикл
			Вариант = ГруппаДереваНаборСтрок.Добавить();
			ЗаполнитьЗначенияСвойств(Вариант, СтрокаТаблицы);
			Если Вариант.КлючВарианта = ТекущийКлючВарианта Тогда
				Элементы.ДеревоВариантовНастроек.ТекущаяСтрока = Вариант.ПолучитьИдентификатор();
			КонецЕсли;
			Вариант.КартинкаАвтора = ?(Вариант.ТолькоДляАвтора, -1, 0);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьВариантНаСервере(Ссылка, ИндексКартинки)
	
	ВариантОбъект = Ссылка.ПолучитьОбъект();
	ПометкаУдаления = НЕ ВариантОбъект.ПометкаУдаления;
	Пользовательский = ВариантОбъект.Пользовательский;
	ВариантОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
	ИндексКартинки = ?(ПометкаУдаления, 4, 3);
	
КонецПроцедуры

#КонецОбласти 




