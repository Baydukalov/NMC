
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КлючНастройки = Параметры.КлючОбъекта;
	ПрототипКлюч = Параметры.КлючТекущихНастроек;
	ВидПеревозки = Параметры.ВидПеревозки;
	
	НастройкаИнформация = Новый Структура("Объект, ТипОтчета, НастройкаПолноеИмя, НастройкаИмя");
	НастройкаИнформация.НастройкаПолноеИмя = КлючНастройки;
	
	ПозицияТочки = Найти(КлючНастройки, ".");
	Если ПозицияТочки = 0 Тогда
		Префикс = "";
		НастройкаИнформация.НастройкаИмя = КлючНастройки;
	Иначе
		Префикс = Лев(КлючНастройки, ПозицияТочки - 1);
		НастройкаИнформация.НастройкаИмя = Сред(КлючНастройки, ПозицияТочки + 1);
	КонецЕсли;
	НастройкаИнформация.Объект = КлючНастройки;
	НастройкаИнформация = Новый ФиксированнаяСтруктура(НастройкаИнформация);
	
	ПолныеПраваНаВариантыНастроек = упВариантыНастроек.ПолныеПраваНаВариантыНастроек();
	
	Если НЕ ПолныеПраваНаВариантыНастроек Тогда
		Элементы.Доступен.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ЗаполнитьСписокВариантов();
	ВариантыНастроекОбработчикАктивизацииСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(ВариантНаименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Поле ""Наименование"" не заполнено'"),
			,
			"Наименование");
		Отказ = Истина;
	ИначеЕсли упВариантыНастроек.НаименованиеЗанято(НастройкаИнформация.Объект, ВариантСсылка, ВариантНаименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '""%1"" занято, необходимо указать другое Наименование.'"),
				ВариантНаименование
			),
			,
			"Наименование");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ИмяФормы Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеВариантаНастроек" И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Ссылка") Тогда
		
		Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("Ссылка", Параметр.Ссылка));
		Если Найденные.Количество() = 1 Тогда
			Вариант = Найденные[0];
			ЗаполнитьЗначенияСвойств(Вариант, Параметр);
			Вариант.АвторТекущийПользователь = (Вариант.Автор = ТекущийПользователь);
			ВариантыНастроекОбработчикАктивизацииСтроки(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.Наименование;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("Наименование", ВариантНаименование));
	Если Найденные.Количество() > 0 Тогда
		Элементы.ВариантыНастроек.ТекущаяСтрока = Найденные[0].ПолучитьИдентификатор();
		ВариантыНастроекОбработчикАктивизацииСтроки(ЭтотОбъект);
	Иначе
		ВариантСсылка = Неопределено;
		УстановитьЧтоБудетДальше(ЭтотОбъект, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("Наименование", Текст));
	УстановитьЧтоБудетДальше(ЭтотОбъект, Найденные.Количество() > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступенПриИзменении(Элемент)
	
	ВариантТолькоДляАвтора = (Доступен = "1");

КонецПроцедуры

&НаКлиенте
Процедура ВариантыНастроекПриАктивизацииСтроки(Элемент)
	
	Если ИдентификаторТекущейСтроки = Элементы.ВариантыНастроек.ТекущаяСтрока Тогда
		Возврат;
	КонецЕсли;
	ВариантыНастроекОбработчикАктивизацииСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьВариантДляИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыНастроекПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	Вариант = Элементы.ВариантыНастроек.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПолныеПраваНаВариантыНастроек И НЕ Вариант.АвторТекущийПользователь Тогда
		ТекстПредупреждения = НСтр("ru = 'Недостаточно прав доступа для удаления варианта настроек ""%1"".'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%1", Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Вариант.ПометкаУдаления Тогда
		ТекстВопроса = НСтр("ru = 'Снять с ""%1"" пометку на удаление?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
	КонецЕсли;
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", Вариант.Наименование);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Вариант", Вариант);
    Обработчик = Новый ОписаниеОповещения("ВариантыНастроекПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыНастроекПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ОписаниеНачалоВыбораЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(Оповещение, Элементы.Описание.ТекстРедактирования, 
		НСтр("ru = 'Описание'"));

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПакет(Результат, Пакет) Экспорт
	
	Если Не Пакет.Свойство("ВариантЭтоНовый") Тогда
		Пакет.Вставить("ВариантЭтоНовый", НЕ ЗначениеЗаполнено(ВариантСсылка));
	КонецЕсли;
	
	// Обработка результата предыдущего шага.
	Если Пакет.ТекущийШаг = "ВопросНаПерезапись" Тогда
		Пакет.ТекущийШаг = Неопределено;
		Если Результат = КодВозвратаДиалога.Да Тогда
			Пакет.Вставить("ВопросНаПерезаписьПройден", Истина);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Пакет.ПроверитьДанные = Истина Тогда
		// Наименование не введено.
		Если Не ЗначениеЗаполнено(ВариантНаименование) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Наименование"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
			Возврат;
		КонецЕсли;
		
		// Введено наименование существующего варианта настройки.
		Если Не Пакет.ВариантЭтоНовый Тогда
			Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("Ссылка", ВариантСсылка));
			Вариант = Найденные[0];
			Если НЕ ПолныеПраваНаВариантыНастроек Тогда
				ТекстОшибки = НСтр("ru = 'Недостаточно прав для изменения варианта ""%1"", необходимо выбрать другой вариант или изменить Наименование.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВариантНаименование);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
				Возврат;
			КонецЕсли;
			
			Если Не Пакет.Свойство("ВопросНаПерезаписьПройден") Тогда
				Если Вариант.ПометкаУдаления = Истина Тогда
					ТекстВопроса = НСтр("ru = 'Вариант настроек ""%1"" помечен на удаление.
					|Заменить помеченный на удаление вариант настроек?'");
					КнопкаПоУмолчанию = КодВозвратаДиалога.Нет;
				Иначе
					ТекстВопроса = НСтр("ru = 'Заменить ранее сохраненный вариант настроек ""%1""?'");
					КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
				КонецЕсли;
				ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ВариантНаименование);
				Пакет.ТекущийШаг = "ВопросНаПерезапись";
				Обработчик = Новый ОписаниеОповещения("ВыполнитьПакет", ЭтотОбъект, Пакет);
				ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КнопкаПоУмолчанию);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка завершена.
		Пакет.ПроверитьДанные = Ложь;
	КонецЕсли;
	
	Если Пакет.ПроверитьИЗаписатьСервер = Истина Тогда
		
		ВыполнитьПакетСервер(Пакет);
		Если Пакет.Отказ = Истина Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Пакет.ЗакрытьПослеЗаписи = Истина Тогда
		
		Оповестить("ИзменениеВариантаНастроек", Неопределено, ИмяФормы);
		Закрыть(Новый ВыборНастроек(ВариантКлючВарианта));
		Пакет.ЗакрытьПослеЗаписи = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантДляИзменения()
	
	Вариант = Элементы.ВариантыНастроек.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ПравоНастройкиВарианта(Вариант, ПолныеПраваНаВариантыНастроек) Тогда
		ТекстПредупреждения = НСтр("ru = 'Недостаточно прав доступа для изменения варианта ""%1"".'");
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, Вариант.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Пакет = Новый Структура;
	Пакет.Вставить("ПроверитьДанные"         , Истина);
	Пакет.Вставить("ПроверитьИЗаписатьСервер", Истина);
	Пакет.Вставить("ЗакрытьПослеЗаписи"      , Истина);
	Пакет.Вставить("ТекущийШаг"              , Неопределено);
	
	ВыполнитьПакет(Неопределено, Пакет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыНастроекПередУдалениемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Вариант = ДополнительныеПараметры.Вариант;
		УдалитьВариантНаСервере(Вариант.Ссылка, Вариант.ИндексКартинки, Вариант.ПометкаУдаления);
		ВариантыНастроекОбработчикАктивизацииСтроки(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВариантыНастроекОбработчикАктивизацииСтроки(Форма)
	
	Форма.ИдентификаторТекущейСтроки = Форма.Элементы.ВариантыНастроек.ТекущаяСтрока;
	
	Вариант = Форма.ВариантыНастроек.НайтиПоИдентификатору(Форма.ИдентификаторТекущейСтроки);
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПравоНастройкиВарианта = ПравоНастройкиВарианта(Вариант, Форма.ПолныеПраваНаВариантыНастроек);
	Если ПравоНастройкиВарианта Тогда
		Форма.ВариантСсылка = Вариант.Ссылка;
		Форма.ВариантАвтор  = Вариант.Автор;
		Форма.ВариантНаименование = Вариант.Наименование;
		Форма.ВариантОписание     = Вариант.Описание;
		Форма.ВариантТолькоДляАвтора      = Вариант.ТолькоДляАвтора;
	Иначе
		Форма.ВариантСсылка = Неопределено;
		Форма.ВариантАвтор  = Форма.ТекущийПользователь;
		Форма.ВариантНаименование = СформироватьСвободноеНаименование(Вариант, Форма.ВариантыНастроек);
		Форма.ВариантОписание     = "";
		Форма.ВариантТолькоДляАвтора      = Истина;
	КонецЕсли;
	
	Форма.Доступен = ?(Форма.ВариантТолькоДляАвтора, "1", "2");
	
	УстановитьЧтоБудетДальше(Форма, ЗначениеЗаполнено(Форма.ВариантСсылка));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПравоНастройкиВарианта(Вариант, ПолныеПраваНаВариантыНастроек)
	Возврат (ПолныеПраваНаВариантыНастроек ИЛИ Вариант.АвторТекущийПользователь) И ЗначениеЗаполнено(Вариант.Ссылка);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьСвободноеНаименование(Вариант, ВариантыНастроек)
	
	ШаблонИмениВарианта = СокрЛП(Вариант.Наименование) +" - "+ НСтр("ru = 'копия'");
	
	СвободноеНаименование = ШаблонИмениВарианта;
	Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("Наименование", СвободноеНаименование));
	Если Найденные.Количество() = 0 Тогда
		Возврат СвободноеНаименование;
	КонецЕсли;
	
	НомерВарианта = 1;
	Пока Истина Цикл
		НомерВарианта = НомерВарианта + 1;
		СвободноеНаименование = ШаблонИмениВарианта +" (" + Формат(НомерВарианта, "") + ")";
		Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("Наименование", СвободноеНаименование));
		Если Найденные.Количество() = 0 Тогда
			Возврат СвободноеНаименование;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЧтоБудетДальше(Форма, Перезапись)
	
	Если Перезапись Тогда
		Форма.Элементы.ПерезаписьИлиНовый.ТекущаяСтраница = Форма.Элементы.Перезапись;
	Иначе
		Форма.Элементы.ПерезаписьИлиНовый.ТекущаяСтраница = Форма.Элементы.Новый;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПакетСервер(Пакет)
	
	Пакет.Вставить("Отказ", Ложь);
	
	Если Пакет.ПроверитьИЗаписатьСервер = Истина Тогда
		ПроверитьИЗаписатьНаСервере(Пакет);
		Пакет.ПроверитьИЗаписатьСервер = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьВариантНаСервере(Ссылка, ИндексКартинки, ПометкаУдаления)
	
	ВариантОбъект = Ссылка.ПолучитьОбъект();
	ВариантОбъект.УстановитьПометкуУдаления(НЕ ВариантОбъект.ПометкаУдаления);
	ПометкаУдаления = ВариантОбъект.ПометкаУдаления;
	ИндексКартинки = ?(ПометкаУдаления, 4, 3);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьИЗаписатьНаСервере(Пакет)
	
	ВариантЭтоНовый = НЕ ЗначениеЗаполнено(ВариантСсылка);
	
	Если ВариантЭтоНовый И упВариантыНастроек.НаименованиеЗанято(НастройкаИнформация.Объект, ВариантСсылка, ВариантНаименование) Тогда
		ТекстОшибки = НСтр("ru = '""%1"" занято, необходимо указать другое Наименование.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВариантНаименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ВариантНаименование");
		Пакет.Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ВариантЭтоНовый Тогда
		ВариантОбъект = Справочники.упВариантыНастроек.СоздатьЭлемент();
		ВариантОбъект.Объект           = НастройкаИнформация.Объект;
		ВариантАвтор                   = ТекущийПользователь;
		ВариантОбъект.КлючВарианта     = Строка(Новый УникальныйИдентификатор());
	Иначе
		ВариантОбъект = ВариантСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	ВариантОбъект.Автор           = ВариантАвтор;
	ВариантОбъект.ВидПеревозки    = ВидПеревозки;
	ВариантОбъект.Наименование    = ВариантНаименование;
	ВариантОбъект.Описание        = ВариантОписание;
	ВариантОбъект.ТолькоДляАвтора = ВариантТолькоДляАвтора;
	
	ВариантОбъект.Записать();
	
	ВариантСсылка       = ВариантОбъект.Ссылка;
	ВариантКлючВарианта = ВариантОбъект.КлючВарианта;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВариантов()
	
	ТекущийКлючВарианта = ПрототипКлюч;
	Если ЗначениеЗаполнено(Элементы.ВариантыНастроек.ТекущаяСтрока) Тогда
		ТекущаяСтрока = ВариантыНастроек.НайтиПоИдентификатору(Элементы.ВариантыОтчета.ТекущаяСтрока);
		Если ЗначениеЗаполнено(ТекущаяСтрока.КлючВарианта) Тогда
			ТекущийКлючВарианта = ТекущаяСтрока.КлючВарианта;
		КонецЕсли;
	КонецЕсли;
	
	ВариантыНастроек.Очистить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упВариантыНастроек.Ссылка КАК Ссылка,
	|	упВариантыНастроек.Наименование КАК Наименование,
	|	упВариантыНастроек.Автор КАК Автор,
	|	упВариантыНастроек.Описание КАК Описание,
	|	упВариантыНастроек.КлючВарианта КАК КлючВарианта,
	|	упВариантыНастроек.ТолькоДляАвтора КАК ТолькоДляАвтора,
	|	упВариантыНастроек.ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА упВариантыНастроек.Автор = &ТекущийПользователь
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АвторТекущийПользователь,
	|	ВЫБОР
	|		КОГДА упВариантыНастроек.ПометкаУдаления
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА упВариантыНастроек.ПометкаУдаления
	|			ТОГДА 3
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.упВариантыНастроек КАК упВариантыНастроек
	|ГДЕ
	|   упВариантыНастроек.ВидПеревозки = &ВидПеревозки
	|	И упВариантыНастроек.Объект = &Объект
	|	И НЕ упВариантыНастроек.ПометкаУдаления
	|	И (упВариантыНастроек.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ упВариантыНастроек.Автор = &ТекущийПользователь
	|			ИЛИ упВариантыНастроек.КлючВарианта = &ТекущийКлючВарианта)";
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидПеревозки"       , ВидПеревозки);
	Запрос.УстановитьПараметр("Объект"             , НастройкаИнформация.Объект);
	Запрос.УстановитьПараметр("ТекущийКлючВарианта", ТекущийКлючВарианта);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	ВариантыНастроек.Загрузить(ТаблицаЗначений);
	
	ВариантыНастроек.Сортировать("Наименование Возр");
	
	ИдентификаторТекущейСтроки = -1;
	Найденные = ВариантыНастроек.НайтиСтроки(Новый Структура("КлючВарианта", ТекущийКлючВарианта));
	Если Найденные.Количество() > 0 Тогда
		Вариант = Найденные[0];
		ПрототипСсылка = Вариант.Ссылка;
		ВариантОписание = Вариант.Описание;
		ИдентификаторТекущейСтроки = Вариант.ПолучитьИдентификатор();
		Элементы.ВариантыНастроек.ТекущаяСтрока = ИдентификаторТекущейСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеНачалоВыбораЗавершение(Знач ВведенныйТекст, Знач ДополнительныеПараметры) Экспорт
	
	Если ВведенныйТекст = Неопределено Тогда
		Возврат;
	КонецЕсли;	

	ВариантОписание = ВведенныйТекст;
	
КонецПроцедуры

#КонецОбласти 


