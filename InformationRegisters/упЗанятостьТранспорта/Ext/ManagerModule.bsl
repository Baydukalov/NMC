#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура выполняет проверку на пересечение периодов занятости транспортного средства,
//  после проведения документа.
//
// Параметры:
//  ДокументОбъект		 - ДокументОбъект.упРейс					 - ДокументОбъект.упРемонтИТОТС, объект документа.
//  ТранспортноеСредство	 - СправочникСсылка.упТранспортныеСредства	 - транспортное средство.
//  Отказ				 - Булево								 - Признак проведения документа.
//  ПроверятьТолькоФакт	 - Булево								 - проверять только факт.
//
Процедура ВыполнитьКонтрольРезультатовПроведения(ДокументОбъект, ТранспортноеСредство, Отказ, ПроверятьТолькоФакт = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	упРасписаниеРаботыТС.Документ КАК Документ,
|	упРасписаниеРаботыТС.ДатаНачала КАК ДатаНачала,
|	ДОБАВИТЬКДАТЕ(упРасписаниеРаботыТС.ДатаОкончания, СЕКУНДА, упРасписаниеРаботыТС.Транспорт.ПерерывМеждуРейсами * 3600) КАК ДатаОкончания,
|	ЛОЖЬ КАК ЭтоФакт
|ПОМЕСТИТЬ втПериодыДоступности
|ИЗ
|	РегистрСведений.упЗанятостьТранспорта.СрезПоследних КАК упРасписаниеРаботыТС
|ГДЕ
|	упРасписаниеРаботыТС.Транспорт = &ТранспортноеСредство И &ПроверятьТолькоФакт <> ИСТИНА
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	упРасписаниеРаботыТС.ДокументОснование,
|	упРасписаниеРаботыТС.ДатаНачала,
|	упРасписаниеРаботыТС.ДатаОкончания,
|	ИСТИНА
|ИЗ
|	РегистрСведений.упРасписаниеРаботыТС КАК упРасписаниеРаботыТС
|ГДЕ
|	упРасписаниеРаботыТС.ТранспортноеСредство = &ТранспортноеСредство
|
|ИНДЕКСИРОВАТЬ ПО
|	упРасписаниеРаботыТС.Документ
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ПД1.Документ КАК Документ1,
|	ПД2.Документ КАК Документ2
|ИЗ
|	втПериодыДоступности КАК ПД1
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериодыДоступности КАК ПД2
|		ПО (НЕ ПД1.Документ = ПД2.Документ)
|			И (ПД1.Документ = &Документ)
|			И (ВЫБОР
|				КОГДА ПД1.ДатаНачала МЕЖДУ ПД2.ДатаНачала И ПД2.ДатаОкончания
|					ТОГДА ИСТИНА
|				КОГДА ПД1.ДатаОкончания МЕЖДУ ПД2.ДатаНачала И ПД2.ДатаОкончания
|					ТОГДА ИСТИНА
|				КОГДА ПД2.ДатаНачала МЕЖДУ ПД1.ДатаНачала И ПД1.ДатаОкончания
|					ТОГДА ИСТИНА
|				КОГДА ПД2.ДатаОкончания МЕЖДУ ПД1.ДатаНачала И ПД1.ДатаОкончания
|					ТОГДА ИСТИНА
|				ИНАЧЕ ЛОЖЬ
|			КОНЕЦ)
|
|УПОРЯДОЧИТЬ ПО
|	ПД2.ЭтоФакт УБЫВ";
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Документ", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПроверятьТолькоФакт", ПроверятьТолькоФакт);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ТекстСообщения = НСтр(СтрШаблон("ru = 'Транспортное средство ""%1"" занято на период документа документом %3. Проведение не выполнено. %2'",
			Строка(ТранспортноеСредство), Строка(ДокументОбъект), Выборка.Документ2));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтрольРезультатовПроведения()

#КонецОбласти 

#КонецЕсли
