
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Не ОбменДанными.Загрузка Тогда
		текДата = ТекущаяДата();
		ТипМаршрутизатора = Константы.упТипМаршрутизатора.Получить();
		УчитыватьКоэффициентыРасчетаВремениВПути = ПолучитьФункциональнуюОпцию("упУчитыватьКоэффициентыРасчетаВремениВПути");
		Для Каждого СтрокаНабора Из ЭтотОбъект Цикл 
			СтрокаНабора.ДатаИзменения = текДата;
			Если Истина
				И СтрокаНабора.ПытатьсяРассчитатьСнова 
				И (СтрокаНабора.ПопыткаРасчета >= 10 Или СтрокаНабора.ПопыткаРасчета = 0) 
			Тогда
				СтрокаНабора.ПытатьсяРассчитатьСнова = Ложь;
			КонецЕсли; 
			Если Истина
				И УчитыватьКоэффициентыРасчетаВремениВПути
				И (Ложь
					Или ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.Graphhopper 
					Или ТипМаршрутизатора = Перечисления.упТипыМаршрутизаторов.AxelotMaps)
			Тогда
				РассчитатьВремяПутиПоЗональнымКоэффициентам(СтрокаНабора);
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

Процедура РассчитатьВремяПутиПоЗональнымКоэффициентам(СтрокаНабораЗаписейРасстояний) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    СтрокаНабораЗаписейРасстояний = РегистрыСведений.упРасстоянияМеждуТочками.СоздатьНаборЗаписей().Добавить();
	КонецЕсли;
	Если СтрокаНабораЗаписейРасстояний.Время = 0 Тогда
		Возврат;
	КонецЕсли; 
	Запрос = Новый Запрос;
	Запрос.Текст = "
|//{Запрос: 0 ////////////////////////////////////////
|ВЫБРАТЬ
|	ЗональныеНастройки.Граница КАК Граница,
|	ЗональныеНастройки.КоэффициентДоГраницы КАК КоэффициентДоГраницы,
|	ЗональныеНастройки.КоэффициентПослеГраницы КАК КоэффициентПослеГраницы,
|	ИерархияОтправления.Родитель КАК РодительОтправления,
|	ИерархияОтправления.Уровень КАК УровеньОтправления,
|	ИерархияПолучения.Родитель КАК РодительПолучения,
|	ИерархияПолучения.Уровень КАК УровеньПолучения
|ПОМЕСТИТЬ ПодходящииеЗональныеНастройки
|ИЗ
|	РегистрСведений.упЗональныеНастройкиМаршрутизации КАК ЗональныеНастройки
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК ИерархияОтправления
|	ПО ИерархияОтправления.Родитель = ЗональныеНастройки.ЗонаОтправления
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК АдресОтправления
|	ПО ИерархияОтправления.Зона = АдресОтправления.ГеографическаяЗона
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК ИерархияПолучения
|	ПО ИерархияОтправления.Родитель = ЗональныеНастройки.ЗонаОтправления
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК АдресПолучения
|	ПО ИерархияПолучения.Зона = АдресПолучения.ГеографическаяЗона
|ГДЕ ИСТИНА
|	И АдресОтправления.Широта = &НачальнаяШирота
|	И АдресОтправления.Долгота = &НачальнаяДолгота
|	И АдресПолучения.Долгота = &КонечнаяДолгота
|	И АдресПолучения.Широта = &КонечнаяШирота
|ОБЪЕДИНИТЬ ВСЕ
|ВЫБРАТЬ
|	упЗональныеНастройкиМаршрутизацииТ.Граница КАК Граница,
|	упЗональныеНастройкиМаршрутизацииТ.КоэффициентДоГраницы КАК КоэффициентДоГраницы,
|	упЗональныеНастройкиМаршрутизацииТ.КоэффициентПослеГраницы КАК КоэффициентПослеГраницы,
|	упЗональныеНастройкиМаршрутизацииТ.ЗонаОтправления КАК РодительОтправления,
|	-1 КАК УровеньОтправления,
|	упЗональныеНастройкиМаршрутизацииТ.ЗонаПолучения КАК РодительПолучения,
|	-1 КАК УровеньПолучения
|ИЗ
|	РегистрСведений.упЗональныеНастройкиМаршрутизации КАК упЗональныеНастройкиМаршрутизацииТ
|ГДЕ ИСТИНА
|	И упЗональныеНастройкиМаршрутизацииТ.ЗонаОтправления = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
|	И упЗональныеНастройкиМаршрутизацииТ.ЗонаПолучения = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
|
|
|;
|//{Запрос: 1 ////////////////////////////////////////
|ВЫБРАТЬ
|	МАКСИМУМ(ПодходящииеЗональныеНастройкиТ.УровеньОтправления) КАК УровеньОтправления
|ПОМЕСТИТЬ МаксимальныйУровеньОтправления
|ИЗ
|	ПодходящииеЗональныеНастройки КАК ПодходящииеЗональныеНастройкиТ
|;
|//{Запрос: 2 ////////////////////////////////////////
|ВЫБРАТЬ
|	МАКСИМУМ(ПодходящииеЗональныеНастройкиТ.УровеньПолучения) КАК УровеньПолучения
|ПОМЕСТИТЬ МаксимальныйУровеньПолучения
|ИЗ
|	МаксимальныйУровеньОтправления КАК МаксимальныйУровеньОтправленияТ
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПодходящииеЗональныеНастройки КАК ПодходящииеЗональныеНастройкиТ
|	ПО ИСТИНА
|;
|//{Запрос: 3 ////////////////////////////////////////
|ВЫБРАТЬ
|	ПодходящииеЗональныеНастройкиТ.Граница КАК Граница,
|	ПодходящииеЗональныеНастройкиТ.КоэффициентДоГраницы КАК КоэффициентДоГраницы,
|	ПодходящииеЗональныеНастройкиТ.КоэффициентПослеГраницы КАК КоэффициентПослеГраницы
|ИЗ
|	ПодходящииеЗональныеНастройки КАК ПодходящииеЗональныеНастройкиТ
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныйУровеньОтправления КАК МаксимальныйУровеньОтправленияТ
|	ПО ПодходящииеЗональныеНастройкиТ.УровеньОтправления = МаксимальныйУровеньОтправленияТ.УровеньОтправления
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныйУровеньПолучения КАК МаксимальныйУровеньПолученияТ
|	ПО ПодходящииеЗональныеНастройкиТ.УровеньПолучения = МаксимальныйУровеньПолученияТ.УровеньПолучения
	|";
	Запрос.УстановитьПараметр("НачальнаяШирота", СтрокаНабораЗаписейРасстояний.НачальнаяШирота);
	Запрос.УстановитьПараметр("НачальнаяДолгота", СтрокаНабораЗаписейРасстояний.НачальнаяДолгота);
	Запрос.УстановитьПараметр("КонечнаяШирота", СтрокаНабораЗаписейРасстояний.КонечнаяШирота);
	Запрос.УстановитьПараметр("КонечнаяДолгота", СтрокаНабораЗаписейРасстояний.КонечнаяДолгота);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Если ТаблицаРезультата.Количество() = 0 Тогда
		ВызватьИсключение "В регистре зональных настроек маршрутизации отсутствует строка с пустыми зонами";
	КонецЕсли; 
	ЗональныеКоэффициенты = ТаблицаРезультата[0];
	Если Ложь Тогда // Для контекстной подсказки
	    ЗональныеКоэффициенты = РегистрыСведений.упЗональныеНастройкиМаршрутизации.СоздатьНаборЗаписей().Добавить();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(СтрокаНабораЗаписейРасстояний.ВремяБазовое) Тогда
		СтрокаНабораЗаписейРасстояний.ВремяБазовое = СтрокаНабораЗаписейРасстояний.Время;
	КонецЕсли; 
	Если СтрокаНабораЗаписейРасстояний.ВремяБазовое = 0 Тогда
		Скорость = 0;
	Иначе
		Скорость = СтрокаНабораЗаписейРасстояний.Расстояние / СтрокаНабораЗаписейРасстояний.ВремяБазовое;
	КонецЕсли; 
	ПерваяПоловинаПути = Мин(ЗональныеКоэффициенты.Граница, СтрокаНабораЗаписейРасстояний.Расстояние);
	ВтораяПоловинаПути = СтрокаНабораЗаписейРасстояний.Расстояние - ПерваяПоловинаПути;
	Если Скорость * ЗональныеКоэффициенты.КоэффициентПослеГраницы = 0 Тогда
		ВремяНовое = 0;
	Иначе
		ВремяНовое = ПерваяПоловинаПути * ЗональныеКоэффициенты.КоэффициентДоГраницы / Скорость + ЗональныеКоэффициенты.КоэффициентПослеГраницы * ВтораяПоловинаПути / Скорость;
	КонецЕсли; 
	СтрокаНабораЗаписейРасстояний.Время = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяВоскресенье = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяВторник = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяПонедельник = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяПятница = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяСреда = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяСуббота = ВремяНовое;
	СтрокаНабораЗаписейРасстояний.ВремяЧетверг = ВремяНовое;

КонецПроцедуры

#КонецОбласти