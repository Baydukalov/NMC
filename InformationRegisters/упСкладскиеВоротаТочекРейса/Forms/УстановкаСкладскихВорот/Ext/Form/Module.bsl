#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("Рейс") Или Не ЗначениеЗаполнено(Параметры.Рейс) Тогда
		ВызватьИсключение НСтр("ru = 'Не указан рейс!'");
	КонецЕсли; 
	
	ЭтаФорма.Рейс = Параметры.Рейс;
	
	ОбновитьДанныеФормы();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура МаршрутПриАктивизацииСтроки(Элемент)
	
	СтрокаТочка = Элемент.ТекущиеДанные;
	Если Не СтрокаТочка = Неопределено Тогда
		Элементы.СкладскиеВорота.ОтборСтрок = Новый ФиксированнаяСтруктура("Идентификатор", СтрокаТочка.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладскиеВоротаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока Тогда
		текДанные = Элементы.Маршрут.ТекущиеДанные;
		
		текВорота = Элементы.СкладскиеВорота.ТекущиеДанные;
		текВорота.Идентификатор = текДанные.Идентификатор;
	КонецЕсли; 
	Модифицированность = Истина;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	ЗаписатьИзмененияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ОбновитьДанныеФормы()
	
	ЭтаФорма.Маршрут.Очистить();
	ЭтаФорма.СкладскиеВорота.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упМаршрутПоРейсуСрезПоследних.Идентификатор КАК Идентификатор,
	|	упМаршрутПоРейсуСрезПоследних.Адрес КАК Адрес,
	|	упМаршрутПоРейсуСрезПоследних.Адрес.Склад КАК Склад,
	|	упМаршрутПоРейсуСрезПоследних.ТипТочки КАК ТипТочки,
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер КАК СтрокаНомер,
	|	упМаршрутПоРейсуСрезПоследних.Пройдена КАК Пройдена,
	|	упМаршрутПоРейсуСрезПоследних.Отменена КАК Отменена,
	|	упМаршрутПоРейсуСрезПоследних.Отложена КАК Отложена,
	|	упМаршрутПоРейсуСрезПоследних.ТипТочки.Порядок КАК ИндексКартинки
	|ИЗ
	|	РегистрСведений.упМаршрутПоРейсу.СрезПоследних(, Рейс = &Рейс) КАК упМаршрутПоРейсуСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	упМаршрутПоРейсуСрезПоследних.СтрокаНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упСкладскиеВоротаТочекРейса.Идентификатор КАК Идентификатор,
	|	упСкладскиеВоротаТочекРейса.ИдентификаторРаботы КАК ИдентификаторРаботы,
	|	упСкладскиеВоротаТочекРейса.СкладскиеВорота КАК СкладскиеВорота,
	|	упСкладскиеВоротаТочекРейса.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.упСкладскиеВоротаТочекРейса КАК упСкладскиеВоротаТочекРейса
	|ГДЕ
	|	упСкладскиеВоротаТочекРейса.Рейс = &Рейс
	|
	|УПОРЯДОЧИТЬ ПО
	|	упСкладскиеВоротаТочекРейса.Идентификатор,
	|	упСкладскиеВоротаТочекРейса.СкладскиеВорота";
	Запрос.УстановитьПараметр("Рейс", Рейс);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоТаблиц = РезультатЗапроса.Количество();
	
	Если Не РезультатЗапроса[КоличествоТаблиц - 2].Пустой() Тогда
		ВыборкаМаршрут = РезультатЗапроса[КоличествоТаблиц - 2].Выбрать();
		Пока ВыборкаМаршрут.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Маршрут.Добавить(), ВыборкаМаршрут);
		КонецЦикла; 
	КонецЕсли;
	
	Если Не РезультатЗапроса[КоличествоТаблиц - 1].Пустой() Тогда
		ВыборкаВорота = РезультатЗапроса[КоличествоТаблиц - 1].Выбрать();
		Пока ВыборкаВорота.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СкладскиеВорота.Добавить(), ВыборкаВорота);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияНаСервере()
	
	Отказ = Ложь;
	
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упСкладскиеВоротаТочекРейса");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Рейс", Рейс);
	
	упСервисныеФункции.УстановитьУправляемуюБлокировку(БлокировкаДанных, Рейс, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.упСкладскиеВоротаТочекРейса.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Рейс.Установить(Рейс);
	
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Для каждого текВорота Из СкладскиеВорота Цикл
		Если ЗначениеЗаполнено(текВорота.Идентификатор) И ЗначениеЗаполнено(текВорота.СкладскиеВорота) Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Рейс = Рейс;
			НоваяЗапись.Идентификатор = текВорота.Идентификатор;
			НоваяЗапись.СкладскиеВорота = текВорота.СкладскиеВорота;
			НоваяЗапись.Комментарий     = текВорота.Комментарий;
		КонецЕсли; 
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
	Иначе	
	     ЗафиксироватьТранзакцию();
	 КонецЕсли; 
	 
	 Модифицированность = Ложь;
	
КонецПроцедуры	

#КонецОбласти