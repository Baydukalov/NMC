
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура выполняет проверку на пересечение периодов пересечение плановых ТО для ТС/оборудование,
// после проведения документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.упРемонтИТОТС, ДокументОбъект.упЗаявкаНаРемонтИТОТС - объект документа.
//  Отказ          - Булево - Признак проведения документа.
//
Процедура ВыполнитьКонтрольРезультатовПроведения(ДокументОбъект, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПлановыеДатыТО.ТранспортноеСредство КАК ТранспортноеСредство,
	|	упПлановыеДатыТО.НачалоРемонта КАК НачалоРемонта,
	|	упПлановыеДатыТО.ОкончаниеРемонта КАК ОкончаниеРемонта,
	|	упПлановыеДатыТО.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ вт_Данные
	|ИЗ
	|	РегистрСведений.упПлановыеДатыТО КАК упПлановыеДатыТО
	|ГДЕ
	|	упПлановыеДатыТО.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Данные.ТранспортноеСредство КАК ТранспортноеСредство,
	|	вт_Данные.НачалоРемонта КАК НачалоРемонта,
	|	вт_Данные.ОкончаниеРемонта КАК ОкончаниеРемонта,
	|	вт_Данные.ВидРемонта КАК ВидРемонта
	|ИЗ
	|	вт_Данные КАК вт_Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПройденныеТО.СрезПоследних(
	|				,
	|				ТранспортноеСредство В
	|					(ВЫБРАТЬ
	|						вт_Данные.ТранспортноеСредство КАК ТранспортноеСредство
	|					ИЗ
	|						вт_Данные КАК вт_Данные)) КАК упПройденныеТОСрезПоследних
	|		ПО вт_Данные.ТранспортноеСредство = упПройденныеТОСрезПоследних.ТранспортноеСредство
	|			И (упПройденныеТОСрезПоследних.Период МЕЖДУ вт_Данные.НачалоРемонта И вт_Данные.ОкончаниеРемонта)
	|			И вт_Данные.ВидРемонта = упПройденныеТОСрезПоследних.ВидРемонта
	|ГДЕ
	|	НЕ упПройденныеТОСрезПоследних.Регистратор ЕСТЬ NULL
	|	И упПройденныеТОСрезПоследних.Регистратор <> &Регистратор"; 
	Запрос.УстановитьПараметр("Регистратор", ДокументОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ТекстСообщения = НСтр(СтрШаблон("ru = 'Для выбранных видов ремонта по ТС/оборудованию на указанный плановый период уже пройдено ТО. Проведение не выполнено. %1'", Строка(ДокументОбъект)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтрольРезультатовПроведения()

#КонецОбласти 

#КонецЕсли
