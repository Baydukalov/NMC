#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура выполняет проверку на пересечение периодов доступности мест ремонта гаража,
// после проведения документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.упРемонтИТОТС - объект документа.
//  Отказ          - Булево - Признак проведения документа.
//
Процедура ВыполнитьКонтрольРезультатовПроведения(ДокументОбъект, Отказ) Экспорт
	
	Гараж        = ДокументОбъект.Гараж;
	МестоРемонта = ДокументОбъект.МестоРемонта;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПлановоеРасписаниеРаботыГаража.Регистратор,
	|	упПлановоеРасписаниеРаботыГаража.НачалоРемонта КАК ДатаНачала,
	|	упПлановоеРасписаниеРаботыГаража.ОкончаниеРемонта КАК ДатаОкончания
	|ПОМЕСТИТЬ втПериодыДоступностиПодготовка
	|ИЗ
	|	РегистрСведений.упПлановоеРасписаниеРаботыГаража КАК упПлановоеРасписаниеРаботыГаража
	|ГДЕ
	|	упПлановоеРасписаниеРаботыГаража.Гараж = &Гараж
	|	И упПлановоеРасписаниеРаботыГаража.МестоРемонта = &МестоРемонта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	упПлановоеРасписаниеРаботыГаража.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериодыДоступности.Регистратор,
	|	втПериодыДоступности.ДатаНачала,
	|	втПериодыДоступности.ДатаОкончания
	|ПОМЕСТИТЬ втПериодыДоступности
	|ИЗ
	|	втПериодыДоступностиПодготовка КАК втПериодыДоступности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыРемонтовИТОТС.СрезПоследних(
	|				,
	|				Документ В
	|					(ВЫБРАТЬ
	|						втПериодыДоступностиПодготовка.Регистратор
	|					ИЗ
	|						втПериодыДоступностиПодготовка)) КАК упСтатусыСрезПоследних
	|		ПО втПериодыДоступности.Регистратор = упСтатусыСрезПоследних.Документ
	|			И (упСтатусыСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыРемонтаИТОТС.Запланирован))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	втПериодыДоступности.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПериодыДоступности1.Регистратор КАК Регистратор
	|ИЗ
	|	втПериодыДоступности КАК втПериодыДоступности1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериодыДоступности КАК втПериодыДоступности2
	|		ПО (НЕ втПериодыДоступности1.Регистратор = втПериодыДоступности2.Регистратор)
	|			И (ДОБАВИТЬКДАТЕ(втПериодыДоступности1.ДатаНачала, СЕКУНДА, 1) МЕЖДУ втПериодыДоступности2.ДатаНачала И втПериодыДоступности2.ДатаОкончания)"; 
	Запрос.УстановитьПараметр("Гараж", Гараж);
	Запрос.УстановитьПараметр("МестоРемонта", МестоРемонта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ТекстСообщения = НСтр(СтрШаблон("ru = 'Для места ремонта %1 на указанный плановый период уже запланированы другие работы. Проведение не выполнено. %2'", Строка(МестоРемонта), Строка(ДокументОбъект)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтрольРезультатовПроведения()

#КонецОбласти 

#КонецЕсли
