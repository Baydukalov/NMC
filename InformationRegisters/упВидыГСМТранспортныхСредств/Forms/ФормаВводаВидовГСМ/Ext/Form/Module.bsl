#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Период               = ТекущаяДатаСеанса();
	ТранспортноеСредство = Параметры.ТранспортноеСредство;
	
	ЗаполнитьСписокВидовГСМ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ЗаполнитьСписокВидовГСМ();
КонецПроцедуры

&НаКлиенте
Процедура СписокОсновнойПриИзменении(Элемент)
	ТекущиеДанные     = Элементы.Список.ТекущиеДанные;
	мсвОсновныеВидГСМ = Список.НайтиСтроки(Новый Структура("Основной",Истина));
	
	Если мсвОсновныеВидГСМ.Количество() = 0 Тогда
		ТекущиеДанные.Основной = Истина;
	ИначеЕсли мсвОсновныеВидГСМ.Количество() > 1 Тогда	
		Для каждого Строка Из мсвОсновныеВидГСМ Цикл
			Если Элементы.Список.ТекущаяСтрока <> Строка.ПолучитьИдентификатор() Тогда
				Строка.Основной = Ложь;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокИспользуетсяПриИзменении(Элемент)
	ТекущиеДанные     = Элементы.Список.ТекущиеДанные;
	Если Истина
		И ТекущиеДанные.Основной 
		И НЕ ТекущиеДанные.Используется 
	Тогда
	     мсвСтроки = Список.НайтиСтроки(Новый Структура("Используется", Истина));
		Если мсвСтроки.Количество() > 0 Тогда
			ТекстСообщения = Нстр("ru = 'Запрещено отклчать использование у основного вида ГСМ'");
			ИндексСтроки = Список.Индекс(ТекущиеДанные);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Список[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].Используется");							
			ТекущиеДанные.Используется = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ТекущиеДанные  = Элемент.ТекущиеДанные;
		ТекущиеДанные.Используется = Истина;	
		ТекущиеДанные.Основной     = Ложь;	
		
		мсвОсновныеВидГСМ = Список.НайтиСтроки(Новый Структура("Основной",Истина));
		Если мсвОсновныеВидГСМ.Количество() = 0 Тогда
			ТекущиеДанные.Основной = Истина;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НЕ ОтменаРедактирования 	Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ВидГСМ) Тогда
			сткОтбора = Новый Структура("ВидГСМ", ТекущиеДанные.ВидГСМ);
			мсвСтроки = Список.НайтиСтроки(сткОтбора);
			Если мсвСтроки.Количество() > 1 Тогда
				ТекстСообщения = Нстр("ru = 'В табличной части уже есть строки с видом гсм ""%1""'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекущиеДанные.ВидГСМ);	
				ИндексСтроки   = Список.Индекс(ТекущиеДанные);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Список[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].ВидГСМ",,Отказ);				

			КонецЕсли;
		Иначе
			ТекстСообщения = Нстр("ru = 'Поле ""Вид ГСМ"" не заполнено'");
			ИндексСтроки = Список.Индекс(ТекущиеДанные);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Список[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].ВидГСМ",,Отказ);				
		КонецЕсли;
	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	Отказ = Ложь;
	ПрименитьИзмененияНаСервере(Отказ);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.упВидыГСМТранспортныхСредств"));
	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьСписокВидовГСМ()
	
	Список.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	тбВидыГСМ.ВидГСМ КАК ВидГСМ,
	               |	тбВидыГСМ.Используется КАК Используется,
	               |	тбВидыГСМ.ОбъемБака КАК ОбъемБака,
	               |	тбВидыГСМ.Основной КАК Основной,
	               |	тбВидыГСМ.ПроцентИспользования КАК ПроцентИспользования
	               |ИЗ
	               |	РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(&Период, ТранспортноеСредство = &ТранспортноеСредство) КАК тбВидыГСМ";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Список.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияНаСервере(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	тбВидыГСМ.ВидГСМ КАК ВидГСМ,
	|	тбВидыГСМ.Используется КАК Используется,
	|	тбВидыГСМ.ОбъемБака КАК ОбъемБака,
	|	тбВидыГСМ.Основной КАК Основной,
	|	тбВидыГСМ.ПроцентИспользования КАК ПроцентИспользования
	|ПОМЕСТИТЬ втВидыГСМ
	|ИЗ
	|	&ВидыГСМ КАК тбВидыГСМ
	|;	
	|ВЫБРАТЬ
	|	ЕстьNull(тбВидыГСМТ.ВидГСМ, ЗНАЧЕНИЕ(Справочник.упВидыГСМ.ПустаяСсылка)) КАК ВидГСМ,
	|	тбВидыГСМТ.Используется КАК Используется,
	|	тбВидыГСМТ.ОбъемБака КАК ОбъемБака,
	|	тбВидыГСМТ.Основной КАК Основной,
	|	тбВидыГСМТ.ПроцентИспользования КАК ПроцентИспользования,
	|	ЕстьNull(ВидыГСМСрезПоследних.ВидГСМ, ЗНАЧЕНИЕ(Справочник.упВидыГСМ.ПустаяСсылка)) КАК ВидГСМТекущий,
	|	ВидыГСМСрезПоследних.Используется КАК ИспользуетсяТекущий,
	|	ВидыГСМСрезПоследних.ОбъемБака КАК ОбъемБакаТекущий,
	|	ВидыГСМСрезПоследних.Период КАК ПериодТекущий,
	|	ВидыГСМСрезПоследних.Основной КАК ОсновнойТекущий,
	|	ВидыГСМСрезПоследних.ПроцентИспользования КАК ПроцентИспользованияТекущий
	|ИЗ
	|	втВидыГСМ КАК тбВидыГСМТ
	|	ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.упВидыГСМТранспортныхСредств.СрезПоследних(
	|		&Период,
	|		ТранспортноеСредство = &ТранспортноеСредство) КАК ВидыГСМСрезПоследних
	|	ПО ИСТИНА
	|		И ВидыГСМСрезПоследних.ВидГСМ = тбВидыГСМТ.ВидГСМ
	|";
	
	
	Запрос.УстановитьПараметр("ВидыГСМ", Список.Выгрузить(,"ВидГСМ, Используется, ОбъемБака, Основной, ПроцентИспользования"));
	Запрос.УстановитьПараметр("Период",  Период);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Выборка	= Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Запись = Неопределено;
		
		Если Истина
			И ЗначениеЗаполнено(Выборка.ВидГСМТекущий)
			И ЗначениеЗаполнено(Выборка.ВидГСМ) Тогда
		
			Если Ложь
				Или Выборка.ИспользуетсяТекущий <> Выборка.Используется
				Или Выборка.ОбъемБакаТекущий <> Выборка.ОбъемБака
				Или Выборка.ОсновнойТекущий <> Выборка.Основной
				Или Выборка.ПроцентИспользованияТекущий <> Выборка.ПроцентИспользования
			Тогда
			
				Запись = РегистрыСведений.упВидыГСМТранспортныхСредств.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
				Запись.Период               = Период;
				Запись.ТранспортноеСредство = ТранспортноеСредство;
		
			КонецЕсли;
		ИначеЕсли Истина
				И ЗначениеЗаполнено(Выборка.ВидГСМТекущий)
				И НЕ ЗначениеЗаполнено(Выборка.ВидГСМ) Тогда
				
				Запись = РегистрыСведений.упВидыГСМТранспортныхСредств.СоздатьМенеджерЗаписи();
				Запись.Период               = Выборка.ПериодТекущий;
				Запись.ТранспортноеСредство = ТранспортноеСредство;
				Запись.ВидГСМ = Выборка.ВидГСМТекущий;
				Запись.Прочитать();
				
				Запись.Используется = Ложь;
				Запись.Основной = Ложь;
				
		ИначеЕсли Истина
				И НЕ ЗначениеЗаполнено(Выборка.ВидГСМТекущий)
				И ЗначениеЗаполнено(Выборка.ВидГСМ) Тогда				
				
				Запись = РегистрыСведений.упВидыГСМТранспортныхСредств.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
				Запись.Период               = Период;
				Запись.ТранспортноеСредство = ТранспортноеСредство;
				
		КонецЕсли;
		
		Если Запись <> Неопределено Тогда
				
			Попытка
				Запись.Записать();
			Исключение
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
					ТекстОшибки = ОписаниеОшибки();
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
				ЗаписьЖурналаРегистрации("Ошибка при записи вида ГСМ", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецПопытки;
		КонецЕсли;
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла; 
	
	Если НЕ Отказ Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
