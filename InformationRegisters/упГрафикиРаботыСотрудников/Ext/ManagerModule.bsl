#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПроконтролироватьНаложениеПериодовДействияШаблоновГрафиков(ДатаНачала, ДатаОкончания, СписокОбъектов, Отказ) Экспорт
	
	мсвСписокОбъектов = Новый Массив;
	
	Если ТипЗнч(СписокОбъектов) = Тип("СправочникСсылка.упСотрудники") Тогда
		мсвСписокОбъектов.Добавить(СписокОбъектов);
	Иначе
		мсвСписокОбъектов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(СписокОбъектов);
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|
	|//{Запрос: 0 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикиРаботы.Сотрудник КАК Сотрудник,
	|	ГрафикиРаботы.Период КАК Период,
	|	ГрафикиРаботы.ДатаНачала КАК ДатаНачала,
	|	ГрафикиРаботы.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ втГрафики
	|ИЗ
	|	РегистрСведений.упГрафикиРаботыСотрудников КАК ГрафикиРаботы
	|ГДЕ ИСТИНА
	|	И ГрафикиРаботы.Сотрудник В  (&Объекты)
	|	И ГрафикиРаботы.Период <= &ДатаОкончания
	|	И ГрафикиРаботы.Период >= &ДатаНачала
	|;
	|//{Запрос: 1 ////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГрафикиТ.ДатаНачала КАК ДатаНачала1,
	|	втГрафикиТ.ДатаОкончания КАК ДатаОкончания1,
	|	втГрафикиТ1.ДатаНачала КАК ДатаНачала,
	|	втГрафикиТ1.ДатаОкончания КАК ДатаОкончания,
	|	втГрафикиТ1.Сотрудник КАК Сотрудник
	|ИЗ
	|	втГрафики КАК втГрафикиТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втГрафики КАК втГрафикиТ1
	|	ПО ИСТИНА
	|		И втГрафикиТ1.Сотрудник = втГрафикиТ.Сотрудник
	|		И втГрафикиТ1.Период = втГрафикиТ.Период
	|		И (втГрафикиТ1.ДатаНачала > втГрафикиТ.ДатаНачала
	|		 Или втГрафикиТ1.ДатаОкончания > втГрафикиТ.ДатаОкончания)
	|		И втГрафикиТ1.ДатаНачала < втГрафикиТ.ДатаОкончания
	|		И втГрафикиТ1.ДатаОкончания > втГрафикиТ.ДатаНачала
	|";

	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Объекты", СписокОбъектов);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = НСтр("ru = 'Обнаружены пересекающиеся интервалы по %1.(%2:%3-%4:%5) и (%6:%7-%8:%9)'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
							Выборка.Сотрудник, 
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Час(Выборка.ДатаНачала), 2, "0"),
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Минута(Выборка.ДатаНачала), 2, "0"), 
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Час(Выборка.ДатаОкончания), 2, "0"), 
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Минута(Выборка.ДатаОкончания), 2, "0"),
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Час(Выборка.ДатаНачала1), 2, "0"),
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Минута(Выборка.ДатаНачала1), 2, "0"), 
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Час(Выборка.ДатаОкончания1), 2, "0"), 
							СтроковыеФункцииКлиентСервер.ДополнитьСтроку(""+Минута(Выборка.ДатаОкончания1), 2, "0")
							);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);

		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

// Создает в менеджере временную таблицу ВТРасписанияРаботы с колонками.
// Подробнее см. комментарий к функции РасписанияРаботыНаПериод.
//
Процедура СоздатьВТРасписанияРаботыНаПериод(МенеджерВременныхТаблиц, Сотрудник, ДатаНачала, ДатаОкончания) Экспорт
	
	ТекстЗапроса = 
	"
	|ВЫБРАТЬ
	|		ГрафикиРаботы.Сотрудник КАК Сотрудник,
	|		ГрафикиРаботы.Период КАК ДатаГрафика,
	|		ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(ГрафикиРаботы.Период, ГрафикиРаботы.ДатаНачала, СЕКУНДА)) КАК ВремяНачала,
	|		ДОБАВИТЬКДАТЕ(ДАТАВРЕМЯ(1, 1, 1), СЕКУНДА, РАЗНОСТЬДАТ(ГрафикиРаботы.Период, ГрафикиРаботы.ДатаОкончания, СЕКУНДА)) КАК ВремяОкончания,
	|		ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК ГрафикРаботы
	|	ПОМЕСТИТЬ ВТРасписанияРаботы
	|	ИЗ
	|		РегистрСведений.упГрафикиРаботыСотрудников КАК ГрафикиРаботы
	|	ГДЕ ИСТИНА
	|		И ГрафикиРаботы.Сотрудник = (&Сотрудник)
	|		И ГрафикиРаботы.Период <= &ДатаОкончания
	|		И ГрафикиРаботы.Период >= &ДатаНачала
	|	ИНДЕКСИРОВАТЬ ПО
	|		ГрафикиРаботы.Период
	|";
	
	// Для вычисления номера в цикле произвольной длины для дня, включенного в график, используется следующая формула:
	// Номер дня = Дней от даты отсчета % Длина цикла, где % - операция деления по модулю.
	
	// Операция деления по модулю в свою очередь производится по формуле:
	// Делимое - Цел(Делимое / Делитель) * Делитель, где Цел() - функция выделения целой части.
	
	// Для выделения целой части используется конструкция:
	// если результат округления числа по правилам «1.5 как 2» больше исходного значения, уменьшаем результат на 1.
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыФункции

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ВидФормы = "ФормаСписка" Тогда
		ВызватьИсключение НСтр("ru = 'Содержимое регистра ""Графики работы сотрудников"" 
								|редактируется обработкой ""РМ: Планирование графиков работы"".'");

	КонецЕсли;
		
КонецПроцедуры
	
#КонецОбласти

#КонецЕсли
