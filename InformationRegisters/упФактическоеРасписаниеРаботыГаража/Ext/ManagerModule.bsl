#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура выполняет проверку на пересечение периодов доступности мест ремонта гаража,
// после проведения документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.упРемонтИТОТС - объект документа.
//  Отказ - Булево - Признак проведения документа.
//
Процедура ВыполнитьКонтрольРезультатовПроведения(ДокументОбъект, Отказ) Экспорт
	
	Гараж        = ДокументОбъект.Гараж;
	МестоРемонта = ДокументОбъект.МестоРемонта;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПланРасписаниеГаража.Регистратор КАК Документ,
	|	ПланРасписаниеГаража.ТранспортноеСредство КАК ТС,
	|	РАЗНОСТЬДАТ(ПланРасписаниеГаража.НачалоРемонта, ПланРасписаниеГаража.ОкончаниеРемонта, СЕКУНДА) КАК КоличествоСекунд
	|ПОМЕСТИТЬ вт_ПлановыеДанные
	|ИЗ
	|	РегистрСведений.упПлановоеРасписаниеРаботыГаража КАК ПланРасписаниеГаража
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактРасписаниеГаража.Регистратор,
	|	ФактРасписаниеГаража.НачалоРемонта КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ФактРасписаниеГаража.ОкончаниеРемонта = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ФактРасписаниеГаража.НачалоРемонта, СЕКУНДА, ЕСТЬNULL(вт_ПлановыеДанные.КоличествоСекунд, 1))
	|		ИНАЧЕ ФактРасписаниеГаража.ОкончаниеРемонта
	|	КОНЕЦ КАК ДатаОкончания
	|ПОМЕСТИТЬ втПериодыДоступности
	|ИЗ
	|	РегистрСведений.упФактическоеРасписаниеРаботыГаража КАК ФактРасписаниеГаража
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ПлановыеДанные КАК вт_ПлановыеДанные
	|		ПО ФактРасписаниеГаража.Регистратор = вт_ПлановыеДанные.Документ
	|			И ФактРасписаниеГаража.ТранспортноеСредство = вт_ПлановыеДанные.ТС
	|ГДЕ
	|	ФактРасписаниеГаража.Гараж = &Гараж
	|	И ФактРасписаниеГаража.МестоРемонта = &МестоРемонта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФактРасписаниеГаража.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПД1.Регистратор КАК ТранспротноеСредство
	|ИЗ
	|	втПериодыДоступности КАК ПД1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПериодыДоступности КАК ПД2
	|		ПО (НЕ ПД1.Регистратор = ПД2.Регистратор)
	|			И (ВЫБОР
	|				КОГДА ПД1.ДатаНачала МЕЖДУ ПД2.ДатаНачала И ПД2.ДатаОкончания
	|					ТОГДА ИСТИНА
	|				КОГДА ПД1.ДатаОкончания МЕЖДУ ПД2.ДатаНачала И ПД2.ДатаОкончания
	|					ТОГДА ИСТИНА
	|				КОГДА ПД2.ДатаНачала МЕЖДУ ПД1.ДатаНачала И ПД1.ДатаОкончания
	|					ТОГДА ИСТИНА
	|				КОГДА ПД2.ДатаОкончания МЕЖДУ ПД1.ДатаНачала И ПД1.ДатаОкончания
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)"; 
	Запрос.УстановитьПараметр("Гараж", Гараж);
	Запрос.УстановитьПараметр("МестоРемонта", МестоРемонта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ТекстСообщения = НСтр(СтрШаблон("ru = 'Для места ремонта %1 на указанный фактический период уже запланированы другие работы. Проведение не выполнено. %2'", Строка(МестоРемонта), Строка(ДокументОбъект)));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтрольРезультатовПроведения()

#КонецОбласти 

#КонецЕсли
