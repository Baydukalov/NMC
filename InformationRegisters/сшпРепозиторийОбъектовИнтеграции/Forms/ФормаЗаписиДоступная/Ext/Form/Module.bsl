 
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Запись.ИдентификаторШаблона) тогда
		текЗапрос = Новый Запрос("ВЫБРАТЬ
		|	тбСостояние.Статус,
		|	тбСостояние.ДатаИзменения
		|ИЗ
		|	РегистрСведений.сшпСтатусыОбработчиков КАК тбСостояние
		|ГДЕ
		|	тбСостояние.ИдентификаторОбработчика = &ИдентификаторОбработчика");
		текЗапрос.УстановитьПараметр("ИдентификаторОбработчика", Запись.ИдентификаторШаблона);
		текВыборка = текЗапрос.Выполнить().Выбрать();
		текВыборка.Следующий();
		
		Статус = текВыборка.Статус;
		СтатусОбработчика = Статус;
		ДатаИзменения = текВыборка.ДатаИзменения;
		
		хэшСуммаОбработчика = сшпКэшируемыеФункции.ПолучитьХэшСумму(Запись.ПроцедураОбработки + Строка(Запись.МетодХранения));
	Иначе
		Запись.ИдентификаторШаблона = Новый УникальныйИдентификатор;
		Статус = Перечисления.сшпСтатусыОбработчиков.Включен;
		СтатусОбработчика = Перечисления.сшпСтатусыОбработчиков.ПустаяСсылка();
		ДатаИзменения = ТекущаяДатаСеанса();
		
		хэшСуммаОбработчика = сшпКэшируемыеФункции.ПолучитьХэшСумму(Запись.ПроцедураОбработки + Строка(Запись.МетодХранения));
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ СтатусОбработчика = Статус тогда
		сшпРаботаСДанными.УстановитьСтатусОбработчика(Запись.ИдентификаторШаблона, Статус);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ хэшСуммаОбработчика = сшпКэшируемыеФункции.ПолучитьХэшСумму(ТекущийОбъект.ПроцедураОбработки + Строка(ТекущийОбъект.МетодХранения)) тогда
		Если ЗначениеЗаполнено(ТекущийОбъект.Версия) Тогда 
			ТекущийОбъект.Версия        = Строка(Число(ТекущийОбъект.Версия) + 1);
		Иначе
			ТекущийОбъект.Версия        = "1";
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	Если НЕ СтатусОбработчика = Статус тогда
		ДатаИзменения = ПолучитьДатуСеанса();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДатуСеанса()
	Возврат  ТекущаяДатаСеанса();
КонецФункции	

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Заголовок = ТекущийОбъект.Наименование;
КонецПроцедуры

#КонецОбласти