
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Заголовок = ТекущийОбъект.Наименование;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Запись.ИдентификаторШаблона) тогда
		текЗапрос = Новый Запрос("ВЫБРАТЬ
		|	тбСостояние.Статус,
		|	тбСостояние.ДатаИзменения
		|ИЗ
		|	РегистрСведений.сшпСтатусыОбработчиков КАК тбСостояние
		|ГДЕ
		|	тбСостояние.ИдентификаторОбработчика = &ИдентификаторОбработчика");
		текЗапрос.УстановитьПараметр("ИдентификаторОбработчика", Запись.ИдентификаторШаблона);
		текВыборка = текЗапрос.Выполнить().Выбрать();
		текВыборка.Следующий();
		
		Статус = текВыборка.Статус;
		СтатусОбработчика = Статус;
		ДатаИзменения = текВыборка.ДатаИзменения;
		
		хэшСуммаОбработчика = сшпКэшируемыеФункции.ПолучитьХэшСумму(Запись.ПроцедураОбработки + Строка(Запись.МетодХранения));
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ СтатусОбработчика = Статус тогда
		сшпРаботаСДанными.УстановитьСтатусОбработчика(Запись.ИдентификаторШаблона, Статус);
		
		xmlЗапись = Новый ЗаписьXML;
		xmlЗапись.УстановитьСтроку("UTF-8");
		xmlЗапись.ЗаписатьНачалоЭлемента("TemplateState");
		сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, Запись.ИдентификаторШаблона, "Id");
		сшпФункцииРаботыXML.СформироватьЭлементXML(xmlЗапись, Статус = Перечисления.сшпСтатусыОбработчиков.Включен, "IsStarted");
		xmlЗапись.ЗаписатьКонецЭлемента();
		
		РезультатОбработки = сшпОбщегоНазначения.СформироватьСтруктуруПакета("SSM","Esb-1c-TemplateChangeState", xmlЗапись.Закрыть());
		
		сшпВзаимодействиеСАдаптером.ОтправитьСообщение(РезультатОбработки);
		//xmlПакет = сшпОбщегоНазначения.СериализоватьОбъект(сшпФункциональныеОпции.ФорматСообщения(), РезультатОбработки);
		//сшпРаботаСДанными.ПоместитьВСистемнуюОчередь(Новый УникальныйИдентификатор, сшпФункциональныеОпции.ФорматСообщения(), "SSM", xmlПакет);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ хэшСуммаОбработчика = сшпКэшируемыеФункции.ПолучитьХэшСумму(ТекущийОбъект.ПроцедураОбработки + Строка(ТекущийОбъект.МетодХранения)) тогда
		ТекущийОбъект.Версия = Строка(Число(ТекущийОбъект.Версия) + 1);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	Если НЕ СтатусОбработчика = Статус тогда
		ДатаИзменения = ПолучитьДатуСеанса();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДатуСеанса()
	Возврат  ТекущаяДатаСеанса();
КонецФункции

#КонецОбласти