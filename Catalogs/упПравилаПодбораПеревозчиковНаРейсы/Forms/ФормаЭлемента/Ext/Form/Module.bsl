
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	СхемаКомпоновкиДанных = ТекущийОбъект.СхемаРейс.Получить();
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
			
	// Переданный в параметре адрес сохраняется в качестве адреса исходной схемы
	Если Не ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда 
		СхемаКомпоновкиДанных = ПолучитьСКД();
		Если СхемаКомпоновкиДанных <> Неопределено Тогда 
			АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
		КонецЕсли;
	Иначе	
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	КонецЕсли;	
	
	Если СхемаКомпоновкиДанных <> Неопределено Тогда 
		Попытка
			КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
			КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			КомпоновщикНастроек.Восстановить();
		Исключение
			СхемаКомпоновкиДанныхАктуальная = ПолучитьСКД();
			Если СхемаКомпоновкиДанныхАктуальная <> Неопределено Тогда 
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Отбор, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор);
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Порядок, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Порядок);
				АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхАктуальная, УникальныйИдентификатор);
				
				КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
				КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию);
				КомпоновщикНастроек.Восстановить();
			КонецЕсли;
			Модифицированность = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если СхемаКомпоновкиДанных = Неопределено Тогда 
		Элементы.ГруппаНастройкаСКД.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Активно = Истина;
		Объект.ПериодАнализаДанных = Перечисления.упПериодыАнализаДанныхПриПодбореПеревозчиков.МесяцНачалаРейса;
	КонецЕсли;
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	КомпоновщикНастроек.Восстановить();
	
	Если ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		
		упСервисныеФункцииВызовСервера.ОчиститьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		упСервисныеФункцииВызовСервера.СкопироватьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанных.НастройкиПоУмолчанию, КомпоновщикНастроек.ПолучитьНастройки());
		
		ТекущийОбъект.СхемаРейс = Новый ХранилищеЗначения(СхемаКомпоновкиДанных);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовокПеревозчики()
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
		
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикиДоляПриИзменении(Элемент)
	
	ПроверимИтоговуюДолюПеревозчиков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикиПриИзменении(Элемент)
	
	Если Объект.Перевозчики.Количество() Тогда
		ПроверимИтоговуюДолюПеревозчиков();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикиПриАктивизацииСтроки(Элемент)
	УстановитьЗаголовокПеревозчики();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверимИтоговуюДолюПеревозчиков()

	Если НЕ ДоляПеревозчиковСтоПроцентов() Тогда
		ПоказатьОповещениеПользователя("Доля перевозчика",,"Суммарное значение долей перевозчиков должно быть 100%.",БиблиотекаКартинок.Информация32);
	КонецЕсли;

КонецПроцедуры // ПроверимИтоговуюДолюПеревозчиков()

&НаКлиенте
Процедура УстановитьЗаголовокПеревозчики()

	Элементы.ГруппаПеревозчики.Заголовок = СтрШаблон("Перевозчики (%1)", Формат(Объект.Перевозчики.Количество(),"ЧН=0; ЧГ=0"));

КонецПроцедуры // УстановитьЗаголовокПеревозчики()

&НаКлиенте
// Выполним проверку что доля равна 100%.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Булево - Истина, что доля 100%.
//
Функция ДоляПеревозчиковСтоПроцентов()

	Итог_Доля = Объект.Перевозчики.Итог("Доля");
	Возврат Итог_Доля = 100;

КонецФункции // ДоляПеревозчиковСтоПроцентов()

&НаСервере
Функция ПолучитьСКД()
	
	Возврат Справочники.упПравилаПодбораПеревозчиковНаРейсы.ПолучитьМакет("Рейс");
	
КонецФункции // ПолучитьСКД()

#КонецОбласти 