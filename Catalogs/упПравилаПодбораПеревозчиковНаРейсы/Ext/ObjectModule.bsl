

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ПометкаУдаления Тогда 
		Активно = Ложь;
	ИначеЕсли Ссылка.ПометкаУдаления Тогда 
		Активно = Истина;
	КонецЕсли;
	
	Если Перевозчики.Количество() Тогда
		Если НЕ Перевозчики.Итог("Доля") = 100 Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка распределения долей перевозчиков! Суммарное значение долей должно быть 100%.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,"Объект.Перевозчики[0].Доля",, Отказ);
		КонецЕсли;
		
	Иначе
		ТекстСообщения = НСтр("ru = 'Заполните список Перевозчиков! Суммарное значение долей должно быть 100%.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,"Объект.Перевозчики",, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Сч=0;
		Для каждого ТекущаяСтрокаТЧ Из Перевозчики Цикл
			Отбор = Новый Структура("Перевозчик", ТекущаяСтрокаТЧ.Перевозчик);
			НайденныеСтроки = Перевозчики.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество()>1 Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Повторяющихся значений быть не должно: %1.'"), ТекущаяСтрокаТЧ.Перевозчик);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,СтрШаблон("Объект.Перевозчики[%1].Перевозчик", Формат(Сч, "ЧН=0; ЧГ=0")),, Отказ);
				Прервать;
			КонецЕсли;
			Сч=Сч+1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли