
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя() Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Справочники.Пользователи.ПустаяСсылка());
	Иначе	
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Пользователи.ТекущийПользователь());
	КонецЕсли;
	
	текЗона = Неопределено;
	Если Параметры.Отбор.Свойство("ГеографическаяЗона", текЗона) Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокЗон.Отбор, "Ссылка", текЗона, ВидСравненияКомпоновкиДанных.Равно,, Истина);
		Элементы.СписокЗон.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.упАдреса) Тогда
		Элементы.ФормаГрупповоеИзменениеОбъектов.Видимость = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПросмотрСпискомПриИзменении(Элемент)
	
	УстановитьОтборПоЗоне();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗонПриАктивизацииСтроки(Элемент)

	ПодключитьОбработчикОжидания("УстановитьОтборПоЗонеКлиент", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьАдресаВоВложенныхЗонахПриИзменении(Элемент)

	УстановитьОтборПоЗоне();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
// Процедура - обработчик команды "ГрупповоеИзменениеОбъектов"
//
Процедура ГрупповоеИзменениеОбъектов(Команда)
	
	НетПраваРедактировать = НЕ ПолучитьПравоКорректироватьАдрес();
	
	Индекс = 0;
	
	Для каждого ТекущийАдрес Из Элементы.Список.ВыделенныеСтроки Цикл		
		Если НетПраваРедактировать И ТекущийАдрес.Подтвержден Тогда
			Элементы.Список.ВыделенныеСтроки.Удалить(Индекс);
			ТекстОшибки = СтрШаблон("Адрес %1 утвержден, вносить изменения запрещено",ТекущийАдрес);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ТекущийАдрес);
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры // ГрупповоеИзменениеОбъектов()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Выполним обращение для получения прав редактировать.
//
//
// Возвращаемое значение:
//   Булево - Разрешение на редактирование.
//
Функция ПолучитьПравоКорректироватьАдрес()

	Возврат Справочники.упАдреса.ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных();

КонецФункции // ПолучитьПравоКорректироватьАдрес()

&НаСервере
Процедура УстановитьОтборПоЗоне()
	
	Если Элементы.СписокЗон.ВыделенныеСтроки.Количество() = 1 Тогда 
		текЗона = Элементы.СписокЗон.ТекущаяСтрока;
		
		Если ОтображатьАдресаВоВложенныхЗонах Тогда 
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВИерархии;
		Иначе	
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГеографическаяЗона", текЗона, ВидСравненияСКД,, Не ПросмотрСписком);
	Иначе 	
		спзЗон = Новый СписокЗначений;
		спзЗон.ЗагрузитьЗначения(Элементы.СписокЗон.ВыделенныеСтроки);
	
		Если ОтображатьАдресаВоВложенныхЗонах Тогда 
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
		Иначе	
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСписке;
		КонецЕсли;	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГеографическаяЗона", спзЗон, ВидСравненияСКД,, Не ПросмотрСписком);
	КонецЕсли;	
		
	Элементы.СписокЗон.Доступность = Не ПросмотрСписком;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоЗонеКлиент()
	
	УстановитьОтборПоЗоне();
	
КонецПроцедуры

#КонецОбласти
