
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановленОтборПоЗоне = Ложь;
	ЗонаПриОткрытии = Неопределено;
	Если Параметры.Отбор.Свойство("ГеографическаяЗона") И ЗначениеЗаполнено(Параметры.Отбор.ГеографическаяЗона) Тогда 
		ЗонаПриОткрытии = Параметры.Отбор.ГеографическаяЗона;
		СписокЗон.АвтоматическоеСохранениеПользовательскихНастроек = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокЗон.Отбор, "Ссылка", ЗонаПриОткрытии, ВидСравненияКомпоновкиДанных.Равно,, Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		Элементы.СписокЗон.Отображение = ОтображениеТаблицы.Список;
		Элементы.СписокЗон.ТекущаяСтрока = ЗонаПриОткрытии;
		УстановленОтборПоЗоне = Истина;
	КонецЕсли;
	
	мсвАдресов = Новый Массив;
	Если Параметры.Отбор.Свойство("Партнер") И ЗначениеЗаполнено(Параметры.Отбор.Партнер) Тогда	
		мсвАдресов = Справочники.упПартнеры.ПолучитьАдресаПартнера(Параметры.Отбор.Партнер);
	КонецЕсли;
	
	Если мсвАдресов.Количество() Тогда
		Список.АвтоматическоеСохранениеПользовательскихНастроек = Ложь;
		
		// добавление в динамический список нового поля для упорядочивания
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Список.ТекстЗапроса);
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
		ЗапросВыбораСхемыЗапроса = ПакетЗапросов[0];
		ОператорВыбратьСхемыЗапроса = ЗапросВыбораСхемыЗапроса.Операторы[0];
		КоличествоПолей = ОператорВыбратьСхемыЗапроса.ВыбираемыеПоля.Количество();
		ОператорВыбратьСхемыЗапроса.ВыбираемыеПоля.Добавить("ВЫБОР КОГДА Ссылка В (&МассивАдресов) ТОГДА 0 ИНАЧЕ 1 КОНЕЦ");
		ЗапросВыбораСхемыЗапроса.Колонки[КоличествоПолей].Псевдоним = "Порядок";
		
		Список.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		Список.Порядок.Элементы.Очистить();
		УсловиеСортировки = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных")); 
		УсловиеСортировки.Поле = Новый ПолеКомпоновкиДанных("Порядок"); 
		УсловиеСортировки.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр; 
		УсловиеСортировки.Использование = Истина; 
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "МассивАдресов", мсвАдресов, Истина);
		УстановитьУсловноеОформлениеСписка();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(ЗонаПриОткрытии) Тогда
		ОтображатьАдресаВоВложенныхЗонах = Истина;
		ПросмотрСписком = Ложь;
	Иначе
		ПросмотрСписком = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ОсновнойАдрес) Тогда
		Элементы.Список.ТекущаяСтрока = ОсновнойАдрес;
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПросмотрСпискомПриИзменении(Элемент)
	
	УстановитьОтборПоЗонеКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗонПриАктивизацииСтроки(Элемент)

	ПодключитьОбработчикОжидания("УстановитьОтборПоЗонеКлиент", 0.3, Истина);	

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьАдресаВоВложенныхЗонахПриИзменении(Элемент)

	УстановитьОтборПоЗонеКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	Список.УсловноеОформление.Элементы.Очистить();
	
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловия = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементУсловия.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Порядок");
	ЭлементУсловия.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементУсловия.ПравоеЗначение = 0;
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.упСреднийШрифт);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.СветлоЖелтый);
	
КонецПроцедуры // УстановитьУсловноеОформлениеСписка()

&НаСервере
Процедура УстановитьОтборПоЗонеСервер(мсвГеографическиеЗоны)
	
	Если мсвГеографическиеЗоны.Количество() = 1 Тогда 
		текЗона = мсвГеографическиеЗоны[0];
		
		Если ОтображатьАдресаВоВложенныхЗонах Тогда 
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВИерархии;
		Иначе	
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГеографическаяЗона", текЗона, ВидСравненияСКД,, Не ПросмотрСписком);
	Иначе 	                                                                                                             
		Если ОтображатьАдресаВоВложенныхЗонах Тогда 
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
		Иначе	
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСписке;
		КонецЕсли;	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ГеографическаяЗона", мсвГеографическиеЗоны, ВидСравненияСКД,, Не ПросмотрСписком);
	КонецЕсли;	
		
	Элементы.СписокЗон.Доступность = Не ПросмотрСписком;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоЗонеКлиент()
	
	мсвГеографическиеЗоны = Новый Массив;
	
	Для каждого текИдентификаторСтроки Из Элементы.СписокЗон.ВыделенныеСтроки Цикл
	
		мсвГеографическиеЗоны.Добавить(текИдентификаторСтроки);
	
	КонецЦикла;
	
	УстановитьОтборПоЗонеСервер(мсвГеографическиеЗоны);
	
КонецПроцедуры

#КонецОбласти
