
#Область ОбластьТекущихПеременных

// Флаг подтверждения, используется при не модальном режиме.
&НаКлиенте
Перем ПодтверждениеЗакрытияФормы;

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТабличныйДокумент = Справочники.упАдреса.ПолучитьМакет("МакетЗагрузки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Если ПодтверждениеЗакрытияФормы <> Истина Тогда
			
			Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормыЗавершение", ЭтотОбъект);
			Отказ = Истина;
			
			Текст = НСтр("ru = 'При закрытии несохраненные данные будут потеряны. Сохранить?'");
			ПоказатьВопрос(Оповещение,Текст,РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да,"Сохранить изменения");
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытием()

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаАдресовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Элементы.ТаблицаАдресов.ТекущиеДанные;
	Если ЗначениеЗаполнено(текСтрока.АдресДубль) И ТипЗнч(текСтрока.АдресДубль) = Тип("СправочникСсылка.упАдреса") Тогда 
		ПоказатьЗначение(, текСтрока.АдресДубль);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАдресовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СнятьПометкиСоВсехАдресов(Команда)
	
	Для Каждого текСтрока Из ТаблицаАдресов Цикл 
		текСтрока.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если ПроверитьЗаполнение() Тогда 
		ЗагрузитьСервер();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьВсеАдреса(Команда)
	
	Для Каждого текСтрока Из ТаблицаАдресов Цикл 
		текСтрока.Пометка = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура РазобратьАдреса(Команда)
	
	ТаблицаАдресов.Очистить();
	
	сткПоискАдрес      = Новый Структура("АдресСтрокой");
	сткПоискКоординаты = Новый Структура("Долгота, Широта");
	Для Сч = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл 
		текЯчейка  = ТабличныйДокумент.Область(Сч, 1, Сч, 1);
		текШирота  = СокрП(ТабличныйДокумент.Область(Сч, 2, Сч, 2).Текст);
		текДолгота = СокрП(ТабличныйДокумент.Область(Сч, 3, Сч, 3).Текст);
		
		АдресСтрокой = Лев(СокрЛП(текЯчейка.Текст), 150);
		Если ЗначениеЗаполнено(АдресСтрокой) Тогда 
			АдресДубль          = "";
			Пометка             = Истина;
			СовпалоНаименование = Ложь;
			СовпалиКоординаты   = Ложь;
			Координаты          = Неопределено;
			
			сткПоискАдрес.АдресСтрокой = АдресСтрокой;
			мсвСтрок = ТаблицаАдресов.НайтиСтроки(сткПоискАдрес);
			Если мсвСтрок.Количество() Тогда 
				Если ЗначениеЗаполнено(мсвСтрок[0].АдресДубль) Тогда 
					АдресДубль = мсвСтрок[0].АдресДубль;
				Иначе	
					АдресДубль = мсвСтрок[0].АдресСтрокой;
				КонецЕсли;	
				Пометка             = Ложь;
				СовпалоНаименование = Истина;
				
				Координаты = Новый Структура("ПолноеПредставление, Долгота, Широта", мсвСтрок[0].ПолноеПредставление, мсвСтрок[0].Долгота, мсвСтрок[0].Широта);
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(текШирота) И ЗначениеЗаполнено(текДолгота) Тогда
				Координаты = Новый Структура("ПолноеПредставление, Долгота, Широта", АдресСтрокой, текДолгота, текШирота);
			КонецЕсли; 
			
			Если Координаты = Неопределено Тогда 
				Координаты = упГеокодированиеКлиентСервер.НайтиКоординатыАдреса(АдресСтрокой);
			КонецЕсли;
			
			Если Координаты <> Неопределено И Не СовпалоНаименование И (Координаты.Широта <> 0 ИЛИ Координаты.Долгота <> 0) Тогда 
				сткПоискКоординаты.Долгота = Координаты.Долгота;
				сткПоискКоординаты.Широта  = Координаты.Широта;
				мсвСтрок = ТаблицаАдресов.НайтиСтроки(сткПоискКоординаты);
				Если мсвСтрок.Количество() Тогда 
					Если ЗначениеЗаполнено(мсвСтрок[0].АдресДубль) Тогда 
						АдресДубль = мсвСтрок[0].АдресДубль;
					Иначе	
						АдресДубль = мсвСтрок[0].АдресСтрокой;
					КонецЕсли;	
					Пометка             = Ложь;
					СовпалиКоординаты   = Истина;
				КонецЕсли;	
			КонецЕсли;	
			
			НоваяСтрока = ТаблицаАдресов.Добавить();
			НоваяСтрока.Пометка             = Пометка;
			НоваяСтрока.АдресСтрокой        = АдресСтрокой;
			НоваяСтрока.АдресДубль          = АдресДубль;
			НоваяСтрока.СовпалоНаименование = СовпалоНаименование;
			НоваяСтрока.СовпалиКоординаты   = СовпалиКоординаты;
			НоваяСтрока.Комментарий         = Комментарий;
			
			Если Координаты <> Неопределено Тогда 
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Координаты);
			КонецЕсли;
		КонецЕсли;
		
		Состояние(НСтр("ru = 'Разбор адресов'"), Цел(Сч * 100/ ТабличныйДокумент.ВысотаТаблицы), НСтр("ru = 'Поиск координат и дублирующихся аресов'"), БиблиотекаКартинок.Информация32);
	КонецЦикла;	
	
	РазобратьАдресаНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Общий обработчик подтверждения закрытия.
//
// Параметры:
//  РезультатВопроса  - КодВозвратаДиалога - Результат ответа.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ЗакрытиеФормыЗавершение(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗагрузитьСервер();		
		// Закрываем без дополнительных вопросов.
		ПодтверждениеЗакрытияФормы = Истина;
		Закрыть();
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		// Закрываем без дополнительных вопросов.
		ПодтверждениеЗакрытияФормы = Истина;
		Закрыть();
	Иначе 
		ПодтверждениеЗакрытияФормы = Неопределено;
	КонецЕсли;
	
КонецПроцедуры // ЗакрытиеФормыЗавершение()

&НаСервере
Процедура ЗагрузитьСервер()
	
	Для Каждого текСтрока Из ТаблицаАдресов Цикл 
		Если текСтрока.Пометка Тогда
			НовыйОбъект = Справочники.упАдреса.СоздатьЭлемент();
			НовыйОбъект.Наименование        = текСтрока.АдресСтрокой;
			НовыйОбъект.ВидАдреса           = ВидАдреса;
			НовыйОбъект.ПолноеПредставление = текСтрока.ПолноеПредставление;
			НовыйОбъект.ПолноеНаименование  = текСтрока.ПолноеПредставление;
			НовыйОбъект.Долгота             = текСтрока.Долгота;
			НовыйОбъект.Широта              = текСтрока.Широта;
			НовыйОбъект.Комментарий         = текСтрока.Комментарий;
			
			Попытка
				НовыйОбъект.Записать();
				текСтрока.Пометка = Ложь;
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось записать адрес ""%1"" : %2'"), текСтрока.АдресСтрокой, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
			КонецПопытки;	
		КонецЕсли;	
	КонецЦикла;	
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура РазобратьАдресаНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВЫРАЗИТЬ(втАдресаСтрокой.АдресСтрокой КАК СТРОКА(150)) КАК АдресСтрокой,
	|	втАдресаСтрокой.Долгота,
	|	втАдресаСтрокой.Широта
	|ПОМЕСТИТЬ втАдреса
	|ИЗ
	|	&втАдресаСтрокой КАК втАдресаСтрокой
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАдреса.АдресСтрокой КАК АдресСтрокой,
	|	упАдреса.Ссылка КАК АдресДубльНаименование,
	|	упАдресаКоординаты.Ссылка КАК АдресДубльКоординаты,
	|	ВЫБОР
	|		КОГДА упАдреса.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СовпалоНаименование,
	|	ВЫБОР
	|		КОГДА упАдресаКоординаты.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СовпалиКоординаты
	|ИЗ
	|	втАдреса КАК втАдреса
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдреса
	|		ПО втАдреса.АдресСтрокой = упАдреса.Наименование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК упАдресаКоординаты
	|		ПО втАдреса.Долгота = упАдресаКоординаты.Долгота
	|			И втАдреса.Широта = упАдресаКоординаты.Широта
	|ГДЕ
	|	(НЕ упАдреса.Ссылка ЕСТЬ NULL 
	|			ИЛИ НЕ упАдресаКоординаты.Ссылка ЕСТЬ NULL )
	|
	|УПОРЯДОЧИТЬ ПО
	|	АдресСтрокой,
	|	СовпалоНаименование УБЫВ,
	|	СовпалиКоординаты УБЫВ";
	Запрос.УстановитьПараметр("втАдресаСтрокой", ТаблицаАдресов.Выгрузить());
	
	сткПоиска = Новый Структура("АдресСтрокой");
	
	тбзДубли = Запрос.Выполнить().Выгрузить();
	Для Каждого текСтрока Из тбзДубли Цикл 
		текСтрока.АдресСтрокой = СокрЛП(текСтрока.АдресСтрокой);
	КонецЦикла;
	
	Для Каждого текСтрока Из ТаблицаАдресов Цикл
		сткПоиска.АдресСтрокой = текСтрока.АдресСтрокой;
		
		мсвСтрок = тбзДубли.НайтиСтроки(сткПоиска);
		Если мсвСтрок.Количество() Тогда 
			Если мсвСтрок[0].СовпалоНаименование Тогда 
				текСтрока.АдресДубль          = мсвСтрок[0].АдресДубльНаименование;
				текСтрока.СовпалоНаименование = Истина;
				текСтрока.Пометка             = Ложь;
			ИначеЕсли мсвСтрок[0].СовпалиКоординаты И Не текСтрока.СовпалоНаименование Тогда 
				текСтрока.АдресДубль          = мсвСтрок[0].АдресДубльКоординаты;
				текСтрока.СовпалиКоординаты   = Истина;
				текСтрока.Пометка             = Ложь;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти 