#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает коэффициент для расчета времени в пути.
//
// Параметры:
//  Адрес  - СправочникСсылка - адрес.
//  ДеньНедели  - Число - День недели.
//
// Возвращаемое значение:
//   Число - Коэффициент.
//
Функция _ПолучитьКоэффициентВремениРабочейЗоны(Адрес, ДеньНедели) Экспорт
	
	текЗапрос = Новый Запрос;
	текЗапрос.Текст = "ВЫБРАТЬ
	|	упГеографическиеЗоныКоэффициентыВремени.КоэффициентРасчетаВремениВПути КАК Коэффициент
	|ИЗ
	|	Справочник.упГеографическиеЗоны.КоэффициентыВремени КАК упГеографическиеЗоныКоэффициентыВремени
	|ГДЕ
	|	упГеографическиеЗоныКоэффициентыВремени.Ссылка = &Ссылка
	|	И упГеографическиеЗоныКоэффициентыВремени.ДеньНедели = &ДеньНедели";
	текЗапрос.УстановитьПараметр("Ссылка", 		Адрес);
	текЗапрос.УстановитьПараметр("ДеньНедели", 	ДеньНедели);
	
	текРезультат = 1;
	
	текВыборка = текЗапрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		текРезультат = текВыборка.Коэффициент;	
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
//
// Параметры:
//  Адрес  - СправочникСсылка - адрес.
//  ВидЗоны  - ПеречислениеСсылка.упВидыЗон - Вид зоны.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.упВидыЗон - Результат выполнения.
//
Функция ПолучитьПредставлениеАдреса(Адрес, ВидЗоны) Экспорт

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Адрес, ОбщегоНазначения.ИмяЗначенияПеречисления(ВидЗоны));

КонецФункции // ПолучитьПредставлениеАдреса()

// Возвращает список подобных параметру адресов.
// 
// Параметры:
//  Текст  - Строка - текст.
//
// Возвращаемое значение:
//   СписокЗначений - Результат выполнения.
//
Функция СформироватьДанныеВыбораАдреса(Текст) Экспорт
	
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упАдреса.Ссылка,
	|	0 КАК Рейтинг
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Наименование ПОДОБНО &Наименование_П
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упАдреса.Ссылка,
	|	1
	|ИЗ
	|	Справочник.упАдреса КАК упАдреса
	|ГДЕ
	|	упАдреса.Наименование = &Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Рейтинг УБЫВ";
	Запрос.УстановитьПараметр("Наименование_П", "%"+Текст+"%");
	Запрос.УстановитьПараметр("Наименование", Текст);
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		МассивРезультат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		Результат.ЗагрузитьЗначения(МассивРезультат);
	КонецЕсли;
	Возврат Результат;

КонецФункции // СформироватьДанныеВыбораАдреса()

// Возвращает массив зон для указанных адресов.
//
// Параметры:
//  МассивАдресов  - Массив - ссылки.
//
// Возвращаемое значение:
//   Массив - Результат выполнения.
//
Функция ПолучитьОбщиеЗоныАдресов(МассивАдресов) Экспорт
	
	 мсвОбщихЗон = Новый Массив;
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	упАдреса.Ссылка КАК Адрес,
	 |	упИерархияГеографическихЗон.Родитель,
	 |	упИерархияГеографическихЗон.Уровень
	 |ПОМЕСТИТЬ втЗонаАдресов
	 |ИЗ
	 |	Справочник.упАдреса КАК упАдреса
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	 |		ПО (упАдреса.Ссылка В (&МассивАдресов))
	 |			И упАдреса.ГеографическаяЗона = упИерархияГеографическихЗон.Зона
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втЗонаАдресов.Адрес) КАК КоличествоАдресов
	 |ПОМЕСТИТЬ втКоличествоАдресов
	 |ИЗ
	 |	втЗонаАдресов КАК втЗонаАдресов
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	втЗонаАдресов.Родитель
	 |ПОМЕСТИТЬ втОбщиеЗоны
	 |ИЗ
	 |	втЗонаАдресов КАК втЗонаАдресов
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	втЗонаАдресов.Родитель
	 |
	 |ИМЕЮЩИЕ
	 |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втЗонаАдресов.Адрес) В
	 |		(ВЫБРАТЬ
	 |			втКоличествоАдресов.КоличествоАдресов
	 |		ИЗ
	 |			втКоличествоАдресов)
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	втОбщиеЗоны.Родитель КАК Зона,
	 |	МИНИМУМ(втЗонаАдресов.Уровень) КАК Уровень
	 |ИЗ
	 |	втОбщиеЗоны КАК втОбщиеЗоны
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗонаАдресов КАК втЗонаАдресов
	 |		ПО втОбщиеЗоны.Родитель = втЗонаАдресов.Родитель
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	втОбщиеЗоны.Родитель
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	Уровень УБЫВ";
	 Запрос.УстановитьПараметр("МассивАдресов", МассивАдресов);
	 Результат = Запрос.Выполнить();
	 Если Не Результат.Пустой() Тогда
		 Выборка = Результат.Выбрать();
		 Пока Выборка.Следующий() Цикл
			 мсвОбщихЗон.Добавить(Выборка.Зона);
		 КонецЦикла;
	 КонецЕсли; 
	 
	 Возврат мсвОбщихЗон;
	
КонецФункции // ПолучитьОбщиеЗоныАдресов()

// Проверим доступны ли роли текущему пользователю.
//
// Возвращаемое значение:
//   Булево - Разрешено редактировать.
//
Функция ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных() Экспорт
	
	Результат = Ложь;
	
	Если РольДоступна("упПодтверждениеКорректностиГеографическихДанных") Тогда
		Результат = Истина;
	ИначеЕсли РольДоступна("ПолныеПрава") Тогда	
		Результат = Истина;
	ИначеЕсли РольДоступна("упРазработчик") Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных()

// Процедура - Применить новые координаты для адреса
//
// Параметры:
//  Адрес			 - СправочникСсылка.упАдреса	 - адрес точки.
//  НоваяШирота	 - Число	 - новая широта.
//  НоваяДолгота	 - Число	 - новая долгота.
//  Отказ			 - Булево	 - Истина, если произошла ошибка при записи адреса.
//
Процедура ПрименитьНовыеКоординатыАдреса(Адрес, НоваяШирота, НоваяДолгота, Отказ) Экспорт
	Если ЗначениеЗаполнено(Адрес) Тогда
		АдресОбъект = Адрес.ПолучитьОбъект();	
		АдресОбъект.Широта = НоваяШирота;
		АдресОбъект.Долгота = НоваяДолгота;
		Попытка
			АдресОбъект.Записать();
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Адрес,,,Отказ);
			ЗаписьЖурналаРегистрации("Запись адреса", УровеньЖурналаРегистрации.Ошибка, ,Адрес, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Функция - Подбирает адрес по переданному наименованию, если такого нет, то
//           создает новый адрес.
//
// Параметры:
//  АдресСтрока - Строка	 - наименование адреса.
//  ВидАдреса	 - СправочникСсылка.упВидыАдресов - вид адреса по умолчанию.
// 
// Возвращаемое значение:
//  СправочникСсылка.упАдреса - подобранный или созданный адрес.
//
Функция НовыйАдресПоСтроке(АдресСтрока, ВидАдреса = Неопределено) Экспорт
	
	Результат = Справочники.упАдреса.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(АдресСтрока) Тогда
		СписокЗначений = СформироватьДанныеВыбораАдреса(АдресСтрока);
		Если СписокЗначений.Количество() Тогда
			Результат = СписокЗначений[0].Значение;
		Иначе
			НовыйАдрес = Справочники.упАдреса.СоздатьЭлемент();
			НовыйАдрес.Наименование = АдресСтрока;
			НовыйАдрес.ВидАдреса = ВидАдреса;
			НовыйАдрес.ПолноеНаименование = АдресСтрока;
			НовыйАдрес.ПолноеПредставление = АдресСтрока;
			НовыйАдрес.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
			Попытка
				НовыйАдрес.Записать();
				Результат = НовыйАдрес.Ссылка;
			Исключение
				ТекстОшибки = СтрШаблон(Нстр("ru = 'При создании адреса:%1 произошла ошибка:%2'"), АдресСтрока, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание адреса'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				 					УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			Конецпопытки;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
