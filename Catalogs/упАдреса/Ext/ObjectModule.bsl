#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Нет.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПравоРедактировать = Справочники.упАдреса.ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных();
	
	Если ДополнительныеСвойства.Свойство("ЗаписьПриИнтеграции") Тогда
		ЕстьПравоРедактировать = Истина;
	КонецЕсли; 
	
	Если НЕ ЕстьПравоРедактировать И Подтвержден Тогда
		ТекстОшибки = СтрШаблон("У вас нет права корректировать адрес: %1. Адрес подтвержден!!!",Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("упНормализоватьАдресаСервисомDadata") Тогда
		НормализованноеНаименованиеЛ = упГеокодированиеСервер.НормализованныйАдрес(Наименование);
		Если ЗначениеЗаполнено(НормализованноеНаименованиеЛ) Тогда
			ЭтотОбъект.НормализованноеНаименование = НормализованноеНаименованиеЛ;
		КонецЕсли;
	КонецЕсли; 
	Если НЕ ПометкаУдаления Тогда
		
		Наименование = СокрЛП(Наименование);
		Наименование = СтрЗаменить(Наименование, Символы.ПС, "");
		Наименование = СтрЗаменить(Наименование, Символы.НПП, " ");
		
		Если Широта = 0 И Долгота = 0
			И ПолучитьФункциональнуюОпцию("упИспользуетсяГеокодированиеАдресов") Тогда 
			
			Структура = Новый Структура("Город,Дом,НаселенныйПункт,Район,Страна,Строение,Улица,Индекс");
			ЗаполнитьЗначенияСвойств(Структура,ЭтотОбъект);
			Структура.Вставить("Регион",СокрЛП(Регион));
			Структура.Вставить("Страна",СокрЛП(Страна));
			Структура.Вставить("КодАльфа2",?(ЗначениеЗаполнено(Страна),Страна.КодАльфа2,""));
			
			Если ЗначениеЗаполнено(НормализованноеНаименование) И ПолучитьФункциональнуюОпцию("упНормализоватьАдресаСервисомDadata") Тогда
				СтрокаПоиска = НормализованноеНаименование;
			ИначеЕсли ЗначениеЗаполнено(ПолноеНаименование) Тогда 
				СтрокаПоиска = ПолноеНаименование;
			Иначе
				СтрокаПоиска = Наименование;
			КонецЕсли;
			
			Структура.Вставить("Представление", СтрокаПоиска);
			
			Если ЗначениеЗаполнено(Структура) Тогда 
				сткКоординаты = упГеокодированиеСервер.НайтиКоординатыАдреса(Структура);
				
				Если НЕ сткКоординаты = Неопределено Тогда 
					ЗаполнитьЗначенияСвойств(ЭтотОбъект, сткКоординаты);
					
					Если НЕ ЗначениеЗаполнено(ПолноеНаименование) Тогда 
						ПолноеНаименование = ПолноеПредставление;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если (Широта <> 0 ИЛИ Долгота <> 0) И Не ЗначениеЗаполнено(ГеографическаяЗона) Тогда 
			сткГеозона = Справочники.упГеографическиеЗоны.ВернутьГеографическуюЗону(Долгота, Широта);
			Если сткГеозона <> Неопределено Тогда
				ГеографическаяЗона = сткГеозона.Зона;
				Если ЗначениеЗаполнено(сткГеозона.Страна) И сткГеозона.Страна <> Страна Тогда 
					Страна = сткГеозона.Страна;
					Регион = Неопределено;
				КонецЕсли;	
				Если ЗначениеЗаполнено(сткГеозона.Регион) Тогда 
					Регион = сткГеозона.Регион;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Широта) И ЗначениеЗаполнено(Долгота) Тогда
			ПредставлениеКоординаты = упСервисныеФункцииКлиентСервер.СформироватьПредставлениеКоординаты(Широта, Долгота);
		Иначе
			ПредставлениеКоординаты = "";
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)

	НаборЗаписей = РегистрыСведений.упРасстоянияМеждуАдресами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НачальныйАдрес.Установить(Ссылка);
	НаборЗаписей.Записать(Истина);

	НаборЗаписей = РегистрыСведений.упРасстоянияМеждуАдресами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КонечныйАдрес.Установить(Ссылка);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли


