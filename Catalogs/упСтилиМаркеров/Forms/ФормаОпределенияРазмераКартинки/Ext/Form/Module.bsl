
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	HTMLДокумент = Справочники.упСтилиМаркеров.ПолучитьМакет("HTML_ОпределениеРазмераКартинки").ПолучитьТекст();
	ИмяВременногоФайла = Параметры.ИмяФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИмяФайла = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ДлительнаяОперация48);
	HTMLДокумент = СтрЗаменить(HTMLДокумент, "%Картинка%", ИмяФайла);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	
	ПодключитьОбработчикОжидания("ОпределитьРазмерИзображения", 1, Истина);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте	
Процедура ОпределитьРазмерИзображения()

	ВыполнитьПроцедуруHTML("setImageDimention(""" + ИмяВременногоФайла + """)");
	ПодключитьОбработчикОжидания("УстановитьРазмерИзображения", 0.4, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРазмерИзображения()
	
	сткРазмеры = Новый Структура("Ширина, Высота", 0, 0);
	стрВозврат = ВыполнитьФункциюHTML("getImageDimention()");
	мсвРазмеры = СтрРазделить(стрВозврат, ";", Ложь);
	Если ТипЗнч(мсвРазмеры) = Тип("Массив") И мсвРазмеры.Количество() = 2 Тогда
		сткРазмеры.Ширина = мсвРазмеры[0];
		сткРазмеры.Высота = мсвРазмеры[1];
	КонецЕсли;

	Закрыть(сткРазмеры);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроцедуруHTML(текстСкрипта)	
	
	Попытка
		Элементы.HTMLДокумент.Документ.parentWindow.eval(текстСкрипта);	
	Исключение
		ПоказатьОповещениеПользователя(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;	
	
КонецПроцедуры

&НаКлиенте
Функция ВыполнитьФункциюHTML(текстСкрипта)
	
	Попытка
		Возврат Элементы.HTMLДокумент.Документ.parentWindow.eval(текстСкрипта);
	Исключение
		ПоказатьОповещениеПользователя(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат "";
	КонецПопытки;	
	
КонецФункции

#КонецОбласти 


