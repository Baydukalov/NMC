#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если НЕ ЭтоГруппа Тогда
		Если НЕ ЗначениеЗаполнено(ВариантОпределенияВременногоОкнаОтправления) Тогда
			ВариантОпределенияВременногоОкнаОтправления = Перечисления.упВариантыОпределенияВременныхОкон.ПолноеОграничение;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВариантОпределенияВременногоОкнаПолучения) Тогда
			ВариантОпределенияВременногоОкнаПолучения = Перечисления.упВариантыОпределенияВременныхОкон.ПолноеОграничение;
		КонецЕсли;
		
		Для каждого Строка Из ПромежуточныеТочкиМаршрута Цикл
			
			Если НЕ ЗначениеЗаполнено(Строка.ВариантОпределенияВременногоОкнаОтправления) Тогда
				Строка.ВариантОпределенияВременногоОкнаОтправления = Перечисления.упВариантыОпределенияВременныхОкон.ПолноеОграничение;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Строка.ВариантОпределенияВременногоОкнаПолучения) Тогда
				Строка.ВариантОпределенияВременногоОкнаПолучения = Перечисления.упВариантыОпределенияВременныхОкон.ПолноеОграничение;
			КонецЕсли;
			
			Если Истина
				И НЕ ОпределятьВременныеОкнаЗаданий
				И НЕ ФормироватьВременныеОкнаПоГрафикамРаботыАдресов Тогда
				
				Если ЗначениеЗаполнено(Строка.ГрафикРаботыАдресаОтправления) Тогда
					Строка.ГрафикРаботыАдресаОтправления = Справочники.Календари.ПустаяСсылка();
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Строка.ГрафикРаботыАдресаПолучения) Тогда
					Строка.ГрафикРаботыАдресаПолучения = Справочники.Календари.ПустаяСсылка();
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПроверитьСовместимостьВидовПеревозок(Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСовместимостьВидовПеревозок(Отказ)
	
	мсвВидыПеревозок = Новый Массив;
	Если ЗначениеЗаполнено(ВидПеревозки) Тогда
		мсвВидыПеревозок.Добавить(ВидПеревозки);
	КонецЕсли;
	
	Для каждого ПромежуточнаяТочка Из ПромежуточныеТочкиМаршрута Цикл
		Если ЗначениеЗаполнено(ПромежуточнаяТочка.ВидПеревозки) Тогда
			мсвВидыПеревозок.Добавить(ПромежуточнаяТочка.ВидПеревозки);
		КонецЕсли;
	КонецЦикла;

	сткРеквизитыОбъекта = Новый Структура();
	сткРеквизитыОбъекта.Вставить("ОбъектПроверки", Ссылка);
	сткРеквизитыОбъекта.Вставить("ВидыПеревозок", мсвВидыПеревозок);
	
	Если НЕ Справочники.упВидыПеревозок.ВидыПеревозокСовместимы(сткРеквизитыОбъекта)  Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(сткРеквизитыОбъекта.ТекстСообщения,ЭтотОбъект,,,Отказ);
		ЗаписьЖурналаРегистрации(Справочники.упСхемыМультимодальныхПеревозок.СобытиеЖРПроверкаСовместимостиВидовПеревозок(), 
			УровеньЖурналаРегистрации.Предупреждение,
			,
			, 
			сткРеквизитыОбъекта.ТекстСообщения,
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти 