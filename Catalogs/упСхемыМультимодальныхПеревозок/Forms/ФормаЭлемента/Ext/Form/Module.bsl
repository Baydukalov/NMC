
#Область ПеременныеМодуляФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И Не ЗначениеЗаполнено(Объект.ОрганизацияОтбор) Тогда 
		Объект.ОрганизацияОтбор = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;	
	
	ЗаблокироватьЭлементы();
	ОбновитьВидимостьНаСервере();
	
	КоличествоПромежуточныхТочек = Объект.ПромежуточныеТочкиМаршрута.Количество();
	Если КоличествоПромежуточныхТочек > 0 Тогда
		Для Сч = 1 По КоличествоПромежуточныхТочек Цикл
			ДобавитьТочкуМаршрутаНаСервере(Сч, Ложь, Ложь);
		КонецЦикла; 
	КонецЕсли;
		
	УстановитьДоступностьКнопокПеремещения();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ШрифтВидаПеревозки          = Элементы.ВидПеревозкиПредставление.Шрифт;
	ШрифтПредставленияАдреса    = Элементы.ПредставлениеНачалаМаршрута.Шрифт;
	
	КартинкаВыбораВидаПеревозки        = БиблиотекаКартинок.упЗаполнитьКоричневый;
	КартинкаПромежуточнаяТочкаМаршрута = БиблиотекаКартинок.упПромежуточнаяТочкаМаршрута;
	КартинкаСледующаяТочкаМаршрута     = БиблиотекаКартинок.упПеремещениеВниз;

	ЦветТекстаВидаПеревозки     = Новый Цвет(97, 65, 30);
	ЦветФонаПредставленияТочки  = Новый Цвет(255, 232, 179);
	ЦветРамкиВидаПеревозки      = Новый Цвет(255, 255, 255);
	ЦветФонаГруппыТочкиМаршрута = Новый Цвет(255, 255, 205);

	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьВидимостьВременныхОкон();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АдресОтправленияПриИзменении(Элемент)
	
	АдресОтправленияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПолученияПриИзменении(Элемент)
	
	АдресПолученияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ОпределятьВременныеОкнаЗаданийПриИзменении(Элемент)
	ОбновитьВидимостьВременныхОкон();	
	Если НЕ Объект.ОпределятьВременныеОкнаЗаданий Тогда
		Объект.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов = Ложь;
	КонецЕсли; 
	ЗаблокироватьЭлементыНаКлиенте();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СформироватьНаименованиеСхемы(Команда)
	
	ПредставлениеНаименования = Объект.ПредставлениеНачалаМаршрута;
	Для Сч = 0 По КоличествоПромежуточныхТочек - 1 Цикл
		ПредставлениеНаименования = СтрШаблон("%1 - %2", ПредставлениеНаименования, Объект.ПромежуточныеТочкиМаршрута[Сч].Представление);
	КонецЦикла;
	ПредставлениеНаименования = СтрШаблон("%1 - %2", ПредставлениеНаименования, Объект.ПредставлениеОкончанияМаршрута);
	Объект.Представление = ПредставлениеНаименования;
	Объект.Наименование = ПредставлениеНаименования;
	Модифицированность = Истина;
	
КонецПроцедуры // СформироватьНаименованиеСхемы()

&НаКлиенте
Процедура ДобавитьТочкуМаршрута(Команда)
	
	КоличествоПромежуточныхТочек = КоличествоПромежуточныхТочек + 1;
	ДобавитьТочкуМаршрутаНаСервере(КоличествоПромежуточныхТочек);
	Модифицированность = Истина;
	
КонецПроцедуры // ДобавитьТочкуМаршрута()

&НаКлиенте
Процедура УдалитьТочкуМаршрута(Команда)
	
	УдалитьТочкуМаршрутаНаСервере();
	Модифицированность = Истина;
	
КонецПроцедуры // УдалитьТочкуМаршрута()

&НаКлиенте
Процедура УказатьПараметрыЗаполненияПеревозки(Команда)
	
	НомерЗаполняемойТочки = Число(СтрЗаменить(ТекущийЭлемент.Имя, "КомандаУказатьПараметры", ""));
	СтрокаТочки = Объект.ПромежуточныеТочкиМаршрута.Получить(НомерЗаполняемойТочки - 1);
	сткПараметрыПеревозки = Новый Структура;
	сткПараметрыПеревозки.Вставить("Отправитель");
	сткПараметрыПеревозки.Вставить("Получатель");
	сткПараметрыПеревозки.Вставить("КонтактноеЛицоОтправителя");
	сткПараметрыПеревозки.Вставить("КонтактноеЛицоПолучателя");
	сткПараметрыПеревозки.Вставить("КонтрагентОтправителя");
	сткПараметрыПеревозки.Вставить("КонтрагентПолучателя");
	сткПараметрыПеревозки.Вставить("НомерСтроки");
	сткПараметрыПеревозки.Вставить("ВремяВПути");
	сткПараметрыПеревозки.Вставить("ЧасовойПояс");
	сткПараметрыПеревозки.Вставить("ВремяНахожденияВТочке");
	сткПараметрыПеревозки.Вставить("ВариантОпределенияВременногоОкнаПолучения");
	сткПараметрыПеревозки.Вставить("ГрафикРаботыАдресаПолучения");
	сткПараметрыПеревозки.Вставить("ВариантОпределенияВременногоОкнаОтправления");
	сткПараметрыПеревозки.Вставить("ГрафикРаботыАдресаОтправления");
	ЗаполнитьЗначенияСвойств(сткПараметрыПеревозки, СтрокаТочки);
	сткПараметрыПеревозки.Вставить("ОпределятьВременныеОкнаЗаданий", Объект.ОпределятьВременныеОкнаЗаданий);
	сткПараметрыПеревозки.Вставить("ФормироватьВременныеОкнаПоГрафикамРаботыАдресов", Объект.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов);
	ОткрытьФорму("Справочник.упСхемыМультимодальныхПеревозок.Форма.ФормаНастроекПеревозки", Новый Структура("ПараметрыПеревозки", сткПараметрыПеревозки),,,,,Новый ОписаниеОповещения("УказаниеПараметровПеревозкиЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьТочкуМаршрутаВверх(Команда)
	
	ПереместитьТочкуМаршрутаВверхНаСервере();
	
КонецПроцедуры // ПереместитьТочкуМаршрутаВверх()

&НаКлиенте
Процедура ПереместитьТочкуМаршрутаВниз(Команда)
	
	ПереместитьТочкуМаршрутаВнизНаСервере();
	
КонецПроцедуры // ПереместитьТочкуМаршрутаВниз()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АдресОтправленияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.АдресОтправления) Тогда
		Объект.ЗонаОтправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресОтправления, "ГеографическаяЗона");
		Элементы.ЗонаОтправления.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ЗонаОтправления.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры // АдресОтправленияПриИзмененииНаСервере()

&НаСервере
Процедура АдресПолученияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.АдресПолучения) Тогда
		Объект.ЗонаПолучения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.АдресПолучения, "ГеографическаяЗона");
		Элементы.ЗонаПолучения.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ЗонаПолучения.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры // АдресПолученияПриИзмененииНаСервере()

&НаСервере
Процедура ДобавитьТочкуМаршрутаНаСервере(НомерТочки, ДобавлятьСтроку = Истина, ОбновлятьДоступность = Истина)
	
	Если ДобавлятьСтроку Тогда
		НоваяСтрока = Объект.ПромежуточныеТочкиМаршрута.Добавить();
	КонецЕсли;
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	РодительТочек = Элементы.ГруппаПромежуточныеТочки;
	
	НоваяГруппаТочки = Элементы.Добавить(СтрШаблон("ГруппаПромежуточнаяТочка%1", НомерТочки), Тип("ГруппаФормы"), РодительТочек);
	НоваяГруппаТочки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаТочки.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаТочки.ОтображатьЗаголовок = Ложь;
	НоваяГруппаТочки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	// вид перевозки и направление
	Если ПолучитьФункциональнуюОпцию("упИспользуютсяРазличныеВидыПеревозок") Тогда 
		НовоеПолеВидПеревозки = Элементы.Добавить(СтрШаблон("ВидПеревозкиПромежуточнойТочки%1", НомерТочки), Тип("ПолеФормы"), НоваяГруппаТочки);
		НовоеПолеВидПеревозки.Заголовок = НСтр("ru = 'Вид перевозки'", КодЯзыка);
		НовоеПолеВидПеревозки.Вид = ВидПоляФормы.ПолеВвода;
		НовоеПолеВидПеревозки.ПодсказкаВвода = НСтр("ru = 'вид перевозки определяется в заявке'", КодЯзыка);
		НовоеПолеВидПеревозки.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовоеПолеВидПеревозки.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		НовоеПолеВидПеревозки.ЦветРамки = ЦветРамкиВидаПеревозки;
		НовоеПолеВидПеревозки.ЦветТекста = ЦветТекстаВидаПеревозки;
		НовоеПолеВидПеревозки.Шрифт = ШрифтВидаПеревозки;
		НовоеПолеВидПеревозки.КнопкаВыбора = Истина;
		НовоеПолеВидПеревозки.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВПолеВвода;
		НовоеПолеВидПеревозки.КартинкаКнопкиВыбора = КартинкаВыбораВидаПеревозки;
		НовоеПолеВидПеревозки.КнопкаВыпадающегоСписка = Ложь;
		НовоеПолеВидПеревозки.КнопкаОткрытия = Ложь;
		НовоеПолеВидПеревозки.АвтоМаксимальнаяШирина = Ложь;
		НовоеПолеВидПеревозки.ПутьКДанным = СтрШаблон("Объект.ПромежуточныеТочкиМаршрута[%1].ВидПеревозки", НомерТочки - 1);
	Иначе
		Если НоваяСтрока <> Неопределено Тогда
			НоваяСтрока.ВидПеревозки = Справочники.упВидыПеревозок.Основной;
		КонецЕсли;
	КонецЕсли;	
	
	НоваяГруппаДекорация = Элементы.Добавить(СтрШаблон("ГруппаДекорация%1", НомерТочки), Тип("ГруппаФормы"), НоваяГруппаТочки);
	НоваяГруппаДекорация.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаДекорация.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаДекорация.ОтображатьЗаголовок = Ложь;
	НоваяГруппаДекорация.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	НоваяГруппаДекорация.Высота = 1;
	
	НовоеНаправление = Элементы.Добавить(СтрШаблон("ДекорацияНаправление%1", НомерТочки), Тип("ДекорацияФормы"), НоваяГруппаДекорация);
	НовоеНаправление.Вид = ВидДекорацииФормы.Картинка;
	НовоеНаправление.Картинка = КартинкаСледующаяТочкаМаршрута;
	НовоеНаправление.РастягиватьПоГоризонтали = Истина;
	
	НоваяПустаяДекорация = Элементы.Добавить(СтрШаблон("ПустаяДекорация%1", НомерТочки), Тип("ДекорацияФормы"), НоваяГруппаДекорация);
	НоваяПустаяДекорация.Вид = ВидДекорацииФормы.Картинка;
	НоваяПустаяДекорация.Ширина = 2;
	НоваяПустаяДекорация.Высота = 1;
	НоваяПустаяДекорация.АвтоМаксимальнаяВысота = Ложь;
	НоваяПустаяДекорация.РастягиватьПоВертикали = Ложь;
	
	// основная группа
	НоваяГруппаОсновная = Элементы.Добавить(СтрШаблон("ГруппаОсновная%1", НомерТочки), Тип("ГруппаФормы"), НоваяГруппаТочки);
	НоваяГруппаОсновная.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаОсновная.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаОсновная.ОтображатьЗаголовок = Ложь;
	НоваяГруппаОсновная.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	НоваяГруппаОсновная.ЦветФона = ЦветФонаГруппыТочкиМаршрута;
	
	// декорация
	НоваяДекорация = Элементы.Добавить(СтрШаблон("ДекорацияПромежуточнойТочки%1", НомерТочки), Тип("ДекорацияФормы"), НоваяГруппаОсновная);
	НоваяДекорация.Вид = ВидДекорацииФормы.Картинка;
	НоваяДекорация.Картинка = КартинкаПромежуточнаяТочкаМаршрута;
	НоваяДекорация.Высота = 2;
	НоваяДекорация.Ширина = 6;
	
	// реквизиты
	НоваяГруппаРеквизитыТочки = Элементы.Добавить(СтрШаблон("ГруппаРеквизитыПромежуточнаяТочка%1", НомерТочки), Тип("ГруппаФормы"), НоваяГруппаОсновная);
	НоваяГруппаРеквизитыТочки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаРеквизитыТочки.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаРеквизитыТочки.ОтображатьЗаголовок = Ложь;
	НоваяГруппаРеквизитыТочки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовоеПолеПредставление = Элементы.Добавить(СтрШаблон("ПредставлениеПромежуточнойТочки%1", НомерТочки), Тип("ПолеФормы"), НоваяГруппаРеквизитыТочки);
	НовоеПолеПредставление.Вид = ВидПоляФормы.ПолеВвода;
	НовоеПолеПредставление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовоеПолеПредставление.ПутьКДанным = СтрШаблон("Объект.ПромежуточныеТочкиМаршрута[%1].Представление", НомерТочки - 1);
	НовоеПолеПредставление.ПодсказкаВвода = НСтр("ru = 'укажите представление точки маршрута'", КодЯзыка);
	НовоеПолеПредставление.ЦветФона = ЦветФонаПредставленияТочки;
	НовоеПолеПредставление.Шрифт = ШрифтПредставленияАдреса;
	НовоеПолеПредставление.АвтоМаксимальнаяШирина = Ложь;
	
	НовоеПолеАдрес = Элементы.Добавить(СтрШаблон("АдресПромежуточнойТочки%1", НомерТочки), Тип("ПолеФормы"), НоваяГруппаРеквизитыТочки);
	НовоеПолеАдрес.Заголовок = НСтр("ru = 'Адрес'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	НовоеПолеАдрес.Вид = ВидПоляФормы.ПолеВвода;
	НовоеПолеАдрес.ПутьКДанным = СтрШаблон("Объект.ПромежуточныеТочкиМаршрута[%1].Адрес", НомерТочки - 1);
	НовоеПолеАдрес.АвтоМаксимальнаяШирина = Ложь;
	
	// команды
	НоваяГруппаКомандыТочки = Элементы.Добавить(СтрШаблон("ГруппаКомандыПромежуточнаяТочка%1", НомерТочки), Тип("ГруппаФормы"), НоваяГруппаОсновная);
	НоваяГруппаКомандыТочки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаКомандыТочки.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаКомандыТочки.ОтображатьЗаголовок = Ложь;
	НоваяГруппаКомандыТочки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НоваяГруппаКомандыТочкиВерх = Элементы.Добавить(СтрШаблон("ГруппаКомандыВерхПромежуточнаяТочка%1", НомерТочки), Тип("ГруппаФормы"), НоваяГруппаКомандыТочки);
	НоваяГруппаКомандыТочкиВерх.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаКомандыТочкиВерх.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаКомандыТочкиВерх.ОтображатьЗаголовок = Ложь;
	НоваяГруппаКомандыТочкиВерх.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	НоваяКомандаУдалитьТочку = Элементы.Добавить(СтрШаблон("КомандаУдалитьТочку%1", НомерТочки), Тип("КнопкаФормы"), НоваяГруппаКомандыТочкиВерх);
	НоваяКомандаУдалитьТочку.ИмяКоманды = "УдалитьТочкуМаршрута";
	
	НоваяКомандаУказатьПараметры = Элементы.Добавить(СтрШаблон("КомандаУказатьПараметры%1", НомерТочки), Тип("КнопкаФормы"), НоваяГруппаКомандыТочкиВерх);
	НоваяКомандаУказатьПараметры.ИмяКоманды = "УказатьПараметрыЗаполненияПеревозки";
	
	НоваяГруппаКомандыТочкиНиз = Элементы.Добавить(СтрШаблон("ГруппаКомандыНизПромежуточнаяТочка%1", НомерТочки), Тип("ГруппаФормы"), НоваяГруппаКомандыТочки);
	НоваяГруппаКомандыТочкиНиз.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаКомандыТочкиНиз.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаКомандыТочкиНиз.ОтображатьЗаголовок = Ложь;
	НоваяГруппаКомандыТочкиНиз.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;

	
	НоваяКомандаПереместитьТочкуВверх = Элементы.Добавить(СтрШаблон("КомандаПереместитьТочкуВверх%1", НомерТочки), Тип("КнопкаФормы"), НоваяГруппаКомандыТочкиНиз);
	НоваяКомандаПереместитьТочкуВверх.ИмяКоманды = "ПереместитьТочкуМаршрутаВверх";
	
	НоваяКомандаПереместитьТочкуВниз = Элементы.Добавить(СтрШаблон("КомандаПереместитьТочкуВниз%1", НомерТочки), Тип("КнопкаФормы"), НоваяГруппаКомандыТочкиНиз);
	НоваяКомандаПереместитьТочкуВниз.ИмяКоманды = "ПереместитьТочкуМаршрутаВниз";
	
	Если ОбновлятьДоступность Тогда
		УстановитьДоступностьКнопокПеремещения();
	КонецЕсли;
	
КонецПроцедуры // ДобавитьТочкуМаршрутаНаСервере()

&НаСервере
Процедура УдалитьТочкуМаршрутаНаСервере()
	
	НомерУдаляемойТочки = Число(СтрЗаменить(ТекущийЭлемент.Имя, "КомандаУдалитьТочку", ""));
	
	Если НомерУдаляемойТочки = КоличествоПромежуточныхТочек Тогда
		УдаляемаяГруппаФормы = Элементы[СтрШаблон("ГруппаПромежуточнаяТочка%1", НомерУдаляемойТочки)];
		Элементы.Удалить(УдаляемаяГруппаФормы);
		Объект.ПромежуточныеТочкиМаршрута.Удалить(НомерУдаляемойТочки - 1);
		
	Иначе	
		Для Сч = НомерУдаляемойТочки По КоличествоПромежуточныхТочек Цикл
			УдаляемаяГруппаФормы = Элементы[СтрШаблон("ГруппаПромежуточнаяТочка%1", Сч)];
			Элементы.Удалить(УдаляемаяГруппаФормы);
		КонецЦикла;
		
		Объект.ПромежуточныеТочкиМаршрута.Удалить(НомерУдаляемойТочки - 1);
		Для Сч = НомерУдаляемойТочки По КоличествоПромежуточныхТочек - 1 Цикл
			ДобавитьТочкуМаршрутаНаСервере(Сч, Ложь, Ложь);
		КонецЦикла; 
		
	КонецЕсли;
	
	КоличествоПромежуточныхТочек = КоличествоПромежуточныхТочек - 1;
	УстановитьДоступностьКнопокПеремещения();
	
КонецПроцедуры // УдалитьТочкуМаршрутаНаСервере()

&НаСервере
Процедура ПереместитьТочкуМаршрутаВверхНаСервере()
	
	НомерПеремещаемойТочки = Число(СтрЗаменить(ТекущийЭлемент.Имя, "КомандаПереместитьТочкуВверх", ""));
	Объект.ПромежуточныеТочкиМаршрута.Сдвинуть(НомерПеремещаемойТочки - 1, -1);
	
КонецПроцедуры // ПереместитьТочкуМаршрутаВверхНаСервере()

&НаСервере
Процедура ПереместитьТочкуМаршрутаВнизНаСервере()
	
	НомерПеремещаемойТочки = Число(СтрЗаменить(ТекущийЭлемент.Имя, "КомандаПереместитьТочкуВниз", ""));
	Объект.ПромежуточныеТочкиМаршрута.Сдвинуть(НомерПеремещаемойТочки - 1, 1);
	
КонецПроцедуры // ПереместитьТочкуМаршрутаВнизНаСервере()

&НаСервере
Процедура УстановитьДоступностьКнопокПеремещения()
	
	Если КоличествоПромежуточныхТочек > 0 Тогда
		Если КоличествоПромежуточныхТочек = 1 Тогда 
			Элементы.КомандаПереместитьТочкуВверх1.Доступность = Ложь;
			Элементы.КомандаПереместитьТочкуВниз1.Доступность  = Ложь;
		Иначе
			Элементы.КомандаПереместитьТочкуВверх1.Доступность = Ложь;
			Элементы.КомандаПереместитьТочкуВниз1.Доступность  = Истина;
 
			Для Сч = 2 По КоличествоПромежуточныхТочек - 1 Цикл
				Элементы[СтрШаблон("КомандаПереместитьТочкуВверх%1", Сч)].Доступность = Истина;
				Элементы[СтрШаблон("КомандаПереместитьТочкуВниз%1", Сч)].Доступность  = Истина;
			КонецЦикла;
			
			Элементы[СтрШаблон("КомандаПереместитьТочкуВверх%1", Сч)].Доступность = Истина;
			Элементы[СтрШаблон("КомандаПереместитьТочкуВниз%1", Сч)].Доступность  = Ложь;
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры // УстановитьДоступностьКнопокПеремещения()

&НаСервере
Процедура ОбновитьВидимостьНаСервере()
	
	Если ЗначениеЗаполнено(Объект.АдресОтправления) Тогда 
		Элементы.ЗонаОтправления.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АдресПолучения) Тогда 
		Элементы.ЗонаПолучения.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры // ОбновитьВидимостьНаСервере()

&НаКлиенте
Процедура ОбновитьВидимостьВременныхОкон()
	Элементы.ВариантОпределенияВременногоОкнаОтправления.Видимость 	= Объект.ОпределятьВременныеОкнаЗаданий;
	Элементы.ВариантОпределенияВременногоОкнаПолучения.Видимость 	= Объект.ОпределятьВременныеОкнаЗаданий;
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#Область ОповещенияПоСобытиям

&НаКлиенте
// Процедура завершения после закрытия формы настроек.
//
Процедура УказаниеПараметровПеревозкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		СтрокаТочки = Объект.ПромежуточныеТочкиМаршрута.Получить(Результат.НомерСтроки - 1);
		ЗаполнитьЗначенияСвойств(СтрокаТочки, Результат);
	КонецЕсли;
	
КонецПроцедуры // УказаниеПараметровПеревозкиЗавершение()

#КонецОбласти

&НаКлиенте
Процедура ЗаблокироватьЭлементыНаКлиенте()
	Элементы.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов.Доступность = Объект.ОпределятьВременныеОкнаЗаданий;		
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьЭлементы()
	Элементы.ФормироватьВременныеОкнаПоГрафикамРаботыАдресов.Доступность = Объект.ОпределятьВременныеОкнаЗаданий;		
КонецПроцедуры

#КонецОбласти