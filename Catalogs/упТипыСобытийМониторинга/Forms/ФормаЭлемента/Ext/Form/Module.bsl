#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	УстановитьДоступностьЭлементовПредопределенногоОбъекта();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииПриЧтенииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидСобытияПриИзменении(Элемент)
	
	Объект.НастраиваемоеСобытиеAndroidКлиента = (Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.СобытиеAndroidКлиента"));
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатчикПриИзменении(Элемент)
	
	ИзменитьДоступностьТипЗначенияДатчика();
	
КонецПроцедуры

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции
// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗаблокированныеРеквизиты = ЗапретРедактированияРеквизитовОбъектовКлиент.Реквизиты(ЭтаФорма);
	Если ЗаблокированныеРеквизиты.Количество() > 0 Тогда
		ОбработчикОповещенияПоказатьВопрос = Новый ОписаниеОповещения("ВопросОРазрешенииРедактированияРеквизитов", ЭтаФорма, ЗаблокированныеРеквизиты);
		ПоказатьВопрос(ОбработчикОповещенияПоказатьВопрос, НСтр("ru='Разрешить редактирование реквизитов формы?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ВопросОРазрешенииРедактированияРеквизитов(РезультатВопроса, ЗаблокированныеРеквизиты) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма,ЗаблокированныеРеквизиты);
	Иначе
		ЗапретРедактированияРеквизитовОбъектовКлиент.ПоказатьПредупреждениеВсеВидимыеРеквизитыРазблокированы();
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаСервере
Процедура УстановитьДоступностьЭлементовПредопределенногоОбъекта()

	Если Объект.Предопределенный Тогда 
		Элементы.ЧастотаОповещения.Доступность                  = Ложь;
		Элементы.НеФиксироватьСобытиеВТочкеМаршрута.Доступность = Ложь;
		Элементы.Активно.Доступность                            = Ложь;
		Элементы.ВидСобытия.Доступность                         = Ложь;
		Элементы.ГруппаСтраницы.Доступность                     = Ложь;
		Элементы.ТранспортныеСредства.Доступность               = Ложь;
		
		Элементы.ИнициирующиеМониторингДействия.ТолькоПросмотр  = Истина;
		Элементы.ОтменяющиеМониторингДействия.ТолькоПросмотр    = Истина;
		Элементы.ФиксирующиеСобытиеДействия.ТолькоПросмотр      = Истина;
		
		Элементы.ТранспортныеСредства.Видимость                 = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.НеФиксироватьСобытиеВТочкеМаршрута.Видимость = Истина;
	Если Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.СобытиеAndroidКлиента") И Объект.НастраиваемоеСобытиеAndroidКлиента Тогда
		Элементы.ГруппаСтраницыОбщая.ТекущаяСтраница = Элементы.ГруппаСтраницаНастраиваемоеСобытиеAndroidКлиента;
	Иначе	
		Элементы.ГруппаСтраницыОбщая.ТекущаяСтраница = Элементы.СтраницаТипыСобытийДляСпутниковогоМониторинга;
		
		Если Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.Остановка") Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаОстановка;
		ИначеЕсли Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.ВъездВЗону") 
			ИЛИ Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.ВыездИзЗоны") Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаВъездВыезд;
		ИначеЕсли Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.ЗначениеДатчика") Тогда 	
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаЗначенияДатчика;
			ИзменитьДоступностьТипЗначенияДатчика();
		ИначеЕсли Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.ИзмененияЗначенияДатчика") Тогда 	
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаИзмененияЗначенияДатчика;
			СформироватьСписокВыбораНепрерывныхДатчиков();
		ИначеЕсли Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.ОтклонениеОтМаршрута") Тогда 	
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаОтклонениеОтМаршрута;
		ИначеЕсли Объект.ВидСобытия = ПредопределенноеЗначение("Перечисление.упВидыСобытийМониторинга.ДвижениеБезРейса") Тогда 	
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаДвижениеБезРейса;
			Элементы.НеФиксироватьСобытиеВТочкеМаршрута.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли; 
 
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСписокВыбораНепрерывныхДатчиков()
	
	СписокВыбора = Элементы.ДатчикИзменениеЗначения.СписокВыбора;
	мсвНепрерывныеДатчики = ПолучитьСписокНепрерывныхДатчиков();
	СписокВыбора.ЗагрузитьЗначения(мсвНепрерывныеДатчики);
	
КонецПроцедуры
 
&НаСервере
Процедура ИзменитьДоступностьТипЗначенияДатчика()
	
	ТипЗначенияДатчика = Неопределено;
	Если ЗначениеЗаполнено(Объект.Датчик.Назначение)Тогда
		ТипЗначенияДатчика = Объект.Датчик.Назначение.ТипЗначения;
	КонецЕсли;
	
	Если ТипЗначенияДатчика = ПредопределенноеЗначение("Перечисление.упТипыЗначений.Дискретный") Тогда
		Объект.ВидСравнения = ПредопределенноеЗначение("Перечисление.упВидыСравненияЗначенийДатчиков.Равно");
		Элементы.ВидСравнения.Доступность = Ложь;
	Иначе
		Элементы.ВидСравнения.Доступность = Истина;
	КонецЕсли; 
	Элементы.ЗначениеДатчика.РежимВыбораИзСписка = НЕ Элементы.ВидСравнения.Доступность;
	
	СписокВыбора = Элементы.ЗначениеДатчика.СписокВыбора;
	Если Элементы.ЗначениеДатчика.РежимВыбораИзСписка Тогда
		СписокВыбора.Очистить();
		спз = СформироватьСписокДанныхВыбора(Объект.Датчик);
		Если спз.Количество() Тогда
			Для каждого строка Из спз Цикл
				СписокВыбора.Добавить(строка.Значение, строка.Представление);
			КонецЦикла; 
			
		КонецЕсли; 
	//Иначе
	//	СписокВыбора.Очистить();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокНепрерывныхДатчиков()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упДатчики.Ссылка КАК Датчик
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|ГДЕ
	|	упДатчики.Назначение.ТипЗначения = ЗНАЧЕНИЕ(Перечисление.упТипыЗначений.Непрерывный)
	|	И НЕ упДатчики.ПометкаУдаления";
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	Массив = ТаблицаЗначений.ВыгрузитьКолонку("Датчик");
	
	Возврат Массив;
	
КонецФункции //ПолучитьСписокНепрерывныхДатчиков()

&НаСервереБезКонтекста
Функция СформироватьСписокДанныхВыбора(Датчик)
	
	СписокВыбора = Новый СписокЗначений;
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	упДатчики.Ссылка КАК Датчик,
	|	упЗначенияДатчиков.Ссылка КАК Значение,
	|	упДатчики.Назначение КАК Назначение,
	|	упЗначенияДатчиков.Представление КАК Представление
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упЗначенияДатчиков КАК упЗначенияДатчиков
	|		ПО упДатчики.Назначение = упЗначенияДатчиков.Владелец
	|ГДЕ
	|	упДатчики.Ссылка = &Датчик");	
	Запрос.УстановитьПараметр("Датчик", Датчик);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокВыбора.Добавить(Выборка.Значение, Выборка.Представление);
	КонецЦикла;
	Возврат СписокВыбора;
КонецФункции // СформироватьСписокДанныхВыбора()

#КонецОбласти
