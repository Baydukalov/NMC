
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("Код");
	Поля.Добавить("Наименование");
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	Представление = НСтр(СтрШаблон("ru = '%1 (серия №%2)'", Данные.Наименование, Данные.Код));
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 51
	|	упШиныУзлыИАгрегаты.Ссылка КАК ШиныУзлыИАгрегаты
	|ИЗ
	|	Справочник.упШиныУзлыИАгрегаты КАК упШиныУзлыИАгрегаты
	|ГДЕ
	|	НЕ упШиныУзлыИАгрегаты.ПометкаУдаления
	|	И упШиныУзлыИАгрегаты.Наименование ПОДОБНО &СтрокаПоиска";
	Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
	Если Параметры.Отбор.Свойство("ТС") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"
			|	НЕ упШиныУзлыИАгрегаты.ПометкаУдаления"	, "
			|	НЕ упШиныУзлыИАгрегаты.ПометкаУдаления
			|	И упШиныУзлыИАгрегаты.Тип.МодельТС В (&МодельТС)" );
		МоделиТС = Новый Массив;
		МоделиТС.Добавить(Справочники.упМоделиТС.ПустаяСсылка());
		МоделиТС.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Отбор.ТС,"Модель"));
		Запрос.УстановитьПараметр("МодельТС",МоделиТС);	
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		спзДанныеВыбора	= Новый СписокЗначений;
		спзДанныеВыбора.ЗагрузитьЗначения(Результат.Выгрузить().ВыгрузитьКолонку("ШиныУзлыИАгрегаты"));
		ДанныеВыбора = спзДанныеВыбора;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 	
	
#Область ПрограммныйИнтерфейс
	
// Проверка элемента справочника это ШИНА.
//
// Параметры:
//  Элемент  - СправочникСсылка.упШиныУзлыИАгрегаты - Элемент справочника который проверяем.
//
// Возвращаемое значение:
//	Булево - если истина это шина.
//
Функция ЭтоШина(Элемент) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	|	упШиныУзлыИАгрегаты.Ссылка
	|ИЗ
	|	Справочник.упШиныУзлыИАгрегаты КАК упШиныУзлыИАгрегаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТипыШинУзловИАгрегатов КАК упТипыШинУзловИАгрегатов
	|		ПО упШиныУзлыИАгрегаты.Тип = упТипыШинУзловИАгрегатов.Ссылка
	|ГДЕ
	|	упШиныУзлыИАгрегаты.Ссылка = &Ссылка
	|	И упТипыШинУзловИАгрегатов.Вид = ЗНАЧЕНИЕ(Перечисление.упВидыАгрегатов.Шины)";
	Запрос.УстановитьПараметр("Ссылка",Элемент);
	
	Результат = Истина;
	
	Выборка = Запрос.Выполнить();
	
	Если Выборка.Пустой() Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ЭтоШина()

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции
	
	// Нет.
	
#КонецОбласти
	
#КонецЕсли


