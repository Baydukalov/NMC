#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
	|	упГаражи.Ссылка КАК Гараж
	|ИЗ
	|	Справочник.упГаражи КАК упГаражи
	|ГДЕ
	|	НЕ упГаражи.ПометкаУдаления
	|	И НЕ упГаражи.Закрыт
	|	И упГаражи.Наименование ПОДОБНО &СтрокаПоиска";
	
	НеКонтролироватьРасписаниеРаботыГаража = Неопределено;
	Если Параметры.Отбор.Свойство("НеКонтролироватьРасписаниеРаботыГаража", НеКонтролироватьРасписаниеРаботыГаража) Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И упГаражи.НеКонтролироватьРасписаниеРаботыГаража = &НеКонтролироватьРасписаниеРаботыГаража";
		Запрос.УстановитьПараметр("НеКонтролироватьРасписаниеРаботыГаража", НеКонтролироватьРасписаниеРаботыГаража);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		спзДанныеВыбора	= Новый СписокЗначений;
		спзДанныеВыбора.ЗагрузитьЗначения(Результат.Выгрузить().ВыгрузитьКолонку("Гараж"));
		ДанныеВыбора = спзДанныеВыбора;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
	
#Область ПрограммныйИнтерфейс

// Возвращает структуру данных гаража для указанного транспортного средства.
//
// Параметры:
//  ТранспортноеСредство  - СправочникСсылка.упТранспортныеСредства, 
//                          СправочникСсылка.упГеографическиеЗоны, Массив - Ссылка на элемент.
//  ПунктыОтправления  - СправочникСсылка.упАдреса, Массив - Ссылка на элемент.
//  ПунктыПрибытия  - СправочникСсылка.упАдреса, Массив - Ссылка на элемент.
//
// Возвращаемое значение:
//   Структура   - Результат выполнения.
//
Функция ПодобратьГаражиДляТранспортногоСредства(ТранспортноеСредство = Неопределено, ПунктыОтправления = Неопределено, ПунктыПрибытия = Неопределено) Экспорт
	
	Результат = Новый Структура("ГаражВыезда, АдресВыезда, ГаражВозвращения, АдресВозвращения", Неопределено, Неопределено, Неопределено, Неопределено);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("ПунктыОтправления",    ПунктыОтправления);
	Запрос.УстановитьПараметр("ПунктыПрибытия",       ПунктыПрибытия);
	
	Если ТипЗнч(ПунктыОтправления) = Тип("СправочникСсылка.упГеографическиеЗоны") Тогда
		Запрос.Текст = 
		"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	упТранспортныеСредстваГаражи.Зона КАК Зона,
		|	упТранспортныеСредстваГаражи.Гараж КАК Гараж,
		|	упТранспортныеСредстваГаражи.Гараж.Адрес КАК ГаражАдрес,
		|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство
		|ПОМЕСТИТЬ втГаражиПоЗонам
		|ИЗ
		|	Справочник.упТранспортныеСредства.Гаражи КАК упТранспортныеСредстваГаражи
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|	ПО упТранспортныеСредстваГаражи.Ссылка = упТранспортныеСредства.Ссылка
		|ГДЕ НЕОПРЕДЕЛЕНО В (&ТранспортноеСредство)
		|	ИЛИ упТранспортныеСредства.Ссылка В  (&ТранспортноеСредство)
		|;
		|ВЫБРАТЬ
		|	втГаражиПоЗонам.Гараж КАК Гараж,
		|	упИерархияГеографическихЗон.Уровень КАК Приоритет,
		|	втГаражиПоЗонам.ГаражАдрес.ГеографическаяЗона КАК ЗонаГаража,
		|	втГаражиПоЗонам.ГаражАдрес КАК АдресГаража,
		|	Зоны.Ссылка КАК Адрес,
		|	втГаражиПоЗонам.ТранспортноеСредство КАК ТранспортноеСредство
		|ИЗ
		|	втГаражиПоЗонам КАК втГаражиПоЗонам
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
		|	ПО упИерархияГеографическихЗон.Родитель = втГаражиПоЗонам.Зона
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК Зоны
		|	ПО упИерархияГеографическихЗон.Зона = Зоны.Ссылка
		|		И Зоны.Ссылка В  (&ПунктыОтправления, &ПунктыПрибытия)
		|ГДЕ
		|	НЕ (втГаражиПоЗонам.Зона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка))
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втГаражиПоЗонам.Гараж КАК Гараж,
		|	-1 КАК Приоритет,
		|	втГаражиПоЗонам.ГаражАдрес.ГеографическаяЗона КАК ЗонаГаража,
		|	втГаражиПоЗонам.ГаражАдрес КАК АдресГаража,
		|	Зоны.Ссылка КАК Адрес,
		|	втГаражиПоЗонам.ТранспортноеСредство КАК ТранспортноеСредство
		|ИЗ
		|	втГаражиПоЗонам КАК втГаражиПоЗонам
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны КАК Зоны
		|	ПО Зоны.Ссылка В (&ПунктыОтправления, &ПунктыПрибытия)
		|ГДЕ
		|	втГаражиПоЗонам.Зона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет УБЫВ
		|";
	ИначеЕсли Ложь
		     Или ПунктыОтправления <> Неопределено
			Или ПунктыПрибытия <> Неопределено Тогда
		Запрос.Текст = 
		"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	упТранспортныеСредстваГаражи.Зона КАК Зона,
		|	упТранспортныеСредстваГаражи.Гараж КАК Гараж,
		|	упТранспортныеСредстваГаражи.Гараж.Адрес КАК ГаражАдрес,
		|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство
		|ПОМЕСТИТЬ втГаражиПоЗонам
		|ИЗ
		|	Справочник.упТранспортныеСредства.Гаражи КАК упТранспортныеСредстваГаражи
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|	ПО упТранспортныеСредстваГаражи.Ссылка = упТранспортныеСредства.Ссылка
		|ГДЕ НЕОПРЕДЕЛЕНО В (&ТранспортноеСредство) 
		|	ИЛИ упТранспортныеСредства.Ссылка В  (&ТранспортноеСредство)
		|;
		|ВЫБРАТЬ
		|	втГаражиПоЗонам.Гараж КАК Гараж,
		|	упИерархияГеографическихЗон.Уровень КАК Приоритет,
		|	втГаражиПоЗонам.ГаражАдрес.ГеографическаяЗона КАК ЗонаГаража,
		|	втГаражиПоЗонам.ГаражАдрес КАК АдресГаража,
		|	Адреса.Ссылка КАК Адрес,
		|	втГаражиПоЗонам.ТранспортноеСредство КАК ТранспортноеСредство
		|ИЗ
		|	втГаражиПоЗонам КАК втГаражиПоЗонам
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
		|	ПО упИерархияГеографическихЗон.Родитель = втГаражиПоЗонам.Зона
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК Адреса
		|	ПО упИерархияГеографическихЗон.Зона = Адреса.ГеографическаяЗона
		|		И Адреса.Ссылка В (&ПунктыОтправления, &ПунктыПрибытия)
		|ГДЕ
		|	НЕ (втГаражиПоЗонам.Зона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка))
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	втГаражиПоЗонам.Гараж КАК Гараж,
		|	-1 КАК Приоритет,
		|	втГаражиПоЗонам.ГаражАдрес.ГеографическаяЗона КАК ЗонаГаража,
		|	втГаражиПоЗонам.ГаражАдрес КАК АдресГаража,
		|	Адреса.Ссылка КАК Адрес,
		|	втГаражиПоЗонам.ТранспортноеСредство КАК ТранспортноеСредство
		|ИЗ
		|	втГаражиПоЗонам КАК втГаражиПоЗонам
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упАдреса КАК Адреса
		|	ПО Адреса.Ссылка В (&ПунктыОтправления, &ПунктыПрибытия)
		|ГДЕ
		|	втГаражиПоЗонам.Зона = ЗНАЧЕНИЕ(Справочник.упГеографическиеЗоны.ПустаяСсылка)
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет УБЫВ
		|";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	упТранспортныеСредстваГаражи.Гараж КАК Гараж,
		|	0 КАК Приоритет,
		|	упТранспортныеСредстваГаражи.Зона КАК Зона,
		|	упТранспортныеСредстваГаражи.Гараж.Адрес КАК АдресГаража,
		|	упТранспортныеСредстваГаражи.Гараж.Адрес КАК Адрес,
		|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство
		|ИЗ
		|	Справочник.упТранспортныеСредства.Гаражи КАК упТранспортныеСредстваГаражи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|		ПО упТранспортныеСредстваГаражи.Ссылка = упТранспортныеСредства.Ссылка
		|ГДЕ
		|	упТранспортныеСредства.Ссылка В (&ТранспортноеСредство)";
	
	КонецЕсли; 
	//СхемаЗапроса = Новый СхемаЗапроса;
	//СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	//ПакетЗапросовСхемы 		= СхемаЗапроса.ПакетЗапросов;
	//ЗапросГаражиОтправления = ПакетЗапросовСхемы[Индекс_ГаражиОтправления];
	//ЗапросГаражиЗавершения 	= ПакетЗапросовСхемы[Индекс_ГаражиЗавершения];
	//Если ТипЗнч(АдресОтправления) = Тип("СправочникСсылка.упГеографическиеЗоны") Тогда
	//	ЗапросГаражиОтправления.Операторы[0].Источники[0].Соединения[0].Условие = Новый ВыражениеСхемыЗапроса("&АдресОтправления = упИерархияГеографическихЗон.Зона");	
	//	ЗапросГаражиЗавершения.Операторы[0].Источники[0].Соединения[0].Условие 	= Новый ВыражениеСхемыЗапроса("&АдресЗавершения = упИерархияГеографическихЗон.Зона");	
	//КонецЕсли;
	//Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	ТаблицаГаражей = Запрос.Выполнить().Выгрузить();
	Если Истина 
		И ПунктыОтправления = Неопределено
		И ПунктыПрибытия = Неопределено 
	Тогда
		Если ТаблицаГаражей.Количество() > 0 Тогда
			СтрокаГаража = ТаблицаГаражей[0];
	
			Результат.ГаражВыезда      = СтрокаГаража.Гараж;
			Результат.АдресВыезда      = СтрокаГаража.АдресГаража;
			Результат.ГаражВозвращения = СтрокаГаража.Гараж;
			Результат.АдресВозвращения = СтрокаГаража.АдресГаража;
		КонецЕсли;
	Иначе
		СтрокаГаражаОтправления = ТаблицаГаражей.Найти(ПунктыОтправления, "Адрес");
		Если СтрокаГаражаОтправления <> Неопределено Тогда
			Результат.ГаражВыезда = СтрокаГаражаОтправления.Гараж;
			Результат.АдресВыезда = СтрокаГаражаОтправления.АдресГаража;
		КонецЕсли; 
		СтрокаГаражаПрибытия = ТаблицаГаражей.Найти(ПунктыПрибытия, "Адрес");
		Если СтрокаГаражаПрибытия <> Неопределено Тогда
			Результат.ГаражВозвращения = СтрокаГаражаПрибытия.Гараж;
			Результат.АдресВозвращения = СтрокаГаражаПрибытия.АдресГаража;
		КонецЕсли; 
	КонецЕсли;
	Результат.Вставить("Гаражи", ТаблицаГаражей);
	Возврат Результат;
	
Конецфункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли