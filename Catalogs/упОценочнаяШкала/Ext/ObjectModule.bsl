
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроверкиЗаполнения".
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	тбзИнтервалы = ЭтотОбъект.Диапазоны.Выгрузить();
	тбзИнтервалы.Колонки.Добавить("СтрокаНомер", Новый ОписаниеТипов("Число"));
	
	Для каждого текСтрока Из тбзИнтервалы Цикл
		текСтрока.СтрокаНомер = текСтрока.НомерСтроки;
	КонецЦикла;
	тбзИнтервалы.Сортировать("Начало");
	
	текНачало 	 = Неопределено;
	текОкончание = Неопределено;
	текНомерПредыдущейСтроки = 0;
		
	Для каждого текСтрока Из  тбзИнтервалы Цикл
		
		текНомерСтроки  = текСтрока.СтрокаНомер;
		текИндексСтроки = текНомерСтроки - 1; 
		
		Если текСтрока.Начало >=текСтрока.Окончание Тогда
			
			ТекстСообщения = НСтр("ru = 'Начало интервала, должно быть меньше окончания'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				,
				"Диапазоны["+текИндексСтроки+"].Начало",
				"Объект",
				Отказ);
			Прервать;
		КонецЕсли;
		
		Если текНачало = Неопределено И текОкончание = Неопределено Тогда
			текНачало 		= текСтрока.Начало;
			текОкончание 	= текСтрока.Окончание;
			текНомерПредыдущейСтроки 	= текСтрока.СтрокаНомер;
			Продолжить;
		КонецЕсли;
		
		Если текСтрока.Начало < текОкончание Тогда
			
			ТекстСообщения = НСтр("ru = 'Интервалы строк "+текНомерСтроки + " и " + текНомерПредыдущейСтроки+" пересекаются'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				,
				"Диапазоны["+текИндексСтроки+"].Начало",
				"Объект",
				Отказ);
			Прервать;
		Иначе
			текНомерПредыдущейСтроки = текСтрока.СтрокаНомер;
			текОкончание = текСтрока.Окончание;
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 

#КонецЕсли