#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Для каждого текСтрока Из Объект.Диапазоны Цикл
		текСтрока.Представление = ПолучитьПредставлениеИнтервала(текСтрока.Начало, текСтрока.Окончание, текСтрока.Значение); 
	КонецЦикла;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов.
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов.	
КонецПроцедуры

&НаСервере
// Процедура - Обработчик события "ПриСозданииНаСервере".
//
// Параметры:
//  Отказ - Булево - Выполнить отображение формы.
//  СтандартнаяОбработка - Булево - признак обрбаотки.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПередЗаписьюНаСервере" формы.
//
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	упТарификацияВызовСервера.ПроверитьУникальностьКодаПоказателя(ТекущийОбъект.Код, "упОценочнаяШкала", Отказ);
	
КонецПроцедуры

// Процедура - После записи
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписанНовыйПоказатель");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события "ПриОкончанииРедактирования" элемента Диапазоны.
//
// Параметры:
//  Элемент - 	Элемент формы, источник события.
//  НоваяСтрока - 	Булево - если строка добавлена или скопирована.
//  ОтменаРедактирования - Булево - отказ от редактиорования.
//
Процедура ДиапазоныПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТекущиеДанные  = Элемент.ТекущиеДанные;
	ТекущиеДанные.Представление = ПолучитьПредставлениеИнтервала(ТекущиеДанные.Начало, ТекущиеДанные.Окончание, ТекущиеДанные.Значение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	// Нет.
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
// Функция - Возвращает текстовое представление заданного интервала.
//
// Параметры:
//  Начало - Число - начало интеравала.
//  Окончание - Число - окончания интервала.
//  Значение - Число - значение.
//
// Возвращаемое значение:
//   Строка - представление.
//
Функция ПолучитьПредставлениеИнтервала(Начало, Окончание, Значение)
	
	текСимволМеньшеИлиРавно = Символ(8804);
	текРезультат = Нстр("ru = 'Если %1 "+текСимволМеньшеИлиРавно+"x< %2, то y = %3'"); 
	текРезультат  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(текРезультат , Начало, Окончание, Значение);
	
	Возврат текРезультат;
	
КонецФункции

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" элемента Наименование.
//
//  Параметры:
//   Элемент - Элемент формы, источник события.
//
Процедура НаименованиеПриИзменении(Элемент)
	Объект.Код = упТарификацияКлиентСервер.ИдентификаторПоказателяПоНаименованию(Объект.Наименование);
КонецПроцедуры

#КонецОбласти
