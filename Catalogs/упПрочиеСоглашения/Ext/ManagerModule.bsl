
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	текПартнер = Справочники.упПартнеры.ПустаяСсылка();
	Если Параметры.Отбор.Свойство("Владелец", текПартнер) Тогда
		СтандартнаяОбработка = Ложь;
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 51
		|	упПрочиеСоглашения.Ссылка КАК Соглашение
		|ИЗ
		|	Справочник.упПрочиеСоглашения КАК упПрочиеСоглашения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияПартнеров КАК упИерархияПартнеров
		|		ПО упПрочиеСоглашения.Владелец = упИерархияПартнеров.Родитель
		|ГДЕ
		|	упПрочиеСоглашения.Наименование ПОДОБНО &СтрокаПоиска
		|	И упИерархияПартнеров.Партнер = &Партнер
		|	И упПрочиеСоглашения.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыСоглашений.Действует)";
		текЗапрос.УстановитьПараметр("Партнер",      текПартнер);
		текЗапрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		Если Параметры.Отбор.Свойство("ТипСоглашения") Тогда 
			текЗапрос.Текст = текЗапрос.Текст + " И упПрочиеСоглашения.ТипСоглашения = &ТипСоглашения";
			текЗапрос.УстановитьПараметр("ТипСоглашения", Параметры.Отбор.ТипСоглашения);
		КонецЕсли;	
		
		тбзСоглашения	= текЗапрос.Выполнить().Выгрузить();
		
		спзДанныеВыбора	= Новый СписокЗначений;
		спзДанныеВыбора.ЗагрузитьЗначения(тбзСоглашения.ВыгрузитьКолонку("Соглашение"));
		
		ДанныеВыбора = спзДанныеВыбора;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает ссылку на типовое соглашение.
//
// Параметры:
//  Партнер  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упПрочиеСоглашения - Результат выполнения.
//
Функция СоглашениеПоУмолчанию(Партнер) Экспорт
	
	Соглашение = Справочники.упПрочиеСоглашения.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	упПрочиеСоглашения.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.упПрочиеСоглашения КАК упПрочиеСоглашения
	|ГДЕ
	|	НЕ упПрочиеСоглашения.ПометкаУдаления
	|	И упПрочиеСоглашения.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Партнер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		Соглашение = Выборка.Соглашение;
	КонецЕсли;
	
	Возврат Соглашение;

КонецФункции

// Возвращает ссылку на валюту переданного соглашения.
//
// Параметры:
//  Соглашение  - СправочникСсылка.упПрочиеСоглашения - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.Валюты - Результат выполнения.
//
Функция ВалютаСоглашения(Соглашение) Экспорт
	
	Валюта = Справочники.Валюты.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упГруппыТарифов.Валюта
	|ИЗ
	|	Справочник.упПрочиеСоглашения КАК упПрочиеСоглашения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГруппыТарифов КАК упГруппыТарифов
	|		ПО упПрочиеСоглашения.ГруппаТарифов = упГруппыТарифов.Ссылка
	|ГДЕ
	|	упПрочиеСоглашения.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Соглашение);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Валюта = текВыборка.Валюта;
	КонецЕсли;
	
	Возврат Валюта;
			
КонецФункции

#КонецОбласти

#КонецЕсли
