#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();		
	КонецЕсли; 
	СостояниеОборудования = РегистрыСведений.упСостоянияОборудования.ПолучитьСостояние(ТекущийОбъект.Ссылка);
	Элементы.СостояниеОборудывания.Видимость = ЗначениеЗаполнено(СостояниеОборудования);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	ЗаполнитьНормыГСМ();	
	ЗаблокироватьЭлементы();
	
	НастроитьПанельНавигации();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НастроитьПанельНавигации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "КорректировкаОстатковГСМ" Тогда
		Если Параметр = Объект.Ссылка Тогда
			ЗаполнитьНормыГСМ();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидыГСМПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Если Объект.ВидыГСМ.Количество() = 1 Тогда
			Элемент.ТекущиеДанные.Основной = Истина;
		КонецЕсли
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидыГСМПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если НЕ ОтменаРедактирования Тогда
		
		ВидГСМ = ТекущиеДанные.ВидГСМ;
		мсвСтроки = Объект.ВидыГСМ.НайтиСтроки(Новый Структура("ВидГСМ",ВидГСМ));
		
		Если мсвСтроки.Количество() > 1 Тогда
			ТекстСообщения = Нстр("ru = 'Уже есть строка с видом ГСМ:%1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, ВидГСМ); 
			ИндексСтроки = ТекущиеДанные.НомерСтроки - 1;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Объект.Ссылка,"Объект.ВидыГСМ[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].ВидГСМ");							
		КонецЕсли;
	Иначе
		Если ТекущиеДанные.Основной Тогда
			Если Объект.ВидыГСМ.Количество() > 1 Тогда
				Объект.ВидыГСМ[0].Основной = Истина;
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидыГСМПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТекущиеДанные 	= Элементы.ВидыГСМ.ТекущиеДанные;
	ТекущиеДанные.Изменить = "Изменить";
	сткОстаткиГСМ = УровеньТоплива(ЭтотОбъект.Объект.Ссылка, ТекущиеДанные.ВидГСМ);
	ТекущиеДанные.КоличествоОстаток = сткОстаткиГСМ.Количество;
	ТекущиеДанные.СтоимостьОстаток = сткОстаткиГСМ.Стоимость;
КонецПроцедуры

&НаКлиенте
Процедура ВидыГСМВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ВидыГСМИзменить" Тогда
		ТекущиеДанные = Элементы.ВидыГСМ.ТекущиеДанные;
		ТранспортноеСредство 	= Объект.Ссылка;
		ВидГСМ 				= ТекущиеДанные.ВидГСМ;
		сткПараметры = Новый Структура("ТранспортноеСредство, ВидГСМ", ТранспортноеСредство, ВидГСМ);
		ОткрытьФорму("Документ.упКорректировкаОстатковГСМ.Форма.ФормаДокумента", сткПараметры);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидыГСМПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ВидыГСМ.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		Если ТекущиеДанные.Основной = Истина Тогда
			Для каждого СтрокаНорма Из Объект.ВидыГСМ Цикл
				СтрокаНорма.Основной = Ложь;
			КонецЦикла;
			ТекущиеДанные.Основной  = Истина;
		Иначе
			ТекущиеДанные.Основной  = Ложь;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидыГСМОсновнойПриИзменении(Элемент)
	ТекущиеДанные 	= Элементы.ВидыГСМ.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		мсвСтроки = Объект.ВидыГСМ.НайтиСтроки(Новый Структура("Основной",Истина));
		Если мсвСтроки.Количество() = 0 Тогда
			ТекущиеДанные.Основной = Истина;	
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяУчетГСМПриИзменении(Элемент)
	ЗаблокироватьЭлементы();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаКомментарий, Объект.Комментарий);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьНормыГСМ()
	
	тбзОстатки = упПутевойЛистУчетГСМ.УровеньТопливаПоРегиструОстатковПоТС(Объект.Ссылка);
	
	Для каждого Строка Из Объект.ВидыГСМ Цикл
		Строка.Изменить = "Изменить";
		мсвСтроки = тбзОстатки.НайтиСтроки(Новый Структура("ВидГСМ",Строка.ВидГСМ));
		Если мсвСтроки.Количество() > 0 Тогда
			Строка.КоличествоОстаток	= мсвСтроки[0].КоличествоОстаток;
			Строка.СтоимостьОстаток	= мсвСтроки[0].СтоимостьОстаток;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УровеньТоплива(ТранспортноеСредство, ВидГСМ)
	сткОстаткиГСМ	= упПутевойЛистУчетГСМ.УровеньТопливаПоРегиструОстатков(ТранспортноеСредство, ВидГСМ);
	Возврат сткОстаткиГСМ;
КонецФункции

&НаСервере
Процедура ЗаблокироватьЭлементы()
	Элементы.ВидыГСМ.ТолькоПросмотр = НЕ Объект.ИспользуетсяУчетГСМ;
КонецПроцедуры

&НаСервере
Процедура НастроитьПанельНавигации()

	сткНастроек = Новый Структура;
	сткНастроек.Вставить("ИспользоватьДанныеРазличныхРежимовРаботы", Объект.ИспользуютсяРазличныеРежимыРаботы);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);

КонецПроцедуры

#КонецОбласти
