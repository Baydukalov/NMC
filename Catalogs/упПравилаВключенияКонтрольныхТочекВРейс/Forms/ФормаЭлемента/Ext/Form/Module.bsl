
&НаКлиенте
Процедура ПроверятьОтборПоНачальнойТочкеПриИзменении(Элемент)
	Если Не Объект.ПроверятьОтборПоНачальнойТочке Тогда
		Объект.АдресНачальный = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		Объект.ВидАдресаНачальный = ПредопределенноеЗначение("Справочник.упВидыАдресов.ПустаяСсылка");
		Объект.ЗонаНачальная = ПредопределенноеЗначение("Справочник.упГеографическиеЗоны.ПустаяСсылка");
	КонецЕсли;
	НастроитьЭлементыФормы(ЭтаФорма);
КонецПроцедуры
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НастроитьЭлементыФормы(ЭтаФорма);
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(ЭтаФорма)
	
	Если Истина
		И ЭтаФорма.Объект.ПроверятьОтборПоНачальнойТочке
		И ЭтаФорма.Объект.ПроверятьОтборПоКонечнойТочке Тогда
		
		ЭтаФорма.ПодсказкаОтборовПоАдресам = Нстр("ru = 'Контрольные точки добавляются строго между двумя соседними точками, удовлетворяющим условиям отбора.'");
		ЭтаФорма.ПодсказкаЗаголовкаОтбораПоАдресам = Нстр("ru = 'Н - К'");
		
	ИначеЕсли	Истина
		И ЭтаФорма.Объект.ПроверятьОтборПоНачальнойТочке
		И Не ЭтаФорма.Объект.ПроверятьОтборПоКонечнойТочке Тогда
		
		ЭтаФорма.ПодсказкаОтборовПоАдресам = Нстр("ru = 'Контрольные точки добавляются сразу после найденной начальной точкой, удовлетворяющей условиям отбора.'");
		ЭтаФорма.ПодсказкаЗаголовкаОтбораПоАдресам = Нстр("ru = 'Н - *'");
		
	ИначеЕсли	Истина
		И Не ЭтаФорма.Объект.ПроверятьОтборПоНачальнойТочке
		И ЭтаФорма.Объект.ПроверятьОтборПоКонечнойТочке Тогда
		
		ЭтаФорма.ПодсказкаОтборовПоАдресам = Нстр("ru = 'Контрольные точки добавляются сразу перед найденной конечной точкой, удовлетворяющей условиям отбора.'");
		ЭтаФорма.ПодсказкаЗаголовкаОтбораПоАдресам = Нстр("ru = '* - К'");
		
	Иначе	
		ЭтаФорма.ПодсказкаОтборовПоАдресам = Нстр("ru = 'Контрольные точки добавляются между каждыми соседними точками маршрута.'");
		ЭтаФорма.ПодсказкаЗаголовкаОтбораПоАдресам = Нстр("ru = 'Не установлены'");
	КонецЕсли;
	
	ЭтаФорма.Элементы.ГруппаНачальныйАдрес.Доступность = ЭтаФорма.Объект.ПроверятьОтборПоНачальнойТочке;
	ЭтаФорма.Элементы.ГруппаКонечныйАдрес.Доступность = ЭтаФорма.Объект.ПроверятьОтборПоКонечнойТочке;
	
	Если ЗначениеЗаполнено(ЭтаФорма.Объект.АдресНачальный) Тогда
		ЭтаФорма.Элементы.ВидАдресаНачальный.Доступность = Ложь;
		ЭтаФорма.Элементы.ЗонаНачальная.Доступность = Ложь;
	Иначе
		ЭтаФорма.Элементы.ВидАдресаНачальный.Доступность = Истина;
		ЭтаФорма.Элементы.ЗонаНачальная.Доступность = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтаФорма.Объект.АдресКонечный) Тогда
		ЭтаФорма.Элементы.ВидАдресаКонечный.Доступность = Ложь;
		ЭтаФорма.Элементы.ЗонаКонечная.Доступность = Ложь;
	Иначе
		ЭтаФорма.Элементы.ВидАдресаКонечный.Доступность = Истина;
		ЭтаФорма.Элементы.ЗонаКонечная.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьОтборПоКонечнойТочкеПриИзменении(Элемент)
	Если Не Объект.ПроверятьОтборПоКонечнойТочке Тогда
		Объект.АдресКонечный = ПредопределенноеЗначение("Справочник.упАдреса.ПустаяСсылка");
		Объект.ВидАдресаКонечный = ПредопределенноеЗначение("Справочник.упВидыАдресов.ПустаяСсылка");
		Объект.ЗонаКонечная = ПредопределенноеЗначение("Справочник.упГеографическиеЗоны.ПустаяСсылка");
	КонецЕсли;
	НастроитьЭлементыФормы(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура АдресНачальныйПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.АдресНачальный) Тогда
		сткРеквизитыАдреса = РеквизитыАдреса(Объект.АдресНачальный);
		Объект.ВидАдресаНачальный = сткРеквизитыАдреса.ВидАдреса;
		Объект.ЗонаНачальная = сткРеквизитыАдреса.ГеографическаяЗона;
	КонецЕсли;
	НастроитьЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресКонечныйПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.АдресКонечный) Тогда
		сткРеквизитыАдреса = РеквизитыАдреса(Объект.АдресКонечный);
		Объект.ВидАдресаКонечный = сткРеквизитыАдреса.ВидАдреса;
		Объект.ЗонаКонечная = сткРеквизитыАдреса.ГеографическаяЗона;
	КонецЕсли;
	НастроитьЭлементыФормы(ЭтаФорма);
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыАдреса(Адрес)
	
	ИменаРеквизитов = "ВидАдреса, ГеографическаяЗона";
	Результат = Новый Структура(ИменаРеквизитов, Справочники.упВидыАдресов.ПустаяСсылка(), Справочники.упГеографическиеЗоны.ПустаяСсылка());
	
	Если ЗначениеЗаполнено(Адрес) Тогда
		Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Адрес, ИменаРеквизитов);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
