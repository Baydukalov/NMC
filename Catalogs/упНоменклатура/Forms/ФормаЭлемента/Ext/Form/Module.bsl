#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	ЗаполнитьНавигационнуюСсылкуИзображенияСервер();
	
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаОписание, Объект.Комментарий);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ЕИ_Объем  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияОбъема");
	ЭтаФорма.ЕИ_Масса  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияМассы");
	ЭтаФорма.Валюта    = упСервисныеФункции.ПолучитьЗначениеКонстанты("упВалютаУправленческогоУчета");
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
		
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.упНоменклатураПрисоединенныеФайлы") Тогда
		Объект.ИзображениеПрисоединенныйФайл = ВыбранноеЗначение;
		Модифицированность = Истина;
		ЗаполнитьНавигационнуюСсылкуИзображения();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если ИмяСобытия = "Запись_ПрисоединенныйФайл" И ЗагрузкаФайлаИзображения Тогда	
		Если ТипЗнч(Источник) = Тип("Массив") Тогда
			Если Источник.Количество() > 0 Тогда
				Объект.ИзображениеПрисоединенныйФайл = Источник[0];
				Модифицированность = Истина;
				ЗаполнитьНавигационнуюСсылкуИзображения();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура МассаПриИзменении(Элемент)

	Если Объект.Масса < Объект.МассаНетто Тогда 
		Объект.МассаНетто = Объект.Масса;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура МассаНеттоПриИзменении(Элемент)

	Если Объект.Масса < Объект.МассаНетто Тогда 
		Объект.Масса      = Объект.МассаНетто;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)

	Если Объект.Объем < Объект.ОбъемНетто Тогда 
		Объект.ОбъемНетто = Объект.Объем;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемНеттоПриИзменении(Элемент)

	Если Объект.Объем < Объект.ОбъемНетто Тогда 
		Объект.Объем      = Объект.ОбъемНетто;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзображениеАдресНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Перед добавлением изображения, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	Иначе
		Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
			ПрисоединенныеФайлыКлиент.ОткрытьФайл(ПрисоединенныеФайлыКлиент.ПолучитьДанныеФайла(Объект.ИзображениеПрисоединенныйФайл, УникальныйИдентификатор));
		Иначе	
			ЗагрузитьФайлИзображения();		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	ОбновитьЭлементыДополнительныхРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаОписание, Объект.Комментарий);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)

	Если Не Объект.Ссылка.Пустая() Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаЗакрытияФормыРазблокированияРеквизитов",ЭтаФорма);
		ОткрытьФорму("Справочник.упНоменклатура.Форма.РазблокированиеРеквизитов", ,ЭтаФорма, ,,,ОписаниеОповещенияОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Упаковки(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаОповещенияОбСохранении", ЭтаФорма), НСтр("ru = 'Данные еще не записаны.'") 
							+ Символы.ПС + Нстр("ru='Переход к ""Упаковки"" возможен только после записи данных.'")
							+ Символы.ПС + Нстр("ru='Данные будут записаны.'") ,РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ОткрытьФормуУпаковки();						
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзображение(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Перед добавлением изображения, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗагрузитьФайлИзображения();		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображение(Команда)
	Объект.ИзображениеПрисоединенныйФайл = ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
	Модифицированность = Истина;
	ЗаполнитьНавигационнуюСсылкуИзображения();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзПрисоединенныхФайлов(Команда)
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	ПрисоединенныеФайлыКлиент.ОткрытьФормуВыбораФайлов(Объект.Ссылка,ЭтаФорма);	
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступность()

	Элементы.ГруппаСтраницаПараметрыУчетаТовара.Видимость       = Истина;
	КоманднаяПанель.ПодчиненныеЭлементы.ФормаУпаковки.Видимость	= Истина;
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после ответа на вопрос.
//
Процедура ОбработкаОповещенияОбСохранении(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Записать();
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ОткрытьФормуУпаковки();
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры // ОбработкаОповещенияОбСохранении()

&НаКлиенте
Процедура ОткрытьФормуУпаковки()
	Отбор	= Новый Структура("Владелец", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор",  Отбор);
	ОткрытьФорму("Справочник.упУпаковкиНоменклатуры.ФормаСписка",ПараметрыФормы,ЭтаФорма,УникальныйИдентификатор,ЭтаФорма.Окно);  
КонецПроцедуры // ОткрытьФормуУпаковки()

&НаКлиенте
// Процедура завершения после закрытия формы разблокировки реквизитов.
//
Процедура ОбработкаЗакрытияФормыРазблокированияРеквизитов(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") И РезультатЗакрытия.Количество() > 0 Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, РезультатЗакрытия);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОЗаписи(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = НЕ ПроверитьЗаполнение();
		Если НЕ Отказ Тогда
			Записать();
		КонецЕсли;
		Если НЕ Отказ Тогда
			ЗагрузитьФайлИзображения();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлИзображения()
	ЗагрузкаФайлаИзображения = Истина;
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	ПрисоединенныеФайлыКлиент.ДобавитьФайлы(Объект.Ссылка, УникальныйИдентификатор);
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
	ЗагрузкаФайлаИзображения = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНавигационнуюСсылкуИзображения()
	Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
		ИзображениеАдрес = НавигационнаяСсылкаИзображения();
	Иначе
		ИзображениеАдрес = "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНавигационнуюСсылкуИзображенияСервер()
	Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
		ИзображениеАдрес = НавигационнаяСсылкаИзображения();
	Иначе
		ИзображениеАдрес = "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НавигационнаяСсылкаИзображения()
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(Объект.ИзображениеПрисоединенныйФайл, УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла;
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
КонецФункции

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	  
КонецПроцедуры

#КонецОбласти 