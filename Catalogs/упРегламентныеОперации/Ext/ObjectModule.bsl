
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ПометкаУдаления Тогда
		УдалитьРегламентноеЗадание(Отказ);
	Иначе
		ОбновитьРегламентноеЗадание(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РегламентноеЗаданиеGUID = "";
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

// Выполняет удаление регламентного задания.
//
// Параметры:
//  Отказ - Булево - флаг отказа. Если в процессе выполнения процедуры были обнаружены ошибки,
//                                       то флаг отказа устанавливается в значение Истина.
// 
Процедура УдалитьРегламентноеЗадание(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// определяем регламентное задание.
	РегламентноеЗаданиеОбъект = ОбменДаннымиВызовСервера.НайтиРегламентноеЗаданиеПоПараметру(РегламентноеЗаданиеGUID);
	
	Если РегламентноеЗаданиеОбъект <> Неопределено Тогда
		Попытка
			РегламентноеЗаданиеОбъект.Удалить();
			РегламентноеЗаданиеGUID         = "";
			ИспользоватьРегламентноеЗадание = Ложь;
		Исключение
			СтрокаСообщения = НСтр("ru = 'Ошибка при удалении регламентного задания: %1'");
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбменДаннымиСервер.СообщитьОбОшибке(СтрокаСообщения, Отказ);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьРегламентноеЗадание(Отказ)
	
	Если ЭтоНовый() Тогда 
		СсылкаНаОбъект = Справочники.упРегламентныеОперации.ПолучитьСсылку(Новый УникальныйИдентификатор);
		УстановитьСсылкуНового(СсылкаНаОбъект);
	Иначе
		СсылкаНаОбъект = Ссылка;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(РегламентноеЗаданиеGUID) Тогда 
		РегламентноеЗадание = ОбменДаннымиВызовСервера.НайтиРегламентноеЗаданиеПоПараметру(РегламентноеЗаданиеGUID);
		Если РегламентноеЗадание = Неопределено Тогда 
			РегламентноеЗаданиеGUID = "";
		Иначе	
			Если НЕ РегламентноеЗадание.Использование = (ИспользоватьРегламентноеЗадание И Активно) Тогда
				РегламентноеЗадание.Использование = (ИспользоватьРегламентноеЗадание И Активно);
				Если ДополнительныеСвойства.Свойство("Расписание") Тогда 
					РегламентноеЗадание.Расписание = ДополнительныеСвойства.Расписание;
				КонецЕсли;	
				
				Попытка
					РегламентноеЗадание.Записать();
				Исключение
					СтрокаСообщения = НСтр("ru = 'Ошибка при изменении регламентного задания: %1'");
					СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, Ссылка, "РасписаниеРегламентногоЗадания",, Отказ);
				КонецПопытки;
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РегламентноеЗаданиеGUID) И ИспользоватьРегламентноеЗадание И Активно Тогда 
		РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание("упРегламентныеОперации");
		РегламентноеЗадание.Использование = ИспользоватьРегламентноеЗадание И Активно;
		РегламентноеЗадание.Наименование  = "Регламентная операция: " + Наименование;
		мсвПараметров = Новый Массив;
		мсвПараметров.Добавить(СсылкаНаОбъект);
		РегламентноеЗадание.Параметры     = мсвПараметров;
		РегламентноеЗадание.Ключ          = СсылкаНаОбъект;
		Если ДополнительныеСвойства.Свойство("Расписание") Тогда 
			РегламентноеЗадание.Расписание = ДополнительныеСвойства.Расписание;
		КонецЕсли;	
		
		Попытка
			РегламентноеЗадание.Записать();
		Исключение
			СтрокаСообщения = НСтр("ru = 'Ошибка при создании регламентного задания: %1'");
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, Ссылка, "РасписаниеРегламентногоЗадания",, Отказ);
		КонецПопытки;
		
		РегламентноеЗаданиеGUID = РегламентноеЗадание.УникальныйИдентификатор;
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти  

#КонецЕсли