#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.РегламентноеЗаданиеGUID) Тогда 
		РегламентноеЗадание = ОбменДаннымиВызовСервера.НайтиРегламентноеЗаданиеПоПараметру(Объект.РегламентноеЗаданиеGUID);
		Если РегламентноеЗадание = Неопределено Тогда 
			Объект.ИспользоватьРегламентноеЗадание = Ложь;
			Объект.РегламентноеЗаданиеGUID         = "";
			РасписаниеРегламентногоЗадания         = "<Расписание не задано>";
			Расписание                             = Новый РасписаниеРегламентногоЗадания;
		Иначе
			Расписание                             = РегламентноеЗадание.Расписание;
			Если Объект.ИспользоватьРегламентноеЗадание Тогда
				ПрефиксРасписания = "Расписание: ";
			Иначе
				ПрефиксРасписания = "Расписание (НЕ АКТИВНО): ";
			КонецЕсли;
			РасписаниеРегламентногоЗадания         = ПрефиксРасписания + Строка(РегламентноеЗадание.Расписание);
		КонецЕсли;
	Иначе
		РасписаниеРегламентногоЗадания         = "<Расписание не задано>";
		Расписание                             = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Расписание", Расписание);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользоватьРегламентноеЗаданиеПриИзменении(Элемент)

	УстановитьДоступность();
	Если Объект.ИспользоватьРегламентноеЗадание Тогда
		ПрефиксРасписания = "Расписание: ";
	Иначе
		ПрефиксРасписания = "Расписание (НЕ АКТИВНО): ";
	КонецЕсли;
	РасписаниеРегламентногоЗадания = ПрефиксРасписания + Строка(Расписание);
	
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеРегламентногоЗаданияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Диалог.Показать();
	Расписание = Диалог.Расписание;
	
	Если Объект.ИспользоватьРегламентноеЗадание Тогда
		ПрефиксРасписания = "Расписание: ";
	Иначе
		ПрефиксРасписания = "Расписание (НЕ АКТИВНО): ";
	КонецЕсли;
	РасписаниеРегламентногоЗадания = ПрефиксРасписания + Строка(Расписание);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	// Нет.
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.ГруппаРасписание.Доступность = Объект.ИспользоватьРегламентноеЗадание;
	
КонецПроцедуры

#КонецОбласти