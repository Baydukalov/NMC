#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	УстановитьДоступностьЭлементов();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" элемента "Назначение".
//
Процедура НазначениеПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов();
	
	Если Объект.Показатели.Количество() Тогда			
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Таблица показателей заполнена очистить, содержимое?';"
		+"en = 'Do you want to continue?'"), Режим, 0);
	КонецЕсли;
	
КонецПроцедуры // НазначениеПриИзменении()

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" элемента "ПоказателиЗначение".
//
Процедура ПоказателиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	Если ЗначениеЗаполнено(Объект.Назначение)Тогда
		СтандартнаяОбработка = Ложь;
		
		сткОтбор     = Новый Структура("Владелец", Объект.Назначение);
		сткПараметры = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("Справочник.упЗначенияДатчиков.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("ЗначенияДатчиковНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры // ПоказателиЗначениеНачалоВыбора()

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

&НаКлиенте
// Выполнить действия после получения оповещения для начало выбора.
//
// Параметры:
//  Результат  - КодВозвратаДиалога - Результат ответа на вопрос.
//  ДополнительныеПараметры  - Структура - Параметры ответа.
//
Процедура ЗначенияДатчиковНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда
		Элементы.Показатели.ТекущиеДанные.Значение = Результат;		
	КонецЕсли;
		
КонецПроцедуры // ЗначенияДатчиковНачалоВыбораЗавершение()

&НаКлиенте
// Выполнить действия после получения оповещения.
//
// Параметры:
//  Результат  - КодВозвратаДиалога - Результат ответа на вопрос.
//  Параметры  - Структура - Параметры ответа.
//
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    Объект.Показатели.Очистить();
	Элементы.ПоказателиЗначение.РежимВыбораИзСписка = Ложь;
	Элементы.ПоказателиЗначение.СписокВыбора.Очистить();

КонецПроцедуры // ПослеЗакрытияВопроса()

&НаСервере
// Выполнить установку доступности для элементов отображения.
//
// Параметры:
//  Нет.
//
Процедура УстановитьДоступностьЭлементов()
	
	флгОтображения = Ложь;
	Элементы.НазначениеТипЗначения.Видимость = флгОтображения;
	Элементы.ПоказателиЗначение.РежимВыбораИзСписка = Ложь;
	Если ЗначениеЗаполнено(Объект.Назначение)Тогда
		Элементы.НазначениеТипЗначения.Видимость = Истина;
		ТипЗначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Назначение, "ТипЗначения");
		флгОтображения = (ТипЗначения = ПредопределенноеЗначение("Перечисление.упТипыЗначений.Дискретный"));		
		Если флгОтображения Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	упЗначенияДатчиков.Ссылка КАК Ссылка,
			|	упЗначенияДатчиков.Представление КАК Представление
			|ИЗ
			|	Справочник.упЗначенияДатчиков КАК упЗначенияДатчиков
			|ГДЕ
			|	упЗначенияДатчиков.Владелец = &Владелец";
			
			Запрос.УстановитьПараметр("Владелец", Объект.Назначение);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Элементы.ПоказателиЗначение.РежимВыбораИзСписка = Истина;
			СписокВыбора = Элементы.ПоказателиЗначение.СписокВыбора;
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.Представление);
			КонецЦикла;			
		КонецЕсли;
	КонецЕсли;
	Элементы.ПоказателиЗначение.Видимость = флгОтображения;	
	Элементы.ПоказателиВыход.Видимость = НЕ флгОтображения;	
	
КонецПроцедуры // УстановитьДоступностьЭлементов()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

#КонецОбласти



