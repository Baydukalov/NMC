#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ЭтаФорма.ЭтоСтатьяРасходов 		= Объект.ТипСтатьиДоходовИРасходов = ПредопределенноеЗначение("Перечисление.упТипыСтатейДоходовРасходов.СтатьяРасходов");
	ЭтаФорма.ЭтоПрямыеРасходы 		= ЭтаФорма.ЭтоСтатьяРасходов И Объект.ВариантРасходов = ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Прямые");
	ЭтаФорма.ЭтоКосвенныеРасходы 		= ЭтаФорма.ЭтоСтатьяРасходов И Объект.ВариантРасходов = ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Косвенные");
	ЭтаФорма.ЭтоРасходыБудущихПериодов	= ЭтаФорма.ЭтоСтатьяРасходов И Объект.ВариантРасходов = ПредопределенноеЗначение("Перечисление.упВариантыРасходов.РасходыБудущихПериодов");
		
	Если Объект.ТранспортноеСредство Тогда
		ВидАналитики = "ТранспортноеСредство";
	ИначеЕсли Объект.ВидПеревозки Тогда
		ВидАналитики = "ВидПеревозки";	
	ИначеЕсли Объект.Сотрудник Тогда
		ВидАналитики = "Сотрудник";
	ИначеЕсли Объект.Агрегат Тогда
		ВидАналитики = "Агрегат";	
	Иначе
		ВидАналитики = "НеИспользуется";		
	КонецЕсли;
 		
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаОписание, Объект.Комментарий);	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьВидимость();
	УстановитьДоступность();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипСтатьиДоходовИРасходовПриИзменении(Элемент)
	
	ЭтаФорма.ЭтоСтатьяРасходов 			= Объект.ТипСтатьиДоходовИРасходов = ПредопределенноеЗначение("Перечисление.упТипыСтатейДоходовРасходов.СтатьяРасходов");
		
	Если НЕ ЭтаФорма.ЭтоСтатьяРасходов Тогда
		Объект.ВариантРасходов 		= ПредопределенноеЗначение("Перечисление.упВариантыРасходов.ПустаяСсылка");
		Объект.ПравилаРаспределения	= ПредопределенноеЗначение("Справочник.упПравилаРаспределенияРасходов.ПустаяСсылка");
		Объект.ПравилоРаспределенияКосвенныхРасходовПоРейсам              = ПредопределенноеЗначение("Справочник.упПравилаРаспределенияКосвенныхРасходов.ПустаяСсылка");
		Объект.ПравилоРаспределенияКосвенныхРасходовПоТС                  = ПредопределенноеЗначение("Справочник.упПравилаРаспределенияКосвенныхРасходов.ПустаяСсылка");
		Объект.ПравилоРаспределенияКосвенныхРасходовНаФинансовыйРезультат = ПредопределенноеЗначение("Справочник.упПравилаРаспределенияКосвенныхРасходов.ПустаяСсылка");
		Объект.ВидРасхода			= ПредопределенноеЗначение("Перечисление.упВидыРасходов.ПустаяСсылка");
		Объект.КодСчета			= "";
		Объект.СтатьяСписанияРасходов = ПредопределенноеЗначение("Справочник.упСтатьиДоходовРасходов.ПустаяСсылка");
		Объект.ТранспортноеСредство 	= Ложь;
		Объект.ВидПеревозки 		= Ложь;
		Объект.Сотрудник 			= Ложь;
		Объект.Агрегат 			= Ложь;
	КонецЕсли;
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРасходовПриИзменении(Элемент)
	
	ЭтаФорма.ЭтоСтатьяРасходов 		= Объект.ТипСтатьиДоходовИРасходов = ПредопределенноеЗначение("Перечисление.упТипыСтатейДоходовРасходов.СтатьяРасходов");
	ЭтаФорма.ЭтоПрямыеРасходы 		= ЭтаФорма.ЭтоСтатьяРасходов И Объект.ВариантРасходов = ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Прямые");
	ЭтаФорма.ЭтоКосвенныеРасходы 		= ЭтаФорма.ЭтоСтатьяРасходов И Объект.ВариантРасходов = ПредопределенноеЗначение("Перечисление.упВариантыРасходов.Косвенные");
	ЭтаФорма.ЭтоРасходыБудущихПериодов	= ЭтаФорма.ЭтоСтатьяРасходов И Объект.ВариантРасходов = ПредопределенноеЗначение("Перечисление.упВариантыРасходов.РасходыБудущихПериодов");
	
	Если ЭтаФорма.ЭтоПрямыеРасходы Тогда
		Объект.СтатьяСписанияРасходов 			= ПредопределенноеЗначение("Справочник.упСтатьиДоходовРасходов.ПустаяСсылка");
	ИначеЕсли ЭтаФорма.ЭтоКосвенныеРасходы Тогда	
		Объект.СтатьяСписанияРасходов 			= ПредопределенноеЗначение("Справочник.упСтатьиДоходовРасходов.ПустаяСсылка");
	ИначеЕсли ЭтаФорма.ЭтоРасходыБудущихПериодов Тогда	
		Объект.ПравилаРаспределения				= ПредопределенноеЗначение("Справочник.упПравилаРаспределенияРасходов.ПустаяСсылка");
	КонецЕсли;
	
	Если НЕ ЭтаФорма.ЭтоКосвенныеРасходы Тогда
		Объект.ПравилоРаспределенияКосвенныхРасходовПоРейсам              = ПредопределенноеЗначение("Справочник.упПравилаРаспределенияКосвенныхРасходов.ПустаяСсылка");
		Объект.ПравилоРаспределенияКосвенныхРасходовПоТС                  = ПредопределенноеЗначение("Справочник.упПравилаРаспределенияКосвенныхРасходов.ПустаяСсылка");
		Объект.ПравилоРаспределенияКосвенныхРасходовНаФинансовыйРезультат = ПредопределенноеЗначение("Справочник.упПравилаРаспределенияКосвенныхРасходов.ПустаяСсылка");
		Объект.ПорядокРаспределения = 0;
	КонецЕсли;
		
	Если Ложь
		Или ЭтаФорма.ЭтоКосвенныеРасходы 
		Или ЭтаФорма.ЭтоРасходыБудущихПериодов Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ВидАналитики = "НеИспользуется";
		КонецЕсли;	              
	КонецЕсли;
	
	ОбновитьВидимость();
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаОписание, Объект.Комментарий);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАналитикиПриИзменении(Элемент)
	Модифицированность = Истина;
	Объект.ТранспортноеСредство 	= ВидАналитики = "ТранспортноеСредство";
	Объект.ВидПеревозки 		= ВидАналитики = "ВидПеревозки";
	Объект.Сотрудник 			= ВидАналитики = "Сотрудник";
	Объект.Агрегат 			= ВидАналитики = "Агрегат";
	Если НЕ Объект.ТранспортноеСредство Тогда
		Объект.ОпределятьНаправлениеДеятельностиПриРаспределении = Ложь;
		Объект.ОпределятьПодразделениеПриРаспределении = Ложь;
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьВидимость()
	
	Элементы.ВидРасхода.Видимость                                  = ЭтаФорма.ЭтоСтатьяРасходов;
	Элементы.КодСчета.Видимость                                    = ЭтаФорма.ЭтоСтатьяРасходов;
	Элементы.ПравилаРаспределения.Видимость                        = ЭтаФорма.ЭтоСтатьяРасходов И (ЭтаФорма.ЭтоПрямыеРасходы Или ЭтаФорма.ЭтоКосвенныеРасходы);
	Элементы.ВариантРасходов.Видимость                             = ЭтаФорма.ЭтоСтатьяРасходов;
	Элементы.СтатьяСписанияРасходов.Видимость                      = ЭтаФорма.ЭтоРасходыБудущихПериодов;
	Элементы.ВидАналитики.Видимость                                = ЭтаФорма.ЭтоКосвенныеРасходы Или ЭтаФорма.ЭтоРасходыБудущихПериодов;
	Элементы.ГруппаПравилаРаспределенияКосвенныхРасходов.Видимость = ЭтаФорма.ЭтоКосвенныеРасходы;
	Элементы.ПорядокРаспределения.Видимость                        = ЭтаФорма.ЭтоКосвенныеРасходы;
			
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	Элементы.ОпределятьНаправлениеДеятельностиПриРаспределении.Доступность = Объект.ТранспортноеСредство;	
	Элементы.ОпределятьПодразделениеПриРаспределении.Доступность = Объект.ТранспортноеСредство;
КонецПроцедуры


#КонецОбласти 
