#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция рассчитывает начало и окончание периода от точки отсчета.
//
// Параметры:
//  ПериодСсылка - СправочникСсылка.упПериодыПланирования - Ссылка на элемент справочника.
//  ТочкаОтсчета - Дата - Дата, относительно которой рассчитывается период (по умолчанию - текущая дата сеанса).
//
// Возвращаемое значение:
//   Структура   - содержит описание полученного периода:
//     * НачалоПериода    - Дата - Дата начала периода.
//     * ОкончаниеПериода - Дата - Дата окончания периода.
//
Функция РассчитатьПериодПланирования(ПериодСсылка, ТочкаОтсчета = Неопределено) Экспорт
	
	НачалоПериода    = '00010101';
	ОкончаниеПериода = '00010101';
	
	Если ТочкаОтсчета = Неопределено Тогда
		ТочкаОтсчета = ТекущаяДатаСеанса();
	КонецЕсли;
	ТочкаОтсчета = НачалоДня(ТочкаОтсчета);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПериодыПланирования.ВариантПредставленияПериода,
	|	упПериодыПланирования.ВремяНачалаПериода,
	|	упПериодыПланирования.ВремяОкончанияПериода,
	|	упПериодыПланирования.ПроизводственныйКалендарь,
	|	упПериодыПланирования.ДеньНеделиНачалоПериода,
	|	упПериодыПланирования.ДеньНеделиОкончаниеПериода,
	|	упПериодыПланирования.КоличествоДнейПлюсНачалоПериода,
	|	упПериодыПланирования.КоличествоДнейПлюсОкончаниеПериода,
	|	упПериодыПланирования.КоличествоНедельПлюсНачалоПериода,
	|	упПериодыПланирования.КоличествоНедельПлюсОкончаниеПериода
	|ИЗ
	|	Справочник.упПериодыПланирования КАК упПериодыПланирования
	|ГДЕ
	|	упПериодыПланирования.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ПериодСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ВариантПредставленияПериода = 0 Тогда // дни
		НачалоПериода    = ТочкаОтсчета + Выборка.КоличествоДнейПлюсНачалоПериода*86400;
		ОкончаниеПериода = ТочкаОтсчета + Выборка.КоличествоДнейПлюсОкончаниеПериода*86400;
		
	ИначеЕсли Выборка.ВариантПредставленияПериода = 1 Тогда // рабочие дни
		МассивДней = Новый Массив;
		МассивДней.Добавить(Выборка.КоличествоДнейПлюсНачалоПериода);
		МассивДней.Добавить(Выборка.КоличествоДнейПлюсОкончаниеПериода);
		МассивДат = КалендарныеГрафики.ПолучитьМассивДатПоКалендарю(Выборка.ПроизводственныйКалендарь, ТочкаОтсчета, МассивДней);
		
		НачалоПериода = МассивДат[0];
		ОкончаниеПериода = МассивДат[1];
		
	ИначеЕсли Выборка.ВариантПредставленияПериода = 2 Тогда // дни недели
		НачалоПериода     = НачалоНедели(ТочкаОтсчета) + (Выборка.ДеньНеделиНачалоПериода - 1)*86400 + Выборка.КоличествоНедельПлюсНачалоПериода*604800;
		ОкончаниеПериода  = НачалоНедели(ТочкаОтсчета) + (Выборка.ДеньНеделиОкончаниеПериода - 1)*86400 + Выборка.КоличествоНедельПлюсОкончаниеПериода*604800;
		
	КонецЕсли;
	
	НачалоПериода    = НачалоПериода + Час(Выборка.ВремяНачалаПериода)*3600 + Минута(Выборка.ВремяНачалаПериода)*60;
	ОкончаниеПериода = ОкончаниеПериода + Час(Выборка.ВремяОкончанияПериода)*3600 + Минута(Выборка.ВремяОкончанияПериода)*60;
	
	Возврат Новый Структура("НачалоПериода, ОкончаниеПериода", НачалоПериода, ОкончаниеПериода); 
	
КонецФункции // РассчитатьПериодПланирования()

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упПериодыПланирования.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции
	
#КонецОбласти  	

#КонецЕсли
