#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимостьЭлементовНастройкиПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Представление) Тогда
		СформироватьПредставлениеПериодаПланирования();
	КонецЕсли;
	Объект.Наименование = Объект.Представление;

КонецПроцедуры
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВариантПредставленияПериодаПриИзменении(Элемент)
	
	ОбновитьВидимостьЭлементовНастройкиПериода();
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДнейПлюсОкончаниеПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Ложь);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДнейПлюсНачалоПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Истина);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяНачалаПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Истина);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяОкончанияПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Ложь);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоРабочихДнейПлюсНачалоПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Истина);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоРабочихДнейПлюсОкончаниеПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Ложь);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньНеделиНачалоПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Истина);
	СформироватьПредставлениеПериодаПланирования();

КонецПроцедуры

&НаКлиенте
Процедура КоличествоНедельПлюсНачалоПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Истина);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньНеделиОкончаниеПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Ложь);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоНедельПлюсОкончаниеПериодаПриИзменении(Элемент)
	
	ПроверитьКорректностьВведенныхДанных(Ложь);
	СформироватьПредставлениеПериодаПланирования();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьВидимостьЭлементовНастройкиПериода()
	
	Если Объект.ВариантПредставленияПериода = 0 Тогда // дни
		Элементы.ГруппаНачалоПериодаВДнях.Видимость           = Истина;
		Элементы.ГруппаОкончаниеПериодаВДнях.Видимость        = Истина;
		Элементы.ГруппаНачалоПериодаВРабочихДнях.Видимость    = Ложь;
		Элементы.ГруппаОкончаниеПериодаВРабочихДнях.Видимость = Ложь;
		Элементы.ГруппаНачалоПериодаВДняхНедели.Видимость    = Ложь;
		Элементы.ГруппаОкончаниеПериодаВДняхНедели.Видимость  = Ложь;
		Элементы.ПроизводственныйКалендарь.Видимость = Ложь;
	ИначеЕсли Объект.ВариантПредставленияПериода = 1 Тогда// рабочие дни
		Элементы.ГруппаНачалоПериодаВДнях.Видимость           = Ложь;
		Элементы.ГруппаОкончаниеПериодаВДнях.Видимость        = Ложь;
		Элементы.ГруппаНачалоПериодаВРабочихДнях.Видимость    = Истина;
		Элементы.ГруппаОкончаниеПериодаВРабочихДнях.Видимость = Истина;
		Элементы.ГруппаНачалоПериодаВДняхНедели.Видимость    = Ложь;
		Элементы.ГруппаОкончаниеПериодаВДняхНедели.Видимость  = Ложь;
		Элементы.ПроизводственныйКалендарь.Видимость          = Истина;
	Иначе // дни недели
		Элементы.ГруппаНачалоПериодаВДнях.Видимость           = Ложь;
		Элементы.ГруппаОкончаниеПериодаВДнях.Видимость        = Ложь;
		Элементы.ГруппаНачалоПериодаВРабочихДнях.Видимость    = Ложь;
		Элементы.ГруппаОкончаниеПериодаВРабочихДнях.Видимость = Ложь;
		Элементы.ГруппаНачалоПериодаВДняхНедели.Видимость    = Истина;
		Элементы.ГруппаОкончаниеПериодаВДняхНедели.Видимость  = Истина;
		Элементы.ПроизводственныйКалендарь.Видимость          = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ОбновитьВидимостьЭлементовНастройкиПериода()

&НаКлиенте
Процедура СформироватьПредставлениеПериодаПланирования()
	
	ПредставлениеПериода = "";
	Если Объект.ВариантПредставленияПериода = 0 Тогда // дни
		Если Объект.КоличествоДнейПлюсНачалоПериода = 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + "С сегодняшнего дня";
		ИначеЕсли Объект.КоличествоДнейПлюсНачалоПериода = 1 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + "С завтрашнего дня";
		Иначе
			ПредставлениеПериода = ПредставлениеПериода + "С +" + Строка(Объект.КоличествоДнейПлюсНачалоПериода) + " дня(-ей)";
		КонецЕсли;
		
		ПредставлениеПериода = ПредставлениеПериода + " " + ?(ЗначениеЗаполнено(Объект.ВремяНачалаПериода), Формат(Объект.ВремяНачалаПериода, "ДФ=ЧЧ:мм"), "00:00");
		
		Если Объект.КоличествоДнейПлюсОкончаниеПериода = 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " по " + ?(ЗначениеЗаполнено(Объект.ВремяОкончанияПериода), Формат(Объект.ВремяОкончанияПериода, "ДФ=ЧЧ:мм"), "00:00");
		ИначеЕсли Объект.КоличествоДнейПлюсОкончаниеПериода = 1 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " по завтрашний день";
		Иначе	
			ПредставлениеПериода = ПредставлениеПериода + " по +" + Строка(Объект.КоличествоДнейПлюсОкончаниеПериода) + " дня(-ей)";
		КонецЕсли;
		
		Если Объект.КоличествоДнейПлюсОкончаниеПериода > 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " " + ?(ЗначениеЗаполнено(Объект.ВремяОкончанияПериода), Формат(Объект.ВремяОкончанияПериода, "ДФ=ЧЧ:мм"), "00:00");
		КонецЕсли;
		
	ИначеЕсли Объект.ВариантПредставленияПериода = 1 Тогда // рабочие дни
		Если Объект.КоличествоДнейПлюсНачалоПериода = 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + "С сегодняшнего рабочего дня";
		ИначеЕсли Объект.КоличествоДнейПлюсНачалоПериода = 1 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + "Со следующего рабочего дня";
		Иначе
			ПредставлениеПериода = ПредставлениеПериода + "С +" + Строка(Объект.КоличествоДнейПлюсНачалоПериода) + " рабочих дня(-ей)";
		КонецЕсли;
		
		ПредставлениеПериода = ПредставлениеПериода + " " + ?(ЗначениеЗаполнено(Объект.ВремяНачалаПериода), Формат(Объект.ВремяНачалаПериода, "ДФ=ЧЧ:мм"), "00:00");
		
		Если Объект.КоличествоДнейПлюсОкончаниеПериода = 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " по " + ?(ЗначениеЗаполнено(Объект.ВремяОкончанияПериода), Формат(Объект.ВремяОкончанияПериода, "ДФ=ЧЧ:мм"), "00:00");	
		ИначеЕсли Объект.КоличествоДнейПлюсОкончаниеПериода = 1 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " по следующий рабочий день";
		Иначе	
			ПредставлениеПериода = ПредставлениеПериода + " по +" + Строка(Объект.КоличествоДнейПлюсОкончаниеПериода) + " рабочих дня(-ей)";
		КонецЕсли;
		
		Если Объект.КоличествоДнейПлюсОкончаниеПериода > 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " " + ?(ЗначениеЗаполнено(Объект.ВремяОкончанияПериода), Формат(Объект.ВремяОкончанияПериода, "ДФ=ЧЧ:мм"), "00:00");
		КонецЕсли;

	Иначе // дни недели
		
		ПредставлениеПериода = "С";

		Если Объект.ДеньНеделиНачалоПериода = 1 Тогда
			ПредставлениеПериода = "С понедельника";
		ИначеЕсли Объект.ДеньНеделиНачалоПериода = 2 Тогда	
			ПредставлениеПериода = "Со вторника";
		ИначеЕсли Объект.ДеньНеделиНачалоПериода = 3 Тогда	
			ПредставлениеПериода = "Со среды";
		ИначеЕсли Объект.ДеньНеделиНачалоПериода = 4 Тогда	
			ПредставлениеПериода = "С четверга";
		ИначеЕсли Объект.ДеньНеделиНачалоПериода = 5 Тогда	
			ПредставлениеПериода = "С пятницы";
		ИначеЕсли Объект.ДеньНеделиНачалоПериода = 6 Тогда	
			ПредставлениеПериода = "С субботы";
		ИначеЕсли Объект.ДеньНеделиНачалоПериода = 7 Тогда	
			ПредставлениеПериода = "С воскресенья";
		КонецЕсли;
		
		ПредставлениеПериода = ПредставлениеПериода + " " + ?(ЗначениеЗаполнено(Объект.ВремяНачалаПериода), Формат(Объект.ВремяНачалаПериода, "ДФ=ЧЧ:мм"), "00:00");
		
		Если Объект.КоличествоНедельПлюсНачалоПериода = 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " текущей недели";
		ИначеЕсли Объект.КоличествоНедельПлюсНачалоПериода = 1 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " следующей недели";	
		ИначеЕсли Объект.КоличествоНедельПлюсНачалоПериода = 2 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " через неделю";		
		КонецЕсли;
		
		ПредставлениеПериода = ПредставлениеПериода + " по";
		
		Если Объект.ДеньНеделиОкончаниеПериода = 1 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " понедельник";
		ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = 2 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + " вторник";
		ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = 3 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + " среду";
		ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = 4 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + " четверг";
		ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = 5 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + " пятницу";
		ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = 6 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + " субботу";
		ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = 7 Тогда	
			ПредставлениеПериода = ПредставлениеПериода + " воскресенье";
		КонецЕсли;
		
		ПредставлениеПериода = ПредставлениеПериода + " " + ?(ЗначениеЗаполнено(Объект.ВремяОкончанияПериода), Формат(Объект.ВремяОкончанияПериода, "ДФ=ЧЧ:мм"), "00:00");
		
		Если Объект.КоличествоНедельПлюсОкончаниеПериода = 0 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " текущей недели";
		ИначеЕсли Объект.КоличествоНедельПлюсОкончаниеПериода = 1 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " следующей недели";	
		ИначеЕсли Объект.КоличествоНедельПлюсОкончаниеПериода = 2 Тогда
			ПредставлениеПериода = ПредставлениеПериода + " через неделю";		
		КонецЕсли;

	КонецЕсли;
	
	Объект.Представление = ПредставлениеПериода;
	
КонецПроцедуры // СформироватьПредставлениеПериодаПланирования()

&НаКлиенте
Процедура ПроверитьКорректностьВведенныхДанных(ЭтоЭлементНачальногоПериода = Ложь)
	
	Ошибка = Ложь;
	
	Если Объект.ВариантПредставленияПериода < 2 Тогда // дни или рабочие дни
		Если Объект.КоличествоДнейПлюсОкончаниеПериода > 0 Тогда
			Если Объект.КоличествоДнейПлюсОкончаниеПериода < Объект.КоличествоДнейПлюсНачалоПериода Тогда
				Ошибка = Истина;
			ИначеЕсли Объект.КоличествоДнейПлюсОкончаниеПериода = Объект.КоличествоДнейПлюсНачалоПериода Тогда
				Если Объект.ВремяОкончанияПериода <= Объект.ВремяНачалаПериода Тогда
					Ошибка = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Ошибка Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Окончание периода должно быть больше начала периода '"),,);
			ИзменитьГраницыПериода(ЭтоЭлементНачальногоПериода);
			СформироватьПредставлениеПериодаПланирования();
		КонецЕсли;
		
	Иначе // дни недели
		Если Объект.КоличествоНедельПлюсОкончаниеПериода < Объект.КоличествоНедельПлюсНачалоПериода Тогда
			Ошибка = Истина;
		ИначеЕсли Объект.КоличествоНедельПлюсОкончаниеПериода = Объект.КоличествоНедельПлюсНачалоПериода Тогда
			Если Объект.ДеньНеделиОкончаниеПериода < Объект.ДеньНеделиНачалоПериода Тогда
				Ошибка = Истина;
			ИначеЕсли Объект.ДеньНеделиОкончаниеПериода = Объект.ДеньНеделиНачалоПериода Тогда
				Если Объект.ВремяОкончанияПериода <= Объект.ВремяНачалаПериода Тогда
					Ошибка = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Ошибка Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Окончание периода должно быть больше начала периода '"),,);
			ИзменитьГраницыПериода(ЭтоЭлементНачальногоПериода);
			СформироватьПредставлениеПериодаПланирования();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьКорректностьВведенныхДанных()

&НаКлиенте
Процедура ИзменитьГраницыПериода(ЭтоЭлементНачальногоПериода)
	
	Если ЭтоЭлементНачальногоПериода Тогда 
		// приведение окончания периода к дате, превышающей начало периода
		Если Объект.ВариантПредставленияПериода < 2 Тогда // дни или рабочие дни
			Объект.КоличествоДнейПлюсОкончаниеПериода = Объект.КоличествоДнейПлюсНачалоПериода;
		Иначе // дни недели
			Объект.ДеньНеделиОкончаниеПериода = Объект.ДеньНеделиНачалоПериода;
			Объект.КоличествоНедельПлюсОкончаниеПериода = Объект.КоличествоНедельПлюсНачалоПериода;
		КонецЕсли;
		Объект.ВремяОкончанияПериода = Объект.ВремяНачалаПериода + 1;
	Иначе
		Если Объект.ВариантПредставленияПериода < 2 Тогда // дни или рабочие дни
			Объект.КоличествоДнейПлюсНачалоПериода = Объект.КоличествоДнейПлюсОкончаниеПериода;
		Иначе // дни недели
			Объект.ДеньНеделиНачалоПериода = Объект.ДеньНеделиОкончаниеПериода;
			Объект.КоличествоНедельПлюсНачалоПериода = Объект.КоличествоНедельПлюсОкончаниеПериода;
		КонецЕсли;
		Объект.ВремяНачалаПериода = Объект.ВремяОкончанияПериода - 1;
	КонецЕсли;
	
КонецПроцедуры // ИзменитьГраницыПериода()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов


#КонецОбласти 	
