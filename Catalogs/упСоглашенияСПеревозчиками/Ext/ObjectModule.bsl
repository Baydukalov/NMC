
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Номер) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упСоглашенияСПеревозчиками.Ссылка,
		               |	упСоглашенияСПеревозчиками.Владелец
		               |ИЗ
		               |	Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
		               |ГДЕ
		               |	упСоглашенияСПеревозчиками.Номер = &Номер
		               |	И (НЕ упСоглашенияСПеревозчиками.Ссылка = &Ссылка
		               |			ИЛИ &Ссылка = ЗНАЧЕНИЕ(Справочник.упСоглашенияСПеревозчиками.ПустаяСсылка))";
		Запрос.УстановитьПараметр("Номер", Номер);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		текВыборка = Запрос.Выполнить().Выбрать();
		Если текВыборка.Следующий() Тогда
			ТекстСообщения = Нстр("ru = 'Нарушена уникальность номера. Для соглашения %1 перевозчика %2 задан такой же номер'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текВыборка.Ссылка, текВыборка.Владелец);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,"Объект.Номер",,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачалаДействия) И ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда
		Если ДатаОкончанияДействия < ДатаНачалаДействия Тогда
			ТекстСообщения = Нстр("ru = 'Период действия задан неправильно. Дата окончания меньше чем дата начала'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,"Объект.ДатаОкончанияДействия",,Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.упДоговорыКонтрагентов") Тогда
		// Заполнение шапки
		Дата = ДанныеЗаполнения.Дата;
		ДатаНачалаДействия = ДанныеЗаполнения.ДатаНачалаДействия;
		ДатаОкончанияДействия = ДанныеЗаполнения.ДатаОкончанияДействия;
		Комментарий = ДанныеЗаполнения.Комментарий;
		КонтактноеЛицо = ДанныеЗаполнения.КонтактноеЛицо;
		Контрагент = ДанныеЗаполнения.Контрагент;
		Наименование = ДанныеЗаполнения.Наименование;
		Организация = ДанныеЗаполнения.Организация;
		Владелец = ДанныеЗаполнения.Партнер;
		Договор = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 