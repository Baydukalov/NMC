#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает ссылку на типовое соглашение партнера.
//
// Параметры:
//  Партнер	 - СправочникСсылка	 - Ссылка на элемент.
//  Организация - СправочникСсылка.Организации 	 - организация.
// 
// Возвращаемое значение:
//  СправочникСсылка.упСоглашенияСПеревозчиками - Результат выполнения.
//
Функция СоглашениеПоУмолчанию(Партнер, Организация = Неопределено) Экспорт
	
	Соглашение = Справочники.упСоглашенияСПеревозчиками.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	упСоглашенияСПеревозчиками.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
	|ГДЕ
	|	НЕ упСоглашенияСПеревозчиками.ПометкаУдаления
	|	И упСоглашенияСПеревозчиками.Владелец = &Владелец
	|	И &Организация В (упСоглашенияСПеревозчиками.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
	Запрос.УстановитьПараметр("Владелец", Партнер);
	Запрос.УстановитьПараметр("Организация", ?(Организация = Неопределено, Справочники.Организации.ПустаяСсылка(),Организация));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Если есть соглашения по заданной организации.
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Если Истина 
			И Выборка.Следующий()
			И Выборка.Количество() = 1 Тогда
			Соглашение = Выборка.Соглашение;	
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Соглашение) Тогда
		// Если есть соглашения с незаполненным полем организация.
		Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПустаяСсылка());
		Выборка = Запрос.Выполнить().Выбрать();
		Если Истина
			И Выборка.Следующий()
			И Выборка.Количество() = 1 Тогда
			Соглашение = Выборка.Соглашение;
		КонецЕсли;	
	КонецЕсли;

	Возврат Соглашение;

КонецФункции

// Возвращает ссылку на валюту переданного соглашения.
//
// Параметры:
//  Соглашение  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упСоглашенияСПеревозчиками - Результат выполнения.
//
Функция ВалютаСоглашения(Соглашение) Экспорт
	
	Валюта = Справочники.Валюты.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упГруппыТарифов.Валюта
	|ИЗ
	|	Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГруппыТарифов КАК упГруппыТарифов
	|		ПО упСоглашенияСПеревозчиками.ГруппаТарифов = упГруппыТарифов.Ссылка
	|ГДЕ
	|	упСоглашенияСПеревозчиками.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Соглашение);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Валюта = текВыборка.Валюта;
	КонецЕсли;
	
	Возврат Валюта;
			
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	текПартнер = Справочники.упПартнеры.ПустаяСсылка();
	Если Параметры.Отбор.Свойство("Владелец", текПартнер) Тогда
		СтандартнаяОбработка = Ложь;
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 51
		|	упСоглашенияСПеревозчиками.Ссылка КАК Соглашение
		|ИЗ
		|	Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияПартнеров КАК упИерархияПартнеров
		|		ПО упСоглашенияСПеревозчиками.Владелец = упИерархияПартнеров.Родитель
		|ГДЕ
		|	упСоглашенияСПеревозчиками.Наименование ПОДОБНО &СтрокаПоиска
		|	И упИерархияПартнеров.Партнер = &Партнер
		|	И упСоглашенияСПеревозчиками.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыСоглашений.Действует)";
		текЗапрос.УстановитьПараметр("Партнер",      текПартнер);
		текЗапрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		
		тбзСоглашения	= текЗапрос.Выполнить().Выгрузить();
		
		спзДанныеВыбора	= Новый СписокЗначений;
		спзДанныеВыбора.ЗагрузитьЗначения(тбзСоглашения.ВыгрузитьКолонку("Соглашение"));
		
		ДанныеВыбора = спзДанныеВыбора;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли




