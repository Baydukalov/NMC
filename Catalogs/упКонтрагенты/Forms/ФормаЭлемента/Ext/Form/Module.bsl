
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, "ГруппаКонтактнаяИнформация");
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ИНН) И (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицоНеРезидент"))Тогда
		Объект.ИНН = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.КПП) И НЕ Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицо") ИЛИ НЕ ЗначениеЗаполнено(Объект.ИНН) Тогда
		Объект.КПП = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.КодПоОКПО) И (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицоНеРезидент") ИЛИ Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ФизЛицо")) Тогда
		Объект.КодПоОКПО = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Источник = Объект.Ссылка  Тогда
		Прочитать();
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)

	УправлениеДоступностью();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ МЕХАНИЗМА СВОЙСТВ

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


&НаКлиенте
Процедура ЗаполнитьКПП(Команда)
	
	ЗаполнитьКППпоИНН();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Процедура заполняет КПП на базе ИНН
//
// Параметры
//  ИНН  - Строка - ИНН на основании которого будет сгенерирован КПП.
//  КПП  - Строка - КПП, текущий КПП контрагента.
//  БезусловноеЗаполнениеКПП  - Булево - если Истина, то не будет запрашиваться подтверждение пользователя при смене КПП.
//
Процедура ЗаполнитьКППпоИНН()
	
	// Если КПП формируется стандартным образом по ИНН, то для КПП берутся.
	// первые 4 цифры ИНН + 01001, например:
	// ИНН 7712563009.
	// КПП 771201001.
		
	// если не указано ИНН или заполнено КПП то прерываем выполнение операции.
	Если (СтрДлина(Объект.ИНН) < 4) Тогда
		Возврат;
	КонецЕсли;
	
	ПревыеСимволыИНН = Лев(Объект.ИНН, 4);
	НовоеЗначениеКПП = ПревыеСимволыИНН + "01001";
	
	// Если новое значение КПП и старое значение КПП совпадают, то прерываем выполнение операции.
	Если (НовоеЗначениеКПП = Объект.КПП) Тогда
		Возврат;
	КонецЕсли;
	
	// Если безусловно нужно поменять - меняем.
	Если НЕ ЗначениеЗаполнено(Объект.КПП) Тогда
		Объект.КПП = НовоеЗначениеКПП;
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Изменить КПП  на ""%1""?'"), НовоеЗначениеКПП);
	ДополнительныеПараметры = НовоеЗначениеКПП;
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросПроКПП",ЭтаФорма,ДополнительныеПараметры);
	ПоказатьВопрос(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ОбработкаОтветаНаВопросПроКПП(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.КПП = ДополнительныеПараметры;	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура УправлениеДоступностью()
	
	Элементы.КПП.Доступность          = (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицо") И ЗначениеЗаполнено(Объект.ИНН));
	Элементы.КодПоОКПО.Доступность    = (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицо") ИЛИ
	                                     Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ИндивидуальныйПредприниматель"));
	Элементы.ИНН.Доступность          = НЕ Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицоНеРезидент");
	Элементы.ЗаполнитьКПП.Доступность = (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.упЮрФизЛицо.ЮрЛицо") И ЗначениеЗаполнено(Объект.ИНН));
	
КонецПроцедуры

#Область Подсистема_КонтактнаяИнформация

// СтандартныеПодсистемы.КонтактнаяИнформация

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
	Подключаемый_ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПредставлениеОчистка(ЭтотОбъект, Элемент.Имя);
	Подключаемый_ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПодключаемаяКоманда(ЭтотОбъект, Команда.Имя);
	Подключаемый_ОбновитьКонтактнуюИнформацию(Результат);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуВводаАдреса(ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаСервере
Функция Подключаемый_ОбновитьКонтактнуюИнформацию(Результат) 
	
	Возврат УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	
КонецФункции

// Конец СтандартныеПодсистемы.КонтактнаяИнформация

#КонецОбласти 

#КонецОбласти 




