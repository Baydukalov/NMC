
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Групповое изменение объектов.

// Возвращает список реквизитов, которые не нужно редактировать,
// с помощью обработки группового изменения объектов.
//
// Возвращаемое значение:
//   Массив   - Результат выполнения.
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Основной");
	
	Возврат Результат;
	
КонецФункции

// Возвращает ссылку на основного контрагента.
//
// Параметры:
//  Партнер - СправочникСсылка - ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.упКонтрагенты   - Результат выполнения.
//
Функция ПолучитьОсновногоКонтрагента(Партнер) Экспорт
	
	текРезультат = Справочники.упКонтрагенты.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Партнер) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	упКонтрагенты.Ссылка КАК Контрагент,
		|	ВЫБОР
		|		КОГДА упКонтрагенты.Основной
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ОсновнойКонтрагент
		|ИЗ
		|	Справочник.упКонтрагенты КАК упКонтрагенты
		|ГДЕ
		|	упКонтрагенты.Партнер = &Партнер
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновнойКонтрагент";
		Запрос.УстановитьПараметр("Партнер", Партнер);
		текВыборка	= Запрос.Выполнить().Выбрать();
		
		Если текВыборка.Следующий() Тогда
			текРезультат = текВыборка.Контрагент;
		КонецЕсли;
	КонецЕсли;
	
	Возврат текРезультат;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	текПартнер = Справочники.упПартнеры.ПустаяСсылка();
	Если Параметры.Отбор.Свойство("Партнер", текПартнер) Тогда
		СтандартнаяОбработка = Ложь;
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 51
		|	упКонтрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.упКонтрагенты КАК упКонтрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияПартнеров КАК упИерархияПартнеров
		|		ПО упКонтрагенты.Партнер = упИерархияПартнеров.Родитель
		|ГДЕ
		|	упКонтрагенты.Наименование ПОДОБНО &СтрокаПоиска
		|	И упИерархияПартнеров.Партнер = &Партнер";
		текЗапрос.УстановитьПараметр("Партнер",      текПартнер);
		текЗапрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		
		тбзСсылка	= текЗапрос.Выполнить().Выгрузить();
		
		спзДанныеВыбора	= Новый СписокЗначений;
		спзДанныеВыбора.ЗагрузитьЗначения(тбзСсылка.ВыгрузитьКолонку("Ссылка"));
		
		ДанныеВыбора = спзДанныеВыбора;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

