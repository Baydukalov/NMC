
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		УстановитьПривилегированныйРежим(Истина);
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	упКонтрагенты.Ссылка,
		|	упКонтрагенты.Наименование
		|ИЗ
		|	Справочник.упКонтрагенты КАК упКонтрагенты
		|ГДЕ
		|	упКонтрагенты.ИНН = &ИНН
		|	И упКонтрагенты.КПП = &КПП
		|	И упКонтрагенты.Ссылка <> &Ссылка";
	
		Запрос.УстановитьПараметр("ИНН",    ИНН);
		Запрос.УстановитьПараметр("КПП",    КПП);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Данные ИНН и КПП уже указаны для контрагента %1.'"), Выборка.Наименование), Ссылка, "Объект.ИНН",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ТекстСообщения = "";
	ЭтоЮрЛицо = (ЮрФизЛицо = Перечисления.упЮрФизЛицо.ЮрЛицо ИЛИ ЮрФизЛицо = Перечисления.упЮрФизЛицо.ЮрЛицоНеРезидент);
	
	Если Не ЭтоНовый() Тогда
		ЗначенияРеквизитовВИнформационнойБазе = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(Ссылка, Новый Структура("ИНН,КПП,КодПоОКПО"));
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИНН) Тогда 
		Если (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.ИНН <> ИНН)
			И НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ЭтоЮрЛицо, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			"ИНН",
			,
			Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЮрЛицо Тогда
		Если НЕ ПустаяСтрока(КПП) 
			И (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.КПП <> КПП)
			И НЕ РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			"КПП",
			,
			Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЮрФизЛицо = Перечисления.упЮрФизЛицо.ЮрЛицо 
		ИЛИ ЮрФизЛицо = Перечисления.упЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		Если НЕ ПустаяСтрока(КодПоОКПО) 
			И (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.КодПоОКПО <> КодПоОКПО)
			И НЕ РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО, ЭтоЮрЛицо, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			"КодПоОкпо",
			,
			Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли
