#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	СхемаКомпоновкиДанных = ТекущийОбъект.СхемаКомпоновкиДанных.Получить();
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	//// СтандартныеПодсистемы.ВерсионированиеОбъектов
	//ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	//// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	//	
	//// подсистема запрета редактирования ключевых реквизитов объектов	
	//ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
			
	// Переданный в параметре адрес сохраняется в качестве адреса исходной схемы
	Если Не ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда 
		СхемаКомпоновкиДанных = ПолучитьСКД();
		Если СхемаКомпоновкиДанных <> Неопределено Тогда 
			АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
		КонецЕсли;
	Иначе	
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	КонецЕсли;	
	
	Если СхемаКомпоновкиДанных <> Неопределено Тогда 
		Попытка
			УстановитьПараметрУчетаНаправленияДеятельности(СхемаКомпоновкиДанных);
			КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
			КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			КомпоновщикНастроек.Восстановить();
		Исключение
			СхемаКомпоновкиДанныхАктуальная = ПолучитьСКД();
			Если СхемаКомпоновкиДанныхАктуальная <> Неопределено Тогда 
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Отбор, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор);
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Порядок, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Порядок);
				АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхАктуальная, УникальныйИдентификатор);
				
				КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
				КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию);
				КомпоновщикНастроек.Восстановить();
			КонецЕсли;
			Модифицированность = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если СхемаКомпоновкиДанных = Неопределено Тогда 
		Элементы.ГруппаНастройкаСКД.Доступность = Ложь;
	КонецЕсли;
	
	//ОбновитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АлгоритмПроверкиПриИзменении(Элемент)
	Если Объект.АлгоритмПроверки <> ПредопределенноеЗначение("Перечисление.упАлгоритмыПроверкиСобытия.ПроизвольныйАлгоритм") Тогда 
    		ЗагрузитьСКД();
	КонецЕсли;
	НастройкаФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	КомпоновщикНастроек.Восстановить();
	
	Если ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		
		упСервисныеФункцииВызовСервера.ОчиститьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		упСервисныеФункцииВызовСервера.СкопироватьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанных.НастройкиПоУмолчанию, КомпоновщикНастроек.ПолучитьНастройки());
		
		ТекущийОбъект.СхемаКомпоновкиДанных = Новый ХранилищеЗначения(СхемаКомпоновкиДанных);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастройкаФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Функция ПолучитьСКД()
	
	Возврат Справочники.упВариантыРаспределенияРасходов.ПолучитьМакет("Рейс");
	
КонецФункции // ПолучитьСКД()

&НаСервере
Процедура ЗагрузитьСКД()
	
	// Переданный в параметре адрес сохраняется в качестве адреса исходной схемы
	Если ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда 
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	Иначе
		СхемаКомпоновкиДанных = Неопределено;
	КонецЕсли;
		
	СхемаКомпоновкиДанныхНовая = ПолучитьСКД();

	Если СхемаКомпоновкиДанныхНовая = Неопределено Тогда 
		Элементы.ГруппаНастройкаСКД.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	Элементы.ГруппаНастройкаСКД.Доступность = Истина;
	
	Если СхемаКомпоновкиДанных <> Неопределено Тогда 
		упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию.Отбор, КомпоновщикНастроек.Настройки.Отбор);
		упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию.Порядок, КомпоновщикНастроек.Настройки.Порядок);
	КонецЕсли;
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхНовая, УникальныйИдентификатор);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФормы()
	
	Если Объект.АлгоритмПроверки = ПредопределенноеЗначение("Перечисление.упАлгоритмыПроверкиСобытия.ПроизвольныйАлгоритм") Тогда 
		Элементы.ГруппаПроверка.ТекущаяСтраница = Элементы.ГруппаПроизвольныйАлгоритмПроверки;
	Иначе
		Элементы.ГруппаПроверка.ТекущаяСтраница = Элементы.ГруппаНастройкаСКД;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрУчетаНаправленияДеятельности(СхемаКомпоновкиДанных)
	
	ПараметрСхемыУчетНаправленияДеятельности = СхемаКомпоновкиДанных.Параметры.Найти("ИспользуетсяОтборПоНаправлениюДеятельности");
	Если ПараметрСхемыУчетНаправленияДеятельности <> Неопределено Тогда
		ПараметрСхемыУчетНаправленияДеятельности.ОграничениеИспользования = Не ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПоНаправлениямДеятельности");
	КонецЕсли;
	
	// Если используется учет по направлениям деятельности, то проверяется включено ли поле в выбранных полях.
	Если ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПоНаправлениямДеятельности") Тогда
		
		ПолеНаправлениеДеятельности = Новый ПолеКомпоновкиДанных("НаправлениеДеятельности");
		
		Для каждого ВыбранноеПоле Из СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Выбор.Элементы Цикл
			
			Если ВыбранноеПоле.Поле = ПолеНаправлениеДеятельности Тогда
				Если Не ВыбранноеПоле.Использование Тогда
					Модифицированность = Истина;
					ТекстСообщения = Нстр("ru = 'Вариант распределения был создан при отключенной опции ""Используется учет по направлениям деятельности"", необходимо записать элемент'");
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					ВыбранноеПоле.Использование = Истина;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
