
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		Элементы.ИспользоватьКакОсновной.Пометка = ТекущиеДанные.Основной;
	КонецЕсли;		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ИспользоватьКакОсновной(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		Отказ = Ложь;
		Если ТекущиеДанные.Основной Тогда
			ТекущийСчет = ПредопределенноеЗначение("Справочник.упБанковскиеСчетаКонтрагентов.ПустаяСсылка");
		Иначе	
			ТекущийСчет = ТекущиеДанные.Ссылка;
		КонецЕсли;
		
		УстановитьСчетПоУмолчанию(ТекущиеДанные.Владелец, ТекущийСчет,Отказ);
		Если НЕ Отказ Тогда
			ТекущиеДанные.Основной = НЕ ТекущиеДанные.Основной;	
			Элементы.ИспользоватьКакОсновной.Пометка = ТекущиеДанные.Основной;
			Элементы.Список.Обновить();
			Оповестить(,,ТекущиеДанные.Владелец);
		КонецЕсли;
	
	КонецЕсли;		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьСчетПоУмолчанию(Владелец, Счет, Отказ)
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Владелец);
	Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ТекстСообщения = Нстр("ru = 'Перед изменением основного счета необходимо записать контрагента.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Владелец,,,Отказ);
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, ,Владелец, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	КонецПопытки;	
	Если НЕ Отказ Тогда
		упБанковскиеСчетаВызовСервера.УстановитьСчетПоУмолчанию(Владелец, Счет, Отказ);		
	КонецЕсли;	
КонецПроцедуры
	
#КонецОбласти

