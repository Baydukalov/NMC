#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	ИспользуетсяБанкДляРасчетов = ЗначениеЗаполнено(Объект.БанкДляРасчетов);
	ДоступностьБанкаДляРасчетов(ЭтаФорма);	
	Если НЕ ЗначениеЗаполнено(Параметры.Ключ)
			И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Валюта	= ЭтаФорма.ВалютаРеглУчета;
		Объект.Валютный = Объект.Валюта <> ЭтаФорма.ВалютаРеглУчета;	
	КонецЕсли;
	
	ОбновитьЗначенияРеквизитовФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ВалютаРеглУчета = упСервисныеФункцииВызовСервера.ВалютаРегламентированногоУчета();
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьФорму();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользуетсяБанкДляРасчетовПриИзменении(Элемент)
	Если НЕ ИспользуетсяБанкДляРасчетов И ЗначениеЗаполнено(Объект.БанкДляРасчетов) Тогда
		Объект.БанкДляРасчетов = ПредопределенноеЗначение("Справочник.КлассификаторБанковРФ.ПустаяСсылка");
	КонецЕсли;
	ДоступностьБанкаДляРасчетов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура БанкАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	упБанковскиеСчетаКлиентСервер.ПодборБанка(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	мсвБанки = Новый Массив;
	Если ЗначениеЗаполнено(Объект.Банк) Тогда
		мсвБанки.Добавить(Объект.Банк);
	КонецЕсли;
	ОбновитьЗначенияРеквизитовФормы(мсвБанки);
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаПриИзменении(Элемент)
	УстановитьНаименованиеСчета();
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
		
	Если НЕ ЗначениеЗаполнено(Объект.Валюта) Тогда
		Объект.Валюта = ВалютаРеглУчета;
	КонецЕсли;
	
	Объект.Валютный = Объект.Валюта <> ВалютаРеглУчета;
	
	УстановитьНаименованиеСчета();
КонецПроцедуры

&НаКлиенте
Процедура БанкДляРасчетовПриИзменении(Элемент)
	мсвБанки = Новый Массив;
	Если ЗначениеЗаполнено(Объект.БанкДляРасчетов) Тогда
		мсвБанки.Добавить(Объект.БанкДляРасчетов);
	КонецЕсли;
	ОбновитьЗначенияРеквизитовФормы(мсвБанки);
	ОбновитьФорму();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДоступностьБанкаДляРасчетов(Форма)
	Форма.Элементы.БанкДляРасчетов.Доступность = Форма.ИспользуетсяБанкДляРасчетов;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНаименованиеСчета()
	Объект.Наименование = упБанковскиеСчетаКлиентСервер.НаименованиеБанковскогоСчета(Объект.НомерСчета, Объект.Банк, Объект.Валюта, Объект.Валютный);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму()
	Элементы.СостояниеБанка.ТекущаяСтраница = ?(ДеятельностьБанкаПрекращена, Элементы.БанкЗакрыт, Элементы.БанкРаботает);
	Элементы.СостояниеБанкРасчетов.ТекущаяСтраница = ?(ДеятельностьБанкаРасчетовПрекращена, Элементы.БанкРасчетовЗакрыт, Элементы.БанкРасчетовРаботает);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначенияРеквизитовФормы(мсвБанки = Неопределено)
	
	ДеятельностьБанкаПрекращена 		= Ложь;
	ДеятельностьБанкаРасчетовПрекращена = Ложь;
	
	Если мсвБанки = Неопределено Тогда
		мсвБанки = Новый Массив;
		Если ЗначениеЗаполнено(Объект.Банк) Тогда
			мсвБанки.Добавить(Объект.Банк);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.БанкДляРасчетов) Тогда
			мсвБанки.Добавить(Объект.БанкДляРасчетов);
		КонецЕсли;
	КонецЕсли;
	
	Если мсвБанки.Количество() > 0 Тогда
			
		мсвИндексыБанков = упБанковскиеСчетаВызовСервера.ИндексыБанковЧьяДеятельностьПрекращена(мсвБанки);
		
		Для каждого ЭлементМассива Из мсвИндексыБанков Цикл
			Если Объект.Банк = мсвБанки[ЭлементМассива] Тогда
				ДеятельностьБанкаПрекращена 		= Истина;	
			КонецЕсли;
			Если Объект.БанкДляРасчетов = мсвБанки[ЭлементМассива] Тогда
				ДеятельностьБанкаРасчетовПрекращена 		= Истина;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти
