
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Проверка заполнения реквизитов.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если ВыборПеревозчика <> Перечисления.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Перевозчик");
	КонецЕсли;	
	
	Если ИспользуютсяУслугиИнкассации Тогда
		ПроверяемыеРеквизиты.Добавить("СпособУчетаИнкассируемыхЦенностей");
	КонецЕсли;
	
	Если ПорядокИсполненияЗаявки = Перечисления.упПорядокИсполненияЗаявок.ПутевымЛистом Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СпособПрохожденияТочекМаршрута");	
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВариантФормированияМаршрута");	
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидыТочекОтправления");	
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВыборПеревозчика");	
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВыборТС");	
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СпособУчетаВозвращенныхДокументов");	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ВидыАдресовДляСтатистики.Свернуть("ВидАдреса");
	
	Если Не ОбменДанными.Загрузка Тогда
		Если Истина
			И ПараметрыВводаИнформацииОГрузовыхМестах <> ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса")
			И ПараметрыВводаИнформацииОГрузовыхМестах <> ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку")
		Тогда
			ЭтотОбъект.ПравилоСвязиТиповГрузовВРейсе = Неопределено;
		КонецЕсли; 
		
		// Проверяются дублирующие значения в ТЧ."Виды точек отправления".
		ТекстСообщения	= НСтр("ru = 'В табличной части ""Виды точек отправления"" присутствуют строки с продублированными видами адресов.'");			
		Для каждого Строка Из ВидыТочекОтправления Цикл
			ТекущийИндекс = ВидыТочекОтправления.Индекс(Строка);
			Отбор = Новый Структура("ВидТочкиОтправления",Строка.ВидТочкиОтправления);	  
			НайденныеСтроки = ВидыТочекОтправления.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 1 Тогда
				Для каждого СтрокаНайденная Из НайденныеСтроки Цикл
					ИндексСтроки = ВидыТочекОтправления.Индекс(СтрокаНайденная);
					Если ТекущийИндекс <> ИндексСтроки Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Ссылка,"Объект.ВидыТочекОтправления[" + Формат(ИндексСтроки,"ЧН=0; ЧГ=0") + "].ВидТочкиОтправления");
						ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись вида перевозки'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
						 					УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
						 
					КонецЕсли;
				КонецЦикла;		
				Отказ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;  
		
		// Устанавливается реквизит "Вид точки отправления".
		Если Не ЗначениеЗаполнено(ВидТочкиОтправления) Тогда
			тбзВидыТочекОтправления = ВидыТочекОтправления.Выгрузить();
			тбзВидыТочекОтправления.Сортировать("ПриоритетИспользования");
			Если тбзВидыТочекОтправления.Количество() > 0 Тогда
				ВидТочкиОтправления = тбзВидыТочекОтправления[0].ВидТочкиОтправления;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли