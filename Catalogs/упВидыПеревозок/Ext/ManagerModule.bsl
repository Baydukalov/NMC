#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура производит первоначальное заполнение предопределенных элементов справочника.
//
Процедура ПервоначальноеЗаполнение() Экспорт
	
	текЭлемент = Справочники.упВидыПеревозок.Основной.ПолучитьОбъект();
	текЭлемент.СтильОтображения = Справочники.упСтилиЛиний.Основной;
	текЭлемент.ПараметрыВводаИнформацииОГрузовыхМестах = Перечисления.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится;
	текЭлемент.ВыборПеревозчика = Перечисления.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе;
	текЭлемент.ВыборТС = Перечисления.упВариантыПодбораТС.НепосредственныйПодборВРейсе;
	текЭлемент.СпособУчетаВозвращенныхДокументов = Перечисления.упСпособыУчетаВозвращенныхДокументов.СТочностьюДоТиповДокументов;
	текЭлемент.ПорядокИсполненияЗаявки = Перечисления.упПорядокИсполненияЗаявок.Рейсом;
	текЭлемент.ВидТранспортнойУслуги = Перечисления.упВидыТранспортныхУслуг.ПеревозкаГрузов;
	текЭлемент.Записать();

	Константы.упИспользуетсяОдинВидПеревозки.Установить(Истина);
	
КонецПроцедуры	

// Функция возвращает значение по умолчанию.
//
// Параметры:
//  ВидПеревозки					 - СправочникиСсылка.упВидыПеревозок - ссылка на элемент.
//  ВидТранспортнойУслуги			 - ПеречислениеСсылка.упВидыТранспортныхУслуг - вид услуги.
//  ПорядокИсполненияЗаявки			 - ПеречислениеСсылка.упПорядокИсполненияЗаявок - порядок исполнения.
//  РазрешеноФормированиеКонтейнеров	 - Булево -  Истина, если при подборе надо учитывать признак формирования контейнеров.
// 
// Возвращаемое значение:
//  СправочникиСсылка.упВидыПеревозок - Результат выполнения.
//
Функция ЗначениеПоУмолчанию(ВидПеревозки = Неопределено, ВидТранспортнойУслуги = Неопределено, ПорядокИсполненияЗаявки = Неопределено, РазрешеноФормированиеКонтейнеров = Ложь) Экспорт
		
	Если ВидПеревозки = Неопределено Тогда 
		Результат = Справочники.упВидыПеревозок.ПустаяСсылка();
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	упВидыПеревозок.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ ИСТИНА
	|	И НЕ (упВидыПеревозок.ПометкаУдаления)
	|";
	
	ИспользуютсяРазличныеВидыПеревозок = ПолучитьФункциональнуюОпцию("упИспользуютсяРазличныеВидыПеревозок");
	
	Если ВидТранспортнойУслуги <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И упВидыПеревозок.ВидТранспортнойУслуги = &ВидТранспортнойУслуги";
		Запрос.УстановитьПараметр("ВидТранспортнойУслуги", ВидТранспортнойУслуги);
	КонецЕсли;
	
	Если Истина
		И ИспользуютсяРазличныеВидыПеревозок
		И ПорядокИсполненияЗаявки <> Неопределено
	Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И упВидыПеревозок.ПорядокИсполненияЗаявки = &ПорядокИсполненияЗаявки";
		Запрос.УстановитьПараметр("ПорядокИсполненияЗаявки", ПорядокИсполненияЗаявки);
	КонецЕсли;
	
	Если Не ИспользуютсяРазличныеВидыПеревозок Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И упВидыПеревозок.Предопределенный
		|	И упВидыПеревозок.Наименование = ""Основной""";
	КонецЕсли;
	
	Если РазрешеноФормированиеКонтейнеров Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И упВидыПеревозок.РазрешеноФормированиеКонтейнеров";
	КонецЕсли;

	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда 
		Выборка.Следующий();
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

// Функция - Виды точек отправления
//
// Параметры:
//  ВидПеревозки - СправочникСсылка.упВидыПеревозок - вид перевозки.
// 
// Возвращаемое значение:
//  Массив(СправочникСсылка.упВидыАдресов) - вид адреса.
//
Функция ВидыТочекОтправления(ВидПеревозки) Экспорт
	
	мсвРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упВидыПеревозокВидыТочекОтправления.ВидТочкиОтправления КАК ВидТочкиОтправления
	               |ИЗ
	               |	Справочник.упВидыПеревозок.ВидыТочекОтправления КАК упВидыПеревозокВидыТочекОтправления
	               |ГДЕ
	               |	упВидыПеревозокВидыТочекОтправления.Ссылка = &ВидПеревозки";
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвРезультат.Добавить(Выборка.ВидТочкиОтправления);
	КонецЦикла; 
	
	Возврат мсвРезультат;
		
КонецФункции

// Возвращает ссылку на вид перевозки.
//
// Параметры:
//  Ссылка  - ДокументСсылка - ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникиСсылка.упВидыПеревозок - Результат выполнения.
//
Функция ПодобратьВидПеревозки(Ссылка) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	упВидыПеревозок.Ссылка
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	НЕ упВидыПеревозок.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА упВидыПеревозок.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Высокая)
	|			ТОГДА 0
	|		КОГДА упВидыПеревозок.Важность = ЗНАЧЕНИЕ(Перечисление.упВариантыВажности.Низкая)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ";
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
	ЗапросВыбораСхемыЗапроса = ПакетЗапросов[0];
	ОператорыВыбрать = ЗапросВыбораСхемыЗапроса.Операторы[0];
	КоличествоПолей = ОператорыВыбрать.ВыбираемыеПоля.Количество();
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗапросУсловийПеревозки") Тогда
		ОператорыВыбрать.ВыбираемыеПоля.Добавить("упВидыПеревозок.СхемаЗапросУсловийПеревозки");
		ОператорыВыбрать.Отбор.Добавить("упВидыПеревозок.ВидТранспортнойУслуги <> ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ПеревозкаПассажиров)");
		ОператорыВыбрать.Отбор.Добавить("упВидыПеревозок.ВидТранспортнойУслуги <> ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.УслугиСпецтехники)");
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		ОператорыВыбрать.ВыбираемыеПоля.Добавить("упВидыПеревозок.СхемаЗаявкаНаПеревозкуГруза");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	ЗапросВыбораСхемыЗапроса.Колонки[КоличествоПолей].Псевдоним = "СхемаКомпоновкиДанных";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
						
			СхемаКомпоновкиДанных = Выборка.СхемаКомпоновкиДанных.Получить();
			Если СхемаКомпоновкиДанных = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор, "Ссылка", Ссылка, ВидСравненияКомпоновкиДанных.Равно,, Истина);
			
			КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
			КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
			КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			
			// Компоновка макета.
			КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
			МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
			
			// Инициализация процессора компоновки.
			ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
			ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
			
			// Таблица значений, в которую будет получен результат.
			Результат = Новый ТаблицаЗначений;
			
			// Получение результата.
			ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
			ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.УстановитьОбъект(Результат);
			ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений.Вывести(ПроцессорКомпоновкиДанных);
			
			Если Результат.Количество() Тогда
				Возврат Выборка.Ссылка;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат Неопределено;

КонецФункции // ПодобратьВидПеревозки()

// Возвращает количество элементов справочника Организации.
// Не учитывает предопределенные и помеченные на удаление элементы.
//
// Возвращаемое значение:
//     Число - количество организаций.
//
Функция КоличествоВидовПеревозки() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Количество = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	НЕ упВидыПеревозок.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Количество;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упВидыПеревозок.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Возвращает текст запроса соответвие транспорта виду перевозки.
//
// Возвращаемое значение:
//   Строка - Текст запроса.
//
Функция ПолучитьТекстЗапросаСоответствиеТранспортаВидуПеревозки() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	СписокТС.Перевозчик,
	|	СписокТС.ТипТС,
	|	СписокТС.ТранспортноеСредство,
	|	СписокТС.Количество,
	|	СписокТС.КоличествоЦиклов,
	|	СписокТС.ПланироватьЦиклически
	|ПОМЕСТИТЬ втТранспортныеСредстваПодготовка
	|ИЗ
	|	&ТранспортныеСредства КАК СписокТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упВидыПеревозок.ВыборПеревозчика КАК ВариантВыбораПеревозчика,
	|	упВидыПеревозок.ВыборТС,
	|	упВидыПеревозок.Перевозчик
	|ПОМЕСТИТЬ втВидыПеревозок
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТранспортныеСредства.Перевозчик,
	|	втТранспортныеСредства.ТипТС,
	|	втТранспортныеСредства.ТранспортноеСредство,
	|	втТранспортныеСредства.Количество,
	|	втТранспортныеСредства.КоличествоЦиклов,
	|	втТранспортныеСредства.ПланироватьЦиклически
	|ПОМЕСТИТЬ втТранспортныеСредства
	|ИЗ
	|	втТранспортныеСредстваПодготовка КАК втТранспортныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (&ИспользоватьВиртуальныеТранспортныеСредства
	|				ИЛИ втТранспортныеСредства.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчики
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаСобственнымиТС))
	|			И (упПартнеры.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втВидыПеревозок.Перевозчик
	|ИЗ
	|	втВидыПеревозок КАК втВидыПеревозок
	|ГДЕ
	|	втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (упПартнеры.Перевозчик)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТранспортныеСредства.Перевозчик,
	|	втТранспортныеСредства.ТипТС,
	|	втТранспортныеСредства.ТранспортноеСредство,
	|	втТранспортныеСредства.Количество,
	|	втТранспортныеСредства.КоличествоЦиклов,
	|	втТранспортныеСредства.ПланироватьЦиклически
	|ИЗ
	|	втТранспортныеСредства КАК втТранспортныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втТранспортныеСредства.Перевозчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТранспортныеСредства.Перевозчик,
	|	втТранспортныеСредства.ТипТС,
	|	втТранспортныеСредства.ТранспортноеСредство,
	|	втТранспортныеСредства.Количество,
	|	втТранспортныеСредства.КоличествоЦиклов,
	|	втТранспортныеСредства.ПланироватьЦиклически
	|ИЗ
	|	втТранспортныеСредства КАК втТранспортныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
	|		ПО втТранспортныеСредства.Перевозчик = втПеревозчики.Перевозчик
	|			И (втТранспортныеСредства.ТранспортноеСредство <> ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТранспортныеСредства.Перевозчик,
	|	втТранспортныеСредства.ТипТС,
	|	втТранспортныеСредства.ТранспортноеСредство,
	|	ВЫБОР
	|		КОГДА ДоступныеТипыТС.КоличествоМакс < втТранспортныеСредства.Количество
	|			ТОГДА ДоступныеТипыТС.КоличествоМакс
	|		ИНАЧЕ втТранспортныеСредства.Количество
	|	КОНЕЦ,
	|	втТранспортныеСредства.КоличествоЦиклов,
	|	втТранспортныеСредства.ПланироватьЦиклически
	|ИЗ
	|	втТранспортныеСредства КАК втТранспортныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
	|		ПО втТранспортныеСредства.Перевозчик = втПеревозчики.Перевозчик
	|			И (втТранспортныеСредства.ТранспортноеСредство = ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры.ДоступныеТипыТС КАК ДоступныеТипыТС
	|		ПО (втПеревозчики.Перевозчик = ДоступныеТипыТС.Ссылка)
	|			И втТранспортныеСредства.ТипТС = ДоступныеТипыТС.ТипТС";
	
КонецФункции

// Возвращает текст запроса соответвие перевозчика виду перевозки.
//
// Возвращаемое значение:
//   Строка - Текст запроса.
//
Функция ПолучитьТекстЗапросаСоответствиеПеревозчикаВидуПеревозки() Экспорт
	
	Возврат 
	"ВЫБРАТЬ
	|	упВидыПеревозок.Ссылка,
	|	упВидыПеревозок.ВыборПеревозчика КАК ВариантВыбораПеревозчика,
	|	упВидыПеревозок.Перевозчик
	|ПОМЕСТИТЬ втВидыПеревозок
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчики
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаСобственнымиТС))
	|           И (упПартнеры.Организация = &Организация)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втВидыПеревозок.Перевозчик
	|ИЗ
	|	втВидыПеревозок КАК втВидыПеревозок
	|ГДЕ
	|	втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (упПартнеры.Перевозчик)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеревозчики.Перевозчик
	|ИЗ
	|	втПеревозчики КАК втПеревозчики
	|ГДЕ
	|	втПеревозчики.Перевозчик = &Перевозчик";
	
КонецФункции

// Функция - Есть услуги спецтехники или пассажирские перевозки.
// 
// Возвращаемое значение:
//  Структура - структура с ключами:
//  * ЕстьУслугиСпецтехники     - Булево - Истина, если имеются услуги спецтехники.
//  * ЕстьПассажирскиеПеревозки - Булево - Истина, если имеются услуги по перевозке пассажиров.
//
Функция ЕстьУслугиСпецтехникиИлиПассажирПеревозки() Экспорт
	
	Результат = Новый Структура("ЕстьУслугиСпецтехники, ЕстьПассажирскиеПеревозки", Ложь, Ложь);
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА упВидыПеревозок.ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.ПеревозкаПассажиров)
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ЕстьПассажирскиеПеревозки,
	               |	СУММА(ВЫБОР
	               |			КОГДА упВидыПеревозок.ВидТранспортнойУслуги = ЗНАЧЕНИЕ(Перечисление.упВидыТранспортныхУслуг.УслугиСпецтехники)
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ЕстьУслугиСпецтехники
	               |ИЗ
	               |	Справочник.упВидыПеревозок КАК упВидыПеревозок
	               |ГДЕ
	               |	упВидыПеревозок.ВидТранспортнойУслуги В(&ВидыТранспортныхУслуг)
	               |	И НЕ упВидыПеревозок.ПометкаУдаления";

	ВидыТранспортныхУслуг = Новый Массив;
	ВидыТранспортныхУслуг.Добавить(Перечисления.упВидыТранспортныхУслуг.УслугиСпецтехники);
	ВидыТранспортныхУслуг.Добавить(Перечисления.упВидыТранспортныхУслуг.ПеревозкаПассажиров);
	
	Запрос.УстановитьПараметр("ВидыТранспортныхУслуг", ВидыТранспортныхУслуг);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЕстьПассажирскиеПеревозки = null Тогда
			Продолжить;
		КонецЕсли;
		Если Выборка.ЕстьПассажирскиеПеревозки > 0 Тогда
			Результат.Вставить("ЕстьПассажирскиеПеревозки", Истина);
		КонецЕсли;			
		Если Выборка.ЕстьУслугиСпецтехники > 0 Тогда
			Результат.Вставить("ЕстьУслугиСпецтехники", Истина);
		КонецЕсли;			
	КонецЦикла; 
	
	Возврат Результат;
		
КонецФункции

Функция ВидыПеревозокСовместимы(РеквизитыОбъекта) Экспорт
	
	Результат = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "";
	
	ОбъектПроверки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РеквизитыОбъекта, "ОбъектПроверки", Неопределено);
	
	Если ТипЗнч(ОбъектПроверки) = Тип("СправочникСсылка.упСхемыМультимодальныхПеревозок") Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	упВидыПеревозокТ.Ссылка КАК ВидПеревозки,
		|	упВидыПеревозокТ.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах
		|ПОМЕСТИТЬ втПараметрыВидовПеревозок
		|ИЗ
		|	Справочник.упВидыПеревозок КАК упВидыПеревозокТ
		|ГДЕ упВидыПеревозокТ.Ссылка В  (&ВидыПеревозок)
		|;";
		
		Запрос.УстановитьПараметр("ВидыПеревозок", РеквизитыОбъекта.ВидыПеревозок);
		
	ИначеЕсли ТипЗнч(ОбъектПроверки) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|//{Запрос: 0 ////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаданиеНаПеревозкуГруза.ВидПеревозки КАК ВидПеревозки,
		|	ЗаданиеНаПеревозкуГруза.ВидПеревозки.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах,
		|	ЗаданиеНаПеревозкуГруза.Ссылка КАК ЗаданиеНаПеревозкуГруза
		|ПОМЕСТИТЬ втПараметрыВидовПеревозок
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упСтатусыЗаданийНаПеревозкуГруза.СрезПоследних(
		|		,
		|		) КАК Статусы
		|	ПО Статусы.Документ = ЗаданиеНаПеревозкуГруза.Ссылка
		|ГДЕ ИСТИНА
		|	И ЗаданиеНаПеревозкуГруза.ЗаявкаНаПеревозкуГруза = &ЗаявкаНаПеревозкуГруза
		|	И ЗаданиеНаПеревозкуГруза.Ссылка <> &ЗаданиеНаПеревозкуГруза
		|	И ЕстьNUll(Статусы.Статус, ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Новое)) <> ЗНАЧЕНИЕ(Перечисление.упСтатусыЗаданияНаПеревозкуГруза.Отменено)
		|	И Не ЗаданиеНаПеревозкуГруза.ПометкаУдаления
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	упВидыПеревозокТ.Ссылка КАК ВидПеревозки,
		|	упВидыПеревозокТ.ПараметрыВводаИнформацииОГрузовыхМестах КАК ПараметрыВводаИнформацииОГрузовыхМестах,
		|	&ЗаданиеНаПеревозкуГруза КАК ЗаданиеНаПеревозкуГруза
		|ИЗ
		|	Справочник.упВидыПеревозок КАК упВидыПеревозокТ
		|ГДЕ упВидыПеревозокТ.Ссылка = &ВидПеревозки
		|;";
		
		Запрос.УстановитьПараметр("ЗаявкаНаПеревозкуГруза", РеквизитыОбъекта.ЗаявкаНаПеревозкуГруза);
		Запрос.УстановитьПараметр("ЗаданиеНаПеревозкуГруза", РеквизитыОбъекта.ОбъектПроверки);
		Запрос.УстановитьПараметр("ВидПеревозки", РеквизитыОбъекта.ВидПеревозки);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|//{Запрос: 1 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втПараметрыВидовПеревозок.*
		|ИЗ
		|	втПараметрыВидовПеревозок КАК втПараметрыВидовПеревозок
		|ГДЕ втПараметрыВидовПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах = ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|;
		|//{Запрос: 2 ////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втПараметрыВидовПеревозок.*
		|ИЗ
		|	втПараметрыВидовПеревозок КАК втПараметрыВидовПеревозок
		|ГДЕ втПараметрыВидовПеревозок.ПараметрыВводаИнформацииОГрузовыхМестах <> ЗНАЧЕНИЕ(Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится)
		|";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
				
		Если Истина
			И НЕ РезультатЗапроса[1].Пустой() 
			И НЕ РезультатЗапроса[2].Пустой() 
		Тогда
		
			Результат = Ложь;
			
			Если ТипЗнч(ОбъектПроверки) = Тип("СправочникСсылка.упСхемыМультимодальныхПеревозок") Тогда
				
				мсвВидыПеревозокНеВводятсяДанныеПоГрузовымМестам = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("ВидПеревозки");
				
				мсвВидыПеревозокВводятсяДанныеПоГрузовымМестам = РезультатЗапроса[2].Выгрузить().ВыгрузитьКолонку("ВидПеревозки");
				
				ТекстСообщения = Нстр("ru = 'Виды перевозок: ""%1"" несовместимы с ""%2"". (Виды перевозок, по которым информация о грузовых местах не вводится, несовместимы ни с какими другими)'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, 
				СтрСоединить(мсвВидыПеревозокНеВводятсяДанныеПоГрузовымМестам, """, """),
				СтрСоединить(мсвВидыПеревозокВводятсяДанныеПоГрузовымМестам, """, """));
				
			ИначеЕсли ТипЗнч(ОбъектПроверки) = Тип("ДокументСсылка.упЗаданиеНаПеревозкуГруза") Тогда
				
				Выборка = РезультатЗапроса[1].Выбрать();
				
				ТекстСообщения1 = "";
				мсвСообщений = Новый Массив;
				
				Пока Выборка.Следующий() Цикл
					
					ТекстСообщенияШаблон = Нстр("ru = '""%1"" по %2'");
					ТекстСообщенияШаблон = СтрШаблон(ТекстСообщенияШаблон, Выборка.ВидПеревозки, ?(ЗначениеЗаполнено(Выборка.ЗаданиеНаПеревозкуГруза), Выборка.ЗаданиеНаПеревозкуГруза, Нстр("ru = 'текущему заданию'")));
					мсвСообщений.Добавить(ТекстСообщенияШаблон);
					
				КонецЦикла;
				
				ТекстСообщения1 = СтрСоединить(мсвСообщений, Символы.ПС);
				
				Выборка = РезультатЗапроса[2].Выбрать();
				
				ТекстСообщения2 = "";
				мсвСообщений = Новый Массив;
				
				Пока Выборка.Следующий() Цикл
					
					ТекстСообщенияШаблон = Нстр("ru = '""%1"" по %2'");
					ТекстСообщенияШаблон = СтрШаблон(ТекстСообщенияШаблон, Выборка.ВидПеревозки, ?(ЗначениеЗаполнено(Выборка.ЗаданиеНаПеревозкуГруза), Выборка.ЗаданиеНаПеревозкуГруза, Нстр("ru = 'текущему заданию'")));
					мсвСообщений.Добавить(ТекстСообщенияШаблон);
					
				КонецЦикла;
				
				ТекстСообщения2 = СтрСоединить(мсвСообщений, "," + Символы.ПС);
				
				ТекстСообщения = Нстр("ru = 'Виды перевозок:
				|%1
				|несовместимы с видами перевозок
				|%2 
				|(Виды перевозок, по которым информация о грузовых местах не вводится, несовместимы ни с какими другими)'");
								
				ТекстСообщения = СтрШаблон(ТекстСообщения, ТекстСообщения1, ТекстСообщения2);
								
			КонецЕсли;
			РеквизитыОбъекта.Вставить("ТекстСообщения", ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
			
	Возврат Результат;
	
КонецФункции


#КонецОбласти

#Область ОбработчикиСобытий

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли
	