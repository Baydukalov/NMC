
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	СхемаКомпоновкиДанныхЗапросУсловийПеревозки = ТекущийОбъект.СхемаЗапросУсловийПеревозки.Получить();
	Если СхемаКомпоновкиДанныхЗапросУсловийПеревозки <> Неопределено Тогда
		АдресСхемыЗапросУсловийПеревозки = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхЗапросУсловийПеревозки, УникальныйИдентификатор);
	КонецЕсли;
	
	СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза = ТекущийОбъект.СхемаЗаявкаНаПеревозкуГруза.Получить();
	Если СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза <> Неопределено Тогда
		АдресСхемыЗаявкаНаПеревозкуГруза = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза, УникальныйИдентификатор);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Для Каждого текЗначение Из Объект.ПеревозчикиДляТендера Цикл 
		СписокПеревозчиков.Добавить(текЗначение.Перевозчик);
	КонецЦикла;	
	
	Если Не ЗначениеЗаполнено(АдресСхемыЗапросУсловийПеревозки) Тогда 
		СхемаКомпоновкиДанныхЗапросУсловийПеревозки = Справочники.упВидыПеревозок.ПолучитьМакет("ЗапросУсловийПеревозки");
		АдресСхемыЗапросУсловийПеревозки = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхЗапросУсловийПеревозки, УникальныйИдентификатор);
	Иначе	
		СхемаКомпоновкиДанныхЗапросУсловийПеревозки = ПолучитьИзВременногоХранилища(АдресСхемыЗапросУсловийПеревозки);
	КонецЕсли;	
	Попытка
		КомпоновщикНастроекЗапросУсловийПеревозки.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыЗапросУсловийПеревозки));
		КомпоновщикНастроекЗапросУсловийПеревозки.ЗагрузитьНастройки(СхемаКомпоновкиДанныхЗапросУсловийПеревозки.НастройкиПоУмолчанию);
		КомпоновщикНастроекЗапросУсловийПеревозки.Восстановить();
	Исключение
		СхемаКомпоновкиДанныхАктуальная = Справочники.упВидыПеревозок.ПолучитьМакет("ЗапросУсловийПеревозки");
		Если СхемаКомпоновкиДанныхАктуальная = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Отбор,   СхемаКомпоновкиДанныхЗапросУсловийПеревозки.НастройкиПоУмолчанию.Отбор);
		АдресСхемыЗапросУсловийПеревозки = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхАктуальная, УникальныйИдентификатор);
		
		КомпоновщикНастроекЗапросУсловийПеревозки.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыЗапросУсловийПеревозки));
		КомпоновщикНастроекЗапросУсловийПеревозки.ЗагрузитьНастройки(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию);
		КомпоновщикНастроекЗапросУсловийПеревозки.Восстановить();
		
		Модифицированность = Истина;
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(АдресСхемыЗаявкаНаПеревозкуГруза) Тогда 
		СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза = Справочники.упВидыПеревозок.ПолучитьМакет("ЗаявкаНаПеревозкуГруза");
		АдресСхемыЗаявкаНаПеревозкуГруза = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза, УникальныйИдентификатор);
	Иначе	
		СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза = ПолучитьИзВременногоХранилища(АдресСхемыЗаявкаНаПеревозкуГруза);
	КонецЕсли;	
	Попытка
		КомпоновщикНастроекЗаявкаНаПеревозкуГруза.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыЗаявкаНаПеревозкуГруза));
		КомпоновщикНастроекЗаявкаНаПеревозкуГруза.ЗагрузитьНастройки(СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза.НастройкиПоУмолчанию);
		КомпоновщикНастроекЗаявкаНаПеревозкуГруза.Восстановить();
	Исключение
		СхемаКомпоновкиДанныхАктуальная = Справочники.упВидыПеревозок.ПолучитьМакет("ЗаявкаНаПеревозкуГруза");
		Если СхемаКомпоновкиДанныхАктуальная = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Отбор,   СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза.НастройкиПоУмолчанию.Отбор);
		АдресСхемыЗаявкаНаПеревозкуГруза = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхАктуальная, УникальныйИдентификатор);
		
		КомпоновщикНастроекЗаявкаНаПеревозкуГруза.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыЗаявкаНаПеревозкуГруза));
		КомпоновщикНастроекЗаявкаНаПеревозкуГруза.ЗагрузитьНастройки(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию);
		КомпоновщикНастроекЗаявкаНаПеревозкуГруза.Восстановить();
		
		Модифицированность = Истина;
	КонецПопытки;
	
	Элементы.ГруппаТендер.Видимость = ПолучитьФункциональнуюОпцию("упИспользоватьВиртуальныеТранспортныеСредства");
	ИспользуютсяКонтейнерныеПеревозки = ПолучитьФункциональнуюОпцию("упИспользуютсяКонтейнерныеПеревозки");
	Элементы.Декорация11.Видимость = ИспользуютсяКонтейнерныеПеревозки;
	Элементы.Декорация12.Видимость = ИспользуютсяКонтейнерныеПеревозки;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаПрочее, Объект.Комментарий);
	
КонецПроцедуры
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	упСервисныеФункции.УстановитьЗаголовкиПолейДляПоиска(ЭтаФорма);
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступность();
	СформироватьТекстПодсказкиПоВводуИнформацииОГрузовыхМестах();
	ОбработатьВыборПеревозчика(Истина);
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ПеревозчикиДляТендера.Очистить();
	Для Каждого текЗначение Из СписокПеревозчиков Цикл 
		НоваяСтрока = ТекущийОбъект.ПеревозчикиДляТендера.Добавить();
		НоваяСтрока.Перевозчик = текЗначение.Значение;
	КонецЦикла;	

	Если ЗначениеЗаполнено(АдресСхемыЗапросУсловийПеревозки) Тогда 
		СхемаКомпоновкиДанныхЗапросУсловийПеревозки = ПолучитьИзВременногоХранилища(АдресСхемыЗапросУсловийПеревозки);
		упСервисныеФункцииВызовСервера.ОчиститьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанныхЗапросУсловийПеревозки.НастройкиПоУмолчанию);
		упСервисныеФункцииВызовСервера.СкопироватьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанныхЗапросУсловийПеревозки.НастройкиПоУмолчанию, КомпоновщикНастроекЗапросУсловийПеревозки.ПолучитьНастройки());
		ТекущийОбъект.СхемаЗапросУсловийПеревозки = Новый ХранилищеЗначения(СхемаКомпоновкиДанныхЗапросУсловийПеревозки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресСхемыЗаявкаНаПеревозкуГруза) Тогда 
		СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза = ПолучитьИзВременногоХранилища(АдресСхемыЗаявкаНаПеревозкуГруза);
		упСервисныеФункцииВызовСервера.ОчиститьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза.НастройкиПоУмолчанию);
		упСервисныеФункцииВызовСервера.СкопироватьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза.НастройкиПоУмолчанию, КомпоновщикНастроекЗаявкаНаПеревозкуГруза.ПолучитьНастройки());
		ТекущийОбъект.СхемаЗаявкаНаПеревозкуГруза = Новый ХранилищеЗначения(СхемаКомпоновкиДанныхЗаявкаНаПеревозкуГруза);
	КонецЕсли;
	
	ОбновитьИнтерфейс = (ТекущийОбъект.ЭтоНовый() И НЕ ПолучитьФункциональнуюОпцию("упИспользуютсяРазличныеВидыПеревозок") И Справочники.упВидыПеревозок.КоличествоВидовПеревозки() = 1);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ОбновитьИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ИспользоватьСтатистикуПриОпределенииВремениПрохожденияТочки И Объект.ВидыАдресовДляСтатистики.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо указать виды адресов, по которым будет определяться статистическое время прохожения!'"),, "Объект.ВидыАдресовДляСтатистики",, Отказ);
	КонецЕсли; 
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТребуетсяСогласованиеЗаявкиНаПеревозкуГрузаПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуютсяМультимодальныеПеревозкиПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьЗаданияНаПеревозкуПоочередноПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыВводаИнформацииОГрузовыхМестахПриИзменении(Элемент)

	ОбработатьИзменениеПараметровВводаИнофрмацииОГрузовыхМестах();
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСписокПеревозчиковНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВведенСписокПеревозчиков", ЭтотОбъект);
	сткПараметры = Новый Структура("СписокЗначений", СписокПеревозчиков);
	ОткрытьФорму("Справочник.упВидыПеревозок.Форма.ФормаВводСпискаПеревозчиков", сткПараметры, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПеревозчикаПриИзменении(Элемент)
	
	ОбработатьВыборПеревозчика();
	ОбработатьИспользуютсяПравилаПодбораПеревозчиков();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТСПриИзменении(Элемент)
	
	Если (Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально")
		                        Или Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер"))
								И Объект.ВыборТС = ПредопределенноеЗначение("Перечисление.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС") Тогда
		Элементы.СхемаВыбораПеревозчика.Доступность = Истина;
	Иначе
		Если ЗначениеЗаполнено(Объект.СхемаВыбораПеревозчика) Тогда
			Объект.СхемаВыбораПеревозчика = Неопределено;
		КонецЕсли; 
		
		Элементы.СхемаВыбораПеревозчика.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуютсяУслугиИнкассацииПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ВариантФормированияМаршрутаПриИзменении(Элемент)
	Если Объект.ВариантФормированияМаршрута = ПредопределенноеЗначение("Перечисление.упВариантыФормированияМаршрута.Самовывоз") Тогда
		Объект.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке = Ложь;
		Объект.ИспользуютсяМультимодальныеПеревозки			  = Ложь;
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
	Если ТекущаяСтраница.Имя = "ГруппаДополнительныеРеквизиты"
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаПрочее, Объект.Комментарий);
КонецПроцедуры

&НаКлиенте
Процедура СпособПрохожденияТочекМаршрутаПриИзменении(Элемент)
	Если Объект.СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе") Тогда
		Объект.ИспользуетсяСкладскойУчетНаХабах = Ложь;
		Объект.ПересчитыватьПлановоеВремяПосещенияТочекМаршрута = Ложь;
		Объект.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава = Ложь;
		Объект.ПравилоРежимаТрудаИОтдыхаСотрудников = ПредопределенноеЗначение("Справочник.упПравилаРежимаТрудаИОтдыхаСотрудников.ПустаяСсылка");
		Объект.ДопустимыйПериодРассогласованностиСФактическимиДанными = 0;
		Объект.РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу = Ложь;
	КонецЕсли;	
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПорядокИсполненияЗаявкиПриИзменении(Элемент)
	Если Объект.ПорядокИсполненияЗаявки = ПредопределенноеЗначение("Перечисление.упПорядокИсполненияЗаявок.ПутевымЛистом") Тогда
		Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку");
		ОбработатьИзменениеПараметровВводаИнофрмацииОГрузовыхМестах();
		Объект.ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке = Ложь;
		Объект.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке = Ложь;
		Объект.ИспользуютсяМультимодальныеПеревозки = Ложь;
		Объект.КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению = Ложь;
		Объект.СоздаватьЗаданияНаПеревозкуПоочередно = Ложь;
		Объект.ВозможнаКорректировкаПоследовательностиМультимодальнойПеревозки = Ложь;
		Объект.ИспользуютсяУслугиИнкассации = Ложь;
		
		
		// Закладка "Задания"
		Объект.ЗапрещеноОформлениеЗаданияНаПеревозкуБезЗаявки = Ложь;
		Объект.АвтоматическиДобавлятьЗаданияНаВозврат = Ложь;
		Объект.АвтоматическиДобавлятьЗаданияИнкассации = Ложь;
			
		// Закладка "Рейсы"
		Объект.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		Объект.ВидЗоныДляФормированияПредставленияРейса = ПредопределенноеЗначение("Перечисление.упВидыЗон.ПустаяСсылка");
		Объект.АвтоматическиПередаватьРейсКПланированию = Ложь;
		Объект.СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ПустаяСсылка");
		Объект.ВариантФормированияМаршрута = ПредопределенноеЗначение("Перечисление.упВариантыФормированияМаршрута.ПустаяСсылка");
		Объект.ПрисваиватьРейсуПодразделениеТранспортногоСредства = Ложь;
		Объект.Подразделение = ПредопределенноеЗначение("Справочник.упСтруктураПредприятия.ПустаяСсылка");
		Объект.ВидТочкиОтправления = ПредопределенноеЗначение("Справочник.упВидыАдресов.ПустаяСсылка");
		Объект.ВидыТочекОтправления.Очистить();
		Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ПустаяСсылка");
		Объект.Перевозчик = ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка");
		Объект.ПеревозчикиДляТендера.Очистить();
		Объект.СхемаВыбораПеревозчика = ПредопределенноеЗначение("Справочник.упСхемыВыбораПеревозчика.ПустаяСсылка");
		Объект.ВыборТС = ПредопределенноеЗначение("Перечисление.упВариантыПодбораТС.ПустаяСсылка");
		Объект.ПакетРегламентныхДокументов = ПредопределенноеЗначение("Справочник.упПакетыРегламентныхДокументов.ПустаяСсылка");
		Объект.СпособУчетаВозвращенныхДокументов = ПредопределенноеЗначение("Перечисление.упСпособыУчетаВозвращенныхДокументов.ПустаяСсылка");
		Объект.СостояниеТСПриИсполненииРейса = ПредопределенноеЗначение("Справочник.упСостоянияТС.ПустаяСсылка");
		Объект.ТребуетсяОформлениеПутевогоЛиста = Ложь;
		Объект.ИспользуетсяСкладскойУчетНаХабах = Ложь;
		Объект.ЗапрещеноЗавершениеРейсаПриНаличииНедополученныхДокументов = Ложь;
		Объект.КонтролироватьГрафикиРаботыВодителейПриПередачеРейсаКВыполнению = Ложь;
		Объект.ПересчитыватьПлановоеВремяПосещенияТочекМаршрута = Ложь;
		Объект.ДопустимыйПериодРассогласованностиСФактическимиДанными = 0;
		Объект.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава = Ложь;
		Объект.ЗапрещеноНепосредственноеУдалениеОбъектаРейс = Ложь;
		Объект.ПравилоРежимаТрудаИОтдыхаСотрудников = ПредопределенноеЗначение("Справочник.упПравилаРежимаТрудаИОтдыхаСотрудников.ПустаяСсылка");
		Объект.ПравилоСвязиТиповГрузовВРейсе = ПредопределенноеЗначение("Справочник.упПравилаСвязиТиповГрузов.ПустаяСсылка");
	КонецЕсли;
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСтатистикуПриОпределенииВремениПрохожденияТочкиПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СхемаВыбораПеревозчикаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СхемаВыбораПеревозчика) Тогда
		Если Объект.ИспользуютсяПравилаПодбораПеревозчиков Тогда
			Объект.ИспользуютсяПравилаПодбораПеревозчиков = Ложь;
			ОчиститьСообщения();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Значение поля ""Используются правила подбора перевозчиков"" автоматически отключено.'"),,,"ИспользуютсяПравилаПодбораПеревозчиков");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СхемаВыбораПеревозчикаПриИзменении()

&НаКлиенте
Процедура ИспользуютсяПравилаПодбораПеревозчиковПриИзменении(Элемент)
	Если Объект.ИспользуютсяПравилаПодбораПеревозчиков Тогда
		Если ЗначениеЗаполнено(Объект.СхемаВыбораПеревозчика) Тогда
			Объект.СхемаВыбораПеревозчика = ПредопределенноеЗначение("Справочник.упСхемыВыбораПеревозчика.ПустаяСсылка");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Значение поля ""Схема выбора перевозчика"" автоматически изменено!'"),,,"СхемаВыбораПеревозчика");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазрешеноФормированиеКонтейнеровПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнераПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
    УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборПеревозчика(Открытие = Ложь)

	Если Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком") Тогда
		Элементы.Перевозчик.Доступность = Истина;
	Иначе 
		Элементы.Перевозчик.Доступность = Ложь;
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Объект.Перевозчик = ПредопределенноеЗначение("Справочник.упПартнеры.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	
	Если Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер") Тогда
		Элементы.ДекорацияСписокПеревозчиков.Доступность = Истина;
		Элементы.ВыборТС.Доступность                     = Ложь;
		Если Не Открытие И Объект.ВыборТС <> ПредопределенноеЗначение("Перечисление.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС") Тогда
			Объект.ВыборТС = ПредопределенноеЗначение("Перечисление.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС");
			ОчиститьСообщения();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Значение поля ""Выбор транспортных средств"" автоматически изменено на ""Оформляется заявка на ТС""!'"),,,"ВыборТС");
		КонецЕсли;
		Если Объект.ИспользуютсяПравилаПодбораПеревозчиков Тогда
			Объект.ИспользуютсяПравилаПодбораПеревозчиков = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Значение поля ""Используются правила подбора перевозчиков"" автоматически отключено.'"),,,"ИспользуютсяПравилаПодбораПеревозчиков");
		КонецЕсли;
	Иначе
		Элементы.ДекорацияСписокПеревозчиков.Доступность = Ложь;
		Элементы.ВыборТС.Доступность                     = Истина;
		СписокПеревозчиков.Очистить();
	КонецЕсли;
	
	Если (Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально")
		                        Или Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер"))
								И Объект.ВыборТС = ПредопределенноеЗначение("Перечисление.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС") Тогда
		Элементы.СхемаВыбораПеревозчика.Доступность = Истина;
	Иначе
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.СхемаВыбораПеревозчика) Тогда
			Объект.СхемаВыбораПеревозчика = Неопределено;
		КонецЕсли; 
		
		Элементы.СхемаВыбораПеревозчика.Доступность = Ложь;
	КонецЕсли;
	
	ОбновитьЗаголовокСпискаПеревозчиков();
	
КонецПроцедуры // ОбработатьВыборПеревозчика()

&НаКлиенте
Процедура СформироватьТекстПодсказкиПоВводуИнформацииОГрузовыхМестах()
	
	Если Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится") Тогда 
		ТекстПодсказкиПоВводуИнформацииОГрузовыхМестах = НСтр("ru = 'Информация о грузовых местах не вводится, работа ведется с заявкой как с единым грузом. При погрузке / разгрузке указываются не грузовые места, а номера заявок.'");	
	ИначеЕсли Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса") Тогда 
		ТекстПодсказкиПоВводуИнформацииОГрузовыхМестах = НСтр("ru = 'Планирование рейсов ведется по заявке в целом. До начала исполнения рейса необходимо указать информацию по грузовым местам в задании на перевозку. Контроль состояния заявки и погрузка / разгрузка ведется по номерам грузовых мест.'");	
	ИначеЕсли Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса") Тогда 
		ТекстПодсказкиПоВводуИнформацииОГрузовыхМестах = НСтр("ru = 'До начала планирования рейса необходимо указать информацию по грузовым местам в задании на перевозку. Минимальный уровень требующийся для возможности планирования перевозки задания несколькими рейсами.'");	
	ИначеЕсли Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку") Тогда 
		ТекстПодсказкиПоВводуИнформацииОГрузовыхМестах = НСтр("ru = 'Информацию по грузовым местам необходимо указать сразу в заявке на перевозку до передачи к выполнению. Уровень требуется для возможности указания различных точек погрузки / разгрузки в одной заявке на перевозку (планирование нескольких заданий на перевозку по одной заявке).'");	
	КонецЕсли;
	
КонецПроцедуры // СформироватьТекстПодсказкиПоВводуИнформацииОГрузовыхМестах()

&НаКлиенте
Процедура УстановитьДоступность()
	
	ЭтоСпособПрохожденияТочекМаршрутаВРейсе = Объект.СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ВРейсе");
	
	ПорядокИсполненияЗаявкиПутевымЛистом = Объект.ПорядокИсполненияЗаявки = ПредопределенноеЗначение("Перечисление.упПорядокИсполненияЗаявок.ПутевымЛистом");
	
	Если Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку") Тогда
		Элементы.ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;
		Элементы.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;		
	Иначе
		Элементы.ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке.Доступность = Ложь;
		Элементы.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке.Доступность = Ложь;
	КонецЕсли; 
	
	Если Истина
		И (Ложь
		   Или Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально")
		   Или Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер"))
		И Объект.ВыборТС = ПредопределенноеЗначение("Перечисление.упВариантыПодбораТС.ОформляетсяЗаявкаНаТС") 
	Тогда
		Элементы.СхемаВыбораПеревозчика.Доступность = Истина;
	Иначе
		Элементы.СхемаВыбораПеревозчика.Доступность = Ложь;
	КонецЕсли; 
	
	УстановитьДоступностьПериодаРассогласованности();
	
	Элементы.ИспользуютсяУслугиИнкассации.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;
	Элементы.СпособУчетаИнкассируемыхЦенностей.Доступность = Объект.ИспользуютсяУслугиИнкассации;
	
	Если Объект.ВариантФормированияМаршрута = ПредопределенноеЗначение("Перечисление.упВариантыФормированияМаршрута.Самовывоз") Тогда
		Элементы.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке.Доступность = Ложь;	
		Элементы.ИспользуютсяМультимодальныеПеревозки.Доступность = Ложь;	
	Иначе	
		Элементы.ИспользуютсяМультимодальныеПеревозки.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;	
	КонецЕсли;
		
	Элементы.КонтролироватьПопаданиеВоВременныеОкнаПриПередачеЗаявкиКВыполнению.Доступность = Объект.ИспользуютсяМультимодальныеПеревозки;
	Элементы.СоздаватьЗаданияНаПеревозкуПоочередно.Доступность = Объект.ИспользуютсяМультимодальныеПеревозки;
	Элементы.ВозможнаКорректировкаЦепейМультимодальнойПеревозки.Доступность = Объект.ИспользуютсяМультимодальныеПеревозки;
	Элементы.АвтоматическиПередаватьСогласованныеЗаявкиКИсполнению.Доступность = Объект.ТребуетсяСогласованиеЗаявкиНаПеревозкуГруза;
	

	Элементы.ПравилоСвязиТиповГрузовВРейсе.Доступность = Ложь
		Или Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса")
		Или Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку");
		
	Элементы.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава.Доступность = (Ложь
		Или Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку")	
		Или Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоПланированияРейса")	
		Или Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаданииДоНачалаВыполненияРейса")
		) И НЕ ЭтоСпособПрохожденияТочекМаршрутаВРейсе;	
		
	Элементы.РазрешеноДосрочноеУбытиеИзТочкиМаршрута.Доступность = НЕ ЭтоСпособПрохожденияТочекМаршрутаВРейсе;
		
	Элементы.ИспользуетсяСкладскойУчетНаХабах.Доступность = Истина
		И НЕ Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится")
		И НЕ ЭтоСпособПрохожденияТочекМаршрутаВРейсе;
		
	Элементы.ПересчитыватьПлановоеВремяПосещенияТочекМаршрута.Доступность = НЕ ЭтоСпособПрохожденияТочекМаршрутаВРейсе;
	Элементы.ПравилаРежимаТрудаИОтдыхаСотрудников.Доступность = НЕ ЭтоСпособПрохожденияТочекМаршрутаВРейсе;
		
	Элементы.ПараметрыВводаИнформацииОГрузовыхМестах.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;	
	Элементы.ГруппаНастройкиЗаданияНаПеревозкуГруза.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;	
	Элементы.ГруппаПодборПеревозкиЗапрос.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом; 
	Элементы.ГруппаНастройкиРейса.Доступность = НЕ ПорядокИсполненияЗаявкиПутевымЛистом;
	
	Элементы.ВидыАдресовДляСтатистики.Доступность = Объект.ИспользоватьСтатистикуПриОпределенииВремениПрохожденияТочки;
	Элементы.ВидыАдресовДляСтатистики.АвтоОтметкаНезаполненного = Объект.ИспользоватьСтатистикуПриОпределенииВремениПрохожденияТочки;
	
	Элементы.КоличествоСмен.Доступность = ПорядокИсполненияЗаявкиПутевымЛистом;
	Элементы.РазрешеноОтложенноеВыполнениеЧастиРаботПоРейсу.Доступность = Объект.СпособПрохожденияТочекМаршрута = ПредопределенноеЗначение("Перечисление.упСпособыПрохожденияТочекМаршрута.ОтдельнымДокументом");
	
	Элементы.ВидПеревозкиКонтейнера.Доступность = Объект.РазрешеноФормированиеКонтейнеров И Объект.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера;
	Элементы.ТребуетсяСозданиеЗаявкиНаПеревозкуКонтейнера.Доступность = Объект.РазрешеноФормированиеКонтейнеров;
	                                                                                        
	ОбработатьИспользуютсяПравилаПодбораПеревозчиков();
	
КонецПроцедуры // УстановитьДоступность()

&НаКлиенте
Процедура ОбновитьЗаголовокСпискаПеревозчиков()
	
	КолПеревозчиковДляТендера = СписокПеревозчиков.Количество();
	Если КолПеревозчиковДляТендера = 0 Тогда
		Элементы.ДекорацияСписокПеревозчиков.Заголовок = НСтр("ru= '<уточнить перевозчиков / владельцев>'");
	Иначе	
		Элементы.ДекорацияСписокПеревозчиков.Заголовок = СтрШаблон(НСтр("ru= '<список перевозчиков / владельцев ограничен (%1)>'"), КолПеревозчиковДляТендера);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура после закрытия формы ввода списка перевозчиков.
//
Процедура ВведенСписокПеревозчиков(Значение, Параметры) Экспорт
	
	Если Не Значение = Неопределено Тогда
		СписокПеревозчиков = Значение;
		ОбновитьЗаголовокСпискаПеревозчиков();
		Модифицированность = Истина;
	КонецЕсли; 
	
КонецПроцедуры  // ВведенСписокПеревозчиков()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура УстановитьДоступностьПериодаРассогласованности()
	Если Объект.ПересчитыватьПлановоеВремяПосещенияТочекМаршрута Тогда
		Элементы.ДопустимыйПериодРассогласованностиСФактическимиДанными.Доступность = Истина;
	Иначе	
		Элементы.ДопустимыйПериодРассогласованностиСФактическимиДанными.Доступность = Ложь;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитыватьПлановоеВремяПосещенияТочекМаршрутаПриИзменении(Элемент)
	Если НЕ Объект.ПересчитыватьПлановоеВремяПосещенияТочекМаршрута Тогда
		 Объект.ДопустимыйПериодРассогласованностиСФактическимиДанными = 0;
	КонецЕсли;
	УстановитьДоступностьПериодаРассогласованности();
КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	упСервисныеФункцииКлиент.СтрокаПоискаЭлементовФормыПриИзменении(ЭтаФорма, Элементы.ГруппаСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтрокаПоискаПриИзменении(Элементы.СтрокаПоиска);

КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбработатьИзменениеПараметровВводаИнофрмацииОГрузовыхМестах()
	СформироватьТекстПодсказкиПоВводуИнформацииОГрузовыхМестах();
	
	Если Объект.ПараметрыВводаИнформацииОГрузовыхМестах <> ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.ВводитсяВЗаявкеНаПеревозку") Тогда
		Если Объект.ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке Тогда
			Объект.ВозможноУказаниеРазличныхТочекПогрузкиВЗаявке = Ложь;
		КонецЕсли; 
		Если Объект.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке Тогда
			Объект.ВозможноУказаниеРазличныхТочекРазгрузкиВЗаявке = Ложь;
		КонецЕсли; 
	КонецЕсли;
	
	Если Объект.ПараметрыВводаИнформацииОГрузовыхМестах = ПредопределенноеЗначение("Перечисление.упПараметрыВводаИнформацииОГрузовыхМестах.НеВводится") Тогда
		Объект.РазрешенаПогрузкаРазгрузкаСТочностьюДоТоварногоСостава = Ложь;
		Объект.ИспользуетсяСкладскойУчетНаХабах                       = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИспользуютсяПравилаПодбораПеревозчиков()

	Элементы.ИспользуютсяПравилаПодбораПеревозчиков.Доступность = НЕ(Объект.ВыборПеревозчика = ПредопределенноеЗначение("Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер"));

КонецПроцедуры // ОбработатьИспользуютсяПравилаПодбораПеревозчиков()

#КонецОбласти 
