#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает ссылку на типовое соглашение партнера.
//
// Параметры:
//  Партнер	 - СправочникСсылка				 - Ссылка на элемент.
//  Организация - СправочникСсылка.Организации	 - организация.
// 
// Возвращаемое значение:
//  СправочникСсылка.упСоглашенияСКлиентами - Результат выполнения.
//
Функция СоглашениеПоУмолчанию(Партнер, Организация = Неопределено) Экспорт
	
	Соглашение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Партнер, "ОсновноеСоглашение");
	Если ЗначениеЗаполнено(Соглашение) И ЗначениеЗаполнено(Организация) И Организация <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Соглашение, "Организация") Тогда
		Соглашение = Справочники.упСоглашенияСКлиентами.ПустаяСсылка();
	КонецЕсли; 	
	
	Если Не ЗначениеЗаполнено(Соглашение) Тогда
		
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Организация = Справочники.Организации.ПустаяСсылка();
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	упСоглашенияСКлиентами.Ссылка КАК Соглашение
		|ИЗ
		|	Справочник.упСоглашенияСКлиентами КАК упСоглашенияСКлиентами
		|ГДЕ
		|	НЕ упСоглашенияСКлиентами.ПометкаУдаления
		|	И упСоглашенияСКлиентами.Владелец = &Владелец
		|	И &Организация В (упСоглашенияСКлиентами.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
		Запрос.УстановитьПараметр("Владелец"   , Партнер);
		Запрос.УстановитьПараметр("Организация", Организация);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Соглашение = Выборка.Соглашение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Соглашение;

КонецФункции

// Возвращает ссылку на валюту переданного соглашения.
//
// Параметры:
//  Соглашение  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.Валюты - Результат выполнения.
//
Функция ВалютаСоглашения(Соглашение) Экспорт
	
	Валюта = Справочники.Валюты.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упГруппыТарифов.Валюта
	               |ИЗ
	               |	Справочник.упСоглашенияСКлиентами КАК упСоглашенияСКлиентами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГруппыТарифов КАК упГруппыТарифов
	               |		ПО упСоглашенияСКлиентами.ГруппаТарифов = упГруппыТарифов.Ссылка
	               |ГДЕ
	               |	упСоглашенияСКлиентами.Ссылка = &Ссылка
	               |	И НЕ упГруппыТарифов.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка", Соглашение);
	текВыборка	= Запрос.Выполнить().Выбрать();
	
	Если текВыборка.Следующий() Тогда
		Валюта = текВыборка.Валюта;
	КонецЕсли;
	
	Возврат Валюта;
			
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	текПартнер = Справочники.упПартнеры.ПустаяСсылка();
	Если Параметры.Отбор.Свойство("Владелец", текПартнер) Тогда
		СтандартнаяОбработка = Ложь;
		
		текЗапрос = Новый Запрос;
		текЗапрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 51
		|	упСоглашенияСКлиентами.Ссылка КАК Соглашение
		|ИЗ
		|	Справочник.упСоглашенияСКлиентами КАК упСоглашенияСКлиентами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияПартнеров КАК упИерархияПартнеров
		|		ПО упСоглашенияСКлиентами.Владелец = упИерархияПартнеров.Родитель
		|ГДЕ
		|	упСоглашенияСКлиентами.Наименование ПОДОБНО &СтрокаПоиска
		|	И упИерархияПартнеров.Партнер = &Партнер
		|	И упСоглашенияСКлиентами.Статус = ЗНАЧЕНИЕ(Перечисление.упСтатусыСоглашений.Действует)";
		текЗапрос.УстановитьПараметр("Партнер",      текПартнер);
		текЗапрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		
		тбзСоглашения	= текЗапрос.Выполнить().Выгрузить();
		
		спзДанныеВыбора	= Новый СписокЗначений;
		спзДанныеВыбора.ЗагрузитьЗначения(тбзСоглашения.ВыгрузитьКолонку("Соглашение"));
		
		ДанныеВыбора = спзДанныеВыбора;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нет.

#КонецОбласти

#КонецЕсли






