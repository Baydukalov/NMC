#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Элементы.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению.Доступность = Объект.ТребуетсяСогласованиеЗаявкиНаРемонт;
	
	Если Объект.ВремяРаботПлан > 0 Тогда
		ВремяРаботПланЧасов = Цел(Объект.ВремяРаботПлан);
		ВремяРаботПланМинут = Окр((Объект.ВремяРаботПлан - ВремяРаботПланЧасов)*60);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.ВремяРаботПлан = ВремяРаботПланЧасов + ВремяРаботПланМинут/60;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РаботыПоРемонтуПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПоказатьВопросОПересчете();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура Пересчитать(Команда)
	
	ПоказатьВопросОПересчете();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВопросОПересчете()
	
	ТекстВопроса = НСтр("ru = 'Пересчитать плановое время работ по работам по ремонту?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("ПересчитатьПлановоеВремяРаботОтвет", ЭтаФорма), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
// Продолжение процедуры (см. выше).
//
Процедура ПересчитатьПлановоеВремяРаботОтвет(Ответ, ДополнительныеСвойства) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПересчитатьПлановоеВремяРабот();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьПлановоеВремяРабот()
	
	текВремяРаботПлан = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Работы.РаботаПоРемонту,
	|	Работы.Количество
	|ПОМЕСТИТЬ втРаботы
	|ИЗ
	|	&Работы КАК Работы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(упРаботыПоРемонту.ВремяРаботПлан * втРаботы.Количество) КАК ВремяРаботПлан
	|ИЗ
	|	втРаботы КАК втРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упРаботыПоРемонту КАК упРаботыПоРемонту
	|		ПО втРаботы.РаботаПоРемонту = упРаботыПоРемонту.Ссылка";
	Запрос.УстановитьПараметр("Работы", Объект.РаботыПоРемонту.Выгрузить(, "РаботаПоРемонту, Количество"));
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		текВремяРаботПлан = Выборка.ВремяРаботПлан;
	КонецЕсли; 
	
	ВремяРаботПланЧасов = Цел(текВремяРаботПлан);
	ВремяРаботПланМинут = Окр((текВремяРаботПлан - ВремяРаботПланЧасов)*60);
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяСогласованиеЗаявкиНаРемонтПриИзменении(Элемент)
	
	Элементы.АвтоматическиПередаватьСогласованныеЗаявкиНаРемонтКИсполнению.Доступность = Объект.ТребуетсяСогласованиеЗаявкиНаРемонт;
	
КонецПроцедуры

#КонецОбласти 	