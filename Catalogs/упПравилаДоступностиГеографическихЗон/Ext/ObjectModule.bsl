
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗапрещенаРаботаВУказанныхЗонах Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ГеографическиеЗоны");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упИерархияГеографическихЗон.Зона
	|ПОМЕСТИТЬ втЗоны
	|ИЗ
	|	Справочник.упПравилаДоступностиГеографическихЗон.ГеографическиеЗоны КАК упПравилаДоступностиГеографическихЗонГеографическиеЗоны
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	|		ПО упПравилаДоступностиГеографическихЗонГеографическиеЗоны.ГеографическаяЗона = упИерархияГеографическихЗон.Родитель
	|ГДЕ
	|	упПравилаДоступностиГеографическихЗонГеографическиеЗоны.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка КАК Зона
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗоны КАК втЗоны
	|		ПО упГеографическиеЗоны.Ссылка = втЗоны.Зона
	|ГДЕ
	|	(&ЗапрещенаРабота
	|				И втЗоны.Зона ЕСТЬ NULL 
	|			ИЛИ НЕ &ЗапрещенаРабота
	|				И НЕ втЗоны.Зона ЕСТЬ NULL )";
	Запрос.УстановитьПараметр("Ссылка",          Ссылка);
	Запрос.УстановитьПараметр("ЗапрещенаРабота", ЗапрещенаРаботаВУказанныхЗонах);
	
	НаборЗаписей = РегистрыСведений.упДоступныеГеографическиеЗоны.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПравилаДоступности.Установить(Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Запись = НаборЗаписей.Добавить();
		Запись.ПравилаДоступности = Ссылка;
		Запись.ГеографическаяЗона = Выборка.Зона;
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли