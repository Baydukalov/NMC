#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если Объект.Предопределенный Тогда 
		Элементы.ГеографическиеЗоны.ТолькоПросмотр             = Истина;
		Элементы.ЗапрещенаРаботаВУказанныхЗонах.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТекстПодсказки();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы


&НаКлиенте
Процедура ЗапрещенаРаботаВУказанныхЗонахПриИзменении(Элемент)
	
	УстановитьТекстПодсказки();
	
КонецПроцедуры

&НаКлиенте
Процедура ГеографическиеЗоныПриИзменении(Элемент)
	
	УстановитьТекстПодсказки();
	
КонецПроцедуры

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекстПодсказки()
	
	Если Объект.ЗапрещенаРаботаВУказанныхЗонах Тогда 
		Если Объект.ГеографическиеЗоны.Количество() = 0 Тогда 
			Комментарий = НСтр("ru = 'Разрешена работа во всех географических зонах'");
		ИначеЕсли Объект.ГеографическиеЗоны.Количество() = 1 Тогда 
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Разрешена работа во всех географических зонах кроме %1'"), Объект.ГеографическиеЗоны[0].ГеографическаяЗона);
		Иначе 
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Разрешена работа во всех географических зонах кроме %1 указанных зон'"), Объект.ГеографическиеЗоны.Количество());
		КонецЕсли;
	Иначе	
		Если Объект.ГеографическиеЗоны.Количество() = 0 Тогда 
			Комментарий = НСтр("ru = 'Запрещена работа во всех географических зонах'");
		ИначеЕсли Объект.ГеографическиеЗоны.Количество() = 1 Тогда 
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Разрешена работа только в %1'"), Объект.ГеографическиеЗоны[0].ГеографическаяЗона);
		Иначе 
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Разрешена работа только в %1 указанных географических зонах'"), Объект.ГеографическиеЗоны.Количество());
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти