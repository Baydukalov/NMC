
&НаКлиенте
Перем ОтветПередЗакрытием;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СправочникОбъект = РеквизитФормыВЗначение("Объект");
		ПриЧтенииНаСервере(СправочникОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЭтаФорма.АдресДанныхТарифнойСетки = ПоместитьВоВременноеХранилище(ТекущийОбъект.ДанныеТарифнойСетки.Получить(), УникальныйИдентификатор);
	ТарифнаяСетка = Справочники.упТарифныеСетки.СобратьДанныеТарифнойСетки(ТекущийОбъект);
	Если ТарифнаяСетка = Неопределено Тогда
		ДобавитьКолонкиНаСервере(1);
		ПерваяСтрока = Таблица.Добавить();
		ВтораяСтрока = Таблица.Добавить();
	Иначе
		ДобавитьКолонкиНаСервере(ТарифнаяСетка.Колонки.Количество() - 1, Истина);
		Таблица.Загрузить(ТарифнаяСетка);
	КонецЕсли; 
	ПриИзмененииПоказателей();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	СортироватьОсиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = Справочники.упТарифныеСетки.СоздатьЭлемент();
	КонецЕсли;
	упТарификацияВызовСервера.ПроверитьУникальностьКодаПоказателя(ТекущийОбъект.Код, "упТарифныеСетки", Отказ);
	Если Не Отказ Тогда
		ТекущийОбъект.ЗагрузитьТаблицу(Таблица.Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписанНовыйПоказатель");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Если ОтветПередЗакрытием <> Истина Тогда
			Отказ = Истина;
			ОповещениеВопросаОЗакрытии = Новый ОписаниеОповещения("ОповещениеВопросаОЗакрытии", ЭтаФорма, Новый Структура());
			ПоказатьВопрос(ОповещениеВопросаОЗакрытии, "Данные были изменены. Закрыть форму без их применения?", РежимДиалогаВопрос.ОКОтмена);
		КонецЕсли
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Элементы.Таблица.ТекущаяСтрока = Таблица[0].ПолучитьИдентификатор() 
		И Элементы.Таблица.ТекущийЭлемент = Элементы.Таблица.ПодчиненныеЭлементы[0] Тогда
		
	ИначеЕсли Элементы.Таблица.ТекущаяСтрока = Таблица[0].ПолучитьИдентификатор() Тогда
		
		Если ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтолбцам) Тогда
			Для ИндексКолонки = 1 По Элементы.Таблица.ПодчиненныеЭлементы.Количество() - 1 Цикл
				ИмяКолонки = "Колонка" + ИндексКолонки;
				ИмяПоля = Элементы.Таблица.Имя + ИмяКолонки;
				Элементы.Таблица.ТекущиеДанные[ИмяКолонки] 	= ТипЗначенийПоказателяСтолбцов.ПривестиЗначение(Элементы.Таблица.ТекущиеДанные[ИмяКолонки]);
				Элементы[ИмяПоля].ОграничениеТипа 			= ТипЗначенийПоказателяСтолбцов;
			КонецЦикла;
		КонецЕсли; 
		
	ИначеЕсли Элементы.Таблица.ТекущийЭлемент <> Неопределено 
		И Элементы.Таблица.ТекущаяСтрока <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтрокам) Тогда
			Элементы.Таблица.ТекущиеДанные.Колонка0 				= ТипЗначенийПоказателяСтрок.ПривестиЗначение(Элементы.Таблица.ТекущиеДанные.Колонка0);
			Элементы.Таблица.ПодчиненныеЭлементы[0].ОграничениеТипа = ТипЗначенийПоказателяСтрок;
		КонецЕсли; 
		
		ТипЗначенийДанных = Новый ОписаниеТипов("Число");
		
		Для ИндексКолонки = 1 По Элементы.Таблица.ПодчиненныеЭлементы.Количество() - 1 Цикл
			ИмяКолонки 	= "Колонка" + ИндексКолонки;
			ИмяПоля 	= Элементы.Таблица.Имя + ИмяКолонки;
			Элементы[ИмяПоля].ОграничениеТипа = ТипЗначенийДанных;
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПоСтрокамПриИзменении(Элемент)
	
	ПриИзмененииПоказателей();

КонецПроцедуры

&НаКлиенте
Процедура ПоказательПоСтолбцамПриИзменении(Элемент)
	
	ПриИзмененииПоказателей();

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Объект.Код = упТарификацияКлиентСервер.ИдентификаторПоказателяПоНаименованию(Объект.Наименование);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьКолонку(Команда)
	
	УдалитьКолонкуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКолонкуНаСервере()
	
	Если Элементы.Таблица.ТекущийЭлемент = Неопределено Или Элементы.Таблица.ТекущийЭлемент = Элементы.ТаблицаЛегенда Тогда
		Возврат;
	КонецЕсли;
	ФрагментыПути = СтрРазделить(Элементы.Таблица.ТекущийЭлемент.ПутьКДанным, ".");
	ИмяКолонки = ФрагментыПути[ФрагментыПути.ВГраница()];
	ТаблицаЗначений = Таблица.Выгрузить();
	ТаблицаЗначений.Колонки.Удалить(ТаблицаЗначений.Колонки.Индекс(ТаблицаЗначений.Колонки[ИмяКолонки]));
	Для Индекс = 0 По ТаблицаЗначений.Колонки.Количество() - 1 Цикл
		ТаблицаЗначений.Колонки[Индекс].Имя = "Колонка" + Индекс;
	КонецЦикла;
	Таблица.Загрузить(ТаблицаЗначений);
	УдаляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты.Добавить(Элементы.Таблица.ПутьКДанным + ".Колонка" + ТаблицаЗначений.Колонки.Количество());
	ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	Если Элементы.Таблица.ТекущаяСтрока = Неопределено Или Элементы.Таблица.ТекущаяСтрока = Таблица[0] Тогда
		Возврат;
	КонецЕсли; 
	Таблица.Удалить(Таблица.Индекс(Элементы.Таблица.ТекущиеДанные));
	
КонецПроцедуры

&НаКлиенте
Процедура Сортировать(Команда)
	
	СортироватьОсиНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонкиИСтроки(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеДобавитьКолонкиИСтроки", ЭтаФорма);
	ОткрытьФорму("Справочник.упПравилаРасчета.Форма.ДобавлениеСтрокИСтолбцов",, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОповещениеДобавитьКолонкиИСтроки(Результат, ДопПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат.ДобавитьСтолбцов > 0 Тогда
		ДобавитьКолонкиНаСервере(Результат.ДобавитьСтолбцов);
	КонецЕсли; 
	Для Счетчик = 1 По Результат.ДобавитьСтрок Цикл
		Таблица.Добавить();
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросаОЗакрытии(Результат, ДопПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ОтветПередЗакрытием = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкиНаСервере(ДобавитьСтолбцов, УдалитьСуществующие = Ложь)
	
	ДобавляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты = Новый Массив;
	Если УдалитьСуществующие Тогда
		ТекущиеРеквизиты = ПолучитьРеквизиты("Таблица");
		Для Счетчик = 1 По ТекущиеРеквизиты.Количество() - 1 Цикл
			Реквизит = ТекущиеРеквизиты[Счетчик];
			УдаляемыеРеквизиты.Добавить(Реквизит.Путь + "." + Реквизит.Имя);
			Элементы.Удалить(Элементы[Элементы.Таблица.Имя + Реквизит.Имя]);
		КонецЦикла;
		ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	КонецЕсли; 
	ТекущиеРеквизиты = ПолучитьРеквизиты("Таблица");
	Для Счетчик = 1 По ДобавитьСтолбцов Цикл
		РеквизитТаблицы = Новый РеквизитФормы("Колонка" + (ТекущиеРеквизиты.Количество() - 1 + Счетчик), ТекущиеРеквизиты[0].ТипЗначения, "Таблица");
		ДобавляемыеРеквизиты.Добавить(РеквизитТаблицы);
	КонецЦикла;
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	Для Каждого ДобавленныйРеквизит Из ДобавляемыеРеквизиты Цикл
		ПолеФормы = Элементы.Добавить(Элементы.Таблица.Имя + ДобавленныйРеквизит.Имя, Тип("ПолеФормы"), Элементы.Таблица);
		ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
		ПолеФормы.ПутьКДанным = ДобавленныйРеквизит.Путь + "." + ДобавленныйРеквизит.Имя;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПоказателей()
	
	ОбновитьЗаголовкиПоказателейВТаблице();
	МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(Объект.ПоказательТарифнойСеткиПоСтрокам.ПараметрРасчета));
	ЭтаФорма.ТипЗначенийПоказателяСтрок = Новый ОписаниеТипов(МассивТипов);
	МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(Объект.ПоказательТарифнойСеткиПоСтолбцам.ПараметрРасчета));
	ЭтаФорма.ТипЗначенийПоказателяСтолбцов = Новый ОписаниеТипов(МассивТипов);
	СортироватьОсиНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовкиПоказателейВТаблице()
	
	Таблица[0].Колонка0 = "" + Объект.ПоказательТарифнойСеткиПоСтрокам + " / " + Объект.ПоказательТарифнойСеткиПоСтолбцам;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПриИзменении(Элемент)
	
	Если Ложь
		Или Элементы.Таблица.ТекущаяСтрока = Таблица[0].ПолучитьИдентификатор() 
		Или Элементы.Таблица.ТекущийЭлемент = Элементы.Таблица.ПодчиненныеЭлементы[0] 
	Тогда
		//СортироватьОсиНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СортироватьОсиНаСервере()
	
	Если Ложь
		Или Не ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтолбцам) 
		Или Не ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтрокам) 
	Тогда
		Возврат;
	КонецЕсли; 
	СлужебноеЗначение = "" + Новый УникальныйИдентификатор;
	ТаблицаЗначений = Таблица.Выгрузить();
	Если Ложь Тогда // Для контекстной подсказки
	    ТаблицаЗначений = Новый ТаблицаЗначений;
	КонецЕсли;
	ТаблицаЗначений[0][0] = СлужебноеЗначение;
	ТаблицаЗначений = упСервисныеФункции.ТранспонироватьТаблицуЗначений(ТаблицаЗначений);
	Если Ложь Тогда // Для контекстной подсказки
	    ТаблицаЗначений = Новый ТаблицаЗначений;
	КонецЕсли;
	Если Не упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗначенийПоказателяСтолбцов.Типы()[0]) Тогда
		ТаблицаЗначений.Сортировать(ТаблицаЗначений.Колонки[0].Имя);
	КонецЕсли; 
	ПерваяСтрока = ТаблицаЗначений.Найти(СлужебноеЗначение);
	ТаблицаЗначений.Сдвинуть(ПерваяСтрока, -ТаблицаЗначений.Индекс(ПерваяСтрока));
	ТаблицаЗначений = упСервисныеФункции.ТранспонироватьТаблицуЗначений(ТаблицаЗначений);
	Если Ложь Тогда // Для контекстной подсказки
	    ТаблицаЗначений = Новый ТаблицаЗначений;
	КонецЕсли;
	Если Не упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗначенийПоказателяСтрок.Типы()[0]) Тогда
		ТаблицаЗначений.Сортировать(ТаблицаЗначений.Колонки[0].Имя);
	КонецЕсли; 
	ПерваяСтрока = ТаблицаЗначений.Найти(СлужебноеЗначение);
	ТаблицаЗначений.Сдвинуть(ПерваяСтрока, -ТаблицаЗначений.Индекс(ПерваяСтрока));
	Таблица.Загрузить(ТаблицаЗначений);
	ОбновитьЗаголовкиПоказателейВТаблице();
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьОбъектВСтрокуНаСервере()
	
	ОбъектЛ = РеквизитФормыВЗначение("Объект");
	Возврат ОбщегоНазначения.ЗначениеВСтрокуXML(ОбъектЛ);
	
КонецФункции

&НаСервере
Функция ЗагрузитьОбъектИзСтрокиНаСервере(Строка)
	
	СправочникОбъект = ОбщегоНазначения.ЗначениеИзСтрокиXML(Строка);
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	ПриЧтенииНаСервере(СправочникОбъект);
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОповещениеВыбранФайлВыгрузки", ЭтаФорма);
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = "Файлы выгрузки (*.XML)|*.xml";
	ДиалогВыбораФайла.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыбранФайлВыгрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		Текст = Новый ТекстовыйДокумент;
		Текст.УстановитьТекст(ВыгрузитьОбъектВСтрокуНаСервере());
		Текст.Записать(ВыбранныеФайлы[0]);
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОповещениеВыбранФайлЗагрузки", ЭтаФорма);
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = "Файлы выгрузки (*.XML)|*.xml";
	ДиалогВыбораФайла.Показать(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыбранФайлЗагрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		Текст = Новый ТекстовыйДокумент;
		Текст.Прочитать(ВыбранныеФайлы[0]);
		ЗагрузитьОбъектИзСтрокиНаСервере(Текст.ПолучитьТекст());
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзТабличногоДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтолбцам) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо указать показатель по столбцам", , "ПоказательТарифнойСеткиПоСтолбцам", "Объект");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтрокам) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо указать показатель по строкам", , "ПоказательТарифнойСеткиПоСтрокам", "Объект");
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказательПоСтрокам", Объект.ПоказательТарифнойСеткиПоСтрокам);
	ПараметрыФормы.Вставить("ПоказательПоСтолбцам", Объект.ПоказательТарифнойСеткиПоСтолбцам);
	ОткрытьФорму("Справочник.упТарифныеСетки.Форма.ЗагрузкаИзТабличногоДокумента", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗагруженаТаблицаТарифнойСетки" Тогда
		ЗагрузитьТаблицуНаСервере(Параметр);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуНаСервере(Адрес)
	
	ТаблицаСетки = ПолучитьИзВременногоХранилища(Адрес);
	УдалитьИзВременногоХранилища(Адрес);
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	Если Ложь Тогда // Для контекстной подсказки
	    СправочникОбъект = Справочники.упТарифныеСетки.СоздатьЭлемент();
	КонецЕсли;
	СправочникОбъект.ЗагрузитьТаблицу(ТаблицаСетки);
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	ПриЧтенииНаСервере(СправочникОбъект);
	
КонецПроцедуры
	
#КонецОбласти
