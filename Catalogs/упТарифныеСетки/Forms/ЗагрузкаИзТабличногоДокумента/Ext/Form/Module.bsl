
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(Параметры.ПоказательПоСтрокам.ПараметрРасчета));
	ЭтаФорма.ТипПоказателяСтрок = Новый ОписаниеТипов(МассивТипов);
	МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(Параметры.ПоказательПоСтолбцам.ПараметрРасчета));
	ЭтаФорма.ТипПоказателяКолонок = Новый ОписаниеТипов(МассивТипов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТабличныйДокументПриАктивизацииОбласти(Элемент)
	
	Если ВыбираемаяОбласть = "" Тогда
		Возврат;
	КонецЕсли; 
	ЭтаФорма[ВыбираемаяОбласть] = ТабличныйДокумент.ВыделенныеОбласти[0].Имя;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ЗавершитьВыборОбласти();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластьЗначенийПоказателяСтрокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачатьВыборОбласти("ОбластьЗначенийПоказателяСтрок");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластьЗначенийПоказателяКолонокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачатьВыборОбласти("ОбластьЗначенийПоказателяКолонок");

КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьФайл(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОповещениеВыбранФайлЗагрузкиИзТабличногоДокумента", ЭтаФорма);
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = НСтр("ru = 'Табличный документ (*.MXL;*.XLS;*.XLSX)|*.mxl;*.XLS;*.XLSX'");
	ДиалогВыбораФайла.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборОбласти(Команда = Неопределено)
	
	ВыбираемаяОбласть = "";
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)

	АдресРезультата = ЗагрузитьДанныеНаСервере();
	Оповестить("ЗагруженаТаблицаТарифнойСетки", АдресРезультата, ЭтаФорма);
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура НачатьВыборОбласти(ИмяОбласти)
	
	ВыделитьОбласть(ИмяОбласти);
	ЭтаФорма.ТекущийЭлемент = Элементы.ТабличныйДокумент;
	ЭтаФорма.ВыбираемаяОбласть = ИмяОбласти;
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьОбласть(Знач ИмяОбласти)
	
	Если ЗначениеЗаполнено(ИмяОбласти) Тогда
		ВыделитьОбластьНаСервере(ИмяОбласти);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВыделитьОбластьНаСервере(Знач ИмяОбласти)
	
	МассивОбластей = Новый Массив;
	Попытка
		ТекущаяВыбраннаяОбласть = ТабличныйДокумент.Область(ЭтаФорма[ИмяОбласти]);
	Исключение
		ТекущаяВыбраннаяОбласть = Неопределено;
	КонецПопытки;
	Если ТекущаяВыбраннаяОбласть <> Неопределено Тогда
		МассивОбластей.Добавить(ТекущаяВыбраннаяОбласть);
	КонецЕсли;
	Элементы.ТабличныйДокумент.УстановитьВыделенныеОбласти(МассивОбластей);

КонецПроцедуры

&НаСервере
Функция ЗагрузитьДанныеНаСервере()
	
	ОбластьПоказателяКолонок = ТабличныйДокумент.Область(ОбластьЗначенийПоказателяКолонок);
	ОбластьПоказателяСтрок = ТабличныйДокумент.Область(ОбластьЗначенийПоказателяСтрок);
	ДанныеСетки = Новый ТаблицаЗначений;
	ДанныеСетки.Колонки.Добавить("Колонка0");
	СтрокаТаблицы = ДанныеСетки.Добавить();
	Для СчетчикКолонок = ОбластьПоказателяКолонок.Лево По ОбластьПоказателяКолонок.Право Цикл
		ИмяКолонки = "Колонка" + (СчетчикКолонок - ОбластьПоказателяКолонок.Лево + 1);
		ДанныеСетки.Колонки.Добавить(ИмяКолонки);
		Область = ТабличныйДокумент.Область(ОбластьПоказателяКолонок.Верх, СчетчикКолонок, ОбластьПоказателяКолонок.Верх, СчетчикКолонок);
		СтрокаТаблицы[ИмяКолонки] = ПреобразоватьПредставлениеВЗначение(ТипПоказателяКолонок.Типы()[0], Область);
	КонецЦикла;
	Для СчетчикСтрок = ОбластьПоказателяСтрок.Верх По ОбластьПоказателяСтрок.Низ Цикл
		СтрокаТаблицы = ДанныеСетки.Добавить();
		Область = ТабличныйДокумент.Область(СчетчикСтрок, ОбластьПоказателяСтрок.Лево, СчетчикСтрок, ОбластьПоказателяСтрок.Лево);
		СтрокаТаблицы["Колонка0"] = ПреобразоватьПредставлениеВЗначение(ТипПоказателяСтрок.Типы()[0], Область);
		Для СчетчикКолонок = ОбластьПоказателяКолонок.Лево По ОбластьПоказателяКолонок.Право Цикл
			ИмяКолонки = "Колонка" + (СчетчикКолонок - ОбластьПоказателяКолонок.Лево + 1);
			СтрокаТаблицы[ИмяКолонки] = ТабличныйДокумент.Область(СчетчикСтрок, СчетчикКолонок, СчетчикСтрок, СчетчикКолонок).Значение; 
		КонецЦикла;
	КонецЦикла;
	АдресРезультата = ПоместитьВоВременноеХранилище(ДанныеСетки, УникальныйИдентификатор);
	Возврат АдресРезультата;

КонецФункции

&НаСервере
Функция ПреобразоватьПредставлениеВЗначение(Тип, Область)
	
	Попытка
		ПредставлениеЗначения = Область.Значение;
	Исключение
		ПредставлениеЗначения = Область.Текст;
	КонецПопытки;
	Результат = ПредставлениеЗначения;
	ОбъектМД = Метаданные.НайтиПоТипу(Тип);
	Если ОбъектМД <> Неопределено Тогда
		Если Метаданные.Справочники.Содержит(ОбъектМД) Тогда
			Результат = Справочники[ОбъектМД.Имя].НайтиПоНаименованию(ПредставлениеЗначения);
			//Если Не ЗначениеЗаполнено(Результат) Тогда
			//	Результат = Справочники[ОбъектМД.Имя].НайтиПоКоду(ПредставлениеЗначения);
			//КонецЕсли; 
		КонецЕсли; 
		Если Не НеОкрашиватьЦветФонаПриЗагрузке Тогда
			Если Не ЗначениеЗаполнено(Результат) И ЗначениеЗаполнено(ПредставлениеЗначения) Тогда
				Область.ЦветФона = WebЦвета.СветлоРозовый;
			Иначе
				Область.ЦветФона = WebЦвета.Белый;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступность()
	
	Элементы.ФормаЗавершитьВыборОбласти.Доступность = ВыбираемаяОбласть <> "";
	Элементы.ОбластьЗначенийПоказателяКолонок.Доступность = Не ЗначениеЗаполнено(ВыбираемаяОбласть) ;
	Элементы.ОбластьЗначенийПоказателяСтрок.Доступность = Не ЗначениеЗаполнено(ВыбираемаяОбласть) ;
	Если ЗначениеЗаполнено(ВыбираемаяОбласть) Тогда
		Элементы[ВыбираемаяОбласть].Доступность = Истина;
	КонецЕсли; 
	Элементы.ФормаЗагрузитьДанные.Доступность = Истина
		И ЗначениеЗаполнено(ОбластьЗначенийПоказателяКолонок)
		И ЗначениеЗаполнено(ОбластьЗначенийПоказателяСтрок)
		И ТабличныйДокумент.ВысотаТаблицы > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыбранФайлЗагрузкиИзТабличногоДокумента(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ВыбранныйФайл = Новый Файл(ВыбранныеФайлы[0]);
		ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ВыбранныйФайл.ПолноеИмя);
		АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла);
		ПрочитатьТабличныйДокументНаСервере(АдресФайла, Сред(ВыбранныйФайл.Расширение, 2));
		//ЭтаФорма.ОбластьЗначенийПоказателяКолонок = "";
		//ЭтаФорма.ОбластьЗначенийПоказателяСтрок = "";
		ОбновитьДоступность();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТабличныйДокументНаСервере(Знач АдресФайла, Расширение = "mxl")
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
	ИмяФайла = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанныеФайла.Записать(ИмяФайла);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	
КонецПроцедуры

#КонецОбласти


