
#Область ОбрабочикиПолучения

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Код");
	Поля.Добавить("ТипГруза");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Не Данные.Ссылка.Пустая() Тогда
		СтандартнаяОбработка = Ложь;
		// При изменении алгоритма формирования представления, 
		// требуется проанализировать и при необходимости изменить
		// поиск по представлению в "ОбработкаПолученияДанныхВыбора"
		Представление = Строка(Данные.ТипГруза) + РазделительДляПредставления() + Данные.Код;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
		
	// Поиск по представлению, представление преобразуется в код.
	Разделитель = РазделительДляПредставления();
	СтрокаПоиска = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтрокаПоиска");
	Если СтрокаПоиска <> Неопределено Тогда
		ПозицияРазделителя = СтрНайти(СтрокаПоиска, Разделитель);
		Если ПозицияРазделителя > 0 Тогда
			СтрокаПоиска = Прав(СтрокаПоиска, СтрДлина(СтрокаПоиска) - СтрДлина(Разделитель) - ПозицияРазделителя + 1);
			Параметры.Вставить("СтрокаПоиска", СтрокаПоиска);
		КонецЕсли;
	КонецЕсли;
	
	СсылкаНаДокумент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Задание", Неопределено);
	Если СсылкаНаДокумент = Неопределено Тогда
		СсылкаНаДокумент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ссылка", Неопределено);	
	КонецЕсли;
		
	Если СсылкаНаДокумент <> Неопределено Тогда
		ЭтоТара = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "Тара", Ложь); 
		Запрос = Новый Запрос;
		ЭтоПоиск = СтрокаПоиска <> Неопределено;
		Запрос.Текст = ТекстЗапросаГрузовыеМеста(СсылкаНаДокумент, ЭтоТара, ЭтоПоиск);
		Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
		Если СтрокаПоиска <> Неопределено Тогда
			Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.ГрузовоеМесто);
		КонецЦикла;
		
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...],
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с,
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упГрузовыеМеста.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

Функция ГрузовыеМестаПоДокументу(ДокументСсылка, ЭтоТара = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаГрузовыеМеста(ДокументСсылка, ЭтоТара, Ложь);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГрузовоеМесто");
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаГрузовыеМеста(ДокументСсылка, ЭтоТара = Ложь, ЭтоПоиск = Ложь)
	
	Результат = "";
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда
		Результат = Результат + "ВЫБРАТЬ
		|	ГрузовыеМестаДокумент.ГрузовоеМесто КАК ГрузовоеМесто
		|ИЗ
		|	Документ.упЗаявкаНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМестаДокумент";		
	Иначе	
		Результат = Результат + "ВЫБРАТЬ
		|	ГрузовыеМестаДокумент.ГрузовоеМесто КАК ГрузовоеМесто
		|ИЗ
		|	Документ.упЗаданиеНаПеревозкуГруза.ГрузовыеМеста КАК ГрузовыеМестаДокумент";
	КонецЕсли;
	
	Если Ложь
		Или ЭтоТара
		Или ЭтоПоиск 
	Тогда
		Результат = Результат + "
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГрузовыеМеста КАК упГрузовыеМестаТ
		|	ПО упГрузовыеМестаТ.Ссылка = ГрузовыеМестаДокумент.ГрузовоеМесто";
	КонецЕсли; 
		
	Результат = Результат + "
	|ГДЕ ИСТИНА
	|	И ГрузовыеМестаДокумент.Ссылка = &Ссылка";
	
	Если ЭтоТара Тогда
		Результат = Результат + "
		|	И упГрузовыеМестаТ.Тара";
	КонецЕсли;
	
	Если ЭтоПоиск Тогда
		Результат = Результат + "
		|	И упГрузовыеМестаТ.Код ПОДОБНО &СтрокаПоиска";
	КонецЕсли;
	
	Возврат Результат;	
		
КонецФункции


#КонецОбласти 

#Область СлужебныеПроцедурыФункции

Функция РазделительДляПредставления()
	Возврат " № ";
КонецФункции

#КонецОбласти
