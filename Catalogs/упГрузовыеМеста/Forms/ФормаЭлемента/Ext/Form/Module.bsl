
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Параметры.Свойство("ЗаявкаНаПеревозку") И ЗначениеЗаполнено(Параметры.ЗаявкаНаПеревозку) Тогда
			сткРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ЗаявкаНаПеревозку, "Дата, Номер");
			Объект.Код = Формат(сткРеквизиты.Дата, "ДФ=гг") + СокрЛП(сткРеквизиты.Номер) + ?(Параметры.Свойство("СчетчикГрузов"), "-" + Параметры.СчетчикГрузов, "");
			Объект.ДокументОснование = Параметры.ЗаявкаНаПеревозку
		КонецЕсли; 
	КонецЕсли; 
	
	УстановитьДоступность();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
    ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.ЕИ_Длины  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияЛинейныхГабаритов");
	ЭтаФорма.ЕИ_Объем  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияОбъема");
	ЭтаФорма.ЕИ_Масса  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияМассы");
	ЭтаФорма.ЕИ_Грузов = упСервисныеФункции.ПолучитьЗначениеКонстанты("упЕдиницаИзмеренияГрузовыхМест");
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры") И Не ПолучитьФункциональнуюОпцию("упУчитыватьОбъем") Тогда 
		Элементы.ГруппаШГВ.Видимость              = Ложь;
	КонецЕсли;	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьОбъем") Тогда 
		Элементы.ГруппаОбъем.Видимость            = Ложь;
	КонецЕсли;	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьМассу") Тогда 
		Элементы.ГруппаГрузоподъемность.Видимость = Ложь;
	КонецЕсли;	
	Если Не ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест") Тогда 
		Элементы.ГруппаКоличествоМест.Видимость   = Ложь;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
		
	ЭтаФорма.ВедетсяВалютныйУчет	= ПолучитьФункциональнуюОпцию("упВедетсяВалютныйУчет");
	Если ЭтаФорма.ВедетсяВалютныйУчет Тогда
		 ЭтаФорма.ВалютаУпрУчета = упСервисныеФункцииВызовСервера.ВалютаУправленческогоУчета();	
	КонецЕсли;

	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
		
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ЭтоНовый = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущийОбъект.ДополнительныеСвойства,"Создание", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Источник = Объект.ТипГруза Тогда
		УстановитьДоступность();			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Если ЭтоНовый Тогда
			Оповестить("Справочник.упГрузовыеМеста.Создание", Объект.ДокументОснование, Объект.Ссылка);
		Иначе	
			Оповестить("Справочник.упГрузовыеМеста.Запись", Объект.ДокументОснование, Объект.Ссылка);	
		КонецЕсли;
		ЭтоНовый = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипГрузаПриИзменении(Элемент)
	
	ОбновитьЭлементыДополнительныхРеквизитов();
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.Ширина * Объект.Высота * Объект.Длина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПересчитатьИтоговыеСуммы();
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаПослеУдаления(Элемент)
	ПересчитатьИтоговыеСуммы();
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаЦенаПриИзменении(Элемент)

	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаКоличествоПриИзменении(Элемент)

	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	
	РассчитатьВГХГруза();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаСуммаПриИзменении(Элемент)

	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаСуммаПриИзменении(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаСтавкаНДСПриИзменении(Элемент)

	упОбработкаТабличнойЧастиКлиент.ТоварныйСоставГрузаРассчитатьСуммы(Элементы.ТоварныйСоставГруза.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаНоменклатураПриИзменении(Элемент)
	
	ТоварныйСоставГрузаНоменклатураПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТоварныйСоставГрузаНоменклатураПриИзмененииНаСервере()

	РассчитатьВГХГруза();
	
	мсвНоменклатуры = Неопределено;
	Если ПолучитьФункциональнуюОпцию("упУчитыватьКлассыОпасностиГрузов") Тогда 
		мсвНоменклатуры = Объект.ТоварныйСоставГруза.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упНоменклатура.КлассОпасности
		|ИЗ
		|	Справочник.упНоменклатура КАК упНоменклатура
		|ГДЕ
		|	упНоменклатура.Ссылка В(&Ссылка)
		|	И упНоменклатура.КлассОпасности <> ЗНАЧЕНИЕ(Справочник.упКлассыОпасности.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	упНоменклатура.КлассОпасности.Класс";
		Запрос.УстановитьПараметр("Ссылка", мсвНоменклатуры);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			Объект.КлассОпасности = Выборка.КлассОпасности;
		КонецЕсли;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("упКонтролироватьТемпературныеРежимы") Тогда 
		Если мсвНоменклатуры = Неопределено Тогда 
			мсвНоменклатуры = Объект.ТоварныйСоставГруза.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	МАКСИМУМ(упНоменклатура.ТемпературныйРежим.ТемператураОт) КАК ТемпературныйРежимТемператураОт,
		|	МИНИМУМ(упНоменклатура.ТемпературныйРежим.ТемператураДо) КАК ТемпературныйРежимТемператураДо
		|ПОМЕСТИТЬ втДопустимыйДиапазон
		|ИЗ
		|	Справочник.упНоменклатура КАК упНоменклатура
		|ГДЕ
		|	упНоменклатура.Ссылка В(&Ссылка)
		|	И упНоменклатура.ТемпературныйРежим <> ЗНАЧЕНИЕ(Справочник.упТемпературныережимы.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упТемпературныеРежимы.Ссылка,
		|	упТемпературныеРежимы.ТемператураДо - упТемпературныеРежимы.ТемператураОт КАК Порядок
		|ИЗ
		|	Справочник.упТемпературныеРежимы КАК упТемпературныеРежимы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДопустимыйДиапазон КАК втДопустимыйДиапазон
		|		ПО упТемпературныеРежимы.ТемператураОт >= втДопустимыйДиапазон.ТемпературныйРежимТемператураОт
		|			И упТемпературныеРежимы.ТемператураДо <= втДопустимыйДиапазон.ТемпературныйРежимТемператураДо
		|ГДЕ
		|	НЕ упТемпературныеРежимы.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок УБЫВ";
		Запрос.УстановитьПараметр("Ссылка", мсвНоменклатуры);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			Объект.ТемпературныйРежим = Выборка.Ссылка;
		Иначе
			Объект.ТемпературныйРежим = Неопределено;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьВГХГруза(Параметр = Неопределено)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	тбТовары.Номенклатура,
	|	тбТовары.УпаковкаНоменклатуры,
	|	тбТовары.Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&тбТовары КАК тбТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(упУпаковкиНоменклатуры.Масса, упНоменклатура.Масса) * втТовары.Количество) КАК Масса,
	|	СУММА(ЕСТЬNULL(упУпаковкиНоменклатуры.Объем, упНоменклатура.Объем) * втТовары.Количество) КАК Объем
	|ИЗ
	|	втТовары КАК втТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упНоменклатура КАК упНоменклатура
	|		ПО втТовары.Номенклатура = упНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упУпаковкиНоменклатуры КАК упУпаковкиНоменклатуры
	|		ПО втТовары.УпаковкаНоменклатуры = упУпаковкиНоменклатуры.Ссылка";
	Запрос.УстановитьПараметр("тбТовары", Объект.ТоварныйСоставГруза.Выгрузить(, "Номенклатура, УпаковкаНоменклатуры, Количество"));
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Если ЗначениеЗаполнено(Выборка.Масса) И (Выборка.Масса > Объект.Масса ИЛИ Параметр = "Масса") Тогда 
			Объект.Масса = Выборка.Масса;
		ИначеЕсли Параметр = "Масса" Тогда 
			Объект.Масса = 0;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Объем) И (Выборка.Объем > Объект.Объем ИЛИ Параметр = "Объем") Тогда 
			Объект.Объем = Выборка.Объем;
		ИначеЕсли Параметр = "Объем" Тогда 
			Объект.Объем = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварныйСоставГрузаУпаковкаНоменклатурыПриИзменении(Элемент)
	
	РассчитатьВГХГруза();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьОбъем(Команда)
	
	РассчитатьВГХГруза("Объем");
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьМассу(Команда)
	
	РассчитатьВГХГруза("Масса");
	
КонецПроцедуры

#КонецОбласти

#Область ЗапретРедактированияРеквизитовОбъектов

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	  
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПересчитатьИтоговыеСуммы()
	Объект.Сумма 	= Объект.ТоварныйСоставГруза.Итог("Сумма");
	Объект.СуммаНДС = Объект.ТоварныйСоставГруза.Итог("СуммаНДС");		
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность()
	
	ВидГруза = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипГруза, "ВидГруза"); 
	
	Если ВидГруза = Перечисления.упВидыГрузов.Тара Тогда
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
		Элементы.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
		Элементы.ДоступнаяФормаОплаты.Видимость = Ложь;
	ИначеЕсли ВидГруза = Перечисления.упВидыГрузов.Груз Тогда
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаГруз;
		Элементы.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Истина;
		Элементы.ДоступнаяФормаОплаты.Видимость = Истина;
		Элементы.ДоступнаяФормаОплаты.Доступность = Объект.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки;
	ИначеЕсли ВидГруза = Перечисления.упВидыГрузов.ИнкассируемаяЦенность Тогда
		Элементы.ГруппаВалюта.ТекущаяСтраница = Элементы.ГруппаВалютаИнкассируемаяЦенность;
		Элементы.ДоступнаяФормаОплаты.Видимость = Истина;
		Элементы.ВыемкаИнкассируемыхЦенностейВТочкеРазгрузки.Видимость = Ложь;
	КонецЕсли; 
		
КонецПроцедуры // УстановитьДоступность()

&НаКлиенте
Процедура ТоварныйСоставГрузаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ТоварныйСоставГрузаНоменклатура Тогда
		Если Ложь
			Или ТолькоПросмотр
			Или Элементы.ТоварныйСоставГруза.ТолькоПросмотр
			Или Поле.ТолькоПросмотр
		Тогда
			ПоказатьЗначение(, Объект.ТоварныйСоставГруза.НайтиПоИдентификатору(ВыбраннаяСтрока).Номенклатура);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


