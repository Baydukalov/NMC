
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	НетПраваРедактировать = НЕ ПолучитьПравоКорректироватьАдрес();
	
	Если НетПраваРедактировать И ПараметрКоманды.Подтверждена Тогда		
		ТекстОшибки = СтрШаблон("Зона %1 утверждена, вносить изменения запрещено",ПараметрКоманды);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ПараметрКоманды);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Параметры = Новый Структура;
	Параметры.Вставить("Объект",ПараметрКоманды);
	Параметры.Вставить("Источник",ПараметрыВыполненияКоманды.Источник);
	ФормаОбъекта = ПолучитьФорму("Справочник.упГеографическиеЗоны.ФормаОбъекта");   
	Параметры.Вставить("ЭтаФормаОбъекта",ПараметрыВыполненияКоманды.Источник.ИмяФормы = ФормаОбъекта.ИмяФормы);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОткрытиеФайла", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Выполнить импорт координат полигона?';"+ " en = 'Do you want to continue?'"), Режим, 0);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаОповещенияВопросов

&НаКлиенте
// Выполнить действия после получения оповещения.
//
// Параметры:
//  РезультатВопроса  - КодВозвратаДиалога - Результат ответа на вопрос.
//  ДополнительныеПараметры  - Структура - Параметры ответа.
//
Процедура ПослеЗакрытияВопросаОткрытиеФайла(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса =  КодВозвратаДиалога.Да Тогда
		ПараметрКоманды = ДополнительныеПараметры.Объект;
		Режим = РежимДиалогаВыбораФайла.Открытие;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		ДиалогОткрытияФайла.ПолноеИмяФайла = СокрЛП(ПараметрКоманды)+".csv";
		Фильтр = НСтр("ru = 'Универсальный текстовый формат'; en = 'Comma-Separated Values'")+ "(*.csv)|*.csv|" + НСтр("ru = 'Текстовый файл'; en = 'Text'")+ "(*.txt)|*.txt";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ИмяФайла = "";
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = "Выберите файлы";
		Если ДиалогОткрытияФайла.Выбрать() Тогда		
			ИмяФайла = ДиалогОткрытияФайла.ПолноеИмяФайла;		
			// Используется для доступа к параметрам файлов или каталогов. 
			ТекстовыйФайл = Новый Файл(ИмяФайла);
			Если НЕ ТекстовыйФайл.Существует() Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файла ""%1"", не существует!'"), ИмяФайла));
			Иначе
				Разделитель = Символы.Таб;
				// чтение файла.
				ЗагружаемыйФайл = Новый ТекстовыйДокумент;
				ЗагружаемыйФайл.Прочитать(ИмяФайла);
				КолСтрок = ЗагружаемыйФайл.КоличествоСтрок();
				мсвФайл = Новый Массив;
				ЕстьОшибки = Ложь;
				Для i=1 По КолСтрок Цикл
					Если i=1 Тогда
						Продолжить;
					КонецЕсли;
					мсвДанные = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗагружаемыйФайл.ПолучитьСтроку(i), Разделитель);				
					Если мсвДанные.Количество() >= 3 Тогда
						Структура = Новый Структура;
						ТекущаяОшибка = Ложь;
						Структура.Вставить("Широта",ПредставлениеКоординатыВЧисло(мсвДанные[2],ТекущаяОшибка));
						Структура.Вставить("Долгота",ПредставлениеКоординатыВЧисло(мсвДанные[1],ТекущаяОшибка));
						Если ТекущаяОшибка Тогда
							ЕстьОшибки = Истина;
						Иначе
							мсвФайл.Добавить(Структура);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				Если ЕстьОшибки И мсвФайл.Количество() > 0 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В файле: ""%1"" присутствуют некорректные данные!'"), ИмяФайла));
				КонецЕсли;
				ЗагружаемыйФайл = Неопределено;
				Если мсвФайл.Количество() Тогда
					Количество = ПолучитьКоличествоТБЧПолигон(ПараметрКоманды);
					ТекстВопроса = НСтр("ru = 'Из указанного файла получены координаты полигона, табличная часть "+Символ(34)+"Полигон"+Символ(34)+" будет очищена. Продолжить импорт координат?';"+ " en = 'Do you want to continue?'");
					Если НЕ Количество Тогда
						ТекстВопроса = НСтр("ru = 'Из указанного файла получены координаты полигона. Продолжить импорт координат?';"+ " en = 'Do you want to continue?'");
					КонецЕсли;
					Режим = РежимДиалогаВопрос.ДаНет;
					Параметры = Новый Структура;
					Параметры.Вставить("Объект",ПараметрКоманды);
					Параметры.Вставить("Массив",мсвФайл);
					Параметры.Вставить("Источник",ДополнительныеПараметры.Источник);
					Параметры.Вставить("ЭтаФормаОбъекта",ДополнительныеПараметры.ЭтаФормаОбъекта);
					Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗагрузкаФайла", ЭтотОбъект, Параметры);
					ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
				Иначе	
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не верный формат файла: ""%1""!'"), ИмяФайла));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;		
	
КонецПроцедуры // ПослеЗакрытияВопросаОткрытиеФайла()

&НаСервере
// Выполнить на стороне сервера считываение количества строчек полигона.
//
// Параметры:
//  Источник  - Справочник.упГеографическиеЗоны - Ссылка на элемент справочника.
//
// Возвращаемое значение:
//   Число   - Количество строк в тбч полигон.
//
Функция ПолучитьКоличествоТБЧПолигон(Источник)

	Количество = Источник.Полигон.Количество();
	
	Возврат Количество;

КонецФункции // ПолучитьКоличествоТБЧПолигон()

&НаКлиенте
// Выполнить действия после получения оповещения.
//
// Параметры:
//  РезультатВопроса  - КодВозвратаДиалога - Результат ответа на вопрос.
//  ДополнительныеПараметры  - Структура - Параметры ответа.
//
Процедура ПослеЗакрытияВопросаЗагрузкаФайла(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса =  КодВозвратаДиалога.Да Тогда
		ПараметрКоманды = ДополнительныеПараметры.Объект;
		мсвФайл = ДополнительныеПараметры.Массив;
		Если ДополнительныеПараметры.ЭтаФормаОбъекта Тогда
			Оповестить("ИмпортCSVФайлаВПолигон",ДополнительныеПараметры,ДополнительныеПараметры.Источник);
		Иначе
			ИмпортCSVФайлаВПолигон(мсвФайл,ПараметрКоманды);
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры // ПослеЗакрытияВопросаЗагрузкаФайла()

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
// Выполним обращение для получения прав редактировать.
//
//
// Возвращаемое значение:
//   Булево - Разрешение на редактирование.
//
Функция ПолучитьПравоКорректироватьАдрес()

	Возврат Справочники.упАдреса.ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных();

КонецФункции // ПолучитьПравоКорректироватьАдрес() 

&НаСервере
// Безопастно преобразует строковое представление кооридинаты в число.
//
// Параметры:
//	ПредставлениеКоординаты - строка - Текст координат.
//
// Возвращаемое значение:
//	Число - Результат выполнения.
//
Функция ПредставлениеКоординатыВЧисло(ПредставлениеКоординаты,Ошибка)
	
	Результат = 0;
	
	Попытка
		Результат = Число(ПредставлениеКоординаты);
	Исключение
		Ошибка = Истина;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
// Выполняет заполнения тбч полигон для географической зоны.
//
// Параметры:
//	СтрокиФайла - Массив - структур, ключи структуры "Долготы, Широта".
//	ГеографическаяЗона - СправочникСсылка - ссылка на зону в которой изменяем полигон.
//
// Возвращаемое значение:
//	Нет.
//
Функция ИмпортCSVФайлаВПолигон(СтрокиФайла,ГеографическаяЗона)
	
	ТекущаяОшибка = Ложь;
	
	Разделитель = Символы.Таб;
	
	ТекущийОбъект = ГеографическаяЗона.ПолучитьОбъект();	
	Попытка
		ТекущийОбъект.Заблокировать();
	Исключение
		ТекущаяОшибка = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Географический объект ""%1"", не удалось заблокировать для изменения.'"), ГеографическаяЗона));
	КонецПопытки;
	Если ТекущийОбъект.Заблокирован() Тогда
		ИнформацияПоКолонкам = ТекущийОбъект.Полигон;
		ИнформацияПоКолонкам.Очистить();	
		
		Для Каждого текЭлемент Из СтрокиФайла Цикл
			
			НоваяСтрока = ИнформацияПоКолонкам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,текЭлемент)
		КонецЦикла;
		
		Попытка
			ТекущийОбъект.Записать();
		Исключение
			ТекущаяОшибка = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'НЕ удалось записать изменения географического объекта: ""%1"".'"), ГеографическаяЗона));
		КонецПопытки;
		ТекущийОбъект.Разблокировать();
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

