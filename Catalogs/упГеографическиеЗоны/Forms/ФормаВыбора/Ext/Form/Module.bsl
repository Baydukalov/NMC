
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.упГеографическиеЗоны) Тогда
		Элементы.ФормаСоздатьПоСтранам.Видимость = Ложь;	
		Элементы.ФормаСоздатьПоРегионам.Видимость = Ложь;	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура СоздатьПоСтранам(Команда)

	сткПараметры = Новый Структура("МножественныйВыбор, РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина, Ложь);
	ОткрытьФорму("Справочник.СтраныМира.ФормаВыбора", сткПараметры, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоРегионам(Команда)

	сткПараметры = Новый Структура("МножественныйВыбор, РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина, Ложь);
	ОткрытьФорму("Справочник.упРегионы.ФормаВыбора", сткПараметры, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение)

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда 
		Для Каждого текЗначение Из ВыбранноеЗначение Цикл 
			ОбработкаЗначенияНаСервере(текЗначение);
		КонецЦикла;	
	Иначе	
		ОбработкаЗначенияНаСервере(ВыбранноеЗначение);
	КонецЕсли;	
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗначенияНаСервере(ВыбранноеЗначение)

	СоздатьЭлемент = Ложь;
	
	Родитель = Элементы.Список.ТекущийРодитель;
	Если Родитель = Неопределено Тогда 
		Родитель = Справочники.упГеографическиеЗоны.ПустаяСсылка();
	КонецЕсли;
		
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СтраныМира") Тогда 
		Если ЗначениеЗаполнено(Родитель) И ЗначениеЗаполнено(Родитель.Страна) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Создание элемента %1: В родительской зоне уже указана страна'"), ВыбранноеЗначение), Родитель, "Объект.Страна");
			Возврат;
		КонецЕсли;
		
		СоздатьЭлемент = Истина;
		Страна         = ВыбранноеЗначение;
		Регион         = Справочники.упРегионы.ПустаяСсылка();
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.упРегионы") Тогда 
		Если ЗначениеЗаполнено(Родитель) И ЗначениеЗаполнено(Родитель.Регион) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Создание элемента %1: В родительской зоне уже указан регион'"), ВыбранноеЗначение), Родитель, "Объект.Регион");
			Возврат;
		ИначеЕсли ЗначениеЗаполнено(Родитель) И ЗначениеЗаполнено(Родитель.Страна) И Родитель.Страна <> ВыбранноеЗначение.Владелец Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Создание элемента %1: В родительской зоне уже указана другая страна'"), ВыбранноеЗначение), Родитель, "Объект.Страна");
			Возврат;
		КонецЕсли;
		
		СоздатьЭлемент = Истина;
		Страна         = ВыбранноеЗначение.Владелец;
		Регион         = ВыбранноеЗначение;
	КонецЕсли;	
	
	Если СоздатьЭлемент Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	упГеографическиеЗоны.Ссылка
		|ИЗ
		|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
		|ГДЕ
		|	упГеографическиеЗоны.Родитель = &Родитель
		|	И упГеографическиеЗоны.Страна = &Страна
		|	И упГеографическиеЗоны.Регион = &Регион";
		Запрос.УстановитьПараметр("Родитель", Родитель);
		Запрос.УстановитьПараметр("Страна",   Страна);
		Запрос.УстановитьПараметр("Регион",   Регион);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Создание элемента %1: Указанный элемент уже создан'"), ВыбранноеЗначение), Выборка.Ссылка, "Объект.Наименование");
		Иначе
			НовыйЭлемент = Справочники.упГеографическиеЗоны.СоздатьЭлемент();
			НовыйЭлемент.Наименование = ВыбранноеЗначение.Наименование;
			НовыйЭлемент.Родитель     = Родитель;
			НовыйЭлемент.Страна       = Страна;
			НовыйЭлемент.Регион       = Регион;
			НовыйЭлемент.Записать();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
