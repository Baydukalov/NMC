#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	ЕстьПравоРедактировать = Справочники.упАдреса.ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И Объект.Подтверждена Тогда
		// Для утвержденных объектов нельзя вносить изменения.		
		ТолькоПросмотр = НЕ ЕстьПравоРедактировать;
		// Отключим доступность к командам.
		//Элементы.КоэффициентыРасчетаВремениВПутиПоДнямОбновитьКоэффициентыРасчетаВремениВПути.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.Подтверждена.ТолькоПросмотр = НЕ ЕстьПравоРедактировать;
	
	Элементы.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД.Видимость = Истина
		И ЭтаФорма.РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД
		И ЭтаФорма.ТипМаршрутизатора = ПредопределенноеЗначение("Перечисление.упТипыМаршрутизаторов.СитиГИД");
		
	Набор = РегистрыСведений.упЗональныеНастройкиМаршрутизации.СоздатьНаборЗаписей();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Набор.Отбор.ЗонаОтправления.Установить(Объект.Ссылка);
		Набор.Прочитать();
	КонецЕсли; 
	ЭтаФорма.ЗональныеНастройкиМаршрутизации.Загрузить(Набор.Выгрузить());

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства.
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства.
	
	ЭтаФорма.ТипМаршрутизатора  = упСервисныеФункции.ПолучитьЗначениеКонстанты("упТипМаршрутизатора");
	РассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД = ПолучитьФункциональнуюОпцию("упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД");
		
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = Справочники.упГеографическиеЗоны.СоздатьЭлемент();
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("упУчитыватьКоэффициентыРасчетаВремениВПути") Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗональныеНастройкиМаршрутизации", ЗональныеНастройкиМаршрутизации.Выгрузить());
	КонецЕсли;
	ТекущийОбъект.ПолигонПростой.Очистить(); // Можно очищать только после сравнения старой и новой ТЧ Полигон

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства. 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства.
	
	Если ИмяСобытия = "ИмпортCSVФайлаВПолигон" И Источник = ЭтаФорма Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаИмпорта",ЭтаФорма,Параметр);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Заполнить табличную часть полигон географической зоны?';"+ " en = 'Do you want to continue?'"), Режим, 0);
	КонецЕсли;
	упСервисныеФункцииКлиент.ФормаСсылочныйОбъект_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	 
	Если ЗначениеЗаполнено(Объект.Родитель) Тогда 
		ЗаполнитьСтрануИРегион();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолигонДолготаПриИзменении(Элемент)
	
	Если Объект.Полигон.Количество() Тогда 
		МинМеридиан  = Объект.Полигон[0].Долгота;
		МаксМеридиан = Объект.Полигон[0].Долгота;
		
		Для Каждого текВершина Из Объект.Полигон Цикл 
			МинМеридиан  = Мин(МинМеридиан, текВершина.Долгота);
			МаксМеридиан = Макс(МаксМеридиан, текВершина.Долгота);
		КонецЦикла;
		
		Если МаксМеридиан - МинМеридиан > 270 Тогда 
			Объект.ПроходитЧерез180Меридиан = Истина;
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоныПриИзменении(Элемент)
	
	Элементы.упРассчитыватьРасстоянияСУчетомДополнительныхОграниченийСитиГИД.ТолькоПросмотр = НЕ Объект.РассчитыватьПопарныеРасстоянияМеждуТочкамиЗоны;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Выполнить действия после получения оповещения.
//
// Параметры:
//  РезультатВопроса  - КодВозвратаДиалога - Результат ответа на вопрос.
//  ДополнительныеПараметры  - Структура - Параметры ответа.
//
Процедура ПослеЗакрытияВопросаИмпорта(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса =  КодВозвратаДиалога.Да Тогда
		СтрокиФайла = ДополнительныеПараметры.Массив;
		Объект.Полигон.Очистить();
		Объект.ПолигонПростой.Очистить();
		Для Каждого текЭлемент Из СтрокиФайла Цикл
			НоваяСтрока = Объект.Полигон.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,текЭлемент)			
		КонецЦикла;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопросаИмпорта()

&НаСервере
Процедура ЗаполнитьСтрануИРегион()
	
	сткДанныеРодителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Родитель, "Регион, Страна");
	Если ЗначениеЗаполнено(сткДанныеРодителя.Страна) Тогда 
		Объект.Страна = сткДанныеРодителя.Страна;
	КонецЕсли;	
	Если ЗначениеЗаполнено(сткДанныеРодителя.Регион) Тогда 
		Объект.Регион = сткДанныеРодителя.Регион;
	ИначеЕсли ЗначениеЗаполнено(Объект.Регион) Тогда 
		Если Объект.Страна <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Регион, "Владелец") Тогда 
			Объект.Регион = сткДанныеРодителя.Регион;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	  
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства.
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства.
КонецПроцедуры

#КонецОбласти