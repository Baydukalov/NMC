 #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ЕстьПравоРедактировать = Справочники.упАдреса.ВыполнитьПроверкуПраваПодтверждениеКорректностиГеографическихДанных();
	
	Если НЕ ЕстьПравоРедактировать И Подтверждена Тогда
		ТекстОшибки = СтрШаблон("У вас нет права корректировать зону: %1. Установлен признак подтверждения!!!",Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
	КонецЕсли;
	Если Не ЭтотОбъект.ДополнительныеСвойства.Свойство("ПолигонПростойАктуален") Тогда
		ЭтотОбъект.ПолигонПростой.Очистить();
	КонецЕсли; 
	Если Полигон.Количество() Тогда 
		Для Каждого текТочка Из Полигон Цикл 
			Пока текТочка.Широта > 180 Цикл 
				текТочка.Широта = текТочка.Широта - 180;
			КонецЦикла;
			Пока текТочка.Широта < -180 Цикл 
				текТочка.Широта = текТочка.Широта + 180;
			КонецЦикла;
			Пока текТочка.Долгота > 360 Цикл 
				текТочка.Долгота = текТочка.Долгота - 360;
			КонецЦикла;
			Пока текТочка.Долгота < -360 Цикл 
				текТочка.Долгота = текТочка.Долгота + 360;
			КонецЦикла;
		КонецЦикла;	
			
		МинШирота   = 180;
		МаксШирота  = -180;
		МинДолгота  = 180;
		МаксДолгота = -180;
		Для Каждого текТочка Из Полигон Цикл 
			МинШирота  = Мин(МинШирота, текТочка.Широта);
			МаксШирота = Макс(МаксШирота, текТочка.Широта);
			
			Если Не ПроходитЧерез180Меридиан Тогда 
				МинДолгота  = Мин(МинДолгота, текТочка.Долгота);
				МаксДолгота = Макс(МаксДолгота, текТочка.Долгота);
			ИначеЕсли текТочка.Долгота < 0 Тогда
				МаксДолгота = Макс(МаксДолгота, текТочка.Долгота);
			Иначе
				МинДолгота  = Мин(МинДолгота, текТочка.Долгота);
			КонецЕсли;	
		КонецЦикла;	
		//Если упСервисныеФункции.ПолучитьЗначениеКонстанты(Метаданные.Константы.упРассчитыватьПростыеПолилинии.Имя) Тогда
		//	ПолигонПростой.Загрузить(Полигон.Выгрузить(упРаботаСКартами.ВыполнитьУпрощениеПолигональнойЦепи(Полигон,, 200)));
		//КонецЕсли;
	Иначе	
		МинШирота   = 0;
		МаксШирота  = 0;
		МинДолгота  = 0;
		МаксДолгота = 0;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьИерархиюЗон();
	
	Если ПолучитьФункциональнуюОпцию("упКонтролироватьДоступностьГеографическихЗон") Тогда 
		ЗаписатьПравилаДоступности();
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("ЗональныеНастройкиМаршрутизации") Тогда
		ДополнительныеСвойства.ЗональныеНастройкиМаршрутизации.ЗаполнитьЗначения(Ссылка, "ЗонаОтправления");
		Набор = РегистрыСведений.упЗональныеНастройкиМаршрутизации.СоздатьНаборЗаписей();
		Набор.Отбор.ЗонаОтправления.Установить(Ссылка);
		Набор.Загрузить(ДополнительныеСвойства.ЗональныеНастройкиМаршрутизации);
		Набор.Записать();
	КонецЕсли; 

КонецПроцедуры // ПриЗаписи()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если ЗначениеЗаполнено(Родитель) И Родитель = Ссылка Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Родителем географической зоны не может быть она же.'"), Ссылка, "Объект.Родитель",, Отказ);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)

	НаборЗаписей = РегистрыСведений.упДоступныеГеографическиеЗоны.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГеографическаяЗона.Установить(Ссылка);
	НаборЗаписей.Записать(Истина);

	НаборЗаписей = РегистрыСведений.упРасстоянияМеждуЗонами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НачальнаяЗона.Установить(Ссылка);
	НаборЗаписей.Записать(Истина);

	НаборЗаписей = РегистрыСведений.упРасстоянияМеждуЗонами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КонечнаяЗона.Установить(Ссылка);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		Если ДанныеЗаполнения.Свойство("Родитель") Тогда 
			сткДанныеРодителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.Родитель, "Регион, Страна");
			Если Не ДанныеЗаполнения.Свойство("Страна") Тогда 
				ДанныеЗаполнения.Вставить("Страна", сткДанныеРодителя.Страна);
			КонецЕсли;	
			Если Не ДанныеЗаполнения.Свойство("Регион") Тогда 
				ДанныеЗаполнения.Вставить("Регион", сткДанныеРодителя.Регион);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИерархияЗон

// Процедура записывает информацию об иерархии зон
// 
Процедура ЗаписатьИерархиюЗон()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|ГДЕ
	|	упГеографическиеЗоны.Ссылка В ИЕРАРХИИ(&Ссылка)
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ДеревоИерархии = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ТекущийЭлемент = ДеревоИерархии.Строки.Найти(Ссылка,"Ссылка",Истина);
	
	Если ТекущийЭлемент <> Неопределено Тогда
		ВыполнитьЗаписьВИерархию(ТекущийЭлемент);
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Процедура записывает информацию об иерархии подчиненных зон
// 
Процедура ВыполнитьЗаписьПоПодчиненным(СтрокиДерева, ТекущийЭлемент)
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Ссылка <> ТекущийЭлемент Тогда
			ВыполнитьЗаписьВИерархию(СтрокаДерева);
		КонецЕсли;
	КонецЦикла; 	
	
КонецПроцедуры

// Процедура записывает в регистр информацию об иерархии зон
// 
Процедура ВыполнитьЗаписьВИерархию(СтрокаДерева);
	
	НаборЗаписей = РегистрыСведений.упИерархияГеографическихЗон.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Зона.Установить(СтрокаДерева.Ссылка);
	
	РодительСтроки = СтрокаДерева;
	Пока РодительСтроки <> Неопределено Цикл
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Зона     = СтрокаДерева.Ссылка;
		ЗаписьНабора.Родитель = РодительСтроки.Ссылка;
		ЗаписьНабора.Уровень  = РодительСтроки.Уровень();
		
		РодительСтроки = РодительСтроки.Родитель;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	ВыполнитьЗаписьПоПодчиненным(СтрокаДерева.Строки, СтрокаДерева.Ссылка);
	
КонецПроцедуры

#КонецОбласти 

#Область ПравилаДоступности

// Процедура записывает информацию о правилах доступности
// 
// Параметры:
//  Нет.
//
Процедура ЗаписатьПравилаДоступности()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упИерархияГеографическихЗон.Зона КАК Зона,
	|	упПравилаДоступностиГеографическихЗон.Ссылка КАК Правила,
	|	упПравилаДоступностиГеографическихЗон.ЗапрещенаРаботаВУказанныхЗонах
	|ПОМЕСТИТЬ втЗоныДляПерезаписи
	|ИЗ
	|	РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон,
	|	Справочник.упПравилаДоступностиГеографическихЗон КАК упПравилаДоступностиГеографическихЗон
	|ГДЕ
	|	упИерархияГеографическихЗон.Родитель = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правила,
	|	Зона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	упПравилаДоступностиГеографическихЗонГеографическиеЗоны.Ссылка КАК Правила,
	|	упИерархияГеографическихЗон.Зона КАК Зона
	|ПОМЕСТИТЬ втЗоныПравил
	|ИЗ
	|	Справочник.упПравилаДоступностиГеографическихЗон.ГеографическиеЗоны КАК упПравилаДоступностиГеографическихЗонГеографическиеЗоны
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	|		ПО упПравилаДоступностиГеографическихЗонГеографическиеЗоны.ГеографическаяЗона = упИерархияГеографическихЗон.Родитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правила,
	|	Зона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗоныДляПерезаписи.Правила,
	|	втЗоныДляПерезаписи.Зона КАК Зона
	|ИЗ
	|	втЗоныДляПерезаписи КАК втЗоныДляПерезаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗоныПравил КАК втЗоныПравил
	|		ПО втЗоныДляПерезаписи.Зона = втЗоныПравил.Зона
	|			И втЗоныДляПерезаписи.Правила = втЗоныПравил.Правила
	|ГДЕ
	|	(втЗоныДляПерезаписи.ЗапрещенаРаботаВУказанныхЗонах
	|				И втЗоныПравил.Зона ЕСТЬ NULL 
	|			ИЛИ НЕ втЗоныДляПерезаписи.ЗапрещенаРаботаВУказанныхЗонах
	|				И НЕ втЗоныПравил.Зона ЕСТЬ NULL )
	|ИТОГИ ПО
	|	Зона";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ВыборкаЗоны = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗоны.Следующий() Цикл 
		НаборЗаписей = РегистрыСведений.упДоступныеГеографическиеЗоны.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ГеографическаяЗона.Установить(ВыборкаЗоны.Зона);
		
		Выборка = ВыборкаЗоны.Выбрать();
		Пока Выборка.Следующий() Цикл 
			Запись = НаборЗаписей.Добавить();
			Запись.ПравилаДоступности = Выборка.Правила;
			Запись.ГеографическаяЗона = Выборка.Зона;
		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;	
	
КонецПроцедуры // ЗаписатьПравилаДоступности()

#КонецОбласти 

#КонецОбласти

#КонецЕсли


