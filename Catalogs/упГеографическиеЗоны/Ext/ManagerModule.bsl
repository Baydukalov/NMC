#Если Сервер Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет поиск зоны в которую поподает координаты.
//
// Параметры:
//  мсвАдресов - Массив - Долгота, Широта - Координата широты.
//
// Возвращаемое значение:
//   СправочникСсылка.упГеографическиеЗоны - ссылка на географическую зону.
//
Функция УстановитьГеографическуюЗонуАдресам(мсвАдресов) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().УстановитьГеографическуюЗонуАдресам(мсвАдресов);
	
КонецФункции // УстановитьГеографическуюЗонуАдресам()

// Выполняет поиск зоны в которую поподает координаты.
//
// Параметры:
//  Долгота - Число - Координата долготы.
//  Широта - Число - Координата широты.
//
// Возвращаемое значение:
//   СправочникСсылка.упГеографическиеЗоны  - ссылка на географическую зону.
//
Функция ВернутьГеографическуюЗону(Долгота, Широта) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ВернутьГеографическуюЗону(Долгота, Широта);
	
КонецФункции // ВернутьГеографическуюЗону()

// Выполняет поиск зоны по переданным параметрам.
//
// Параметры:
//  тбзДолготаШиротаПериод - ТаблизаЗначений - Таблица данных.
//  Зона - СправочникСсылка - Ссылка на элемент.
//  ВидСобытия - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   Произвольное - Результат выполнения.
//
Функция ВернутьГеографическиеЗоныТочекСобытия(тбзДолготаШиротаПериод, Зона, ВидСобытия) Экспорт
	
	Возврат упСЛК.СоздатьОбъект().ВернутьГеографическиеЗоныТочекСобытия(тбзДолготаШиротаПериод, Зона, ВидСобытия);
	
КонецФункции // ВернутьГеографическуюЗону()

// Не используется.
// Функция возвращает приведенное время в пути между точками с учетом коэффициентов времени.
//
// Параметры:
//  ТаблицаТочек - ТаблицаЗначений - Таблица даных с колонками:
//     * Номер   - Число - Порядок прохождения точки.
//     * Широта  - Число - Координата широты.
//     * Долгота - Число - Координата Долготы.
//     * Время   - Число - Время от предыдущей точки в секундах.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция _ВернутьВремяВПутиПоДнямНедели(ТаблицаТочек) Экспорт
	
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Время"           , 0);
	сткВозврат.Вставить("ВремяПонедельник", 0);
	сткВозврат.Вставить("ВремяВторник"    , 0);
	сткВозврат.Вставить("ВремяСреда"      , 0);
	сткВозврат.Вставить("ВремяЧетверг"    , 0);
	сткВозврат.Вставить("ВремяПятница"    , 0);
	сткВозврат.Вставить("ВремяСуббота"    , 0);
	сткВозврат.Вставить("ВремяВоскресенье", 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаТочек", ТаблицаТочек);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТочек.Номер КАК Точка,
	|	ТаблицаТочек.Широта КАК У,
	|	ТаблицаТочек.Долгота КАК Х,
	|	ТаблицаТочек.Время
	|ПОМЕСТИТЬ втТочки
	|ИЗ
	|	&ТаблицаТочек КАК ТаблицаТочек
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упКоэффициентыВремени.Ссылка КАК Зона,
	|	упКоэффициентыВремени.ДеньНедели,
	|	упКоэффициентыВремени.КоэффициентРасчетаВремениВПути КАК Коэффициент
	|ПОМЕСТИТЬ втКоэффициенты
	|ИЗ
	|	Справочник.упГеографическиеЗоны.КоэффициентыВремени КАК упКоэффициентыВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упГеографическиеЗоны.Ссылка КАК Зона,
	|	упГеографическиеЗоны.МинШирота КАК Широта_Мин,
	|	упГеографическиеЗоны.МинДолгота КАК Долгота_Мин,
	|	упГеографическиеЗоны.МаксШирота КАК Широта_Макс,
	|	упГеографическиеЗоны.МаксДолгота КАК Долгота_Макс,
	|	упГеографическиеЗоны.ПроходитЧерез180Меридиан КАК Меридиан_180
	|ПОМЕСТИТЬ втПолигонПараметры
	|ИЗ
	|	Справочник.упГеографическиеЗоны КАК упГеографическиеЗоны
	|ГДЕ
	|	упГеографическиеЗоны.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				втКоэффициенты.Зона
	|			ИЗ
	|				втКоэффициенты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Точки.Точка КАК Точка,
	|	ВЫБОР
	|		КОГДА втПолигонПараметры.Меридиан_180
	|			ТОГДА ВЫБОР
	|					КОГДА Точки.Х < 0
	|						ТОГДА 360 + Точки.Х
	|					ИНАЧЕ Точки.Х
	|				КОНЕЦ
	|		ИНАЧЕ Точки.Х
	|	КОНЕЦ КАК Х,
	|	Точки.У КАК У,
	|	втПолигонПараметры.Меридиан_180 КАК Меридиан_180,
	|	втПолигонПараметры.Зона КАК Зона
	|ПОМЕСТИТЬ втСоответствиеПолигоновТочке
	|ИЗ
	|	втПолигонПараметры КАК втПолигонПараметры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТочки КАК Точки
	|		ПО (ВЫБОР
	|				КОГДА втПолигонПараметры.Меридиан_180
	|					ТОГДА ВЫБОР
	|							КОГДА Точки.Х < 0
	|								ТОГДА 360 + Точки.Х
	|							ИНАЧЕ Точки.Х
	|						КОНЕЦ
	|				ИНАЧЕ Точки.Х
	|			КОНЕЦ МЕЖДУ втПолигонПараметры.Долгота_Мин И втПолигонПараметры.Долгота_Макс)
	|			И (Точки.У МЕЖДУ втПолигонПараметры.Широта_Мин И втПолигонПараметры.Широта_Макс)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСоответствиеПолигоновТочке.Точка КАК Точка,
	|	втСоответствиеПолигоновТочке.Х КАК Точка_Х,
	|	втСоответствиеПолигоновТочке.У КАК Точка_У,
	|	втСоответствиеПолигоновТочке.Зона КАК Зона,
	|	упГеографическиеЗоныПолигон.НомерСтроки КАК НомерСтроки,
	|	упГеографическиеЗоныПолигон.Широта КАК У,
	|	ВЫБОР
	|		КОГДА втСоответствиеПолигоновТочке.Меридиан_180
	|			ТОГДА ВЫБОР
	|					КОГДА упГеографическиеЗоныПолигон.Долгота < 0
	|						ТОГДА 360 + упГеографическиеЗоныПолигон.Долгота
	|					ИНАЧЕ упГеографическиеЗоныПолигон.Долгота
	|				КОНЕЦ
	|		ИНАЧЕ упГеографическиеЗоныПолигон.Долгота
	|	КОНЕЦ КАК Х,
	|	втСоответствиеПолигоновТочке.Меридиан_180 КАК Меридиан_180
	|ПОМЕСТИТЬ втПолигоны
	|ИЗ
	|	втСоответствиеПолигоновТочке КАК втСоответствиеПолигоновТочке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упГеографическиеЗоны.Полигон КАК упГеографическиеЗоныПолигон
	|		ПО втСоответствиеПолигоновТочке.Зона = упГеографическиеЗоныПолигон.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПолигоны.Зона КАК Зона,
	|	втПолигоны.НомерСтроки КАК НомерТочкиПолигона,
	|	втПолигоны.Точка КАК Точка,
	|	втПолигоны.Точка_Х КАК Точка_Х,
	|	втПолигоны.Точка_У КАК Точка_У,
	|	втПолигоны.Х КАК Полигон_Х,
	|	втПолигоны.У КАК Полигон_У,
	|	втПолигоны.Х - втПолигоны.Точка_Х КАК Разница_Х,
	|	втПолигоны.У - втПолигоны.Точка_У КАК Разница_У,
	|	ВЫБОР
	|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х < 0
	|				И втПолигоны.У - втПолигоны.Точка_У < 0
	|			ТОГДА 2
	|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х < 0
	|				И втПолигоны.У - втПолигоны.Точка_У >= 0
	|			ТОГДА 1
	|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х >= 0
	|				И втПолигоны.У - втПолигоны.Точка_У < 0
	|			ТОГДА 3
	|		КОГДА втПолигоны.Х - втПолигоны.Точка_Х >= 0
	|				И втПолигоны.У - втПолигоны.Точка_У >= 0
	|			ТОГДА 0
	|	КОНЕЦ КАК К,
	|	втРазмеры.Размер КАК Размер
	|ПОМЕСТИТЬ Таб
	|ИЗ
	|	втПолигоны КАК втПолигоны
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			втПолигоны.Зона КАК Зона,
	|			КОЛИЧЕСТВО(втПолигоны.НомерСтроки) КАК Размер
	|		ИЗ
	|			втПолигоны КАК втПолигоны
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втПолигоны.Зона) КАК втРазмеры
	|		ПО втПолигоны.Зона = втРазмеры.Зона
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Зона,
	|	Точка,
	|	НомерТочкиПолигона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.Зона,
	|	Таб.Точка,
	|	Таб.Точка_Х,
	|	Таб.Точка_У,
	|	Таб.НомерТочкиПолигона,
	|	Таб.Полигон_Х,
	|	Таб.Полигон_У,
	|	Таб.К КАК К,
	|	ТабПред.К КАК ПредК,
	|	ВЫБОР
	|		КОГДА Таб.К - ТабПред.К = -3
	|			ТОГДА 1
	|		КОГДА Таб.К - ТабПред.К = 3
	|			ТОГДА -1
	|		КОГДА Таб.К - ТабПред.К = -2
	|				И ТабПред.Разница_Х * Таб.Разница_У >= ТабПред.Разница_У * Таб.Разница_Х
	|			ТОГДА 1
	|		КОГДА Таб.К - ТабПред.К = 2
	|				И НЕ ТабПред.Разница_Х * Таб.Разница_У >= ТабПред.Разница_У * Таб.Разница_Х
	|			ТОГДА -1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Результат
	|ПОМЕСТИТЬ Развернуто
	|ИЗ
	|	Таб КАК Таб
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Таб КАК ТабПред
	|		ПО Таб.Точка = ТабПред.Точка
	|			И Таб.Зона = ТабПред.Зона
	|			И Таб.Точка_Х = ТабПред.Точка_Х
	|			И Таб.Точка_У = ТабПред.Точка_У
	|			И (Таб.НомерТочкиПолигона = ТабПред.НомерТочкиПолигона + 1
	|				ИЛИ Таб.НомерТочкиПолигона = 1
	|					И ТабПред.НомерТочкиПолигона = Таб.Размер)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Развернуто.Точка КАК Точка,
	|	Развернуто.Зона КАК Зона,
	|	СУММА(Развернуто.Результат) КАК Результат
	|ПОМЕСТИТЬ втЗоны
	|ИЗ
	|	Развернуто КАК Развернуто
	|
	|СГРУППИРОВАТЬ ПО
	|	Развернуто.Зона,
	|	Развернуто.Точка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗоны.Точка КАК Точка,
	|	втЗоны.Зона КАК Зона,
	|	упИерархияГеографическихЗон.Уровень КАК Уровень
	|ПОМЕСТИТЬ втПодходящиеЗоны
	|ИЗ
	|	втЗоны КАК втЗоны
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упИерархияГеографическихЗон КАК упИерархияГеографическихЗон
	|		ПО втЗоны.Зона = упИерархияГеографическихЗон.Зона
	|			И втЗоны.Зона = упИерархияГеографическихЗон.Родитель
	|ГДЕ
	|	втЗоны.Результат <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПодходящиеЗоны.Точка,
	|	втКоэффициенты.ДеньНедели,
	|	втКоэффициенты.Коэффициент
	|ПОМЕСТИТЬ втПодходящиеКоэффициенты
	|ИЗ
	|	втПодходящиеЗоны КАК втПодходящиеЗоны
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			втПодходящиеЗоны.Точка КАК Точка,
	|			МАКСИМУМ(втПодходящиеЗоны.Уровень) КАК Уровень
	|		ИЗ
	|			втПодходящиеЗоны КАК втПодходящиеЗоны
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втПодходящиеЗоны.Точка) КАК втПодходящийУровень
	|		ПО втПодходящиеЗоны.Точка = втПодходящийУровень.Точка
	|			И втПодходящиеЗоны.Уровень = втПодходящийУровень.Уровень
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКоэффициенты КАК втКоэффициенты
	|		ПО втПодходящиеЗоны.Зона = втКоэффициенты.Зона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТочки.Точка,
	|	втТочки.Время,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 1
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяПонедельник,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 2
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяВторник,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 3
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяСреда,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 4
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяЧетверг,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 5
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяПятница,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 6
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяСуббота,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(втКоэффициенты.ДеньНедели, 0) = 7
	|				ТОГДА втТочки.Время * втКоэффициенты.Коэффициент
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВремяВоскресенье
	|ПОМЕСТИТЬ втВремяПоТочкам
	|ИЗ
	|	втТочки КАК втТочки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПодходящиеКоэффициенты КАК втКоэффициенты
	|		ПО втТочки.Точка = втКоэффициенты.Точка
	|
	|СГРУППИРОВАТЬ ПО
	|	втТочки.Точка,
	|	втТочки.Время
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втВремяПоТочкам.Время) КАК Время,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяПонедельник = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяПонедельник
	|		КОНЕЦ) КАК ВремяПонедельник,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяВторник = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяВторник
	|		КОНЕЦ) КАК ВремяВторник,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяСреда = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяСреда
	|		КОНЕЦ) КАК ВремяСреда,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяЧетверг = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяЧетверг
	|		КОНЕЦ) КАК ВремяЧетверг,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяПятница = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяПятница
	|		КОНЕЦ) КАК ВремяПятница,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяСуббота = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяСуббота
	|		КОНЕЦ) КАК ВремяСуббота,
	|	СУММА(ВЫБОР
	|			КОГДА втВремяПоТочкам.ВремяВоскресенье = 0
	|				ТОГДА втВремяПоТочкам.Время
	|			ИНАЧЕ втВремяПоТочкам.ВремяВоскресенье
	|		КОНЕЦ) КАК ВремяВоскресенье
	|ИЗ
	|	втВремяПоТочкам КАК втВремяПоТочкам";  
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(сткВозврат, Выборка);
	КонецЕсли; 
	
	Возврат сткВозврат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
