#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	сткНадписи = СтруктураНадписейНорм(Объект.ТипНормыРасходаГСМ);
	НадписьИзменениеРасхода = сткНадписи.ИзменениеРасхода;
	НадписьРасход		    = сткНадписи.Расход;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов.
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов.
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипНормыПриИзменении(Элемент)
	
	сткНадписи = СтруктураНадписейНорм(Объект.ТипНормыРасходаГСМ);
	НадписьИзменениеРасхода = сткНадписи.ИзменениеРасхода;
	НадписьРасход		    = сткНадписи.Расход;

	Если Объект.ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаОдинЧасРаботы") Тогда
		Объект.ИзменениеРасхода = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормулаРасчетаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ФормулаРасчета) Тогда
		Объект.ШкалаТемпературныхКоэффициентов = ПредопределенноеЗначение("Справочник.упОценочнаяШкала.ПустаяСсылка");
		Элементы.ШкалаТемпературныхКоэффициентов.Доступность = Ложь;
		Элементы.ШкалаТемпературныхКоэффициентов.ПодсказкаВвода = Нстр("ru = 'Шкала задается в формуле расчета'");
	Иначе
		Элементы.ШкалаТемпературныхКоэффициентов.ПодсказкаВвода = "";
		Элементы.ШкалаТемпературныхКоэффициентов.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураНадписейНорм(ТипНормыРасходаГСМ)
	
	сткРезультат = Новый Структура("Расход, ИзменениеРасхода","","");
	
	сткРезультат.Расход = СтрШаблон("(%1)", ТипНормыРасходаГСМ);
	
	Если ТипНормыРасходаГСМ = ПредопределенноеЗначение("Перечисление.упТипыНормРасходаГСМ.НаСтоКилометров") Тогда
		сткРезультат.ИзменениеРасхода = Нстр("ru = '(на 100 тонно-километров)'");
	КонецЕсли;
	
	Возврат сткРезультат;
	
КонецФункции

#КонецОбласти
