
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
		
	//// СтандартныеПодсистемы.Свойства
	//УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	//// Конец СтандартныеПодсистемы.Свойства	
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	//// СтандартныеПодсистемы.ВерсионированиеОбъектов
	//ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	//// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Элементы.КонтролироватьНарушениеВременныхОкон.Доступность = Ложь;
	Элементы.ГруппаКонтролировать.Доступность = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ВариантОптимизацииМаршрута = Перечисления.упВариантыОптимизацииМаршрута.ПоВремени;
	Иначе		
		Если Объект.КонтролироватьНарушениеВременныхОкон Тогда
			Элементы.ГруппаКонтролировать.Доступность = Истина;
		Иначе
			Элементы.ГруппаКонтролировать.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВариантОптимизацииМаршрута = Перечисления.упВариантыОптимизацииМаршрута.ПоВремени Тогда
		Элементы.КонтролироватьНарушениеВременныхОкон.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	//// Обработчик подсистемы запрета редактирования реквизитов объектов
	//ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
		
	//// СтандартныеПодсистемы.Свойства
	//ДополнительныеПараметры = Новый Структура;
	//ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	//УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	//// Конец СтандартныеПодсистемы.Свойства
	
	Если ЗначениеЗаполнено(Объект.МетодРешенияЗК) Тогда
		МетодРешенияЗК = Объект.МетодРешенияЗК;
	ИначеЕсли ЗначениеЗаполнено(Объект.АлгоритмОптимизации) Тогда
		МетодРешенияЗК = Объект.АлгоритмОптимизации;
	КонецЕсли;
	ПриСозданииПриЧтенииНаСервере();
	
	ОбработатьИзменениеМетодаОптимизации(Ложь);
	
	ЗаполнитьСписокВыбораМетодовРешенияЗК();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(МетодРешенияЗК) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан метод решения задачи коммивояжера",, "МетодРешенияЗК",, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПространственныйВесПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Ложь, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВременнойВесПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Истина, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВесСрочностиПриИзменении(Элемент)
	
	ПривестиСуммуВесовКЕдинице(Истина, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролироватьНарушениеВременныхОконПриИзменении(Элемент)
	
	Если Объект.КонтролироватьНарушениеВременныхОкон Тогда
		Элементы.ГруппаКонтролировать.Доступность = Истина;
	Иначе
		Элементы.ГруппаКонтролировать.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОптимизацииМаршрутаПриИзменении(Элемент)
	
	Если Объект.ВариантОптимизацииМаршрута = ПредопределенноеЗначение("Перечисление.упВариантыОптимизацииМаршрута.ПоВремени") Тогда
		Элементы.КонтролироватьНарушениеВременныхОкон.Доступность = Истина;
	Иначе
		Элементы.КонтролироватьНарушениеВременныхОкон.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
КонецПроцедуры
	
	&НаКлиенте
Процедура МетодРешенияЗКПриИзменении(Элемент)
	
	Если МетодРешенияЗК = "Поставляемый" Тогда
		сткПараметры = Новый Структура;
		сткПараметры.Вставить("Отбор", Новый Структура("ВидЗадачиОптимизации", ПредопределенноеЗначение("Перечисление.упВидыЗадачОптимизации.ЗадачаКоммивояжера")));
		ОткрытьФорму("Справочник.упАлгоритмыОптимизации.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("АлгоритмыОптимизацииЗКВыборЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОбработатьИзменениеМетодаОптимизации();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	// Нет.
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ПривестиСуммуВесовКЕдинице(ИзменятьПространственныйВес, ИзменятьВременнойВес, ИзменятьВесСрочности)
	
	СуммаВесов = Объект.ПространственныйВес + Объект.ВременнойВес + Объект.ВесСрочности;
	Если СуммаВесов > 1 Тогда
		СуммаУменьшить = СуммаВесов - 1;
		Если Не ИзменятьПространственныйВес Тогда
			УменьшитьВес(СуммаУменьшить, Объект.ВременнойВес); 
			УменьшитьВес(СуммаУменьшить, Объект.ВесСрочности);
		КонецЕсли;
		
		Если Не ИзменятьВременнойВес Тогда
			УменьшитьВес(СуммаУменьшить, Объект.ВесСрочности);
			УменьшитьВес(СуммаУменьшить, Объект.ПространственныйВес); 
		КонецЕсли;
		
		Если Не ИзменятьВесСрочности Тогда
			УменьшитьВес(СуммаУменьшить, Объект.ПространственныйВес); 
			УменьшитьВес(СуммаУменьшить, Объект.ВременнойВес);
		КонецЕсли;
		
	ИначеЕсли СуммаВесов < 1 Тогда
		СуммаУвеличить = 1 - СуммаВесов;
		Если Не ИзменятьПространственныйВес Тогда
			УвеличитьВес(СуммаУвеличить, Объект.ВременнойВес); 
			УвеличитьВес(СуммаУвеличить, Объект.ВесСрочности);
		КонецЕсли;
		
		Если Не ИзменятьВременнойВес Тогда
			УвеличитьВес(СуммаУвеличить, Объект.ВесСрочности);
			УвеличитьВес(СуммаУвеличить, Объект.ПространственныйВес); 
		КонецЕсли;
		
		Если Не ИзменятьВесСрочности Тогда
			УвеличитьВес(СуммаУвеличить, Объект.ПространственныйВес); 
			УвеличитьВес(СуммаУвеличить, Объект.ВременнойВес);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПривестиСуммуВесовКЕдинице()

&НаКлиенте 
Процедура УвеличитьВес(Сумма, Вес)
	
	текУвеличение = Мин(Сумма, 1 - Вес);
	Вес = Вес + текУвеличение;
	Сумма = Сумма - текУвеличение;
	
КонецПроцедуры // УвеличитьВес()

&НаКлиенте 
Процедура УменьшитьВес(Сумма, Вес)
	
	текУменьшение = Мин(Сумма, Вес);
	Вес = Вес - текУменьшение;
	Сумма = Сумма - текУменьшение;
	
КонецПроцедуры // Уменьшитьвес()

&НаКлиенте
Процедура АлгоритмыОптимизацииЗКВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭлементУжеВСписке = Ложь;
		
		СписокВыбораМетодаЗК = Элементы.МетодРешенияЗК.СписокВыбора;
		Для каждого текЗначение Из СписокВыбораМетодаЗК Цикл
			Если Результат = текЗначение.Значение Тогда
			    ЭлементУжеВСписке = Истина;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если Не ЭлементУжеВСписке И СписокВыбораМетодаЗК.Количество() < 10 Тогда
			СписокВыбораМетодаЗК.Вставить(0, Результат, ВернутьПредставлениеПоставляемогоАлгоритма(Результат));
		КонецЕсли; 
	    МетодРешенияЗК = Результат;
	Иначе	
		МетодРешенияЗК = Неопределено;
	КонецЕсли;
	ОбработатьИзменениеМетодаОптимизации();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораМетодовРешенияЗК()
	
	СписокВыбораМетодаЗК = Элементы.МетодРешенияЗК.СписокВыбора;
	СписокВыбораМетодаЗК.Очистить();
	Если ТипЗнч(МетодРешенияЗК) = Тип("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		СписокВыбораМетодаЗК.Добавить(МетодРешенияЗК, ВернутьПредставлениеПоставляемогоАлгоритма(МетодРешенияЗК));	
	КонецЕсли; 
	СписокВыбораМетодаЗК.Добавить(Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритм, НСтр("ru = 'Жадный алгоритм'"));
	СписокВыбораМетодаЗК.Добавить(Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон, НСтр("ru = 'Жадный алгоритм с учетом временных окон'"));
	СписокВыбораМетодаЗК.Добавить(Перечисления.упМетодыРешенияЗК.МетодВетвейИГраниц, НСтр("ru = 'Метод ветвей и границ'"));
	СписокВыбораМетодаЗК.Добавить("Поставляемый", НСтр("ru = 'Выбрать поставляемый...'"));
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеМетодаОптимизации(ИзменитьДанные = Истина)

	Если МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритмСУчетомВременныхОкон Тогда
		ВариантОптимизацииМаршрута = Перечисления.упВариантыОптимизацииМаршрута.ПоВремени;
		Элементы.ВариантОптимизацииМаршрута.Доступность = Ложь;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗКВО;
	ИначеЕсли (МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ЖадныйАлгоритм Или МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.МетодВетвейИГраниц) Тогда
		Элементы.ВариантОптимизацииМаршрута.Доступность = Истина;
		Элементы.СтраницыНастройкиЗК.ТекущаяСтраница = Элементы.СтраницаЗК;
	КонецЕсли;
	
	Если ИзменитьДанные Тогда
	Объект.МетодРешенияЗК = Перечисления.упМетодыРешенияЗК.ПустаяСсылка();
	Объект.АлгоритмОптимизации = Справочники.упАлгоритмыОптимизации.ПустаяСсылка();
	Если ТипЗнч(МетодРешенияЗК) = ТИП("СправочникСсылка.упАлгоритмыОптимизации") Тогда
		Объект.АлгоритмОптимизации = МетодРешенияЗК;
	ИначеЕсли ТипЗнч(МетодРешенияЗК) = ТИП("ПеречислениеСсылка.упМетодыРешенияЗК") Тогда
		Объект.МетодРешенияЗК = МетодРешенияЗК;
	КонецЕсли;
	Модифицированность = Истина;
	КонецЕсли;
		
КонецПроцедуры // ОбработатьИзменениеМетодаОптимизации()

&НаСервереБезКонтекста
Функция ВернутьПредставлениеПоставляемогоАлгоритма(АлгоритмОптимизации)
	
	сткЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(АлгоритмОптимизации, "Наименование, Версия");
	Если ЗначениеЗаполнено(сткЗначенияРеквизитов.Версия) Тогда
		ПредставлениеАлгоритма = НСтр(СтрШаблон("ru = 'Поставляемый: %1, вер. %2'", сткЗначенияРеквизитов.Наименование, сткЗначенияРеквизитов.Версия));
	Иначе
		ПредставлениеАлгоритма = НСтр(СтрШаблон("ru = 'Поставляемый: %1'", сткЗначенияРеквизитов.Наименование));
	КонецЕсли;
	
	Возврат ПредставлениеАлгоритма;

КонецФункции







#КонецОбласти