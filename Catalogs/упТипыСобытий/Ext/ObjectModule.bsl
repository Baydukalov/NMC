
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ПометкаУдаления Тогда 
		Активно = Ложь;
	ИначеЕсли Ссылка.ПометкаУдаления Тогда 
		Активно = Истина;
	КонецЕсли;		
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	Если АлгоритмОбработки <> Перечисления.упАлгоритмыОбработкиСобытия.ПроизвольныйАлгоритм Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПроизвольныйАлгоритмОбработки");
	КонецЕсли;

	Если АлгоритмОбработки <> Перечисления.упАлгоритмыОбработкиСобытия.НапоминаниеПользователю Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Пользователь");
	КонецЕсли;

	Если Истина
		И АлгоритмОбработки <> Перечисления.упАлгоритмыОбработкиСобытия.ЗадачаПользователю
		И АлгоритмОбработки <> Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗадачуНаПодтверждениеАдреса
		И АлгоритмОбработки <> Перечисления.упАлгоритмыОбработкиСобытия.СоздатьЗадачуНаСогласованиеИзмененийМаршрута
	Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Исполнитель");
	КонецЕсли;
	
	Если АлгоритмОбработки <> Перечисления.упАлгоритмыОбработкиСобытия.ОповеститьПользователя Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ШаблонПисьмаСсылка");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ШаблонПисьма");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Получатели");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ВидПолучателей");
	КонецЕсли;
	
	Если НЕ АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОтправитьSMS Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ТекстSMS");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПолучателиSMS");
	КонецЕсли;
		
	Если АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ПроизвольныйАлгоритм 
		ИЛИ АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОповеститьПользователя Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "РасчетПлановогоВремениИсполненияЗадач");
	КонецЕсли;	
	
	Если АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОповеститьПользователя Тогда 
		Если ВидШаблонаПисьма = 0 тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ШаблонПисьмаСсылка");
		Иначе
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ШаблонПисьма");
		КонецЕсли;
		Если НЕ (ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Пользователь
			ИЛИ ВидПолучателей = Перечисления.упВидыПолучателейСообщений.ГруппаПользователей
			ИЛИ ВидПолучателей = Перечисления.упВидыПолучателейСообщений.Строка) тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Получатели");
		КонецЕсли;
	КонецЕсли;
	
	Если АлгоритмОбработки = Перечисления.упАлгоритмыОбработкиСобытия.ОптимизироватьМаршрут Тогда 
		ПроверяемыеРеквизиты.Добавить("НастройкаОптимизацииМаршрута");
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#КонецЕсли