
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	СхемаКомпоновкиДанных = ТекущийОбъект.СхемаКомпоновкиДанных.Получить();
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
			
	// Переданный в параметре адрес сохраняется в качестве адреса исходной схемы
	Если Не ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда 
		СхемаКомпоновкиДанных = ПолучитьСКД();
		Если СхемаКомпоновкиДанных <> Неопределено Тогда 
			АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
		КонецЕсли;
	Иначе	
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	КонецЕсли;	
	
	Если СхемаКомпоновкиДанных <> Неопределено Тогда 
		Попытка
			КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
			КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
			КомпоновщикНастроек.Восстановить();
		Исключение
			СхемаКомпоновкиДанныхАктуальная = ПолучитьСКД();
			Если СхемаКомпоновкиДанныхАктуальная <> Неопределено Тогда 
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Отбор, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Отбор);
				упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию.Порядок, СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Порядок);
				АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхАктуальная, УникальныйИдентификатор);
				
				КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
				КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанныхАктуальная.НастройкиПоУмолчанию);
				КомпоновщикНастроек.Восстановить();
			КонецЕсли;
			Модифицированность = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если СхемаКомпоновкиДанных = Неопределено Тогда 
		Элементы.ГруппаНастройкаСКД.Доступность = Ложь;
	КонецЕсли;
	
	//ОбновитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	КомпоновщикНастроек.Восстановить();
	
	Если ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		
		упСервисныеФункцииВызовСервера.ОчиститьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		упСервисныеФункцииВызовСервера.СкопироватьНастройкиКомпоновкиДанных(СхемаКомпоновкиДанных.НастройкиПоУмолчанию, КомпоновщикНастроек.ПолучитьНастройки());
		
		ТекущийОбъект.СхемаКомпоновкиДанных = Новый ХранилищеЗначения(СхемаКомпоновкиДанных);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастройкаФормы(Истина);
	УстановитьДоступностьЭлементовВремени();
	ВидШаблонаПисьмаПриИзменении(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
		
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АлгоритмПроверкиНаСобытиеПриИзменении(Элемент)
	
	Если Объект.АлгоритмПроверкиНаСобытие <> ПредопределенноеЗначение("Перечисление.упАлгоритмыПроверкиСобытия.ПроизвольныйАлгоритм") Тогда 
    	ЗагрузитьСКД();
	КонецЕсли;
	НастройкаФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИсходногоДействияПриИзменении(Элемент)
	
	НастройкаФормы();
	
	Если Объект.АлгоритмПроверкиНаСобытие <> ПредопределенноеЗначение("Перечисление.упАлгоритмыПроверкиСобытия.ПроизвольныйАлгоритм") Тогда 
    	ЗагрузитьСКД();
	КонецЕсли;
	                        
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмОбработкиПриИзменении(Элемент)
	
	НастройкаФормы();
	Если (Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОповеститьПользователя")
		ИЛИ Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОтправитьЗаявкуНаТСПеревозчику")) И Не ЗначениеЗаполнено(Объект.ШаблонПисьма) Тогда 
		Объект.ШаблонПисьма = "Тема = """ + Объект.Наименование + """;
		|Текст = ""Добрый день. "" + Символы.ПС + Строка(ДокументОснование);";
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ПроизвольныйАлгоритм") И Не ЗначениеЗаполнено(Объект.ПроизвольныйАлгоритмОбработки) Тогда 
		Объект.ПроизвольныйАлгоритмОбработки = "//Успешно = Истина;
		|//
		|//НовыйДокумент = СоздатьДокументНаОсновании(ДокументОснование);
		|//Если НовыйДокумент <> Неопределено Тогда 
		|//    НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		|//	
		|//    ДокументРезультат = НовыйДокумент.Ссылка;
		|//Иначе
		|//    Успешно = Ложь;
		|//    ДокументРезультат = Неопределено;
		|//КонецЕсли;";
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ЗадачаПользователю") И Не ЗначениеЗаполнено(Объект.Описание) Тогда 
		Объект.Описание = "Отследить выполнение ";
	ИначеЕсли (Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОтправитьSMS")) И НЕ ЗначениеЗаполнено(Объект.ТекстSMS) Тогда
		Объект.ТекстSMS = "ТекстSMS = ""Водитель: "" + Символы.ПС + ""ТС: "" + Символы.ПС + Строка(ДокументОснование);";
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяОбработкиСобытияПриИзменении(Элемент)

	УстановитьДоступностьЭлементовВремени();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидШаблонаПисьмаПриИзменении(Элемент)
	
	Элементы.Группа4.ТекущаяСтраница = ?(Объект.ВидШаблонаПисьма = 0, Элементы.ШаблонПисьмаПроизвольныйАлгоритм, Элементы.ШаблонПисьмаПоШаблону);
	Элементы.Группа5.ТекущаяСтраница = ?(Объект.ВидШаблонаПисьма = 0, Элементы.ШаблонПисьмаПроизвольныйАлгоритм1, Элементы.ШаблонПисьмаПоШаблону1);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПолучателейПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовПоВидуПолучателей();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСКД()
	
	Если Ложь
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаПеревозку 
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗаявкиНаПеревозкуГруза
	Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ЗаявкаНаПеревозкуГруза");
		
	ИначеЕсли Ложь
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаРейса 
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьРейса
	Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("Рейс");
			
	ИначеЕсли Ложь
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаОперацийСКонтейнером
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьОперацийСКонтейнером
	Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ОперацииСКонтейнером");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьПодтвержденияЗаявкиНаТС Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ПодтверждениеЗаявкиНаТС");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьПрохождениеТочки Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ПрохождениеТочки");
		
	ИначеЕсли Ложь
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаданияНаПеревозку 
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗаданияНаПеревозкуГруза
	Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ЗаданиеНаПеревозкуГруза");
		
	ИначеЕсли Ложь
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьПутевогоЛиста 
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаПутевогоЛиста 
	Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ПутевойЛист");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗаявкиНаТС Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ЗаявкаНаТС");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьПредложенияПеревозчика Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ПредложениеПеревозчика");	
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьСобытияМониторинга Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("СобытияМониторинга");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеМаршрутаПоРейсу Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ИзменениеМаршрутаПоРейсу");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеРаботПоРейсу Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ИзменениеРаботПоРейсу");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеЭкипажаПоРейсу Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ИзменениеЭкипажаПоРейсу");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьФактическихРасходов Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ФактическиеРасходы");
		
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ПрисоединениеФайла Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ПрисоединенныйФайл");
		
	ИначеЕсли Ложь
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗапросаУсловийПеревозки
		Или Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ИзменениеСтатусаЗапросаУсловийПеревозки
	Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ЗапросУсловийПеревозки");
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗадачиНаОтправкуФайлов Тогда
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ЗадачаНаОтправкуФайлов");
	ИначеЕсли Объект.ТипИсходногоДействия = Перечисления.упТипыДействийСобытий.ЗаписьЗадачиИсполнителя Тогда 
		Возврат Справочники.упТипыСобытий.ПолучитьМакет("ЗадачаИсполнителя");
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьСКД()

&НаСервере
Процедура ЗагрузитьСКД()
	
	// Переданный в параметре адрес сохраняется в качестве адреса исходной схемы
	Если ЗначениеЗаполнено(АдресСхемыКомпоновкиДанных) Тогда 
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	Иначе
		СхемаКомпоновкиДанных = Неопределено;
	КонецЕсли;
		
	СхемаКомпоновкиДанныхНовая = ПолучитьСКД();

	Если СхемаКомпоновкиДанныхНовая = Неопределено Тогда 
		Элементы.ГруппаНастройкаСКД.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	Элементы.ГруппаНастройкаСКД.Доступность = Истина;
	
	Если СхемаКомпоновкиДанных <> Неопределено Тогда 
		упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию.Отбор, КомпоновщикНастроек.Настройки.Отбор);
		упСервисныеФункцииВызовСервера.СкопироватьЭлементы(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию.Порядок, КомпоновщикНастроек.Настройки.Порядок);
	КонецЕсли;
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанныхНовая, УникальныйИдентификатор);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанныхНовая.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФормы(Открытие = Ложь)
	
	Если Объект.АлгоритмПроверкиНаСобытие = ПредопределенноеЗначение("Перечисление.упАлгоритмыПроверкиСобытия.ПроизвольныйАлгоритм") Тогда 
		Элементы.ГруппаПроверка.ТекущаяСтраница = Элементы.ГруппаПроизвольныйАлгоритмПроверки;
	Иначе
		Элементы.ГруппаПроверка.ТекущаяСтраница = Элементы.ГруппаНастройкаСКД;
	КонецЕсли;
	
	Если Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ПроизвольныйАлгоритм") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_ПроизвольныйАлгоритм;
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.НапоминаниеПользователю") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_НапоминаниеПользователю;
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОповеститьПользователя") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_ОповеститьПользователя;
		УстановитьВидимостьЭлементовПоВидуПолучателей();
		Элементы.Группа4.ТекущаяСтраница = ?(Объект.ВидШаблонаПисьма = 0, Элементы.ШаблонПисьмаПроизвольныйАлгоритм, Элементы.ШаблонПисьмаПоШаблону);
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОтправитьSMS") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_ОтправитьSMS;
		
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОтправитьЗаявкуНаТСПеревозчику") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_ОтправитьЗаявкуНаТС;
		Элементы.Группа5.ТекущаяСтраница = ?(Объект.ВидШаблонаПисьма = 0, Элементы.ШаблонПисьмаПроизвольныйАлгоритм1, Элементы.ШаблонПисьмаПоШаблону1);
		
	ИначеЕсли Ложь
		Или Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ЗадачаПользователю")
		Или Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.СоздатьЗадачуНаПодтверждениеАдреса")
		Или Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.СоздатьЗадачуНаСогласованиеИзмененийМаршрута") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_СоздатьЗадачуПользователю;
	ИначеЕсли Объект.АлгоритмОбработки = ПредопределенноеЗначение("Перечисление.упАлгоритмыОбработкиСобытия.ОптимизироватьМаршрут") Тогда 
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_ОптимизироватьМаршрут;
	Иначе
		Элементы.ГруппаДанныеАлгоритмаОбработки.ТекущаяСтраница = Элементы.ГруппаДанныеАлгоритмаОбработки_Пустая;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовВремени()

	Если Объект.ВремяОбработкиСобытия = ПредопределенноеЗначение("Перечисление.упВремяОбработкиСобытия.СоздатьСЗадержкой") Тогда 
		Элементы.Задержка.Доступность    = Истина;
		Элементы.Декорация10.Доступность = Истина;
	Иначе
		Элементы.Задержка.Доступность    = Ложь;
		Элементы.Декорация10.Доступность = Ложь;
	КонецЕсли;

	Если Объект.ВремяОбработкиСобытия = ПредопределенноеЗначение("Перечисление.упВремяОбработкиСобытия.ВОпределенноеВремя") Тогда 
		Элементы.ВремяСозданияЗадачи.Доступность = Истина;
	Иначе
		Элементы.ВремяСозданияЗадачи.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементовПоВидуПолучателей();
	
	ВидимостьПолучателей = Объект.ВидПолучателей = ПредопределенноеЗначение("Перечисление.упВидыПолучателейСообщений.Пользователь")
	                                                Или Объект.ВидПолучателей = ПредопределенноеЗначение("Перечисление.упВидыПолучателейСообщений.ГруппаПользователей")
	                                                Или Объект.ВидПолучателей = ПредопределенноеЗначение("Перечисление.упВидыПолучателейСообщений.Строка");
	
	Элементы.Получатели.Видимость = ВидимостьПолучателей;
	Если ВидимостьПолучателей тогда
		Если Объект.ВидПолучателей = ПредопределенноеЗначение("Перечисление.упВидыПолучателейСообщений.Пользователь") тогда
			Элементы.Получатели.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		ИначеЕсли Объект.ВидПолучателей = ПредопределенноеЗначение("Перечисление.упВидыПолучателейСообщений.ГруппаПользователей") тогда
			Элементы.Получатели.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ГруппыПользователей");
		Иначе
			Элементы.Получатели.ОграничениеТипа = Новый ОписаниеТипов("Строка");
		КонецЕсли;
		Значение = Объект.Получатели;
		Объект.Получатели = Элементы.Получатели.ОграничениеТипа.ПривестиЗначение(Значение);
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти 
