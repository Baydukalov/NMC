
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Штрихкод) Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	упСкладскиеЯчейки.Ссылка,
		|	упСкладскиеЯчейки.Код
		|ИЗ
		|	Справочник.упСкладскиеЯчейки КАК упСкладскиеЯчейки
		|ГДЕ
		|	упСкладскиеЯчейки.Ссылка <> &Ссылка
		|	И упСкладскиеЯчейки.Штрихкод = &Штрихкод";
		Запрос.УстановитьПараметр("Ссылка",   Ссылка);
		Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Данный штрихкод указан в другой ячейке: %1.'"), Выборка.Код), Ссылка, "Объект.Штрихкод",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Код      = СокрЛП(Код);
	Штрихкод = СокрЛП(Штрихкод);
	
	Если Не ЗначениеЗаполнено(Код) Или Не ЗначениеЗаполнено(Штрихкод) Тогда
		АдресЯчейки = СформироватьАдресЯчейки(Проезд, Стеллаж, Ярус, Позиция);
		Код 	 = ?(ЗначениеЗаполнено(Код), Код, АдресЯчейки.Код);
		Штрихкод = ?(ЗначениеЗаполнено(Штрихкод), Штрихкод, АдресЯчейки.Штрихкод);
	КонецЕсли;	
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьАдресЯчейки(Проезд, Стеллаж, Ярус, Позиция)
	
	Префикс 	  = "-";
	НовыйКод      = Префикс;
	НовыйШтрихкод = Префикс;
	
	Если ЗначениеЗаполнено(Проезд) Тогда 
		НовыйКод       = НовыйКод + Проезд;
		НовыйШтрихкод  = НовыйШтрихкод + Проезд;
		Если ЗначениеЗаполнено(Стеллаж) ИЛИ ЗначениеЗаполнено(Ярус) ИЛИ ЗначениеЗаполнено(Позиция) Тогда 
			НовыйКод = НовыйКод + "-";
		КонецЕсли;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Стеллаж) Тогда 
		НовыйКод       = НовыйКод + Стеллаж;
		НовыйШтрихкод  = НовыйШтрихкод + Стеллаж;
		Если ЗначениеЗаполнено(Ярус) ИЛИ ЗначениеЗаполнено(Позиция) Тогда 
			НовыйКод = НовыйКод + "-";
		КонецЕсли;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ярус) Тогда 
		НовыйКод       = НовыйКод + Ярус;
		НовыйШтрихкод  = НовыйШтрихкод + Ярус;
		Если ЗначениеЗаполнено(Позиция) Тогда 
			НовыйКод = НовыйКод + "-";
		КонецЕсли;	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Позиция) Тогда 
			НовыйКод       = НовыйКод + Позиция;
			НовыйШтрихкод  = НовыйШтрихкод + Позиция;
	КонецЕсли;
		
	АдресЯчейки = Новый Структура;
	АдресЯчейки.Вставить("Код", НовыйКод);
	АдресЯчейки.Вставить("Штрихкод", НовыйШтрихкод);
	Возврат АдресЯчейки;
	
КонецФункции // СформироватьАдресЯчейки()

#КонецОбласти

#КонецЕсли