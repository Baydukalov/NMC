#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Соответствие = Справочники.упШаблоныПакетовПечатныхФорм.ПолучитьВидыДокументов();
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ПустаяСтрока(Объект.ВидДокумента) Тогда
		Для каждого ТекущийЭлементКоллекции Из Соответствие Цикл
			Объект.ВидДокумента = ТекущийЭлементКоллекции.Ключ;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	МассивЗначений = Новый Массив;
	Для каждого ТекущийЭлементКоллекции Из Соответствие Цикл
		МассивЗначений.Добавить(ТекущийЭлементКоллекции.Ключ);
	КонецЦикла;
	Элементы.ВидДокумента.СписокВыбора.ЗагрузитьЗначения(МассивЗначений);
	Элементы.ВидДокумента.РежимВыбораИзСписка = Истина;
	Элементы.ВидДокумента.РедактированиеТекста = Ложь;
	
	ВидДокумента = Объект.ВидДокумента;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
		
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидДокументаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ВидДокумента = Объект.ВидДокумента;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	Если ВидДокумента = Объект.ВидДокумента Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.МакетыПФ.Количество() Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		ПараметрыВопроса = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаВидДокумента", ЭтаФорма,ПараметрыВопроса);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Табличная часть содержащая макеты печатных форм будет очищена, продолжить?';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
	Иначе
		ЗаполнитьМакетыПечатныхФорм();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура МакетыПФПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Элементы.МакетыПФ.ТолькоПросмотр Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Шаблоны пакетов печатных форм...'"),, НСтр("ru = 'Запрещено редактирование реквизитов объекта.'"), БиблиотекаКартинок.Информация32);
		Возврат;
	КонецЕсли;	
	
	Если Объект.МакетыПФ.Количество() Тогда		
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		ПараметрыВопроса = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаполнить", ЭтаФорма,ПараметрыВопроса);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Таблица списка макетов печатных форм содержит строки, нажмите «Да» для заполнения очистив таблицу, «Нет» добавить новые макеты, «Отмена» не выполнять заполнение.';"
		+ " en = 'Do you want to continue?'"), Режим, 0);
	Иначе
		ЗаполнитьМакетыПечатныхФорм();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
// Выполним заполнение
//
// Параметры:
//  ВариантЗаполнения  - Булево - Выполнить заполнение.
//
Процедура ЗаполнитьМакетыПечатныхФорм(ВариантЗаполнения = Неопределено)

	ЗаполнениеВыполнено = ВыполнитьЗаполнениеМакетовНаСервере(ВариантЗаполнения);
	
	Если ЗаполнениеВыполнено = Ложь Тогда
        ПоказатьОповещениеПользователя(НСтр("ru = 'Шаблоны пакетов печатных форм...'"),, НСтр("ru = 'Заполнение макетов печатных форм не выполнено.'"), БиблиотекаКартинок.Информация32);
	КонецЕсли;

КонецПроцедуры // ЗаполнитьМакетыПечатныхФорм()

&НаКлиенте
Процедура ПослеЗакрытияВопросаВидДокумента(Результат, ПараметрыВопроса) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Объект.ВидДокумента = ВидДокумента;
        Возврат;
	КонецЕсли;
	Заполнение = Истина;
	Объект.МакетыПФ.Очистить();
	ВидДокумента = Неопределено;
	ЗаполнитьМакетыПечатныхФорм(Заполнение);

КонецПроцедуры // ПослеЗакрытияВопросаВидДокумента()

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаполнить(Результат, ПараметрыВопроса) Экспорт
	
	Заполнение = Истина;
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
    ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
        Заполнение = Ложь;
	КонецЕсли;
	
	Если Заполнение Тогда
		Объект.МакетыПФ.Очистить();
	КонецЕсли;
	
	ЗаполнитьМакетыПечатныхФорм(Заполнение);

КонецПроцедуры // ПослеЗакрытияВопросаЗаполнить()

&НаСервере
// Выполнить заполнение списка макетов.
//
// Параметры:
//  ВариантЗаполнения  - Булево - Выполнить заполнение.
//
Функция ВыполнитьЗаполнениеМакетовНаСервере(ВариантЗаполнения = Неопределено)
	
	Результат = Истина;

	Соответствие = Справочники.упШаблоныПакетовПечатныхФорм.ПолучитьВидыДокументов();
	Структура = Соответствие.Получить(Объект.ВидДокумента);
	
	Если Структура = Неопределено Тогда
		Результат = Ложь;
	Иначе		
		ТекущееИмяФормы = Структура.ИмяФормы;
		КомандыПечати = УправлениеПечатью.КомандыПечатиФормы(ТекущееИмяФормы); // ТаблицаЗначений.
		
		Для каждого ТекущаяКоманда Из КомандыПечати Цикл
			Если СтрЧислоВхождений(ТекущаяКоманда.Идентификатор,",")Тогда
				Продолжить;
			КонецЕсли;
			Если ВариантЗаполнения = Ложь Тогда
				ТекущийОтбор = Новый Структура("ИмяМакета", ТекущаяКоманда.Идентификатор);
				НайденныеСтроки = Объект.МакетыПФ.НайтиСтроки(ТекущийОтбор);
				Если НайденныеСтроки.Количество() Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока = Объект.МакетыПФ.Добавить();
			НоваяСтрока.ИмяМакета = ТекущаяКоманда.Идентификатор;
			НоваяСтрока.ПредставлениеМакета = ТекущаяКоманда.Представление;
			НоваяСтрока.Порядок = 1;
			НоваяСтрока.Количество = 1;
		КонецЦикла;
		
		Если Объект.МакетыПФ.Количество() = 0 Тогда
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ВыполнитьЗаполнениеМакетовНаСервере()

#КонецОбласти