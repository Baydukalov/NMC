#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ТекущееКоличествоЗаписей = Объект.Интервалы.Количество();
	Если ТекущееКоличествоЗаписей > 0 Тогда
		Для Сч = 1 По ТекущееКоличествоЗаписей Цикл
			ДобавитьЗаписиИнтервалаНаСервере(Сч, Ложь, Ложь);
		КонецЦикла; 
	КонецЕсли;
	
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.ЦветПоУмолчанию = упСервисныеФункцииКлиентСервер.ПреобразоватьЦветВАбсолютный(ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет);
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьЦвет(Команда)
	
	НомерРедактируемойЗаписи = Число(СтрЗаменить(ТекущийЭлемент.Имя, "КомандаИзменитьЦвет", ""));	
	ИнтервалыЦвет = Элементы[СтрШаблон("ИнтервалыЦвет%1", Формат(НомерРедактируемойЗаписи,"ЧН=0; ЧГ=0"))];	
	
	ТекущиеПараметры = Новый Структура("НомерСтроки",НомерРедактируемойЗаписи);
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения",ЭтаФорма,ТекущиеПараметры);
	
	Диалог = Новый ДиалогВыбораЦвета;
	Диалог.Цвет = ИнтервалыЦвет.ЦветФона;
	
	Диалог.Показать(Оповещение);
	
КонецПроцедуры	

&НаКлиенте
Процедура Добавить(Команда)
	
	ТекущееКоличествоЗаписей = Объект.Интервалы.Количество();
	
	ДобавитьЗаписиИнтервалаНаСервере(ТекущееКоличествоЗаписей+1);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	УдалитьЗаписиИнтервалаНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ПослеВводаЗначения(Цвет, ДополнительныеПараметры) Экспорт
    Если НЕ Цвет = Неопределено Тогда		
		ИзменитьЦветЗаписиИнтервалаНаСервере(Цвет,ДополнительныеПараметры.НомерСтроки);
		Модифицированность = Истина;
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИзменитьЦветЗаписиИнтервалаНаСервере(ТекущийЦвет,НомерСтроки)
	
	НовыйЦвет = упСервисныеФункцииКлиентСервер.ПреобразоватьЦветВАбсолютный(ТекущийЦвет);
	
	ИнтервалыЦвет = Элементы[СтрШаблон("ИнтервалыЦвет%1", Формат(НомерСтроки,"ЧН=0; ЧГ=0"))];	
	ИнтервалыЦвет.ЦветФона = ТекущийЦвет;
	
	ТекущаяСтрокаИнтервала = Объект.Интервалы[НомерСтроки-1];	
	НовыйЦвет = упСервисныеФункцииКлиентСервер.ПреобразоватьЦветВАбсолютный(ТекущийЦвет);
	ТекущаяСтрокаИнтервала.Цвет = упСервисныеФункцииКлиентСервер.ЦветВHexСтр(НовыйЦвет);
	
КонецПроцедуры // ИзменитьЦветЗаписиИнтервалаНаСервере()

&НаСервере
Процедура УдалитьЗаписиИнтервалаНаСервере()
	
	ТекущееКоличествоЗаписей = Объект.Интервалы.Количество();
	
	НомерУдаляемойЗаписи = Число(СтрЗаменить(ТекущийЭлемент.Имя, "КомандаУдалитьЗапись", ""));
	
	Если НомерУдаляемойЗаписи = ТекущееКоличествоЗаписей Тогда
		УдаляемаяГруппаФормы = Элементы[СтрШаблон("ГруппаЦветоваяШкала%1", Формат(НомерУдаляемойЗаписи,"ЧН=0; ЧГ=0"))];	
		Элементы.Удалить(УдаляемаяГруппаФормы);		
		Объект.Интервалы.Удалить(НомерУдаляемойЗаписи - 1);
	Иначе
		ГруппаПодчиненныеЭлементы = Элементы.ГруппаЦветоваяШкала.ПодчиненныеЭлементы;
		Пока ГруппаПодчиненныеЭлементы.Количество() Цикл
			ЭлементУдаления = ГруппаПодчиненныеЭлементы[0];
			Элементы.Удалить(ЭлементУдаления);		
		КонецЦикла;
		
		Объект.Интервалы.Удалить(НомерУдаляемойЗаписи - 1);
		Для каждого ТекущаяСтрока Из Объект.Интервалы Цикл
			НомерКДанным = Формат(ТекущаяСтрока.НомерСтроки,"ЧН=0; ЧГ=0");
			ДобавитьЗаписиИнтервалаНаСервере(ТекущаяСтрока.НомерСтроки, Ложь, Ложь);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УдалитьЗаписиИнтервалаНаСервере()

&НаСервере
Процедура ДобавитьЗаписиИнтервалаНаСервере(НомерЗаписи, ДобавлятьСтроку = Истина, ОбновлятьДоступность = Истина)
	
	Если ДобавлятьСтроку Тогда
		НоваяСтрока = Объект.Интервалы.Добавить();
		НоваяСтрока.Значение = НовоеЗначение;			
		НоваяСтрока.Цвет     = упСервисныеФункцииКлиентСервер.ЦветВHexСтр(ЦветПоУмолчанию);
	КонецЕсли;
	
	НомерКДанным = Формат(НомерЗаписи-1,"ЧН=0; ЧГ=0");
	ПредставлениеНомерЗаписи = Формат(НомерЗаписи,"ЧН=0; ЧГ=0");
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	РодительИнтервалов = Элементы.ГруппаЦветоваяШкала;
	
	НоваяГруппаЗаписи = Элементы.Добавить(СтрШаблон("ГруппаЦветоваяШкала%1", ПредставлениеНомерЗаписи), Тип("ГруппаФормы"), РодительИнтервалов);
	НоваяГруппаЗаписи.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппаЗаписи.Отображение = ОтображениеОбычнойГруппы.Нет;
	НоваяГруппаЗаписи.ОтображатьЗаголовок = Ложь;
	НоваяГруппаЗаписи.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	НоваяГруппаЗаписи.ЦветФона = Новый Цвет(255, 255, 205);
	
	НовоеПолеНомер = Элементы.Добавить(СтрШаблон("ИнтервалыНомер%1", ПредставлениеНомерЗаписи), Тип("ДекорацияФормы"), НоваяГруппаЗаписи);
	НовоеПолеНомер.Заголовок = ПредставлениеНомерЗаписи;
	НовоеПолеНомер.Вид = ВидДекорацииФормы.Надпись;
	НовоеПолеНомер.АвтоМаксимальнаяШирина = Ложь;
	НовоеПолеНомер.Ширина = 6;
	
	НовоеПолеЗначение = Элементы.Добавить(СтрШаблон("ИнтервалыЗначение%1", ПредставлениеНомерЗаписи), Тип("ПолеФормы"), НоваяГруппаЗаписи);
	НовоеПолеЗначение.Заголовок = НСтр("ru = 'Значение'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	НовоеПолеЗначение.Вид = ВидПоляФормы.ПолеВвода;	
	НовоеПолеЗначение.ПутьКДанным = СтрШаблон("Объект.Интервалы[%1].Значение", НомерКДанным);
	НовоеПолеЗначение.АвтоМаксимальнаяШирина = Ложь;
	НовоеПолеЗначение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовоеПолеЗначение.РастягиватьПоГоризонтали = Ложь;
	НовоеПолеЗначение.Ширина = 12;
	
	НовоеПолеЦвет = Элементы.Добавить(СтрШаблон("ИнтервалыЦвет%1", ПредставлениеНомерЗаписи), Тип("ДекорацияФормы"), НоваяГруппаЗаписи);
	НовоеПолеЦвет.Заголовок = "";
	НовоеПолеЦвет.Вид = ВидДекорацииФормы.Надпись;
	НовоеПолеЦвет.АвтоМаксимальнаяШирина = Ложь;
	
	ТекущийЦветФона = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(Объект.Интервалы[НомерЗаписи-1].Цвет);
	
	НовоеПолеЦвет.ЦветФона = ТекущийЦветФона;
	НовоеПолеЦвет.Ширина = 12;
	
	НоваяКомандаУдалитьЗапись = Элементы.Добавить(СтрШаблон("КомандаИзменитьЦвет%1", ПредставлениеНомерЗаписи), Тип("КнопкаФормы"), НоваяГруппаЗаписи);
	НоваяКомандаУдалитьЗапись.ИмяКоманды = "ИзменитьЦвет";
	
	НоваяКомандаУдалитьЗапись = Элементы.Добавить(СтрШаблон("КомандаУдалитьЗапись%1", ПредставлениеНомерЗаписи), Тип("КнопкаФормы"), НоваяГруппаЗаписи);
	НоваяКомандаУдалитьЗапись.ИмяКоманды = "УдалитьЗапись";
	
КонецПроцедуры // ДобавитьЗаписиИнтервалаНаСервере()

#КонецОбласти