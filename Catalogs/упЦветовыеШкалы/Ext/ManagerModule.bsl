

#Область ОбработчикиСобытий

// Получим таблицу интервалов.
//
// Параметры:
//  ЦветоваяШкала  - СправочникСсылка.упЦветовыеШкалы - Ссылка на цветовую шкалу.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица интервалов цветовой шкалы.
//
Функция ПолучитьИнтервалыШкалы(ЦветоваяШкала) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИнтервалыШкалы.Значение,
	|	ИнтервалыШкалы.Цвет
	|ПОМЕСТИТЬ вт_ИнтервалыШкалы
	|ИЗ
	|	Справочник.упЦветовыеШкалы.Интервалы КАК ИнтервалыШкалы
	|ГДЕ
	|	ИнтервалыШкалы.Ссылка = &ЦветоваяШкала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(вт_ИнтервалыШкалы.Значение) КАК Значение
	|ПОМЕСТИТЬ вт_Макс
	|ИЗ
	|	вт_ИнтервалыШкалы КАК вт_ИнтервалыШкалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ИнтервалыШкалы.Значение,
	|	вт_ИнтервалыШкалы.Цвет
	|ПОМЕСТИТЬ вт_Шкала
	|ИЗ
	|	вт_ИнтервалыШкалы КАК вт_ИнтервалыШкалы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	""#0000FF""
	|ГДЕ
	|	НЕ 0 В
	|				(ВЫБРАТЬ
	|					вт_ИнтервалыШкалы.Значение
	|				ИЗ
	|					вт_ИнтервалыШкалы КАК вт_ИнтервалыШкалы
	|				ГДЕ
	|					вт_ИнтервалыШкалы.Значение = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	вт_Макс.Значение + 32000,
	|	вт_ИнтервалыШкалы.Цвет
	|ИЗ
	|	вт_Макс КАК вт_Макс
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ИнтервалыШкалы КАК вт_ИнтервалыШкалы
	|		ПО вт_Макс.Значение = вт_ИнтервалыШкалы.Значение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т1.Значение КАК От,
	|	МИНИМУМ(Т2.Значение) КАК До,
	|	Т1.Цвет КАК Цвет
	|ПОМЕСТИТЬ вт_Интервалы
	|ИЗ
	|	вт_Шкала КАК Т1
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Шкала КАК Т2
	|		ПО Т1.Значение < Т2.Значение
	|			И (НЕ Т1.Значение = Т2.Значение)
	|ГДЕ
	|	НЕ Т2.Значение ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Т1.Значение,
	|	Т1.Цвет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Интервалы.От КАК От,
	|	вт_Интервалы.До,
	|	вт_Интервалы.Цвет
	|ИЗ
	|	вт_Интервалы КАК вт_Интервалы
	|
	|УПОРЯДОЧИТЬ ПО
	|	От";
	
	Запрос.УстановитьПараметр("ЦветоваяШкала", ЦветоваяШкала);
	
	Выборка = Запрос.Выполнить();
	Результат = Выборка.Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат.Скопировать();
	
	
КонецФункции // ПолучитьИнтервалыШкалы()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// Нет.
#КонецОбласти 
