
#Область ОбработчикиСобытийФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ВыгрузитьСтандартнуюЗаготовку(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузки", ЭтаФорма);
	ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Продолжение процедуры (см. выше). 
//
Процедура ОткрытьДиалогВыгрузки(Результат, ДополнительныеПараметры) Экспорт
	
	ОповещениеПолученияКаталогаДокументов = Новый ОписаниеОповещения("ОбработкаПолученияКаталогаДокументов", ЭтаФорма);
	НачатьПолучениеКаталогаДокументов(ОповещениеПолученияКаталогаДокументов);

КонецПроцедуры

&НаКлиенте
// Процедура открытия диалога выбора файла.
//
Процедура ОбработкаПолученияКаталогаДокументов(ИмяКаталогаДокументов, ДополнительныеПараметры) Экспорт
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог'");
	Диалог.Каталог   = ИмяКаталогаДокументов;
	
	ОписаниеОповещенияОЗакрытиеДиалога = Новый ОписаниеОповещения("ОбработатьВыборКаталога", ЭтаФорма);
	Диалог.Показать(ОписаниеОповещенияОЗакрытиеДиалога);
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьСтандартнуюЗаготовкуНаСервере()
	
	ДвоичныеДанные = Справочники.упШаблоныПисем.ПолучитьМакет("ВнешняяОбработкаДляШаблоновПисем");
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
КонецФункции

&НаКлиенте
// Процедура завершения после выбора каталога.
//
Процедура ОбработатьВыборКаталога(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		ОбработатьЗавершениеПолученияФайлов = Новый ОписаниеОповещения("ОбработкаЗавершенияПолученияФайлов",ЭтаФорма);
		мсвПолучаемыеФайлы = Новый Массив;
		мсвПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла("СтандартнаяЗаготовкаШаблоновПисем.epf", ВыгрузитьСтандартнуюЗаготовкуНаСервере()));
		НачатьПолучениеФайлов(ОбработатьЗавершениеПолученияФайлов, мсвПолучаемыеФайлы, ВыбранныеФайлы[0], Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после получения файла.
//
Процедура ОбработкаЗавершенияПолученияФайлов(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ПолученныеФайлы = Неопределено Тогда
		
		Для каждого ПолученныйФайл Из ПолученныеФайлы Цикл
			
			ТекстСообщения = Нстр("ru = 'Шаблон выгружен:%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПолученныйФайл.Имя);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЦикла;
	Иначе
		ТекстСообщения = Нстр("ru = 'Не удалось выгрузить шаблон'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

