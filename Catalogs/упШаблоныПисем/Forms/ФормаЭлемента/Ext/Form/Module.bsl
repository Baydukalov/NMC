#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Шаблон) тогда
		Шаблон = ПолучитьНавигационнуюСсылку(Объект.Шаблон, "ТекстХранилище");
    	ИмяШаблона = Объект.Шаблон.Наименование;
	Иначе
		ИмяШаблона = "Шаблон не указан";
	КонецЕсли;
	
	Для Каждого СтрокаВложения Из Объект.Вложения Цикл
		Если ЗначениеЗаполнено(СтрокаВложения.Шаблон) тогда
	    	СтрокаВложения.ШаблонАдрес 	= ПолучитьНавигационнуюСсылку(СтрокаВложения.Шаблон, "ТекстХранилище");
			СтрокаВложения.ИмяШаблона	= СтрокаВложения.Шаблон.Наименование;
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьСписокВыбора();
	ЗаполнитьСписокВыбораТиповШаблонов();
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(Шаблон) Тогда
		Если Не ТекущийОбъект.Шаблон.Пустая() Тогда
			НовыйШаблон = ТекущийОбъект.Шаблон.ПолучитьОбъект();
		Иначе
			НовыйШаблон = Справочники.Файлы.СоздатьЭлемент();
		КонецЕсли;
		НовыйШаблон.ВладелецФайла = Справочники.ПапкиФайлов.Шаблоны;
		НовыйШаблон.ТекстХранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Шаблон));
		НовыйШаблон.Наименование = ИмяШаблона;
		НовыйШаблон.ПолноеНаименование  = ИмяШаблона;
		НовыйШаблон.Описание = ИмяШаблона;
		НовыйШаблон.Автор = Пользователи.ТекущийПользователь();
		НовыйШаблон.ХранитьВерсии = Ложь;
		НовыйШаблон.Записать();
		ТекущийОбъект.Шаблон = НовыйШаблон.Ссылка;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ТаблицаВложений") Тогда
		Вложения = ПараметрыЗаписи.ТаблицаВложений;
		Если Вложения.Количество() Тогда
			Для Каждого СтрокаВложения ИЗ Вложения Цикл
				ИмяШаблона 	= СтрокаВложения.ИмяШаблона;
				Шаблон 		= СтрокаВложения.ШаблонАдрес;
				Если ЭтоАдресВременногоХранилища(Шаблон) Тогда
					Если Не СтрокаВложения.Шаблон.Пустая() Тогда
						НовыйШаблон = СтрокаВложения.Шаблон.ПолучитьОбъект();
					Иначе
						НовыйШаблон = Справочники.Файлы.СоздатьЭлемент();
					КонецЕсли;
					НовыйШаблон.ВладелецФайла 		= Справочники.ПапкиФайлов.Шаблоны;
					НовыйШаблон.ТекстХранилище 		= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Шаблон));
					НовыйШаблон.Наименование 		= ИмяШаблона;
					НовыйШаблон.ПолноеНаименование  = ИмяШаблона;
					НовыйШаблон.Описание 			= ИмяШаблона;
					НовыйШаблон.Автор 				= Пользователи.ТекущийПользователь();
					НовыйШаблон.ХранитьВерсии 		= Ложь;
					
					НовыйШаблон.Записать();
					
					СтрокаВложения.Шаблон 		= НовыйШаблон.Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ТекущийОбъект.Вложения.Загрузить(Вложения.Выгрузить());
	КонецЕсли;		
	
	// удалим из справочника "Файлы" вложения, удаленные из шаблона
	Для Каждого ШаблонУдалить Из СписокУдаленныхВложений Цикл
		ОбъектУдалить = ШаблонУдалить.Значение.ПолучитьОбъект();
		ОбъектУдалить.УстановитьПометкуУдаления(Истина);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Отказ = НЕ ПроверитьДублиВТаблице();
	Если Отказ Тогда
		ТекстСообщения = НСтр("ru = 'Указаны одинаковые строки в таблице вложений'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
		Возврат;
	КонецЕсли;
	ПараметрыЗаписи.Вставить("ТаблицаВложений", Объект.Вложения);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого СтрокаВложения Из Объект.Вложения Цикл
		Если ЗначениеЗаполнено(СтрокаВложения.Шаблон) тогда
	    	СтрокаВложения.ШаблонАдрес 	= ПолучитьНавигационнуюСсылку(СтрокаВложения.Шаблон, "ТекстХранилище");
			СтрокаВложения.ИмяШаблона	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаВложения.Шаблон, "Наименование");
		КонецЕсли;		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьВложения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
	
&НаКлиенте
Процедура ИмяШаблонаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Выгрузить(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)

	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаЗавершенияПомещенияФайла", ЭтаФорма);
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, ,,Истина, УникальныйИдентификатор); 

КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если ЗначениеЗаполнено(Шаблон) Тогда
		Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузки", ЭтаФорма);
		ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
		ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВложение(Команда)

	Если Элементы.Вложения.ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(Элементы.Вложения.ТекущиеДанные.ИмяШаблона) Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'В текущей строке уже находится шаблон вложения. Заменить его новым шаблоном?'"), РежимДиалогаВопрос.ДаНет, 0);		
		Иначе
			ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаЗавершенияПомещенияФайлаВложения", ЭтаФорма);
			НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, ,,Истина, УникальныйИдентификатор); 
		КонецЕсли;
	Иначе
		ТекстСообщения = Нстр("ru = 'Необходимо указать строку для загрузки шаблона'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВложение(Команда)
	
	Если Элементы.Вложения.ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(Элементы.Вложения.ТекущиеДанные.Шаблон) Тогда
			Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузкиВложения", ЭтаФорма, Элементы.Вложения.ТекущиеДанные);
			ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
			ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
		КонецЕсли;
	Иначе
		ТекстСообщения = Нстр("ru = 'Необходимо указать шаблон для выгрузки'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
// Открытие формы получения файла.
//
Процедура ОткрытьДиалогВыгрузки(Результат, ДополнительныеПараметры) Экспорт
	ОповещениеЗавершенияПолученияФайлов = Новый ОписаниеОповещения("ОбработкаЗавершенияПолученияФайла", ЭтаФорма);
	мсвПолучаемыеФайлы = Новый Массив;
	мсвПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ИмяШаблона,Шаблон));
	НачатьПолучениеФайлов(ОповещениеЗавершенияПолученияФайлов, мсвПолучаемыеФайлы);
КонецПроцедуры

&НаКлиенте
// Открытие формы получения файла.
//
Процедура ОткрытьДиалогВыгрузкиВложения(Результат, ДополнительныеПараметры) Экспорт
	ОповещениеЗавершенияПолученияФайлов = Новый ОписаниеОповещения("ОбработкаЗавершенияПолученияФайла", ЭтаФорма);
	мсвПолучаемыеФайлы = Новый Массив;
	мсвПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ДополнительныеПараметры.ИмяШаблона, ДополнительныеПараметры.ШаблонАдрес));
	НачатьПолучениеФайлов(ОповещениеЗавершенияПолученияФайлов, мсвПолучаемыеФайлы);
КонецПроцедуры

&НаКлиенте
// Процедура заверешения после загрузки файла.
//
Процедура ОбработкаЗавершенияПомещенияФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Разделитель = "\";
		Строки      = СтрЗаменить(ВыбранноеИмяФайла, Разделитель, Символы.ПС);
		ИмяШаблона 	= СтрПолучитьСтроку(Строки, СтрЧислоСтрок(Строки));
		Шаблон 		= Адрес;
	КонецЕсли;
	
	УстановитьДоступностьВложения();
КонецПроцедуры

&НаКлиенте
// Процедура заверешения после загрузки файла.
//
Процедура ОбработкаЗавершенияПомещенияФайлаВложения(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат и Элементы.Вложения.ТекущиеДанные <> Неопределено Тогда
		Разделитель = "\";
		Строки      = СтрЗаменить(ВыбранноеИмяФайла, Разделитель, Символы.ПС);
		Элементы.Вложения.ТекущиеДанные.ИмяШаблона 	= СтрПолучитьСтроку(Строки, СтрЧислоСтрок(Строки));
		Элементы.Вложения.ТекущиеДанные.ШаблонАдрес	= Адрес;
		Элементы.Вложения.ТекущиеДанные.ТипШаблона = "DOCX";
		Элементы.Вложения.ТекущиеДанные.КоличествоЭкземпляров = 1;
	ИначеЕсли НЕ Результат И Элементы.Вложения.ТекущиеДанные <> Неопределено Тогда
		Объект.Вложения.Удалить(Элементы.Вложения.ТекущиеДанные.НомерСтроки-1);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура заверешения после выгрузки файла.
//
Процедура ОбработкаЗавершенияПолученияФайла(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	Если НЕ ПолученныеФайлы = Неопределено Тогда
		Для каждого ПолученныйФайл Из ПолученныеФайлы Цикл
			ТекстСообщения = Нстр("ru = 'Шаблон выгружен:%1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПолученныйФайл.Имя);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	Иначе
		ТекстСообщения = Нстр("ru = 'Не удалось выгрузить шаблон'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбора()
	
	СписокВыбора = Элементы.ТипДокумента.СписокВыбора;
	Для Каждого ЭлементМетаданных Из Метаданные.Документы Цикл
		СписокВыбора.Добавить(ЭлементМетаданных.Синоним);
	КонецЦикла;
	СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораТиповШаблонов()
	
	СписокВыбора = Элементы.Вложения.ПодчиненныеЭлементы.ВложенияТипШаблона.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("DOCX");
	СписокВыбора.Добавить("PDF");
	СписокВыбора.Добавить("XLS");
	СписокВыбора.Добавить("XLSX");
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПередУдалением(Элемент, Отказ)
	Если Элементы.Вложения.ТекущиеДанные <> Неопределено и ЗначениеЗаполнено(Элементы.Вложения.ТекущиеДанные.Шаблон) Тогда
		СписокУдаленныхВложений.Добавить(Элементы.Вложения.ТекущиеДанные.Шаблон);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаЗавершенияПомещенияФайлаВложения", ЭтаФорма);
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении, ,,Истина, УникальныйИдентификатор); 
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И НЕ Копирование Тогда
		ЗагрузитьВложение(Неопределено);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВложения()
	
	Элементы.ГруппаШаблоныВложений.Доступность = (ИмяШаблона <> "Шаблон не указан");
	
КонецПроцедуры

&НаСервере
Функция ПроверитьДублиВТаблице()
	
	Результат = Объект.Вложения.Выгрузить(,"ИмяШаблона, ТипШаблона, Описание");
	Результат.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТЧ ИЗ Результат Цикл
		СтрокаТЧ.Счетчик = 1;
	КонецЦикла;
	
	Результат.Свернуть("ИмяШаблона, ТипШаблона, Описание", "Счетчик");
	
	Для Каждого СтрокаТЧ ИЗ Результат Цикл
		Если СтрокаТЧ.Счетчик > 1 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
