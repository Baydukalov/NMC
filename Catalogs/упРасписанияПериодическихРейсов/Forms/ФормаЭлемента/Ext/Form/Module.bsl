#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И Не ЗначениеЗаполнено(Объект.Организация) Тогда 
		Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;	

	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимость();	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипТСПриИзменении(Элемент)
	
	ОбновитьВидимость();	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменятьМаршрутСледованияРейсаПриИзменении(Элемент)
	
	ОбновитьВидимость();
	
КонецПроцедуры


&НаКлиенте
Процедура СоглашениеПриИзменении(Элемент)
	
	СоглашениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	сткПараметры = Новый Структура("ВидПеревозки, Перевозчик, Организация", Объект.ВидПеревозки, Объект.Перевозчик, Объект.Организация);
	ОткрытьФорму("Справочник.упТипыТС.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
 
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	сткПараметры = Новый Структура("ВидПеревозки, Перевозчик, Организация, ТипТС", Объект.ВидПеревозки, Объект.Перевозчик, Объект.Организация, Объект.ТипТС);
	ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаВыбора", сткПараметры, Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ТранспортноеСредствоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗоныАдресОтправленияПриИзменении(Элемент)
	
	идСтроки = Элементы.Зоны.ТекущаяСтрока;
	АдресОтправленияПриИзмененииНаСервере(идСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗоныАдресПолученияПриИзменении(Элемент)
	
	идСтроки = Элементы.Зоны.ТекущаяСтрока;
	АдресПолученияПриИзмененииНаСервере(идСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования, 
	ЭтотОбъект, 
	"Объект.Комментарий");
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	упСервисныеФункцииКлиент.РедактироватьПериод(Объект, Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачалаДействия", "ДатаОкончанияДействия"));
		
КонецПроцедуры // УстановитьИнтервал()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		сткЗначенияРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ТранспортноеСредство, Новый Структура("Перевозчик, ТипТС","Партнер", "ТипТС"));
		ЗаполнитьЗначенияСвойств(Объект, сткЗначенияРеквизиты);
		Элементы.Перевозчик.ТолькоПросмотр = Истина;
		Элементы.ТипТС.ТолькоПросмотр = Истина;
	КонецЕсли; 
	
КонецПроцедуры // ТранспортноеСредствоПриИзмененииНаСервере()

&НаСервере
Процедура АдресОтправленияПриИзмененииНаСервере(Идентификатор)
	
	текСтрока = Объект.Зоны.НайтиПоИдентификатору(Идентификатор);
	Если текСтрока <> Неопределено И ЗначениеЗаполнено(текСтрока.АдресОтправления) Тогда
	    текСтрока.ЗонаОтправления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрока.АдресОтправления, "ГеографическаяЗона");
	КонецЕсли; 
	
КонецПроцедуры // АдресОтправленияПриИзмененииНаСервере();

&НаСервере
Процедура АдресПолученияПриИзмененииНаСервере(Идентификатор)
	
	текСтрока = Объект.Зоны.НайтиПоИдентификатору(Идентификатор);
	Если текСтрока <> Неопределено И ЗначениеЗаполнено(текСтрока.АдресПолучения) Тогда
	    текСтрока.ЗонаПолучения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текСтрока.АдресПолучения, "ГеографическаяЗона");
	КонецЕсли; 
	
КонецПроцедуры // АдресПолученияПриИзмененииНаСервере();

&НаСервере
Процедура СоглашениеПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		сткПериодДействия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Соглашение, "ДатаНачалаДействия, ДатаОкончанияДействия");
		Если ЗначениеЗаполнено(сткПериодДействия.ДатаНачалаДействия) Или ЗначениеЗаполнено(сткПериодДействия.ДатаОкончанияДействия) Тогда
			ЗаполнитьЗначенияСвойств(Объект, сткПериодДействия);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // СоглашениеПриИзмененииНаСервере()

&НаСервере
Процедура ОбновитьВидимость()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		Элементы.Перевозчик.ТолькоПросмотр = Истина;
		Элементы.ТипТС.ТолькоПросмотр = Истина;
	КонецЕсли;
	Элементы.МаршрутСледования.Доступность               = Объект.ИзменятьМаршрутСледованияРейса;
	Элементы.МаршрутСледования.АвтоОтметкаНезаполненного = Объект.ИзменятьМаршрутСледованияРейса;
	
КонецПроцедуры // ОбновитьВидимость()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти 
