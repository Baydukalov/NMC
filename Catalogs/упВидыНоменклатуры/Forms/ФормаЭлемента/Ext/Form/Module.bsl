
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если ПолучитьФункциональнуюОпцию("упКонтролироватьСовместимостьТовара") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	упНесовместимыеВидыНоменклатурыТ.ВидНоменклатуры2 ЕСТЬ НЕ NULL  КАК Пометка,
		               |	упВидыНоменклатурыТ.Ссылка КАК ВидНоменклатуры
		               |ИЗ
		               |	Справочник.упВидыНоменклатуры КАК упВидыНоменклатурыТ
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упНесовместимыеВидыНоменклатуры КАК упНесовместимыеВидыНоменклатурыТ
		               |		ПО (упНесовместимыеВидыНоменклатурыТ.ВидНоменклатуры1 = упВидыНоменклатурыТ.Ссылка)
		               |			И (упНесовместимыеВидыНоменклатурыТ.ВидНоменклатуры2 = &Ссылка)
		               |ГДЕ
		               |	упВидыНоменклатурыТ.Ссылка <> &Ссылка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ВидНоменклатуры";
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		НесовместимыеВидыНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = Справочники.упВидыНоменклатуры.СоздатьЭлемент();
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("упКонтролироватьСовместимостьТовара") Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НесовместимыеВидыНоменклатуры", НесовместимыеВидыНоменклатуры.Выгрузить(Новый Структура("Пометка", Истина)).ВыгрузитьКолонку(0));
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// Нет


#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура УстановитьВсеПометки(Команда)
	
	ИзменитьПометкуВидаНоменклатуры(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеПометки(Команда)
	
	ИзменитьПометкуВидаНоменклатуры(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьПометкуВидаНоменклатуры(НоваяПометка)
	
	Если Элементы.НесовместимыеВидыНоменклатуры.ВыделенныеСтроки.Количество() > 1 Тогда
		СтрокиДляОбработки = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из Элементы.НесовместимыеВидыНоменклатуры.ВыделенныеСтроки Цикл
			СтрокиДляОбработки.Добавить(НесовместимыеВидыНоменклатуры.НайтиПоИдентификатору(ВыделеннаяСтрока));
		КонецЦикла;
	Иначе
		СтрокиДляОбработки = НесовместимыеВидыНоменклатуры;
	КонецЕсли; 
	
	Для Каждого СтрокаВидаНоменклатуры Из СтрокиДляОбработки Цикл
		СтрокаВидаНоменклатуры.Пометка = НоваяПометка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 




