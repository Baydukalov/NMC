#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если НЕ ЗначениеЗаполнено(Объект.СпособЗаполнения) Тогда
			Объект.СпособЗаполнения = Перечисления.упСпособыЗаполненияГрафикаРаботы.ПоДнямНедели;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.Цвет) Тогда
			Объект.Цвет = упСервисныеФункцииКлиентСервер.ЦветВHexСтр(Новый Цвет(0,128,0));	
		КонецЕсли;
	КонецЕсли;
	Цвет = упСервисныеФункцииКлиентСервер.HexСтрВЦвет(Объект.Цвет);	
	ЗаполнитьШаблонЗаполнения(ЭтаФорма, Объект.ШаблонЗаполнения, Объект.СпособЗаполнения, Объект.ДлинаЦикла);
	НастроитьЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Цвет = упСервисныеФункцииКлиентСервер.ЦветВHexСтр(Цвет);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
		
	Если Ложь
		Или ВыбранноеЗначение = Неопределено 
		Или ТолькоПросмотр
	Тогда
		Возврат;
	КонецЕсли;

	мсвСтроки = Объект.РасписаниеРаботы.НайтиСтроки(Новый Структура("НомерДня", КонтекстЗаполненияРасписания.НомерДня));
	Для каждого Строка Из мсвСтроки Цикл
		Объект.РасписаниеРаботы.Удалить(Строка);
	КонецЦикла;
		
	Для Каждого Строка Из ВыбранноеЗначение.РасписаниеРаботы Цикл
		НоваяСтрока = Объект.РасписаниеРаботы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.НомерДня = КонтекстЗаполненияРасписания.НомерДня;
	КонецЦикла;
	
	СтрокаШаблона = Объект.ШаблонЗаполнения.НайтиПоИдентификатору(КонтекстЗаполненияРасписания.ИдентификаторСтрокиШаблона);
	СтрокаШаблона.ДеньВключенВГрафик = ВыбранноеЗначение.РасписаниеРаботы.Количество() > 0;
	СтрокаШаблона.Расписание = ПредставлениеРасписания(ЭтотОбъект, КонтекстЗаполненияРасписания.НомерДня);
	
	Модифицированность = Истина
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЦветПриИзменении(Элемент)
	Если Цвет.Вид <> ВидЦвета.Абсолютный Тогда 
		Цвет = ОсновнойЦветПриИзмененииНаСервере(Цвет);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПоЦикламПриИзменении(Элемент)
	ЗаполнитьШаблонЗаполненияНаКлиенте();
	НастроитьЭлементыФормы(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПоНеделямПриИзменении(Элемент)
	ЗаполнитьШаблонЗаполненияНаКлиенте();
	НастроитьЭлементыФормы(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаЦиклаПриИзменении(Элемент)
	ЗаполнитьШаблонЗаполненияНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьШаблонЗаполненияНаКлиенте()
	ЗаполнитьШаблонЗаполнения(ЭтаФорма, Объект.ШаблонЗаполнения, Объект.СпособЗаполнения, Объект.ДлинаЦикла);
КонецПроцедуры

&НаКлиенте
Процедура ШаблонЗаполненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СтрокаШаблона = Объект.ШаблонЗаполнения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	КонтекстЗаполненияРасписания = Новый Структура;
	КонтекстЗаполненияРасписания.Вставить("НомерДня", СтрокаШаблона.НомерСтроки);
	КонтекстЗаполненияРасписания.Вставить("Расписание", СтрокаШаблона.Расписание);
	КонтекстЗаполненияРасписания.Вставить("ИдентификаторСтрокиШаблона", ВыбраннаяСтрока);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РасписаниеРаботы", РасписаниеРаботы(КонтекстЗаполненияРасписания.НомерДня));
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.Календари.Форма.РасписаниеРаботы", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

#Если Сервер И Не Сервер Тогда
	&НаСервере
#Иначе
	&НаКлиентеНаСервереБезКонтекста
#КонецЕсли
Процедура НастроитьЭлементыФормы(Форма)
	Если Форма.Объект.СпособЗаполнения = ПредопределенноеЗначение("Перечисление.упСпособыЗаполненияГрафикаРаботы.ПоДнямНедели") Тогда
		Форма.Элементы.ШаблонЗаполненияНомерСтроки.Видимость = Ложь;	
		Форма.Элементы.ШаблонЗаполненияДень.Видимость = Истина;	
	Иначе	
		Форма.Элементы.ШаблонЗаполненияНомерСтроки.Видимость = Истина;
		Форма.Элементы.ШаблонЗаполненияДень.Видимость = Ложь;	
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновнойЦветПриИзмененииНаСервере(Цвет)
	
	Возврат упСервисныеФункцииКлиентСервер.ПреобразоватьЦветВАбсолютный(Цвет);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьШаблонЗаполнения(Форма, ШаблонЗаполнения, СпособЗаполнения, ЗНАЧ ДлинаЦикла)
	
	КоличествоСтрок = ШаблонЗаполнения.Количество();
	
	Если СпособЗаполнения = ПредопределенноеЗначение("Перечисление.упСпособыЗаполненияГрафикаРаботы.ПоДнямНедели") Тогда
		ДлинаЦикла = 7;
	КонецЕсли;
	
	Если КоличествоСтрок <= ДлинаЦикла Тогда
		Для й = КоличествоСтрок По ДлинаЦикла - 1 Цикл
			НоваяСтрока = ШаблонЗаполнения.Добавить();	
		КонецЦикла;
	Иначе	
		КоличествоУдалить = КоличествоСтрок - ДлинаЦикла;
		ИндексПоследнейСтроки = КоличествоСтрок - 1;
		Для й = 1 По КоличествоУдалить Цикл
			ШаблонЗаполнения.Удалить(ИндексПоследнейСтроки);	
			ИндексПоследнейСтроки = ИндексПоследнейСтроки - 1;
		КонецЦикла;		
	КонецЕсли;
		
	Если СпособЗаполнения = ПредопределенноеЗначение("Перечисление.упСпособыЗаполненияГрафикаРаботы.ПоДнямНедели") Тогда
		ШаблонЗаполнения[0].День = Нстр("ru = 'Понедельник'");
		ШаблонЗаполнения[1].День = Нстр("ru = 'Вторник'");
		ШаблонЗаполнения[2].День = Нстр("ru = 'Среда'");
		ШаблонЗаполнения[3].День = Нстр("ru = 'Четверг'");
		ШаблонЗаполнения[4].День = Нстр("ru = 'Пятница'");
		ШаблонЗаполнения[5].День = Нстр("ru = 'Суббота'");
		ШаблонЗаполнения[6].День = Нстр("ru = 'Воскресенье'");
	КонецЕсли;
	
	Для каждого Строка Из ШаблонЗаполнения Цикл
		Строка.Расписание = ПредставлениеРасписания(Форма, Строка.ИсходныйНомерСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция РасписаниеРаботы(НомерДня)
	
	РасписаниеДня = Новый Массив;
	
	Для Каждого СтрокаРасписания Из Объект.РасписаниеРаботы Цикл
		Если СтрокаРасписания.НомерДня = НомерДня Тогда
			РасписаниеДня.Добавить(Новый Структура("ВремяНачала, ВремяОкончания", СтрокаРасписания.ВремяНачала, СтрокаРасписания.ВремяОкончания));
		КонецЕсли;
	КонецЦикла;
	
	Возврат РасписаниеДня;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРасписания(Форма, НомерДня)
	
	Результат = "";
	
	Секунд = 0;
	мсвИнтервалы = Новый Массив;
	Для Каждого Строка Из Форма.Объект.РасписаниеРаботы Цикл
		Если Строка.НомерДня <> НомерДня Тогда
			Продолжить;
		КонецЕсли;
		Интервал = СтрШаблон("%1-%2", Формат(Строка.ВремяНачала, "ДФ=ЧЧ:мм; ДП="), Формат(Строка.ВремяОкончания, "ДФ=ЧЧ:мм; ДП="));
		мсвИнтервалы.Добавить(Интервал);

		Если Не ЗначениеЗаполнено(Строка.ВремяОкончания) Тогда
			СекундИнтервала = КонецДня(Строка.ВремяОкончания) - Строка.ВремяНачала + 1;
		Иначе
			СекундИнтервала = Строка.ВремяОкончания - Строка.ВремяНачала;
		КонецЕсли;
		Секунд = Секунд + СекундИнтервала;
	КонецЦикла;
		
	Если мсвИнтервалы.Количество() = 0 Тогда
		Результат = НСтр("ru = 'Заполнить расписание'");
	Иначе
		Часов = Окр(Секунд / 3600, 1);
		Результат = СтрСоединить(мсвИнтервалы, ", ");
		Результат = СтрШаблон(НСтр("ru = '%1 ч. (%2)'"), Часов, Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
