#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упТипыТС.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

Функция ПолучитьДоступныеТипыТС(Параметры) Экспорт
	
	мсвТиповТС = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВидыПеревозок.Ссылка,
	|	упВидыПеревозок.ВыборПеревозчика КАК ВариантВыбораПеревозчика,
	|	упВидыПеревозок.Перевозчик
	|ПОМЕСТИТЬ втВидыПеревозок
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчики
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаСобственнымиТС))
	|			И (упПартнеры.Организация = &Организация)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втВидыПеревозок.Перевозчик
	|ИЗ
	|	втВидыПеревозок КАК втВидыПеревозок
	|ГДЕ
	|	втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (упПартнеры.Перевозчик)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упТипыТС.Ссылка КАК ТипТС
	|ИЗ
	|	Справочник.упТипыТС КАК упТипыТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|			И (&Перевозчик = ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упТипыТС.Ссылка
	|ИЗ
	|	Справочник.упТипыТС КАК упТипыТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры.ДоступныеТипыТС КАК ДоступныеТипыТС
	|		ПО упТипыТС.Ссылка = ДоступныеТипыТС.ТипТС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
	|		ПО (втПеревозчики.Перевозчик = ДоступныеТипыТС.Ссылка)
	|			И (&Перевозчик В (втПеревозчики.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)))";
	Запрос.УстановитьПараметр("ВидПеревозки", Параметры.ВидПеревозки);
	Запрос.УстановитьПараметр("Перевозчик", Параметры.Перевозчик);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	
	СтрокаПоиска = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтрокаПоиска", Неопределено);
			
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ упТипыТС.Наименование ПОДОБНО &Наименование";
		Запрос.УстановитьПараметр("Наименование", "%" + Параметры.СтрокаПоиска + "%");
	КонецЕсли; 	
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		мсвТиповТС = Результат.Выгрузить().ВыгрузитьКолонку("ТипТС");
	КонецЕсли; 
	
	Возврат мсвТиповТС;
	
КонецФункции

#КонецОбласти 

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если ВидФормы = "ФормаОбъекта" Тогда
		ОбъектСсылка = Неопределено;
		ВидТранспорта = Неопределено;
		ЗначенияЗаполнения = Неопределено;
		
		Если Параметры.Свойство("Ключ", ОбъектСсылка) Тогда
			ВидТранспорта = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "ВидТранспорта"); 			
		ИначеЕсли Истина
			И Параметры.Свойство("ЗначенияЗаполнения",ЗначенияЗаполнения)
			И ЗначенияЗаполнения.Свойство("ВидТранспорта",ВидТранспорта) Тогда
	
		КонецЕсли;
		
		Если ВидТранспорта = ПредопределенноеЗначение("Перечисление.упВидыТранспорта.Контейнер") Тогда	
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаКонтейнера";
		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаСписка" Тогда	
		
		ВидТранспорта = Неопределено;

		Если Параметры.Свойство("ВидТранспорта", ВидТранспорта) Тогда
			
			Если ВидТранспорта = ПредопределенноеЗначение("Перечисление.упВидыТранспорта.Контейнер") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаСпискаКонтейнера";
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда	
		
		ВидТранспорта = Неопределено;

		Если Параметры.Свойство("ВидТранспорта", ВидТранспорта) Тогда
			
			Если ВидТранспорта = ПредопределенноеЗначение("Перечисление.упВидыТранспорта.Контейнер") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораКонтейнера";
			КонецЕсли;

		КонецЕсли;
	
	КонецЕсли;
	

КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Истина
		И Параметры.Свойство("ВидПеревозки")
		И Параметры.Свойство("Перевозчик")
		И Параметры.Свойство("Организация")
	Тогда
	
		СтрокаПоиска = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтрокаПоиска");
			
		Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
			ДанныеВыбора = Новый СписокЗначений;
			ДанныеВыбора.ЗагрузитьЗначения(ПолучитьДоступныеТипыТС(Параметры));
		КонецЕсли;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСписокПараметровОтбора() Экспорт

	ТекстИменПараметров = "СтрокаПоиска, ВидПеревозки, Перевозчик, Организация";
	СписокПараметров = СтрРазделить(ТекстИменПараметров, ", ", Ложь);
	Возврат СписокПараметров;

КонецФункции // ПолучитьСписокПараметровОтбора()

#КонецОбласти

#КонецЕсли