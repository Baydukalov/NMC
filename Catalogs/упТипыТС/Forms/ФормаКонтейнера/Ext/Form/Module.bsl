#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Элементы.ГруппаШГВ.Видимость = ЭтаФорма.УчитыватьЛинейныеРазмеры;
	
	КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	УстановитьЗаголовки();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.УчитыватьМассу = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьЛинейныеРазмеры = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	
	упСервисныеФункции.УстановитьЗаголовокФормы(ЭтаФорма);
	
	ПриСозданииПриЧтенииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписанТипТС",,Объект.Ссылка);	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда         
		ЕИ_Грузов = сткПредставления.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли; 
	
КонецПроцедуры // УстановитьЗаголовки()

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.Ширина * Объект.Высота * Объект.Длина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов


#КонецОбласти
