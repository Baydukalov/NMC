
#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события "ПередЗаписьюНаСервере" формы.
//
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для каждого текСтрока Из Объект.Датчики Цикл		
		Если ЗначениеЗаполнено(текСтрока.Датчик) И ЗначениеЗаполнено(текСтрока.КалибровочныйГрафик) Тогда
			Если текСтрока.Датчик.Назначение <> текСтрока.КалибровочныйГрафик.Назначение Тогда
				ИндексСтрокиКоллекции = Объект.Датчики.Индекс(текСтрока);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Реквизит Назначение в колонке Датчик и Калибровочный график не совподают",Объект.Ссылка,
				"Датчики[" + Строка(ИндексСтрокиКоллекции) + "].Датчик","Объект",Отказ);
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписьюНаСервере()


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" элемента "ДатчикиКалибровочныйГрафик".
//
Процедура ДатчикиКалибровочныйГрафикПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Датчики.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Датчик) И ЗначениеЗаполнено(ТекущиеДанные.КалибровочныйГрафик)Тогда
		Структура = Новый Структура;
		Структура.Вставить("Датчик", ТекущиеДанные.Датчик);
		Структура.Вставить("КалибровочныйГрафик",ТекущиеДанные.КалибровочныйГрафик);
		
		взСтруктура = ПолучитьНазначениеОбъектов(Структура, "Назначение");
		Если взСтруктура.Датчик <> взСтруктура.КалибровочныйГрафик Тогда			
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Поле калибровочного графика заполнено, назначение датчика и калибровочного графика различны, очистить его содержимое?';"
			+"en = 'Do you want to continue?'"), Режим, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //ДатчикиКалибровочныйГрафикПриИзменении()

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" элемента "ДатчикиКалибровочныйГрафик".
//
Процедура ДатчикиКалибровочныйГрафикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Датчики.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Датчик)Тогда
		СтандартнаяОбработка = Ложь;
		
		сткОтбор     = Новый Структура("Назначение", ПолучитьЗначениеРеквизита(ТекущиеДанные.Датчик,"Назначение"));
		сткПараметры = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("Справочник.упКалибровочныеГрафики.ФормаВыбора", сткПараметры,,,,, Новый ОписаниеОповещения("КалибровочныеГрафикиНачалоВыбораЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" элемента "ДатчикиДатчик".
//
Процедура ДатчикиДатчикПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Датчики.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Датчик) И ЗначениеЗаполнено(ТекущиеДанные.КалибровочныйГрафик)Тогда
		Структура = Новый Структура;
		Структура.Вставить("Датчик", ТекущиеДанные.Датчик);
		Структура.Вставить("КалибровочныйГрафик",ТекущиеДанные.КалибровочныйГрафик);
		
		взСтруктура = ПолучитьНазначениеОбъектов(Структура, "Назначение");
		Если взСтруктура.Датчик <> взСтруктура.КалибровочныйГрафик Тогда			
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Поле калибровочного графика заполнено очистить его содержимое?';"
			+"en = 'Do you want to continue?'"), Режим, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДатчикиДатчикПриИзменении()

#КонецОбласти

#Область СлужебныеПроцедурыФункции

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
// Выполнить действия после получения оповещения для начало выбора.
//
// Параметры:
//  Результат  - КодВозвратаДиалога - Результат ответа на вопрос.
//  ДополнительныеПараметры  - Структура - Параметры ответа.
//
Процедура КалибровочныеГрафикиНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда
		Элементы.Датчики.ТекущиеДанные.КалибровочныйГрафик = Результат;		
	КонецЕсли;
		
КонецПроцедуры // КалибровочныеГрафикиНачалоВыбораЗавершение()

&НаКлиенте
// Выполнить действия после получения оповещения.
//
// Параметры:
//  Результат  - КодВозвратаДиалога - Результат ответа на вопрос.
//  Параметры  - Структура - Параметры ответа.
//
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    ТекущиеДанные = Элементы.Датчики.ТекущиеДанные;
	ТекущиеДанные.КалибровочныйГрафик = ПредопределенноеЗначение("Справочник.упКалибровочныеГрафики.ПустаяСсылка");

КонецПроцедуры //ПослеЗакрытияВопроса()

&НаСервереБезКонтекста
// Получить значение реквизита для объектов.
//
// Параметры:
//  стрСсылок  - Структура - Структура полей обрбаотки.
//  ИмяРеквизита  - Строка - Текстовое имя реквизита.
// 
// Возвращаемое значение:
//  Структура - Значение реквизитов.
// 
Функция ПолучитьНазначениеОбъектов(стрСсылок, ИмяРеквизита)
	
	Структура = Новый Структура();
	
	Для каждого текЭлемент Из стрСсылок Цикл
	
		Структура.Вставить(текЭлемент.Ключ, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(текЭлемент.Значение, ИмяРеквизита));
	
	КонецЦикла;

	Возврат Структура;

КонецФункции // ПолучитьНазначениеОбъектов()

&НаСервереБезКонтекста
// Получить значение реквизита для объекта.
//
// Параметры:
//  ТекущаяСсылка - СправочникСсылка - Ссылка на обект.
//  ИмяРеквизита - Строка - Текстовое имя реквизита.
// 
// Возвращаемое значение:
//  Произвольный - зависит от типа значения прочитанного реквизита.
//
Функция ПолучитьЗначениеРеквизита(ТекущаяСсылка, ИмяРеквизита)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСсылка, ИмяРеквизита);

КонецФункции // ПолучитьЗначениеРеквизита()

#КонецОбласти
