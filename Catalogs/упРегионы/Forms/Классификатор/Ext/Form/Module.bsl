
#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимОткрытияОкна") Тогда
		РежимОткрытияОкна = Параметры.РежимОткрытияОкна
	КонецЕсли;
	
	Параметры.Свойство("РежимВыбора", РежимВыбора);
	Элементы.Классификатор.РежимВыбора = РежимВыбора;
	
	// Служебные реквизиты
	ПоляКлассификатора = "Код, Наименование, Владелец";
	
	Мета = Метаданные.Справочники.упРегионы;
	ПредставлениеОбъектаКлассификатора = Мета.РасширенноеПредставлениеОбъекта;
	Если ПустаяСтрока(ПредставлениеОбъектаКлассификатора) Тогда
		ПредставлениеОбъектаКлассификатора = Мета.ПредставлениеОбъекта;
	КонецЕсли;
	Если ПустаяСтрока(ПредставлениеОбъектаКлассификатора) Тогда
		ПредставлениеОбъектаКлассификатора = Мета.Представление();
	КонецЕсли;
	Если Не ПустаяСтрока(ПредставлениеОбъектаКлассификатора) Тогда
		ПредставлениеОбъектаКлассификатора = " (" + ПредставлениеОбъектаКлассификатора + ")";
	КонецЕсли;
	
	ДанныеКлассификатора = СостояниеКлассификатора();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СтраныМира.Ссылка,
	|	СтраныМира.КодАльфа2
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира";
	Выборка = Запрос.Выполнить().Выбрать();
	соотСтран = Новый Соответствие;
	Пока Выборка.Следующий() Цикл 
		соотСтран.Вставить(Выборка.КодАльфа2, Выборка.Ссылка);
	КонецЦикла;
	
	ДанныеКлассификатора.Колонки.Добавить("Владелец");
	Для Каждого текСтрока Из ДанныеКлассификатора Цикл 
		Если текСтрока.КодСтраны = "RU" Тогда 
			текСтрока.Владелец = Справочники.СтраныМира.Россия;
		Иначе
			текСтрока.Владелец = соотСтран[текСтрока.КодСтраны];
		КонецЕсли;
	КонецЦикла;	
		
	Классификатор.Загрузить(ДанныеКлассификатора);
	
	Фильтр = Классификатор.НайтиСтроки(Новый Структура("Код, Владелец", Параметры.ТекущаяСтрока.Код, Параметры.ТекущаяСтрока.Владелец));
	Если Фильтр.Количество() > 0 Тогда
		Элементы.Классификатор.ТекущаяСтрока = Фильтр[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Классификатор
//

&НаКлиенте
Процедура КлассификаторВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не РежимВыбора Тогда 
		ОткрытьФормуЭлементаКлассификатора(Элемент.ТекущиеДанные);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбраннаяСтрока)=Тип("Массив") Тогда
		ИдентификаторСтрокиВыбора = ВыбраннаяСтрока[0];
	Иначе
		ИдентификаторСтрокиВыбора = ВыбраннаяСтрока;
	КонецЕсли;
	
	ОповеститьОВыбореЭлементаКлассификатора(ИдентификаторСтрокиВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ОповеститьОВыбореЭлементаКлассификатора(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьФормуЭлементаКлассификатора(Элементы.Классификатор.ТекущиеДанные);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыФункции


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

&НаКлиенте
Процедура ОткрытьФормуЭлементаКлассификатора(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура(ПоляКлассификатора));
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы.ЗначенияЗаполнения, ДанныеЗаполнения);
	
	ОткрытьФорму("Справочник.упРегионы.ФормаОбъекта", ПараметрыФормы, Элементы.Классификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОВыбореЭлементаКлассификатора(ИдентификаторСтрокиВыбора)
	
	ВсеДанныеСтроки = Классификатор.НайтиПоИдентификатору(ИдентификаторСтрокиВыбора);
	Если ВсеДанныеСтроки <> Неопределено Тогда
		ДанныеСтроки = Новый Структура(ПоляКлассификатора);
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВсеДанныеСтроки);
		
		ДанныеВыбора = ДанныеВыбораЭлементаКлассификатора(ДанныеСтроки);
		Если ДанныеВыбора.ЭтоНовый Тогда
			ОповеститьОСозданииЭлементов(ДанныеВыбора.Ссылка);
		КонецЕсли;
		
		ОповеститьОВыборе(ДанныеВыбора.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораЭлементаКлассификатора(Знач ДанныеСтраны)

	Ссылка = Справочники.упРегионы.НайтиПоКоду(ДанныеСтраны.Код,, ДанныеСтраны.Владелец);
	
	ЭтоНовый = Не ЗначениеЗаполнено(Ссылка);
	Если ЭтоНовый Тогда
		Регион = Справочники.упРегионы.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Регион, ДанныеСтраны);
		Регион.Записать();
		Ссылка = Регион.Ссылка;
	КонецЕсли;
	
	Возврат Новый Структура("Ссылка, ЭтоНовый, Код", Ссылка, ЭтоНовый, ДанныеСтраны.Код);
	
КонецФункции

&НаСервереБезКонтекста
Функция СостояниеКлассификатора()
	
	Данные = Справочники.упРегионы.ТаблицаКлассификатора();
	
	Данные.Колонки.Добавить("ИндексПиктограммы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0)));
	Данные.ЗаполнитьЗначения(8, "ИндексПиктограммы");
	
	Возврат Данные;
	
КонецФункции

&НаКлиенте
Процедура ОповеститьОСозданииЭлементов(Ссылка)
	
	ОповеститьОЗаписиНового(Ссылка);

КонецПроцедуры

#КонецОбласти
