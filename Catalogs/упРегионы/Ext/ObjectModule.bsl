
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
//

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Существующий = СуществующийЭлемент();
	Если Существующий <> Неопределено Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Существующий.ОписаниеОшибки,, "Объект.Наименование");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
//

// Контролирует уникальность элемента в базе,
// Возвращает структуру - описание элемента, существующего в базе.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//   Структура - Результат выполнения.
//
Функция СуществующийЭлемент() Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	упРегионы.Код КАК Код,
	|	упРегионы.Наименование КАК Наименование,
	|	упРегионы.Ссылка КАК Ссылка,
	|	упРегионы.Владелец
	|ИЗ
	|	Справочник.упРегионы КАК упРегионы
	|ГДЕ
	|	упРегионы.Код = &Код
	|	И упРегионы.Ссылка <> &Ссылка
	|	И упРегионы.Владелец = &Владелец");
	Запрос.УстановитьПараметр("Ссылка",   Ссылка);
	Запрос.УстановитьПараметр("Код",      Код);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Новый Структура("ОписаниеОшибки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='С кодом %1 уже существует регион %2 в стране %3. Измените код или используйте уже существующие данные.'"), 
			Код, Выборка.Наименование, Владелец));
		
		Для Каждого Поле Из РезультатЗапроса.Колонки Цикл
			Результат.Вставить(Поле.Имя, Выборка[Поле.Имя]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти 

#КонецЕсли