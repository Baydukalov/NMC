#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Подобрать экземпляр документа
//
// Параметры:
//  сткПараметрыПодбораЭкземпляра - Структура - структура параметров для поиска и формирования экземпляра документа
//								
//  Ошибки - Неопределено - будет создан новый список,
//         - значение, установленное при первом вызове этой процедуры со значением Неопределено.
// 
// Возвращаемое значение:
//   СправочникСсылка.упЭкземпляры - ссылка на экземпляр документа.
//
Функция ПодобратьЭкземплярДокумента(сткПараметрыПодбораЭкземпляра, Ошибки = Неопределено) Экспорт
	
	Результат = Справочники.упЭкземплярыДокументов.ПустаяСсылка();  
	
	ДокументОснование = сткПараметрыПодбораЭкземпляра.ДокументОснование;
	Рейс = сткПараметрыПодбораЭкземпляра.Рейс; 
	Дата = сткПараметрыПодбораЭкземпляра.Дата; 
	Номер = сткПараметрыПодбораЭкземпляра.Номер; 
	ДатаКИС = сткПараметрыПодбораЭкземпляра.ДатаКИС;
	НомерКИС = сткПараметрыПодбораЭкземпляра.НомерКИС; 
	СпособОпределенияДатыЭкземпляра = сткПараметрыПодбораЭкземпляра.СпособОпределенияДатыЭкземпляра; 
	СпособОпределенияНомераЭкземпляра = сткПараметрыПодбораЭкземпляра.СпособОпределенияНомераЭкземпляра; 
	ФормулаДата = сткПараметрыПодбораЭкземпляра.ФормулаДата; 
	ФормулаНомер = сткПараметрыПодбораЭкземпляра.ФормулаНомер; 
	
	НомерЭкземпляра = "";
	Если СпособОпределенияНомераЭкземпляра = Перечисления.упСпособыОпределенияНомераДокумента.Номер Тогда
		НомерЭкземпляра = Номер;	
	ИначеЕсли СпособОпределенияНомераЭкземпляра = Перечисления.упСпособыОпределенияНомераДокумента.НомерКИС Тогда
		НомерЭкземпляра = НомерКИС;	
	ИначеЕсли СпособОпределенияНомераЭкземпляра = Перечисления.упСпособыОпределенияНомераДокумента.ПоФормуле Тогда	
		Попытка
			Выполнить(ФормулаНомер);
		Исключение
			
			текИнформацияОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТекстСообщения = Нстр("ru = 'Не удалось вычислить формулу определения номера экземпляра. Формула: %1. Описание ошибки: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФормулаНомер, текИнформацияОбОшибке);
			
			Событие = Нстр("ru = 'Ошибка при определении номера экземпляра.'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,ТекстСообщения); 
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,,ТекстСообщения,,);
			
		КонецПопытки;	
	КонецЕсли;
		
	ДатаЭкземпляра = "";
	Если СпособОпределенияДатыЭкземпляра = Перечисления.упСпособыОпределенияДатыДокумента.Дата Тогда
		ДатаЭкземпляра = Дата;	
	ИначеЕсли СпособОпределенияДатыЭкземпляра = Перечисления.упСпособыОпределенияДатыДокумента.ДатаКИС Тогда
		ДатаЭкземпляра = ДатаКИС;	
	ИначеЕсли СпособОпределенияДатыЭкземпляра = Перечисления.упСпособыОпределенияДатыДокумента.ПоФормуле Тогда	
		Попытка
			Выполнить(ФормулаДата);
		Исключение
			
			текИнформацияОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТекстСообщения = Нстр("ru = 'Не удалось вычислить формулу определения даты экземпляра. Формула: %1. Описание ошибки: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ФормулаДата, текИнформацияОбОшибке);
			
			Событие = Нстр("ru = 'Ошибка при определении номера экземпляра.'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,ТекстСообщения); 
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,,ТекстСообщения,,);
			
		КонецПопытки;	
	КонецЕсли;
	
	ТекстНаименование = ПолучитьНаименование(НомерЭкземпляра, ДатаЭкземпляра);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упЭкземплярыДокументов.Ссылка
	               |ИЗ
	               |	Справочник.упЭкземплярыДокументов КАК упЭкземплярыДокументов
	               |ГДЕ
	               |	упЭкземплярыДокументов.Наименование = &Наименование
	               |	И упЭкземплярыДокументов.Рейс = &Рейс
	               |	И НЕ упЭкземплярыДокументов.ПометкаУдаления";
	Запрос.УстановитьПараметр("Наименование", ТекстНаименование);
	Запрос.УстановитьПараметр("Рейс", Рейс);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;		
	Иначе	
		ЭкземплярОбъект = Справочники.упЭкземплярыДокументов.СоздатьЭлемент();
		ЭкземплярОбъект.Номер = НомерЭкземпляра;
		ЭкземплярОбъект.Дата = ДатаЭкземпляра;
		ЭкземплярОбъект.Рейс = Рейс;

		Попытка
			ЭкземплярОбъект.Записать();	
			Результат = ЭкземплярОбъект.Ссылка;
		Исключение
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			Событие = Нстр("ru = 'Ошибка при записи экземпляра дкоумента.'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки); 
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,,ТекстОшибки);
		КонецПопытки;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

// Функция - Получить наименование
//
// Параметры:
//  Номер	 - Строка	 - номер документа
//  Дата	 - Дата	 - дата документа
// 
// Возвращаемое значение:
//  Строка - наименование экземпляра документа
//
Функция ПолучитьНаименование(Номер, Дата) Экспорт
	
	ТекстНаименование = Нстр("ru = '%1 от %2'");
	ТекстНаименование = СтрШаблон(ТекстНаименование, ?(ПустаяСтрока(Номер), "Неопределен", Номер), Формат(Дата, "ДФ=dd.MM.yyyy"));
	
	Возврат ТекстНаименование;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
