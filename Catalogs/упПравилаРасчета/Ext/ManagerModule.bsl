#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упПравилаРасчета.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Функция - Собрать данные произвольного расчета
//
// Параметры:
//  Ссылка - Ссылка	 - ссылка
// 
// Возвращаемое значение:
//  ДанныеПроизвольногоРасчета - данные.
//
Функция СобратьДанныеПроизвольногоРасчета(Ссылка) Экспорт 
	
	Если Ложь Тогда // Для контекстной подсказки
	    Ссылка = Справочники.упПравилаРасчета.ПустаяСсылка();
	КонецЕсли;
	ДанныеПроизвольногоРасчета = Ссылка.ДанныеПроизвольногоРасчета.Получить();
	Если ДанныеПроизвольногоРасчета <> Неопределено Тогда
		Если ЗначениеЗаполнено(Ссылка.ПоказательТарифнойСеткиПоСтолбцам) Тогда
			СоотвествиеСсылок = Новый Соответствие;
			Для Каждого СтрокаИдентификатора Из Ссылка.ИдентификаторыСсылок Цикл
				СоотвествиеСсылок.Вставить(СтрокаИдентификатора.Идентификатор, СтрокаИдентификатора.Значение);
			КонецЦикла;
			СтрокаЗаголовков = ДанныеПроизвольногоРасчета[0];
			Для Индекс = 0 По ДанныеПроизвольногоРасчета.Колонки.Количество() - 1 Цикл
				НовоеЗначение = СоотвествиеСсылок[СтрокаЗаголовков[Индекс]];
				Если НовоеЗначение <> Неопределено Тогда
					СтрокаЗаголовков[Индекс] = НовоеЗначение;
				КонецЕсли; 
			КонецЦикла;
			КолонкаЗаголовков = ДанныеПроизвольногоРасчета.Колонки[0];
			Для Индекс = 0 По ДанныеПроизвольногоРасчета.Количество() - 1 Цикл
				НовоеЗначение = СоотвествиеСсылок[ДанныеПроизвольногоРасчета[Индекс][0]];
				Если НовоеЗначение <> Неопределено Тогда
					ДанныеПроизвольногоРасчета[Индекс][0] = НовоеЗначение;
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли;
	Возврат ДанныеПроизвольногоРасчета;
	
КонецФункции
	
#КонецОбласти 