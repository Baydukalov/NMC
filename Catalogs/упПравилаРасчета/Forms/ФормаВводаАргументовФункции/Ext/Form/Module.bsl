
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мсвАргументов			= Параметры.ОписаниеФункции.Аргументы;
	Описание 				= Параметры.ОписаниеФункции.Подсказка;
	Шаблон	 				= Параметры.ОписаниеФункции.Шаблон;
	КоличествоАргументов	= Параметры.ОписаниеФункции.КоличествоАргументов;
	ТипРасчета				= Параметры.ТипРасчета;
	ТекущееКоличествоАргументов	= мсвАргументов.Количество();
	
	Элементы.АргументыФункции.Заголовок = Параметры.ОписаниеФункции.Наименование;
	
	шаблонФункции	= Параметры.ОписаниеФункции.Шаблон;
	
	мсвРеквизиты 			 = Новый Массив;
	мсвСтруктураЭлементов	 = Новый Массив; 	
	
	текИмя	= "Аргумент";
	й_Шаг 		= 1;
	
	Для каждого текЭлемент Из мсвАргументов Цикл
		
		
		Для каждого текАргумент Из текЭлемент Цикл
			текИмяРеквизита = текИмя + й_Шаг;
			ДобавитьРеквизит(мсвРеквизиты, текАргумент.Значение, текИмяРеквизита);		
			
			ПараметрыВыбора = Неопределено;
			
			Если текАргумент.Значение.СодержитТип(Тип("СправочникСсылка.упОценочнаяШкала")) Тогда
				НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипРасчета", ТипРасчета);
				НовыйМассив = Новый Массив();
				НовыйМассив.Добавить(НовыйПараметр);
				ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
			КонецЕсли;											
			
			мсвСтруктураЭлементов.Добавить(Новый Структура("Тип, Имя, Значение, ТолькоПросмотр, ПутьКДанным, Заголовок, ДействиеПриИзменении, ДействиеНачалоВыбора, ГруппаФормы, ВыборИзСписка, ПараметрыВыбора", 
											Тип("ПолеФормы"), 
											текИмяРеквизита, 
											0, 
											Ложь,
											текИмяРеквизита,
											текАргумент.Ключ,
											"ПолеВводаПриИзмененииЗначения",
											"ПолеВводНачалоВыбора",
											"АргументыФункции",
											текАргумент.Значение.СодержитТип(Тип("СправочникСсылка.упОценочнаяШкала")),
											ПараметрыВыбора));
														
			й_Шаг = й_Шаг + 1;
		КонецЦикла;
	КонецЦикла;
	
	ИзменитьРеквизиты(мсвРеквизиты);
	
	Для каждого текЭлемент Из мсвСтруктураЭлементов Цикл
		Отказ = Ложь;
		ДобавитьЭлементПолеФормы(текЭлемент, Отказ);
	КонецЦикла;									
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СформироватьФункцию();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолеВводаПриИзмененииЗначения(Элемент)
	СформироватьФункцию();
	Если КоличествоАргументов = -1 Тогда
		
		текИмяОбрабатываемогоРеквизита = Элемент.Имя;
		текНомерАргумента	= Число(Прав(текИмяОбрабатываемогоРеквизита, СтрДлина(текИмяОбрабатываемогоРеквизита) - СтрДлина("Аргумент")));
		
		Если текНомерАргумента = ТекущееКоличествоАргументов Тогда
			ТекущееКоличествоАргументов = ТекущееКоличествоАргументов + 1;
			текИмяРеквизита = "Аргумент" + ТекущееКоличествоАргументов;
			текЗаголовок	= "Число" + ТекущееКоличествоАргументов;
			ДобавитьЭлементНаФорму(текИмяРеквизита, текЗаголовок);	
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолеВводНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	
	Если НЕ ТипЗнч(ЭтаФорма[Элемент.Имя]) = Тип("СправочникСсылка.упОценочнаяШкала") Тогда
		списокВыбора = Новый СписокЗначений;
		
		текИмяРеквизита = Элемент.Имя;
		ЭтоДата 		= Ложь;
		текИмяТипа		= "Число";	
		
		Если НЕ СписокАргументовТипаДата.НайтиПоЗначению(текИмяРеквизита) = Неопределено Тогда
			текИмяТипа	= "Дата";	
			ЭтоДата 	= Истина;
		КонецЕсли;		
		
		сткДополнительныеПараметры = Новый Структура("ЭтоДата, ИмяРеквизита", ЭтоДата, текИмяРеквизита);
		
		списокВыбора.Добавить(текИмяТипа);
		списокВыбора.Добавить("Показатели расчета");
		списокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ОбработатьВыборТипа",ЭтаФорма, сткДополнительныеПараметры),, Элемент);
		
		СтандартнаяОбработка	= Ложь;

	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура завершения после ввода дат.
//
// Параметры:
//  ВведеннаяДата	 - Строка 	- Дата.
//  ДополнительныеПараметры - Структура	- Параметры.
//
Процедура ОбработатьВводДаты(ВведеннаяДата, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВведеннаяДата = Неопределено Тогда
		ЭтаФорма[ДополнительныеПараметры] = ВведеннаяДата;	
		СформироватьФункцию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора типа.
//
// Параметры:
//  ВыбранныйЭлемент - Элемент - Элемент.
//  ДополнительныеПараметры - Структура	- Параметры.
//
Процедура ОбработатьВыборТипа(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент  = Неопределено Тогда
		Если ВыбранныйЭлемент.Значение  = "Показатели расчета" Тогда
			сткПараметры = Новый Структура("ЭтоДата, Отбор", ДополнительныеПараметры.ЭтоДата, Новый Структура("ТипРасчета",ТипРасчета));
			ЭтаФорма[ДополнительныеПараметры.ИмяРеквизита] = ПредопределенноеЗначение("Справочник.упПоказателиРасчета.ПустаяСсылка");
			ОткрытьФорму("Справочник.упПоказателиРасчета.ФормаВыбора", сткПараметры, Элементы[ДополнительныеПараметры.ИмяРеквизита]);
		Иначе
			Если ДополнительныеПараметры.ЭтоДата Тогда
				ЭтаФорма[ДополнительныеПараметры.ИмяРеквизита] = '00010101';	
				ПоказатьВводДаты(Новый ОписаниеОповещения("ОбработатьВводДаты",ЭтаФорма, ДополнительныеПараметры.ИмяРеквизита),ТекущаяДата());
			Иначе
				ЭтаФорма[ДополнительныеПараметры.ИмяРеквизита] = 0;		
			КонецЕсли;
			СформироватьФункцию();
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакончитьЗаполнениеАргументов(Команда)
	
	сткПараметры = Новый Структура("ФункцияСПараметрами", ЭтаФорма.Функция);
	ОповеститьОВыборе(сткПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
// Процедура - Формирует массив реквизитов.
//
// Параметры:
//  мсвДобавляемыеРеквизиты	 - Массив 	- массив в который добавляется реквизит.
//  ТипРеквизита			 - Тип 		- тип реквизита.
//  Имя						 - Строка	- имя реквизита.
//
Процедура ДобавитьРеквизит(мсвДобавляемыеРеквизиты, ТипРеквизита, Имя)

	Если ТипРеквизита.СодержитТип(Тип("СправочникСсылка.упОценочнаяШкала")) Тогда
		текТип	= ТипРеквизита;
	Иначе
		текТип	= Новый ОписаниеТипов(ТипРеквизита, "СправочникСсылка.упПоказателиРасчета");
	КонецЕсли;
	текРеквизит = Новый РеквизитФормы(Имя, текТип);
	мсвДобавляемыеРеквизиты.Добавить(текРеквизит);
	
	Если ТипРеквизита.СодержитТип(Тип("Дата")) Тогда
		СписокАргументовТипаДата.Добавить(Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Процедура - Добавляет реквизит и элемент на форму.
//
// Параметры:
//  ИмяРеквизита	 - Строка - имя добавляемого элемента.
//  Заголовок		 - Строка - заголовок.
//
Процедура ДобавитьЭлементНаФорму(ИмяРеквизита, Заголовок)
	
	мсвРеквизиты = Новый Массив;
	
	ДобавитьРеквизит(мсвРеквизиты, Новый ОписаниеТипов("Число") , ИмяРеквизита);		
	
	ИзменитьРеквизиты(мсвРеквизиты);
	
	сткЭлемент = Новый Структура("Тип, Имя, Значение, ТолькоПросмотр, ПутьКДанным, Заголовок, ДействиеПриИзменении, ДействиеНачалоВыбора, ГруппаФормы, ВыборИзСписка", 
											Тип("ПолеФормы"), 
											ИмяРеквизита, 
											0, 
											Ложь,
											ИмяРеквизита,
											Заголовок,
											"ПолеВводаПриИзмененииЗначения",
											"ПолеВводНачалоВыбора",
											"АргументыФункции",
											Ложь);
	Отказ = Ложь;
	ДобавитьЭлементПолеФормы(сткЭлемент, Отказ);
	
КонецПроцедуры

&НаСервере
// Процедура - Добавляет на форму элемент типа "ПолеФормы".
//
// Параметры:
//  Элемент	 - Структура - Описание элемента формы.
//  Отказ	 - Булево	 - Истина, в случае ошибки при добавлении.
//
Процедура ДобавитьЭлементПолеФормы(Элемент, Отказ)
	
	ЭлементФормы	= Элементы.Добавить(Элемент.Имя, Тип("ПолеФормы"), Элементы[Элемент.ГруппаФормы]);
	
	ЭлементФормы.РастягиватьПоГоризонтали	= Истина;

	текВид = Неопределено;
	Если НЕ Элемент.Свойство("Вид", текВид) Тогда
		текВид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	ЭлементФормы.Вид = текВид;
	
	текЗаголовок	= Неопределено;
	Если Элемент.Свойство("Заголовок", текЗаголовок) Тогда
		ЭлементФормы.Заголовок = текЗаголовок;			
	Иначе
		ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	КонецЕсли;
	
	текВыборИзСписка = Неопределено;
	Если Элемент.Свойство("ВыборИзСписка", текВыборИзСписка) Тогда
		ЭлементФормы.КнопкаВыпадающегоСписка  = текВыборИзСписка;
	КонецЕсли;
	

	текТолькоПросмотр =  Неопределено;
	Если Элемент.Свойство("ТолькоПросмотр", текТолькоПросмотр) Тогда
		ЭлементФормы.ТолькоПросмотр = текТолькоПросмотр;			
	КонецЕсли;
	
	текПутьКДанным =  Неопределено;
	Если Элемент.Свойство("ПутьКДанным", текПутьКДанным) Тогда
		ЭлементФормы.ПутьКДанным = текПутьКДанным;			
	КонецЕсли;

	
	текПодсказка = Неопределено;
	Если Элемент.Свойство("Подсказка", текПодсказка) Тогда
		ЭлементФормы.Подсказка				= текПодсказка;
		ЭлементФормы.ОтображениеПодсказки	= ОтображениеПодсказки.Кнопка;	
	КонецЕсли;
	
	текДействиеПриИзменении = Неопределено;
	Если Элемент.Свойство("ДействиеПриИзменении", текДействиеПриИзменении) Тогда
		ЭлементФормы.УстановитьДействие("ПриИзменении", текДействиеПриИзменении);		
	КонецЕсли;
	
	текДействиеНачалоВыбора = Неопределено;
	Если Элемент.Свойство("ДействиеНачалоВыбора", текДействиеНачалоВыбора) Тогда
		ЭлементФормы.УстановитьДействие("НачалоВыбора", текДействиеНачалоВыбора);		
	КонецЕсли;

	текЭтоГиперссылка = Неопределено;
	Если Элемент.Свойство("Гиперссылка", текЭтоГиперссылка) Тогда
		ЭлементФормы.Гиперссылка = текЭтоГиперссылка;			
	КонецЕсли;

	текПараметрыВыбора = Неопределено;
	Если Элемент.Свойство("ПараметрыВыбора", текПараметрыВыбора) И ЗначениеЗаполнено(текПараметрыВыбора) Тогда
		ЭлементФормы.ПараметрыВыбора = текПараметрыВыбора;
	КонецЕсли;
	

	Если текВид = ВидПоляФормы.ПолеКартинки Тогда
		ЭлементФормы.РастягиватьПоГоризонтали = Ложь;
		ЭлементФормы.РастягиватьПоВертикали = Ложь;
		ЭлементФормы.Ширина = 2;
		ЭлементФормы.Высота = 1;
		ЭлементФормы.КартинкаЗначений = БиблиотекаКартинок.упТипыПоказателей;
		ЭлементФормы.Рамка	= Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	КонецЕсли;
	
	текЗначение = Неопределено;
	Если Элемент.Свойство("Значение", текЗначение) Тогда
		ЭтаФорма[Элемент.Имя] = текЗначение;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СформироватьФункцию()
	текПозицияСкобки = Найти(Шаблон, "(");
	
	текФункция = "";
	Если текПозицияСкобки > 0 Тогда
		текФункция = Лев(Шаблон, текПозицияСкобки);	
	КонецЕсли;
		
	Для й = 1 По ТекущееКоличествоАргументов Цикл
		текАргумент = ЭтаФорма["Аргумент"+й];
		
		текИдентификатор = "";
		Если ТипЗнч(текАргумент) = Тип("Дата") Тогда
			текИдентификатор = "'"+Формат(Год(текАргумент),"ЧГ=0") 
							+ Формат(Месяц(текАргумент), "ЧЦ=2; ЧН=; ЧВН=") 
							+ Формат(День(текАргумент), "ЧЦ=2; ЧН=; ЧВН=") 
							+ Формат(Час(текАргумент), "ЧЦ=2; ЧН=; ЧВН=") 
							+ Формат(Минута(текАргумент), "ЧЦ=2; ЧН=; ЧВН=") 
							+ Формат(Секунда(текАргумент), "ЧЦ=2; ЧН=; ЧВН=")+"'";
		ИначеЕсли ТипЗнч(текАргумент) = Тип("СправочникСсылка.упПоказателиРасчета") 
					ИЛИ ТипЗнч(текАргумент) = Тип("СправочникСсылка.упОценочнаяШкала")  Тогда 
			текИдентификатор = ПолучитьИдентификатор(текАргумент); 					
		КонецЕсли;
						
		текФункция = текФункция + текИдентификатор +", ";	
	КонецЦикла;
	
	Если ТекущееКоличествоАргументов > 0 Тогда
		текФункция = Лев(текФункция, СтрДлина(текФункция) - 2);		
	КонецЕсли;
	
	ЭтаФорма.Функция = текФункция + ")";

	
КонецПроцедуры

&НаСервере
Функция ПолучитьИдентификатор(ПоказательРасчета)
	Возврат Сокрлп(ПоказательРасчета.Код);
КонецФункции

#КонецОбласти

