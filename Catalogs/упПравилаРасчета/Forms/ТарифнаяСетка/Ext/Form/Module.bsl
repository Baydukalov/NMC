&НаКлиенте
Перем ОтветПередЗакрытием;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.АдресПроизвольныхДанных = "" Тогда
		ДобавитьКолонкиНаСервере(1);
		ПерваяСтрока = Таблица.Добавить();
		ВтораяСтрока = Таблица.Добавить();
	Иначе
		ТарифнаяСетка = ПолучитьИзВременногоХранилища(Параметры.АдресПроизвольныхДанных);
		ДобавитьКолонкиНаСервере(ТарифнаяСетка.Колонки.Количество() - 1);
		Таблица.Загрузить(ТарифнаяСетка);
		УдалитьИзВременногоХранилища(Параметры.АдресПроизвольныхДанных);
	КонецЕсли; 
	ЭтаФорма.ПоказательПоСтолбцам = Параметры.ПоказательПоСтолбцам;
	ЭтаФорма.ПоказательПоСтрокам = Параметры.ПоказательПоСтрокам;
	ПриИзмененииПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Если ОтветПередЗакрытием <> Истина Тогда
			Отказ = Истина;
			ОповещениеВопросаОЗакрытии = Новый ОписаниеОповещения("ОповещениеВопросаОЗакрытии", ЭтаФорма, Новый Структура());
			ПоказатьВопрос(ОповещениеВопросаОЗакрытии, "Данные были изменены. Закрыть форму без их применения?", РежимДиалогаВопрос.ОКОтмена);
		КонецЕсли
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Элементы.Таблица.ТекущаяСтрока = Таблица[0].ПолучитьИдентификатор() И Элементы.Таблица.ТекущийЭлемент = Элементы.Таблица.ПодчиненныеЭлементы[0] Тогда
	ИначеЕсли Элементы.Таблица.ТекущаяСтрока = Таблица[0].ПолучитьИдентификатор() Тогда
		Если ЗначениеЗаполнено(ПоказательПоСтолбцам) Тогда
			Для ИндексКолонки = 1 По Элементы.Таблица.ПодчиненныеЭлементы.Количество() - 1 Цикл
				ИмяКолонки = "Колонка" + ИндексКолонки;
				ИмяПоля = Элементы.Таблица.Имя + ИмяКолонки;
				Элементы.Таблица.ТекущиеДанные[ИмяКолонки] = ТипЗначенийПоказателяСтолбцов.ПривестиЗначение(Элементы.Таблица.ТекущиеДанные[ИмяКолонки]);
				Элементы[ИмяПоля].ОграничениеТипа = ТипЗначенийПоказателяСтолбцов;
		КонецЦикла;
		КонецЕсли; 
	ИначеЕсли Элементы.Таблица.ТекущийЭлемент <> Неопределено И Элементы.Таблица.ТекущаяСтрока <> Неопределено Тогда
		Если ЗначениеЗаполнено(ПоказательПоСтрокам) Тогда
			Элементы.Таблица.ТекущиеДанные.Колонка0 = ТипЗначенийПоказателяСтрок.ПривестиЗначение(Элементы.Таблица.ТекущиеДанные.Колонка0);
			Элементы.Таблица.ПодчиненныеЭлементы[0].ОграничениеТипа = ТипЗначенийПоказателяСтрок;
		КонецЕсли; 
		ТипЗначенийДанных = Новый ОписаниеТипов("Число");
		Для ИндексКолонки = 1 По Элементы.Таблица.ПодчиненныеЭлементы.Количество() - 1 Цикл
			ИмяКолонки = "Колонка" + ИндексКолонки;
			ИмяПоля = Элементы.Таблица.Имя + ИмяКолонки;
			Элементы[ИмяПоля].ОграничениеТипа = ТипЗначенийДанных;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПоСтрокамПриИзменении(Элемент)
	
	ПриИзмененииПоказателей();

КонецПроцедуры

&НаКлиенте
Процедура ПоказательПоСтолбцамПриИзменении(Элемент)
	
	ПриИзмененииПоказателей();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	СортироватьОсиНаСервере();
	ОтветПередЗакрытием = Истина;
	Закрыть(Новый Структура("ПоказательПоСтолбцам, ПоказательПоСтрокам, АдресПроизвольныхДанных", ПоказательПоСтолбцам, ПоказательПоСтрокам, ПолучитьАдресТаблицы()));
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКолонку(Команда)
	
	УдалитьКолонкуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКолонкуНаСервере()
	
	Если Элементы.Таблица.ТекущийЭлемент = Неопределено Или Элементы.Таблица.ТекущийЭлемент = Элементы.ТаблицаЛегенда Тогда
		Возврат;
	КонецЕсли;
	ФрагментыПути = СтрРазделить(Элементы.Таблица.ТекущийЭлемент.ПутьКДанным, ".");
	ИмяКолонки = ФрагментыПути[ФрагментыПути.ВГраница()];
	ТаблицаЗначений = Таблица.Выгрузить();
	ТаблицаЗначений.Колонки.Удалить(ТаблицаЗначений.Колонки.Индекс(ТаблицаЗначений.Колонки[ИмяКолонки]));
	Для Индекс = 0 По ТаблицаЗначений.Колонки.Количество() - 1 Цикл
		ТаблицаЗначений.Колонки[Индекс].Имя = "Колонка" + Индекс;
	КонецЦикла;
	Таблица.Загрузить(ТаблицаЗначений);
	УдаляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты.Добавить(Элементы.Таблица.ПутьКДанным + ".Колонка" + ТаблицаЗначений.Колонки.Количество());
	ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	Если Элементы.Таблица.ТекущаяСтрока = Неопределено Или Элементы.Таблица.ТекущаяСтрока = Таблица[0] Тогда
		Возврат;
	КонецЕсли; 
	Таблица.Удалить(Таблица.Индекс(Элементы.Таблица.ТекущиеДанные));
	
КонецПроцедуры

&НаКлиенте
Процедура Сортировать(Команда)
	
	СортироватьОсиНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонкиИСтроки(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеДобавитьКолонкиИСтроки", ЭтаФорма);
	ОткрытьФорму("Справочник.упПравилаРасчета.Форма.ДобавлениеСтрокИСтолбцов",, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ОповещениеДобавитьКолонкиИСтроки(Результат, ДопПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат.ДобавитьСтолбцов > 0 Тогда
		ДобавитьКолонкиНаСервере(Результат.ДобавитьСтолбцов);
	КонецЕсли; 
	Для Счетчик = 1 По Результат.ДобавитьСтрок Цикл
		Таблица.Добавить();
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросаОЗакрытии(Результат, ДопПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ОтветПередЗакрытием = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкиНаСервере(ДобавитьСтолбцов)
	
	ТекущиеРеквизиты = ПолучитьРеквизиты("Таблица");
	ДобавляемыеРеквизиты = Новый Массив;
	Для Счетчик = 1 По ДобавитьСтолбцов Цикл
		РеквизитТаблицы = Новый РеквизитФормы("Колонка" + (ТекущиеРеквизиты.Количество() - 1 + Счетчик), ТекущиеРеквизиты[0].ТипЗначения, "Таблица");
		ДобавляемыеРеквизиты.Добавить(РеквизитТаблицы);
	КонецЦикла;
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	Для Каждого ДобавленныйРеквизит Из ДобавляемыеРеквизиты Цикл
		ПолеФормы = Элементы.Добавить(Элементы.Таблица.Имя + ДобавленныйРеквизит.Имя, Тип("ПолеФормы"), Элементы.Таблица);
		ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
		ПолеФормы.ПутьКДанным = ДобавленныйРеквизит.Путь + "." + ДобавленныйРеквизит.Имя;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПоказателей()
	
	ОбновитьЗаголовкиПоказателейВТаблице();
	МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(ПоказательПоСтрокам.ПараметрРасчета));
	ЭтаФорма.ТипЗначенийПоказателяСтрок = Новый ОписаниеТипов(МассивТипов);
	МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(ПоказательПоСтолбцам.ПараметрРасчета));
	ЭтаФорма.ТипЗначенийПоказателяСтолбцов = Новый ОписаниеТипов(МассивТипов);
	СортироватьОсиНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовкиПоказателейВТаблице()
	
	Таблица[0].Колонка0 = "" + ПоказательПоСтрокам + " / " + ПоказательПоСтолбцам;

КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицы()
	
	Результат = ПоместитьВоВременноеХранилище(Таблица.Выгрузить(), Новый УникальныйИдентификатор);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаПриИзменении(Элемент)
	
	Если Ложь
		Или Элементы.Таблица.ТекущаяСтрока = Таблица[0].ПолучитьИдентификатор() 
		Или Элементы.Таблица.ТекущийЭлемент = Элементы.Таблица.ПодчиненныеЭлементы[0] 
	Тогда
		//СортироватьОсиНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СортироватьОсиНаСервере()
	
	Если Ложь
		Или Не ЗначениеЗаполнено(ПоказательПоСтолбцам) 
		Или Не ЗначениеЗаполнено(ПоказательПоСтрокам) 
	Тогда
		Возврат;
	КонецЕсли; 
	СлужебноеЗначение = "" + Новый УникальныйИдентификатор;
	ТаблицаЗначений = Таблица.Выгрузить();
	Если Ложь Тогда // Для контекстной подсказки
	    ТаблицаЗначений = Новый ТаблицаЗначений;
	КонецЕсли;
	ТаблицаЗначений[0][0] = СлужебноеЗначение;
	ТаблицаЗначений = упСервисныеФункции.ТранспонироватьТаблицуЗначений(ТаблицаЗначений);
	Если Ложь Тогда // Для контекстной подсказки
	    ТаблицаЗначений = Новый ТаблицаЗначений;
	КонецЕсли;
	//Если Не упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗначенийПоказателяСтолбцов.Типы()[0]) Тогда
		ТаблицаЗначений.Сортировать(ТаблицаЗначений.Колонки[0].Имя);
	//КонецЕсли; 
	ПерваяСтрока = ТаблицаЗначений.Найти(СлужебноеЗначение);
	ТаблицаЗначений.Сдвинуть(ПерваяСтрока, -ТаблицаЗначений.Индекс(ПерваяСтрока));
	ТаблицаЗначений = упСервисныеФункции.ТранспонироватьТаблицуЗначений(ТаблицаЗначений);
	Если Ложь Тогда // Для контекстной подсказки
	    ТаблицаЗначений = Новый ТаблицаЗначений;
	КонецЕсли;
	//Если Не упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗначенийПоказателяСтрок.Типы()[0]) Тогда
		ТаблицаЗначений.Сортировать(ТаблицаЗначений.Колонки[0].Имя);
	//КонецЕсли; 
	ПерваяСтрока = ТаблицаЗначений.Найти(СлужебноеЗначение);
	ТаблицаЗначений.Сдвинуть(ПерваяСтрока, -ТаблицаЗначений.Индекс(ПерваяСтрока));
	Таблица.Загрузить(ТаблицаЗначений);
	ОбновитьЗаголовкиПоказателейВТаблице();
	
КонецПроцедуры

#КонецОбласти
