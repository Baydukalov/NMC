#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = Справочники.упПравилаРасчета.СоздатьЭлемент();
	КонецЕсли;
	Если Не Параметры.Свойство("ТолькоПросмотр") Тогда
		ОбновитьДоступность(ЭтаФорма);
	КонецЕсли; 
	ЭтаФорма.ИмяФайлаВнешнейОбработки = Объект.ВнешняяОбработка.Наименование;
	ЭтаФорма.АдресДанныхПроизвольногоРасчета = ПоместитьВоВременноеХранилище(ТекущийОбъект.ДанныеПроизвольногоРасчета.Получить(), УникальныйИдентификатор);
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ПоказателиРасчета, 	"ТипРасчета", Объект.ТипРасчета, ВидСравненияКомпоновкиДанных.Равно);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ОценочныеШкалы, 		"ТипРасчета", Объект.ТипРасчета, ВидСравненияКомпоновкиДанных.Равно);
	
	Если Объект.ТипРасчета = Перечисления.упТипРасчета.Топливо Тогда
		Элементы.РасчетПроводитсяПоКаждойСтроке.Видимость = Ложь;
	КонецЕсли;
	
	ЕстьШкалы = Истина;
	
	// В список загружаются переменные используемые в формуле.
	тбзПеременных 				 = Объект.Переменные.Выгрузить(,"Идентификатор"); 
	мсвИдентификаторовПеременных = тбзПеременных.ВыгрузитьКолонку("Идентификатор");
	СписокПеременных.ЗагрузитьЗначения(мсвИдентификаторовПеременных);
	
	// Загружаются допустимые функции.
	СписокФункций.Загрузить(упТарификацияВызовСервера.ПолучитьТаблицуДопустимыхФункций(ЕстьШкалы));
	СписокДопустимыхФункций.ЗагрузитьЗначения(упТарификацияВызовСервера.ПолучитьСписокДопустимыхФункций(ЕстьШкалы));
	
	ЗаполнитьТаблицуПоказателей();
	ОбновитьДоступность(ЭтаФорма);
	Если ЗначениеЗаполнено(Объект.ВнешняяОбработка) тогда
		ЭтаФорма.АдресФайла = ПолучитьНавигационнуюСсылку(Объект.ВнешняяОбработка, "ТекстХранилище");
	Иначе
		ЭтаФорма.ИмяФайлаВнешнейОбработки = "Файл не указан";
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов.
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов.
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если СтрДлина(Объект.Формула) > 0 Тогда
		Элементы.Формула.УстановитьГраницыВыделения(СтрДлина(Объект.Формула),СтрДлина(Объект.Формула));	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанНовыйПоказатель" Тогда
		Элементы.ПоказателиРасчета.Обновить();
		ЗаполнитьТаблицуПоказателей();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ТекущийОбъект = Справочники.упПравилаРасчета.СоздатьЭлемент();
	КонецЕсли;
	Если Модифицированность Тогда
		Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Произвольный") Тогда
		Иначе
			ТекущийОбъект.СкомпилированнаяФормула = СкомпилироватьФормулуНаСервере(ТекущийОбъект.Формула, Отказ);
			Если НЕ Отказ Тогда
				тбзПеременные	= упТарификацияВызовСервера.ПолучитьПеременные(СписокПеременных.ВыгрузитьЗначения(), Ложь);
				Если НЕ тбзПеременные = Неопределено Тогда
					ТекущийОбъект.Переменные.Загрузить(тбзПеременные);	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли; 
	Если ЗначениеЗаполнено(АдресДанныхПроизвольногоРасчета) Тогда
		ДанныеПроизвольногоРасчета = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресДанныхПроизвольногоРасчета));
	Иначе 
		ДанныеПроизвольногоРасчета = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли; 
	ТекущийОбъект.ДанныеПроизвольногоРасчета = ДанныеПроизвольногоРасчета;
	ОбновитьДанныеФайлаВБазе(ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФайлаВБазе(Знач ТекущийОбъект)
	
	Если ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		Если Не ТекущийОбъект.ВнешняяОбработка.Пустая() Тогда
			НовыйФайл = ТекущийОбъект.ВнешняяОбработка.ПолучитьОбъект();
		Иначе
			НовыйФайл = Справочники.Файлы.СоздатьЭлемент();
		КонецЕсли;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Попытка
			СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ИмяФайлаВнешнейОбработки);
			
			НовыйФайл.ВладелецФайла = Справочники.ПапкиФайлов.ПравилаРасчета;
			НовыйФайл.ТекстХранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФайла));
			НовыйФайл.Наименование = СтруктураИмениФайла.ИмяБезРасширения;
			НовыйФайл.Описание = ИмяФайлаВнешнейОбработки;
			НовыйФайл.ПолноеНаименование = СтруктураИмениФайла.ИмяБезРасширения;
			НовыйФайл.ТекущаяВерсияДатаМодификацииФайла = ТекущаяУниверсальнаяДата();
			НовыйФайл.Автор = Пользователи.ТекущийПользователь();
			НовыйФайл.ХранитьВерсии = Ложь;
			НовыйФайл.Записать();
			
			СведенияОФайле 		= РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
			СведенияОФайле.ИмяБезРасширения 			= СтруктураИмениФайла.ИмяБезРасширения;
			СведенияОФайле.РасширениеБезТочки 			= ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураИмениФайла.Расширение);
			СведенияОФайле.ХранитьВерсии 				= НовыйФайл.ХранитьВерсии;
			СведенияОФайле.ВремяИзмененияУниверсальное 	= НовыйФайл.ТекущаяВерсияДатаМодификацииФайла;
			СведенияОФайле.АдресВременногоХранилищаФайла = АдресФайла;
			Версия = РаботаСФайламиСлужебныйВызовСервера.СоздатьВерсию(НовыйФайл.Ссылка, СведенияОФайле);
			РаботаСФайламиСлужебныйВызовСервера.ОбновитьВерсиюВФайле(НовыйФайл.Ссылка, Версия, СведенияОФайле.АдресВременногоХранилищаФайла);
			ЗафиксироватьТранзакцию();
			УдалитьИзВременногоХранилища(СведенияОФайле.АдресВременногоХранилищаФайла);
		Исключение
			ОтменитьТранзакцию();
			ОписаниеОшибки = ОписаниеОшибки();
			ЗаписьЖурналаРегистрации("Ошибка записи", УровеньЖурналаРегистрации.Ошибка,, НовыйФайл.Ссылка, ОписаниеОшибки);
		КонецПопытки;
		
		ТекущийОбъект.ВнешняяОбработка 	= НовыйФайл.Ссылка;
		ЭтаФорма.ИмяФайлаВнешнейОбработки 	= ТекущийОбъект.ВнешняяОбработка.Наименование;
	КонецЕсли;

КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		текЗначение = "";
		Если ВыбранноеЗначение.Свойство("Аргументы") Тогда
			
			Если  ВыбранноеЗначение.КоличествоАргументов = 0 Тогда
				ВставитьТекстВФормулу(" " + СокрЛП(ВыбранноеЗначение.Шаблон));	
			Иначе
				сткПараметры =Новый Структура("ОписаниеФункции, ТипРасчета", ВыбранноеЗначение, Объект.ТипРасчета);
				ОткрытьФорму("Справочник.упПравилаРасчета.Форма.ФормаВводаАргументовФункции",сткПараметры,ЭтаФорма,УникальныйИдентификатор);				
			КонецЕсли;

		ИначеЕсли ВыбранноеЗначение.Свойство("ФункцияСПараметрами", текЗначение) Тогда
			ВставитьТекстВФормулу(" " + СокрЛП(текЗначение));
		КонецЕсли;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.упПоказателиРасчета") Тогда	
		ВставитьТекстВФормулу(" " + СокрЛП(ВыбранноеЗначение));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПравило(Команда)
	
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула") Тогда
		Отказ = Ложь;
		Объект.СкомпилированнаяФормула = СкомпилироватьФормулуНаСервере(Объект.Формула, Отказ);
		Если НЕ Отказ Тогда
			сткПараметры = Новый Структура("Формула, СкомпилированнаяФормула, Переменные", Объект.Формула, Объект.СкомпилированнаяФормула, СписокПеременных);
			ОткрытьФорму("Справочник.упПравилаРасчета.Форма.ФормаПроверки", сткПараметры);
		КонецЕсли;
	Иначе
		Если Модифицированность Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗаписатьИВычислитьПроизвольныйРасчет", ЭтаФорма);
			ПоказатьВопрос(ОписаниеОповещения, "Для продолжения нужно записать объект. Продолжить?", РежимДиалогаВопрос.ОКОтмена);
		Иначе
			ВычислитьПроизвольныйРасчет();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИВычислитьПроизвольныйРасчет(Результат, ДопПараметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли; 
	ЭтаФорма.Записать();
	ВычислитьПроизвольныйРасчет();

КонецПроцедуры

&НаКлиенте
Процедура ВычислитьПроизвольныйРасчет()
	
	Отказ = Ложь;
	СтруктураЗначенийПеременных = Новый Структура;
	Для Каждого СтрокаПеременной Из Объект.Переменные Цикл
		Если ТипЗнч(СтрокаПеременной.Значение) <> Тип("СправочникСсылка.упПоказателиРасчета") Тогда 
			СтруктураЗначенийПеременных.Вставить(СтрокаПеременной.Идентификатор, СтрокаПеременной.Значение);
		Иначе
			ПараметрРасчета = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(СтрокаПеременной.Значение, "ПараметрРасчета");
			МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(упТарификацияКлиентСервер.ПолучитьТипПараметра(ПараметрРасчета));
			ТипЗначенийПоказателя = Новый ОписаниеТипов(МассивТипов);
			СтруктураЗначенийПеременных.Вставить(СтрокаПеременной.Идентификатор, ТипЗначенийПоказателя.ПривестиЗначение());
		КонецЕсли;
	КонецЦикла;
	Попытка
		упТарификацияВызовСервера.ВычислитьФормулуПравилаРасчета(Объект.Ссылка, СтруктураЗначенийПеременных,Отказ);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	Если ОписаниеОшибки <> Неопределено Тогда
		ТекстСообщения = Нстр("ru = 'Ошибка проверки расчета: " + ОписаниеОшибки + "'");
	ИначеЕсли Отказ Тогда 
		//ТекстСообщения = Нстр("ru = 'Проверка произвольного расчета завершилась ошибкой'");
	Иначе
		ТекстСообщения = Нстр("ru = 'Проверка произвольного расчета выполнена успешно'");
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 

КонецПроцедуры // ПроверитьФормулу()

&НаКлиенте
Процедура ВставитьТестовуюФормулу(Команда)
	
	Если НЕ СокрЛП(Объект.Формула) = "" Тогда
		текВопрос	= Нстр("ru = 'Поле формула не пустое. Очистить?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ВставитьТекстТестовойФормулы", ЭтаФорма), текВопрос,РежимДиалогаВопрос.ДаНет);
	Иначе
		текФормула = ПолучитьДемоФормулуНаСервере();
		ВставитьТекстВФормулу(" " + СокрЛП(текФормула));		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТернарныйОператор(Команда)
	ВставитьТекстВФормулу(" " + СокрЛП("?(,,)"));
КонецПроцедуры

&НаКлиенте
Процедура ВставитьФункцию(Команда)
	сткПараметры = Новый Структура("ТипРасчета", Объект.ТипРасчета);
	ОткрытьФорму("Справочник.упПравилаРасчета.Форма.ФормаВыбораФункции", ,ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ВставитьПоказатель(Команда)
	сткПараметры =Новый Структура("Отбор, ВернутьКод",Новый Структура("ТипРасчета",Объект.ТипРасчета), Истина);
	ОткрытьФорму("Справочник.упПоказателиРасчета.Форма.ФормаВыбора", сткПараметры, ЭтаФорма, УникальныйИдентификатор,,,Новый ОписаниеОповещения("ОбработкаОповещенияВыбораПоказателя", ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ВставитьПоказательВФормулу(Команда)
	
	Если НЕ Элементы.ПоказателиРасчета.ТекущиеДанные = Неопределено Тогда
		ВставитьТекстВФормулу(" " + СокрЛП(Элементы.ПоказателиРасчета.ТекущиеДанные.Идентификатор));	
	КонецЕсли;

КонецПроцедуры // ВставитьПоказательВФормулу()

&НаКлиенте
Процедура ВставитьШкалуВФормулу(Команда)
	
	ВставитьТекстВФормулу(" " + СокрЛП(Элементы.ОценочныеШкалы.ТекущиеДанные.Шкала));
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТарифнуюСеткуВФормулу(Команда)
	Если НЕ Элементы.ТарифныеСетки.ТекущиеДанные = Неопределено Тогда
		ВставитьТекстВФормулу(" " + СокрЛП(Элементы.ТарифныеСетки.ТекущиеДанные.ТарифнаяСетка));	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоказателиРасчетаНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула") Тогда
		ПараметрыПеретаскивания.Значение = Сокрлп(Элемент.ТекущиеДанные.Идентификатор);
	Иначе
		ПараметрыПеретаскивания.Значение = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
			СтруктураЗначения = Новый Структура("Идентификатор, Ссылка");
			ДанныеСтроки = Элемент.ДанныеСтроки(ВыделеннаяСтрока);
			СтруктураЗначения.Идентификатор = ДанныеСтроки.Идентификатор; 
			СтруктураЗначения.Ссылка = ДанныеСтроки.Ссылка; 
			ПараметрыПеретаскивания.Значение.Добавить(СтруктураЗначения);
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры // ПоказателиРасчетаНачалоПеретаскивания()

&НаКлиенте
Процедура ПоказателиРасчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула") Тогда
		ВставитьТекстВФормулу(" " + СокрЛП(Элемент.ТекущиеДанные.Идентификатор));
	КонецЕсли; 
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	текЗначение = ПараметрыПеретаскивания.Значение;
	Если ТипЗнч(текЗначение) = Тип("Массив") И текЗначение.Количество() > 0  Тогда
		ПараметрыПеретаскивания.Значение = СписокФункций.НайтиПоИдентификатору(текЗначение[0]).Шаблон;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	текФункция = Элементы.СписокФункций.ТекущиеДанные.Шаблон;
	ВставитьТекстВФормулу(" " + СокрЛП(текФункция));
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийПриАктивизацииСтроки(Элемент)
	ОписаниеФункции = Элемент.ТекущиеДанные.Подсказка;
КонецПроцедуры

&НаКлиенте
Процедура ОценочныеШкалыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула") Тогда
		ПараметрыПеретаскивания.Значение = Сокрлп(Элемент.ТекущиеДанные.Шкала);
	Иначе
		ПараметрыПеретаскивания.Значение = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
			СтруктураЗначения = Новый Структура("Идентификатор, Ссылка");
			ДанныеСтроки = Элемент.ДанныеСтроки(ВыделеннаяСтрока);
			СтруктураЗначения.Идентификатор = СокрЛП(ДанныеСтроки.Шкала); 
			СтруктураЗначения.Ссылка = ДанныеСтроки.Ссылка; 
			ПараметрыПеретаскивания.Значение.Добавить(СтруктураЗначения);
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОценочныеШкалыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВставитьТекстВФормулу(" " + СокрЛП(Элемент.ТекущиеДанные.Шкала));
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТарифныеСеткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВставитьТекстВФормулу(" " + СокрЛП(Элемент.ТекущиеДанные.ТарифнаяСетка));
	СтандартнаяОбработка = Ложь;
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
// Проедура завершения после выбора показателя.
//
// Параметры:
//  РезультатЗакрытия  - КодВозвратаДиалога - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОбработкаОповещенияВыбораПоказателя(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатЗакрытия = Неопределено Тогда
		ВставитьТекстВФормулу(" " + СокрЛП(РезультатЗакрытия));	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
// Процедура - Вставляет текст в поле формулы.
//
// Параметры:
//  ТекстДляВставки	 - Строка - текст предназначенный для вставки.
//
Процедура ВставитьТекстВФормулу(ТекстДляВставки)

	Модифицированность = Истина;
	
	ЭлементФормула		= Элементы.Формула;
	
	текНачалоСтроки		= 0;
	текНачалоКолонки	= 0;
	текКонецСтроки		= 0;
	текКонецКолонки		= 0;
	
	ЭлементФормула.ПолучитьГраницыВыделения(текНачалоСтроки, текНачалоКолонки, текКонецСтроки, текКонецКолонки);
	текПозицияКурсораX	= текКонецСтроки;
	текПозицияКурсораY	= текКонецКолонки;
		
	ЭлементФормула.УстановитьГраницыВыделения(текПозицияКурсораX, текПозицияКурсораY, текПозицияКурсораX, текПозицияКурсораY);
	ЭлементФормула.ВыделенныйТекст = ТекстДляВставки;
	ТекущийЭлемент = Элементы.Формула;
	Активизировать();
КонецПроцедуры // ВставитьТекстВФормулу()

&НаСервере
// Функция - Возвращает формулу в том виде в котором она может выполнятся, при этом заполняется список,
//				переменных, задействованных в формуле.
//
// Параметры:
//  Формула	 - Строка - Текст формулы.
//  Отказ	 - Булево - Если компиляция невозможна, то устанавливается в Истина.
//
Функция СкомпилироватьФормулуНаСервере(Формула, Отказ)
	текРезультат = "";
	мсвИдентификаторовПеременных = Неопределено;
	сткИнформацияобОшибке		 = Неопределено;
	текРезультат = упТарификацияКлиентСервер.СкомпилироватьФормулуПравилаРасчета(Формула, мсвИдентификаторовПеременных,СписокДопустимыхПеременных, СписокДопустимыхФункций, Отказ, сткИнформацияобОшибке);
	Если НЕ сткИнформацияобОшибке = Неопределено Тогда
		текНомерСтроки 	= сткИнформацияобОшибке.НомерСтроки;
		текНомерСтолбца	= сткИнформацияобОшибке.НомерСтолбца;
		текНомерСтроки	= ?(текНомерСтроки > 0, текНомерСтроки, 1);
		текНомерСтолбца = ?(текНомерСтолбца > 0, текНомерСтолбца, 1);
		Элементы.Формула.УстановитьГраницыВыделения(текНомерСтроки, текНомерСтолбца, текНомерСтроки, текНомерСтолбца);
	КонецЕсли;
	СписокПеременных.ЗагрузитьЗначения(мсвИдентификаторовПеременных);
	
	Возврат текРезультат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДемоФормулуНаСервере()
	текДемоФормула	= упТарификацияВызовСервера.ПолучитьДемоПримерФормулы();
	Возврат текДемоФормула;
КонецФункции

&НаСервере
Процедура ОчиститьПолеФормула()
	Объект.Формула = "";
КонецПроцедуры

&НаКлиенте
// Процедура - получает текст формулы.
//
// Параметры:
//  РезультатВопроса  - КодВозвратаДиалога - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВставитьТекстТестовойФормулы(РезультатВопроса, ДополнительныеПараметры) Экспорт
	текФормула = ПолучитьДемоФормулуНаСервере();
	Если РезультатВопроса =  КодВозвратаДиалога.Да Тогда
		ОчиститьПолеФормула();
	КонецЕсли;		
	ВставитьТекстВФормулу(" " + СокрЛП(текФормула));		
КонецПроцедуры

&НаСервере
// Процедура - Заполняет таблицы: показатели расчета, функции, оценочные шкалы.
//
Процедура ЗаполнитьТаблицуПоказателей()
	
	СписокДопустимыхПеременных.Очистить();
	
	тбзШкалы = упТарификацияВызовСервера.ПолучитьТаблицуШкал();
	
	Для каждого текСтрока Из тбзШкалы Цикл
		СписокДопустимыхПеременных.Добавить(ВРег(СокрЛП(текСтрока.Шкала)));	
	КонецЦикла;
	
	тбзТарифныеСетки = упТарификацияВызовСервера.ПолучитьТаблицуТарифныхСеток();
	
	Для каждого текСтрока Из тбзТарифныеСетки Цикл
		СписокДопустимыхПеременных.Добавить(ВРег(СокрЛП(текСтрока.ТарифнаяСетка)));	
	КонецЦикла;
	
	тбзПоказатели = упТарификацияВызовСервера.ПолучитьТаблицуПоказателей(Объект.ТипРасчета);
		
	Для каждого текСтрока Из тбзПоказатели Цикл
		текИдентификатор = СокрЛП(текСтрока.Идентификатор);		
		Если ЗначениеЗаполнено(текИдентификатор) Тогда
			СписокДопустимыхПеременных.Добавить(ВРег(текИдентификатор));
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ЗаполнитьТаблицуПоказателей()

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

&НаКлиенте
Процедура СпособРасчетаПриИзменении(Элемент)
	
	ОбновитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОбновитьДоступность(ЭтаФорма)
	
	Элементы = ЭтаФорма.Элементы;
	Объект = ЭтаФорма.Объект;
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Произвольный") Тогда
		Элементы.ГруппаСпособыРасчета.ТекущаяСтраница = Элементы.ГруппаПроизвольный;
	Иначе
		Элементы.ГруппаСпособыРасчета.ТекущаяСтраница = Элементы.ГруппаФормулаРасчета;
	КонецЕсли;
	//Элементы.ГруппаОценочныеШкалы.Доступность  = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула");
	Элементы.ГруппаФункции.Доступность  = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула");
	Элементы.ПоказателиРасчетаВставитьПоказательВФормулу.Доступность  = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула");
	Элементы.ОценочныеШкалыВставитьШкалуВФормулу.Доступность  = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула");
	Элементы.Переменные.Видимость = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Произвольный");
	Элементы.ФормаРедактироватьДанные.Видимость = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Произвольный");
	//Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула") Тогда 
	//	Элементы.ФормаРедактироватьДанные.Заголовок = "Тарифная сетка";
	//Иначе
	//	Элементы.ФормаРедактироватьДанные.Заголовок = "Данные расчета";
	//КонецЕсли;

КонецФункции

&НаСервере
Функция ПодключитьВнешнююОбработку(ФайлСсылка)
	
	Если Ложь Тогда // Для контекстной подсказки
	    ФайлСсылка = Справочники.Файлы.ПустаяСсылка();
	КонецЕсли;
	ИмяФайла = упТарификацияВызовСервера.ПроверитьОбновитьФайлВнешнейОбработкиПравила(Объект.Ссылка);
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		ИмяОбработки = ВнешниеОбработки.Создать(ИмяФайла, Объект.БезопасныйРежимВнешнейОбработки).Метаданные().Имя;
		//ИмяОбработки = ПоместитьВоВременноеХранилище(ФайлСсылка.ФайлХранилище.Получить());
		ВерсияФайла = ФайлСсылка.ТекущаяВерсия;
		Если ЗначениеЗаполнено(ВерсияФайла) Тогда
			КлючЗаписи 		= РегистрыСведений.ХранимыеФайлыВерсий.СоздатьКлючЗаписи(Новый Структура("ВерсияФайла", ВерсияФайла)); 
			ИмяОбработки 	= ПолучитьНавигационнуюСсылку(КлючЗаписи, "ХранимыйФайл");
			ИмяОбработки 	= ВнешниеОбработки.Подключить(ИмяОбработки,, Объект.БезопасныйРежимВнешнейОбработки);
		КонецЕсли;
	КонецЕсли; 
	Возврат ИмяОбработки;
	
КонецФункции

&НаКлиенте
Процедура ПроизвольныеДанные(Команда)
	
	//ОбновитьДанныеФайлаВБазе(Объект);
	Если Не ЭтаФорма.Записать() Тогда 
		Возврат;
	КонецЕсли; 
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПроизвольныеДанные", ЭтаФорма);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресПроизвольныхДанных", АдресПроизвольныхДанных());
	ПараметрыФормы.Вставить("ПоказательПоСтолбцам", Объект.ПоказательТарифнойСеткиПоСтолбцам);
	ПараметрыФормы.Вставить("ПоказательПоСтрокам", Объект.ПоказательТарифнойСеткиПоСтрокам);
	Если ЗначениеЗаполнено(Объект.ВнешняяОбработка) Тогда
		ИмяОбработки = ПодключитьВнешнююОбработку(Объект.ВнешняяОбработка);
		ФормаОбработки = ПолучитьФорму("ВнешняяОбработка." + ИмяОбработки + ".Форма", ПараметрыФормы, ЭтаФорма);
		ФормаОбработки.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаОбработки.Открыть();
	Иначе
		СообщитьОНезаполненностиВнешнейОбработки();
	КонецЕсли; 
	//Если ФормаОбработки = Неопределено Тогда
	//	ФормаОбработки = ПолучитьФорму("Справочник.упПравилаРасчета.Форма.ТарифнаяСетка", ПараметрыФормы, ЭтаФорма);
	//КонецЕсли;
	//ФормаОбработки.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	//ФормаОбработки.Открыть();

КонецПроцедуры

&НаКлиенте
Процедура СообщитьОНезаполненностиВнешнейОбработки()
	
	ТекстСообщения = НСтр("ru = 'Не загружена внешняя обработка с произвольным алгоритмом.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПроизвольныеДанные(Результат, Параметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Результат = Новый Структура("АдресПроизвольныхДанных", Результат);
	КонецЕсли; 
	ЗагрузитьПроизвольныеДанныеНаСервере(Результат);
	ЭтаФорма.Модифицированность = Истина;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПроизвольныеДанныеНаСервере(ОписаниеПроизвольныхДанных)
	
	ДанныеПроизвольногоРасчета = ПолучитьИзВременногоХранилища(ОписаниеПроизвольныхДанных.АдресПроизвольныхДанных);
	УдалитьИзВременногоХранилища(ОписаниеПроизвольныхДанных.АдресПроизвольныхДанных);
	Объект.ИдентификаторыСсылок.Очистить();
	Объект.ПоказательТарифнойСеткиПоСтолбцам = Неопределено;
	Объект.ПоказательТарифнойСеткиПоСтрокам = Неопределено;
	Если ОписаниеПроизвольныхДанных.Свойство("ПоказательПоСтолбцам") Тогда
		Для Каждого Колонка Из ДанныеПроизвольногоРасчета.Колонки Цикл
			ЗначениеПоказателя = ПреобразоватьЗначениеПоказателя(ДанныеПроизвольногоРасчета[0][Колонка.Имя]);
			ДанныеПроизвольногоРасчета[0][Колонка.Имя] = ЗначениеПоказателя;
		КонецЦикла;
		Для Каждого СтрокаТаблицы Из ДанныеПроизвольногоРасчета Цикл
			ЗначениеПоказателя = ПреобразоватьЗначениеПоказателя(СтрокаТаблицы[0]);
			СтрокаТаблицы[0] = ЗначениеПоказателя;
		КонецЦикла;
		Объект.ПоказательТарифнойСеткиПоСтолбцам = ОписаниеПроизвольныхДанных.ПоказательПоСтолбцам;
		Объект.ПоказательТарифнойСеткиПоСтрокам = ОписаниеПроизвольныхДанных.ПоказательПоСтрокам;
	//Иначе
	//	СтрокаXML = ОбщегоНазначения.ЗначениеВСтрокуXML(ДанныеПроизвольногоРасчета);
	КонецЕсли; 
	ЭтаФорма.АдресДанныхПроизвольногоРасчета = ПоместитьВоВременноеХранилище(ДанныеПроизвольногоРасчета, УникальныйИдентификатор);
	Если ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтолбцам) Тогда
		Объект.Переменные.Очистить();
		Если ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтолбцам) Тогда
			СтрокаПеременной = Объект.Переменные.Добавить();
			СтрокаПеременной.Идентификатор = СокрП(Объект.ПоказательТарифнойСеткиПоСтолбцам.Код);
			СтрокаПеременной.Значение = Объект.ПоказательТарифнойСеткиПоСтолбцам;
		КонецЕсли; 
		Если ЗначениеЗаполнено(Объект.ПоказательТарифнойСеткиПоСтрокам) Тогда
			СтрокаПеременной = Объект.Переменные.Добавить();
			СтрокаПеременной.Идентификатор = СокрП(Объект.ПоказательТарифнойСеткиПоСтрокам.Код);
			СтрокаПеременной.Значение = Объект.ПоказательТарифнойСеткиПоСтрокам;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Функция ПреобразоватьЗначениеПоказателя(ЗначениеПоказателя)
	
	Если упСервисныеФункции.ЛиТипСсылкиНаОбъектБД(ТипЗнч(ЗначениеПоказателя)) Тогда
		СтрокиТЧ = Объект.ИдентификаторыСсылок.НайтиСтроки(Новый Структура("Значение", ЗначениеПоказателя));
		Если СтрокиТЧ.Количество() > 0 Тогда
			СтрокаТЧ = СтрокиТЧ[0];
		Иначе
			СтрокаТЧ = Объект.ИдентификаторыСсылок.Добавить();
			СтрокаТЧ.Значение = ЗначениеПоказателя;
			СтрокаТЧ.Идентификатор = Новый УникальныйИдентификатор;
		КонецЕсли; 
		ЗначениеПоказателя = СтрокаТЧ.Идентификатор;
	КонецЕсли;
	Возврат ЗначениеПоказателя;

КонецФункции

&НаСервере
Функция АдресПроизвольныхДанных()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	Если Ложь Тогда // Для контекстной подсказки
	    СправочникОбъект = Справочники.упПравилаРасчета.СоздатьЭлемент();
	КонецЕсли;
	Если ЗначениеЗаполнено(АдресДанныхПроизвольногоРасчета) Тогда
		СправочникОбъект.ДанныеПроизвольногоРасчета = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресДанныхПроизвольногоРасчета));
		ДанныеПроизвольногоРасчета = Справочники.упПравилаРасчета.СобратьДанныеПроизвольногоРасчета(СправочникОбъект);
		Если ДанныеПроизвольногоРасчета <> Неопределено Тогда
			АдресДанных = ПоместитьВоВременноеХранилище(ДанныеПроизвольногоРасчета, УникальныйИдентификатор);
		КонецЕсли; 
	КонецЕсли;
	Возврат АдресДанных;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьВнешнююОбработку(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		ТекстСообщения = Нстр("ru = 'Сначала нужно заполнить наименование'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Наименование");
		Возврат;
	КонецЕсли; 
	Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузки", ЭтаФорма, Новый Структура("ИмяШаблона", ""));
	ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
// Открытие формы получения файла.
//
Процедура ОткрытьДиалогВыгрузки(Результат, ДополнительныеПараметры) Экспорт
	
	ОповещениеЗавершенияПолученияФайлов = Новый ОписаниеОповещения("ОбработкаЗавершенияПомещенияФайла", ЭтаФорма);
	ПолучаемыеФайлы = Новый Массив;
	Если ДополнительныеПараметры.ИмяШаблона = "" Тогда
		Если Не ЗначениеЗаполнено(АдресФайла) тогда
			ТекстСообщения = НСтр("ru = 'Файл внешней обработки еще не был загружен.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли; 
	Иначе
		ПоместитьШаблонВнешнейОбработкиВоВременноеХранилище(ДополнительныеПараметры.ИмяШаблона);
	КонецЕсли; 
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляСохранения(Объект.ВнешняяОбработка, УникальныйИдентификатор);
	РаботаСФайламиСлужебныйКлиент.СохранитьКак(Неопределено, ДанныеФайла, Неопределено);
	
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выгрузки файла.
//
Процедура ОбработкаЗавершенияПомещенияФайла(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПолученныеФайлы <> Неопределено Тогда
		Для каждого ПолученныйФайл Из ПолученныеФайлы Цикл
			ТекстСообщения = Нстр("ru = 'Внешняя обработка выгружена: %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрЗаменить(ПолученныйФайл.Имя, "/", "\"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	//Иначе
	//	ТекстСообщения = Нстр("ru = 'Не удалось выгрузить внешнюю обработку'");
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьШаблонВнешнейОбработкиВоВременноеХранилище(ИмяШаблона)
	
	ДвоичныеДанные = Справочники.упПравилаРасчета.ПолучитьМакет(ИмяШаблона);
	ЭтаФорма.АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	ЭтаФорма.ИмяФайлаВнешнейОбработки = Объект.Наименование;
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВнешнююОбработку(Команда)
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработкаЗавершенияПолученияФайла", ЭтаФорма);
	НачатьПомещениеФайла(ОписаниеОповещенияОЗавершении,, ИмяФайлаВнешнейОбработки, Истина, УникальныйИдентификатор); 

КонецПроцедуры

&НаКлиенте
// Процедура заверешения после загрузки файла.
//
Процедура ОбработкаЗавершенияПолученияФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Если Не ПроверитьИнтерфейсВнешнейОбработкиНаСервере(Адрес) Тогда
			Возврат;
		КонецЕсли; 
		сткИмяФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла);
		ЭтаФорма.ИмяФайлаВнешнейОбработки = сткИмяФайла.Имя;
		ЭтаФорма.АдресФайла = Адрес;
		ЭтаФорма.Модифицированность = Истина;
		ТекстСообщения = Нстр("ru = 'Внешняя обработка загружена: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВыбранноеИмяФайла);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьИнтерфейсВнешнейОбработкиНаСервере(Адрес)
	
	Попытка
		ИмяВнешнейОбработки = ВнешниеОбработки.Подключить(Адрес,, Объект.БезопасныйРежимВнешнейОбработки);
		ВнешняяОбработкаПроверки = ВнешниеОбработки.Создать(ИмяВнешнейОбработки, Объект.БезопасныйРежимВнешнейОбработки);
		МетодРеализован = упСервисныеФункции.МетодРеализован(ВнешняяОбработкаПроверки, "Рассчитать");
	Исключение
		ТекстСообщения = Нстр("ru = 'Ошибка компиляции указаной внешней обработки: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецПопытки;
	Если Не МетодРеализован Тогда 
		ТекстСообщения = Нстр("ru = 'В указанной внешней обработке отсутствует обязательная экспортная функция Рассчитать(Показатели, ПроизвольныеДанные)'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 
	
КонецФункции

&НаКлиенте
Процедура ФайлДляОтладки(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ВнешняяОбработка) Тогда
		СообщитьОНезаполненностиВнешнейОбработки();
		Возврат;
	КонецЕсли; 
	КаталогВнешнихОбработокДляОтладки = КонстантаКаталогВнешнихОбработок();
	Если ЗначениеЗаполнено(КаталогВнешнихОбработокДляОтладки) Тогда
		Если Модифицированность Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗаписатьИСообщитьФайлДляОтладки", ЭтаФорма);
			ПоказатьВопрос(ОписаниеОповещения, "Для продолжения нужно записать объект. Продолжить?", РежимДиалогаВопрос.ОКОтмена);
		Иначе
			СообщитьФайлДляОтладкиНаСервере();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КонстантаКаталогВнешнихОбработок()
	
	Возврат Константы.упКаталогВнешнихОбработокДляОтладки.Получить();

КонецФункции

&НаКлиенте
Процедура ЗаписатьИСообщитьФайлДляОтладки(Результат, ДопПараметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли; 
	ЭтаФорма.Записать();
	СообщитьФайлДляОтладки();

КонецПроцедуры

&НаКлиенте
Процедура СообщитьФайлДляОтладки() Экспорт 
	
	СообщитьФайлДляОтладкиНаСервере();

КонецПроцедуры

&НаСервере
Процедура СообщитьФайлДляОтладкиНаСервере()
	
	ПолноеИмяФайлаВнешнейОбработки = упТарификацияВызовСервера.ПроверитьОбновитьФайлВнешнейОбработкиПравила(Объект.Ссылка);
	Если ЗначениеЗаполнено(ПолноеИмяФайлаВнешнейОбработки) Тогда
		ТекстСообщения = ПолноеИмяФайлаВнешнейОбработки;
	Иначе
		ТекстСообщения = "Нет доступа к файлу для отладки";
	КонецЕсли; 
     ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьШаблонБезФормы(Команда)

	Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузки", ЭтаФорма, Новый Структура("ИмяШаблона", "ШаблонВнешнейОбработкиБезФормы"));
	ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьШаблонСФормой(Команда)

	Оповещение = Новый ОписаниеОповещения("ОткрытьДиалогВыгрузки", ЭтаФорма, Новый Структура("ИмяШаблона", "ШаблонВнешнейОбработкиСФормой"));
	ТекстСообщения = НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(Оповещение, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеременныеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Копирование;
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеременныеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Для Каждого Структура Из ПараметрыПеретаскивания.Значение Цикл
		СуществующиеСтроки = Объект.Переменные.НайтиСтроки(Новый Структура("Значение", Структура.Ссылка));
		Если СуществующиеСтроки.Количество() > 0 Тогда
			Элемент.ТекущаяСтрока = СуществующиеСтроки[0].ПолучитьИдентификатор();
			Продолжить;
		КонецЕсли; 
		СтрокаТЧ = Объект.Переменные.Добавить();
		СтрокаТЧ.Значение = Структура.Ссылка;
		СтрокаТЧ.Идентификатор = Структура.Идентификатор;
		Элемент.ТекущаяСтрока = СтрокаТЧ.ПолучитьИдентификатор();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифныеСеткиНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
		
	Если Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.упСпособыРасчета.Формула") Тогда
		ПараметрыПеретаскивания.Значение = Сокрлп(Элемент.ТекущиеДанные.ТарифнаяСетка);
	Иначе
		ПараметрыПеретаскивания.Значение = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из Элемент.ВыделенныеСтроки Цикл
			СтруктураЗначения = Новый Структура("Идентификатор, Ссылка");
			ДанныеСтроки = Элемент.ДанныеСтроки(ВыделеннаяСтрока);
			СтруктураЗначения.Идентификатор = СокрЛП(ДанныеСтроки.ТарифнаяСетка); 
			СтруктураЗначения.Ссылка = ДанныеСтроки.Ссылка; 
			ПараметрыПеретаскивания.Значение.Добавить(СтруктураЗначения);
		КонецЦикла;
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

