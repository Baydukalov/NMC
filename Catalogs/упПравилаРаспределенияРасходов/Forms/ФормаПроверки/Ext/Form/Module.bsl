
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Формула        = Параметры.Формула;
	ФормулаРасчета = Параметры.ФормулаРасчета;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаДляРаспределенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда 
		текДанные = Элементы.ТаблицаДляРаспределения.ТекущиеДанные;
		текДанные.Документ    = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ № %1'"), Элементы.ТаблицаДляРаспределения.ТекущаяСтрока);
		текДанные.Коэффициент = 0;
		текДанные.Сумма       = 0;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Рассчитать(Команда)
	
	РассчитатьНаСервере();
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура РассчитатьНаСервере()

	Попытка
		ТаблицаДокументов = ТаблицаДляРаспределения.Выгрузить();
		Отказ = Ложь;
		упРаспределениеРасходов.РассчитатьКоэффициентыРаспределения(ФормулаРасчета, ТаблицаДокументов, Отказ);
		
		Если Отказ Тогда
			Результат = НСтр("ru = 'Ошибка: Не удалось рассчитать коэффициенты распределения'");
			Возврат;
		КонецЕсли; 
		
		ТаблицаДляРаспределения.Загрузить(ТаблицаДокументов);
		
		ОбщийКоэффициент = ТаблицаДляРаспределения.Итог("Коэффициент");
		Если ОбщийКоэффициент = 0 Тогда 
			Результат = НСтр("ru = 'Ошибка: Не удалось рассчитать коэффициенты распределения'");
			Возврат;
		КонецЕсли;
		
		Для Каждого текСтрока Из ТаблицаДляРаспределения Цикл 
			текСтрока.Сумма = СуммаДляРаспределения * текСтрока.Коэффициент / ОбщийКоэффициент;
		КонецЦикла;	
		
		ОбщаяСумма = ТаблицаДляРаспределения.Итог("Сумма");
		Если ОбщаяСумма <> СуммаДляРаспределения Тогда 
			ПоследняяСтрока = ТаблицаДляРаспределения[ТаблицаДляРаспределения.Количество() - 1];
			ПоследняяСтрока.Сумма = ПоследняяСтрока.Сумма + СуммаДляРаспределения - ОбщаяСумма;
		КонецЕсли;	
		
		Результат = НСтр("ru = 'Формула корректна'");
	Исключение	
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка: %1'"), КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
