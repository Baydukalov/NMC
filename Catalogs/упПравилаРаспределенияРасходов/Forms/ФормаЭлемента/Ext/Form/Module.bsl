#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	текФормула = Объект.Формула;
	Пока Найти(текФормула, """""") Цикл  
		текФормула = СтрЗаменить(текФормула, """""", """");
	КонецЦикла;	
	текФормула = СтрЗаменить(текФормула, "СуммаПоВсемДокументам(""", "Σ(");
	текФормула = СтрЗаменить(текФормула, """, ТаблицаДокументов)", ")");
	Формула.УстановитьТекст(текФормула);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов.
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов.
	
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СпособРаспределенияПриИзменении(Неопределено);
	
КонецПроцедуры

&НаСервере
// Процедура - обработчик события "ПриОткрытии" формы.
//
// Параметры:
//  Отказ - Булево - отказ от записи.
//
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Объект.СпособРаспределения = Перечисления.упСпособыРаспределенияРасходов.УказатьФормулуРаспределения Тогда 
		ТекущийОбъект.Формула = СформироватьТекстФормулы(Формула.ПолучитьТекст(), Объект.Ссылка, Отказ);
	КонецЕсли;	
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособРаспределенияПриИзменении(Элемент)

	Если Объект.СпособРаспределения = ПредопределенноеЗначение("Перечисление.упСпособыРаспределенияРасходов.УказатьФормулуРаспределения") Тогда 
		Элементы.СтраницыСпособРаспределения.ТекущаяСтраница = Элементы.СтраницаПоФормуле;
		Элементы.ФормаПроверитьФормулу.Доступность           = Истина;
	Иначе	
		Элементы.СтраницыСпособРаспределения.ТекущаяСтраница = Элементы.СтраницаПропорциональноПоказателю;
		Элементы.ФормаПроверитьФормулу.Доступность           = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события "Нажатие" элемента Декорация13.
//
// Параметры:
//  Элемент  - Элемент - формы, источник события.
//
Процедура Декорация13Нажатие(Элемент)
	
	ОткрытьСправку("Справочник.упПравилаРаспределенияРасходов");
	
КонецПроцедуры // Декорация13Нажатие()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды "КомандаЦел".
//
// Параметры:
//  Команда  - Элемент - формы, источник события.
//
Процедура КомандаЦел(Команда)
	
	ВставитьТекстВФормулу(" Цел() ")
	
КонецПроцедуры // КомандаЦел()

&НаКлиенте
// Процедура - обработчик команды "КомандаОкр".
//
// Параметры:
//  Команда  - Элемент - формы, источник события.
//
Процедура КомандаОкр(Команда)
	
	ВставитьТекстВФормулу(" Окр(,) ")
	
КонецПроцедуры // КомандаОкр()

&НаКлиенте
// Процедура - обработчик команды "КомандаМакс".
//
// Параметры:
//  Команда  - Элемент - формы, источник события.
//
Процедура КомандаМакс(Команда)
	
	ВставитьТекстВФормулу(" Макс(,) ")
	
КонецПроцедуры // КомандаМакс()

&НаКлиенте
// Процедура - обработчик команды "КомандаМин".
//
// Параметры:
//  Команда  - Элемент - формы, источник события.
//
Процедура КомандаМин(Команда)
	
	ВставитьТекстВФормулу(" Мин(,) ")
	
КонецПроцедуры // КомандаМин()

&НаКлиенте
// Процедура - обработчик команды "КомандаУсловие".
//
// Параметры:
//  Команда  - Элемент - формы, источник события.
//
Процедура КомандаУсловие(Команда)
	
	ВставитьТекстВФормулу(" ?(,,) ")
	
КонецПроцедуры // КомандаУсловие()

&НаКлиенте
Процедура КомандаСуммаПоВсемДокументам(Команда)
	
	ВставитьТекстВФормулу(" Σ() ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательВремя(Команда)
	
	ВставитьТекстВФормулу(" Время ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательКоличествоЗагрузочныхМетров(Команда)
	
	ВставитьТекстВФормулу(" КоличествоЗагрузочныхМетров ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательКоличествоМест(Команда)
	
	ВставитьТекстВФормулу(" КоличествоМест ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательКоличествоТоварныхНакладных(Команда)
	
	ВставитьТекстВФормулу(" КоличествоТоварныхНакладных ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательМасса(Команда)
	
	ВставитьТекстВФормулу(" Масса ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательОбъем(Команда)
	
	ВставитьТекстВФормулу(" Объем ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПлановыеРасходыПоРейсу(Команда)
	
	ВставитьТекстВФормулу(" ПлановыеРасходыПоРейсу ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательРасстояние(Команда)
	
	ВставитьТекстВФормулу(" Расстояние ")
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательОценочнаяСтоимостьГруза(Команда)
	
	ВставитьТекстВФормулу(" ОценочнаяСтоимостьГруза ");
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик команды "ПроверитьФормулу".
//
// Параметры:
//  Команда  - Элемент - формы, источник события.
//
Процедура ПроверитьФормулу(Команда)
	
	Отказ = Ложь;
	текФормула = СформироватьТекстФормулы(Формула.ПолучитьТекст(), Объект.Ссылка, Отказ);
	Если Не Отказ Тогда 
		сткПараметров = Новый Структура("Формула, ФормулаРасчета", Формула.ПолучитьТекст(), текФормула);
		ОткрытьФорму("Справочник.упПравилаРаспределенияРасходов.Форма.ФормаПроверки", сткПараметров, ЭтаФорма);
	КонецЕсли;
		
КонецПроцедуры // ПроверитьФормулу()

#КонецОбласти

#Область  СлужебныеПроцедурыИФункции

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ.
	  
&НаКлиенте
// Процедура вставляет текст, передаваемый в качестве параметра в поле табличного документа,
// ПолеТабличногоДокументаПроцедура.
//
// Параметры:
//  ТекстДляВставки   - Строка - вставляемая строка.
//
Процедура ВставитьТекстВФормулу(ТекстДляВставки)
	
	Элементы.Формула.ВыделенныйТекст = ТекстДляВставки;
	Модифицированность = Истина;
	
	ТекущийЭлемент = Элементы.Формула;
	
КонецПроцедуры // ВставитьТекстВФормулу()


&НаКлиентеНаСервереБезКонтекста
// Процедура - обработчик события "ПриОткрытии" формы.
//
// Параметры:
//  Отказ - Булево - отказ от записи.
//
Функция СформироватьТекстФормулы(текФормула, Ссылка, Отказ)
	
	мсвВложенности = Новый Массив;
	
	Пока Найти(текФормула, "Σ(") > 0 Цикл 
		ПоложениеНачала = Найти(текФормула, "Σ(");
		
		УровеньВложенности = 0;
		КоличествоУровней  = мсвВложенности.Количество();
		Для Сч = 0 По КоличествоУровней - 1 Цикл 
			Если мсвВложенности[КоличествоУровней - 1 - Сч] > ПоложениеНачала Тогда 
				УровеньВложенности = КоличествоУровней - Сч;
				Прервать;
			Иначе
				мсвВложенности.Удалить(КоличествоУровней - 1 - Сч);
			КонецЕсли;
		КонецЦикла;	
		
		СчетчикСкобок   = 1;
		Для Сч = ПоложениеНачала + 2 По СтрДлина(текФормула) Цикл 
			Символ = Сред(текФормула, Сч, 1);
			Если Символ = "(" Тогда 
				СчетчикСкобок = СчетчикСкобок + 1;
			ИначеЕсли Символ = ")" Тогда 
				СчетчикСкобок = СчетчикСкобок - 1;
			Иначе
				Продолжить;
			КонецЕсли;
			
			Если СчетчикСкобок = 0 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СчетчикСкобок <> 0 Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Формула неверна: количество открывающих и закрывающих скобок не равно'"), Ссылка, "Формула",, Отказ);
			Возврат "";
		КонецЕсли;
		
		Кавычки = "";
		КоличествоИтераций = Pow(2, УровеньВложенности);
		Для СчИтераций = 1 По КоличествоИтераций Цикл 
			Кавычки = Кавычки + """";
		КонецЦикла;	
		
		текФормула = Лев(текФормула, Сч - 1) + Кавычки + ", ТаблицаДокументов" + Сред(текФормула, Сч);
		текФормула = Лев(текФормула, ПоложениеНачала - 1) + "СуммаПоВсемДокументам(" + Кавычки + Сред(текФормула, ПоложениеНачала + 2);
		
		Для СчИтераций = 0 По мсвВложенности.Количество() - 1 Цикл 
			мсвВложенности[СчИтераций] = мсвВложенности[СчИтераций] + 20 + КоличествоИтераций + 19 + КоличествоИтераций;
		КонецЦикла;	
		мсвВложенности.Добавить(Сч + 20 + КоличествоИтераций);
	КонецЦикла;
		
	Возврат текФормула;
	
КонецФункции

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.СпособРаспределения = Перечисления.упСпособыРаспределенияРасходов.УказатьФормулуРаспределения Тогда 
		Если Не ЗначениеЗаполнено(Формула.ПолучитьТекст()) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана формула рассчета'"), Объект.Ссылка, "Формула",, Отказ);
		КонецЕсли;
	Иначе	
		Если Не ЗначениеЗаполнено(Объект.Показатель) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан показатель'"), Объект.Ссылка, "Объект.Показатель",, Отказ);
		КонецЕсли;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Объект");
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.

#КонецОбласти

