#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если Не ОбменДанными.Загрузка Тогда
		Если НЕ Ссылка.Партнер = Партнер Тогда
			ДополнительныеСвойства.Вставить("ПроизошлоИзменениеПеревозчика", Истина);
		КонецЕсли;
		
		Если Прицеп Тогда
			Если ЗначениеЗаполнено(ОсновнойВодитель) Тогда
				ОсновнойВодитель = Справочники.упСотрудники.ПустаяСсылка();
			КонецЕсли;
			Если ЗначениеЗаполнено(ВторойОсновнойВодитель) Тогда
				ВторойОсновнойВодитель = Справочники.упСотрудники.ПустаяСсылка();
			КонецЕсли;
			Если ЗначениеЗаполнено(ОсновнойЭкспедитор) Тогда
				ОсновнойЭкспедитор = Справочники.упСотрудники.ПустаяСсылка();
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтильЛиний) Тогда
				СтильЛиний = Справочники.упСтилиЛиний.ПустаяСсылка();
			КонецЕсли;		
			Если ЗначениеЗаполнено(СтильМаркера) Тогда
				СтильМаркера = Справочники.упСтилиМаркеров.ПустаяСсылка();
			КонецЕсли;
			Если ЗначениеЗаполнено(ПерерывМеждуРейсами) Тогда
				ПерерывМеждуРейсами = 0;
			КонецЕсли;
			Если ЗначениеЗаполнено(ПечатнаяФормаПутевогоЛиста) Тогда
				ПечатнаяФормаПутевогоЛиста = Неопределено;
			КонецЕсли;
			
			УдалитьВидыГСМ.Очистить();
			
			Если ЗначениеЗаполнено(ДатчикУровняТоплива) Тогда
				ДатчикУровняТоплива = Справочники.упДатчики.ПустаяСсылка();
			КонецЕсли;		
		КонецЕсли;	
		Если Не ЗначениеЗаполнено(ДоступныеЗоны) Тогда
			ЭтотОбъект.ДоступныеЗоны = Справочники.упПравилаДоступностиГеографическихЗон.ДоступныВсеЗоны;
		КонецЕсли; 
		Если ВидТранспорта = Перечисления.упВидыТранспорта.Контейнер Тогда
			Справочники.упТранспортныеСредства.ПроверитьУникальностьРегистрационногоНомера(РегистрационныйНомер, Ссылка, Отказ);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.упДоступныеТС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", Ссылка);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ТекстОшибки = ОписаниеОшибки();
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка,,,Отказ);
		ЗаписьЖурналаРегистрации("ОшибкаБлокировки", УровеньЖурналаРегистрации.Ошибка, ,Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		Отказ = Истина;
	КонецПопытки;
	
	упПравилаДоступностиРесурсов.СформироватьЗаписиДоступныеТС(Ссылка);
	
	Если ДополнительныеСвойства.Свойство("ПроизошлоИзменениеПеревозчика") И ДополнительныеСвойства.ПроизошлоИзменениеПеревозчика Тогда
		НоваяЗапись = РегистрыСведений.упИсторияПеревозчиковТС.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период = ТекущаяДатаСеанса();
		НоваяЗапись.ТранспортноеСредство = Ссылка;
		НоваяЗапись.Партнер = Партнер;
		НоваяЗапись.Записать();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.упМоделиТС") Тогда
		Высота 					= ДанныеЗаполнения.Высота;
		Длина  					= ДанныеЗаполнения.Длина;
		Ширина 					= ДанныеЗаполнения.Ширина;
		Грузоподъемность 			= ДанныеЗаполнения.Грузоподъемность;
		Объем 					= ДанныеЗаполнения.Объем;
		КоличествоМест 			= ДанныеЗаполнения.КоличествоМест;
		КоличествоЗагрузочныхМетров	= ДанныеЗаполнения.КоличествоЗагрузочныхМетров;
		СнаряженнаяМасса			= ДанныеЗаполнения.СнаряженнаяМасса;
		ТипТС 					= ДанныеЗаполнения.ТипТС;
		ТипКузова 				= ДанныеЗаполнения.ТипКузова;
		Модель                  = ДанныеЗаполнения;
		КоличествоПосадочныхМест = ДанныеЗаполнения.КоличествоПосадочныхМест;
		Если ЗначениеЗаполнено(ТипТС) Тогда
			ВидТранспорта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипТС, "ВидТранспорта");	
		КонецЕсли;

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.упТипыТС") Тогда	
		Высота 					= ДанныеЗаполнения.Высота;
		Длина  					= ДанныеЗаполнения.Длина;
		Ширина 					= ДанныеЗаполнения.Ширина;
		Грузоподъемность 			= ДанныеЗаполнения.Грузоподъемность;
		Объем 					= ДанныеЗаполнения.Объем;
		КоличествоМест 			= ДанныеЗаполнения.КоличествоМест;
		КоличествоЗагрузочныхМетров	= ДанныеЗаполнения.КоличествоЗагрузочныхМетров;
		СнаряженнаяМасса			= ДанныеЗаполнения.СнаряженнаяМасса;
		ТипТС 					= ДанныеЗаполнения;
		КоличествоПосадочныхМест = ДанныеЗаполнения.КоличествоПосадочныхМест;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда	
		Если ДанныеЗаполнения.Свойство("ТипТС") Тогда 
			Высота 						= ДанныеЗаполнения.ТипТС.Высота;
			Грузоподъемность 			= ДанныеЗаполнения.ТипТС.Грузоподъемность;
			Длина 						= ДанныеЗаполнения.ТипТС.Длина;
			Объем 						= ДанныеЗаполнения.ТипТС.Объем;
			Ширина 						= ДанныеЗаполнения.ТипТС.Ширина;
			КоличествоМест 				= ДанныеЗаполнения.ТипТС.КоличествоМест;
			КоличествоЗагрузочныхМетров	= ДанныеЗаполнения.ТипТС.КоличествоЗагрузочныхМетров;
			КоличествоПосадочныхМест = ДанныеЗаполнения.ТипТС.КоличествоПосадочныхМест;
		КонецЕсли;	
		Если ДанныеЗаполнения.Свойство("Модель") Тогда 
			Высота 						= ДанныеЗаполнения.Модель.Высота;
			Грузоподъемность 			= ДанныеЗаполнения.Модель.Грузоподъемность;
			Длина 						= ДанныеЗаполнения.Модель.Длина;
			Объем 						= ДанныеЗаполнения.Модель.Объем;
			Ширина 						= ДанныеЗаполнения.Модель.Ширина;
			КоличествоМест 				= ДанныеЗаполнения.Модель.КоличествоМест;
			КоличествоЗагрузочныхМетров	= ДанныеЗаполнения.Модель.КоличествоЗагрузочныхМетров;
			ТипКузова 					= ДанныеЗаполнения.Модель.ТипКузова;
			ТипТС 						= ДанныеЗаполнения.Модель.ТипТС;
			КоличествоПосадочныхМест = ДанныеЗаполнения.Модель.КоличествоПосадочныхМест;
			Если ЗначениеЗаполнено(ТипТС) Тогда
				ВидТранспорта				= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипТС, "ВидТранспорта");	
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ Отказ Тогда
		РольДоступнаупРазработчик = РольДоступна("упРазработчик");
		РольДоступнаПолныеПрава = РольДоступна("ПолныеПрава");
		РольДоступнаупПеревозчик	= РольДоступна("упПеревозчик");
		Если СобственноеТранспортноеСредство Тогда
			РольДоступнаУпДобавлениеИзменениеСобственныхТранспортныхСредств = РольДоступна("упДобавлениеИзменениеСобственныхТранспортныхСредств");
			РазрешеноИзменятьСобственныеТС = РольДоступнаупРазработчик ИЛИ РольДоступнаПолныеПрава ИЛИ РольДоступнаУпДобавлениеИзменениеСобственныхТранспортныхСредств;
			Если НЕ РазрешеноИзменятьСобственныеТС Тогда
				ТекстСообщения = Нстр("ru = 'Недостаточно прав для изменения собственных транспортных средств'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"СобственноеТранспортноеСредство","Объект",Отказ);
				Событие = Нстр("ru = 'Ошибка при проверке заполнения транспортного средства'");
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
			КонецЕсли;	
		Иначе
			РольДоступнаУпДобавлениеИзменениеТранспортныхСредств = РольДоступна("упДобавлениеИзменениеТранспортныхСредств");
			РазрешеноИзменятьНеСобственныеТС = РольДоступнаупРазработчик 
												ИЛИ РольДоступнаПолныеПрава 
												ИЛИ РольДоступнаУпДобавлениеИзменениеТранспортныхСредств
												ИЛИ РольДоступнаупПеревозчик;
			Если НЕ РазрешеноИзменятьНеСобственныеТС Тогда
				ТекстСообщения = Нстр("ru = 'Недостаточно прав для изменения не собственных транспортных средств'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, );
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"СобственноеТранспортноеСредство","Объект",Отказ);
				Событие = Нстр("ru = 'Ошибка при проверке заполнения транспортного средства'");
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ И ЗначениеЗаполнено(Ссылка) И НЕ СобственноеТранспортноеСредство Тогда
		сткВозврат = упУчетТранспортныхСредств.УчетноеСостояниеТранспортногоСредства(Ссылка);
		Если сткВозврат.ПринятоКУчету  Тогда
			ТекстСообщения = Нстр("ru = 'Транспортное средство принято к учету документом %1. 
										|Перед снятием признака собственного транспортного средства, необходимо снять транспортное средство с учета'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,  сткВозврат.Регистратор);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			Событие = Нстр("ru = 'Ошибка при проверке заполнения транспортного средства'");
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,, , ТекстСообщения);    
		КонецЕсли;	
	КонецЕсли;
	
	Если ВидТранспорта = Перечисления.упВидыТранспорта.Контейнер Тогда
		ПроверяемыеРеквизиты.Добавить("РегистрационныйНомер");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Наименование");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#КонецЕсли

