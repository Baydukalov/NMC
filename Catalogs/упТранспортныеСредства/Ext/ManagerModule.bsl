#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура возвращает массив недоступных элементов.
//
// Возвращаемое значение:
//   Массив - Результат выполнения.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Для каждого текРек из Метаданные.Справочники.упТранспортныеСредства.Реквизиты цикл
		Если НЕ текРек.Имя = "Комментарий" тогда
			БлокируемыеРеквизиты.Добавить(текРек.Имя);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции // ПолучитьБлокируемыеРеквизитыОбъекта()

// Обеспечивает единую логику установки отбора при выборе элементов справочника.
//
// Параметры:
//  ОтборСписка - Структура	 - ОтборКомпоновкиДанных
//  Параметры	 - Структура	 - значения параметров выбора.
//
Процедура УстановитьОтборПриВыбореПоПараметрам(ОтборСписка, Параметры, ИмяРеквизитаПартнер = "Партнер") Экспорт 

	Если Ложь Тогда // Для контекстной подсказки.
		Параметры = Новый Структура;
		Текущие_Настройки = Новый НастройкиКомпоновкиДанных;
		ОтборСписка = Текущие_Настройки.Отбор;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидПеревозки") И ЗначениеЗаполнено(Параметры.ВидПеревозки) Тогда
		мсвТранспортныхСредств = ПолучитьДоступныеТС(Параметры);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "Ссылка", мсвТранспортныхСредств, ВидСравненияКомпоновкиДанных.ВСписке);	
	ИначеЕсли Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, СтрШаблон("%1.Организация", ИмяРеквизитаПартнер), Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно);
	ИначеЕсли Параметры.Свойство("ВидГСМ") И ЗначениеЗаполнено(Параметры.ВидГСМ) Тогда	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "Модель.НормыРасходаГСМ.ВидГСМ", Параметры.ВидГСМ, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если Параметры.Свойство("ЭтоСобственноеТС") И ЗначениеЗаполнено(Параметры.ЭтоСобственноеТС) Тогда
		ИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков = ПолучитьФункциональнуюОпцию("упИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков");
		Если НЕ ИспользуетсяУчетЗанятостиЭкипажейСтороннихПеревозчиков Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "СобственноеТранспортноеСредство", Истина, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;	
	КонецЕсли;	
	
	Если Параметры.Свойство("СкрытьНеучетные") И Параметры.СкрытьНеучетные Тогда
		ГруппаОтбора = ОтборСписка.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "СобственноеТранспортноеСредство", Ложь, ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаОтбора, "ПринятоКУчету", Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли; 

КонецПроцедуры // УстановитьОтборПриВыбореПоПараметрам()

// Процедура - Добавить условное оформление колонки состояние ТС
//
// Параметры:
//  Список					 - ДинамическийСписок	 - список.
//  ПолноеИмяРеквизитаСостояниеТС	 - Строка	 - имя реквизита.
//  ИмяЭлементаСостояниеТС		 - Строка	 - имя элемента.
//
Процедура ДобавитьУсловноеОформлениеКолонкиСостояниеТС(Список, ПолноеИмяРеквизитаСостояниеТС = Неопределено, ИмяЭлементаСостояниеТС = Неопределено) Экспорт 
	
	ИмяРеквизитаСостояниеТС = ?(ЗначениеЗаполнено(ПолноеИмяРеквизитаСостояниеТС), ПолноеИмяРеквизитаСостояниеТС, "СостояниеТС.ТипСостоянияТС"); 
	ИмяЭлемента             = ?(ЗначениеЗаполнено(ИмяЭлементаСостояниеТС), ИмяЭлементаСостояниеТС, "СостояниеТС"); 
	
	СоответствиеЦветовСостояний = упПодборТС.СоответствиеТиповСостоянийТСЦветам();
	Для Каждого КлючИЗначение Из СоответствиеЦветовСостояний Цикл
		ЭлементУсловноеОформление = Список.УсловноеОформление.Элементы.Добавить();
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭлементУсловноеОформление.Отбор, ИмяРеквизитаСостояниеТС, КлючИЗначение.Ключ);
		ПолеУсловногоОформления = ЭлементУсловноеОформление.Поля.Элементы.Добавить();
		ПолеУсловногоОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
		ПолеУсловногоОформления.Использование = Истина;
		ЭлементУсловноеОформление.Оформление.УстановитьЗначениеПараметра("ЦветФона", КлючИЗначение.Значение);
		ЭлементУсловноеОформление.Использование = Истина;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСписокПараметровОтбора() Экспорт

	ТекстИменПараметров = "ТипТС, Перевозчик, ТекущаяСтрока, НачалоИнтервалаВыделения, КонецИнтервалаВыделения, НачалоПериода, ОкончаниеПериода, ВидПеревозки, Организация, ВидГСМ, ЭтоСобственноеТС, СкрытьНеучетные, РегНомер";
	СписокПараметров = СтрРазделить(ТекстИменПараметров, ", ", Ложь);
	Возврат СписокПараметров;

КонецФункции // ПолучитьСписокПараметровОтбора()

Функция ПолучитьДоступныеТС(Параметры, МенеджерВременныхТаблиц = Неопределено) Экспорт 
	
	мсвТранспортныхСредств = Новый Массив;
	
	Если Параметры.Свойство("РежимФормированияКонтейнеров") И Параметры.РежимФормированияКонтейнеров Тогда
		мсвВидыТранспорта = ПолучитьДоступныеВидыТранспорта(Истина);	
	Иначе
		мсвВидыТранспорта = ПолучитьДоступныеВидыТранспорта(Ложь);	
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	КонецЕсли; 
	Запрос.Текст = 
	"
	|ВЫБРАТЬ
	|	упВидыПеревозок.Ссылка,
	|	упВидыПеревозок.ВыборПеревозчика КАК ВариантВыбораПеревозчика,
	|	упВидыПеревозок.Перевозчик
	|ПОМЕСТИТЬ втВидыПеревозок
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчики
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаСобственнымиТС))
	|			И (упПартнеры.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			И (ЛОЖЬ
	|				ИЛИ упПартнеры.Организация = &Организация
	|				ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втВидыПеревозок.Перевозчик
	|ИЗ
	|	втВидыПеревозок КАК втВидыПеревозок
	|ГДЕ
	|	втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (упПартнеры.Перевозчик)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
	|		ПО упТранспортныеСредства.Партнер = втПеревозчики.Перевозчик
	|			И (&Перевозчик В (втПеревозчики.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)))
	|			И (&ТипТС В (упТранспортныеСредства.ТипТС, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)))
	|			И упТранспортныеСредства.ВидТранспорта В (&ВидыТранспорта)";
	
	Если Параметры.Свойство("РегНомер") И ЗначениеЗаполнено(Параметры.РегНомер) Тогда
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ упТранспортныеСредства.РегистрационныйНомер ПОДОБНО &РегистрационныйНомер";
		Запрос.УстановитьПараметр("РегистрационныйНомер", "%" + Параметры.СтрокаПоиска + "%");
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("ВидПеревозки", Параметры.ВидПеревозки);
	Запрос.УстановитьПараметр("Перевозчик", Параметры.Перевозчик);
	Запрос.УстановитьПараметр("Организация", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Организация", Справочники.Организации.ПустаяСсылка()));
	Запрос.УстановитьПараметр("ТипТС", Параметры.ТипТС);
	Запрос.УстановитьПараметр("ВидыТранспорта", мсвВидыТранспорта);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		мсвТранспортныхСредств = Результат.Выгрузить().ВыгрузитьКолонку("ТранспортноеСредство");
	КонецЕсли; 

	Возврат мсвТранспортныхСредств;
	
КонецФункции

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("РегНомер") Тогда
		
		СтрокаПоиска = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтрокаПоиска");
			
		Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
			
			ДанныеВыбора = Новый СписокЗначений;
			Если Параметры.Свойство("ВидПеревозки") И ЗначениеЗаполнено(Параметры.ВидПеревозки) Тогда
				ДанныеВыбора.ЗагрузитьЗначения(ПолучитьДоступныеТС(Параметры));
			ИначеЕсли Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ
				|	упТранспортныеСредства.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
				|ГДЕ
				|	упТранспортныеСредства.Партнер.Организация = &Организация
				|	И упТранспортныеСредства.РегистрационныйНомер ПОДОБНО &РегистрационныйНомер";
				Запрос.УстановитьПараметр("Организация", Параметры.Организация);
				Запрос.УстановитьПараметр("РегистрационныйНомер", "%" + Параметры.СтрокаПоиска + "%");
				ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
			КонецЕсли;	
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		ОбъектСсылка = Неопределено;
		ВидТранспорта = Неопределено;
		ЗначенияЗаполнения = Неопределено;
		ЗначениеКопирования = Неопределено;
		
		Если Параметры.Свойство("Ключ", ОбъектСсылка) Тогда
			ВидТранспорта = упСервисныеФункцииВызовСервера.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "ВидТранспорта"); 			
		ИначеЕсли Истина
			И Параметры.Свойство("ЗначенияЗаполнения",ЗначенияЗаполнения)
			И ЗначенияЗаполнения.Свойство("ВидТранспорта",ВидТранспорта) Тогда
			
		ИначеЕсли Истина
			И Параметры.Свойство("ЗначениеКопирования",ЗначениеКопирования) Тогда	
	          ВидТранспорта = ЗначениеКопирования.ВидТранспорта;
		КонецЕсли;
		
		Если ВидТранспорта = ПредопределенноеЗначение("Перечисление.упВидыТранспорта.Контейнер") Тогда	
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаКонтейнера";
		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаСписка" Тогда	
		
		ВидТранспорта = Неопределено;

		Если Параметры.Свойство("ВидТранспорта", ВидТранспорта) Тогда
			
			Если ВидТранспорта = ПредопределенноеЗначение("Перечисление.упВидыТранспорта.Контейнер") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаСпискаКонтейнера";
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда	
		
		ВидТранспорта = Неопределено;

		Если Параметры.Свойство("ВидТранспорта", ВидТранспорта) Тогда
			
			Если ВидТранспорта = ПредопределенноеЗначение("Перечисление.упВидыТранспорта.Контейнер") Тогда	
				СтандартнаяОбработка = Ложь;
				ВыбраннаяФорма = "ФормаВыбораКонтейнера";
			КонецЕсли;

		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

// Процедура - Проверить уникальность регистрационного номера.
//
// Параметры:
//  РегистрационныйНомер	 - Строка	 - Рег. номер
//  Ссылка			 - СправочникСсылка.упТранспортныеСредства - Текущее транспортное средство.
//  Отказ				 - Булево	 - Истина, если проверка прошла неудачно.
//
Процедура ПроверитьУникальностьРегистрационногоНомера(РегистрационныйНомер, Ссылка, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	упТранспортныеСредства.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	               |ГДЕ
	               |	упТранспортныеСредства.ВидТранспорта = ЗНАЧЕНИЕ(Перечисление.упВидыТранспорта.Контейнер)
	               |	И упТранспортныеСредства.РегистрационныйНомер = &РегистрационныйНомер
	               |	И НЕ упТранспортныеСредства.ПометкаУдаления
	               |	И упТранспортныеСредства.Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("РегистрационныйНомер", РегистрационныйНомер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстСообщения = Нстр("ru = 'Существует контейнер с таким же номером:%1(%2)'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, РегистрационныйНомер, Выборка.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"РегистрационныйНомер","Объект",Отказ);
			ЗаписьЖурналаРегистрации(НСтр("ru = 'ТранспортныеСредства.Проверка'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			 					УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
			 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив доступных видов транспорта для отбора
//
// Параметры:
//  РежимФормированияКонтейнеров - Булево
//
Функция ПолучитьДоступныеВидыТранспорта(РежимФормированияКонтейнеров = Ложь) Экспорт
	
	мсвВидыТранспорта = Новый Массив;
	Если РежимФормированияКонтейнеров Тогда
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.Контейнер);	
	Иначе
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.Водный);
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.Воздушный);
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.ГрузовойАвтомобиль);
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.Железнодорожный);
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.ЛегковойАвтомобиль);
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.Автобус);
		мсвВидыТранспорта.Добавить(Перечисления.упВидыТранспорта.Спецтехника);
	КонецЕсли;
	
	Возврат мсвВидыТранспорта;
	
КонецФункции

#КонецОбласти

#КонецЕсли
