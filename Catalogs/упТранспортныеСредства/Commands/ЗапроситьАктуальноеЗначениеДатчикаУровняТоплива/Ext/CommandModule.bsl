
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды=Неопределено ИЛИ ПараметрКоманды.Количество()=0 Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Значение уровня топлива.'"),, НСтр("ru = 'Выберите список транспортных срудств.'"), БиблиотекаКартинок.Ошибка32);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаУровняТоплива", ЭтотОбъект, ПараметрКоманды);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Выпонить обращение к сервису мониторинга, для получения значения уровня топлива?';"
	+ " en = 'Do you want to continue?'"), Режим, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаУровняТоплива(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ТекстСообщения = ВыполнитьОбращениеКСервисуТопливаНаСервере(Параметры, Отказ);
	Если Отказ Тогда
		ДлиннаСообщения = СтрДлина(ТекстСообщения);
		Если ДлиннаСообщения<50 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Значение уровня топлива.'"),, ТекстСообщения, БиблиотекаКартинок.Ошибка32);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Значение уровня топлива.'"),, ТекстСообщения, БиблиотекаКартинок.Информация32);
		Для каждого ТекущееТС Из Параметры Цикл
			Оповестить("ЗначениеУровняТопливаАктуализировано", ТекущееТС);	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопросаУровняТоплива()

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыполнитьОбращениеКСервисуТопливаНаСервере(СписокТранспортныхСредств, Отказ)
	
	ТекстОшибки = упИнтеграцияССерверамиСбораДанных.ПолучитьДанныеТопливаТранспортныхСредствScoutSOAP(СписокТранспортныхСредств, Отказ);
	Возврат ТекстОшибки;
	
КонецФункции // ВыполнитьОбращениеКСервисуТопливаНаСервере()

#КонецОбласти 
