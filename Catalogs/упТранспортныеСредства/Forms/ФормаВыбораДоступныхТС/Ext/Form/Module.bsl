
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = Параметры.НачалоПериода;
	ОкончаниеПериода = Параметры.ОкончаниеПериода;
	Если Параметры.Свойство("РежимФормированияКонтейнеров") Тогда
		РежимФормированияКонтейнеров = Параметры.РежимФормированияКонтейнеров;
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(НачалоПериода) Или Не ЗначениеЗаполнено(ОкончаниеПериода) Тогда
		ТекстСообщения = НСтр("ru = 'Не задан период'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	ВидПеревозки = Параметры.ВидПеревозки;
	РежимПросмотраДиаграммыГанта = 1; // по умолчанию реальные ТС выводятся списком
	ЭтаФорма.МножественныйВыбор = Параметры.МножественныйВыбор;
	
	СхемаКомпоновки = упСервисныеФункции.СхемаКомпоновкиПоОбъектуМетаданных(Метаданные.Справочники.упТранспортныеСредства);
	Если Ложь Тогда // Для контекстной подсказки
	    СхемаКомпоновки = Новый СхемаКомпоновкиДанных;
	КонецЕсли;
	Справочники.упТранспортныеСредства.УстановитьОтборПриВыбореПоПараметрам(СхемаКомпоновки.НастройкиПоУмолчанию.Отбор, Параметры);
	упСервисныеФункции.НайтиДобавитьЭлементНастроекКомпоновкиПоПолю(СхемаКомпоновки.НастройкиПоУмолчанию.Выбор, "Ссылка");
	упСервисныеФункции.СкомпоноватьВКоллекциюЗначенийПоСхеме(СхемаКомпоновки,, СписокТСПоПараметрамВыбора);
	
	ИспользоватьРеальныеТранспорныеСредства = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПеревозки, "ВыборПеревозчика") <> Перечисления.упВариантыВыбораПеревозчика.ОформляетсяТендер);
	Элементы.ГруппаРеальныеТС.Видимость            = ИспользоватьРеальныеТранспорныеСредства;
	Элементы.ГруппаПодменюРежимПросморта.Видимость = ИспользоватьРеальныеТранспорныеСредства;
	
	ИспользоватьВиртуальныеТранспортныеСредства = ПолучитьФункциональнуюОпцию("упИспользоватьВиртуальныеТранспортныеСредства") И МножественныйВыбор;
	Элементы.УстановитьПолноеИспользованиеВиртуальныхТранспортныхСредств.Видимость = ИспользоватьВиртуальныеТранспортныеСредства;
	Элементы.ГруппаВиртуальныеТС.Видимость = ИспользоватьВиртуальныеТранспортныеСредства;
	Если Не ИспользоватьВиртуальныеТранспортныеСредства Тогда
		Элементы.ГруппаРеальныеТС.ОтображатьЗаголовок = Ложь;
	КонецЕсли; 
	Если Не МножественныйВыбор Тогда
		ЭтаФорма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	КонецЕсли; 
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	
	ЦветВыбраннойСтроки        = WebЦвета.СветлоЗеленый;
	ЦветВыбраннойСтрокиСЦиклами = WebЦвета.СветлоЗолотистый;
	КартинкаПеревозчик         = БиблиотекаКартинок.упПеревозчик;
	КартинкаТипТС              = БиблиотекаКартинок.упТипТС;
	
	КартинкаВыбор              = БиблиотекаКартинок.упВыбор;
	КартинкаВыборПериодичность = БиблиотекаКартинок.упВыборПериодичность;
	КартинкаПустая             = БиблиотекаКартинок.упПустаяКартинка;
	
	Если Параметры.ТаблицаРезультат <> Неопределено Тогда
		тзТаблицаРезультат = ДанныеФормыВЗначение(Параметры.ТаблицаРезультат, Тип("ТаблицаЗначений"));
		ТаблицаРезультат.Загрузить(тзТаблицаРезультат);
	КонецЕсли; 
	Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		СтрокаТС = ТаблицаРезультат.Добавить();
		СтрокаТС.ТранспортноеСредство = Параметры.ТекущаяСтрока;
	КонецЕсли;
	УстановитьПараметрыДиаграммыПоУмолчанию();
	ОбновитьШкалу(ЭтаФорма);
	ОбновитьДанныеОбАвтопарке(Истина, Истина);
	Если ЗначениеЗаполнено(Параметры.НачалоИнтервалаВыделения) Тогда
		ИнтервалВыделения = Диаграмма.ИнтервалыФона.Добавить(Параметры.НачалоИнтервалаВыделения, Параметры.КонецИнтервалаВыделения);
		ИнтервалВыделения.Цвет = Новый Цвет(230, 230, 255); // Светлосиний
	КонецЕсли; 
	
	ОбновитьВидФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимость();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДиаграммаВыбор(Элемент, Значения, СтандартнаяОбработка, Дата)
	
	СтандартнаяОбработка = Ложь;
	НеОткрыватьФормуОбъекта = Ложь;
	Если ТипЗнч(Значения) = Тип("Массив") Тогда
		Для каждого Значение Из Значения Цикл
			Если ТипЗнч(Значение) = Тип("ИнтервалДиаграммыГанта") Тогда 
				Если ТипЗнч(Значение.Расшифровка) = Тип("ДокументСсылка.упРейс") Тогда
					ОткрытьФорму("Документ.упРейс.ФормаОбъекта", Новый Структура("Ключ", Значение.Расшифровка), ЭтаФорма,,,, Новый ОписаниеОповещения("ОткрытиеФормыЭлементаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
					НеОткрыватьФормуОбъекта = Истина;
				ИначеЕсли ТипЗнч(Значение.Расшифровка) = Тип("ДокументСсылка.упРемонтИТОТС") Тогда 
					ОткрытьФорму("Документ.упРемонтИТОТС.ФормаОбъекта", Новый Структура("Ключ", Значение.Расшифровка), ЭтаФорма,,,, Новый ОписаниеОповещения("ОткрытиеФормыЭлементаЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
					НеОткрыватьФормуОбъекта = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	Если ТипЗнч(Значения) = Тип("ТочкаДиаграммыГанта") И ТипЗнч(Значения.Значение) = Тип("СправочникСсылка.упТранспортныеСредства") Тогда 
		ТранспортноеСредство = Значения.Значение;
		ТочкаДиаграммыГанта = Диаграмма.УстановитьТочку(ТранспортноеСредство);
		СтандартнаяСерия = Диаграмма.Серии.Получить(ИндексСтандартнойСерии);
		ЗначениеДиаграммыГанта = Диаграмма.ПолучитьЗначение(ТочкаДиаграммыГанта, СтандартнаяСерия);
		Если Не МножественныйВыбор Тогда
			ОповеститьОВыборе(ТранспортноеСредство);
			Возврат;
		КонецЕсли; 
		ИскомыеСтроки = ТаблицаРезультат.НайтиСтроки(Новый Структура("ТранспортноеСредство", ТранспортноеСредство));
		Если ИскомыеСтроки.Количество() Тогда
			НайденнаяСтрока = ИскомыеСтроки[0]; 
			Если НайденнаяСтрока.ПланироватьЦиклически Тогда
				НайденнаяСтрока.ПланироватьЦиклически = Ложь;
				Для каждого СтрокаВыбор Из ИскомыеСтроки Цикл
					ТаблицаРезультат.Удалить(СтрокаВыбор);
				КонецЦикла; 
				АвтоЦвет = Новый Цвет;
				ЗначениеДиаграммыГанта.ЦветФона = АвтоЦвет;
				ТочкаДиаграммыГанта.ЦветФона    = АвтоЦвет;
				ТочкаДиаграммыГанта.Картинка    = КартинкаПустая;
				ТочкаДиаграммыГанта.Текст = ПолучитьПредставлениеТС(ТранспортноеСредство);
			Иначе
				НайденнаяСтрока.ПланироватьЦиклически = Истина;
				НайденнаяСтрока.КоличествоЦиклов = ?(НайденнаяСтрока.КоличествоЦиклов = 0, 2, НайденнаяСтрока.КоличествоЦиклов); 
				ЗначениеДиаграммыГанта.ЦветФона = ЦветВыбраннойСтрокиСЦиклами;
				ТочкаДиаграммыГанта.ЦветФона    = ЦветВыбраннойСтрокиСЦиклами;
				ТочкаДиаграммыГанта.Картинка    = КартинкаВыборПериодичность;
				ТочкаДиаграммыГанта.Текст = НСтр(СтрШаблон("ru = '%1 - %2 цкл.'", ТочкаДиаграммыГанта.Текст, НайденнаяСтрока.КоличествоЦиклов));
			КонецЕсли; 
			
		Иначе
			ЗначениеДиаграммыГанта.ЦветФона = ЦветВыбраннойСтроки;
			ТочкаДиаграммыГанта.Картинка    = КартинкаВыбор;
			ТочкаДиаграммыГанта.ЦветФона    = ЦветВыбраннойСтроки;
			ДобавитьСтрокуВТаблицуРезультат(ТранспортноеСредство);
		КонецЕсли;
		НеОткрыватьФормуОбъекта = Истина;
	КонецЕсли;
	Если Не НеОткрыватьФормуОбъекта Тогда 
		Если ТипЗнч(Значения) = Тип("Массив") Тогда
			ТранспортноеСредство = Значения[0].Точка.Значение;
		ИначеЕсли ТипЗнч(Значения) = Тип("ЗначениеДиаграммыГанта") Тогда 
			ТранспортноеСредство = Значения.Точка.Значение;
		ИначеЕсли ТипЗнч(Значения) = Тип("ТочкаДиаграммыГанта") Тогда 
			ТранспортноеСредство = Значения.Значение;
		КонецЕсли; 
		ПоказатьЗначение(, ТранспортноеСредство);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеТС(ТранспортноеСредство)

	Результат = "";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА упТранспортныеСредства.СобственноеТранспортноеСредство = ИСТИНА
	|				И НЕ ЕСТЬNULL(упУчетныеПараметрыТранспортныхСредствТ.ПринятоКУчету, ЛОЖЬ)
	|				И НЕ упТранспортныеСредства.ВидТранспорта = ЗНАЧЕНИЕ(Перечисление.упВидыТранспорта.Контейнер)
	|			ТОГДА ""(неучетное) "" + упТранспортныеСредства.Наименование
	|		ИНАЧЕ упТранспортныеСредства.Наименование
	|	КОНЕЦ КАК ПредставлениеТС
	|ИЗ
	|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(, ТранспортноеСредство = &ТранспортноеСредство) КАК упУчетныеПараметрыТранспортныхСредствТ
	|		ПО (упУчетныеПараметрыТранспортныхСредствТ.ТранспортноеСредство = упТранспортныеСредства.Ссылка)
	|ГДЕ упТранспортныеСредства.Ссылка = &ТранспортноеСредство";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.ПредставлениеТС;
	КонецЕсли; 

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ДеревоТранспортныеСредстваПометкаПриИзменении(Элемент)
	
	текСтрока = Элементы.ДеревоТранспортныеСредства.ТекущиеДанные;
	Если текСтрока.Пометка И текСтрока.Количество = 0 Тогда
		текСтрока.Количество = 1;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоТранспортныеСредстваКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДеревоТранспортныеСредства.ТекущиеДанные;
	
	Если ТекущаяСтрока.КоличествоВиртуальныхМакс < ТекущаяСтрока.Количество Тогда
		КомментарийОшибки = СтрШаблон("ru = 'Ошибка! Превышено максимально доступное количество машин на %1 ед'", (ТекущаяСтрока.Количество - ТекущаяСтрока.КоличествоВиртуальныхМакс));
		ТекущаяСтрока.Количество = 0;
		ТекущаяСтрока.Пометка = Ложь;
		ПоказатьПредупреждение(, НСтр(КомментарийОшибки));
	ИначеЕсли ТекущаяСтрока.Количество = 0 Тогда	
		ТекущаяСтрока.Пометка = Ложь;
	Иначе	
		ТекущаяСтрока.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

&НаКлиенте
Процедура ОбновитьДанныеНаФорме(Команда)
	
	ОбновитьДанныеОбАвтопарке(Истина, Истина);
	ОбновитьВидимость();
	
КонецПроцедуры // ОбновитьДанныеНаФорме()

&НаКлиенте
Процедура УстановитьПолноеИспользованиеВиртуальныхТранспортныхСредств(Команда, НовоеИспользование = Истина)
	
	ПеревозчикЭлементы = ДеревоТранспортныеСредства.ПолучитьЭлементы();
	Для каждого Перевозчик Из ПеревозчикЭлементы Цикл
		Если Не ЗначениеЗаполнено(Перевозчик.Перевозчик) Тогда
			Продолжить;
		КонецЕсли; 
		ТипТСЭлементы = Перевозчик.ПолучитьЭлементы();
		Для каждого ТипТС Из ТипТСЭлементы Цикл
			ТипТС.Количество = ТипТС.КоличествоВиртуальныхМакс;
			ТипТС.Пометка = НовоеИспользование;
		КонецЦикла; 
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьДанныеНаДиаграммеДеревом(Команда)
	
	Если РежимПросмотраДиаграммыГанта = 1 Тогда // список
		РежимПросмотраДиаграммыГанта = 0;
		ОбновитьДанныеОбАвтопарке(Истина, Ложь);
		ОбновитьВидимость();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьДанныеНаДиаграммеСписком(Команда)
	
	Если РежимПросмотраДиаграммыГанта = 0 Тогда // дерево
		РежимПросмотраДиаграммыГанта = 1;
		ОбновитьДанныеОбАвтопарке(Истина, Ложь);
		ОбновитьВидимость();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	сткРезультат = ОформитьРезультатВыбора();
	Закрыть(сткРезультат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВыбранныеТранспорты(Команда)
	
	ТаблицаРезультат.Очистить();
	ОбновитьДанныеОбАвтопарке(Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоличествоЦиклов(Команда)
	
	ПродолжитьОткрытиеФормы = Ложь;
	Для каждого текТС Из ТаблицаРезультат Цикл
		Если текТС.ПланироватьЦиклически Тогда
			ПродолжитьОткрытиеФормы = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПродолжитьОткрытиеФормы Тогда
		сткПараметры = Новый Структура("СписокТранспорта, РежимФормированияКонтейнеров", ТаблицаРезультат, РежимФормированияКонтейнеров);
		ОткрытьФорму("Справочник.упТранспортныеСредства.Форма.ФормаУстановкиЦикловТранспорта", сткПараметры, ЭтаФорма,,,, Новый ОписаниеОповещения("ИзменитьКоличествоЦикловЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(СтрШаблон("ru = 'В списке реальных %1 отсутствуют выбранные с доступным циклическим планированием. Операция отменена.'", ?(РежимФормированияКонтейнеров,"контейнеров", "транспортных средств"))));
	КонецЕсли; 
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Результат - соответствие, где ключ - транспортное средство, значение - количесвто циклов.
//
Процедура ИзменитьКоличествоЦикловЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Для каждого текТС Из ТаблицаРезультат Цикл
			Если текТС.ПланироватьЦиклически Тогда
				текТС.КоличествоЦиклов = Результат.Получить(текТС.ТранспортноеСРедство);
			КонецЕсли; 
		КонецЦикла;
		ОбновитьДанныеОбАвтопарке(Истина, Ложь);
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Функция ОформитьРезультатВыбора()
	
	// удаление устаревших значений из таблицы результата выбора ТС
	мсвТранспортныхСредств = Новый Массив;
	Если РежимПросмотраДиаграммыГанта = 0 Тогда // дерево
		Для каждого ТочкаПеревозчик Из Диаграмма.Точки Цикл
			Для каждого ТочкаТипТС Из ТочкаПеревозчик.Точки Цикл
				Для каждого ТочкаТС Из ТочкаТипТС.Точки Цикл
					мсвТранспортныхСредств.Добавить(ТочкаТС.Значение);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла; 
	Иначе // список
		Для каждого ТочкаТС Из Диаграмма.Точки Цикл
			мсвТранспортныхСредств.Добавить(ТочкаТС.Значение);
		КонецЦикла;
	КонецЕсли;
	
	СтрокиДляУдаления = ТаблицаРезультат.НайтиСтроки(Новый Структура("ТранспортноеСредство", Справочники.упТранспортныеСредства.ПустаяСсылка()));
	Для каждого текСтрока Из СтрокиДляУдаления Цикл
		ТаблицаРезультат.Удалить(текСтрока);
	КонецЦикла; 
	
	ПеревозчикЭлементы = ДеревоТранспортныеСредства.ПолучитьЭлементы();
	Для каждого Перевозчик Из ПеревозчикЭлементы Цикл
		 ТипТСЭлементы = Перевозчик.ПолучитьЭлементы();
		 Для каждого ТипТС Из ТипТСЭлементы Цикл
			 Если ТипТС.Пометка Тогда
				 ЗаполнитьЗначенияСвойств(ТаблицаРезультат.Добавить(), ТипТС);
			КонецЕсли;	 
		 КонецЦикла; 
	КонецЦикла; 
	
	КоличествоСтрок = ТаблицаРезультат.Количество();
	Сч = 0;
	Пока Сч < КоличествоСтрок Цикл
		текСтрока = ТаблицаРезультат.Получить(Сч);
		Если ЗначениеЗаполнено(текСтрока.ТранспортноеСредство) Тогда
			Если мсвТранспортныхСредств.Найти(текСтрока.ТранспортноеСредство) = Неопределено Тогда
				ТаблицаРезультат.Удалить(текСтрока);
				КоличествоСтрок = КоличествоСтрок - 1;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	
	сткРезультат = Новый Структура;
	сткРезультат.Вставить("ТаблицаРезультат", ТаблицаРезультат);
	
	Возврат сткРезультат;
	
КонецФункции

&НаКлиенте
// Процедура вызывается при закрытии формы элемента документов "упРейс" или "упРемонтИТОТС".
// После возможного изменения документа производится обновление диаграммы Ганта.
//
// Параметры:
//  Результат  - Структура - Результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ОткрытиеФормыЭлементаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьДанныеОбАвтопарке(Истина, Ложь);
	ОбновитьВидимость();
	
КонецПроцедуры // ОткрытиеФормыЭлементаЗавершение()

&НаКлиенте
Процедура ОбновитьВидимость()
	
	Если РежимПросмотраДиаграммыГанта = 0 Тогда // дерево
		Элементы.ОтобразитьДанныеНаДиаграммеДеревом.Пометка = Истина;
	    Элементы.ОтобразитьДанныеНаДиаграммеСписком.Пометка = Ложь;
		Диаграмма.ОбластьПостроения.Заголовок = НСтр("ru = 'Перевозчик/Тип ТС/ТС'", КодЯзыка);
	Иначе
		Элементы.ОтобразитьДанныеНаДиаграммеДеревом.Пометка = Ложь;
	    Элементы.ОтобразитьДанныеНаДиаграммеСписком.Пометка = Истина;
		Диаграмма.ОбластьПостроения.Заголовок = НСтр("ru = 'Транспортное средство'", КодЯзыка);
	КонецЕсли;
 	
КонецПроцедуры // ОбновитьВидимость()

&НаСервере
Функция ПолучитьРасписаниеРаботыТС(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("СписокТСПоПараметрамВыбора", СписокТСПоПараметрамВыбора);
	Запрос.Текст = 
"
|ВЫБРАТЬ
|	упТранспортныеСредства.Партнер КАК Перевозчик,
|	упТранспортныеСредства.ТипТС КАК ТипТС,
|	упТранспортныеСредства.Ссылка КАК ТранспортноеСредство,
|	упТранспортныеСредства.ГрафикРаботы,
|	упТранспортныеСредства.ПерерывМеждуРейсами,
|	ВЫБОР
|		КОГДА упТранспортныеСредства.СобственноеТранспортноеСредство = ИСТИНА
|				И НЕ ЕСТЬNULL(упУчетныеПараметрыТранспортныхСредствТ.ПринятоКУчету, ЛОЖЬ)
|				И НЕ упТранспортныеСредства.ВидТранспорта = ЗНАЧЕНИЕ(Перечисление.упВидыТранспорта.Контейнер)
|			ТОГДА ""(неучетное) "" + упТранспортныеСредства.Наименование
|		ИНАЧЕ упТранспортныеСредства.Наименование
|	КОНЕЦ КАК ПредставлениеТС
|ПОМЕСТИТЬ втТранспортныеСредства05
|ИЗ
|	втПеревозчики КАК втПеревозчики
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТранспортныеСредства КАК упТранспортныеСредства
|		ПО втПеревозчики.Перевозчик = упТранспортныеСредства.Партнер
|			И (НЕ упТранспортныеСредства.ПометкаУдаления) И (НЕ упТранспортныеСредства.Прицеп)
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУчетныеПараметрыТранспортныхСредств.СрезПоследних(, ) КАК упУчетныеПараметрыТранспортныхСредствТ
|		ПО (упУчетныеПараметрыТранспортныхСредствТ.ТранспортноеСредство = упТранспортныеСредства.Ссылка)
|ГДЕ упТранспортныеСредства.Ссылка В (&СписокТСПоПараметрамВыбора)
|ИНДЕКСИРОВАТЬ ПО
|	ТранспортноеСредство
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втТранспортныеСредства.ТранспортноеСредство,
|	КОЛИЧЕСТВО(втТранспортныеСредства2.ТранспортноеСредство) КАК Идентификатор
|ПОМЕСТИТЬ втНомераТС05
|ИЗ
|	втТранспортныеСредства05 КАК втТранспортныеСредства
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТранспортныеСредства05 КАК втТранспортныеСредства2
|		ПО втТранспортныеСредства.ТранспортноеСредство >= втТранспортныеСредства2.ТранспортноеСредство
|
|СГРУППИРОВАТЬ ПО
|	втТранспортныеСредства.ТранспортноеСредство
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втТранспортныеСредства.Перевозчик,
|	втТранспортныеСредства.ТипТС,
|	МАКСИМУМ(втНомераТС.Идентификатор) КАК Идентификатор
|ПОМЕСТИТЬ втНомераТС10
|ИЗ
|	втТранспортныеСредства05 КАК втТранспортныеСредства
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераТС05 КАК втНомераТС
|		ПО втТранспортныеСредства.ТранспортноеСредство = втНомераТС.ТранспортноеСредство
|
|СГРУППИРОВАТЬ ПО
|	втТранспортныеСредства.Перевозчик,
|	втТранспортныеСредства.ТипТС
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втТранспортныеСредства.Перевозчик,
|	втТранспортныеСредства.ТипТС,
|	втТранспортныеСредства.ТранспортноеСредство,
|	втТранспортныеСредства.ГрафикРаботы,
|	втТранспортныеСредства.ПерерывМеждуРейсами,
|	втТранспортныеСредства.ПредставлениеТС,
|   ЕСТЬNULL(втНомераТС.Идентификатор, 1) КАК Идентификатор
|ПОМЕСТИТЬ втТранспортныеСредства10
|ИЗ
|	втТранспортныеСредства05 КАК втТранспортныеСредства
|		ЛЕВОЕ СОЕДИНЕНИЕ втНомераТС10 КАК втНомераТС
|		ПО втТранспортныеСредства.Перевозчик = втНомераТС.Перевозчик
|			И втТранспортныеСредства.ТипТС = втНомераТС.ТипТС
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗЛИЧНЫЕ
|	втТранспортныеСредства.ГрафикРаботы
|ПОМЕСТИТЬ втГрафикиРаботы
|ИЗ
|	втТранспортныеСредства10 КАК втТранспортныеСредства
|ГДЕ
|	втТранспортныеСредства.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ШаблонЗаполнения.Ссылка КАК ГрафикРаботы,
|	МАКСИМУМ(ШаблонЗаполнения.НомерСтроки) КАК ДлинаЦикла
|ПОМЕСТИТЬ ВТДлинаЦиклаГрафиков
|ИЗ
|	Справочник.Календари.ШаблонЗаполнения КАК ШаблонЗаполнения
|ГДЕ
|	ШаблонЗаполнения.Ссылка В
|			(ВЫБРАТЬ
|				втГрафикиРаботы.ГрафикРаботы
|			ИЗ
|				втГрафикиРаботы)
|
|СГРУППИРОВАТЬ ПО
|	ШаблонЗаполнения.Ссылка
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	Календари.Ссылка КАК ГрафикРаботы,
|	ДанныеПроизводственногоКалендаря.Дата КАК ДатаГрафика,
|	ДанныеПроизводственногоКалендаря.ДатаПереноса,
|	ВЫБОР
|		КОГДА ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
|			ТОГДА ИСТИНА
|		ИНАЧЕ ЛОЖЬ
|	КОНЕЦ КАК ПредпраздничныйДень
|ПОМЕСТИТЬ ВТСведенияПоКалендарю
|ИЗ
|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
|		ПО ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = Календари.ПроизводственныйКалендарь
|			И (Календари.Ссылка В
|				(ВЫБРАТЬ
|					втГрафикиРаботы.ГрафикРаботы
|				ИЗ
|					втГрафикиРаботы))
|			И (ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
|			И (ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
|				ИЛИ ДанныеПроизводственногоКалендаря.ДатаПереноса <> ДАТАВРЕМЯ(1, 1, 1))
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	КалендарныеГрафики.Календарь КАК ГрафикРаботы,
|	КалендарныеГрафики.ДатаГрафика КАК ДатаГрафика,
|	РАЗНОСТЬДАТ(Календари.ДатаОтсчета, КалендарныеГрафики.ДатаГрафика, ДЕНЬ) + 1 КАК ДнейОтДатыОтсчета,
|	СведенияПоКалендарю.ПредпраздничныйДень,
|	СведенияПоКалендарю.ДатаПереноса
|ПОМЕСТИТЬ ВТДниВключенныеВГрафик
|ИЗ
|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
|		ПО КалендарныеГрафики.Календарь = Календари.Ссылка
|			И (КалендарныеГрафики.Календарь В
|				(ВЫБРАТЬ
|					втГрафикиРаботы.ГрафикРаботы
|				ИЗ
|					втГрафикиРаботы))
|			И (КалендарныеГрафики.ДатаГрафика МЕЖДУ НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ) И &ОкончаниеПериода)
|			И (КалендарныеГрафики.ДеньВключенВГрафик)
|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияПоКалендарю КАК СведенияПоКалендарю
|		ПО (СведенияПоКалендарю.ГрафикРаботы = КалендарныеГрафики.Календарь)
|			И (СведенияПоКалендарю.ДатаГрафика = КалендарныеГрафики.ДатаГрафика)
|
|
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ДниВключенныеВГрафик.ГрафикРаботы,
|	ДниВключенныеВГрафик.ДатаГрафика,
|	ВЫБОР
|		КОГДА ДниВключенныеВГрафик.РезультатДеленияПоМодулю = 0
|			ТОГДА ДниВключенныеВГрафик.ДлинаЦикла
|		ИНАЧЕ ДниВключенныеВГрафик.РезультатДеленияПоМодулю
|	КОНЕЦ КАК НомерДня,
|	ДниВключенныеВГрафик.ПредпраздничныйДень
|ПОМЕСТИТЬ ВТДатыНомераДней
|ИЗ
|	(ВЫБРАТЬ
|		ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
|		ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
|		ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
|		ДниВключенныеВГрафик.ДлинаЦикла КАК ДлинаЦикла,
|		ДниВключенныеВГрафик.ДнейОтДатыОтсчета - ДниВключенныеВГрафик.ЦелаяЧастьРезультатаДеления * ДниВключенныеВГрафик.ДлинаЦикла КАК РезультатДеленияПоМодулю
|	ИЗ
|		(ВЫБРАТЬ
|			ДниВключенныеВГрафик.ГрафикРаботы КАК ГрафикРаботы,
|			ДниВключенныеВГрафик.ДатаГрафика КАК ДатаГрафика,
|			ДниВключенныеВГрафик.ПредпраздничныйДень КАК ПредпраздничныйДень,
|			ДниВключенныеВГрафик.ДнейОтДатыОтсчета КАК ДнейОтДатыОтсчета,
|			ДлинаЦиклов.ДлинаЦикла КАК ДлинаЦикла,
|			(ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) - ВЫБОР
|				КОГДА (ВЫРАЗИТЬ(ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла КАК ЧИСЛО(15, 0))) > ДниВключенныеВГрафик.ДнейОтДатыОтсчета / ДлинаЦиклов.ДлинаЦикла
|					ТОГДА 1
|				ИНАЧЕ 0
|			КОНЕЦ КАК ЦелаяЧастьРезультатаДеления
|		ИЗ
|			ВТДниВключенныеВГрафик КАК ДниВключенныеВГрафик
|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
|				ПО ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
|					И (Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоЦикламПроизвольнойДлины))
|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДлинаЦиклаГрафиков КАК ДлинаЦиклов
|				ПО ДниВключенныеВГрафик.ГрафикРаботы = ДлинаЦиклов.ГрафикРаботы) КАК ДниВключенныеВГрафик) КАК ДниВключенныеВГрафик
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	ДниВключенныеВГрафик.ГрафикРаботы,
|	ДниВключенныеВГрафик.ДатаГрафика,
|	ВЫБОР
|		КОГДА ДниВключенныеВГрафик.ДатаПереноса ЕСТЬ NULL 
|			ТОГДА ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаГрафика)
|		ИНАЧЕ ДЕНЬНЕДЕЛИ(ДниВключенныеВГрафик.ДатаПереноса)
|	КОНЕЦ,
|	ДниВключенныеВГрафик.ПредпраздничныйДень
|ИЗ
|	ВТДниВключенныеВГрафик КАК ДниВключенныеВГрафик
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
|		ПО ДниВключенныеВГрафик.ГрафикРаботы = Календари.Ссылка
|ГДЕ
|	Календари.СпособЗаполнения = ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияГрафикаРаботы.ПоНеделям)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗЛИЧНЫЕ
|	ДниВключенныеВГрафик.ГрафикРаботы,
|	ДниВключенныеВГрафик.ДатаГрафика,
|	ДниВключенныеВГрафик.НомерДня,
|	ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяНачала, РасписанияРаботы.ВремяНачала) КАК ВремяНачала,
|	ЕСТЬNULL(РасписанияРаботыПредпраздничногоДня.ВремяОкончания, РасписанияРаботы.ВремяОкончания) КАК ВремяОкончания
|ПОМЕСТИТЬ ВТРасписанияРаботы
|ИЗ
|	ВТДатыНомераДней КАК ДниВключенныеВГрафик
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботы
|		ПО (РасписанияРаботы.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы)
|			И (РасписанияРаботы.НомерДня = ДниВключенныеВГрафик.НомерДня)
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Календари.РасписаниеРаботы КАК РасписанияРаботыПредпраздничногоДня
|		ПО (РасписанияРаботыПредпраздничногоДня.Ссылка = ДниВключенныеВГрафик.ГрафикРаботы)
|			И (РасписанияРаботыПредпраздничногоДня.НомерДня = 0)
|			И (ДниВключенныеВГрафик.ПредпраздничныйДень)
|
|ИНДЕКСИРОВАТЬ ПО
|	ДниВключенныеВГрафик.ГрафикРаботы,
|	ДниВключенныеВГрафик.ДатаГрафика
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	РасписанияРаботы.ГрафикРаботы,
|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяНачала) * 3600 + МИНУТА(РасписанияРаботы.ВремяНачала) * 60 + СЕКУНДА(РасписанияРаботы.ВремяНачала)), РасписанияРаботы.ДатаГрафика) КАК НачалоПериода,
|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяОкончания) * 3600 + МИНУТА(РасписанияРаботы.ВремяОкончания) * 60 + СЕКУНДА(РасписанияРаботы.ВремяОкончания)), КОНЕЦПЕРИОДА(РасписанияРаботы.ДатаГрафика, ДЕНЬ)) КАК КонецПериода
|ПОМЕСТИТЬ втРабочийГрафикПодготовка
|ИЗ
|	ВТРасписанияРаботы КАК РасписанияРаботы
|ГДЕ
|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяНачала) * 3600 + МИНУТА(РасписанияРаботы.ВремяНачала) * 60 + СЕКУНДА(РасписанияРаботы.ВремяНачала)), РасписанияРаботы.ДатаГрафика) < &ОкончаниеПериода
|	И ЕСТЬNULL(ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, СЕКУНДА, ЧАС(РасписанияРаботы.ВремяОкончания) * 3600 + МИНУТА(РасписанияРаботы.ВремяОкончания) * 60 + СЕКУНДА(РасписанияРаботы.ВремяОкончания)), КОНЕЦПЕРИОДА(РасписанияРаботы.ДатаГрафика, ДЕНЬ)) > &НачалоПериода
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРабочийГрафик.ГрафикРаботы,
|	ВЫБОР
|		КОГДА втРабочийГрафик.НачалоПериода < &НачалоПериода
|			ТОГДА &НачалоПериода
|		ИНАЧЕ втРабочийГрафик.НачалоПериода
|	КОНЕЦ КАК ДатаНачала,
|	ВЫБОР
|		КОГДА втРабочийГрафик.КонецПериода > &ОкончаниеПериода
|			ТОГДА &ОкончаниеПериода
|		ИНАЧЕ втРабочийГрафик.КонецПериода
|	КОНЕЦ КАК ДатаОкончания
|ПОМЕСТИТЬ втРабочийГрафик
|ИЗ
|	втРабочийГрафикПодготовка КАК втРабочийГрафик
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втТранспортныеСредства.Перевозчик,
|	втТранспортныеСредства.ТипТС,
|	втТранспортныеСредства.ТранспортноеСредство,
|	втТранспортныеСредства.ПерерывМеждуРейсами,
|	втТранспортныеСредства.ПредставлениеТС,
|	втТранспортныеСредства.Идентификатор,
|	ЕСТЬNULL(упРасписаниеРаботыТС.ДатаНачала, &НачалоПериода) КАК ДатаНачала,
|	ЕСТЬNULL(упРасписаниеРаботыТС.ДатаОкончания, &ОкончаниеПериода) КАК ДатаОкончания,
|	упРасписаниеРаботыТС.СостояниеТранспорта.ТипСостоянияТС КАК ТипСостояния,
|	//упРасписаниеРаботыТС.ДокументОснование КАК Расшифровка
|	упРасписаниеРаботыТС.Документ КАК Расшифровка
|ПОМЕСТИТЬ втРасписание
|ИЗ
|	втТранспортныеСредства10 КАК втТранспортныеСредства
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.упЗанятостьТранспорта.СрезПоследних КАК упРасписаниеРаботыТС
|		ПО втТранспортныеСредства.ТранспортноеСредство = упРасписаниеРаботыТС.Транспорт
|			И упРасписаниеРаботыТС.ПланАктуален
|			И (&ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1)
|				ИЛИ упРасписаниеРаботыТС.ДатаНачала <= &ОкончаниеПериода)
|			И (&НачалоПериода = ДАТАВРЕМЯ(1, 1, 1)
|				ИЛИ упРасписаниеРаботыТС.ДатаОкончания >= &НачалоПериода)
|
|ИНДЕКСИРОВАТЬ ПО
|	втТранспортныеСредства.ТранспортноеСредство,
|	упРасписаниеРаботыТС.СостояниеТранспорта.ТипСостоянияТС
|
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРасписание.Перевозчик,
|	втРасписание.ТипТС,
|	втРасписание.ТранспортноеСредство,
|	втРасписание.ДатаНачала,
|	втРасписание.ДатаОкончания,
|	втРасписание.ТипСостояния,
|	ЛОЖЬ КАК ТребуетсяДопОбработка,
|	втРасписание.Расшифровка,
|	втРасписание.Идентификатор,
|	втРасписание.ПредставлениеТС
|ПОМЕСТИТЬ втИтог
|ИЗ
|	втРасписание КАК втРасписание
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	втТранспортныеСредства.Перевозчик,
|	втТранспортныеСредства.ТипТС,
|	втТранспортныеСредства.ТранспортноеСредство,
|	&НачалоПериода,
|	&ОкончаниеПериода,
|	ЗНАЧЕНИЕ(Перечисление.упТипыСостоянийТС.Свободно),
|	ИСТИНА,
|	NULL,
|	втТранспортныеСредства.Идентификатор,
|	втТранспортныеСредства.ПредставлениеТС
|ИЗ
|	втТранспортныеСредства10 КАК втТранспортныеСредства
|ГДЕ
|	втТранспортныеСредства.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	втТранспортныеСредства.Перевозчик,
|	втТранспортныеСредства.ТипТС,
|	втТранспортныеСредства.ТранспортноеСредство,
|	ЕСТЬNULL(втРабочийГрафик.ДатаНачала, &НачалоПериода),
|	ЕСТЬNULL(втРабочийГрафик.ДатаОкончания, ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, 1)),
|	ЗНАЧЕНИЕ(Перечисление.упТипыСостоянийТС.Свободно),
|	ИСТИНА,
|	NULL,
|	втТранспортныеСредства.Идентификатор,
|	втТранспортныеСредства.ПредставлениеТС
|ИЗ
|	втТранспортныеСредства10 КАК втТранспортныеСредства
|		ЛЕВОЕ СОЕДИНЕНИЕ втРабочийГрафик КАК втРабочийГрафик
|		ПО втТранспортныеСредства.ГрафикРаботы = втРабочийГрафик.ГрафикРаботы
|			И (втТранспортныеСредства.ГрафикРаботы <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка))
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	втРасписание.Перевозчик,
|	втРасписание.ТипТС,
|	втРасписание.ТранспортноеСредство,
|	ДОБАВИТЬКДАТЕ(втРасписание.ДатаОкончания, СЕКУНДА, 1),
|	ДОБАВИТЬКДАТЕ(втРасписание.ДатаОкончания, СЕКУНДА, 1 + втРасписание.ТранспортноеСредство.ПерерывМеждуРейсами * 3600),
|	""Reserve"",
|	ЛОЖЬ,
|	NULL,
|	втРасписание.Идентификатор,
|	втРасписание.ПредставлениеТС
|ИЗ
|	втРасписание КАК втРасписание
|ГДЕ
|	втРасписание.ПерерывМеждуРейсами > 0
|	И втРасписание.ТипСостояния = ЗНАЧЕНИЕ(Перечисление.упТипыСостоянийТС.ВРейсе)
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	втРасписание.Перевозчик,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втРасписание.Перевозчик) КАК ПредставлениеПеревозчика,
|	втРасписание.ТипТС,
|	ПРЕДСТАВЛЕНИЕССЫЛКИ(втРасписание.ТипТС) КАК ПредставлениеТипаТС,
|	втРасписание.ТранспортноеСредство,
|	втРасписание.ПредставлениеТС КАК ПредставлениеТранспортногоСредства,
|	втРасписание.ДатаНачала,
|	втРасписание.ДатаОкончания,
|	втРасписание.ТипСостояния,
|	втРасписание.ТребуетсяДопОбработка,
|	втРасписание.Расшифровка,
|	втРасписание.Идентификатор
|ИЗ
|	втИтог КАК втРасписание
|";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьРасписаниеРаботыТС()

&НаСервере
Функция ПолучитьДоступныеВиртуальныеТС(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 	
    "ВЫБРАТЬ
    |	втПеревозчики.Перевозчик КАК Перевозчик,
    |	ДоступныеТипыТС.ТипТС КАК ТипТС,
    |	ДоступныеТипыТС.ТипТС.Грузоподъемность КАК Грузоподъемность,
    |	ДоступныеТипыТС.ТипТС.Объем КАК Объем,
    |	ДоступныеТипыТС.ТипТС.КоличествоМест КАК КоличествоМест,
    |	ДоступныеТипыТС.ТипТС.КоличествоЗагрузочныхМетров КАК КоличествоЗагрузочныхМетров,
    |	0 КАК Порядок,
    |	ЕСТЬNULL(ДоступныеТипыТС.КоличествоМакс, 99999) КАК КоличествоВиртуальныхМакс
    |ИЗ
    |	втПеревозчики КАК втПеревозчики
    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры.ДоступныеТипыТС КАК ДоступныеТипыТС
    |		ПО втПеревозчики.Перевозчик = ДоступныеТипыТС.Ссылка
    |			И (ДоступныеТипыТС.ТипТС.ВидТранспорта В (&ВидыТранспорта))
    |
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |	ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка),
    |	упТипыТС.Ссылка,
    |	упТипыТС.Грузоподъемность,
    |	упТипыТС.Объем,
    |	упТипыТС.КоличествоМест,
    |	упТипыТС.КоличествоЗагрузочныхМетров,
    |	1,
    |	99999
    |ИЗ
    |	Справочник.упТипыТС КАК упТипыТС
    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
    |		ПО (НЕ упТипыТС.ПометкаУдаления)
    |			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
    |			И (упТипыТС.ВидТранспорта В (&ВидыТранспорта))
    |
    |УПОРЯДОЧИТЬ ПО
    |	Порядок,
    |	Перевозчик,
    |	ТипТС
    |ИТОГИ
    |	МАКСИМУМ(Порядок)
    |ПО
    |	Перевозчик";
	Запрос.УстановитьПараметр("ВидыТранспорта", Справочники.упТранспортныеСредства.ПолучитьДоступныеВидыТранспорта(РежимФормированияКонтейнеров));
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
КонецФункции // ПолучитьДоступныеВиртуальныеТС()

&НаСервере
Процедура ОбновитьДанныеОбАвтопарке(ОбновлятьРеальные, ОбновлятьВиртуальные)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ПараметрыЗапроса = Новый Структура("ВидПеревозки, Организация, Перевозчик, ТипТС, РежимФормированияКонтейнеров", ВидПеревозки, Справочники.Организации.ПустаяСсылка(),,, РежимФормированияКонтейнеров);
	Справочники.упТранспортныеСредства.ПолучитьДоступныеТС(ПараметрыЗапроса, МенеджерВременныхТаблиц);
	
	Если ИспользоватьРеальныеТранспорныеСредства И ОбновлятьРеальные Тогда
		ТаблицаРасписание = ПолучитьРасписаниеРаботыТС(МенеджерВременныхТаблиц);
		ОбновитьДанныеНаДиаграмме(ТаблицаРасписание);
	КонецЕсли;
	
	Если ИспользоватьВиртуальныеТранспортныеСредства И ОбновлятьВиртуальные Тогда
		ДеревоТиповТС = ПолучитьДоступныеВиртуальныеТС(МенеджерВременныхТаблиц);
		ОбновитьДеревоТранспортныхСредствНаСервере(ДеревоТиповТС);
	КонецЕсли;
	
КонецПроцедуры // ОбновитьДанныеОбАвтопарке()

// Процедура перезаполняет дерево доступных типов ТС
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура ОбновитьДеревоТранспортныхСредствНаСервере(ДеревоРезультатов)

	ДеревоТранспортныеСредства.ПолучитьЭлементы().Очистить();
	Дерево = РеквизитФормыВЗначение("ДеревоТранспортныеСредства");
	
	// определение структуры для поиска ранее выбранных значений
	сткПоиска = Новый Структура;
	сткПоиска.Вставить("Перевозчик");
	сткПоиска.Вставить("ТипТС");
	сткПоиска.Вставить("ТранспортноеСредство", Справочники.упТранспортныеСредства.ПустаяСсылка());
	
	Для каждого Стр Из ДеревоРезультатов.Строки Цикл  // перевозчики
		НоваяСтрокаПеревозчик = Дерево.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПеревозчик, Стр);
		НоваяСтрокаПеревозчик.Представление = ?(ЗначениеЗаполнено(Стр.Перевозчик), Строка(Стр.Перевозчик), "Виртуальные");
		
		Для каждого СтрокаТипТС Из Стр.Строки Цикл // типы ТС
			
			ЗаполнитьЗначенияСвойств(сткПоиска, СтрокаТипТС);
			
			НоваяСтрокаТип = НоваяСтрокаПеревозчик.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТип, СтрокаТипТС);
			НоваяСтрокаТип.Представление = Строка(СтрокаТипТС.ТипТС);
			
			ИскомыеСтроки = ТаблицаРезультат.НайтиСтроки(сткПоиска);
			Для каждого СтрокаВыбор Из ИскомыеСтроки Цикл
				новКоличество = НоваяСтрокаТип.Количество + СтрокаВыбор.Количество;
				НоваяСтрокаТип.Количество = Мин(новКоличество, НоваяСтрокаТип.КоличествоВиртуальныхМакс);
			КонецЦикла; 
			Если НоваяСтрокаТип.Количество Тогда
				НоваяСтрокаТип.Пометка = Истина;
			КонецЕсли;
			
		КонецЦикла; 
	КонецЦикла; 
	
	ЗначениеВДанныеФормы(Дерево, ДеревоТранспортныеСредства);
	
КонецПроцедуры // ОбновитьДеревоТранспортныхСредствНаСервере()

&НаСервере
Процедура ОбновитьВидФормы()
	
	Если РежимФормированияКонтейнеров Тогда
		Элементы.ФормаОчиститьВыбор.Заголовок = НСтр("ru = 'Выбранные контейнеры'");
		Элементы.ГруппаВиртуальныеТС.Заголовок = НСтр("ru = 'Виртуальные контейнеры'");
		Элементы.ГруппаРеальныеТС.Заголовок = НСтр("ru = 'Реальные контейнеры'");
		Элементы.ДеревоТранспортныеСредстваПредставление.Заголовок = НСтр("ru = 'Владелец / Тип контейнера'");
		Заголовок = НСтр("ru = 'Выбор доступных контейнеров'");
	КонецЕсли; 
	
КонецПроцедуры

#Область ДиаграммаГанта

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьШкалу(Форма)

	Диаграмма = Форма.Диаграмма;
	
	ШкалаВремениЭлементы = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы;
	
	ПервыйЭлементШкалы = ШкалаВремениЭлементы[0];
	Для Сч = -ШкалаВремениЭлементы.Количество()+1 по -1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[-Сч]);
	КонецЦикла;
	
	// День, час
	ПервыйЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.День;
	ПервыйЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
	ПервыйЭлементШкалы.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяцаДеньНедели;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = ТипЕдиницыШкалыВремени.Час;
	ЭлементШкалы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДиаграммыПоУмолчанию()

	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	Диаграмма.ПоддержкаМасштаба               = ПоддержкаМасштабаДиаграммыГанта.Авто;
	Диаграмма.Анимация                        = АнимацияДиаграммы.Использовать;
	Диаграмма.ВертикальнаяПрокрутка           = Истина;
	Диаграмма.ОтображениеИнтервала            = ОтображениеИнтервалаДиаграммыГанта.Плоский;
	Диаграмма.ОтображатьПустыеЗначения        = Ложь;
	Диаграмма.ОтображатьЗаголовок             = Ложь;

КонецПроцедуры // УстановитьПараметрыДиаграммыПоУмолчанию()

&НаСервере
Процедура ОбновитьДанныеНаДиаграмме(ТаблицаРасписание)
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	Если ТаблицаРасписание.Количество() > 0 Тогда
		
		Если РежимПросмотраДиаграммыГанта = 0 Тогда // дерево Перевозчик -> Тип ТС -> Транспортное средство
 
			ТаблицаПеревозчики = ТаблицаРасписание.Скопировать(, "Перевозчик, ПредставлениеПеревозчика");
			ТаблицаПеревозчики.Свернуть("Перевозчик, ПредставлениеПеревозчика");
			ТаблицаПеревозчики.Сортировать("Перевозчик");
			Для каждого СтрокаПеревозчик Из ТаблицаПеревозчики Цикл
				ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаПеревозчик.Перевозчик);
				ТочкаДиаграммы.Текст = СтрокаПеревозчик.ПредставлениеПеревозчика;
				ТочкаДиаграммы.Картинка = КартинкаПеревозчик;
			КонецЦикла; 
			
			ТаблицаТипыТС = ТаблицаРасписание.Скопировать(, "Перевозчик, ТипТС, ПредставлениеТипаТС, Идентификатор");
			ТаблицаТипыТС.Свернуть("Перевозчик, ТипТС, ПредставлениеТипаТС, Идентификатор");
			ТаблицаТипыТС.Сортировать("ТипТС");
			Для каждого СтрокаТипТС Из ТаблицаТипыТС Цикл
				ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаТипТС.Идентификатор, СтрокаТипТС.Перевозчик);
				ТочкаДиаграммы.Текст = СтрокаТипТС.ПредставлениеТипаТС;
				ТочкаДиаграммы.Картинка = КартинкаТипТС;
			КонецЦикла;
			КопируемыеРеквизиты = "Идентификатор, ТранспортноеСредство, ПредставлениеТранспортногоСредства";
			
		Иначе	
			
			КопируемыеРеквизиты = "ТранспортноеСредство, ПредставлениеТранспортногоСредства";
			
		КонецЕсли;	
		
		ТаблицаТС = ТаблицаРасписание.Скопировать(, КопируемыеРеквизиты);
		ТаблицаТС.Свернуть(КопируемыеРеквизиты);
		ТаблицаТС.Сортировать("ТранспортноеСредство");
		
		ТаблицаТипыСостояний = ТаблицаРасписание.Скопировать(, "ТипСостояния");
		ТаблицаТипыСостояний.Свернуть("ТипСостояния");
		СтандартнаяСерия = ОпределитьСтандартныеСерии(ТаблицаТипыСостояний.ВыгрузитьКолонку("ТипСостояния"), Диаграмма);
		СтандартнаяСерия.ШтриховкаМеждуИнтервалами = Истина;
		СтандартнаяСерия.ЦветШтриховкиМеждуИнтервалами = Новый Цвет(34, 43, 34);
		ИндексСтандартнойСерии = Диаграмма.Серии.Индекс(СтандартнаяСерия);
		
		Для каждого СтрокаТС Из ТаблицаТС Цикл
			
			Если РежимПросмотраДиаграммыГанта = 0 Тогда // дерево
				ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаТС.ТранспортноеСредство, СтрокаТС.Идентификатор);
			Иначе
				ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаТС.ТранспортноеСредство);
			КонецЕсли;
			
			сткПоиска = Новый Структура("ТранспортноеСредство", СтрокаТС.ТранспортноеСредство);
			тзПериоды = ТаблицаРасписание.Скопировать(сткПоиска, "ДатаНачала, ДатаОкончания, ТипСостояния, ТребуетсяДопОбработка, Расшифровка");
			тзПериоды = ПривестиПериодыДоступности(тзПериоды);
			//тзПериоды = тзПериоды.Скопировать(Новый Структура("ТребуетсяДопОбработка", Ложь), "ДатаНачала, ДатаОкончания, ТипСостояния, Расшифровка");
			
			// Добавляем фиктивные интервалы для возможности отображения штриховки между интервалами.
			НоваяСтрока = тзПериоды.Добавить();
			НоваяСтрока.ТипСостояния = Перечисления.упТипыСостоянийТС.Прочее;
			НоваяСтрока.ДатаНачала = НачалоПериода - 1;
			НоваяСтрока.ДатаОкончания = НачалоПериода;
			
			НоваяСтрока = тзПериоды.Добавить();
			НоваяСтрока.ТипСостояния = Перечисления.упТипыСостоянийТС.Прочее;
			НоваяСтрока.ДатаНачала = ОкончаниеПериода;
			НоваяСтрока.ДатаОкончания = ОкончаниеПериода + 1;
			
			ЗначениеДиаграммыГанта = ЗаполнитьПериодыГруппы(тзПериоды, ТочкаДиаграммы, СтандартнаяСерия);
			
			ИскомыеСтроки = ТаблицаРезультат.НайтиСтроки(сткПоиска);
			Если ИскомыеСтроки.Количество() Тогда
				НайденнаяСтрока = ИскомыеСтроки[0];
				Если НайденнаяСтрока.ПланироватьЦиклически Тогда
					ТочкаДиаграммы.Картинка         = КартинкаВыборПериодичность;
					ТочкаДиаграммы.ЦветФона         = ЦветВыбраннойСтрокиСЦиклами;
					ЗначениеДиаграммыГанта.ЦветФона = ЦветВыбраннойСтрокиСЦиклами;
					ТочкаДиаграммы.Текст            = НСтр(СтрШаблон("ru = '%1 - %2 цкл.'", СтрокаТС.ПредставлениеТранспортногоСредства, НайденнаяСтрока.КоличествоЦиклов));
				Иначе
					ТочкаДиаграммы.Картинка         = КартинкаВыбор;
					ТочкаДиаграммы.ЦветФона         = ЦветВыбраннойСтроки;
					ЗначениеДиаграммыГанта.ЦветФона = ЦветВыбраннойСтроки;
					ТочкаДиаграммы.Текст            = СтрокаТС.ПредставлениеТранспортногоСредства;
				КонецЕсли; 
			Иначе	
				ТочкаДиаграммы.Картинка = КартинкаПустая;
				ТочкаДиаграммы.Текст = СтрокаТС.ПредставлениеТранспортногоСредства;
			КонецЕсли;
		КонецЦикла;
		
		Если РежимПросмотраДиаграммыГанта = 0 Тогда
			// Разворачивание точек
			Для каждого СтрокаПеревозчик Из ТаблицаПеревозчики Цикл
				ТочкаДиаграммы = Диаграмма.УстановитьТочку(СтрокаПеревозчик.Перевозчик);
				Диаграмма.РазвернутьТочку(ТочкаДиаграммы, Истина);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	Диаграмма.УстановитьПолныйИнтервал(НачалоПериода, ОкончаниеПериода);
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры // ОбновитьДанныеНаДиаграмме()

&НаСервере
Функция ЗаполнитьПериодыГруппы(СписокСтрок, ТочкаДиаграммы, СерияДиаграммы)

	ЗначениеПериод = Диаграмма.ПолучитьЗначение(ТочкаДиаграммы, СерияДиаграммы);
	
	Для каждого СтрокаПериод Из СписокСтрок Цикл
		
		Если СтрокаПериод.ТипСостояния = Null Тогда
			Продолжить;
		КонецЕсли;	
			
		сткВозврат = ОпределитьХарактеристикиСостоянияТС(СтрокаПериод.ТипСостояния);
		
		Интервал = ЗначениеПериод.Добавить();
		Интервал.Начало      = СтрокаПериод.ДатаНачала;
		Интервал.Конец       = СтрокаПериод.ДатаОкончания;			
		Интервал.Расшифровка = СтрокаПериод.Расшифровка;
		Интервал.Цвет        = сткВозврат.Цвет;
		
		Если НачалоДня(СтрокаПериод.ДатаНачала) = НачалоДня(СтрокаПериод.ДатаОкончания) Тогда
			ПериодСтрока = "(" + Формат(СтрокаПериод.ДатаНачала, "ДФ=ЧЧ:мм") + " - " 
								+ Формат(СтрокаПериод.ДатаОкончания, "ДФ=ЧЧ:мм") + ")";
		Иначе	
			ПериодСтрока = "(" + Формат(СтрокаПериод.ДатаНачала, "ДФ='дд.ММ ЧЧ:мм'") + " - "
							+ Формат(СтрокаПериод.ДатаОкончания, "ДФ='дд.ММ ЧЧ:мм'") + ")";
		КонецЕсли; 
		
		ТекстИнтервала = СтрШаблон(сткВозврат.Текст, ПериодСтрока);
		Интервал.Текст = НСтр(ТекстИнтервала, КодЯзыка);
		
	КонецЦикла; 
	
	Возврат ЗначениеПериод;
	
КонецФункции // ЗаполнитьПериодыГруппы()

&НаСервере
Функция ПривестиПериодыДоступности(ТаблицаПериодов)
	
	// вычитание из свободных отрезков доступности в графике ТС текущей занятости
	тзДоступныеПериоды = ТаблицаПериодов.Скопировать(Новый Структура("ТребуетсяДопОбработка", Истина), "ДатаНачала, ДатаОкончания, ТипСостояния, Расшифровка");
	тзЗанятость = ТаблицаПериодов.Скопировать(Новый Структура("ТребуетсяДопОбработка", Ложь), "ДатаНачала, ДатаОкончания, ТипСостояния, Расшифровка");
	Для каждого СтрокаЗанятость Из тзЗанятость Цикл
		времДоступныеПериоды = тзДоступныеПериоды.СкопироватьКолонки();
		Для каждого СтрокаДоступность Из тзДоступныеПериоды Цикл
			Если Истина
				И СтрокаЗанятость.ДатаНачала <= СтрокаДоступность.ДатаНачала 
				И СтрокаЗанятость.ДатаОкончания > СтрокаДоступность.ДатаНачала 
				И СтрокаЗанятость.ДатаОкончания < СтрокаДоступность.ДатаОкончания 
			Тогда
				НоваяСтрока = времДоступныеПериоды.Добавить();
				НоваяСтрока.ТипСостояния = СтрокаДоступность.ТипСостояния;
				НоваяСтрока.ДатаНачала = СтрокаЗанятость.ДатаОкончания;
				НоваяСтрока.ДатаОкончания = СтрокаДоступность.ДатаОкончания;
			ИначеЕсли Истина
				И СтрокаЗанятость.ДатаОкончания >= СтрокаДоступность.ДатаОкончания 
				И СтрокаЗанятость.ДатаНачала > СтрокаДоступность.ДатаНачала 
				И СтрокаЗанятость.ДатаНачала < СтрокаДоступность.ДатаОкончания 
			Тогда
				НоваяСтрока = времДоступныеПериоды.Добавить();
				НоваяСтрока.ТипСостояния = СтрокаДоступность.ТипСостояния;
				НоваяСтрока.ДатаНачала = СтрокаДоступность.ДатаНачала;
				НоваяСтрока.ДатаОкончания = СтрокаЗанятость.ДатаНачала;
			ИначеЕсли Истина
				И СтрокаЗанятость.ДатаНачала > СтрокаДоступность.ДатаНачала И СтрокаЗанятость.ДатаНачала < СтрокаДоступность.ДатаОкончания
				И СтрокаЗанятость.ДатаОкончания > СтрокаДоступность.ДатаНачала И СтрокаЗанятость.ДатаОкончания < СтрокаДоступность.ДатаОкончания 
			Тогда
				НоваяСтрока = времДоступныеПериоды.Добавить();
				НоваяСтрока.ТипСостояния = СтрокаДоступность.ТипСостояния;
				НоваяСтрока.ДатаНачала = СтрокаДоступность.ДатаНачала;
				НоваяСтрока.ДатаОкончания = СтрокаЗанятость.ДатаНачала;
				НоваяСтрока = времДоступныеПериоды.Добавить();
				НоваяСтрока.ТипСостояния = СтрокаДоступность.ТипСостояния;
				НоваяСтрока.ДатаНачала = СтрокаЗанятость.ДатаОкончания;
				НоваяСтрока.ДатаОкончания = СтрокаДоступность.ДатаОкончания;
			ИначеЕсли Истина
				И СтрокаЗанятость.ДатаНачала <= СтрокаДоступность.ДатаНачала 
				И СтрокаЗанятость.ДатаОкончания >= СтрокаДоступность.ДатаОкончания 
			Тогда
				// ничего не делаем, весь свободный период уже занят
			Иначе
				НоваяСтрока = времДоступныеПериоды.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДоступность);				
			КонецЕсли;
		КонецЦикла;
		
		тзДоступныеПериоды = времДоступныеПериоды.Скопировать();
	КонецЦикла; 
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(тзДоступныеПериоды, тзЗанятость);
	
	Возврат тзЗанятость;
	
КонецФункции // ПривестиПериодыДоступности()

&НаСервере
Функция ОпределитьХарактеристикиСостоянияТС(Знач ТипСостояния)
	
	СоответствиеЦветов = упПодборТС.СоответствиеТиповСостоянийТСЦветам();
	Цвет = СоответствиеЦветов[ТипСостояния];
	Если Цвет = Неопределено Тогда
		Цвет = СоответствиеЦветов[Перечисления.упТипыСостоянийТС.ПустаяСсылка()];
	КонецЕсли;
	сткВозврат = Новый Структура;
	сткВозврат.Вставить("Цвет");
	сткВозврат.Вставить("Текст");
	сткВозврат.Вставить("ТекстСерии");
	сткВозврат.Цвет = Цвет;
	Если ТипСостояния = Перечисления.упТипыСостоянийТС.Недоступно Тогда
		//сткВозврат.Цвет  = WebЦвета.Красный;
		сткВозврат.Текст = "ru = 'Недоступно %1'";
	ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.Ремонт Тогда
		//сткВозврат.Цвет  = WebЦвета.Серый;
		сткВозврат.Текст = "ru = 'Ремонт %1'";
	ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.ВРейсе Тогда
		//сткВозврат.Цвет  = WebЦвета.Синий;
		Если РежимФормированияКонтейнеров Тогда
			сткВозврат.Текст = "ru = 'Используется %1'";
		Иначе
			сткВозврат.Текст = "ru = 'В рейсе %1'";
		КонецЕсли;
	ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.Свободно Тогда
		//сткВозврат.Цвет  = Новый Цвет(94,188,74);
		сткВозврат.Текст = "ru = 'Свободно %1'";
	ИначеЕсли ТипСостояния = "Reserve" Тогда // страховой запас, на который ТС может опоздать из рейса
		//сткВозврат.Цвет  = WebЦвета.СинийСПороховымОттенком;
		сткВозврат.Текст = "ru = 'Страховой запас %1'";
	Иначе
		//сткВозврат.Цвет  = WebЦвета.Циан;
		сткВозврат.Текст = "ru = 'Прочее %1'";
	КонецЕсли;
	
	Возврат сткВозврат;
	
КонецФункции // ОпределитьХарактеристикиСостоянияТС()

&НаСервере
Функция ОпределитьСтандартныеСерии(МассивСостояний, Диаграмма)
	
	СерияДиаграммы = Неопределено;
	СоответствиеЦветов = упПодборТС.СоответствиеТиповСостоянийТСЦветам();
	Для каждого ТипСостояния Из МассивСостояний Цикл
		Если ТипСостояния = Null Тогда
			Продолжить;
		КонецЕсли;
		Цвет = СоответствиеЦветов[ТипСостояния];
		Если Цвет = Неопределено Тогда
			Цвет = СоответствиеЦветов[Перечисления.упТипыСостоянийТС.ПустаяСсылка()];
		КонецЕсли;
		СерияДиаграммы = Диаграмма.УстановитьСерию(ТипСостояния);
		Если ТипСостояния = Перечисления.упТипыСостоянийТС.Недоступно Тогда
			//Цвет  = WebЦвета.Красный;
			СерияДиаграммы.Текст = НСтр("ru = 'Недоступно'");
		ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.Ремонт Тогда
			//Цвет  = WebЦвета.Серый;
			СерияДиаграммы.Текст = НСтр("ru = 'Ремонт'");
		ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.ВРейсе Тогда
			//Цвет  = WebЦвета.Синий;
			СерияДиаграммы.Текст = НСтр("ru = 'В рейсе'");
		ИначеЕсли ТипСостояния = Перечисления.упТипыСостоянийТС.Свободно Тогда
			//Цвет  = Новый Цвет(94,188,74);
			СерияДиаграммы.Текст = НСтр("ru = 'Свободно'");
		ИначеЕсли ТипСостояния = "Reserve" Тогда // страховой запас, на который ТС может опоздать из рейса
			//Цвет  = WebЦвета.СинийСПороховымОттенком;
			СерияДиаграммы.Текст = НСтр("ru = 'Страховой запас'");
		Иначе
			//Цвет  = WebЦвета.Циан;
			СерияДиаграммы.Текст = НСтр("ru = 'Прочее'");
		КонецЕсли;
		СерияДиаграммы.Цвет  = Цвет;
	КонецЦикла; 
	Возврат СерияДиаграммы; 
	
КонецФункции // ОпределитьСтандартныеСерии()

&НаКлиенте
Процедура ДобавитьСтрокуВТаблицуРезультат(ТранспортноеСредство)
	
	НоваяСтрока = ТаблицаРезультат.Добавить();
	НоваяСтрока.ТранспортноеСредство = ТранспортноеСредство;
	
	сткДанные = упСервисныеФункцииВызовСервера.ЗначенияРеквизитовОбъекта(ТранспортноеСредство, "Партнер, ТипТС");
	НоваяСтрока.Перевозчик = сткДанные.Партнер;
	НоваяСтрока.ТипТС = сткДанные.ТипТС;
	НоваяСтрока.Количество = 1;
	
КонецПроцедуры	// ДобавитьСтрокуВТаблицуРезультат()		

&НаКлиенте
Процедура ДеревоТранспортныеСредстваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ДеревоТранспортныеСредстваПредставление Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
		Если ЗначениеЗаполнено(ДанныеСтроки.ТипТС) Тогда
			ОткрываемоеЗначение = ДанныеСтроки.ТипТС;
		Иначе
			ОткрываемоеЗначение = ДанныеСтроки.Перевозчик;
		КонецЕсли; 
		ПоказатьЗначение(, ОткрываемоеЗначение);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 
