
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокТранспорта.Загрузить(Параметры.СписокТранспорта.Выгрузить(Новый Структура("ПланироватьЦиклически", Истина), "ТранспортноеСредство, КоличествоЦиклов"));
	РежимФормированияКонтейнеров = Параметры.РежимФормированияКонтейнеров;
	
	ОбновитьВидФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПодтвердитьЗакрытиеФормыЗавершение", ЭтаФорма);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокТранспортаКоличествоЦикловПриИзменении(Элемент)
	
	текСтрока = Элементы.СписокТранспорта.ТекущиеДанные;
	Если текСтрока <> Неопределено Тогда
		ВыделенныеСтроки = Элементы.СписокТранспорта.ВыделенныеСтроки;
		Если ВыделенныеСтроки.Количество() > 1 Тогда
			Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
				ИскомаяСтрока = СписокТранспорта.НайтиПоИдентификатору(ИдентификаторСтроки);
				Если ИскомаяСтрока <> Неопределено И ИскомаяСтрока <> текСтрока Тогда
					ИскомаяСтрока.КоличествоЦиклов = текСтрока.КоличествоЦиклов;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Модифицированность = Ложь;
	соотВозврат = Новый Соответствие;
	Для каждого текТС Из СписокТранспорта Цикл
		соотВозврат.Вставить(текТС.ТранспортноеСредство, текТС.КоличествоЦиклов);
	КонецЦикла; 
	
	Закрыть(соотВозврат);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодтвердитьЗакрытиеФормыЗавершение(Ответ, Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ЗавершитьРедактирование(Неопределено);
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидФормы()
	
	Если РежимФормированияКонтейнеров Тогда
		Элементы.СписокТранспортаТранспортноеСредство.Заголовок = НСтр("ru = 'Контейнер'");
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
