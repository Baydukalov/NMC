#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	УстановитьЗаголовки();
	
	Если ТипЗнч(Пользователи.АвторизованныйПользователь()) <> Тип("СправочникСсылка.ВнешниеПользователи") Тогда 
		Если Элементы.Найти("ТекущийПробег") <> Неопределено Тогда 
			ОбновитьПробегМоточасы();
		КонецЕсли;
		
		Если НЕ Элементы.Найти("НаправлениеДеятельности") = Неопределено Тогда 
			ОбновитьНаправленияДеятельности();
		КонецЕсли;
		
	Иначе
		Если Элементы.Найти("ТекущийПробег") <> Неопределено Тогда 
			Элементы.ТекущийПробег.Видимость = Ложь;
		КонецЕсли;
		Если НЕ Элементы.Найти("НаправлениеДеятельности") = Неопределено Тогда 
			Элементы.НаправлениеДеятельности.Видимость = Ложь;
		КонецЕсли;		
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("упИспользуетсяУчетПоНаправлениямДеятельности") Тогда
		Элементы.ГруппаНаправленияДеятельности.Видимость = Ложь;
	КонецЕсли;		
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов.
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов.
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ОбновитьСвойстваЭлементовФормы();
	
	РольДоступнаупРазработчик 	= РольДоступна("упРазработчик");
	РольДоступнаПолныеПрава 	= РольДоступна("ПолныеПрава");
	РольДоступнаУпДобавлениеИзменениеСобственныхТранспортныхСредств = РольДоступна("упДобавлениеИзменениеСобственныхТранспортныхСредств");
	РольДоступнаУпДобавлениеИзменениеТранспортныхСредств = РольДоступна("упДобавлениеИзменениеТранспортныхСредств");
	РольДоступнаупПеревозчик	= РольДоступна("упПеревозчик");

	
	РазрешеноИзменятьСобственныеТС 		= РольДоступнаупРазработчик ИЛИ РольДоступнаПолныеПрава ИЛИ РольДоступнаУпДобавлениеИзменениеСобственныхТранспортныхСредств;
	РазрешеноИзменятьНеСобственныеТС	= РольДоступнаупПеревозчик ИЛИ РольДоступнаупРазработчик ИЛИ РольДоступнаПолныеПрава ИЛИ РольДоступнаУпДобавлениеИзменениеТранспортныхСредств;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Объект.СобственноеТранспортноеСредство Тогда
			ТолькоПросмотр = НЕ РазрешеноИзменятьСобственныеТС;
		Иначе	
			ТолькоПросмотр = НЕ РазрешеноИзменятьНеСобственныеТС;
		КонецЕсли;	
	КонецЕсли;
		
	Элементы.СобственноеТранспортноеСредство.Доступность = РазрешеноИзменятьСобственныеТС;
	Элементы.ГруппаШГВ.Видимость = ЭтаФорма.УчитыватьЛинейныеРазмеры;
	Элементы.ПроверятьНаличиеУстановленныхТрекеровКромеМобильногоКлиента.Доступность = Объект.ЗапретитьФиксироватьДанныеОМестоположенииСМобильногоКлиента;
	Элементы.ДекорацияМобильныйКлиент.Видимость = ПолучитьФункциональнуюОпцию("упИспользуетсяАндроидКлиент");
	
	ЗаполнитьНавигационнуюСсылкуИзображенияСервер();
	НастроитьДоступностьВводаМоточасовПоТС();
	
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаКомментарий, Объект.Комментарий);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Подразделение = упУчетТранспортныхСредств.ПодразделениеТранспортногоСредстваНаДату(,Объект.Ссылка);
	КонецЕсли;
	
	УстановитьПараметрыCпискаВидыГСМ();
	УстановитьЗначениеДатчикУровняТоплива();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоНовый = НЕ ЗначениеЗаполнено(Объект.Ссылка);
	
	ЭтаФорма.УчитыватьМассу                       = ПолучитьФункциональнуюОпцию("упУчитыватьМассу");
	ЭтаФорма.УчитыватьОбъем                       = ПолучитьФункциональнуюОпцию("упУчитыватьОбъем");
	ЭтаФорма.УчитыватьКоличествоМест              = ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест");
	ЭтаФорма.УчитыватьЛинейныеРазмеры             = ПолучитьФункциональнуюОпцию("упУчитыватьЛинейныеРазмеры");
	
	ЭтаФорма.КоэффициентПересчетаОбъема = упСервисныеФункции.ПолучитьЗначениеКонстанты("упКоэффициентПересчетаОбъема");
	
	// СтандартныеПодсистемы.Свойства.
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства.
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	// Если ТС является прицепом - установим видимость элементов.
	Массив = Новый Массив();
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упВидыТранспорта.ЛегковойАвтомобиль"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упВидыТранспорта.ГрузовойАвтомобиль"));
	ВидимостьЭлемента = Истина;
	Если Массив.Найти(Объект.ВидТранспорта) = Неопределено Тогда
		ВидимостьЭлемента = Ложь;
	КонецЕсли;
	Элементы.Прицеп.Видимость = ВидимостьЭлемента;
	Если ВидимостьЭлемента Тогда
		НастройкаВидимостиЭлементовФормы(НЕ Объект.Прицеп);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ВыполнитьОтложенныйПереходНаЭлемент",0.1,Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "КорректировкаПробега" Тогда
		Если Параметр = Объект.Ссылка Тогда
			ОбновитьПробегМоточасы();	
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ПринятКУчету" Тогда 	
		Если Параметр = Объект.Ссылка Тогда
			ОбновитьЗаголовокПринятияКУчету();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_ПрисоединенныйФайл" И ЗагрузкаФайлаИзображения Тогда	
		Если ТипЗнч(Источник) = Тип("Массив") Тогда
			Если Источник.Количество() > 0 Тогда
				Объект.ИзображениеПрисоединенныйФайл = Источник[0];
				Модифицированность = Истина;
				ЗаполнитьНавигационнуюСсылкуИзображения();
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзменениеНаправленияДеятельности" Тогда
		Если Параметр = Объект.Ссылка Тогда
			ОбновитьНаправленияДеятельности();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ЗначениеУровняТопливаАктуализировано" Тогда
		Если Параметр = Объект.Ссылка Тогда
			ПроверитьУровеньТоплива();	
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства.
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства.
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства.
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства.
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	 УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	УстановитьПараметрыCпискаВидыГСМ();
	УстановитьЗначениеДатчикУровняТоплива();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ЭтоНовый", ЭтоНовый);
	Оповестить("ЗаписанТранспортноеСредство", ПараметрыОповещения, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)

	ПриИзмененииРазмеров();
	
КонецПроцедуры

&НаКлиенте
Процедура ГаражиЗонаПриИзменении(Элемент)
	
	текСтрока = Элементы.Гаражи.ТекущиеДанные;
	Если текСтрока <> Неопределено И ЗначениеЗаполнено(текСтрока.Зона) Тогда
		Для каждого текГараж Из Объект.Гаражи Цикл
			Если текГараж <> текСтрока И текГараж.Зона = текСтрока.Зона Тогда
				текСтрока.Зона = Неопределено;
				ТекстСообщения = НСтр("ru = 'В данной зоне уже определен гараж!'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТипТСПриИзменении(Элемент)
	Если Ложь
		Или Объект.Высота <> 0
		Или Объект.Длина <> 0
		Или Объект.Ширина <> 0
		Или Объект.Грузоподъемность <> 0
		Или Объект.Объем <> 0
		Или Объект.КоличествоМест <> 0
		Или Объект.КоличествоЗагрузочныхМетров <> 0
	Тогда
	     ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветНаВопросЗаполнениеВГХПоТипуТС", ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Перезаполнить ВГХ по типу транспортного средства?'"),РежимДиалогаВопрос.ДаНет);		
	Иначе
		ЗаполнитьПоТипуТСНаСервере();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	ПартнерПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВидТранспортаПриИзменении(Элемент)
	Массив = Новый Массив();
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упВидыТранспорта.ЛегковойАвтомобиль"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.упВидыТранспорта.ГрузовойАвтомобиль"));
	Если Массив.Найти(Объект.ВидТранспорта) = Неопределено Тогда
		Элементы.Прицеп.Доступность = Ложь;
	Иначе
		Элементы.Прицеп.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрицепПриИзменении(Элемент)
	ВидимостьЭлемента = НЕ Объект.Прицеп;
	НастройкаВидимостиЭлементовФормы(ВидимостьЭлемента);
КонецПроцедуры

// Процедура - Собственное транспортное средство при изменении
//
// Параметры:
//  Элемент	- Элементы - Элемент формы.
//
&НаКлиенте
Процедура СобственноеТранспортноеСредствоПриИзменении(Элемент)
	ОбновитьСвойстваЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура СчетчикМоточасовПриИзменении(Элемент)
	НастроитьДоступностьВводаМоточасовПоТС();
КонецПроцедуры

&НаКлиенте
Процедура ЗапретитьФиксироватьДанныеОМестоположенииСМобильногоКлиентаПриИзменении(Элемент)
	
	Элементы.ПроверятьНаличиеУстановленныхТрекеровКромеМобильногоКлиента.Доступность = Объект.ЗапретитьФиксироватьДанныеОМестоположенииСМобильногоКлиента;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	упСервисныеФункцииКлиентСервер.УстановитьКартинкуКомментария(Элементы.ГруппаКомментарий, Объект.Комментарий);
КонецПроцедуры

&НаКлиенте
Процедура ВидыГСМВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ВидыГСМИзменить" Тогда
		ТекущиеДанные = Элементы.ВидыГСМ.ТекущиеДанные;
		ТранспортноеСредство = Объект.Ссылка;
		ВидГСМ = ТекущиеДанные.ВидГСМ;
		сткПараметры = Новый Структура("ТранспортноеСредство, ВидГСМ", ТранспортноеСредство, ВидГСМ);
		ОткрытьФорму("Документ.упКорректировкаОстатковГСМ.Форма.ФормаДокумента", сткПараметры, ЭтаФорма, УникальныйИдентификатор);	
	Иначе
		ОткрытьФормуВидовГСМ();
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДатчикУровняТопливаПриИзменении(Элемент)
	
	ПроверитьУровеньТоплива();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкорректироватьТекущийПробег(Команда)
	ТранспортноеСредство 	= Объект.Ссылка;
	сткПараметры = Новый Структура("ТранспортноеСредство", ТранспортноеСредство);
	ОткрытьФорму("Документ.упКорректировкаПробегаТС.Форма.ФормаДокумента", сткПараметры);
КонецПроцедуры

&НаКлиенте
Процедура СкорректироватьТекущееНаправлениеДеятельности(Команда)
	
	ТранспортноеСредство 	= Объект.Ссылка;
	сткПараметры = Новый Структура("ТранспортноеСредство", ТранспортноеСредство);
	сткПараметры.Вставить("НаправлениеДеятельности", НаправлениеДеятельности);
	ОткрытьФорму("Документ.упИзменениеНаправленияДеятельностиТранспортногоСредства.Форма.ФормаДокумента", сткПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьКУчетуТранспортноеСредство(Команда)
	
	Если ЗначениеЗаполнено(ДокументСнятияСУчета) Тогда
		сткЗначенияЗаполнения = Новый Структура("ТранспортноеСредство, ДатаСнятияСУчета",Объект.Ссылка, ТекущаяДата());
		сткПараметры = Новый Структура("Ключ, ЗначенияЗаполнения", ДокументСнятияСУчета, сткЗначенияЗаполнения);
		ОткрытьФорму("Документ.упСнятиеСУчетаТранспортногоСредства.Форма.ФормаДокумента",сткПараметры);
	Иначе	
		сткЗначенияЗаполнения = Новый Структура("ТранспортноеСредство, ДатаПринятияКУчету",Объект.Ссылка, ТекущаяДата());
		сткПараметры = Новый Структура("Ключ, ЗначенияЗаполнения", ДокументПринятияКУчету, сткЗначенияЗаполнения);
		ОткрытьФорму("Документ.упПринятиеКУчетуТранспортногоСредства.Форма.ФормаДокумента",сткПараметры);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидыГСМ(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОткрытьФормуВидовГСМ();
	Иначе	
		ДополнительныеПараметры = Новый Структура("ОткрытьФормуВидовГСМ", Истина);
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Для изменения видов ГСМ, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// По умолчанию осуществляет переход на элемент Наимеование,
//	осуществляется переход на первый элемент группы "Страницы",
//  для изменения порядка принудительно устанавливаем переход.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьОтложенныйПереходНаЭлемент()	
	ТекущийЭлемент = Элементы.Наименование;	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаВидимостиЭлементовФормы(ВидимостьЭлемента)
	Элементы.ОсновнойВодитель.Видимость 	 = ВидимостьЭлемента;
	Элементы.ВторойОсновнойВодитель.Видимость= ВидимостьЭлемента;
	Элементы.ОсновнойЭкспедитор.Видимость    = ВидимостьЭлемента;
	// вкл. дополнительно.
	Элементы.СтильЛиний.Видимость           = ВидимостьЭлемента;
	Элементы.СтильМаркера.Видимость         = ВидимостьЭлемента;
	Элементы.ПерерывМеждуРейсами.Видимость  = ВидимостьЭлемента;
	Элементы.ПечатнаяФормаПутевогоЛиста.Видимость= ВидимостьЭлемента;
	
	// данные о топливе и ГСМ.
	Элементы.ВидыГСМ.Видимость             = ВидимостьЭлемента;
	Элементы.ДатчикУровняТоплива.Видимость = ВидимостьЭлемента;
	Элементы.ГруппаПробегГСМ.Заголовок     = ?(ВидимостьЭлемента,"Пробег / ГСМ", "Пробег");
	
	//учет посадочных мес.
	Элементы.КоличествоПосадочныхМест.Видимость = ВидимостьЭлемента;
	
КонецПроцедуры // НастройкаВидимостиЭлементовФормы()

&НаСервере
Процедура УстановитьЗаголовки()
	
	сткПредставления = упСервисныеФункции.ПолучитьПредставлениеПоказателейЗагрузки(УчитыватьМассу, УчитыватьОбъем, УчитыватьКоличествоМест, УчитыватьЛинейныеРазмеры);
	
	Если УчитыватьМассу Тогда
		ЕИ_Масса = сткПредставления.Масса;
	КонецЕсли;
	
	Если УчитыватьОбъем Тогда
		ЕИ_Объем = сткПредставления.Объем;
	КонецЕсли;
	
	Если УчитыватьКоличествоМест Тогда         
		ЕИ_Грузов = сткПредставления.КоличествоМест;
	КонецЕсли;
	
	Если УчитыватьЛинейныеРазмеры Тогда
		ЕИ_Длины = сткПредставления.КоличествоЗагрузочныхМетров;
	КонецЕсли; 
	
КонецПроцедуры // УстановитьЗаголовки()

&НаКлиенте
Процедура ПриИзмененииРазмеров()

	Объект.Объем = Объект.Ширина * Объект.Высота * Объект.Длина * КоэффициентПересчетаОбъема;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПробегМоточасы()
	сткПробегМоточасы = упПутевойЛистУчетГСМ.ПробегМоточасыПоРегиструОстатков(Объект.Ссылка);
	ТекущийПробег 		= сткПробегМоточасы.Пробег;
	ТекущиеМоточасы	= сткПробегМоточасы.Моточасы;
	ТекущийОдометр		= сткПробегМоточасы.Одометр;
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаправленияДеятельности()
	
	НаправлениеДеятельности = упУчетТранспортныхСредств.НаправлениеДеятельностиТранспортногоСредстваНаДату(,Объект.Ссылка);

КонецПроцедуры // ОбновитьНаправленияДеятельности()

&НаСервере
Процедура ЗаполнитьПоТипуТСНаСервере()
	ОбъектТС = РеквизитФормыВЗначение("Объект");
	ОбъектТС.Заполнить(Объект.ТипТС);
	ЗначениеВРеквизитФормы(ОбъектТС, "Объект");
КонецПроцедуры

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов.


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ МЕХАНИЗМА СВОЙСТВ.

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура МодельПриИзменении(Элемент)
	
	Если Ложь
		Или Объект.Высота <> 0
		Или Объект.Длина <> 0
		Или Объект.Ширина <> 0
		Или Объект.Грузоподъемность <> 0
		Или Объект.Объем <> 0
		Или Объект.КоличествоМест <> 0
		Или Объект.КоличествоЗагрузочныхМетров <> 0
	Тогда
	     ОповещениеНовый = Новый ОписаниеОповещения("ОбработатьОтветНаВопросЗаполнениеВГХПоМоделиТС", ЭтаФорма);
		ПоказатьВопрос(ОповещениеНовый, НСтр("ru = 'Перезаполнить ВГХ по модели транспортного средства?'"),РежимДиалогаВопрос.ДаНет);		
	Иначе
		МодельПриИзмененииНаСервере();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МодельНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;
	сткОтбор = Новый Структура("Прицеп", Объект.Прицеп);
	сткОтбор.Вставить("ТипТС",Объект.ТипТС);
	сткПараметры = Новый Структура("Отбор", сткОтбор);
	ОткрытьФорму("Справочник.упМоделиТС.Форма.ФормаВыбора", сткПараметры, Элемент);
КонецПроцедуры

&НаСервере
Процедура МодельПриИзмененииНаСервере()
	
	ОбъектТС = РеквизитФормыВЗначение("Объект");
	ОбъектТС.Заполнить(Объект.Модель);
	ЗначениеВРеквизитФормы(ОбъектТС, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатнаяФормаПутевогоЛистаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("ДополнительныеОтчетыИОбработки",		"Дополнительные отчеты и обработки");
	СписокВыбора.Добавить("упТипыПечатныхФормПутевогоЛиста", 	"Типы печатных форм путевого листа");
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработкаВыбораТипаПечатныхФорм", ЭтаФорма);
	ПоказатьВыборИзСписка(ОписаниеОповещенияОЗакрытии, СписокВыбора, Элемент);

КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора типа печатной формы.
//
Процедура ОбработкаВыбораТипаПечатныхФорм(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		Если ВыбранныйЭлемент.Значение = "ДополнительныеОтчетыИОбработки" Тогда
			ИмяОбъектаМетаданных = "Документ.упПутевойЛист";
			СписокВыбора = упСервисныеФункцииВызовСервера.ПолучитьСписокДополнительныхВнешнихПечатныхФорм(ИмяОбъектаМетаданных);
			ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработкаВыбораВнешнейПечатнойФормы", ЭтаФорма);
			ПоказатьВыборИзСписка(ОписаниеОповещенияОЗакрытии, СписокВыбора, Элементы.ПечатнаяФормаПутевогоЛиста);
		ИначеЕсли ВыбранныйЭлемент.Значение = "упТипыПечатныхФормПутевогоЛиста" Тогда	
			ОткрытьФорму("Перечисление.упТипыПечатныхФормПутевогоЛиста.ФормаВыбора",,Элементы.ПечатнаяФормаПутевогоЛиста);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
// Процедура завершения после выбора печатной формы.
//
Процедура ОбработкаВыбораВнешнейПечатнойФормы(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если НЕ ВыбранныйЭлемент = Неопределено Тогда
		Объект.ПечатнаяФормаПутевогоЛиста = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Процедура - Партнер при изменении на сервере.
//
Процедура ПартнерПриИзмененииНаСервере()
	
	Объект.СобственноеТранспортноеСредство = Ложь;
	Если ЗначениеЗаполнено(Объект.Партнер) Тогда
		ОрганизацияПартнера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Партнер, "Организация");
		Объект.СобственноеТранспортноеСредство 	= ЗначениеЗаполнено(ОрганизацияПартнера);
	КонецЕсли;
	ОбновитьСвойстваЭлементовФормы();	
КонецПроцедуры

&НаСервере
// Процедура - Обновить свойства элементов формы.
//
Процедура ОбновитьСвойстваЭлементовФормы()
	Если Объект.СобственноеТранспортноеСредство Тогда
		ОбновитьЗаголовокПринятияКУчету();				
		Элементы.ПринятьКУчетуТранспортноеСредство.Видимость = Истина;
	Иначе
		Элементы.ПринятьКУчетуТранспортноеСредство.Видимость = Ложь;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
// Процедура - Обновить заголовок принятия к учету.
//
Процедура ОбновитьЗаголовокПринятияКУчету()
	
	сткВозврат = упУчетТранспортныхСредств.УчетноеСостояниеТранспортногоСредства(Объект.Ссылка);
	Если сткВозврат.СнятоСУчета Тогда	
		ДокументСнятияСУчета	= сткВозврат.Регистратор;
		ДокументПринятияКУчету	= Документы.упПринятиеКУчетуТранспортногоСредства.ПустаяСсылка();
		текЗаголовок = Нстр("ru = 'Снято с учета'");
	ИначеЕсли сткВозврат.ПринятоКУчету  Тогда
		ДокументСнятияСУчета	= Документы.упСнятиеСУчетаТранспортногоСредства.ПустаяСсылка();
		ДокументПринятияКУчету 	= сткВозврат.Регистратор;
		текЗаголовок = Нстр("ru = 'Принято к учету'");
	Иначе
		текЗаголовок = Нстр("ru = 'Не принято к учету'")
	КонецЕсли; 
	 
	Элементы.ПринятьКУчетуТранспортноеСредство.Заголовок = текЗаголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзображение(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Перед добавлением изображения, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗагрузитьФайлИзображения();		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображение(Команда)
	Объект.ИзображениеПрисоединенныйФайл = ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка");
	Модифицированность = Истина;
	ЗаполнитьНавигационнуюСсылкуИзображения();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзПрисоединенныхФайлов(Команда)
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	ПрисоединенныеФайлыКлиент.ОткрытьФормуВыбораФайлов(Объект.Ссылка,ЭтаФорма);	
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.упТранспортныеСредстваПрисоединенныеФайлы") Тогда
		Объект.ИзображениеПрисоединенныйФайл = ВыбранноеЗначение;
		Модифицированность = Истина;
		ЗаполнитьНавигационнуюСсылкуИзображения();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзображениеАдресНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаписи", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, "Перед добавлением изображения, необходимо записать объект. Записать?", РежимДиалогаВопрос.ДаНет);
	Иначе
		Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
			ПрисоединенныеФайлыКлиент.ОткрытьФайл(ПрисоединенныеФайлыКлиент.ПолучитьДанныеФайла(Объект.ИзображениеПрисоединенныйФайл, УникальныйИдентификатор));
		Иначе	
			ЗагрузитьФайлИзображения();		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОЗаписи(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = НЕ ПроверитьЗаполнение();
		Если НЕ Отказ Тогда
			Записать();
		КонецЕсли;
		
		Если НЕ Отказ Тогда

			Если Истина
				И НЕ ДополнительныеПараметры = Неопределено 
				И ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
				И ДополнительныеПараметры.Свойство("ОткрытьФормуВидовГСМ")
				Тогда
				ОткрытьФормуВидовГСМ();
			Иначе	
				ЗагрузитьФайлИзображения();	
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлИзображения()
	ЗагрузкаФайлаИзображения = Истина;
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	ПрисоединенныеФайлыКлиент.ДобавитьФайлы(Объект.Ссылка, УникальныйИдентификатор);
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
	ЗагрузкаФайлаИзображения = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНавигационнуюСсылкуИзображения()
	Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
		ИзображениеАдрес = НавигационнаяСсылкаИзображения();
	Иначе
		ИзображениеАдрес = "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНавигационнуюСсылкуИзображенияСервер()
	Если ЗначениеЗаполнено(Объект.ИзображениеПрисоединенныйФайл) Тогда
		ИзображениеАдрес = НавигационнаяСсылкаИзображения();
	Иначе
		ИзображениеАдрес = "";	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НавигационнаяСсылкаИзображения()
	// СтандартныеПодсистемы.ПрисоединенныеФайлы
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(Объект.ИзображениеПрисоединенныйФайл, УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла;
	// Конец СтандартныеПодсистемы.ПрисоединенныеФайлы
КонецФункции

&НаСервере
Процедура НастроитьДоступностьВводаМоточасовПоТС()
	
	сткНастроек = Новый Структура;
	сткНастроек.Вставить("ИспользоватьСчетчикМоточасовТранспортныхСредств", Объект.СчетчикМоточасов);
	упСервисныеФункции.НастроитьФормуПоПараметрам(ЭтаФорма, сткНастроек);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросЗаполнениеВГХПоТипуТС(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоТипуТСНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросЗаполнениеВГХПоМоделиТС(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		МодельПриИзмененииНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыCпискаВидыГСМ()
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ВидыГСМ, "ТранспортноеСредство", Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВидовГСМ()
	
	сткПараметры = Новый Структура();
	сткПараметры.Вставить("ТранспортноеСредство", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.упВидыГСМТранспортныхСредств.Форма.ФормаВводаВидовГСМ", сткПараметры, ЭтаФорма, УникальныйИдентификатор);	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеДатчикУровняТоплива()
	
	УровеньТоплива = 0;
	Если ЗначениеЗаполнено(Объект.ДатчикУровняТоплива) Тогда
		Элементы.ГруппаУровеньТоплива.Видимость = Истина;
		УровеньТоплива = ПолучитьУровеньТоплива(Объект.Ссылка, Объект.ДатчикУровняТоплива);
	Иначе
		Элементы.ГруппаУровеньТоплива.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьЗначениеДатчикУровняТоплива()

&НаСервереБезКонтекста
Функция ПолучитьУровеньТоплива(ТС, Датчик)
	
	УровеньТоплива = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упДанныеДатчиковСрезПоследних.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.упДанныеДатчиков.СрезПоследних(
	|			,
	|			ТранспортноеСредство = &ТранспортноеСредство
	|				И Датчик = &Датчик) КАК упДанныеДатчиковСрезПоследних";
	
	Запрос.УстановитьПараметр("Датчик", Датчик);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТС);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			УровеньТоплива = Выборка.Значение;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат УровеньТоплива;
	
КонецФункции // ПолучитьУровеньТоплива()

&НаКлиенте
Процедура ПроверитьУровеньТоплива()

	УровеньТоплива = 0;
	Если ЗначениеЗаполнено(Объект.ДатчикУровняТоплива) Тогда
		Элементы.ГруппаУровеньТоплива.Видимость = Истина;
		УровеньТоплива = ПолучитьУровеньТоплива(Объект.Ссылка, Объект.ДатчикУровняТоплива);
	Иначе
		Элементы.ГруппаУровеньТоплива.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры // ПроверитьУровеньТоплива()

#КонецОбласти
