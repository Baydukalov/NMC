
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ВидПеревозки) Тогда
		мсвПеревозчиков = ПолучитьДоступныхПеревозчиков();
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", мсвПеревозчиков, ВидСравненияКомпоновкиДанных.ВСписке);	
	КонецЕсли;
	
	Если Параметры.ЭтоАЗС Тогда
		ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, "ЭтоАЗС", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		мсвГруппаЭтоАЗС = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, ,"ЭтоАЗС");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(мсвГруппаЭтоАЗС[0],"АЗС", ВидСравненияКомпоновкиДанных.Равно,Истина,"АЗС");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(мсвГруппаЭтоАЗС[0],"СкладАЗС", ВидСравненияКомпоновкиДанных.Равно,Истина,"СкладАЗС");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

// Нет.

#КонецОбласти 

#Область ОбработчикиСобытийКоманд

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДоступныхПеревозчиков()
	
	мсвПеревозчиков = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упВидыПеревозок.Ссылка,
	|	упВидыПеревозок.ВыборПеревозчика КАК ВариантВыбораПеревозчика,
	|	упВидыПеревозок.Перевозчик
	|ПОМЕСТИТЬ втВидыПеревозок
	|ИЗ
	|	Справочник.упВидыПеревозок КАК упВидыПеревозок
	|ГДЕ
	|	упВидыПеревозок.Ссылка = &ВидПеревозки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Перевозчик
	|ПОМЕСТИТЬ втПеревозчики
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаСобственнымиТС))
	|			И (упПартнеры.Организация = &Организация)
	|			И (упПартнеры.Перевозчик)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втВидыПеревозок.Перевозчик
	|ИЗ
	|	втВидыПеревозок КАК втВидыПеревозок
	|ГДЕ
	|	втВидыПеревозок.ВариантВыбораПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	упПартнеры.Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втВидыПеревозок КАК втВидыПеревозок
	|		ПО (упПартнеры.Перевозчик)
	|			И (НЕ упПартнеры.ПометкаУдаления)
	|			И (втВидыПеревозок.ВариантВыбораПеревозчика В (ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.НепосредственныйВыборВРейсе), ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПеревозчики.Перевозчик
	|ИЗ
	|	втПеревозчики КАК втПеревозчики
	|ГДЕ
	|	&ТипТС = ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втПеревозчики.Перевозчик
	|ИЗ
	|	втПеревозчики КАК втПеревозчики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упПартнеры.ДоступныеТипыТС КАК ДоступныеТипыТС
	|		ПО втПеревозчики.Перевозчик = ДоступныеТипыТС.Ссылка
	|			И (ДоступныеТипыТС.ТипТС = &ТипТС)";
	Запрос.УстановитьПараметр("ВидПеревозки", Параметры.ВидПеревозки);
	Запрос.УстановитьПараметр("Организация" , Параметры.Организация);
	Запрос.УстановитьПараметр("ТипТС"       , Параметры.ТипТС);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		мсвПеревозчиков = Результат.Выгрузить().ВыгрузитьКолонку("Перевозчик");
	КонецЕсли; 
	
	Возврат мсвПеревозчиков;

КонецФункции

#КонецОбласти 
