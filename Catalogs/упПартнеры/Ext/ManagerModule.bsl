#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу перевозчиков по рейсу.
//
// Параметры:
//  ОбъектСсылка  - СправочникСсылка - Ссылка на элемент.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Результат выполнения.
//
Функция ПолучитьСписокПеревозчиков(ОбъектСсылка) Экспорт
	
	тбзПеревозчики = Новый ТаблицаЗначений;
	
	Если ТипЗнч(ОбъектСсылка) = тип("ДокументСсылка.упРейс") Тогда
		текЗапрос = Новый Запрос;
		текЗапрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.ТипТС, ЗНАЧЕНИЕ(Справочник.упТипыТС.ПустаяСсылка)) КАК ТипТС,
		|	упРейс.ВидПеревозки КАК ВидПеревозки,
		|	упРейс.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(упПеревозчикПоРейсуСрезПоследних.Перевозчик, ЗНАЧЕНИЕ(Справочник.упПартнеры.ПустаяСсылка)) КАК Перевозчик
		|ПОМЕСТИТЬ втРейс
		|ИЗ
		|	Документ.упРейс КАК упРейс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упПеревозчикПоРейсу.СрезПоследних(, Рейс = &Ссылка) КАК упПеревозчикПоРейсуСрезПоследних
		|		ПО упРейс.Ссылка = упПеревозчикПоРейсуСрезПоследних.Рейс
		|ГДЕ
		|	упРейс.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упПартнеры.Ссылка КАК Перевозчик
		|ПОМЕСТИТЬ втПеревозчики
		|ИЗ
		|	Справочник.упПартнеры КАК упПартнеры
		|ГДЕ
		|	упПартнеры.Перевозчик
		|	И НЕ упПартнеры.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	упВидыПеревозок.Перевозчик КАК Перевозчик
		|ПОМЕСТИТЬ втПеревозчикиПоВидуПеревозки
		|ИЗ
		|	Справочник.упВидыПеревозок КАК упВидыПеревозок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
		|		ПО упВидыПеревозок.Перевозчик = втПеревозчики.Перевозчик
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейс КАК втРейс
		|		ПО упВидыПеревозок.Ссылка = втРейс.ВидПеревозки
		|ГДЕ
		|	упВидыПеревозок.ВыборПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ПеревозкаОпределеннымПеревозчиком)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	упВидыПеревозокПеревозчикиДляТендера.Перевозчик
		|ИЗ
		|	Справочник.упВидыПеревозок.ПеревозчикиДляТендера КАК упВидыПеревозокПеревозчикиДляТендера
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
		|		ПО упВидыПеревозокПеревозчикиДляТендера.Перевозчик = втПеревозчики.Перевозчик
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейс КАК втРейс
		|		ПО упВидыПеревозокПеревозчикиДляТендера.Ссылка = втРейс.ВидПеревозки
		|ГДЕ
		|	упВидыПеревозокПеревозчикиДляТендера.Ссылка.ВыборПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОформляетсяТендер)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	втПеревозчики.Перевозчик
		|ИЗ
		|	Справочник.упВидыПеревозок КАК упВидыПеревозок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПеревозчики КАК втПеревозчики
		|		ПО (ИСТИНА)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейс КАК втРейс
		|		ПО упВидыПеревозок.Ссылка = втРейс.ВидПеревозки
		|ГДЕ
		|	упВидыПеревозок.ВыборПеревозчика = ЗНАЧЕНИЕ(Перечисление.упВариантыВыбораПеревозчика.ОпределяетсяДляКаждогоРейсаИндивидуально)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	упЗаявкаНаТС.Перевозчик КАК Перевозчик
		|ПОМЕСТИТЬ втПеревозчикиЕстьЗаявкаНаТС
		|ИЗ
		|	Документ.упЗаявкаНаТС КАК упЗаявкаНаТС
		|ГДЕ
		|	упЗаявкаНаТС.Рейс = &Ссылка
		|	И упЗаявкаНаТС.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	упТранспортныеСредства.Партнер КАК Перевозчик
		|ПОМЕСТИТЬ втПеревозчикиПоТипуТС
		|ИЗ
		|	Справочник.упТранспортныеСредства КАК упТранспортныеСредства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРейс КАК втРейс
		|		ПО упТранспортныеСредства.ТипТС = втРейс.ТипТС
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПеревозчикиПоВидуПеревозки КАК втПеревозчикиПоВидуПеревозки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	упТранспортныеСредства.Партнер.Перевозчик
		|	И втПеревозчикиПоВидуПеревозки.Перевозчик ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПеревозчики.Перевозчик КАК Перевозчик,
		|	МИНИМУМ(втПеревозчики.Флаг) КАК Флаг,
		|	МАКСИМУМ(втПеревозчики.СформированыЗаявки) КАК СформированыЗаявки,
		|	МИНИМУМ(втПеревозчики.УказанВВидеПеревозки) КАК УказанВВидеПеревозки,
		|	МИНИМУМ(втПеревозчики.СовпадаетТипТС) КАК СовпадаетТипТС
		|ПОМЕСТИТЬ втПеревозчикиИтог
		|ИЗ
		|	(ВЫБРАТЬ
		|		втПеревозчики.Перевозчик КАК Перевозчик,
		|		ИСТИНА КАК Флаг,
		|		ЛОЖЬ КАК СформированыЗаявки,
		|		ИСТИНА КАК УказанВВидеПеревозки,
		|		ЛОЖЬ КАК СовпадаетТипТС
		|	ИЗ
		|		втПеревозчикиПоВидуПеревозки КАК втПеревозчики
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		втПеревозчики.Перевозчик,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		втПеревозчикиЕстьЗаявкаНаТС КАК втПеревозчики
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		втПеревозчики.Перевозчик,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		втПеревозчикиПоТипуТС КАК втПеревозчики
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		втПеревозчики.Перевозчик,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		втПеревозчики КАК втПеревозчики
		|			ЛЕВОЕ СОЕДИНЕНИЕ втПеревозчикиПоВидуПеревозки КАК втПеревозчикиПоВидуПеревозки
		|			ПО (ИСТИНА)
		|	ГДЕ
		|		втПеревозчикиПоВидуПеревозки.Перевозчик ЕСТЬ NULL) КАК втПеревозчики
		|
		|СГРУППИРОВАТЬ ПО
		|	втПеревозчики.Перевозчик
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПеревозчики.Перевозчик КАК Перевозчик,
		|	втПеревозчики.Флаг КАК Флаг,
		|	втПеревозчики.СформированыЗаявки КАК СформированыЗаявки,
		|	втПеревозчики.УказанВВидеПеревозки КАК УказанВВидеПеревозки,
		|	втПеревозчики.СовпадаетТипТС КАК СовпадаетТипТС,
		|	упСоглашенияСПеревозчиками.Ссылка КАК Соглашение
		|ИЗ
		|	втПеревозчикиИтог КАК втПеревозчики
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.упСоглашенияСПеревозчиками КАК упСоглашенияСПеревозчиками
		|		ПО втПеревозчики.Перевозчик = упСоглашенияСПеревозчиками.Владелец";
		текЗапрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
		тбзПеревозчики = текЗапрос.Выполнить().Выгрузить();
		Если тбзПеревозчики.Количество()>0 Тогда
			тбзПеревозчики.Колонки.Добавить("Расходы", Новый ОписаниеТипов("Число"));
			упТарификацияВызовСервера.Рассчитать(тбзПеревозчики, Новый Структура("СпособРасчета, Рейс", "РассчитатьПоСпискуПеревозчиков", ОбъектСсылка));
			тбзПеревозчики.Сортировать("Расходы");
		КонецЕсли;
	КонецЕсли;

	Возврат тбзПеревозчики;	
	
КонецФункции

// Функция возвращает адреса партнера.
//
// Параметры:
//  Ссылка  - СправочникСсылка.упПартнеры - Ссылка на элемент.
//
// Возвращаемое значение:
//   Массив - Массив адресов, отсортированных по дате изменения (первыми стоят последние использовавшиеся).
//
Функция ПолучитьАдресаПартнера(Ссылка) Экспорт
	
	мсвАдресов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упАдресаПартнеров.Адрес
	|ИЗ
	|	РегистрСведений.упАдресаПартнеров КАК упАдресаПартнеров
	|ГДЕ
	|	упАдресаПартнеров.Партнер = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	упАдресаПартнеров.ДатаИзменения УБЫВ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			мсвАдресов.Добавить(Выборка.Адрес);
		КонецЦикла; 	
	КонецЕсли; 
	
	Возврат мсвАдресов;
	
КонецФункции // ПолучитьАдресаПартнера()

// Функция возвращает график работы точки партнера.
//
// Параметры:
//  Партнер  - СправочникСсылка.упПартнеры - Ссылка на элемент.
//  Адрес    - СправочникСсылка.упАдреса - Ссылка на элемент.
//
// Возвращаемое значение:
//   СправочникСсылка.Календари - Неопределено, Результат выполнения.
//
Функция ПолучитьГрафикРаботыТочкиПартнера(Партнер, Адрес) Экспорт
	
	ГрафикРаботы = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упАдресаПартнеров.ГрафикРаботы
	|ИЗ
	|	РегистрСведений.упАдресаПартнеров КАК упАдресаПартнеров
	|ГДЕ
	|	упАдресаПартнеров.Партнер = &Партнер
	|	И упАдресаПартнеров.Адрес = &Адрес";
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("Адрес", Адрес);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
	    ГрафикРаботы = Выборка.ГрафикРаботы;
	КонецЕсли; 
	
	Возврат ГрафикРаботы;
	
КонецФункции // ПолучитьГрафикРаботыТочкиПартнера()

#Область ИерархияПартнеров

// Процедура создает записи в РС "упИерархияПартнеров"
//
// Параметры:
//  Партнер	- СправочникСсылка.упПартнеры - Ссылка на элемент.
//
Процедура ЗаписатьИерархиюПартнера(Партнер) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	упПартнеры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.упПартнеры КАК упПартнеры
	|ГДЕ
	|	упПартнеры.Ссылка В ИЕРАРХИИ(&Партнер)
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Партнер",Партнер);
	
	ДеревоИерархии = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ТекущийЭлемент = ДеревоИерархии.Строки.Найти(Партнер, "Ссылка", Истина);
	Если ТекущийЭлемент <> Неопределено Тогда
		ВыполнитьЗаписьВИерархию(ТекущийЭлемент);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

// Нет.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьЗаписьПоПодчиненным(СтрокиДерева,ТекущийЭлемент)
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Ссылка <> ТекущийЭлемент Тогда
			ВыполнитьЗаписьВИерархию(СтрокаДерева);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьЗаписьВИерархию(СтрокаДерева);
	
	НаборЗаписей = РегистрыСведений.упИерархияПартнеров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Партнер.Установить(СтрокаДерева.Ссылка);
	
	РодительСтроки = СтрокаДерева;
	Пока РодительСтроки <> Неопределено Цикл
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Партнер  = СтрокаДерева.Ссылка;
		ЗаписьНабора.Родитель = РодительСтроки.Ссылка;
		ЗаписьНабора.Уровень  = РодительСтроки.Уровень();
		
		РодительСтроки 	= РодительСтроки.Родитель;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
	ВыполнитьЗаписьПоПодчиненным(СтрокаДерева.Строки, СтрокаДерева.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



