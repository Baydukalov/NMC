#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если упСервисныеФункции.ЭтоВызовПослеОткрытияФормы(ЭтаФорма) Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	Если Объект.ТипРасчета = Перечисления.упТипРасчета.Топливо Тогда
		Элементы.РазмерыЗначенийПараметра.Видимость = Ложь;
		Элементы.ТипПоказателя.Видимость			= Ложь;
	КонецЕсли;
	
	сткПараметрыУчета = Новый Структура();
	сткПараметрыУчета.Вставить("УчитыватьКоличествоЗагрузочныхМетров", ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоЗагрузочныхМетров"));
	сткПараметрыУчета.Вставить("УчитыватьКоличествоМест"			 , ПолучитьФункциональнуюОпцию("упУчитыватьКоличествоМест"));
	сткПараметрыУчета.Вставить("УчитыватьМассу"						 , ПолучитьФункциональнуюОпцию("упУчитыватьМассу"));
	сткПараметрыУчета.Вставить("ИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками",	ПолучитьФункциональнуюОпцию("упИспользуетсяГлобальнаяПодсистемаУправлениеПеревозками"));
	сткПараметрыУчета.Вставить("ИспользуютсяУслугиИнкассации",		   ПолучитьФункциональнуюОпцию("упИспользуютсяУслугиИнкассации"));
	сткПараметрыУчета.Вставить("КонтролироватьТемпературныеРежимы",	   ПолучитьФункциональнуюОпцию("упКонтролироватьТемпературныеРежимы"));

	Если Объект.Предопределенный Тогда
		сткПараметрыУчета.Вставить("ИспользуетсяРейс",						Истина);	
	Иначе	
		сткПараметрыУчета.Вставить("ИспользуетсяРейс",						Ложь);	
	КонецЕсли;
		
	Элементы.ПараметрРасчета.СписокВыбора.ЗагрузитьЗначения(упТарификацияКлиентСервер.ПолучитьСписокДоступныхПараметров(Объект.ТипРасчета, сткПараметрыУчета));
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ТолькоПросмотр = Объект.Предопределенный;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбработатьИзменениеПараметраРасчета();
	ТипПоказателя = Объект.ТипПоказателя;
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписанНовыйПоказатель");
	
КонецПроцедуры // ПослеЗаписи()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	упТарификацияВызовСервера.ПроверитьУникальностьКодаПоказателя(ТекущийОбъект.Код, "упПоказателиРасчета", Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)

	Объект.Код = упТарификацияКлиентСервер.ИдентификаторПоказателяПоНаименованию(Объект.Наименование);
	
КонецПроцедуры // НаименованиеПриИзменении()

&НаКлиенте
Процедура ПараметрРасчетаПриИзменении(Элемент)
	
	Если Объект.ПараметрРасчета <> ПараметрРасчета Тогда 
		
		// Если предыдущий параметр был "ссылочный"
		Если ЗначениеЗаполнено(ПараметрРасчета) 
				И упТарификацияКлиентСервер.ЭтоСсылочныйПараметр(ПараметрРасчета) 
				И Объект.ЗначенияПараметра.Количество() > 0 Тогда
					
			ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьЗначения",ЭтаФорма),Нстр("ru = 'Табличная часть заполнена. Очистить?'"),РежимДиалогаВопрос.ДаНет);
		Иначе
			ОбработатьИзменениеПараметраРасчета();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПараметрРасчетаПриИзменении()

&НаКлиенте
Процедура ТипПоказателяПриИзменении(Элемент)
	
	Если Объект.ТипПоказателя = ПредопределенноеЗначение("Перечисление.упТипыПоказателейРасчета.Фиксированный") Тогда
		Если ЗначениеЗаполнено(Объект.ПараметрРасчета) Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьЗначенияРасчетногоПоказателя",ЭтаФорма),Нстр("ru = 'Заполнены данные по расчетному показателю. Очистить?'"),РежимДиалогаВопрос.ДаНет);	
		Иначе	
			УправлениеЭлементамиФормы();
		КонецЕсли;
	Иначе
		ТипПоказателя = Объект.ТипПоказателя;
		УправлениеЭлементамиФормы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТекущиеЗначенияПоказателя(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		сткОтбор =Новый Структура("ПоказательРасчета", Объект.Ссылка);
		сткПараметры = Новый Структура("Отбор", сткОтбор);
		ОткрытьФорму("РегистрСведений.упЗначенияПоказателейРасчета.Форма.ФормаСпискаСрезПоследних",сткПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
// Процедура - очищает табличную часть "РазмерыЗначенийПараметра".
//
//  Параметры
//   нет.
//
Процедура ОчиститьЗначения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ЗначенияПараметра.Очистить();			
		ОбработатьИзменениеПараметраРасчета();
	Иначе
		Объект.ПараметрРасчета = ПараметрРасчета;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
// Процедура - очищает параметр расчета и табличную часть "РазмерыЗначенийПараметра".
//
//  Параметры
//   РезультатВопроса - КодВозвратаДиалога - Результат.
//   ДополнительныеПараметры - Структура - Параметры.
//
Процедура ОчиститьЗначенияРасчетногоПоказателя(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ПараметрРасчета = ПредопределенноеЗначение("Перечисление.упПараметрыРасчета.ПустаяСсылка");
		Объект.ЗначенияПараметра.Очистить();			
		ОбработатьИзменениеПараметраРасчета();
		ТипПоказателя = Объект.ТипПоказателя;
	Иначе
		Объект.ТипПоказателя = ТипПоказателя;	
		УправлениеЭлементамиФормы();
	КонецЕсли; 
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
// В процедуре выполняется установка свойств элементов формы.
//
// Параметры:
//  Нет
//
Процедура УправлениеЭлементамиФормы()
	
	Если Объект.ТипПоказателя = ПредопределенноеЗначение("Перечисление.упТипыПоказателейРасчета.Расчетный") Тогда
		Элементы.ПараметрРасчета.АвтоОтметкаНезаполненного 	= Истина;	
		Элементы.ПараметрРасчета.Доступность 				= Истина;
		
		Элементы.РазмерыЗначенийПараметра.АвтоОтметкаНезаполненного = ?(ЭтоСсылочныйПараметрРасчета, Неопределено, Ложь);
		Если ЗначениеЗаполнено(Объект.ПараметрРасчета) И ЭтоСсылочныйПараметрРасчета Тогда
			Элементы.РазмерыЗначенийПараметра.АвтоОтметкаНезаполненного = Неопределено;
			Элементы.РазмерыЗначенийПараметра.Доступность 				= Истина;
		Иначе
			Элементы.РазмерыЗначенийПараметра.АвтоОтметкаНезаполненного = Ложь;
			Элементы.РазмерыЗначенийПараметра.ОтметкаНезаполненного		= Ложь;
			Элементы.РазмерыЗначенийПараметра.Доступность 				= Ложь;	
		КонецЕсли;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаТекущиеЗначенияПоказателя.Доступность = Ложь;
	Иначе
		Элементы.РазмерыЗначенийПараметра.АвтоОтметкаНезаполненного = Ложь;	
		Элементы.РазмерыЗначенийПараметра.ОтметкаНезаполненного		= Ложь;
		Элементы.РазмерыЗначенийПараметра.Доступность 				= Ложь;	
		Элементы.ПараметрРасчета.АвтоОтметкаНезаполненного 			= Ложь;	
		Элементы.ПараметрРасчета.Доступность 		 				= Ложь;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаТекущиеЗначенияПоказателя.Доступность = Истина;
	КонецЕсли;	
	
КонецПроцедуры // УправлениеЭлементамиФормы()

&НаКлиенте
// Процедура - изменяет свойства формы в зависимости от праметра расчета .
//
//  Параметры
//   нет.
//
Процедура ОбработатьИзменениеПараметраРасчета()
	
	ЭтоСсылочныйПараметрРасчета = упТарификацияКлиентСервер.ЭтоСсылочныйПараметр(Объект.ПараметрРасчета);
	
	// Если новый параметр "ссылочный"
	Если ЭтоСсылочныйПараметрРасчета Тогда
		текТипПараметра = упТарификацияКлиентСервер.ПолучитьТипПараметра(Объект.ПараметрРасчета);
		мсвТипов = Новый Массив();
		Если НЕ текТипПараметра = Неопределено Тогда
			мсвТипов.Добавить(текТипПараметра);
		КонецЕсли;
		Элементы.РазмерыЗначенийПараметраЗначениеПараметра.ОграничениеТипа = Новый ОписаниеТипов(мсвТипов);
	КонецЕсли;
	
	ПараметрРасчета = Объект.ПараметрРасчета;

	УправлениеЭлементамиФормы();
	
КонецПроцедуры


#КонецОбласти
