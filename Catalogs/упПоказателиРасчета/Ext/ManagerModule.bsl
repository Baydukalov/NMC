
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	Поля.Добавить("Код");
	Поля.Добавить("Наименование");
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Представление = Данные.Наименование + " [" + СокрЛП(Данные.Код) + "]";
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Процедура - Заполнить предопределенный показатель рейс
//
// Параметры:
//  Отказ	 - 	Булево - возвращает состояние процесса заполнения.
//
Процедура ЗаполнитьПредопределенныйПоказательРейс(Отказ) Экспорт
	
	ПоказательРейсОбъект = Справочники.упПоказателиРасчета.Рейс.ПолучитьОбъект();
	
	ЕстьИзменения = НЕ (ПоказательРейсОбъект.ПараметрРасчета 	= Перечисления.упПараметрыРасчета.Рейс
						И ПоказательРейсОбъект.ТипПоказателя 	= Перечисления.упТипыПоказателейРасчета.Расчетный
						И ПоказательРейсОбъект.Код 				= "Рейс"
						И ПоказательРейсОбъект.ТипРасчета 		= Перечисления.упТипРасчета.Тарификация);
	
	Если ЕстьИзменения Тогда
		Попытка
			ПоказательРейсОбъект.ПараметрРасчета 	= Перечисления.упПараметрыРасчета.Рейс;
			ПоказательРейсОбъект.ТипПоказателя 		= Перечисления.упТипыПоказателейРасчета.Расчетный;
			ПоказательРейсОбъект.Код 				= "Рейс";
			ПоказательРейсОбъект.ТипРасчета 		= Перечисления.упТипРасчета.Тарификация;
			ПоказательРейсОбъект.Записать();
		Исключение
			Отказ = Истина;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда 
				ТекстОшибки = ОписаниеОшибки();
			КонецЕсли;
			ЗаписьЖурналаРегистрации("ОшибкаПриЗаписиПоказателяРасчета", УровеньЖурналаРегистрации.Ошибка, ,ПоказательРейсОбъект.Ссылка, ТекстОшибки, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли