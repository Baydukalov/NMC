

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СерверСбораДанных = Параметры.Ссылка;
	ЗащищенноеСоединение = Параметры.ЗащищенноеСоединение;
	Пароль = Параметры.Пароль;
	Пользователь = Параметры.Пользователь;
	Порт = Параметры.Порт;
	Сервер = Параметры.Сервер;
	Тип = Параметры.Тип;
	Токен = Параметры.Токен;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШаблонИмениТрекераПриИзменении(Элемент)
	
	ПредставлениеШаблонИмениТрекера();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если НЕ ФлагДействия Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Получение данных Wialon'"),,
		НСтр("ru = 'Выполните действия с выбранными объектами.'"),БиблиотекаКартинок.Информация32);
		Возврат;
	КонецЕсли;
	
	ФлагДействия = Ложь;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап1 Тогда
        Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап2;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап2 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап3;
		ФлагДействия = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап3 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап4;
		ФлагДействия = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап4 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап5;
		ФлагДействия = Истина;
	КонецЕсли;
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры // Далее()

&НаКлиенте
Процедура Назад(Команда)
	ФлагДействия = Истина;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап2 Тогда
        Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап1;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап3 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап2;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап4 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап3;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап5 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап4;	
	КонецЕсли;
	УстановитьВидимостьЭлементов();
КонецПроцедуры // Назад()

&НаКлиенте
Процедура Завершить(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ВыполнитьЗавершениеРаботы", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Завершить работу с сервисом получения данных от сервера сбора данных? Все не сохраненные изменения будут потеряны.'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьССервера(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ВыполнитьОбращениеНаСервереWialon", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Получение данных списка объектов сервера сбора данных может занять длительное времени продолжить обращения к сервису?'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
	
КонецПроцедуры // ЗаполнитьССервера()

&НаКлиенте
Процедура ПолучиьССервера(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ВыполнитьОбращениеНаСервереWialonДатчики", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Получить список датчиков для выбранных записей?'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
	
КонецПроцедуры // ПолучиьССервера()

&НаКлиенте
Процедура СоздатьДатчики(Команда)
	Обработчик = Новый ОписаниеОповещения("СоздатьДатчикиМобильныхУстройствИТрекеров", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Создать элементы справочника датчики для транспортных средств выбранных строк?'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	УстановитьПометкуСписка(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометки(Команда)
	УстановитьПометкуСписка(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НайтиТрекеры(Команда)
	Обработчик = Новый ОписаниеОповещения("НайтиМобильныеУстройстваИТрекеры", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Сопоставить трекеры по идентификатору устройства?'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
КонецПроцедуры // НайтиТрекеры()

&НаКлиенте
Процедура СоздатьТрекеры(Команда)
	Обработчик = Новый ОписаниеОповещения("СоздатьМобильныеУстройстваИТрекеры", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Создать элементы справочника мобильные устройства и трекеры для выбранных строк?'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТрекерыНаТС(Команда)
	Обработчик = Новый ОписаниеОповещения("УстановитьМобильныеУстройстваИТрекеры", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Установить мобильные устройства и трекеры для выбранных строк на транспортные средства?'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0); 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап1 Тогда
		Заголовок = СтрШаблон("Сопоставить трекеры сервиса %1 с объектами TMS", Строка(СерверСбораДанных));
		Элементы.Назад.Видимость = Ложь;
		Элементы.Далее.Видимость = Истина;
		Элементы.Завершить.Видимость = Ложь;
		Элементы.СписокТрекеровЗаполнитьССервера.КнопкаПоУмолчанию = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап2 Тогда
		Заголовок = "Сопоставить мобильные устройства и трекеры";
		Элементы.Назад.Видимость = Истина;
		Элементы.Далее.Видимость = Истина;
		Элементы.Завершить.Видимость = Ложь;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Если ПустаяСтрока(ШаблонИмениТрекера) Тогда
			ШаблонИмениТрекера="ID_Сервер";
		КонецЕсли;
		ПредставлениеШаблонИмениТрекера();
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап3 Тогда
		Заголовок = "Сопоставить мобильные устройства и трекеры с установленными на транспортные средства";
		Элементы.Назад.Видимость = Истина;
		Элементы.Далее.Видимость = Истина;
		Элементы.Завершить.Видимость = Ложь;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап4 Тогда
		Заголовок = "Запросить датчики для выбранных трекеров";
		Элементы.Назад.Видимость = Истина;
		Элементы.Далее.Видимость = Ложь;
		Элементы.Завершить.Видимость = Истина;
		Элементы.Завершить.КнопкаПоУмолчанию = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап5 Тогда
		Заголовок = "Установить соответствие назначению датчиков";
		Элементы.Назад.Видимость = Истина;
		Элементы.Далее.Видимость = Ложь;
		Элементы.Завершить.Видимость = Истина;
		Элементы.Завершить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.Далее.Доступность = ?(СписокТрекеров.Количество(), Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуСписка(Пометка)
	
	Для каждого ТекущаяСтрокаУстройства Из СписокТрекеров Цикл
		ТекущаяСтрокаУстройства.Пометка=Пометка;
	КонецЦикла;
	
КонецПроцедуры // УстановитьПометкуСписка()

&НаКлиенте
// Выполним обращение к сервису.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыполнитьЗавершениеРаботы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если ФлагДействия Тогда
		Закрыть();
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Получение данных Wialon'"),,
		НСтр("ru = 'Выполните действия с выбранными объектами.'"),БиблиотекаКартинок.Информация32);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьЗавершениеРаботы()

&НаКлиенте
// Выполним обращение к сервису.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыполнитьОбращениеНаСервереWialon(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Получение данных Wialon'"),,
	НСтр("ru = 'Обращение к сервису Wialon.'"),БиблиотекаКартинок.Информация32);
	
	Если Тип = ПредопределенноеЗначение("Перечисление.упТипСервераСбораДанных.WialonPro") Тогда
		ПолучитьТСНаСервереWialonPro();
	ИначеЕсли Тип = ПредопределенноеЗначение("Перечисление.упТипСервераСбораДанных.WialonHosting") Тогда
		ПолучитьТСНаСервереWialonHosting();
	КонецЕсли;
	ФлагДействия=Истина;
	Если СписокТрекеров.Количество() Тогда
		Элементы.Далее.Доступность = Истина;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.Далее.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбращениеНаСервереWialon()

&НаКлиенте
// Выполним обращение к сервису.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура ВыполнитьОбращениеНаСервереWialonДатчики(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Строки трекеров сервиса не выбраны.'"),БиблиотекаКартинок.Информация32);
	Иначе
		
		Отбор.Вставить("ТранспортноеСредство", ПредопределенноеЗначение("Справочник.упТранспортныеСредства.ПустаяСсылка"));
		НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
			НСтр("ru = 'Строки трекеров сервиса выбран с не заполненым транспортным средством.'"),БиблиотекаКартинок.Информация32);
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Получение данных Wialon'"),,
		НСтр("ru = 'Обращение к сервису Wialon.'"),БиблиотекаКартинок.Информация32);
		
		Если Тип = ПредопределенноеЗначение("Перечисление.упТипСервераСбораДанных.WialonPro") Тогда
			ПолучитьДатчикиТСНаСервереWialonPro();
		ИначеЕсли Тип = ПредопределенноеЗначение("Перечисление.упТипСервераСбораДанных.WialonHosting") Тогда
			ПолучитьДатчикиТСНаСервереWialonHosting();
		КонецЕсли;
		ФлагДействия=Истина;
	КонецЕсли;
	
	Если СписокДатчиков.Количество() Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЭтап5;
		ФлагДействия = Истина;
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
			НСтр("ru = 'Данных по датчикам выбранных строк не обнаружено.'"),БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьОбращениеНаСервереWialonДатчики()

&НаКлиенте
// Выполним поиск объектов и трекеров.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура НайтиМобильныеУстройстваИТрекеры(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Строки трекеров сервиса не выбраны.'"),БиблиотекаКартинок.Информация32);
	Иначе
		ПоискТрекеровПоИдентификатору();
		ФлагДействия=Истина;
	КонецЕсли;
	
КонецПроцедуры // НайтиМобильныеУстройстваИТрекеры()

&НаКлиенте
// Создадим элементы справочника Справочник.упМобильныеУстройстваИТрекеры.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СоздатьМобильныеУстройстваИТрекеры(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Строки трекеров сервиса не выбраны.'"),БиблиотекаКартинок.Информация32);
	Иначе
		СоздатьТрекерыПоИдентификатору();
		ФлагДействия=Истина;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Сопостовление датчиков выполнено.'"),БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры // СоздатьМобильныеУстройстваИТрекеры()

&НаКлиенте
// Создадим элементы справочника Справочник.упДатчики.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура СоздатьДатчикиМобильныхУстройствИТрекеров(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Назначение", ПредопределенноеЗначение("Справочник.упНазначенияДатчиков.ПустаяСсылка"));
	НайденныеСтроки = СписокТипыДатчиков.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Укажите все сопостовление типов датчиков и назначения датчика.'"),БиблиотекаКартинок.Информация32);
	Иначе
		СоздатьДатчикиТранспортныхСрдеств();
		ФлагДействия=Истина;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Для выбранных строк выполнено сопостовление датчиков.'"),БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры // СоздатьДатчикиМобильныхУстройствИТрекеров()

&НаКлиенте
// Установим сопостовление трекеров.
//
// Параметры:
//  Результат  - Структура - результат выбора.
//  ДополнительныеПараметры  - Структура - Параметры.
//
Процедура УстановитьМобильныеУстройстваИТрекеры(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сопоставление данных'"),,
		НСтр("ru = 'Строки трекеров сервиса не выбраны.'"),БиблиотекаКартинок.Информация32);
	Иначе
		УстановитьТрекерыНаТССервере();
		ФлагДействия=Истина;
	КонецЕсли;
	
КонецПроцедуры // УстановитьМобильныеУстройстваИТрекеры()

&НаСервере
Процедура УстановитьТрекерыНаТССервере()
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		Возврат;
	Иначе
		ТекущаяДатаУстановки = ТекущаяДатаСеанса();
		Для каждого ТекущаяСтрокаТрекеров Из НайденныеСтроки Цикл
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаТрекеров.Трекер) Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаТрекеров.ТранспортноеСредство) Тогда
				Продолжить;
			КонецЕсли;
			
			НовЗапись = РегистрыСведений.упУстановленныеТрекеры.СоздатьМенеджерЗаписи();
			НовЗапись.Период = ?(ЗначениеЗаполнено(ДатаУстановки), ДатаУстановки, ТекущаяДатаУстановки);
			НовЗапись.Трекер = ТекущаяСтрокаТрекеров.Трекер;
			НовЗапись.ТранспортноеСредство = ТекущаяСтрокаТрекеров.ТранспортноеСредство;
			НовЗапись.Установлен = Истина;
			Попытка
				НовЗапись.Записать(Истина);			
			Исключение
				ТекстОшибки = СтрШаблон("Ошибка при записи в РС упУстановленныеТрекеры: %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // УстановитьТрекерыНаТССервере()

&НаСервере
Процедура СоздатьТрекерыПоИдентификатору()
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		Возврат;
	Иначе
		ИмяСервера = Строка(СерверСбораДанных);
		Для каждого ТекущаяСтрокаТрекеров Из НайденныеСтроки Цикл
			НаименованиеТрекера=СтрШаблон("%1_%2", ТекущаяСтрокаТрекеров.ID, ИмяСервера);
			Если ШаблонИмениТрекера="ID_Сервер_Представление" Тогда
				НаименованиеТрекера = СтрШаблон("%1_%2_%3", ТекущаяСтрокаТрекеров.ID, ИмяСервера, ТекущаяСтрокаТрекеров.Представление);
			ИначеЕсли ШаблонИмениТрекера="ID_Сервер_Представление_Описание" Тогда
				НаименованиеТрекера = СтрШаблон("%1_%2_%3_%4", ТекущаяСтрокаТрекеров.ID, ИмяСервера, ТекущаяСтрокаТрекеров.Представление, ТекущаяСтрокаТрекеров.Описание);
			ИначеЕсли ШаблонИмениТрекера="ID_Представление" Тогда
				НаименованиеТрекера = СтрШаблон("%1_%2", ТекущаяСтрокаТрекеров.ID, ТекущаяСтрокаТрекеров.Представление);
			ИначеЕсли ШаблонИмениТрекера="ID_Представление_Описание" Тогда
				НаименованиеТрекера = СтрШаблон("%1_%2_%3", ТекущаяСтрокаТрекеров.ID, ТекущаяСтрокаТрекеров.Представление, ТекущаяСтрокаТрекеров.Описание);
			КонецЕсли;
			
			НовыйТрекер = Справочники.упМобильныеУстройстваИТрекеры.СоздатьЭлемент();
			НовыйТрекер.Наименование = НаименованиеТрекера;
			НовыйТрекер.ИдентификаторУстройства = ТекущаяСтрокаТрекеров.ID;
			НовыйТрекер.СерверСбораДанных = СерверСбораДанных;
			НовыйТрекер.Модель = Модель;
			НовыйТрекер.НомерSIMКарты = ТекущаяСтрокаТрекеров.Телефон;
			Попытка
				НовыйТрекер.Записать();
			Исключение
				ТекстОшибки = СтрШаблон("Ошибка при записи элемента справочника МобильныеУстройстваИТрекеры: %1", ОписаниеОшибки());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Продолжить;
			КонецПопытки;
			
			ТекущаяСтрокаТрекеров.Трекер = НовыйТрекер.Ссылка;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры // СоздатьТрекерыПоИдентификатору()

&НаСервере
Процедура СоздатьДатчикиТранспортныхСрдеств()
	
	
	СоответствиеТипыДатчиков = Новый Соответствие;
	Для каждого ТекущийТипДатчика Из СписокТипыДатчиков Цикл
		СоответствиеТипыДатчиков.Вставить(ТекущийТипДатчика.Тип, ТекущийТипДатчика.Назначение);
	КонецЦикла;
	
	МассивДатчиков = СписокДатчиков.Выгрузить(, "Наименование");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДатчиков", МассивДатчиков);
	Запрос.Текст = "ВЫБРАТЬ
	|	упДатчики.Ссылка КАК Ссылка,
	|	упДатчики.Наименование КАК Наименование
	|ИЗ
	|	Справочник.упДатчики КАК упДатчики
	|ГДЕ
	|	упДатчики.Наименование В(&МассивДатчиков)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеДатчиков = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеДатчиков.Вставить(Выборка.Наименование, Выборка.Ссылка);
	КонецЦикла;

	ТекущаяДатаУстановкиДатчиков = ?(ЗначениеЗаполнено(ДатаУстановкиДатчика), ДатаУстановкиДатчика, ТекущаяДатаСеанса());
	
	Для каждого ТекущийДатчик Из СписокДатчиков Цикл
		СсылкаНаДатчик = СоответствиеДатчиков.Получить(ТекущийДатчик.Наименование);
		Если СсылкаНаДатчик=Неопределено Тогда
			ОбъектДатчика = Справочники.упДатчики.СоздатьЭлемент();
			ОбъектДатчика.Наименование = ТекущийДатчик.Наименование;
			ОбъектДатчика.ИдентификаторУстройства = ТекущийДатчик.ID;
			ОбъектДатчика.Назначение = СоответствиеТипыДатчиков.Получить(ТекущийДатчик.Тип);
			ОбъектДатчика.ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина);
			ОбъектДатчика.ОбменДанными.Загрузка = Истина;
			Попытка
				ОбъектДатчика.Записать();
			Исключение
				ТекстОшибки = СтрШаблон("Ошибка при записи элемента справочника Датчики: %1", ОписаниеОшибки());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Продолжить;
			КонецПопытки;
			СсылкаНаДатчик = ОбъектДатчика.Ссылка;
		КонецЕсли;
		
		НовЗапись = РегистрыСведений.упДатчикиТранспортныхСредств.СоздатьМенеджерЗаписи();
		НовЗапись.Период = ТекущаяДатаУстановкиДатчиков;
		НовЗапись.Датчик = СсылкаНаДатчик;
		НовЗапись.ТранспортноеСредство = ТекущийДатчик.ТранспортноеСредство;
		Попытка
			НовЗапись.Записать(Истина);			
		Исключение
			ТекстОшибки = СтрШаблон("Ошибка при записи в РС упДатчикиТранспортныхСредств: %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры // СоздатьДатчикиТранспортныхСрдеств()

&НаСервере
Процедура ПоискТрекеровПоИдентификатору()
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		Возврат;
	Иначе
		КопияТаблицы = СписокТрекеров.Выгрузить(НайденныеСтроки, "ID");
		МассивИдентификаторов = КопияТаблицы.ВыгрузитьКолонку("ID");
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("МассивИдентификаторов", МассивИдентификаторов);
		Запрос.УстановитьПараметр("СерверСбораДанных", СерверСбораДанных);
		Запрос.Текст = "ВЫБРАТЬ
		|	упМобильныеУстройстваИТрекеры.Ссылка КАК Ссылка,
		|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства КАК ID,
		|	ЕСТЬNULL(устТрекерСП.ТранспортноеСредство, ЗНАЧЕНИЕ(Справочник.упТранспортныеСредства.ПустаяСсылка)) КАК ТС
		|ИЗ
		|	Справочник.упМобильныеУстройстваИТрекеры КАК упМобильныеУстройстваИТрекеры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.упУстановленныеТрекеры.СрезПоследних КАК устТрекерСП
		|		ПО упМобильныеУстройстваИТрекеры.Ссылка = устТрекерСП.Трекер
		|ГДЕ
		|	упМобильныеУстройстваИТрекеры.ИдентификаторУстройства В(&МассивИдентификаторов)
		|	И упМобильныеУстройстваИТрекеры.СерверСбораДанных = &СерверСбораДанных";
		Выборка = Запрос.Выполнить().Выбрать();
		Отбор = Новый Структура();
		Пока Выборка.Следующий() Цикл
			Отбор.Вставить("ID", Выборка.ID);
			НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() Тогда
				СтрокаСписка = НайденныеСтроки[0];
				СтрокаСписка.Трекер = Выборка.Ссылка;
				СтрокаСписка.ТранспортноеСредство = Выборка.ТС;
				Если ЗначениеЗаполнено(СтрокаСписка.Трекер) И ЗначениеЗаполнено(СтрокаСписка.ТранспортноеСредство) Тогда 
					СтрокаСписка.Пометка = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ПоискТрекеровПоИдентификатору()

&НаСервере
Процедура ПолучитьТСНаСервереWialonPro()
	
	ssid = ""; // уникальный идентификатор сессии
	
	Попытка
		Если НЕ ПустаяСтрока(Пользователь) Тогда 
			РезультатОбращения = упИнтеграцияССерверамиСбораДанных.ПолучитьУникальныйИдентификаторСессииWialonPro(Сервер, Пользователь, Пароль, Порт, ЗащищенноеСоединение);
			Если РезультатОбращения=Неопределено Тогда 
				Возврат;
			КонецЕсли;
			ssid = РезультатОбращения.ssid;
		КонецЕсли;
		
		тбзУстройства = упИнтеграцияССерверамиСбораДанных.ПолучитьСписокУстройствWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid, РезультатОбращения.itemsId);
		РезультатЗавершения = упИнтеграцияССерверамиСбораДанных.ЗавершитьСессиюПодключенияWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid);
		Если НЕ РезультатЗавершения=Неопределено Тогда 
			ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер, РезультатЗавершения.ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;
		
		СписокТрекеров.Очистить();
		МассивТрекеров = Новый Массив;
		СоответствиеТрекеров = Новый Соответствие;
		Для каждого ТекущаяСтрокаУстройства Из тбзУстройства Цикл
			НоваяСтрока = СписокТрекеров.Добавить();
			НоваяСтрока.ID = ТекущаяСтрокаУстройства.Идентификатор;
			НоваяСтрока.Представление = ТекущаяСтрокаУстройства.Наименование;
			НоваяСтрока.Телефон = ТекущаяСтрокаУстройства.Телефон;
			НоваяСтрока.Описание = ТекущаяСтрокаУстройства.ТипТС;
		КонецЦикла;
		
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры // ПолучитьТСНаСервереWialonPro()

&НаСервере
Процедура ПолучитьТСНаСервереWialonHosting()
	
	sid = ""; // уникальный идентификатор сессии
	
	Попытка
		Если НЕ ПустаяСтрока(Токен) Тогда 
			РезультатОбращения = упИнтеграцияССерверамиСбораДанных.ПолучитьУникальныйИдентификаторСессииWialonHosting(Сервер, Токен, Порт, ЗащищенноеСоединение);
			Если РезультатОбращения=Неопределено Тогда 
				Возврат;
			КонецЕсли;
			sid = РезультатОбращения.sid;
		КонецЕсли;
		
		тбзУстройства = упИнтеграцияССерверамиСбораДанных.ПолучитьСписокУстройствWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid);
		РезультатЗавершения = упИнтеграцияССерверамиСбораДанных.ЗавершитьСессиюПодключенияWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid);
		Если НЕ РезультатЗавершения=Неопределено Тогда 
			ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер, РезультатЗавершения.ТекстОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;
		
		СписокТрекеров.Очистить();
		МассивТрекеров = Новый Массив;
		СоответствиеТрекеров = Новый Соответствие;
		Для каждого ТекущаяСтрокаУстройства Из тбзУстройства Цикл
			НоваяСтрока = СписокТрекеров.Добавить();
			НоваяСтрока.ID = ТекущаяСтрокаУстройства.Идентификатор;
			НоваяСтрока.Представление = ТекущаяСтрокаУстройства.Наименование;
			НоваяСтрока.Телефон = ТекущаяСтрокаУстройства.Телефон;
			НоваяСтрока.Описание = ТекущаяСтрокаУстройства.ТипТС;
		КонецЦикла;
		
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры // ПолучитьТСНаСервереWialonHosting()

&НаКлиенте
Процедура ПредставлениеШаблонИмениТрекера()
	Элементы.Декорация2.Заголовок = "";
	СтрокаСписка = СписокТрекеров[0];
	Если ШаблонИмениТрекера="ID_Сервер" Тогда
		Элементы.Декорация2.Заголовок = СтрШаблон("%1_%2", СтрокаСписка.ID, Строка(СерверСбораДанных));
	ИначеЕсли ШаблонИмениТрекера="ID_Сервер_Представление" Тогда
		Элементы.Декорация2.Заголовок = СтрШаблон("%1_%2_%3", СтрокаСписка.ID, Строка(СерверСбораДанных), СтрокаСписка.Представление);
	ИначеЕсли ШаблонИмениТрекера="ID_Сервер_Представление_Описание" Тогда
		Элементы.Декорация2.Заголовок = СтрШаблон("%1_%2_%3_%4", СтрокаСписка.ID, Строка(СерверСбораДанных), СтрокаСписка.Представление, СтрокаСписка.Описание);
	ИначеЕсли ШаблонИмениТрекера="ID_Представление" Тогда
		Элементы.Декорация2.Заголовок = СтрШаблон("%1_%2", СтрокаСписка.ID, СтрокаСписка.Представление);
	ИначеЕсли ШаблонИмениТрекера="ID_Представление_Описание" Тогда
		Элементы.Декорация2.Заголовок = СтрШаблон("%1_%2_%3", СтрокаСписка.ID, СтрокаСписка.Представление, СтрокаСписка.Описание);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПолучитьДатчикиТСНаСервереWialonHosting()
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		Возврат;
	Иначе
		КопияТаблицы = СписокТрекеров.Выгрузить(НайденныеСтроки, "ID");
		МассивИдентификаторов = КопияТаблицы.ВыгрузитьКолонку("ID");
		
		sid = ""; // уникальный идентификатор сессии
		
		Попытка
			Если НЕ ПустаяСтрока(Токен) Тогда 
				РезультатОбращения = упИнтеграцияССерверамиСбораДанных.ПолучитьУникальныйИдентификаторСессииWialonHosting(Сервер, Токен, Порт, ЗащищенноеСоединение);
				Если РезультатОбращения=Неопределено Тогда 
					Возврат;
				КонецЕсли;
				sid = РезультатОбращения.sid;
			КонецЕсли;
			
			тбзУстройства = упИнтеграцияССерверамиСбораДанных.ПолучитьСписокДатчиковУстройствWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid, МассивИдентификаторов);
			РезультатЗавершения = упИнтеграцияССерверамиСбораДанных.ЗавершитьСессиюПодключенияWialonHosting(Сервер, Порт, ЗащищенноеСоединение, sid);
			Если НЕ РезультатЗавершения=Неопределено Тогда 
				ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер, РезультатЗавершения.ТекстОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Возврат;
			КонецЕсли;
			
			СписокДатчиков.Очистить();
			СоответствиеТрекеров = Новый Соответствие;
			Для каждого ТекущаяСтрокаУстройства Из тбзУстройства Цикл
				СтруктураТрекера = СоответствиеТрекеров.Получить(ТекущаяСтрокаУстройства.Трекер);
				Если СтруктураТрекера=Неопределено Тогда 
					Отбор = Новый Структура("ID", ТекущаяСтрокаУстройства.Трекер);
					НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
					Если НайденныеСтроки.Количество() Тогда
						СтруктураТрекера = Новый Структура();
						СтруктураТрекера.Вставить("Трекер", НайденныеСтроки[0].Трекер);
						СтруктураТрекера.Вставить("ТранспортноеСредство", НайденныеСтроки[0].ТранспортноеСредство);
						СоответствиеТрекеров.Вставить(ТекущаяСтрокаУстройства.Трекер, СтруктураТрекера);
					Иначе
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				НоваяСтрока = СписокДатчиков.Добавить();
				НоваяСтрока.ID = ТекущаяСтрокаУстройства.Идентификатор;
				НоваяСтрока.Тип = ТекущаяСтрокаУстройства.Тип;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураТрекера);
				НоваяСтрока.Наименование = СтрШаблон("%1 %2 (%3 %4)", ТекущаяСтрокаУстройства.Трекер, ТекущаяСтрокаУстройства.Наименование, ТекущаяСтрокаУстройства.Тип, ТекущаяСтрокаУстройства.ЕдИзм);
			КонецЦикла;
		Исключение
			
		КонецПопытки;
		
	КонецЕсли;
	ТаблицаТипыДатчиков = СписокДатчиков.Выгрузить(,"Тип");
	ТаблицаТипыДатчиков.Свернуть("Тип");
	СписокТипыДатчиков.Загрузить(ТаблицаТипыДатчиков);
	
КонецПроцедуры // ПолучитьДатчикиТСНаСервереWialonHosting()

&НаСервере
Процедура ПолучитьДатчикиТСНаСервереWialonPro()
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пометка", Истина);
	НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
	Если НЕ НайденныеСтроки.Количество() Тогда
		Возврат;
	Иначе
		КопияТаблицы = СписокТрекеров.Выгрузить(НайденныеСтроки, "ID");
		МассивИдентификаторов = КопияТаблицы.ВыгрузитьКолонку("ID");
		
		ssid = ""; // уникальный идентификатор сессии
		
		Попытка
			Если НЕ ПустаяСтрока(Пользователь) Тогда 
				РезультатОбращения = упИнтеграцияССерверамиСбораДанных.ПолучитьУникальныйИдентификаторСессииWialonPro(Сервер, Пользователь, Пароль, Порт, ЗащищенноеСоединение);
				Если РезультатОбращения=Неопределено Тогда 
					Возврат;
				КонецЕсли;
				ssid = РезультатОбращения.ssid;
			КонецЕсли;
			
			тбзУстройства = упИнтеграцияССерверамиСбораДанных.ПолучитьСписокДатчиковУстройствWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid, МассивИдентификаторов);
			РезультатЗавершения = упИнтеграцияССерверамиСбораДанных.ЗавершитьСессиюПодключенияWialonPro(Сервер, Порт, ЗащищенноеСоединение, ssid);
			Если НЕ РезультатЗавершения=Неопределено Тогда 
				ТекстОшибки = СтрШаблон("Для сервера сбора данных %1 завершение сессии произошло с ошибкой: %2", Сервер, РезультатЗавершения.ТекстОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				Возврат;
			КонецЕсли;
			
			СписокДатчиков.Очистить();
			СоответствиеТрекеров = Новый Соответствие;
			Для каждого ТекущаяСтрокаУстройства Из тбзУстройства Цикл
				СтруктураТрекера = СоответствиеТрекеров.Получить(ТекущаяСтрокаУстройства.Трекер);
				Если СтруктураТрекера=Неопределено Тогда 
					Отбор = Новый Структура("ID", ТекущаяСтрокаУстройства.Трекер);
					НайденныеСтроки = СписокТрекеров.НайтиСтроки(Отбор);
					Если НайденныеСтроки.Количество() Тогда
						СтруктураТрекера = Новый Структура();
						СтруктураТрекера.Вставить("Трекер", НайденныеСтроки[0].Трекер);
						СтруктураТрекера.Вставить("ТранспортноеСредство", НайденныеСтроки[0].ТранспортноеСредство);
						СоответствиеТрекеров.Вставить(ТекущаяСтрокаУстройства.Трекер, СтруктураТрекера);
					Иначе
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				НоваяСтрока = СписокДатчиков.Добавить();
				НоваяСтрока.ID = ТекущаяСтрокаУстройства.Идентификатор;
				НоваяСтрока.Тип = ТекущаяСтрокаУстройства.Тип;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураТрекера);
				НоваяСтрока.Наименование = СтрШаблон("%1 %2 (%3 %4)", ТекущаяСтрокаУстройства.Трекер, ТекущаяСтрокаУстройства.Наименование, ТекущаяСтрокаУстройства.Тип, ТекущаяСтрокаУстройства.ЕдИзм);
			КонецЦикла;
		Исключение
			
		КонецПопытки;
		
	КонецЕсли;
	ТаблицаТипыДатчиков = СписокДатчиков.Выгрузить(,"Тип");
	ТаблицаТипыДатчиков.Свернуть("Тип");
	СписокТипыДатчиков.Загрузить(ТаблицаТипыДатчиков);
	
КонецПроцедуры // ПолучитьДатчикиТСНаСервереWialonPro()

#КонецОбласти