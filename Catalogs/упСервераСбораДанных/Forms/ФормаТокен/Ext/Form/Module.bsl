

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТокенПользователя = "";
	СерверПолученияТокен = Параметры.СерверПолученияТокен;
	Тип = Параметры.Тип;
	
	Если Тип = ПредопределенноеЗначение("Перечисление.упТипСервераСбораДанных.WialonHosting") Тогда
		Элементы.СерверПолученияТокен.ПодсказкаВвода = "hosting.wialon.com";
		СерверПолученияТокен = "hosting.wialon.com";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьСтрокуHTMLДокумент();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ОтключитьОбработчикОжидания("ОтправитьРезультатВладельцу");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)
	ПодключитьОбработчикОжидания("ПроверитьСостоянииеHTMLДокумент", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура HTMLДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	#Если НЕ ВебКлиент Тогда
		Если ДанныеСобытия.Document.ReadyState="complete" ИЛИ ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда
			ПодключитьОбработчикОжидания("ПроверитьСостоянииеHTMLДокумент", 3, Истина);
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры // HTMLДокументПриНажатии()

&НаКлиенте
Процедура ПроверитьСостоянииеHTMLДокумент() Экспорт
	
	Если НЕ ПустаяСтрока(Элементы.HTMLДокумент.Документ.url) Тогда
		МассивСтрок = СтрРазделить(Элементы.HTMLДокумент.Документ.url, "&");
		Для каждого ЭлементАдреса Из МассивСтрок Цикл
			Если СтрЧислоВхождений(ЭлементАдреса, "access_token=")>0 Тогда
				ТокенПользователя=СтрЗаменить(ЭлементАдреса, "access_token=", "");						
				ПодключитьОбработчикОжидания("ОтправитьРезультатВладельцу", 0.1, Истина);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьСостоянииеHTMLДокумент()

&НаКлиенте
Процедура ОтправитьРезультатВладельцу() Экспорт
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Токен", ТокенПользователя);
	СтруктураВозврата.Вставить("СерверПолученияТокен", СерверПолученияТокен);
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры // ОтправитьРезультатВладельцу()

&НаКлиенте
Процедура СерверПолученияТокенПриИзменении(Элемент)
	
	УстановитьСтрокуHTMLДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	// Нет.
#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура УстановитьСтрокуHTMLДокумент()
	
	Если НЕ ПустаяСтрока(СерверПолученияТокен) Тогда
		duration = Формат(0, "ЧН=0; ЧГ=0"); //время жизни токена (в секундах) 2592000 (30 дней в секундах) 
		ТекстШаблона ="http://%1/login.html?client_id=TMS&access_type=-1&activation_time=0&duration=%2&lang=ru&flags=7&&response_type=token";
		HTMLДокумент = СтрШаблон(ТекстШаблона, СерверПолученияТокен, duration);
	КонецЕсли;
	
КонецПроцедуры // УстановитьСтрокуHTMLДокумент()

#КонецОбласти