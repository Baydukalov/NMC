#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Маршрут
//
// Параметры:
//  ТиповойМаршрут	 - СправочникСсылка.упТиповыеМаршруты 	 - типовой маршрут.
// 
// Возвращаемое значение:
//   Массив - типовые маршруты.
//
Функция Маршрут(ТиповойМаршрут) Экспорт
	
	мсвМаршрут = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	упТиповыеМаршрутыМаршрут.Адрес КАК Адрес
	|ИЗ
	|	Справочник.упТиповыеМаршруты.Маршрут КАК упТиповыеМаршрутыМаршрут
	|ГДЕ
	|	упТиповыеМаршрутыМаршрут.Ссылка = &ТиповойМаршрут";
	Запрос.УстановитьПараметр("ТиповойМаршрут", ТиповойМаршрут);
	Результат = Запрос.Выполнить();
	Выборка	= Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
		мсвМаршрут.Добавить(Выборка.Адрес);		
	КонецЦикла; 
	
	Возврат мсвМаршрут; 
	
	
КонецФункции

// Функция - Маршруты по адресам отправления получения
//
// Параметры:
//  АдресОтправления - СправочникСсылка.упАдреса	 - адрес отправления.
//  АдресПолучения	 - СправочникСсылка.упАдреса	 - адрес получения.
// 
// Возвращаемое значение:
//  Массив - массив типовых маршрутов подходящих для адресов (СправочникСсылка.упТиповыеМаршруты). 
//
Функция МаршрутыПоАдресамОтправленияПолучения(АдресОтправления, АдресПолучения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст ="
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втМаршрутыОтправленияТ.Адрес = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|			ТОГДА втМаршрутыПолученияТ.Ссылка
	|		ИНАЧЕ втМаршрутыОтправленияТ.Ссылка
	|	КОНЕЦ КАК Маршрут
	|ИЗ
	|	Справочник.упТиповыеМаршруты.Маршрут КАК втМаршрутыОтправленияТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упТиповыеМаршруты.Маршрут КАК втМаршрутыПолученияТ
	|	ПО (ИСТИНА
	|			И втМаршрутыПолученияТ.НомерСтроки > втМаршрутыОтправленияТ.НомерСтроки
	|			И втМаршрутыПолученияТ.Ссылка = втМаршрутыОтправленияТ.Ссылка)
	|ГДЕ ИСТИНА
	|	И втМаршрутыОтправленияТ.Адрес = &АдресОтправления
	|	И втМаршрутыПолученияТ.Адрес = &АдресПолучения
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втМаршруты.Ссылка КАК Маршрут
	|ИЗ
	|	Справочник.упТиповыеМаршруты.Маршрут КАК втМаршруты
	|ГДЕ ИСТИНА
	|	И &АдресОтправления = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|	И втМаршруты.Адрес = &АдресПолучения
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	втМаршруты.Ссылка КАК Маршрут
	|ИЗ
	|	Справочник.упТиповыеМаршруты.Маршрут КАК втМаршруты
	|ГДЕ ИСТИНА
	|	И &АдресПолучения = ЗНАЧЕНИЕ(Справочник.упАдреса.ПустаяСсылка)
	|	И втМаршруты.Адрес = &АдресОтправления
	|	
	|";

	Запрос.УстановитьПараметр("АдресОтправления", АдресОтправления);
	Запрос.УстановитьПараметр("АдресПолучения", АдресПолучения);
	
	мсвРезультат = Новый Массив;
	
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мсвРезультат.Добавить(Выборка.Маршрут);		
	КонецЦикла; 
	
	Возврат мсвРезультат;
		
КонецФункции


#КонецОбласти

#КонецЕсли

