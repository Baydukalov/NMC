#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает подразделение по умолчанию.
//
// Параметры:
//  ИсточникВидаПеревозки - ИсточникВидаПеревозки	 - источник вида перевозки.
//  Пользователь		 - СправочникСсылка.Пользователи 	 - пользователь
// 
// Возвращаемое значение:
//  СправочникСсылка.упСтруктураПредприятия - ссылка на подразделение.
//
Функция ПодразделениеПоУмолчанию(ИсточникВидаПеревозки, Пользователь = Неопределено) Экспорт
	
	Результат = Справочники.упСтруктураПредприятия.ПустаяСсылка();
	Запрос = Новый Запрос;
	Запрос.Текст = "";
	
	Если ЗначениеЗаполнено(ИсточникВидаПеревозки) Тогда
		Если ТипЗнч(ИсточникВидаПеревозки) = Тип("СправочникСсылка.упВидыПеревозок") Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	упВидыПеревозок.Подразделение КАК Подразделение
			|ИЗ
			|	Справочник.упВидыПеревозок КАК упВидыПеревозок
			|ГДЕ
			|	упВидыПеревозок.Ссылка = &ИсточникВидаПеревозки
			|	И НЕ упВидыПеревозок.Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)";
			
			
		ИначеЕсли ТипЗнч(ИсточникВидаПеревозки) = Тип("ДокументСсылка.упЗаявкаНаПеревозкуГруза") Тогда	
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	упВидыПеревозок.Подразделение КАК Подразделение
			|ИЗ
			|	Документ.упЗаявкаНаПеревозкуГруза КАК упЗаявкаНаПеревозкуГруза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозок
			|		ПО упЗаявкаНаПеревозкуГруза.ВидПеревозки = упВидыПеревозок.Ссылка
			|ГДЕ
			|	упЗаявкаНаПеревозкуГруза.Ссылка = &ИсточникВидаПеревозки
			|	И НЕ упВидыПеревозок.Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)";
			
		ИначеЕсли ТипЗнч(ИсточникВидаПеревозки) = Тип("ДокументСсылка.упРейс") Тогда		
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	упВидыПеревозок.Подразделение КАК Подразделение
			|ИЗ
			|	Документ.упРейс КАК упРейс
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.упВидыПеревозок КАК упВидыПеревозок
			|		ПО упРейс.ВидПеревозки = упВидыПеревозок.Ссылка
			|ГДЕ
			|	упРейс.Ссылка = &ИсточникВидаПеревозки
			|	И НЕ упВидыПеревозок.Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)";
			
		КонецЕсли;
	КонецЕсли; 	
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		
			РазделительОбъединить = "
									|
									|ОБЪЕДИНИТЬ ВСЕ
									|
									|";
			Запрос.Текст = Запрос.Текст + ?(ЗначениеЗаполнено(Запрос.Текст),РазделительОбъединить,"") 
										+ "ВЫБРАТЬ
										  |	Пользователи.Подразделение
										  |ИЗ
										  |	Справочник.Пользователи КАК Пользователи
										  |ГДЕ
										  |	Пользователи.Ссылка = &Пользователь
										  |	И НЕ Пользователи.Подразделение = ЗНАЧЕНИЕ(Справочник.упСтруктураПредприятия.ПустаяСсылка)";
					
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		Запрос.УстановитьПараметр("ИсточникВидаПеревозки", ИсточникВидаПеревозки);
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Выборка.Подразделение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции


#КонецОбласти

#КонецЕсли
